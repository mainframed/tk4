IEX60000 CSECT
*0011057200-057400,058400                                          0050
         SPACE 2
         USING *,WBASE
         SPACE
* REGISTER DEFINITIONS
         SPACE
R1       EQU   1                        POINTS TO PARAMETER LIST
WBUFF    EQU   1                        RETURNS ADDR OF PRINTBUFFER
WBASE    EQU   2                        BASE REGISTER FOR COTEMER
WORK     EQU   3                        WORKREGISTER
WBASEEMP EQU   4                        BASE REGISTER FOR MESSAGE POOL
WPTEP    EQU   5                        POINTER TO ENTRY IN ERROR POOL
WPTIC    EQU   6                        POINTER TO INSERTION CODE
WPTMP    EQU   7                        POINTER TO ENTRY IN MSG POOL
WORK1    EQU   9                        WORK
WORK2    EQU   10                            RE-
WORK3    EQU   11                               GIS-
WORK4    EQU   12                                   TERS
WWRKA    EQU   13                       BASE REGISTER FOR WORKAREA
WBACK    EQU   14                       FOR RETURN TO CALLING ROUTINE
WPTAREA  EQU   14                       PTR TO MESSAGE TEXT WORKAREA
WRETURN  EQU   15                       RETURN REGISTER
WNBAS    EQU   15                       BASE REGISTER FOR NEXT PHASE
WORK5    EQU   15                       WORKREGISTER
         SPACE
* BIT PATTERNS
         SPACE
HIZERO   EQU   X'0F'                    TO ZERO FIRST 4 BITS IN A BYTE
PRTERR   EQU   X'80'                    INDICATES SYSPRINT-ERROR
         SPACE
* OTHER DEFINITIONS
         SPACE
SYMTST   EQU   256                      TO TEST ALGOL SYMBOL
LTXT     EQU   70                       LENGTH OF TEXT ON A PRINTLINE
LPRT     EQU   89                       LENGTH OF FIRST PRINTLINE
BLANK    EQU   C' '                     BLANK BYTE
         EJECT
* INITIALIZATION
         SPACE
         USING WORKAREA,WWRKA           BASEREG FOR WORKAREA DSECT
         SPACE
         MVC   AWEMPOOL(10),0(R1)       GET ADDRESSES AND MOD NR
         SPACE
COT00    LA    WORK1,WDIRET1            STORE RETURN-ADDR
         ST    WORK1,ERET                                 IN WORKAREA
         STM   WBACK,WORK5,SVAR1        SAVE RETURN ADDRESS AND BASE
         TM    HCOMPMOD+2,X'90'         IS SYSPRINT DOWN/NOT OPENED
         BC    7,COT18                  YES, GIVE CONSOLE MESSAGE
         L     WPTEP,ERRPOOL            ADDRESS ERROR POOL
         SPACE
         CL    WPTEP,NEXTERR            IS ERROR POOL EMPTY
         BE    COT28                    YES
         SPACE
         CLC   PAGEHEAD+39(11),HDING1   ARE HEADINGS ALREADY SET
         BE    COT01                    YES, BYPASS HEADINGS
         SPACE
         MVI   PAGEHEAD+2,BLANK                 BLANK LIST-
         MVC   PAGEHEAD+3(81),PAGEHEAD+2                   HEADING 1
         MVC   PAGEHEAD+39(11),HDING1           PUT HEADING 1
         MVI   PAGEHEAD,X'02'                   BLANK BETWEEN
         MVI   PAGEHEAD+1,X'11'                               HDINGS
         MVC   PAGEHEAD+94(90),HDING2           PUT HEADING 2
         MVI   PAGEHEAD+92,X'02'        BLANK LINE AFTER
         MVI   PAGEHEAD+93,X'11'                         2ND HDING
         MVI   PAGEHEAD+186,X'FF'               NO HEADING 3
         MVI   LINCNT,X'7F'             NEW PAGE
         EJECT
* HANDLING OF THE ENTRIES IN ERROR POOL
         SPACE
COT01    EQU   *                   OBTAIN CORRESP ENTRY IN ERR MSG POOL
         L     WBASEEMP,AWEMPOOL        ADDRESS ERROR MESSAGE POOL
         SR    WPTMP,WPTMP
         IC    WPTMP,1(0,WPTEP)         GET ERROR MSG NUMBER
         LA    WORK2,200                IS IT A
         CR    WPTMP,WORK2                      DIRECTORY MESSAGE
         BL    COT36                    NO
         LH    WORK2,MODNUMB            YES, MODIFY
         SR    WPTMP,WORK2                          NUMBER
COT36    SLL   WPTMP,2                  GET IT FOUR TIMES
         L     WORK1,AWADDTAB           ADDRESS ADDRESS-TABLE
         AR    WPTMP,WORK1              GET CORRECT ENTRY
         L     WPTMP,0(0,WPTMP)         LOAD ADR TO ENTRY IN ERMSG POOL
         SPACE
COT02    EQU   *                   GET LENGTH OF INSERTION CODE PART
         LA    WPTIC,2(0,WPTMP)         POINT TO FIRST INSERTION CODE
         SR    WORK1,WORK1
         IC    WORK1,1(0,WPTMP)         GET NUMBER OF INSERTION CODES
         LR    WORK2,WORK1
         LR    WORK4,WORK1
         LA    WORK4,1(0,WORK4)         SET INSERTION CODE COUNTER
         SLL   WORK1,1                  DOUBLE IT
         AR    WORK1,WORK2              GET IT 3 TIMES
         SPACE
COT03    EQU   *                   HANDLE SEVERITY CODE
         LA    WORK2,2(WORK1,WPTMP)     GET ADDRESS TO BYTE BEFORE TEXT
         SPACE
         MVC   WAREA+9(1),0(WORK2)      INSERT SEVERITY CODE
         CLI   0(WORK2),C'W'            SET
         BNE   *+12                         BIT FOR
         OI    HCOMPMOD,X'20'                       WARNING MESSAGE
         B     *+28
         CLI   0(WORK2),C'S'            SET
         BNE   *+16                         BIT FOR
         OI    HCOMPMOD,X'10'                       SEVERE ERROR
         OI    HCOMPMOD,X'80'                       AND SYNTAX CHECK
         B     *+8
         OI    HCOMPMOD,X'08'           SET BIT FOR TERMINATING ERROR
         SPACE
COT04    EQU   *                   INSERTION OF MSG NR AND SC COUNTER
         SR    WORK2,WORK2              CLEAR REGISTER
         IC    WORK2,1(WPTEP)           GET ERROR MSG NUMBER
         CVD   WORK2,WDEC+8             CONVERT TO
         UNPK  WAREA+3(3),WDEC+8(8)                READABLE DECIMAL
         MVZ   WAREA+5(1),WAREA+4       PUT ZONE-BITS
         SPACE
         TM    0(WPTEP),X'80'           BLANKS FOR SC
         BZ    COT26                    NO
         MVC   WAREA+12(5),HDING2+15    YES, MOVE BLANKS
         NI    0(WPTEP),X'7F'           REMOVE TAG
         B     COT31
COT26    MVC   WDEC(2),2(WPTEP)         GET SEMI-
         LH    WORK2,WDEC                        COLON COUNTER
         CVD   WORK2,WDEC+8             CONVERT TO
         UNPK  WAREA+12(5),WDEC+8(8)               READABLE DECIMAL
         MVZ   WAREA+16(1),WAREA+15     PUT ZONE-BITS
         SPACE
COT31    EQU   *                   INSERTION OF FIXED MESSAGE
         LA    WPTAREA,WAREA+20         POINT TO 1ST TEXTBYTE IN WAREA
         CLI   1(WPTMP),X'00'           ANY INSERTION CODE
         BNE   COT05                    YES
         SPACE
         LA    WORK2,3(0,WPTMP)         GET ADDRESS OF MSG TXT
         SPACE
         SR    WORK3,WORK3
         IC    WORK3,0(0,WPTMP)         GET LENGTH OF ENTRY
         AR    WORK3,WPTMP              GET NEXT ENTRY
         SR    WORK3,WORK2              GET LENGTH OF MSG TXT
         SPACE
         EX    WORK3,WMOVE1             MOVE MSG TXT TO WORKAREA
         SPACE
         LA    WPTAREA,1(WORK3,WPTAREA) UPDATE WAREA-POINTER
COT05    BCT   WORK4,COT06              GO TO INS CODE TREATMENT IF ANY
         B     COT12                    ALL INS CODES TREATED
         EJECT
* INSERTION CODE TREATMENT
         SPACE
COT06    EQU   *                   TEST ACTION PART OF INSERTION CODE
         LA    WORK1,HIZERO             LOAD REG TO ZERO HIGHORDER BITS
         SR    WORK2,WORK2
         SR    WORK3,WORK3
         SPACE
         TM    0(WPTIC),X'F0'
         BO    COT37                    GO TO 'INSERT BLANKS'
         BZ    COT30                    GO TO 'INSERT MESSAGE TEXT'
         TM    0(WPTIC),X'80'
         BO    COT07                    GO TO 'UNALTERED TEXT'
         TM    0(WPTIC),X'40'
         BO    COT10                    GO TO 'ALGOL SYMBOL'
         TM    0(WPTIC),X'20'
         BO    COT33                    GO TO 'INTERNAL CHARACTERS'
         SPACE
COT37    EQU   *                   INSERTION OF BLANKS AT END OF PRTLIN
         LA    WORK,WAREA+90            CALC FREE SPACE
         SR    WORK,WPTAREA                             ON 1ST PRT LINE
         BC    12,COT09                 BYPASS BLANKING IF LINE FULL
COT38    MVI   0(WPTAREA),C' '          INSERT BLANK
         LA    WPTAREA,1(0,WPTAREA)     STEP WAREA POINTER
         BCT   WORK,COT38               BRANCH IF 1ST PRT LINE NOT FULL
         B     COT09                    GO TO NEXT INS CODE
         SPACE
COT30    EQU   *                   INSERTION OF MESSAGE TEXT PART
         IC    WORK2,2(0,WPTIC)         GET ADDRESS OF
         LA    WORK2,0(WORK2,WPTMP)                    TEXT PART
         IC    WORK3,1(0,WPTIC)         GET LENGTH OF TEXT PART
         SPACE
COT08    EX    WORK3,WMOVE1             MOVE TO WORKAREA
         SPACE
COT35    LA    WPTAREA,1(WORK3,WPTAREA) UPDATE WORKAREA POINTER
COT09    LA    WPTIC,3(0,WPTIC)         UPDATE INS CODE POINTER
         B     COT05                    GO TO NEXT INS CODE
         SPACE
COT07    EQU   *                   UNALTERED TEXT
         IC    WORK3,0(0,WPTEP)         GET
         LA    WORK2,5                      LENGTH OF
         SR    WORK3,WORK2                            SOURCE TEXT
         BC    4,COT09                  NO MOVE IF NO SOURCE TEXT
         SPACE
         LA    WORK2,4(0,WPTEP)         GET ADDRESS OF SOURCE TEXT
         SPACE
         B     COT08                    MOVE TEXT, GO TO NEXT INS CODE
         SPACE
COT10    EQU   *                   ALGOL SYMBOL
         TM    HCOMPMOD+2,X'02'         TEST CHARACTER SET USED
         BO    COT32                    IF 53 CHAR SET
         LA    WORK3,WORDSISO           USE ISO-TABLE
         B     *+8
COT32    LA    WORK3,WORDSEBC           USE EBCDIC-TABLE
         LA    WORK,WSYMBSTK            TABLE FOR STACK OPERATOR
         SPACE
         IC    WORK2,0(0,WPTIC)         GET ONE-BYTE
         NR    WORK2,WORK1                           ALGOL
         IC    WORK2,0(WORK2,WPTEP)                        SYMBOL
         STC   WORK2,WDEC               TEST IF
         TM    WDEC,X'80'                       SOURCE OPERATOR
         BZ    *+16                     NO
         LA    WORK,WSYMBSRC            YES, CHANGE TABLE
         NI    WDEC,X'7F'               REMOVE
         IC    WORK2,WDEC                      TAG
         SPACE
         SLL   WORK2,1                  MULTIPLY BY 2
         LH    WORK2,0(WORK2,WORK)      CONVERT TO EBCDIC OR DISPLACEMT
         SPACE
         LA    WORK1,SYMTST             TEST KIND
         CLR   WORK2,WORK1                        OF SYMBOL
         BL    COT11                    ONE-CHARACTER SYMBOL
         SPACE
         LA    WORK2,0(WORK2,WORK3)     ADDRESS WORDS-ENTRY
         SR    WORK3,WORK3
         IC    WORK3,0(0,WORK2)         GET LENGTH OF SYMBOL
         LA    WORK2,1(0,WORK2)         GET ADDRESS OF SYMBOL
         B     COT08                    MOVE SYMB, GO TO NEXT INS CODE
         SPACE
COT11    SR    WORK3,WORK3              GET LENGTH-1 OF ONE-CHAR SYMBOL
         STC   WORK2,0(0,WPTAREA)       MOVE SYMBOL TO MSG AREA
         B     COT35                    GO TO NEXT INS CODE
         SPACE
COT33    EQU   *                   INTERNAL CHARACTERS
         LA    WORK1,5                  GET NUMBER
         IC    WORK3,0(0,WPTEP)                    OF INT
         SR    WORK3,WORK1                                 CHARACTERS-1
         STC   WORK3,COT34+1            STORE IT IN TRANSLATE-INSTR
         SPACE
         LA    WORK2,4(0,WPTEP)         GET ADDRESS OF SOURCE TEXT
         EX    WORK3,WMOVE1
         SPACE
         LA    WORK1,WINTEBC            GET ADDRESS OF TRANSLATE-TABLE
COT34    TR    0(0,WPTAREA),0(WORK1)    TRANSLATE
         B     COT35                    UPDATE PTR, GO TO NEXT INS CODE
         EJECT
* TRANSLATION AND OUTPUT OF MESSAGE
         SPACE
COT12    EQU   *                   TRANSLATE MESSAGE TEXT
         LA    WORK1,WEBCDIC            LOAD EBCDIC-TABLE
         TR    WAREA+20(250),0(WORK1)   TRANSLATE MSG TEXT
          SPACE
         TM    SWTO,X'80'               MESSAGE TO CONSOLE TYPEWRITER
          BO    COT25                   YES
         SPACE
COT16    EQU   *                   PUT MESSAGE TO SYSPRINT
         LR    WORK3,WPTAREA
         LA    WORK1,WAREA+20           GET LENGTH OF
         SR    WORK3,WORK1                            MESSAGE TEXT
         LA    WORK1,LTXT               GET LENGTH OF TEXT ON 1ST LINE
         SR    WORK3,WORK1              SUBTRACT IT
         LA    WORK1,LPRT               GET LENGTH OF PRINTLINE
         LA    WORK2,WAREA              GET ADDRESS OF ERROR MESSAGE
         SPACE
         BAL   WRETURN,COT27            OBTAIN OUTPUT BUFFER,BLANK IT
         SPACE
         LTR   WORK3,WORK3              IS LENGTH OF MSG TXT GRT 70
         BNL   COT17                    YES
         ALR   WORK1,WORK3              OBTAIN PRTLINE LENGTH IF LT 90
         SPACE
COT17    EX    WORK1,WMOVE3             MOVE FIRST PART OF ERROR MSG
         SPACE
COT20    LA    WORK2,1(WORK1,WORK2)     INCREASE ADDR IN WORKAREA
         LTR   WORK3,WORK3              IS THERE MORE TEXT TO BE MOVED
         BNH   COT21                    NO
         SPACE
         LA    WORK1,LTXT               YES,GET LENGTH OF NEXT PRTLINE
         SR    WORK3,WORK1              SUBTRACT IT FROM REMAIN. TXTLGT
         SPACE
         BAL   WRETURN,COT27            OBTAIN OUTPUT BUFFER, BLANK IT
         SPACE
         LTR   WORK3,WORK3              IS LENGTH GREATER THAN 70
         BNL   COT19                    YES
         ALR   WORK1,WORK3              OBTAIN LENGTH IF LESS THAN 70
         SPACE
COT19    LA    WORK5,1                  MODIFY LENGTH
         SR    WORK1,WORK5                            FOR MOVE-INSTR
         EX    WORK1,WMOVE4             MOVE TEXT TO OUTPUT BUFFER
         SPACE
         B     COT20                    PUT OUT NEXT TEXTLINE IF ANY
         SPACE
COT25     EQU  *                   PUT MESSAGE TO CONSOLE TYPEWRITER
         LA    WORK3,40(WORK3)          GET LENGTH FOR             0050
         STC   WORK3,*+5                     MOVE INSTRUCTION      0050
         MVC   COT24+8(0),WAREA         MOVE MESSAGE
          SPACE
         DS    0F
COT24    WTO   '                                                       X
                      ',ROUTCDE=11,DESC=7                          0050
         SPACE
         B     WDIRET2                  TERMINATE
         SPACE 2
* OBTAIN OUTPUT BUFFER
         SPACE
COT27    STM   14,15,SVAR2              SAVE REGS FOR CALL OF PRINT RTN
         L     WORK5,PRTRTADD           LOAD ADDR OF PRINT ROUTINE
         BALR  WBACK,WORK5              CALL PRINT ROUTINE
         LM    14,15,SVAR2              RESTORE REGS
         BR    WRETURN
         EJECT
* GO TO NEXT MESSAGE OR TERMINATE
         SPACE
COT21    SR    WORK1,WORK1
         IC    WORK1,0(0,WPTEP)         GET LENGTH OF TREATED ENTRY
         AR    WPTEP,WORK1              POINT TO NEXT ENTRY
         L     WORK1,NEXTERR            GET ADDR OF FIRST FREE ENTRY
         CR    WPTEP,WORK1              NEXT ENTRY = FIRST FREE
         BL    COT01                    NO, HANDLE NEXT ENTRY
         LA    WORK2,WDIRET2            LOAD NEW
         ST    WORK2,ERET                        RETURN ADDRESS
         L     WORK2,ERRPOOL            YES, POOL
         ST    WORK2,NEXTERR                     EXHAUSTED, ZERO PTR
         SPACE
         TM    HCOMPMOD,X'08'           TEST IF TERMINATING ERROR
COT28    LM    WBACK,WORK5,SVAR1        LOAD RETURN ADDRESS AND BASE
         BO    COT29                    YES
         BR    WBACK                    RETURN
COT29    XCTL  EP=IEX51002              BRANCH TO TERMINATION
         EJECT
* ACTION TO BE PERFORMED AFTER I/O-ERROR OR PROGRAM INTERRUPTION
         SPACE
WDIRET1  EQU   *                   DIRECTORY RETURN-ADDRESS
         TM    HCOMPMOD+2,PRTERR        IS IT SYSPRINT-ERROR
         BZ    *+12                     NO
COT18    OI    SWTO,X'80'               YES, SET SWITCH ON
         B     LAST
         SPACE
         TM    HCOMPMOD+1,X'08'         IS IT PROGRAM INTERRUPT
         BZ    LAST                     NO
         LA    WPTMP,211                YES, GET MESSAGE NUMBER
         LH    WORK2,MODNUMB            MODIFY
         SR    WPTMP,WORK2                     IT
         SLL   WPTMP,2                  GET ENTRY
         L     WORK1,AWADDTAB                     IN ADDRESS TABLE
         AR    WPTMP,WORK1              GET ADDRESS
         L     WPTMP,0(0,WPTMP)                     OF MESSAGE
         SPACE
         BAL   WRETURN,COT27            GET PRINT BUFFER
         MVC   0(20,WBUFF),CPI          MOVE MSG CODE ETC
         MVC   20(51,WBUFF),0(WPTMP)    MOVE MSG TEXT
         BAL   WRETURN,COT27            GET PRINT BUFFER
         L     WORK1,NEXTERR            GET
         LA    WORK2,16                     ADDRESS
         SR    WORK1,WORK2                          OF PSW
         MVC   20(16,WBUFF),0(WORK1)    MOVE PSW
         MVI   37(WBUFF),C'.'           INSERT PERIOD
          SPACE
WDIRET2  OI    HCOMPMOD+1,X'80'         SET BIT FOR TERMINATING ERROR
         B     COT29                    BRANCH TO TERMINATION
         SPACE
LAST     EQU   *                   HANDLE LAST ERROR PATTERN
         L     WPTEP,NEXTERR            GET ADDR OF FIRST FREE ENTRY
         LA    WORK2,12                 GET ADDR OF
         SR    WPTEP,WORK2                          LAST PATTERN
         B     COT01                    GO TO NORMAL TREATMENT
         EJECT
* WORKAREAS AND MOVE-INSTRUCTIONS
         SPACE
         CNOP  0,4
WMOVE1   MVC   0(0,WPTAREA),0(WORK2)
WMOVE3   MVC   0(0,WBUFF),0(WORK2)
WMOVE4   MVC   20(0,WBUFF),0(WORK2)
         SPACE
WDEC     DS    2D
WAREA    DC    C'IEX000I  0  00000   '
         DS    250C
         SPACE
* HEADINGS AND MESSAGE 211 (PART OF)
         SPACE
HDING1   DC    C'DIAGNOSTICS'
HDING2   DC    C'CODE    SEV  SC     MESSAGE'
         DC    63C' '
CPI      DC    C'IEX211I  T          '  FIRST PART OF MESSAGE 211
         SPACE
* ADDRESSES AND CONSTANT
         SPACE
AWEMPOOL DS    F                        ADDRESS OF MESSAGE TEXTS
AWADDTAB DS    F                        ADDRESS OF ADDRESS TABLE
MODNUMB  DS    H                        MODIFICATION NUMBER
         SPACE
* VARIOUS
         SPACE
SWTO     DC    X'00'                    SWITCH FOR OUTPUT ON CONSOLETYP
SVAR2    DS    2F                       TO SAVE LINKREGS FOR PRINT RTN
SVAR1    DS    2F                       TO SAVE RETURN ADDRESS AND BASE
         EJECT
* TABLES FOR CONVERSION AND TRANSLATION
         SPACE 2
         DS    0F
         SPACE
WINTEBC  EQU   *                  TRANSLATES INT CHARACTERS TO EBCDIC
         DC    C'+-*/##(:'
         DC    C'###;####'
         DC    C'=<>'
         DC    13C'#'
         DC    C'^#×&&#,):'
         DC    C'### #.''#'
         DC    C'0123456789'
         DC    C'####.'''
         DC    C'ABCDEFGHI'
         DC    C'JKLMNOPQR'
         DC    C'STUVWXYZ'
         EJECT
         DS    0F
         SPACE 2
WSYMBSRC EQU   *         LIST FOR CONVERSION OF SOURCE OPERATORS
         SPACE
         DC    X'00'               00        PLUS
         DC    C'+'
         DC    X'00'               01        MINUS
         DC    C'-'
         DC    X'00'               02        MULTIPLY
         DC    C'*'
         DC    X'00'               03        DIVIDE
         DC    C'/'
         DC    H'256'              04        INTEGER DIVIDE
         DC    H'260'              05        'POWER'
         DC    X'00'               06        LEFT PARENTHESIS
         DC    C'('
         DC    H'268'              07        COLON
         DC    H'271'              08        LEFT BRACKET
         DC    H'274'              09        'ARRAY'
         DC    H'282'              0A        'SWITCH'
         DC    H'291'              0B        SEMICOLON
         DC    H'294'              0C        'BEGIN'
         DC    H'294'              0D        'BEGIN'
         DC    H'469'              0E        'PROCEDURE'
         DC    H'469'              0F        'PROCEDURE'
         DC    H'302'              10        'EQUAL'
         DC    H'310'              11        'LESS'
         DC    H'317'              12        'GREATER'
         DC    H'327'              13        'NOTEQUAL'
         DC    H'338'              14        'NOTGREATER
         DC    H'351'              15        'NOTLESS'
         DC    H'361'              16        COLON EQUAL
         DC    H'364'              17        'GOTO'
         DC    H'371'              18        'FOR'
         DC    H'377'              19        'STEP'
         DC    H'384'              1A        'UNTIL'
         DC    H'392'              1B        'WHILE'
         DC    H'400'              1C        'DO'
         DC    H'405'              1D        'IF'
         DC    H'410'              1E        'THEN'
         DC    H'417'              1F        'ELSE'
         DC    H'424'              20        'NOT'
         DC    H'430'              21        'IMPL'
         DC    H'437'              22        'OR'
         DC    H'442'              23        'AND'
         DC    H'448'              24        'EQUIV'
         DC    X'00'               25        COMMA
         DC    C','
         DC    X'00'               26        RIGHT PARENTHESIS
         DC    C')'
         DC    H'268'              27        COLON
         DC    H'456'              28        RIGHT SUBSCRIPT BRACKET
         DC    H'291'              29        DELTA
         DC    H'459'              2A        'END'
         DC    H'291'              2B        ETA
         DC    H'459'              2C        'END'
         DC    X'00'               2D        OMEGA
         DC    C' '
         DC    H'481'              2E        'CODE'
         EJECT
WSYMBSTK EQU   *         LIST FOR CONVERSION OF STACK OPERATORS
         SPACE
         DC    X'00'               00        PLUS
         DC    C'+'
         DC    X'00'               01        MINUS
         DC    C'-'
         DC    X'00'               02        MULTIPLY
         DC    C'*'
         DC    X'00'               03        DIVIDE
         DC    C'/'
         DC    H'256'              04        INTEGER DIVIDE
         DC    H'260'              05        'POWER'
         DC    X'00'               06        LEFT PARENTHESIS
         DC    C'('
         DC    H'268'              07        COLON
         DC    H'271'              08        LEFT BRACKET
         DC    H'274'              09        'ARRAY'
         DC    H'282'              0A        'SWITCH'
         DC    H'291'              0B        SEMICOLON
         DC    H'294'              0C        'BEGIN'
         DC    H'294'              0D        'BEGIN'
         DC    H'469'              0E        'PROCEDURE'
         DC    H'469'              0F        'PROCEDURE'
         DC    H'302'              10        'EQUAL'
         DC    H'310'              11        'LESS'
         DC    H'317'              12        'GREATER'
         DC    H'327'              13        'NOTEQUAL'
         DC    H'338'              14        'NOTGREATER
         DC    H'351'              15        'NOTLESS'
         DC    H'361'              16        COLON EQUAL
         DC    H'364'              17        'GOTO'
         DC    H'371'              18        'FOR'
         DC    H'377'              19        'STEP'
         DC    H'384'              1A        'UNTIL'
         DC    H'392'              1B        'WHILE'
         DC    H'400'              1C        'DO'
         DC    H'405'              1D        'IF'
         DC    H'410'              1E        'THEN'
         DC    H'417'              1F        'ELSE'
         DC    H'424'              20        'NOT'
         DC    H'430'              21        'IMPL'
         DC    H'437'              22        'OR'
         DC    H'442'              23        'AND'
         DC    H'448'              24        'EQUIV'
         DC    X'00'               25        ALPHA
         DC    C' '
         DC    H'405'              26        IFS
         DC    H'410'              27        THENS
         DC    H'417'              28        ELSES
         DC    X'00'               29        LEFT PARENTHESIS
         DC    C'('
         DC    X'00'               2A        LEFT PARENTHESIS
         DC    C'('
         DC    X'00'               2B        MONADIC MINUS
         DC    C'-'
         DC    H'271'              2C        LEFT SUBSCRIPT BRACKET
         DC    H'361'              2D        COLON EQUAL
         DC    H'361'              2E        COLON EQUAL
         EJECT
         DS    0F
         SPACE 2
WORDSEBC EQU   *-256             LIST OF COMPOUND ALGOL SYMBOLS, EBCDIC
         SPACE
         DC    X'02'                         256       INTEGER DIVIDE
         DC    C'''/'''
         DC    X'01'                         260       POWER
         DC    C'**     '
         DC    X'00'                         268       COLON
         DC    C': '
         DC    X'01'                         271       LEFT BRACKET
         DC    C'(/'
         DC    X'06'                         274       'ARRAY'
         DC    C'''ARRAY'''
         DC    X'07'                         282     'SWITCH'
         DC    C'''SWITCH'''
         DC    X'00'                         291       SEMICOLON
         DC    C'; '
         DC    X'06'                         294       'BEGIN'
         DC    C'''BEGIN'''
         DC    X'00'                         302       EQUAL
         DC    C'=      '
         DC    X'00'                         310       LESSTHAN
         DC    C'<     '
         DC    X'00'                         317       GREATER THAN
         DC    C'>        '
         DC    X'01'                         327       NOT EQUAL
         DC    C'^=        '
         DC    X'01'                         338       LESS THAN OR EQU
         DC    C'<=          '
         DC    X'01'                         351       GR THAN OR EQU
         DC    C'>=       '
         DC    X'01'                         361       COLON EQUAL
         DC    C':='
         DC    X'05'                         364       'GOTO'
         DC    C'''GOTO'''
         DC    X'04'                         371       'FOR'
         DC    C'''FOR'''
         DC    X'03'                         377       'STEP'
         DC    C'''STEP'''
         DC    X'06'                         384       'UNTIL'
         DC    C'''UNTIL'''
         DC    X'06'                         392       'WHILE'
         DC    C'''WHILE'''
         DC    X'03'                         400       'DO'
         DC    C'''DO'''
         DC    X'03'                         405       'IF'
         DC    C'''IF'''
         DC    X'05'                         410       'THEN'
         DC    C'''THEN'''
         DC    X'05'                         417       'ELSE'
         DC    C'''ELSE'''
         DC    X'00'                         424       NOT
         DC    C'^    '
         DC    X'05'                         430       'IMPL'
         DC    C'''IMPL'''
         DC    X'00'                         437       OR
         DC    C'×   '
         DC    X'00'                         442       AND
         DC    C'&&    '
         DC    X'06'                         448       'EQUIV'
         DC    C'''EQUIV'''
         DC    X'01'                         456       RIGHT BRACKET
         DC    C'/)'
         DC    X'04'                         459       'END'
         DC    C'''END'''
         DC    X'02'                         465       LEFT STRINGQUOTE
         DC    C'''('''
         DC    X'0A'                         469       'PROCEDURE'
         DC    C'''PROCEDURE'''
         DC    X'05'                         481       'CODE'
         DC    C'''CODE'''
         EJECT
         DS    0F
         SPACE 2
WORDSISO EQU   *-256               LIST OF COMPOUND ALGOL SYMBOLS, ISO
         SPACE
         DC    X'02'                         256       INTEGER DIVIDE
         DC    C'''/'''
         DC    X'06'                         260       'POWER'
         DC    C'''POWER'''
         DC    X'01'                         268       COLON
         DC    C'..'
         DC    X'01'                         271       LEFT BRACKET
         DC    C'(/'
         DC    X'06'                         274       'ARRAY'
         DC    C'''ARRAY'''
         DC    X'07'                         282       'SWITCH'
         DC    C'''SWITCH'''
         DC    X'01'                         291       SEMICOLON
         DC    C'.,'
         DC    X'06'                         294       'BEGIN'
         DC    C'''BEGIN'''
         DC    X'06'                         302       'EQUAL'
         DC    C'''EQUAL'''
         DC    X'05'                         310       'LESS'
         DC    C'''LESS'''
         DC    X'08'                         317       'GREATER'
         DC    C'''GREATER'''
         DC    X'09'                         327       'NOTEQUAL'
         DC    C'''NOTEQUAL'''
         DC    X'0B'                         338       'NOTGREATER'
         DC    C'''NOTGREATER'''
         DC    X'08'                         351       'NOTLESS'
         DC    C'''NOTLESS'''
         DC    X'01'                         361       COLON EQUAL
         DC    C'.='
         DC    X'05'                         364       'GOTO'
         DC    C'''GOTO'''
         DC    X'04'                         371       'FOR'
         DC    C'''FOR'''
         DC    X'03'                         377       'STEP'
         DC    C'''STEP'''
         DC    X'06'                         384       'UNTIL'
         DC    C'''UNTIL'''
         DC    X'06'                         392       'WHILE'
         DC    C'''WHILE'''
         DC    X'03'                         400       'DO'
         DC    C'''DO'''
         DC    X'03'                         405       'IF'
         DC    C'''IF'''
         DC    X'05'                         410       'THEN'
         DC    C'''THEN'''
         DC    X'05'                         417       'ELSE'
         DC    C'''ELSE'''
         DC    X'04'                         424       'NOT'
         DC    C'''NOT'''
         DC    X'05'                         430       'IMPL'
         DC    C'''IMPL'''
         DC    X'03'                         437       'OR'
         DC    C'''OR'''
         DC    X'04'                         442       'AND'
         DC    C'''AND'''
         DC    X'06'                         448       'EQUIV'
         DC    C'''EQUIV'''
         DC    X'01'                         456       RIGHT BRACKET
         DC    C'/)'
         DC    X'04'                         459       'END'
         DC    C'''END'''
         DC    X'02'                         465       LEFT STRINGQUOTE
         DC    C'''('''
         DC    X'0A'                         469       'PROCEDURE'
         DC    C'''PROCEDURE'''
         DC    X'05'                         481       'CODE'
         DC    C'''CODE'''
         EJECT
         DS    0F
         SPACE 2
WEBCDIC  EQU   *-64                TABLE FOR EBCDIC-EDCDIC TRANSLATION.
*                                  IS PROVIDED TO GIVE THE USER A
*                                  POSSIBILITY TO ALTER THE OUTPUT
*                                  ACCORDING TO HIS REQUIREMENTS.
         SPACE
         DC    C' '
         DC    9C'#'
         DC    C'{.<(+×&&'
         DC    9C'#'
         DC    C'}$*);^-/'
         DC    9C'#'
         DC    C',%_>?'
         DC    10C'#'
         DC    C':#@''="'
         DC    65C'#'
         DC    C'ABCDEFGHI'
         DC    7C'#'
         DC    C'JKLMNOPQR'
         DC    8C'#'
         DC    C'STUVWXYZ'
         DC    6C'#'
         DC    C'0123456789'
