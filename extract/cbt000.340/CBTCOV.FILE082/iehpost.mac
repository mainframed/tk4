         MACRO
&LABEL   IEHPOST &REG,&OPTION,&USED
         LCLA  &SAVE,&DIFF,&WORK
         AIF   ('&OPTION' EQ '').SEQ0A
&WORK    SETA  0
.SEQ0    ANOP
&WORK    SETA  &WORK+1
         AIF   (&WORK GT K'&OPTION).SEQ0A
         AIF   ('&OPTION'(&WORK,1) NE 'C').SEQ0
*
*              THIS MACRO IS USED WITH THE IEHPRE MACRO IN THE
*              MOVE/COPY PROGRAM TO RESTORE THE SAVE AREAS
*              AFTER CALLING A SUBROUTINE
*
.SEQ0A   ANOP
&SAVE    SETA  72
         AIF   ('&USED' EQ '').SEQ1
         AIF   ((T'&USED NE 'N') OR ('&USED'(1,1) LT '0') OR ('&USED'(1X
               ,1) GT '9')).ERR3
&DIFF    SETA  (&USED/4)*4
         AIF   (&DIFF NE &USED).ERR3
&SAVE    SETA  &USED
.SEQ1    ANOP
&LABEL   L     13,4(0,13)
         AIF   ('&REG(1)' EQ '').SEQ3
         AIF   (('&REG(1)' NE '14') AND ('&REG(1)' NE '15') AND ('&REG(X
               1)' NE '0') AND ('&REG(1)' NE '1')).ERR1
         AIF   (N'&REG NE 1).SEQ2
         L     &REG(1),&SAVE.(0,13)
         AGO   .SEQ3
.SEQ2    AIF   (('&REG(2)' NE '14') AND ('&REG(2)' NE '15') AND ('&REG(X
               2)' NE '0') AND ('&REG(2)' NE '1')).ERR1
&DIFF    SETA  &REG(2)-&REG(1)
         AIF   ((&DIFF NE 1) AND (&DIFF NE 0-13) AND (&DIFF NE 0-14) ANX
               D (&DIFF NE 0-15)).ERR1
         LM    &REG(1),&REG(2),&SAVE.(13)
.SEQ3    AIF   ('&OPTION' EQ '').EXIT
&WORK    SETA  0
.SEQ4    ANOP
&WORK    SETA  &WORK+1
         AIF   (&WORK GT K'&OPTION).EXIT
         AIF   ('&OPTION'(&WORK,1) NE 'T').SEQ4
         MVI   12(13),X'FF'
.EXIT    MEXIT
.ERR1    MNOTE 1,'** REGISTER OPERAND IN ERROR'
         MEXIT
.ERR3    MNOTE 1,'** USED OPERAND IN ERROR'
         MEXIT
         MEND
