         MACRO
&NAME    IEMKENT &TRACE,&TYPE=
.* THIS MACRO GENERATES TYPE 1, 2 OR 3 ENTRY LINKAGES. IF
.* GLOBAL VARIABLES FOR THE TYPE AND MODULE NAME
.* ARE SET UP FOR USE IN THE IEMKALVE MACRO.
         GBLB  &GBASAV1
         GBLB  &GBALLSW
         GBLB  &GBATRSW
         GBLC  &GCATYPE
&GBALLSW SETB  0
&GCATYPE SETC  '1'
         AIF   (T'&TYPE EQ 'O').SS0002
         AIF   (K'&TYPE EQ 1).SS0001
&GBALLSW SETB  ('&TYPE'(1,2) EQ 'LL')
         AIF   (K'&TYPE EQ 2).SS0002
&GCATYPE SETC  '&TYPE'(3,1)
         AGO   .SS0002
.SS0001  ANOP
&GCATYPE SETC  '&TYPE'
.SS0002  AIF   ('&GCATYPE' EQ '1').SSTYP1
         AIF   ('&GCATYPE' EQ '2').SSTYP2
         AIF   ('&GCATYPE' EQ '3').SSTYP3
         MNOTE 8,'INVALID TYPE PARAMETER - TYPE=1 ASSUMED'
&GBALLSW SETB  0
&GCATYPE SETC  '1'
.* GENERATE TYPE 1 ENTRY LINKAGE
.SSTYP1   ANOP
&NAME    ST    RR,4(0,SAREG) .          STORE RETURN POINT
         AIF   (&GBASAV1).SSTYP11
         STM   GR2,SAREG,8(SAREG) . SAVE GEN. REGS. AND SAVE AREA PTR.
         AGO   .SSTYP12
.SSTYP11 STM   PR1,SAREG,8(SAREG) . SAVE GEN. REGS. AND SAVE AREA PTR.
.SSTYP12 ANOP
         AIF   (&GBALLSW).SSTRAC        TEST FOR LOWEST LEVEL MODULE
         AIF   (&GBASAV1).SSTYP13
         LA    SAREG,4*(SAREG-GR2+2)(0,SAREG) . UPDATE SAVE AREA PTR
         AGO   .SSTRAC
.SSTYP13 LA    SAREG,4*(SAREG-GR2+3)(0,SAREG) . UPDATE SAVE AREA PTR.
         AGO   .SSTRAC
.* GENERATE TYPE 2 LINKAGE
.SSTYP2  ANOP
&NAME    ST    RR,4(0,SAREG) .          SAVE RETURN POINT
         AIF   (&GBALLSW).SSTRAC    TEST FOR LOWEST LEVEL MODULE
         LA    SAREG,4(0,SAREG) .       UPDATE SAVE AREA POINTER
         AGO   .SSTRAC
.* GENERATE TYPE 3 LINKAGE
.SSTYP3  ANOP
&NAME    DS    0H .                     NO REGISTERS SAVED FOR TYPE 3
.SSTRAC  ANOP
*
         MEND
