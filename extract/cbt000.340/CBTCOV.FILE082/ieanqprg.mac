         MACRO
         IEANQPRG
* DISPLACEMENTS
HEADPTR  EQU   12
RETREG   EQU   14
CVTAD    EQU   16
TWENTY8  EQU   28
ABENDAD  EQU   52
* REG EQU'S
R3       EQU   3
CURMAJ   EQU   7
CURMIN   EQU   8
CURQEL   EQU   9
WORKREG  EQU   10
SVRBRG   EQU   11
WAITCT   EQU   12
WORKREG2 EQU   12
LINKREG  EQU   13
IEAATPRG EQU   *
         LA    TEMPTCB,0(TEMPTCB)
         L     CURMAJ,CVTAD             GET PTR
         L     CURMAJ,ABENDAD(CURMAJ)       TO FIRST QCB THRU CVT
         L     CURMAJ,HEADPTR(CURMAJ)          AND ABTERM
MAJLOOP  L     CURMAJ,ZERO(CURMAJ)      GET NEXT MAJOR
         LTR   CURMAJ,CURMAJ            IS THERE ANOTHER
         BC    8,GETTCB                 NO
         L     CURMIN,EIGHT(CURMAJ)     GET NEXT MINOR
MINLOOP  LTR   CURMIN,CURMIN            IS THERE ANOTHER
         BC    EIGHT,ENDMAJOR           NO
         L     CURQEL,ZERO(CURMIN)
QELLOOP  LA    CURQEL,ZERO(CURQEL)      CLEAR HIGH ORDER BYTE
         LTR   CURQEL,CURQEL            IS THERE ANOTHER
         BC    EIGHT,ENDMINOR           NO
         CL    TEMPTCB,EIGHT(CURQEL)    DOES QEL BELONG TO TCB
         BE    FOUNDQEL                 YES
         L     CURQEL,ZERO(CURQEL)
         B     QELLOOP
FOUNDQEL L     WORKREG,FOUR(CURQEL)     GET BACK PTR
         LA    WORKREG,ZERO(WORKREG)
         CR    WORKREG,CURMIN           IS BACK PTR A MINOR
         BE    NEXTQEL                  YES -BELOW QEL WAS NOT FIRST
         TM    FOUR(CURQEL),X'80'       IS CURQEL SHARED
         BC    ONE,DEQQEL               YES                 X,S
         TM    FOUR(WORKREG),X'80'      IS LAST QEL EXCLUSIVE ALSO
         BC    ZERO,DEQQEL              YES                 E,E
         L     WORKREG,ZERO(CURQEL)     GET NEXT QEL        S,E
         LA    WORKREG,ZERO(WORKREG)    IS IT ALSO
         LTR   WORKREG,WORKREG
         BZ    DEQQEL
         TM    FOUR(WORKREG),X'80'         SHARED
         BZ    DEQQEL      S,E,E
SEARCHQ  L     WORKREG,FOUR(CURQEL)     GET BACK PTR X,S,E,S
LOOP     TM    FOUR(WORKREG),X'80'      IS IT SHARED
         BC    EIGHT,DEQQEL             NO
         L     WORKREG,FOUR(WORKREG)    GET NEXT BACK PTR
         CR    WORKREG,CURMIN           HAVE I GONE BACK TO MINOR YET
         BNE   LOOP                     NO
         L     WORKREG,ZERO(CURQEL)
         LA    WORKREG,ZERO(WORKREG)    CLEAR HI BYTE
         B     NEXTQEL2
NEXTQEL  TM    FOUR(CURQEL),X'80'       IS CURRENT SHARED
         L     WORKREG,ZERO(CURQEL)     GET NEXT
         BC    ONE,TESTNEXT            YES      S,X
NEXTQEL2 EQU   *
         LTR   WORKREG,WORKREG          QEL EXIST
         BZ    DEQQEL                   NO
         L     SVRBRG,TWELVE(WORKREG)   GET SVRB FROM NEXT QEL CURQEL
*                                       . IS EXCLUSIVE
         SR    WAITCT,WAITCT
         IC    WAITCT,TWENTY8(SVRBRG)   GET WAIT COUNT
         LTR   WAITCT,WAITCT            IS IT 0
         BC    EIGHT,NOSWTCH            YES
         BCTR  WAITCT,0                 DECREMENT
         STC   WAITCT,TWENTY8(SVRBRG)   STORE
TESTRESV TM    FOUR(WORKREG),X'40'      IS WORKING QEL RESERVED
         BC    EIGHT,NORESV             NO
         L     WORKREG2,EIGHT(WORKREG)  GET TCB
         L     WORKREG2,104(WORKREG2)   GET PARM POINTER
         ST    WORKREG2,TWELVE(WORKREG)
         IC    SVRBRG,40(WORKREG2)      INC.
         LA    SVRBRG,ONE(SVRBRG)          RESERVE
         STC   SVRBRG,40(WORKREG2)            COUNT
NORESV   EQU    *
         LTR   WAITCT,WAITCT            IS IT 0
         BNZ    NOSWTCH
SWITCH   L     WORKREG2,EIGHT(WORKREG)
         CLC   35(ONE,TEMPTCB),35(WORKREG2) IS THIS TCB OF HIGHER PIROR
         BC    10,NOSWTCH               NO
         L     SVRBRG,CVTAD
         L     SVRBRG,ZERO(SVRBRG)      GET NEW/OLD @
         ST    WORKREG2,ZERO(SVRBRG)
NOSWTCH  L     WORKREG2,ZERO(WORKREG)        GET NEW  NEXT
         TM    FOUR(WORKREG),X'80'      IS LAST 'NEXT'EXCLUSIVE
         BC    ZERO,DEQQEL              YES, DO NO MORE
         LR    WORKREG,WORKREG2         SET REG TO
         LA    WORKREG,ZERO(WORKREG)
         LTR   WORKREG,WORKREG
         BZ    DEQQEL
         TM    FOUR(WORKREG),X'80'
         BC    8,DEQQEL
         B     NEXTQEL2                   REPEAT LOOP
TESTNEXT EQU   *
         LA    WORKREG,ZERO(WORKREG)
         LTR   WORKREG,WORKREG
         BZ    DEQQEL
         TM    FOUR(WORKREG),X'80'      IS NEXT ALSO SHARED
         BC    EIGHT,NEXTQEL2            NO       S,E
         B     DEQQEL                  YES       S,S
ENDMINOR L     WORKREG,ZERO(CURMIN)     DOES MINOR
         LTR   WORKREG,WORKREG            HAVE ANY QELS
         BC    EIGHT,DEQMIN             NO
         L     CURMIN,EIGHT(CURMIN)      GET NEXT MINOR
         B     MINLOOP
ENDMAJOR L     WORKREG,EIGHT(CURMAJ)    DOES MAJOR
         LTR   WORKREG,WORKREG            HAVE ANY MINORS
         BC    EIGHT,DEQMAJ             NO
         B     MAJLOOP
DEQ      L     WORKREG,ZERO(WORKREG2)         SUBROUTINE
         LA    WORKREG,ZERO(WORKREG)               TO
         LTR   WORKREG,WORKREG
         BC    EIGHT,ENDCHAIN                       REMOVE  A
         MVC   FOUR(FOUR,WORKREG),FOUR(WORKREG2)           CONTROL
ENDCHAIN L     WORKREG,FOUR(WORKREG2)                   BLOCK
         MVC   ONE(3,WORKREG),ONE(WORKREG2)               FROM A
         BR    SVRBRG                                       QUEUE
DEQQEL   LR    WORKREG2,CURQEL       GO TO REMOVE
         BAL   SVRBRG,DEQ                QEL FROM QUEUE
         TM    FOUR(WORKREG2),X'40'  IS DEQ'D QEL RESERVED
         BC    EIGHT,NOTRESV
         STM   1,3,XRBESA               QEL IS RESERVED SO MUST GO TO
         STM   6,14,XRBESA+12             RESIDENT CORE TO RELEASE
         L     R3,TWELVE(CURQEL)
         BALR  LINKREG,DEQADDR
         LM    1,3,XRBESA               RESTORE
         LM    6,14,XRBESA+12               REGS
NOTRESV  LR    ONE,CURQEL               GET NEXT QEL AND
         L     CURQEL,ZERO(CURQEL)
         L     ZERO,FREEQEL               PREPARE TO FREE CORE
         SVC   10
         B      QELLOOP                 BRANCH BACK TO MAINLINE
DEQMIN   L     WORKREG2,EIGHT(CURMIN)
         LTR   WORKREG2,WORKREG2
         BC    EIGHT,ONLY
         MVC   FOUR(FOUR,WORKREG2),FOUR(CURMIN)
ONLY     L     WORKREG2,FOUR(CURMIN)
         MVC   EIGHT(FOUR,WORKREG2),EIGHT(CURMIN)
         LR    ONE,CURMIN               GET NEXT MINOR
         L     CURMIN,EIGHT(CURMIN)        AND PREPARE FOR FREEMAIN
         SR    ZERO,ZERO
         IC    ZERO,TWELVE(CURMIN)      GET SIZE OF
         LA    ZERO,21(ZERO)          MINOR
         N     ZERO,DOUBRND
         O     ZERO,FREEMIN
         SVC   10
         B     MINLOOP
DEQMAJ   LR    WORKREG2,CURMAJ          BRANCH TO
         BAL   SVRBRG,DEQ                  DEQ MAJOR
         LR    ONE,CURMAJ               GET NEXT MAJOR
         L     CURMAJ,ZERO(CURMAJ)         AND FREE
         L     ZERO,FREEMAJ                  CORE
         SVC   10
         B     MAJLOOP+FOUR             RETURN TO MAINLINE
         DS    0F
DOUBRND  DC    X'FFFFFFF8'
FREEMAJ  DC    X'FF000018'              INDICATES
FREEMIN  DC    X'FF000000'                 SUBPOOLS AND
FREEQEL  DC    X'FF000010'                   SIZES FOR FREEMAINS
         MEND
