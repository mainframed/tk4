         MACRO
&NAME    STAEPARM  &FLAG=,                                             X
               &PSEUDO=,                                               X
               &LIST=,                                                 X
               &ADDR=,                                                 X
               &MF=
.*
.*       THIS MACRO BUILDS THE CONTROL BLOCK THAT IS PASSED TO THE
.*       IKJEHSIR EXIT ROUTINE VIA THE PARAM= PARAMETER OF THE STAE
.*       MACRO.
.*
.*       &FLAG  IS THE FLAG TO SET IN THE BLOCK, CAN BE ONE OF TWO
.*             SETTINGS:  CP OR TMP.
.*             IF IT IS CP, THE DEFAULT, IT SAYS THE CP WANTS TO HANDLE
.*             SOME ABENDS.  IF IT IS TMP, IT SAYS THAT THE CP WANTS
.*             THE TMP TO HANDLE ALL ABENDS.
.*       &PSEUDO  THE ADDRESS OF THE PSEUDO RETRY ROUTINE BUILT WITH
.*             THE PSEUDO MACRO.  FOR THE FULL EXPLAINATION OF THE
.*             PSEUDO RETRY ROUTINE, SEE THE PSEUDO MACRO.
.*       &LIST  THIS IS THE ADDRESS OF THE RETRY CODE TABLE BUILD
.*             WITH THE STABUILD MACRO. FOR A FULLER EXPLAINATION,
.*             OF THE RETRY CODE TABLE SEE THE STABUILD MACRO.
.*       &ADDR  THIS IS THE ADDRESS OF THE MF=L FORM OF THIS MACRO.
.*             IT IS USED BY THE MF=E MACRO TO FILL IT IN, OR IT IS
.*             USED BY THE MF=L FORM AS A NAME IF NO NAME IS PROVIDED
.*
         LCLA  &FNUM
         LCLB  &F(8)
.*
.*       IF NO FLAG IS GIVEN, DEFAULT TO CP
.*
         AIF   (T'&FLAG EQ 'O').SET1
.*
.*       LOOP THRU THE FLAG POSSIBILITIES AND SET SOME BITS
.*
&FNUM    SETA  N'&FLAG   NUMBER OF FLAG SETTINGS
.LOOP1   AIF   ('&FLAG(&FNUM)' NE 'CP').LOOP2
&F(8)    SETB  1
.LOOP2   AIF   ('&FLAG(&FNUM)' NE 'TMP').LOOP3
&F(7)    SETB  1
.LOOP3   ANOP
&FNUM    SETA  &FNUM-1
         AIF   (&FNUM NE 0).LOOP1
         AIF   (&F(8) EQ 1 AND &F(7) EQ 1).BADFLAG  CANT BE BOTH
&FNUM    SETA  &F(7)*2+&F(8)
         AGO   .SET
.SET1    ANOP
&FNUM    SETA  1        DEFAULT TO CP
.SET     ANOP
.*
.*             IF ITS LIST FORM, GO CHECK ITS REQUIREMENTS
.*
         AIF   ('&MF' EQ 'L').MFL
.*
.*   EXECUTE FORM OF MACRO
.*
         AIF   (T'&PSEUDO EQ 'O').NOPS   NEED PSEUDO PARM
         AIF (&F(7) EQ 1).ADDR  DONT NEED LIST IF TMP SPECIFIED
         AIF   (T'&LIST EQ 'O').NOLST    NEED A LIST
.ADDR    AIF   (T'&ADDR EQ 'O').NOADDR  NEED AN ADDRESS
         SPACE 1
.*
.*             CHECK FOR THINGS BEING IN REGISTERS
.*
         AIF   ('&ADDR'(1,1) EQ '(').REGAD
         AIF   (T'&LIST EQ 'O').NLIST  MAY NOT HAVE A LIST
            AIF ('&LIST'(1,1) EQ '(').RLIST
&NAME    LA    15,&LIST            LIST OF CODES OR POINTERS
         AGO   .NLIST1             BYPASS
.NLIST   ANOP
&NAME    LA    15,0                LIST OMITTED
.NLIST1  ST    15,&ADDR            STORE LIST POINTER
         AGO   .RLIST1
.RLIST   ANOP
&NAME    ST    &LIST(1),&ADDR      LIST OF CODES
.RLIST1  MVI   &ADDR,&FNUM         STORE FLAG
         AIF   (T'&PSEUDO EQ 'O').NPSE  MAY NOT NEED IT
           AIF ('&PSEUDO'(1,1) EQ '(').RPS
         LA    15,&PSEUDO          PSEUDO RETRY ROUTINE
         AGO   .NPSE1              VYPASS
.NPSE    LA    15,0                PSEUDO POINTER OMITTED
.NPSE1   ST    15,&ADDR+4          STORE PSEUDO POINTER
         AGO   .END
.RPS     ST    &PSEUDO(1),&ADDR+4  STORE PSEUDO RETRY ROUTINE
.END     SPACE 1
         MEXIT
.REGAD   AIF   (T'&LIST EQ 'O').NLIST2  NEED NOT HAVE IT
&NAME    LA    15,&LIST            LIST OF CODES OR POINTERS
         AGO   .NLIST3
.NLIST2  ANOP
&NAME    LA    15,0                LIST OMITTED
.NLIST3  ST    15,0(&ADDR(1))      STORE LIST POINTER
         AGO   .REGAD2
.REGAD1  ANOP
&NAME    ST    &LIST(1),0(&ADDR(1))  LIST OF CODES
.REGAD2  MVI   0(&ADDR(1)),&FNUM   STORE FLAG BYTE
         AIF   (T'&PSEUDO EQ 'O').PSE2
          AIF ('&PSEUDO'(1,1) EQ '(').REGAD3
         LA    15,&PSEUDO          PSEUDO RETRY ROUTINE
         AGO   .PSE3
.PSE2    LA    15,0                PSEUDO POINTER OMITTED
.PSE3    ST    15,4(&ADDR(1))      STORE PSEUDO POINTER
         AGO   .END
.REGAD3  ST    &PSEUDO(1),4(&ADDR(1))  STORE PSEUDO RETRY ROUTINE
         AGO   .END
.*
.*                            LIST FORM
.*
.*       NEED EITHER A NAME OR AN ADDR FOR MAKING A NAME FOR THIS
.*
.MFL     AIF   (T'&NAME EQ 'O' AND T'&ADDR EQ 'O').BOTH
.*
.*             THE NAME WILL OVER-RIDE THE ADDR PARAMETER
.*
         AIF   (T'&ADDR EQ 'O').NAME
         AIF   (T'&NAME NE 'O').NAME
.*
.*             IF ADDR IS REG-FORM, NO GOOG
.*
         AIF   ('&ADDR'(1,1) EQ '(').BADADR
&ADDR    DS    0F                  FULLWORD ALIGNMENT
         AGO   .DCNUM
.NAME    ANOP
&NAME    DS    0F                  FULLWORD ALIGNMENT
.DCNUM   AIF   (T'&FLAG EQ 'O').DCNUM1     NEED NOT AHVE A FLAG
         DC    AL1(&FNUM)          FLAG BYTE
         AGO   .DCNUM2      BYPASS
.DCNUM1  DC    AL1(0)               FLAG BYTE
.DCNUM2  AIF   (T'&LIST EQ 'O').MFL1
         DC    AL3(&LIST)          POINTER TO CODES
         AGO   .MFL2
.MFL1    DC    AL3(0)              POINTER TO CODES
.MFL2    AIF   (T'&PSEUDO EQ 'O').MFL3
         DC    A(&PSEUDO)          PSEUDO RETRY ROUTINE
         AGO   .END
.MFL3    DC    A(0)                PSEUDO RETYR ROUTINE
         AGO   .END
.NOPS    MNOTE 8,'PSEUDO PARAMETER OMITTED'
         SPACE 1
         MEXIT
.BADFLAG  MNOTE  8,'CANT HAVE BOTH FLAG=CP AND TMP'
         SPACE 1
         MEXIT
.NOLST   MNOTE 8,'LIST PARAMETER OMITTED'
         SPACE 1
         MEXIT
.NOADDR  MNOTE 8,'ADDR PARAMETER OMITTED'
         SPACE 1
         MEXIT
.BOTH    MNOTE 8,'BOTH ADDR AND NAME PARAMETERS OMITTED'
         SPACE 1
         MEXIT
.BADADR  MNOTE 8,'CANNOT NAME STAEPARM WITH A REGISTER'
         SPACE 1
         MEXIT
         MEND
