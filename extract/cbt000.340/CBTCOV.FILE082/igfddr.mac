         MACRO
         IGFDDR  &MCS
         SPACE 2
IGFDDRSR CSECT
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*                            SYSRES DDR                               *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*STATUS                                                               *
*                                                                     *
*     RELEASE 21.8
*                                                                     *
*FUNCTION                                                             *
*                                                                     *
*   TO RECOVER SVC FETCH OPERATIONS BY SWAPPING SYSRES TO ANOTHER     *
*   DEVICE,AND TO PERFORM SWAP AT THE REQUEST OF THE OPERATOR.VERY    *
*   BRIEFLY SPEAKING,THE OPERATIONS ARE AS FOLLOWS,                   *
*   1) TIE UP SYSRES UCB,                                             *
*   2) LOCK OUT ALLOCATION,                                           *
*   3) TEST AND TIE UP 'TO' DEVICE,                                   *
*   4) SWAP UCB INFORMATION,ETC.,                                     *
*   5) WRITE MESSAGES TO OPERATOR,                                    *
*   6) VERIFY VOLID AFTER A SWAP,                                     *
*   7) RECYCLE THE SWAP,IF NECESSARY.                                 *
*                                                                     *
*ENTRY POINTS                                                         *
*                                                                     *
*   FOR SYSTEM INITIATED SWAP,FROM IEAATC00 IN MFT,FROM IEAQTR33 OR   *
*   IEAQNU00 IN MVT,FROM IEAQTAMP OR IEAQNU00 IN MP.                  *
*   FOR OPERATOR INITIATED SWAP,FROM IGFDDR01.                        *
*   FROM ALL THESE CALLING ROUTINES,THE ENTRY POINT IS                *
*        IGFDDRSR
*   FOR SYSTEM INITIATED SWAP,THE RESIDENT DISK ERP ALSO CALLS SYSRES *
*   DDR.THE ENTRY POINT IS                                            *
*        IGFDDR05                                                     *
*                                                                     *
*EXTERNAL REFERENCES                                                  *
*                                                                     *
*   RQE USED EXCLUSIVELY BY SYSRES DDR                                *
*        DDRRQESR                                                     *
*   DDR APPENDAGE VECTOR TABLE IN IOS                                 *
*        DDRAPNVT                                                     *
*   I/O ERROR RECYCLE COUNT UPDATED BY IEAATC00 OR IEAQNU00           *
*        ERCOUNT                                                      *
*   SAVE AREA IN IOS FOR THE UCB OF 'TO' DEVICE CANDIDATE             *
*        DDRSRTO                                                      *
*   FIRST TCB OF THE TCB CHAIN                                        *
*        IEAHEAD                                                      *
*                                                                     *
*INPUT                                                                *
*                                                                     *
*   FROM IGFDDR01 REGISTER 11 POINTS TO DDR COMM AREA                 *
*                                                                     *
*OUTPUT                                                               *
*                                                                     *
*   MESSAGES                                                          *
*                                                                     *
*EXITS,NORMAL                                                         *
*   TO CALLER WITH RETURN CODE 0 IN REGISTER 15,INDICATING SWAP DONE  *
*                                                                     *
*EXITS,ERROR                                                          *
*                                                                     *
*   TO CALLER WITH RETURN CODE 4 IN REGISTER 15,INDICATING SWAP FAILED*
*                                                                     *
*TABLES/WORK AREA                                                     *
*                                                                     *
*   THE TABLES USED ARE CVT,IORMS COMM AREA,UCM BASE,UCM PREFIX,      *
*   UCM BASE,UCM ENTRY,AND DCM.                                       *
*   WORK AREA INCLUDES ONE BYTE OF SWITCHES.                          *
*                                                                     *
*ATTRIBUTES                                                           *
*   DISABLED,SUPERVISOR MODE,PROTECTION KEY 0,SERIALLY REUSABLE,      *
*   AND RESIDENT.                                                     *
*                                                                     *
*NOTES                                                                *
*                                                                     *
*   IBM I/O RECOVERY STANDARDS ARE NOT FOLLOWED DUE TO THE FACT THAT  *
*   SYSRES IS NOT ACCESSIBLE DURING THE SWAP OPERATION                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*                            ENTRY POINTS                             *
*                                                                     *
***********************************************************************
   SPACE    1
         ENTRY IGFDDR05            SYSRES DDR COUNT ROUTINE
         ENTRY IGF0IOB             IOB ADDR
         ENTRY IGF0DEB             DEB ADDR
   SPACE    1
***********************************************************************
*                                                                     *
*                              REGISTERS                              *
*                                                                     *
***********************************************************************
   SPACE    1
RG0      EQU   0                   REGISTER 0
RQERG    EQU   1                   PTS TO SYSRES DDR RQE
RG1      EQU   1                   REG 1
IOBRG    EQU   2                   POINTS TO IOB
DEBRG    EQU   3                   PTS TO A DEB
CVTRG    EQU   3                   PTS TO CVT
WORKRG2  EQU   4                        SECOND WORK REGISTER
WORKRG3  EQU   5                        THIRD WORK REGISTER
         AIF   (&MCS NE 1).AB1
MSGPTR   EQU   2                   PTS TO A MESSAGE
.AB1     ANOP
FROMUCB  EQU   6                   SYSRES UCB PTR
RG6      EQU   6                   REGISTER 6
UCBRG    EQU   7                   PTS TO A UCB
UCMPTR   EQU   8                   PTS TO UCM BASE
MCSPTR   EQU   8                   PTS TO MCS PREFIX
LINKRG   EQU   8                        MAIN LINKRG
DCMPTR   EQU   9                   PTS TO DCM
LINKRG1  EQU   9                   SECOND LINKRG
WORKRG4  EQU   10                  WORK REGISTER
UCMENTRY EQU   10                  PTS TO A UCM ENTRY
COUNT    EQU   10                  COUNT REG USED IN APPENDAGES
DDRCMPTR EQU   11                  DDR COMM AREA PTR
WORKRG5  EQU   11                  WORK REGISTER IN COUNT ROUTINE
VTABPTR  EQU   11
BASE     EQU   12                  BASE REGISTER
WORKRG1  EQU   13                  MAIN WORK REG
RG13     EQU   13                  REG 13
TDCMPTR  EQU   13                  GRAPHICS TRANSIENT DCM PTR
LINKRG2  EQU   14                  SECOND LINK REG
RTN      EQU   14                  RETURN REGISTER
RG15     EQU   15                  REGISTER 15
         AIF   (&MCS  NE  0).AB2
MSGPTR   EQU   15                  PTS TO A MESSAGE
.AB2     ANOP
         EJECT
***********************************************************************
*                       UCB FLAGS AND OFFSETS                         *
***********************************************************************
   SPACE    1
UCBFL1   EQU   6                   UCB FLAG FLD OFFSET
UCBNAME  EQU   13                  EBCIDIC UNIT NAME FLD OFFSET
UNITYP   EQU   16                  UNIT TYPE FLD OFFSET
UCBSNS   EQU   22                  UCB SENSE FLD
SRTEVOLI EQU   28                  UCB VOLID FLD OFFSET
UCBNRY   EQU   X'40'               UCB NOT READY FLAG
UCBERR   EQU   X'01'               UCB ERROR FLAG
   SPACE    1
***********************************************************************
*                       IOB FLAGS AND OFFSETS                         *
***********************************************************************
  SPACE 1
IOBFL1   EQU   0                   IOB FLAG 1 OFFSET
IOBCOD   EQU   4                   IOB ECB CODE FLD OFFSET
IOBCSW   EQU   9                   IOB CSW FLD
IOBSIOCC EQU   16                  SIO CONDITION CODE
IOBURL   EQU   X'02'               IOB UNRELATED FLAG
IOBEX    EQU   X'04'               EXEPTIONAL CONDITION
DEBTCB   EQU   0                   OFFSET OF TCB FLD IN DEB
  SPACE 1
***********************************************************************
*                       WORK SWITCHES                                 *
***********************************************************************
  SPACE 1
WTO      EQU   X'80'               SWITCH FOR WTO REQUEST
NOP      EQU   X'40'               SWITCH FOR NOP REQUEST
READD    EQU   X'20'               SWITCH FOR READ REQUEST
SWAPP    EQU   X'10'               SWITCH FOR SWAPP REQUEST
OPFIRST  EQU   X'08'               FIRST OPER.SWAP RECOVERY
SCNDNTRY EQU   X'04'               APPENDAGE 2ND ENTRY SWITCH
INTERVN  EQU   X'02'               INTERVENTION REQUIRED
HARDCOPY EQU   X'01'               HARD COPY SWITCH
         EJECT
***********************************************************************
*                       MISCELLANEOUS MASKS AND OFFSETS               *
***********************************************************************
     SPACE  2
ZERO     EQU   0                   OFFSET 0
O        EQU   0                   ZERO
EXCP     EQU   0                   ZERO
L1       EQU   1                   ONE
I        EQU   1                   ONE
II       EQU   2                   TWO
L2       EQU   2                   TWO
L3       EQU   3                   THREE
III      EQU   3                   THREE
FOUR     EQU   4                   CURRENT TCB OFFSET
IV       EQU   4                   FOUR
L4       EQU   4                   FOUR
FIVE     EQU   5                   FETCH RECYCLE COUNT
V        EQU   5                   FIVE
L6       EQU   6                   SIX
VII      EQU   7                   SEVEN
EIGHT    EQU   8                   DECIMAL 8
NOTZERO  EQU   7
OFF      EQU   8
ON       EQU   1
HEADTCB  EQU   160                 ADDRESS OF FIRST TCB IN SYSTEM
VIII     EQU   8                   EIGHT
L8       EQU   8                   EIGHT
TEN      EQU   10                  TEN TIMES
XII      EQU   12                  DECIMAL 12
XVI      EQU   16                  DECIMAL 16
XXVIII   EQU   28                  DECIMAL 28
XSTAB    EQU   10                  STATUS AND ATTRIBUTE BYTES OFFSET
XVIII    EQU   18                  DECIMAL 18
XX       EQU   20                  DECIMAL 20
XXV      EQU   25                  DECIMAL 25
XXXII    EQU   32                  DECIMAL 32
XL       EQU   40                  DECIMAL 40
D0       EQU   X'D0'              TRANSIENT SVRB MASK
LII      EQU   52                  DECIMAL 52
PERMEROR EQU   X'41'               PERM I/O ERROR
CC3      EQU   X'30'               CONDITION CODE 3
GRAPHICS EQU   X'10'               GRAPHIC DEVICE
NONDISP  EQU   1                   ALLOCATION NON-DISPATCHABLE
ACTIVE   EQU   X'10'               DEVICE IS ACTIVE
A2311    EQU   X'01'                   2311 DEV TYPE MASK         00226
A2321    EQU   X'05'                   2321 DEV TYPE MASK         00226
A2314    EQU   X'08'               DASD,2314
FF       EQU   X'FF'               HEX FF
COMPLET  EQU   X'7F'               ECB COMPLETION CODE
RQEUCB   EQU   2                   RQE UCB FLD OFFSET
RQETCB   EQU   13                  RQE TCB FLD OFFSET
TCBDSP   EQU   35                  TCB DISPATCHING PRIORITY FLD OFFSET
TCBPKE   EQU   28                  TCB PROTECTION KEY FLD OFFSET
TCBFLGS  EQU   33                  DECIMAL 33
UNITRCOD EQU   X'08'               UNIT RECORD DEVICE
MFT      EQU   X'20'               MFT FLAG IN CVT
NIPIN    EQU   X'10'
Z        EQU   X'E9'               CHARACTER Z
CHANCHK  EQU   X'0F'             CHANNEL CHECKS BIT SETTING
EROREXCP EQU   15                  DECIMAL 15
ZEROFOX  EQU   15                  HEX F
SIRB     EQU   X'80'               BIT SETTING FOR SIRB
UNITYPII EQU   UNITYP+III
UNITYPI  EQU   UNITYP+II
RQETCBF  EQU   12                  TCB FLD OFFSET IN RQE
IOBCSW2  EQU   IOBCSW+III
IOBCSW3  EQU   IOBCSW+IV
INTREQ   EQU   X'40'           BIT SETTING FOR INTERVENTION-REQUIRED
TCBRG12  EQU   97                  REG 12 SAVE AREA OFFSET
TCBRG14  EQU   105                 REG 14 SAVE AREA OFFSET
RBOPSW   EQU   21                  RESUME PSW FLD IN RB
RBWCT    EQU   28                  RB WAIT COUNT FLD
XXIV     EQU   24
OPSWAP1  EQU   X'01'               OP INIT NOP PREEMPTION SWITCH
OPSWAP2  EQU   X'02'               OP INIT READ PREEMPTION SWITCH
VOLID    EQU   X'04'               WHEN ON, VOLID'S DO NOT MATCH
N1       EQU   1
N2       EQU   2
N3       EQU   3
N19      EQU   19
N26      EQU   26
N228     EQU   228
N0       EQU   0
TCBADDR  EQU   4                 REG 4  ADDR ALLOC TCB
TJBSIZE  EQU   4                  REG 4  TJB SIZE
TJOBID   EQU   5                  REG 5  TJID OF TSO TASK
TJBADDR  EQU   13                 REG 13  BASE FOR TJB DSECT
TSCVTPTR EQU   13                 REG 13  TSO CVT PTR
R5       EQU   5                  REG 5   =TJB SIZE X TJID
TCBFLGS5 EQU   33                 DISPL FOR PRIMARY NON DISP BIT
TCBNDSP1 EQU   173                DISPL FOR OTHER NON-DISP BITS
TSOREADY EQU   X'80'              TSO READY BIT
NDCTL    EQU   X'01'              TCB PRIMARY NON-DISP BIT
TCBDDRND EQU   X'08'              TCB NON-DISP BIT FOR DDR
DDRXCPSR EQU   36
         EJECT
***********************************************************************
*                                                                     *
*                  SYSRES EFFECTOR                                    *
*                                                                     *
***********************************************************************
   SPACE  2
         USING *,RG15              USE REG 15 AS TEMPORARY BASE
         STM   RG0,RTN,REGSAVE     SAVE REGISTERS
         BALR  BASE,RG0            SET UP BASE REG
         USING *,BASE              USE REGISTER 12 AS BASE
  SPACE  1
IGF100   NI    SYSFLG1,ZERO        INITIALIZE SWITCHES
         MVI   SWAPCNT,EIGHT       INITIALIZE SWAP COUNT
         L     CVTRG,CVTPTR        PICK UP PT TO CVT
         USING IGF00CVT,CVTRG           SET CVT ADDRESSABILITY
         L     WORKRG1,CVTDCB      PICK UP PT TO LOGREC DCB
         L     DDRCMPTR,O(WORKRG1)  REG 11 PTS TO DDR COMM AREA
         USING IORMS,DDRCMPTR      USE REG 11 AS IORMS DSECT BASE
   SPACE  1
*
*       PREVENT ALLOCATION FROM PROCESSING
*      DETERMINE IF ALLOCATION IS ACTIVE
*        IF NO INITIALIZE ALLOC. ECB SO ALLOCATION WILL WAIT
*        IF YES,DDR OR TSO SUPERVISOR SETS TCB IN ALLOC. NON-DISP.
*
IGF101    NC    ALLOCTCB+N1(N3),ALLOCTCB+N1  DID ALLOC STORE A TCB
          BNZ   CHKTSO                        YES,IS IT A TSO TASK
*
ZEROECB   EQU   *                             INITIALIZE ALLOC ECB
*
          MVI   ALLOCECB,ZERO              ALLOC WILL WAIT ON THIS ECB
          B     IGF102             GO GET SYSRES UCB
*
CHKTSO    EQU   *                IS TSO TASK IN ALLOCATION
*
         NC    ALLOTJID(N2),ALLOTJID                        IS TASK TSO
         BZ    SETNDISP           NO,SET IT NON-DISP
         TM    N228(CVTRG),TSOREADY      YES,IS TSO READY
         BZ    ZEROECB            NO,THEN ZERO ALLOC. ECB
*
*      TSO IS READY. LOCATE TJB FOR TSO TASK
*
         L     TSCVTPTR,N228(CVTRG)    GET TSO CVT PTR
         LH    TJBSIZE,N26(TSCVTPTR)   GET TJBSIZE
         N     TJBSIZE,ZEROHIGH        CLEAR 2 H.O. BYTES
         MH   TJBSIZE,ALLOTJID        TJB SIZE  X  TJID
         A     TJBSIZE,N0(TSCVTPTR)      + TJB TABLE ORIGIN
         LR    TJBADDR,TJBSIZE           = TJB ADDR
         ST    TJBADDR,TJBSAVE          SAVE TJB ADDRESS
*
         USING TJB,TJBADDR         MAKE TJB ADDRESSABLE
         TM    TJBSTAT,TJBINCOR         IS TASK IN CORE
         BZ    TSOSETND           NO,TELL TSO WHAT TO DO
*
*      TASK IS IN CORE DDR WILL SET IT NON DISPATCHABLE
*
         L      TCBADDR,ALLOCTCB     GET TCB ADDR
         OI    TCBFLGS5(TCBADDR),NDCTL  SET PRIMARY NON-DISP BIT
         OI    TCBNDSP1(TCBADDR),TCBDDRND  SET BIT FOR DDR
*
         TSEVENT ENQWAIT,TJID=(TJOBID) TSO SWAP TASK OUT NOW ID  21048
         B     IGF102             GO GET SYSRES UCB
*
TSOSETND EQU   *
*
*
*      TSO WILL SET TASK NON-DISP IF IT IS PUT INTO CORE
*
         OI    TJBRSTOR,TJBDDRND  INFORM TSO RESTORE MODULE
         B     IGF102             GO GET SYSRES UCB
*
SETNDISP EQU   *                  DDR WILL SET TASK NON-DISP.
         L     TCBADDR,ALLOCTCB   GET ALLOC TCB ADDR
         OI    TCBFLGS5(TCBADDR),NDCTL  SET PRIMARY BIT
         OI    TCBNDSP1(TCBADDR),TCBDDRND  SET BIT FOR DDR
*
   SPACE  1
IGF102   L     FROMUCB,CVTSYSAD    PICK UP SYSRES UCB
         MVC   MSG1B(L3),UCBNAME(FROMUCB)  SET MSG1 'FROM' ADDR
         C     DDRCMPTR,RG11SAV    IS SWAP OPERATOR INITIATED
         BNE   IGF111              NO,BRANCH OUT
         LA    LINKRG,IGF107       YES,SET UP LINK REGISTER
         OI    SYSFLG2,OPSWAP1     SET OP INIT NOP PREEMPT SWITCH
         LR    UCBRG,FROMUCB       REG 7 PTS TO SYSRES UCB
   SPACE  1
IGF103   LA    WORKRG1,NOPCCW      PICK UP NOP CCW
         OI    SYSFLG1,NOP         SET INDICATOR FOR APPENDAGES
         L     WORKRG2,ADDRSRTO    PICK UP UCB SAVE AREA ADDR IN IOS
         STH   UCBRG,O(WORKRG2)    SAVE 'TO' CANDIDATE UCB ADDR FOR IOS
  SPACE 1
IGF104   ST    WORKRG1,IOBSTART    SET IOB START FLD
IGF105   LA    WORKRG2,TEN         INITIALIZE SIO CC 3 RETRY COUNTER
IGF105A  STH   UCBRG,DEBUCB        SET DEB UCB FLD
         L     WORKRG1,CVTTCBP     PICK UP NEW AND CURRENT TCB FLD PTR
         MVC   DEBTCBAD(L3),V(WORKRG1)  SET DEB TCB FLD
         MVI   COUNTSAV,TEN        RESET APPENDAGE RETRY COUNTER
         NI    IGF0ECB,ZERO        RE-INITIALIZE ECB
         TM    SYSFLG1,WTO         IS I/O FOR WTO
         BCR   I,LINKRG            YES,RETURN TO CALLING SUBROUTINE
     SPACE 2
         L     RQERG,IGF0RQE       PICK UP SYSRES RQE ADDR
         TM    CVTDCB,MFT          IS THIS AN MFT SYSTEM
         BO    IGF105B             YES,SKIP NEXT INSTRUCTION
         MVC   RQETCB(L3,RQERG),V(WORKRG1)  SET RQE TCB FLD
IGF105B  STH   UCBRG,RQEUCB(RQERG) SET RQE UCB FLD
         NI    SYSFLG1,FF-SCNDNTRY-INTERVN  RESET SWITCHES
   SPACE    1
         SVC   EROREXCP            ISSUE ERROR EXCP
   SPACE    1
         TM    SYSFLG1,SWAPP       IS SWAP SWITCH ON
         BCR   I,LINKRG            YES,RETURN TO CALLING SUBROUTINE
   SPACE  1
IGF106   WAIT  ECB=IGF0ECB         WAIT ON I/O
   SPACE  2
*   REGAINING CONTROL AFTER COMPLETION OF I/O
         SPACE 1
         NI    SYSFLG2,FF-OPSWAP1  TURN OFF NOP PREEMPT SWITCH
IGF106B  SSM   BUFADR              DISABLE I/O INTERRUPT AGAIN
         TM    IOBSIOCD,CC3    IS SIO CONDITION CODE 3
         BNO   IGF106A             NO,BRANCH OUT
         BCT   WORKRG2,IGF105A     YES,RETRY 10 TIMES
IGF106A  L     WORKRG2,ADDRSRTO    PICK UP UCB SAVE AREA ADDR IN IOS
         XC    0(2,WORKRG2),0(WORKRG2)   RESET UCB SAVER IN IOS
         NI    SYSFLG1,FF-NOP-READD-WTO  TURN OFF SWITCHES
         CLI   IGF0ECB,COMPLET     IS I/O SUCCESSFUL
         BR    LINKRG              RETURN TO CALLING SUBROUTINE
     SPACE  1
IGF107   LH    UCBRG,DDRTOUCB      PICK UP OPERATOR SPECIFIED UCB
         BNE   IGF215              I/O ERROR BRANCH OUT
         OI    SYSFLG2,OPSWAP2     SET OP INIT READ PREEMPT FLAG
   SPACE  1
IGF108   BAL   LINKRG,IGF103       GO TO TEST 'TO' DEVICE
         BE    IGF109              I/O SUCCESSFUL,GO TO SWAP UCB INFO
         C     DDRCMPTR,RG11SAV    IS SWAP OPERATOR INITIATED
         BNE   IGF204A             NO,READY QUIT
         NI    UCBFL1(FROMUCB),FF-UCBERR  RESET UCBERR ON SYSRES
         B     IGF215              GET READY TO QUIT
   SPACE  1
IGF109   LA    LINKRG1,IGF114      SET UP 2ND LINK REG
IGF109A  STH   UCBRG,IGFTOUCB      SET UP 'TO' SWAP PARAMETER
         STH   FROMUCB,IGFRMUCB    SET 'FROM' UCB FLD FOR SWAP
IGF110   OI    SYSFLG1,SWAPP       SET SWITCH FOR SWAP
         BAL   LINKRG,IGF105A      GO TO SWAP UCB INFO
         NI    SYSFLG1,FF-SWAPP    TURN OFF SWITCH FOR SWAP
         OI    UCBFL1(UCBRG),UCBERR  TIE UP FOR POSSIBLE RECOVERY
         BR    LINKRG1             RETURN TO CALLING SUBROUTINE
   SPACE  1
IGF111   TM    SYSFLG2,OPSWAP1     WAS OP INIT NOP PREEMPTED
         BZ    IGF111B             NO, BRANCH OUT
         LR    UCBRG,FROMUCB       PUT SYSRES UCB ADDR IN REG 7
         BAL   LINKRG1,IGF110      GO TO DEQUEUE THE RQE
IGF111B  TM    SYSFLG2,OPSWAP1+OPSWAP2  WAS NOP OR READ PREEMPTED
         BZ    IGF111A             NO, BRANCH OUT
         L     WORKRG1,RMSTCB      PICK UP DDR TCB ADDR
         L     WORKRG2,O(WORKRG1)  PICK UP DDR RB ADDR
         MVC   TCBRG12(3,WORKRG1),TCBRG14(WORKRG1)  FIX UP REG 12
         MVC   RBOPSW(3,WORKRG2),TCBRG14(WORKRG1)  FIX UP RESUME PSW
         NI    RBWCT(WORKRG2),ZERO   FIX UP WAIT COUNT
         MVI   DDRFLGSB,EIGHT  SET CODE FOR DDR SVC
         NI    DDRFLGSD,FF-DDRMSG  TURN OFF DDR MSG SWITCH
         TM    SYSFLG2,OPSWAP2     WAS OP INIT READ PREEMPTED
         BZ    IGF111A              NO, BRANCH OUT
         NI    DDRFLGSD,FF-SYSRESBR  CONSIDER OP INIT COMPLETED
IGF111A  LH    UCBRG,ALTCUASR+II   PICK UP ALTERNATE UCB ADDR
         CR    FROMUCB,UCBRG       IS THERE AN ALT DEVICE
         BE    IGF109              NO,SWAP TO ITSELF
*   VALIDATE ALT SYSRES
         CLI   UNITYPII(UCBRG),A2311  IS DEVICE A 2311
         BE    IGF111A1            YES, 2311- USE IT
         CLI   UNITYPII(UCBRG),A2321  IS DEVICE A 2321
         BE    IGF111A1            YES,CONTINUE DDR
         CLI   UNITYPII(UCBRG),A2314  IS DEVICE A 2314 OR 3330
         BL    IGF204A             NO, QUIT
IGF111A1 CLC   N19(N1,FROMUCB),N19(UCBRG)  ARE DRIVES THE SAME
         BNE   IGF204A             NO,QUIT
         B     IGF108              YES, GO TEST ALT SYSRES
   SPACE  1
IGF114   MVC   MSG1C(L3),UCBNAME(FROMUCB)  MOVE 'TO' ADDR TO MSG1
         LA    MSGPTR,MSG1A        SET MSGPTR
         EJECT
***********************************************************************
*                                                                     *
*                  NOTIFY OPERATOR                                    *
***********************************************************************
   SPACE  2
IGF200   L     UCMPTR,CVTCUCB      PICK UP UCM BASE
         USING UCMBASE,UCMPTR      DSECT ADDRESSABILITY FOR UCM BASE
         USING UCMNTRY,UCMENTRY    DSECT ADDRESSABILITY FOR UCM ENTRY
         AIF   (&MCS NE 1).A201
         LA    WORKRG1,FOUR        PUT 4 IN WORK REG
         SR    UCMPTR,WORKRG1      SUBTRACT UCMPTR BY 4
         L     MCSPTR,O(UCMPTR)    PICK UP UCM PREFIX PT
         USING UCMPRFIX,MCSPTR     DSECT ADDRESSABILITY FOR UCM PREFIX
         L     UCMENTRY,UCMMCENT   PICK UP MASTER CONSOLE UCM ENTRY
         SPACE 1
         AGO   .A202
  SPACE  1
.A201    ANOP
IGF201   L     UCMENTRY,UCMVEA     NO,PICK UP FIRST UCM ENTRY
         L     WORKRG2,UCMVEZ      PICK UP UCM ENTRY SIZE
         L     WORKRG3,UCMVEL      PICK UP LAST UCM ENTRY
         SPACE 1
.A202    ANOP
IGF202   TM    UCMATR,ACTIVE       IS THIS CONSOLE ACTIVE
         BO    IGF206              YES,GO TO USE IT
         SPACE 1
         AIF   (&MCS  NE  1).A203
IGF209   CLC   UCMMCENT(L4),UCMALTEN  IS MASTER CNSL CHAIN EXHAUSTED
         BE    IGF204              YES,READY TO QUIT
         L     UCMENTRY,UCMALTEN   NO,PICK UP ITS ALTERNATE
         B     IGF202              TRY TO WRITE ON IT
         AGO   .A204
.A203    ANOP
IGF203   BXLE  UCMENTRY,WORKRG2,IGF202  NO,GET NEXT UCM ENTRY
.A204    ANOP
   SPACE  1
IGF204   LH    UCBRG,IGFTOUCB      PICK UP 'TO' UCB ADDR
         BAL   LINKRG1,IGF110      GO TO SWAP BACK UCB INFORMATION
IGF204A  LA    LINKRG1,IGF301A     SET UP LINK REG FOR SYSTEM SWAP
  SPACE  1
IGF215   LA    RG15,FOUR           SET ERROR RETURN CODE
IGF215A  LA    LINKRG2,IGF302      SET UP LINKRG FOR OPER. SWAP
         C     DDRCMPTR,RG11SAV    IS SWAP OPERATOR INITIATED
         BE    IGF215B             YES,BRANCH OUT
         LR    LINKRG2,LINKRG1     RESET LINKRG2
         TM    DDRFLGSD,SYSRESBR   WAS THERE AN OPER SWAP PRE-EMPTED
         BCR   VIII,LINKRG1        NO,BRANCH OUT
         B     IGF215C             SKIP NEXT INSTRUCTION
     SPACE  1
IGF215B  NI    DDRFLGSD,FF-SYSRESBR  SET OPER SWAP SWITCH OFF
IGF215C  MVI   DDRFLGSB,EIGHT      SET INDEX FOR DDR ROUTER SVC
         BR    LINKRG2             BRANCH OUT
     SPACE 2
         AIF   (&MCS NE 1).A205
IGF206   L     UCBRG,UCMUCB        PICK UP IT'S UCB
         CLI   UNITYPI(UCBRG),GRAPHICS  IS CNSL A GRAPHIC DEVICE
         BE    IGF208              YES,BRANCH OUT
         BH    IGF209              TP DEVICE BRANCH OUT
         LA    WORKRG1,WRITECCW    UNIT RECORD,PICK UP CCW
         LR    WORKRG2,WORKRG1     PUT THE SAME VALUE IN WORKRG2
         AGO   .A206
.A205    ANOP
IGF206   L     UCBRG,UCMUCB        PICK UP CONSOLE UCB
         CLI   UNITYPI(UCBRG),UNITRCOD  IS IT A UNIT RECORD DEVICE
         BNE   IGF203              NO,BRANCH OUT
         LA    WORKRG1,WRITECCW    UNIT RECORD,PICK UP CHANNEL PROG
         LR    WORKRG2,WORKRG1     SET WORKRG2=WORKRG1
.A206    ANOP
         SPACE 1
IGF207   MVC   I(L3,WORKRG2),I(MSGPTR)  SET MSG ADDRESS IN CCW
         MVC   VII(L1,WORKRG2),O(MSGPTR)  SET BYTE COUNT IN CCW
         OI    SYSFLG1,WTO+SCNDNTRY  SET SWITCHES
         AIF   (&MCS NE 1).A2061
         LR    WORKRG4,MCSPTR      SAVE PREFIX ADDRESSIBILITY
.A2061   ANOP
         BAL   LINKRG,IGF104       GO SET UP CONTROL BLOCKS
         LA    RG1,IGF0IOB         PUT IOB ADDR IN REG 1
   SPACE  1
         SVC   EXCP                ISSUE EXCP
   SPACE  1
         LA    WORKRG2,N1           SET RETRY LOOP CNT TO ONE
         BAL   LINKRG,IGF106       GO WAIT ON I/O
         SPACE 1
         AIF   (&MCS  NE  1).A207
         LR    MCSPTR,WORKRG4      RESTORE PREFIX ADDRESSIBILITY
         TM    SYSFLG1,HARDCOPY    IS HARDCOPY SWITCH ON
         BO    IGF210              YES,READY TO GO TO ATTENTION ROUTINE
         CLI   IGF0ECB,COMPLET     IS I/O SUCCESSFUL
          BE     IGFTHCU               YES, GO TEST FOR HARD COPY
          L      UCMENTRY,UCMMCENT     LOAD MASTER CONSOLE UCM
          B      IGF209                GO TRY ALTERNATE
IGFTHCU   L      UCMENTRY,UCMHCUCM     LOAD HARD COPY UCM ADDR
         LTR   UCMENTRY,UCMENTRY   IS IT ZERO
         BZ    IGF300              YES,GO TO ATTENTION ROUTINE
         OI    SYSFLG1,HARDCOPY    NO,SET HARD COPY SWITCH
         B     IGF206              GO TO WRITE HARD COPY
         SPACE 1
         AGO   .A210
.A207    ANOP
         BNE   IGF204              I/O NOT SUCCESSFUL,BRANCH OUT
         AGO   .A300
  SPACE  1
.A210    ANOP
IGF208   L     DCMPTR,UCMXB        PICK UP PTR TO DCM
         USING RDCM,DCMPTR          ADDRESSABILITY FOR RESIDENT DCM
          LTR    DCMPTR,DCMPTR    DOES DCM EXIST?                 69874
          BZ     IGF300           NO, GO TO ATTENTION RTN         69874
         L     TDCMPTR,0(DCMPTR)    GET TRANSIENT DCM BASE        21003
         USING TDCM,TDCMPTR                                       21003
*
         TM    DCMRFLGS,DCMTYPE     IS DCM TRANSIENT              21003
         BZ    FULLSCRN             NO,SET FULL SCREEN IND        21003
*
         LR    WORKRG2,TDCMPTR      DCM IS TRANSIENT              21003
         SH    WORKRG2,XFOUR        POINT TO DCM PREFIX           21003
         L     WORKRG2,0(WORKRG2)   POINT TO CONSOLE USING DCM    21003
         LA    WORKRG2,0(WORKRG2)   CLEAR FLAG BYTE               21003
         CR    WORKRG2,UCMENTRY     IS DCM IN CORE                21003
         BNE   PURJSTAT             NO,SET PURGE STATUS DISPLAYS
*
FULLSCRN OI    DCMDSTAT,DCMDSTFL    SET FULL SCREEN IND           21003
         MVI   DCMRQDEL,C'Z'        INDICATE DELETE PENDING       21003
PURJSTAT OI    DCMR3FLG,DCMRXSCN    PURGE STATUS DISPLAYS         21003
         OI    DCMR2FLG,DCMRXSFL+DCMRXDEL  INDICATE DELETE PENDING
         L     WORKRG1,XX(DCMPTR)  PICK UP CHANNEL PROG ADDR
      XR    WORKRG2,WORKRG2     ZERO OUT INSERT CHARACTER REG
         IC    WORKRG2,XX(DCMPTR)  GET INDEX VALUE
         BCTR  WORKRG2,RG0         DECREMENT 27 BY 1
         SLL   WORKRG2,III         MULTIPLY BY EIGHT
         AR    WORKRG2,WORKRG1     GET CCW ADRS FOR MSG
         B     IGF207              GO TO WRITE ON GRAPHIC DEVICE
IGF210   NI    SYSFLG1,FF-HARDCOPY TURN OFF HARD COPY SWITCH
.A300    ANOP
         EJECT
***********************************************************************
*                                                                     *
*                  SYSRES ATTENTION ROUTINE                           *
*                                                                     *
***********************************************************************
         SPACE    2
IGF300   DS    0H
         LH    FROMUCB,IGFRMUCB         REINITIALIZE UCB REG
         LR    UCBRG,FROMUCB            REINITIALIZE UCB REGISTER
         LA    WORKRG1,READCCW     WORKRG1 PTS TO CCW CHAIN
         OI    SYSFLG1,READD       SET INDICATOR FOR APPENDAGES
         NI    SYSFLG2,FF-VOLID    CLEAR VOLID NOT MATCH FLAG
         BAL   LINKRG,IGF104       SET UP CONTROL BLOCKS,SVC 15 & WAIT
  SPACE  1
         BNE   IGF306              I/O ERROR,BRANCH OUT
         TM    SYSFLG1,INTERVN     IS INTERVENTION REQUIRED
         BO    IGF308              YES,BRANCH OUT
         TM    SYSFLG2,VOLID       DID VOLID'S MATCH
         BO    IGF307              NO, BRANCH OUT
         NI    SYSFLG2,FF-OPSWAP2  CLEAR OP INIT READ PREEMPT SWITCH
  SPACE  1
         NI    DDRFLGSD,FF-DDRMSG  SET OPER. SWAP CANCELLED SWITCH OFF
         BAL   LINKRG1,IGF215A     SEE IF OPER.SWAP WAS PRE-EMPTED
         MVC   ALTCUASR+II(L2),IGFTOUCB RESET ALTERNATE SYSRES UCB
         XR    RG15,RG15           SET RETURN CODE
IGF301A  TM    DDRFLGSA,DDRBUSY    IS NON-SYSRES DDR IN PROCESS
         BO    IGF304              YES,BRANCH OUT
  SPACE  1
*
*      ALLOW ALLOCATION TO PROCEED
*      POST ALLOCATION ECB COMPLETE
*      DETERMINE IF ALLOCATION HAD BEEN PROCESSING
*        NO,THEN RESTORE REGS AND RETIRN TO CALLER
*        YES,DDR OR TSO SUPERVISOR WILL MARK TSSK DISPATCHABLE AGAIN
*
IGF302   LA    TJOBID,ALLOTJID    GET TJID ADDR
         POST  ALLOCECB,TJID=(TJOBID)  POST ALLOC ECB         ID  21048
         L     TCBADDR,ALLOCTCB   GET ALLOC TCB ADDR          ID  21048
         LTR   TCBADDR,TCBADDR    WAS A TASK USING ALLOC      ID  21048
         BZ    IGF304             NO,GO RESTORE REGS          ID  21048
*
         NC    ALLOTJID(N2),ALLOTJID    YES,IS TASK TSO
         BZ    RESETDSP           NO,SET TASK DISP            ID  21048
*
         L     TJBADDR,TJBSAVE    RESTORE TJB BASE            ID  21048
         USING TJB,TJBADDR        MAKE TJB ADDRESSABLE        ID  21048
         TM    TJBSTAT,TJBINCOR   YES,IS TASK IN CORE         ID  21048
         BO    RESETDSP           YES,SET IT DISPATCHABLE     ID  21048
*
         OI    TJBRSTOR,TJBDDRD    NO,TSO WILL SET IT DISP    ID  21048
         LH    TJOBID,ALLOTJID    GET TJID
         N     TJOBID,ZEROHIGH    CLEAR 2 H.O. BYTES
         TSEVENT USERRDY,TJID=(TJOBID)  TSO BRING IT TO CORE  ID  21048
         B     IGF304             GO RESTORE REGS             ID  21048
*
RESETDSP EQU   *                  RESET TCB NON-DISP BITS
         NI    TCBNDSP1(TCBADDR),FF-TCBDDRND  RESET DDR BIT   ID  21048
         NC    TCBNDSP1(N3,TCBADDR),TCBNDSP1(TCBADDR)         ID  21048
         BO    IGF304
         NI    TCBFLGS5(TCBADDR),FF-NDCTL  RESET PRIMARY BIT  ID 21048
*
*        RESTORE REGS AND RETURN TO CALLER
*
   SPACE  1
IGF304   LM    RG0,RTN,REGSAVE     RESTORE REGISTERS
  SPACE  1
RETURN   BR    RTN                 RETURN TO CALLER
     SPACE 2
IGF306   C     DDRCMPTR,RG11SAV    IS SWAP OPERATOR INITIATED
         BE    IGF308B             YES,SWAP SYSRES BACK
         CLI   UNITYPII(UCBRG),A2311     NO,IS IT 2311            00226
         BE    IGF306A                 YES CONTINUE DDR
         CLI   UNITYPII(UCBRG),A2321   IS DEVICE A 2321           00226
         BE    IGF306A                 YES,CONTINUE DDR           00226
         CLI   UNITYPII(UCBRG),A2314   IS DEVICE A 2314           00226
         BL    IGF204              NO,BRANCH OUT                  00226
*
IGF306A  EQU   *
         XR    WORKRG1,WORKRG1     CLEAR WORK REG
         IC    WORKRG1,SWAPCNT     PICK UP SWAP COUNTER
         BCT   WORKRG1,IGF309      RETRY SWAP
         B     IGF204              RETRY 8 TIMES AND FAILED
IGF309   STC   WORKRG1,SWAPCNT     STORE SWAP COUNT
         SPACE  2
IGF308   MVC   MSG3B(L3),UCBNAME(FROMUCB)  MOVE IN 'FROM' ADDR
IGF308A  MVC   MSG3C(L3),UCBNAME(FROMUCB)  MOVE IN 'TO' ADDR
         LA    MSGPTR,MSG3A        SET MSGPTR
         B     IGF200              GO TO NOTIFY OPERATOR
  SPACE  1
IGF308B  TM    SYSFLG1,OPFIRST     IS THIS THE 1ST SWAP BACK
         BO    IGF308              NO,BRANCH OUT
         OI    SYSFLG1,OPFIRST     SET SECOND SWAP BACK SWITCH
         EX    RG0,IGF308          MOVE IN 'FROM' ADDR
         BAL   LINKRG1,IGF110      GO TO SWAP BACK UCB INFO
         B     IGF308A             BRANCH OUT
   SPACE  1
IGF307   MVC   MSG2B(L3),UCBNAME(FROMUCB)  MOVE 'TO' ADDR TO MSG 2
         LA    MSGPTR,MSG2A        SET MSGPTR
         B     IGF200              GO TO NOTIFY OPERATOR
         EJECT
***********************************************************************
*                                                                     *
*                  APPENDAGE ROUTINE                                  *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*   THE APPENDAGE ROUTINE GETS CONTROL FROM IOS. ITS FUNCTION IS TO   *
*   ROUTE CONTROL FOR 1ST ENTRIES,TO CHECK FOR GENUINE INTERVENTION-  *
*   REQUIRED CONDITIONS,TO RETRY ERROR FOR 10 TIMES,TO TAKE C1RE OF   *
*   CONDITION CODE 3 SITUATIONS,AND TO SCHEDULE THE POSTING OF TASKS. *
*                                                                     *
***********************************************************************
   SPACE  2
         USING *,RG15              USE REG 15 AS BASE
IGF400   DS    0H
         LA    RG13,IGF506              INITIALIZE BRANCH REGISTER
         L     VTABPTR,IGFRVT           PICK UP DDR RETURN TABLE ADDR
         TM    SYSFLG1,SCNDNTRY    TEST FOR 2ND ENTRY
         BO    IGF401              YES,BRANCH OUT
         OI    SYSFLG1,SCNDNTRY    NO,SET SECOND ENTRY SWITCH
         TM    SYSFLG1,NOP         IS REQUEST FOR NOP
         BO    VIII(VTABPTR)           YES,RE-EXCP WITHOUT CONTROL
         TM    SYSFLG1,READD       IS REQUEST FOR READ
         BO    IV(VTABPTR)             YES,RE-EXCP WITH CONTROL
         TM    SYSFLG2,OPSWAP1     IS REQUEST FOR DEQUEUE
         BO    XXXII(VTABPTR)          YES,TAKE DEQUEUE ENTRY
         LA    RG0,IGFRMUCB        SWAP REQUEST,PICK UP PARAMETER
         B     XXIV(VTABPTR)           TAKE SWAP EXIT
  SPACE  1
IGF401   TM    IOBFL1(IOBRG),IOBEX  IS IOB EXCEPTION FLAG ON
         BO    IGF503              YES,BRANCH OUT
IGF402   MVI   IOBCOD(IOBRG),COMPLET  SET I/O SUCCESSFUL CODE
         TM    SYSFLG1,READD       IS THIS FOR A READ
         BO    IGF508              YES, CHECK VOLID'S
         TM    SYSFLG1,NOP         IS THIS FOR A NOP
         BZ    ZERO(RTN)           NO, POST FOR WTO
         B     XVI(VTABPTR)        YES, POST TASK WITH CONTROL
  SPACE 1
IGF502   MVI   IOBCOD(IOBRG),PERMEROR  SET PERMANENT ERROR CODE
         TM    SYSFLG1,NOP         TEST FOR NOP
         BO    XII(VTABPTR)            POST TASK WITHOUT CONTROL
         TM    SYSFLG1,READD       IS REQUEST FOR READ
         BO    XVI(VTABPTR)            YES,POST WITH CONTROL
         BR    RTN             POST FOR WTO
     SPACE 2
IGF503   LH    WORKRG4,IOBCSW2(IOBRG)  NO,PICK UP IOB CSW
         N     WORKRG4,BUFADR      ANY ERROR OTHER THAN UNIT CHECK
         BCR   NOTZERO,RG13             YES,GO TO RETRY
         TM    UCBSNS(UCBRG),INTREQ     IS INTERVENTION REQUIRED
         BCR   OFF,RG13                 NO, GO TO RETRY
         TM    IOBSIOCC(IOBRG),CC3 IS SIO CC 3
         BCR   ON,RG13                  YES, GO TO RETRY
         TM    SYSFLG1,WTO         IS I/O FOR WTO
         BO    IGF502              YES,POST PERM. ERROR
IGF504   NI    IOBFL1(IOBRG),FF-IOBEX TURN OFF IOB EXCEPTION FLAG
         OI    SYSFLG1,INTERVN     SET INTERVENTION REQUIRED SWITCH
         B     IGF402              POST TASK WITH CONTROL
  SPACE 1
IGF506   XR    COUNT,COUNT         ZERO OUT COUNT REG
         IC    COUNT,COUNTSAV      PICK UP SAVED COUNT
         BCT   COUNT,IGF507        RETRY 10 TIMES
         B     IGF502              POST PERM. ERROR
IGF507   STC   COUNT,COUNTSAV      SAVE RETRY COUNT
         B     DDRXCPSR(VTABPTR)        DO NOT ALLOW RE-SEEK
         SPACE 1
IGF508   LH    WORKRG4,IGFRMUCB    RESTORE SYSRES UCB ADDRESS
         CLC   BUFFER(L6),SRTEVOLI(WORKRG4) DO VOLID'S MATCH
         BNE   IGF509              N/, BRANCH OUT
         LH    WORKRG4,IGFTOUCB    RESTORE FROM UCB ADDRESS
         NI    UCBFL1(WORKRG4),FF-UCBERR  FREE FROM UCB
         B     XII(VTABPTR)        POST TASK WITHOUT CONTROL
IGF509   OI    SYSFLG2,VOLID       INDICATE VOLID'S DON'T MATCH
         LH    WORKRG4,IGFRMUCB          GET NEW UCB ADDR         62155
         OI    L6(WORKRG4),INTREQ       SET NOT READY             62155
         B     XVI(VTABPTR)         POST WITH CONTROL
         EJECT
***********************************************************************
*                  SYSRES COUNT ROUTINE                               *
***********************************************************************
*                                                                     *
*   THIS ROUTINE RECEIVES CONTROL FROM THE DISK ERP.                  *
*                                                                     *
*   WHEN THE RECYCLE COUNT KEPT BY THE ROUTINE RESPONSIBLE FOR THE    *
*   SVC FETCH OPERATION REACHES 5,THIS ROUTINE CHECKS THE ERROR       *
*   CONDITION.IF DDR IS APPLICABLE,RETURN CODE IS SET TO 4.IF NOT,    *
*   THE COUNT IS SET TO X'0F' WITH A RETURN CODE 0.IF THE COUNT IS NOT*
*   5,CONTROL IS PASSED BACK WITH A RETURN CODE 0.LOADING OF          *
*   APPENDAGES IS SCREENED OUT BY CHECKING FOR SYSTEM TASKS.          *
*                                                                     *
*   ERROR CONDITIONS APPLICABLE TO DDR ARE AS FOLLOWS,                *
*   CHANNEL CONTROL CHECK,CHANNEL DATA CHECK,INTERFACE CONTROL        *
*   CHECK,UNIT CHECK,DATA CHECK,SEEK CHECK,EQUIPMENT CHECK,BUS OUT    *
*   CHECK,OVERRUN,COUNT AREA CHECK,UNSAFE,ALU CHECK,UNSELECTED CHECK, *
*   END OF CYLINDER,MULTIPLE MODULE SELECT,SEEK INCOMPLETE.           *
*                                                                     *
***********************************************************************
  SPACE 1
         USING *,BASE              USE REG 12 AS BASE
IGFDDR05 STM   13,11,REGSAVE       SAVE ALL REGS BUT REG12
         L     RG6,CVTPTR          GET CVT PTR
         USING IGF00CVT,RG6        ESTABLISH CVT ADDRESSIBILITY
         TM    CVTDCB,MFT          IS THIS AN MFT SYSTEM
         BO    IGF607              YES,BRANCH OUT
         L     WORKRG2,RQETCBF(RQERG)   PICK UP TCB ADDR
         TM    TCBDSP(WORKRG2),FF  IS IT A SYSTEM TASK
         BNO   IGF601              NO,RETURN
         L     WORKRG2,O(WORKRG2)  PICK UP RB ADDR
         TM    XSTAB(WORKRG2),SIRB      IS IT THE SIRB
         BO    IGF605              YES,BRANCH OUT
         TM     XSTAB(WORKRG2),D0        IS THIS AN SVRB(TRANSIENT)
         BNO    IGF601                 RETURN TO DISK ERP
         L      WORKRG2,XL(WORKRG2)    PICK UP TACT ENTRY ADDR.
IGF600   TM    CVTOPTA,NIPIN       IS NIP IN PROCESS
         BO    IGF606              YES, DDR SHOULD NOT APPLY
         CLI   XII(WORKRG2),FIVE   IS COUNT FIVE?
         BE    IGF603              YES,BRANCH OUT
IGF601   LM    13,11,REGSAVE      RESTORE REGS EXCEPT 12
         XR    BASE,BASE          SET RETURN CODE = 0
         BR    RG13                RETURN TO DISK ERP
  SPACE 1
IGF603   LH    WORKRG3,IOBCSW2(IOBRG)  PICK UP CSW BYTES
         N     WORKRG3,IGF707      IS THERE ANY ABNORMAL STATUS
         BZ    IGF606              DDR DO NOT APPLY,RETURN
         TM    IOBCSW3(IOBRG),CHANCHK  CHANNEL CHK OR CHAINING CHK
         BNZ   IGF604              YES,BRANCH OUT
         MVC   BUFFER(L4),UCBSNS(UCBRG) MOVE SENSE INFO INTO WORK AREA
         NC    BUFFER(L4),IGF708   EXAMINE SENSE INFO
         BZ    IGF606              DDR DO NOT APPLY,RETURN
IGF604   LM    13,11,REGSAVE      RESTORE REGS EXCEPT 12
         LA    BASE,FOUR          SET RETURN CODE = 4
         BR    RG13               RETURN TO DISK ERP
  SPACE 1
IGF607   L     WORKRG2,DEBTCB(DEBRG)  PICK TCB ADDR
         CLC   I(L3,DEBRG),CVTHEAD+I   IS THIS SYSTEM FETCH TCB?
         BNE   IGF601              NO,RETURN
IGF605   L     WORKRG2,AERCOUNT    PICK UP ERROR COUNT ADRS
         B     IGF600              GO TO TEST COUNT
IGF606   MVI   XII(WORKRG2),ZEROFOX     SET COUNT TO X'0F'
         B     IGF601              RETURN
         EJECT
***********************************************************************
*                  READ,WRITE AND NOP CHANNEL PROGRAMS                *
***********************************************************************
   SPACE    1
         DS    0D
READCCW  DC    X'31'               READ VOLID SEARCH COMMAND
         DC    AL3(IOBSEEK+3)      PTS TO RECORD ADDR
         DC    X'60'               CHAINED COMMAND,SUPPRESS LENGTH
         DC    XL3'05'             BYTE COUNT=5
         DC    X'08'               TIC COMMAND
         DC    AL3(READCCW)        LOOP BACK IF NOT FOUND
         DC    X'60'               CHAINED COMMAND,SUPPRESS LENGTH
         DC    XL3'08'             BYTE COUNT=8
         DC    X'06'               READ COMMAND
         DC    AL3(BUFFER-4)       BUFFER ADDRESS
         DC    X'20'               SUPPRESS WRONG LENGTH
         DC    X'00000A'           BYTE COUNT 10
  SPACE  2
WRITECCW DC    X'09'               WRITE COMMAND FOR UNIT RECORD
         DC    3X'0'               PTS TO MSG
         DC    X'60'               CHAINED COMMAND,SUPPRESS LENGTH
         DC    3X'0'               BYTE COUNT
  SPACE  2
NOPCCW   DC    X'03'               NOP COMMAND
         DC    AL3(BUFADR)         ANY ADDRESS
         DC    X'20'               SUPPRESS WRONG LENGTH
         DC    XL3'1'              BYTE COUNT=1
         EJECT
***********************************************************************
*                                 IOB                                 *
***********************************************************************
         DS    0D
   SPACE  1
IGF0IOB  DC    X'02'               UNRELATED
         DC    4X'0'
         DC    AL3(IGF0ECB)        ADDR OF ECB
IOBCSWFD DC    2F'0'               IOB CSW FLD
IOBSTART DC    F'0'                CHANNEL PROG START FLD
IOBSIOCD EQU   IOBSTART            IOB SIO CONDITION CODE
IOBDCB   DC    A(IGF0DCB)          DCB ADDRESS
         DC    2F'0'
IOBSEEK  DC    X'0000000000000003' MBBCCHHR OF DASD VOLUME LABEL
***********************************************************************
*                                 DCB                                 *
***********************************************************************
   SPACE  1
IGF0DCB  EQU   *-44
         DC    X'0C'               IBM SUPPLIED ERP NOT NEEDED
         DC    AL3(IGF0DEB)        PT TO DEB
***********************************************************************
*                              DEB                                    *
***********************************************************************
  SPACE  1
DEBAPNDG DC    A(RETURN)           END OF EXTENT APPDGE
         DC    A(RETURN)           SIO APPENDAGE
         DC    A(RETURN)           PCI APPENDAGE
         DC    A(IGF400)           PTS TO DDR APPENDAGE RTN
         DC    A(IGF400)           PTS TO DDR APPENDAGE RTN
   SPACE  1
IGF0DEB  DC    X'0'
DEBTCBAD DC    XL3'0'              DEB TCB FIELD
         DC    F'0'
         DC    X'40'               DATA SET STATUS ,OLD
         DC    XL3'0'
         DC    X'10'               TYPE OF I/O
         DC    XL3'0'
         DC    X'01'               NO OF EXTENTS,1
         DC    XL3'0'
DEBPRTY  DC    X'FF'               DEB PRIORITY
         DC    XL3'0'
         DC    X'0F'               PROTECTION KEY 0,DEBID,F
         DC    AL3(IGF0DCB)        DEB DCB FIELD
         DC    X'04'               EXTENT SCALE,FOR DASD
         DC    AL3(DEBAPNDG)       PTS TO DEB APPENDAGE TABLE
         DC    X'C000'             FILE MASK,ALL ALLOWED
DEBUCB   DC    XL2'0'              DEB UCB FIELD
         DC    X'000000000000000000000001' BBCCHHCCHH & NO OF TRACKS
***********************************************************************
*                                 ECB                                 *
***********************************************************************
  SPACE  1
IGF0ECB  DC    F'0'                ECB
         EJECT
***********************************************************************
*                  CONSTANTS AND WORK AREA                            *
***********************************************************************
   SPACE  2
REGSAVE  DC    11F'0'              REGISTER SAVE AREA
RG11SAV  DC    F'0'                REGISTER SAVE AREA
         DC    3F'0'               REGISTER SAVE AREA
IGF0RQE  DC    V(DDRRQESR)         SYSRES DDR RQE
IGFRVT   DC    V(DDRAPNVT)         DDR RETURN VECTOR TABLE ADDR
AERCOUNT DC    V(ERCOUNT)          ERROR FETCH SEQUENCE COUNT SAVE FLD
ADDRSRTO DC    V(DDRSRTO)          'TO' CANDIDATE UCB SAVE AREA ADDR
BUFADR   DC    XL2'0'              TWO BYTES OF ZERO
         DC    X'01FF'             USED TO TEST CSW STATUS
IGFRMUCB DC    H'0'                'FROM' UCB ADDR
IGFTOUCB DC    H'0'                'TO' UCB ADDR
IGF707   DC    X'0000020F'         UNIT CHK AND CHANNEL CHK
IGF708   DC    X'3D808C27'         USED TO EXAMINE SENSE INFO
BUFFER   DC    6X'0'               BUFFER AND WORK AREA
SYSFLG1  DC    X'0'                WORK SWITCHES
SWAPCNT  DC    X'08'               SWAP COUNTER
COUNTSAV DC    X'0A'               APPDGE COUNT SAVE AREA
SYSFLG2  DC    X'0'                WORK SWITCH
         DS    0F
ZEROHIGH DC    X'0000FFFF'
TJBSAVE  DS    1F                 TJB ADDR SAVE AREA
         DS    0H
XFOUR    DC    X'0004'
   SPACE  2
***********************************************************************
*                       MESSAGES                                      *
***********************************************************************
   SPACE  2
MSG1A    DC    X'23'               BYTE COUNT OF MSG 1         @S68605P
         DC    AL3(MSG1)           ADDRESS OF MSG 1
MSG1     DC    C'IGF501D SWAP SYSRES FROM '
MSG1B    DC    C'XXX TO '
MSG1C    DC    C'YYY'
     SPACE  1
MSG2A    DC    X'23'               BYTE COUNT OF MSG 2         @S68605P
         DC    AL3(MSG2)           ADDRESS OF MSG 2
MSG2     DC    C'IGF511A WRONG VOLUME MOUNTED ON '
MSG2B    DC    C'YYY'
     SPACE  1
MSG3A    DC    X'37'               BYTE COUNT OF MSG 3         @S68605P
         DC    AL3(MSG3)           ADDRESS OF MSG 3
MSG3     DC    C'IGF507A VOLUME ON '
MSG3B    DC    C'YYY '
         DC    C'UNIDENTIFIABLE,SWAP SYSRES TO '
MSG3C    DC    C'ZZZ'
         EJECT
***********************************************************************
*                            CVT DSECT                                *
***********************************************************************
  SPACE 1
IGF00CVT DSECT
   CVT
         EJECT
***********************************************************************
*                  IORMS COMMUNICATIONS AREA DSECT                    *
***********************************************************************
  SPACE 1
         IORMSCOM
         EJECT
         IKJTJB
         EJECT
*                       MCS PREFIX DSECT                              *
  SPACE 1
UCMPRFIX DSECT
UCMMCENT DS    F                   MASTER CONSOLE UCM ENTRY
         DS    20F
UCMSFLGS DS    F                   SYSTEM CONTROL FLAGS
         DS    F
UCMHCUCM DS    F                   HARDCOPY UCM ENTRY OR 0 IF SYSLOG
  SPACE 1
*                       UCM BASE DSECT                                *
  SPACE 1
UCMBASE  DSECT
UCMXECB  DS    17F
UCMMODE  DS    X                   MODE FLAGS
         DS    3X
UCMVEA   DS    F                   FIRST UCM ENTRY
UCMVEZ   DS    F                   SIZE OF UCM ENTRY
UCMVEL   DS    F                   LAST UCM ENTRY
  SPACE 1
*                       UCM ENTRY DSECT                               *
  SPACE 1
UCMNTRY  DSECT
         DS    3F
UCMUCB   DS    F                   UCB FLD
         DS    2F
UCMSTS   DS    X                   STATUS FLAGS
UCMATR   DS    X                   ATTRIBUTE FLAGS
         DS    2X
UCMXB    DS    F              DCM OR ZERO
         DS    10X
UCMDISP  DS    2X             DISPOSITION FLAGS
UCMALTEN DS    F              ALTERNATE INPUT UCM ENTRY
UCMOAOEN DS    F              ALTERNATE OUTPUT UCM ENTRY
     AIF  (&MCS  NE  1).XY1
         SPACE  2
*                       GRAPHICS DCM DSECT                            *
     SPACE  1
RDCM     DSECT                      GRAPHICS RESIDENT DCM         21003
         DS    F                                                  21003
         DS    C                                                  21003
DCMRFLGS DS    C                                                  21003
DCMTYPE  EQU   X'40'                DCM IS TRANSIENT              21003
         DS    38C                                                21003
DCMR2FLG DS    C                                                  21003
DCMRXSFL EQU   X'80'      SCREEN IS FULL                          21003
DCMRXDEL EQU   X'08'                DELETE IS PENDING             21003
DCMR3FLG DS    C                                                  21003
DCMRXSCN EQU   X'10'                PURGE STATUS DISPLAYS         21003
*
*
TDCM     DSECT                      GRAPHICS TRANSIENT DCM        21003
         DS    60F                                                21003
DCMRQDEL DS    54C                  DELETE REQUEST BUFFER         21003
DCMDSTAT DS    C                                                  21003
DCMDSTFL EQU   X'80'                SCREEN FULL INDICATOR         21003
.XY1     ANOP
         MEND
