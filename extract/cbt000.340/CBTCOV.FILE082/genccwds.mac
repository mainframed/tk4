         MACRO
         GENCCWDS &TYPE=DSECT
         AIF   ('&TYPE' EQ 'DSECT').GO
IHKGCW   CSECT                      CHANNEL PROGRAM GENERATORS
         AGO   .GO2
.GO      ANOP
GENCCWDS DSECT ,                  DEFINITION OF GENERATOR LINKAGE
IHKGCW   EQU   *
GENMASGN EQU   *
NDXINTGN EQU   *
CHKNDXGN EQU   *
LOCNDXGN EQU   *
OLDNDX   EQU   *
NUINGN   EQU   *
WRITECK  EQU   *
WRECCHAN EQU   *
GETRECGN EQU   *
INSRTREC EQU   *
REPLCREC EQU   *
DUMMYGN  EQU   *
GETXKEY  EQU   *
RDKEY    EQU   *
NDXUGN   EQU   *
RDTLSTGN EQU   * .
RDRTCHAN EQU   * .
RDALLKGN EQU   * .
SRCHKEY  EQU   * .
RDDKEY   EQU   * .
RDRCHNGN EQU   * .
RDRTCHN0 EQU   * .
RDTCGN   EQU   *
READKWC  EQU   *
.GO2     ANOP
GEN      EQU   * .                ORIGIN OF CCW GENERATOR ROUTINE TABLE
DUMMYCW  EQU   *-GEN .            NOP.  MUST BE FIRST IN TABLE.
         DC    Y(DUMMYGN-GEN) .
GENMASCW EQU   *-GEN .            GENERATE MASTER INDEX RECORD
         DC    Y(GENMASGN-GEN) .
NDXINTCW EQU   *-GEN .            FORMAT INDEX RECORD
         DC    Y(NDXINTGN-GEN) .
CHKNDXCW EQU   *-GEN .            CHECKPOINT INDEX RECORD ADDRESS
DELNDXCW EQU   *-GEN .            RESET MASTER INDEX ENTRY
FRSTR1CW EQU   *-GEN .            UPDATE EOS FOR FIRST SEGMENT
         DC    Y(CHKNDXGN-GEN) .       IN MASTER INDEX (GENERATOR).
LOCNDXCW EQU   *-GEN .            LOCATE INDEX TRACK THROUGH MASTER
MAST1CW  EQU   *-GEN .
         DC    Y(LOCNDXGN-GEN) .       INDEX
OLDXCCWS EQU   *-GEN .            OLD INDEX CHARACTERISTICS CCW'S
         DC    Y(OLDNDX-GEN) .
NUINCW   EQU   *-GEN .
         DC    Y(NUINGN-GEN) .
RDTLSTCW EQU   *-GEN .
         DC    Y(RDTLSTGN-GEN) .
WRITCKCW EQU   *-GEN .            WRITE CHECK DATA TRACK
         DC    Y(WRITECK-GEN) .
WRCHANCW EQU   *-GEN .            OUTPUT RECORD CHAIN
         DC    Y(WRECCHAN-GEN) .
RDTCCW   EQU   *-GEN
         DC    Y(RDTCGN-GEN)
GETRECW  EQU   *-GEN .            READ A DATA RECORD
         DC    Y(GETRECGN-GEN) .
INSCW    EQU   *-GEN .            INSERT A DATA RECORD
         DC    Y(INSRTREC-GEN) .
REPRECW  EQU   *-GEN .            REPLACE A DATA RECORD
         DC    Y(REPLCREC-GEN) .
GETXKCW  EQU   *-GEN .            READ KEY WITH WRITE CHECKING
         DC    Y(GETXKEY-GEN) .
REDKWCCW EQU   *-GEN
         DC    Y(READKWC-GEN)
RDKEYCW  EQU   *-GEN .            SIMPLE READ OF KEY FIELD
         DC    Y(RDKEY-GEN) .
NDXUCW   EQU   *-GEN .
         DC    Y(NDXUGN-GEN) .
RDRTCCW  EQU   *-GEN
         DC    Y(RDRTCHAN-GEN)
RDALLKCW EQU   *-GEN
         DC    Y(RDALLKGN-GEN)
SRCHKYCW EQU   *-GEN
         DC    Y(SRCHKEY-GEN)
RDDKEYCW EQU   *-GEN
         DC    Y(RDDKEY-GEN)
RDRCHNCW EQU   *-GEN
         DC    Y(RDRCHNGN-GEN)
RDRTCW0  EQU   *-GEN
         DC    Y(RDRTCHN0-GEN)
         AIF   ('&TYPE' EQ  'DSECT').GO4
*
***********************************************************************
*
* SORTNTR - SORT THE SEARCH ARGUMENT LIST BY RECORD NUMBER AND
*        BRANCH TO THE CCW GENERATOR SUBROUTINES AS SPECIFIED.
*
*        INPUT REGISTERS:  CCWRG = ORIGIN OF CCW LIST - 8.
*                          SRCHRG = ORIGIN OF SEARCH ARGUMENT LIST - 8
*                          GR0RG = LENGTH OF SEARCH ARGUMENT LIST
*
         AGO   .GO5
.GO4     ANOP
         USING IHKGCW,BASERG
          USING RAFKNBX,KBXRG
         USING EXCPFWDS,AFWRG
         USING CNTSRCH,SRCHRG
         USING CCWMSK,CCWRG
.GO5     ANOP
SORTNTR  DS    0H .               SORT ENTRY POINT FROM SCHEDULER
CHAN001  LR    LINK1RG,GR0RG .
         SLA   LINK1RG,3 .             ESTABLISH INDEX REGISTER
         L     GR3RG,FCOUNT1 .         HI-ORDER BYTE IS RECORD NUMBER
CHAN002  CL    GR3RG,FCOUNT0(LINK1RG) .
         BNE   CHAN003 .          BRANCH IF NOT THROUGH WITH SORT
         LH    LINK1RG,GEN(GR3RG) .         PICK UP ROUTINE DISPLACEMNT
         BAL   LINK1RG,IHKGCW(LINK1RG)     AND GO TO GENERATOR
         BCT   GR0RG,CHAN001 .    BACK FOR MORE IF NOT FINISHED
         B     CHAN006 .               EXIT TABLE DRIVEN CCW GENERATOR
CHAN003  BL    CHAN004 .          BRANCH IF NOT LOWER THAN FIRST
         LA    GR1RG,SRCHARG0(LINK1RG) .
         LM    GR2RG,GR3RG,0(GR1RG) .  PICK UP NEW LOW ENTRY
         MVC   0(8,GR1RG),SRCHARG1 .   MOVE OLD LOW TO NEW'S OLD PLACE
         STM   GR2RG,GR3RG,SRCHARG1 .  STORE NEW LOW
CHAN004  SH    LINK1RG,H8KN .     BUMP TO NEXT LIST ENTRY
         B     CHAN002 .
SORTLGH  EQU   *-SORTNTR .        NON-SORT ENTRY POINT
CHAN005  L     GR3RG,FCOUNT1 .    PICK UP LINKAGE TABLE INDEX
         LH    LINK1RG,GEN(GR3RG) .    LOCATE ROUTINE DISPLACEMENT
         BAL   LINK1RG,IHKGCW(LINK1RG)     AND GO THERE
         BCT   GR0RG,CHAN005 .         CONTINUE IF MORE ARGUMENTS
CHAN006  NI   CCWFLGS0,X'FF'-CC . RESET LAST CC FLAG IF ON
GENRET   L     LINK1RG,EXRET2LK . RETURN TO SCHEDULER WITH COMPLETE
         BR    LINK1RG .               CHANNEL PROGRAM
         AIF   ('&TYPE' EQ 'DSECT').GO7
         MEXIT
.GO7     ANOP
         DROP  BASERG .
         DROP  KBXRG .
         DROP  AFWRG .
         DROP  SRCHRG .
         DROP  CCWRG .
         MEND
