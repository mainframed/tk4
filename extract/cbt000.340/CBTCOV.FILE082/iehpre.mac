         MACRO
&LABEL   IEHPRE &REG,&OPTION,&USED
         LCLA  &NEW,&SAVE,&ADDRS,&DIFF,&WORK
         AIF   ('&OPTION' EQ '').SEQ0A
&WORK    SETA  0
.SEQ0    ANOP
&WORK    SETA  &WORK+1
         AIF   (&WORK GT K'&OPTION).SEQ0A
         AIF   ('&OPTION'(&WORK,1) NE 'C').SEQ0
*
*              THIS MACRO IS USED IN THE MOVE/COPY PRPGRAM TO LINK
*              SAVE AREAS TOGETHER PRIOR TO CALLING A SUBROUTINE
*              IN ADDITION, ANY OF THE REGISTERS NORMALLY DESTROYED
*              DURING A CALL (14-1) MAY BE SAVED, CERTAIN TRACE-BACK
*              FLAGS MAY BE SET, AND A PRIVATE STORAGE AREA MAY BE
*              MAINTAINED
*
.SEQ0A   ANOP
&WORK    SETA  0
&SAVE    SETA  72
         AIF   ('&USED' EQ '').SEQ1
         AIF   ((T'&USED NE 'N') OR ('&USED'(1,1) LT '0') OR ('&USED'(1X
               ,1) GT '9')).ERR3
&DIFF    SETA  (&USED/4)*4
         AIF   (&DIFF NE &USED).ERR3
&SAVE    SETA  &USED
.SEQ1    AIF   ('&REG(1)' NE '').SEQ2
&ADDRS   SETA  1
&NEW     SETA  &SAVE
&LABEL   ST    1,&SAVE.(0,13)
         AGO   .LA
.SEQ2    AIF   (('&REG(1)' NE '14') AND ('&REG(1)' NE '15') AND ('&REG(X
               1)' NE '0') AND ('&REG(1)' NE '1')).ERR1
         AIF   (N'&REG NE 1).SEQ4
         AIF   ('&REG(1)' NE '15').SEQ3
&ADDRS   SETA  1
&NEW     SETA  &SAVE+4
&WORK    SETA  4
&LABEL   STM   15,1,&SAVE.(13)
         AGO   .LA
.SEQ3    ANOP
&ADDRS   SETA  &REG(1)+1
&NEW     SETA  &SAVE+4
&LABEL   STM   &REG(1),&ADDRS,&SAVE.(13)
         AGO   .LA
.SEQ4    AIF   (('&REG(2)' NE '14') AND ('&REG(2)' NE '15') AND ('&REG(X
               2)' NE '0') AND ('&REG(2)' NE '1')).ERR1
&DIFF    SETA  &REG(2)-&REG(1)
         AIF   ((&DIFF NE 1) AND (&DIFF NE 0-13) AND (&DIFF NE 0-14) ANX
               D (&DIFF NE 0-15)).ERR1
         AIF   (&DIFF GE 0).SEQ5
&DIFF    SETA  &DIFF+16
.SEQ5    ANOP
&NEW     SETA  &SAVE+&DIFF*4+4
         AIF   ('&REG(2)' NE '15').SEQ6
&ADDRS   SETA  1
&WORK    SETA  4
&LABEL   STM   14,1,&SAVE.(13)
         AGO   .LA
.SEQ6    ANOP
&ADDRS   SETA  &REG(2)+1
&LABEL   STM   &REG(1),&ADDRS,&SAVE.(13)
.LA      LA    &ADDRS,&NEW.(0,13)
         ST    13,4(0,&ADDRS.)
         ST    &ADDRS,8(0,13)
         LR    13,&ADDRS
         L     &ADDRS,&WORK.(0,13)
         AIF   ('&OPTION' EQ '').EXIT
&WORK    SETA  0
.SEQ7    ANOP
&WORK    SETA  &WORK+1
         AIF   (&WORK GT K'&OPTION).SEQ8
         AIF   ('&OPTION'(&WORK,1) NE 'T').SEQ7
         MVI   12(13),X'00'
.SEQ8    ANOP
&WORK    SETA  0
.SEQ9    ANOP
&WORK    SETA  &WORK+1
         AIF   (&WORK GT K'&OPTION).EXIT
         AIF   ('&OPTION'(&WORK,1) NE 'F').SEQ9
         MVI   8(13),X'FF'
.EXIT    MEXIT
.ERR1    MNOTE 1,'** REGISTER OPERAND IN ERROR'
         MEXIT
.ERR3    MNOTE 1,'** USED OPERAND IN ERROR'
         MEXIT
         MEND
