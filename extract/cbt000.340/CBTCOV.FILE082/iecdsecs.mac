         MACRO
         IECDSECS &PARMS,&EXPAND=
.*
.*  THIS MACRO IS CALLED ONLY BY OPEN/CLOSE/EOV TRANSIENT SVC MODULES.
.*  IT ACCUMULATES INDICATORS FROM SEVERAL CALLS, AND CAUSES THE
.*  SELECTED DSECTS TO BE INVOKED ON THE FINAL CALL TO THIS MACRO,
.*  WHICH IS INDICATED BY THE KEYWORD PARAMETER EXPAND=YES.
.*
.*  PARMS MAY BE ANY LIST OF CONTROL BLOCK NAMES, SEPERATED BY COMMAS.
.*  THE CONTROL BLOCK NAMES MUST BE ONES DEFINED WITHIN THIS MACRO.
.*  ALL OF PARMS IS NOT ENCLOSED BY PARENTHESIS, BUT INDIVIDUAL
.*  CONTROL BLOCK NAMES MAY BE, SO THAT SUB-QUALIFIERS MAY BE INCLUDED.
.*       IECDSECS CVT,TCB,(DCB,-DEVD,DA,TA,-DSORG,PS,IS),MAIN,WTG
.*
.*  EXPAND=ATEND,ENDALL CAN ONLY BE USED WITH ASSEMBLER H.
.*  IT CAN NOT BE USED WITH ASSEMBLER F.  IT IS USED TO OVERRIDE
.*  THE END CARD DURING ASSEMBLIES, SO THAT SEVERAL DECKS CAN
.*  BE ASSEMBLED TOGETHER JUST BY PRECEDING THE DECKS WITH
.*       IECDSECS EXPAND=ATEND          IGNORE EXPAND=YES AND END CARDS
.*       MACRO
.*       END
.*       PRINT ON,NODATA,GEN            RESTORE STANDARD PRINT DEFAULTS
.*       DROP  ,                        DROP ALL PREVIOUS USINGS
.*       MEND
.*  AND FOLLOWING THEM WITH
.*       IECDSECS EXPAND=ENDALL         EXPAND DSECTS HERE ABSOLUTELY
.*       ENDALL ,                       'END' CARD
.*
         GBLB  &SYSECDS(40)             CUMULATIVE CONTROL BLOCK INDRS
         GBLC  &LIB                     OS/VS EXPANSION SWITCH
         LCLA  &N,&NB,&M                INDEXES TO ARRAYS
         LCLC  &A(40),&B                CONTROL BLK NAMES, CURRENT PARM
.*
.*  DEFINE ACCEPTABLE CONTROL BLOCK NAMES WITH THEIR INDR SUBSCRIPTS.
.*                                      TASK ORIENTED CONTROL BLOCKS
&A(1)    SETC  'CVT'                    COMMUNICATIONS VECTOR TABLE
&A(2)    SETC  'TCB'                    TASK CONTROL TABLE
&A(3)    SETC  'RB'                     REQUEST BLOCK
&A(4)    SETC  'TIOT'                   TASK INPUT/OUTPUT TABLE
&A(5)    SETC  'SMF'                    SMF RECORD-BUILDING MACRO
&A(6)    SETC  'SMFRCD'
&A(7)    SETC  'SMFTCT'                 SMF TIMING CONTROL TABLE
&A(8)    SETC  'JMR'
&A(9)    SETC  'ICB'                    INTERRUPT CONTROL BLOCK
&A(10)   SETC  'JSCB'                   JOB STEP CONTROL BLOCK
&A(11)   SETC  'PSCB'                   PROTECTED STEP CONTROL BLOCK
&A(12)   SETC  'UPT'                    USER PROFILE TABLE
&A(13)   SETC  'TJB'
&A(14)   SETC  'TJBX'
&A(15)   SETC  'TSCVT'
&A(16)   SETC  'DSE'                    DATA SET ENTRY
&A(17)   SETC  'BASE'                   MASTER SCHEDULER LINK TABLE
.*
.*                                      DATA SET ORIENTED CONTROL BLKS
&A(20)   SETC  'DCB'                    DATA CONTROL BLOCK
&A(21)   SETC  'DEB'                    DATA EXTENT BLOCK
&A(22)   SETC  'UCB'                    UNIT CONTROL BLOCK
&A(25)   SETC  'DSCB1'                  DATA SET CONTROL BLOCK FORMAT 1
&A(26)   SETC  'DSCB2'                  DATA SET CONTROL BLOCK FORMAT 2
&A(27)   SETC  'DSCB3'                  DATA SET CONTROL BLOCK FORMAT 3
&A(28)   SETC  'DSCB4'                  DATA SET CONTROL BLOCK FORMAT 4
&A(29)   SETC  'DSCB5'                  DATA SET CONTROL BLOCK FORMAT 5
.*
.*                                      OPEN/CLOSE/EOV WORK AREAS
&A(30)   SETC  'MAIN'                   MAIN DCB WORK AREA
&A(31)   SETC  'UNITTAB'                UNIT TABLE
&A(32)   SETC  'USERLAB'                USER LABEL WORK AREA
&A(33)   SETC  'USERTOT'                USER TOTALING WORK AREA
&A(34)   SETC  'WTG'                    XCTL / WHERE-TO-GO TABLE
&A(35)   SETC  'SECCORE'                PASSWORD SECURITY WORK AREA
&A(36)   SETC  'TSENQ'                  TSO ENQ WORK AREA        Y01018
&A(37)   SETC  'IDAPDPRM'               VSAM PROB DET PARM LIST  X02989
.*
.* ....................................................................
.*
&NB      SETA  N'&SYSLIST               NUMBER OF PARAMETERS IN PARMS
&N       SETA  0                        INDEX TO PARMS
         AGO   .NULL                    GO TEST IF ANY PARMS
.LOOP1   ANOP
&B       SETC  '&SYSLIST(&N,1)'         GET CURRENT PARM
         AIF   ('&B' EQ '').NULL
&M       SETA  1                        INDEX TO CONTROL BLOCK NAMES
.LOOP2   AIF   ('&B' EQ '&A(&M)').MATCH IF CONTROL BLOCK RECOGNIZED
&M       SETA  &M+1                     INCR INDEX TO NAMES
         AIF   (&M LE 37).LOOP2         IF MORE NAMES TO TEST    X02989
         MNOTE 4,'''&B'' IS NOT A RECOGNIZABLE CONTROL BLOCK NAME.'
         AGO   .NULL
.MATCH   ANOP
&SYSECDS(&M) SETB 1                     INDICATE CONTROL BLOCK DESIRED
.*       AIF   ('&SYSLIST(&N)'(1,1) EQ '(').PAREN  IF SUB-QUALIFIERS
.NULL    ANOP
&N       SETA  &N+1                     INCR INDEX TO PARMS
         AIF   (&N LE &NB).LOOP1        IF MORE PARMS
.*
.* ....................................................................
.*
         AIF   ('&EXPAND' EQ 'ENDALL').EXPAND  BR IF VERY FINAL CALL
         AIF   (&SYSECDS(40)).EXP1      IF IGNORING EXPAND=YES
         AIF   ('&EXPAND' NE 'ATEND').EXP2  IF NOT ASM H END OVERRIDE
&SYSECDS(40) SETB 1                     INDICATE IGNORE EXPAND=YES
&B       SETC  ''                       IN ORDER TO FOOL F ASSEMBLER
ENDALL   OPSYN&B END                    ENDALL IS NOW AN END CARD
END      OPSYN&B DROP                   END IS NOW A DROP (IF NO MACRO)
.EXP1    MEXIT
.*
.EXP2    AIF   ('&EXPAND' EQ 'YES').EXPAND  BR IF FINAL CALL
         MEXIT
.*
.**********************************************************************
.EXPAND  EJECT
         SPACE 1
***********************************************************************
*                                                                     *
*              TASK-ORIENTED CONTROL BLOCKS                           *
*                                                                     *
***********************************************************************
         AIF  (NOT &SYSECDS(1)).SKIPCVT     BYPASS CVT IF NOT REQUESTED
         SPACE 1
***********************************************************************
*              COMMUNICATIONS VECTOR TABLE DEFINITION                 *
***********************************************************************
*
CVT      DSECT
*        CVT                           INVOKE CVT DSECT MACRO
         CVT
*
**********
         EJECT
.SKIPCVT ANOP                          ENTRANCE LABEL ONLY
         AIF  (NOT &SYSECDS(2)).SKIPTCB     BYPASS TCB IF NOT REQUESTED
         SPACE 1
***********************************************************************
*              TASK CONTROL BLOCK DEFINITION                          *
***********************************************************************
*
*        IKJTCB                    INVOKE TCB DSECT MACRO
         AIF   ('&LIB' EQ 'LIB1').LIB1CS0
         IKJTCB  SYS=BOTH
.LIB1CS0 ANOP
         AIF   ('&LIB' NE 'LIB1').LIB2CS1
         IKJTCB
.LIB2CS1 ANOP
*
DXTCB    EQU   TCB                      IECDSECS EQUS TO BE REMOVED
TCBPRB   EQU   TCBRBP                   MAYBE
TCBTTRN  EQU   TCBTRN
TCBPRTK  EQU   TCBPKF
         DS    X'00'
TCBFLG   EQU   TCBFLGS
TCBLODPT EQU   TCBLLS
TCBINACT EQU   TCBJPQ
TCBSIZ   EQU   TCBPIBT
TCBPRTY  EQU   TCBTCB
TCBTIME  EQU   TCBTME
TCBJSE   EQU   TCBJPQ             LIST OF INACTIVE PROGRAMS ADDR
         DS    CL24
*
TCBTCT0M EQU   TCBTCT-TCB
TCBTIOTC EQU   TCBTIO-TCB
*
**********
         EJECT
.SKIPTCB ANOP                           ENTRANCE LABEL ONLY
         AIF  (NOT &SYSECDS(3)).SKIPRB      BYPASS RB IF NOT REQUESTED
         SPACE 1
***********************************************************************
*              REQUEST BLOCK DEFINITION                               *
***********************************************************************
*
*        IKJRB                          INVOKE RB DSECT MACRO
         IKJRB
*
RBLOCK   EQU   RBSECT                   IECDSECS EQUS
RBSTABYT EQU   RBFTCKPT+X'08'
SVRB34   EQU   RBTRSVRB
PRBSIZE  EQU   PRBEND-RBSECT
SVRBOFF4 EQU   RBEXSAVE-RBSECT
SVRBOFF2 EQU   RBGRS2-RBSECT
NOTPRB   EQU   RBFTSVRB
*
**********
         EJECT
.SKIPRB  ANOP                          ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(16)).SKIPDSE  BYPASS DSE IF NOT REQUIRED
         SPACE 1
***********************************************************************
*              DATA SET ENTRY DEFINITION                              *
***********************************************************************
*
*        IKJDSE                         INVOKE DATA SET ENTRY MACRO
         IKJDSE
*
**********
         EJECT
.SKIPDSE ANOP                      ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(4)).SKIPTIO    BYPASS TIOT IF NOT REQ'ED
         SPACE 1
***********************************************************************
*              TASK INPUT/OUTPUT TABLE DEFINITION                     *
***********************************************************************
*
TIOT     DSECT
*        IEFTIOT1                      INVOKE TIOT DSECT MACRO
         IEFTIOT1
*
*  IECDSECS EQUS
TIOSPOUT EQU   X'40'                    INDICATES 'SPOOLED' SYSOUT
TIOSPLIN EQU   X'20'                    INDICATES 'SPOOLED' SYSIN
*
**********
         EJECT
.SKIPTIO ANOP                           ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(5)).SKIPSMF BYPASS SMF IF NOT REQ'D
         SPACE 1
*        IEESMCA                   INVOKE SYSTEMS MANAGEMENT
*                                    FACILITIES DSECT MACRO
         IEESMCA
*
**********
         EJECT
.SKIPSMF ANOP                            ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(6)).SKIPRCD BYPASS SMF RCD IF NOT REQ'D
         SPACE 1
SMF14RCD DSECT
*        IFASMFR (14)               INVOKE SMF RECORD-BUILDING MACRO
         IFASMFR (14)
*
REGSAVE  EQU   SMF14UCB
*
**********
         EJECT
.SKIPRCD ANOP                            ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(7)).SKIPTCT BYPASS SMF TCT IF NOT REQ'D
         SPACE 1
*
*        IEFTCT                   INVOKE SMF TIMING CONTROL MACRO DSECT
         IEFTCT
*
**********
         EJECT
.SKIPTCT ANOP                            ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(8)).SKIPJMR  BYPASS JMR IF NOT REQUIRED
         SPACE 1
*        IEFJMR                         INVOKE JOB MANAGEMENT RCD MACRO
         IEFJMR
*
**********
         EJECT
.SKIPJMR ANOP                           ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(11)).SKPPSCB  BYPASS PSCB IF NOT REQUESTED
         SPACE 1
***********************************************************************
*              PROTECTED STEP CONTROL BLOCK DEFINITION                *
***********************************************************************
*
*        IKJPSCB                   INVOKE PSCB DSECT MACRO
         IKJPSCB
*
**********
         EJECT
.SKPPSCB ANOP                      ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(9)).SKIPICB  BYPASS ICB IF NOT REQUIRED
         SPACE 1
***********************************************************************
*              INTERRUPT CONTROL BLOCK DEFINITION                     *
***********************************************************************
*
ICB      DSECT
ICBLNKAD DS    F                        ADDRESS OF NEXT ICB
ICBECB   DS    F                        ECB FOR THIS ICB
ICBFLAG1 DS    C                        FLAG BYTE 1
ICBFLAG2 DS    C                        FLAG BYTE 2
ICBSNS1  DS    C                        SENSE BYTE 1
ICBSNS2  DS    C                        SENSE BYTE 2
ICBECBAD DS    F                        ECB ADDRESS
ICBFLAG3 DS    C                        FLAG BYTE 3
ICBCSW   DS    CL7                      LOW ORDER 7 BYTES OF LAST CSW
ICBCPPTR DS    F                        PT TO START OF CHAN PROGRAM
ICBINCR  DS    H                        FOR TAPE- USED TO INCR BLK CT
ICBINDIC DS    H                        TWO BYTES OF INDICATOR BITS
ICBMBBCC DS    D                        SEEK ADDR- DA DEVICE ONLY
*
**********
         EJECT
.SKIPICB ANOP                           ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(10)).SKPJSCB  BYPASS JSCB IF NOT REQUESTED
         SPACE 1
*        IEZJSCB                   INVOKE JOB/STEP CONTROL BLOCK MACRO
         IEZJSCB
*
**********
         EJECT
.SKPJSCB ANOP                      ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(12)).SKIPUPT  BYPASS UPT IF NOT REQUIRED
         SPACE 1
***********************************************************************
*              USER PROFILE TABLE DEFINITION                          *
***********************************************************************
*
*        IKJUPT                         INVOKE USER PROFILE TABLE MACRO
         IKJUPT
*
**********
         EJECT
.SKIPUPT ANOP                      ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(13)).SKIPTJB  BYPASS TJB IF NOT REQUIRED
         SPACE 1
***********************************************************************
*              TJB DEFINITION                                         *
***********************************************************************
*
*        IKJTJB                         INVOKE IKJTJB MACRO
         IKJTJB
*
**********
         EJECT
.SKIPTJB ANOP                      ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(14)).SKPTJBX BYPASS TJBX IF NOT REQ'D
***********************************************************************
*              TJBX DEFINITION                                        *
***********************************************************************
*
*        IKJTJBX                        INVOKE IKJTJBX MACRO
         IKJTJBX
*
**********
         EJECT
.SKPTJBX ANOP                      ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(15)).SKTSCVT  BYPASS TSCVT IF NOT REQ'D
         SPACE 1
***********************************************************************
*              TSCVT DEFINITION                                       *
***********************************************************************
*
*        IKJTSCVT                       INVOKE TSCVT MACRO
         IKJTSCVT
*
**********
         EJECT
.SKTSCVT ANOP                           ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(17)).SKIPBAS    SKIP IEEBASEA IF NOT REQ'D
         SPACE 1
***********************************************************************
*        IEEBASE DSECT DEFINITION                                     *
***********************************************************************
*
*        IEEBASEA                  INVOKE IEEBASE DSECT MACRO
         IEEBASEA
*
***********************************************************************
*
         EJECT
.SKIPBAS ANOP                      ENTRANCE LABEL ONLY
         SPACE 1
***********************************************************************
*                                                                     *
*              DATA SET-ORIENTED CONTROL BLOCKS                       *
*                                                                     *
***********************************************************************
         AIF   (NOT &SYSECDS(20)).SKIPDCB   BYPASS DCB IF NOT REQUESTED
         SPACE 1
***********************************************************************
*              DATA CONTROL BLOCK DEFINITION                          *
***********************************************************************
*
*        DCBD  DSORG=(PS,IS,DA,PO,BS)    INVOKE THE DCB DSECT MACRO
         DCBD  DSORG=(PS,IS,DA,PO,BS)
*
DCBMCRF1 EQU   DCBMACRF+1          BYTE 2 OF DCBMACRF FIELD       OC038
*
**********
         EJECT
.SKIPDCB ANOP                      ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(21)).SKIPDEB    BYPASS DEB IF NOT REQ' ED
         SPACE 1
***********************************************************************
*              DATA EXTENT BLOCK DEFINITION                           *
***********************************************************************
*
IHADEB   DSECT
DEB      EQU   IHADEB
DEBNMSUB DS    0CL1
DEBTCBAD DS    CL4
DEBAMLNG DS    0CL1
DEBAMDEP DS    0CL1
DEBDEBAD DS    CL4
DEBOFLGS DS    0CL1
DEBIRBAD DS    CL4
DEBOPATB DS    CL1                  TYPE OF I/O
DEBQSCNT DS    CL1                  PURGE - QUIESCE COUNT
DEBFLGS1 DS    CL1                      DATA SET FLAGS
         DS    CL1                      RESERVED
DEBSYSPG DS    0CL4
DEBNMEXT DS    0CL1
DEBUSRPC DS    0A
DEBUSRPG DS    CL4
DEBPRIOR DS    0CL1
DEBECBAD DS    CL4
DEBPROTG DS    0CL1
DEBDEBID DS    0CL1
DEBDCBAD DS    CL4
DEBSCALE DS    0CL1
DEBEXSCL DS    0CL1
DEBAPPAD DS    CL4
DEBMODIF DS    0CL1
DEBDVMOD DS    0CL1
DEBUCBAD DS    CL4
DEBTVLSQ DS    0CL2
DEBBINUM DS    CL2
DEBTVLNM DS    0CL2
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
DEBVOLSQ DS    H
DEBVOLNM DS    H
DEBSUBID DS    H
         DS    F
         ORG   DEBDVMOD
DEBNIEE  DS    0CL1
DEBFIEAD DS    CL4
DEBNPEE  DS    0CL1
DEBFPEAD DS    CL4
DEBNOEE  DS    0CL1
DEBFOEAD DS    CL4
DEBDISAD DS    CL4
*
**********
         EJECT
.SKIPDEB ANOP                          ENTRANCE LABEL ONLY
         AIF  (NOT &SYSECDS(22)).SKIPUCB    BYPASS UCB IF NOT REQUESTED
         SPACE 1
***********************************************************************
*              UNIT CONTROL BLOCK DEFINITION                          *
***********************************************************************
*
UCB      DSECT
SRT      EQU   UCB
*        IEFUCBOB                      INVOKE UCB DSECT MACRO
         IEFUCBOB
*
*  IECDSECS EQUS
UCBMAIN  EQU   X'FF' *                  INDICATES THAT DEV IS NOT 2321
UCBRPS   EQU   X'10' *                  INDICATES RPS DEVICE
UCBPRTCT EQU   X'02' -                  FILE PROTECT RING INSTALLED
*
UCBIDFR  EQU   UCBMAIN
*
***********************************************************************
         EJECT
.SKIPUCB ANOP                           ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(25)).SKIPDS1  BYPASS DSCB1 IF NOT REQUESTD
         SPACE 1
***********************************************************************
*              DATA SET CONTROL BLOCK -- FORMAT 1                     *
***********************************************************************
*
DSCB1    DSECT
*        IECSDSL1 (1)                   INVOKE DCSB FMT 1 DSECT MACRO
         IECSDSL1 (1)
*
***********************************************************************
         EJECT
.SKIPDS1 ANOP                           ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(26)).SKIPDS2  BYPASS DSCB2 IF NOT REQUESTD
         SPACE 1
***********************************************************************
*              DATA SET CONTROL BLOCK -- FORMAT 2                     *
***********************************************************************
*
DSCB2    DSECT
*        IECSDSL1 (2)                   INVOKE DCSB FMT 2 DSECT MACRO
         IECSDSL1 (2)
*
***********************************************************************
         EJECT
.SKIPDS2 ANOP                           ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(27)).SKIPDS3  BYPASS DSCB3 IF NOT REQUESTD
         SPACE 1
***********************************************************************
*              DATA SET CONTROL BLOCK -- FORMAT 3                     *
***********************************************************************
*
DSCB3    DSECT
*        IECSDSL1 (3)                   INVOKE DCSB FMT 3 DSECT MACRO
         IECSDSL1 (3)
*
***********************************************************************
         EJECT
.SKIPDS3 ANOP                           ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(28)).SKIPDS4  BYPASS DSCB4 IF NOT REQUESTD
         SPACE 1
***********************************************************************
*              DATA SET CONTROL BLOCK -- FORMAT 4                     *
***********************************************************************
*
DSCB4    DSECT
*        IECSDSL1 (4)                   INVOKE DCSB FMT 4 DSECT MACRO
         IECSDSL1 (4)
*
***********************************************************************
         EJECT
.SKIPDS4 ANOP                           ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(29)).SKIPDS5  BYPASS DSCB5 IF NOT REQUESTD
         SPACE 1
***********************************************************************
*              DATA SET CONTROL BLOCK -- FORMAT 5                     *
***********************************************************************
*
DSCB5    DSECT
*        IECSDSL1 (5)                   INVOKE DCSB FMT 5 DSECT MACRO
         IECSDSL1 (5)
*
***********************************************************************
         EJECT
.SKIPDS5 ANOP                           ENTRANCE LABEL ONLY
         SPACE 1
***********************************************************************
*                                                                     *
*              OPEN/CLOSE/END-OF-VOLUME WORK AREAS                    *
*                                                                     *
***********************************************************************
         AIF   (NOT &SYSECDS(30)).SKIPMN     BYPASS MAIN IF NOT REQ'D
         SPACE 1
***********************************************************************
*              PRIMARY OPEN/CLOSE/END-OF-VOLUME WORK AREA             *
***********************************************************************
*
FORCORE  DSECT
*        IECDSECT                       INVOKE O/C/EOV DSECT MACRO
         IECDSECT
**********
*
*****          THE FOLLOWING FIELDS ARE NEW-
*
         ORG   DXCCW5
DXDSNAME DS    CL17                     TEMP SAVE AREA FOR DSNAME OF
*                                       GENERATION DATA GROUP DATA SET
**********
         ORG   DXCCW8
DXREGSAV DS    0CL32                    REG SAVE AREA FOR RES RTN
DXREG9   DS    F                        REGISTER 9
DXREGA   DS    F                        REGISTER 10
DXREGB   DS    F                        REGISTER 11
DXREGC   DS    F                        REGISTER 12
DXREGD   DS    F                        REGISTER 13
DXREGE   DS    F                        REGISTER 14
DXREG0   DS    F                        REGISTER 0
DXREG1   DS    F                        REGISTER 1
**********
         AIF   ('&LIB' EQ 'LIB1').LIB1CSA
         ORG   DXREG1
DXBTAMX  DS    CL1                      SET TO 0 BY IFG0196V AND YM0947
*                                       USED BY THE BTAM OPEN
*                                       EXECUTORS.
         DS    AL3                      REMAINDER OF DXREG1      YM0947
.LIB1CSA ANOP
*
DXRESIND DS    X                        INDICATORS FOR THE RES RTN
DXIDENT  DS    0CL7                     MODULE IDENTIFIERS
DXCALLID DS    CL2                      ID OF CALLING MODULE
DXRETMOD DS    0CL5                     ID AND TTR OF RET MODULE
DXRETID  DS    CL2                      ID OF RETURN MODULE
DXRETTTR DS    0CL3                     TTR OF RETURN MODULE
**********
*
DXABCODE DS    CL2                      SYSTEM ABEND CODE ISSUED BY
*                                       PROBLEM DETERMINATION ROUTINE
DXRETCOD DS    CL1                      RET CODE ISSUED TO USER BY
*                                       PROBLEM DETERMINATION AT ABEND
**********
*
         AIF   ('&LIB' EQ 'LIB1').LIB1CSD
         ORG   DXPDEBAD+8
.LIB1CSD ANOP
DXXCTL   DS    CL8                      XCTL PARAMETER LIST FOR
*                                       OPEN, CLOSE AND END-OF-VOLUME
**********
*
DXVOLMT1 DS    CL6                      VOL TO BE MOUNTED BY 'OPEN'
DXVOLMT2 DS    0CL6                     VOL SER SAVE AREA FOR 'OPEN'
DXVOLSEQ DS    H                        'OPEN' WORK FIELD
DXWORK1  DS    F                        'OPEN' WORK FIELD
DXWORK2  DS    F                        'OPEN' WORK FIELD
DXOWAEND EQU   *                        END OF OPEN WORK AREA
DXCWAEND EQU   *                        END OF CLOSE WORK AREA
**********
*
*****    END-OF-VOLUME WHERE-TO-GO TABLE
*
*        NOTE- CHANGES TO THIS TABLE OR TO THE WHERE-TO-GO TABLE
*              DSECT SHOULD BE COORDINATED, BECAUSE THE TWO TABLES
*              ARE VIRTUALLY IDENTICAL IN USAGE AND ORGANIZATION.
*
         ORG   DXVOLMT1
DXXAREA  DS    0CL29                    WTG TABLE BASIC SECTION
DXXMODNM DS    0CL8                     MODULE NAME
         DS    CL6                      FIRST 6 BYTES OF MODULE NAME
DXXMODID DS    CL2                      MODULE ID
         DS    CL6                      'TTRKZC' FLD OF PSEUDO BLDL LST
DXXTTR   DS    0CL3                     'TTR' OF FIRST TEXT RCD OF MOD
*                                       (PLACED HERE BY RESIDENT RTN)
         DS    CL2
DXXECBAD DS    CL4                      ECB LIST ADDR (USED BY RES RTN)
         DS    CL2
DXXATTRB DS    CL2                      ATTRIBUTES OF MOD TO BE LOADED
DXXMODSZ DS    CL3                      MAIN STORAGE REQ'D FOR MODULE
DXXTXTLN DS    CL2                      LNG OF FIRST TEXT RCD OF NOD
DXXLNG   DS    0C                       (NOT USED BY E-O-V)
DXOPCLSW DS    C                        SWITCHES FOR COMMUNICATING
*                                       BETWEEN OPEN, CLOSE AND E-O-V
DXXPATHS DS    CL2                      (NOT USED BY E-O-V)
*
DXXENTRY DS    0CL8                     FIRST ENTRY IN WTG TABLE
DXXIDTTR DS    CL5                      ID AND TTR OF NEXT MODULE
DXXCORE  DS    CL3                      ADDRESS OF MAIN WORK AREA
DXXEND   EQU   * -                      END OF PSEUDO WTG TABLE
**********
*
DXVOLSR  DS    0CL18                    VOL SER NOS. FOR EOV VOL SW'ING
DXVOLSR1 DS    CL6                      CURRENT VOL FOR INPUT/OUTPUT,
*                                       MOUNT-AHEAD VOL FOR RDBACK
DXVOLSR2 DS    CL6                      NEXT VOLUME TO BE PROCESSED
DXVOLSR3 DS    CL6                      MOUNT-AHEAD VOL FOR INPUT OR
*                                       OUTPUT, CURRENT VOL FOR RDBACK
**********
*
DXNOUNIT DC    H'0'                     NO. UNITS ALLOCATED TO DATA SET
**********
*
DXDCBLST DS    F                        PSEUDO DCB PARAMETER LIST (SO
*                                       THAT EOV CAN INTERFACE WITH THE
*                                       RES RTN LIKE OPEN AND CLOSE)
DXEWAEND EQU   *                        END OF EOV WORK AREA
*
***********************************************************************
         EJECT
.SKIPMN  ANOP                          ENTRANCE LABEL ONLY
         AIF  (NOT &SYSECDS(33)).SKIPUT  BYPASS USERTOT IF NOT REQ'D
         SPACE 1
***********************************************************************
*              USER TOTALING FACILITY SAVE AND WORK AREA              *
***********************************************************************
*
TOTSAVWA DSECT
TOTSVPTR DS    F                        ADDR OF THE SAVE RTN
TOTCURNT DS    F                        ADDR OF CURRENT SAVE SEGMENT
TOTSTART DS    F                        BEGINNING ADDR OF SAVE SEGMENTS
TOTENDAD DS    F                        ADDR OF LAST SAVE SEGMENT
TOTSVLNG DS    F                        LENGTH OF THE USER'S SAVE AREA
TOTUSRWA DS    F                        ADDR OF THE USERS TOT AREA
TOTTOTLN DS    F                        TOTAL LENGTH OF GETMAIN
TOTEOVPT DS    F                        POINTER TO EOV SEG
TOTREGSV DS    D                        SAVE AREA FOR R11 & R12
         DS    D                        SAVE AREA  FOR REG'S
*
**********
         EJECT
.SKIPUT  ANOP                          ENTRANCE LABEL ONLY
         AIF  (NOT &SYSECDS(32)).SKIPUL  BYPASS USERLAB IF NOT REQ'D
         SPACE 1
***********************************************************************
*              USER LABEL WORK AREA DEFINITION                        *
***********************************************************************
*
ULWK     DSECT
ULREGSAV DS    0F                       SAVE AREA FOR CALLER'S REGS
ULREG5   DS    F                        SAVE AREA FOR REGISTER 5
ULREG6   DS    F                        SAVE AREA FOR REGISTER 6
ULREG7   DS    F                        SAVE AREA FOR REGISTER 7
ULREG8   DS    F                        SAVE AREA FOR REGISTER 8
ULREG9   DS    F                        SAVE AREA FOR REGISTER 9
ULREG10  DS    F                        SAVE AREA FOR REGISTER 10
ULREG11  DS    F                        SAVE AREA FOR REGISTER 11
ULREQ    DS    F                        SAVE AREA FOR REGISTER 12,
*                                       CONTAINS UL DCB EXIT LIST ENTRY
*
ULWK1    DS    1F                       WORK AREA
ULRETCOD EQU   ULWK1+3                  SAVE AREA FOR USER RETURN CODE
ULWK2    DS    1F                       BYTE 0 USED FOR SWITCH
ULCNT    EQU   ULWK2+2
ULWK3    DS    1F                       WORK AREA
ULWK4    DS    1F                       WORK AREA
ULWK5    DS    1F                       WORK AREA
ULWK6    DS    1F                       WORK AREA
ULWK7    DS    1F                       WORK AREA
ULWK8    DS    1F                       WORK AREA
ULWK9    DS    1F                       WORK AREA
ULWK10   DS    1F                       WORK AREA
*
ULBUFR   DS    CL80                     USER LABEL BUFFER
*
ULPARM   DS    0CL16                    PARAMETER LIST
ULBUFPTR DS    F                        POINTER TO USER LABEL BUFFER
ULDCBPTR DS    F                        ADDR OF USER'S DCB
ULERRPTR DS    F                        POINTER TO WORK IOB ERROR INFO
ULTOTPTR DS    F                        POINTER TO USER TOTALING AREA
ULWKEND  EQU   *                        END OF USER LABEL WORK AREA
*
*****    USER LABEL EXIT RETURN CODES
*
READNEXT EQU   4 *                      USER LABEL RETURN CODE- READ
*                                       ..NEXT LABEL AND REURN TO USER
WRITNEXT EQU   8 *                      USER LABEL RETURN CODE- WRITE
*                                       ..LABEL AND REURN TO USER
*
USERL    EQU   ULWKEND-ULWK
RDNEXT   EQU   READNEXT
WRITEUPD EQU   WRITNEXT
WRITERET EQU   WRITNEXT
*
**********
         EJECT
.SKIPUL  ANOP                          ENTRANCE LABEL ONLY
         AIF  (NOT &SYSECDS(31)).SKIPTAB BYPASS UNITTAB IF NOT REQ'D
         SPACE 1
***********************************************************************
*              UNIT TABLE WORK AREA DEFINITION                        *
***********************************************************************
*
UNITABLE DSECT
UNITSICC DS    H
UNITSIHH DS    H
UNITTRLN DS    H
UNITDVHI DS    CL1
UNITOVHL DS    CL1
UNITOVHK DS    CL1
UNITFLAG DS    CL1
UNITTOLR DS    H
UNITTULR DS    H
UNITTVLR DS    H
*
**********
         EJECT
.SKIPTAB ANOP                          ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(34)).SKIPWTG   BYPASS WTG IF NOT REQUESTED
         SPACE 1
***********************************************************************
*              WHERE-TO-GO TABLE DEFINITION                           *
***********************************************************************
*
*        NOTE- CHANGES TO THIS TABLE OR THE END-OF-VOLUME WTG TABLE
*              IN THE 'FORCORE' DSECT SHOULD BE COORDINATED, SINCE
*              THE TWO TABLES ARE VIRTUALLY IDENTICAL IN USAGE AND
*              ORGANIZATION.
*
WTG      DSECT
WTGAREA  DS    0CL29                    WTG TABLE BASIC SECTION
WTGMODNM DS    0CL8                     MODULE NAME
         DS    CL6                      FIRST 6 BYTES OF MODULE NAME
WTGMODID DS    CL2                      MODULE ID
         DS    CL6                      'TTRKZC' FLD OF PSEUDO BLDL LST
WTGTTR   DS    0CL3                     'TTR' OF FIRST TEXT RCD OF MOD
*                                       (PLACED HERE BY RESIDENT RTN)
         DS    CL2
WTGECBAD DS    CL4                      ECB LIST ADDR (USED BY RES RTN)
         DS    CL2
WTGATTRB DS    CL2                      ATTRIBUTES OF MOD TO BE LOADED
WTGMODSZ DS    CL3                      MAIN STORAGE REQ'D FOR MODULE
WTGTXTLN DS    CL2                      LNG OF FIRST TEXT RCD OF NOD
WTGLNG   DS    C                        LNG OF WTG TABLE IN DOUBLE WDS
WTGPATHS DS    CL2                      'OPEN'/'CLOSE' PROCESSING INDS
*** WTGPATHS INDICATORS
WTGPRLSE EQU   X'80' -                  DA PARTIAL RELEASE WAS REQ'D
WTGNSL   EQU   X'40' -                  NSL TAPE INPUT PROCESSING
*                                       ..REQUIRES THE DSNAME WTO MOD
WTGTCLOS EQU   X'20' -                  TCLOSE XCTL'D TO NSL ROUTINES
WTGSMF   EQU   X'10'                    SMF IS ACTIVE AND REQUIRED
*                                       ..FOR THIS DATA SET
WTGJFCBW EQU   X'08'                    JFCB HAS BEEN ALTERED AND MUST
*                                       ..BE WRITTEN BACK TO JOBQUEUE
*** WTGPATHS+1 INDICATORS
         AIF   ('&LIB' EQ 'LIB1').LIB1CSG
WTGTSENQ EQU   X'04' -                  WAS TSO ENQ SUCCESSFUL?  Y01018
.LIB1CSG ANOP
WTGTRACE EQU   X'80' -                  OPTIONAL TRACE FACILITY REQ'D
WTGRECHK EQU   X'02' -                  INCORRECT NSL VOL WAS MOUNTED
WTGOPD   EQU   X'01' -                  LAST LOAD OF OPEN REQUIRES
*                                       ..PROBLEM DETERMINATION, BUT
*                                       ..ADD'L DCB'S MUST FIRST
*                                       ..BE COMPLETED
WTGENTRY DS    0CL8 -                   FIRST ENTRY IN WTG TABLE
WTGIDTTR DS    CL5 -                    ID AND TTR OF NEXT MODULE
WTGCORE  DS    CL3 -                    ADDRESS OF MAIN WORK AREA
WTGEND   EQU   * -                      END OF THE WHERE-TO-GO TABLE
*
**********
         EJECT
.SKIPWTG ANOP                           ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(35)).SKIPSEC  BYPASS SEC IF NOT REQ'D
         SPACE 1
***********************************************************************
*              SECURITY WORK AREA DEFINITION                          *
***********************************************************************
*
SECCORE  DSECT
MYXCTL   DS    2A                       XCTL PARAMETER LIST
MYMODNM  DS    CL8                      NAME OF MODULE TO XCTL TO
*
MYDSNAM1 DS    CL44                     KEY PART 1 = DATA SET NAME
MYREPLY1 DS    CL8                      KEY PART 2 = PASSWORD REPLY
*
         ORG   SECCORE                  TO USE STORAGE FOR 2 FUNCTIONS
MYRPLLTH DS    0X'08'                   WTOR REPLY LENGTH = 8
MYRPLADR DS    A                        WTOR REPLY ADDR
MYRPLECB DS    A                        WTOR REPLY ECB ADDR
MYMSIZE  DS    H                        WTO/R MESSAGE LENGTH
MYMCSFLG DS    X'8000'                  WTO/R MCS FLAG
MYMAREA  DS    0CL64                    TEXT OF MESSAGE AND MCS CODES
MYMIEC   DS    C'IEC301A '              MESSAGE ID AND A BLANK
MYMNOID  DS    C'S JOB '                FIXED PART OF MESSAGE AFTER ID
MYMJOBN  DS    CL8                      TIOT JOB NAME INSERTED HERE
MYMSTEP  DS    C',STEP '                FIXED PART OF MESSAGE
MYMSTEPN DS    CL8                      TIOT STEP NAME INSERTED HERE
MYMDDNM  DS    C',DDNAME '              FIXED PART OF MESSAGE
MYMDDNMN DS    CL8                      TIOT DDNAME INSERTED HERE
MYMCONC  DS    C',CONC'                 FIXED PART OF MESSAGE
MYMCONCN DS    C'000'                   CONCATENATION NO. INSERTED HERE
MYMFLAGC DS    XL4                      MCS ROUTCDE AND DESC CODES
MYMDSN   EQU   MYMNOID+2                'DSNAME ' FIXED PART OF MSG
MYMDSNN  EQU   MYMDSN+7                 DSNAME INSERTED HERE
*
MYENQSW1 DS    F                        ENQ/DEQ CONSTANT
MYENQSW2 DS    A                        POINTER TO 8 BYTE QNAME
MYENQSW3 DS    A                        POINTER TO 44 BYTE RNAME
MYENQSW4 DS    C'SYSPSWRD'              8 BYTE QNAME = SYSPSWRD
*
MYCODE1  DS    X                        OPEN/EOV/RENAME/SCRATCH INDR
MYCODE2  DS    X                        CURRENT ENTRY/ RETURN CODE
MYUSECNT DS    H                        NO. OF SECURITY WK AREA USERS
*
MYYYY    DS    0F                       ALIGN WORK DEB TO FULL WORD
MYDEB    EQU   MYYYY-4                  BASE ADDR OF THIS WORK DEB
MYDEBDEB DS    A                        ADDRESS OF NEXT DEB (NOT USED)
MYDEBIRB DS    A                        ADDRESS OF IRB (NOT USED)
MYDEBOPA DS    F                        OPEN ATTRIBUTE, ETC. (NOT USED)
MYDEBNMX DS    0X'01'                   NO. OF EXTENTS = 1
MYDEBUSR DS    A                        ADDR IOB PURGE CHAIN (NOT USED)
MYDEBECB DS    A                        ADDRESS PURGE ECB (NOT USED)
MYBPAMSW EQU   MYDEBECB                 BPAM CHAINED DSCB INDICATOR
MYDEBID  DS    0X'0F'                   0 PROTECT KEY, DEB ID
MYDEBDCB DS    A                        ADDRESS OF WORK DCB
MYDEBAPP DS    A                        ADDR SYS1.SVCLIB APP VECTOR TBL
MYDEBMOD DS    0X                       DIRECT ACCESS FILE MASK
MYDEBUCB DS    A                        ADDRESS OF UCB
MYDEBBIN DS    H                        BIN NUMBER
MYDEBSCC DS    H                        CYLINDER START ADDRESS
MYDEBSHH DS    H                        HEAD START ADDRESS
MYDEBECC DS    H                        CYLINDER END ADDRESS
MYDEBEHH DS    H                        HEAD END ADDRESS
MYDEBNTR DS    H                        NO. OF TRACKS IN EXTENT
*
MYCVDWA  DS    0D                       CONVERT TO DECIMAL WORK AREA
*
MYDSNAME DS    CL44                     KEY PART 1 = DATA SET NAME
MYREPLY  DS    CL8                      KEY PART 2 = PASSWORD REPLY
*
         ORG   MYDSNAME                 TO READ DATA OVER PASSWORD
MYLOGFLD DS    0CL80                    CNTR, MODE, & LOG FIELD
MYCNTR   DS    H                        BINARY USE COUNTER
MYMODE   DS    X                        PROTECTION MODE INDICATOR
         DS    77C                      LOGGING FIELD (OPTIONAL INFO)
SECSIZE  EQU   *-SECCORE                LENGTH OF THIS DSECT
*
***********************************************************************
.SKIPSEC ANOP                           ENTRANCE LABEL ONLY
         AIF   (NOT &SYSECDS(36)).SKIPTS  BYPASS TSENQ IF NOT REQ'D
         SPACE 1
***********************************************************************
*        TSO ENQ WORK AREA DEFINITION                                 *
***********************************************************************
*
TSOENQ   DSECT                                                   Y01018
ENQMFL   DS    CL12                     LIST FORM OF ENQ MACRO   Y01018
TSQNAME  DS    CL8                      QNAME FOR ENQ OR DEQ     Y01018
TSRNAME  DS    CL4                      RNAME FOR ENQ OR DEQ     Y01018
*
***********************************************************************
.SKIPTS  ANOP
         AIF   (NOT &SYSECDS(37)).SKIPVSP BYPASS VSAM PARM LIST  X02989
         IDAPDPRM                                                X02989
.SKIPVSP ANOP                                                    X02989
         MEND
