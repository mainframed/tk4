         MACRO
&NAME    IEMKLVE &NORC,&TYPE=,&RC=,&RP=
.* THIS MACRO GENERATES TYPE 1, 2 OR 3 EXIT LINKAGES. IF
.* DEFAULT VALUES FOR THE TYPE ARE PASSED FROM THE LAST IEMKENT MACRO
.* BY MEANS OF GLOBAL VARIABLES.
         GBLB  &GBASAV1
         GBLB  &GBALLSW
         GBLC  &GCATYPE
         LCLB  &LBLLSW
         LCLB  &LBERSW
         LCLC  &LCTYPE
&NAME    DS    0H
.* GENERATE CONDITION CODE SETTING CODE
         AIF   ('&NORC' EQ 'NORC').SS0000
         AIF   (T'&RC EQ 'O').SSZERO
         IEMKLRG RC,&RC
         AGO   .SS0000
.SSZERO  SR    RC,RC .                  SET ZERO RETURN CODE
.SS0000  ANOP
.* GENERATE EXIT LINKAGE CODE
&LBLLSW  SETB  (&GBALLSW)               SET DEFAULT VALUES
&LCTYPE  SETC  '&GCATYPE'                 FOR TYPE
         AIF   (T'&TYPE EQ 'O').SS0002    TEST IF TYPE OMITTED
         AIF   (K'&TYPE EQ 1).SS0001
&LBLLSW  SETB  ('&TYPE'(1,2) EQ 'LL')
         AIF   (K'&TYPE EQ 2).SS0002
&LCTYPE  SETC  '&TYPE'(3,1)
         AGO   .SS0002
.SS0001  ANOP
&LCTYPE  SETC   '&TYPE'
.SS0002  AIF   ('&LCTYPE' EQ '1').SSTYP1  TEST TYPE OF
         AIF   ('&LCTYPE' EQ '2').SSTYP2    LINKAGE TO BE
         AIF   ('&LCTYPE' EQ '3').SSTYP3    GENERATED
         AIF   (&LBERSW).SSERRR
       MNOTE 8,'INVALID TYPE PARAMETER - TYPE OF LAST IEMKAENT ASSUMED'
&LBERSW  SETB  1
&LBLLSW  SETB  (&GBALLSW)
&LCTYPE  SETC  '&GCATYPE'
         AGO   .SS0002
.SSERRR MNOTE 12,'TYPE (&LCTYPE,&LBLLSW) STILL IN ERROR - NO GEN.'
         MEXIT
.* GENERATE TYPE 1 LINKAGE
.SSTYP1  ANOP
         AIF   (&LBLLSW).SS0003
         L     SAREG,0(0,SAREG) .       RESET SAVE AREA POINTER
.SS0003  AIF   (&GBASAV1).SSTYP11
         LM    GR2,GR9,8(SAREG) . RESTORE GENERAL REGS.
         AGO   .SSTYP12
.SSTYP11 LM    PR1,GR9,8(SAREG) .  RESTORE GENERAL REGS.
.SSTYP12 ANOP
         AIF   (T'&RP EQ 'O').SS0004
         IEMKLRG RR,&RP
         AGO   .SS0005
.SS0004  L     RR,4(0,SAREG) .          LOAD RETURN POINT
.SS0005  BR    RR .                     EXIT
         AGO   .SSEND
.* GENERATE TYPE 2 LINKAGE
.SSTYP2  ANOP
         AIF   (&LBLLSW).SS0006
         SH    SAREG,*+10 .             RESET SAVE AREA POINTER
.SS0006  L     RR,4(0,SAREG) .          LOAD RETURN POINT
         BR    RR .                     RETURN
         AIF   (&LBLLSW).SSEND
         DC    H'4'
         AGO   .SSEND
.* GENERATE TYPE 3 LINKAGE
.SSTYP3  ANOP
         AIF   (T'&RP EQ 'O').SSEND
         AIF   ('&RP'(1,1) EQ '(').SSREG
         B     &RP
         AGO   .SSEND
.SSREG   BR     &RP
.SSEND   ANOP
         MEND
