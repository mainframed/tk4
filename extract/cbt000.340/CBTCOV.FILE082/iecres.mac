         MACRO
&NAME    IECRES &FUNC,&IDTTR,&BCODE
.*
.* THIS MACRO IS CALLED ONLY BY OPEN/CLOSE/EOV TRANSIENT SVC MODULES.
.* IT GENERATES LINKAGE TO THE OPEN/CLOSE/EOV IFG019RA RESIDENT RTN.
.*
         AIF   ('&FUNC' EQ 'WAIT').WAIT  WAITS ON DXECB IN WORK AREA
         AIF   ('&FUNC' EQ 'XCTL').XCTL  XCTL TO NEXT MODULE
         AIF   ('&FUNC' EQ 'SYNCHDCB').SYNCHDC ALL DCB'S TO SAME POINT
         MNOTE 8,'FIRST PARAMETER IS NOT WAIT, XCTL, OR SYNCHDCB'
&NAME    DC    H'0'                     DEFINE NAME, HALT EXECUTION
         MEXIT
.*
.WAIT    AIF   (N'&SYSLIST EQ 1).WAIT1
         MNOTE 4,'WAIT CAN HAVE NO PARAMETERS'
.WAIT1   ANOP
&NAME    BALR  RET,RES                  BRANCH TO RESIDENT WAIT SUBR
         MEXIT
.*
.XCTL    AIF   (N'&SYSLIST LE 3).XCTL1
         MNOTE 4,'XCTL CAN HAVE ONLY 2 PARAMETERS'
.XCTL1   AIF   ('&IDTTR' EQ '').XCTL6
.*
         AIF   ('&IDTTR'(1,1) EQ '(').XCTL2
&NAME    MVC   0(5,RWTGC),&IDTTR        MOVE ID & TTR INTO WTG TABLE
         AGO   .XCTL3
.XCTL2   ANOP
&NAME    MVC   0(5,RWTGC),0&IDTTR       MOVE ID & TTR INTO WTG TABLE
.XCTL3   AIF   ('&BCODE' EQ '').XCTL5
         AIF   ('&BCODE'(1,1) EQ '(').XCTL4
         LA    RET,&BCODE               LOAD BRANCH TABLE OFFSET / INDR
         AGO   .XCTL5
.XCTL4   AIF   ('&BCODE' EQ '(RET)').XCTL5
         LR    RET,&BCODE(1)            LOAD BRANCH TABLE OFFSET / INDR
.XCTL5   B     RESXCTL(,RES)            BRANCH TO RESIDENT TO DO XCTL
         MEXIT
.*
.XCTL6   AIF   ('&BCODE' EQ '').XCTL9
         AIF   ('&BCODE'(1,1) EQ '(').XCTL7
&NAME    LA    RET,&BCODE               LOAD BRANCH TABLE OFFSET / INDR
         AGO   .XCTL8
.XCTL7   AIF   ('&BCODE' EQ '(RET)').XCTL9
&NAME    LR    RET,&BCODE(1)            LOAD BRANCH TABLE OFFSET / INDR
.XCTL8   B     RESXCTL(,RES)            BRANCH TO RESIDENT TO DO XCTL
         MEXIT
.*
.XCTL9   ANOP
&NAME    B     RESXCTL(,RES)            BRANCH TO RESIDENT TO DO XCTL
         MEXIT
.*
.SYNCHDC AIF   (N'&SYSLIST EQ 1).SYNCHD1
         MNOTE 4,'SYNCHDCB CAN HAVE NO PARAMETERS'
.SYNCHD1 ANOP
&NAME    BAL   RET,RESSYNCH(,RES)       BR TO RESIDENT SYNCH DCB SUBR
         MEND
