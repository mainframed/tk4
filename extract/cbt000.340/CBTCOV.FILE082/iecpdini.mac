         MACRO
         IECPDINI &COMP
         GBLC  &LIB                     DEFINE GLOBAL VARIABLE   SM5268
.* THE ABOVE GLOBAL MUST BE SET BY THE CALLER OF IECPDINI        SM5268
         LCLC  &A,&B,&C                 DEFINE LOCAL CHAR VARIABLES
*
         AIF   ('&COMP' EQ 'OPEN').OPSCT
.*                                      IS OPEN EXPANSION REQUIRED
         AIF   ('&COMP' EQ 'OPENEX').OPEXSCT
.*                                      IS OPEN EXECUTOR EXPANSION
.*                                      REQUIRED
         AIF   ('&COMP' EQ 'CLOSE').CLSCT
.*                                      IS CLOSE EXPANSION REQUIRED
         AIF   ('&COMP' EQ 'TCLOSE').TCSCT
.*                                      IS TCLOSE EXPANSION REQUIRED
         AIF   ('&COMP' EQ 'EOV').EOVSCT
.*                                      IS EOV EXPANSION REQUIRED
         MEXIT                          INVALID CODE PASSED, DO NOT
.*                                      EXPAND
.OPSCT   ANOP
&A       SETC  'IFG0190P'               SET MODULE ID
&B       SETC  'X''F13'''               OPEN ABEND CODE
&C       SETC  '1'                      OPEN MSG ID
IFG0190P CSECT
         TITLE 'IFG0190P - OPEN ABEND INTERPRETATION AND RECOVERY INITIX
               AL ROUTINE'
         AGO   .GENMAIN
.CLSCT   ANOP
IFG0200P CSECT
&A       SETC  'IFG0200P'               SET MODULE ID
&B       SETC  'X''F14'''               CLOSE ABEND CODE
&C       SETC  '2'                      CLOSE MSG ID
         TITLE 'IFG0200P, ALIAS IGG0206M  - CLOSE ABEND INTERPRETATION X
               AND RECOVERY INITIAL ROUTINE'
         AGO   .GENMAIN
.EOVSCT  ANOP
&A       SETC  'IFG0550P'               SET MODULE ID
&B       SETC  'X''F37'''               EOV ABEND CODE
&C       SETC  '0'                      EOV MSG ID
IFG0550P CSECT
         TITLE 'IFG0550P - EOV ABEND INTERPRETATION AND RECOVERY INITIAX
               L ROUTINE'
         AGO   .GENMAIN
.TCSCT   ANOP
&A       SETC  'IFG0230P'               SET MODULE ID
&B       SETC  'X''F17'''               TCLOSE ABEND CODE
&C       SETC  '2'                      CLOSE MSG ID
IFG0230P CSECT
         TITLE 'IFG0230P - TCLOSE ABEND INTERPRETATION AND RECOVERY INIX
               TIAL ROUTINE'
         AGO   .GENMAIN
.OPEXSCT ANOP
&A       SETC  'IGG0196M'               SET MODULE ID
&B       SETC  'X''F13'''               OPEN ABEND CODE
&C       SETC  '1'                      OPEN MSG ID
IGG0196M CSECT
         TITLE 'IGG0196M - OPEN EXECUTOR ABEND INTERPRETATION AND RECOVX
               ERY INITIAL ROUTINE'
.GENMAIN ANOP
***********************************************************************
*                                                              @X04ACFY
*        VS1 RELEASE 04 DELETIONS/CHANGES
*0000                                                          @X04ACFY
*
*        VS1 RELEASE 02 DELETIONS/CHANGES
*0000                                                            XM0855
*0000083000                                                      XM0880
*0000                                                           XL02942
*        VS1 RELEASE 2.6 DELETION/CHANGES
*0000                                                           XA01822
*0000026500-028000,260000-260050                                 XM2899
*0000                                                            XM2966
*        VS1 RELEASE 03 DELETION/CHANGES
*0000142500,148500                                               XM3491
*0000                                                           XA03145
*        VS1 RELEASE 03.1 DELETIONS/CHANGES
*SEE SA63266                                                    XA03527
*SEE SA70631                                                    XA05411
*SEE SA70632                                                    XA05412
*SEE SA70633                                                    XA05413
*SEE SA70634                                                    XA05414
*SEE SA70635                                                    XA05415
*
*
*        VS1 RELEASE 040 DELETIONS/CHANGES
*0000218460                                                    @XA06868
*0000275000                                                    @XA07756
*0000071500,082000-085000,091500,092000,096500,097000,097500,  @XA06864
*101500-104500,112600-113000,313060,313380,313520              @XA06864
*0000172300,172400                                             @OX10281
*21889031-21889061                                             @OX10475
*
*        VS2 RELEASE 01 DELETIONS/CHANGES
*0000002500                                                      YM2133
*0000                                                            YM3213
*21889031-21889061                                             @OX10475
*
*          RELEASE 21 DELETIONS/CHANGES
*                                                                M0035
*                                                                M0069
*                                                                M0179
*                                                                A48604
*0000                                                           SA53143
*                                                                A47372
*0000                                                           SA48558
*                                                               SA47382
*          RELEASE 21.7 DELETIONS/CHANGES
*0000                                                           SA46620
*0000                                                            S21940
*0000108500                                                     SA54604
*0000                                                           SA52379
*0000149000                                                     SA55580
*0000                                                           SA53753
*0000                                                           SA58102
*0000                                                           SA59307
*0000                                                           SA58019
*0000226600,226700                                               SM5456
*          RELEASE 21.8 DELETIONS/CHANGES                             *
*0000                                                           SA65517
*0000                                                           SA66776
*0000                                                           SA63266
*0000                                                           SA70631
*0000                                                           SA70632
*0000                                                           SA70633
*0000                                                           SA70634
*0000                                                           SA70635
*          RELEASE 99.9 DELETIONS/CHANGES                             *
*0000                                                          @SA72925
*0000                                                          @SA77662
* STATUS CHANGE LEVEL 000
*
         AIF   ('&LIB' NE 'LIB1').LIB2NIS
* FUNCTION -
*    THIS ROUTINE IS GIVEN CONTROL WHEN AN ABEND
*    CONDITION IS DETECTED IN OPEN/CLOSE/EOV, TCLOSE
*    OR RDJFCB. IT ISSUES A GETMAIN FOR A 128 BYTE
*    WORK AREA TO BE USED BY THE REST OF THE PROBLEM
*    DETERMINATION ROUTINES. IT WILL DETERMINE THE
*    REAL ABEND CODE AND RETURN CODE FORM THE
*    INTERNAL CODE PASSED TO IT AND A TABLE IN THE
*    MODULE ITSELF.  THE ABEND CODE, RETURN CODE AND
*    TRACE CODE WILL BE MOVED INTO THE WKAREA.
*    A 7 BYTE MESSAGE ID FOR A WRITE-TO-PROGRAMMER MSG
*    IS MOVED INTO THE 5TH THROUGH 11TH BYTES OF THE
*    WKAREA. THE ID OF THE LAST LOAD OF THE ABENDING
*    STRING IS MOVED INTO THE CURRENT WTG TABLE ENTRY.
*    CONTROL IS THEN PASSED TO A GENERAL
*    WRITE-TO-PROGRAMMER ROUTINE VIA XCTL.
*
.LIB2NIS ANOP
         AIF   ('&LIB' EQ 'LIB1').LIB1NIS
* FUNCTION -
*    THIS ROUTINE IS GIVEN CONTROL WHEN AN ABEND
*    CONDITION IS DETECTED IN OPEN/CLOSE/EOV, TCLOSE
*    OR RDJFCB. IT ISSUES A GETMAIN FOR A 176 BYTE               XM0855
*    WORK AREA TO BE USED BY THE REST OF THE PROBLEM
*    DETERMINATION ROUTINES. IT WILL DETERMINE THE
*    REAL ABEND CODE AND RETURN CODE FORM THE
*    INTERNAL CODE PASSED TO IT AND A TABLE IN THE
*    MODULE ITSELF.  THE ABEND CODE, RETURN CODE AND
*    TRACE CODE WILL BE MOVED INTO THE WKAREA.
*    A 7 BYTE MESSAGE ID FOR A WRITE-TO-PROGRAMMER MSG
*    IS MOVED INTO THE 5TH THROUGH 11TH BYTES OF THE
*    WKAREA. THE ID OF THE LAST LOAD OF THE ABENDING
*    STRING IS MOVED INTO THE CURRENT WTG TABLE ENTRY.
*    CONTROL IS THEN PASSED TO A GENERAL
*    WRITE-TO-PROGRAMMER ROUTINE VIA XCTL.
*
.LIB1NIS ANOP
* ENTRY POINTS -
*    EACH OF THE FOLLOWING NAMES (EXCEPT IGG0206M) ARE SEPARATE
*    PHYSICAL MODULES.
*    THEIR MAIN DIFFERENCE IS THE ABEND CODE TABLES THEY CONTAIN.
*    IFG0190P. ENTRY VIA XCTL FROM MAINLINE OPEN.
*    IFG0200P. ENTRY VIA XCTL FROM MAINLINE CLOSE.
*    IGG0206M. ALIAS ENTRY TO IFG0200P VIA XCTL FROM CLOSE EXECUTORS.
*    IFG0550P. ENTRY VIA XCTL FROM MAINLINE EOV.
*    IFG0230P. ENTRY VIA XCTL FROM MAINLINE TCLOSE.
*    IGG0196M. ENTRY VIA XCTL FROM OPEN EXECUTORS AND RDJFCB
*
* INPUT -
*    REG 0 CONTAINS AN INTERNAL ABEND CODE.
*    REGS 2-14 ARE AS THEY WERE IN CALLING ROUTINE.
*    TWO BYTE ID OF CALLING MODULE IS IN DXCALLID.
*    TWO BYTE ID OF MODULE TO WHOM CONTROL IS RETURNED
*    (WHERE APPLICABLE) IN DXRETID.
*
* OUTPUT -
*    REG 1 CONTAINS PTR TO PROBLEM DETERMINATION WKAREA.
*    ABEND CODE, RETURN CODE, AND TRACE CODE ARE IN
*    PROB DET WKAREA. THE 7 BYTE MSG ID OF THE WRITE-TO-PROGRAMMER
*    MSG IS PUT IN THE BUFFER
*
* EXTERNAL REFERENCES - N/A
*
* EXITS, NORMAL -
*    EACH OF THE FOLLOWING NAMES ARE THE SAME PHYSICAL MODULE.
*    IFG0199B IS THE REAL NAME AND THE OTHERS ARE ALIASES.
*    XCTL TO IFG0199B FOR OPEN AND RDJFCB.
*    XCTL TO IFG0209B FOR CLOSE.
*    XCTL TO IFG0559B FOR EOV.
*    XCTL TO IFG0239B FOR TCLOSE.
*
* EXITS, ERROR -
*    ABEND WITH ABEND CODE FROM TABLE IF UNABLE TO GETMAIN A WKAREA.
*    ABEND WITH FXX ABEND IF UNABLE TO FIND CODE IN TABLE
*    WHERE XX=13 FOR OPEN AND RDJFCB.
*    14 FOR CLOSE
*    17 FOR TCLOSE
*    37 FOR EOV
*
* TABLES/WORK AREA -
*    THE FORMAT AND CONTENTS OF TABLES AND WORK AREAS
*    USED ARE DESCRIBED BY DSECTS AT END OF LISTING.
*
* ATTRIBUTES -
*    REENTRANT, REFRESHABLE, READ-ONLY, ENABLED, PRIVELEGED
*
* CHARACTER CODE DEPENDENCY -
*    CLASS ONE CHARACTER CODE DEPENDENCY - THE EBCDIC
*    CHARACTER CODE WAS USED FOR ASSEMBLY.  THE MODULE MUST
*    BE REASSEMBLED IF A DIFFERENT CHARACTER SET IS USED
*    FOR EXECUTION.
*
* NOTES - N/A
*
***********************************************************************
         EJECT
R0       EQU   0                        WORK/PARAMETER REG
R1       EQU   1                        WORK/PARAMETER REG
RDCB     EQU   2                        PTR TO DCB
RBASE    EQU   3                        BASE REG
RCORE    EQU   4                        PTR TO O/C/EOV WKAREA
RES      EQU   5                        PTR TO RESIDENT RTN
RWTGC    EQU   8                        PTR TO WTG TABLE CURRENT ENTRY
RPARC    EQU   7                        PTR TO PARALIST CURRENT ENTRY
RWTG     EQU   6                        PTR TO WTG TABLE
RTIOT    EQU   9                        PTR TO TIOT ENTRY
RUCB     EQU   10                       PTR TO UCB
RB       EQU   11                       WORK/PARAMETER REG
RC       EQU   12                       WORK/PARAMETER REG
RD       EQU   13                       WORK/PARAMETER REG
RET      EQU   14                       WORK/PARAMETER REG
RF       EQU   15                       WORK/PARAMETER REG
SVRBEXSA EQU   96                       OFFSET OF SVRB EXT S/A IN SVRB
CVTPTR   EQU   16                       LOCATION OF CVT PTR
         AIF   ('&LIB' EQ 'LIB1').LIB1PDS
TWENTY4  EQU   24                       CONSTANT OF 24          XA01822
TWENTY5  EQU   25                       CONSTANT OF 25          XA01822
TWENTY7  EQU   27                       CONSTANT OF 27          XA01822
.LIB1PDS ANOP
TABLLEN  EQU   7                        LNG OF CODE TABLE ENTRY
FIVE     EQU   5                        WORK VALUE
SIX      EQU   6                        WORK VALUE
THREE    EQU   3                        WORK VALUE
FOURTEEN EQU   14                       WORK VALUE
SEVEN    EQU   7                        WORK VALUE
ORG      EQU   0                        WORK VALUE
TWO      EQU   2                        WORK VALUE
FOUR     EQU   4                        WORK VALUE
ONE      EQU   1                        WORK VALUE
EIGHT    EQU   8                        WORK VALUE
TWELVE   EQU   12                       WORK VALUE
ZERO     EQU   0                        WORK VALUE
ABENDCOD EQU   &B                       INVALID CODE ABEND
CHARF    EQU   C'F'                     WORK VALUE
K0       EQU   0                        CONSTANT 0             @XA06864
K6       EQU   6                        CONSTANT 6             @XA06864
K8       EQU   8                        CONSTANT 8             @XA06864
K12      EQU   12                       CONSTANT 12            @XA06864
K24      EQU   24                       CONSTANT 24            @XA06864
K52      EQU   52                       CONSTANT 52            @XA06864
         EJECT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         USING PDDSECT,R1
         USING TABLE,RD
         USING FORCORE,RCORE
         L     RF,CVTPTR                CVT ADDR               @XA06864
         L     RF,0(RF)                 TCB WDS                @XA06864
         L     RF,4(RF)                 TCB PTR                @XA06864
         L     RF,0(RF)                 SVRB PTR               @XA06864
         STM   RUCB,R1,SVRBEXSA(RF)     SAVE REGS 10-1 IN SVRB @XA06864
         LR    RC,R0                    SAVE CODE
         N     RC,CODEMASK              ZERO HI 3 BYTES
         LA    RD,CODTABLE              POINT TO TABLE
         SR    RET,RET                  ZERO WORK REG
PDF01020 IC    RET,CODE                 GET CODE IN TABLE ENTRY
         CR    RC,RET                   RIGHT TABLE ENTRY        M0035
         BE    PDF01040                 YES
         LTR   RET,RET                  END OF TABLE             M0035
         BE    PDF01040                 YES, GET CORE FOR WTP
         LA    RD,TABLLEN(RD)           POINT TO NEXT ENTRY
         B     PDF01020                 CHECK NEXT ENTRY IN TABLE
PDF01040 EQU   *
         LA    R0,PDBFRLEN              LENGTH OF MSG BUFFER   @XA06864
         LM    RUCB,RC,DXCCW8           SAVE WORKAREA          @XA06864
         MVC   DXCCW8(GMPARMLN),COREADDR
*                                       MOVE GETMAIN PARMLIST TO WKAREA
         LA    R1,DXCCW8                PUT PARMLIST ADDR IN REG1
*        GETMAIN EC,LV=(0),A=DXCCW8,MF=(E,(1))
         GETMAIN EC,LV=(0),A=DXCCW8,MF=(E,(1))
*                                       GET CORE FOR WTP
         L     R1,DXCCW8                GET ADDR OF CORE
         STM   RUCB,RC,DXCCW8           RESTORE WKAREA         @XA06864
         LM    RTIOT,RUCB,DXREG9        RELOAD TIOT AND UCB    @XA06864
         LTR   RF,RF                    WAS CORE ALLOCATED       M0035
         BNZ   PDF01080                 NO
         XC    PDDSECT(PDBFRLEN),PDDSECT ZERO WKAREA           @XA06864
         L     RF,CVTPTR                CVT ADDR               @XA06864
         L     RF,0(RF)                 TCB WDS                @XA06864
         L     RF,4(RF)                 TCB PTR                @XA06864
         L     RF,0(RF)                 SVRB PTR               @XA06864
         LM    RUCB,RC,SVRBEXSA(RF)     RESTORE REGS 10-12     @XA06864
         MVC   PDREGSAV(K8),SVRBEXSA+K24(RF) ERROR REGS 0-1    @XA06864
         STM   RDCB,RC,PDREGSAV+K8      ERROR REGS 2-12        @XA06864
         MVC   PDREGSAV+K52(K8),SVRBEXSA+K12(RF) ERR REG 13-14 @XA06864
         ST    RDCB,SVRBEXSA(RF)        SAVE REG 2 IN SVRB EXT @XA06864
         STM   RCORE,RWTGC,SVRBEXSA+FOUR(RF)                   @XA06864
*                                       SAVE REGS 4-8 IN SVRB EXT S/A
         MVC   PDERRMOD(K6),K0(RWTG)    1ST PART ERROR MODNM   @XA06864
         MVC   PDERRMOD+K6(L'DXCALLID),DXCALLID ERROR MOD ID   @XA06864
         MVC   PDRETCOD,RETCODE         RETURN CODE TO WKAREA  @XA06864
         MVC   PDABCODE,ABCODE          ABEND CODE TO WKAREA   @XA06864
         MVC   PDCODE,TRCCODE           TRACE CODE TO WKAREA   @XA06864
         MVC   PDMSG(EIGHT),ID          MSG ID TO PD WKAREA    @XA06864
         MVC   PDMSG+FOUR(TWO),MSGID    MOVE LAST TWO CHARS    @XA06864
*                                       OF ID TO WKAREA        @XA06864
         CLI   FOUR(RTIOT),X'40'        TEST FOR CONCAT DS     @SA77662
         BNE   PDF01050                 BR NO, CONTINUE        @SA77662
         NI    PDABCODE+ONE,X'FB'       RESET IGNORE OPTION    @SA77662
PDF01050 EQU   *                                               @SA77662
         CLI   CODE,ZERO                WAS CODE PASSED VALID    M0035
         BZ    PDF01060                 NO, ISSUE WTP AND ABEND
         SR    RF,RF                    ZERO WORK REG
         MVC   SIX(TWO,RWTG),NEXTMOD    MOVE ID OF WTP MOD TO WTG TBL
         MVC   FOURTEEN(THREE,RWTG),NEXTMOD+TWO
*                                       MOVE TTR TO WTG TABLE
         AIF   ('&LIB' EQ 'LIB1').LIB2NIX
         IC    RF,NEXTMOD+FIVE          INSERT LENGTH           XA01822
         SLL   RF,11                    SHIFT LEFT 11           XA01822
         ST    RF,TWENTY4(RWTG)                                 XA01822
         MVC   TWENTY7(TWO,RWTG),TWENTY5(RWTG)                  XA01822
.LIB2NIX ANOP
         MVC   ORG(FIVE,RWTGC),LASTLOAD MOVE ID AND TTR OF LAST LOAD
         LA    RF,DXXCTL                SET UP FOR XCTL
         MVI   ONE(RWTG),CHARF          ENSURE IFG PREFIX       SA54604
*                                       MODULE NAME
         AIF   ('&COMP' EQ 'OPENEX').GNXXCTL  IF IGG0206M XCTL BY NAME
*        XCTL  DE=(RWTG),SF=(E,(15))    XCTL TO IFG0199B
         XCTL  DE=(RWTG),SF=(E,(15))    XCTL TO IFG0199B
         AGO   .GNCOM                   CONTINUE NORMAL GENERATION
.GNXXCTL ANOP
*        XCTL  EPLOC=(RWTG),SF=(E,(15)) XCTL TO IFG0199B
         XCTL  EPLOC=(RWTG),SF=(E,(15)) XCTL TO IFG0199B
.GNCOM   ANOP
*
PDF01060 EQU   *
         MVC   PDMSGLNG(ENDMSG+FOUR),NOCODMSG
*                                       MOVE MESSAGE TO BUFFER
         LR    RD,R1                    SAVE PTR TO WKAREA
         WTO   MF=(E,(1))               ISSUE WRITE-TO-PROGRAMMER
         LR    R1,RD                    RESTORE PTR TO WKAREA
*        FREEMAIN R,LV=PDBFRLEN,A=(1),SP=250                   @XA06864
         FREEMAIN R,LV=PDBFRLEN,A=(1),SP=250                   @XA06864
*                                       FREE PD BUFFER
PDF01070 EQU   *
         LA    R1,ABENDCOD              GET FXX ABEND CODE
         SLL   R1,TWELVE                INDICATE SYSTEM ABEND
         B     PDF01100                 GO TO ISSUE FXX ABEND
PDF01080 EQU   *
         CLI   CODE,ZERO                WAS CODE PASSED VALID
         BE    PDF01070                 NO, ABEND
         SR    R1,R1                    ZERO ABEND REG
         IC    R1,ABCODE                PUT HALF OF ABEND CODE IN R1
         SLL   R1,EIGHT                 INDICATE SYSTEM ABEND    M0035
         IC    R1,ABCODE+ONE            PUT SECOND HALF OF CODE IN R1
         SRL   R1,FOUR                  SHIFT ABEND OPTION OUT   M0035
         SLL   R1,TWELVE                INDICATE SYSTEM ABEND    M0035
PDF01100 EQU   *
*        ABEND (1),DUMP                 ISSUE ABEND
         ABEND (1),DUMP                 ISSUE ABEND
*
***********************************************************************
*
*        CONSTANTS
*
         DC    0F'0'                    ALIGN MASK TO FULLWORD
CODEMASK DC    X'000000FF'              MASK TO ZERO HI-3 BYTES OF
*                                       INTERNAL CODE
NOCODMSG DC    AL2(ENDMSG)              LNG OF MSG
         DC    X'8000'                  MCSFLAGS
         DC    CL8'IEC900I'             MSG ID
         DC    C'INVALID ABEND CODE PASSED TO '
         DC    C'&A'                    MODULE ID
ENDMSG   EQU   *-NOCODMSG               LNG OF MSG
         DC    X'04000020'              ROUT AND DESCRIPTOR CODES
*
*COREADDR GETMAIN EC,SP=250,MF=L        GETMAIN PARALIST
COREADDR GETMAIN EC,SP=250,MF=L         GETMAIN PARALIST
GMPARMLN EQU   *-COREADDR               LNG OF GETMAIN PARALIST
*
ID       DC    C'IEC'                   DATA MAN ID
         DC    C'&C'                    OPEN/CLOSE/EOV ID
         DC    C'  I '
         EJECT
*        THE FOLOWING TABLE IS USED TO DETERMINE THE EXTERNAL ABEND
*        CODE, RETURN CODE, DATA TO BE TRACED, AND ID OF A WRITE-
*        TO-PROGRAMMER MSG TO BE ISSUED.  THE TABLE CONSISTS OF
*        SEVEN-BYTE ENTRIES IN THE FOLLOWING FORMAT.
*              BYTE 0 - INTERNAL CODE
*              BYTE 1-2 - BITS 0-11 EXTERNAL ABEND CODE
*                       BITS 12-15 FLAGS INDICATING VALID OPTIONS THE
*                                       USER MAY EXERCISE IN HIS ABEND
*                                       EXIT ROUTINE FOR THE PARTICULAR
*                                       ABEND.
*                                       THE POSSIBLE BIT SETTINGS IN
*                                       THIS FIELD ARE AS FOLLOWS.
*                                        X'08' - RECOVERY PERMITTED
*                                        X'04' - IGNORE PERMITTED
*                                        X'02' - DELAY ABEND PERMITTED
*                                        X'01' - DISSALLOW DCB @XA06864
*                                         ABEND EXIT - BAD DCB @XA06864
*              BYTE 3 - RETURN CODE TO BE PASSED IN REGISTER 15 WHEN
*                       ABEND IS ISSUED
*              BYTE 4 - CODE INDICATING THE DATA TO BE TRACED FOR
*                       PARTICULAR ABEND
*                                       THE POSSIBLE CODES IN THIS
*                                       FIELD ARE AS FOLLOWS. (HEX)
*                                       00 - NO TRACE
*                                       04 - IOB AND CCWS
*                                       08 - TIOT ENTRY AND UCBS
*                                       0C - JFCBVOLS
*                                       10 - TAPE LABEL
*                                       14 - JFCBDSNM
*                                       18 - VOLUME LABEL
*                                       1C - DSCB (ALL ASSOCIATED WITH
*                                            ABEND THAT ARE IN CORE.
*                                       28 - DCB
*                                       34 - DCB, DSCB
*                                       38 - JFCBELNM
*                                       3C - DCB EXIT LIST ENTRIES
*                                       40 - DCB, TAPE LABEL
*                                       44 - DCB, JFCBDSNM
*                                       48 - JFCBDSNM
*                                       4C - JVCBVOLS, DSCB
*              BYTE 5-6 - TWO BYTE ID TO BE PUT IN WRITE-TO-PROGRAMMER
*                       MESSAGE.
CODTABLE EQU   *
         AIF   ('&COMP' EQ 'OPEN').GENOP
.*                                      IS OPEN EXPANSION REQUIRED
         AIF   ('&COMP' EQ 'OPENEX').GENOPEX
.*                                      IS OPEN EXECUTOR EXPANSION
.*                                      REQUIRED
         AIF   ('&COMP' EQ 'CLOSE').GENCL
.*                                      IS CLOSE EXPANSION REQUIRED
         AIF   ('&COMP' EQ 'TCLOSE').GENTC
.*                                      IS TCLOSE EXPANSION REQUIRED
         AIF   ('&COMP' EQ 'EOV').GENEOV
.*                                      IS EOV EXPANSION REQUIRED
         MEXIT
.GENOP   ANOP
         DC    X'0141360408'            1
         DC    C'45'
         DC    X'0241360804'            2
         DC    C'45'
         DC    X'0341360C04'            3
         DC    C'45'
         DC    X'0441361004'            4
         DC    C'45'
         DC    X'0541361404'            5
         DC    C'45'
         DC    X'0651360420'            6
         DC    C'46'
         DC    X'0711360404'            7
         DC    C'42'
         DC    X'08413E180C'            8
         DC    C'45'
         DC    X'096136040C'            9
         DC    C'47'
         DC    X'0AA136040C'            10
         DC    C'51'
         DC    X'0B613E0804'            11
         DC    C'47'
         DC    X'0C613E0C10'            12
         DC    C'47'
         DC    X'0D713E0400'            13                       XM3491
         DC    C'48'
         DC    X'0E813E0414'            14
         DC    C'49'
         DC    X'0F613E1004'            15
         DC    C'47'
         DC    X'10613E1404'            16
         DC    C'47'
         DC    X'1101360400'            17
         DC    C'41'
         DC    X'1201360800'            18
         DC    C'41'
         DC    X'1391360410'            19
         DC    C'50'
         DC    X'1491360810'            20
         DC    C'50'
         DC    X'1521360804'            21
         DC    C'43'
         DC    X'1691300C00'            22
         DC    C'50'
         DC    X'1741362408'            23
         DC    C'45'
         DC    X'189136100C'            24                       Y01048
         DC    C'50'                                             Y01048
         DC    X'1941362808'            25                      SA53143
         DC    C'45'                                            SA53143
         DC    X'1A41360408'            26
         DC    C'45'
         DC    X'1B41362004'            27
         DC    C'45'
         DC    X'1C71360800'            28                       XM3491
         DC    C'48'                                            SA55580
         DC    X'1D11360804'            29
         DC    C'42'
         DC    X'1E213E0448'            30
         DC    C'43'
         DC    X'1F21361804'            31
         DC    C'43'
         DC    X'2011360804'            32
         DC    C'42'
         DC    X'2111360404'            33
         DC    C'42'
         DC    X'2211360C3C'            34
         DC    C'42'
         DC    X'2311361004'            35
         DC    C'42'
         DC    X'2431360404'            36
         DC    C'44'
         DC    X'2541361C00'            37
         DC    C'45'
         DC    X'26C1360C04'            38
         DC    C'53'
         DC    X'27C1360804'            39
         DC    C'53'
         DC    X'28C1360448'            40
         DC    C'53'
         DC    X'2AC1361008'            42
         DC    C'53'
         DC    X'2B01366444'            43
         DC    C'41'
         DC    X'2C01366828'            44
         DC    C'41'
         DC    X'2E01366C08'            46
         DC    C'41'
         DC    X'2F01367010'            47
         DC    C'41'
         DC    X'3111361404'            49
         DC    C'42'
         DC    X'3211361804'            50
         DC    C'42'
         AIF   ('&LIB' EQ 'LIB1').LIB2PDM BR IF OS ASSEM       @X04ACFY
         DC    X'6F41302C00'            111                    @X04ACFY
         DC    C'45'                                           @X04ACFY
         DC    X'7041303000'            112                    @X04ACFY
         DC    C'45'                                           @X04ACFY
.LIB2PDM ANOP                                                  @X04ACFY
         DC    X'98A1360808'            152                     SA70631
         DC    C'51'                                            SA70631
         DC    X'99A1360C08'            153                     SA70632
         DC    C'51'                                            SA70632
         DC    X'9AA1361010'            154                     SA70633
         DC    C'51'                                            SA70633
         DC    X'9BA1361410'            155                     SA70634
         DC    C'51'                                            SA70634
         DC    X'9CA1361810'            156                     SA70635
         DC    C'51'                                            SA70635
         DC    X'D603D6044C'            214
         DC    C'56'
         DC    X'D703D6084C'            215
         DC    C'56'
         DC    X'D821360C04'            216
         DC    C'43'
         DC    X'D921361004'            217
         DC    C'43'
         DC    X'DCC1361808'            220                     SA66776
         DC    C'53'                                            SA66776
         DC    X'E021361C04'            224                     SA46620
         DC    C'43'                                            SA46620
         DC    X'E803D60C34'            232                    @OX10281
         DC    C'56'                                           @OX10281
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'00'                    END OF TABLE MARKER
*
*        XCTLTABL ID=(NEXTMOD,9B,LASTLOAD,8N),SVC=019
         XCTLTABL ID=(NEXTMOD,9B,LASTLOAD,8N),SVC=019
         AGO   .GENDSCT
.GENOPEX ANOP
         AIF   ('&LIB' EQ 'LIB1').LIB1051  JES, VS ONLY          S21940
         DC    X'2E01306C28'            46                      XL02942
         DC    C'41'                                            XL02942
         DC    X'2F01307028'            47                      XL02942
         DC    C'41'                                            XL02942
.LIB1051 ANOP                                                    S21940
         DC    X'3301300C28'            51
         DC    C'41'
         DC    X'340130102C'            52
         DC    C'41'
         DC    X'3501301430'            53
         DC    C'41'
         DC    X'3601301834'            54
         DC    C'41'
         DC    X'3701301C34'            55
         DC    C'41'
         DC    X'3801302028'            56
         DC    C'41'
         DC    X'3901302428'            57
         DC    C'41'
         DC    X'3A01302828'            58
         DC    C'41'
         DC    X'3B01302C28'            59
         DC    C'41'
         DC    X'3C01303C28'            60
         DC    C'41'
         DC    X'3D01303428'            61
         DC    C'41'
         DC    X'3E01303828'            62
         DC    C'41'
         DC    X'3F01303028'            63
         DC    C'41'
         DC    X'4001304028'            64
         DC    C'41'
         DC    X'4101304428'            65
         DC    C'41'
         DC    X'4201304828'            66
         DC    C'41'
         DC    X'43B1301004'            67
         DC    C'52'
         DC    X'44B1301400'            68
         DC    C'52'
         DC    X'45B1300C04'            69
         DC    C'52'
         DC    X'46B1300800'            70                       YM3213
         DC    C'52'                                             YM3213
         DC    X'47B1300400'            71
         DC    C'52'
         DC    X'4801304C28'            72
         DC    C'41'
         DC    X'4901305024'            73
         DC    C'41'
         DC    X'4A01305424'            74
         DC    C'41'
         DC    X'4B01305808'            75
         DC    C'41'
         DC    X'4C01305C28'            76
         DC    C'41'
         DC    X'4E14000404'            78
         DC    C'54'
         DC    X'4F24000428'            79
         DC    C'55'
         DC    X'5024000828'            80
         DC    C'55'
         DC    X'5124000C38'            81
         DC    C'55'
         DC    X'5224001028'            82
         DC    C'55'
         DC    X'5301306028'            83
         DC    C'41'
         DC    X'5401308428'            84
         DC    C'41'
         DC    X'5501307400'            85
         DC    C'41'
         DC    X'5601307820'            86
         DC    C'41'
         DC    X'5701307C20'            87
         DC    C'41'
         DC    X'5801308020'            88
         DC    C'41'
         DC    X'59B1301804'            89
         DC    C'52'
         DC    X'5AB1301C00'            90
         DC    C'52'
         DC    X'5BC1300400'            91
         DC    C'57'
         DC    X'5CD1300400'            92
         DC    C'58'
         DC    X'5DE1300400'            93
         DC    C'59'
         DC    X'5E01308800'            94                     @XA06868
         DC    C'41'
         DC    X'5FB1302000'            95                       YM3213
         DC    C'52'
         DC    X'60B1302400'            96                       YM3213
         DC    C'52'
         DC    X'61B1302800'            97                       YM3213
         DC    C'52'
         DC    X'62B1300804'            98                      XL03127
         DC    C'52'                                            XL03127
         DC    X'63B1301000'            99                      XL03127
         DC    C'52'                                            XL03127
         DC    X'65B1302C04'            101                      YM3213
         DC    C'52'                                             YM3213
         DC    X'66B1303004'            102                      YM3213
         DC    C'52'                                             YM3213
         DC    X'67B1303404'            103                      YM3213
         DC    C'52'                                             YM3213
         DC    X'6901308C28'            105                     SA55520
         DC    C'41'                                            SA55520
         AIF   ('&LIB' EQ 'LIB1').LIB1221  JES, 3890, VS ONLY    S21940
         DC    X'6A0130A428'            106                     XL02942
         DC    C'41'                                            XL02942
         DC    X'6B0130A828'            107                     XL02942
         DC    C'41'                                            XL02942
         AIF   ('&LIB' EQ 'LIB1').LIB2PDN BR IF OS ASSEM       @X04ACFY
         DC    X'6F41302C00'            111                    @X04ACFY
         DC    C'45'                                           @X04ACFY
.LIB2PDN ANOP                                                  @X04ACFY
         DC    X'7101349000'            113                     XL03123
         DC    C'41'                                            XL03123
         DC    X'7201349428'            114                     XL03123
         DC    C'41'                                            XL03123
         DC    X'7301349828'            115                     XL03123
         DC    C'41'                                            XL03123
         DC    X'7401349C28'            116                     XL03123
         DC    C'41'                                            XL03123
         DC    X'750134A028'            117                     XL03123
         DC    C'41'                                            XL03123
.LIB1221 ANOP                                                    S21940
         DC    X'D302060428'            211                    @OX10475
         DC    C'38'                                           @OX10475
         DC    X'D402060834'            212                    @OX10475
         DC    C'38'                                           @OX10475
         DC    X'D5013ED000'            213                    @SA72925
         DC    C'41'                                           @SA72925
         DC    X'DD0130B028'            221                     SA49404
         DC    C'41'                                            SA49404
         DC    X'DE0130B428'            222                     SA51107
         DC    C'41'                                            SA51107
         AIF   ('&LIB' EQ 'LIB1').LIB1223  3890, VS ONLY         S21940
         DC    X'DF0134B828'            223                     XL03123
         DC    C'41'                                            XL03123
.LIB1223 ANOP                                                    S21940
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'00'                    END OF TABLE MARKER
*
*  PSEUDO XCTL TABLE FOR XCTL BY NAME SINCE IEHIOSUP CANNOT LOOK UP
*  AN IFGXXXXX NAME FROM AN IGGXXXXX MODULE.
*
NEXTMOD  DC    C'9B'                    ID OF MODULE IFG0199B
         DC    X'00000000'              ZERO TTRL
LASTLOAD DC    C'8N'                    ID OF MODULE IFG0198N
         DC    X'00000000'              ZERO TTRL
*
*        XCTLTABL ID=,SVC=019           DUMMY XCTL TABLE
         XCTLTABL ID=,SVC=019           DUMMY XCTL TABLE
         AGO   .GENDSCT
.GENCL   ANOP
         DC    X'3D71460404'            61
         DC    C'15'
         DC    X'3E71460C04'            62
         DC    C'15'
         DC    X'3F71460804'            63
         DC    C'15'
         DC    X'4021460404'            64
         DC    C'10'
         DC    X'4121460804'            65
         DC    C'10'
         DC    X'5631460404'            86
         DC    C'11'
         DC    X'57A1460404'            87
         DC    C'16'
         DC    X'5841460404'            88
         DC    C'12'
         DC    X'5931460804'            89
         DC    C'11'
         DC    X'5A61460404'            90
         DC    C'14'
         DC    X'6F51460404'            111
         DC    C'13'
         DC    X'7000000000'            112
         DC    C'23'
         DC    X'78D1400400'            120
         DC    C'24'
         DC    X'79B1400400'            121
         DC    C'17'
         DC    X'7AB1400800'            122
         DC    C'17'
         DC    X'7BB1400C00'            123
         DC    C'17'
         DC    X'7CB1401000'            124
         DC    C'17'
         DC    X'7DB1401444'            125                     SA63266
         DC    C'17'                                            SA63266
         DC    X'7EB1401838'            126                     SA63266
         DC    C'17'                                            SA63266
         DC    X'ED31460C1C'            237                      XM2899
         DC    C'11'                                             XM2899
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'00'                    END OF TABLE MARKER
*
*        XCTLTABL ID=(NEXTMOD,9B,LASTLOAD,2L),SVC=020
         XCTLTABL ID=(NEXTMOD,9B,LASTLOAD,2L),SVC=020
         AGO   .GENDSCT
.GENTC   ANOP
         DC    X'7921760404'            121
         DC    C'19'
         DC    X'7A71760404'            122
         DC    C'22'
         DC    X'7B71760804'            123
         DC    C'22'
         DC    X'7C71760804'            124
         DC    C'22'
         DC    X'7D71760C04'            125
         DC    C'22'
         DC    X'7E71760C04'            126
         DC    C'22'
         DC    X'7F11761004'            127
         DC    C'18'
         DC    X'8011761004'            128
         DC    C'18'
         DC    X'8111761804'            129
         DC    C'18'
         DC    X'8211761C04'            130
         DC    C'18'
         DC    X'8311760404'            131
         DC    C'18'
         DC    X'8431760404'            132
         DC    C'20'
         DC    X'8541760404'            133
         DC    C'21'
         DC    X'8611762004'            134
         DC    C'18'
         DC    X'8711762404'            135
         DC    C'18'
         DC    X'8811762804'            136
         DC    C'18'
         DC    X'8911761404'            137
         DC    C'18'
         DC    X'8A71761004'            138
         DC    C'22'
         DC    X'8B11760804'            139
         DC    C'18'
         DC    X'8C11762C04'            140
         DC    C'18'
         DC    X'8D1176300C'            141
         DC    C'18'
         DC    X'8E00000034'            142                     SA48558
         DC    C'25'                                            SA48558
         DC    X'8F00000434'            143                     SA48558
         DC    C'25'                                            SA48558
         DC    X'9000000834'            144                     SA48558
         DC    C'25'                                            SA48558
         DC    X'9100000C34'            145                     SA48558
         DC    C'25'                                            SA48558
         DC    X'9700001034'            151                     SA58019
         DC    C'25'                                            SA58019
         DC    X'EC3176081C'            236                      XM2899
         DC    C'20'                                             XM2899
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'00'                    END OF TABLE MARKER
*
*        XCTLTABL ID=(NEXTMOD,9B,LASTLOAD,2Z),SVC=023
         XCTLTABL ID=(NEXTMOD,9B,LASTLOAD,2Z),SVC=023
         AGO   .GENDSCT
.GENEOV  ANOP
         AIF   ('&LIB' EQ 'LIB1').LIB2PDO BR IF OS ASSEM       @X04ACFY
         DC    X'6F73703400'            111                    @X04ACFY
         DC    C'27'                                           @X04ACFY
         DC    X'7073703800'            112                    @X04ACFY
         DC    C'27'                                           @X04ACFY
.LIB2PDO ANOP                                                  @X04ACFY
         DC    X'8D13740404'            141
         DC    C'22'
         DC    X'8EB3740408'            142
         DC    C'30'
         DC    X'8F83740404'            143
         DC    C'28'
         DC    X'9063740404'            144
         DC    C'26'
         DC    X'9143740400'            145
         DC    C'25'
         DC    X'9213740804'            146
         DC    C'22'
         DC    X'9313740C04'            147
         DC    C'22'
         DC    X'94237C043C'            148                    @XA07756
         DC    C'23'
         DC    X'9563740804'            149
         DC    C'26'
         DC    X'9613741004'            150
         DC    C'22'
         DC    X'9763741404'            151
         DC    C'26'
         DC    X'9863741804'            152
         DC    C'26'
         DC    X'9963741C04'            153
         DC    C'26'
         DC    X'9A63742004'            154
         DC    C'26'
         DC    X'9B63742404'            155
         DC    C'26'
         DC    X'9CB3740408'            156
         DC    C'30'
         DC    X'9D63742804'            157
         DC    C'26'
         DC    X'9E63742C08'            158
         DC    C'26'
         DC    X'9F63743004'            159
         DC    C'26'
         DC    X'A063743404'            160
         DC    C'26'
         DC    X'A163743804'            161
         DC    C'26'
         DC    X'A263743C04'            162
         DC    C'26'
         DC    X'A363744004'            163
         DC    C'26'
         DC    X'A413741404'            164
         DC    C'22'
         DC    X'A523740814'            165
         DC    C'23'
         AIF   ('&LIB' NE 'LIB1').LIB2168  2321, OS ONLY         S21940
         DC    X'A6B3700408'            166
         DC    C'30'
         DC    X'A773701804'            167
         DC    C'27'
.LIB2168 ANOP                                                    S21940
         DC    X'A8B3700408'            168
         DC    C'30'
         DC    X'A973700804'            169
         DC    C'27'
         DC    X'AA73702004'            170
         DC    C'27'
         DC    X'AB73700C04'            171
         DC    C'27'
         DC    X'AC73700804'            172
         DC    C'27'
         AIF   ('&LIB' NE 'LIB1').LIB2174  2321, OS ONLY         S21940
         DC    X'AD73701804'            173
         DC    C'27'
.LIB2174 ANOP                                                    S21940
         DC    X'AE33700428'            174
         DC    C'24'
         DC    X'AF73701404'            175
         DC    C'27'
         DC    X'B063740C28'            176
         DC    C'26'
         DC    X'B1B3740800'            177                      M0179
         DC    C'30'                                             M0179
         DC    X'B273741C04'            178
         DC    C'27'
         DC    X'B373741C04'            179
         DC    C'27'
         DC    X'B413741C10'            180
         DC    C'22'
         DC    X'B553740408'            181                     SA47382
         DC    C'16'                                            SA47382
         DC    X'B643710828'            182                    @XA06864
         DC    C'25'                                             A48604
         DC    X'B7E3740800'            183                      A47372
         DC    C'32'                                             A47372
         DC    X'B8E3740400'            184
         DC    C'32'
         DC    X'B9B3740408'            185
         DC    C'30'
         DC    X'BAD374041C'            186
         DC    C'31'
         DC    X'BB73741004'            187
         DC    C'27'
         DC    X'BC73740404'            188
         DC    C'27'
         DC    X'BD73740804'            189
         DC    C'27'
         DC    X'BE73740404'            190
         DC    C'27'
         DC    X'BF13741804'            191
         DC    C'22'
         DC    X'C063744404'            192
         DC    C'26'
         DC    X'C163744804'            193
         DC    C'26'
         DC    X'C263741010'            194
         DC    C'26'
         DC    X'C313742010'            195
         DC    C'22'
         DC    X'C463744C04'            196
         DC    C'26'
         DC    X'C583740800'            197
         DC    C'28'
         DC    X'C6A3710444'            198                    @XA06864
         DC    C'15'                                            SA52379
         AIF   ('&LIB' EQ 'LIB1').LIB1218  DEBCHK, 3886, VS ONLY S21940
         DC    X'C743700C28'            199                      YM2133
         DC    C'25'                                             YM2133
         DC    X'C800500400'            200                     XL03127
         DC    C'41'                                            XL03127
         DC    X'C900500800'            201                     XL03127
         DC    C'41'                                            XL03127
.LIB1218 ANOP                                                    S21940
         DC    X'DA73702438'            218                     SA58102
         DC    C'27'                                            SA58102
         DC    X'DBA3710828'            219                    @XA06864
         DC    C'15'                                            SA59307
         DC    X'E173742804'            225                     SA46620
         DC    C'27'                                            SA46620
         DC    X'EB7376301C'            235                      XM2899
         DC    C'27'                                             XM2899
         DC    X'FB6374500C'            251                     SA65517
         DC    C'26'                                            SA65517
         DC    X'FC33700828'            252                     XA03145
         DC    C'24'                                            XA03145
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'0000000000'            END OF TBL - ZAP NEW CODES HERE
         DC    C'  '
         DC    X'00'                    END OF TABLE MARKER
*
*        XCTLTABL ID=(NEXTMOD,9B,LASTLOAD,2X),SVC=055
         XCTLTABL ID=(NEXTMOD,9B,LASTLOAD,2X),SVC=055
.GENDSCT ANOP
         EJECT
TABLE    DSECT
CODE     DS    CL1
ABCODE   DS    CL2
RETCODE  DS    CL1
TRCCODE  DS    CL1
MSGID    DS    CL2
*        IECPDSCT
         IECPDSCT
*        IECDSECS MAIN,EXPAND=YES
         IECDSECS MAIN,EXPAND=YES
         MEND
