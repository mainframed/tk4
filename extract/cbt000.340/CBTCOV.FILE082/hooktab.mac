          GENERATE;                /*
         MACRO
&NAME    HOOKTAB    &EL1=,&EL2=,&EL3=,&EL4=,&EL5=,&EL6=,&EL7=,&EL8=,   X
               &EL9=,&EL10=,&EL11=,&EL12=,&EL13=,&EL14=,&EL15=,&EL16=, X
               &EL17=,&EL18=,&EL19=,&EL20=,&EL21=,&EL22=,&EL23=,&EL24=,X
               &EL25=,&TERMAD=0,                                       X
               &FC1=,&FC2=,&FC3=,&FC4=,&FC5=,&FC6=,&FC7=,&FC8=,        X
               &FC9=,&FC10=,&FC11=,&FC12=,&FC13=,&FC14=,&FC15=,&FC16=, X
               &FC17=,&FC18=,&FC19=,&FC20=,&FC21=,&FC22=,&FC23=,&FC24=,X
               &FC25=
         GBLA  &N,&M,&R,&DEC(100)
         GBLB  &B1(25),&B2(25),&B3(25),&B4(25),&B5(25),&B6(25),&B7(25)
         GBLC  &H(25),&HX(25),&A(25),&DEN(100),&E(25)
         LCLA  &L,&BOT
         LCLB  &B4T
.*A000000-999999                                                 S21014
.*                      INITIALIZE GLOBALS                         INIT
&M       SETA  0                                                   INIT
.CK      AIF   (&N EQ 0).CKR                                       INIT
&HX(&N)  SETC  ''                                                  INIT
&B1(&N)  SETB  0                                                   INIT
&B2(&N)  SETB  0                                                   INIT
&B3(&N)  SETB  0                                                   INIT
&B4(&N)  SETB  0                                                   INIT
&B5(&N)  SETB  0                                                   INIT
&B6(&N)  SETB  0                                                   INIT
&B7(&N)  SETB  0                                                   INIT
&N       SETA  &N-1                                                INIT
         AGO   .CK                                                 INIT
.CKR     AIF   (&R EQ 0).START                                     INIT
&DEC(&R) SETA  0                                                   INIT
&R       SETA  &R-1                                                INIT
         AGO   .CKR                                                INIT
.*                                                                 INIT
.START   AIF   ('&EL1' EQ '').E2
         HKENT &EL1(1),&EL1(2),&EL1(3),&FC1,EL1
.E2      AIF   ('&EL2' EQ '').E3
         HKENT &EL2(1),&EL2(2),&EL2(3),&FC2,EL2
.E3      AIF   ('&EL3' EQ '').E4
         HKENT &EL3(1),&EL3(2),&EL3(3),&FC3,EL3
.E4      AIF   ('&EL4' EQ '').E5
         HKENT &EL4(1),&EL4(2),&EL4(3),&FC4,EL4
.E5      AIF   ('&EL5' EQ '').E6
         HKENT &EL5(1),&EL5(2),&EL5(3),&FC5,EL5
.E6      AIF   ('&EL6' EQ '').E7
         HKENT &EL6(1),&EL6(2),&EL6(3),&FC6,EL6
.E7      AIF   ('&EL7' EQ '').E8
         HKENT &EL7(1),&EL7(2),&EL7(3),&FC7,EL7
.E8      AIF   ('&EL8' EQ '').E9
         HKENT &EL8(1),&EL8(2),&EL8(3),&FC8,EL8
.E9      AIF   ('&EL9' EQ '').E10
         HKENT &EL9(1),&EL9(2),&EL9(3),&FC9,EL9
.E10     AIF   ('&EL10' EQ '').E11
         HKENT &EL10(1),&EL10(2),&EL10(3),&FC10,EL10
.E11     AIF   ('&EL11' EQ '').E12
         HKENT &EL11(1),&EL11(2),&EL11(3),&FC11,EL11
.E12     AIF   ('&EL12' EQ '').E13
         HKENT &EL12(1),&EL12(2),&EL12(3),&FC12,EL12
.E13     AIF   ('&EL13' EQ '').E14
         HKENT &EL13(1),&EL13(2),&EL13(3),&FC13,EL13
.E14     AIF   ('&EL14' EQ '').E15
         HKENT &EL14(1),&EL14(2),&EL14(3),&FC14,EL14
.E15     AIF   ('&EL15' EQ '').E16
         HKENT &EL15(1),&EL15(2),&EL15(3),&FC15,EL15
.E16     AIF   ('&EL16' EQ '').E17
         HKENT &EL16(1),&EL16(2),&EL16(3),&FC16,EL16
.E17     AIF   ('&EL17' EQ '').E18
         HKENT &EL17(1),&EL17(2),&EL17(3),&FC17,EL17
.E18     AIF   ('&EL18' EQ '').E19
         HKENT &EL18(1),&EL18(2),&EL18(3),&FC18,EL18
.E19     AIF   ('&EL19' EQ '').E20
         HKENT &EL19(1),&EL19(2),&EL19(3),&FC19,EL19
.E20     AIF   ('&EL20' EQ '').E21
         HKENT &EL20(1),&EL20(2),&EL20(3),&FC20,EL20
.E21     AIF   ('&EL21' EQ '').E22
         HKENT &EL21(1),&EL21(2),&EL21(3),&FC21,EL21
.E22     AIF   ('&EL22' EQ '').E23
         HKENT &EL22(1),&EL22(2),&EL22(3),&FC22,EL22
.E23     AIF   ('&EL23' EQ '').E24
         HKENT &EL23(1),&EL23(2),&EL23(3),&FC23,EL23
.E24     AIF   ('&EL24' EQ '').E25
         HKENT &EL24(1),&EL24(2),&EL24(3),&FC24,EL24
.E25     AIF   ('&EL25' EQ '').E26
         HKENT &EL25(1),&EL25(2),&EL25(3),&FC25,EL25
.E26     AIF   (N'&SYSLIST GT 2*&N+1).ERR1
         AIF   (N'&SYSLIST GT 2*&N AND '&TERMAD' EQ '0').ERR1
.*       TEST STATI OF ELEMENTS
&L       SETA  &N+1
.HT1     ANOP
&L       SETA  &L-1
         AIF   (&L EQ 0).GENTAB
&BOT     SETA  &L
.HT2     AIF   (&B6(&L)).HT1
&L       SETA  &L-1
         AIF   (&B3(&L) EQ &B3(&BOT)).HT2
&B3(&L)  SETB  (&B3(&BOT))
         AIF   (NOT &B7(&L)).HT2
         MNOTE 4,' ISM30105 *** &E(&L) *** STATUS CONFLICTS WITH CHAIN'
         AGO   .HT2
.GENTAB  SPACE
         CNOP  0,4
&NAME    EQU   *
         DC    AL2(&N)            NUMBER OF ELEMENTS SPECIFIED
         DC    AL2(&N+&M)         NUMBER OF PHYSICAL ENTRIES
         DC    A(&TERMAD)         TERMAD ADDRESS
         SPACE
         AIF   (&N EQ 0).END
&B4(&N)  SETB  0
.GMORE   ANOP
&L       SETA  &L+1
&B4T     SETB  (&B4(&L))
&B4(&L)  SETB  (&B4(&L) OR '&HX(&L)' NE '')
.IRANGE  ANOP
         DC    BL1'&B1(&L)&B2(&L)&B3(&L)&B4(&L)&B5(&L)&B6(&L).00'
         DC    AL1(0)             AID
         DC    XL2'&H(&L)'
         DC    A(&A(&L))          ROUTINE ADDRESS
         AIF   ('&HX(&L)' EQ '').NRANGE
&H(&L)   SETC  '&HX(&L)'
&HX(&L)  SETC  ''
&B4(&L)  SETB  (&B4T)
&B5(&L)  SETB  1
&B6(&L)  SETB  0
         AGO   .IRANGE
.NRANGE  SPACE
         AIF   (&L NE &N).GMORE
.*                     CONTROL INFORMATION
&L       SETA  0
.DMORE   ANOP
&L       SETA  &L+1
         DC    AL1(&DEC(&L))
         DC    AL1(&DEN(&L))
         AIF   (&DEC(&L) NE 0).TESTL
         SPACE
.TESTL   AIF   (&L LT &R).DMORE
.END     MEXIT
.ERR1    MNOTE 8,' ISM30101 *** TOO MANY OPERANDS '
         MEND
         MACRO
         HKENT &E1,&E2,&E3,&E4,&ELN
         GBLA  &N,&M,&R,&DEC(100)
         GBLB  &B1(25),&B2(25),&B3(25),&B4(25),&B5(25),&B6(25),&B7(25)
         GBLC  &H(25),&HX(25),&A(25),&DEN(100),&E(25)
         LCLA  &X,&Y
         LCLC  &R1,&R2
&N       SETA  &N+1
         AIF   (K'&E1 NE 4).NHK
         AIF   ('&E1' EQ 'NULL').NULL
&H(&N)   SETC  '&E1'
         AGO   .ADDR
.NULL    ANOP
&H(&N)   SETC  '0000'
&B5(&N)  SETB  1
         AGO   .ADDR
.NHK     AIF   (K'&E1 NE 1).NCL
&H(&N)   SETC  '&E1'.'000'
&B2(&N)  SETB  1
         AGO   .ADDR
.NCL     AIF   (K'&E1 NE 11).NRG
&H(&N)   SETC  '&E1'(2,4)
&HX(&N)  SETC  '&E1'(7,4)
&Y       SETA  1
.RCK     AIF   (&Y GT 5).ROK
&Y       SETA  &Y+1
&R1      SETC  '&E1'(&Y,1)
&R2      SETC  '&E1'(&Y+5,1)
         AIF   ('&R1' EQ '&R2').RCK
         AIF   ('&R1' GE '0' AND '&R2' GE '0' AND '&R1' LE '&R2').ROK
         AIF   ('&R1' LT '0' AND '&R2' LT '0' AND '&R1' LE '&R2').ROK
         AIF   ('&R1' GE '0' AND '&R2' LT '0').ROK
         MNOTE 4,' ISM30108 *** &ELN *** IMPROPER RANGE SPECIFICATION '
.ROK     ANOP
&B1(&N)  SETB  1
&M       SETA  &M+1
         AGO   .ADDR
.NRG     AIF   ('&E1' NE 'ALL').ERR3
&H(&N)   SETC  '0000'
&B1(&N)  SETB  1
&B2(&N)  SETB  1
         AGO   .ADDR
.ERR3    MNOTE 8,' ISM30102 *** &ELN *** INVALID HOOK SPECIFICATION '
.ADDR    AIF   ('&E2' NE '').ADDOK
         MNOTE 8,' ISM30103 *** &ELN *** NO ADDRESS SPECIFIED '
.ADDOK   ANOP
&A(&N)   SETC  '&E2'
         AIF   ('&E3' EQ '' OR '&E3' EQ 'E' OR '&E3' EQ 'D').VALID
         AIF   ('&E3' EQ 'L' OR '&E3' EQ 'LE' OR '&E3' EQ 'LD').VALID
         MNOTE 8,' ISM30104 *** &ELN *** INVALID STATUS '
&B4(&N)  SETB  1
         AGO   .OPTS
.VALID   ANOP
.*                     CHAIN BIT
&B4(&N)  SETB  ('&E3' EQ '' OR '&E3' EQ 'E' OR '&E3' EQ 'D')
.*                     DISABLE-ENABLE BIT
&B3(&N)  SETB  ('&E3' EQ 'D' OR '&E3' EQ 'LD')
&B7(&N)  SETB  ('&E3' EQ 'E' OR '&E3' EQ 'D')
&E(&N)   SETC  '&ELN'
.OPTS    ANOP
         AIF   (&N EQ 1).FIRSTE
         AIF   (&B4(&N-1)).OPTSC
.FIRSTE  ANOP
&B6(&N)  SETB  1
.OPTSC   ANOP
&R       SETA  &R+1
         AIF   (&R GT 100).ERR7
&X       SETA  &X+1
         AIF   (&X LE N'&E4).MORE
&DEN(&R) SETC  '0'
         MEXIT
.MORE    AIF   (&DEC(&R) EQ 4).ERR6
&DEC(&R) SETA  &DEC(&R)+1
         AIF   ('&E4(&X)'(1,2) NE 'E+E-D+D-'(2*&DEC(&R)-1,2)).MORE
&DEN(&R) SETC  '&E4(&X)'(3,K'&E4(&X)-2)
         AGO   .OPTSC
.ERR6    MNOTE 8,' ISM30106 *** &ELN *** INVALID OPERAND IN FC '
         AGO   .OPTSC
.ERR7    MNOTE 8,' ISM30107 *** &ELN *** FC MAXIMUM OVERRUN '
         MEND
 @ENDGEN                       */
