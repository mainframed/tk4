         MACRO
&NAME    CWRITE &RESP
         LCLA  &A,&OPNUM
         LCLB  &CHRNUM,&SETRSP
         LCLC  &PARAM
&A       SETA  4
&OPNUM   SETA  N'&SYSLIST               NUMBER OF OPERANDS
         AIF   (&OPNUM EQ 0).CONT       NO OPERAND
         AIF   (&OPNUM GT 1).CHECK      MORE THAN ONE OPERAND SPEC
&CHRNUM  SETB  (K'&RESP GT 1)
         AIF   (&CHRNUM).ERR2           OPERAND WITH MORE THAN 1 CHAR
&PARAM   SETC  '&RESP'
&SETRSP  SETB  ('&PARAM' EQ 'R')
         AIF   (NOT &SETRSP).ERR3
&A       SETA  6
.CONT    ANOP
         CNOP  0,4
&NAME    OI    4(15),X'&A'              SET WRITE/RESP FLAG
         BAL   1,*+8                    LOAD ADD OF NEXT IN REG 1
.PRMLST  DC    F'0'                     FULL WORD
         ST    15,0(0,1)                STORE ADDRESS IN PARM LIST
         L     15,IHKYYLAD              GET ENTRY POINT ADDRESS
         BALR  14,15                    LINK TO LINE ADMINISTRATOR
         MEXIT
.CHECK   AIF   (N'&SYSLIST(1) EQ 0).ERR0      FIRST OPERAND OMITTED
         MNOTE 7,'MORE THAN ONE OPERAND SPECIFIED'
         MEXIT
.ERR2    MNOTE 7,'SINGLE OPERAND CONSISTS OF MORE THAN ONE CHAR'
         MEXIT
.ERR3    MNOTE 7,'SINGLE OPERAND SPECIFIED WRONG,-SHOULD BE CHAR R'
         MEXIT
.ERR0    MNOTE 7,'MORE OPERANDS SPECIFIED AND FIRST OPER OMITTED'
         MEND
