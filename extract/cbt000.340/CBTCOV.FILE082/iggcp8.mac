         MACRO
      IGGCP8   &OPTCD=
.*         RELEASE 23 DELETIONS/CHANGES
.*         RELEASE 22 DELETIONS/CHANGES
.*         RELEASE 21 DELETIONS/CHANGES
*              CP8--SRCH TRK IX AND PRIME DATA TRACK
CB1      CCW   SIDEQ,0,CC,5             READ R0 WHICH MAY BE
CB2      CCW   TIC,CB1-CB1,0,0          CYLINDER OVERFLOW CONTROL
CB3      CCW   RDATA,CB22-CB1,CC+SILI,6    RECORD
CB4      CCW   RDCNT+MT,CB22+6-CB1,CC+SILI,5     READ COUNT OF IX ENTRY
CB5      CCW   SKEQHI,0,CC,0            SEARCH FOR CORRECT ENTRY
CB6      CCW   TIC,CB4-CB1,X'00',0
CB7      CCW   RDATA,CB10+7-CB1,CC,10   READ TRACK INDEX ENTRY
CB8      CCW   RDCNT+MT,CB24-CB1,CC,8   READ COUNT OF NEXT ENTRY
CB8A     CCW   RDATA,CB25-CB1,CC,10     READ DATA OF NEXT ENTRY
CB9      CCW   TIC,CB12-CB1,0,0
CB10     DC    2F'0'                    - - - - - - - M
CB11     DC    2F'0'                    B B C C H H R F
CB12     CCW   X'00',CB11-CB1,CC,6      P SEEK TO PRIME DATA TRK
CB16     CCW   NOP,CB16+5-CB1,CC+SILI,1     SET SECTOR 0 IF RPS
         AIF   ('&OPTCD' NE 'V' AND '&OPTCD' NE 'VW').FIXED
CB17     CCW   RHA,0,CC+SILI+SKIP,1     ORIENT TO H.A.
CB18     CCW   TIC,CB18B-CB1,X'00',0    AVOID SKIPPING FIRST RECORD
CB18A    CCW   RDATA,0,CC+SILI,0        READ IN BLOCK PRIOR TO INS BLK
CB18B    CCW   RDCNT,CB23+3-CB1,CC+SILI,5  GET PROBABLE INS BLK COUNT
CB19     CCW   SKEQHI,0,CC,0            SEARCH FOR PROBABLE INSERT BLK
CB20     CCW   TIC,CB18A-CB1,X'00',0
         AGO   .COMMON
.FIXED   ANOP
CB17     CCW   SIDEQ,CB11+2-CB1,CC,5    SEARCH FOR PRIME RECORD
CB18     CCW   TIC,CB17-CB1,X'00',0
CB18A    CCW   TIC,CB19-CB1,X'00',0     AVOID SKIPPING FIRST RECORD
CB18B    CCW   RDCNT,CB23+3-CB1,CC+SILI,5  GET COUNT OF INSERT RECORD
CB19     CCW   SKEQHI,0,CC+SILI,0       SEARCH TRK FOR INSERT BLOCK
CB20     CCW   TIC,CB18B-CB1,X'00',0
.COMMON  ANOP
CB21     CCW   NOP,0,SILI,1             READ INSERT BLK SECTOR IF RPS
CB22     DC    D'0'                     H H R Y Y T C C
CB23     DC    D'0'                     H H R C C H H R
CB24     DC    D'0'                     C C H H R KL DL DL
CB25     DC    D'0'                     M B B C C H H R
CB26     DC    D'0'                     F P - - - - - -
SZ8      EQU   CB26+8-CB1
         AIF   ('&OPTCD' NE 'V' AND '&OPTCD' NE 'VW').END
SZ8VW    EQU   SZ8+0
.END     ANOP
         MEND
