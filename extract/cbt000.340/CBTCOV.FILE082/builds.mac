         MACRO
&NAME    BUILDS &BUFLIST,&NUMSIZE
         AIF   ('&BUFLIST' NE '(1)' OR '&NUMSIZE' NE '(0)').NOGOOD
&NAME    STM   RC,RE,12(RN)        SAVE REGS
         L     RC,0(0,RB)          FIRST BUFFER ADDRESS-8
         LR    RD,RA               MOVE # OF BUFFERS AND LENGTH TO RD
         LA    RD,3(0,RD)          UP IT BY 3
         ST    RD,4(0,RC)          STORE IN FIRST BUFFER-4
         NI    7(RC),X'FC'         ROUND LENGTH TO FULL WORD
         LA    RE,8(0,RC)          RE POINTS TO ACTUAL BUFFER
         ST    RE,0(0,RC)          STORE BUFFER ADDRESS IN 8 BYTE CONT.
         SRL   RD,16               POSITION # OF BUFFERS
BLDCONT  BCT   RD,BLDLOOP          ARE WE DONE
         B     BUILDEND            YES
BLDLOOP  LA    RB,4(0,RB)          NO - UP POINTER TO PARAMETER LIST
            L     RC,0(0,RB)          GET ADDRESS OF NEXT BUFFER
         ST    RC,0(0,RE)          LINK BUFFERS
         LR    RE,RC               KEEP ADDRESS OF NEW BUFFER
         B     BLDCONT             CONTINUE BUILDING
BUILDEND SR    RC,RC
         ST    RC,0(0,RE)          PUT ZERO'S IN LAST LINK LOCATION
         LM    RC,RE,12(RN)        RESTORE REGISTERS
         MEXIT
.NOGOOD  MNOTE 'OPERAND OF BUILDS MACRO MUST BE ''(1),80)'''
         MEND
