         MACRO
         IEEGGLDN
         TITLE 'DEFINE'
DEFINE   EQU   *
*                  OS360 TASK SCHEDULERS/MASTER SCHEDULER
*                  P. GUM  B84  705(2-N-6) 34532  LD10660114
*                  VERSION 3 12/19/64
SEVEN    EQU   7
COMD     EQU   MSCMD
         USING COMD,P
         LA    P,1(P,Q)           COUNT+1+ORIGIN OF PREVIOUS LABEL.
         SR    Q,Q                RE-INITIALIZE COUNT TO 0.
*
         CLI   COMD,BLANK    RETURN
         BCR   E,R            SET FOR
         CLI   COMD,EOB       ALTERNATE EXIT
         BCR   E,R            FOR FIRST CHARACTER TEST.
         LA    P,1(P,0)      MOVE OVER ONE-CHARACTER DELIMITER.
         DROP  P
         USING COMD,S
GNEXT    EQU   *
         LA    S,0(P,Q)           LABEL CHARACTER COUNT + ORIGIN
         CLI   COMD+1,COMMA
         BE    GEXIT
         CLI   COMD+1,EQUAL
         BE    GEXIT
         CLI   COMD+1,BLANK
         BE    GEXIT
         CLI   COMD+1,EOB
         BE    GEXIT
         LA    Q,1(Q,0)           INCREMENT COUNTER
         B     GNEXT
GEXIT    LA    S,SEVEN       SET CONDITION CODE FOR LABEL EXCEEDING
         CLR   Q,S             EIGHT CHARACTERS (BH, ERROR)
         LA    S,1(P,Q)      ADDRESS OF TRAILING DELIMITER.
         BC    15,4(0,R)     NORMAL RETURN.
         DROP  S
         MEND
