         MACRO
         LCBD  &QTAM=
*  LINE CONTROL BLOCK (LCB) DEFINITION
         SPACE
IECKLCB0 DSECT
LCB      EQU   IECKLCB0
LCBSTATE DS    XL1
*                      01=FREE              *  10=INITIATE
*                      02=PARTIAL MSG IN Q  *  20=CONVERSE
*                      04=SEND              *  40=RECALL
*                      08=RECEIVE           *  80=CLEANUP
LCBENDOP DS    AL3
*                  IF INCOMING MSG, CONTENTS OF RREG1 FROM ROUTE MACRO
*                  IF OUTGOING MSG, ADDR OF LCB FOR ORIGINATING LINE
LCBCECB  DS    XL1                 BTAM OPCODE FOR CURR SEG OF CURR MSG
LCBRCADD DS    AL3 DISK ADDR OF LAST CORRECTLY TRANS SEG IN CURR MSG
LCBSCHAD DS    A                   ADDR OF 1ST WAITING QTAM SUBTASK
         SPACE
LCBCPRI  DS    FL1                 PRIORITY OF RECEIVE SCHEDULER
LCBSCHLK DS    AL3                 LINK ADDR OF RECEIVE SCHEDULER
LCBCHDR  DS    AL3                 DISK ADDR OF CURR MSG HEADER
LCBCSEG  DS    AL3                 DISK ADDR OF CURR MSG SEGMENT
LCBNASEG DS    AL3     PTR TO 1ST SEG NOT SENT OF INCOMING MESSAGE
LCBSORCE DS    AL3 ADDR OF LCB FOR ANOTHER LINE CURRENTLY SENDING TO   *
               SAME DEST.  (LOW ORDER BIT='IN-SOURCE CHAIN' CODE)
LCBMSGPR DS    FL1                 PRIORITY OF CURR INCOMING MSG
LCBDESTQ DS    AL3                 ADDR OF QCB FOR DESTINATION TERMINAL
LCBMPLRT DS    AL1                 SCAN POINTER FOR NEXT DESTINATION
LCBCLPCI DS    AL3                 ADDR OF LAST CCW FOR WHICH PCI RCVD
LCBCLCCW DS    A  ADDR OF LAST CCW FOR WHICH A BUFFER WAS ASSIGNED
LCBERRST DS    XL2                 ERROR HALFWORD
LCBBRKCT DS    H IF RECEIVING, CONTAINS LAST STATUS OF SEQIN (TERMTBL)
*                IF NOT RECEIVING, CONTAINS TIME OF REQUESTED INTERRUPT
LCBTTIND DS    H                   PTR TO TERMTBL ENTRY FOR CURR MSG
LCBDLPTR DS    H  PTR TO NEXT ENTRY IN DISTRIBUTION LIST
LCBFLAG1 DS    XL1                 STATUS BITS USED BY I/O SUPERVISOR
LCBFLAG2 DS    XL1                 STATUS BITS USED BY I/O SUPERVISOR
LCBSENSE DS    H    ERROR INFO STORED BY I/O SUPERVISOR
LCBECBPT DS    A                   NOT USED BY QTAM
LCBCSW   DS    D                   CHANNEL STATUS WORD
LCBSIOCC DS    XL1                 START I/O CONDITION CODE
LCBSTART DS    AL3 PTR TO 1ST CCW EXECUTED IN CHANNEL PROGRAM
LCBWGHT  DS    0XL1                NOT USED BY QTAM
LCBDCBPT DS    A                   PTR TO DCB
LCBRESTR DS    A PTR TO CCW FOR SIO COMMAND FOR 1ST MSG DATA TRANSFER
LCBINCAM DS    H                   ZERO
LCBERRCT DS    H                   NOT USED BY QTAM
LCBUCBX  DS    FL1                 INDEX TO UCB IN DEB
LCBPTEMP DS    FL1   TEMPORARY STORAGE FOR MESSAGE PRIORITY
LCBTRST  DS    H                   ADDR OF EOB CHAR RELATIVE TO ADDR OF
*                          LAST CORRECTLY TRANSMITTED SEG OF CURR MSG
LCBPOLCT DS    FL1                 CNT OF MSGS RECEIVED FROM TERMINAL
LCBPOLPT DS    AL3      PTR TO CURR ACTIVE ENTRY IN POLLING LIST
         AIF   ('&QTAM' EQ '').NO
         AIF   ('&QTAM' NE '3').NO
LCBERCCW DS    D                   CCW AREA FOR ERP
.NO      ANOP
LCBCPA   DS    0D                  CHANNEL PROGRAM AREA
         SPACE
         MEND
