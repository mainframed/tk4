         MACRO
        IEAAPL &PA
         LCLB  &ONETASK
&ONETASK SETB  (T'&PA EQ 'O')
         AIF   (&ONETASK EQ 0).SKIPD
         TITLE 'ROUTINE NAME - PROLOG.  MODULE NAME - IEAAPL00.'
*GENERATION- DEC65,CHANGE LEVEL 000
*    ROUTINE NAME - PROLOG.  MODULE NAME - IEAAPL00.
         AGO   .SKIPE
.SKIPD   ANOP
         TITLE 'ROUTINE NAME - PROLOG.  MODULE NAME - IEAGPL00.'
*GENERATION- DEC65,CHANGE LEVEL 000
*    ROUTINE NAME - PROLOG.  MODULE NAME - IEAGPL00.
.SKIPE   ANOP
*
*
*    FUNCTION - PROLOG DETERMINES THE TASK WITH WHICH THIS PROGRAM
*    INTERRUPT IS ASSOCIATED. IT THEN SETS UP THE ABTERM LINKAGE AND
*    BRANCHES TO THE ABTERM MODULE.
*
*
*    ENTRY - THE PROLOG ENTRY POINT NAME IS IEA0PL00. PROLOG MAY BE
*    ENTERED USING A BRANCH INSTRUCTION. PROLOG MUST BE ENTERED IN
*    SUPERVISOR STATE, WITH SUPERVISOR PROTECTION KEY, AND WITH INPUT/
*    OUTPUT AND EXTERNAL INTERRUPTS DISABLED.
*
*
*    INPUT - THE PROGRAM INTERRUPT OPSW MUST NOT HAVE BEEN MODIFIED.
*    REGISTERS 10 THROUGH 1 AT THE TIME OF PROGRAM INTERRUPT MUST BE
*    SAVED IN THE PROGRAM INTERRUPT FLIH SAVE AREA (PDSAV).
*
*
*    OUTPUT -
*    REGISTER 0 CONTAINS THE ADDRESS OF THE TASK TO BE ABNORMALLY
*               TERMINATED.
*    REGISTER 1 CONTAINS THE COMPLETION CODE.
*    REGISTER 14 CONTAINS THE ADDRESS OF THE LOCATION TO WHICH ABTERM
*                WILL RETURN CONTROL.
*    IN THE CASE OF A PROGRAM INTERRUPT FROM A PROGRAM HAVING A
*    SCHEDULED REQUEST BLOCK,
*    . THE PIOPSW IS SAVED IN THE RESUME PSW SLOT IN THE REQUEST BLOCK.
*    . REGISTERS 10 THROUGH 1 AT THE TIME OF PROGRAM INTERRUPT ARE
*      SAVED IN THE TCB REGISTER SAVE AREA.
*
*
*
*
*    EXTERNAL REFERENCES -
*    . DISMISS - AN ENTRY POINT IN THE I/O FLIH.
*    . IEA0DS - AN ENTRY POINT IN THE DISPATCHER
*    . IEA0AB00 - THE ENTRY POINT IN ABTERM.
         AIF   (&ONETASK EQ 1).SKIPF
*    . IEATCBLK - A TABLE WHICH CONTAINS THE ADDRESSES OF THE TCB'S.
.SKIPF   ANOP
*    . IORGSW - ADDRESS OF THE I/O FLIH SWITCH.
*    . IEATCBP - ADDR. OF CURRENTLY DISPATCHED TASK IS AT IEATCBP+4
*    . PISAV-ADDRESS OF THE PROGRAM INTERRUPT FLIH SAVE AREA
*    . IEA0XE00 - AN ENTRY POINT IN THE SVC INTERRUPT HANDLER.
*    . SVF - ADDRESS OF THE SVC TYPE I SWITCH.
*
*
*    ERROR MESSAGES - NONE.
*
*
*    NOTES - PROLOG NEED NOT RESIDE IN THE FIRST 4096 BYTES OF MAIN
*    STORAGE.
*
*
*    OPERATION -
*    IF THE PROGRAM INTERRUPT OCCURED WITH THE I/O FLIH, PROLOG
*    PERFORMS THE FOLLOWING FUNCTIONS -
*    . LOADS REGISTER 0 WITH THE ADDRESS OF THE TASK             A29774
*       TO BE TERMINATED.                                        A29774
*      (THIS IS THE TASK WITH WHICH THE I/O IS ASSOCIATED).
*    . LOADS REGISTER 1 WITH A COMPLETION CODE TO INDICATE THAT THE
*      PROGRAM INTERRUPT OCCURED IN THE I/O FLIH.
*    . LOADS REGISTER 14 WITH THE ADDRESS OF AN ENTRY POINT IN THE I/O
*    . FLITH.
*    . BRANCHES TO ABTERM.
*    IF THE PROGRAM INTERRUPT OCCURED IN A TYPE 1 SVC ROUTINE, PROLOG
*    PERFORMS THE FOLLOWING FUNCTIONS -
*    . LOADS REGISTER 0 WITH THE ADDRESS OF THE TASK WHICH ISSUED THE
*      SVC.
*    . LOADS REGISTER 1 WITH A COMPLETION CODE WHICH INDICATES THAT THE
*      PROGRAM INTERRUPT OCCURED IN A TYPE 1 SVC.
*    . LOADS REGISTER 14 WITH THE ADDRESS OF AN ENTRY POINT IN THE SVC
*      INTERRUPT HANDLER.
*    . BRANCHES TO ABTERM.
*    OTHERWISE PROLOG PERFORMS THE FOLLOWING FUNCTIONS -
*    . LOADS REGISTER 0 WITH THE ADDRESS OF THE TASK WHICH WAS LAST
*      DISPATCHED.
*    . LOADS REGISTER 1 WITH A COMPLETION WHICH INDICATES THE PROGRAM
*      INTERRUPT TYPE.
*    . LOADS REGISTER 14 WITH THE ADDRESS OF AN ENTRY POINT IN THE
*      DISPATCHER.
*    . SAVES THE PROGRAM INTERRUPT OPSW IN THE RESUME PSW SLOT IN THE
*      REQUEST BLOCK.
*    . SAVES REGISTERS 10 THROUGH 1 AT THE TIME OF PROGRAM INTERRUPT
*      IN THE TASK CONTROL BLOCK.
*    . BRANCHES TO ABTERM.
*
*
*REMARKS:          CERTAIN UNIQUE SEPARATOR CARDS ARE USED THROUGH OUT
*              THE ASSEMBLY,AS FOLLOWS:
*              1.  THE FOLLOWING CARD PRECEDES AND SUCCEEDS A
*              CONDITIONAL ASSEMBLY INSTRUCTION.
*CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
*              2.  THE FOLLOWING CARD PRECEDES AND SUCCEEDS A
*              TEMPORARY OR DEBUGGING INSTRUCTION OR SET OF
*              INSTRUCTIONS.
*TTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTT
*              3.  THE FOLLOWING CARD PRECEDES AND SUCCEEDS A
*              SEQUENCE OF 'DC-ORG' OPERATIONS REQUIRED TO SIMULATE
*              INSTRUCTIONS WITH ADDRESSES AS EXTERNAL SYMBOLS WHERE
*              THESE SYMBOLS ARE KNOWN TO BE WITHIN THE FIRST 4095
*              BYTES OF THE MACHINE.
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
*
*
         EXTRN IEA0DS
         EXTRN IEA0AB00
         AIF   (&ONETASK EQ 1).SKIPA
         EXTRN IECCPL00 .                                        A24739
         AGO   .SKIPA1                                           A24739
.SKIPA   ANOP
         EXTRN DISMISS .                                         A24739
.SKIPA1  ANOP                                                    A24739
         EXTRN IORGSW
         EXTRN IEATCBP
         EXTRN PISAV
         EXTRN IEA0XE00
         EXTRN SVF
ADREG    EQU   15
COMREG   EQU   1
         AIF   (&ONETASK NE 1).IO5                               A24739
IOSCODE  EQU   X'0F1'                   IOS PROGRAM CHECK CODE
.IO5     ANOP                                                    A24739
PIOPSW   EQU   40
SVCOPSW  EQU   32 .                                              A24739
PRBREG   EQU   11
RETREG   EQU   14
SVCCODE   EQU   X'0F2'          TYPE 1 SVC PROG. CHECK
TCBGRS   EQU   48
TCBRBP   EQU   0
TCBREG   EQU   12
TCBREG0  EQU   0
WKREG    EQU   13
XRBPSW   EQU   16
*
*
         DS    0D
         BALR  ADREG,0             ESTABLISH ADDRESSABILITY.
         USING *,ADREG
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
*        L     TCBREG,IEATCBP+4        GET TCB ADDRESS FROM OLD.
*
         DC    AL4(IEATCBP+4)
         ORG   *-4
         BALR  TCBREG,0
         ORG   *-2
         DC    X'58'
         ORG   *+3
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
*        TM    IORGSW+1,X'F0'      IS IO SWITCH ON...
*
         DC    AL4(IORGSW+1)
         ORG   *-4
         DC    X'91F0'
         ORG   *+2
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
         AIF   (&ONETASK EQ 1).IO2                               A24739
         BNZ   TOIOS .                  HANDLE IOS PROG CK       A24739
         AGO   .IO3                                              A24739
.IO2     ANOP                                                    A24739
         BC    7,APIO              YES,BRANCH TO APIO.
.IO3     ANOP                                                    A24739
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
*        TM    SVF+1,X'F0'     NO. IS SVC TYPE I SWITCH ON...
*
         DC    AL4(SVF+1)
         ORG   *-4
         DC    X'91F0'
         ORG   *+2
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
         BC    7,APTI              YES, BRANCH TO APTI.
         L     PRBREG,TCBRBP(TCBREG) NO. GET ADDR OF CURRENT RB.
         MVC   XRBPSW(8,PRBREG),PIOPSW MOVE PIOPSW TO RESUME PSW SLOT
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
*        MVC   TCBGRS(32,TCBREG),PDSAV SAVE REGISTERS 10-1 IN TCB.
*
         ORG   *+2
         DC    AL4(PISAV)
         ORG   *-6
         MVI   TCBGRS(TCBREG),31
         ORG   *-4
         DC    X'D2'
         ORG   *+5
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
         LH    COMREG,PIOPSW+2     COMPL. CODE IS INTERRUPT TYPE.
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
*        LA    RETREG,IEA0DS           ABTERM WILL RETURN TO DISPATCH.
*
         DC    AL4(IEA0DS)
         ORG   *-4
         BALR  RETREG,0
         ORG   *-2
         DC    X'41'
         ORG   *+3
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
APOUT    LR    TCBREG0,TCBREG      FINISH ABTERM LINKAGE.
         SLL   COMREG,12               POSITION COMPLETION CODE.
         O     COMREG,APAND            SET DUMP BIT
         L     WKREG,ABTCON
         BCR   15,WKREG            BRANCH TO ABTERM.
*
APTI     EQU   * .                                               A24739
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
*        LA    RETREG,IEA0XE00         ABTERM WILL RETURN TO TYPE I XT.
*
         DC    AL4(IEA0XE00)
         ORG   *-4
         BALR  RETREG,0
         ORG   *-2
         DC    X'41'
         ORG   *+3
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
         AIF   (&ONETASK EQ 1).IO4                               A24739
*
         CLI   SVCOPSW+3,X'00' .        Q. SVC 0                 A24739
         BE    TOIOS .                  YES-BRANCH               A24739
         CLI   SVCOPSW+3,X'0F' .        Q. SVC 15                A24739
         BE    TOIOS .                  YES-BRANCH               A24739
.IO4     ANOP                                                    A24739
         LA    COMREG,SVCCODE .         SET UP COMP CODE         A24739
         B     APOUT .                  TO EXIT                  A24739
         AIF   (&ONETASK EQ 1).IO1                               A24739
TOIOS    EQU   * .                                               A24739
         L     WKREG,ADICCPL0 .                                   M4075
         BR    WKREG .                  GO TO IOS PGM CK HNDLR    M4075
ADICCPL0 DC    A(IECCPL00) .            IOS PGM CK HANDLER       M4075
         AGO   .SKIPG                                            A24739
.IO1     ANOP                                                    A24739
APIO     DS    0H
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
*       LA     RETREG,DISMISS          RETURN ADDRESS TO DISMISS   7928
*
         DC    AL4(DISMISS)                                        7928
         ORG   *-4                                                 7928
         BALR  RETREG,0                                            7928
         ORG   *-2                                                 7928
         DC    X'41'                                               7928
         ORG   *+3                                                 7928
*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
         LA    COMREG,IOSCODE      GET COMPLETION CODE.
         BC    15,APOUT            BRANCH TO APOUT.
.SKIPG   ANOP
ABTCON   DC    A(IEA0AB00)
APAND    DC    X'800C0000'         CONSTANT TO OR DUMP BIT.
         MEND
