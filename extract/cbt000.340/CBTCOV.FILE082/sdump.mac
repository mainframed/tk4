         MACRO
&NAME    SDUMP &HDR=,&HDRAD=,&DCB=,&STORAGE=,&LIST=,&SDATA=,&MF=
         LCLA  &FLAG1,&FLAG2,&CTR1,&CTR2,&DISP
         LCLB  &B0,&B1,&B2,&B3
         LCLC  &GNAME,&C1,&OP
         AIF   ('&HDR' NE '' OR '&HDRAD' NE '').SKCK
         AIF   ('&MF' EQ 'L' OR '&MF' EQ '').MSG9
.SKCK    ANOP
         AIF   ('&HDR' NE '' AND '&HDRAD' NE '').MSG7
         AIF   ('&STORAGE' NE '' AND '&LIST' NE '').MSG8
         AIF   ('&MF' NE 'L').NOTL
         AIF   ('&HDRAD' EQ '').SKPHDCK
         AIF   ('&HDRAD'(1,1) EQ '(').MSG6
.SKPHDCK ANOP
         AIF   ('&LIST' EQ '').SKPLTCK
         AIF   ('&LIST'(1,1) EQ '(').MSG6
.SKPLTCK ANOP
         AIF   ('&DCB' EQ '').SKPDBCK
         AIF   ('&DCB'(1,1) EQ '(').MSG6
.SKPDBCK ANOP
         AIF   ('&STORAGE' EQ '').NOTL
&CTR1    SETA  N'&STORAGE
&CTR2    SETA  1
.LOOPS   AIF   ('&CTR2' GT '&CTR1').LPSEND
         AIF   ('&STORAGE(&CTR2)'(1,1) EQ '(').MSG6
&CTR2    SETA  &CTR2+1
         AGO   .LOOPS
.LPSEND  ANOP
.NOTL    ANOP
         AIF   ('&STORAGE' NE '' OR '&LIST' NE '').SORL
&B0      SETB  1
         AGO   .CKDCB
.SORL    AIF   ('&SDATA' EQ '').CKDCB
&CTR1    SETA  1
&CTR2    SETA  N'&SDATA
.SLLOOP  AIF   ('&CTR1' GT '&CTR2').CKDCB
         AIF   ('&SDATA(&CTR1)' NE 'NONUC').CKSQA
&B2      SETB  1
&CTR1    SETA  &CTR1+1
         AGO   .SLLOOP
.CKSQA   AIF   ('&SDATA(&CTR1)' NE 'NOSQA').SQA
&B1      SETB  1
.SQA     ANOP
&CTR1    SETA  &CTR1+1
         AGO   .SLLOOP
.CKDCB   ANOP
         AIF   ('&DCB' NE '').DCBPRO
&B3      SETB  1
.DCBPRO  ANOP
&FLAG1   SETA  128*&B0+64*&B1+32*&B2
&FLAG2   SETA  128*&B3
         AIF   ('&MF' EQ '').STYPE
         AIF   ('&MF' EQ 'L').LTYPE
         AIF   ('&HDR' NE '').MSG5
         AIF   ('&MF'(1,3) NE '(E,').MSG1
         AIF   ('&NAME' EQ '').NONAM
&NAME    DS    0H
.NONAM   ANOP
         AIF   ('&MF(2)'(1,1) EQ '(').REG
         LA    1,&MF(2)                 LOAD PARM LIST @ INTO REG1
         AGO   .MFEDN
.REG     ANOP
&C1      SETC  '&MF(2)'(2,K'&MF(2)-2)
         AIF   ('&C1' EQ '1').MFEDN
         LR    1,&C1                    LOAD PARM LIST @ INTO REG1
.MFEDN   ANOP
         MVI   2(1),&FLAG1              INITIALIZE FLAG FIELDS IN
         MVI   3(1),&FLAG2              PARAMETER LIST
         AIF   ('&HDRAD' EQ '').NOHDRAD
         AIF   ('&HDRAD'(1,1) EQ '(').HADREG
         LA    0,&HDRAD                 LOAD ADDRESS OF HEADER RECORD
         AGO   .HADDON
.HADREG  ANOP
&C1      SETC  '&HDRAD'(2,K'&HDRAD-2)
&OP      SETC  'HDRAD'
         AIF   ('&C1' EQ '1').MSG2
         LR    0,&C1                    LOAD HEADER RCD ADDRESS INTO 0
.HADDON  ANOP
         ST    0,8(0,1)                STORE H/R ADDRESS IN PARM LIST
.NOHDRAD ANOP
         OI    8(1),X'80'              SET DUMP INDICATOR
         AIF   ('&DCB' EQ '').NODCB
         AIF   ('&DCB'(1,1) EQ '(').DCBRG
         LA    0,&DCB                   LOAD DCB ADDRESS INTO REG 0
         ST    0,4(0,1)                 STORE DCB ADDRESS IN PARM LIST
         AGO   .NODCB
.DCBRG   ANOP
&C1      SETC  '&DCB'(2,K'&DCB-2)
&OP      SETC  'DCB'
         AIF   ('&C1' EQ '0' OR '&C1' EQ '1').MSG2
         ST    &C1,4(1)                 STORE DCB ADDR IN PARM LIST
.NODCB   ANOP
         AIF   ('&STORAGE' EQ '').NOSTOR
         AGO   .DIV0
.RET     ANOP
         L     15,12(1)                 GET STORAGE LIST ADDRESS
&DISP    SETA  8
.LOOP1   AIF   ('&STORAGE(&CTR1)'(1,1) EQ '(').STOREG
         LA    0,&STORAGE(&CTR1)        GET START/END ADDRESSES
         ST    0,&DISP.(0,15)           STORE IN STORAGE LIST
.LOOP2   ANOP
&CTR1    SETA  &CTR1+1
&DISP    SETA  &DISP+4
         AIF   ('&CTR1' GT '&CTR2').HIBIT
         AGO   .LOOP1
.STOREG  ANOP
&C1      SETC   '&STORAGE(&CTR1)'(2,K'&STORAGE(&CTR1)-2)
         AIF   ('&C1' EQ '0' OR '&C1' EQ '1' OR '&C1' EQ '15').MSG2
&OP      SETC  'STORAGE'
         ST    &C1,&DISP.(0,15)         STORE IN LIST
         OI    &DISP.(15),X'00'       TURN OFF INDICATOR BIT
         AGO   .LOOP2
.HIBIT   ANOP
&DISP    SETA  &DISP-4
         OI    &DISP.(15),X'80'       END OF LIST INDICATOR
.NOSTOR  AIF   ('&LIST' EQ '').LSTEND
         AIF   ('&LIST'(1,1) EQ '(').LISTREG
         LA    0,&LIST                  GET ADDRESS OF STORAGE LIST
         ST    0,12(0,1)                PLACE IT IN PARM LIST
         AGO   .LSTEND
.LISTREG ANOP
&C1      SETC  '&LIST'(2,K'&LIST-2)
&OP      SETC  'LIST'
         AIF   ('&C1' EQ '0' OR '&C1' EQ '1' OR '&C1' EQ '15').MSG2
         ST    &C1,12(0,1)              STORE LIST ADDRESS IN PARM LIST
.LSTEND  ANOP
         SVC   51                       SVC DUMP
         MEXIT
.LTYPE   ANOP
         AIF   ('&NAME' EQ '').LTYPE1
&NAME    DS    0F
         AGO   .LSTYPE
.LTYPE1  ANOP
         DS    0F
         AGO   .LSTYPE
.STYPE   ANOP
&GNAME   SETC  'IHB'.'&SYSNDX'
         CNOP  0,4
         AIF   ('&NAME' EQ '').LSTYPEA
&NAME    DS    0H
.LSTYPEA ANOP
         BAL   1,&GNAME
.LSTYPE  ANOP
         DC    H'0'            RESERVED BYTES
         DC    AL1(&FLAG1)              FLAG BYTE 1
         DC    AL1(&FLAG2)              FLAG BYTE 2
         AIF   ('&DCB' EQ '').REGDCB
         AIF   ('&DCB'(1,1) EQ '(').REGDCB
         DC    A(&DCB)                  ADDRESS OF DCB
         AGO   .NRDCB
.REGDCB  ANOP
         DC    A(0)                     ADDRESS OF DCB
.NRDCB   ANOP
         AIF   ('&HDRAD' NE '').HDRADSP
         DC    A(*+8)                   ADDRESS OF HEADER RECORD
         AIF   ('&MF' NE 'L').SKPORG
         ORG   *-4
         DC    X'80'
         ORG   *+3
.SKPORG  ANOP
&CTR1    SETA  &HDR(2)
         AIF   (&CTR1 GT 1004).MSG3
&CTR1    SETA  &CTR1+24
         AGO   .HDRDCDO
.HDRADSP ANOP
&CTR1    SETA  4
         AIF   ('&HDRAD'(1,1) EQ '(').HDREG
         DC    A(&HDRAD)                ADDRESS OF HEADER RECORD
         AGO  .HDRDCDN
.HDREG   ANOP
         DC    A(0)                     ADDRESS OF HEADER RECORD
.HDRDCDN ANOP
         AIF   ('&MF' NE 'L').HDRDCDO
         ORG   *-4
         DC    X'80'
         ORG   *+3
.HDRDCDO ANOP
         AIF   ('&STORAGE' NE ''  OR '&LIST' NE '').SNLSPEC
         DC    A(0)                     ADDRESS OF STORAGE LIST
         AGO   .STRGDN
.SNLSPEC ANOP
         AIF   ('&STORAGE' EQ '').LIST
         DC    A(*+&CTR1)               ADDRESS OF STORAGE LIST
         AGO   .STRGDN
.LIST    AIF   ('&LIST'(1,1) EQ '(').LSTRG
         DC    A(&LIST)                 ADDRESS OF STORAGE LIST
         AGO   .STRGDN
.LSTRG   ANOP
         DC    A(0)                     ADDRESS OF STORAGE LIST
.STRGDN  ANOP
         AIF   ('&HDRAD' NE '').SKPHDRC
         DC    CL8'&HDR(1)'             MODULE NAME
         DC    2F'0'                    RESERVED
         DC    1H'0'                    RESERVED
         DC    AL2(&HDR(2))             LENGTH FIELD
         AIF   ('&HDR(2)' EQ '0').SKPHDRC
         DS    CL&HDR(2)                USER DATA AREA
.SKPHDRC ANOP
         AIF   ('&STORAGE' EQ '').TRYLST
         DC    A(0)                    FIRST 2 WDS OF LIST
         DC    A(0)
         AGO   .DIV1
.LOOP3   AIF   ('&CTR1' GT '&CTR2').ENDLP3
         AIF   ('&STORAGE(&CTR1)'(1,1) EQ '(').STREGS
         DC    A(&STORAGE(&CTR1))       STORAGE LIST
.LOOP4   ANOP
&CTR1    SETA  &CTR1+1
         AGO   .LOOP3
.STREGS  ANOP
         DC    A(0)                     STORAGE LIST
         AGO   .LOOP4
.ENDLP3  ANOP
         AIF   ('&MF' EQ '').TRYLST
         ORG   *-4
         DC    X'80'                  INSERT EOL INDICATOR
         ORG   *+3
.TRYLST  ANOP
         AIF       ('&MF' EQ 'L').EXIT
&GNAME   DS    0H
         AIF   ('&HDRAD' EQ '').NOHDR
         AIF   ('&HDRAD'(1,1) NE '(').NOHDR
&C1      SETC  '&HDRAD'(2,K'&HDRAD-2)
&OP      SETC  'HDRAD'
         AIF   ('&C1' EQ '0' OR '&C1' EQ '1' OR '&C1' EQ '15').MSG2
         ST    &C1,8(0,1)               STORE H/R ADDRESS INTO PARM LST
.NOHDR   ANOP
         OI    8(1),X'80'             TURN ON INDICATOR BIT IN LIST
         AIF   ('&DCB' EQ '').NODCB1
         AIF   ('&DCB'(1,1) NE '(').NODCB1
&C1      SETC  '&DCB'(2,K'&DCB-2)
&OP      SETC  'DCB'
         AIF   ('&C1' EQ '0' OR '&C1' EQ '1' OR '&C1' EQ '15').MSG2
         ST    &C1,4(0,1)               STORE DCB ADDRESS
.NODCB1  ANOP
         AIF   ('&STORAGE' EQ '').NSTGE
         AGO   .DIV2
.DIV2R   ANOP
&DISP    SETA  8
         L     15,12(0,1)               LOAD STORAGE LIST ADDRESS IN 15
.LOOP7   ANOP
         AIF   ('&CTR1' GT '&CTR2').STDNE
         AIF   ('&STORAGE(&CTR1)'(1,1) NE '(').LOOP8
&C1      SETC  '&STORAGE(&CTR1)'(2,K'&STORAGE(&CTR1)-2)
&OP      SETC  'STORAGE'
         AIF   ('&C1' EQ '0' OR '&C1' EQ '1' OR '&C1' EQ '15').MSG2
         ST    &C1,&DISP.(0,15)         STORE ADDRESS PARAM IN LIST
.LOOP8   ANOP
&CTR1    SETA  &CTR1+1
&DISP    SETA  &DISP+4
         AGO   .LOOP7
.STDNE   ANOP
&DISP    SETA  &DISP-4
         OI    &DISP.(15),X'80'         TURN ON INDICATOR BIT
.NSTGE   ANOP
         AIF   ('&LIST' EQ '').NOLSTRG
         AIF   ('&LIST'(1,1) NE '(').NOLSTRG
&C1      SETC  '&LIST'(2,K'&LIST-2)
&OP      SETC  'LIST'
         AIF   ('&C1' EQ '0' OR '&C1' EQ '1' OR '&C1' EQ '15').MSG2
         ST    &C1,12(0,1)
.NOLSTRG ANOP
         SVC   51
.EXIT    ANOP
         MEXIT
.MSG7    ANOP
         MNOTE 12,'INVALID KEYWORD SPEC. HDR AND HDRAD'
         MEXIT
.MSG8    ANOP
         MNOTE 12,'INVALID KEYWORD SPEC. LIST AND STORAGE'
         MEXIT
.MSG6    ANOP
         IHBERMAC 69
         MEXIT
.MSG5    ANOP
         IHBERMAC 48,,HDR
         MEXIT
.DIV0    ANOP
&C1      SETC  '0'
         AGO   .DIVIDE
.DIV1    ANOP
&C1      SETC  '1'
         AGO   .DIVIDE
.DIV2    ANOP
&C1      SETC  '2'
.DIVIDE  ANOP
&CTR2    SETA   N'&STORAGE/2
&CTR1    SETA  (N'&STORAGE-1)/2
         AIF   (&CTR1 EQ &CTR2).MSG4
&CTR2    SETA  N'&STORAGE
&CTR1    SETA  1
         AIF   ('&C1' EQ '0').RET
         AIF   ('&C1' EQ '1').LOOP3
         AGO   .DIV2R
.MSG4    ANOP
         IHBERMAC 204
         MEXIT
.MSG1    ANOP
         IHBERMAC 46,,MF
         MEXIT
.MSG2    ANOP
         IHBERMAC 63,,&OP
         MEXIT
.MSG3    ANOP
         IHBERMAC 44,,HDR
         MEXIT
.MSG9    ANOP
         IHBERMAC 24
         MEND
