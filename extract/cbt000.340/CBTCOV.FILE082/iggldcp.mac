         MACRO
&LABEL   IGGLDCP &OPTCD=N,&RECFM=F
         GBLB  &CPOPS
         LCLC  &C
         AIF   (&CPOPS).NOCPOPS
&CPOPS   SETB  1
         IGGCPOPS
.NOCPOPS ANOP
&C       SETC  '8'
&LABEL   DS    0D
.*
.*   FUNCTION - MACRO CREATES ALL THE SKELETON CHANNEL PROGRAMS
.*        REQUIRED FOR LOAD MODE, ACCORDING TO THE RECORD FORMAT
.*        AND WHETHER WRITE CHECKING HAS BEEN SPECIFIED.
.*
.*   INPUT -
.*        RECFM = F   (DEFAULT)   FIXED LENGTH RECORDS
.*              = V   VARIABLE LENGTH RECORDS
.*        OPTCD = N   (DEFAULT)   NO WRITE CHECKING
.*              = W   WRITE CHECKING
.*
         SPACE 5
***************************
* CP18 - WRITE DATA RECORDS
***************************
*
CL0      CCW   SETSECT,0,SILI+CC,1 *    SETSECTOR
CL1      CCW   SIDEQ,0,CC,5 *           SEARCH ID EQUAL
CL2      CCW   TIC,0,0,0 *              TIC
CL3      CCW   TIC,0,0,0 *              TIC
CL4      CCW   WCKD,0,CC,8 *            WRITE COUNT KEY DATA
*
* N ENTRIES WILL BE GENERATED, ONE FOR EACH BUFFER
*
CL5      CCW   TIC,0,0,0 *              TIC
*
*
CL6      CCW   WCKD,0,DC,8 *            WRITE COUNT,
CL7      CCW   0,0,DC,0 *               KEY AND
CL8      CCW   0,0,CC,0 *               DATA
         AIF   ('&OPTCD' EQ 'N').CP19
&C       SETC  '9'
*
*  CP18- EXTENSION FOR WRITE CHECK
*
CL9      CCW   RCKD,0,CC+SKIP,0 *       READ COUNT KEY AND DATA
*
.CP19    ANOP
CP18LEN  EQU   (CL&C+8-CL0)             LENGTH OF CP18
         SPACE 5
**************************************************************
* CP19 - PREFORMAT SHARED TRACK AND/OR WRITE CYL OVFL CTRL RCD
**************************************************************
*
CM0      CCW   SETSECT,0,CC+SILI,1 *    SET SECTOR
CM1      CCW   SIDEQ,0,CC,5 *           SEARCH ID EQUAL
CM2      CCW   TIC,0,0,0 *              TIC
CM3      CCW   WD,0,SILI+CC,6 *         WRITE DATA
*
CM4      CCW   SEEKHH,0,CC,6 *          SEEK HEAD
CM40     CCW   NOP,0,CC+SILI,1 *        NOP - SETSECTOR SLOT
*
CM5      CCW   SIDEQ,0,CC,5 *           SEARCH ID EQUAL
CM6      CCW   TIC,0,0,0 *              TIC
*
CM7      CCW   WCKD,0,DC,8 *            WRITE COUNT,
CM8      CCW   0,0,CC,0 *               KEY AND DATA
CM9      CCW   WCKD,0,DC,8 *            WRITE COUNT,
CM10     CCW   0,0,CC,0 *               KEY AND DATA
CM11     CCW   WCKD,0,DC,8 *            WRITE COUNT,
CM12     CCW   0,0,CC,0 *               KEY AND DATA
CM13     CCW   WCKD,0,DC,8 *            WRITE COUNT,
CM14     CCW   0,0,CC,0 *               KEY AND DATA
CM15     CCW   WCKD,0,DC,8 *            WRITE COUNT,
CM16     CCW   0,0,CC,0 *               KEY AND DATA
CM17     CCW   WCKD,0,DC,8 *            WRITE COUNT,
CM18     CCW   0,0,CC,0 *               KEY AND DATA
CM19     CCW   WCKD,0,DC,8 *            WRITE COUNT,
CM20     CCW   0,0,CC,0 *               KEY AND DATA
CM21     CCW   WCKD,0,DC,8 *            WRITE COUNT,
CM22     CCW   0,0,CC,0 *               KEY AND DATA
CM23     CCW   WCKD,0,DC,8 *            WRITE COUNT,
CM24     CCW   0,0,CC,0 *               KEY AND DATA
CM25     CCW   WCKD,0,DC,8 *            WRITE COUNT,
CM26     CCW   0,0,00,0 *               KEY AND DATA
*
CM27     DC    X'0000000000000000'
*
CP19LEN  EQU   (CM27+8-CM0)             LENGTH OF CP19
*
         AIF   ('&RECFM' EQ 'V').VAR
         SPACE 5
**********************************
* CP20 - WRITE TRACK INDEX ENTRIES
**********************************
*
CQ0      CCW   SETSECT,0,CC+SILI,1 *    SET SECTOR
CQ1      CCW   SIDEQ,0,CC,5 *           SEARCH ID EQUAL
CQ2      CCW   TIC,0,0,0 *              TIC
CQ3      CCW   WKD,0,DC,0 *             WRITE KEY
CQ4      CCW   0,0,CC,10 *              AND DATA
CQ5      CCW   SIDEQ+MT,0,CC,5 *        SEARCH MULTI TRACK HOME ADDRESS
CQ6      CCW   TIC,0,0,0 *              TIC
CQ7      CCW   WKD,0,DC,0 *             WRITE KEY
CQ8      CCW   0,0,CC,10 *              AND DATA
CQ9      CCW   TIC,0,0,0 *              TIC
CQ10     CCW   SIDEQ+MT,0,CC,5 *        SEARCH ID EQUAL
CQ11     CCW   TIC,0,0,0 *              TIC
CQ12     CCW   WKD,0,CC,0 *             WRITE KEY AND DATA
         AGO   .CP20COM
.VAR     ANOP
         SPACE 5
**********************************
* CP20 - WRITE TRACK INDEX ENTRIES
**********************************
CQ0      CCW   SETSECT,0,SILI+CC,1      SETSECTOR
CQ1      CCW   SIDEQ,0,CC,5             SEARCH ID EQUAL
CQ2      CCW   TIC,0,0,0                TIC
CQ3      CCW   WD,0,CC+SILI,3           WRITE TRK CAPACITY RECORD
CQ4      CCW   TIC,0,SILI,5             TIC CL1,CQ3
CQ5      DC    XL8'00'                 MAX REX LENGTH,CCHHR
CQ6      DC    XL8'00'                 AVAILABLE
CQ7      DC    XL4'00'
         DC    XL4'00000008'
CQ8      DC    XL8'00'                 AVAILABLE
CQ9      DC    XL8'00'                 LST SCD REC RR,CUR CAP
CQ10     DC    XL8'00'                 AVAILABLE
CQ11     DC    XL8'00'                 PTR-CP18,STAT LRECL
CQ12     DC    XL8'00'                 AVAILABLE
***********************************************************************
.CP20COM ANOP
CQ13     CCW   X'1B',0,X'40',6          CTL SEEK HH -  COMMON EXIT
CQ14     CCW   X'08',0,X'20',5          TIC, NOP DURING CLOSE
CQ14A    DC    X'0000000000000000'      MBBCCHHR
*
*
CQ14B    CCW   SETSECT,0,SILI+CC,1      SETSECTOR
CQ15     CCW   SIDEQ,0,CC,5             SEARCH ID EQUAL
CQ16     CCW   TIC,0,0,0                TIC
CQ17     CCW   WCKD,0,DC,8              WRITE COUNT,
CQ18     CCW   0,0,DC,0                 KEY
CQ19     CCW   0,0,CC,10                AND DATA
CQ20     CCW   X'08',0,X'00',0          TIC
CQ21     CCW   WCKD,0,DC,8              WRITE COUNT,
CQ22     CCW   0,0,DC,0                 KEY
CQ23     CCW   0,0,CC,10                AND DATA
CQ24     CCW   TIC,0,0,0                TIC
CQ25     CCW   WCKD,0,CC,0              WRITE COUNT KEY AND DATA
CQ26     CCW   TIC,0,0,0                TIC
CQ27     CCW   SIDEQ+MT,0,CC,5          SEARCH ID EQUAL - MT
CQ28     CCW   TIC,0,0,0                TIC
CQ29     CCW   TIC,0,0,0                TIC
CQ30     DC    X'0000000000000000'      MBBCCHHR
*
CP20LEN  EQU   (CQ30+8-CQ0)             LENGTH OF CP20
*
         AIF   ('&OPTCD' NE 'W').CP21
*
* CP20 - EXTENTION FOR WR CHK
*
CQT0     CCW   NOP,0,SILI+CC,1          NOP/ SETSECTOR
CQT1     CCW   SIDEQ,0,CC,5             SEARCH ID EQUAL
CQT2     CCW   TIC,0,0,0                TIC
CQT3     CCW   RKD,0,CC+SKIP,0 *        READ NORMAL INDEX
CQT4     CCW   SIDEQ+MT,0,CC,5 *        SEARCH FOR OVERFLOW ENTRY
CQT4A    CCW   TIC,0,0,0 *              TIC TO CQT4
CQT5     CCW   RKD,0,CC+SKIP,0 *        READ OVERFLOW INDEX
*   THIS SLOT CHANGED DEPENDING ON WHETHER INACTIVE ENTRY WAS WRITTEN.
CQT5A    CCW   TIC,0,CC+SILI,1 *        TIC TO CQT7 NO INACTIVE
*        CCW   TIC,0,CC+SILI,1          NOP IF INACTIVE ENTRY.
*
CQT5B    CCW   SIDEQ+MT,0,CC,5 *        SEARCH FOR INACTIVE ENTRY
CQT5C    CCW   TIC,0,0,0 *              TIC TO CQT5B
CQT6     CCW   RKD,0,CC+SKIP,0 *        READ INACTIVE ENTRY
CQT7     CCW   SEEKHH,0,CC,6            SEEK HEAD
CQT8     CCW   TIC,0,SILI,5             TIC - NOP DURING CLOSE
*
CP20WRCK EQU   (CQT8+8-CQT0)            LENGTH OF CP20
*                                       *    WRITE EXTENSION
.CP21    ANOP
         SPACE 5
*************************************
* CP21 - WRITE HIGH LEVEL INDEX ENTRY
*************************************
*
CQ39A    CCW   SETSECT,0,SILI+CC,1      SETSECTOR
CQ40     CCW   SIDEQ,0,CC,5             SEARCH ID EQUAL
CQ41     CCW   TIC,0,0,0                TIC
CQ42     CCW   WCKD,0,DC,8              WRITE COUNT,
CQ43     CCW   0,0,DC,0                 KEY
         AIF   ('&OPTCD' EQ 'W').ACQ44
CQ44     CCW   0,0,0,10                 DATA
         AGO   .BCQ44
.ACQ44   ANOP
CQ44     CCW   0,0,CC,10                DATA
.BCQ44   ANOP
*
CP21LEN  EQU   (CQ44+8-CQ39A)           LENGTH OF CP21
*
         AIF   ('&OPTCD' NE 'W').END
*
* CP21 - EXTENTION FOR WR CHK
*
CQ44A    CCW   NOP,0,CC+SILI,1          NOP/SET SECTOR SLOT
CQ45     CCW   SIDEQ,0,CC,5             SEARCH ID EQUAL
CQ46     CCW   TIC,0,0,0                TIC
CQ47     CCW   RCKD,0,SKIP,0            READ COUNT KEY AND DATA
*
CP21WRCK EQU   (CQ47+8-CQ44A)           LENGTH OF CP21 WRITE
*                                       *    CHECK EXTENSION
.END     ANOP
         MEND
