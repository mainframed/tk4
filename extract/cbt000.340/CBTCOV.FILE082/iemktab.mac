         MACRO
&LABEL   IEMKTAB &CODE,&TABID,&AENT,&SA=,&AATO=,&SATO=,&OPT=,          -
               &PTR=,&ETA=,&TRTAB=,&FBTO=,&NTA=,&PSATO=
.*
.*  THIS MACRO GENERATES LINKAGES TO THE TABLE HANDLING ROUTINES IN
.*  PHASE KA. THE LINKAGES ARE GENERATED ACCORDING TO PARAMETERS
.*  SUPPLIED IN THE MACRO-INSTRUCTION TOGETHER WITH INFORMATION
.*  OBTAINED FROM THE DTCA. THIS INFORMATION IS PASSED FROM THE DTCA
.*  MACRO TO THE HTAB MACRO BY MEANS OF GLOBAL VARIABLES.
.*  CHECKS ARE MADE TO ENSURE THAT THE TABLE HANDLING OPERATIONS
.*  REQUESTED ARE CONSISTENT WITH THE DECLARATIONS IN THE DTCA. THE
.*  TABLE SPECIFIED BY &TABID IS MATCHED WITH ITS CORRESPONDING DTCA
.*  BY MEANS OF THE GLOBAL ARRAY &GCAIDS. EACH ELEMENT OF &GCAIDS UP
.*  TO &GCAIDS(&N) CONTAINS THE TABLE IDENTIFIER FROM THE LABEL FIELD
.*  OF A DTCA MACRO-INSTRUCTION PREVIOUSLY ENCOUNTERED. (&N CONTAINS
.*  THE NUMBER OF DTCAS SO FAR ENCOUNTERED). &GCAIDS IS SCANNED TO
.*  FIND AN ELEMENT MATCHING &TABID. WHEN ONE IS FOUND, THE
.*  CORRESPONDING ELEMENTS OF THE OTHER GLOBAL ARRAYS CONTAIN THE
.*  INFORMATION PERTAINING TO THAT TABLE.
.*
         GBLA  &N                  COUNT OF DTCAS
         GBLA  &GAANPTS(15)        NPTRS PARAMETER FROM DTCA
         GBLB  &GBAETF(15)         BIT ON IF ET=F CODED IN DTCA
         GBLB  &GBAETV(15)         BIT ON IF ET=V CODED IN DTCA
         GBLB  &GBAOPSR(15)        BIT ON IF OPS=R CODED IN DTCA
         GBLB  &GBAOPSS(15)        BIT ON IF OPS=S CODED IN DTCA
         GBLC  &GCAIDS(15)         TABLE IDENTIFIERS
.*
         LCLA  &I
         LCLA  &LAACNT,&LAADSP
         LCLA  &LAADSP2
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB001-TSS
*        ----------------------------------------------------TAB001-TSS
         LCLB  &LBATEMP
.*
.*  THE FOLLOWING SECTION PERFORMS AN INITIAL VALIDITY CHECK ON THE
.*  PARAMETERS, SCANS &GCAIDS FOR AN ELEMENT MATCHING &TABID AND
.*  SELECTS THE APPROPRIATE SECTION OF THE MACRO DEPENDING ON THE
.*  OPERATION CODE PARAMETER &CODE.
.*
*
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB002-TSS
         AIF   (T'&LABEL EQ 'O').SSIC01
&LABEL   DS    0H
*        ----------------------------------------------------TAB002-TSS
.SSIC01  ANOP
&LAACNT  SETA  &LAACNT+1           POINT TO NEXT &TABID SUBLIST ITEM
         AIF   (T'&CODE NE 'O').SSIC02
         MNOTE 12,'NO OPERATION CODE PARAMETER'
*
         MEXIT
.SSIC02  AIF   (T'&TABID NE 'O').SSIC03
         MNOTE 12,'NO TABLE IDENTIFIER'
*
         MEXIT
.SSIC03  ANOP
&I       SETA  0                   INITIALISE ARRAY SUBSCRIPT
.SSIC04  AIF   (&I LT &N).SSIC05
         MNOTE 12,'NO DTCA FOR TABLE &TABID(&LAACNT)'
*
         MEXIT
.SSIC05  ANOP
&I       SETA  &I+1                UPDATE SUBSCRIPT
         AIF   ('&TABID(&LAACNT)' NE '&GCAIDS(&I)').SSIC04
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB003-TSS
*        ----------------------------------------------------TAB003-TSS
         AIF   ((NOT &GBAETF(&I)) AND (NOT &GBAETV(&I))).SSTX01
         AIF   ('&CODE' EQ 'ULDR').SSFD01
         AIF   ('&CODE' EQ 'FREE').SSFD02
         AIF   ('&CODE' EQ 'DEACT').SSFD02
         AIF   ('&CODE' EQ 'BLDC').SSBD01
         AIF   ('&CODE' EQ 'BLDT').SSBD03
         AIF   ('&CODE' EQ 'DR').SSDR01
         AIF   ('&CODE' EQ 'SET').SSST01
         AIF   ('&CODE' EQ 'SETZ').SSST01
         AIF   ('&CODE' EQ 'SCAN').SSSC01
         AIF   ('&CODE' EQ 'TEST').SSTS01
         MNOTE 12,'INVALID OPERATION CODE'
*
         MEXIT
.*
.*  THE FOLLOWING SECTION GENERATES CODE FOR TEST
.SSTS01  AIF   (T'&NTA NE 'O').SSTS011
         MNOTE 12,'NO NTA PARAMETER SUPPLIED'
*
         MEXIT
.SSTS011 ANOP
&LBATEMP SETB  (T'&AENT EQ 'O' AND T'&SA EQ 'O' AND T'&AATO EQ 'O')
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB004-TSS
         AIF   (&LBATEMP AND T'&SATO EQ 'O' AND T'&OPT EQ 'O' AND      -
               T'&PTR EQ 'O' AND T'&ETA EQ 'O' AND T'&TRTAB EQ 'O' AND -
               T'&FBTO EQ 'O').SSTS02
         MNOTE 8,'SURPLUS PARAMETER(S) SUPPLIED'
*        ----------------------------------------------------TAB004-TSS
.SSTS02  L     PR1,&TABID(&LAACNT)
         TM    4(PR1),X'80'
         AIF   ('&NTA'(1,1) EQ '(').SSTS03
         BO    &NTA
         AGO   .SSEX01
.SSTS03  BO    0(0,&NTA(1))
         AGO   .SSEX01
.*
.*  THE FOLLOWING SECTION GENERATES CODE FOR ULDR, FREE AND DEACT.
.*
.SSFD01  AIF   (&GBAOPSR(&I)).SSFD02
         MNOTE 12,'DIRECT REFERENCES NOT SPECIFIED IN DTCA OPS PARAM.'
*
         MEXIT
.SSFD02  ANOP
&LBATEMP SETB  (T'&FBTO EQ 'O' AND T'&NTA EQ 'O')
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB005-TSS
         AIF   (T'&AENT EQ 'O' AND T'&SA EQ 'O' AND T'&AATO EQ 'O' AND -
               T'&SATO EQ 'O' AND T'&OPT EQ 'O' AND T'&PTR EQ 'O' AND  -
               T'&ETA EQ 'O' AND T'&TRTAB EQ 'O' AND &LBATEMP).SSFD03
         MNOTE 8,'SURPLUS PARAMETER(S) SUPPLIED'
*        ----------------------------------------------------TAB005-TSS
.SSFD03  L     PR1,&TABID(&LAACNT)
         AIF   ('&CODE' NE 'ULDR').SSFD04
         BAL   RR,KAHUDR
         AGO   .SSFD06
.SSFD04  AIF   ('&CODE' NE 'FREE').SSFD05
         BAL   RR,KAHFRE
         AGO   .SSFD06
.SSFD05  BAL   RR,KAHDAC
.SSFD06  AIF   (T'&TABID(&LAACNT+1) NE 'O').SSIC01
*
         MEXIT
.*
.*  THE FOLLOWING SECTION GENERATES CODE FOR BLDC AND BLDT.
.*
.SSBD01  AIF   (T'&AENT NE 'O').SSBD02
         MNOTE 12,'NO ENTRY ADDRESS SUPPLIED FOR BLDC'
*
         MEXIT
.SSBD02  IEMKLRG PR0,&AENT
         AGO   .SSBD06
.SSBD03  AIF   (&GBAETF(&I)).SSBD04
         MNOTE 12,'BLDT ILLEGAL - ET=V IN DTCA'
*
         MEXIT
.SSBD04  AIF   (T'&AENT EQ 'O').SSBD05
         MNOTE 8,'ENTRY ADDRESS PARAMETER ILLEGAL FOR BLDT - IGNORED'
.SSBD05  SR    PR0,PR0
.SSBD06  L     PR1,&TABID(&LAACNT)
         BAL   RR,KAHBLD
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB006-TSS
         AIF   (T'&SA EQ 'O' AND T'&OPT EQ 'O' AND T'&PTR EQ 'O' AND   -
               T'&ETA EQ 'O' AND T'&TRTAB EQ 'O' AND T'&FBTO EQ 'O'    -
               AND T'&NTA EQ 'O').SSRP01
         MNOTE 8,'SURPLUS PARAMETER(S) SUPPLIED'
*        ----------------------------------------------------TAB006-TSS
.*
.*  THE FOLLOWING SECTION IS USED BY BLDC, BLDT, DR AND SCAN FOR
.*  GENERATING THE RETURNED PARAMETER HANDLING INSTRUCTIONS
.*
.SSRP01  AIF   (T'&AATO EQ 'O').SSRP03
         AIF   ('&AATO'(1,1) EQ '(').SSRP02
         ST    PR0,&AATO
         AGO   .SSRP03
.SSRP02  ANOP
         LR    &AATO(1),PR0
.SSRP03  AIF   (T'&SATO EQ 'O').SSEX01
         AIF   ('&SATO'(1,1) EQ '(').SSRP04
         MVC   &SATO.(3),0(PR1)
         AGO   .SSEX01
.SSRP04  ANOP
         MVC   0(3,&SATO(1)),0(PR1)
.*
.*  THE FOLLOWING SECTION IS USED AS AN EXIT ROUTINE BY BLDC, BLDT,
.*  DR, SET, SETZ AND SCAN. IT CHECKS THAT ONLY ONE TABLE WAS SPECIFIED
.*  IN THE &TABID PARAMETER AND GENERATES THE FINAL BLANK COMMENTS LINE
.*
.SSEX01  AIF   (N'&TABID EQ 1).SSEX02
         MNOTE 8,'ONLY ONE TABLE MAY BE SPECIFIED FOR &CODE'
.SSEX02  ANOP
*
         MEXIT
.*
.*  THE FOLLOWING SECTION GENERATES CODE FOR DR.
.*
.SSDR01  AIF   (&GBAOPSR(&I)).SSDR02
         MNOTE 12,'DIRECT REFERENCES NOT SPECIFIED IN DTCA OPS PARAM.'
*
         MEXIT
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB007-TSS
.SSDR02  AIF   (T'&AENT EQ 'O' AND T'&PTR EQ 'O' AND T'&ETA EQ 'O' AND -
               T'&TRTAB EQ 'O' AND T'&FBTO EQ 'O' AND T'&NTA EQ 'O'    -
               ).SSDR03
         MNOTE 8,'SURPLUS PARAMETER(S) SUPPLIED'
*        ----------------------------------------------------TAB007-TSS
.SSDR03  AIF   (T'&SA NE 'O').SSDR04
         MNOTE 12,'NO SA PARAMETER'
*
         MEXIT
.SSDR04  IEMKLRG PR0,&SA
         L     PR1,&TABID(&LAACNT)
         AIF   (T'&OPT EQ 'O' OR '&OPT' EQ 'N').SSDR06
         AIF   ('&OPT' EQ 'L').SSDR05
         MNOTE 8,'INVALID OPT PARAMETER - OPT=L ASSUMED'
.SSDR05  OI    36(PR1),X'04'
.SSDR06  BAL   RR,KAHMDR
         AGO   .SSRP01
.*
.*  THE FOLLOWING SECTION GENERATES CODE FOR SET AND SETZ
.*
.SSST01  AIF   (&GBAOPSS(&I)).SSST02
         MNOTE 12,'SEQUENTIAL SCANS NOT SPECIFIED IN DTCA OPS PARAM.'
*
         MEXIT
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB008-TSS
.SSST02  AIF   (T'&AENT EQ 'O' AND T'&ETA EQ 'O' AND T'&NTA EQ 'O' AND -
               T'&TRTAB EQ 'O' AND T'&FBTO EQ 'O').SSST03
         MNOTE 8,'SURPLUS PARAMETER(S) SUPPLIED'
*        ----------------------------------------------------TAB008-TSS
.SSST03  AIF   (NOT &GBAOPSR(&I)).SSST04
&LAADSP  SETA  12
.SSST04  AIF   (T'&PTR EQ 'O').SSST07
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB009-TSS
         AIF   ('&PTR' EQ '1' OR '&PTR' EQ '2' OR '&PTR' EQ '3' OR     -
               '&PTR' EQ '4').SSST05
         MNOTE 12,'INVALID PTR PARAMETER'
*
         MEXIT
*        ----------------------------------------------------TAB009-TSS
.SSST05  AIF   (&PTR LE &GAANPTS(&I)).SSST06
         MNOTE 12,'PTR PARAMETER GREATER THAN DTCA NPTRS PARAMETER'
*
         MEXIT
.SSST06  ANOP
&LAADSP  SETA  &LAADSP+12*(&PTR-1)
.SSST07  ANOP
&LAADSP2 SETA  &LAADSP
         AIF   ('&CODE' EQ 'SETZ').SSST10
         AIF   (T'&OPT EQ 'O').SSST08
         MNOTE 8,'OPT PARAMETER ILLEGAL FOR SET - IGNORED'
.SSST08  AIF   (T'&SA NE 'O').SSST09
         MNOTE 12,'SA PARAMETER OMITTED'
*
         MEXIT
.SSST09  IEMKLRG PR0,&SA
         AGO   .SSST13
.SSST10  AIF   (T'&OPT EQ 'O' OR '&OPT' EQ 'F' OR '&OPT' EQ 'B').SSST11
         MNOTE 12,'INVALID OPT PARAMETER'
*
         MEXIT
.SSST11  AIF   (NOT('&OPT' EQ 'B' AND &GBAETV(&I))).SSST111
         MNOTE 12,'OPT=B NOT PERMITTED FOR ET=V'
*
         MEXIT
.SSST111 AIF   (T'&SA EQ 'O').SSST12
         MNOTE 8,'SA PARAMETER ILLEGAL FOR SETZ - IGNORED'
.SSST12  SR    PR0,PR0
.SSST13  L     PR1,&TABID(&LAACNT)
.SSST14  AIF   (NOT('&CODE' EQ 'SETZ' AND '&OPT' EQ 'B')).SSST15
&LAADSP  SETA  36+&LAADSP
         OI    &LAADSP.(PR1),X'02'
.SSST15  BAL   RR,KAHSET
         DC    H'&LAADSP2'
         AGO   .SSEX01
.*
.*  THE FOLLOWING SECTION GENERATES CODE FOR SCAN
.*
.SSSC01  AIF   (&GBAOPSS(&I)).SSSC02
         MNOTE 12,'SEQUENTIAL SCANS NOT SPECIFIED IN DTCA OPS PARAM.'
*
         MEXIT
.SSSC02  AIF   (T'&OPT EQ 'O' OR '&OPT' EQ 'F' OR '&OPT' EQ 'B').SSSC03
         MNOTE 12,'INVALID OPT PARAMETER'
*
         MEXIT
.SSSC03  AIF   (NOT('&OPT' EQ 'B' AND &GBAETV(&I))).SSSC04
         MNOTE 12,'OPT=B NOT PERMITTED FOR ET=V'
*
         MEXIT
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB010-TSS
.SSSC04  AIF   (T'&PTR EQ 'O' OR '&PTR' EQ '1' OR '&PTR' EQ '2' OR     -
               '&PTR' EQ '3' OR '&PTR' EQ '4').SSSC05
         MNOTE 12,'INVALID PTR PARAMETER'
*
         MEXIT
*        ----------------------------------------------------TAB010-TSS
.SSSC05  AIF   (T'&PTR EQ 'O').SSSC06
         AIF   (&PTR LE &GAANPTS(&I)).SSSC06
         MNOTE 12,'PTR PARAMETER GREATER THAN DTCA NPTRS PARAMETER'
*
         MEXIT
.SSSC06  AIF   (T'&TRTAB EQ 'O').SSSC07
         MNOTE 8,'SELECTIVE SCAN ONLY AVAILABLE FOR TEXT TABLES. TRTAB -
               OPERAND IGNORED'
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB011-TSS
.SSSC07  AIF   (T'&AENT EQ 'O' AND T'&SA EQ 'O' AND                    -
               T'&FBTO EQ 'O' AND T'&NTA EQ 'O').SSSC08
         MNOTE 8,'SURPLUS PARAMETER(S) SUPPLIED'
*        ----------------------------------------------------TAB011-TSS
.SSSC08  AIF   (NOT &GBAOPSR(&I)).SSSC09
&LAADSP  SETA  12
.SSSC09  AIF   (T'&PTR EQ 'O').SSSC10
&LAADSP  SETA  &LAADSP+12*(&PTR-1)
.SSSC10  ANOP
&LAADSP2 SETA  &LAADSP
         L     PR1,&TABID(&LAACNT)
.SSSC11  AIF   ('&OPT' NE 'B').SSSC12
&LAADSP  SETA  36+&LAADSP
         OI    &LAADSP.(PR1),X'02'
.SSSC12  BAL   RR,KAHSCN
         DC    H'&LAADSP2'
         AIF   (T'&ETA EQ 'O').SSSC14
         AIF   ('&ETA'(1,1) EQ 'C').SSSC13
         B     &ETA
         AGO   .SSRP01
.SSSC13  ANOP
         B     0(0,&ETA(1))
         AGO   .SSRP01
.SSSC14  B     KAHERR
         AGO   .SSRP01
.*
.*  THE FOLLOWING SECTION GENERATES LINKAGES TO THE TEXT TABLE
.*  HANDLING ROUTINE
.*
.SSTX01  AIF   ('&CODE' EQ 'DR').SSTX03
         AIF   ('&CODE' EQ 'SET').SSTX10
         AIF   ('&CODE' EQ 'SETZ').SSTX13
         AIF   ('&CODE' EQ 'SCAN').SSTX19
         AIF   ('&CODE' EQ 'DEACT').SSTX34
         AIF   ('&CODE' EQ 'BLDC').SSTX02
         AIF   ('&CODE' EQ 'BLDT').SSTX02
         AIF   ('&CODE' EQ 'ULDR').SSTX02
         AIF   ('&CODE' EQ 'FREE').SSTX02
         AIF   ('&CODE' EQ 'TEST').SSTX02
         MNOTE 12,'INVALID OPERATION CODE'
*
         MEXIT
.SSTX02  MNOTE 12,'&CODE OPERATION NOT PERMITTED FOR ET=T'
*
         MEXIT
.*
.*  GENERATE CODE FOR DR
.*
.SSTX03  AIF   (T'&SA NE 'O').SSTX04
         MNOTE 12,'NO SA PARAMETER SUPPLIED'
*
         MEXIT
.SSTX04  AIF   (T'&AATO NE 'O').SSTX05
         MNOTE 8,'NO AATO PARAMETER SUPPLIED'
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB012-TSS
.SSTX05  AIF   (T'&AENT EQ 'O' AND T'&SATO EQ 'O' AND T'&OPT EQ 'O'    -
               AND T'&PTR EQ 'O' AND T'&ETA EQ 'O' AND T'&TRTAB EQ 'O' -
               AND T'&FBTO EQ 'O').SSTX06
         MNOTE 8,'SURPLUS PARAMETER(S) SUPPLIED'
*        ----------------------------------------------------TAB012-TSS
.SSTX06  AIF   ('&SA'(1,1) EQ '(').SSTX07
         MVC   PAR1+1(3),&SA
         AGO   .SSTX08
.SSTX07  MVC   PAR1+1(3),0&SA
.SSTX08  MVI   PAR1,X'80'
         L     LR,ZTXTAB
         BALR  RR,LR
         AIF   (T'&AATO EQ 'O').SSEX01
         AIF   ('&AATO'(1,1) EQ '(').SSTX09
         MVC   &AATO.(4),PAR1
         AGO   .SSEX01
.SSTX09  L     &AATO(1),PAR1
         AGO   .SSEX01
.*
.*  GENERATE CODE FOR SET/SETZ
.*
.SSTX10  AIF   (T'&SA NE 'O').SSTX11
         MNOTE 12,'NO SA PARAMETER SUPPLIED'
*
         MEXIT
.SSTX11  ANOP
         IEMKLRG PR0,&SA
         AGO   .SSTX16
.SSTX13  AIF   (T'&SA EQ 'O').SSTX14
         MNOTE 8,'SA PARAMETER ILLEGAL FOR SETZ - IGNORED'
.SSTX14  AIF   (T'&OPT EQ 'O').SSTX15
         MNOTE 8,'OPT PARAMETER ILLEGAL FOR ET=T - IGNORED'
.SSTX15  SR    PR0,PR0
.SSTX16  AIF   (T'&PTR EQ 'O').SSTX17
         MNOTE 8,'PTR PARAMETER ILLEGAL FOR ET=T - IGNORED'
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB013-TSS
.SSTX17  AIF   (T'&AENT EQ 'O' AND T'&AATO EQ 'O' AND T'&SATO EQ 'O'   -
               AND T'&ETA EQ 'O' AND T'&TRTAB EQ 'O' AND T'&FBTO EQ    -
               'O' AND(NOT('&CODE' EQ 'SET' AND T'&OPT NE 'O'))).SSTX18
         MNOTE 8,'SURPLUS PARAMETER(S) SUPPLIED'
*        ----------------------------------------------------TAB013-TSS
.SSTX18  ANOP
         BAL   RR,KAHTST
         AGO   .SSEX01
.*
.SSTX20  AIF   (T'&PTR EQ 'O').SSTX21
.*  GENERATE CODE FOR SCAN
.*
.SSTX19  AIF   (T'&OPT EQ 'O').SSTX20
         MNOTE 8,'OPT PARAMETER ILLEGAL FOR ET=T - IGNORED'
         MNOTE 8,'PTR PARAMETER ILLEGAL FOR ET=T - IGNORED'
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB014-TSS
.SSTX21  AIF   ((T'&TRTAB EQ 'O' AND T'&FBTO EQ 'O') OR (T'&TRTAB NE   -
               'O' AND T'&FBTO NE 'O')).SSTX22
         MNOTE 12,'TRTAB AND FBTO MUST BOTH BE INCLUDED.'
*
         MEXIT
*        ----------------------------------------------------TAB014-TSS
.SSTX22  AIF   (T'&AENT EQ 'O' AND T'&SA EQ 'O').SSTX23
         MNOTE 8,'SURPLUS PARAMETER(S) SUPPLIED'
.SSTX23  ANOP
         AIF   (T'&TRTAB EQ 'O').SSTX24
         IEMKLRG PR0,&TRTAB
         AGO   .SSTX25
.SSTX24  SR    GR0,GR0
.SSTX25  BAL   RR,KAHTSC
         AIF   (T'&ETA EQ 'O').SSTX27
         AIF   ('&ETA'(1,1) EQ '(').SSTX26
         B     &ETA
         AGO   .SSTX28
.SSTX26  B     0(0,&ETA(1))
         AGO   .SSTX28
.SSTX27  B     KAHERR
.SSTX28  AIF   (T'&AATO EQ 'O').SSTX30
         AIF   ('&AATO'(1,1) EQ '(').SSTX29
         MVC   &AATO.(4),12(PR1)
         AGO   .SSTX30
.SSTX29  L     &AATO(1),12(PR1)
.SSTX30  AIF   (T'&SATO EQ 'O').SSTX32
         AIF   ('&SATO'(1,1) EQ '(').SSTX31
         MVC   &SATO.(3),17(PR1)
         AGO   .SSTX32
.SSTX31  MVC   0(3,&SATO(1)),17(PR1)
.SSTX32  AIF   (T'&FBTO EQ 'O').SSEX01
         AIF   ('&FBTO'(1,1) EQ '(').SSTX33
         STC   PR0,&FBTO
         AGO   .SSTX332
.SSTX33  LR    &FBTO(1),PR0
.SSTX332 AIF   (T'&PSATO EQ 'O').SSEX01
         AIF   ('&PSATO'(1,1) EQ '(').SSTX334
         MVC   &PSATO.(3),21(PR1)
         AGO   .SSEX01
.SSTX334 MVC   0(3,&PSATO(1)),21(PR1)
         AGO   .SSEX01
.*
.*  GENERATE CODE FOR DEACT
.*
.SSTX34  ANOP
&LBATEMP SETB  (T'&AENT EQ 'O' AND T'&SA EQ 'O' AND T'&AATO EQ 'O')
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TAB015-TSS
         AIF   (&LBATEMP AND T'&SATO EQ 'O' AND T'&OPT EQ 'O' AND      -
               T'&PTR EQ 'O' AND T'&ETA EQ 'O' AND T'&TRTAB EQ 'O' AND -
               T'&FBTO EQ 'O').SSTX35
         MNOTE 8,'SURPLUS PARAMETER(S) SUPPLIED'
*        ----------------------------------------------------TAB015-TSS
.SSTX35  ANOP
       BAL   RR,KAHTDA
         AIF   (T'&TABID(&LAACNT+1) NE 'O').SSIC01
*
         MEND
