         MACRO
&NAME    RESPL &DSECT=YES,&EQUATES=NO
         LCLC  &A0010,&A0020,&A0030,&A0040,&A0050
         LCLC  &A0060,&A0070,&A0080,&A0090,&A0095                S22024
*
*        TOTE RESIDENT CONTROL BLOCK
*
*              3/31/73                                           S22024
*
         AIF   ('&DSECT' EQ 'NO').BYP010
&A0010   SETC  '0'
&A0020   SETC  '0'
&A0030   SETC  '0'
&A0040   SETC  '0'
&A0050   SETC  '0'
&A0060   SETC  '0'
&A0070   SETC  '0'
&A0080   SETC  '0'
&A0090   SETC  '0'
&A0095   SETC  '0'                                               S22024
         AGO   .BYP020
.BYP010  ANOP
&A0010   SETC  'ERROR'
&A0020   SETC  'SMGR'
&A0030   SETC  'IEDQWA'
&A0040   SETC  'OLTETXR'
&A0050   SETC  'PRFSTXT-PRFSUNIT'
&A0060   SETC  'QREMFRX'
&A0070   SETC  'QREMELX'
&A0080   SETC  'QADDNDX'
&A0090   SETC  'QADDAFX'
&A0095   SETC  'STAERT'                                          S22024
.BYP020  ANOP
         AIF   ('&DSECT' EQ 'NO').BYP030
&NAME    DSECT
         AGO   .BYP040
.BYP030  ANOP
&NAME    DS    0F
.BYP040  ANOP
RESTECBS DC    A(0)               PARAMETER LIST FOR TOTE WAIT
         DC    XL1'80'            SET ECB CODE TO WAITING
         DC    AL3(RESTECB1)      ADDRESS OF USBTASK ECB
RESTECB1 DC    F'0'               SUBTASK REQUEST ECB
*
RESOBQCB DS    0F                 OLTCB QUEUE CONTROL BLOCK
RESOBFWD DC    A(RESOBQCB)        OLTCB QUEUE FORWARD POINTER
RESOBBKW DC    A(RESOBQCB)        OLTCB QUEUE BACKWARD POINTER
*
RESTTLST DC    A(0)               LAST TOTE TNT ENTRY ADDRESS
RESBKTOT DC    H'12'              TOTAL STORAGE BLOCKS FOR TOTE  S22024
RESBKAVL DC    H'8'               CURRENT FREE STORAGE BLOCKS
RESEFQCB DC    A(RESEFQCB)        EXTENDED AREA FREE QUEUE
*                                   CONTROL BLOCK
RESTNTCT DC    X'00'              TOTAL NUMBER OF TNT ENTRIES
RESDMTTE DC    AL3(0)             POINTER TO TOTE EXTENDED AREA
*                                   FOR TTE'S AND QCB'S
RESETXRA DC    A(&A0040)          END OF TASK EXIT ROUTINE ADDRESS
RESSTAEA DC    A(&A0095)          STAE EXIT ROUTINE ADDRESS      S22024
RESSMGRA DC    A(&A0020)          SERVICE MANAGER ENTRY ADDRESS
RES#TBLE EQU   RESSMGRA-28        BRANCH TABLE ADDRESS
RESWBFNC DC    X'00'              IEDQWB FUNCTION REQUEST CODE
*
RESFLAGS DC    XL1'0'             TOTE RESIDENT FLAG BYTE
*
RESINIT  EQU   X'80'                   TOTE INITIALIZED FLAG
RESFIRST EQU   X'40'                   FIRST-TIME INDICATOR
*
         DC    X'00'              UNUSED
RESTRMQ  DC    A(RESTRMQ)         TOTE TRM QUEUE CONTROL BLOCK
         DS    XL1'0'             UNUSED
RESTNTPT DS    AL3(0)             ADDRESS OF START OF TOTE TNT
*                                   ENTRIES
*
*           BRANCH TABLE FOR QUEUE HANDLERS
*
RESQHBL  DS    0F                 ALIGN ON FULLWORD
RESREMFR DC    A(&A0060)          ADDRESS OF REMOVE ELEMENT FROM
*                                   FRONT HANDLER
RESREMEL DC    A(&A0070)          ADDRESS OF REMOVE SPECIFIED
*                                   ELEMENT HANDLER
RESADDND DC    A(&A0080)          ADDRESS OF ADD ELEMENT TO END
*                                   HANDLER
RESADDAF DC    A(&A0090)          ADDRESS OF ADD ELEMENT AFTER
*                                   ANOTHER SPECIFIED ELEMENT HANDLER
RESAPDCB DCB   DSORG=PS,MACRF=WC,DDNAME=DIAGMSG,BLKSIZE=80      SA21903
RESAPOPN EQU   RESAPDCB+48                                      SA21903
         AIF   ('&EQUATES' EQ 'NO').MEND
*
*        LEVEL 1 EQUATES
*
QFLAGS2  EQU   RESWBFNC
TOTEECB1 EQU   RESTECB1
OLTACTQ  EQU   RESOBQCB
BLKTOTAL EQU   RESBKTOT
BLKAVAIL EQU   RESBKAVL
ETXRADR  EQU   RESETXRA
BRTABLE  EQU   RES#TBLE
TOTEECBS EQU   RESTECBS
.MEND    ANOP
*
         MEND
