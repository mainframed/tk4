         MACRO
     MIEDQHM    &X
         LCLA  &A
.*CHANGE ACTIVITY= AS FOLLOWS:
.*A526200                                                        S22029
.*C159600                                                        S22029
.*A103200,243600,286800,307200,316200,423000,423600,820200       S21101
.*A904800                                                        S21101
.*C123000,131400,300000,301800,338400,400800-402000              S21101
.*C211800-212400                                                 S21101
.*D235200-238200,263400-270600,309600                            S21101
.*C121800,447000                                                 A42363
.*C123000,865200                                                 A42367
.*A868800                                                        A42367
.*C123000                                                        A42400
.*A525000                                                        A42400
.*D527400                                                        A42400
.*A199200,202800                                                 S21101
.*A707000                                                        A47137
.*C160800-162000,528600-529800,535800                            S22026
.*A109900-110100,162300,544860-545340,906300                     S22026
.*A106800,131630,131952                                          A51765
.*A489100-489300                                                SA52986
.*C487200                                                       SA52986
.*D487400-487600                                                SA52986
.*A103500,393700-394100,397280-397680                           SA57334
.*D395400-396600                                                SA57334
.*A103400,110130,128460,129600,131670,261600,323400,377400,434400S22025
.*A910800,918000                                                 S22025
.*C009000,012000,111000,121200,213300,253200,262800,328800,378600S22025
.*C385800,416400,489600,559800,772800,902400,906400,907800,909000S22025
.*C913200,915000,916800,922200,924000                            S22025
.*A480600,579600,846600,847800,860400,900000,915300             SA52971
.*C639000-639400,640200-642600,849000-849930,866400-892200      SA52971
.*C901200-902280                                                SA52971
.*D850800                                                       SA52971
.*A275470,484800-486800                                         SA51078
.*A275540-275890                                                SA50192
.*A381030-381570                                                SA51783
.*D484800-489300,909000                                         SA51078
.*D886800                                                       SA58988
.*A886800-886860                                                SA58988
.*A018600                                                        S21903
.*A546600                                                       SA59006
.*A432200                                                       SA60012
.*C002399,019800                                                SA60012
.*C117000-117500,532800,543600-544200,545340                    SA61768
.*A532800                                                       SA61768
.*D536400,541800-542400                                         SA61768
.*A654000,654600                                                SA61085
.*A110340,894600,897600                                         OX02183
.*C307720,308400                                                SA66611
*A547000                                                        OX05307
.*A894600,897900,103400                                        @YA06890
.*C668400-677400,681600,718800-719400,720600                   @SA72466
.*D680400,709200,687000                                        @SA72466
.*A682800,713400,723600                                        @SA72466
*A849030                                                       @SA74018
*C547200,553200                                                @SA74250
* A686400                                                      @SA74250
*A377700                                                       @XA06353
*A275680                                                       @SA71682
*C349800,356400,360600                                         @XA11314
*A275680                                                       @SA76275
*A588600                                                       @ZA06194
*A849046                                                       @SA76309
*A588600                                                       @OY12977
*D589200                                                       @OY12977
*A674400                                                       @OY14492
*D686700-687300                                                @OY14492
*A103450,849720                                                @OX16398
*C849870                                                       @OX16398
         SPACE 3
         AIF   ('&X' NE 'CORE').CKD
&A       SETA  1
IEDQHM1  TITLE '''IEDQHM'' - DESTINATION SCHEDULER (CORE ONLY)'  S22025
IEDQHM1  CSECT
         AGO   .GO
.CKD     AIF   ('&X' NE 'DISK').BOTH
&A       SETA  2
IEDQHM2  TITLE '''IEDQHM'' - DESTINATION SCHEDULER (DISK ONLY)'  S22025
IEDQHM2  CSECT
         AGO   .GO
.BOTH    ANOP
&A       SETA  3
IEDQHM3  TITLE '''IEDQHM'' - DESTINATION SCHEDULER (DISK AND CORE)'
IEDQHM   CSECT
.GO      ANOP
         ENTRY IEDQHM02
*
***********************************************************************
*                                                                     *
         AIF   ('&X' NE 'CORE').TAG1                             S21903
*  MODULE NAME = IEDQHM1 ( CORE )                                     *
         AGO   .TAG3                                             S21903
.TAG1    AIF   ('&X' NE 'DISK').TAG2                             S21903
*  MODULE NAME = IEDQHM2 ( DISK )                                     *
         AGO   .TAG3                                             S21903
.TAG2    ANOP                                                    S21903
*  MODULE NAME = IEDQHM ( MIXED - CORE / DISK )                       *
.TAG3    ANOP                                                    S21903
*                                                                     *
*  DESCRIPTIVE NAME = DESTINATION SCHEDULER                           *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
*FUNCTION -- THIS SUBTASK ASSIGNS A BUFFER TO A LOCATION IN A         *
*   MESSAGE QUEUE3 DATA SET (REUSEABLE DISK, NONREUSEABLE DISK, OR    *
*   CORE AS APPLICABLE).  THIS BUFFER WILL BE CHAINED TO OTHER        *
*   BUFFERS OF THIS MESSAGE AND THIS MESSAGE WILL BE CHAINED TO       *
*   OTHER MESSAGES IN THE SAME QUEUE.                                 *
*    THE HEADER OF THE MESSAGE MUST CONTAIN THE ADDRESS OF THE        *
*    NEXT TEXT SEGMENT AND THE NEXT MESSAGE HEADER.  ALL BUFFERS      *
*    MUST HAVE THE ADDRESS OF THE NEXT TEXT (IF IT IS NOT THE         *
*    LAST BUFFER), THE ADDRESS OF THE ADDITIONAL RECORES (IF ANY),    *
*    AND  IF IT IS NOT A HEADER, IT WILL HAVE THE ADDRESS OF ITS      *
*    HEADER.                                                          *
*                        DISK QUEUEING                                *
*    IF THE MESSAGE IS DISK QUEUED, THE HEADER AND LAST TEXT          *
*    BUFFERS ILL CONTAIN THE QBACK CHAIN POINTERS.  QBACK CHAIN IS    *
*    A TIME SEQUENTIAL RECORD OF THE EVENTS (BOTH SENDING AND         *
*    RECEIVING) FOR THIS QCB.  IF THE QCB REPRESENTS THE DESTINATION  *
*    OF THE MESSAGE, THE HEADER WILL APPEAR IN THE QBACKCHAIN FROM    *
*    THAT QCB.  IF THE QCB REPRESENTS THE SOURCE OF THE MESSAGE       *
*    (LINE OR TERMINAL) THE LAST BUFFER OF THE MESSAGE WILL APPEAR    *
*    IN THE QBACK CHAIN OF THE QCB.                                   *
*    DISK MESSAGE QUEUEING (REUSABLE OR NONREUSABLE) WILL BE          *
*    ACCOMPLISHED BY ASSIGNING RECORD NUMBERS AHEAD ON DISK.          *
*    THERE EXISTS A VALUE CALLED 'ADDRESS' FOR BOTH REUSABLE (RADDR)  *
*    AND NONREUSABLE (NADDR) DISK.  THERE IS A CORRESPONDENCE         *
*    BETWEEN THE VALUE OF ADDRESS AND THE PHYSICAL LOCATION           *
*    (MBBCCHHR) OF THE RECORD ON DISK.  WHEN ADDRESS MODULO           *
*    (THE TOTAL NUMBER OF RECORDS IN THE DATA SET) IS USED, THIS      *
*    IS A ONE TO ONE CORRESPONDENCE.  WHEN A 'HEADER NOT LAST'        *
*    BUFFER IS RECEIVED, A VALUE OF ADDRESS IS RESERVED FOR THE NEXT  *
*    HEADER FOR THE QCB AND THE NEXT TEXT BUFFER OF THIS MESSAGE.     *
*    SEQUENTIAL VALUES OF ADDR ARE RESERVED FOR ANY ADDITIONAL        *
*    RECORDS REQUIRED.  WHEN A 'TEXT NOT LAST' IS RECEIVED LOCA-      *
*    TIONS FOR NEXT TEXT AND ADDITIONAL RECORDS ARE RESERVED.         *
*    WHEN A 'LAST' BUFFER IS RECEIVED, NO NEXT TEXT IS RESERVED.      *
*                        CORE QUEUEING                                *
*    THE CORE QUEUES DATA SET IS NOT DIVIDED INTO NUMBERED            *
*    RECORDS.  ONE RECORD CORRESPONDS IN SIZE TO ONE BUFFER UNIT      *
*    UNITS ARE NOT ASSIGNED AHEAD AS IN DISK QUEUES, HOWEVER THE      *
*    MESSAGES  IN ONE QUEUE ARE CHAINED TOGETHER AND THE BUFFERS      *
*    OF A MESSAGE ARE CHAINED TOGETHER,  THERE IS A VALUE CADDR       *
*    SIMILIAR TO ADDRESS ON DISK WHICH CORRESPONDS TO THE NUMBER OF   *
*    UNITS WHICH ARE USED OUT OF THE NUMBER RESERVED FOR CORE         *
*    QUEUES.  CHAINING IS NOT DONE BY RECORD NUMBER FOR CORE QUEUES   *
*    BUT BY THE ACTUAL ADDRESS.   THE ADDITIONAL RECORDS AND          *
*    LOCATED THROUGH THE TIC FIELDS OF THE BUFFER UNITS WHILE IN      *
*    THE CORE QUEUE.  WHEN A BUFFER IS RECIEVED, IF IT IS A HEADER    *
*    IT IS CHANED TO THE PREVIOUS HEADER IN THE QUEUE.  IF IT IS A    *
*    TEXT, IT IS CHAINED TO THE PREVIOUS BUFFER OF THIS NESSAGE.  A
*    NUMBER OF UNITS CORRESPONDING TO THE NUMBER OF UNITS IN THE
*    BUFFER WILL BE REMOVED FROM THE FREE CORE QUEUE UNITS (LINE      *
*    BUFFER POOL).  IF THIS QCB'S MESSAGES ARE TO BE CORE QUEUED ONLY *
*    THESE UNITS ARE POSTED TO THE AVAILABLE BUFFER POOL IN           *
*    RETURN FOR THE BUFFER.  THE BUFFER IS THEN PLACED THE THE        *
*    QCB'S CORE QUEUE OF MESSAGES.   IF THE MESSAGE IS THE BE DISK    *
*    QUEUED ALSO, THE MESSAGE WILL BE COPIED INTO THESE UNITS AND     *
*    BUFFER FORMED WILL BE PLACED IN THE QCB'S MESSAGE QUEUE.         *
*    THE ORIGINAL BUFFER WILL THEN BE DISK QUDUED.                    *
*                                                                     *
*ENTRY POINTS -- THIS ROUTINE HAS TWO ENTRY POINTS                    *
*    1. IEDQHM / FROM THE DISPATCHER WITH A FULL BUFFER TO BE         *
*       QUEUED.                                                       *
*       CALLING SEQUENCE -                                            *
*                  L      R15,ENTRY POINT ADDRESS                     *
*                  L    R1,BUFFER ADDRESS                             *
*                  BR    R15                                          *
*    2. IEDQHM02 / FROM THE REUS-RECOPY SUBTASK WITH ONE UNIT OF      *
*       A BUFFER TO BE QUEUED.                                        *
*       CALLING SEQUENCE                                              *
*                  L    R15,ENTRY POINT ADDRESS                       *
*                  L    R1,UNIT ADDRESS                               *
*                  BALR  R14,R15                                      *
*                                                                     *
*    3. IEDQHM03 -- CALLED BE THE REUS COPY SUBTASK WITH A            *
*       PRIORITY QCB                                                  *
*       CALLING SEQUENCE --             L     R15,ADDR HM03           *
*                                       BALR  R14,R15                 *
*       RETURN CODES -- IF RETURN IS BR R14 - THE MESSAGE ON THE      *
*          PRIORITY QCB IS NOT BEING SENT                             *
*          IF RETURN IS B  4(R14) - THE MESSAGE IS BEING SENT         *
*                                                                     *
*    4. FINDSTCB -- CALLED FROM REUS - COPY WITH A DESTINATION        *
*       QCB.  UPON RETURN THE SEND SCHEDULER WILL HAVE BEEN           *
*       NOTIFIED THAT THERE IS A MESSAGE TO SEND.                     *
*       CALLING SEQUENCE -              L     R12,HMBASE              *
*                                       L     RX,OFFSET OF SUBROUTINE *
*                                       BAL   R14,R12+RX              *
*                                                                     *
*INPUT -- R15 WILL CONTAIN THE ENTRY POINT ADDRESS                    *
*    R1 - THE BUFFER ADDRESS                                          *
*    R13  THE AVT ADDRESS AND SAVE AREA ADDRESS                       *
*                                                                     *
*OUTPUT -- THE BUFFER WILL CONTAIN THE QUEUEING POINTERS MENTIONED    *
*    ABOVE.                                                           *
*    IF THE BUFFER WAS DISK QUEUED THE BUFFER WILL BE POSTED TO THE   *
*    DISK O /I QCB TO BE WRITTEN ON DISK.                             *
*    IF THE BUFFER WAS CORE QUEUED ONLY, A CORRESPONDING NUMBER OF    *
*    UNITS WILL BE RETURNED TO THE AVAILABLE BUFFER POOL.             *
*    IF THE ENTRY WAS FROM COPY SUBTASK WITH A CORE ONLY BUFFER,      *
*    THE FERE CORE UNIT WILL BE RETURNED TO COPY.                     *
*                                                                     *
*EXTERNAL ROUTINES -- DSPPOSTR - TO INSERT AN ELEMENT BY PRIORITY     *
*    ON THE READY QUEUE.                                              *
*    DSPUNAVR - TO REMOVE A STCB FROM THIS QCBS STCB CHAIN AND        *
*    INSERT IT BY PRIORITY INTO THE STCB CHAIN OF ANORHER QCV.        *
*    AVTRNMPT - TO FIND THE ADDRESS OF THE TERM TABLE ENTRY FROM      *
*    THE OFFEST INTO THE TERM NAME TABLE                              *
*                                                                     *
*EXITS-NORMAL -- TO THE DISPATCHER AT DSPPOST OR DSPDISP OR IF        *
*    CALLED BY COPY - TO THE CALLING ROUTINE WITH R15 =0              *
*                                                                     *
*EXITS -ERROR -- NONE                                                 *
*                                                                     *
*TABLES/WORK AREAS -- DSECTS- LCB,DCB,SCB,PREFIX,AVT 4,QCB            *
*   AVT FIELDS - RADDR,NADDR,CADDR                                    *
*                                                                     *
*ATTRIBUTES -- REUSEABLE,REFRESHABLE,ENABLED,RESIDENT                 *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*    PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER     *
*    SET.                                                             *
*                                                                     *
***********************************************************************
         EJECT
R0       EQU   0                        WORK REGISTER
R1       EQU   1                        ELEMENT ADDRESS FORM DISP
*                                       PARAMETER TO TERM NAME TBL
*                                       ELEMENT ADDR TO DISP
R2       EQU   2                        ADDRESS OF THE VALUE
*                                       OF ADDRESS BEING USED
*                                       (RADDR,NADDR)
*                                       ADDRESS OF QCB TO POST T TO
RSCB     EQU   3                        ADDR OF SCB
RLCB     EQU   4                        ADDR OF LCB
R5       EQU   5                        WORK REGISTER
*                                       ADDR OF BUFFER FORMED
*                                       FROM CORE UNITS
RPREFIX  EQU   6                        ADDRESS OF BUFFER
RPRF     EQU   6                        ADDRESS OF BUFFER
R6       EQU   6
RQCB     EQU   7                        ADDRESS OF MASTER QCB
R7       EQU   7                        WORK REGISETER
RPQ      EQU   8                        ADDR OF PRIORITY QCB
R9       EQU   9                        WORK REGISTER
R10      EQU   10                       WORK REGISTER
R11      EQU   11                       WORK REGISTER
*                                       DISP BASE REG
R12      EQU   12                       PROGRAM BASE
R13      EQU   13                       AVT ADDRESS
R14      EQU   14                       NUMBER OF CORE UNITS TO GET
*                                       WORK REG. AND RETURN REG
R15      EQU   15                       WORK REGISTER
***********************************************************************
         SPACE 2
ONE      EQU   1 .                                               S22025
TWO      EQU   2 .                      CONSTANT               @YA06890
D30      EQU   30                       DISPLACEMENT           @OX16398
D34      EQU   34                       DISPLACEMENT           @OX16398
HEX01    EQU   X'01'                    TEST FOR MULTI-UNIT BFR SA57334
MULT4    EQU   2 .                      VALUE FOR RRN ADJUSTMENT S21101
DIV4     EQU   2 .                      VALUE FOR RRN ADJUSTMENT S21101
ROUNDER  EQU   3 .                      VALUE FOR RRN ADJUSTMENT S21101
INCR     EQU   4 .                      VALUE FOR ADDR INCREMENT S21101
THREE    EQU   4
         SPACE 1
*        PRFTIC FLAGS WILL BE AS FOLLOWS
*              X'28' - TO BE QUEUED ON REUSABLE DISK
*              X'18' - TO BE QUEUED ON NONREUSABLE DISK
*              X'40' - TO BE QUEUED IN MS QUEUES
CONC     EQU   X'20' .                                           S22026
CLOCK    EQU   X'80' .                                           S22026
INTVL    EQU   X'70' .                                           S22026
         SPACE 2 .                                               S22025
*****************************************************************S22025
*                                                                S22025
*** CONDITION CODE MASKS                                         S22025
*                                                                S22025
*****************************************************************S22025
         SPACE 2 .                                               S22025
ZEROES   EQU   8 .                      ZEROES CONDITION         S22025
FOUR     EQU   4 .                      CONSTANT                OX02183
         EJECT
         USING AVTSAVE2,R13 .                                    S22025
         USING IEDQLCB,RLCB
         USING IEDQPRF,RPRF
         USING IEDQSCB,RSCB
          USING  IEDQQCB,RQCB
          USING  IEDQPQCB,RPQ
         USING IEDQDISP,R11
         USING IEDQDATA,R2
         SPACE 2
         DC    AL1(DSPMCPL6)
         DC    AL3(FINDSTCA)            ADDRESS OF SUBROUTINE   SA61768
*              CALLED BY QFA AND 19RP TO ACTIVATE THE SEND      SA61768
*              SCHEDULER                                        SA61768
          DC    X'00',X'00'
         SPACE 2
         USING *,12
         SPACE 1
DESTSCH  EQU   *
         LA    R12,0(0,R15)             SET BASE REG
IEDQHM&A IEDHJN QUEUE .                 MODULE ID AND DATE       S22025
          LR    RPREFIX,R1
         L     RLCB,PRFLCB-1
         L     RSCB,LCBSCBA-1           SCB ADDR
         L    RQCB,SCBDESTQ-1
         LA    RPQ,QCBMSIZE(RQCB)       ADDR OF FIRST PQCB
         LA    R11,QCBPEND-IEDQPQCB     SIZE OF A PRTY QCB
         SR    R10,R10
         TM    PRFSTAT1,PRFDUPLN        DUP HEADER               S22025
         BO    BUMPCNT                  YES, BUMP COUNT          S22025
         TM    PRFSTAT1,PRFNLSTN        EOM                      S22025
         BO    CNTOK                    NO, DON'T BUMP COUNT     S22025
BUMPCNT  EQU   *                                                 S22025
         LH    R9,QCBMSGCT .            THE COUNT OF MESSAGES    S22025
         LA    R9,1(,R9) .              SHOULD BE INCREMENTED    S22025
         STH   R9,QCBMSGCT .            EACH EOM OR DUP HEADER   S22025
CNTOK    EQU   *                                                 S22025
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER
         BO    TXTBFR                   BR NO
         TM    PRFSTAT1,PRFERMGN        IS THIS AN ERROR MSG     S22025
         BO    COMPARE                  YES, BRANCH              S22025
         NI    SCBQTYPE,X'0F' .         CLEAR QTYPE FOR HDRS
         TM    LCBSTAT1,LCBINITN        INITIAE MODE
         BZ    COMPARE .                BRANCH NO INITIATE MODE  S21101
         SPACE
         L     R15,QCBDCBAD-1 .         ADDR OF DEST DCB         A51765
         TM    DCBPCI-IHADCB(R15),PCISEND IS PCI=A/PCI=X         S22025
*                                            SPECIFIED FOR SEND  S22025
         BZ    NOTINIT .                BR IF NO PCI             S22025
         CLI   LCBRSKEY,DSPBUFSC .      INITIATE FUNCTION NOT    S21101
         BNE   OUT .                      SUPPORTED FOR BUFFERED S21101
*                                         TERMINAL AS SOURCE     S21101
NOTINIT  EQU   * .                                               A51765
         NI    LCBSTAT1,X'FF'-LCBINITN . TREAT AS FEFO MESSAGE   S21101
COMPARE  EQU   *
         CLC   SCBPRI(1),QCBPRIPQ  IS THIS MSG PRTY SAME
*                                       AS THIS QCB PRTY
         BNL   OUT .                    BR IF THIS MSG IS HI OR EQ
*                                       LAST QCB HAS 0 PRI
         LA    R10,1(,R10) .            SET OFFSET TO NEXT QCB
         AR    RPQ,R11 .                ADDR OF NEXT PQCB
         B     COMPARE .                GO LOOK AT IT
         SPACE 1
NOINIT   EQU   *
*        IF QCB IS FOR 24 HOUR DELAY DO NOT REMOVE IT
         NI    LCBSTAT1,LCBINITF        RESET INIT MODE
         B     GETSIZE
POSTSUB  EQU   *
         LA    R2,AVTBFRTB              ADDR OF BFRRTN QCB
         MVI    PRFPRI-IEDQPRF(R1),PRIBFRTB
POSTSUBA EQU   *
         ST   RSCB,AVTSAVE3
          BAL   R14,POSTA
         L    RSCB,AVTSAVE3
          L     RQCB,SCBDESTQ-1         RESTORE QCB ADDR
         BR    R10 .                    RETURN
SENDINIT EQU   *
         TM    LCBSTAT1,LCBINITN+LCBRECVN
*        RECEIVING AN INITIATE MODE MESSAGE
         BCR   14,R14 .                 BR NOT INIT
         TM    LCBINSRC+2,XXXON .       NOW BEING SENT
         BCR   1,R14 .                  BR NOT GEING SENT
         L     R1,LCBINSRC-1 .          ADDR OF DEST LCB
         TM    LCBSTAT1-IEDQLCB(R1),LCBINITN .   STILL BEING SENT
         BCR   14,R14 .                 BR NOT BEING SENT
         B     4(R14)                   BR INIT BEING SENT
TXTBFR   EQU   *
         IC    R10,SCBPRI .             OFFSET TO PRI QCB
         MR     R10,R10 .
          AR    RPQ,R11                 + QCB ADDR
         B     GETSIZE .
         SPACE 1
OUT      EQU   *
         STC     R10,SCBPRI     REPLACE PRI WITH OFFSET
         TM    PRFSTAT1,PRFDUPLN        DUPL HDR
         BO    POSTDISK                 BR YES TO BYPASS NEXT THING
         TM    LCBSTAT1,LCBINITN+LCBRECVN
*        RECEIVING AN INITIATE MODE MESSAGE
         BNO   GETSIZE
         TM    SCBSTATE,SCBMSGLN+SCBLCK1N .  LOCK MODE
         BNZ   NOINIT .                 IF LOCK - NO INIT MODE
         TM    QCBDSFLG,QCBTSQ          TIME-SHARING QUEUEING?   S22029
         BO    NOINIT                   BRANCH ON YES               TSO
         CLI   QCBSTVTO,CONC .          DEST A CONC              S22026
         BE    NOINIT .                 BRANCH IF YES            S22026
         CLI   QCBSTPRI,CLOCK .         CLOCK OPTION             S22026
         BE    NOINIT .                 BRANCH IF YES            S22026
         CLI   QCBSTPRI,INTVL .         INTVL OPTION             S22026
         BNE   NOTDIAL .                BRANCH IF NO             S22026
         BAL   R10,CKDELAYQ             CK IF IN DELAY Q
NOTDIAL  EQU   *
         LA    R7,0(R7) .               CLEAR HI
         LR    R2,R7 .                  SET R2
*        FIND THE LAST LCB IN INSRC CHAIN BEFORE INSERT THIS ONE
INITLOOP EQU   *
         LR    R1,R2 .                  SET ADDR OF PVEV IN Q
         L     R2,QCBINSRC-1-IEDQQCB(R1) .  ADDR OF NEXT
         N     R2,CLEAROFF .            CLEAR HI AND INSRC BITS
         CLR   R2,R7 .                  IS THIS LAST ONE
*        LAST IN CHAIN POINTS TO ITSELF
         BNE   INITLOOP .               BR NOT LAST TO GET NEXT
         MVC   LCBINSRC(3),QCBINSRC-IEDQQCB(R1)
         MVC   QCBINSRC-IEDQQCB(3,R1),PRFLCB
*        CHAIN LCB INTO CHAIN
         OI    QCBINSRC+2-IEDQQCB(R1),XXXON .   SET INSRC FLAG ON
         OI    LCBINSRC+2,XXXON .      SET INSRC FLAG ON
GETSIZE  EQU   *
         LR    R10,RPREFIX .            INITIALIZE FOR LOOP WITH FIRST
*                                       UNIT ADDR
         SR    R2,R2 .                  INITIALIZE UNIT COUNT TO ZERO
         LH    R5,PRFSIZE
         SR    R11,R11
         IC    R11,PRFNBUNT             NUMBER OF UNITS IN BFR
COMPARE1 EQU   *
          L     R10,PRFTIC-IEDQPRF(R10)
         LA    R2,1(0,R2)
         SH    R5,AVTKEYLE              SUBTRACT AMT. IN THIS UNIT
         BP    COMPARE1 .               IF UNITS REMAIN, GO COUNT THEM
ALL      EQU   *
         STC   R2,PRFNBUNT              REPLACE CORRECT NO. UNITS
         CR    R2,R11
         BE    POSTDISK
          SR    R11,R2
          STC   R11,PRFNBUNT-IEDQPRF(R10)
          LR    R1,R10
         BAL   R10,POSTSUB .            POST BFR
POSTDISK EQU   *
         CLI   QCBSTPRI,DIALPRTY        DIAL QCB
         BNH   NODIAL                   BR NOT DIAL
         TM    PRFSTAT1,PRFNLSTN        IS THIS A LAST SEG
         BO    NODIAL                   BR IF NOT LAST
         IC    R14,QCBPRIPQ             PRTY OF THIS QCB
         EX    R14,COMPRI               THIS MSG PRTY HIGHER THAN
         BH    NODIAL                   PREVIOUS MSG - BR NO
         STC   R14,QCBPRLVL             SET HIGHSET PRTY MSG
NODIAL   EQU   *
*        IF A LOGICAL READ ERROR OCCURS IN INITIATE MODE - FA WILL
*        DROP THE ERB LEAVING LCBDLNK SWITCH SET TO 'NOT POSTABLE'
*        PCI WILL NOT POST THE ERB.  LOGICAL READ ERROR WILL ALSO BE
*        SET IN ERBST.  WHEN HM RECEIVES A BFR IN INIT MODE UNDER
*        THESE CONDITIONS - THE ERB FOR THE DESTINATION LINE WILL
*        BE REPOSTED TO FA WITH AN E0 PRIORITY REQUESTING
*        THE BUFFERS OF THIS MESSAGE AGAIN.  THE DESTINATION LCB
*        WILL BE FOUND FROM THE LCBINSRCE CHAIN OF THE SOURCE LCB.
         BAL   R14,SENDINIT
         B     NOERB
         TM    LCBERBST-IEDQLCB(R1),LCBDLNKN+LCBEOMSG
*        CAN THE ERB BE POSTED
         BNZ   NOERB                    BR YES PCI MAY POST IT
         TM    LCBERBST-IEDQLCB(R1),LCBRDERR .   HAS A READ ERROR
         BNO   NOERB .                  BR IF NO ERROR
         CLI   LCBERBKY-IEDQLCB(R1),AVTEZERO  SHOULD HM POST ERB S21101
         BE    NOERB NO IT IS ALREADY ON READY Q                 S21101
         CLI   LCBERBPY-IEDQLCB(R1),PRIFSPCI . SHOULD PTRY BE CHGED
         BNE   PRIOK .                  BR NO
         MVI   LCBERBPY-IEDQLCB(R1),PRISBPCI . SET PRTY
PRIOK    EQU   *
         LA    R1,LCBERB-IEDQLCB(R1) .  ADDR OF ERB TO POST
         LA    R2,AVTDSIOB .            QCB ADDR FOR FA
*                                   .   STORE OF R2 WILL ZERO    S21101
*                                   .   FIRST BYTE OF ERB        S21101
         BAL   R10,POSTSUBA .           POST ERB TO FA
NOERB    EQU   *
         AIF   (&A NE 1).H001
         B     COREQUE
         AGO   .H005
.H001    ANOP
         TM    PRFSTAT1,PRFDUPLN        DUPL HDR
         AIF   (&A NE 2).H002
         BNO   QUEUE1
         AGO   .H003
.H002    ANOP
         BO    DUPL                     BR YES
          TM    QCBDSFLG,QCBCORE
*                       IS THIS A CORE QUEUE QCB
         BNO   QUEUE1 .                   NO, DISK ONLY          S21101
*                                         YES, HAS CORE Q        S21101
         CLI   PRFPRI,COPYPRI .         IS THIS BUF FROM COPY    S21101
         BNE   COREQUE .                  NO, NOT COPY, GO ENQUE S21101
*                                           ONTO CORE QUEUE      S21101
*                                         YES, FROM COPY         S21101
         TM    QCBDSFLG,QCBDISK .       DOES THIS CORE Q HAVE    S21101
*                                         ANY DISK BACKUP        S21101
         BZ    COREQUE .                  NO, CORE ONLY          S21101
*                                         YES, CORE WITH DISK    S21101
         B     QUEUE1 .                 ENQUEUE ONTO DISK        S21101
.H003    ANOP
         TITLE '''IEDQHM'' - DUPLICATE MESSAGE TESTING' .        S22025
DUPL     EQU   *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*        WHEN A DUPL. HDR IS BEING PROCESSED--
**       A COPY MAY OR MAY NOT BE ON THE QUEUEING MEDUUM INVOLVED
*        WITH THIS DESTINATION.
**       WHEN THE INITIAL RECALL IS DONE (IEDQBD), THE CONTENTS
*        OF SCBQTYPE - HIGH HALF BYTE -- INDICATING THE ORIGINAL
*        COPY OF THE MESSAGE IS PUT INTO SCBCQT ( HBFNO) AND SCB
*        QTYPE IS CLEARED TO 0.
**       WHEN THE SUBTASK GETS THE ERB FROM THE INITIAL RECALL
*        - USUALLY IEDQBD -- THE XTRA RECORDS ADDRESS IS COPCOPIED
*        INTO SCBRDX (SCBMBSSA) FOR RESUABLE DISK AND FOR NON-
*        REUSABLE DISK INTO SCBNDX (SCBMBSSA+4).  FOR A CORE COPY
*        NO COPY SHOULD BE MADE OF XTRA.
**       SCBDCHDR WILL REFLECT THE DCHDR OF THE DISK Q TYPE INDICATED
*        IN SCBQTYPE. THE OTHER DISK QTYPE WILL BE IN SCBOTHER
*        (SCBTRANS).  IF ONE EXISTS.
**       SCBCQT HIGH HALF BYTE WILL BE THE COPY OF SCBQTYPE MADE
*        EARLIER.  THE LOW HALF BYTE IS THE INDICATION OF ALL MED-
*        IUMS WHICH CONTAIN A COPY OF THES MESSAGE.
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
         L     RSCB,LCBSCBA-1
         AIF   (&A EQ 2).H003A
         LA    R1,256*SCBCC+SCBCC+SCBCOREQ
*        SET TO MARK FISLD AS HAVING A COPY ON THIS MEDIUM
*        SET REG TO TEST FOR MSG ORIGINAL OR COPY ON THIS MEDIUM
         LA    R14,COREQUE .            ASSUME COPY THERE - TRN TO Q
         TM    QCBDSFLG,QCBREUS+QCBNREUS
         BZ    EXTM
.H003A   ANOP
*     IS COPY IS TO BE CALLED AND THE ORIGINAL IS IN CORE, THEN
*     IT MUST NOT BE FREED (SERVICED) UNITL AFTER COPY IS DONE.
*     THEREFORE  IT WILL BE FLAGGED AS A DUPL WITH ONE MORE DUPL HDR
*        COUNTED THAN EXISTS.  THEN WHEN COPY FINISHES. IT WILL
*    REDUCE THE COUNT BY ONE.
         TM    QCBDSFLG,QCBREUS .       RESU DISK
         BO    TSTSCB .                 BR YES
         LA    R1,256*SCBCN+SCBCN+SCBNREUS
*        SET REG TO TEST FOR MSG ORIGINAL OR COPY ON THIS MEDIUM
*        SET TO MARK FISLD AS HAVING A COPY ON THIS MEDIUM
         LA    R14,QUEUE1 .             ASSUME COPY THERE  - SET RTN
EXTM     EQU   *
         EX    R1,TM .                  TEST FOR ORIG. OR COPY
         BNZ   EXBR .                   BR IF ONE THERE
         AIF   (&A EQ 2).H003B .                                 S21101
         TM    SCBCQT,SCBREUS+SCBNREUS .ANY DISK-COPY WILL USE ITS21101
         BNZ   NOCORE .                   YES, DISK              S21101
*                                         NO, Q=MO               S21101
         LR    R14,R1 .                 SAVE REG 1               S21101
         BAL   R1,DUPLCORE .                                     S21101
*                                                                S21101
         BAL   R1,INCRCNT .             INCR COUNT OF DUPLS-COPY S21101
*                                                                S21101
         LR    R1,R14 .                 RESTORE REG 1            S21101
NOCORE   EQU   * .                                               S21101
.H003B   ANOP , .                                                S21101
         LA    R14,POSTCOPY .           SET RTN TO PUT ONE THER)
EXBR     EQU   *
         SRL   R1,8 .                   SET R1 TO OI MASK
         EX    R1,OI .                  SET COPY HERE
         BR    R14 .                    RETURN
TSTSCB   EQU   *
         LA    R1,256*SCBCR+SCBCR+SCBREUS
*        SET TO MARK FISLD AS HAVING A COPY ON THIS MEDIUM
*        SET REG TO TEST FOR MSG ORIGINAL OR COPY ON THIS MEDIUM
         BAL   R14,EXTM .               ASSUME COPY THERE - RTN HERE
         BAL   R14,REUSDUPL .           IF DUPL TO BE QUEUED HEÖE
*        CHECK FOR TOO FAR
         TITLE '''IEDQHM'' - DISK ASSIGNMENT' .                  S22025
QUEUE1   EQU   *
          LA    R2,AVTDSIOB             SET FOR LATER POST
         ST    R2,PRFQCBA-1
         MVI    PRFPRI,PRIDESTQ
         LA    R2,AVTNADDR              NON REUSABLE VALUE OF ADDR
         MVI   PRFTIC,XPRFNRUS+XTIC     SET TIC FLAGS TO NON REUS
         LA    R9,SCBNREUS .            SET FOR LATER OI
         TM    QCBDSFLG,QCBREUS
*                                       IS THIS FOR REUS DISK
         BZ    NONREUS                  BR NO
         LA    R2,AVTRADDR              ADDR OF REUS VALUE OF
*                                       ADDRESS
         MVI   PRFTIC,XPRFREUS+XTIC     SET TIC FLAGS TO REUS DSK
*                       TELL TIC OP CODE FIELD IS USED TO
*                       TELL DISK I/O WHICH DISK DATA SET
*                       IS TO BE USED
         LA    R9,SCBREUS .             SET FOR LATER OI
NONREUS  EQU   * .                                               S22025
         MVC   PRFCRCD(3),SCBDNSEG .    SET RRNS ASSUMING TEXT   S22025
         MVC   PRFCHDR(3),SCBDCHDR .         BUFFER              S22025
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER
         BO    TXTSAME .                NO, SKIP HEADER PROCESS  S22025
QUESAME  EQU   *
         TM    PRFSTAT1,PRFDUPLN+PRFERMGN  DUPL OR ERR MSG
         BNZ   NOEX .                   BR IF EITHER
         EX    R9,QTYPEOI
NOEX     EQU   *
         MVC   PRFCRCD(3),QCBDNHDR
         MVC   PRFHQBCK(3),QCBQBACK
         MVC    QCBQBACK(3),PRFCRCD
         MVC   QCBPQBCK,PRFCRCD         UPDATE PRI Q BACK PTR   SA51078
         TM    QCBPREVF+L'QCBPREVF-1,CPBQTYPE                   SA50192
*                                       FIRST MSG ON QUEUE      SA50192
         BNZ   PREVFSET                 BRANCH IF NO            SA50192
         BAL   R9,LOCKMSG               IS IT LOCK MODE        @SA76275
         B     LKPFEFO                  BR NOT LOCK            @SA76275
         B     PREVFSET                 BYPASS SETTING UP PFEFO@SA76275
*                                       IF ONLY A LOCK RESPONSE@SA76275
LKPFEFO  EQU   *                                               @SA76275
         TM    PRFSTAT1,PRFCNCLN        CANCELLED HEADER?      @SA71682
         BO    PREVFSET                 YES, DOES NOT GET IN   @SA71682
*                                        FEFO CHAIN            @SA71682
         MVC   QCBPREVF,PRFCRCD         SET FOR RESTART IF NO   SA50192
*                                       PFEFO IN CHKPOINT       SA50192
PREVFSET EQU   *                                                SA50192
         MVC   QCBDNHDR(3),1(R2)
         MVC   PRFNHDR(3),QCBDNHDR
         BAL   R9,ADDONE
         TM    PRFSTAT1,PRFERMGN        ERR MSG IN THIS BFR
         BO    TXTSAME                  BR YES - DO NOT UPDATE QCB
         MVC   SCBDCHDR(3),PRFCRCD      SET CORRENT HDR ADDR
         TM    PRFSTAT1,PRFDUPLN        DUPL HDR
         BO    DUPLHDR                  BR YES
         BAL   R9,LOCKMSG
         B     TXTSAME                  BR NOT LOCK
         MVC   QCBLKRRN(3),PRFCRCD
*                        SET DEST QCB LOCK FIELD
TXTSAME  EQU   *
         XC    PRFXTRA(3),PRFXTRA .     CLEAR THE XTRA ADDRESS
         SR    R14,R14
         IC    R14,PRFNBUNT             NUMBER OF UNITS CONTAINING
         BCTR  R14,R0                   DATA  -1
         LTR   R14,R14                  IS THERE MORE THAN 1 UNIT
         BZ    ISITLAST .               BR IF JUST ONE
         MVC   PRFXTRA(3),1(R2)         SET LOCATION OF XTRA AS ADDR
         SLL   R14,MULT4 .              ADJUST NO. OF RECORDS    S21101
         A     R14,0(R2)           ADD NO. ADD. RECS. TO ADR
         ST    R14,0(R2)
ISITLAST EQU   *
         TM    PRFSTAT1,PRFNLSTN   IS THIS THE LAST SEG
         BZ    LASTSEG                  BR YES
         MVC   SCBDNSEG(3),1(R2)
         MVC   PRFNTXT(3),SCBDNSEG
         BAL   R9,ADDONE
CKLDPT   EQU   *
         TM    QCBDSFLG,QCBNREUS
          BO    DISKPOST                IF NON REUS RETURN
CKLOADPT EQU   *
         TM    PRFSTAT1,PRFERMGN+PRFDUPLN .  THIS ERMSG OR DUPL
         BNZ   NOABEND .                BR NO
         L     R14,AVTRADDR .           NEXT VALUE OF ADDR
         L     R11,SCBDCHDR-1 .         ADDR OF HDR THIS MSG
         LA    R11,0(R11) .             CLEAR HI
         SR    R14,R11 .                GET DIFFERENCE FOR MSG SPAN
         L     R11,AVTTOTNR .           ADJUST AVTTOTNR TO       S21101
         SLL   R11,MULT4 .              REFLECT THE RRN NUMBERINGS21101
*                                       SYSTEM WHICH INCREMENTS  S21101
*                                       BY 4                     S21101
         CR    R14,R11 .                COMPARE MSG SPAN TO SIZE S21101
         BL    NOABEND .                DOES MSG SPAN THE DISK
         LA    R15,ABCODE7 .           SET ABEND CODE
         BAL   R14,AVTABEND .          SET 14 TO ADDR OF ABEND
*        AND GO TO RTN TO ABEND
NOABEND  EQU   *
         L     R2,AVTRADDR .            ADJUST AVTRADDR          S21101
         LA    R2,ROUNDER(R2) .         TO REFLECT               S21101
         SRL   R2,DIV4 .                THE ABSOLUTE             S21101
         CL    R2,AVTLODPT .            NO. OF RECORDS           S21101
          BL    DISKPOST                LESS THAN LDPT - RETURN
         SR    R10,R10                  DIVIDE RADDR BY TOTAL NO.
         LR    R11,R2 .                 OF RECORDS. IF REMAINDER S21101
         D     R10,AVTTOTNR             ER IS LESS THAN 1/4 OF THE
         SR    R14,R14                  TOT NO OF OF RECORDS RADDR
         L     R15,AVTTOTNR             SHOULD BE CHANGED TO ITS
         LA    R5,4                     MODULO VALUE IF THE 2 TO
         DR    R14,R5                   23RD BIT IS ALSO ON
         CR    R15,R10                  IS THE REMAINDER LESS THAN
         BL    SETREUS                  1/4 OF TOTAL BR NO
         TM    AVTRADDR+1,AVTMODBT      IS THE 2 TO 23RD BIT ON
         BNO   SETREUS                  BR  NO
         SLL   R10,MULT4 .              ADJUST THE VALUE         S21101
         A    R10,ROUND                 REINITIALIZE RADDR      SA66611
         ST     R10,AVTRADDR            SET REUS TO MODULO VALUE
         SRL   R10,MULT4                DIVIDE BY FOUR          SA66611
         ST    R10,AVTLODPT             SET NEW VALUE           SA66611
SETREUS  EQU   *
         IC    R2,AVTADEBR              SINCE 1/4 OF TOTAL MAY
         LA    R2,1(R2)                 HAVE A REMAINDER, EVERY 4TH
         STC   R2,AVTADEBR              TIME LDPT IS CHANGED, THE
         CLI   AVTADEBR,THREE           QUDTIENT AND REMAINDER MUST
         BL    ADD1QTR                  BE ADDED TO KEEP THE ZONE
         MVI   AVTADEBR,AVTEZERO        BOUNDARIES ON THE SAME DISK
         AR   R15,R14                   LOCATION
ADD1QTR  EQU   *
         L     R2,AVTLODPT              ADD 1/4 ( OR 1/4 + REMAINDER)
         AR    R2,R15                   TO LDPT FOR NEXT POINT
         ST    R2,AVTLODPT              TO CALL RESUABILITY
         L     R1,AVTIA .               GET ADDRESS OF REUS QCB  S21101
         L     R11,AVTEA .              SET DISPATCHER BASE      S21101
         BAL   R14,DSPPOSTR .           POST REUS QCB TO ITSELF  S21101
*                                         TO ACTIVATE REUS ZONE  S21101
*                                         CLEANING               S21101
* THE REUS QCB ALREADY HAS ASSEMBLED IN IT THE PROPER PRIORITY & S21101
* THE ADDRESS OF ITSELF IN ITS FIRST WORD TO SIMPLIFY TPOSTING.  S21101
*                                                                S21101
DISKPOST EQU    *
         L     R2,PRFQCBA-1             FOR COMMON CODE
.H005    ANOP
NRPOST    EQU   *
         LTR   R12,R12
         BM    COPYRTN
          LR    R1,RPREFIX
          L     R11,AVTEA
         LA    R15,DSPPOST
         B     POSTB
         AIF   (&A EQ 1).H006
LASTSEG  EQU   *
         BAL   R14,SAMELAST .                                    S22025
         LA    R10,CKLOCK .             SET RETURN ADDR
TQBACK   EQU   *
*                                       IF VALID THE SRCE QCB MUST
*                                       BE LOCATED TO UPDATE THE
*                                       SOURCE QBACK CHAIN
         LR    R5,R15
         LH    R1,PRFSRCE
         N     R1,AVTCLRHI .            CLEAR HI-ORDER HALF AND TEST
         BCR   ZEROES,R10 .             NO, BR                   S22025
         L     R15,AVTRNMPT
         BALR  R14,R15
         L     R1,0(R1)
         MVC   PRFTQBCK(3),QCBQBACK-IEDQQCB(R1)
*                       QBACK PTR INTO BFR
         MVC   QCBQBACK-IEDQQCB(3,R1),PRFCRCD
*                       UPDATE QCB QBACK WITH THIS REC. ADDR.
         LR    R15,R5
         BR    R10
QTYPEOI  OI    SCBQTYPE,X'00' .         SET A VALUE IN QTYPE
ADDONE   EQU   *
         LA    R0,INCR .                ADD 4 TO THE VALUE OF    S21101
         A     R0,0(R2)                 ADDRESS CURRENTLY BEING
         ST    R0,0(R2)                 USED
         BR    R9
.H006    ANOP
SAMELAST EQU   *
         LTR   R12,R12            IF THIS IS COPY OR REUS
         BCR   4,R14                    NO CHECKING REQUIRED
         BAL   R9,LOCKMSG
         B     CKINIT                   BR NOT LOCK
         AIF   (&A EQ 1).H007
         TM    QCBDSFLG,QCBREUS+QCBNREUS    ANY DSIK
         BNZ   POSTLCB                  IF CORE ONLY  CLEAR BITS
.H007    ANOP
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN
         BNO   POSTLCB
         NI    SCBSTATE,X'FF'-(SCBLCK1N+SCBMSGLN)       MSG LOCK OFF
*********    INITIALIZE LCB TO SEND
POSTLCB  EQU   *
         ST    R14,AVTPARM3             SAVE RETURN ADDRESS    @XA11314
         L     R1,QCBDCBAD-1            DCB ADDR
         CLI   QCBSTVTO,DSPLOGSC        IS QCB FOR LOGTYPE       S22024
         BE    NOLGB                    BRANCH ON YES            S22024
         TM    DCBDSORG-IHADCB(R1),AVTE80    IS THIS AN LGB      S22024
         BO    LGB                      BRANCH ON YES            S22024
NOLGB    EQU   *                                                 S22024
         SR    R14,R14                  CLEAR FOR IC
         LR    R15,R14                  CLEAR FOR IC
         IC    R14,QCBLKRLN .           REL LN  O OF LOCKED LINE
         IC    R15,DCBEIOBX-IHADCB(R1)  SIZE OF LCB
         MR    R14,R14                  SIZE X LN NO.
         AL    R15,DCBIOBAD-IHADCB(R1)  + ADDR FIRST IOB - SIZE OF
         LA    R1,LCBFLAG1-IEDQLCB      IOB -- SIZE OF LCB-IOB
         SR    R15,R1                   - LCB ADDR
         LR    R1,R15                   LCB ADDR FOR POST
RESUME   EQU   *                                                 S22024
         L     R14,AVTPARM3             RESTORE REGISTER 14    @XA11314
         NI    LCBINSRC+2-IEDQLCB(R1),X'FF'-X'40'
         BCR   7,R14
         ST    R1,LCBQCBA-1-IEDQLCB(R1)
         MVI   LCBPRI-IEDQLCB(R1),PRILNFRE
         L     R11,AVTEA                RSSTORE DISP ADDR
         BAL   R14,DSPPOSTR
         L     R14,AVTPARM3             RESTORE RETURN ADDRESS @XA11314
         L     RSCB,LCBSCBA-1           RESTORE R3
         L     RQCB,SCBDESTQ-1          RESTORE R7
         BR    R14
LGB      EQU   *                                                 S22024
         LR    R1,RQCB                  PUT QCB ADDR IN R1       S22024
         SH    R1,AVTHA4                BACK UP FOUR BYTES       S22024
         L     R1,QCBPLCBA-1-IEDNQCB(R1)  ADDRESS OF PLCB        S22024
         B     RESUME                                            S22024
CKINIT   EQU   *
         TM    LCBSTAT1,LCBINITN+LCBRECVN
*        RECEIVING AN INITIATE MODE MESSAGE
         BCR   14,R14 .                 BR NOT RCV INIT
         TM    LCBINSRC+2,XXXON         IF THELOW ORDER BIT IS NOT
         BO    GETINSRC .               BR IF LCB IS IN SRCE CHAIN
         L     R1,LCBINSRC-1 .          ADDR OF DEST LCB
         OI    LCBERBST-IEDQLCB(R1),XXHMSG .    SET EOM TO HM
         BR    R14
GETINSRC EQU   *
*    IS LCB IS IN TE SRCE CHAIN REMOVE IT
         LR    R2,RQCB .                INITIALIZE FOR LOOP
         LA    RLCB,0(0,RLCB)
CKLCB    EQU   *
         LR    R15,R2
         L     R2,LCBINSRC-IEDQLCB-1(R2)  ADDR NEXT LCB
         N     R2,CLEAROFF              CLEAR HI AND INSRC BIT
         CR    R2,RLCB .                IS THIS THE LCB TO REMOVE
         BNE   CKLCB .                  IF NOT, GO LOOK AT NEXT ONE
REMOVE   EQU   *
         MVC   LCBINSRC-IEDQLCB(3,R15),LCBINSRC-IEDQLCB(R2)
*                        REMOVE LCB FROM SOURCE CHAIN
        BR     R14
         AIF   (&A EQ 1).H007A
DUPLHDR  EQU   *
         BAL   R10,GETNTXT
CKLOCK   EQU   * .                                               S22025
         TM    PRFSTAT1,PRFCNCLN        CANCELLED MSG          @XA06353
         BO    CKLDPT                   BRANCH IF YES          @XA06353
*                                                              @XA06353
         BAL    R14,FINDSTCB
         B     CKLDPT .                 GO, SEE IF REUS NEEDED   S22025
         SPACE 1 .                                               S22025
POSTCOPY EQU   *
         MVI   PRFPRI,PRICOPY
         SPACE 1 .                                              SA51783
*** QCB MESSAGE COUNT HAS BEEN INCREMENTED FOR THIS DUPLICATE   SA51783
*   HEADER; BUT, COPY WILL TPOST THE BUFFERS OF THE MESSAGE     SA51783
*   BACK HERE WHEREUPON THE MESSAGE WOULD BE RECOUNTED.  HEADERSSA51783
*   OF MESSAGES WHICH WILL BE DISK QUEUED WILL NOT RETURN HERE. SA51783
*   SO, TO AVOID AN INCORRECT MESSAGE COUNT, WE MUST DECREMENT  SA51783
*   IT FOR CORE-ONLY QUEUED OR DISK QUEUED MULTI-BUFFER MSGS.   SA51783
         SPACE 1 .                                              SA51783
         AIF   (&A EQ 2).H900 .                                 SA51783
         TM    QCBDSFLG,QCBDISK .       IS THIS DISK QUEUED     SA51783
         BZ    DECRCT .                 NO, GO DECREMENT COUNT  SA51783
.H900    ANOP  , .                                              SA51783
         TM    PRFSTAT1,PRFNLSTN .      IS IT LAST BUFFER       SA51783
         BZ    NODECRCT .               YES, IT WON'T RETURN    SA51783
DECRCT   EQU   * .                                              SA51783
         LH    R2,QCBMSGCT .            DECREMENT MESSAGE COUNT SA51783
         BCTR  R2,0 .                     BY ONE                SA51783
         STH   R2,QCBMSGCT .            RESTORE IT              SA51783
NODECRCT EQU   * .                                              SA51783
         L    R2,AVTCOPY                QCB ADDR FOR POST
         LA    R2,0(R2)                 CLEAR HI BYTE
         LTR   R2,R2                    COPY THERE
         BNZ   NRPOST .                 BR YES
         LA    R15,ABCODE4 .           SET ABEND CODE IN 15
         BAL   R14,AVTABEND .          SET 14 TO INSTR OF ABEND
*        AND GO TO RTN TO ABEND
         SPACE 3 .                                               S22025
REUSDUPL EQU   *
         MVC   AVTDOUBL+1(3),QCBDNHDR   HEADER RECORD           SA57334
         MVC   AVTDOUBL+5(3),PRFXTRA    EXTRA SEGMENT           SA57334
         CLI   PRFNBUNT,HEX01           ANY EXTRAS              SA57334
         BNE   PASTNTXT                 BR YES                  SA57334
         SPACE
         TM    PRFSTAT1,PRFNLSTN        LAST SEG
         BCR   14,R14                   BR YES - NO NTXT
         MVC   AVTDOUBL+5(3),PRFNTXT
*                                                               SA57334
*        IF A DUPL HDR IS MORE THAN 1 ZONE AWAY FROM ITS        SA57334
*        PREVIOUSLY ASSIGNED NEXT TEXT OR EXTRA SEGMENT         SA57334
*        THE WHOLE MSG SHOULD BE RECOPIED                       SA57334
*                                                               SA57334
PASTNTXT EQU   *                                                SA57334
         L     R5,AVTDOUBL
         LA    R5,0(0,R5)
         MVI   AVTDOUBL+4,AVTEZERO
         S     R5,AVTDOUBL+4
         SR    R10,R10
         L     R11,AVTTOTNR .           AVTTOTNR IS THE ABSOLUTE S21101
*                                       NUMBER OF RECORDS. THIS  S21101
*                                       IS ALSO THE SIZE OF A    S21101
*                                       ZONE IN THE RRN NUMBERINGS21101
*                                       SYSTEM WHICH INCREMENTS  S21101
*                                       BY 4                     S21101
         CR    R5,R11
         BH    POSTCOPY
       L    R7,SCBDESTQ-1               RESTORE QCB ADDR
         BR    R14
.H007A   ANOP
POST   EQU   *
         LR    R1,RPREFIX
POSTA    EQU   *
         L     R11,AVTEA
         LA    R15,DSPPOSTR
POSTB    EQU   *
         ST   R2,PRFQCBA-1-IEDQPRF(R1)
         BR    R15                      RETURN  SET
         AIF   (&A EQ 1).H008
GETNTXT  EQU   *
         CLI   PRFNBUNT,X'01'           JUST ONE UNIT
         BNE   NOCLEAR                  BR MORE THAN 1
         XC    PRFXTRA(3),PRFXTRA       THERE IS NO EXTR REC
         TM    PRFSTAT1,PRFNLSTN        LAST BFR
         BNO   TQBACK                   BR YES - NO NTXT EITHER
NOCLEAR  EQU   *
         MVC   PRFNTXT(3),SCBNDX
         TM    PRFTIC,XPRFNRUS .        THIS FOR NREUS DISK
*        ALREADY HAVE A COPY FOR THIS MSG ON THE ONE NEEDED
         BNZ   CKUNITS .                BR YES                   S22025
         MVC   PRFNTXT(3),SCBRDX        ASSUME REUS - GET NTXT
CKUNITS  EQU   *
         CLI   PRFNBUNT,X'01'           JUST ONE UNIT
         BCR   8,R10                    BR YES
         MVC   PRFXTRA(3),PRFNTXT       SET XTRA
         TM    PRFSTAT1,PRFNLSTN        LST BFR
         BNO   TQBACK                   BR IF LAST
         SR    R1,R1
         IC    R1,PRFNBUNT              NO UNITS
         BCTR  R1,0                     SUBTR 1 FOR ADD
         L     R0,PRFXTRA-1             ADDR OF XTRA
         SLL   R1,MULT4 .               ADJUST FOR 4'S           S21101
*                                       ARITHMETIC               S21101
         AR    R0,R1
         SRL   R1,DIV4 .                ADJUST BACK              S21101
         STC   R0,PRFNTXT+2             SET VALUE FOR NTXT SEGS
         SRL   R0,8
         STH   R0,PRFNTXT
         BR    R10                      RETURN
.H008    ANOP
LOCKMSG  EQU   *
         LTR   R12,R12 .                COPY CALL
         BCR   4,R9 .                   BR IF OCPY - NOT LOCK
         TM    PRFSTAT1,PRFDUPLN+PRFERMGN
*        DUPL HDR OR ERRMSG COULD BE FROM ORIGINAL LOCK MSG
         BCR   7,R9 .                   BR IF EITHER
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN    LOCK MSG
         BCR    8,R9                    BR NOT LOCK
         CLI   LCBRSKEY,DSPPUTSC        PUT RESPONSE MSG        SA60012
         BCR   7,R9                     BRANCH IF NO            SA60012
         TM    QCBFLAG,QCBPROC          THIS THE REQ. MSG OR RESP MSG
         BNO   4(R9)                    BR IF THE RESPONSE MSG
         BR    R9                       BR IF REQUESTING MSG
         AIF   (&A EQ 2).H009
         TITLE '''IEDQHM'' - CORE ENQUEUE' .                     S22025
*      THE FIRST 12 BYTES OF THE RECORDS WILL BE USED AS A KEY FIEDS
*     ON DISK.  THE TIC PORTION OF THIS FIELD WILL BE USED TO
*     COUNT DUPLICATE HEADERS
COREQUE  EQU   *
         AIF   (&A NE 3).H011A .                                 A51776
         TM    LCBSTAT1,LCBRECVN+LCBINITN INITIATE MESSAGE       A51776
         BNO   INITCORE .               NO, GO QUEUE IT          A51776
         TM    QCBDSFLG,QCBREUS+QCBNREUS IS IT DISK BACK-UP      A51776
         BNZ   CLEARCOR .               YES, GO QUEUE ON DISK    A51776
*                                            ONLY                A51776
INITCORE EQU   * .                                               A51776
.H011A   ANOP  , .                                               A51776
      TM    PRFSTAT1,PRFNHDRN
         BNO    NOCHECK
         NC    SCBCCHDR(3),SCBCCHDR .   HDR THERE OR LOST
         BZ    NOQUEUE1 .               BR IF LOST
         MVC    AVTDOUBL+1(3),SCBCCHDR
         L    R2,AVTDOUBL
         TM    PRFSTAT1,PRFCNCLN
         BNO   NOTCNCL                  BR NO
         OI    DATFLAGS-IEDQDATA(R2),DATCNCLD     SET CNCL FLAG
NOTCNCL  EQU   *
         TM    DATFLAGS-IEDQDATA(R2),DATLOSTN+DATINITL
*                                       HAS THIS MSG BEEN LOST
         BNZ   NOQUEUE
NOCHECK   EQU   *
         XC    0(8,RPRF),0(RPRF)        ZERO DATA FIELD
         XC    PRFNTXT(L'PRFNTXT+L'PRFCRCD),PRFNTXT .            A42363
         BAL   R9,CNTUNITS
         TM    PRFSTAT1,PRFDUPLN
         BO    COREDUPL
         AIF   (&A EQ 1).H009A
          TM     QCBDSFLG,QCBREUS+QCBNREUS
*                        IS THIS TO BE DISK QUEUED ALSO
         BNZ    SWAPNOT
.H009A   ANOP
INITRTN  EQU   *
          LR    R1,R5
         BAL   R10,POSTSUB .            POST BFR
UNITQUE EQU   *
         BAL   R10,QUEUNITS
         AIF   (&A EQ 1).H009B
         BAL   R9,SETDISK
.H009B   ANOP
         TM    PRFSTAT1,PRFNLSTN        IS THIS A LAST SEG
         BO    RETURN
         LTR   R12,R12                  IS THIS REUS OR COPY
         BM    COPYRTN                  BR YES
         BAL   R9,SETFEFO
          BAL   R14,FINDSTCB
         BAL   R14,SAMELAST
         NI    LCBINSRC+2,X'FF'-XXXON . SET INSRC FLAG OFF
         NI    LCBSTAT1,LCBINITF .      SET INIT MODE OFF
RETURN    EQU   *
         LTR   R12,R12
         BM    COPYRTN
          L     R11,AVTEA
          B     DSPDISP
COREHDR  EQU   *
         TM    PRFSTAT1,PRFCNCLN
         BNO   NOTCNCLD                 BR NO
         OI    DATFLAGS-IEDQDATA(RPREFIX),DATCNCLD      SET CNCL
NOTCNCLD EQU   *
         TM    PRFSTAT1,PRFDUPLN+PRFERMGN  DUPL OR ERRMSG
         BNZ   NOQTYPE .                BR IF EITHER - NO QTYPE TO SET
         OI    SCBQTYPE,SCBCOREQ
NOQTYPE  EQU   *
         AIF   (&A EQ 1).H009C
         TM    QCBDSFLG,QCBREUS+QCBNREUS
         BNZ   ASSIGN
.H009C   ANOP
         BAL   R9,LOCKMSG
         B     ASSIGN                   BR NOT LOCK
         MVC   QCBLKRRN(3),AVTDOUBL+1
ASSIGN   EQU   *
         BAL   R9,ASSIGN1
         NC    PRFTIC+1(3),PRFTIC+1
         BCR   8,R10
         L     R2,PRFTIC
         MVI   PRFTIC-IEDQPRF(R2),AVTEZERO
         BR    R10
ASSIGN1  EQU   *
         MVC   PRFCORE(3),AVTDOUBL+1
ASSIGN1A EQU   *
         MVC   PRFCRCD(3),PRFCORE       SET CURRENT RECORD IN   SA52971
*                                         CASE MAIN ONLY        SA52971
         XC    PRFNHDR(3),PRFNHDR        ZERO NEXT HDR POINTER
         TM    PRFSTAT1,PRFERMGN        ERROR MSG THIS BFR
         BO    SKIP                     BR YES DO NOT UPDATE SCB
         MVC   SCBCCHDR(3),PRFCORE      NEW SCB CURR HDR ADDR
         MVC   SCBCLSEG(3),PRFCORE      >ET NEW LAST SEGMENT
SKIP     EQU   *
         MVC   PRFNHDR,QCBCFHDR         LINK HDR ON QUE         SA51078
         MVC   QCBCFHDR,PRFCRCD         UPDATE FIRST HDR        SA51078
         BR    R9                       RETURN                  SA51078
         SPACE 1 .                                               S22025
DUPLCORE EQU   *
         L     R2,SCBCCHDR-1            HDR ADDR
         TM    LCBSTAT1,LCBSENDN        SENDING
         BNO   NOTSEND                  BR NO   CCHDR IS OK
         L     R2,SCBSCHDR-1            GET HDR ADDR
NOTSEND  EQU   *
         ST    R2,AVTDOUBL .            HAS THIS  MSG BEEN FREED
         CLC   AVTDOUBL+1(3),PRFCORE-IEDQPRF(R2) .   FROM THE CORE
        BNE   RCQCB .                  QUEUE- ALREADY SENT
*        THE ORIGINAL HDR ADDRESS MUST REMAIN IN R2 FOR COREDUPL
         OI    PRFSTAT1-IEDQPRF(R2),PRFDUPLN .  FLAG ORIG AS DUPL
         MVC   PRFNTXT(3),PRFNTXT-IEDQPRF(R2)
         L     R9,PRFTIC-IEDQPRF(R2)    ADDR OF NEXT UNIT
         ST    R9,AVTDOUBL
         MVI   AVTDOUBL,AVTEZERO .      CLEAR HI BYTE
         NC    PRFTIC+1-IEDQPRF(3,R2),PRFTIC+1-IEDQPRF(R2)
         BNZ   DUPLCNT
         MVC   AVTDOUBL+1(3),PRFNTXT-IEDQPRF(R2)
        L     R9,AVTDOUBL
         LTR   R9,R9 .                  IS THE COUNT TO BE UPDATED
         BCR   8,R1 .              BR NO - NO TIC TO COUNT IN
DUPLCNT  EQU   *
*       IF THE DUPL HDR HAS A SINGLE UNIT, THE COUNT OF HEADERS
*       WILL BE KEPT IN THE TIC FIELD OF HTE NEXT TEXT
*        SEGMENT.  IF IT HAS MORE THAN ONE UNIT, THE COUNT WILL
*        BE IN THE TIC OF THE FIRST UNIT
*        IF THERE IS ONE UNIT AND ONE SEGMENT, NO COUNT IS NEEDED.
         CLI   PRFTIC-IEDQPRF(R9),X'FF' .  DO NOT ALLOW MORE THAN
         BE    RCQCB .                  255 DUPL ENTRIES IN CORE Q
        BR    R1 .                     RETURN
INCRCNT EQU   *
         L     R9,AVTDOUBL .            ADDR OF UNIT TO PUT CNT IN
         LTR   R9,R9 .             IS ONE THERE
         BCR   8,R1 .              BR IF NO COUNT
         IC    R14,PRFTIC-IEDQPRF(R9)   COUNT OF DUPL HDRS
         LA    R14,1(0,R14)
         STC   R14,PRFTIC-IEDQPRF(R9)
         BR    R1
COREDUPL EQU   *
         BAL   R1,INCRCNT .             COUNT THIS DUPL HDR
         MVC    DATFLAGS-IEDQDATA(AVTUMALN,R5),DATFLAGS-IEDQDATA(R2)
*        COPY TIC, FLAGS AND LINK FIELD
         XC    DATFEFO-IEDQDATA(3,R5),DATFEFO-IEDQDATA(R5)
*        CLEAR FEFO PRT
         LH    R9,AVTKEYLE
         BCTR  R9,0                     ADJUST COUNT
         EX    R9,DUPLTRAN
         LH    R9,PRFDEST .             COPY THE DEST FOR THIS
         STH   R9,PRFDEST-IEDQPRF(R5) . DUPL HDR
         IC    R9,PRFCORE-1-IEDQPRF(R5)
         ST    R5,PRFCORE-1-IEDQPRF(R5)
         STC    R9,PRFCORE-1-IEDQPRF(R5)
         ST    RPREFIX,AVTDOUBL+4       SAVE ADDR OF ORIGINAL BFR
         LR    RPREFIX,R5               USE R6 FOR SUBROUTINE
         BAL   R9,ASSIGN1A
         BAL   R9,SETFEFO1
         L     RPREFIX,AVTDOUBL+4       RESTORE R6
RCQCB    EQU   *
         MVI   PRFTIC,X'08'
         L     R2,LCBRCQCB
         MVI   PRFPRI,PRIRCQCB          SET FOR POST
         BAL   R14,POST
         LA    R14,RETURN
.H009    ANOP
FINDSTCB EQU   *
         L     RSCB,LCBSCBA-1 .         RESTORE SCB              A42400
         L     RQCB,SCBDESTQ-1 .        RESTORE QCB              A42400
FINDSTCA EQU   * .                                               A42400
         LTR   R12,R12                 COPY CALL
         BCR   4,R14                    BR IF COPY
         TM    QCBFLAG,QCBTSSES         TERMINAL IN TIME-SHARING S22029
*                                       SESSION                  S22029
         BCR   7,R14                    YES, DO NOT SEND ANY MSG S22029
*                                       TRAFFIC AT THIS TIME     S22029
         L    R11,AVTEA
         CLI   QCBSTPRI,CLOCK .         CLOCK OPTION             S22026
         BCR   8,R14 .                  BRANCH IF YES            S22026
         CLI   QCBSTPRI,INTVL .         INTVL OPTION             S22026
         BNE   CONTINUE .               BRANCH IF NO             S22026
*        IF THE STCB PRTY IS X'70' CHECK IF THE QCB IS IN THE TIME
*        QUEUE.  IF YES - REMOVE THE QCB AND PUT THE SEND SCHED.
*        BACK INTO THE QCB STCB CHAIN.
         BAL   R10,CKDELAYQ
CONTINUE EQU   *
         STM   R0,R15,AVTSAVE4          SAVE CALLERS REGISTERS  SA61768
*        SAVE AREAS USED AT THIS POINT ARE                      SA61768
*        19R4 - AVTSAVE3+4, R14                                 SA61768
*        IEDQHG - AVTSAVE2 ( IN R13) +C FOR R14 - R12           SA61768
*        PRIOR TO THIS APAR FIX, IEDQFA USED AVTSAVE4+10 FOR    SA61768
*        REGS 0 - 15, IGG019RP USED AVTSAVE3+36 FOR REGS 3-12   SA61768
CONTINU2 EQU   *                                                SA61768
         LA    R15,QCBSTCHN-1           IS THE SCHEDULER THE
         L     RSCB,QCBSTCHN-1          FIRST STCB ON THE QCB
         LA    RSCB,0(RSCB)             STCB CHAIN
         CLR   RSCB,R15                 BR NO
         BNE   CHKCONC .               TO CHECK IF CONC          S22026
         SR    R15,R15
         IC    R15,QCBSTVTO             OFFSET TO THE SCHEDULER
         AR    R15,R15                  DOUBLED TO GET TO THE
         L     RSCB,AVTDISP-24(R15)     ADDRESS IN THE DISP V CON
         SH    RSCB,AVTHA2              TABLE - HALF WORD BEFORE
         LH    R15,0(RSCB)              THE CODE IS THE OFFSET
         AR    R15,RSCB                 TO THE SUBROUTINE TO FIND
         BALR  R14,R15
RESET    EQU   *
         LM    R0,R15,AVTSAVE4          RESTORE REGISTERS       SA61768
         BR    R14
CHKCONC  CLI   QCBSTVTO,CONC .         CONCENTRATOR              S22026
         BNE   RESET .                 BRANCH IF NO              S22026
         TM    QCBDSFLG,QCBDRQQ .      DRQ                       S22026
         BO    RESET .                 BRANCH IF YES             S22026
         LH    RSCB,QCBEXTO .          EXT OFFSET                S22026
         AR    RSCB,RQCB .             QCBE ADDRESS              S22026
         L     RSCB,QCBECONC-1-IEDQQCBE(RSCB) TRM ENTRY          S22026
         L     RQCB,TRMDESTQ-1-IEDQTRM(RSCB) .DRQ ADDRESS        S22026
         B     CONTINU2                 CONTINUE                SA61768
CKDELAYQ EQU   *
         TM    QCBINSRC+2,QCBDELAY      QCB IN DELAY Q
         BCR   14,R10                   BR NO
         TM    QCBSTAT,QCBTRMHO         TERMINAL HELD           SA59006
         BCR   1,R10                    BR YES, RETURN          SA59006
         TM    AVTBIT2,AVTREUSN         REUSABILITY RUNNING     OX05307
         BNO   CONT                     BR NO, FORGET IT        OX05307
         LA    R1,IEDQQCB+QCBMSIZE-QCBPSIZE INIT FOR SCAN LOOP  OX05307
SCANLOOP EQU   *                                                OX05307
         LA    R1,QCBPSIZE(0,R1)        NEXT PRIORITY QCB       OX05307
         OC    QCBFFEFO-IEDQPQCB(3,R1),QCBFFEFO-IEDQPQCB(R1)    OX05307
*                                         UNSENT FEFO MESSAGE   OX05307
         BNZ   CONT                     BR YES, CONTINUE        OX05307
         CLI   QCBPRIPQ-IEDQPQCB(R1),AVTEZERO LAST PRIORITY QCB OX05307
         BNE   SCANLOOP                 BR NO, CONTINUE         OX05307
         BR    R10                      BR RETURN               OX05307
CONT     EQU   *                                                OX05307
         ST    R14,AVTPARM3             SAVE RETURN ADDRESS    @SA74250
         LR    R1,RQCB                  QCB ADDR TO REMOVE
         L     R15,AVTHG02              ADDR OF SUBRTN
         BALR  R14,R15
         NI    QCBINSRC+2,X'FF'-QCBDELAY
         LA    R15,QCBSTCHN-1           ADDR OF SEND SCHED
         MVC   QCBSLINK(3),QCBSTCHN     MOVE THE STCB LINK FIELD
         IC    R1,QCBSTCHN-1            PUT SEND SCH BACK IN CHAIN
         ST    R15,QCBSTCHN-1
         STC   R1,QCBSTCHN-1
         L     R14,AVTPARM3             SAVE RETURN ADDRESS    @SA74250
         BR    R10                      RETURN
         AIF   (&A NE 3).H010
SWAPNOT  EQU  *
         BAL    R10,NOSWAP
         B    UNITQUE
SETDISK  EQU   *
          TM     QCBDSFLG,QCBREUS+QCBNREUS
*                        IS THIS A CORE ONLY QCB
         BCR   8,R9
         MVC   PRFCRCD(3),QCBDNHDR
         TM    PRFSTAT1,PRFNHDRN
         BZ    DISKALL .                YES, WE'RE SET           S22025
         MVC   PRFCRCD(3),SCBDNSEG .    SEG DISK ADDRESS OF THIS BFR
DISKALL   EQU   *
         L     RPREFIX,AVTDOUBL+4
         B     QUEUE1
.H010    AIF   (&A EQ 2).H011
QUEUNITS EQU   *
         ST    RPREFIX,AVTDOUBL
         LTR   R12,R12
         BM    OUTSZE
         LH    R9,PRFSIZE
COMPSZE  EQU   *
         CH    R9,AVTKEYLE
         BNH   FIXSZE .                 BR IF END IN THIS UNIT
        SH    R9,AVTKEYLE
        L    R6,PRFTIC
         XC    1(7,R6),1(R6)            CLEAR DATA AREA
         MVI   DATFLAGS-IEDQDATA(R6),DATNPRFX
         B      COMPSZE
FIXSZE   EQU   *
         STC    R9,DATCOUNT-IEDQDATA(RPRF)
OUTSZE   EQU   *
         XC    PRFTIC+1(3),PRFTIC+1
         L    R6,AVTDOUBL
         XC    PRFKEY(6),PRFKEY .       ZERO IF SINGLE UNIT
         XC    PRFNTXT(3),PRFNTXT       CLEAR THE NTXT OF BFR
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER
         BNO   COREHDR                  BR YES
         MVI   PRFTIC,AVTEZERO
         MVC   PRFCHDR(3),SCBCCHDR      CORE CURR HDR ADDR
         MVC   PRFCORE(3),AVTDOUBL+1
         MVC   PRFCRCD(3),PRFCORE       SET CURRENT RECORD IN   SA52971
*                                         CASE MAIN ONLY        SA52971
         MVC   AVTDOUBL+1(3),SCBCLSEG   ADDR OF PREV. TXT
         L     R9,AVTDOUBL
         MVC    PRFNTXT-IEDQPRF(3,R9),PRFCORE
*                         ADDR OF THIS RECORD IN PREVIOUS UN TXT
         MVC    SCBCLSEG(3),PRFCORE
         BR    R10
CNTUNITS EQU *
         SR    R14,R14
         IC    R14,PRFNBUNT             MO. OF UNITS IN TGUS BFR
         TM    PRFSTAT1,PRFDUPLN
         BNO   UNITCNT
         LR    R10,R9 .                 SAVE RTN ADDR
         BAL   R1,DUPLCORE .            FIND THIS ORIG. HDR
         LR    R9,R10 .                 RESTORE ADDR
         LA    R14,1
UNITCNT  EQU   *
         SR    R5,R5 .                  CLEAR TO INITIALIZE
         SR    R0,R0 .                  CLEAR TO INITIALIZE
         L     R10,AVTCADDR             NO. UNITS USED IN COREQ@OY12977
         TM    QCBSTAT,QCBTRMHO         TERM HELD              @ZA06194
         BO    TOOMANY                  BR, YES                @ZA06194
         AR    R10,R14                  + NO. IN THIS BFR
        C   R10,AVTTOTNC
         BH    TOOMANY                  NO. OF CORE UNITS-BR YES
          C    R10,AVTCMAX
         BL    NOBITSET
         OI    AVTSYSER,AVTCMAXN        SET CORE MAX
         AIF   (&A EQ 1).H012A
*        IF DISK BACKUP - STOP CORE QUEUEING WHEN OVER CORE
*        MAX TO PREVENT FILLING TJE CORE QUEUE
         TM    QCBDSFLG,QCBREUS+QCBNREUS
         BM    TOOMANY .                BR IF DISK BACKUP
.H012A   ANOP
NOBITSET EQU   *
         ST    R10,AVTCADDR .           SET NEW NO. USED FROM CORE Q
         C     R10,AVTCMIN
         BL    GETUNITS
         NI    AVTSYSER,AVTCMINF
GETUNITS  EQU  *
         LR    R11,R14 .                SAVE NUMBER GOTTEN
TAKEUNIT EQU   *
        NC    AVTBFREB+1(3),AVTBFREB+1
        BZ    ALLGONE
         L     R5,AVTBFREB
         MVC   AVTBFREB+1(3),PRFLINK-IEDQPRF(R5)
*                        REMOVE THE FIRST UNIT
         ST    R0,PRFTIC-IEDQPRF(R5)    LINK THIS UNIT TO PREV. ONE
         LR    R0,R5                    GET NOE PREV. UNIT
         BCT   R14,TAKEUNIT             WHEN THE NECESSARY UNITS
*                                       HAVE BEEN REMOVED, SET UP
*                                       TO POST THEM AS ONE UNIT
         IC    R14,PRFNBUNT        PUT NUMB OF UNITS
         STC   R14,PRFNBUNT-IEDQPRF(R5)   IN NEXT BFR
         XC    PRFLINK-IEDQPRF(3,R5),PRFLINK-IEDQPRF(R5)
         LH    R0,AVTAVFCT .            SUBTRACT THE NO. OF UNITS
*        TO BE USED FROM THE AVAILABLE COUNT
         SR    R0,R11 .                 DECR NO GOTTEN
         STH   R0,AVTAVFCT
         BR    R9
ALLGONE  EQU   *
         LA     R5,0(0,R5)
         LTR   R5,R5
         BZ    TOOMANY
         MVC   PRFLINK-IEDQPRF(3,R5),AVTBFREB+1
         ST    R5,AVTBFREB
        L     R5,PRFTIC-IEDQPRF(R5)
         B     ALLGONE
         AIF   (&A EQ 1).H012
NOSWAP   EQU   *
         LH    R2,AVTKEYLE
         XC    0(8,R5),0(R5)            CLEAR FIRST 8 BYTES OF BFR
         ST    RPRF,AVTDOUBL+4
         LR    R0,R5
         LR    R9,RPRF
         BCTR    R2,0                    FOR EXECUTE INSTR.
         SR    RPRF,RPRF                CLEAR
         IC    RPRF,PRFNBUNT-IEDQPRF(R9)     UNITS IN BFR
MOVE     EQU   *
         EX    R2,TRANSFER
         BCT   RPRF,NXTUNT              DECR UNIT CNT
         LR    RPREFIX,R0
          BR    R10
NXTUNT   EQU   *
         L     R5,PRFTIC-IEDQPRF(R5)
         L     R9,PRFTIC-IEDQPRF(R9)
         B     MOVE
.H012    ANOP
SETFEFO EQU   *
         LTR    R12,R12                 CALLED BY REUS
         BCR    8,R9                    YES - RETURN
         LR    R2,R9 .                  SAVE RTN ADDR
         BAL   R9,LOCKMSG
         B     INITFEFO .               IF RTN HERE - NOT LOCK MSG
*        SO THE FEFO PRTR SHOULD BE SET
         LR    R9,R2 .                  RESTORE RTN ADDR
         BR    R9 .                     RETURN - NO FEFO PTR ON LOCK
INITFEFO EQU   *
         LR    R9,R2 .                  RESTORE RTN ADDR
         BAL   R14,SENDINIT
         B     SETFEFO1
         BR    R9
SETFEFO1 EQU   *
         MVC    AVTDOUBL+1(3),PRFCORE
         TM    PRFSTAT1,PRFNHDRN        THIS A HDR
         BNO   RECOK                    BR YES
         MVC   AVTDOUBL+1(3),PRFCHDR    NEW HDR ADR
RECOK    EQU   *
*        IF ONLY 1 THERE THE MSG COULD HAVE BEEN READ BUT THE
*        FEFO PTR MAY NOT BE UPDATED YET , SO THE SCB IN WHICH
*        THE FEFO PTR WAS SAVED MUST BE UPDATED.
         STM   R14,R12,AVTSAVE2+12 .    SAVE REGS               SA52971
         LA    R9,QCBLFEFO .            SET HM03 INPUT          SA52971
         LA    R10,AVTDOUBL+1 .           PARAMETERS            SA52971
         BAL   R14,IEDQHM03
         LM    R14,R12,AVTSAVE2+12 .    RESTORE REGS            SA52971
         LH    R2,QCBLFEFO              KEEP ADDR OF
         SLL   R2,8                     LAST COMPLETELY RCVD MSG
         IC    R2,QCBLFEFO+2
         MVC   QCBLFEFO(3),AVTDOUBL+1
         NC    QCBFFEFO(3),QCBFFEFO     IS THERE ONE FEFO MSG
         BZ    FIRSTFFO
         MVC   DATFEFO(3),QCBLFEFO
         BR   R9
FIRSTFFO EQU   *
         MVC   QCBFFEFO(3),QCBLFEFO
         BR    R9
MSGCNT   EQU   *
         LH    R5,QCBMSGCT .            MSGCNT HAS ALREADY BEEN
         BCTR   R5,0                    CHANGED FOR THIS HDR
         STH   R5,QCBMSGCT .            FIX IT BACK
         B     RCQCB
TOOMANY  EQU   *
         LTR   R12,R12 .                REUS OR COPY
         BM    4(R9) .                  RETURN AT +4
         TM    QCBDSFLG,QCBREUS+QCBNREUS ANY DISK               SA61085
         BM    NOTLOST .                BRANCH IF YES           SA61085
         OI    SCBERR3,SCBLOSTN .       SET LOST MSG
NOTLOST  EQU   *                                                SA61085
         SR    R10,R14
         ST    R6,AVTDOUBL+4            SAVE BFR ADDR
         TM    PRFSTAT1,PRFNHDRN
         BO    NOTHDR
         TM    PRFSTAT1,PRFDUPLN .      DUPL HDR - NO QUEUEING
         BO    MSGCNT .                 BR IF DUPL TO DECR MSG COUNT
         AIF   (&A EQ 1).H013
         TM    PRFSTAT1,PRFERMGN .      IS THIS AN ERRORMSG
         BO    NOCLR .                  BR IF ERR MSG
CLEARCOR EQU   * .                                               A51776
         XC    SCBCCHDR(3),SCBCCHDR .   CHEAR FOR NOQUEUE
NOCLR    EQU   *
          TM     QCBDSFLG,QCBREUS+QCBNREUS
         BM    QUEUE1
.H013    ANOP
         XC    SCBMRFSD(4),SCBMRFSD     STOP MULT RT AND DLIST
         BAL   R14,SENDINIT
         B     NOHMSG
         OI    DATFLAGS-IEDQDATA(R6),DATINITL .  SET INIT LOST
NOHMSG   EQU   *
         LA    R10,1(R10)
         C     R10,AVTTOTNC             IF ONE UNIT IS AVAILABLE
*              THE RCV. SWITCH SHOULD NOT BE SET
         BNH   UNITHERE                 BR IF AVAILABLE
         OI    AVTBIT3,AVTRECVN         SET RCV SW.
UNITHERE EQU   *
         SR    R5,R5                    CLEAR TO SET NO BUF RET@SA72466
         ST    R5,AVTDOUBL+4            SET NO BFR TO RETURN   @SA72466
         MVC   AVTDOUBL+4(1),PRFSTAT1   SAVE PRFSTAT           @SA72466
         SR    R9,R9                    CLEAR REGISTER         @OY14492
         IC    R9,PRFNBUNT              NO. OF UNITS IN BUFFER @OY14492
         AR    R10,R9                   SET TO CADDR+NBUNT     @OY14492
         BCTR  R10,R0                   ACCOUNT FOR 1 UNIT     @OY14492
         ST    R10,AVTCADDR             UPDATE NO USED UNITS
         ST    R6,AVTDOUBL
         BAL   R9,ASSIGN1
         TM    PRFSTAT1,PRFCNCLN .      IS THIS TO BE CNCLD
         BNO   NOCNCL .                 BR NO
         OI    DATFLAGS-IEDQDATA(R6),DATCNCLD
NOCNCL   EQU   *
         XC    AVTDOUBL+1(3),AVTDOUBL+1
         LR    R2,R6
         B     FOUNDMSG                 GO FREE EXTRA UNITS    @SA72466
NOLOST   EQU   *
         ST    R6,AVTDOUBL+4            SAVE BFR ADDR
         MVC   AVTDOUBL+4(1),PRFSTAT1   SAVE PRFSTAT           @SA72466
         L     R2,AVTDOUBL
FOUNDMSG EQU   *
         MVC   AVTDOUBL+1(3),PRFNTXT-IEDQPRF(R2)
         XC    PRFNTXT-IEDQPRF(3,R2),PRFNTXT-IEDQPRF(R2)
         SR    R9,R9
         IC    R9,PRFNBUNT-IEDQPRF(R2)
         LH    R1,AVTAVFCT              COUNT OF AVAILABLE BFRS
         OI    DATFLAGS-IEDQDATA(R2),DATLOSTN
         NI    PRFSTAT1-IEDQPRF(R2),PRFNLSTF
         CLC   PRFSIZE-IEDQPRF(2,R2),AVTKEYLE
         BNH   SIZEOK
         MVC   PRFSIZE-IEDQPRF(2,R2),AVTKEYLE
SIZEOK   EQU   *
         MVI   PRFNBUNT-IEDQPRF(R2),X'01'
*              SET NB UNITS TO ONE IN BFR TO LEAVE
         BCT   R9,NEXTUNIT
         XC    PRFTIC+1-IEDQPRF(3,R2),PRFTIC+1-IEDQPRF(R2)
NEXTBFR  EQU   *
         NC    AVTDOUBL+1(3),AVTDOUBL+1
         BZ    LASTBFR
         L     R5,AVTDOUBL
         IC    R9,PRFNBUNT-IEDQPRF(R5)       NO UNITS IS BFR
         MVC   AVTDOUBL+1(3),PRFNTXT-IEDQPRF(R5)
         B     FREEUNIT
NEXTUNIT EQU   *
         L     R5,PRFTIC-IEDQPRF(R2)
         XC    PRFTIC+1-IEDQPRF(3,R2),PRFTIC+1-IEDQPRF(R2)
FREEUNIT EQU   *
         MVC    PRFLINK-IEDQPRF(3,R5),AVTBFREB+1
         ST    R5,AVTBFREB
         LR    R2,R5
         BCTR  R10,R0
         LA    R1,1(R1)                 INCREASE COUNT FOR NO. RTNED
         BCT   R9,NEXTUNIT
         B     NEXTBFR
LASTBFR  EQU   *
         MVC   SCBCLSEG(3),SCBCCHDR .   SET LAST SET (ALSO       A47137
*        SCBSCHDR) TO THE LAST SEGMENT ON THE QUEUE              A47137
         STH   R1,AVTAVFCT              RESTORE COUNT OF BFRS
         ST    R10,AVTCADDR
         AIF   (&A EQ 1).H0013A
         TM    QCBDSFLG,QCBREUS+QCBNREUS
         BM    QUEUE1
.H0013A  ANOP
         B     CKLAST
NOQUEUE   EQU   *
         BM    NOQUEUE1 .               BR NOT INIT LOST
         TM    PRFSTAT1,PRFNLSTN .      IS THIS THE LAST BFR
         BNO   NOTHDR .                 BR IF LAST BFR
NOQUEUE1 EQU   *
         ST    R6,AVTDOUBL+4
         MVC   AVTDOUBL+4(1),PRFSTAT1   SAVE PRFSTAT           @SA72466
         MVC    PRFCHDR(3),SCBCCHDR
         AIF  (&A EQ 1).H0013B
         BAL   R9,SETDISK
.H0013B  ANOP
CKLAST   EQU   *
        LA    R6,0(R6)
         LTR    R6,R6
         BZ    RTNBFR
         TM    AVTDOUBL+4,PRFNLSTN      WAS THIS LAST          @SA72466
         BO    TESTLAST                 BR IF NO               @SA72466
         LTR   R12,R12
         BM    TESTLAST                 BR IF YES              @SA72466
         BAL   R9,SETFEFO
         BAL   R14,FINDSTCB
         BAL   R14,SAMELAST
         NI    LCBSTAT1,LCBINITF .      SET INIT MODE OFF
         NI    LCBINSRC+2,X'FF'-XXXON . SET INSRC FLAG OFF
TESTLAST EQU   *                                               @SA72466
         L     R6,AVTDOUBL+4            RESTORE ADDR OF BUFFER @SA72466
*                                         TO FREE              @SA72466
         LA    R6,0(R6)                 CLEAR HIGH ORDER BYTE  @SA72466
RTNBFR   EQU   *
         L     R11,AVTEA                DISP ADDR
         LTR   R6,R6
         BZ    DSPDISP                  BR NO
         MVI    PRFPRI,PRIBFRTB
          LA    R2,AVTBFRTB
          B     NRPOST
NOTHDR   EQU   *
         MVC   AVTDOUBL+1(3),SCBCCHDR
         MVC   PRFCHDR(3),SCBCCHDR
         BAL   R14,SENDINIT
         B     NOLOST
         L     R2,AVTDOUBL .            ADDR OF HDR
         OI    DATFLAGS-IEDQDATA(R2),DATLOSTN+DATINITL
*        SET INIT LOST
         TM    PRFSTAT1,PRFNLSTN .      LAST BFR
         BO    RTNBFR .                 BR NOT LAST - DO NOT QUEUE IT
         OI    LCBERBST-IEDQLCB(R1),XXHMSG .  SET EOM HERE
         L     R1,LCBSCBA-1-IEDQLCB(R1) .    DEST SCB
         OI    SCBERR3-IEDQSCB(R1),SCBLOSTN .  SET MSG LOST
CKBFR    EQU   *
         NC    AVTBFREB+1(3),AVTBFREB+1 .   ANY BFRS
         BZ    INITNOBF .               BR NO
         L     R5,AVTBFREB .            UNIT ADDR
         MVC   AVTBFREB+1(3),PRFLINK-IEDQPRF(R5) .   DELINK VFR
         MVI   PRFNBUNT-IEDQPRF(R5),X'01' .   SET NO UNITS
         LH    R1,AVTAVFCT .            DECR COUNT OF AVAIL BFRS
         BCTR  R1,0
         STH   R1,AVTAVFCT
         SR    R2,R2                    CLEAR 2
         L     R1,AVTCADDR .            NO. OF CORE UNITS USED
         IC    R2,PRFNBUNT .            NO THIS BFR
         AR    R1,R2 .                  NEW TOTAL USED
         ST    R1,AVTCADDR
         NI    PRFSTAT1,PRFNLSTF
         B     INITRTN
INITNOBF EQU   *
         L     R1,LCBINSRC-1 .          DEST LCB ADDR
         TM    LCBERBST-IEDQLCB(R1),LCBRDERR
*        NOW WAITING FOR THIS BFR TO SEND
         L     R1,LCBSCBA-1-IEDQLCB(,R1) DEST SCB
         BNO   SETLAST .                BR NOT WAITING FOR THIS BFR
         MVC   SCBCORE-IEDQSCB(3,R1),SCBCLSEG RESET LAST BFR
SETLAST  EQU   *
         L     R1,SCBCLSEG-1 .          ADDR FOR LAST BFR
         NI    PRFSTAT1-IEDQPRF(R1),PRFNLSTF .  SET EON
         B     RTNBFR
.H011    ANOP
         TITLE '''IEDQHM'' - INTER-MODULE SUB-ROUTINES' .        S22025
*
*
*        ENTRY HERE FROM REUS - COPY AND CKPT-RESTART
*
*
         DC    A(IEDQHM03)
IEDQHM02 EQU   *
         USING *,R15
         STM   14,12,12(R13)
         L    R12,BASE
         DROP  R15
*        REUS, COPY, AND RESTART ALL SET UP PQCB
         AIF   (&A NE 3).H014
         TM    QCBDSFLG,QCBREUS+QCBNREUS
*                   IS THES A CORE ONLY QCB
         BM    QUEUE1                   BR NO
.H014    AIF   (&A NE 2).H014A
         B     QUEUE1
         AGO   .H015
.H014A   ANOP
         ST    R6,AVTDOUBL+4 .          SAVE PASSED UNIT
         XC    PRFTIC(4),PRFTIC .       CLEAR TIC FIELD
         TM    DATFLAGS-IEDQDATA(R6),DATNPRFX
         BNO   SETNBUNT                 BR IF UNIT HAS PRFX
         L     R6,SCBCLSEG-1            ADDR LAST PRFX PASSED
         B     NOMVI                    BR NOT TO SET NBUNT
SETNBUNT EQU   *
         MVI   PRFNBUNT,X'01' .         SET 1 UNIT IN BFR
NOMVI    EQU   *
         LA    R14,1
         BAL   R9,UNITCNT
         B     TMDAT .                  HAVE A UNIT TO USE
         BCTR  R10,0 .                  DECRMENT  MSUNIT COUNT
         ST    R10,AVTCADDR .           UPDATE
         ST    R5,AVTPARM
         L     R2,SCBCCHDR-1 .          HDR THIS MSG
         L     R10,AVTCADDR .           NO MSUNITS USED
         L     R1,AVTDOUBL+4 .          ADDR OF PASSED UNIT
         TM    DATFLAGS-IEDQDATA(R1),DATNPRFX
         BO    FOUNDMSG .               BR NO PRFX
         TM    PRFSTAT1,PRFNHDRN .      THIS A HDR
         BO    FOUNDMSG .               BR NOT HDR
.H015    ANOP
COPYRTN  EQU   *
         L     R5,AVTPARM
         LTR   R15,R5                   PASS BUFFER ADDR TO 19RP
         L     R14,12(13,0)             RESTORE 14
         LM    0,12,20(13)              RESTORE REUS REGISTERS
         BR    R14                      RETURN
         AIF   (&A EQ 2).H017
TMDAT    EQU   *
         ST    R5,AVTPARM
         L     R2,AVTDOUBL+4 .          ADDR OF PASSED UNIT
         TM    DATFLAGS-IEDQDATA(R2),DATNPRFX  PRFX IN TUNIT
*                  DOES THIS UNIT JAVE A PRFFIX
         BNO   UNITQUE .                BR IF PRFX THIS UNIT
         IC    R1,PRFNBUNT .            ADD 1 TO NBUNT OF LAST BFR
         LA    R1,1(R1)
         STC   R1,PRFNBUNT .
CHECKTIC EQU   *
         NC    PRFTIC+1(2),PRFTIC+1
         BZ    NOTIC
         L    R6,PRFTIC
         B    CHECKTIC
NOTIC    EQU    *
         ST    R2,PRFTIC                LINK TO PREV UNIT
         MVI    PRFTIC,X'00'
         B     COPYRTN
         AIF   (&A EQ 1).H016
TRANSFER EQU   *
         MVC    PRFSUNIT-IEDQPRF(0,R5),PRFSUNIT-IEDQPRF(R9)
.H016    ANOP
DUPLTRAN EQU   *
         MVC   PRFSUNIT-IEDQPRF(0,R5),PRFSUNIT-IEDQPRF(R2)
.H017    ANOP
COMPRI   CLI   QCBPRLVL,X'00'
         DS    0F
BASE     DC    X'80'
         DC    AL3(DESTSCH)
ROUND    DC    F'3' .                   CONSTANT VALUE           S21101
CLEAROFF DC    X'00FFFFFC'
ABCODE4  EQU   X'04' .                  COPY NOT PRESENT IN SYSTEM
ABCODE7  EQU   X'07' .                 MSG WRAPPED TOE DISK
TM       TM    SCBCQT,X'00'
OI       OI    SCBCQT,X'00'
XTIC     EQU   X'08'                    TIC OP CODE
DIALPRTY EQU   X'40'                    PRTY OF DIAL SCHEDULER
         EJECT
IEDQHM03 DS    0H
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
                        ENTRY IEDQHM03
*
* QUESTION - IS THE MSG POINTED TO BY FFEFO THE ONLY MSG ON THIS Q --
* AND IS THAT ONE MSG BEING SENT -- IF SO, WHERE IS THE SCB --
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* INPUT - R14 RETURN ADDRESS
*         R7  MASTER QCB
*         R8  PRIORITY QCB
*        R13 AVTSAVE2
*
*OUTPUT - IF THE ONLY MSG IS BEING SENT, RETURN TO R14+4, SCB IN R2.
*     IF OTHERWISE, RETURN TO R14.  THIS COULD BE FOR TWO REASONS --
*     A. IF FIRST MSG WAS NOT BEING SENT, RETURN TO R14 (POSITIVE).
*     B. IF THE FIRST MSG WAS BEING SENT, BUT WAS NOT THE ONLY
*        MSG ON THE QUEUE, RETURN TO R14 (NEGATIVE), SCB IN R2.
*
* WORK REGS MODIFIED - R0,R2,R15,R1
*
* SCRATCH AREA MODIFIED - AVTPARM3
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         LA    R14,0(,R14) .            FORCE POSITIVE
* IF ONLY 1 MSG THERE,THE MSG COULD HAVE BEEN READ BUT THE FEFO PTR
* MAY NOT BE UPDATED YET, SO THE SCB IN WHICH THE FEFO PTR WAS
* SAVED MUST BE UPDATED BY THE CALLER.
*
         TM    QCBFLAG,QCBSDFFO .       IS THAT ONE MSG BEING SENT
         BCR   8,R14 .                    NO, NOT BEING SENT
*                                         YES, SOME MSG IS BEING
*                                           SENT, BUT IT MAY BE
*                                           FROM SOME OTHER PRIORITY
*                                           LEVEL OF THIS SAME QCB
         STM   R14,R12,AVTSAVE4+12      SAVE REGS               SA52971
         BALR  R15,0 .                  ESTABLISH ADDRESSABILITY
         USING *,R15 .                    FOR HM03 IN R15
         SR    R0,R0 .                  ZERO WORK REG           SA52971
         TM    QCBFLAG,QCBPROC .        IS QCB FOR PROCESS ENTRY
         BZ    FINDLCB .                NO, FIND CORRECT LCB    SA52971
         L     RSCB,QCBPREN .           GET TPROCESS ENTRY ADDR SA52971
         CLI   TRMSTAT+1-IEDQTRM(RSCB),AVTEZERO                @SA74018
*                                        PROCESS ENTRY CLOSED  @SA74018
         BNE   PROCGO                   BRANCH IF NO           @SA74018
         NI    QCBFLAG,AVTEFF-QCBSDFFO  SET NOT SENDING        @SA74018
         LM    R14,R12,AVTSAVE4+12      RESTORE REGS           @SA76309
         BR    R14                      RETURN TO CALLER       @SA74018
PROCGO   EQU   *                                               @SA74018
         L     RSCB,TRMSTAT-IEDQTRM(,RSCB) GET PEWA ADDR        SA52971
         LA    RLCB,EOMSAVE-4-IEDQPEWA(,RSCB) SET FOR SAVED EOM SA52971
         LA    R2,(SCBFEFO-IEDQSCB)-(PRFSRCE-IEDQPRF) MAKE SAVEDSA52971
*                                         FEFO LOOK LIKE SCB    SA52971
         BAL   R14,APPBFR .             GO CHECK SAVED BFR      SA52971
         LA    RLCB,PERAQCB-4-IEDQPEWA(,RSCB) SET FOR READ-     SA52971
*                                         AHEAD QUEUE           SA52971
         TM    LCBSTAT1-LCBCHAIN(RSCB),LCBRCLLN RETRIEVING      SA52971
         BNO   NORCL .                  NO, WE HAVE BFR CHAIN   SA52971
         LA    RLCB,PERAQCB-IEDQPEWA(,RSCB) GET CORRECT BFR CHN SA52971
NORCL    EQU   * .                                              SA52971
         LA    R2,(SCBFEFO-IEDQSCB)-(PRFQCBA-IEDQPRF) MAKE SAVEDSA52971
*                                         FEFO LOOK LIKE SCB    SA52971
         BAL   R14,APPBFR .             GO CHECK SAVED BFRS     SA52971
LOOPBGN  EQU   * .                                              SA52971
         CLC   PRFLINK-IEDQPRF(3,RLCB),AVTDELAD+1 IS THAT ALL   SA52971
         BE    LOOPEND .                YES, EXIT               SA52971
         LR    R1,RLCB .                SAVE PREVIOUS           SA52971
         L     RLCB,PRFLINK-1-IEDQPRF(,RLCB) GET NEXT           SA52971
         MVC   PRFQCBA-IEDQPRF(3,RLCB),PRFQCBA-IEDQPRF(R1) SET  SA52971
*                                         NEXT BFR WITH FEFO    SA52971
         B     LOOPBGN .                GO LOOK AT NEXT         SA52971
LOOPEND  EQU   * .                                              SA52971
         LA    R5,PESAVE+D30-IEDQPEWA(RSCB) MSG SAVED IN RETRVE@OX16398
         LR    R1,RSCB .                SAVE LCB ADDRESS        SA52971
         LA    RSCB,LCBCHAIN-IEDQLCB .                          SA52971
         SLR   R1,RSCB .                                        SA52971
         L     R2,LCBSCBA-1-IEDQLCB(,R1) SCB ADDRESS            SA52971
         TM    LCBSTAT1-IEDQLCB(R1),LCBRCLLN RETRIEVE          @OX16398
         BZ    TSTLEVEL                 NO                     @OX16398
         LA    R2,(PESAVE+D34-IEDQPEWA)-(SCBFEFO-IEDQSCB)(R1,RSCB)
*        SET PESAVE+34 TO LOOK LIKE SCBFEFO FOR LATER CODE     @OX16398
         B     FEFOCK                   CHECK TO UPDATE FEFO PT@OX16398
         SPACE 1 .                                              SA52971
FINDLCB  EQU   * .                                              SA52971
         L     R2,QCBDCBAD-1 .          GET ADDR OF DCB
         CLI   QCBSTVTO,DSPLOGSC        IS QCB FOR LOGTYPE       S22024
         BE    NOT3705                  BRANCH ON YES            S22024
         TM    DCBDSORG-IHADCB(R2),AVTE80 IS THIS AN LGB ?       S22024
         BNO   NOT3705                  NO,GO GET SCB ADDR.      S22024
         LR    R2,RQCB                  PUT QCB ADDR IN R2       S22024
         SH    R2,AVTHA4                BACK UP TO PLCB ADDR     S22024
         L     R2,QCBPLCBA-1-IEDNQCB(R2)  GET PLCB ADDRESS       S22024
         B     RITELCB                  EXIT TO COMMON CODE
NOT3705  EQU   *
         IC    R0,DCBEIOBX-IHADCB(,R2) .GET SIZE OF AN LCB
         ST    R0,AVTPARM3 .            SAVE SIZE OF AN LCB
         L     R2,DCBIOBAD-IHADCB(,R2) .GET PSEUDO IOB ADDR
         LA    R1,LCBFLAG1-IEDQLCB .    GET OFFSET INTO LCB OF IOB
         SR    R2,R1 .                  BACK UP TO PSEUDO LCB
         AR    R2,R0 .                  BUMP TO FIRST REAL LCB
         TM    LCBSTAT2-IEDQLCB(R2),LCBDIAL IS THIS A DIAL LCB
         BO    DIALCB .                   YES, DIAL
*                                         NO, NON-DIAL
         SR    R1,R1 .                  CLEAR WORK REG
         IC    R1,QCBRELLN .            GET REL. LINE NO.
         BCTR  R1,0 .                   ADJUST FOR ONE BUMP
*                                         ALREADY DONE
         MR    R1-1,R0 .                MULT. REL. LINE NO(-1) TIMES
*                                         LCB SIZE, GET OFFSET TO LCB
         AR    R2,R1 .                  BUMP TO PROPER LCB
         LR    R11,R2 .                 SAVE LCB ADDRESS        SA52971
         L     R2,LCBSCBDA-1-IEDQLCB(,R2) GET ADDR FIRST SCB
         SR    R0,R0 .                  CLEAR BOTH
         LR    R1,R0 .                    WORK REGS
         IC    R1,QCBSCBOF .            GET INDEX TO PROPER SCB
         IC    R0,AVTSCBSZ .            GET SIZE OF ONE SCB
         MR    R1-1,R0 .                MULT. NO. OF SCB TIMES SIZE
*                                         OF ONE SCB, GET OFFSET
         ALR   R2,R1 .                  BUMP ADDR OF FIRST SCB TOA42367
*                                         PROPER SCB FOR THIS QCB
         LR    RSCB,R2 .                SAVE SCB ADDR           SA52971
         TM    SCBQTYPE-IEDQSCB(R2),SCBCONC IS THIS A CONCEN    SA52971
         BZ    RELOAD .                 BRANCH IF NO            SA52971
         LR    R2,RQCB .                GET QCB ADDR            SA52971
         AH    R2,QCBEXTO .             GET QCB EXTENSION       SA52971
         LR    R12,R2 .                 SAVE PRIMARY ADDR       SA52971
         TM    QCBEFLG-IEDQQCBE(R2),QCBEOPL MSG,L QUEUE         SA52971
         BZ    RSETLCB .                NO, GO RESET LCB        SA52971
         LA    RLCB,IEDQPQCB-QCBPSIZE . GET 0TH PRI QCB         SA52971
EXTLOOP  EQU   * .                                              SA52971
         IC    R0,QCBELGTH-IEDQQCBE(,R2) GET EXTENSION LENGTH   SA52971
         AR    R2,R0 .                  GO TO NEXT EXTENSION    SA52971
         LA    RLCB,QCBPSIZE(,RLCB) .   POINT TO NEXT PRI QCB   SA52971
         CR    RLCB,RPQ .               IS THIS CORRECT ONE     SA52971
         BNE   EXTLOOP .                NO, LOOK AT NEXT        SA52971
         LR    R12,R2 .                 SAVE PRIMARY ADDR       SA52971
         LA    R5,(SCBFEFO-IEDQSCB)-(QCBEFEFO-IEDQQCBE) GET DECRSA52971
*                                         FOR DUMMY SCB         SA52971
         SLR   R2,R5 .                  MAKE FEFO PTR LOOK LIKE SA52971
*                                         IT'S IN SCB           SA52971
         LA    R5,((SCBFEFO-IEDQSCB)-(QCBEFEFO-QCBEHDR))(,R2) PTSA52971
*                                         TO HEADER ADDR        SA52971
         BAL   R14,FEFOCOMP .           GO CHECK MSG            SA52971
RSETLCB  EQU   * .                                              SA52971
         TM    QCBEFLG-IEDQQCBE(R12),QCBEPEND QACTION HERE      SA52971
         BZ    RELOAD .                 NO, WE'RE SET           SA52971
         LR    R2,RQCB                  GET QCB ADDR            SA58988
         AH    R2,QCBEXTO               CALC QCB EXTENSION      SA58988
         MVC   AVTSAVE4+1(3),QCBELCB-IEDQQCBE(R2)  RESET TO     SA58988
         L     R11,AVTSAVE4 .           DUMMY LCB IN QACTION    SA52971
RELOAD   EQU   * .                                              SA52971
         LR    R1,R11 .                 SET LCB ADDRESS         SA52971
         LR    R2,RSCB .                RESET SCB ADDR          SA52971
         B     TSTLEVEL .               GO CHECK SCB            SA52971
         SPACE 1 .                                              SA52971
DIALCB   EQU   * .                      R2 IS ADDR OF LCB
         LA    R0,QCBSTVTO .            GET ADDR OF MASTER QCB STCB
NXTDIAL  EQU   * .
         L     R1,LCBSTCBA-1-IEDQLCB(,R2) GET ADDR OF FIRST STCB ON
         TM    LCBSTCBA+2-IEDQLCB(TWO),ONE LINE STOPPED FOR    @YA06890
*                                          CLOSEDOWN           @YA06890
         BO    NXTDIAL2                 BRANCH IF YES          @YA06890
NXTDIAL1 EQU   *                                                OX02183
         LA    R1,0(,R1) .                STCB CHAIN OF LCB
         CR    R1,R0 .                  DOES 3RD WORD OF LCB POINT
*                                         TO 3RD WORD OF MASTER QCB
         BE    RITELCB .                  YES, GOT PROPER DIAL LCB
*                                         NO, TRY NEXT DIAL LCB
         CLI   FOUR(R1),AVTEZERO .      END OF STCB CHAIN       OX02183
         L     R1,FOUR(,R1) .           GET NEXT STCB ADDRESS   OX02183
         BNE   NXTDIAL1 .               BRANCH IF YES           OX02183
NXTDIAL2 EQU   *                                               @YA06890
         A     R2,AVTPARM3 .            BUMP BY SIZE OF LCB
         B     NXTDIAL .                MAYBE NEXT LCB IS THE ONE
*
RITELCB  EQU   * .                      FOUND DIAL LCB
         LR    R1,R2 .                  SET LCB ADDRESS         SA52971
         L     R2,LCBSCBA-1-IEDQLCB(,R2) GET SCB OF THIS DIAL LCB
         SR    R0,R0 .                  ZERO WORK REG           SA52971
TSTLEVEL EQU   * .                                              SA52971
         LA    R5,SCBSCHDR-IEDQSCB(,R2) GET HEADER              SA52971
         TM    SCBHBFNO-IEDQSCB(R2),QCBDISK+QCBCORE RECALLING   SA52971
         BZ    FEFOCK .                 NO, GOT POINTER         SA52971
         L     R1,LCBLSPCI-1-IEDQLCB(,R1) GET SERVICE BFR ADDRESSA52971
         LA    R1,AVTEZERO(,R1) .       ZERO HI-ORDER           SA52971
         LTR   R1,R1 .                  ONE THERE               SA52971
         BZ    EXIT .                   NO, EXIT ROUTINE        SA52971
         LA    R5,PRFCRCD-IEDQPRF(,R1) .ASSUME HDR; GET HDR ADDRSA52971
         TM    PRFSTAT1-IEDQPRF(R1),PRFNHDRN IS IT HEADER       SA52971
         BZ    FEFOCK .                 YES, GO CHECK IT        SA52971
         LA    R5,PRFCHDR-IEDQPRF(,R1) .GET HEADER'S RRN        SA52971
FEFOCK   EQU   * .                                              SA52971
         BAL   R14,FEFOCOMP .           GO CHECK SCBFEFO        SA52971
EXIT     EQU   * .                                              SA52971
         LR    R2,RPRF .                SET RETURN PARAMETER    SA52971
         LM    R14,R1,AVTSAVE4+12 .     RESTORE REGS            SA52971
         LM    RSCB,R12,AVTSAVE4+32 .   RESTORE REGS            SA52971
         BR    R14 .                    RETURN                  SA52971
         SPACE 1 .                                              SA52971
APPBFR   EQU   * .                                              SA52971
         CLC   PRFLINK-IEDQPRF(3,RLCB),AVTDELAD+1 IS ANYTHING   SA52971
*                                         THERE                 SA52971
         BCR   8,R14 .                  NO, RETURN              SA52971
         NC    PRFLINK-IEDQPRF(3,RLCB),PRFLINK-IEDQPRF(RLCB) IS SA52971
*                                         ANYTHING THERE        SA52971
         BCR   8,R14 .                  NO, RETURN              SA52971
         L     RLCB,PRFLINK-1-IEDQPRF(,RLCB) GET BFR ADDR       SA52971
         LA    R5,PRFCRCD-IEDQPRF(,RLCB) ASSUME HDR             SA52971
         TM    PRFSTAT1-IEDQPRF(RLCB),PRFNHDRN IS IT HDR        SA52971
         BNO   HDRRN .                  YES, THAT'S SET         SA52971
         LA    R5,PRFCHDR-IEDQPRF(,RLCB) SET HEADER PTR         SA52971
HDRRN    EQU   * .                                              SA52971
         SLR   R2,RLCB .                BACK UP TO APPEAR AS    SA52971
         LPR   R2,R2 .                    LCB                   SA52971
FEFOCOMP EQU   * .                                              SA52971
         AIF   (&A NE 3).H020 .                                 SA52971
         TM    L'SCBSCHDR-1(R5),CPBQTYPE CORE COPY              SA52971
         BNZ   DISKPTR .                NO, IT'S SET            SA52971
         NC    AVTEZERO(3,R5),AVTEZERO(R5) USABLE POINTER       SA52971
         BCR   8,R14 .                  NO, RETURN              SA52971
         MVC   AVTSAVE4+1(3),AVTEZERO(R5) ALIGN ADDR            SA52971
         L     R5,AVTSAVE4 .            GET ADDR                SA52971
         LA    R5,PRFCRCD-IEDQPRF(,R5) .POINT TO FEFO ADDR      SA52971
DISKPTR  EQU   * .                                              SA52971
.H020    ANOP  , .                                              SA52971
         CLC   AVTEZERO(3,R9),AVTEZERO(R5) THIS MSG TO UPDATE   SA52971
         BCR   7,R14 .                  NO, DON'T UPDATE        SA52971
         LR    RPRF,R2 .                SAVE SCB ADDRESS        SA52971
         OI    AVTSAVE4+12,AVTE80 .     SET 'FOUND' FLAG FOR RETSA52971
         LTR   R10,R10 .                ARE WE TO UPDATE        SA52971
         BCR   8,R14 .                  NO, THEN DON'T          SA52971
         MVC   SCBFEFO-IEDQSCB(3,R2),AVTEZERO(R10) SET FEFO     SA52971
         BR    R14 .                    RETURN                  SA52971
         TITLE '''IEDQHM'' - DSECTS' .                           S22025
*        TPRIOR
         TPRIOR
COPYPRI  EQU   PRIFEFO-1 .              BUF POSTED BY COPY       S21101
         EJECT
         TQCBED                                                  S22026
         EJECT , .                                               S22025
*        TDISPD
          TDISPD
         EJECT , .                                               S22025
         TDATAD
         EJECT , .                                               S22025
*        DCBD
         DCBD  DSORG=TX
DCBOPEN  EQU   X'10'
PCISEND  EQU   X'50' .                  SEND PCI=A/PCI=X         S22025
         EJECT , .                                               S22025
*        TPRFD
          TPRFD
XPRFREUS EQU   X'20' .                  REUSABLE DISK BUFFER     S22025
XPRFNRUS EQU   X'10' .                  PERMANENT DISK BUFFER    S22025
         EJECT , .                                               S22025
*        TLCBD
         TLCBD
LCBNSRCD EQU   X'01' .                  IN-SOURCE CHAIN FLAG     S22025
XXXON    EQU   LCBNSRCD .                                        S22025
XXHMSG   EQU   LCBMSG .                                          S22025
         EJECT , .                                               S22025
*        TPEWAD , .                                             SA52971
         TPEWAD , .                                             SA52971
         EJECT , .                                              SA52971
*        TTRMD
         TTRMD
         EJECT , .                                               S22025
*        TSCBD
         TSCBD
CPBQTYPE EQU   X'03' .                  QUEUE TYPE FLAGS         S22025
SCBCN    EQU   X'01'
SCBCQT   EQU   SCBHBFNO
SCBRDX   EQU   SCBMBSSA
SCBNDX   EQU   SCBMBSSA+4
SCBCC    EQU   X'02'
SCBCR    EQU   X'04'
         EJECT , .                                               S22025
*        TQCBD
         TQCBD
*        TAVTD , .                                               S22025
         TAVTD , .                                               S22025
         MEND
