* %      GOTO JSWABSL;
* /*******************************************************************/
* /*                                                                 */
* /* STATUS --                                                       */
* /*    RELEASE 20, MODIFICATION LEVEL 00                            */
* /*    LAST MODIFIED ON OCTOBER 6, 1970                            */
* /*    A 0-999999                                            S20029 */
* /*                                                                 */
* /* FUNCTION --                                                     */
* /*    DEFINE THE JOB SCHEDULING WORK AREA (JSWA) USED BY ROUTINES  */
* /*    OF THE JOB SCHEDULING SUBROUTINE                             */
* /*                                                                 */
* /* ENTRY POINTS --                                                 */
* /*         NONE                                                    */
* /*                                                                 */
* /* INPUT --                                                        */
* /*    NONE                                                         */
* /*                                                                 */
* /* OUTPUT --                                                       */
* /*    DATA CONSTANTS (ASSEMBLER) OR DECLARATIONS (BSL) DEFINING    */
* /*    THE JSWA                                                     */
* /*                                                                 */
* /* EXTERNAL REFERENCES --                                          */
* /*         NONE                                                    */
* /*                                                                 */
* /* EXITS, NORMAL --                                                */
* /*         NONE                                                    */
* /*                                                                 */
* /* EXITS, ERROR --                                                 */
* /*         NONE                                                    */
* /*                                                                 */
* /* TABLE/WORK AREAS --                                             */
* /*         NONE                                                    */
* /*                                                                 */
* /* ATTRIBUTES --                                                   */
* /*         NOT APPLICABLE                                          */
* /*                                                                 */
* /* NOTES --                                                        */
* /*    NONE                                                         */
* /*                                                                 */
* /*******************************************************************/
* /* START OF SPECIFICATIONS ****
* 1MODULE-NAME = IEFVJSWA
* 1DESCRIPTIVE-NAME = JOB SCHEDULING WORK AREA
* 1DESCRIPTION = MAPPING MACRO-INSTRUCTION FOR THE MVT JOB           -
*             SCHEDULING SUBROUTINE WORK AREA
* 1FUNCTION = WORK AREA TO PASS VARIOUS PARAMETERS BETWEEN THE       -
*             MODULES OF THE MVT JOB SCHEDULING SUBROUTINE
* 1MODULE-TYPE = TABLE
*   2PROCESSOR = ASSEMBLER AND BSL
*   2TABLE-SIZE = * BYTES
*   2FIELD = JSWALGTH
*     3DISPLACEMENT = 0 BYTES
*     3TYPE = ARITHMETIC
*       4MODE = BINARY
*       4LENGTH = 15 BITS
*       4SCALING = 0
*       4VALUE = LENGTH OF THE JOB SCHEDULING WORK AREA IN BYTES
*   2FIELD = JSWAOPT
*     3DISPLACEMENT = 2 BYTES
*     3TYPE = BIT STRING
*       4BITS-USED = BIT 1
*         5FUNCTION = INDICATE WHETHER A 'TERM=TS' OPERAND IS TO BE  -
*             PROCESSED OR WHETHER IT IS TO BE IGNORED
*         5VALUE = IF THIS BIT IS ON, THE OPERAND IS TO BE           -
*             PROCESSED.  THE DEFAULT IS TO IGNORE THE OPERAND.
*       4BITS-USED = BIT 2
*         5FUNCTION = INDICATE WHETHER TRACK STACKING IS TO BE       -
*             EMPLOYED DURING THE JOB SCHEDULING PROCESS
*         5VALUE = IF THIS BIT IS ON, TRACK STACKING IS EMPLOYED.    -
*             THE DEFAULT IS NOT TO USE TRACK STACKING.
*       4BITS-USED = BIT 3
*         5FUNCTION = INDICATE WHETHER DEDICATED WORKFILES ARE TO BE -
*             SUPPORTED
*         5VALUE = IF THIS BIT IS ON, DEDICATED WORKFILES WILL BE    -
*             SUPPORTED.  THE DEFAULT IS NOT TO SUPPORT DEDICATED    -
*             WORKFILES.
*       4BITS-USED = BIT 4
*         5FUNCTION = RESERVED
*         5VALUE = MUST BE ZERO TO PROVIDE COMPATIBILITY WITH LATER  -
*             EXTENSIONS TO THE JOB SCHEDULING SUBROUTINE.
*       4BITS-USED = BIT 5
*         5FUNCTION = INDICATE WHETHER THE INVOKER OF THE JOB        -
*             SCHEDULING SUBROUTINE PROVIDED A PROTECTION KEY TO BE  -
*             USED IF THE PROGRAM TO BE INVOKED WAS NOT NAMED IN THE -
*             LINKAGE TABLE.
*         5VALUE = IF THIS BIT IS ON, THE INVOKER OF THE JOB         -
*             SCHEDULING SUBROUTINE PROVIDED A PROTECTION KEY TO BE  -
*             USED. THE DEFAULT IS TO ACQUIRE A PROTECTION KEY       -
*             WITHIN THE INITIATOR SUBROUTINE IF THE PROGRAM(S)      -
*             NAMED DO NOT APPEAR IN THE LINKAGE TABLE.
*       4BITS-USED = BIT 6
*         5FUNCTION = INDICATE WHETHER ALLOCATION RECOVERY SHOULD BE -
*             ATTEMPTED OR WHETHER A JOB SHOULD BE AUTOMATICALLY     -
*             FAILED IF ALLOCATION RECOVERY WOULD OTHERWISE BE       -
*             NECESSARY.
*         5VALUE = IF THIS BIT IS ON, ANY JOB RUN BY THE JOB         -
*             SCHEDULING SUBROUTINE WILL BE INELIGIBLE FOR           -
*             ALLOCATION RECOVERY. THE DEFAULT IS TO ALLOW           -
*             ALLOCATION RECOVERY.
*       4BITS-USED = BIT 7
*         5FUNCTION = INDICATE WHETHER THE JOB(S) RUN BY THE JOB     -
*             SCHEDULING SUBROUTINE ARE TO BE ELIGIBLE TO WAIT FOR   -
*             SYSJOBQE SPACE.
*         5VALUE = IF THIS BIT IS ON, THE JOB(S) ARE NOT ELIGIBLE TO -
*             WAIT FOR SYSJOBQE SPACE.  THE DEFAULT IS THAT JOBS ARE -
*             ELIGIBLE.
*       4BITS-USED = BIT 8
*         5FUNCTION = INDICATE WHETHER THE JOB(S) RUN BY THE JOB     -
*             SCHEDULING SUBROUTINE ARE ELIGIBLE TO WAIT FOR THE     -
*             AVAILABILITY OF DATA SETS.
*         5VALUE = IF THIS BIT IS ON, THE DATA SETS REQUIRED BY A    -
*             JOB MUST BE IMMEDIATELY AVAILABLE OR THE JOB WILL BE   -
*             FAILED. THE DEFAULT IS TO ALLOW JOBS TO WAIT FOR THE   -
*             DATA SETS THEY REQUIRE.
*   2FIELD = JSWASMF
*     3DISPLACEMENT = 3 BYTES
*     3TYPE = BIT STRING
*       4BITS-USED = BIT 1
*         5FUNCTION = INDICATE WHETHER SMF DATA COLLECTION           -
*             FACILITIES ARE TO BE ACTIVE DURING JOB SCHEDULING.
*         5VALUE = IF THIS BIT IS ON, SMF DATA COLLECTION FACILITES  -
*             WILL BE ACTIVE.  THE DEFAULT IS THAT NO DATA           -
*             COLLECTION WILL TAKE PLACE.
*       4BITS-USED = BIT 2
*         5FUNCTION = INDICATE WHETHER SMF EXIT ROUTINES ARE TO BE   -
*             ACTIVE DURING JOB SCHEDULING.
*         5VALUE = IF THIS BIT IS ON, SMF EXIT ROUTINES WILL BE      -
*             ACTIVE DURING JOB SCHEDULING. THE DEFAULT IS THAT NO   -
*             EXIT ROUTINES WILL BE ACTIVE.
*       4BITS-USED = BIT 3
*         5FUNCTION = INDICATE HOW TIMING INFORMATION FOR THE JOB IS -
*             TO BE ACQUIRED, USING TIMER MANAGEMENT OR USING THE    -
*             TIMING FIELDS OF THE PROTECTED STEP CONTROL BLOCK (PSCB).
*         5VALUE = IF THIS BIT IS ON, TIMING INFORMATION WILL BE     -
*             ACQUIRED FROM THE PSCB.  THE DEFAULT IS TO UTILIZE     -
*             TIMER MANAGEMENT'S FACILITIES.
*       4BITS-USED = BITS 4 TO 8
*         5FUNCTION = RESERVED
*         5VALUE = MUST BE ZERO TO ALLOW FOR EXTENSION OF THE JOB    -
*             SCHEDULING SUBROUTINE
*   2FIELD = JSWAJSEL
*     3DISPLACEMENT = 4 BYTES
*     3TYPE = ADDRESS/TABLE
*     3TABLE-SIZE = 4 BYTES
*       4FIELD = JSWAOPT2
*         5DISPLACEMENT = 0 BYTES
*         5TYPE = BIT STRING
*           6BITS-USED = BIT 1
*             7FUNCTION = INDICATE WHETHER SYSTEM TASKS ARE PROHIBITED
*             7VALUE = IF THIS BIT IS ON, SYSTEM TASKS ARE PROHIBITED
*           6BITS USED = BIT 2
*             7FUNCTION = INDICATE WHETHER SYSTEM TASK CONTROL       -
*             MESSAGES ARE TO BE SUPPRESSED
*             7VALUE = IF THIS BIT IS ON, SYSTEM TASK CONTROL        -
*             MESSAGES ARE SUPPRESSED
*           6BITS-USED = BIT 3
*             7FUNCTION = INDICATE WHETHER THE WARM START MESSAGE    -
*             'INIT=JOBNAME' SHOULD BE SUPPRESSED
*             7VALUE = IF THIS BIT IS ON, THE MESSAGE IS SUPPRESSED
*       4FIELD = JSWAJSEL
*         5DISPLACEMENT = 1 BYTE
*         5TYPE = ADDRESS
*           6OF = JOB SCHEDULING ENTRANCE LIST (JSEL)
*   2FIELD = JSWAMOD
*     3DISPLACEMENT = 8 BYTES
*     3TYPE = CHARACTER STRING
*       4LENGTH+MODE = 8 CHARACTERS
*           6VALUE = THE NAME OF THE MODULE TO WHICH THE LAST XCTL   -
*                  WAS MADE IN THE JOB SCHEDULING SUBROUTINE
*   2FIELD = JSWATSTK
*     3DISPLACEMENT = 16 BYTES
*     3TYPE = TABLE
*       4TABLE-SIZE = 4 BYTES
*       4FIELD = JSWANTRK
*         5DISPLACEMENT = 0 BYTES
*         5TYPE = ARITHMETIC
*           6MODE = BINARY
*           6LENGTH = 8 BITS
*           6SCALING = 0
*       4FIELD = JSWATSTK
*         5DISPLACEMENT = 1 BYTE
*         5TYPE = ADDRESS
*   2FIELD = JSWAPKEY
*     3DISPLACEMENT = 20 BYTES
*     3TYPE = ARITHMETIC
*       4MODE = BINARY
*       4LENGTH = 8 BITS
*       4SCALING = 0
*       4VALUE = THE PROTECTION KEY TO BE USED IF THE PROGRAM(S)     -
*             INVOKED FOR THIS JOB ARE NOT MENTIONED IN THE LINKAGE  -
*             TABLE.
*   2FIELD = JSWATIOT
*     3DISPLACEMENT = 21 BYTES
*     3TYPE = ADDRESS
*       4OF = THE TIOT INDICATED IN ITS TCB WHEN THE JOB SCHEDULING  -
*             SUBROUTINE WAS INVOKED
*   2FIELD = JSWAXCTL
*     3DISPLACEMENT = 24 BYTES
*     3TYPE = BIT STRING
*       4BITS-USED = BITS 1 TO *
*         5FUNCTION = THE L-FORM OF THE XCTL MACRO-INSTRUCTION IS    -
*             MOVED TO THIS AREA FOR LINKAGE BETWEEN JOB SCHEDULING  -
*             MODULES
*         5VALUE = ESTABLISHED BY THE MAPPING MACRO-INSTRUCTION      -
*             FOR XCTL
*
**** END OF SPECIFICATIONS ***/
         MACRO
&NAME    IEFVJSWA  &PREFIX=JSWA,&PLIST=(NOTSTACK,NOWKFILE,RCVRY,QWAIT),*
               &DDTERM=IGNORE,&HIARCHY=0,&ENQ=HAVE,&DCB=,&MODULE=,     *
               &JSEL=0,&JSOL=(SYSTASK,STCMSG,WSMSG) /*
         LCLA  &A1,&A2
         LCLC  &P
         LCLC  &X0,&X1,&X2,&X3,&X4,&X5,&X6,&X7,&X8,&X9,&XA,&XB,&XC,&XD
         LCLC  &XE,&XF
         LCLC  &Y0,&Y1,&Y2,&Y3,&Y4,&Y5,&Y6,&Y7,&Y8,&Y9,&YA,&YB,&YC,&YD
         LCLC  &YE,&YF
&NAME    DS    0D           ALIGN JSWA
&P       SETC  '&PREFIX'
&P.LGTH  DC    Y(&P.END-&P) LENGTH OF JSWA
         AIF   (T'&DDTERM EQ 'O').JSW0020
         AIF   ('&DDTERM' EQ 'IGNORE').JSW0020
         AIF   ('&DDTERM' EQ 'TERM').JSW0010
         MNOTE *,'INVALID DDTERM OPTION -- &DDTERM -- IGNORED'
         AGO   .JSW0020
.JSW0010 ANOP
&X0      SETC  '+'
&X1      SETC  '&P.TERM'
.JSW0020 AIF   (N'&PLIST EQ 0).JSW0170
.JSW0030 ANOP
&A1      SETA  &A1+1
         AIF   (K'&PLIST(&A1) EQ 4).JSW0050
         AIF   (K'&PLIST(&A1) EQ 5).JSW0070
         AIF   (K'&PLIST(&A1) EQ 6).JSW0090
         AIF   (K'&PLIST(&A1) EQ 7).JSW0120
         AIF   (K'&PLIST(&A1) EQ 8).JSW0140
.JSW0040 MNOTE *,'PLIST OPERAND &A1 INVALID -- IGNORED'
         AGO   .JSW0160
.JSW0050 AIF   ('&PLIST(&A1)' NE 'QWAIT').JSW0060
&XC      SETC  ''
&XD      SETC  ''
         AGO   .JSW0160
.JSW0060 AIF   ('&PLIST(&A1)' NE 'PKEY').JSW0040
&X8      SETC  '+'
&X9      SETC  '&P.PKO'
         AGO   .JSW0160
.JSW0070 AIF   ('&PLIST(&A1)' NE 'RCVRY').JSW0080
&XA      SETC  ''
&XB      SETC  ''
         AGO   .JSW0160
.JSW0080 AIF   ('&PLIST(&A1)' NE 'QWAIT').JSW0040
&XC      SETC  ''
&XD      SETC  ''
         AGO   .JSW0160
.JSW0090 AIF   ('&PLIST(&A1)' NE 'NOPKEY').JSW0100
&X8      SETC  ''
&X9      SETC  ''
         AGO   .JSW0160
.JSW0100 AIF   ('&PLIST(&A1)' NE 'TSTACK').JSW0110
&X4      SETC  '+'
&X5      SETC  '&P.TRST'
         AGO   .JSW0160
.JSW0110 AIF   ('&PLIST(&A1)' NE 'WKFILE').JSW0040
&X6      SETC  '+'
&X7      SETC  '&P.WKFI'
         AGO   .JSW0160
.JSW0120 AIF   ('&PLIST(&A1)' NE 'NORCVRY').JSW0130
&XA      SETC  '+'
&XB      SETC  '&P.NORC'
         AGO   .JSW0160
.JSW0130 AIF   ('&PLIST(&A1)' NE 'NOQWAIT').JSW0040
&XC      SETC  '+'
&XD      SETC  '&P.NOQW'
         AGO   .JSW0160
.JSW0140 AIF   ('&PLIST(&A1)' NE 'NOTSTACK').JSW0150
&X2      SETC  ''
&X3      SETC  ''
         AGO   .JSW0160
.JSW0150 AIF   ('&PLIST(&A1)' NE 'NOWKFILE').JSW0040
&X4      SETC  ''
&X5      SETC  ''
.JSW0160 AIF   (&A1 LT N'&PLIST).JSW0030
.JSW0170 AIF   (T'&ENQ EQ 'O').JSW0190
         AIF   ('&ENQ' EQ 'HAVE').JSW0190
         AIF   ('&ENQ' NE 'USE').JSW0180
&XE      SETC  '+'
&XF      SETC  '&P.ENQU'
         AGO   .JSW0190
.JSW0180 MNOTE *,'INVALID ENQ OPTION -- &ENQ -- IGNORED'
.JSW0190 ANOP
*        OPTIONS FLAG FIELD
&P.OPT   DC    AL1(0&X0&X1&X2&X3&X4&X5&X6&X7&X8&X9&XA&XB&XC&XD&XE&XF)
&P.TERM  EQU   B'10000000' PROCESS 'TERM=TS' FLAG
&P.TRST  EQU   B'01000000' TRACK STACKING FLAG
&P.WKFI  EQU   B'00100000' DEDICATED WORKFILES FLAG
*        EQU   B'00010000' RESERVED
&P.PKO   EQU   B'00001000' PROTECT KEY OPTION FLAG
&P.NORC  EQU   B'00000100' BY-PASS ALLOCATION RECOVERY FLAG
&P.NOQW  EQU   B'00000010' NO WAIT FOR QUEUE SPACE FLAG                *
               (CURRENTLY ONLY SUPPORTED BY THE READER/INTERPRETER)
&P.ENQU  EQU   B'00000001' FLAG TO REQUEST USE OPTION FOR THE INITIATOR*
                   ENQ ON DATA SETS.  IF THE ENQ FAILS, THE JOB IS TO  *
                   BE FAILED.
*        SMF OPTIONS FIELD
&P.SMF   DC    AL1(0)
         AIF   (N'&JSOL EQ 0).JSW0340
.JSW0250 ANOP
&A2      SETA  &A2+1
         AIF   (K'&JSOL(&A2) EQ 5).JSW0270
         AIF   (K'&JSOL(&A2) EQ 6).JSW0280
         AIF   (K'&JSOL(&A2) EQ 7).JSW0290
         AIF   (K'&JSOL(&A2) EQ 8).JSW0310
         AIF   (K'&JSOL(&A2) EQ 9).JSW0320
.JSW0260 MNOTE *,'JSOL OPERAND &A2 INVALID -- IGNORED'
         AGO   .JSW0330
.JSW0270 AIF   ('&JSOL(&A2)' NE 'WSMSG').JSW0260
&Y0      SETC  ''
&Y1      SETC  ''
         AGO   .JSW0330
.JSW0280 AIF   ('&JSOL(&A2)' NE 'STCMSG').JSW0260
&Y2      SETC  ''
&Y3      SETC  ''
         AGO   .JSW0330
.JSW0290 AIF   ('&JSOL(&A2)' NE 'SYSTASK').JSW0300
&Y4      SETC  ''
&Y5      SETC  ''
         AGO   .JSW0330
.JSW0300 AIF   ('&JSOL(&A2)' NE 'NOWSMSG').JSW0260
&Y0      SETC  '+'
&Y1      SETC  '&P.PSYS'
         AGO   .JSW0330
.JSW0310 AIF   ('&JSOL(&A2)' NE 'NOSTCMSG').JSW0260
&Y2      SETC  '+'
&Y3      SETC  '&P.SMS1'
         AGO   .JSW0330
.JSW0320 AIF   ('&JSOL(&A2)' NE 'NOSYSTASK').JSW0260
&Y4      SETC  '+'
&Y5      SETC  '&P.SMS2'
.*       AGO   .JSW0330
.JSW0330 AIF   (&A2 LT N'&JSOL).JSW0250
.JSW0340 ANOP
&P.JSEL  DS    A                  COMPOSITE FIELD
         ORG   &P.JSEL
&P.OPT2  DC    AL1(0&Y0&Y1&Y2&Y3&Y4&Y5&Y6&Y7&Y8&Y9&YA&YB&YC&YD&YE&YF)
&P.PSYS  EQU   B'10000000' PROHIBIT SYSTEM TASK FLAG
&P.SMS1  EQU   B'01000000' SUPPRESS SYSTEM TASK CONTROL MESSAGE FLAG
&P.SMS2  EQU   B'00100000' SUPPRESS 'INIT=JOBNAME' WARM START MESSAGE  *
                           FLAG
*        EQU   B'00010000' RESERVED
*        EQU   B'00001000' RESERVED
*        EQU   B'00000100' RESERVED
*        EQU   B'00000010' RESERVED
*        EQU   B'00000001' RESERVED
&P.JSE1  DC    AL3(&JSEL) POINTER TO THE JOB SCHEDULING ENTRANCE LIST
&P.MOD   DC    CL8'&MODULE ' NAME OF JOB SCHEDULING MODULE TO WHICH THE*
                   LAST XCTL OR LINK WAS MADE.
&P.TSTK  DC    A(0) POINTER TO THE TRACK STACK IF TRACK STACKING HAS   *
                   BEEN SPECIFIED
         ORG   &P.TSTK
&P.NTRK  DS    AL1
&P.TKST  DS    AL3
&P.PKEY  DC    AL1(0) THE PROTECTION KEY TO BE USED IF THE PROGRAM(S)
*        INVOKED FOR THIS JOB ARE NOT MENTIONED IN THE LINKAGE TABLE
*        AND IF THE PROTECTION KEY OPTION HAS BEEN SELECTED BY
*        THE INVOKER OF THE JOB SCHEDULING SUBROUTINE.
&P.TIOT  DC    AL3(0)   TIOT ADDRESS FOR THE TIOT POINTED TO BY ITS TCB*
                        WHEN THE JOB SCHEDULING SUBROUTINE GAINED      *
                        CONTROL
&P&SYSNDX XCTL EPLOC=0,HIARCHY=&HIARCHY,DCB=&DCB,SF=L
&P.END   EQU   *
         ORG   &P&SYSNDX
&P.XCTL  DS    CL(&P.END-&P&SYSNDX)
         ORG   &P&SYSNDX
&P.NEL   DS    A(0)      NEL POINTER USED INTERNALLY BY IEEVRCTL
         ORG   &P.LGTH
&P       DS    CL(&P.END-&P.LGTH)  JOB SCHEDULING WORK AREA
         MEND
**/ %    JSWABSL: ;
* /********************************************************************
*          DEFINE THE JOB SCHEDULING WORK AREA
* ********************************************************************/
* DECLARE
* 1       JSWA BASED(JSWAPTR) BOUNDARY(DWORD),
*         /************************************************************
*               LENGTH FIELD
*         ************************************************************/
*         2     JSWALGTH FIXED(15),
*         /************************************************************
*               PRIMARY OPTIONS FLAG FIELD
*         ************************************************************/
*         2     JSWAOPT BIT(8),
*               /* PROCESS 'TERM=TS' FLAG */
*               3  JSWATERM BIT(1),
*               /* TRACK STACKING FLAG */
*               3  JSWATRST BIT(1),
*               /* DEDICATED WORKFILES FLAG */
*               3  JSWAWKFI BIT(1),
*               /* RESERVED */
*               3  * BIT(1),
*               /* PROTECTION KEY OPTION FLAG */
*               3  JSWAPKO BIT(1),
*               /* BY-PASS ALLOCATION RECOVERY FLAG */
*               3  JSWANORC BIT(1),
*               /* FLAG TO INDICATE WHETHER THE JOB SCHEDULING       */
*               /* SUBROUTINE IS TO CANCEL A JOB AUTOMATICALLY       */
*               /* RATHER THAN WAITING FOR JOBQ SPACE.               */
*               3  JSWANOQW BIT(1),
*               /* FLAG TO INDICATE WHETHER THE JOB SCHEDULING       */
*               /* SUBROUTINE IS TO CANCEL A JOB AUTOMATICALLY       */
*               /* FOR A DATA SET TO BECOME AVAILABLE FOR THE        */
*               /* JOB.                                              */
*               3  JSWAENQU BIT(1),
*         /************************************************************
*               SMF OPTIONS FLAG FIELD
*         ************************************************************/
*         2     JSWASMF BIT(8),
*         /************************************************************
*               COMPOSITE FIELD
*         ************************************************************/
*         2     JSWAJSEL POINTER(32),
*               /******************************************************
*                  SECONDARY OPTIONS FLAG FIELD
*               ******************************************************/
*               3  JSWAOPT2 BIT(8),
*                  /* PROHIBIT SYSTEM TASK FLAG */
*                  4  JSWAPSYS BIT(1),
*                  /* SUPPRESS SYSTEM TASK CONTROL MESSAGE FLAG */
*                  4  JSWASMS1 BIT(1),
*                  /* SUPPRESS 'INIT=JOBNAME' WARM START MESSAGE */
*                  4  JSWASMS2 BIT(1),
*                  /* RESERVED */
*                  4  * BIT(5),
*               /******************************************************
*                  POINTER TO THE JOB SCHEDULING ENTRANCE LIST
*               ******************************************************/
*               3  JSWAJSE1 POINTER(24),
*         /************************************************************
*               NAME OF THE JOB SCHEDULING SUBROUTINE MODULE TO WHICH
*               THE LAST XCTL WAS MADE
*         ************************************************************/
*         2     JSWAMOD CHARACTER(8),
*         /************************************************************
*               POINTER TO THE TRACK STACK IF TRACK STACKING HAS BEEN
*               SPECIFIED.
*         ************************************************************/
*         2     JSWATSTK POINTER(32),
*               3  JSWANTRK POINTER(8),
*               3  JSWATKST POINTER(24),
*         /************************************************************
*               THE PROTECTION KEY TO BE USED IF THE PROGRAM(S) INVOKED
*               FOR THIS JOB ARE NOT MENTIONED IN THE LINKAGE TABLE AND
*               IF THE PROTECTION KEY OPTION HAS BEEN SELECTED BY THE
*               INVOKER OF THE JOB SCHEDULING SUBROUTINE.
*         ************************************************************/
*         2     JSWAPKEY POINTER(8),
*         /************************************************************
*               POINTER TO THE INPUT TIOT FOR THE JOB SCHEDULING
*               SUBROUTINE
*         ************************************************************/
*         2     JSWATIOT POINTER(24),
*         /************************************************************
*               L-FORM OF THE XCTL MACRO-INSTRUCTION -- USED BY THE
*               MODULES OF THE JOB SCHEDULING SUBROUTINE WHICH USE XCTL
*               TO PASS CONTROL TO ONE ANOTHER.
*         ************************************************************/
*         2     JSWAXCTL BIT(64),
*               /* NEL POINTER USED INTERNALLY BY IEEVRCTL   */
*               3  JSWANEL POINTER(32);
