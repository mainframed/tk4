         MACRO
         IEMKPLR &P,&FLR,&SCI,&BASEREG=GR9,&DL=,&LL=
.* THIS MACRO GENERATES THE PHASE LOADING ROUTINE WHICH IS THE
.* ROUTINE IN EACH PHASE ENTERED FROM COMPILER CONTROL. &FLR IS THE
.* NAME OF THE FIRST LEVEL ROUTINE OF THE PHASE. THE &SCI PARAMETER
.* CAUSES INCLUSION OF CODE TO OBTAIN OR RELEASE SCRATCH CORE.
         L     &BASEREG,PAR1 .     LOAD PHASE BASE REGISTER
         USING IEM&P,&BASEREG
         AIF   (T'&SCI EQ 'O').SSNOSC
         AIF   ('&SCI' NE 'G').SSNOSC
         LA    GR3,8 .             GET 4K
         ST    GR3,PAR1 .            OF
         L     LR,ZUGC .             SCRATCH
         BALR  RR,LR .               CORE
         L     SAREG,PAR1 .        RESOLVE SAVE AREA POINTER
         ST    SAREG,XLCRASCR .      AND SCRATCH CORE POINTER IN XLCR
.SSNOSC  ANOP
         MVC   ZMYNAM(2),=C'&P' .  MOVE PHASE NAME TO COMM. REGN.
         BAL   RR,&FLR .           PASS CONTROL TO PHASE
         AIF   (T'&SCI EQ 'O').SSNSC2
         AIF   ('&SCI' NE 'R').SSNSC2
         LA    GR3,1 .             RELEASE SCRATCH CORE
         ST    GR3,PAR1 .            OBTAINED IN
         L     LR,ZURC .             SINGLE CALL OF
         BALR  RR,LR .               ZUGC
.SSNSC2  ANOP
         LA    GR2,=C'&DL' .       STORE TO-BE-DELETED PHASE LIST
         ST    GR2,PAR1 .            ADDRESS IN PAR1
         AIF   ('&LL' EQ '0').SSNLDL
         LA    GR2,=C'&LL' .       STORE TO-BE-LOADED PHASE LIST
         ST    GR2,PAR2 .            ADDRESS IN PAR2
         AGO   .SSKIP9
.SSNLDL  XC    PAR2(4),PAR2 .      INDICATE NO LOAD LIST
.SSKIP9  ANOP
         L     LR,RLSCTL .         CALL RLSCTL TO LOAD
         BR    LR .                  NEXT PHASE
*
         LTORG
         EJECT
         MEND
