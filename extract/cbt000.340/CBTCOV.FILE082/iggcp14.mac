        MACRO
         IGGCP14  &OPTCD=
.*         RELEASE 23 DELETIONS/CHANGES
.*         RELEASE 22 DELETIONS/CHANGES
.*         RELEASE 21 DELETIONS/CHANGES
*  CP14--PART TWO--REWRITE COCR AND TRACK INDEX
CH1      CCW   SIDEQ,0,CC,5             SEARCH FOR COCR
CH2      CCW   TIC,CH1-CB1,X'00',0
CH3      CCW   WD,CB22-CB1,CC+SILI,6    WRITE NEW COCR FROM CP8
LCH3     EQU   CH3+8-CH1                LENGTH OF COMMON SEGMENT
         AIF  ('&OPTCD' NE 'W' AND '&OPTCD' NE 'VW').SKIP1
CH3A1    CCW   NOP,CH3A1+5-CB1,CC+SILI,1 SET SECTOR TO ZERO IF RPS
CH3A     CCW   SIDEQ,0,CC,5             SEARCH FOR COCR AGAIN
CH3B     CCW   TIC,CH3A-CB1,X'00',0
CH3C     CCW   RDATA,0,CC+SILI+SKIP,8   READ IT BACK
LCH3C    EQU   CH3C+8-CH3A1             LENGTH OF WRT CHK SEGMENT
.SKIP1   ANOP
         AIF  ('&OPTCD' NE 'V' AND '&OPTCD' NE 'VW').SK1
CH4      CCW   TIC,0,0,0                CH5, CH8G, OR CH14
         AGO   .SK12
.SK1     ANOP
CH4      CCW   TIC,0,X'00',0            CH5, CH8D, CH9, OR CH14
.SK12    ANOP
CH5      CCW   NOP,0,CC+SILI,6          SEARCH FOR PRIME IX ENTRY
CH55     CCW   SIDEQ,CB22+6-CB1,CC,5    (ENTRY POINT FOR SETUPS 1/2)
CH6      CCW   TIC,CH55-CB1,0,0
CH7      CCW   WKD,0,DC,0               WRITE NEW PRIME IX KEY
CH8      CCW   X'00',CB10+7-CB1,CC,10   AND DATA
LCH8     EQU   CH8+8-CH4                LENGTH OF COMMON
         AIF  ('&OPTCD' NE 'W' AND '&OPTCD' NE 'VW').SKIP2
CH80     CCW   NOP,0,CC+SILI,1          SET SECTOR IF RPS
CH8A     CCW   SIDEQ,CB22+6-CB1,CC,5    SEARCH FOR ENTRY AGAIN
CH8B     CCW   TIC,CH8A-CB1,X'00',0
CH8C     CCW   RKD,0,CC+SKIP,0          READ IT BACK
LCH8C    EQU   CH8C+8-CH80              LENGTH OF WRITE CHK
.SKIP2   ANOP
         AIF   ('&OPTCD' NE 'V' AND '&OPTCD' NE 'VW').SK3
CH8D     CCW   TIC,CH8G5-CB1,0,0        TIC TO CH8G5
CH8E     DC    D'0'                     THIS CCW NOT USED
CH8F     DC    D'0'                     THIS CCW NOT USED
CH8G     CCW   NOP,0,CC+SILI,1          SET SECTOR FOR RPS
CH8G5    CCW   SIDEQ,CB24-CB1,CC,5      SRCH FOR OVERFLOW TRK IX ENTRY
CH9      CCW   TIC,CH8G5-CB1,0,0
CH10     CCW   TIC,0,0,0                TIC TO CH12 OR CH13
         AGO   .SK35
.SK3     ANOP
CH8D     CCW   SIDEQ,CB24-CB1,CC,5      SEARCH FOR OVERFLOW TRACK
CH8E     CCW   TIC,CH8D-CB1,X'00',0     INDEX ENTRY
CH8F     CCW   WD,CB25-CB1,CC,10        WRITE NEW DATA ONLY
CH8G     CCW   TIC,CH13+8-CB1,X'00',0
CH9      CCW   NOP,0,CC+SILI,1          SET SECTOR IF RPS
CH95     CCW   SIDEQ,CB24-CB1,CC,5      SEARCH OVERFLOW TRACK
CH10     CCW   TIC,CH95-CB1,X'00',0     INDEX ENTRY
.SK35    ANOP
CH12     CCW   WKD,0,DC,0
CH13     CCW   WD,CB25-CB1,CC,10        WRITE OVERFLOW INDEX ENTRY
LCH13    EQU   CH13+8-CH8D              LENGTH OF COMMON SEGMENT
         AIF   ('&OPTCD' NE 'W' AND '&OPTCD' NE 'VW').SKIP3
CH130    CCW   NOP,0,CC+SILI,1          SET SECTOR IF RPS
CH13A    CCW   SIDEQ,CB24-CB1,CC,5      SEARCH FOR ENTRY AGAIN
CH13B    CCW   TIC,CH13A-CB1,X'00',0
CH13C    CCW   RKD,0,CC+SKIP,0          READ IT BACK
LCH13C   EQU   CH13C+8-CH130            LENGTH OF WRITE CHK CCWS
.SKIP3   ANOP
         AIF   ('&OPTCD' EQ 'V' OR  '&OPTCD' EQ 'VW').SKIP4
CH14     CCW   X'00',CH23+1-CB1,CC,6    SEEK NEW OVERFLOW RECORD
*        SEEK SET BY APPENDAGE. FOR USER WORKAREA THIS CCW IS A NOP.
         AGO   .SKIP45
.SKIP4   ANOP
CH14     CCW   NOP,0,SILI,1          STOPPING CCW
.SKIP45  ANOP
*  CP14--PART ONE--WRITE OVERFLOW RECORD
CH150    CCW   NOP,0,CC+SILI,1          SET SECTOR IF RPS
CH15     CCW   SIDEQ,CH23+3-CB1,CC,5    SEARCH FOR OVERFLOW SLOT
CH15A    CCW   TIC,CH15-CB1,X'00',0
CH16     CCW   WCKD,CH24-CB1,DC,8       WRITE NEW OVERFLOW RECORD
CH17     CCW   X'00',0,DC,0
CH18     CCW   X'00',0,CC,0
LCH18    EQU   CH18+8-CH14              LENGTH OF COMMON SEGMENT
         AIF   ('&OPTCD' NE 'W' AND '&OPTCD' NE 'VW').SKIP5
CH180    CCW   NOP,0,CC+SILI,1          SET SECTOR IF RPS
CH18A    CCW   SIDEQ,CH23+3-CB1,CC,5    SEARCH FOR NEW OVERFLOW RECORD
CH18B    CCW   TIC,CH18A-CB1,X'00',0
CH18C    CCW   RCKD,0,SKIP,0            READ IT BACK
LCH18C   EQU   CH18C+8-CH180            LENGTH OF WRITE CHK
.SKIP5   ANOP
*        TERMINATION FOR SETUPS 1,2,5, AND 6
CH19     CCW   X'00',CJ11+1-CB1,CC,6    SEEK PRIOR OVERFLOW RECORD
CH200    CCW   NOP,0,CC+SILI,1          SET SECTOR IF RPS
CH20     CCW   SIDEQ,CJ11+3-CB1,CC,5    SEARCH FOR RECORD
CH21     CCW   TIC,CH20-CB1,00,0
         AIF   ('&OPTCD' NE 'W' AND '&OPTCD' NE 'VW').NOCC22
CH22     CCW   WD,0,CC,0                WRITE BACK PRIOR OFLOW REC
         AGO   .REJOIN
.NOCC22  ANOP
CH22     CCW   WD,0,00,0                WRITE BACK PRIOR OFLOW REC
.REJOIN  ANOP
LCH22    EQU   CH22+8-CH19              LENGTH OF COMMON SEGMENT
         AIF   ('&OPTCD' NE 'W' AND '&OPTCD' NE 'VW').SKIP6
CH220    CCW   NOP,0,CC+SILI,1          SET SECTOR IF RPS
CH22A    CCW   SIDEQ,CJ11+3-CB1,CC,5    SEARCH FOR PRIOR OFLOW AGAIN
CH22B    CCW   TIC,CH22A-CB1,0,0
CH22C    CCW   RDATA,0,SKIP,0           READ IT BACK
LCH22C   EQU   CH22C+8-CH220            LENGTH OF WRITE CHK
.SKIP6   ANOP
*        TERMINATION FOR SETUPS 3 AND 4
CH23     DC    D'0'                     M B B C C H H R
CH24     DC    D'0'                     C C H H R KL DL DL
LCH24    EQU   CH24+8-CH23
SZ14     EQU   LCH3+LCH8+LCH13+LCH18+LCH22+LCH24
         AIF   ('&OPTCD' NE 'W' AND '&OPTCD' NE 'VW').SKIP7
SZ14W    EQU   SZ14+LCH3C+LCH8C+LCH13C+LCH18C+LCH22C
.SKIP7   ANOP
         AIF   ('&OPTCD' EQ 'V' OR '&OPTCD' EQ 'VW').EOFX
         MEXIT
.EOFX    ANOP
*        EOF EXTENSION
CH25     CCW   SIDEQ,CH31+3-CB1,CC,5    SEARCH FOR LAST OFLOW REC
CH26     CCW   TIC,CH25-CB1,0,0
CH27     CCW   WCKD,CH32-CB1,CC,8       WRITE EOF
LCH27    EQU   CH27+8-CH23              LENGTH OF COMMON SEGMENT
         AIF   ('&OPTCD' NE 'VW').SK4
CH280    CCW   NOP,0,CC+SILI,1          SET SECTOR IF RPS
CH28     CCW   SIDEQ,CH31+3-CB1,CC,5    SEARCH FOR EOF AGAIN
CH29     CCW   TIC,CH28-CB1,0,0
CH30     CCW   RCKD,0,SKIP,8            READ IT BACK
LCH30    EQU   CH30+8-CH280             LENGTH OF WRITE CHK
.SK4     ANOP
CH31     DC    D'0'                     M B B C C H H R OF LAST OFLOW
CH32     DC    D'0'                     C C H H R KL DL DL OF EOF
LCH32    EQU   CH32+8-CH31
SZ14VW   EQU   SZ14W+LCH27+LCH30+LCH32
         MEND
