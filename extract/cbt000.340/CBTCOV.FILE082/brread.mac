         MACRO
&LABEL   BRREAD  &REG,&TYPE,&TJID=
.***  GLOBAL VARIABLE &PLIST CONTROL GENERATION OF PARMETER LIST  ***
         GBLB  &PARMLST
         GBLC  &PLIST
         LCLC  &TLAB
.***  TEST FOR P-REG  ***
         AIF   ('&REG' NE '').OK1
         MNOTE 12,'MISSIMG ADDR REG'
         MEXIT
.OK1     ANOP
.***  TEST FOR DEMAND P-LIST  ***
         AIF   (&PARMLST).EXTGEN
.***  TEST FOR FIRST CALL OF BRREAD IN THIS CSECT  ***
         AIF   ('&PLIST' EQ 'RDL'.'&SYSECT'(4,5)).NOLIST
.***  IF FIRST CALL GENERATE PARAMETER LIST  ***
&PLIST   SETC  'RDL'.'&SYSECT'(4,5)
         AGO   .NOEXTGN
.EXTGEN  ANOP
&PLIST   SETC  'TMP'.'&SYSNDX'
.NOEXTGN ANOP
         CNOP  0,4                 ALLIGN ON WORD BOUNDARY
&LABEL   ST    &REG(1),&PLIST      STORE ADDRESS OF STORAGE REQUIRED
.ST11    ANOP
.***  GENERATE STORE OF REQUESTED ADDRESS BEFORE POINT TO P-LIST  ***
&TLAB    SETC  'RDL'.'&SYSNDX'
         BAL   1,&TLAB             POINT TO PARAMETER LIST
&PLIST   DS    F                   ADDRESS OF STORAGE REQUIRED
         DS    H                   TJID OF THIS STORAGE
         DS    C                   FUNCTION CODE FOR READ ROUTINE
         DS    C                   RESERVED
&TLAB    EQU   *
         AGO   .TESTID
.NOLIST  ANOP
&LABEL   ST    &REG(1),&PLIST      STORE ADDRESS OF STORAGE REQUIRED
.ST12    ANOP
         LA    1,&PLIST            POINT TO PARAMETER LIST
.TESTID  ANOP
.***  IF TJID IS NOT SPECIFIED, LEAVE PARAMETER LIST AS IS  ***
         AIF   ('&TJID' EQ '').TSTTYPE
.***  DISTINGUISH BETWEEN TJID REG AND TJID ABSOLUTE NUMBER  ***
         AIF   ('&TJID'(1,1) EQ '(').REG
         AIF   ('&TJID' EQ '0').GENSR
         MVC   &PLIST+4(2),&TJID . GET VALUE OF TJID FROM STORAGE
         AGO   .TSTTYPE
.GENSR   ANOP
         SR    15,15
.STORID  ANOP
         STH   15,&PLIST+4         STORE TJID IN PARAMETER LIST
         AGO   .TSTTYPE
.REG     ANOP
         STH   &TJID(1),&PLIST+4   STORE TJID IN PARAMETER LIST
.TSTTYPE AIF   ('&TYPE' EQ 'INIT').SETINIT
         AIF   ('&TYPE' EQ 'ADJUST').SETFREE
         AIF   ('&TYPE' EQ 'DATA' OR '&TYPE' EQ '').SETDATA
         MNOTE 12,'INVALID READ FUNCTION'
         MEXIT
.SETINIT ANOP
         LA    15,4                FUNCTION CODE FOR INITIALIZATION
         AGO   .STORE
.SETFREE ANOP
         LA    15,8                FUNCTION CODE TO INDICATE
*                                  RELEASE READ SWAP ROUTINE
.STORE   ANOP
         STC   15,&PLIST+6         STORE FUNCTION CODE IN
*                                  PARAMETER LIST
         AGO   .CALL
.SETDATA ANOP
         XC    &PLIST+6(1),&PLIST+6  ZERO FUNCTION CODE TO INDICATE
*                                  DATA REQUIRED
.CALL    ANOP
         L     15,READADDR         FETCH READ ROUTINE ADDRESS FROM
*                                  COMMON CSECT
         BALR  14,15               LINK TO READ
         AIF   (NOT(&PARMLST)).OUT
&PLIST   SETC  'RDL'.'&SYSECT'(4,5)
&PARMLST SETB  0
.OUT     ANOP
         MEND
