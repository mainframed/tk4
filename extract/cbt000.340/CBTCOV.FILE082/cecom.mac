         MACRO
&NAME    CECOM &OUTMSG=,&INMSG=,&OUTSD=,&FREE=,&POLL=,&MF=
.*
.* MACRO NAME: CECOM
.*
.* STATUS:     CHANGE LEVEL 001
.*
.* FUNCTION:   THE CECOM MACRO PROVIDES A COMMUNICATION LINK BETWEEN
.*             THE OLT TEST SECTION AND THE CE. THERE ARE BASICALLY
.*             5 FUNCTIONS DEFINABLE UNDER THIS MACRO;
.*
.*                1. THE OLT TEST SECTION WILL OUTPUT A MESSAGE TO THE
.*                   CE AND WAIT FOR THE RETURN OF HIS INPUT RESPONSE.
.*
.*                2. THE OLT TEST SECTION CAN OUTPUT A MESSAGE TO THE
.*                   CE ONLY AND NOT PROVIDE FOR A RESPONSE.
.*
.*                3. THE OLT TEST SECTION CAN OUTPUT A MESSAGE TO THE
.*                   CE WITH AN INPUT RESPONSE PROVIDED FOR BUT NOT
.*                   WAIT FOR IT. THE CE MAY RESPOND AT A LATER TIME
.*                   AND THE OLT WILL CONTINUE TESTING.
.*
.*                4. THE OLT TEST SECTION CAN TEST TO SEE IF THE
.*                   DELAYED RESPONSE IN ITEM 3 HAS OCCURRED YET.
.*
.*                5. THE OLT TEST SECTION CAN CANCEL OR FREE THE
.*                   DELAYED RESPONSE IN ITEM 3 IF IT HAS NOT
.*                   OCCURRED YET.
.*
.* REGISTER 15 RETURN CODE:
.*
.*             THE RETURN CODES HAVE DIFFERENT MEANINGS DEPENDING ON
.*             THE 5 FUNCTIONS DESCRIBED ABOVE;
.*
.*                  CODE  FUNCTION  MEANING
.*
.*                   00     1.      THE MESSAGE HAS BEEN OUTPUTED AND
.*                                  THE CE RESPONSE HAS BEEN RECEIVED.
.*
.*                          2.      THE MESSAGE HAS BEEN OUTPUTED.
.*
.*                          3.      THE MESSAGE HAS BEEN OUTPUTED.
.*
.*                          4.      THE DELAYED RESPONSE HAS BEEN
.*                                  RECEIVED.
.*
.*                          5.      THE DELAYED RESPONSE HAS BEEN
.*                                  CANCELLED.
.*
.*                   04    ALL      THE FACILITY IS NOT AVAILABLE.
.*
.*                   08    ALL      AN ERROR HAS OCCURRED DURING THE
.*                                  OUTPUT OR INPUT MESSAGE.
.*
.*                   0C     4       THE DELAYED RESPONSE HAS NOT BEEN
.*                                  RECEIVED YET.
.*
.*                   10     3       THERE IS AN OUTSTANDING DELAYED
.*                                  RESPONSE THAT MUST BE RECEIVED OR
.*                                  CANCELLED BEFORE A NEW DELAYED
.*                                  RESPONSE CAN BE ISSUED.
.*
.* PARAMETERS AND/OR KEYWORDS:
.*
.*             OUTMSG=  - DEFINES THE MESSAGE TO BE PUT OUT FROM THE
.*                        OLT TEST SECTION TO THE CE.
.*
.*                        ADDR     - ADDRESS OF THE MESSAGE.
.*
.*                        INTEGER  - NUMBER OF BYTES IN THE MESSAGE
.*                                   THAT IS NOT TO EXCEED 51.
.*
.*             INMSG=   - DEFINES THE CE RESPONSE REQUIREMENTS.
.*
.*                        ADDR     - ADDRESS WHERE THE REPLY IS TO BE
.*                                   INSERTED.
.*
.*                        INTEGER  - MAXIMUM NUMBER OF BYTES ACCEPTABLE
.*                                   IN THE REPLY. (SIZE OF THE REPLY
.*                                   BUFFER).
.*
.*             OUTSD=YES  - DEFINES THE 2 WAY COMMUNICATION BETWEEN
.*                          OLT TEST AND CE WITH THE DELAY RESPONSE.
.*
.*             POLL=YES   - DEFINES A REQUEST FOR CHECKING TO SEE IF
.*                          THE OUTSTANDING DELAYED RESPONSE HAS
.*                          OCCURRED YET.
.*
.*             FREE=YES   - DEFINES THE CANCELLATION OR FREEING OF
.*                          THE DELAYED RESPONSE.
.*
.* GLOBALS SET OR RESET BY THIS MACRO: NONE
.*
.* GLOBALS EXAMINED BY THIS MACRO: NONE
.*
.* SYMBOLS GENERATED BY THIS MACRO: NONE
.*
.* MNOTES ISSUED BY THIS MACRO:
.*
.*             8,'INVALID REG IN MF OPERAND'
.*
.*             8,'OUTSD WITHOUT BOTH OUTMSG AND INMSG INVALID'
.*
.*             8,'INMSG WITHOUT OUTMSG INVALID'
.*
.*             8,'NO OUTMSG OPERAND'
.*
.*             8,'INVALID OUTMSG PARAMETER'
.*
.*             8,'INVALID INMSG OPERAND'
.*
.*             8,'FREE OR POLL OPERAND MUST BE ALONE'
.*
.*             8,'OUTMSG INTEGER VALUE EXCEEDS MAXIMUM'
.*
.* REASONS:    THE ABOVE MNOTES ARE SELF EXPLANATORY.
.*
.* EXAMPLE:
.*
.* ********************************************************************
.* *  NAME  * OPERATION *  OPERAND                                    *
.* ********************************************************************
.* *        *           *                                             *
.* * METLK  * CECOM     * OUTMSG=(ADDR,INTEGER)                       *
.* * WETLK  * CECOM     * OUTMSG=(ADDR,INTEGER),INMSG=(ADDR,INTEGER)  *
.* * TLKLTE * CECOM     * OUTMSG=(ADDR,INTG),INMSG=(ADDR,INTG),       *
.* *        *           * OUTSD=YES                                   *
.* * NOTLK  * CECOM     * FREE=YES                                    *
.* * DIDTLK * CECOM     * POLL=YES                                    *
.* *        *           *
.*
         LCLA  &A
         AIF   ('&MF(1)' EQ 'E').E
         AIF   (T'&FREE NE 'O').FREE
         AIF   (T'&POLL NE 'O').POLL
         AIF   (T'&OUTMSG EQ 'O').ERR1
         AIF   (N'&OUTMSG EQ 1).ERR2
.*       AIF   (&OUTMSG(2) GT 58).ERR5
         AIF   (T'&MF EQ 'O').STD
         AIF   ('&MF(1)' EQ 'L').LST
         AGO   .STD
.E       ANOP
&NAME    L     R15,#TABLE .       TABLE OF ENTRY ADDRESSES
         L     R15,28(R15) .      ENTRY TO TRANSIENT MANAGER
         AIF   ('&MF(2)' EQ '(1)').E1
         AIF   ('&MF'(4,1) EQ '(').E12A
         AIF   (N'&MF EQ 1).E1
         LA    R1,&MF(2) .        ADDRESS OF PARAMETER LIST
.E1      ANOP
         BALR  R14,R15
         AGO   .MEND
.E12A    MNOTE 8,'INVALID REG IN MF OPERAND'
         MEXIT
.STD     ANOP
&NAME    L     R15,#TABLE .       TABLE OF ENTRY POINT ADDRESSES
         L     R15,28(R15) .      ENTRY TO TRANSIENT MANAGER
         LA    R14,R&SYSNDX .     RETURN ADDRESS
         CNOP  2,4 ,              ALIGN
         BALR  R1,R15
         DC    A(P&SYSNDX) .   PARAMETER LIST FOR TRANSIENT MANAGER
         AGO   .N03
.LST     ANOP
&NAME    DC    A(P&SYSNDX) .      PARAMETER LIST FOR TRANSIENT MANAGER
         AGO   .N03
.FREE    ANOP
         AIF   (T'&OUTMSG NE 'O' AND T'&INMSG NE 'O').ERR4
         AIF   (T'&POLL NE 'O' AND T'&OUTSD NE 'O').ERR4
&A       SETA  64
         AIF   ('&MF(1)' EQ 'L').LST
         AGO   .STD
.POLL    ANOP
         AIF   (T'&OUTMSG NE 'O' AND T'&INMSG NE 'O').ERR4
         AIF   (T'&POLL NE 'O' AND T'&OUTSD NE 'O').ERR4
&A       SETA  32
         AIF   ('&MF(1)' EQ 'L').LST
         AGO   .STD
.N03     ANOP
         AIF   (T'&OUTSD EQ 'O').N04
         AIF   (T'&OUTMSG NE 'O' AND T'&INMSG NE 'O').N05
         MNOTE 8,'OUTSD WITHOUT BOTH OUTMSG AND INMSG INVALID'
         MEXIT
.N05     ANOP
&A       SETA  128
.N04     ANOP
         DC    A(O&SYSNDX) .      POINTER TO OUTMSG AND COUNT
         DC    A(I&SYSNDX) .      POINTER TO INMSG AND COUNT
         DC    A(F&SYSNDX) .      POINTER TO FLAGS
         AIF   (T'&OUTMSG NE 'O').N06
O&SYSNDX DC    A(0)
         DC    A(0)
         AGO   .N06A
.N06     ANOP
O&SYSNDX DC    A(&OUTMSG(1)) .    OUTMSG ADDRESS
         DC    A(&OUTMSG(2)) .    OUTMSG COUNT
.N06A    ANOP
         AIF   (T'&INMSG NE 'O').N07
I&SYSNDX DC    A(0)
         DC    A(0)
         AGO   .N09
.N07     ANOP
         AIF   (T'&OUTMSG EQ 'O').N08
I&SYSNDX DC    A(&INMSG(1)) .     INMSG ADDRESS
         AIF   (N'&INMSG EQ 1).ERR3
         DC    A(&INMSG(2)) .     INMSG COUNT
         AGO   .N09
.N08     ANOP
I&SYSNDX DC    A(0)
         MNOTE 8,'INMSG WITHOUT OUTMSG INVALID'
         MEXIT
.N09     ANOP
F&SYSNDX DC    AL1(&A) .            FLAGS
P&SYSNDX DC    B'00000000' .      INDICATORS FOR TRANSIENT MANAGER
         DC    AL1(1) .           MACRO LEVEL
         DC    CL2'37'            MACRO ID
R&SYSNDX DS    0H
         MEXIT
.ERR1    MNOTE 8,'NO OUTMSG OPERAND'
         MEXIT
.ERR2    MNOTE 8,'INVALID OUTMSG PARAMETER'
         MEXIT
.ERR3    ANOP
         MNOTE 8,'INVALID INMSG OPERAND'
         MEXIT
.ERR4    ANOP
         MNOTE 8,'FREE OR POLL OPERAND MUST BE ALONE'
         MEXIT
.ERR5    ANOP
         MNOTE 8,'OUTMSG INTEGER VALUE EXCEEDS MAXIMUM'
         MEXIT
.MEND    ANOP
         MEND
