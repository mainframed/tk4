//LBM01199 JOB ('MVSJES3      ','LFALJMVS1BM1               '),
// 'BILL MCCOY 209',MSGLEVEL=1,PRTY=12,PERFORM=4,
// NOTIFY=L11FOTM,MSGCLASS=T
//*MAIN CLASS=Q96
//*PASSWORD DSN=EP.TESTLIBH,P=XXXX
//*DIST  PLEASE DELIVER TO BILL MCCOY  _____CUBICLE 209_____
//*DIST  PLEASE DELIVER TO BILL MCCOY  _____CUBICLE 209_____
//*DIST  PLEASE DELIVER TO BILL MCCOY  _____CUBICLE 209_____
// EXEC ASMFCL,SYSLMOD='EP.TESTLIBH(TSTRACF1)',DISP=SHR,RENT=NORENT
//ASMFCL.SYSPRINT DD SYSOUT=*
//ASMFCL.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=EPNG.MACLIB,DISP=SHR
//         DD DSN=L11FOTM.RACF.MACLIB,DISP=SHR
         TITLE 'TESTING RACF INTERNAL MACROS'
TEST     ENTER REG=12
         EQREG
         OPEN  (SYSIN,(INPUT))
         OPEN  (SYSPRINT,(OUTPUT))
GETCARD  CLI   COL80,C'A'
         BE    ABEND
         GET   SYSIN
         LR    R3,R1
         MVC   USERID,0(R3)
         MVC   GRPNM,8(R3)
         LA    R1,GRPNM+7
         LA    R0,8
CKLGTH   CLI   0(R1),C' '
         BNE   STLGTH
         BCTR  R1,0
         BCT   R0,CKLGTH
         ABEND 222,DUMP
STLGTH   STC   R0,GRPNMLN
         MVC   COL80,79(R3)
         BAL   R2,GETLINE
         MVC   1(5,R4),=C'USER='
         MVC   6(8,R4),0(R3)
         MVC   14(6,R4),=C'GROUP='
         MVC   20(8,R4),8(R3)
         BAL   R2,RACMAC
*
         C     R15,=F'0'
         BE    NOERR
         B     LOGOUT
NOERR    LH    R0,WK1GPAL
         LTR   R0,R0
         BZ    LOGOUT
         UNPK  WORK(3),WK1GRPA(2)
         TR    WORK(2),HEXTAB-240
         MVC   28(8,R4),=C'GRPAUTH='
         MVC   36(2,R4),WORK
         B     GETCARD
LOGOUT   MVC   28(12,R4),=C'RETURN CODE='
         UNPK  WORK(3),RETCODE+3(2)
         TR    WORK(2),HEXTAB-240
         MVC   40(2,R4),WORK
         MVC   43(5,R4),=C'DATA='
         UNPK  WORK2(7),WK1GPAL(4)
         TR    WORK2(6),HEXTAB-240
         MVC   48(6,R4),WORK2
         B     GETCARD
GETLINE  PUT   SYSPRINT
         LR    R4,R1
         MVI   0(R4),C' '
         MVC   1(120,R4),0(R4)
         MVI   0(R4),X'09'
         BR    R2
ABEND    ABEND 111,DUMP
EOF      CLOSE (SYSIN,,SYSPRINT)
         EXIT  RC=0
RACMAC   LA    R9,USERID
         MVI   WK1AREA,C' '
         MVC   WK1AREA+1(45),WK1AREA
         ICHETEST FLDATA=(,(R9)),MF=(E,T7)
         ICHEACTN TESTS=(T7),MF=(E,A26)
         ICHEINTY LOCATE,ENTRY=GRPNMLN,                                X
               OPTIONS=(FLDEF,TESTC,TESTM,ACTION),                     X
               WKAREA=WK1AREA,ACTIONS=(A26),MF=(E,I2)
         ST    R15,RETCODE
         BR    R2
         EJECT
COL80    DC    C' '
WORK     DC    F'0'
WORK2    DC    CL13' '
HEXTAB   DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
         DC    C'THE FOLLOWING IS THE WORK AREA'
RETCODE  DC    F'0'
WK1AREA  DS    0CL46
WK1XXX   DS    CL4
WK1RBA   DS    CL6
WK1YYY   DS    CL14
WK1ZZZ   DS    CL2
WK1SIZE  DS    CL2
WK1GPAL  DS    CL2            SIZE OF GROUP AUTH FOR THE USER
WK1GRPA  DS    CL1            USER'S GROUP AUTHORITY
WK1UACL  DS    CL2            SIZE OF GROUP'S UACC
WK1UACC  DS    CL1            GROUP'S UNIVERSAL ACCESS AUTHORITY
WK1PAD   DS    CL12           PADDING
USERIDL  DC    AL1(7)
USERID   DC    CL8'L11FUR2 '
GRPNMLN  DC    AL1(7)
GRPNM    DC    CL8'L11FGR1'
I2       ICHEINTY LOCATE,TYPE='GRP',ACTIONS=(,,,,,),TESTS=(,,),MF=L,   X
               ENTRY=0,WKAREA=0
T7       ICHETEST FIELD=USERID,FLDATA=(8,0),MF=L
A26      ICHEACTN FIELD=USERACS,TESTS=(),MF=L
SYSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,EODAD=EOF,RECFM=FB,      X
               LRECL=80,BLKSIZE=80
SYSPRINT DCB   DSORG=PS,MACRF=PL,DDNAME=SYSPRINT,RECFM=FBM,            X
               LRECL=121,BLKSIZE=121
         END
//LINK.SYSIN  DD *
 SETCODE AC(1)
