//LBM01199 JOB ('MVSJES3      ','LFALJMVS1BM1               '),
// 'BILL MCCOY   209',MSGLEVEL=1,PRTY=12,NOTIFY=L11FOTM,
//  PERFORM=4,MSGCLASS=T
//*MAIN CLASS=Q12
//   EXEC ASMFCL,SYSLMOD='L11FOTM.LOADLIB(RCVT)',DISP=SHR
//ASMFCL.SYSPRINT DD SYSOUT=*
         TITLE 'DUMP RCVT'
DUMP     ENTER REG=12
         EQREG
         EJECT
         OPEN  (SYSPRINT,(OUTPUT))
         L     R1,16            CVT
         L     R2,992(R1)       RCVT
         L     R3,224(R2)       DSDT
         L     R4,228(R2)       RNGT
         L     R5,4(R3)         NUMBER OF DSNAMES
         LA    R2,41(R3)        ADDRESS OF 1ST DSNAME
         MVC   BUFFER+1(80),=CL80' '
         CVD   R5,WORK
         MVC   BUFFER+1(18),=C'NUMBER OF DSNAMES='
         MVC   BUFFER+19(4),=X'40212020'
         ED    BUFFER+19(4),WORK+6
         PUT   SYSPRINT,BUFFER
LOOP1    MVC   BUFFER+1(80),=CL80' '
         MVC   BUFFER+1(4),=C'DSN='
         MVC   BUFFER+5(44),0(R2) MOVE A DSNAME
         PUT   SYSPRINT,BUFFER
         LA    R2,160(,R2)
         BCT   R5,LOOP1
         MVC   BUFFER+1(80),=CL80' '
         PUT   SYSPRINT,BUFFER
         MVC   BUFFER+1(80),=CL80' '
         L     R5,0(R4)         NUMBER OF RANGES
         CVD   R5,WORK
         MVC   BUFFER+1(17),=C'NUMBER OF RANGES='
         MVC   BUFFER+18(4),=X'40212020'
         ED    BUFFER+18(4),WORK+6
         PUT   SYSPRINT,BUFFER
         LA    R2,4(R4)         1ST RANGE
LOOP2    MVC   BUFFER+1(80),=CL80' '
         MVC   BUFFER+1(6),=C'RANGE='
         MVC   WORD,0(R2)        GET A WORD
         UNPK  WORK(9),WORD(5)   CONVERT TO CHAR
         TR    WORK(8),HEXTAB-240
         MVC   BUFFER+7(8),WORK
         MVI   BUFFER+15,C'*'
         MVC   BUFFER+16(4),0(R2)
         MVI   BUFFER+20,C'*'
         PUT   SYSPRINT,BUFFER
         LA    R2,45(,R2)
         BCT   R5,LOOP2
GOAWAY   CLOSE (SYSPRINT)
         EXIT  RC=0
         EJECT
BUFFER   DC    X'09',CL80' '
WORK     DC    D'0'
         DC    CL1' '
WORD     DC    CL4' '
         DC    CL1' '
HEXTAB   DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBM,            X
               LRECL=81,BLKSIZE=81
         END
