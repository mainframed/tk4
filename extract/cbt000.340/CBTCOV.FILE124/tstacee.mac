//LBM01199 JOB ('MVSJES3      ','LFALJMVS1BM1               '),
// 'BILL MCCOY 209',MSGLEVEL=1,PRTY=12,PERFORM=4,
// NOTIFY=L11FOTM,MSGCLASS=T
//*MAIN CLASS=Q96,ORG=RMT40
//*PASSWORD DSN=EP.TESTLIBH,P=XXXX
//*DIST  PLEASE DELIVER TO BILL MCCOY  _____CUBICLE 209_____
//*DIST  PLEASE DELIVER TO BILL MCCOY  _____CUBICLE 209_____
//*DIST  PLEASE DELIVER TO BILL MCCOY  _____CUBICLE 209_____
// EXEC ASMFCL,SYSLMOD='L11FOTM.LOADLIB(ACEE)',DISP=SHR,RENT=NORENT
//ASMFCL.SYSPRINT DD SYSOUT=*
//ASMFCL.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=EPNG.MACLIB,DISP=SHR
//         DD DSN=L11FOTM.RACF.MACLIB,DISP=SHR
         TITLE 'TESTING RACF ACEE '
TEST     ENTER REG=12
         EQREG
         L     R1,0(R1)          ADDRESS OF PARM
         LH    R0,0(R1)          LENGTH OF PARMS
         LTR   R0,R0             TEST
         BZ    DEFAULT           NONE, TAKE DEFAULT
         CLC   2(4,R1),=C'ACEE'  PARM=ACEE?
         BE    DOACEE            YES,
         CLC   2(5,R1),=C'WHERE' PARM=WHERE
         BE    DOWHERE           YES,
         CLC   2(3,R1),=C'WHO'   PARM=WHO?
         BE    DOWHO             YES,
DEFAULT  DS    0H
DOACEE   MVI   FLAG1,X'80'       SET FOR ACEE DISPLAY
         B     OPENS
DOWHERE  MVI   FLAG1,X'40'       SET FOR TERMINAL ID DISPLAY
         B     OPENS
DOWHO    MVI   FLAG1,X'20'       SET FOR USER ID AND GROUP DISPLAY
         B     OPENS
OPENS    DS    0H
         OPEN  (SYSPRINT,(OUTPUT))
         BAL   R9,GETLINE
         L     R1,16(0,0)        CVT ADDRESS
         L     R2,0(R1)          TCB WORDS
         L     R2,12(R2)         MY ASCB
         ST    R2,MYASCB         MY ASCB
         L     R3,108(R2)        ASXB
         L     R3,200(R3)        ACEE
         CLC   0(4,R3),=C'ACEE'  VALID ACEE
         BNE   ERROR1            NO,
         ST    R3,MYACEE         MY ACEE
         MVC   ACEE,0(R3)        MY ACEE
         TM    FLAG1,X'80'       DISPLAY ACEE?
         BNO   NEXT1             NO,
         LA    R2,25             SET FOR 25 WORDS
LOOP1    LA    R1,4              SET FOR 4 WORDS
         MVI   40(R8),C'*'
         MVC   41(16,R8),0(R3)   CHAR DATA
         MVI   57(R8),C'*'
LOOP2    MVC   WORD,0(R3)        GET A WORD
         UNPK  WORK(9),WORD(5)   CONVERT TO CHAR
         TR    WORK(8),HEXTAB-240
         MVC   1(8,R8),WORK      WORD TO PRINT
         LA    R3,4(R3)          ADDRESS OF NEXT WORD
         LA    R8,9(R8)          ADDRESS FOR NEXT WORD
         BCTR  R2,0              SUBT ONE FROM # WORDS
         LTR   R2,R2             TEST
         BZ    GOAWAY            ZERO, FINISHED
         BCTR  R1,0              SUBT ONE FROM # WORDS ON A LINE
         LA    R8,1(R8)          ADDRESS FOR NEXT WORD
         LTR   R1,R1             TEST
         BNZ   LOOP2             NOT ZERO, GET ANOTHER WORD
         BAL   R9,GETLINE        GET ANOTHER LINE
         B     LOOP1
*
NEXT1    DS    0H
         TM    FLAG1,X'40'       DISPLAY TERMINAL ID?
         BNO   NEXT2             NO,
         MVC   1(18,R8),=C'TERMINAL(XXXXXXXX)'
         MVC   10(8,R8),64(R3)   TERMINAL ID
         B     GOAWAY            THAT ALL
*
NEXT2    DS    0H
         TM    FLAG1,X'20'       DISPLAY GROUP & USERID
         BNO   ERROR1            NO,
         MVC   1(30,R8),=C'GROUP(XXXXXXXX) USER(YYYYYYYY)'
         MVC   7(8,R8),30(R3)    GROUP NAME
         MVC   22(8,R8),21(R3)   USER ID
         B     GOAWAY            THAT ALL
*
ERROR1   DS    0H
         MVC   1(21,R8),=C'**UNABLE TO FIND ACEE**'
         B     GOAWAY            THAT ALL
*
GETLINE  PUT   SYSPRINT
         LR    R8,R1
         MVI   0(R8),C' '
         MVC   1(80,R8),0(R8)
         MVI   0(R8),X'09'
         BR    R9
GOAWAY   CLOSE (SYSPRINT)
         EXIT  RC=0
         EJECT
WORK     DC    D'0'
         DC    CL1' '
WORD     DC    CL4' '
         DC    CL1' '
FLAG1    DC    X'00'
MYASCB   DC    F'0'
MYACEE   DC    F'0'
ACEE     DC    CL100' '
HEXTAB   DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
SYSPRINT DCB   DSORG=PS,MACRF=PL,DDNAME=SYSPRINT,RECFM=FBM,            X
               LRECL=81,BLKSIZE=81
         END
