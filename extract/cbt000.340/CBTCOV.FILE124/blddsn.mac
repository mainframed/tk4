         MACRO
         BLDDSN
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE WILL BUILD A DSNAME FORM NODES.           *
*        THE DSNAME WILL BE BUILT BY INSERTING THE NODES IN ORDER     *
*        FROM "WNODE1" AND "WNODE2" ETC. USING W#NODES TO LIMIT       *
*        THE NUMBER OF NODES TO REBUILD.                              *
*                                                                     *
***********************************************************************
BLDDSN   DS    0H
         STM   R0,R15,DSNSAV       SAVE REGS
         MVI   WDSNAME,C' '        BLANK OUT DSNAME WORK AREA
         MVC   WDSNAME+1(43),WDSNAME
         LA    R1,WNODE1L          POINT TO FIRST NODE LENGTH
         LA    R2,WDSNAME          POINT TO DSNAME AREA
         SR    R3,R3               CLEAR NODE COUNTREG
         IC    R3,W#NODES          LOAD THE NUMBER OF NODES
BLD001   SR    R4,R4               CLEAR FOR NODE LENGTH
         IC    R4,0(R1)            LOAD THE NODE LENGTH
         BCTR  R4,0                CALC EX LENGTH
         EX    R4,BLDX01           MOVE IN THE MODE
         LA    R2,1(R4,R2)         ADDRESS PAST NODE
         BCTR  R3,0                SUBT. ONE FROM # NODES
         LTR   R3,R3               TEST # NODES REMAINDING
         BZ    BLDRET              ZERO,  RETURN WITH DSNAME
         MVI   0(R2),C'.'          INSERT PERIOD
         LA    R2,1(,R2)           BUMP DSNAME TO NEXT NODE AREA
         LA    R1,9(,R1)           BUMP TO NEXT NODE WORK AREA
         B     BLD001              PROCESS NEXT NODE
BLDRET   LA    R1,WDSNAME          START OF NEW DSNAME
         SR    R2,R1               SUBT END OF NEW DSNAME
         STC   R2,WNWDSNL          SAVE NEW DSNAME LENGTH
         LM    R0,R14,DSNSAV       RESTORE ALL BUT R15
         SR    R15,R15             ZERO RETURN CODE
         BR    R14                 RETURN TO CALLER
BLDX01   MVC   0(*-*,R2),1(R1)     MOVE THE NODE TO WORK AREA
         MEND
