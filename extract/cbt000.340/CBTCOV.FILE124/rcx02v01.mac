RCX      TITLE 'RACF EXIT ICHRCX02 - RACHECK POST-PROCESSING ROUTINE'
***********************************************************************
*    NAME:        RACF EXIT ICHRCX02   *TEST*     DATE: 09/25/80      *
*                                                                     *
*    PURPOSE:                                                         *
*                                                                     *
*        THIS EXIT WILL WRITE THE SMF TYPE 180 RECORD                 *
*        FOR ALL RACHECK PROCESSING.                                  *
*                                                                     *
*          THE RACHECK PRE-PROCESSING EXIT ISSUES A GETMAIN           *
*          FOR AN AREA AND SAVES THE ORIGINAL RESOURCE NAME           *
*          AND VOLSER IF THE RESOURCE CLASS IS "DATASET".             *
*          SEE THE MACRO SMF180 AND PASSMAC FOR MORE DETAIL.          *
*                                                                     *
*    REGISTERS:                                                       *
*                                                                     *
*        R0  - WORK REGISTERS                                         *
*        R1  - WORK REGISTERS                                         *
*        R2  - WORK REGISTERS                                         *
*        R3  - ADDRESS OF RESOURCE CLASS                              *
*        R4  - ADDRESS OF ACEE                                        *
*        R5  - ADDRESS OF ABEND WORK AREA                             *
*        R6  - ADDRESS OF RETURN CODE AREA                            *
*        R7  - UNUSED                                                 *
*        R8  - UNUSED                                                 *
*        R9  - UNUSED                                                 *
*        R10 - ADDRESS OF PASSED WORK AREA                            *
*        R11 - ADDRESS OF PARAMETER AREA                              *
*        R12 - BASE REGISTER                                          *
*        R13 - ADDRESS OF SAVE & WORK AREAS                           *
*        R14 - SYSTEM & WORK REGISTERS                                *
*        R15 - SYSTEM & WORK REGISTERS                                *
*                                                                     *
*        TESTING:                                                     *
*        1. A DRIVER IS USED TO CALL THIS ROUTINE.                    *
*        2. THE DRIVED WILL PASS THE PROPER PARM LIST.                *
*        3. THE PARM LIST FIRST ENTRY IS NORMALLY THE NUMER OF        *
*           WORDS IN THE PARM LIST.                                   *
*           A. IF THE FIRST BYTE OF THE FIRST WORD IS X'FF' THEN      *
*              THE FIRST WORD CONTAIN A PSEUDO CVT ADDRESS.           *
*           B. IF THE FIRST BYTE OF THE FIRST WORD IS NOT X'FF'       *
*              THEN PROCEED AS NORMAL.                                *
*        4. AFTER TESTING THE FIRST WORD, EITHER THE REAL CVT ADDRESS *
*           OR A PSEUDO CVT ADDRESS PASSED BY THE DRIVER IS STORED    *
*           AND LATER USED FOR ALL CONTROL BLOCK PROCESSING.          *
*        5. THIS TECHNIQUE ALLOWS FOR PASSING (CVT, RCVT, ASCB, ASXB, *
*           ACEE).                                                    *
*                                                                     *
*_____________________________________________________________________*
*                   CHANGE LOG:                                       *
*_____________________________________________________________________*
* 07/30/80 - ADDED CHECK FOR CLASS(DATASET) BEFORE FREEMAIN OF PASSED *
*            DSNAME WORKAREA.                                         *
* 09/25/80 - CHANGE GETMAIN/FREEMAIN TO SUB POOL 252 FROM 0.          *
*            CHANGE TO NOT WRITE ANY SMF 180 RECORDS.                 *
* 10/06/80 - CHANGE GETMAIN/FREEMAIN TO SUB POOL 253 FROM 252.        *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*        SETUP SAVEAREA AND SAVE CALLER'S REGS
***********************************************************************
ICHRCX02 CSECT
         USING ICHRCX02,R15
         B     ICHRCX                     BRANCH AROUND I.D.
         DC    C'ICHRCX02'                CSECT NAME
         DC    C'&SYSDATE'                ASSEMBLY DATE
         DC    C'&SYSTIME'                ASSEMBLY TIME
         USING SAVEAREA,R13
***********************************************************************
*        SET UP BASE REGISTERS
***********************************************************************
ICHRCX   STM   R14,R12,SAVER14            SAVE REGS
         LR    R12,R15                    SET UP BASE REGISTER
         USING ICHRCX02,R12
         USING RCVT,R1
         USING ACEE,R4
         USING CX01PARM,R11
         DROP  R15
         LR    R11,R1              SETUP PARAMETER REGISTER
         GETMAIN R,LV=WORKSIZE,SP=253 GET SAVEAREA + WORKAREA
         ST    R1,SAVENEW-SAVEAREA(,R13)  FORWARD POINTER TO OLD AREA
         ST    R13,SAVEOLD-SAVEAREA(,R1)  BACKWARD POINTER TO NEW AREA
         LR    R13,R1              SETUP SAVEAREA POINTER
***********************************************************************
*        SET UP CVT ADDRESS
***********************************************************************
         MVC   CVTADR,X'10'        CVT ADDRESS
         CLI   0(R11),X'FF'        DRIVER CALLING
         BNE   CVTOK               NO, USE REAL CVT ADDRESS
         MVC   CVTADR+1(3),1(R11)  USE PASSED CVT ADDRESS
         EJECT
CVTOK    DS    0H
         L     R1,CX01ACEE         ADDRESS OF WORD WITH ACEE
         LTR   R1,R1               IS THERE A PASSED ACEE
         BZ    NOPASS1             NO,
         MVC   PASSACEE,0(R1)      SAVE THE ACEE ADDRESS
NOPASS1  L     R1,CVTADR           CVT ADDRESS
         L     R1,0(,R1)           CVTTCBP
         L     R1,12(,R1)          CURRENT ASCB
ASCBJBNI ICM   R2,7,172+1(R1)      ASCBJBNS
         BNZ   *+4+4               IF NOT ZERO WE GOT JOBNAME
ASCBJBNS ICM   R2,7,176+1(R1)      ASCBJBNS
         MVC   JOBNAME,0(R2)       SAVE THE JOBNAME
         L     R1,108(,R1)         ASXB
         MVC   ASCBACEE,200(R1)    SAVE THE ACEE ADDRESS
         SPACE
         L     R3,24(,R11)         ADDRESS OF CLASS NAME
         CLC   1(7,R3),=C'DATASET' IS THE ENTITY CLASS DATASET?
         BNE   RACFRET2            NO, GO AWAY - DONT FREEMAIN DSN AREA
***********************************************************************
*        OBTAIN ORIGINAL DSNAME & VOLSER FROM PRE-PROCESSOR
***********************************************************************
         L     R1,36(,R11)         LOAD ADDRESS OF WORKAREA
         L     R10,0(,R1)          LOAD ADDRESS OF PASSED AREA
         USING PASSDSCT,R10        ADDRESSABILITY
         LM    R5,R9,12(R11)       ADDRESS OF FLAG3, DSN, VOLSER
         TM    0(R5),X'20'         PROFILE OR DSN RESOURCE
         BZ    *+4+4+4             BR IF DSN
         LA    R9,62(,R7)          ADD OF VOLSER LIST IN PROFILE
         LA    R7,4(,R7)           ADD OF DSN IN PROFILE
         BCTR  R7,0                ADD OF DSN LENGTH
         MVC   0(45,R7),PASSESIZ   RESET TO ORIGINAL DSN & LENGTH
         MVC   0(6,R9),PASSVOLS    RESET TO ORIGINAL VOLSER
         L     R1,4(,R11)          LOAD ADDRESS OF FLAG1
         MVC   0(1,R1),PASSFLG1    RESET TO ORIGINAL FLAG1
         EJECT
***********************************************************************
*        BUILD & WRITE THE SMF180 FROM THE PASSED WORK AREA
***********************************************************************
         XC    SMFRD180(L'SMF180SZ),SMFRD180 CLEAR SMF AREA
         MVC   SMF180LN,=AL2(L'SMF180SZ)  SIZE OF RECORD
         MVC   SMF180SG,=AL2(0)    SIZE OF SEGMENTS
         MVI   SMF180SF,X'02'      VS2 SYSTEM
         MVI   SMF180TP,180        RECORD TYPE
         OI    SMF180F1,SMFFG1CK   RACHECK
         MVI   SMF180F2,SMFFG2OK   PRESET TO OK
         SPACE
         L     R1,CVTADR                  GET CVT ADDRESS
         ICM   R1,7,992+1(R1)             GET CVTRAC ADDRESS
         TM    53(R1),X'80'               IS RACF ACTIVE?
         BNZ   NORACF                     NO
         TM    153(R1),X'80'              HAS RACF INACTIVE BEEN ISSUED
         BNZ   NORACF                     BRANCH IF RVARY WAS ISSUED
         SPACE
RACFUP   DS    0H
         L     R1,16(0,0)          CVT ADDRESS
         L     R1,196(0,R1)        ADDRESS OF SMCA
         LTR   R1,R1               TEST FOR SMF
         BZ    NOSMF               NONE,
         MVC   SMF180ID,16(R1)     SYSTEM ID
         TIME  BIN                 TIME MACRO
         STM   0,1,SMF180TM        TIME AND DATE
         MVC   SMF180JB(8),JOBNAME JOBNAME
         LM    R5,R6,40(R11)       ADDRESS OF ABEND & RETURN CODES
         MVC   SMF180CL,1(R3)      CLASS NAME
         LTR   R10,R10             TEST FOR PASSED RESOURCE NAME
         BZ    NOPASS01            NONE,
         MVC   SMF180RS,PASSENTY   RESOURCE NAME
         MVC   SMF180VL,PASSVOLS   VOLSER
NOPASS02 MVC   SMF180AB,0(R5)      ABEND CODE
         MVC   SMF180RT,0(R6)      RETURN CODE
         L     R4,PASSACEE         ADDRESS OF ACEE
         LTR   R4,R4               ANY PASSED ACEE
         BNZ   ACEEPAS             YES, THEN USE IT
         L     R4,ASCBACEE         ADDRESS OF ASCB ACEE
         LTR   R4,R4               ANY ASCB ACEE
         BZ    NOACEE              NO, ERROR ON ACEE
ACEEPAS  MVC   SMF180EE(100),0(R4) ACEE
         L     R2,ACEEIEP          ACEE INSTALLATION POINTER
         USING IEPDSECT,R2         ADDESSABILITY
         MVC   SMF180E1,IEPAUTH    GROUP AUTHORITY
         DROP  R2
CK4ERR   CLC   SMF180AB,=F'0'      ABENDED ?
         BNZ   LOGERR              YES,
         CLC   SMF180RT,=F'0'      RETURN CODE ERROR ?
         BNZ   LOGERR              YES,
         B     RACFNXT1            GO TO NEXT FUNCTION
NOACEE   OI    SMF180F3,SMFFG3NA   NO ACEE
         MVI   SMF180F2,SMFFG2ER   SOME ERROR
         B     CK4ERR              CHECK RETURN/ABEND CODE
LOGERR   OI    SMF180F3,SMFFG3AA   ACCESS ERROR
         MVI   SMF180F2,SMFFG2ER   SOME ERROR
         B     RACFNXT1            GO TO NEXT FUNCTION
         EJECT
***********************************************************************
*        GENERATE A GTF USER RECORD OF THE RACHECK PARAMETERS
***********************************************************************
RACFNXT1 DS    0H
         L     R1,CVTADR                  GET CVT ADDRESS
         ICM   R1,7,992+1(R1)             GET CVTRAC ADDRESS
         TM    RCVTCAUD+3,X'01'           TEST BIT POS 31. OPTA
         BNO   NOGTF01                    NOT ON, BYPASS GTF
         XC    GTFREC01,GTFREC01          CLEAN OUT THE RECORD
         MVC   GTFJOBNM,SMF180JB          JOBNAME TO GTF RECORD
         MVC   GTFRESNM,SMF180RS          RESOURCE TO GTF RECORD
         MVC   GTFVOLSR,SMF180VL          VOLSER TO GTF RECORD
         MVC   GTFABDCD,SMF180AB          ABEND TO GTF RECORD
         MVC   GTFRETCD,SMF180RT          RETURN TO GTF RECORD
         MVC   GTFACEE,SMF180EE           ACEE TO GTF RECORD
         MVC   GTFLIST(GTFLISTL),GTFPROTO MOVE IN PROTOTYPE LIST
         LA    R0,GTFREC01                ADD OF GTF USER RECORD
         GTRACE DATA=(R0),ID=1,PAGEIN=YES,MF=(E,GTFLIST)
NOGTF01  DS    0H
***********************************************************************
*        WTO THE JOBNAME AND DSNAME OF THE DATASET BEING RACHECKED
***********************************************************************
         L     R1,CVTADR                  GET CVT ADDRESS
         ICM   R1,7,992+1(R1)             GET CVTRAC ADDRESS
         TM    RCVTCSTA+3,X'01'           TEST BIT POS 31. OPTA
         BNO   NOWTO01                    NOT ON, BYPASS WTO
         MVC   WTOLIST(WTOLISTL),WTOPROTO INITIALIZE WTO AREA
         MVC   MSG1(MSG1LGTH),MSG1PRTO    INITIALIZE MESSAGE
         MVC   MSG1JOBN,SMF180JB          MOVE IN THE JOBNAME
         MVC   MSG1RESN,SMF180RS          MOVE IN RESOURCE
         MVC   MSG1VOL,SMF180VL           MOVE IN VOLSER
         UNPK  WORK(9),SMF180AB(5)        CONVERT ABEND CODE
         TR    WORK(8),HEXTAB-240         TO CHAR
         MVC   MSG1ABNC,WORK              MOVE IN ABEND CODE
         UNPK  WORK(9),SMF180RT(5)        CONVERT RETURN CODE
         TR    WORK(8),HEXTAB-240         TO CHAR
         MVC   MSG1RETC,WORK              MOVE IN RETURN CODE
         MVC   WTOLIST+4(MSG1LGTH),MSG1   MESSAGE TO WTO
         WTO   MF=(E,WTOLIST)             WRITE IT OUT
NOWTO01  DS    0H
         EJECT
***********************************************************************
*        WRITE THE SMF180 RECORD.
***********************************************************************
SMFOUT   DS    0H
         B     RACFRET              BYPASS "ALL" SMF180 RECORDS
         CLI   SMF180F2,SMFFG2OK    WAS THE RACHECK OK
         BE    RACFRET              CHANGE TO LOG EVERYTHING
         SPACE
         SMFWTM SMFRD180
         LTR   R15,R15              TEST FOR GOOD SMF WRITE
         BZ    SMFOK                OK
SMFMSG   LA    R2,REPLY             POINT TO REPLY AREA
         LA    R3,WTORECB           POINT TO ECB
         XC    REPLY,REPLY          CLEAR REPLY AREA
         XC    WTORECB,WTORECB      CLEAR ECB
         WTOR  '*** WARNING *** RACF COULD NOT WRITE TO SMF, REPLY C (TX
               O CANCEL) OR U (TO RETRY)',(2),L'REPLY,(3),             X
               ROUTCDE=(9,11)
         WAIT  ECB=WTORECB          WAIT FOR A REPLY
         CLI   REPLY,C'U'           REPLY = RETRY
         BE    SMFOUT               YES,
         CLI   REPLY,C'C'           REPLY = CANCEL
         BE    SMFOK                YES, JUST DO NOT LOG
         B     SMFMSG               ASK AGAIN
SMFOK    DS    0H
         B     RACFRET             RETURN
         EJECT
***********************************************************************
*        ACEE NOT FOUND - WARNING & SHOW IN SMF180 RECORD
***********************************************************************
NOPASS01 DS    0H
         WTO   '*** WARNING *** DSNAME NOT PASSED TO RACHECK - POST',  X
               ROUTCDE=(9,11)
         MVI   SMF180F2,SMFFG2ER   SOME ERROR
         OI    SMF180F3,SMFFG3RE   INVALID RESOURCE NAME
         B     NOPASS02
***********************************************************************
*        RACF TURNED OFF - WARNING & SHOW IN SMF180 RECORD
***********************************************************************
NORACF   DS    0H
         WTO   '*** WARNING *** RACF NOT ACTIVE - ACCESS LOGGED',      X
               ROUTCDE=(9,11)
         MVI   SMF180F2,SMFFG2ER   SOME ERROR
         OI    SMF180F3,SMFFG3NR   RACF TURNED OFF
         B     RACFUP
***********************************************************************
*        SMF TURNED OFF - WARNING & RETURN
***********************************************************************
NOSMF    DS    0H
         WTO   '*** WARNING *** SMF NOT ACTIVE - RACHECK NOT LOGGED',  X
               ROUTCDE=(9,11)
         B     RACFRET
RACFRET  DS    0H
***********************************************************************
*        FREEMAIN PASSED DSNAME AREA FOR ICHRCX01
***********************************************************************
         L     R0,PASSPSIZ         SP & LENGTH OF AREA
         LR    R1,R10              ADDRESS OF AREA
         FREEMAIN R,LV=(0),A=(1)   FREE UP PASSED WORKAREA
RACFRET2 DS    0H
***********************************************************************
*        FREEMAIN NEW SAVEAREA AND RETURN TO CALLER
***********************************************************************
         LR    R1,R13              SAVE NEW SAVEAREA ADR FOR FREEMAIN
         L     R13,SAVEOLD         OLD SAVEAREA
         FREEMAIN R,A=(1),LV=WORKSIZE,SP=253 FREE UP NEW SAVEAREA
         XR    R15,R15             CONTINUE SVC PROCESSING
         ST    R15,SAVER15         PLUG (R15) FOR RESTORE (R14-R12)
         XC    SAVENEW,SAVENEW     BREAK CHAIN
         LM    R14,R12,SAVER14     RESTORE REGS
         BR    R14                 BACK TO MOTHER
         DROP  ,                   DROP EVERYTHING
         EJECT
*
*        PARAMETER LISTS ECT.
*
HEXTAB   DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
GTFPROTO GTRACE LNG=256,ID=1,MF=L
GTFLISTL EQU   *-GTFPROTO              LENGTH OF PROTOTYPE GTRACE LIST
WTOPROTO WTO   '                                                       X
                                                                       X
                         ',                                            X
               MF=L,ROUTCDE=(9,11)
WTOLISTL EQU   *-WTOPROTO              LENGTH OF PROTOTYPE WTO LIST
MSG1PRTO EQU   *
         DC    C'ICHRCX02 JOB='
         DC    CL8' '
         DC    C',RET='
         DC    CL8' '
         DC    C',ABN='
         DC    CL8' '
         DC    C',VOL='
         DC    CL6' '
         DC    C',DSN='
         DC    CL44' '
MSG1LGTH EQU   *-MSG1PRTO
         LTORG
         EJECT
SAVEAREA DSECT
SAVEPLI  DS    F
SAVEOLD  DS    F
SAVENEW  DS    F
SAVER14  DS    F
SAVER15  DS    F
SAVER0   DS    F
SAVER1   DS    F
SAVER2   DS    F
SAVER3   DS    F
SAVER4   DS    F
SAVER5   DS    F
SAVER6   DS    F
SAVER7   DS    F
SAVER8   DS    F
SAVER9   DS    F
SAVER10  DS    F
SAVER11  DS    F
SAVER12  DS    F
SAVESIZE EQU   *-SAVEAREA
WORKAREA EQU   *                          START OF WORKAREA
WORK     DS    CL9                 WORK AREA
CVTADR   DS    F                   ADDRESS OF CVT
PASSACEE DS    F                   ACEE PASSED TO RACHECK
ASCBACEE DS    F                   ACEE CHAINED OFF ASCB
WTORECB  DS    F                   SMF WTOR ECB
REPLY    DS    CL1                 SMF WTOR REPLY AREA
JOBNAME  DS    CL8                 JOBNAME CHAINED OFF ASCB
         SMF180
GTFLIST  GTRACE LNG=256,ID=1,MF=L
WTOLIST  WTO   '                                                       X
                                                                       X
                         ',                                            X
               MF=L,ROUTCDE=(9,11)
MSG1     EQU   *
         DC    C'ICHRCX02 JOB='
MSG1JOBN DC    CL8' '
         DC    C',RET='
MSG1RETC DC    CL8' '
         DC    C',ABN='
MSG1ABNC DC    CL8' '
         DC    C',VOL='
MSG1VOL  DC    CL6' '
         DC    C',DSN='
MSG1RESN DC    CL44' '
GTFREC01 DS    XL256                      GTF USER RECORD TYPE 1
         ORG   GTFREC01                   OVERLAY THE DIFFERENT FIELDS
GTFJOBNM DS    CL8                        JOBNAME
GTFRESNM DS    CL44                       RESOURCE NAME
GTFVOLSR DS    CL6                        VOLSER
GTFABDCD DS    CL4                        ABEND CODE
GTFRETCD DS    CL4                        RETURN CODE
GTFACEE  DS    CL100                      ACEE
         ORG
WORKSIZE EQU   *-SAVEAREA
         EJECT
         PASSMAC
         IEPMACRO
CX01PARM DSECT
CX01WORD DS    F                   ADDRESS OF PARM COUNT IN LIST
CX01FLG1 DS    F                   ADDRESS OF FLAG BYTE 1
CX01FLG2 DS    F                   ADDRESS OF FLAG BYTE 2
CX01FLG3 DS    F                   ADDRESS OF FLAG BYTE 3
CX01INST DS    F                   ADDRESS OF INSTALL PARMS FOR USER
CX01ENTY DS    F                   ADDRESS OF ENTITY NAME
CX01CLAS DS    F                   ADDRESS OF CLASS NAME
CX01VOLN DS    F                   ADDRESS OF VOLSER
CX01USRD DS    F                   ADDRESS OF USER DATA FROM PROFILE
CX01WRKA DS    F                   ADDRESS OF WORKAREA BETWEEN EXITS
CX01ABND DS    F                   ADDRESS OF ABEND CODE FROM RACF-SVC
CX01RTRN DS    F                   ADDRESS OF RETURN CODE FROM RACF-SVC
CX01ACSS DS    F                   ADDRESS OF USERS'S AUTHORITY LEVEL
CX01RSNM DS    F                   ADDRESS OF LEVEL VALUE OF RESOURCE
CX01OLDV DS    F                   ADDRESS OF PREVIOUS DEFINED VOLSER
CX01NAMC DS    F                   ADDRESS OF NAMING CONV. PARM. LIST
CX01APPL DS    F                   ADDRESS OF APPLICATION NAME FROM SVC
CX01ACEE DS    F                   ADDRESS OF ACEE PASSED FROM SVC
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         ICHPRCVT
         IHAACEE
         END
