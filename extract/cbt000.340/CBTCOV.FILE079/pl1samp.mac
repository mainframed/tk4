//PL1SAMP  JOB  ,,MSGLEVEL=1,REGION=100K
//STEP   EXEC  PL1LFCLG,PARM.PL1L='SIZE=100000,L,E,A,X,M,S2'
//PL1L.SYSIN   DD   *
 PL1SAMP: PROC OPTIONS(MAIN);
 /* THIS SAMPLE PROGRAM DRAWS A CIRCLE OF 8 BASIC SIZE X'S WITH A LARGE
    X AS ITS CENTER. DEPENDING ON THE ATTENTION CREATED, THE PROGRAM WIL
    THE FOLLOWING:
      1. LIGHT PEN ATTENTION ON CENTER X LABELS OUTER X'S FROM NUM1
         THROUGH NUM8.
      2. LIGHT PEN ATTENTION ON OUTER X GENERATES A CIRCLE WHOSE CENTER
         IS THE DETECTED X.
      3. LIGHT PEN ATTENTION ON CIRCLE AROUND OUTER X MAKES THAT CIRCLE
         INVISIBLE.
      4. LIGHT PEN ATTENTION ON ANY LABEL MAKES ALL LABELS INVISIBLE.
      5. PROGRAMMED FUNCTION KEY 1 ENDS RUN.
      6. PROGRAMMED FUNCTION KEY 2 MAKES ALL CIRCLES INVISIBLE.
      7. PROGRAMMED FUNCTION KEY 3 MAKES ALL CIRCLES VISIBLE. */
 /*       BEGIN PROGRAM
  SET UP DECLARE STATEMENTS FOR VALUES TO BE USED.*/
       DCL LABEL(8)CHAR(4);
    DCL  (CIRX(8),CIRY(8),SMALX(8,24),SMALY(8,24))  FLOAT BIN;
     DCL (THETA,RADIAN,R,C,CX) FLOAT BIN;
    DCL STPOS ENTRY(,FLOAT BIN,FLOAT BIN);
 DCL(IGSP,I0,I2250,IGDS1,IGDS2,IGDS3,IATL,IX,J,K,L,M,N,IV,JV,NUM,
   NULL,INTCD,I) FIXED BIN (31,0);
       DCL X CHAR(1), INTARY(10) FIXED BIN(31,0);
        X='X';
        LABEL(1)='NUM1';
        LABEL(2)='NUM2';
        LABEL(3)='NUM3';
        LABEL(4)='NUM4';
        LABEL(5)='NUM5';
        LABEL(6)='NUM6';
        LABEL(7)='NUM7';
        LABEL(8)='NUM8';
 /* DEFINE THE NULL VARIABLE.*/
     NULL=-4;
          ON ERROR  SNAP  CALL  IHEDUMP;
      CALL INGSP(IGSP,NULL);
 /*            INITIALIZE THE 2250. ASSIGN IT UNIT NUMBER 10.*/
      I0=10; CALL INDEV(IGSP,I0,I2250);
 /*            INITIALIZE A GRAPHIC DATA SET THAT WILL BE USED TO
               DISPLAY THE CIRCLE OF X'S AND ITS CENTER X.*/
      CALL INGDS(I2250,IGDS1);
 /* INITIALIZE A GRAPHIC DATA SET THAT WILL BE USED TO
  LABEL THE OUTER X'S OF THE CIRCLE.*/
      CALL INGDS(I2250,IGDS2);
 /* INITIALIZE A GRAPHIC DATA SET THAT WILL BE USED TO
 DISPLAY A CIRCLE AROUND ANY OF THE OUTER X'S,*/
      CALL INGDS(I2250,IGDS3);
 /* CREATE AN ATTENTION LEVEL FOR THE 2250.*/
      CALL CRATL(I2250,IATL);
 /* ENABLE PROGRAMMED FUNCTION KEYS 1,2, +3 +LIGHT PEN.*/
           J=-3; K=34; L=3;  M=2; N=4; IV=24;   JV=8; IX=1;
      CALL ENATN(IATL,IX,J,K);
      CALL MLITS(I2250,L);
 /* ALLOW LIGHT PEN ATTENTIONS FOR EACH GRAPHIC DATA SET. */
      CALL SLPAT(IGDS1,IX);
      CALL SLPAT(IGDS2,IX);
      CALL SLPAT(IGDS3,IX);
 /* SET BEAM POSITION TO CENTER OF CIRCLE. */
      CX=2047.0;
      CY=2047.0;
 /* SET CHARACTER MODE FOR LARGE SIZE, PROTECTED. */
      CALL SCHAM(IGDS1,M);
      NUM=-1;
      CALL PTEXT(IGDS1,X,IX,NUM,NULL,IX,CX,CY);
  /*           COMPUTE X AND Y COORDINATES FOR X'S OF OUTER CIRCLE.*/
 /* PLACE THEM IN THE CIRX AND CIRY ARRAYS. */
      R=1200.0;
      C=3.141596/180;
      THETA=45.0;
  TEN:DO I=1 TO 8;
      RADIAN=THETA*FLOAT(I)*C;
      CIRX(I)=CX+R*COS(RADIAN);
      CIRY(I)=CY+R*SIN(RADIAN);
      END TEN;
 /* SET CHARACTER MODE FOR BASIC SIZE, PROTECTED. */
      CALL SCHAM(IGDS1,IX);
 /* GENERATE AN X IN INCLUDE STATUS. CALCULATE
  X-COORDINATE OF LABEL ASSOCIATED WITH THAT X.
  GENERATE LABEL IN INCLUDE STATUS. SET INCR TO
  NEXT LABEL IN ARRAY. DO THIS 8 TIMES
  TO COMPLETE CIRCLE. */
 FIVE:DO NUM=1 TO 8;
      CALL PTEXT(IGDS1,X,IX,NUM,NULL,IX,CIRX(NUM),CIRY(NUM));
      CXNUM=CIRX(NUM)+300.0;
      CALL PTEXT(IGDS2,LABEL(NUM),N,NUM,NULL,IX,CXNUM,CIRY(NUM));
      END FIVE;
 /*            COMPUTE COORDINATES FOR CIRCLES AROUND OUTER X'S.*/
 /* PLACE THEM IN SMALX AND SMALY ARRAYS. */
      R=200.0;
      THETA=360.0/24.0;
  ONE:DO N=1 TO 24;
      RADIAN=THETA*FLOAT(N)*C;
  TWO:DO I=1 TO 8;
      SMALX(I,N)=CIRX(I)+R*COS(RADIAN);
      SMALY(I,N)=CIRY(I)+R*SIN(RADIAN);
      END ONE;
 /*            GENERATE CIRCLES AROUND OUTER X'S IN OMIT STATUS.*/
  SIX:DO NUM=1 TO 8;
      CALL STPOS(IGDS3,CIRX(NUM)+200.0,CIRY(NUM));
      CALL PLINE(IGDS3,SMALX(NUM,IX),SMALY(NUM,IX),NUM,NULL,
      M,IV,IX,IX);
       END SIX;
 /*            OMIT LABELS FROM THE DISPLAY. */
      CALL OMIT(IGDS2);
 /*            EXECUTE ALL GRAPHIC DATA PRODUCED. */
      CALL EXEC(IGDS1);
      CALL EXEC(IGDS2);
      CALL EXEC(IGDS3);
 /*            REQUEST ATTENTION INFORMATION. WAIT FOR ATTN TO OCCUR. */
 HERE:CALL RQATN(IATL,INTCD,M,INTARY(1),IX,M,L,K);
 /* IF LIGHT PEN ATTENTION, GO TO NEXT.
  IF PROGRAMMED FUNCTION KEY ATTENTION, GO TO CONT. */
      IF INTCD=34 THEN GO TO NEXT; ELSE GO TO CONT;
 /*            CHECK WHICH DATA SET ATTENTION OCCURRED IN. */
 NEXT:IF INTARY(1)=IGDS1 THEN GO TO SO;
      IF INTARY(1)=IGDS2 THEN GO TO TO;
      IF INTARY(1)=IGDS3 THEN GO TO MO; ELSE GO TO HERE;
 /*            IF LPEN ATTN WAS ON CENTER OF LARGE CIRCLE, GO TO IN.
               IF ON OUTER CIRCLE, GO TO OUT. */
  SO: IF INTARY(4)>=0 THEN GO TO OUT;
 /*            DISPLAY LABELS AND WAIT FOR ANOTHER ATTN. */
   IN:CALL INCL(IGDS2);
      GO TO HERE;
   OUT:NUM=INTARY(4);
     CALL INCL(IGDS3,NUM);
       GO TO HERE;
 /*            REMOVE LABELS FROM DISPLAY. AWAIT NEW ATTN.*/
   TO: CALL OMIT(IGDS2);
       GO TO HERE;
 /*            REMOVE DETECTED CIRCLE FROM DISPLAY. AWAIT ATTN.*/
   MO: NUM=INTARY(4);
       CALL OMIT(IGDS3,NUM);
       GO TO HERE;
 /*            IF PFK1 DEPRESSED, GO TO THRE. IF PFK2, GO TO FOUR.
               IF PFK3, GO TO FINI. */
 CONT:IF INTCD=1 THEN GO TO THRE;
      IF INTCD=2 THEN GO TO FOUR;
      IF INTCD=3 THEN GO TO FINI;
 /*            TERMINATE THE PROGRAM RUN. */
 THRE:CALL TMGSP(IGSP);
      GO TO EXIT;
 /*            REMOVE ALL CIRCLES AROUND OUTER X'S. AWAIT ATTN. */
 FOUR:DO I=1 TO 8;
      CALL OMIT(IGDS3,I);
      END FOUR;
      GO TO HERE;
 /*            DISPLAY ALL CIRCLES AROUND OUTER X'S. AWAIT ATTN. */
 FINI:DO I=1 TO 8;
      CALL INCL(IGDS3,I);
      END FINI;
      GO TO HERE;
 EXIT: END PL1SAMP;
//LKED.SYSIN   DD   *
  INCLUDE  SYSLIB(IHEGSP01)
  INCLUDE  SYSLIB(IHEGSP02)
  INCLUDE  SYSLIB(IHEGSP03)
//GO.PL1DUMP   DD   SYSOUT=A
//GO.FT10F001 DD UNIT=2250-1
//GO.SYSUDUMP  DD   SYSOUT=A
/*
