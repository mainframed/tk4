//SMFE35B  JOB MSGLEVEL=1
//STEPB  EXEC  ASMFCL
//ASM.SYSIN DD *
*     *     *     *     *     *     *     *     *     *     *    *    *
* THIS ROUTINE  RE-INSERTS THE NON-JOB ORIENTED RECORDS INTO THE FINAL*
* OUTPUT OF OS/SORT.AT THIS POINT,THE JOB ORIENTED RECORDS HAVE BEEN  *
* SORTED INTO INDIVIDUAL JOB SEQUENCE.THE NON-JOB ORIENTED RECORDS    *
* WILL BE PLACED SO AS TO INDICATE CHANGES TO SYSTEM STATUS BETWEEN   *
* JOBS.                                                               *
*                                                                     *
*        CSECT NAME-E35                                               *
*                                                                     *
*        ENTRY POINT-E35                                              *
*                                                                     *
*        EXIT TO OS/SORT                                              *
*                                                                     *
*        RETURN CODES USED                                            *
*          RC=0  NO ACTION                                            *
*          RC=8  DO NOT RETURN                                        *
*          RC=12 INSERT RECORD                                        *
*                                                                     *
*        INPUT A-REGISTER 1 POINTS TO A 3 WORD PARAMETER LIST THAT    *
*                CONTAINS-                                            *
*                   1-ADDRESS OF RECORD LEAVING MERGE                 *
*                   2-ADDRESS OF RECORD IN OUTPUT AREA                *
*                   3-SEQUENCE CHECK SWITCH (NOT USED BY THIS ROUTINE)*
*                                                                     *
*              B-TEMPORARY DATA SET CONTAINING NON-JOB ORIENTED       *
*                RECORDS.THESE RECORDS WERE EXTRACTED DURING THE SORT *
*                PHASE BY AN E15 EXIT ROUTINE                         *
*                                                                     *
*        OUTPUT  RETURN TO OS/SORT WITH RC=12 AND ADDRESS OF RECORD TO*
*                BE INSERTED IN REGISTER 1                            *
*                                                                     *
*        NOTES - THE BUFFER SIZE USED IN THIS ROUTINE WAS DEFINED TO  *
*                HOLD A MAXIMUM RECORD OF 392 BYTES.  SOME INSTALLA-  *
*                TIONS MAY USE LARGER RECORDS AND SHOULD REDEFINE     *
*                THIS BUFFER TO HOLD THEIR MAXIMUM RECORD SIZE.       *
*                                                                     *
*     *     *     *     *     *     *     *     *     *     *    *    *
R00      EQU   0                   GET MOVE RECORD SAVE AREA     A35543
R01      EQU   1                   PARAMETER REGISTER
R02      EQU   2                   WORK REGISTER
IV       EQU   4
VIII     EQU   8
R08      EQU   8                   SOR ADDRESS REGISTER
R10      EQU   10                  WORKING REGISTER FOR SORT RECD ADDR
R11      EQU   11                  PARAMETER LIST ADDRESS REGISTER
R12      EQU   12                  BASE REGISTER
R13      EQU   13                  SAVE AREA ADDRESS REGISTER
R15      EQU   15                  ENTRY POINT REGISTER
INSERT   EQU   X'01'
JTERM    EQU   X'05'
ONCE     EQU   X'20'               1ST SOR PROCESS SWITCH
CLOSONCE EQU   X'24'               SWITCH MASKS/HDRS DONE & 1ST SOR
ONCEOVER EQU   X'DF'
INSOFF   EQU   X'FE'
CLOSED   EQU   X'02'
CLOSEA   EQU   X'04'
OPENA    EQU   X'08'
PASS2    EQU   X'10'
E35      CSECT
* 715000-720500                                                  A35184
* 17050,36550-37000,44000,45550-47000,52000,57550-58500          A35543
* 63550-64550,70000,70050,72100-72150                            A35543
         USING *,R12               ESTABLISH ADDRESSABILTY
         SAVE  (14,12)             SAVE CALLER'S REGS
         LR    R12,R15             LOAD BASE ADDRESS
         USING SORFORM,R08
         USING JORFORM,R10
         ST    R13,SAVDS+4         CHAIN SORT SAVEAREA TO E35
         LR    R02,R13             RETAIN ADDRESS OF SORT'S AREA
         LA    R13,SAVDS           ADDRESS OF E35 SAVE AREA
         ST    R13,8(R02)          CHAIN E35 SAVEAREA TO SORT
         LR    R11,R01             RETAIN PARAMETER LIST ADDRESS
         LA    R02,IV              CREATE NO SEQ CHECK BIT
         ST    R02,VIII(R11)       PLACE IN SORT PARM LIST
         TM    BITS,CLOSEA         IS HDR SET CLOSED
         BO    E35PROC             YES,BYPASS HDR INSERTIONS
         TM    BITS,OPENA          IS HDR SET OPEN
         BO    E35XCPTA            YES,BYPASS OPENING
         OPEN  E35DCBA             NO,OPEN FOR FIRST SCAN
         OI    BITS,OPENA          TURN ON OPEN INDICATOR
E35XCPTA LA    R00,HTBUFF          GET ADDR OF HDR/TLR REC AREA  A35543
         GET   E35DCBA             GET A HDR/TLR RECORD          A35543
         CLC   5(1,R01),ID         IS THE THE RIGHT TYPE
         BNE   E35XCPTA            NO,GET NEXT RECORD
         B     E35INRTN            YES,GO TO INSERT RETURN
E35EODID CLOSE E35DCBA             CLOSE HDR/TLR SET
         TM    BITS,PASS2          HAS SET BEEN CLOSED ONCE
         BO    E35XCPTB            SET END OF PHASE BIT AND GO ON
         MVI   ID,X'03'            NO,CHANGE ID BEING SCANNED FOR
         OPEN  E35DCBA             OPEN HDR/TLR SET SECOND TIME
         OI    BITS,PASS2          INDICATE HDR/TLR SET CLOSED ONCE
         B     E35XCPTA            GO PERFORM SCAN
E35XCPTB OI    BITS,CLOSONCE       SET SWITCHES TO INDICATE HDR SET    X
               SET CLOSED AND FIRST TIME FOR SOR PROCESSING
E35PROC  L     R08,CSORSAV         LOAD ADDRESS OF CURRENT SOR   A35543
         LTR   R08,R08             IS IT ZERO
         BNE   E35PRCS             NO,DATA SET IS OPEN
         OPEN  E35DCB              OPEN DATA SET CREATED AT E15 EXIT
         LA    R00,BUFF1           GET ADDR OF 1ST REC SAVEAREA  A35543
         GET E35DCB                GET THE FIRST RECORD          A35543
         ST    R01,CSORSAV         SAVE THE ADDRESS OF RECORD    A35543
E35PRCS  L     R08,CSORSAV         LOAD DSECT BASE REGISTER      A35543
         L     R10,0(R11)          ADDRESS OF RECORD LEAVING MERGE
         LTR   R10,R10             IS IT ZERO
         BZ    E35ENDA             YES,GO FINISH UP SOR INSERTIONS
         TM    BITS,INSERT         IS INSERT BIT ON
         BO    E35COMPA            YES,CHECK FOR MORE INSERTS NOW
         TM    BITS,ONCE           IS 1ST SOR PROCESS BIT SET
         BO    E35COMPB            YES,GO SET UP FOR NORM PROCESSING
         CLI   JORID,JTERM         IS THIS A JOB TERM RECD
         BE    E35COMPA            YES,CHECK FOR INSERTS
E35NOACT ST    R08,CSORSAV         SAVE ADDRESS OF SOR           A35543
         NI    BITS,INSOFF         TURN OFF INSERT BIT
         L     R01,0(R11)          ADDRESS OF RECD LEAVING MERGE TO R1
         L     R13,SAVDS+4         ADDRESS OF SORT'S SAVE AREA
         ST    R01,24(R13)         ADRESS OF RECD INTO SORT SAVEAREA
         RETURN (14,12),RC=0       RETURN TO SORT-NO ACTION
E35COMPA CLC   SORDATE,JORDATE    COMPARE RECORDED DATES
         BH    E35NOACT            SORDATE GTR,NORMAL RETURN TO SORT
         CLC   SORTIME,JORTIME
         BH    E35NOACT            SORTIME
         TM    BITS,CLOSED         IS SOR SET CLOSED ?
         BO    E35ENDB             YES,MAKE FINAL RETURN TO SORT
         L     R00,NSORSAV         FILL EMPTY BUFF WITH NXT REC  A35543
         GET   E35DCB              GET NEXT RECORD               A35543
         ST    R01,CSORSAV         SAVE ADDRESS OF GOTTEN REC    A35543
         ST    R08,NSORSAV         SET BUFFER PTR FOR NEXT ENTRY A35543
         LR    R01,R08             ADDRESS OF RECD TO INSERT TO REG 1
         OI    BITS,INSERT         TURN ON INSERT SWITCH
E35INRTN L     R13,SAVDS+4         ADDRESS OF SORT SAVE AREA
         ST    R01,24(R13)         ADRESS OF RECD INTO SORT SAVEAREA
         RETURN (14,12),RC=12      RETURN TO SORT W/RC=INSERT
E35COMPB L     R10,0(R11)          ADDRESS OF RECD LEAVING MERGE
         NI    BITS,ONCEOVER       TURN 1ST SOR PROCESS BIT OFF
         B     E35COMPA            GO COMPARE FOR INSERTS
E35ENDA  TM    BITS,CLOSED         IS E35DCB CLOSED
         BO    E35ENDB             YES,MAKE FINAL RETURN TO SORT
         L     R00,NSORSAV         GET EMPTY REC SAVE AREA ADDR  A35543
         GET   E35DCB              GET NEXT SOR                  A35543
         ST    R01,CSORSAV         SAVE ADDRESS OF SOR           A35543
         ST    R08,NSORSAV         SAVE ADDR OF AVAIL BUFF       A35543
E35ENDC  LR    R01,R08             LOAD PARAMETER REG. W/ADDR OF PREV.
         L     R13,SAVDS+4         ADDRESS OF SORT SAVE AREA
         ST    R01,24(R13)         ADRESS OF RECD INTO SORT SAVEAREA
         RETURN (14,12),RC=12
E35EODAD CLOSE E35DCB              CLOSE SOR SET
         OI    BITS,CLOSED         TURN ON CLOSED BIT
         B     E35ENDC             FINISH LAST INSERTION
E35ENDB  L     R13,SAVDS+4
         RETURN (14,12),RC=8       DO NOT RETURN
SAVDS    DS    18F                 REGISTER SAVE AREA
CSORSAV  DC    F'0'                                              A35543
NSORSAV  DC    A(BUFF2)                                          A35543
BITS     DC    X'00'
ID       DC    X'02'
E35DCB   DCB   DSORG=PS,MACRF=GM,DDNAME=SORDATA,EODAD=E35EODAD   A35184
E35DCBA  DCB   DSORG=PS,MACRF=GM,DDNAME=HDRDATA,EODAD=E35EODID   A35184
HTBUFF   DS    CL18                BUFFER FOR HDR/TLR RECORDS    A35184
BUFF1    DS    CL400               BUFF #1 TO REBUILD SEGTD REC  A35543
BUFF2    DS    CL400               BUFF #2 TO REBUILD SEGTD REC  A35543
SORFORM  DSECT
SORLNGTH DS    CL4
SORCONT  DS    C
SORID    DS    C
SORTIME  DS    CL4
SORDATE  DS    CL4
SORCPUID DS    CL4
         DS    CL32
SORJLGNO DS    0CL13
SORJNAME DS    CL8
SORSTIME DS    CL4
SORSDATE DS    CL4
SORSTPNO DS    C
JORFORM  DSECT
JORLNGTH DS    CL4
JORCONT  DS    C
JORID    DS    C
JORTIME  DS    CL4
JORDATE  DS    CL4
JORCPUID DS    CL4
         DS    CL32
JORJLGNO DS    0CL13
JORJNAME DS    CL8
JORSTIME DS    CL4
JORSDATE DS    CL4
JORSTPNO DS    C
         END
//LKED.SYSLMOD DD   DSNAME=SMF1.EXITS,DISP=(OLD,KEEP),                 C
//             VOLUME=REF=SYS1.LINKLIB,SPACE=(TRK,(10,5,1))
//LKED.SYSIN   DD  *
 NAME E35(R)
