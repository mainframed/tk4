IEFACTRT CSECT
* 008240,013200,022800,034400-035000,050400,054400                19874
*0019008400-008800,023000,023200,031800,048400,050600            22133*
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*                                                                     *
* FUNCTION                                                            *
*        THIS MODULE WRITES ACCOUNTING DATA IN THE ACCOUNT DATA SET   *
*        AND TIME AND DATE ON SYSOUT.                                 *
*                                                                     *
* ENTRY POINT                                                         *
*        IEFACTRT                                                     *
*                                                                     *
* INPUT                                                               *
*        REG. 0 CONTAINS A CODE, INDICATING WHICH TIME THE ROUTINE WAS*
*        CALLED                                                       *
*             CODE = 8  -  STEP INITIATION                            *
*                   12  -  STEP TERMINATION                           *
*                   16  -  JOB TERMINATION                            *
*                                                                     *
*        REG. 1 CONTAINS THE ADDRESS OF A LIST OF 4-BYTE POINTERS TO  *
*             JOB NAME                                                *
*             STEP NAME                     *                         *
*             PROGRAMMER NAME                                         *
*             JOB RUNNING TIME                                        *
*             JOB ACCOUNTING DATA                                     *
*             STEP RUNNING TIME             *                         *
*             STEP ACCOUNTING DATA          *                         *
*             FLAGS(BYTE 1), STEP NUMBER(BYTE 2)                      *
*        */ WILL BE ZERO AT JOB TERMINATION                           *
*                                                                     *
* OUTPUT                                                              *
*        A LINE ON SYSOUT CONTAINING DATE AND TIME OF DAY IN HOURS    *
*        WITH FOUR DECIMALS.                                          *
*                                                                     *
*        A RECORD IN THE ACCOUNT DATA SET.                            *
*        DESCRIPTION:                                                 *
*        BYTE     CODE = 8         CODE = 12        CODE = 16         *
*         1 -  3  'ACT'            'ACT'            'ACT'             *
*           4     '00001000'       '00001100'       00010000'         *
*         5 - 12  JOBNAME          JOBNAME          JOBNAME           *
*        13 - 16  DATE (PACKED)    DATE             DATE              *
*        17 - 20  TIME (PACKED)    TIME             TIME              *
*        21 - 28  PRO-             STEP NAME        NUMBER OF STEPS   *
*        29 - 36    GRAM-          RESERVED            (8 BYTES) 19874*
*          37         MER          STEP NUMBER (1 BYTE)          22133*
*        38 -40       NAME                                       22133*
*          41                                                    22133*
*        42 -     ACCOUNT DATA     ACCOUNT DATA                       *
*                   FROM JOB CARD    FROM EXEC CARD                   *
*                                                                     *
*        AT STEP INITIATION, THERE WILL BE OUTPUT ONLY IF THE STEP    *
*        NUMBER IS 1.                                                 *
*                                                                     *
*        IF THE ACCOUNT DATA CAN NOT BE WRITTEN IN THE ACCOUNT DATA   *
*        SET, DATE AND TIME IS WRITTEN TO CONSOLE                     *
*                                                                     *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
R0       EQU   0                   PARAMETER REG., WORK REG.
R1       EQU   1                   PARAMETER REG., WORK REG.
R2       EQU   2                   JOB NAME PTR
R3       EQU   3                   STEP NAME PTR
R4       EQU   4                   PROGRAMMER NAME PTR
RA        EQU  6                   JOB ACCOUNT DATA PTR
*                    IF AT SINIT 1ST BYTE OF DATA =FF   SET FAIL
R8       EQU   8                   STEP ACCOUNT DATA PTR, WORK REG.
R9       EQU   9                   FLAGS PTR
RB       EQU   10                  PROGRAM BASE REGISTER
RF       EQU   11                  FROM WHERE INDICATOR
R12      EQU   12                 POINTER FOR MESSAGE WRITING     19874
R13      EQU   13                  SAVE AREA POINTER
R14      EQU   14                  LINKAGE CONVENTION REG.
R15      EQU   15                  LINKAGE CONVENTION REG.
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         STM   R14,R12,12(R13)
         BALR  RB,0
         USING *,RB
         LM    R2,R9,0(R1)
         ST    R13,SAVE+4
         LA    R1,SAVE
         ST    R1,8(R13)
         LR    R13,R1
         MVI   RECID,0
         MVC   RECID+1(RECEND-RECID),RECID
         LR    RF,R0
         MVC   RECID(3),ACT
         MVC   RECJNAME,0(R2)      JOBNAME
         BAL   R14,YTIME           TIME AND DATE
         B     XFROMWH(RF)
XFROMWH  B     XJEND
         B     XERR3
         B     XINIT
         B     XSEND
         B     XJEND
XERR3    MVI   LINE,X'F4'
         B     XSEND
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
* INITIATION                                                          *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
XINIT    CLI   1(R9),1             IS IT THE 1.ST TIME IN THE JOB
         BNE   XIT                 NO  - EXIT
         MVC   RECPGMER(20),0(R4)  YES - PROGRAMMER NAME
         MVC   LINE+6(4),JOB
         MVC   LINID(8),0(R2)
         MVC   LINWHEN(5),INIT
         CLI   1(RA),X'FF'         IS IT TO BE FAILED
         BNE   XMOVACT             NO  - BRANCH
         MVI   0(R9),X'01'         YES - DO IT
         B     XMOVACT
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
* STEP END                                                            *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
XSEND    LR    RA,R8               SET ACCOUNT DATA ADDRESS
         MVC   LINE+6(4),BLANK
         MVC   RECSNAME,0(R3)      STEPNAME
         MVC   LINID(8),0(R3)
         MVC   LINWHEN(5),TERM
         MVI   SPACE,X'40'             BLANK OUT SPACE IN MESSAGE 19874
         MVC   SPACE+1(7),SPACE                                   19874
         MVC   RECSNO,1(R9)        STEP NUMBER
         B     XMOVACT
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
* JOB END                                                             *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
XJEND    MVC   RECNOSTP(1),1(R9)   NUMBER OF STEPS
         MVC   LINE+6(4),JOB
         MVC   LINID(8),0(R2)
         MVC   LINWHEN(5),TERM
         MVC   RECL,H21
         B     XWRITE
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
* MOVE ACCOUNT FIELDS                                                 *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
XMOVACT  XR    R0,R0
         XR    R15,R15
         LR    R14,RA
         BCTR  R14,R0
         IC    R14,0(R14)
         N     R14,HEXCON
         LTR   R1,RA               ADDRESS TO THE FIRST ACCOUNT PARM.
         LA    R15,143(0,RA)
         LH    R8,H1
XMOVACT1 LTR   R14,R14
         BZ    XAA
         BCTR  R14,R0
         CLR   R1,R15
         BL    XA
XAAA     LH    R1,H143
         B     XB
XAA      CLR   R1,R15
         BL    XMOVACT3
         B     XAAA
XA       IC    R0,0(R1)            NO  - PARAMETER LENGTH
         AR    R1,R0
         AR    R1,R8                     ADDR TO NEXT PARM LENGTH
         B     XMOVACT1
XMOVACT2 MVC   RECACCT(0),0(RA)
XMOVACT3 SR    R1,RA               ACCOUNT DATA LENGTH
XB       EX    R1,XMOVACT2         MOVE PARM.S TO ACCOUNT RECORD
         AH    R1,RECBASEL
         STH   R1,RECL             ACCOUNT RECORD LENGTH
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
* WRITE ACCOUNT DATA IN SYS1.ACCT
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
XWRITE   STC   RF,RECID+3          RECORD IDENTIFICATION
         LA    R1,RECORD
         L     R15,VCONWACT
         BALR  R14,R15             LINK TO ACCOUNT WRITING ROUTINE
         CH    R15,H1              WAS IT WRITTEN
         BH    XWTO                NO  - WRITE ON CONSOLE INSTEAD
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
* WRITE TIME AND DATE ON SYSOUT                                       *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
XWRITEL  MVC   36(4,R12),MSGADDR  MOVE MESSAGE ADDRESS AND        19874
         MVC   42(2,R12),MSGLEN       LENGTH TO SYSTEM TABLE      19874
         LA    R13,SAVE+12
         L     R15,VCONYS
         BALR  R14,R15             LINK TO SYSOUT ROUTINE
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
* EXIT                                                                *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
XIT      L     R13,SAVE+4
         LM    R14,R12,12(R13)
         BR    R14
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
* WRITE ON CONSOL                                                     *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
XWTO     SRL   R15,2
         LA    R14,CODE
         AR    R15,R14
         MVC   RCODE(1),0(R15)
         WTO   MF=(E,CONWTO)             WRITE
         B     XWRITEL
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
* GET TIME OF DAY (HH.HHHH)                                           *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
YTIME    TIME  BIN                      GET TIME AND DATE
         ST    R1,RECDATE               DATE TO ACCOUNT RECORD
         ST    R0,RECTIME               TIME TO ACCOUNT RECORD
         LR    R1,R0
         M     R0,CONVFACT              CONVERT TO HOURS
*             CONVERT TO HOURS AND TEN THOUSANTHS OF AN HOUR
         CVD   R0,TEMP                  CONVERT TO PACKED DECIMAL
         MVC   RECTIME(4),TEMP+4
         UNPK  LINTIME,TEMP+4(4)        TIME TO SYSOUT LINE, ZONED DEC.
         MVC   LINTIME(2),LINTIME+1
         MVI   LINTIME+2,X'4B'          INSERT DECIMAL POINT
         OI    LINTIME+6,X'F0'          ERASE SIGN
         UNPK  LINDATE+1(5),RECDATE     DATE TO SYSOUT LINE, ZONED DEC.
         MVC   LINDATE(2),LINDATE+1
         MVI   LINDATE+2,X'4B'          INSERT DECIMAL POINT
         OI    LINDATE+5,X'F0'          ERASE SIGN
         BR    R14
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
* CONSTANTS                                                           *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         DS    0F
HEXCON   DC    XL4'000000FF'
H1       DC    H'1'
H4       DC    H'4'
H21      DC    H'21'
H143     DC    H'143'
RECBASEL DC    H'41'
CODE     EQU   *-1
         DC    C'1'
         DC    C'2'
         DC    C'3'
         DC    C'4'
         DC    C'5'
         DC    C'6'
         DS    0F
CONVFACT DC    F'119304647'             2E32 / 36
INIT     DC    C'START'
TERM     DC    C'END  '
JOB      DC    C'JOB '
ACT      DC    C'ACT'
BLANK    DC    C'    '
VCONYS   DC    V(IEFYS)            SYSOUT ROUTINE
VCONWACT DC    V(IEFWAD)           WRITE ACCOUNT DATA ROUTINE
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
TEMP     DS    D                   WORK AREA
SAVE     DS    64F                                                22551
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
RECORD   DC    2H'0'               ACCOUNT RECORD
RECWTO   DC    H'0'
RECL     DS    H                        RECORD LENGTH
RECID    DC    C'    '                  IDENTIFICATION
RECJNAME DS    CL8                      JOBNAME
RECDATE  DS    PL4                      DATE
RECTIME  DS    F                        TIME
RECSNAME DS    CL8                      STEPNAME
SPACE    DS    CL8                     RESERVED                   19874
RECSNO   DS    C                        STEP NUMBER
RECACCT  DS    CL145                    ACCOUNT DATA
RECNOSTP EQU   RECSNAME                 NUMBER OF STEPS
RECPGMER EQU   RECSNAME                 PROGRAMMER NAME
RECEND   EQU   *-1
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
CONWTO   DC    AL2(CONEND-*)
         DC    AL2(0)
LINE     DC    C'           '
LINID    DC    C'         '
LINWHEN  DC    C'      '
         DC    C'       DATE '     SYSOUT LINE
LINDATE  DS    CL6                      DATE
         DC    C'      TIME OF DAY '
LINTIME  DS    CL7                      TIME
LINEEND  EQU   *
         DC    C'        '
RCODE    DC    C' '
CONEND   EQU   *
MSGLEN   DC    AL2(LINEEND-LINE)                                  19874
MSGADDR  DC    A(LINE)                                            19874
         END   IEFACTRT
