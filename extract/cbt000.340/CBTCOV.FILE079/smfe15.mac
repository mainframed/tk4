//SMFE15 JOB   MSGLEVEL=1
//STEPA  EXEC  ASMFCL
//ASM.SYSIN DD  *
*     *     *     *     *     *     *     *     *     *       *       *
* THIS ROUTINE IS USED DURING THE SORTING OF SETS CREATED BY SYSTEM   *
* MANGEMENT FACILITIES.IT EXTRACTS NON-JOB ORIENTED RECORDS FROM THE  *
* SORT INPUT SET AND RETAINS THE EXTRACTED RECORDS IN A TEMPORARY SET.*
* THESE NON-JOB ORIENTED RECORDS ARE RE-INSERTED INTO THE OUTPUT SET  *
* BY AN E35 EXIT ROUTINE.                                             *
*        CSECT NAME-E15                                               *
*                                                                     *
*        INPUT-REGISTER 1 POINTS TO A ONE WORD PARAMER LIST.THIS LIST *
*              CONTAINS THE ADDRESS OF THE NEW RECORD.                *
*                                                                     *
*        OUTPUT-A TEMPORARY DATA SET CONTAINING THOSE RECORDS DELETED *
*              FROM THE SORT BY THIS ROUTINE.                         *
*                                                                     *
*        ENTRY E15  FROM OS/SORT                                      *
*                                                                     *
*        EXIT  TO OS/SORT                                             *
*                                                                     *
*        RETURN CODES USED                                            *
*              RC=0   NO ACTION                                       *
*              RC=4   DELETE                                          *
*              RC=8   DO NOT RETURN                                   *
*                                                                     *
*     *     *     *     *     *     *     *     *     *       *       *
R00      EQU   0
R01      EQU   1
R02      EQU   2
R09      EQU   9
R10      EQU   10
DCBREG   EQU   11
R12      EQU   12
R13      EQU   13
R15      EQU   15
HDRID    EQU   X'02'
TLRID    EQU   X'03'
STERMID  EQU   X'04'
JTERMID  EQU   X'05'
OPFORMID EQU   X'06'
OPCLASID EQU   X'07'
E15      CSECT
*930000,980000                                       MODULE MAINTENANCE
         USING *,R12                    DECLARE BASE
         SAVE  (14,12)                  SAVE SORTS REGS
         LR    R12,R15                  SET UP BASE
         USING RECDFORM,R10
         LA    DCBREG,E15DCB            SET UP DCB BASE
         USING IHADCB,DCBREG            DECLARE BASE FOR DCBD
         ST    R13,SAVDS+4              CHAIN SORT &  E15 SAVE AREA
         LR    R02,R13
         LA    R13,SAVDS                PRIME SAVE REGISTER
         ST    R13,8(R02)               CHAIN E15 & SORT SAVE AREA
         L     R10,0(R01)          LOAD PARAMETER FROM SORT
         TM    DCBOFLGS,X'10'           IS DATA SET OPEN          M3353
         BO    E15NOPEN            YES,GO AROUND OPED PROCESSING
         OPEN  (E15DCB,(OUTPUT),E15DCBA,(OUTPUT))
E15NOPEN LTR   R10,R10                  TEST FOR END OF DATA
         BZ    E15NORTN                 YES,GO CLOSE OUT
         CLI   RECDID,HDRID        IS THIS DUMP HDR
         BE    E15XCPTA            YES,COMPARE WITH PREVIOUS
         CLI   RECDID,TLRID        IS THIS DUMP TRAILER
         BE    E15XCPTC            YES,PLACE ON E15DCBA
         CLI   RECDID,STERMID           IS RECD STEP TERMINATE
         BE    E15NOACT                 YES,GO MAKE NORMAL RETURN
         CLI   RECDID,JTERMID           IS RECD JOB TERMINATE
         BE    E15NOACT                 YES,GO TO NORMAL RETURN
         CLI   RECDID,OPFORMID          IS RECD O/P FORMS
         BE    E15NOACT                 YES,GO TO NORMAL RETURN
         CLI   RECDID,OPCLASID          IS RECD O/P CLASSES
         BE    E15NOACT                 YES,GO TO NORMAL RETURN
         LR    R00,R10
         PUT   E15DCB,(0)
*                                       TEMPORARY SET
E15DELET L     R13,SAVDS+4         ADDRESS OF SORT SAVEAREA
         RETURN (14,12),RC=4
E15NOACT L     R13,SAVDS+4         ADDRESS OF SORT'S SAVEAREA
         ST    R10,24(R13)         ADDRESS OF CURRENT RECD TO SAVEAREA
         RETURN (14,12),RC=0
E15XCPTA L     R09,HDRTIME         LOAD 1ST WORD OF PREV RECDS TIMESTMP
         LTR   R09,R09             IS IT ZERO (FIRST TIME THRU)
         BZ    E15XCPTB            YES,BRANCH AROUND COMPARISONS
         CLC   HDRDATE,RSTPDATE    COMPARE PREVIOUS & CURRENT DATE
         BH    E15WARN             PREVIOUS DATE HI,ISSUE WARNING
         CLC   HDRTIME,RSTPTIME    COMPARE PREVIOUS & CURRENT TIME
         BH    E15WARN             PREVIOUS TIME HI,ISSUE WARNING
E15XCPTB MVC   HDRTIME(8),RSTPTIME RETAIN DATE & TIME FOR NEXT COMPARE
E15XCPTC LR    R00,R10             ADDRESS OF RECORD TO REGISTER 0
         PUT   E15DCBA,(0)         EXECUTE I/O
         B     E15DELET            RETURN TO SORT W/DELETE CODE
E15WARN  WTO   'SMFX001-INPUT NOT IN CHRONOLOGICAL SEQUENCE'
         B     E15XCPTB            CONTINUE PROCESSING
E15NORTN CLOSE (E15DCB,,E15DCBA)   CLOSE BOTH TEMPORARY DATA SETS
         L     R13,SAVDS+4
         RETURN (14,12),RC=8
         DS    0D
SAVDS    DS    18F
HDRTIME  DC    F'0'
HDRDATE  DC    F'0'
E15DCB   DCB   DSORG=PS,MACRF=PM,DDNAME=SORDATA
E15DCBA  DCB   DSORG=PS,MACRF=PM,DDNAME=HDRDATA
DCBFORM  DCBD  DSORG=PS
RECDFORM DSECT
RLNGTH   DS    F
RCONT    DS    C
RECDID   DS    C
RSTPTIME DS    CL4
RSTPDATE DS    CL4
RCPUID   DS    CL4
         DS    CL32
RJOBNAME DS    CL8
RSTRTIME DS    CL4
RSTRTDAT DS    CL4
RSTPNUM  DS    C
         END
//LKED.SYSLMOD DD   DSNAME=SMF1.EXITS,DISP=(NEW,KEEP),                 C
//             VOLUME=REF=SYS1.LINKLIB,SPACE=(TRK,(10,5,1))
//LKED.SYSIN   DD *
 NAME E15(R)
