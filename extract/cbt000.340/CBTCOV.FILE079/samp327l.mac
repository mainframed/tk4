//SAMP327L  JOB NAME,MSGLEVEL=1                                @SA78101
//          EXEC ASMFCLG                                       @SA78101
//ASM.SYSIN DD *                                               @SA78101
         TITLE 'LOCAL 3270 SAMPLE PROGRAM'                     @SA78101
SAMP327L CSECT                                                 @SA78101
* STATUS - CHANGE LEVEL 000
*
*
* APAR CHANGES:
*                  @SA76168       12/12/75
*                  @SA78101       04/07/77
*
* ENTRY POINT - SAMP327L
*
* INPUT - REGISTERS 1, 13, AND 14 PROVIDE INPUT AS FOLLOWS
*    1 - ADDRESS OF PARM AREA POINTER
*    13- ADDRESS OF SAVE AREA
*    14- RETURN ADDRESS
*
*    THE PARM AREA HAS THE FOLLOWING FORMAT
*    +0  LENGTH FIELD (HALF WORD)
*    +2  NO. OF DEVICES IN LINE GROUP (1-3 DIGITS,1-255, 1=DEFAULT)
*        NO. OF PRINTERS IN LINE GROUP (1-3 DIGITS,<NO. DEVICES, 0=DEF)
*        RLN'S OF PRINTERS (1-3 DIGITS,NO LARGER THAN NO. OF DEVICES,
*             DEFAULT OF 1 IF NO. OF PRINTERS =1)
*    ALL FIELDS SEPARATED BY COMMAS
*
*    DDNAME FOR 3270 DEVICES = DD3270
*        DD CARDS ARRANGED - PRINTER, DISPLAYS, PRINTER, DISPLAYS, ETC.
*        THOSE FOR PRINTERS OMITTED IF NO PRINTERS
*
*    SYSPRINT  DD  SYSOUT=A  REQUIRED
*
* OUTPUT - DISPLAY OUTPUT, PRINTER OUTPUT
*
ZEROREG  EQU   0
PARMREG  EQU   1                   PARAMETER REGISTER
EVENREG  EQU   2
ODDREG   EQU   3
LOOPREG  EQU   4
INDXREG  EQU   5
CNTREG   EQU   6
RLNREG   EQU   CNTREG
LOOPREG2 EQU   7
DECBREG  EQU   7
INDXREG2 EQU   8
BUFREG   EQU   8
TABBASE  EQU   9                   BASE FOR CONTROL TABLE
BASEREG2 EQU   10
BASEREG  EQU   11                  CSECT BASE REGISTER
SAVEREG  EQU   13
RETNREG  EQU   14
EPREG    EQU   15
*
TYPE     EQU   5                   DISP TO TYPE FIELD IN DECB
COUNT    EQU   18                  DISP TO RESIDUAL COUNT IN DECB
RLN      EQU   25                  DISP TO RLN FIELD IN DECB
POLPT    EQU   36                  DISP TO POLPT FIELD IN DECB
*
         SAVE  (14,12),T,*         SAVE REGISTER
         BALR  BASEREG,0           ESTABLISH BASE REG
         USING *,BASEREG,BASEREG2  & ADDRESSABILITY
         LA    BASEREG2,4095(BASEREG)   INITIALIZE SECOND BASE
         LA    BASEREG2,1(BASEREG2)
         ST    SAVEREG,SAVE+4      ESTABLISH
         LA    EVENREG,SAVE             SAVE AREA
         ST    EVENREG,8(SAVEREG)            CHAIN
         LR    SAVEREG,EVENREG     ESTABLISH NEW SAVE AREA
*
         L     ODDREG,0(PARMREG)   GET ADDR OF PARM AREA
         LH    LOOPREG,0(ODDREG)   GET LENGTH OF PARM AREA
         LTR   LOOPREG,LOOPREG     PARMS PASSED
         BZ    DEFAULTS            NO, TAKE DEFAULTS
         LA    INDXREG,2(ODDREG)   INITIALIZE POINTER TO PARM STRING
         BAL   RETNREG,GETPARM     GET NUMBER OF DEVICES
         LTR   PARMREG,PARMREG     VALUE LESS THAN 1
         BNP   DFLTNO              YES, TREAT AS 1
         C     PARMREG,MAXRLN      VALUE GREATER THAN 255
         BH    DFLTNO              YES, TREAT AS 1
         ST    PARMREG,NODVCS      SAVE NUMBER OF DEVICES
*
PARM2    LTR   LOOPREG,LOOPREG     ANY MORE PARMS
DFLTNO   EQU   PARM2               DEFAULT NO OF DVCS = 1
         BZ    DFLTPTR             NO, DEFAULT NUMBER OF PRINTERS
*
         BAL   RETNREG,GETPARM     GET NUMBER OF PRINTERS
         C     PARMREG,NODVCS      NUMBER OF PRINTERS LESS THAN
         BNL   DFLTPTR             NUMBER OF DEVICES
         ST    PARMREG,NOPTRS      SAVE NUMBER OF PRINTERS
*
GETTAB   L     EVENREG,NODVCS      GET SIZE OF TERMINAL ENTRIES
*
DEFAULTS EQU   GETTAB
DFLTPTR  EQU   GETTAB              DEFAULT NO OF PTRS = 0
*
         SLA   EVENREG,3
         L     ODDREG,NOPTRS       GET SIZE OF PRINTER ECB'S
         SLA   ODDREG,2
         LA    ZEROREG,368(EVENREG,ODDREG)  GET SIZE OF CONTROL TABLE
         ST    ZEROREG,TABSIZE     SAVE SIZE
         GETMAIN  R,LV=(0)
         LR    TABBASE,PARMREG     GET ADDR OF CONTROL TABLE
         MVC   0(2,TABBASE),NODVCS+2  SAVE NO. OF DEVICES
         XC    2(2,TABBASE),2(TABBASE) CLEAR ACTIVE NO. OF DEVICES
         LA    PARMREG,16(EVENREG,TABBASE) GET ADDR OF ECB LIST
         ST    PARMREG,4(TABBASE)  & SAVE IT
         LA    PARMREG,4(PARMREG,ODDREG) GET ADDR OF DECB
         ST    PARMREG,8(TABBASE)  & SAVE IT
         LA    PARMREG,40(PARMREG) GET ADDR OF BUFFER
         ST    PARMREG,12(TABBASE) & SAVE IT
*
         LA    PARMREG,351(EVENREG,ODDREG) GET LENGTH TO CLEAR
         MVI   16(TABBASE),0       CLEAR FIRST BYTE
         LA    EVENREG,16(TABBASE) INITIALIZE ADDRESS POINTER
CLRLOOP  C     PARMREG,F256        MOVE LENGTH OVER 256
         BL    LASTMOVE            NO, DO LAST MOVE
         MVC   1(256,EVENREG),0(EVENREG) CLEAR 256 BYTES
         LA    EVENREG,256(EVENREG) INCREMENT ADDRESS POINTER
         S     PARMREG,F256        DECR MOVE COUNT
         BNZ   CLRLOOP             REPEAT IF MORE TO DO
         B     GETPTRS             GET PRINTER RLNS
*
LASTMOVE BCTR  PARMREG,0           DECR COUNT FOR EXECUTE
         EX    PARMREG,CLRMOVE     CLEAR REST OF AREA
         B     GETPTRS             GET PRINTER RLNS
*
CLRMOVE  MVC   1(0,EVENREG),0(EVENREG)
*
*
GETPTRS  SR    LOOPREG2,LOOPREG2   INITIALIZE COUNTER
         SR    INDXREG2,INDXREG2   INITIALIZE PRINTER ECB INDEX
PTRLOOP  C     LOOPREG2,NOPTRS     ALL PTR RLNS OBTAINED
         BNL   FININIT             YES, COMPLETE INITIALIZATION
ENDLIST  LTR   LOOPREG,LOOPREG     ANY MORE PARMS
         BZ    DFLTRLN             NO, TAKE DEFAULTS
         BAL   RETNREG,GETPARM
         LTR   PARMREG,PARMREG     RLN VALID
         BZ    ENDLIST             NO, IGNORE IT
         C     PARMREG,NODVCS
         BH    ENDLIST
INITPTR  SLA   PARMREG,3           GET INDEX TO ENTRY
         LA    PARMREG,8(TABBASE,PARMREG) GET ADDR OF ENTRY FOR PTR
         MVC   0(2,PARMREG),MIN1   FREE PTR FORMAT
         STH   INDXREG2,2(PARMREG) INDEX TO ECB PTR
         LA    INDXREG2,4(INDXREG2) INCR TO NEXT ECB
         LA    LOOPREG2,1(LOOPREG2) INCR PRINTER COUNTER
         B     PTRLOOP
*
*
DFLTRLN  LTR   LOOPREG2,LOOPREG2   NO PTR RLNS PROCESSED
         BNZ   FININIT             NO, COMPLETE INITIALIZATION
         LA    PARMREG,1           YES, DEFAULT RLN = 1
         B     INITPTR
*
*
FININIT  L     PARMREG,NODVCS      GET NUMBER OF DISPLAYS
         LR    LOOPREG,PARMREG          SAVE LOOP COUNTER        A56819
         S     PARMREG,NOPTRS                                    A56819
         M     ZEROREG,DATASZ      GET SIZE OF DATA AREAS
         LR    ZEROREG,PARMREG
         ST    ZEROREG,DATBLKSZ    SAVE DATA AREA SIZE
         GETMAIN  R,LV=(0)         GET CORE FOR DATA AREAS
         ST    PARMREG,DATBLKAD    SAVE DATA AREA ADDR
         LR    INDXREG,PARMREG     GET ADDRESS OF AREA
         LA    INDXREG2,16(TABBASE) GET ADDR OF ENTRY
         SR    CNTREG,CNTREG       PRINTER RLN
         LA    LOOPREG2,1          RLN COUNTER
*
INITLOOP CLC   0(2,INDXREG2),MIN1  PRINTER ENTRY
         BE    PTRINIT             YES, GET RLN
         XC    0(130,INDXREG),0(INDXREG)  CLEAR DATA AREA
         STH   CNTREG,2(INDXREG2)  STORE PRINTER RLN FOR THIS DISPLAY
         ST    INDXREG,4(INDXREG2) STORE ADDR OF DATA AREA
         A     INDXREG,DATASZ      INCR DATA AREA PTR
INCRPTRS LA    INDXREG2,8(INDXREG2) INCR ENTRY PTR
         LA    LOOPREG2,1(LOOPREG2) INCR RLN
         BCT   LOOPREG,INITLOOP    BR IF MORE TO DO
         L     PARMREG,4(TABBASE)  GET ADDR OF ECB LIST
         L     INDXREG,NOPTRS      GET INDEX PAST PRINTER ECB
         LTR   INDXREG,INDXREG                                   A56819
         BZ    DISPECB                                           A56819
         LA    EVENREG,DUMMYECB                                  A56819
PTRECB   ST    EVENREG,0(PARMREG)                                A56819
         LA    PARMREG,4(PARMREG)                                A56819
         BCT   INDXREG,PTRECB                                    A56819
DISPECB  MVI   0(PARMREG),X'80'                                  A56819
         MVC   1(3,PARMREG),9(TABBASE)  MOVE IN ADDR OF ECB
         B     IOINIT
PTRINIT  LR    CNTREG,LOOPREG2     GET RLN OF PRINTER
         B     INCRPTRS
IOINIT   OPEN  (SYSPRINT,OUTPUT,DD3270)
         WTO   'LOCAL 3270 SAMPLE PROGRAM RUNNING'
         L     LOOPREG,NODVCS      GET LOOP CTR
         L     DECBREG,8(TABBASE)  GET ADDR OF DECB
         L     BUFREG,12(TABBASE)  GET ADDR OF BUFFER
         LA    INDXREG,16(TABBASE) GET ADDR OF FIRST TERM ENTRY
         LA    RLNREG,1            INITIALIZE RLN REG
*
IOLOOP   LA    EPREG,WRTFMT1       GET ADDR OF FIRST WRITE RTN
         CLC   0(2,INDXREG),MIN1   PRINTER ENTRY
         BE    UPCOUNT             YES, DON'T DO WRITE
         LR    PARMREG,RLNREG      PUT RLN IN REG 1
         BALR  RETNREG,EPREG       WRITE FIRST MSG
         LTR   EPREG,EPREG         SUCCESSFUL
         BZ    UPCOUNT             YES, TRY NEXT
         XC    0(2,INDXREG),0(INDXREG) MARK TERMINAL INACTIVE
         B     IOLPCNT
*
UPCOUNT  LH    EVENREG,2(TABBASE)  INCREMENT
         LA    EVENREG,1(EVENREG)       COUNT OF ACTIVE
         STH   EVENREG,2(TABBASE)            DEVICES
IOLPCNT  LA    RLNREG,1(RLNREG)    INCR RLN
         LA    INDXREG,8(INDXREG)  INCR TO NEXT ENTRY
         BCT   LOOPREG,IOLOOP      LOOP IF MORE TO DO
*
         LA    CNTREG,1            INIT RLN
         LA    INDXREG,16(TABBASE) POINTER TO FIRST ENTRY
         L     LOOPREG,NODVCS      LOOP LIMIT
FINDFRST CLC   0(2,INDXREG),MIN1   PRINTER
         BNE   DSPLY1              NO, SAVE RLN
         LA    CNTREG,1(CNTREG)    INCR RLN
         LA    INDXREG,8(INDXREG)  INCR TO NEXT ENTRY
         BCT   LOOPREG,FINDFRST
         LA    CNTREG,1
DSPLY1   ST    CNTREG,READRLN      SAVE RLN FOR READS
*
READ     L     RLNREG,READRLN      GET READ RLN
         XC    0(4,DECBREG),0(DECBREG)
         READ  (DECBREG),TI,DD3270,(BUFREG),308,,(RLNREG),MF=E
         LTR   EPREG,EPREG         OK
         BNZ   ERRABEND            NO, ABEND
TWAIT    L     ODDREG,4(TABBASE)   GET ADDR OF ECB LIST
         TWAIT (EVENREG),ECBLIST=(ODDREG)  WAIT FOR OPERATION TO END
*
         CLI   TYPE(EVENREG),X'01' OPERATION = READ INITIAL
         BNE   PRNTREND            NO, PRINTER OPERATION
         CLI   0(EVENREG),X'44'    INTERCEPTED
         BE    READ                YES, RETRY READ
         CLI   0(EVENREG),X'7F'    SUCCESSFUL
         BNE   ERRABEND            NO, END
         L     PARMREG,POLPT(EVENREG) GET RESPONDING RLN
         LR    ODDREG,PARMREG
         SLA   ODDREG,3            GET INDEX TO TERM ENTRY
         LH    CNTREG,8(TABBASE,ODDREG)  GET FORMAT NO.
         SLA   CNTREG,2            MULTIPLY BY 4
         B     READRTN(CNTREG)
READRTN  B     ERRABEND            RLN = INACTIVE DEVICE
         B     READFMT1            FORMAT 1 ON SCREEN
         B     READFMT2            FORMAT 2 ON SCREEN
         B     READFMT3            FORMAT 3 ON SCREEN
         B     READ                FORMAT 4 ON SCREEN - IGNORE IT
*
*
READFMT1 CLI   0(BUFREG),X'6E'     INTERRUPT IS PA2 KEY
         BNE   CLR1                NO, TEST FOR CLEAR KEY
*
WRTFMT4  LR    RLNREG,PARMREG      SAVE RLN
ENDMSG   EQU   WRTFMT4             RETURN = REESTABLISH READ
         SLA   PARMREG,3           CONVERT RLN TO TABLE INDEX
         LA    EVENREG,4           FORMAT ID
         STH   EVENREG,8(TABBASE,PARMREG)  STORE ID IN TABLE
         CHGNTRY  DD3270,ATTLST,(RLNREG),,SKIP    DEACTIVATE TERMINAL
*                                  FOR READ INITIAL
WRITE4   XC    0(4,DECBREG),0(DECBREG)  CLEAR ECB
         WRITE (DECBREG),TS,DD3270,FORMAT4,FMT4SZ,,(RLNREG),MF=E
         LTR   EPREG,EPREG         END IF RC
         BNZ   ERRABEND                 NON-ZERO
         WAIT  ECB=(DECBREG)
         CLI   0(DECBREG),X'7F'    SATISFACTORY COMPLETION
         BE    QUIESCE             UPDATE ACTIVE TERM COUNT
         CLI   0(DECBREG),X'44'    INTERCEPT
         BE    WRITE4              YES, RETRY
         B     ERRABEND            NO, END
*
*
QUIESCE  LH    EVENREG,2(TABBASE)  GET NO. OF ACTIVE TERMS
         BCT   EVENREG,QCNTSTR     DECR COUNT
         CLOSE (SYSPRINT,,DD3270)  IF ZERO, CLOSE DCB'S
         L     PARMREG,DATBLKAD    LOCATION OF DATA AREAS
         L     ZEROREG,DATBLKSZ    SIZE OF DATA AREAS
         FREEMAIN  R,LV=(0),A=(1)  FREE DATA AREAS
         LR    PARMREG,TABBASE     LOCATION OF CONTROL TABLE
         L     ZEROREG,TABSIZE     SIZE OF CONTROL TABLE
         FREEMAIN  R,LV=(0),A=(1)  FREE CONTROL TABLE
         L     SAVEREG,SAVE+4
         RETURN  (14,12),T         RETURN TO CALLING PROGRAM
*
QCNTSTR  STH   EVENREG,2(TABBASE)  SAVE UPDATE ACTIVE TERM CNT
         B     READ                SET UP READ
*
*
CLR1     CLI   0(BUFREG),X'6D'     CLEAR KEY
         BNE   ENT1                NO, TEST FOR ENTER KEY
*
WRTFMT3  LR    RLNREG,PARMREG      SAVE RLN
CNTRLMSG EQU   WRTFMT3
*                                  WRITE CONTROL OPTIONS
         SLA   PARMREG,3           CONVERT RLN TO TABLE INDEX
         LA    EVENREG,3           FORMAT ID
         STH   EVENREG,8(TABBASE,PARMREG)  STORE ID IN TABLE
WRITE3   XC    0(4,DECBREG),0(DECBREG)  CLEAR ECB
         WRITE (DECBREG),TS,DD3270,FORMAT3,FMT3SZ,,(RLNREG),MF=E
         LTR   EPREG,EPREG         END IF RC
         BNZ   ERRABEND                 NON-ZERO
         WAIT  ECB=(DECBREG)       WAIT FOR COMPLETION
         CLI   0(DECBREG),X'7F'    SATISFACTORY COMPLETION
         BE    READ                YES, SET UP READ
         CLI   0(DECBREG),X'44'    INTERCEPT
         BE    WRITE3              YES, RETRY OPERATION
         B     ERRABEND            NO, END
*
ENT1     CLI   0(BUFREG),X'7D'     ENTER KEY
         BNE   READ                NO, REESTABLISH READ
         L     ODDREG,12(TABBASE,ODDREG) GET ADDR OF DATA AREA
         LA    INDXREG,3(BUFREG)   INDEX PAST AID & CURSOR ADDR
         LA    LOOPREG,308
         SH    LOOPREG,COUNT(EVENREG) GET NUMBER OF BYTES READ
         S     LOOPREG,THREE       ADJUST FOR LENGTH OF AID & ADDR
         LTR   LOOPREG,LOOPREG
         BNP   WRTRSPNS            NOTHING ENTERED
*
         CLC   0(3,INDXREG),FLD1   FIRST FIELD
         BNE   FLD2CHK             NO, CHECK FOR SECOND
         XC    0(33,ODDREG),0(ODDREG)   CLEAR FIELD IN DATA AREA
         BAL   RETNREG,GETFIELD    FIND FIELD DELIMITERS
         LTR   LOOPREG,LOOPREG     END OF DATA
         BNP   WRTRSPNS            YES, WRITE RESPONSE
*
FLD2CHK  LA    ODDREG,34(ODDREG)   INCR TO NEXT FIELD IN DATA AREA
         CLC   0(3,INDXREG),FLD2   SECOND FIELD
         BNE   FLD3CHK             NO, CHECK FOR THIRD FIELD
         XC    0(33,ODDREG),0(ODDREG)   CLEAR FIELD IN DATA AREA
         BAL   RETNREG,GETFIELD    GET FIELD
         LTR   LOOPREG,LOOPREG     END OF DATA
         BNP   WRTRSPNS            YES, WRITE RESPONSE
*
FLD3CHK  LA    ODDREG,34(ODDREG)   INCR TO THIRD FIELD IN DATA AREA
         CLC   0(3,INDXREG),FLD3   THIRD FIELD
         BNE   FLD4CHK             NO, CHECK FOR FOURTH FIELD
         XC    0(33,ODDREG),0(ODDREG)   CLEAR FIELD
         BAL   RETNREG,GETFIELD    GET FIELD
         LTR   LOOPREG,LOOPREG     END OF DATA
         BNP   WRTRSPNS            YES, WRITE RESPONSE
*
FLD4CHK  LA    ODDREG,34(ODDREG)   INCR TO FOURTH FIELD
         CLC   0(3,INDXREG),FLD4   FOURTH FIELD
         BNE   FLD5CHK             NO, CHECK FOR LAST FIELD
         XC    0(21,ODDREG),0(ODDREG)   CLEAR FIELD
         BAL   RETNREG,GETFIELD    GET FIELD
         LTR   LOOPREG,LOOPREG     END OF DATA
         BNP   WRTRSPNS            YES, WRITE RESPONSE
*
FLD5CHK  LA    ODDREG,22(ODDREG)   INCR TO LAST FIELD
         CLC   0(3,INDXREG),FLD5   FIFTH FIELD
         BNE   WRTRSPNS            NO, WRITE RESPONSE
         XC    0(5,ODDREG),0(ODDREG) CLEAR FIELD
         BAL   RETNREG,GETFIELD    GET FIELD
*
*
WRTFMT2  LR    RLNREG,PARMREG      SAVE RLN
WRTRSPNS EQU   WRTFMT2             RETURN = REESTABLISH READ
*                                  WRITE FORMAT 2
         MVC   0(256,BUFREG),FORMAT2  MOVE BASE MESSAGE
         MVC   256(FMT2SZ-256,BUFREG),FORMAT2+256  INTO BUFFER
         SLA   PARMREG,3           CONVERT RLN TO TABLE INDEX
         LA    EVENREG,2           GET FORMAT ID
         STH   EVENREG,8(TABBASE,PARMREG)  & STORE IN TABLE ENTRY
         L     ODDREG,12(TABBASE,PARMREG)  GET DATA AREA ADDR
         MVC   FMT2FLD1(33,BUFREG),0(ODDREG)      MOVE DATA
         MVC   FMT2FLD2(33,BUFREG),34(ODDREG)     FROM
         MVC   FMT2FLD3(33,BUFREG),68(ODDREG)     DATA AREA
         MVC   FMT2FLD4(21,BUFREG),102(ODDREG)    TO
         MVC   FMT2FLD5(5,BUFREG),124(ODDREG)     BUFFER
WRITE2   XC    0(4,DECBREG),0(DECBREG)  CLEAR ECB
         WRITE (DECBREG),TS,DD3270,(BUFREG),FMT2SZ,,(RLNREG),MF=E
         LTR   EPREG,EPREG         END IF RC
         BNZ   ERRABEND                 NON-ZERO
         WAIT  ECB=(DECBREG)       WAIT FOR OPERATION TO END
         CLI   0(DECBREG),X'7F'    COMPLETION SATISFACTORY
         BE    READ                YES, SET UP READ
         CLI   0(DECBREG),X'44'    INTERCEPT
         BE    WRITE2              YES, RETRY
         B     ERRABEND            NO, END
*
READFMT2 CLI   0(BUFREG),X'6C'     PA1 KEY
         BNE   READFMT1            NO, GO TO READ FORMAT 1 LOGIC
         LR    EVENREG,PARMREG     GET RLN OF DISPLAY
         SLA   EVENREG,3           GET INDEX TO ENTRY
         LH    ODDREG,10(EVENREG,TABBASE)  GET RLN OF ASSIGNED PTR
         LTR   ODDREG,ODDREG       PRINTER ASSIGNED
         BZ    SYSOUT              NO, USE SYSOUT
         LR    RLNREG,ODDREG       SAVE RLN
         SLA   ODDREG,3            GET INDEX TO ENTRY OF PRINTER
         LA    ZEROREG,184
         GETMAIN  R,LV=(0)         GET CORE FOR PRINTER REQUEST BLOCK
         XC    0(184,PARMREG),0(PARMREG) CLEAR IT
         LA    LOOPREG,44(PARMREG) GET ADDR OF PRINT BUFFER
         L     INDXREG,12(EVENREG,TABBASE)   GET ADDR OF DATA AREA
         MVI   0(LOOPREG),X'C8'    STORE WCC IN BUFFER
         MVC   1(129,LOOPREG),0(INDXREG) MOVE IN DATA
         MVI   34(LOOPREG),X'15'   NEW LINE
         MVI   68(LOOPREG),X'15'   NEW LINE
         MVI   102(LOOPREG),X'15'  NEW LINE
         MVI   124(LOOPREG),C' '   BLANK
         MVI   130(LOOPREG),X'19'  END OF MESSAGE
*
         LH    ZEROREG,8(TABBASE,ODDREG)  GET CURRENT PRINTER FORMAT
         CH    ZEROREG,MIN1        PRINTER BUSY
         BE    STRTPRTR            NO, START OPERATION
         L     INDXREG,12(TABBASE,ODDREG)  GET POINTER TO LAST REQ
         ST    PARMREG,0(INDXREG)  STORE POINTER
         ST    PARMREG,12(TABBASE,ODDREG)  UPDATE POINTER TO CURR REQ
RSTRKYBD LR    PARMREG,EVENREG     RECOVER RLN OF DISPLAY
         SRA   PARMREG,3           FROM TABLE INDEX
         B     WRTRSPNS            GO TO UNLOCK KEYBOARD
*
STRTPRTR ST    PARMREG,12(TABBASE,ODDREG)  STORE POINTER TO REQ
         LA    PARMREG,4(PARMREG)  GET ADDRESS OF DECB
         L     INDXREG,4(TABBASE)  GET ADDRESS OF ECB LIST
         AH    INDXREG,10(TABBASE,ODDREG)  ADD INDEX TO PRTR'S PTR
         ST    PARMREG,0(INDXREG)  STORE ECB ADDR IN LIST
         BCTR  ZEROREG,0           INDICATE
         STH   ZEROREG,8(TABBASE,ODDREG)  PRINTER BUSY
         WRITE (PARMREG),TS,DD3270,(LOOPREG),131,,(RLNREG),MF=E
         B     RSTRKYBD
*
SYSOUT   L     INDXREG,12(TABBASE,EVENREG)  GET ADDR OF DATA AREA
         MVC   SYSOUTBF(130),0(INDXREG)  MOVE DATA TO PRINT BUFFER
         MVI   SYSOUTBF+33,C','    MOVE IN
         MVI   SYSOUTBF+67,C','         FIELD DELIMITERS
         MVI   SYSOUTBF+101,C','             FOR LINE
         MVI   SYSOUTBF+123,C' '                  TO BE
         MVI   SYSOUTBF+129,C'.'                       PRINTED
         TR    SYSOUTBF(130),FLDXLATE   TRANSLATE TO VALIDATE DATA
         PUT   SYSPRINT,SYSOUTBF   WRITE TO SYSOUT
         B     RSTRKYBD
*
READFMT3 CLI   0(BUFREG),X'F0'     RFTR KEY                    @SA76168
         BE    READ                YES, REISSUE READ           @SA76168
         CLC   0(3,BUFREG),SOHRFT  CHECK FOR RFT HEADER        @SA76168
         BE    READ                YES, REISSUE READ           @SA76168
         CLI   0(BUFREG),X'7D'     ENTER KEY                   @SA76168
         BE    WRTRSPNS            YES, WRITE RESPONSE
         CLI   0(BUFREG),X'6E'     PA2 KEY
         BE    ENDMSG              YES, END TERMINAL
         CLI   0(BUFREG),X'6C'     PA1 KEY
         BNE   CNTRLMSG            NO, WRITE CONTROL OPTIONS MSG
*
         L     EVENREG,12(TABBASE,ODDREG)  GET ADDR OF DATA AREA
         XC    0(130,EVENREG),0(EVENREG)   CLEAR IT
         BAL   RETNREG,WRTFMT1     WRITE OUT FIRST FORMAT
         LTR   EPREG,EPREG         WRITE SUCCESSFUL
         BNZ   ERRABEND            NO, END
         B     READ
*
WRTFMT1  LR    ODDREG,RETNREG      SAVE RETURN ADDRESS
         LR    RLNREG,PARMREG      SAVE RLN
WRITE1   XC    0(4,DECBREG),0(DECBREG)  CLEAR ECB
         WRITE (DECBREG),TS,DD3270,FORMAT1,FMT1SZ,,(RLNREG),MF=E
         LTR   EPREG,EPREG         END IF RC
         BNZ   ERRABEND                 NON-ZERO
         WAIT  ECB=(DECBREG)       WAIT FOR OPERATION TO END
         CLI   0(DECBREG),X'7F'    SATISFACTORY COMPLETION
         BE    FMT1CD              YES, STORE FORMAT CODE IN ENTRY
         CLI   0(DECBREG),X'44'    INTERCEPT
         BE    WRITE1              YES, RETRY OPERATION
         LA    EPREG,*             SET NON-ZERO RETURN CODE
         BR    ODDREG              AND RETURN TO CALLER
*
FMT1CD   LA    EVENREG,1           GET FORMAT ID
         SLA   RLNREG,3            CONVERT RLN TO TABLE INDEX
         STH   EVENREG,8(TABBASE,RLNREG)  STORE ID IN TABLE
         SRA   RLNREG,3            RESTORE RLN REGISTER TO RLN
         SR    EPREG,EPREG         SET RETURN CODE =0
         BR    ODDREG              RETURN TO CALLER
*
PRNTREND SR    ODDREG,ODDREG
         IC    ODDREG,RLN(EVENREG) GET RLN OF ENDING PRINTER
         LR    RLNREG,ODDREG       SAVE RLN
         SLA   ODDREG,3            CONVERT RLN TO TABLE INDEX
         S     EVENREG,FOUR        GET BEGIN OF PRINTER RB
         LA    ODDREG,8(ODDREG,TABBASE)  GET ADDR OF TABLE ENTRY
         L     INDXREG,4(TABBASE)  GET ADDR OF ECB LIST
         AH    INDXREG,2(ODDREG)   GET ADDR OF PRINTER'S ECB
         CLI   4(EVENREG),X'7F'    GOOD END
         BE    PRNTRNXT            YES, CLEAN UP
         CLI   4(EVENREG),X'44'    INTERCEPT
         BE    WRITEPI             YES, RESTART OPERATION
         B     ERRABEND            NO, END
*
PRNTRNXT MVC   4(4,ODDREG),0(EVENREG)   UPDATE LINK POINTER
         LR    PARMREG,EVENREG     ADDRESS OF PRINTER RB
         LA    ZEROREG,184         SIZE OF RB
         FREEMAIN  R,LV=(0),A=(1)  FREE BLOCK
         L     EVENREG,4(ODDREG)   GET ADDR OF NEXT RB
         LTR   EVENREG,EVENREG     END OF CHAIN
         BNZ   WRITEP              NO, START NEXT
         LA    EVENREG,DUMMYECB                                  A56819
         ST    EVENREG,0(INDXREG)                                A56819
         MVC   0(2,ODDREG),MIN1    MARK PRINTER AVAILABLE
         B     TWAIT               WAIT FOR NEXT COMPLETION
*
WRITEP   RESETPL  (DECBREG),ATTENT RESET READ INITIAL
         LTR   EPREG,EPREG         RC = 0
         BZ    WRITEPS             YES, PROCEED
         C     EPREG,FOUR          RC = 4
         BNE   ERRABEND            NO, END
         WAIT  ECB=(DECBREG)       WAIT FOR READ TO COMPLETE
*
WRITEPS  LA    EVENREG,4(EVENREG)  GET ADDR OF ECB
         ST    EVENREG,0(INDXREG)  STORE ADDR OF ECB IN ECB LIST
         LA    ODDREG,40(EVENREG)  GET ADDR OF PRINT BUFFER
         WRITE (EVENREG),TS,DD3270,(ODDREG),131,,(RLNREG),MF=E
         LTR   EPREG,EPREG         END IF RC
         BNZ   ERRABEND                 NON-ZERO
         CLI   0(DECBREG),X'48'    READ INITIAL ENDED BY RESETPL
         BE    READ                YES, REISSUE READ
         B     TWAIT               NO, CHECK STATUS
*
WRITEPI  XC    4(4,EVENREG),4(EVENREG)  CLEAR ECB
         B     WRITEP              RESTART WRITE
*
SOHRFT   DC    X'016C61'           RFT HEADER                  @SA76168
*
ERRABEND ABEND (15),DUMP
GETPARM  SR    EVENREG,EVENREG     CLEAR REGISTER
         SR    CNTREG,CNTREG
GETCHAR  CLI   0(INDXREG),C'0'     CHARACTER A NUMBER
         BL    DELIM               NO, CHECK FOR DELIMITER
         CLI   0(INDXREG),C'9'     MAYBE
         BH    INVLDCHR            NO, INVALID CHARACTER
         SLL   EVENREG,8           CLEAR LOW CHARACTER
         IC    EVENREG,0(INDXREG)  GET CHARACTER
PARMLOOP LA    CNTREG,1(CNTREG)    INCR LENGTH
         LA    INDXREG,1(INDXREG)  INCR STRING POINTER
         BCT   LOOPREG,GETCHAR     GET NEXT CHARACTER, IF ANY LEFT
HAVEPARM C     CNTREG,THREE        CHECK PARM LENGTH
         BNH   CNVRT               BR IF VALID
         N     EVENREG,SIXFS       REDUCE TO THREE BYTES
         L     CNTREG,THREE        SET COUNT TO THREE
CNVRT    ST    EVENREG,RAWPARM     STORE RAW PARM VALUE
         LA    ODDREG,4            LENGTH OF RAWPARM
         SR    ODDREG,CNTREG       SUBT LENGTH OF PARM &
         LA    ODDREG,RAWPARM(ODDREG) GET ADDR OF FIRST CHARACTER
         BCTR  CNTREG,0            DECR COUNT FOR EXECUTE
         EX    CNTREG,PACK         PACK PARM
         CVB   PARMREG,PCKDPARM    & CONVERT TO BINARY
         BR    RETNREG             RETURN TO CALLER
*
DELIM    CLI   0(INDXREG),C','     COMMA
         BNE   INVLDCHR            NO, TREAT AS INVALID
         LTR   CNTREG,CNTREG       PARM OMITTED
         BZ    ZEROPARM            YES, SET IT TO ZERO
         LA    INDXREG,1(INDXREG)  INCR STRING POINTER
         BCTR  LOOPREG,0           DECR LOOP COUNTER
         B     HAVEPARM            BR TO CONVERT PARM
*
ZEROPARM SR    PARMREG,PARMREG     SET PARM = 0
         BR    RETNREG             RETURN
INVLDCHR SLL   EVENREG,8           CLEAR LOW CHARACTER
         IC    EVENREG,C0          SUBSTITUTE C'0' FOR INVALID CHAR
         B     PARMLOOP            BR TO ADJUST COUNTERS & POINTERS
*
PACK     PACK  PCKDPARM(8),0(0,ODDREG)
*
GETFIELD LA    EVENREG,3(INDXREG)  INCR TO FIRST DATA BYTE OF FIELD
         S     LOOPREG,THREE       CORRECT LENGTH FOR SBA SEQUENCE
         SR    CNTREG,CNTREG       SET COUNT = ZERO
FNDLOOP  CLI   0(EVENREG),X'11'    BYTE = SBA ORDER
         BE    MVFLD               YES, MOVE FIELD
         LA    CNTREG,1(CNTREG)    INCR FIELD SIZE
         LA    EVENREG,1(EVENREG)  INCR TO NEXT DATA BYTE
         BCT   LOOPREG,FNDLOOP     LOOP IF MORE DATA
MVFLD    BCTR  CNTREG,0            ADJUST COUNT FOR EXECUTE
         EX    CNTREG,FLDMOVE      MOVE FIELD INTO DATA AREA
         LR    INDXREG,EVENREG     GET START OF NEXT FIELD
         BR    RETNREG             RETURN
*
FLDMOVE  MVC   0(0,ODDREG),3(INDXREG) MOVE FIELD FROM BUFFER
*                                  TO DATA AREA
         DS    0D
PCKDPARM DC    8C'0'               PACKED PARM AREA
SYSOUTBF DS    0D                  SYSOUT PRINT BUFFER
         DS    CL130
SAVE     DS    18F                 SAVE AREA
MAXRLN   DC    F'255'              MAXIMUM RLN
NODVCS   DC    F'1'                NO. OF DEVICES (DEFAULT = 1)
NOPTRS   DC    F'0'                NO. OF PRINTERS (DEFAULT = 0)
TABSIZE  DS    F                   SIZE OF CONTROL TABLE
F256     DC    F'256'
DATASZ   DC    F'130'              SIZE OF DISPLAY TERM DATA AREA
DATBLKSZ DS    F                   SIZE OF DATA AREA CORE BLOCK
DATBLKAD DS    F                   ADDR OF DATA AREA CORE BLOCK
READRLN  DC    F'1'                RLN TO BE USED FOR READ INITIALS
RAWPARM  DS    1F                  ZONED PARM AREA
THREE    DC    F'3'
SIXFS    DC    XL4'00FFFFFF'
FOUR     DC    F'4'
ZEROWRD  DC    F'0'
DUMMYECB DC    F'0'                                              A56819
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,DEVD=DA,MACRF=(PM),RECFM=F,LREC*
               L=130,BLKSIZE=130
DD3270   DCB   DDNAME=DD3270,DSORG=CX,MACRF=(R,W),EROPT=T
MIN1     DC    H'-1'               FREE PRINTER FORMAT ID
C0       DC    C'0'
FLD1     DC    XL3'11C1D6'         SBA SEQ OF FIRST FIELD ( SBA = 86)
FLD2     DC    XL3'11C17E'         SBA SEQ OF SECOND FIELD( SBA =126)
FLD3     DC    XL3'11C2E6'         SBA SEQ OF THIRD FIELD ( SBA =166)
FLD4     DC    XL3'11C34F'         SBA SEQ OF FOURTH FIELD (SBA =207)
FLD5     DC    XL3'11C36A'         SBA SEQ OF FIFTH FIELD ( SBA =234)
FORMAT1  DC    X'C7114040'          WCC, SBA = 0
         DC    C'ENTER DATA REQUESTED BELOW:'
         DC    X'11C150'           SBA = 80
         DC    C'NAME:'
         DC    X'1D401311C1F71D60' SF = UNPROT, IC, SBA=119, SF=PROT
         DC    C'ADDR:'
         DC    X'1D4011C25F1D60'   SF = UNPROT, SBA = 159, SF = PROT
         DC    C'CITY:'
         DC    X'1D4011C3C71D60'   SF = UNPROT, SBA=199, SF=PROT
         DC    C'STATE:'
         DC    X'1D4011C3E41D60'   SF = UNPROT, SBA=228, SF=PROT
         DC    C'ZIP:'
         DC    X'1D5011C36F1D6011C540'  SF=UNPROT, SBA=239, SF=PROT,
*                                  SBA= 320
         DC    C'ENTER KEY: ENTER DATA;'
         DC    X'11C5E8'           SBA = 360
         DC    C'PA2 KEY: END PROGRAM;'
         DC    X'11C650'           SBA = 400
         DC    C'CLEAR KEY: CONTROL OPTIONS.'
FMT1SZ   EQU   *-FORMAT1
*
FORMAT2  DC    X'C7114040'         WCC, SBA = 0
         DC    C'DATA GIVEN BELOW ENTERED:'
         DC    X'11C150'           SBA = 80
         DC    C'NAME:'
         DC    X'1D4013'           SF=UNPROT, IC
FMT2FLD1 EQU   *-FORMAT2
         DC    XL33'0'             DATA FIELD 1
         DC    X'1D60'             SF = PROT
         DC    C'ADDR:'
         DC    X'1D40'             SF= UNPROT
FMT2FLD2 EQU   *-FORMAT2
         DC    XL33'0'             DATA FIELD 2
         DC    X'1D60'             SF = PROT
         DC    C'CITY:'
         DC    X'1D40'             SF = UNPROT
FMT2FLD3 EQU   *-FORMAT2
         DC    XL33'0'             DATA FIELD 3
         DC    X'1D60'             SF = PROT
         DC    C'STATE:'
         DC    X'1D40'             SF = UNPROT
FMT2FLD4 EQU   *-FORMAT2
         DC    XL21'0'
         DC    X'1D60'             SF = PROT
         DC    C'ZIP:'
         DC    X'1D50'             SF = UNPROT, NUM ONLY
FMT2FLD5 EQU   *-FORMAT2
         DC    XL5'0'
         DC    X'1D6011C540'       SF = PROT, SBA = 320
         DC    C'ENTER KEY: UPDATE DATA;'
         DC    X'11C5E8'           SBA = 360
         DC    C'PA1 KEY: PRINT DATA;'
         DC    X'11C650'           SBA = 400
         DC    C'PA2 KEY: END PROGRAM;'
         DC    X'11C6F8'           SBA = 440
         DC    C'CLEAR KEY: CONTROL OPTIONS.'
FMT2SZ   EQU   *-FORMAT2
*
FORMAT3  DC    X'C711404013'       WCC, SBA =0, IC
         DC    C'XXYY3CUU'
         DC    X'11C150'           SBA = 80
         DC    C'ENTER KEY: RESUME AND CONTINUE;'
         DC    X'11C1F8'           SBA = 120
         DC    C'PA1 KEY: BEGIN NEW ENTRY;'
         DC    X'11C260'           SBA = 160
         DC    C'PA2 KEY: END PROGRAM;'
         DC    X'11C3F0'           SBA = 240
         DC    C'TO REQUEST BTAM OLT -- ENTER REQUEST FOR TEST MESSAGE *
               OVER SAMPLE FORMAT ABOVE:  '
         DC    C'XX=TEST NO. (23-28)  YY=REPEATS (01-99)'
         DC    C' CUU=ADDRESS OF TARGET DEVICE'
         DC    X'11C6D1'           SBA = 401
         DC    C'THEN HIT ERASE EOF AND THEN TEST REQ.  USE CLEAR KEY T*
               O RESUME AFTER TEST.'
FMT3SZ   EQU   *-FORMAT3
*
FORMAT4  DC    X'C711C17D'         WCC, SBA = 125
         DC    C'LOCAL 3270 SAMPLE PROGRAM ENDED.'
FMT4SZ   EQU   *-FORMAT4
*
*
*                0123456789ABCDEF
FLDXLATE DC    C'                ' 0
         DC    C'                ' 1
         DC    C'                ' 2
         DC    C'                ' 3
         DC    C'          Ö.<(+×' 4
         DC    C'&&         !$*);^' 5
         DC    C'-/         ,%_>?' 6
         DC    C'          :#@''="' 7
         DC    C' ABCDEFGHI      ' 8
         DC    C' JKLMNOPQR      ' 9
         DC    C'  STUVWXYZ      ' A
         DC    C'                ' B
         DC    C' ABCDEFGHI      ' C
         DC    C' JKLMNOPQR      ' D
         DC    C'  STUVWXYZ      ' E
         DC    C'0123456789      ' F
*                0123456789ABCDEF
         END
//GO.SYSPRINT DD SYSOUT=A                                      @SA78101
//GO.SYSPRINT DD SYSOUT=A                                      @SA78101
