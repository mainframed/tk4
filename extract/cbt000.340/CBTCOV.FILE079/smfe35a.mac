//SMFE35A  JOB MSGLEVEL=1
//STEPB  EXEC  ASMFCL
//ASM.SYSIN DD *
*     *     *     *     *     *     *     *     *     *     *    *    *
* CSECT NAME-E35                                                      *
* ENTRY POINT-E35                                                     *
* EXIT TO OS/SORT                                                     *
* RETURN CODES USED                                                   *
*    RC=0 NO ACTION                                                   *
*    RC=8 DO NOT RETURN                                               *
*    RC=12 INSERT RECORD                                              *
* INPUT A-REGISTER 1 POINTS TO A WORD PARAMETER LIST THAT CONTAINS-   *
*          1-ADDRESS OF RECORD LEAVING MERGE                          *
*          2-ADDRESS OF RECORD IN OUTPUT AREA                         *
*          3-SEQUENCE CHECK SWITCH (SET TO SUPPRESS SEQUENCE CHECKING *
*            WHILE THIS ROUTINE IS ACTIVE                             *
*       B-2 TEMPORARY DATA SETS CREATED BY THE E15 EXIT ROUTINE.      *
*         THE FIRST CONTAINS ALL OF THE HEADERS AND TRAILERS FROM     *
*         THE DUMP TAPE(S) INPUTTED TO THE SORT                       *
*         THE SECOND CONTAINS ALL OTHER SYSTEM ORIENTED RECORDS       *
*         FROM THE DUMP TAPE(S) INPUTTED TO THE SORT                  *
* FUNCTION-THIS VERSION OF THE E35 SORT EXIT RE-INSERTS THE SYSTEM    *
*          ORIENTED RECORDS INTO THE FINAL MERGED OUTPUT IN THE       *
*          FOLLOWING SEQUENCE-                                        *
*          1-ALL DUMP HDR RECORDS                                     *
*          2-ALL DUMP TLR RECORDS                                     *
*          3-ALL OTHER SYSTEM ORIENTED RECORDS IN THE SEQUENCE IN     *
*            WHICH THEY WERE GENERATED BY SYSTEM MANAGEMENT           *
*            FACILITIES                                               *
*            THE SORT WILL THEN PLACE INTO THE FINAL MERGED OUTPUT    *
*            THE SORTED JOB ORIENTED RECORDS                          *
* OPERATION-UPON INITIAL ENTRY,THE HDR/TLR DATA SET IS OPENED.THE DATA*
*         RECORDS ARE SCANNED FOR RECORD TYPE 2 (DUMP HDR).EACH TIME  *
*         A TYPE 2 IS FOUND AND A RETURN IS MADE TO THE SORT TO HAVE  *
*         THE REORD INSERT INTO THE FINAL OUTPUT.WHEN EODAD IS REACHED*
*         THE SET IS CLOSED & RE-OPENED.THIS SCAN IS PERFORMED AGAIN, *
*         THIS TIME FOR TYPE 3 (DUMP TLR) AND ARE INSERTED AGAIN.AT   *
*         EODAD FOR THE SECOND TIME,THIS SET IS CLOSED PERMANENTLY.   *
*         THE OTHER TEMPORARY SET IS OPENED AND A LOOP IS EXECUTED    *
*         INSERTING EACH RECORD AS IT IS GOTTEN UNTIL EODAD IS        *
*         REACHED.AT THIS POINT,ALL SYSTEM ORIENTED RECORDS HAVE      *
*         BEEN PROCESSED AND AFTER CLOSING THE SET RETURN IS MADE TO  *
*         THE SORT WITH A RETURN CODE INDICATING THAT NO RETURN IS TO *
*         BE MADE TO THIS EXIT.                                       *
*                                                                     *
* NOTES-THE BUFFER USED IN THIS ROUTINE WAS DEFINED TO HOLD A         *
*        MAXIMUM RECORD SIZE OF 392 BYTES.  SOME INSTALLATIONS USE    *
*        LARGER RECORDS AND SHOULD REDEFINE THIS BUFFER TO HOLD       *
*        THEIR MAXIMUM RECORD SIZE.                                   *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*     *     *     *     *     *     *     *     *     *     *    *    *
R01      EQU   1                   PARAMETER REGISTER
R02      EQU   2                   WORK REGISTER
IV       EQU   4
VIII     EQU   8
R08      EQU   8                   SOR ADDRESS REGISTER
R10      EQU   10                  WORKING REGISTER FOR SORT RECD ADDR
R11      EQU   11                  PARAMETER LIST ADDRESS REGISTER
R12      EQU   12                  BASE REGISTER
R13      EQU   13                  SAVE AREA ADDRESS REGISTER
R15      EQU   15                  ENTRY POINT REGISTER
INSERT   EQU   X'01'
JTERM    EQU   X'05'
ONCE     EQU   X'20'               1ST SOR PROCESS SWITCH
CLOSONCE EQU   X'24'               SWITCH MASKS/HDRS DONE & 1ST SOR
ONCEOVER EQU   X'DF'
INSOFF   EQU   X'FE'
CLOSED   EQU   X'02'
CLOSEA   EQU   X'04'
OPENA    EQU   X'08'
PASS2    EQU   X'10'
E35      CSECT
* 546000,654000,732000-739000                                    A35184
         USING *,R12               ESTABLISH ADDRESSABILTY
         SAVE  (14,12)             SAVE CALLER'S REGS
         LR    R12,R15             LOAD BASE ADDRESS
         USING SORFORM,R08
         USING JORFORM,R10
         ST    R13,SAVDS+4         CHAIN SORT SAVEAREA TO E35
         LR    R02,R13             RETAIN ADDRESS OF SORT'S AREA
         LA    R13,SAVDS           ADDRESS OF E35 SAVE AREA
         ST    R13,8(R02)          CHAIN E35 SAVEAREA TO SORT
         LR    R11,R01             RETAIN PARAMETER LIST ADDRESS
         LA    R02,IV              CREATE NO SEQ CHECK BIT
         ST    R02,VIII(R11)       PLACE IN SORT PARM LIST
         TM    BITS,CLOSEA         IS HDR SET CLOSED
         BO    E35PROC             YES,BYPASS HDR INSERTIONS
         TM    BITS,OPENA          IS HDR SET OPEN
         BO    E35XCPTA            YES,BYPASS OPENING
         OPEN  E35DCBA             NO,OPEN FOR FIRST SCAN
         OI    BITS,OPENA          TURN ON OPEN INDICATOR
E35XCPTA GET   E35DCBA,HTBUFF      GET A HDR/TLR RECORD          A35184
         CLC   5(1,R01),ID         IS THE THE RIGHT TYPE
         BNE   E35XCPTA            NO,GET NEXT RECORD
         B     E35INRTN            YES,GO TO INSERT RETURN
E35EODID CLOSE E35DCBA             CLOSE HDR/TLR SET
         TM    BITS,PASS2          HAS SET BEEN CLOSED ONCE
         BO    E35XCPTB            SET END OF PHASE BIT AND GO ON
         MVI   ID,X'03'            NO,CHANGE ID BEING SCANNED FOR
         OPEN  E35DCBA             OPEN HDR/TLR SET SECOND TIME
         OI    BITS,PASS2          INDICATE HDR/TLR SET CLOSED ONCE
         B     E35XCPTA            GO PERFORM SCAN
E35XCPTB OI    BITS,CLOSONCE       SET SWITCHES TO INDICATE HDR SET    X
               SET CLOSED AND FIRST TIME FOR SOR PROCESSING
         B     E35PROC             YES,GO PROCESS REST OF SOR'S
E35PROC  L     R08,SORSAV          LOAD ADDRESS OF CURRENT SOR
         LTR   R08,R08             IS IT ZERO
         BNE   E35PRCS             NO,DATA SET IS OPEN
         OPEN  E35DCB              OPEN DATA SET CREATED AT E1K EXIT
E35PRCS  GET E35DCB,BUFF           GET THE FIRST RECORD          A35184
         ST    R01,SORSAV          SAVE THE ADDRESS OF RECORD
E35INRTN L     R13,SAVDS+4         ADDRESS OF SORT SAVE AREA
         ST    R01,24(R13)         ADRESS OF RECD INTO SORT SAVEAREA
         RETURN (14,12),RC=12      RETURN TO SORT W/RC=INSERT
E35EODAD CLOSE E35DCB              CLOSE SOR SET
         OI    BITS,CLOSED         TURN ON CLOSED BIT
E35ENDB  L     R13,SAVDS+4
         RETURN (14,12),RC=8       DO NOT RETURN
SAVDS    DS    18F                 REGISTER SAVE AREA
SORSAV   DC    F'0'
BITS     DC    X'00'
ID       DC    X'02'
E35DCB   DCB   DSORG=PS,MACRF=GM,DDNAME=SORDATA,EODAD=E35EODAD   A35184
E35DCBA  DCB   DSORG=PS,MACRF=GM,DDNAME=HDRDATA,EODAD=E35EODID   A35184
HTBUFF   DS    CL18                BUFFER FOR HDR/TLR RECORDS    A35184
BUFF     DS    CL400               BUFF TO REBUILD SEGMENTED REC A35184
SORFORM  DSECT
SORLNGTH DS    CL4
SORCONT  DS    C
SORID    DS    C
SORTIME  DS    CL4
SORDATE  DS    CL4
SORCPUID DS    CL4
         DS    CL32
SORJLGNO DS    0CL13
SORJNAME DS    CL8
SORSTIME DS    CL4
SORSDATE DS    CL4
SORSTPNO DS    C
JORFORM  DSECT
JORLNGTH DS    CL4
JORCONT  DS    C
JORID    DS    C
JORTIME  DS    CL4
JORDATE  DS    CL4
JORCPUID DS    CL4
         DS    CL32
JORJLGNO DS    0CL13
JORJNAME DS    CL8
JORSTIME DS    CL4
JORSDATE DS    CL4
JORSTPNO DS    C
         END
//LKED.SYSLMOD DD   DSNAME=SMF1.EXITS,DISP=(OLD,KEEP),                 C
//             VOLUME=REF=SYS1.LINKLIB,SPACE=(TRK,(10,5,1))
//LKED.SYSIN   DD *
 NAME E35(R)
