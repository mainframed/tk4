//GSPSAMP JOB 1234567,GSPSAMP,MSGLEVEL=1
//SAMPL EXEC FORTGCLG,PARM.FORT=(NODECK,MAP,LOAD)
//FORT.SYSIN DD *
C     THIS SAMPLE PROGRAM DRAWS A CIRCLE OF 8 BASIC SIZE X'S WITH A
C     LARGE SIZE X AS ITS CENTER. DEPENDING ON THE ATTENTION CREATED,
C     THE PROGRAM WILL DO THE FOLLOWING:
C
C
C         1. LIGHT PEN ATTENTION ON CENTER X LABELS OUTER X'S FROM
C            NUM1 THROUGH NUM8. (STATEMENT 221).
C
C         2. LIGHT PEN ATTENTION ON OUTER X GENERATES A CIRCLE WHOSE
C            CENTER IS THE DETECTED X. (STATEMENT 222).
C
C         3. LIGHT PEN ATTENTION ON CIRCLE AROUND OUTER X MAKES THAT
C            CIRCLE INVISIBLE. (STATEMENT 230).
C
C         4. LIGHT PEN ATTENTION ON ANY LABEL MAKES ALL LABELS
C            INVISIBLE. (STATEMENT 225).
C
C         5. PROGRAMMED FUNCTION KEY 1 ENDS RUN. (STATEMENT 240).
C
C         6. PROGRAMMED FUNCTION KEY 2 MAKES ALL CIRCLES INVISIBLE.
C            (STATEMENT 250).
C
C         7. PROGRAMMED FUNCTION KEY 3 MAKES ALL CIRCLES VISIBLE.
C            (STATEMENT 260).
C                                  BEGIN PROGRAM
C                  SET UP DIMENSION STATEMENT FOR VARIOUS ARRAYS USED.
C
C     C635000                                                   A 45402
C
      DIMENSION INTARY(10),CIRX(8),CIRY(8),SMALX(8,24),SMALY(8,24),
     X LABEL(32),NULL(1)
C                  INITIALIZE THE GRAPHIC SUBROUTINE PACKAGE. DEFINE
C                  THE NULL VARIABLE.
      NULL(1)=-5
      CALL INGSP(IGSP,NULL)
C                  INITIALIZE THE 2250. ASSIGN IT UNIT NUMBER 10.
      CALL INDEV(IGSP,10,I2250)
C                  INITIALIZE A GRAPHIC DATA SET THAT WILL BE USED TO
C                  DISPLAY THE CIRCLE OF X'S AND ITS CENTER X.
      CALL INGDS(I2250,IGDS1)
C                  INITIALIZE A GRAPHIC DATA SET THAT WILL BE USED TO
C                  LABEL THE OUTER X'S OF THE CIRCLE.
      CALL INGDS(I2250,IGDS2)
C                  INITIALIZE A GRAPHIC DATA SET THAT WILL BE USED TO
C                  DISPLAY A CIRCLE AROUND ANY OF THE OUTER X'S.
      CALL INGDS(I2250,IGDS3)
C                  CREATE AN ATTENTION LEVEL FOR THE 2250.
      CALL CRATL(I2250,IATL)
C                  ENABLE PROGRAMMED FUNCTION KEYS 1,2, & 3 & LIGHT PEN.
      CALL ENATN(IATL,1,-3,34)
      CALL MLITS(I2250,3)
C                  ALLOW LIGHT PEN ATTENTIONS FOR EACH GRAPHIC DATA SET.
      CALL SLPAT(IGDS1,1)
      CALL SLPAT(IGDS2,1)
      CALL SLPAT(IGDS3,1)
C                  THE FOLLOWING INFORMATION IS ON CARD RECORDS. DEVICE
C                  NUMBER IS 5. CHARACTER X IS LEFT ALIGNED AND APPEARS
C                  ON THE CARD RECORD AS XBBB . EIGHT 4-DIGIT LABELS
C                  (NUM1,NUM2,ETC.) FOLLOW THE X. STORAGE WAS ALLOCATED
C                  FOR THE ARRAY "LABEL" BY THE PREVIOUS DIMENSION STATE
C                  MENT.
C
C                  PLACE CHARACTER X IN MAIN STORAGE.
   20 FORMAT(A4)
      READ(5,20)X
C                  PLACE LABELS IN MAIN STORAGE.
   30 FORMAT(8A4)
      READ(5,30)(LABEL(INCR),INCR=1,8)
C                  SET BEAM POSITION TO CENTER OF CIRCLE.
      CX=2047.0
      CY=2047.0
C                  SET CHARACTER MODE FOR LARGE SIZE, PROTECTED.
      CALL SCHAM(IGDS1,2)
C                  DISPLAY CENTER X.
      NUM=-1
      CALL PTEXT(IGDS1,X,1,NUM,NULL,1,CX,CY)
C                  COMPUTE X AND Y COORDINATES FOR X'S OF OUTER CIRCLE.
C                  PLACE THEM IN THE CIRX AND CIRY ARRAYS FOR WHICH MAIN
C                  STORAGE HAS BEEN ALLOCATED BY DIMENSION STATEMENT.
      R=1200.0
      C=3.141596/180
      THETA=45.0
      DO 10 I=1,8
      RADIAN=THETA*FLOAT(I)*C
      CIRX(I)=CX+R*COS(RADIAN)
   10 CIRY(I)=CY+R*SIN(RADIAN)
C                  SET CHARACTER MODE FOR BASIC SIZE, PROTECTED X.
      CALL SCHAM(IGDS1,1)
C                  GENERATE AN X IN INCLUDE STATUS. CALCULATE
C                  X-COORDINATE OF LABEL ASSOCIATED WITH THAT X.
C                  GENERATE LABEL IN INCLUDE STATUS. SET INCR TO NEXT
C                  LABEL IN ARRAY. DO THIS 8 TIMES TO COMPLETE CIRCLE.
      DO 50 NUM=1,8
      CALL PTEXT(IGDS1,X,1,NUM,NULL,1,CIRX(NUM),CIRY(NUM))
      CXNUM=CIRX(NUM)+300.0
   50 CALL PTEXT(IGDS2,LABEL(NUM),4,NUM,NULL,1,CXNUM,CIRY(NUM))
C                  COMPUTE COORDINATES FOR CIRCLES AROUND OUTER X'S.
C                  PLACE THEM IN SMALX AND SMALY ARRAYS FOR WHICH MAIN
C                  STORAGE HAS BEEN ALLOCATED BY DIMENSION STATEMENT.
      R=200.0
      THETA=360.0/24.0
      DO 105 N=1,24
      RADIAN=THETA*FLOAT(N)*C
      DO 105 I=1,8
      SMALX(I,N)=CIRX(I)+R*COS(RADIAN)
  105 SMALY(I,N)=CIRY(I)+R*SIN(RADIAN)
C                  GENERATE CIRCLES AROUND OUTER X'S IN OMIT STATUS.
      DO 110 NUM=1,8
      CALL STPOS(IGDS3,CIRX(NUM)+200.0,CIRY(NUM))
  110 CALL PLINE(IGDS3,SMALX(NUM,1),SMALY(NUM,1),NUM,NULL,2,24,8,8)
C                  OMIT LABELS FROM THE DISPLAY.
      CALL OMIT(IGDS2)
C                  EXECUTE ALL GRAPHIC DATA PRODUCED.
      CALL EXEC(IGDS1)
      CALL EXEC(IGDS2)
      CALL EXEC(IGDS3)
C                  REQUEST ATTENTION INFORMATION TO DETERMINE WHETHER
C                  2250 OPERATOR WANTS TO CHANGE OR END THE DISPLAY.
C                  WAIT FOR AN ATTENTION TO OCCUR.
  200 CALL RQATN(IATL,INTCD,2,INTARY,1,2,3,34)
C                  IF LIGHT PEN ATTENTION, GO TO 210.
C                  IF PROGRAMMED FUNCTION KEY ATTENTION, GO TO 235.
      IF (INTCD-34) 235,210,210
C                  IF LIGHT PEN ATTENTION WAS ASSOCIATED WITH IGDS1, GO
C                  TO 220. IF IGDS2, GO TO 225. IF IGDS3, GO TO 230.
  210 IF(INTARY(1)-IGDS1) 201,220,201
  201 IF(INTARY(1)-IGDS2) 202,225,202
  202 IF(INTARY(1)-IGDS3) 200,230,200
C                  IF LIGHT PEN ATTENTION WAS ON CENTER OF LARGE CIRCLE,
C                  GO TO 221. IF ON OUTER CIRCLE, GO TO 222.
  220 IF(INTARY(4)) 221,222,222
C                  DISPLAY LABELS AND WAIT FOR ANOTHER ATTENTION.
  221 CALL INCL(IGDS2)
      GO TO 200
C                  DISPLAY A CIRCLE AROUND THE OUTER X DETECTED AND WAIT
C                  FOR ANOTHER ATTENTION.
  222 NUM=INTARY(4)
      CALL INCL(IGDS3,NUM)
      GO TO 200
C                  REMOVE LABELS FROM DISPLAY. AWAIT NEW ATTENTION.
  225 CALL OMIT(IGDS2)
      GO TO 200
C                  REMOVE DETECTED CIRCLE FROM DISPLAY. AWAIT ATTENTION.
  230 NUM=INTARY(4)
      CALL OMIT(IGDS3,NUM)
      GO TO 200
C                  IF PROGRAMMED FUNCTION KEY 1 DEPRESSED, GO TO 240.
C                  IF KEY 2, GO TO 250. IF KEY 3, GO TO 260.
  235 GO TO (240,250,260),INTCD
C                  TERMINATE THE PROGRAM RUN.
  240 CALL TMGSP(IGSP)
      CALL EXIT
C                  REMOVE ALL CIRCLES AROUND OUTER X'S. AWAIT ATTENTION.
  250 DO 251 I=1,8
  251 CALL OMIT(IGDS3,I)
      GO TO 200
C                  DISPLAY ALL CIRCLES AROUND OUTER X'S. AWAIT ATTENTION
  260 DO 261 I=1,8
  261 CALL INCL(IGDS3,I)
      GO TO 200
      END
//LKED.SYSIN DD *
 INCLUDE SYSLIB(IHCGSP03)
//GO.SYSABEND  DD SYSOUT=A
//GO.FT10F001  DD  UNIT=(2250-1)
//GO.SYSIN    DD *
X
NUM1NUM2NUM3NUM4NUM5NUM6NUM7NUM8
