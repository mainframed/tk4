USERLABL CSECT
         SPACE 3
**********************************************************************
*                                                                    *
*   THIS PROGRAM ILLUSTRATES ONE APPLICATION FOR THE USER LABEL      *
*   FACILITY.  A SINGLE VOLUME BSAM DATA SET IS OPENED FOR OUT-      *
*   PUT.  EIGHT HEADER AND EIGHT TRAILER LABELS ARE CREATED BY       *
*   THE USER LABEL EXIT ROUTINES.                                    *
*                                                                    *
**********************************************************************
         EJECT
******************************
         SPACE 2
*************************REGISTER ASSIGNMENTS*************************
         SPACE 2
******************************
         SPACE
R0       EQU   0
         SPACE
R1       EQU   1
         SPACE
R2       EQU   2
         SPACE
R3       EQU   3
         SPACE
R4       EQU   4
         SPACE
R5       EQU   5
         SPACE
R6       EQU   6
         SPACE
R7       EQU   7
         SPACE
R8       EQU   8
         SPACE
R9       EQU   9
         SPACE
R10      EQU   10
         SPACE
R11      EQU   11
         SPACE
R12      EQU   12
         SPACE
R13      EQU   13
         SPACE
R14      EQU   14
         SPACE
R15      EQU   15
         SPACE
BUFAD    EQU   0        *DISP INTO PARM LIST FOR LBL BUF ADR
         SPACE
DCBAD    EQU   4        *DISP INTO PARM LIST FOR DCB ADR
         SPACE
IOERRAD  EQU   8        *DISP INTO PARM LIST FOR I/O ERR STATUS ADR
         SPACE
*******************************
         EJECT
******************************
         SPACE 2
****************************INITIALIZATION****************************
         SPACE 2
******************************
         SPACE
         SAVE  (14,12)        *SAVE CALLERS REGS.
         SPACE
         BALR  R12,0          *ESTABLISH ADDRESSABILITY
         SPACE
         USING *,12
         SPACE
         LA    R2,MYSAVE      *CHAIN
         SPACE
         ST    R2,8(0,R13)    *SAVE AREAS
         SPACE
         ST    R13,4(0,R2)    *TOGETHER
         SPACE
         LR    R13,R2         *SET SAVE AREA PTR.
         SPACE
*******************************
         EJECT
******************************
         SPACE 2
***********************OPEN DATA SET FOR OUTPUT**********************
         SPACE 2
******************************
         SPACE
OPEN1    OPEN  (OUTDCB,(OUTPUT))  *OPEN FOR OUTPUT
         SPACE
*        ***HEADER LABEL ROUTINE GETS CONTROL DURING OPEN***
         SPACE
CKTRACK  CLI   ERRCODE,X'00'  *WAS THERE AN ERROR IN LABEL PROCESSING
         SPACE
       BNE   DUMP           *YES, GO TO TERMINATE JOB WITH DUMP
         SPACE
*******************************
         EJECT
******************************
         SPACE 2
*************************WRITE EIGHT RECORDS*************************
         SPACE 2
******************************
         SPACE
SETWRITE LA    R5,8(0,0)          *SET CONTROL FOR 8 RECORDS
         SPACE
LOOP1    WRITE WDECB,SF,OUTDCB,RECORD *WRITE DATA (FIRST VOLUME)
         SPACE
         CHECK WDECB                  *VALIDATE WRITE
         SPACE
         BCT   R5,LOOP1               *REPEAT 8 TIMES
         SPACE
*******************************
         EJECT
******************************
         SPACE 2
*************************CLOSE THE DATA SET**************************
         SPACE 2
******************************
         SPACE
         CLOSE (OUTDCB)       *CLOSE DATA SET
         SPACE
*        ***OUTPUT TRAILER ROUTINE GETS CONTROL DURING CLOSE***
         SPACE
         CLI   ERRCODE,X'00'  *WAS THERE AN ERROR IN LABEL PROCESSING
         SPACE
         BNE   DUMP           *YES, GO TO TERMINATE JOB
         SPACE
******************************
         SPACE 2
***********************RETURN TO CONTROL PROGRAM*********************
         SPACE 2
******************************
         SPACE
ENDJOB   L     R13,4(0,R13)   *EXIT
         SPACE
         RETURN (14,12),RC=0
         SPACE
*******************************
         EJECT
*******************************
         SPACE 2
*******************SYNAD ROUTINE*************************************
         SPACE 2
******************************
         SPACE
OUTSYNAD MVI   ERRCODE,X'11'  *SET ERROR CODE
         SPACE
         B     DUMP           *ABORT JOB
         SPACE
*******************************
         SPACE 2
*******************DUMP ROUTINE**************************************
         SPACE 2
*******************************
         SPACE
DUMP     SR    R1,R1          *CLEAR REGISTER ONE
         SPACE
         IC    R1,ERRCODE     *SET UP ERROR CODE
         SPACE
         NI    EXITLIST,X'00'      *DEACTIVATE EXITS 2 AND 4 BEFORE
         SPACE
         NI    EXITLIST+4,X'00'     *ABEND
         SPACE
         ABEND  (1),DUMP
         SPACE
*******************************
         EJECT
******************************
         SPACE 2
*****SUBROUTINE TO PRINT 80 CHAR. MESSAGES***************************
         SPACE 2
******************************
         SPACE
MSGOUT   NI    MSGSWTCH,X'00'     *SET UNABLE TO OPEN SWITCH OFF
         SPACE
         LR    R7,R1              *SAVE MSG ADDRESS
         SPACE
MSGTEST  TM    MSGDCB+48,X'10'    *IS DCB OPEN
         SPACE
         BO    MSGWRITE           *YES, GO WRITE MESSAGE
         SPACE
         TM    MSGSWTCH,X'01'     *NO,IS UNABLE TO OPEN SWITCH ON
         SPACE
         BO    WTOMSG             *YES,ERROR
         SPACE
         OPEN  (MSGDCB,(OUTPUT))  *NO,OPEN DCB
         SPACE
         OI    MSGSWTCH,X'01'     *TURN SWITCH ON
         SPACE
         B     MSGTEST            *REPEAT PROCESS
         SPACE
MSGWRITE PRTOV MSGDCB,12          *SPACE TO NEW PAGE WHEN NEEDED
         EJECT
         WRITE MSGDECB,SF,MSGDCB,(7) *WRITE MSG
         SPACE
         CHECK MSGDECB               *VALIDATE WRITE
         SPACE
         BR    R11                   *RETURN TO CALLER
         SPACE
WTOMSG   WTO   'UNABLE TO OPEN OUTPUT WRITER'
         SPACE
         MVI   ERRCODE,X'63'  *SET ERROR CODE
         SPACE
         B     DUMP           *DUMP
         SPACE
*******************************
         SPACE 2
**********************************************************************
         EJECT
**********************************************************************
         SPACE 2
******************EXIT-2- OUTPUT HEADER LABEL EXIT ROUTINE*************
         SPACE 2
**********************************************************************
         EJECT
******************************
         SPACE 2
*******************CREATE EIGHT HEADER LABELS************************
         SPACE 2
******************************
         SPACE
OHLBL1   LA    R13,EX2SAVE    *PROVIDE A SAVE AREA
         SPACE
         LR    R4,R1         *SAVE PARM LIST ADR
         SPACE
         ST    R14,SAVE14     *SAVE RETURN ADR.
         SPACE
         TM    IOERRAD(R4),X'80'  *WAS I/O ERR ENCOUNT. WRITING LBL
         SPACE
         BO    IOERROR2      *YES, GO TO ERROR ROUTINE
         SPACE
         CLC   BUFAD+1(3,R4),=3X'00'  *NO, WAS LBL TRK ALLOCATED
         SPACE
         BE    REG0RTN2      *NO, GO TO ERROR ROUTINE
         SPACE
         L     R6,BUFAD(R4)  *GET ADR OF LBL BUFFER
         SPACE
         LA    R6,0(R6)      *CLEAR HIGH ORDER BYTE
         SPACE
         MVC   0(1,R6),HCOUNT+3   *GET LABEL COUNT
         SPACE
         OI    0(R6),X'F0'    *MAKE IT EBCDIC
         SPACE
         MVC   1(79,R6),0(R6)  *PROP. COUNT THRU LABEL
         SPACE
         L     R6,HCOUNT      *GET LABEL COUNT
         SPACE
         LA    R6,1(0,R6)     *ADD ONE TO LABEL COUNT
         SPACE
         ST    R6,HCOUNT      *RESTORE LABEL COUNT
         SPACE
         LA    R15,8          *SET RETURN CODE TO EIGHT
         SPACE
RETURN2  L     R14,SAVE14     *RESTORE RETURN ADR.
         SPACE
         RETURN RC=(15)       *RETURN TO I/O SUPPORT
         SPACE
*******************************
         EJECT
******************************
         SPACE 2
*******************ALLOCATION ERROR ROUTINE**************************
         SPACE 2
******************************
         SPACE
REG0RTN2 MVI   ERRCODE,X'22'  *TURN ON ERROR CODE
         SPACE
         LA    R1,EX2RMSG1    *GET ADDRESS OF FIRST ERROR MESSAGE
         SPACE
         MVI   5(R1),C'2'     *SET TYPE OF LABEL ROUTINE IN MESSAGE
         SPACE
         BAL   R11,MSGOUT     *GO TO PRINT ERROR MESSAGE
         SPACE
         LA    R1,EX2RMSG2    *GET ADDRESS OF SECOND ERROR MESSAGE
         SPACE
         BAL   R11,MSGOUT     *GO TO PRINT ERROR MESSAGE
         SPACE
         B     RETURN2        *GO TO RETURN WITH NO RETURN CODE
         SPACE
*******************************
         SPACE 2
*******************I/O ERROR ROUTINE*********************************
         SPACE 2
******************************
         SPACE
IOERROR2 MVI   ERRCODE,X'12'  *SET ERROR CODE
         SPACE
         LA    R1,EX2IOMSG    *GET ADDRESS OF FIRST ERROR MESSAGE
         SPACE
         MVI   5(R1),C'2'     *SET TYPE OF LABEL ROUTINE IN MESSAGE
         SPACE
         BAL   R11,MSGOUT     *GO TO PRINT ERROR MESSAGE
         SPACE
         LA    R1,EX2RMSG2    *GET ADDRESS OF SECOND ERROR MESSAGE
         SPACE
         BAL   R11,MSGOUT    *GO TO PRINT ERROR MESSAGE
         SPACE
         SR    R15,R15        *SET RETURN CODE TO ZERO
         SPACE
         B     RETURN2        *GO TO RETURN TO I/O SUPPORT
         SPACE
******************************
         SPACE 2
***********************************************************************
         EJECT
***********************************************************************
         SPACE 2
******************EXIT-4- OUTPUT TRAILER LABEL EXIT ROUTINE************
         SPACE 2
***********************************************************************
         EJECT
******************************
         SPACE 2
*******************CREATE EIGHT TRAILER LABELS***********************
         SPACE 2
*******************************
         SPACE
OTLBL1   LA    R13,EX4SAVE    *PROVIDE A SAVE AREA
         SPACE
         LR    R4,R1         *SAVE PARM LIST ADR
         SPACE
         ST    R14,SAVE14     *SAVE RETURN ADR.
         SPACE
         TM    IOERRAD(R4),X'80'  *WAS I/O ERR ENCOUNT. WRITING LBL
         SPACE
         BO    IOERROR4      *YES, GO TO ERROR ROUTINE
         SPACE
         CLC   BUFAD+1(3,R4),=3X'00'  *NO, WAS LBL TRK ALLOCATED
         SPACE
         BE    REG0RTN4      *NO, GO TO ERROR ROUTINE
         SPACE
         L     R6,BUFAD(R4)  *GET LBL BUFFER ADR
         SPACE
         LA    R6,0(R6)      *CLEAR HIGH ORDER BYTE
         SPACE
         MVC   0(1,R6),TCOUNT+3   *GET LABEL COUNT
         SPACE
         OI    0(R6),X'F0'    *MAKE IT EBCDIC
         SPACE
         MVC   1(79,R6),0(R6)  *PROP. COUNT THRU LABEL
         SPACE
         L     R6,TCOUNT      *GET LABEL COUNT
         SPACE
         LA    R6,1(0,R6)     *ADD ONE TO LABEL COUNT
         SPACE
         ST    R6,TCOUNT      *RESTORE LABEL COUNT
         SPACE
         LA    R15,8          *SET RETURN CODE TO EIGHT
         SPACE
RETURN4  L     R14,SAVE14     *RESTORE RETURN ADR.
         SPACE
         RETURN RC=(15)       *RETURN TO I/O SUPPORT
         SPACE
*******************************
         EJECT
*******************************
         SPACE 2
*******************ALLOCATION ERROR ROUTINE**************************
         SPACE 2
******************************
         SPACE
REG0RTN4 MVI   ERRCODE,X'24'  *TURN ON ERROR CODE
         SPACE
         LA    R1,EX2RMSG1    *GET ADDRESS OF FIRST ERROR MESSAGE
         SPACE
         MVI   5(R1),C'4'     *SET TYPE OF LABEL ROUTINE IN MESSAGE
         SPACE
         BAL   R11,MSGOUT     *GO TO PRINT ERROR MESSAGE
         SPACE
         LA    R1,EX2RMSG2    *GET ADDRESS OF SECOND ERROR MESSAGE
         SPACE
         BAL   R11,MSGOUT     *GO TO PRINT ERROR MESSAGE
         SPACE
         B     RETURN4        *GO TO RETURN WITH NO RETURN CODE
         SPACE
*******************************
         SPACE 2
*******************I/O ERROR ROUTINE*********************************
         SPACE 2
******************************
         SPACE
IOERROR4 MVI   ERRCODE,X'14'  *SET ERROR CODE
         SPACE
         LA    R1,EX2IOMSG    *GET ADDRESS OF FIRST ERROR MESSAGE
         SPACE
         MVI   5(R1),C'4'     *SET TYPE OF LABEL ROUTINE IN MESSAGE
         SPACE
         BAL   R11,MSGOUT     *GO TO PRINT ERROR MESSAGE
         SPACE
         LA    R1,EX2RMSG2    *GET ADDRESS OF SECOND ERROR MESSAGE
         SPACE
         BAL   R11,MSGOUT     *GO TO PRINT OUT ERROR MESSAGE
         SPACE
         SR    R15,R15        *SET RETURN CODE TO ZERO
         SPACE
         B     RETURN4        *GO TO RETURN TO I/O SUPPORT
         SPACE
******************************
         SPACE 2
*********************************************************************
         EJECT
***********************************************************************
         SPACE 2
*                                CONSTANTS                            *
         SPACE 2
***********************************************************************
         EJECT
******************************
         SPACE 2
*******************EXIT LIST FOR OUTPUT USER LABEL ROUTINES**********
         SPACE 2
*******************************
         SPACE
         DS    0F
         SPACE
EXITLIST DC    X'02'         *EXIT-2 OUTPUT HEADER LABEL EXIT RTN
         SPACE
         DC    AL3(OHLBL1)
         SPACE
         DC    X'84'         *EXIT-4 OUTPUT TRAILER LABEL EXIT RTN
         SPACE
         DC    AL3(OTLBL1)
         SPACE
******************************
         SPACE 2
*******************STORAGE*******************************************
         SPACE 2
*******************************
         SPACE
HCOUNT   DC    F'1'           *NUMBER OF HEADER LABELS TO BE CREATED
         SPACE
TCOUNT   DC    F'1'           *NUMBER OF TRAILER LABELS TO BE CREATED
         SPACE
SAVE14   DC    F'0'
         SPACE
ERRCODE  DC    X'00'
         SPACE
MSGSWTCH DC    X'00'
         SPACE
RECORD   DC    20C'DATA'
         SPACE
******************************
         EJECT
******************************
         SPACE 2
*******************MESSAGES******************************************
         SPACE 2
******************************
         SPACE
EX2RMSG1 DC    CL40'EXIT-2 ENTERED WITH LABEL ADD.=0 INDICAT'
         DC    CL40'ING A LABEL TRACK HAS NOT BEEN ALLOCATED'
         SPACE
EX2RMSG2 DC    CL40'JOB WILL BE TERMINATED WITH NO FURTHER P'
         DC    CL40'ROCESSING DONE.                         '
         SPACE
EX2IOMSG DC    CL40'EXIT-2 ENTERED WITH BIT 0 OF THE FLAG BY'
         DC    CL40'TE ON INDICATING AN I/O ERROR CONDITION.'
         SPACE
*******************************
         EJECT
*******************************
         SPACE 2
*************************DCB FOR OUTPUT DATA SET*********************
         SPACE 2
******************************
         SPACE
OUTDCB   DCB   DSORG=PS,                                               C
               MACRF=(W),                                              C
               DDNAME=OUTPUT,                                          C
               BLKSIZE=80,                                             C
               LRECL=80,                                               C
               RECFM=F,                                                C
               SYNAD=OUTSYNAD,                                         C
               EXLST=EXITLIST
         SPACE
******************************
         EJECT
*******************************
         SPACE 2
***********************DCB FOR PRINTING MESSAGES*********************
         SPACE 2
******************************
         SPACE
MSGDCB   DCB   DSORG=PS,                                               C
               MACRF=(WC),                                             C
               DDNAME=SYSPRINT,                                        C
               RECFM=F,                                                C
               LRECL=80,                                               C
               BLKSIZE=80
         SPACE
******************************
         EJECT
*******************************
         SPACE 2
**************************REGISTER SAVE AREAS************************
         SPACE 2
******************************
         SPACE
MYSAVE   DS    18F
         SPACE
EX2SAVE  DS    18F
         SPACE
EX4SAVE  DS    18F
         SPACE
*******************************
         SPACE 2
*********************************************************************
         END
