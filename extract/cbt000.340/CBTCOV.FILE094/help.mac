HELP     TITLE 'QUEUE COMMAND - DISPLAY HELP'                     ONL02
******************************************************************ONL02
*                                                                 ONL02
* MODULE NAME = HELP                                              ONL02
*                                                                 ONL02
* DESCRIPTIVE NAME = THE QUEUE COMMAND HELP FACILITY DISPLAY      ONL02
*                                                                 ONL02
* AUTHOR = ROBERT M. JINKINS                                      ONL02
*          OAK RIDGE NATIONAL LABORATORY                          ONL02
*          BLDG. 4500-N  MS-259                                   ONL02
*          P.O. BOX X                                             ONL02
*          OAK RIDGE, TENNESSEE  37830                            ONL02
*                                                                 ONL02
*          PHONE: (615) 574-7208/5300                             ONL02
*                  FTS  624-7208/5300                             ONL02
*                                                                 ONL02
*          SHARE INSTALLATION CODE = OR                           ONL02
*                                                                 ONL02
* DATE WRITTEN = JUNE 83                                          ONL02
*                                                                 ONL02
* FUNCTION = TO DISPLAY THE HELP SCREENS IN RESPONSE TO A "HELP"  ONL02
*    SUB-COMMAND OR AN UNDEFINED/UNAUTHORIZED SUB-COMMAND VERB.   ONL02
*    HELP INFORMATION IS ONLY DISPLAYED FOR SUB-COMMANDS WHICH    ONL02
*    THE USER IS AUTHORIZED TO USE.                               ONL02
*                                                                 ONL02
*    THE #H MACRO IS USED TO DEFINE THE HELP SCREENS AND IS       ONL02
*    DISCUSSED IN THE COMMENTS AT THE BEGINNING OF THE #H MACRO.  ONL02
*    AN OPERAND OF THIS MACRO DEFINES THE AUTHORITY REQUIRED      ONL02
*    BEFORE A USER MAY SEE A GIVEN MESSAGE IN THE HELP DISPLAY.   ONL02
*    THE USER'S AUTHORITY FOR HELP DISPLAYS (QAHELP) IS SET IN    ONL02
*    THE AUTH CSECT BY DEFAULT TO THE USER'S AUTHORITY (QAFLAGS). ONL02
*                                                                 ONL02
******************************************************************ONL02
         EJECT ,                                                  ONL02
         MACRO ,                                                  ONL02
&NAME    #H    &TYPE,&OFFSET,&TEXT,&USER=0                        ONL02
.*****************************************************************ONL02
.*                                                                ONL02
.* MACRO NAME = #H                                                ONL02
.*                                                                ONL02
.* MODIFICATION ID = ONL02                                        ONL02
.*                                                                ONL02
.* DESCRIPTIVE NAME = QUEUE COMMAND HELP FACILITY MESSAGE ENTRY   ONL02
.*                                                                ONL02
.* FUNCTION = THIS MACRO IS USED TO GENERATE ENTRIES IN HELP      ONL02
.*    TABLE (IN THE HELP CSECT).  THE HELP TABLE ENTRIES, MAPPED  ONL02
.*    BY THE HTEDSECT, IS UESD TO GENERATE THE HELP SCREENS.      ONL02
.*                                                                ONL02
.* SYNTAX:  NAME  #H  TYPE,OFFSET,TEXT,USER=X                     ONL02
.*                                                                ONL02
.*   WHERE:                                                       ONL02
.*     NAME    - IS AN OPTIONL LABEL.                             ONL02
.*     TYPE    - THIS REQUIRED OPERAND IS CODED AS FOLLOWS:       ONL02
.*                 NORM  - MSG LINE WITH NO RESTRICTIONS.         ONL02
.*                 ACTV  - MSG LINE, QAHFACTV AUTHORITY REQUIRED. ONL02
.*                 BLOG  - MSG LINE, QAHFBLOG AUTHORITY REQUIRED. ONL02
.*                 SSSM  - MSG LINE, QAHFSSSM AUTHORITY REQUIRED. ONL02
.*                 SLOG  - MSG LINE, QAHFSLOG AUTHORITY REQUIRED. ONL02
.*                 EXEC  - MSG LINE, QAHFEXEC AUTHORITY REQUIRED. ONL02
.*                 OPER  - MSG LINE, QAHFOPER AUTHORITY REQUIRED. ONL02
.*                 SYSP  - MSG LINE, QAHFSYSP AUTHORITY REQUIRED. ONL02
.*                 QAHX... - AN EXPRESSION BEGINNING WITH THE     ONL02
.*                           CHARACTERS "QAHX" DEFINING THE       ONL02
.*                           REQUIRED AUTHORITY BITS              ONL02
.*                           (E.G. "QAHXSSSM+QAHXOPER").          ONL02
.*                 PAGE  - INDICATES END OF THE CURRENT SCREEN.   ONL02
.*                 END   - INDICATES END OF THE LAST SCREEN.      ONL02
.*                                                                ONL02
.*               MESSAGE LINES REQUIRING AUTHORITY WILL NOT BE    ONL02
.*               DISPLAYED UNLESS THE CORRESPONDING AUTHORITY     ONL02
.*               BITS ARE ON IN QAHELP.                           ONL02
.*                                                                ONL02
.*     OFFSET  - GIVES THE NUMBER OF SPACES FROM THE RIGHT SIDE   ONL02
.*               OF THE SCREEN TO THE BEGINNING OF THE MESSAGE    ONL02
.*               TEXT.  THE OFFSET MUST BE A SELF-DEFINING TERM   ONL02
.*               WITH A VALUE IN THE RANGE OF 0 TO 79.  THIS      ONL02
.*               OPERAND IS IGNORED FOR TYPES "PAGE" AND "END".   ONL02
.*                                                                ONL02
.*               DEFAULT:  0                                      ONL02
.*                                                                ONL02
.*     TEXT    - IS THE TEXT TO BE DISPLAYED ON THE MESSAGE LINE. ONL02
.*               THE TEXT MUST BE ENCLOSED IN APOSTROPHES.  THE   ONL02
.*               OFFSET PLUS THE ASSEMBLED LENGTH OF THE TEXT     ONL02
.*               MUST NOT EXCEED 80 CHARACTERS.  THIS OPERAND IS  ONL02
.*               IGNORED FOR TYPES "PAGE" AND "END".              ONL02
.*                                                                ONL02
.*               NO DEFAULT, MUST BE CODED IF TYPE IS NOT "PAGE"  ONL02
.*               OR "END"                                         ONL02
.*                                                                ONL02
.*     USER=X  - SPECIFIES OPTIONAL USER FLAGS FOR USE BY HELP    ONL02
.*               CSECT MODS MADE BY YOUR INSTALLATION.  STANDARD  ONL02
.*               HELP FACILITY PROCESSING DOES NOT USE THE USER   ONL02
.*               FLAGS.  THIS OPERAND IS IGNORED FOR TYPES "PAGE" ONL02
.*               AND "END".                                       ONL02
.*                                                                ONL02
.*               DEFAULT:  USER=0                                 ONL02
.*                                                                ONL02
.* CHANGE ACTIVITY = NONE                                         ONL02
.*                                                                ONL02
.*****************************************************************ONL02
         GBLA  &MSGCNT             NUM HELP MSG'S ON CURRENT PAGE ONL02
         LCLA  &OFF                OFFSET FOR MSG TEXT            ONL02
         LCLA  &L                  COMPUTED TEXT LENGTH           ONL02
         LCLA  &N                  CURRENT TEXT INDEX             ONL02
         LCLB  &P                  SPECIAL CHAR PAIR INDICATOR    ONL02
         LCLA  &TLEN               LENGTH OF OFFSET PLUS TEXT     ONL02
         AIF   ('&TYPE' EQ 'NORM').LINE                           ONL02
         AIF   ('&TYPE' EQ 'ACTV').LINE                           ONL02
         AIF   ('&TYPE' EQ 'BLOG').LINE                           ONL02
         AIF   ('&TYPE' EQ 'SSSM').LINE                           ONL02
         AIF   ('&TYPE' EQ 'SLOG').LINE                           ONL02
         AIF   ('&TYPE' EQ 'EXEC').LINE                           ONL02
         AIF   ('&TYPE' EQ 'OPER').LINE                           ONL02
         AIF   ('&TYPE' EQ 'SYSP').LINE                           ONL02
         AIF   ('&TYPE'(1,4) EQ 'QAHX').LINE                      ONL02
         AIF   ('&TYPE' EQ 'PAGE').HTEPAGE                        ONL02
         AIF   ('&TYPE' EQ 'END').HTEEND                          ONL02
         MNOTE 8,'#H MACRO - INVALID "TYPE" OPERAND'              ONL02
         MEXIT ,                                                  ONL02
.LINE    ANOP  ,                   DEFINE HELP MESSAGE LINE       ONL02
         AIF   (T'&OFFSET EQ 'O').SKIPOFF                         ONL02
         AIF   (T'&OFFSET NE 'N').OFFBAD                          ONL02
&OFF     SETA  &OFFSET                                            ONL02
.SKIPOFF ANOP ,                                                   ONL02
         AIF   (&OFF LE 79).OFFOK                                 ONL02
.OFFBAD  ANOP  ,                                                  ONL02
         MNOTE 8,'#H MACRO - INVALID "OFFSET" OPERAND'            ONL02
         MEXIT ,                                                  ONL02
.OFFOK   ANOP  ,                                                  ONL02
         AIF   (K'&TEXT LE 2).BADTEXT                             ONL02
         AIF   ('&TEXT'(1,1) NE '''').BADTEXT                     ONL02
         AIF   ('&TEXT'(K'&TEXT,1) NE '''').BADTEXT               ONL02
&L       SETA  K'&TEXT-2                                          ONL02
&N       SETA  1                                                  ONL02
.CNTLOOP ANOP  ,                                                  ONL02
&N       SETA  &N+1+&P                                            ONL02
         AIF   (&N GE K'&TEXT).GOTLEN                             ONL02
&P       SETB  ('&TEXT'(&N,2) EQ '''' OR '&TEXT'(&N,2) EQ '&&')   ONL02
&L       SETA  &L-&P                                              ONL02
         AGO   .CNTLOOP                                           ONL02
.BADTEXT ANOP  ,                                                  ONL02
         MNOTE 8,'#H MACRO - INVALID "MSG-TEXT" OPERAND'          ONL02
         MEXIT ,                                                  ONL02
.GOTLEN  ANOP  ,                                                  ONL02
&TLEN    SETA  &OFF+&L                                            ONL02
         AIF   (&TLEN LE 80).TLENOK                               ONL02
         MNOTE 8,'#H MACRO - "OFFSET" + "MSG-TEXT" IS TOO LONG'   ONL02
         MEXIT ,                                                  ONL02
.TLENOK  ANOP  ,                                                  ONL02
&MSGCNT  SETA  &MSGCNT+1                                          ONL02
         AIF   ('&TYPE' NE 'NORM').USETYPE                        ONL02
&NAME    DC    AL1(0,&L,&OFF,&USER),CL&L&TEXT                     ONL02
         MEXIT ,                                                  ONL02
.USETYPE ANOP  ,                                                  ONL02
         AIF   ('&TYPE'(1,4) EQ 'QAHX').TYPEWHO                   ONL02
&NAME    DC    AL1(QAHX&TYPE,&L,&OFF,&USER),CL&L&TEXT             ONL02
         MEXIT ,                                                  ONL02
.TYPEWHO ANOP  ,                                                  ONL02
&NAME    DC    AL1(&TYPE,&L,&OFF,&USER),CL&L&TEXT                 ONL02
         MEXIT ,                                                  ONL02
.HTEPAGE ANOP  ,                   END OF PAGE ENTRY              ONL02
         MNOTE *,'    &MSGCNT MESSAGES DEFINED FOR THIS SCREEN'   ONL02
&MSGCNT  SETA  0                                                  ONL02
&NAME    DC    XL3'00FFFF'         END OF PAGE                    ONL02
         MEXIT ,                                                  ONL02
&MSGCNT  SETA  0                                                  ONL02
.HTEEND  ANOP  ,                   END OF TABLE ENTRY             ONL02
         AIF   (&MSGCNT EQ 0).DONE                                ONL02
         MNOTE *,'    &MSGCNT MESSAGES DEFINED FOR THIS SCREEN'   ONL02
&MSGCNT  SETA  0                                                  ONL02
.DONE    ANOP  ,                                                  ONL02
&NAME    DC    XL3'FFFFFF'         END OF HELP DISPLAY            ONL02
         MEND  ,                                                  ONL02
         EJECT ,                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  DO ENTRY POINT HOUSEKEEPING                                    ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
HELP     QSTART ,                  START HELP CSECT ASSEMBLY      ONL02
         SPACE 1                                                  ONL02
         GBLB  &QPFK               PF-KEY OPTION                  ONL02
         GBLB  &LOCK               LOCKHEED OPTIONS                  CL
         GBLB  &QONL               ENABLES ONL SPECIFIC CODE      ONL06
         SPACE 1                                                  ONL02
         USING WORK,R13            NOTE SAVE/WORK ADDRESSABILITY  ONL02
         SPACE 1                                                  ONL02
         L     R10,QVDSPL          ADDR OF DISPLAY WORK AREA      ONL02
         USING QDISPLAY,R10        NOTE DISPLAY ADDRESSABILITY    ONL02
         EJECT ,                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  BUILD HELP DISPLAY FROM HELP TABLE ENTRIES                     ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 3                                                    SBG
******************************************************************  SBG
*  NULLIFY QPDSID FIELD TO SHOW INVALID DSID.                       SBG
         MVC   QPDSID,=X'FFFF'   SET DSID TO BE INVALID             SBG
******************************************************************  SBG
         SPACE 3                                                    SBG
         SPACE 1                                                  ONL02
         MVC   QDHLINE,HEADING     SET HELP DISPLAY HEADING       ONL02
         ZAP   DWORD,QDLNES        GET NUMBER OF AVAILABLE MESSAGEONL02
         CVB   R4,DWORD              LINES PER SCREEN IN BINARY   ONL02
         SLR   R5,R5               ZERO CURRENT LINE NUMBER       ONL02
         LA    R6,HELPTABL         ADDR OF FIRST HELP TABLE ENTRY ONL02
         USING HTEDSECT,R6         NOTE HTE ADDRESSABILITY        ONL02
         SPACE 2                                                  ONL02
*                                                                 ONL02
*  PROCESS NEXT HELP TABLE ENTRY                                  ONL02
*                                                                 ONL02
         SPACE 1                                                  ONL02
NEXTHTE  DS    0H                  PROCESS NEXT HELP TABLE ENTRY  ONL02
         CLC   HTEPAGE,=X'00FFFF'  IS THIS AN END OF PAGE ENTRY?  ONL02
         BE    DONEPAGE            SKIP IF YES                    ONL02
         CLC   HTEEND,=X'FFFFFF'   IS THIS AN END OF TABLE ENTRY? ONL02
         BE    DONETABL            SKIP IF YES                    ONL02
         SPACE 1                                                  ONL02
         ICM   R1,1,HTEFLAGS       GET MSG AUTHORITY REQUIREMENT  ONL02
         BZ    AUTHOK              SKIP IF MSG IS NOT RESTRICTED  ONL02
         EX    R1,AUTHTM           DOES USER HAVE AUTHORITY?      ONL02
         BO    AUTHOK              SKIP IF YES                    ONL02
         SPACE 1                                                  ONL02
*  INSERT CODE HERE IF YOU WISH TO OVERRIDE MESSAGE SUPPRESSION   ONL02
         SPACE 1                                                  ONL02
         B     MSGDONE             SKIP OVER MSG DISPLAY CODE     ONL02
         SPACE 1                                                  ONL02
AUTHTM   TM    QAHELP,*-*          EXECUTED - IS MSG AUTHORIZED?  ONL02
         SPACE 1                                                  ONL02
AUTHOK   DS    0H                  MSG IS AUTHORIZED FOR DISPLAY  ONL02
         SPACE 1                                                  ONL02
*  INSERT CODE HERE IF YOU WISH TO SUPPRESS AN AUTHORIZED MESSAGE ONL02
         SPACE 1                                                  ONL02
         MVC   QDMSG,QBLANK        BLANK OUT MSG WORK AREA        ONL02
         LA    R3,QDMSG            ADDR OF MSG WORK AREA          ONL02
         ST    R3,QDMSGA           SET MSG ADDR FOR DISPLAY RTN   ONL02
         SLR   R1,R1                                              ONL02
         IC    R1,HTEOFF           MSG OFFSET FROM COLUMN 1       ONL02
         LA    R3,0(R1,R3)         ADJUST AREA POINTER PER OFFSET ONL02
         SLR   R2,R2                                              ONL02
         ICM   R2,1,HTETLEN        LENGTH OF MSG TEXT             ONL02
         BZ    MSGLNERR            ERROR IF NO TEXT               ONL02
         LA    R1,0(R2,R1)         LENGTH OF OFFSET + TEXT        ONL02
         CL    R1,=F'80'           IS TOTAL LENGTH OK?            ONL02
         BH    MSGLNERR            ERROR IF TOO LONG              ONL02
         STH   R1,QDMLNG           SET MSG LENGTH FOR DISPLAY RTN ONL02
         BCTR  R2,0                TEXT LENGTH LESS 1 FOR EXECUTE ONL02
         EX    R2,MSGMVC           PLUG TEXT INTO MSG LINE        ONL02
         L     R15,=V(DISPLAY)     ADDR OF DISPLAY ROUTINE        ONL02
         BALR  R14,R15             LINK TO ROUTINE                ONL02
         SPACE 1                                                  ONL02
         LA    R5,1(,R5)           INCREMENT LINE COUNT           ONL02
         CLR   R5,R4               HAVE WE FILLED A SCREEN?       ONL02
         BL    MSGDONE             SKIP IF NO                     ONL02
         SLR   R5,R5               CLEAR LINE COUNT FOR NEW SCREENONL02
         SPACE 1                                                  ONL02
MSGDONE  DS    0H                  FINISHED WITH CURRENT MSG      ONL02
         SLR   R1,R1                                              ONL02
         IC    R1,HTETLEN          LENGTH OF MSG TEXT             ONL02
         LA    R6,HTETEXT(R1)      INDEX TO NEXT HELP TABLE ENTRY ONL02
         B     NEXTHTE             LOOP TO PROCESS NEXT ENTRY     ONL02
         SPACE 2                                                  ONL02
MSGMVC   MVC   0(*-*,R3),HTETEXT   EXECUTED - SET MSG TEXT        ONL02
         SPACE 1                                                  ONL02
MSGLNERR DS    0H                  MSG LENGTH ERROR               ONL02
         QTILT '*** QUEUE LOGIC ERROR - BAD HELP MSG LENGTH ***'  ONL02
         SPACE 2                                                  ONL02
*                                                                 ONL02
*  END OF PAGE ENTRY FOUND, FILL OUT CURRENT HELP SCREEN          ONL02
*                                                                 ONL02
         SPACE 1                                                  ONL02
DONEPAGE DS    0H                  COMPLETE CURRENT SCREEN        ONL02
         LA    R6,L'HTEPAGE(,R6)   POINT TO NEXT TABLE ENTRY      ONL02
         LTR   R5,R5               WAS ANYTHING DISPLAYED?        ONL02
         BNP   NEWPAGE             SKIP IF NO                     ONL02
         SPACE 1                                                  ONL02
         LR    R2,R4               COPY NUMBER OF AVAILABLE LINES ONL02
         SR    R2,R5               COMPUTE NUMBER OF UNUSED LINES ONL02
         BNP   NEWPAGE             SKIP IF SCREEN IS FULL         ONL02
         SPACE 1                                                  ONL02
         LA    R3,QDMSG            ADDR OF MSG WORK AREA          ONL02
         ST    R3,QDMSGA           SET MSG ADDR FOR DISPLAY RTN   ONL02
         MVI   QDMSG,C' '          SET A BLANK MESSAGE            ONL02
         MVC   QDMLNG,=H'1'         WITH A LENGTH OF ONE          ONL02
         SPACE 1                                                  ONL02
BLNKLOOP DS    0H                  FILL OUT CURRENT SCREEN        ONL02
         L     R15,=V(DISPLAY)     ADDR OF DISPLAY ROUTINE        ONL02
         BALR  R14,R15             LINK TO ROUTINE                ONL02
         BCT   R2,BLNKLOOP         LOOP IF MORE LINES TO BLANK    ONL02
         SPACE 1                                                  ONL02
NEWPAGE  DS    0H                  SETUP FOR NEXT SCREEN          ONL02
         SLR   R5,R5               CLEAR LINE COUNT FOR NEXT PAGE ONL02
         B     NEXTHTE             LOOP TO PROCESS NEXT HELP ENTRYONL02
         SPACE 2                                                  ONL02
*                                                                 ONL02
*  END OF HELP TABLE ENTRY FOUND, DONE WITH HELP DISPLAY          ONL02
*                                                                 ONL02
         SPACE 1                                                  ONL02
DONETABL DS    0H                  DONE WITH HELP TABLE           ONL02
         QSTOP ,                   RETURN TO DISPLAY LAST SCREEN  ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  READ-ONLY DATA                                                 ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
HEADING  DC    CL80'THE FOLLOWING SUBCOMMANDS ARE SUPPORTED:'     ONL02
         SPACE 1                                                  ONL02
         LTORG ,                   DEFINE LITERAL ORIGIN          ONL02
         TITLE 'DEFINE HELP SCREENS'                              ONL02
******************************************************************ONL02
*                                                                 ONL02
*  DEFINE HELP SCREENS                                            ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
         PUSH  PRINT               SAVE CURRENT PRINT STATUS      ONL02
         PRINT NOGEN               DON'T PRINT #H MACRO EXPANSIONSONL02
         SPACE 1                                                  ONL02
HELPTABL DS    0C                  START OF HELP TABLE            ONL02
         SPACE 1                                                  ONL02
 #H NORM,4,'END               - EXIT QUEUE COMMAND'
 #H NORM,4,'STAT LEVEL        - DISPLAY ALL JOBS BEGINNING WITH LEVEL'
 #H NORM,4,'DJ   JOBNAME      - DISPLAY JOB BY JOBNAME'
 #H NORM,4,'JCL  JOBNAME      - LIST JCL FOR A JOB'
 #H NORM,4,'JLOG JOBNAME      - LIST JOBLOG FOR A JOB'
 #H NORM,4,'JMSG JOBNAME      - LIST SYSTEM MESSAGES FOR A JOB'
 #H NORM,4,'JHIS JOBNAME      - LIST JOB HISTORY INFORMATION'
 #H NORM,4,'DD   JOBNAME      - LIST SYSIN/SYSOUT DATASETS FOR A JOB'
 #H NORM,4,'PDDB JOBNAME      - LIST PDDB''S FOR A JOB'
 #H NORM,4,'LIST JOBNAME DSID - LIST A SYSIN OR SYSOUT DATASET'
 #H NORM,4,'LIST * +/-N         LIST NEXT OR PREVIOUS N''TH DATASET'
 #H NORM,4,'SAVE DSNAME       - CREATE A COPY OF THE CURRENT DATASET'
 #H NORM,4,'SPIN              - SEND COPY OF CURRENT DATASET TO SYSOUT'
 #H NORM,4,'PRINT ON CLASS DEST  - OPEN SCREEN LOG'
 #H NORM,4,'PRINT             - PRINT CURRENT SCREEN'
 #H NORM,4,'PRINT OFF         - CLOSE SCREEN LOG'
 #H NORM,4,'MODEL #           - SET 3270 MODEL 2, 3, 4, OR 5'
 #H NORM,0,'NOTES:  (1) DEFAULT FOR LEVEL IS LOGON ID'
 #H NORM,8,'(2) JOBNAME CAN BE JOBNAME, NUMBER, OR "*" FOR CURRENT JOB'
 #H NORM,8,'(3) DSID CAN BE DETERMINED BY USING SUBCOMMAND DD OR PDDB'
 #H NORM,8,'(4) DSNAME WILL BE EXPANDED TO USERID.DSNAME.DATA'
         SPACE 1                                                  ONL02
 #H PAGE                           INDICATE END OF PAGE           ONL02
         EJECT ,                                                  ONL02
         AIF  (NOT &QPFK).PFK1
 #H NORM,4,'PFK               - LIST PROGRAM FUNCTION KEYS'
 #H NORM,4,'PFK  NN  TEXT     - REDEFINE PROGRAM FUNCTION KEYS'
.PFK1    ANOP ,
 #H NORM,4,'FIND ''STRING'' COL(SS,EE)  - FIND CHARACTER STRING'
 #H NORM,4,'FALL ''STRING'' COL(SS,EE)  - FIND ALL OCCURRENCES OF'
 #H NORM,4,'                              STRING (COL DEFAULTS TO ALL)'
 #H NORM,4,'COL  #       - REPOSITION HORIZONTALLY TO COLUMN NUMBER'
 #H NORM,4,'@/MD #       - REPOSITION TO SPECIFIC RECORD NUMBER'
 #H NORM,4,'+/D  #       - REPOSITION FORWARD IN DATASET # RECORDS'
 #H NORM,4,'-/UP #       - REPOSITION BACKWARD IN DATASET # RECORDS'
 #H NORM,4,'T/TOP        - REPOSITION TO TOP OF DATASET'
 #H NORM,4,'B/BOTTOM     - REPOSITION TO BOTTOM OF DATASET'
 #H NORM,4,'HF/HB #      - FORWARD/BACKWARD # HALF PAGES'
 #H NORM,4,'PF/PB #      - FORWARD/BACKWARD # PAGES'
 #H NORM,4,' '                                                    ONL13
 #H NORM,0,'NOTES:  (1) STRING SPECIFICATION FOR FIND MAY BE DELIMITED'
         AIF   (&QONL).ONL1                                       ONL12
 #H NORM,8,'    WITH BLANKS, SINGLE QUOTES, OR DOUBLE QUOTES.'    ONL13
         AGO   .ONL2                                              ONL12
.ONL1    ANOP  ,                                                  ONL12
 #H NORM,8,'    WITH BLANKS, SINGLE QUOTES, DOUBLE QUOTES, OR WITH A'
 #H NORM,8,'    SLASH ("/").'                                     ONL12
.ONL2    ANOP  ,                                                  ONL12
         SPACE 1                                                  ONL02
 #H PAGE                           INDICATE END OF PAGE           ONL02
         EJECT ,                                                  ONL02
 #H ACTV,4,'DA           - DISPLAY ALL JOBS IN EXECUTION'
 #H ACTV,4,'DT           - DISPLAY TSO USERS'
 #H ACTV,4,'DS           - DISPLAY SYSTEM TASKS'
 #H ACTV,4,'DC TYPE      - DISPLAY CPU BATCH/STC/TSO  (BLANK,B/J/S/T)'
 #H BLOG,4,'DQ           - DISPLAY INPUT QUEUES'
 #H BLOG,4,'DI CLASS     - DISPLAY ALL JOBS IN INPUT CLASS'
 #H BLOG,4,'AI CLASS     - DISPLAY JOBS AVAILABLE FOR PROCESSING'
 #H BLOG,4,'HI CLASS     - DISPLAY HELD JOBS IN INPUT CLASS'
 #H BLOG,4,'DF           - DISPLAY OUTPUT QUEUES'
 #H BLOG,4,'DO CLASS     - DISPLAY ALL JOBS IN OUTPUT CLASS'
 #H BLOG,4,'WO           - DISPLAY ONLY OUTPUT JOBS WITH WRITER NAME SP+
               ECIFIED'
 #H BLOG,4,'AO CLASS     - DISPLAY AVAILABLE OUTPUT'
 #H BLOG,4,'HO CLASS     - DISPLAY HELD OUTPUT'
 #H BLOG,4,' '
 #H BLOG,8,'DEFAULT FOR CLASS IS ALL CLASSES'
 #H SSSM,4,' '
 #H SSSM,4,'CAN JOBNAME <P>        - CANCEL JOB ("P" OPTION FOR PURGE)'
 #H SSSM,4,'DEL JOBNAME            - DELETE JOB'
 #H SSSM,4,'REQ JOBNAME <NEWCLASS> - RELEASE JOB''S HELD OUTPUT'
         SPACE 1                                                  ONL02
 #H PAGE                           INDICATE END OF PAGE           ONL02
         EJECT ,                                                  ONL02
 #H SLOG,4,'SLOG  ID  <SEQ>   - LIST THE SYSTEM LOG DATASET'
 #H SLOG,4,'FTIME HH.MM.SS    - REPOSITION SYSTEM LOG TO GIVEN TIME'
 #H SLOG,4,' '
 #H SLOG,8,'ID MAY BE A JOB# DETERMINED BY STATUS SYSLOG,'
 #H SLOG,8,'  OR IT MAY SPECIFY THE SYSTEM ID OF THE DESIRED SYSTEM'
 #H SLOG,8,'  (A NULL FIELD WILL LIST THE ACTIVE SYSTEM''S LOG)'
 #H SLOG,8,'DEFAULT FOR SEQ IS 0 (THE CURRENT SYSLOG DATASET)'
 #H SLOG,8,'  (USE A VALUE OF 1, 2, ... TO OBTAIN PREVIOUS DATASETS)'
 #H SLOG,4,' '
 #H EXEC,4,'TSO CMD PRMS      - ISSUE ANY TSO COMMAND W/OPTIONAL PARMS'
 #H SYSP,4,'XB  MTTR          - DISPLAY BLOCK FROM SYS1.HASPACE'
 #H SYSP,4,'                      USE "*" TO RE-SHOW AND "+" TO CHAIN'
 #H SYSP,4,'XD  JOBNAME DSID  - UNRESTRICTED DISPLAY OF DATASETS'
 #H SYSP,4,'XI                - DISPLAY ACTIVE INITIATORS'
 #H SYSP,4,'XJ  JOBNAME       - DISPLAY UNINTERPRETED JQES AND JOES'
 #H SYSP,4,'JQE JOBNAME       - DISPLAY JQE IN HEX/EBCDIC'
 #H SYSP,4,'JCT JOBNAME <OFFSET>  - DISPLAY JCT IN HEX/EBCDIC'
 #H SYSP,4,'IOT JOBNAME       - DISPLAY IOTS IN HEX/EBCDIC'          CL
 #H SYSP,4,'JOE JOBNAME       - DISPLAY JOES IN HEX/EBCDIC'
 #H SYSP,4,'HCT               - DISPLAY HCT $SAVE AREA'
 #H SYSP,4,'TGPS              - LIST HIGH TRACK GROUP USERS'         CL
         SPACE 1                                                  ONL02
 #H PAGE                           INDICATE END OF PAGE           ONL02
         SPACE 1                                                  ONL02
 #H END                            INDICATE END OF TABLE          ONL02
         SPACE 1                                                  ONL02
         POP   PRINT               RESTORE PRINT STATUS           ONL02
         TITLE 'DEFINE CONTROL BLOCK DSECT''S'                    ONL02
******************************************************************ONL02
*                                                                 ONL02
*  HELP TABLE ENTRY DSECT                                         ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
HTEDSECT DSECT ,                   HELP TABLE ENTRY DSECT         ONL02
HTE      DS    0X                  ALTERNATE DSECT NAME           ONL02
HTEFLAGS DS    AL1                 SUB-COMMAND AUTHORITY FLAGS    ONL02
HTETLEN  DS    AL1                 LENGTH OF MESSAGE TEXT         ONL02
HTEOFF   DS    AL1                 SCREEN OFFSET OF MESSAGE TEXT  ONL02
HTEUSER  DS    AL1                 USER FLAG BYTE                 ONL02
HTETEXT  DS    0C                  MESSAGE TEXT                   ONL02
         SPACE 1                                                  ONL02
         ORG   HTE                 REDEFINE DSECT                 ONL02
HTEPAGE  DC    0XL3'00FFFF'        END OF PAGE ENTRY              ONL02
HTEEND   DC    0XL3'FFFFFF'        END OF TABLE ENTRY             ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  SAVE AREA/WORK AREA DSECT                                      ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
WORK     DSECT ,                   SAVE AREA/WORK AREA DSECT      ONL02
         DS    18F                 OS SAVE AREA                   ONL02
DWORD    DS    D                   SCRATCH DOUBLE WORD            ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  GENERATE QCOMMON DSECT                                         ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       ONL02
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL02
         QCOMMON                                                  ONL02
         $HASPEQU
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL02
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    ONL02
         END   ,                                                  ONL02
