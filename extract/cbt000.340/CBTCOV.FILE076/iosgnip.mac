         MACRO
&NAME    IOSGNIP &FUNC,&PARM,&REG,&VAR,&AUTH=CSECTNME
         GBLB    &EXECN                 MULTIPLE EXPANSION SWITCH
         LCLC  &SUFFIX                  CHARACTER VARIABLE
&SUFFIX  SETC  '&SYSNDX'(2,4)           SET 3 CHARACTER SYSNDX VALUE
         AIF   ('&FUNC' EQ 'MINIMAP').MIN010
         AIF   ('&FUNC' EQ 'UCBCHM').NIP010
         AGO   .IOSNOGO                 UNSUPPORTED FUNCTION MNOTE
.NIP010  ANOP
         AIF   ('&AUTH' EQ 'IEAANIP').NIP010A
         AGO   .IOSNOGO                 UNAUTHORIZED USE MNOTE
.NIP010A ANOP
***********************************************************************
***      MACRO DEFINITION - UCBCHM FUNCTION OF IOSGNIP              ***
***            THIS MACRO, OWNED AND MAINTAINED BY IOS PERSONNEL,   ***
***            INSERTS THE PATH MASK CREATED BY MP6500 INTO THE     ***
***            'UCBCHM' FIELD.                                      ***
***                                                                 ***
         AIF   (&EXECN EQ 1).NIP101     SKIP DETAIL 2ND TIME THROUGH
***            FOLLOWING ARE THE PARAMETERS WHICH MUST BE SPECIFIED ***
***                1.&FUNC-UCBCHM                                   ***
***                2.&PARM-NUMBER OF A REGISTER WHICH POINTS TO     ***
***                  THE UCB.                                       ***
***                3.&REG-SPECIFIES THE NUMBER OF A REGISTER WHICH  ***
***                  POINTS TO THE PATH MASK BYTE. IF NOT SPECIFIED ***
***                  THE VAR PARAMETER WILL BE USED TO LOCATE THE   ***
***                  UCB PATH MASK.                                 ***
***                4.&VAR-LABEL OF THE BYTE CONTAINING THE UCB PATH ***
***                  MASK                                           ***
***      THE KEYWORD PARAMETER 'AUTH=CSECTNME' MUST ALSO BE         ***
***      SPECIFIED, NOTING THE CSECT NAME INVOKING THIS MACRO.      ***
.NIP101  ANOP
***********************************************************************
         SPACE 1
         AIF   ('&REG' NE '').NIP110     USING REGISTER FORM...
         SPACE 1
***********************************************************************
***            REGISTER DEFINITION                                  ***
***********************************************************************
IECIU&SUFFIX   EQU &PARM                UCB POINTER REGISTER
         AIF   (&EXECN EQ 1).NIP102     NOT FIRST TIME - SKIP
&EXECN   SETB  1                         SET FIRST TIME SWITCH.
***********************************************************************
***            UCB DEFINITION                                       ***
***********************************************************************
IECIWCHM EQU   12                       DISP TO PATH MSK IN UCB
.NIP102  ANOP
         SPACE 3
&NAME    MVN   IECIWCHM(1,IECIU&SUFFIX),&VAR INSERT MASK INTO UCBCHM.
         AGO   .IOSGEND
.NIP110  ANOP
         SPACE 1
***********************************************************************
***            THIS IS THE REGISTER FORM OF UCBCHM FUNCTION         ***
***********************************************************************
         SPACE 1
***********************************************************************
***            REGISTER DEFINITION                                  ***
***********************************************************************
IECIW&SUFFIX EQU &REG                  PATH MASK POINTER REGISTER
IECIU&SUFFIX EQU &PARM                   UCB POINTER REGISTER
         AIF   (&EXECN EQ 1).NIP103     NOT FIRST TIME - SKIP
&EXECN   SETB  1                        SET FIRST TIME SWITCH
***********************************************************************
***            UCB DEFINITION                                       ***
***********************************************************************
IECIWCHM EQU   12                       DISP TO PATH MSK IN UCB
.NIP103  ANOP
         SPACE 3
&NAME    MVN   IECIWCHM(1,IECIU&SUFFIX),0(IECIW&SUFFIX) PUT MASK IN UCB
         MEXIT                           EXIT MACRO
.MIN010  ANOP
         AIF   ('&AUTH' EQ 'IEAANIP').MIN010A
         AGO   .IOSNOGO                 UNAUTHORIZED USE MNOTE
.MIN010A ANOP
&NAME    B     IECIM010                 SKIP AROUND CONSTANTS
***********************************************************************
*        MACRO DEFINITION - MINIMUM PATH MAP FUNCTION OF IOSGNIP      *
*              THIS MACRO OWNED AND MAINTAINED BY  I O S  PERSONNEL,  *
*              USES A GIVEN UCB TO CONSRUCT AN EIGHT (8) BYTE TABLE   *
*              ENTRY TO CONVEY TO THE CALLER THE POSSIBLE PATHS TO A  *
*              DEVICE.                                                *
*                                                                     *
*              THIS CODE IS GENERATED WHEN 'IOSGNIP' IS INVOKED       *
*              BY NIP WHERE VARIABLES ARE SET ACCORDING TO THE FORMAT *
*              BELOW --                                               *
*        IOSGNIP &FUNC,&PARM,&REG,&VAR            ... WHERE --        *
*             &FUNC = MINIMAP                                         *
*             &PARM = A REGISTER CONTAINING THE ADDRESS OF A 2 WORD   *
*                     AREA IN WHICH THE MINIMAP OF AVAILABLE PATHS    *
*                     WILL BE BUILT.                                  *
*              &REG = A REGISTER CONTAINING THE ADDRESS OF THE UCB FOR*
*                     WHICH PATHS ARE TO BE DETERMINED.               *
*              &VAR = 0 FOR UNIPROCESSING, OR 1 FOR MULTIPROCESSING.  *
*                                                                     *
*        RESTRICTIONS/ASSUMPTIONS --                                  *
*              1)  IOSGNIP(MINIMAP) WILL EXPAND AS IN-LINE CODE.      *
*              2)  REGISTERS 11, 12, AND 13 ARE NOT AVAILABLE.        *
         AIF   (&VAR(1) EQ 0).MIN020   'UNI-PROCESSING'
*                                                                     *
*              FOR A SYSTEM SUPPORTING MULTIPROCESSING, THIS ENTRY    *
*              TAKES THE FOLLOWING FORM --                            *
*                                                                     *
*              *- - IPLED CPU - - - -*- - - - NULL  - - - -*          *
*              *********************************************          *
*              * PRIMARY  * ALTERNATE*          *          *          *
*              *  DEVICE  *  DEVICE  *  00  00  *  00  00  *          *
*              *   ADDRESS*   ADDRESS*          *          *          *
*              *********************************************          *
*              ..2 BYTES...                                           *
*              .................8 BYTES.....................          *
         AGO   .MIN030
.MIN020  ANOP
*                                                                     *
*              FOR A UNIPROCESSING SYSTEM, THIS ENTRY TAKES THE       *
*              FOLLOWING FORM --                                      *
*                                                                     *
*              *********************************************          *
*              * PRIMARY  * ALTERNATE* ALTERNATE* ALTERNATE*          *
*              *  DEVICE  *  ADDRESS *  ADDRESS *  ADDRESS *          *
*              *   ADDRESS*    ONE   *    TWO   *    THREE *          *
*              *********************************************          *
*              ..2 BYTES...                                           *
*              .................8 BYTES.....................          *
.MIN030  ANOP
*                                                                     *
*        THE MACRO WILL PASS TWO RETURN CODES VIA REGISTER 15 --      *
*              0 = ALTERNATE PATHS WERE GENERATED FOR THIS DEVICE.    *
*              4 = NO ALTERNATE PATHS EXIST.                          *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*          R E G I S T E R    D E F I N I T I O N S                   *
***********************************************************************
         SPACE 1
IECIMREG EQU   &REG                     UCB POINTER INPUT
IECIMPRM EQU   &PARM                    NIP WORKAREA POINTER INPUT
*
IECIMW1  EQU   1                        *
IECIMW2  EQU   2                        ** WORK REGISTERS
IECIMW3  EQU   3                        *
IECIMW4  EQU   4                        *
IECIMW15 EQU   15                       *
IECIMUCB EQU   5                        UCB POINTER REGISTER
IECIMNWA EQU   6                        NIP WORKAREA PTR REGISTER
IECIMRC  EQU   15                       RETURN CODE REGISTER 15
         SPACE 2
***********************************************************************
*          U C B    F L A G    D E F I N I T I O N S                  *
***********************************************************************
         SPACE 1
IECIMALC EQU   1                        UCB ALLOCATION CHANNEL MASK
IECIMCHA EQU   4                        UCB CHANNEL ADDRESS FIELD
IECIMUAD EQU   5                        UCB UNIT ADDRESS FIELD
IECIMFLG EQU   6                        UCB ACTIVITY FLAG 1 FIELD
IECIMLCI EQU   10                       UCB LOGICAL CHANNEL INDEX
IECIMAPS EQU   X'01'                    ALTERNATE PATH SWITCH SETTING
         SPACE 2
***********************************************************************
*          M I S C.   M A S K S   A N D   P O I N T E R S             *
***********************************************************************
         SPACE 1
IECIMCVT EQU   16                       POINTER TO CVT
IECIMLCH EQU   140                      POINTER TO IOS LOGICAL CHAN TBL
IECIMTCC EQU   6                        TEST CHANNEL CODE OFFSET
IECIMACU EQU   1                        ALTERNATE CU INDICATOR OFFSET
*                                       IN TEST CHANNEL PREFIX TABLE...
IECIMPRP EQU   X'08'                    MASK FOR TCH PREFIX PRIMARY PTH
IECIMAP1 EQU   X'04'                    PATH INDICATION FOR - ALTER 1
IECIMAP2 EQU   X'02'                                          ALTER 2
IECIMAP3 EQU   X'01'                                          ALTER 3
IECIMNUL EQU   X'00'                    NULL MASK
IECIMRC4 EQU   4                        RETURN CODE FOR NO ALTERNATES
IECIMNXT EQU   2                        INCREMENT FOR TABLES
IECIMMB1 EQU   0                        OFFSET FOR --  FIRST
IECIMMB2 EQU   2                        *              SECOND
IECIMMB3 EQU   4                        *              THIRD
IECIMMB4 EQU   6                        *              FOURTH MAP BOXES
IECIMBSY EQU   X'80'                    UCB DEVICE BUSY FLAG
IECIMPST EQU   X'20'                    UCB POST FLAG
IECIMPIM EQU   X'F0'                    ALTERNATE CU PATH MASK
IECIMPIN EQU   1                        ALTERNATE PATH DISCRIPTOR DISP.
IECIMBTM EQU   X'0F'                    BYTE ANDING MASK
         AIF   (&VAR(1) EQ 0).MIN031     'UNI-PROCESSING'
IECIMSIX DC    H'6'                     OFFSET FROM TCHTAB TO TCHCODE
.MIN031  ANOP
IECIMHWD DS    H                        HALFWORD WORKAREA
IECIMBYT EQU   IECIMHWD                 ONE BYTE WORKAREA
IECIMSA1 DS    F                        FIRST WORD OF SAVEAREA FOR &REG
IECIMSA2 DS    F                        SECOND WORD FOR &PARM...
IECIMHWM DC    X'0000FFFF'              MASK TO ZERO TOP HALF WORD
         AIF   (&VAR(1) EQ 1).MIN040    'MULTI-PROCESSING'
IECIMABL DC    X'0000000F'              MASK TO ZERO ALL BUT LOW 4 BITS
         SPACE 1
*                                       ALTERNATE MVC INSTRUCTIONS TO
IECIMVC  DS    0H   ×                   MOVE ENTRY TO MINIMAP FOR
         MVC   IECIMMB1(2,IECIMNWA),0(IECIMW2) PRIMARY ADDRESS
         MVC   IECIMMB2(2,IECIMNWA),0(IECIMW2) 1ST ALT ADDRESS
         MVC   IECIMMB3(2,IECIMNWA),0(IECIMW2) 2ND ALT ADDRESS
         MVC   IECIMMB4(2,IECIMNWA),0(IECIMW2) 3RD ALT ADDRESS
         SPACE 1
*                                       ALTERNATE INSTRUCTION OFFSETS -
IECIMTAB DC    X'00'                    SLACK
         DC    X'12'                    OFFSET FOR THIRD ALTERNATE
         DC    X'0C'                    OFFSET FOR SECOND ALTERNATE
         DC    X'00'                    SLACK
         DC    X'06'                    OFFSET FOR FIRST ALTERNATE
         DC    X'000000'                SLACK
         DC    X'00'                    OFFSET FOR PRIMARY MOVE INSTR.
.MIN040  DS    0H                       ALIGN AFTER CONSTANTS....
         SPACE 5
IECIM010 ST    IECIMREG,IECIMSA1        SAVE UCB INPUT ADDRESS
         ST    IECIMPRM,IECIMSA2        STORE NIP WORKAREA INPUT ADDR.
         L     IECIMUCB,IECIMSA1        LOAD INTO WORKING REGISTER
         L     IECIMNWA,IECIMSA2        LOAD INTO ACTIVE WORK REGISTER.
         XC    0(8,IECIMNWA),0(IECIMNWA) ZERO MAP ENTRY...
         TM    IECIMALC(IECIMUCB),IECIMAPS Q.  IS THERE ALTERNATE PATH.
         BZ    IECIM020                 A.  NO, GO BUILD SINGLE ENTRY.
*
         L     IECIMW1,IECIMCVT         A.  YES, GET CVT ADDRESS...
         L     IECIMW1,IECIMLCH(IECIMW1) GET POINTER TO LOGICAL CHAN TB
         SR    IECIMW2,IECIMW2          CLEAR WORKREG -
         IC    IECIMW2,IECIMLCI(IECIMUCB) GET LOGICAL CHANNEL INDEX...
         SLL   IECIMW2,3                IECIMW2 = IECIMW2 * 8
         LH    IECIMW1,IECIMTCC(IECIMW1,IECIMW2) ADDR OF TEST CHAN CODE
         N     IECIMW1,IECIMHWM         CLEAR HIGH ORDER HALFWORD...
         AIF   (&VAR(1) EQ 0).MIN050     'UNI-PROCESSING'
         SH    IECIMW1,IECIMSIX         BACK-UP TO PREFIX TABLE START.
*    IECIMW1 POINTS TO TOP OF TEST CHANNEL PREFIX TABLE.
         TM    IECIMACU(IECIMW1),IECIMPRP  Q.  IS TOP PRIMARY PATH.
         BO    IECIM015  ×              A.  YES, TAKE NORMAL SEQUENCE.
*                                       A.  NO, REVERSED.
         MVC   IECIMMB2(2,IECIMNWA),0(IECIMW1) GET ALT. ADDR FROM 1ST.
         MVC   IECIMMB1(2,IECIMNWA),IECIMNXT(IECIMW1) PRIMARY FROM 2ND.
         B     IECIM031                 SKIP TO ALT CU CHECK....
************************
         SPACE 1
IECIM015 MVC   IECIMMB1(2,IECIMNWA),0(IECIMW1) MOVE PRIME DEV ADDR/CPUA
         MVC   IECIMMB2(2,IECIMNWA),IECIMNXT(IECIMW1) ALT DEV ADDR/CPUA
*                                       1ST STAGE MINIMAP BUILT...
         SPACE 1
         AGO   .MIN060                 SKIP UNI-PROC CODE
.MIN050  BCTR  IECIMW1,0                * BACKUP ON HALF WORD TO
         BCTR  IECIMW1,0                * POINT TO LCHTAB SUMMARY ENTRY
         SR    IECIMW2,IECIMW2          CLEAR WORKREG, THEN
         IC    IECIMW2,0(IECIMW1)       PICK UP NUMBER OF PATHS...
         LR    IECIMW3,IECIMW2          COPY FOR LOOP ITERATION LIMIT
         STH   IECIMW3,IECIMHWD         AND AND SAVE...
         AR    IECIMW2,IECIMW2          DOUBLE TO GET TABLE SPAN
         LCR   IECIMW2,IECIMW2          NEGATE VALUE...
         AR    IECIMW2,IECIMW1          ADD TO GET POINTER TO TOP.
*   POINTERS NOW SET AT LIMITS OF TEST CHANNEL PREFIX TABLE _
         SPACE 1
IECIM030 IC    IECIMW15,IECIMACU(IECIMW2) PICK UP PATH INDICATOR
         N     IECIMW15,IECIMABL        CLEAR ALL BUT LOW FOUR BITS
         IC    IECIMW15,IECIMTAB(IECIMW15) PICK UP MVC INSTR. OFFSET
         EX    0,IECIMVC(IECIMW15)      EXECUTE SAME....
         SPACE 1
         LA    IECIMW2,IECIMNXT(IECIMW2) POINT TO NEXT ELEMENT...
         BCT   IECIMW3,IECIM030         LOOP THROUGH TCH PREFIX TABLE
*                                       1ST STAGE MINIMAP BUILT
         LH    IECIMW3,IECIMHWD         PICK UP NUMBER OF PATHS....
.MIN060  SPACE 1
IECIM031 LR    IECIMW4,IECIMNWA         COPY NIP WA ADDRESS
         TM    IECIMACU(IECIMW4),IECIMPIM Q.  IS ALTERNATE PATH C.U.
         BZ    IECIM040                    A.  NO, GO FOR ALTERNATE CHN
*
         AIF   (&VAR(1) EQ 0).MIN065    'UNI-PROCESSING'
         MVN   IECIMMB1+1(1,IECIMNWA),IECIMUAD(IECIMUCB) NORMALIZE MAP
         MVN   IECIMMB2+1(1,IECIMNWA),IECIMUAD(IECIMUCB) ADDRESSES...
         AGO   .MIN070
.MIN065  ANOP
IECIM035 MVN   IECIMMB1+1(1,IECIMW4),IECIMUAD(IECIMUCB) GET UNIT ADDR.
         LA    IECIMW4,IECIMNXT(IECIMW4) BUMP TO NEXT ENTRY...
         BCT   IECIMW3,IECIM035         LOOP UNTIL ADDRESSES NORMALIZED
*
.MIN070  B     IECIM050                 SKIP ALT. CHANNEL CODE...
*************************
         SPACE 1
         AIF   (&VAR(1) EQ 0).MIN075    'UNI-PROCESSING'
IECIM040 MVC   IECIMMB1+1(1,IECIMNWA),IECIMUAD(IECIMUCB) NORMALIZE MAP
         MVC   IECIMMB2+1(1,IECIMNWA),IECIMUAD(IECIMUCB) ADDRESSES....
         AGO   .MIN080
.MIN075  ANOP
IECIM040 MVC   IECIMMB1+1(1,IECIMW4),IECIMUAD(IECIMUCB) GET UNIT ADDR.
         LA    IECIMW4,IECIMNXT(IECIMW4) BUMP TO NEXT ENTRY...
         BCT   IECIMW3,IECIM040         LOOP UNTIL ADDRESSES NORMALIZED
*
.MIN080  ANOP
IECIM050 SR    IECIMRC,IECIMRC          SET RETURN CODE TO ZERO...
         B     IECIMMXT                 EXIT MACRO CODE....
*************************
         SPACE 1
IECIM020 MVC   IECIMMB1(2,IECIMNWA),IECIMCHA(IECIMUCB) P/U DEVICE ADDR.
         NI    IECIMMB1(IECIMNWA),IECIMBTM  ZERO TOP HALFBYTE OF ADDR.
*                                       ALTERNATE PATH ENTRIES REMAIN
*                                       ZERO TO REFLECT LACK OF SAME.
         LA    IECIMRC,IECIMRC4         SET RETURN TO 4 FOR
*                                       NO ALTERNATE PATH INDICATION.
IECIMMXT DS    0H                       EXIT THIS ROUTINE
         MEXIT                           EXIT MACRO
.*
.**********************************************************************
.*
.*       ALL IOSGNIP MNOTES ARE TO BE PLACED BELOW...
.*
.*   1.  .IOSNG1 - AUTHORIZATION VIOLATION
.*                                       USED BY ALL FUNCTIONS
.*
.**********************************************************************
.*
.IOSNOGO ANOP
.IOSNG1  MNOTE 8,'*** ''&AUTH'' MAY NOT INVOKE ''&FUNC'' FUNCTION.'
         MEXIT
.*
.IOSGEND       ANOP
         MEND
