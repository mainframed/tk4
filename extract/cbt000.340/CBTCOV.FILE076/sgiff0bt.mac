         MACRO
&TBLNAME SGIFF0BT &BFRSIZE,&EXPSZE,&NUMDEV,&NUMSEC1,&NUMSEC2,          X
               &NUMSEC3,&NUMSEC4
         LCLA  &TBLNGTH,&TOTAVAL,&NETZONE,&DVCOUNT,&EXPBFR,&TABDISP
         LCLA  &ZONESZ,&TOTGUAR,&NUMSEC(4),&X1,&SAVE
         LCLA  &ZSTBL(4),&GUARTBL(4),&EXTRA,&CTR
.*  MACRO REWRITTEN FOR APAR 26262 FOR RELEASE 19
&TBLNGTH SETA  &BFRSIZE/256        SET TABLE LENGTH
&TOTAVAL SETA  &TBLNGTH            SET TOTAL NO OF BUF SECTIONS AVAIL.
         AIF   (T'&EXPSZE EQ 'O').BT00       CHECK FOR 2250-1
&NETZONE SETA  &TOTAVAL/&NUMDEV    SET INITIAL ZONE SIZE
&DVCOUNT SETA  &NUMDEV             SET NUMBER OF DEVICES
&EXPBFR  SETA  &EXPSZE             SET AMOUNT OF BUFF FOR EXPRESS
         AGO   .BT00G              BRANCH TO GENERATE 1ST 2 WORDS
.BT00    ANOP
.*    SET FIELDS FOR 2250-1
&DVCOUNT SETA  1                   SET NO OF DEV EQ TO 1
&EXPBFR  SETA  &BFRSIZE            SET AMT OF BUFF FOR EXPRESS
&TABDISP SETA  16                  SET DEVICE TABLE DISPLACEMENT
&ZONESZ  SETA  &TOTAVAL            SET ZONE SIZE EQ FULL BUFFER
&TOTGUAR SETA  &TOTAVAL            SET TOTAL GUAR EQ FULL BUFFER
.*    GENERATE 1ST PART OF TABLE
.BT00G   ANOP
         ENTRY &TBLNAME
&TBLNAME DC    H'&TBLNGTH'         BUFFER TABLE LENGTH IN SECTIONS
         DC    H'&DVCOUNT'         NUMBER OF DEVICES
         DC    H'&EXPBFR'          AMOUNT OF BUFFER FOR EXPRESS
         DC    H'&TOTAVAL'         TOTAL AVAIL BUFFER IN SECTIONS
         AIF   (T'&EXPSZE EQ 'O').BTV22     IF 2250-1 GEN WHOLE TABLE
&NUMSEC(1) SETA &NUMSEC1           INITIALIZE THE
&NUMSEC(2) SETA &NUMSEC2                SUBSCRIPTED VARIABLES FOR
&NUMSEC(3) SETA &NUMSEC3                     THE NUMBER OF GUAR SECTS
&NUMSEC(4) SETA &NUMSEC4                          FOR EACH 2250
.BTP     ANOP
.* FIND THE LARGEST NUMBER OF GUARANTEED SECTIONS NOT ASSIGNED YET
&X1      SETA  1                   INITIALIZE COUNTERS
&SAVE    SETA  1                   THIS WILL BE THE INDEX OF LARGEST
.BTR     AIF   (&ZSTBL(&SAVE) EQ 0).BTU  START INDICES WITH VALUES
&SAVE    SETA  &SAVE+1                            THAT HAVE NOT
&X1      SETA  &X1+1                              BEEN ASSIGNED
         AGO   .BTR                               YES.
.BTU     AIF   (&X1 GT &NUMDEV).BTRR  ALL ZONES HAVE BEEN ASSIGNED
.BTQ     AIF   (&X1 EQ &NUMDEV).BTS1  FOUND LARGEST OF REMAINING GUAR.
&X1      SETA  &X1+1               INCREMENT X1
         AIF   (&NUMSEC(&X1) GT &NUMSEC(&SAVE)).BTT
         AGO   .BTQ
.BTT     AIF   (&ZSTBL(&X1) NE 0).BTQ
&SAVE    SETA  &X1            &SAVE WILL CONTAIN THE INDEX OF THE
.*                                 LARGEST REMAINING NUMSEC REQUEST
         AGO   .BTQ
.BTS1    AIF   (&NUMSEC(&SAVE) GT &NETZONE).BTS2 GUAR LARGER THAN ZONE
.* ALL REMAINING ZONE SIZES ARE SET EQUAL TO NETZONE
.* ALL REMAINING GUARANTEED SECTIONS ARE ASSIGNED
&X1      SETA  0                        INITIALIZE INDEX
.BTS11   ANOP
&X1      SETA  &X1+1                    INCREMENT INDEX
         AIF   (&X1 GT &NUMDEV).BTV1    ALL DEVICES HAVE ZONES ASSIGNED
         AIF   (&ZSTBL(&X1) NE 0).BTS11 BRANCH IF ZONE HAS BEEN ASSIGND
&ZSTBL(&X1) SETA &NETZONE               SET ZONE SIZE TO NETZONE
&GUARTBL(&X1) SETA &NUMSEC(&X1)         SET NO. OF GUAR. SEC. TO NUMSEC
         AGO   .BTS11                   PROCESS NEXT DEVICE
.BTS2    ANOP
.*  NUMBER OF GUARANTEED SECTIONS REQUESTED IS GREATER THAN NETZONE
&CTR     SETA  &CTR+1         KEEPS TRACK OF NO. OF DEVICES PROCESSED
         AIF   (&CTR EQ &NUMDEV).BTERR  BRANCH SHOULD NEVER BE TAKEN
&ZSTBL(&SAVE) SETA &NUMSEC(&SAVE)   SET ZONE SIZE TO NO. OF GUAR. SEC.
&GUARTBL(&SAVE) SETA &NUMSEC(&SAVE) SET NO. OF GUARANTEED SECTIONS
&NETZONE SETA  (&TOTAVAL-&ZSTBL(1)-&ZSTBL(2)-&ZSTBL(3)-&ZSTBL(4))/(&NUM*
               DEV-&CTR)            RECALCULATE NETZONE
         AIF   (&NETZONE LE 0).BTV2 BRANCH TAKEN ONLY IF PARAMETERS
.*                                      TO SGIFF0BT ARE INCORRECT
         AGO   .BTP      FIND THE LARGEST REMAINING NUMSEC REQUEST
.BTERR   ANOP
.*  THIS WILL ONLY BE EXECUTED IF BAD PARAMETERS ARE PASSED REQUESTING
.*       MORE BUFFER THAN IS AVAILABLE.
&ZSTBL(&SAVE) SETA &NETZONE        ASSIGN ALL REMAINING BUFFER
&GUARTBL(&SAVE) SETA &NETZONE           SECTIONS TO THIS DEVICE
         AGO   .BTV2               GENERATE THE REST OF THE TABLE
.BTV1    ANOP
&EXTRA   SETA  &TOTAVAL-&ZSTBL(1)-&ZSTBL(2)-&ZSTBL(3)-&ZSTBL(4)
         AIF   (&EXTRA LE 0).BTV2       BRANCH IF NO EXTRA SECTIONS
&ZSTBL(&NUMDEV) SETA &ZSTBL(&NUMDEV)+&EXTRA ASSIGN EXTRA TO LAST DEVICE
.BTV2    ANOP
.*  GENERATE THE REST OF THE TABLE
&X1      SETA  1                        INITIALIZE INDEX
&TABDISP SETA  40                       SET INITIAL TABLE DISPLACEMENT
.BTV21   ANOP
&ZONESZ  SETA  &ZSTBL(&X1)              SET ZONE SIZE IN CONSTANT
&TOTGUAR SETA  &GUARTBL(&X1)            SET GUARANTEED SEC. IN CONSTANT
.BTV22   ANOP
         DC    H'0'                     TOTAL SECTIONS ASSIGNED
         DC    H'&TABDISP'              DEVICE TABLE DISPLACEMENT
         DC    H'&ZONESZ'               DEVICE ZONE SIZE
         DC    H'&TOTGUAR'              NUMBER OF GUARANTEED SECTIONS
         AIF   (T'&EXPSZE EQ 'O').BT10      CHECK FOR 2250-1
         AIF   (&X1 EQ 4).BT10          CHECK FOR LAST TABLE ENTRY
&TABDISP SETA  &TABDISP+&ZSTBL(&X1)     INCREMENT TABLE DISPLACEMENT
&X1      SETA  &X1+1                    INCREMENT INDEX
         AIF   (&X1 LE &NUMDEV).BTV21   CONTINUE GENERATING TABLE
&TABDISP SETA  0              SET DISPL TO 0 FOR UNUSED PORTION OF TABL
         AGO   .BTV21         CONTINUE GENERATING TABLE
.BT10    AIF   (&BFRSIZE EQ 0).BT11     CHECK FOR UNBUFFERED MOD 1
         DC    XL&TBLNGTH'00'           BUFFER SECTIONS TABLE
.BT11    ANOP
         MEND
