          MACRO
         IECINT &TAPE,&SMF,&P3211,&TP,&APR,&VP,&SKORD,&RFIFO,&RPR,     *
               &TWOSW,&VS,&PROTECT,&TRACE,&SSDA,&TCBTYP,&SSTA,&GR,     *
               &NOP1,&RWTAU,&RESJQ,&NOP2,&ASR,&M65MP,&TBASE,&ANR,      X
               &DAVV,&DDR,&M7CH,&ASPN,&NOP4,&OLTEP,&TSO,&CEXT,&RMS,    X
               &TCAM,&MFTATT,&NOP3,&ALTSHR
         GBLB  &D2311,&D2321,&D2301,&D2303,&D2305,&DML            20201
         GBLB  &R2311,&R2321,&R2301,&R2303,&R2305,&RML            20201
         GBLB  &IECITB                                            20201
         LCLB  &RESREL,&RPS,&TBYT                                 20201
         LCLC  &LABEL                                             20201
&RESREL  SETB  (&R2321 OR &R2311)                                 20201
&RPS     SETB  (&D2305 OR &DML)                                   20201
&TBYT    SETB  (&IECITB)        SET IF 2 BYTE CU LOOKUP TABLE     20201
         SPACE 2                                                   RL19
*        THE INPUT OUTPUT INTERRUPT SUPERVISOR                        *
         SPACE 2
*              THE IOS INTERRUPT SUPERVISOR IS THAT PART OF THE
*              INPUT OUTPUT SUPERVISOR WHICH HANDLES THE INPUT OUTPUT
*              INTERRUPTS.
*              THE FOLLOWING IS A GENERAL OUTLINE OF THE OPERATION
*              OF THIS PROGRAM.
         SPACE 1
*                   1. CHECKS FOR CATASTROPHIC ERROR.MAKES INSPECTION
*                      OF STATUS AND SENSE CONDITIONS.
*                   2. LINKS TO ABNORMAL END, CHANNEL END, AND PROGRAM
*                      CONTROLLED INTERRUPT APPENDAGES.
*                   3. INVESTIGATES NON-CHANNEL END CONDITIONS.
*                   4. TESTS FOR ABEOT CONDITIONS
*                   5. ATTEMPTS TO RESTART THE CHANNEL WITH A REQUEST
*                      FROM THE LOGICAL CHANNEL QUEUES.  RETURNS TO
*                      SUPERVISOR.
*                   6. LINKS TO ATTENTION ROUTINE.
*                   7. DETERMINES ERRORS AND BRANCHES OR LINKS TO
*                      PROPER HANDLING ROUTINE.
         SPACE 3
IECINT   DS    0H                                                  TBSE
         AIF   (&TBASE EQ 0).TBS187                                TBSE
         BALR  BASREG,0                                            TBSE
IECINTA  LA    BASREG2,IECINTA-IOSBAS                              TBSE
         SR    BASREG,BASREG2      SET LOW BASE REGISTER           TBSE
         LA    BASREG2,4094(BASREG)     SET UP HIGH BASE REG       RL19
         LA    WKREG1,INT027            SET MAIN RETURN REG        IMP4
         ST    WKREG1,LNKRG1S      SET MAIN RETURN REGISTER        TBSE
         AGO   .TBS190
.TBS187  LA    LNKRG1,INT027            SET MAIN RETURN REG        IMP4
.TBS190  NI    XCPBTE,FF-XCPCNT-XCPERR-XCPST-XCPRECUR-XCPVALID   A24739
*                                       RESET ALL NON-RMS FLAGS  A24739
         L     APBSRG,IECPL2            GET TEMP BASE REG      @SA77644
         SR    ICREG,ICREG              CLEAR INSERT CHARACTER REG
         STH   ICREG,LTSVALSV           NO VALID RQE             A24739
         LH    UAREG,IOPSWO+2           PICK UP UNIT ADDRESS     A24739
         LM    WKREG1,WKREG2,CSWLOC     GET CURRENT CSW           19022
         N     WKREG2,CUBMSK            REMOVE CUB BITS           19022
         ALR   WKREG1,WKREG2            ALL OTHER STATUS = ZERO   19022
         BC    8,GTF01                  ZERO STATUS, TRACE IT     21014
         SPACE 1                                                  21014
*   THE NEXT PROCEDURE ATTEMPTS TO ELIMINATE THE UCB LOOK-UP PROCEDURE
*   IF THE CORRECT UCB ADDRESS HAS BEEN SAVED IN THE CHANNEL SAVE
*   AREA, NO LOOK UP IS NECESSARY.
         SPACE 1
         IC    ICREG,IOPSWO+2           PICK UP CHANNEL ADDRESS
         AR    ICREG,ICREG              DOUBLE CHAN ADDR TO GET INDEX
         LH    UCBREG,UCBSAVE(ICREG)    PICK UP LAST ACTIVE UCB ADDR
         CH    UAREG,UCBCHA(0,UCBREG)   IS THIS THE LAST UCB STARTED
         BC    8,INT01A                YES- GO LOAD REGS           IMP2
         SPACE 1                                                   IMP2
*   GET UCB ADDRESS VIA LOOK-UP METHOD
         LR    WKREG1,UAREG        COPY UNIT ADDRESS               IMP2
         SRDL  UAREG,8                  ISOLATE CHAN ADDR IN UAREG
         SRL   UCBREG,28                ISOLATE CONTROL UNIT ADDR
         IC    UAREG,UCBTAB(UAREG)      FIND CHANNEL CONSTANT
         AIF   (&M7CH EQ 0).M7CH01                                 I267
         AR    UAREG,UAREG              DOUBLE CHAN CONS TO INDEX  I267
.M7CH01  AR    UAREG,UCBREG             CHAN INDEX + CU ADDRESS    I267
         AIF   (&TBYT EQ 0).TBYT01                                 IMP4
         AR    UAREG,UCBREG             CHAN INDX + 2 X CU ADR     IMP4
         LH    UAREG,UCBTAB(UAREG)      GET CU INDEX               IMP4
         AGO .TBYT03                                               IMP4
.TBYT01  IC    UAREG,UCBTAB(UAREG)      GET CU INDEX               IMP4
.TBYT03  N     WKREG1,DEVMSK            ISOLATE DEVICE             IMP4
         AR    UAREG,UAREG              DOUBLE C U CONSTANT
         AR    WKREG1,WKREG1            DOUBLE DEVICE ADDRESS
         AR    WKREG1,UAREG             CU CONST + 2X DEV ADDR
         CH    WKREG1,MAXUCBO           COMPARE WITH HIGHEST OFFSET
         LH    UAREG,IOPSWO+2           RE-INITIALIZE INT. ADD.   M0154
         BC    8+2,SERR-IECCPL02(APBSRG) CHECK FOR CHAN ERRORS @SA77644
         L     UCBREG,LISTA             FIND UCB LIST           SA71362
         LH    UCBREG,0(WKREG1,UCBREG)  FIND UCB POINTER        SA71362
         LTR   UCBREG,UCBREG            CHECK FOR PAD ENTRY
         BC    8,SERR-IECCPL02(APBSRG)  CHECK FOR CHAN ERRORS  @SA77644
          SPACE 1                                                  CCHB
*    INTERFACE TO SERR OR CCH IF CHANNEL ERROR                     CCHB
INT01A   TM    CSWLOC+5,CSWPCI          TEST FOR PCI INTERRUPT    21014
         BC    8,INT01B                 NO, TRACE THE INTERRUPT   21014
         TM    CSWLOC+5,CSWCTK+CSWICK+CATCCH CAT.ERRORS           21014
         BC    8,INTCCH                 PCI WITHOUT CAT. ERROR    21014
INT01B   HOOK  EID=IHLMIO2              TRACE ALL EXCEPT PCI      21014
         TM    CSWLOC+5,CSWCTK+CSWICK+CATCCH CAT.ERRORS           21014
         BC    7,SERR02-IECCPL02(APBSRG) YES, GO HANDLE THEM   @SA77644
INTCCH   DS    0H                                                  CCHB
         CLC   UCBTYP+2(2,UCBREG),IT7330 * IS DEVICE 7330?        ITH01
         BNE   ITIOSINT *               NO, SKIP CMD RTRY ROUTINE ITH01
         L     APBSRG,ITBLKMPX *        GET ADDR OF CMD RTRY MOD  ITH01
         BAL   LNKRG2,4(,APBSRG) *      CALL INTERRUPT ROUTINE    ITH01
ITIOSINT DS    0H *                     CONNECT                   ITH01
*   GET USER'S CONTROL BLOCKS IF RQE IS VALID.
         TM    UCBFL1(UCBREG),UCBPST    IS THERE AN RQE            CCHB
         BC    8,INT009            NO-BY PASS SET-UP               IMP1
         AIF   (&TWOSW EQ 0).SXIOSF                             SA52441
*   FLUSH INTERRUPTS COMING FROM OTHER THAN LAST                SA52441
*   PATH STARTED WHILE DEVICE IS BUSY (UCBPST ON).              SA52441
         LH    WKREG1,UCBCHA(UCBREG)    GET DEVICE ADDRESS      SA52441
         N     WKREG1,CHCUMASK          LAST STARTED CH/CU ADDR SA52441
         XR    WKREG1,UAREG             ZERO CH/CU ADDR IF EQUALSA52441
         N     WKREG1,CHCUMASK          INT. PATH EQUAL LAST PATSA52441
         BC    7,INT027                 NO-TRY TO RESTART CHANNESA52441
.SXIOSF  AIF   (&M65MP EQ 0).MPB060                             SA52441
         LR    WKREG1,UCBREG            TRANSFER UCB POINTER    SA52441
         S     WKREG1,NEGDISP    GET NEGATIVE DISPLACEMENT IN UCSA52441
         IC    ICREG,UCBFL3(WKREG1) GET ID OF CPU THAT STARTED   SM5272
         N     ICREG,CPUIDMSK    MASK OUT ALL BITS EXCEPT IOCPUISA52441
         C     ICREG,IOCPUID           THIS CPU LAST STARTED    SA52441
         BC    7,INT027                 NO-TRY TO RESTART CHANNESA52441
.MPB060  LH    TSTREG,UCBLTS(UCBREG)    GET RQE POINTER FROM UCBSA52441
         STH   TSTREG,LTSVALSV          VALID RQE                A24739
         LM    IOBREG,DEBREG,TSTIOB-1(TSTREG) GET IOB + DEB ADDR A24739
         L     DCBREG,DEBDCB-1(DEBREG)    LOAD DCB ADDRESS
         AIF   (&PROTECT EQ 0).NVALCHK                          SA68418
         SPACE 1
*    VALIDITY CHECK USER'S CONTROL BLOCKS IF NON SUPERVISOR
         SPACE 1
         AIF   (&TCBTYP LT 2).PCP010    NOT MVT, SKIP            A24739
         TM    TSTKEY(TSTREG),X'F0'     CHECK FOR REQUESTOR KEY OF 0
         AGO   .PCP020                                          SA68418
.PCP010  ANOP                                                   SA68418
         TM    DEBPRT(DEBREG),PPKEY     CHECK FOR KEY 0 REQUEST SA68418
.PCP020  ANOP                                                   SA68418
         BC    8,INT002A                YES-SKIP VALCK
         OI    XCPBTE,XCPVALID          SET VDTY CHK FLAG        A24739
         BAL   LNKRG2,VALCHK            NO-GO DO VALIDITY CHECK    VDTY
         NI    XCPBTE,FF-XCPVALID       RESET VDTY CHK FLAG      A24739
.NVALCHK ANOP                                                   SA68418
INT002A  TM    UCBCHA(UCBREG),UCBSMD   WAITING FOR SENSE OPERAT    IMP2
         BC    1,INT006                YES- RE-ENTER SENSE ROUTN   IMP2
         SPACE 1
*   PREPARE LINKAGE TO PCI APPENDAGE IF REQUESTED BY USER.
INT003   TM    CSWLOC+5,CSWPCI         TEST FOR PCI INTERRUPT      IMP2
         BC    8,INT008                 NO-BYPASS PCI APPENDAGE SA52441
         L     APBSRG,DEBAPG-1(DEBREG)  GET APPD TABLE ADDR.
         L     APBSRG,APGPCI(APBSRG)    GET PCI APG ADDR.
         BALR  LNKRG2,APBSRG            TO APPENDATS
         SPACE 1
*    DEFINITION OF PCI APPENDAGE - IOS INTERFACE                 BBBBBB
*    INPUT     TSTREG,IOBREG,DEBREG,DCBREG,UCBREG,               BBBBBB
*              LNKRG2(RET ADDR),APBSRG(BASE ADDR)                BBBBBB
*    REGS CHANGED  WKREG1,WKREG2,WKREG3,LCHREG                   BBBBBB
         SPACE 1
*   RETURN FROM PCI APPENDAGE.
         HOOK  EID=IHLMPCI              TRACE FOR PCI INTERRUPTS  21014
         AIF   (&SMF EQ 0).SMF110                                 TEST2
         AIF   (NOT &D2305).ZU020
         SPACE 1
*              GO GET ADDR OF BASE EXPOSURE UCB IN CASE 2305      20201
         L     WKREG2,AZUSRTN           GET SUBRTN ADDR           20201
         BALR  LNKRG3,WKREG2            GO GET BASE ADDR IF 2305  20201
.ZU020   AIF   (&TCBTYP GT 1).SMF090                              20201
         BAL   LNKRG2,SMF010           GO SET UP TCB ADR           SMF2
         AGO   .SMF095                                             SMF2
.SMF090  L     REG0,TSTTCB(0,TSTREG)    SET UP TCB ADDRESS         SMF2
.SMF095  L     APBSRG,SMFRTN            GET ADDRESS OF SMF RTN     SMF2
         BALR  LNKRG2,APBSRG .          REPORT PCI TO SMF        A31489
         AIF   (NOT &D2305).SMF110                                20201
         LH    UCBREG,TSTUCB(0,TSTREG)  RE-INIT UCB POINTER       20201
.SMF110  LH    WKREG1,CSWLOC+4         GET STATUS                  SMF2
         N     WKREG1,PCIMSK           ANY STATUS EXCEPT PCI       IMP2
         BC    7,INT008                YES, CONTINUE
         SR    TSTREG,TSTREG            CLEAR RQE ADDR REG      SA71481
         STH   TSTREG,LTSVALSV          NO VALID RQE            SA71481
         B     INT027                  GO TO CHANNEL RESTART  M2563
         SPACE 1
*   ENTRY POINT FOR SIO POST INSPECTION
INT008   NI    UCBFL5(UCBREG),FF-UCBTICBT SHOW INTER. RECEIVED   A42261
         MVC   IOBCSW+1(7,IOBREG),CSWLOC+1 MOVE CSW TO IOB
         TM    CSWLOC+4,CSWCHE          TEST FOR CHANNEL END    SA52441
         BC    8,INT010                 NO, FREE UCB            SA52441
         TM    CSWLOC+4,CSWDVE          TEST FOR DEVICE END     SA60254
         BC    8,INT010A                NO, DON'T FREE UCB      SA60254
INT010   NI    UCBFL1(UCBREG),FF-UCBBSY YES-MAKE IT AVAILABLE    BBBBBB
INT010A  LH    WKREG1,CSWLOC+4          CHECK FOR ABNORMAL STATUS
         N     WKREG1,CHEDVE            TEST FOR OTHER BITS ON
         BC    7,INT011B           YES-GO TO STATUS INSPECTION     IMP2
         SPACE 1                                                   IMP2
***********************************************************************
*                                                                     *
*                      TRAP ROUTINE EXIT                              *
*                                                                     *
*              THE FOLLOWING CODING SETS UP THE EXIT                  *
*              ADDRESS TO THE CORRECT TRAP ROUTINE                    *
*                                                                     *
INTTRAP  IC    ICREG,UCBDTI(UCBREG)     GET DEVICE TABLE INDEX
         LH    WKREG1,DEVTAB+DVTTRP(ICREG)   GET TRAP RTN INDEX
         AIF   (&TBASE EQ 0).TBS215                                IMP3
         AR    WKREG1,BASREG            ADJUST ADDRESS             RL19
.TBS215  BALR  LNKRG2,WKREG1           GO TO TRAP ROUTINE          IMP3
INTNOT   DS    0H *                     RETURN FOR NO TRAP DEVICESITH01
         CLC   UCBTYP+2(2,UCBREG),IT7330 * IS IT 7330?            ITH01
         BNE   ITINTNOT *               NO, SKIP IMMED RESTART    ITH01
         TM    UCBFL1(UCBREG),UCBASK+UCBDTR * SEEKEND?            ITH01
         BC    8+1,ITINTNOT *           NO, SKIP IMMED RESTART    ITH01
         NI    UCBFL1(UCBREG),FF-UCBPST * RESET POST FLAG         ITH01
         AIF   (&RMS EQ 0).IRMS03                                 ITH01
         L     WKREG1,RMSFLAD *         ADDR OF RMS FLAG          ITH01
         TM    0(WKREG1),RMSACTV *      TEST IF RMS REQUEST       ITH01
         BO    INT026 *                 YES, RETURN TO CH SEARCH  ITH01
.IRMS03  ANOP                                                     ITH01
         IECLNK1 ,LA,LNKRG1,IINT037,,,,&TBASE SET RET FOR PSIO    ITH01
         BAL   LNKRG3,XCP064 *          GO LOAD LCHREG            ITH01
         LH    UAREG,IOPSWO+2 *         GET UNIT ADDRESS          ITH01
         NI    UCBFL1(UCBREG),X'FF'-UCBCUB * CLEAR C.U.BSY        ITH01
         LA    REG0,IINT037 *           LOAD RET AND IND RESTART  ITH01
         B     RPSSIO *                 GO TO SIO ROUTINE         ITH01
IINT037  B     IINT026 *                BUSY OR SIO SUCCESSFUL    ITH01
         B     IINT026 *                CU BUSY OR CU END         ITH01
         B     IINT026 *                DEVICE BUSY               ITH01
         IECLNK1 IINT026,LA,LNKRG1,INT027,,,,&TBASE SET FOR CH SR ITH01
         B     INT026 *                 GO TO CH SEARCH           ITH01
ITINTNOT DS    0H *                     CONNECT                   ITH01
* IF A MACHINE MALFUNCTION OCCURS CAUSING PREMATURE TERMINATION SA69782
* WITH INVALID STATUS, PREVENT NORMAL POSTING; POST IN ERROR    SA69782
         TM    CSWLOC+4,CSWCHE+CSWDVE+CSWUEX  VALID TERM STATUS SA69782
         BC    4+1,INT019C              YES, CONTINUE           SA69782
         SR    ICREG,ICREG              CLEAR REG               SA69782
         CH    ICREG,CSWLOC+4           ZERO STATUS             SA69782
         BC    8,INT019K                YES, FORCE ERROR        SA69782
         TM    CSWLOC+4,CSWBSY+CSWSMD+CSWCUE  INVALID TERM STAT SA69782
         BC    8,INT019C                NO, CONTINUE            SA69782
         AIF   (&DAVV EQ 0).DAVV04                              SA74387
         TM    UCBTYP+2(UCBREG),DSKTYP  TEST FOR DIRECT ACCESS  SA74387
         BZ    INT019K                  NO, SKIP                SA74387
         TM    UCBFL4(UCBREG),UCBVVR    DAVV IN CONTROL         SA74387
         BO    INTERR4A                 YES, RESCHEDULE DAVV    SA74387
.DAVV04  ANOP                                                   SA74387
INT019K  MVI   UCBFL1(UCBREG),ZERO      CLEAR UCB FLAGS         SA69782
         MVI   IOBCOD(IOBREG),PERMER    SET PERM ERROR CODE     SA69782
         L     WKREG1,IOBFL1(IOBREG)    CLEAR SENSE             SA74387
         N     WKREG1,CLEARM                AND IOB FLAGS       SA74387
         ST    WKREG1,IOBFL1(IOBREG)            ONE AND TWO     SA74387
         OI    IOBFL1(IOBREG),IOBEX     SET IOB EXCEPTION FLAG  SA74387
         LA    APBSRG,APGABE            GET ABN END APG INCR    SA74387
         BC    15,INT12C                ENTER APG AS PERM ERROR SA74387
         SPACE 2
***********************************************************************
*                                                                     *
*           CHANNEL END AND ABNORMAL END APPENDAGE EXIT AND
*              CHANNEL END APPENDAGE BASE INCREMENT SETUP
*                                                                     *
*              THE FOLLOWING CODING IS THE EXIT TO THE                *
*              PROPER APPENDAGE (CHANNEL END OR ABNORMAL END).
*                                                                     *
INT019C  NI    UCBFL1(UCBREG),UCBBSY    RESET ALL EXECPT BUSY    BBBBBB
         TM    18(UCBREG),X'20'         DIRECT ACCESS DEVICE?  @SA76637
         BZ    INT019B                  NO,CONTINUE            @SA76637
         TM    CSWLOC+4,X'08'           CHANNEL END IN CSW?    @SA76637
         BO    INT019B                  YES,CONTINUE           @SA76637
         OI    6(UCBREG),X'08'          SET UCB CONT UNIT BUSY @SA76637
         B     INT17C                   AND RE-EXCP.           @SA76637
*   INCREMENT TO CHANNEL END APPENDAGE.
INT019B  LA    APBSRG,APGCHE            NORMAL END APD. INCR.
INT019   TM    IOBFL1(IOBREG),IOBERR    IS ERROR ROUTINE IN CONTROL
         BC    1,INTERR                 YES-BYPASS APPENDAGE     BBBBBB
         SPACE 1
INT12C   L     WKREG1,DEBAPG-1(DEBREG)  GET APPENDAGE TABLE ADDRESS
         L     APBSRG,0(APBSRG,WKREG1)  FORM APPROPRATE APP. ADDR
*   SET CONDITION AND IOB EXCEPTION CODE BEFORE ENTERING APPENDAGE
         TM    IOBCSW+4(IOBREG),CSWUEX+CSWUCK+CSWBSY TEST FOR UNIT ERR
         BC    7,INT012                 TO APPENDAGE
         TM    IOBCSW+5(IOBREG),CSWIL+CSWPC+CSWPCK+CSWCCK+CATBIT   CCHA
*                                       TEST OTHER CONDITIONS
         BC    8,INT12B                 SKIP SETTING OF FLAG
INT012   OI    IOBFL1(IOBREG),IOBEX     SET IOB EXCEPTION FLAG
INT12B   BALR  LNKRG2,APBSRG            TO PROPER APPENDAGE
         SPACE 1
*    DEFINITION OF CHAN AND ABN END APPENDAGE - IOS INTERFACE    BBBBBB
*    INPUT     TSTREG,IOBREG,DEBREG,DCBREG,UCBREG,               BBBBBB
*              LNKRG2(RET ADDR),APBSRG(BASE ADDR)                BBBBBB
*    REGS CHANGED  WKREG1,WKREG2,WKREG3,LCHREG                   BBBBBB
         SPACE 1
***********************************************************************
*                                                                     *
*              CHANNEL END AND ABNORMAL END VECTOR TABLE              *
*                                                                     *
         BC    15,INT023                NORMAL
         BC    15,INT024B          SKIP POST                       TBSE
         BC    15,INT17A                EXCP
         BC    15,PRGCOMA               SKIP POST, DON'T RETURN RQE
         AIF   (&CEXT EQ 0).SPEXT1                                BBBBB
         BC    15,INT17                 SWITCH EXTENT RETURN      BBBBB
.SPEXT1  ANOP                                                     BBBBB
*                                                                     *
***********************************************************************
*   THE NORMAL RETURN BRANCHES HERE
INT023   TM    IOBFL1(IOBREG),IOBEX     TEST FOR EXCEPTION
         BC    1,INTERR                 YES TO ERROR INTERFACE
INT024   BAL   LNKRG2,XCPPST            BRANCH TO POST ROUTINE
*   SKIP POST RETURN POINT
         AIF   (&TBASE EQ 1).TBS220
*   INT025 (EXTERNED) IS USED BY THE SUPERVISOR EXIT ROUTINE TO
*   RETURN AN RQE AND CHECK FOR PURGE COMPLETE AFTER DEQUEUING THE RQE
*   FROM AN IRB.
*   RETURN IS BACK TO THE SUPERVISOR EXIT ROUTINE.
INT025   DS    0H
.TBS220  ANOP                                                      TBSE
INT024B  BAL   LNKRG2,PRGCOMB          GO CHECK FOR PURGE          IMP4
         SPACE 1
         AIF   (&TCBTYP EQ 1).RQEMFT
INT025A  TM    TSTASSN(TSTREG),FF       IS RQE ALREADY FREE
         AGO   .RETNRQE
.RQEMFT  ANOP
INT025A  TM    TSTTCB(TSTREG),FF        IS RQE ALREADY FREE
.RETNRQE BC    1,INT026                 DON'T FREE IT AGAIN DUMMY
         MVC   TSTLNK(2,TSTREG),NEXAVL  RETURN RQE
         STH   TSTREG,NEXAVL            TO NEXT AVAILABLE
         AIF   (&TCBTYP LT 2).INTPSD    NOT MVT, SKIP            A24739
         MVI   TSTASSN(TSTREG),FF       MARK AS AVAILABLE        BBBBBB
INT025B  BC    0,INTVMSA                TO TASK SW IF LAST RQE SW SET
         AGO   .INTVMSA
.INTPSD  MVI   TSTTCB(TSTREG),FF        PUT FF IN TCBID FIELD    BBBBBB
.INTVMSA ANOP             MS/1 TASK SWITCH ROUTINE
*   POST AND RQE RETURN SKIP
INT026   DS    0H                       GENERAL RETURN POINT     BBBBBB
         XC    LTSVALSV(2),LTSVALSV     NO VALID RQE             A24739
         IECLNK1 ,BCR,15,LNKRG1,,,,&TBASE                        A24739
         SPACE 1
         AIF   (&TBASE EQ 0).TBS245                                TBSE
INT025   DS    0H                                                  TBSE
*   INT025 (EXTERNED) IS USED BY THE SUPERVISOR EXIT ROUTINE TO
*   RETURN AN RQE AND CHECK FOR PURGE COMPLETE AFTER DEQUEUING THE RQE
*   FROM AN IRB.
*   RETURN IS BACK TO THE SUPERVISOR EXIT ROUTINE.
         LR    WKREG1,BASREG2      SAVE REG CONTENTS FOR RETURN    TBSE
         BALR  BASREG,0                                            TBSE
INT0025  LA    BASREG2,INT0025-IOSBAS                              TBSE
         SR    BASREG,BASREG2      SET LOW BASE REGISTER           TBSE
         LA    BASREG2,4094(BASREG)     SET UP HIGH BASE REG       RL19
         ST    WKREG1,BUSYSV           SAVE RETURN TO EXIT         TBSE
         LA    WKREG1,INT0025A         RETURN FROM SELECTV         TBSE
         ST    WKREG1,LNKRG1S                                      TBSE
         BC    15,INT024B                                          TBSE
         SPACE 1
INT0025A L     BASREG2,BUSYSV          RESTORE SUPVSR BASE         TBSE
         BCR   15,BASREG2              RETURN TO SUPRVSR EXIT      TBSE
.TBS245  SPACE 1                                                   TBSE
         AIF   (&CEXT EQ 0).SPEXT2                                BBBBB
*   SPECIAL CH. END APPENDAGE ENTRY FOR EXTENT SWITCHING.         BBBBB
*   THE NEW EXTENT IS INDICATED BY THE IOB MODIFIER BYTE SETTING. BBBBB
*   INITIALIZE REG0 FOR ABTERM IN CASE OF ILLEGAL UCB ADDRESS.    BBBBB
         SPACE 1                                                  BBBBB
INT17    DS    0H                                                 BBBBB
         AIF   (&TCBTYP LT 2).TCB050    NOT MVT, SKIP            A24739
         L     WKREG3,TSTTCB(TSTREG)    GET TCB ADDRESS           BBBBB
         AGO   .TCB051                                            BBBBB
.TCB050  IC    ICREG,TSTTCB(TSTREG)     GET TCB ID                BBBBB
         SLL   ICREG,2
         L     WKREG3,TCBTBL            GET TCB TABLE ADDRESS     BBBBB
         L     WKREG3,0(ICREG,WKREG3)   GET PROPER TCB ADDRESS    BBBBB
.TCB051  LA    REG0,0(WKREG3)           INIT ABERM INTERFACE REG  BBBBB
*   DETERMINE VALIDITY OF NEW EXTENT                              BBBBB
         BAL   LNKRG3,XCP061            USE COMMON RTN TO VAL CHK BBBBB
         SPACE 1                                                  BBBBB
*   NEW EXTENT WAS GOOD - SET UP RQE                              BBBBB
         STH   UCBREG,TSTUCB(TSTREG)    CHANGE UCB PTR IN RQE     BBBBB
.SPEXT2  SPACE 1                                                  BBBBB
*   EXCP EXIT FROM APPENDAGE VECTOR TABLE
INT17A   DS    0H                                                 19022
         AIF   (NOT &D2305).ZUS040                                20201
         SPACE 1
*              GO GET ADDR OF BASE EXPOSURE UCB IN CASE 2305      20201
         L     WKREG2,AZUSRTN           GET SUBRTN ADDR           20201
         BALR  LNKRG3,WKREG2            GO GET BASE ADDR IF 2305  20201
         STH   UCBREG,TSTUCB(0,TSTREG)  RESET IN CASE BASE FOUND  2020
.ZUS040  AIF   (&TSO EQ 0).INTS3        TEST FOR TSO              20001
         NC    DEBEPG(3,DEBREG),DEBEPG(DEBREG)    TEST FOR ADDR   20001
         BC    8,INT17A1                NO-BRANCH AROUND          20001
         L     WKREG2,DEBEPG-1(DEBREG)  GET LIST ADDR FOR TSO PRG 20001
         TM    ZERO(WKREG2),LSTOPT      TEST FOR LIST OPTION      20001
         BC    8,INT17A1                NO-BRANCH AROUND          20001
         L     WKREG1,QPLPTR(WKREG2)    GET LIST POINTER          20001
         LA    WKREG1,FOUR(WKREG1)      UPDATE TO FIRST ENTRY     20001
         AIF   (&TCBTYP EQ 2).INTS1     TEST FOR MVT              20001
INTSLP   L     WKREG2,FOUR(WKREG1)      GET TCB ADDRESS           20001
         CLC   TCBID(1,WKREG2),TSTASSN(TSTREG)    TCB ID COMPARE  20001
         AGO   .INTS2                                             20001
.INTS1   ANOP                                                     20001
INTSLP   CLC   TSTTCB+1(3,TSTREG),FIVE(WKREG1) TEST FOR TCB COMP  20001
.INTS2   BC    7,INTSLP1                NO-TEST FOR LAST          20001
         MVC   IOBRST(3,IOBREG),ONE(WKREG1)  IOB TO TOP OF CHAIN  20001
         ST    IOBREG,ZERO(WKREG1)      IOB ADDR TO CHAIN FLD     20001
         MVI   IOBCC(IOBREG),X'FF'      SET PTK SAVED INDICATOR@SA78151
         MVZ   IOBCC(1,IOBREG),TSTKEY(TSTREG) SAVE PTK IN IOB  @SA78151
         B     INT024B                  GO TO PURGE COMP RTN      20001
INTSLP1  TM    FOUR(WKREG1),QPLFLST     TEST FOR LAST ENTRY       20001
         BC    1,INT17A1                YES-CONTINUE NORMAL       20001
         AH    WKREG1,EIGHT             BUMP TO NEXT ENTRY        20001
         B     INTSLP                   GO TEST NEXT              20001
INT17A1  DS    0H                                                 20001
.INTS3   ANOP                                                     20001
         AIF   (&SMF EQ 0).SMF150                                  SMF2
         AIF   (&TCBTYP GT 1).SMF130    IF MVT, SKIP             A24739
         BAL   LNKRG2,SMF010           GO SET UP TCB ADR           SMF2
         AGO   .SMF135                                             SMF2
.SMF130  L     REG0,TSTTCB(0,TSTREG)   SET UP TCB ADDRESS          SMF2
.SMF135  L     APBSRG,SMFRTN            GET ADDRESS OF SMF RTN     SMF2
         BALR  LNKRG2,APBSRG .          REPORT EXCP RETRY TO SMF A31489
.SMF150  ANOP                                                     19022
INT17B   MVI   IOBCOD(IOBREG),NORMC     SET NORMAL COMPLETION     19022
         XC    IOBFL3(9,IOBREG),IOBFL3(IOBREG) CLEAR FLGS,SENSE   19022
         XC    IOBECT(2,IOBREG),IOBECT(IOBREG)  ZERO ERROR COUNT A43797
         NC    IOBFL1(4,IOBREG),CLEARM  CLEAR FLAGS AND SENSE      IMP5
INT17C   BC    15,INTER7A               PICK UP ENQUEUE            PRER
         EJECT
*  THIS SECTION INVESTIGATES NON-CHANNEL END CONDITIONS
INT009   NI    UCBFL5(UCBREG),FF-UCBTICBT SHOW INTER. RECEIVED   A42261
         TM    UCBCHA(UCBREG),UCBSMD    WAITING FOR SENSE OPER. US05348
         BO    INT011                   YES, CHECK ATTENTION    US05348
         TM    UCBFL1(UCBREG),UCBBSY+UCBASK+UCBCUB INT. EXPECTED A27707
         BC    7,IINT009J *             YES, BRANCH AROUND        ITH01
         CLC   UCBTYP+2(2,UCBREG),IT7330 * IS IT 7330             ITH01
         BNE   INTATT *                 NO, HANDLE LIKE ATTENTION ITH01
         TM    CSWLOC+4,CSWRETRY *      RETRY STATUS FROM HIO     ITH01
         BO    INTSEN3A *               YES, GO ISSUE SENSE       ITH01
         L     WKREG1,UCBWKADB(0,UCBREG) * GET PTR TO EXTENSION   ITH01
         TM    RSBFLG1(WKREG1),RTRYSSS * PURGED RETRY ON SK&SS    ITH01
         BNO   INTATT *                 NO, HANDLE LIKE ATTENTION ITH01
         NI    RSBFLG1(WKREG1),X'FF'-RTRYSSS * CLEAR RETRY SK&SS  ITH01
         B     INTSEN3A *               GO ISSUE SENSE            ITH01
IINT009J DS    0H *                     CONNECT                   ITH01
         TM    CSWLOC+4,CSWUCK+CSWUEX   INTERCEPT COND AT D.E.   BBBBBB
         BC    8,INT011                 NO-GO FREE UCB
*  IF ERP IS ALREADY IN CONTROL (RQE ON QUEUE FOR RESTART) DO    A32507
*   NOT TREAT AS INTERCEPT, BUT GO TO CHANNEL RESTART.           A32507
         TM    UCBFL1(UCBREG),UCBASK+UCBERR+UCBCUB UCBLTS GOOD   A36514
         BC    4+1,INT011 .             YES, DO NOT INTERCEPT    A32507
         SPACE 1
*   NEXT TIME WILL INTERCEPT IOB
         MVC   UCBLTS(2,UCBREG),CSWLOC+4     MOVE STATUS TO UCB
         OI    UCBFL1(UCBREG),UCBITF    SET INTERCEPT FLAG
INT011   NI    UCBFL1(UCBREG),FF-UCBBSY . MARK FREE             US05348
         CLC   UCBTYP+2(2,UCBREG),IT7330 * IS IT 7330?            ITH01
         BNE   IINT011J *               NO, BRANCH AROUND         ITH01
         TM    UCBCHA(UCBREG),UCBSMD *  SENSE WAITING?            ITH01
         BNO   IINT011J *               NO, BRANCH AROUND         ITH01
         NI    UCBCHA(UCBREG),X'FF'-UCBSMD * RESET SENSE FLAG     ITH01
         B     INTSEN3A *               GO DO SENSE               ITH01
IINT011J DS    0H *                     CONNECT                   ITH01
INT011B  EQU   *                                                SA74193
         AIF   (NOT &D2305).ZU000       TEST FOR ZEUS IN SYS    SA74193
         LR    WKREG1,UCBREG            SAVE UCB ADDR           SA74193
         L     WKREG2,AZUSRTN           GO CHECK IF DEVICE IS   SA74193
         BALR  LNKRG3,WKREG2            A 2305 AND GET BASE AD  SA74193
         BC    7,INT011C                NO, CONTINUE            SA74193
         NI    UCBFL1(UCBREG),FF-UCBBSY FREE THE BASE           SA74193
         LR    UCBREG,WKREG1            RESTORE EXPOSURE UCB AD SA74193
.ZU000   SPACE 1                                                SA74193
*    ENTERED FROM MAINLINE ON ANY INTERRUPT WITH ABNORMAL STATUS.
INT011C  TM    CSWLOC+4,CSWATN     CSW CONTAIN ATTENTION           IMP2
         BC    1,INTATT2           YES-GO TO ATTENTION RTN.        IMP2
INT013   TM    UCBCHA(UCBREG),UCBSMD    WAITING FOR SENSE OPER. US05348
         AIF   (&TP EQ 0 AND &RESREL EQ 0).RR020                SA75351
         BO    INTSEN4                  YES, RETURN TO SENSE RT SA75351
         AGO   .RR022                                           SA75351
.RR020   BO    INTSEN6                  YES, RETURN TO SENSE RT SA75351
.RR022   ANOP                                                   SA75351
INT013B  TM    CSWLOC+4,CSWUCK     CSW=UNIT FAILURE                IMP2
         BC    1,INTSEN            YES-GO TO SENSE RTN.            IMP2
         TM    UCBFL1(UCBREG),UCBPST  OPERATION COMPLETE INTERRUPT IMP2
         IECLNK1 ,BCR,8,LNKRG1,,,,&TBASE                           TBSE
*        TEST FOR CATASTROPHIC ERRORS                             19062
         TM    CSWLOC+5,CSWPC+CSWPCK+CSWCDK+CSWCTK+CSWICK+CSWCCK  19062
         BC    8,INTTRAP                NO, RETURN TO NORMAL FLOW 19062
         BC    15,INT031                GO TO ABN APPENDAGE       19062
         SPACE 1                                                 BBBBBB
INT006   DS    0H                                                BBBBBB
         AIF   (&TP EQ 1 OR &RESREL EQ 1).ASR2                    21111
         AIF   (&ASR EQ 0 OR &M65MP EQ 1).RR025                   21111
.ASR2    TM    IOBFL2(IOBREG),IOBSNB    WAITING FOR DEVICE        BBBBB
         AIF   (&TP EQ 0 AND &RESREL EQ 0).ASR2A                  21111
         BC    8,INTSEN4                NO MUST BE END OF SENSE   BBBBB
         AGO   .RR025
.ASR2A   BC    8,INTSEN6                NO MUST BE LOG PEND COND  21111
.RR025   OC    CSWLOC+1(7),IOBCSW+1(IOBREG) OR IN PREVIOUS COND. BBBBBB
         NI    IOBFL2(IOBREG),FF-IOBSNB RESET SENSE FLAG         BBBBBB
         NI    UCBCHA(UCBREG),FF-UCBSMD RESET SENSE FLAG         BBBBBB
         BC    15,INT008
      TITLE 'CHANNEL RESTART INITIALIZATION - INPUT/OUTPUT SUPERVISOR'
*              RETURN TO I/O FLIH SUBROUTINE
*
*                                  AND
*
*              CHANNEL RESTART INITIALIZATION SUBROUTINE
         SPACE 3
* CHECK IF OTHER CHANNELS NEED PROCESSING BEFORE RETURNING         IMP3
INT033   L     WKREG2,CHMSK             GET MASK BYTE             19021
         SR    WKREG3,WKREG3           CLEAR COUNTER               IMP3
INT030A  ALR   WKREG2,WKREG2           DOUBLE THE MASK BYTE        IMP3
         BC    8,INT033C                IF BYTE=0, GO TO TEST EXIT19021
         LA    WKREG3,1(WKREG3)        IF OVERFLOW OR BYTE NOT 0   IMP3
         BC    4,INT030A               CONT. TILL CORRECT CHAN FOUNDMP3
         BCTR  WKREG3,0                ON OVERFLOW, BACK UP CHANADRIMP3
         STC   WKREG3,IOPSWO+2         SET UP CHAN FOR RESTART
* THIS IS A GENERAL RETURN FROM AN I/O INTERRUPT (IECINT) ENTRY,   IMP3
* FOR THE PURPOSE OF RESTARTING THE CHANNEL THAT CAUSED THE        IMP3
* INTERRUPT.  ERREXCP, THE SELECT ROUTINE TRY-AGAIN VECTOR, AND    IMP3
* DISK ERP'S REQUIRING FUTHER I/O ALSO BRANCH HERE.                IMP3
         SPACE 1                                                   IMP5
INT027   DS    0H
         AIF   (&M7CH EQ 0).RST027
         CLI   CHMSK,RSTMSK             RESTART ALL CHANNELS
         BC    7,INTM7CH                NO, CHMSK OK
         MVC   CHMSK(2),RST7MSK         RESTART ALL CHANNELS
.RST027  ANOP
INTM7CH  IC    ICREG,IOPSWO+2           GET CH ADR FOR RESTART   BBBBBB
         AIF   (&M65MP EQ 0).MPB039                                MP1A
         LR    WKREG1,ICREG     TRANSFER CHAN ADDR                 MP1A
         AR    WKREG1,WKREG1     DOUBLE ADDR TO GET INDEX          MP1A
         NI    CHAN0-IEAQFX00(WKREG1),X'FF'-CHBSY   FREE CHANNEL   MP1A
.MPB039  AIF   (&M7CH EQ 1).M7CH002
         SLL   ICREG,2                  CH ADR * 4 TO SPAN TAB ENT BBBB
         LA    WKREG1,IECCST(ICREG)     POINT AT CHAN TAB ENTRY  BBBBBB
         OC    CHMSK(1),LCTMSK(WKREG1) SET ON IN CASE OF INTERRUPT IMP3
         AGO   .M7CH003                                            MSVN
.M7CH002 L     WKREG1,CHANBIT           GET CHANNEL BIT            I267
         SRL   WKREG1,ZERO(ICREG)       DERIVE PSW EQUIVALENT      MSVN
         LR    WKREG2,WKREG1            SAVE IT FOR LATER          MSVN
         O     WKREG1,CHMSK             PICK UP ANY CHANS TO RSTRT MSVN
         ST    WKREG1,CHMSK             SET THIS CHAN IN CASE OF INTSVN
         SLL   ICREG,2                  ICREG = ICREG * 4          IMP3
.M7CH003 ANOP                                                      MSVN
         LH    LNKRG2,IECCST(ICREG)    GET CHANNEL SEARCH CODE ADR IMP3
         AIF   (&TBASE EQ 0).TBS249                                IMP3
         AR    LNKRG2,BASREG            ADJUST ADDRESS             RL19
.TBS249  TM    XCPBTE,XCPCNT+XCPST+XCPRMS EXCP OR MCH ENTRY       19021
         BC    4+1,INT027A              YES, NO ENABLE            19021
* PROCESS ANY STACKED INTERRUPTS BEFORE CHANNEL SEARCH             IMP3
         AIF   (&M65MP EQ 0).MPB053A                               MP1A
INT030B  LPSW  MPENBA                  ENABLE FOR INTERRUPTS       MP1A
         AGO   .MPB053B                                            MP1A
.MPB053A SSM   XCPENB                  ENABLE FOR INTERRUPTS       MP1A
.MPB053B ANOP                                                      MP1A
* IF AN INTERRUPT IS PENDING, THE FLIH WILL RE-ENTER IOS AT IECINT.
* IF NO INTERRUPT IS PENDING, THE NEXT INSTRUCTION IS EXECUTED.    IMP3
         AIF   (&M65MP EQ 0).MPB053C                               MP1A
INT030C  LPSW  MPDSBA                                              MP1A
         AGO   .MPB053D                                            MP1A
.MPB053C SSM   IONPSW                  DISABLE BEFORE CH.SRCH.     MP1A
.MPB053D CLI   CLPCNT,ZERO              CH LOGOUT COUNT ZERO    SA71482
         BE    INT027A                  YES, CONTINUE           SA71482
         L     APBSRG,ACLPRTN           GET ADDR OF CH LOGOUT RTSA71482
         BR    APBSRG                   GO TO CHAN LOG PEND RTN SA71482
         AIF   (&M7CH EQ 1).M7CH004                             SA71482
INT027A  XC    CHMSK(1),LCTMSK(WKREG1)  RESET THIS CHANNEL        19021
         AGO   .M7CH005                                            MSVN
.M7CH004 ANOP                                                      MSVN
INT027A  X     WKREG2,CHMSK             RESET THIS CHANNEL         MSVN
         ST    WKREG2,CHMSK             STORE ALL OTHERS BACK      MSVN
.M7CH005 BR    LNKRG2                   GO TO CHANNEL SEARCH    SA71482
*    SEND EXCP, RMS, AND SHOULDER TAP ENTRIES TO EXIT ROUTINE     19021
INT033C  TM    XCPBTE,XCPCNT+XCPST+XCPRMS TEST NON-INTRPT ENTRY   19021
&LABEL   SETC  'XCP021'                                           19021
         AIF   (&RMS EQ 0 AND &M65MP EQ 0).RMS30                  19021
&LABEL   SETC  'IECEXIT'                                          19021
.RMS30   BC    4+1,&LABEL               YES-GO TO EXIT PROC       19021
*    ENABLE TO ACCEPT ANY PENDING I/O INTERRUPTS                  19021
         AIF   (&M65MP EQ 0).MPB052A                               MP1A
INT033A  LPSW  MPENB                   ENABLE FOR INTERRUPTS       MP1A
         AGO   .MPB052B                                            MP1A
.MPB052A SSM   XCPENB                  ENABLE FOR INTERRUPTS       MP1A
.MPB052B L     WKREG1,DISMIS            GET RETURN ADDRESS
         SPACE 1
*    DISABLE AND RETURN TO I/O FLIH IF NO CHAN NEEDS TO BE RESTARTED
         AIF   (&M65MP EQ 0).MPB052C                               MP1A
INT033B  LPSW  MPDSB                   DISABLE AND TEST FOR EXIT   MP1A
         AGO   .MPB052D
.MPB052C SSM   IONPSW                  DISABLE AND TEST FOR EXIT   MP1A
.MPB052D BCR   15,WKREG1                RETURN TO I/O FLIH        19021
         AIF   (&RMS EQ 0 AND &M65MP EQ 0).RMS31                  19021
         SPACE 1                                                  19021
*    EXIT PROCEDURE FOR NON-INTERRUPT ENTRIES                     19021
IECEXIT  DS    0H                                                 19021
         AIF   (&RMS EQ 0).RMS32                                  19021
         TM    XCPBTE,XCPRMS            TEST FOR RMS IN CTL       19021
         L     APBSRG,RMSEXAD           ADDR OF IECRMS           BBBBBB
         BC    1,INTRMSEX-IECRMS(APBSRG)  YES-GO TO RMS EXIT
.RMS32   AIF   (&M65MP EQ 0).RMS33                                19021
         TM    XCPBTE,XCPST             TEST FOR SHOULDER TAP ENT 19021
         BC    1,IECIOSTV
.RMS33   BC    15,XCP021                GO TO SVC EXIT            19021
.RMS31 TITLE 'COMMON CHANNEL SEARCH ROUTINE - INPUT/OUTPUT SUPERVISOR'
***********************************************************************
*                                                                     *
*                                                                     *
*                  COMMON CHANNEL SEARCH ROUTINE OF IOS               *
*                                                                     *
*    THE FOLLOWING CODE, IN COMBINATION WITH THE CHANNEL-DEPENDENT    *
*    CHANNEL SEARCH MODULES, CONSTITUTES THE CHANNEL SEARCH ROUTINE OF*
*    IOS. EACH CHANNEL SEARCH MODULE INITIALIZES LCHREG WITH THE      *
*    APPROPRIATE LOGICAL CHANNEL WORD ADDRESS, AND ROUTES CONTROL TO  *
*    VARIOUS ENTRY POINTS WITHIN THE COMMON ROUTINE, SO THAT THE      *
*    COMMON ROUTINE MAY DETERMINE THE STARTABILITY OF EACH REQUEST    *
*    ON THE QUEUE IN TURN.                                            *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
*    THIS PORTION GETS CONTROL IF THE CHANNEL HAS SEEKABLE DEVICES.
INT036C  LR    TSTREG,LCHREG            INITIALIZE PTR TO TOP
INTCIC   LH    TSTREG,TSTLNK(TSTREG)    GET NEXT RQE
         SPACE 1
*  RETURN FROM SEARCH CODE- ONLY FOR OVERLAP SEEKS
INT036   LTR   TSTREG,TSTREG            IS THIS THE LAST RQE
         BC    4,CHSNXT(LNKRG2)         YES-SKIP TO NEXT LCH
         LH    UCBREG,TSTUCB(TSTREG)    ADDR. OF DEVICE
         TM    UCBFL1(UCBREG),UCBASK+UCBDTR+UCBBSY+UCBNRY+UCBPST A24150
         BCR   7,LNKRG2                 NO-SKIP IT
         TM    UCBTYP+2(UCBREG),DSKTYP  DIRECT ACCESS TYPE
         BCR   8,LNKRG2                 NO-SKIP THIS ONE
         AIF   (&RPS EQ 0).RPS06                                  19025
         TM    UCBTYP+1(UCBREG),UCBRPS  TEST FOR RPS              19025
         BCR   1,LNKRG2                 YES, SKIP UNTIL DATA XFER 19025
.RPS06   AIF   (NOT &D2301 AND NOT &D2303 AND NOT &D2305).SXIOSS4 20201
         TM    UCBTYP+3(UCBREG),DRUM    IFDRUM NO SEEK
         BCR   1,LNKRG2                 SO SKIP THIS REQUEST
.SXIOSS4 BC    15,INT034B               TO START UP DEVICE
         SPACE 3                                                  20201
*    THIS PORTION GETS CONTROL FOR ALL DATA TRANSFER OPERATIONS.  20201
INT034C  LR    TSTREG,LCHREG            INITIALIZE POINTER TO TOP
INT034D  LH    TSTREG,TSTLNK(0,TSTREG)  PICK NEXT IN QUEUE
INT034   LTR   TSTREG,TSTREG            TEST FOR DUMMY RQE
         BC    4,CHSNXT(LNKRG2)         CONTINUE WITH NEXT DISK OR UNIT
*              THIS WAS A VALID QUEUED REQUEST
         SPACE 1
         LH    UCBREG,TSTUCB(TSTREG)    SELECT
INT034A  TM    UCBFL1(UCBREG),UCBDTR+UCBBSY+UCBNRY+UCBPST          TPF1
*                                       TEST ALL NOT AVAILABLE FLAGS
         BCR   7,LNKRG2                 BRANCH NOT AVAIL  TRY NEXT
         TM    UCBTYP+2(UCBREG),DSKTYP    DIRECT ACCESS
         BC    8,INT034B                NO, BRANCH AROUND         20201
         AIF   (NOT &D2301 AND NOT &D2303 AND NOT &D2305).IODRM10 20201
         TM    UCBTYP+3(UCBREG),DRUM    IS IT A DRUM              20201
         BC    1,INT034B                YES, GO START IT          20201
.IODRM10 AIF   (&RPS EQ 0).RPS07                                  20201
         CLC   UCBTYP+2(2,UCBREG),IT7330 * IS IT 7330             ITH01
         BE    IINT034J *               YES, DONT CHK RPS         ITH01
         TM    UCBTYP+1(UCBREG),UCBRPS  IS IT AN RPS DEVICE       20201
         BC    1,INT034B                YES, GO START IT          20201
.RPS07   SPACE 1                                                  20201
*    SCREEN OUT A REQUEST FOR WHICH THE STAND-ALONE SEEK IS COMPLETE,
*    AND WHICH IS THEREFORE READY TO BEGIN THE DATA TRANSFER OPERATION.
         TM    UCBFL1(UCBREG),UCBASK    IS SEEK COMPLETE          20201
         BCR   8,LNKRG2                 NO, GET NEXT REQUEST      20201
IINT034K DS    0H *                     TEST IF SAME RQE          ITH01
         CH    TSTREG,UCBLTS(0,UCBREG)  YES, IS THIS THE SAME RQE 20201
         BCR   7,LNKRG2                 NO, GET NEXT REQUEST      20201
         AIF   (&RESREL EQ 0 AND &RML EQ 0).RR040                 ITH01
         TM    UCBCHA(UCBREG),UCBHLT    DOES DEVICE REQUIRE RELEAS20201
         BC    1,INTRREL                YES, GO EXECUTE RELEASE   20201
         CLC   RRDCBDEB+1(3),TSTDEB(TSTREG) HAS SIO BEEN TRIED    M3422
         BC    8,INTRREL1               YES, GO RE-INIT. UCB ADR  20201
.RR040   ANOP                                                     20201
INT034B  DS    0H                                                 19021
         AIF   (&RMS EQ 0).RMS03                                  19021
         L     WKREG1,RMSFLAD           ADDR OF RMS FLAG         BBBBBB
         TM    0(WKREG1),RMSACTV        TEST IF RMS REQUEST      BBBBBB
         L     APBSRG,RMSAD             ADDR OF INTRMS           BBBBBB
         BCR   1,APBSRG                 YES-GO TO CHECK RQE      BBBBBB
INTRMS1  DS    0H                                                 19021
.RMS03   ANOP
         STH   TSTREG,LTSVALSV          VALID RQE                A24739
         STM   LCHREG,LNKRG2,BUSYSV     SAVE REGS FOR POSS RETURNA24739
         LM    IOBREG,DEBREG,TSTIOB-1(TSTREG)  FOR RESTART         IMP3
         L     DCBREG,DEBDCB-1(DEBREG)    SET UP REGISTERS         IMP3
         IECLNK1 ,LA,LNKRG1,INT037,,,,&TBASE                       TBSE
         LA    LNKRG2,XCP011            SET UP TO GO TO TEST CHAN  IMP4
         TM    UCBFL1(UCBREG),UCBCUB+UCBERR IS LAST RQE IMPORTANT  IMP4
         BC    7,INT034H                YES - CHECK FURTHER
INT034G  TM    IOBFL1(IOBREG),IOBURL    IS IOB UNRELATED
         BCR   1,LNKRG2                YES, GO TO TEST CHAN OR XCP IMP3
         TM    DCBFL(DCBREG),DCBPER    PERM ERR IN PREVIOUS REQ    IMP3
         BCR   8,LNKRG2                NO, TO TEST CHAN OR XCP     IMP3
         BC    1,INT038B                YES, PURGE REQUEST         IMP3
* THIS DCB IS FLAGGED - ERP IN CONTROL - FIND IOB ERP IS PROCESSING
         TM    IOBFL1(IOBREG),IOBERR   IS ERP IN CONTROL           IMP3
         BCR   7,LNKRG2                YES, TRY TO START THIS REQ  IMP3
         IECLNK1 ,BC,15,SELBSY,0,LNKRG1,,&TBASE                    TBSE
         SPACE 1
INT034H  CH    TSTREG,UCBLTS(0,UCBREG)  WAS IT LAST REQUEST STARTED
         BC    7,INT037+SELBSY          NO - CONTINUE WITH RESTART
         BCR   15,LNKRG2                YES - START IT UP
IINT034J TM    UCBFL1(UCBREG),UCBASK *  IS SEEK COMPLETE?         ITH01
         BO    IINT034K *               YES, GO TEST IF SAME RQE  ITH01
         B     INT034B *                GO START IT               ITH01
         SPACE 1                                                   IMP4
***********************************************************************
*                                                                     *
*                  SELECT ROUTINE RETURN VECTOR TABLE                 *
*                       FOR CHANNEL RESTART                           *
INT037   BC    15,INT035A               ACCEPT VECTOR CONT. WITH SEARCH
         BC    15,INT027                TRY FROM THE TOP AGAIN     IMP5
         BC    15,INT035                BUSY VECTOR-GET NEXT RQE.
*****    BC    15,INT037B               CHANGE LCH VECTOR          IMP5
*                                                                     *
***********************************************************************
INT037B  TM    IOBCC(IOBREG),IOBCC3     WAS REQ REJECTED DUE TO    P340
*                             CONTROL UNIT BUSY OR CC1,2, OR 3     P340
         BC    7,INT035                 YES DO NOT SWITCH LCH WORD P340
         AIF   (&APR EQ 0).APR00C
         AIF   (NOT &D2305).NOZUS                               SA68549
         L     WKREG2,AZUSRTN           GET ZEUS SUBRTN ADDR    SA68549
         BALR  LNKRG3,WKREG2            GO GET BASE ADR IF 2305 SA68549
.NOZUS   ANOP                                                   SA68549
         TM    UCBCHM(UCBREG),OF        IS A PATH OFFLINE
         BC    4+1,INT035               YES, DON'T SWITCH LCH WORDS
.APR00C  LM    LCHREG,LNKRG2,BUSYSV     RECOVER LAST LCH VECTOR  BBBBBB
         XC    LTSVALSV(2),LTSVALSV     NO VALID RQE             A24739
         AIF   (&RPR EQ 1).SXIOSS5                               A24739
         BC    15,CHSNXT(0,LNKRG2)      CONTINUE SEARCH WITH NEXT LCH
         SPACE 1
         AGO   .SXIOSS6
.SXIOSS5 LA    TSTREG,DUMMY             SET LCH TO END OF QUEUE    PRQ1
         BCR   15,LNKRG2                TO CONTINUE SEARCH
.SXIOSS6 SPACE 1
INT035A  DS    0H                                                  SMF1
         L     TSTREG,BLNKPTR          INIT RQE TO BACK LINK PTR   SMF1
INT035   LM    LCHREG,LNKRG2,BUSYSV     RE-INITIALIZE FOR CH. SEARCH
         XC    LTSVALSV(2),LTSVALSV     NO VALID RQE             A24739
         BCR   15,LNKRG2                CONTINUE CHN SRCH        A24739
         SPACE 2                                                  20201
*   THE DCB FOR THIS REQUEST INDICATES A PERMANENT ERROR ON A      IMP4
*   PREVIOUS REQUEST.  THIS REQUEST MUST THEREFORE BE DEQUEUED     IMP4
*   AND THE ABNORMAL END APPENDAGE MUST BE ENTERED.                IMP4
INT038B  DS    0H                                                  IMP4
         BAL   LNKRG2,XCPPDQ            GO DEQUEUE REQUEST      SA66299
         MVI   IOBCOD(IOBREG),PRGCOD    MARK IOB PURGED            IMP4
         CH    TSTREG,UCBLTS(UCBREG)    IS RQE ON UCB?          SA66299
         BC    8,INT031                 YES, CLEAR FLAGS, AB AP SA66299
         BC    15,INT031B               TO ABNORM END APPENDAGE SA66299
         AIF   (&RESREL EQ 0 AND &RML EQ 0).RMS08                 ITH01
         SPACE 1                                                   IMP5
*    THE FOLLOWING ROUTINE INITIALIZES CONTROL BLOCKS FOR          IMP5
*    EXECUTING A RELEASE TO A DEVICE ASSOCIATED WITH A PURGED    BBBBBB
*    REQUEST.  RETURN IS TO INT034B.                             BBBBBB
         SPACE 1                                                   IMP5
INTRREL  DS    0H                       RELEASE ROUTINE            IMP5
         MVI   UCBSKA(UCBREG),ZERO      INSURE M IS 0 IN THE UCB   IMP5
*    NOW INITIALIZE THE RQE.                                       IMP5
         LA    IOBREG,RRIOB             GET IOB ADDRESS            IMP5
         ST    IOBREG,TSTIOB-1(0,TSTREG) STORE IT IN THE RQE       IMP5
         MVC   TSTDEB(3,TSTREG),RRDCBDEB+1 PUT DEB PTR IN RQE     M3422
         AIF   (&TCBTYP EQ 2).MVTRREL   MVT SYSTEM              SA55452
         MVI   TSTTCB(TSTREG),ZERO      SKIP VALIDITY CHECK        IMP5
         AGO   .ENDRREL                                         SA55452
.MVTRREL MVC   TSTTCB(4,TSTREG),ERRTCB  PUT SYS ERR TCB IN RQE  SA55452
.ENDRREL ANOP                                                   SA55452
INTRREL1 STH   UCBREG,RRDEBUCB+2       PUT UCB PTR. INTO DEB       IMP5
         BC    15,INT034B               GO BACK TO MAINLINE IOS    IMP5
.RMS08   TITLE 'QUIESCE COMPLETE ROUTINE - INPUT/OUTPUT SUPERVISOR'
*                     QUIESCE COMPLETE SUBROUTINE
         SPACE 2
*   THIS ROUTINE POSTS THE PURGE ECB WHEN QUIESCE COMPLETE
         SPACE 3
PRGCOMA  LA    LNKRG2,INT026            RETURN ADDR IF BYPASS      TSOS
PRGCOMB  NC    DEBEPG(3,DEBREG),DEBEPG(DEBREG)    TEST FOR ADDR   20001
         BCR   8,LNKRG2                 NO-RETURN                  TSOS
         L     WKREG2,DEBEPG-1(DEBREG)  GET LIST ADDR FOR TSO PRG 20001
         TM    ZERO(WKREG2),TWO         PURGE BY TCB OR DEB      BBBBBB
         BC    8,PRGCOMA1               BRANCH IF BY DEB           TSOS
         AIF   (&TSO EQ 0).PRGNTS       TEST FOR TSO              20001
         TM    ZERO(WKREG2),LSTOPT      TEST FOR LIST OPTION      20001
         BC    8,PRGTCBCM               NO, GO DO TCB ADDR COMP  M5401
         L     WKREG1,QPLPTR(WKREG2)    GET TCB LIST POINTER     M5401
PRGTCBLP CLC   TCBADR+1(3,WKREG1),TSTTCB+1(TSTREG) TCB ADDR COMP M5401
         BE    PRGCOMA1                 YES, CONTINUE WITH COUNT M5401
         TM    TCBADR(WKREG1),QPLFLST   END OF TCB LIST          M5401
         BCR   1,LNKRG2                 YES, RETURN              M5401
         LA    WKREG1,TCBADR(WKREG1)    NO, GET NEXT TCB         M5401
         BC    15,PRGTCBLP                                       M5401
.PRGNTS  AIF   (&TCBTYP LT 2).PRGID     TEST FOR OPTION 4         20001
PRGTCBCM CLC   TCBADR+1(3,WKREG2),TSTTCB+1(TSTREG) TCB ADDR COMP M5401
         AGO   .PRGTCB                                             TSOS
.PRGID   L     WKREG1,TCBADR(WKREG2)    GET TCB ADDRESS          BBBBBB
PRGTCBCM CLC   TCBID(1,WKREG1),TSTASSN(TSTREG) TCB ID COMPARE    M5401
.PRGTCB  BCR   7,LNKRG2                 NO - RETURN                TSOS
PRGCOMA1 IC    ICREG,DEBQUS(DEBREG)     GET COUNT FROM DEB       BBBBBB
         LTR   ICREG,ICREG              TEST FOR ANY COUNT         TSOS
         BCR   12,LNKRG2                NO - RETURN                TSOS
         BCT   ICREG,PRGDEC1            DECREMENT DEB COUNT        TSOS
         XC    DEBEPG(3,DEBREG),DEBEPG(DEBREG) CLR PARM. LIST ADDR TSOS
PRGDEC1  STC   ICREG,DEBQUS(DEBREG)     UPDATE DEB COUNT           TSOS
         IC    ICREG,QCNT(WKREG2)       GET TOTAL CNT FROM LIST  BBBBBB
         LTR   ICREG,ICREG              TEST FOR ANY COUNT      SA61289
         BCR   12,LNKRG2                NO - RETURN             SA61289
         BCT   ICREG,PRGDEC2            DECREMENT COUNT            ABCD
         STC   ICREG,QCNT(WKREG2)       ZERO MAIN COUNTER         20001
PRGPST1  L     WKREG3,ZERO(0,WKREG2)    GET TCB ADDRESS            TSOS
         LA    WKREG2,FOUR(0,WKREG2)    GET ECB ADDRESS          BBBBBB
         LA    WKREG1,NORMC             GET NORMAL COMP CODE     BBBBBB
         SLL   WKREG1,24                SHIFT CODE                 TSOS
         L     APBSRG,POSTADR           GET ADDR. TO POST          TSOS
         BCR   15,APBSRG                GO TO POST                 TSOS
         SPACE 2                                                   TSOS
PRGDEC2  STC   ICREG,QCNT(0,WKREG2)     STORE UPDATED COUNT        ABCD
         BCR   15,LNKRG2                RETURN
         AIF   (&SMF EQ 0 OR &TCBTYP GT 1).SMF070                A24739
         SPACE 2                                                   SMF2
*    THE FOLLOWING ROUTINE PUTS THE ADDRESS OF THE TCB IN REG 0.   SMF2
SMF010   IC    ICREG,TSTTCB(TSTREG)    GET TCB ID                  SMF2
         SLL   ICREG,2                  MULTIPLY BY 4             M4778
         L     WKREG1,TCBTBL           GET TCB TABLE ADDRESS       SMF2
         L     REG0,0(ICREG,WKREG1)     GET TCB ADDRESS            SMF2
         SR    ICREG,ICREG              CLEAR PRIOR TO RETURN     M1542
         BCR   15,LNKRG2                RETURN TO CALLER           SMF2
.SMF070  SPACE 1                                                   RL19
         AIF   (&TCBTYP LT 2).INTATN    NOT MVT, SKIP            A24739
    TITLE 'TASK SWITCH ON LAST RQE RETURN - INPUT/OUTPUT SUPERVISOR'
*         THIS ROUTINE IS ENTERED ONLY IF THE LAST RQE WAS USED AND
*         A EXCP ISSUED CAUSING IOS TO SET THE NON DISPATCHABILITY BIT
*         IN THE TCB OF THE ISSUING  TASK.
*                   1. RESET ALL RQE NON DISP. BITS IN TCBS ON RDY Q
*                   2. INDICATE TASK SWITCH TO IEAHEAD
*                   3. RESET THE NO-OP BRANCH SWITCH FOR LAST RQE
INTVMSA  MVI   INT025B+1,ZERO           RESET LAST RQE SWITCH    BBBBBB
         L     WKREG1,AIEAHEAD          GET ADDRESS OF HEAD TCB
INTSDISP L     WKREG2,AIEADS02          GET ADD. OF TASK SW.
         NI    TCBFLGS(WKREG1),FF-TCBRQENA RESET NON-DISP. BIT   BBBBBB
         BALR  LNKRG2,WKREG2            INVOKE TASK SWITCH
***  RETURN FROM TASK SWITCH ROUTINE
         L     WKREG1,TCBTCB(0,WKREG1)  GET NEXT TCB ON READY Q
         LTR   WKREG1,WKREG1            ARE WE FINISHED
         BC    7,INTSDISP               NO, PROCESS NEXT
         BC    15,INT026                YES, RETURN TO MAINLINE
.INTATN  ANOP
  TITLE 'ATTENTION INTERFACE AND TRAP CODE - INPUT/OUTPUT SUPERVISOR'
*        IOS INTERRUPT SUPERVISOR ATTENTION ROUTINE INTERFACE
         SPACE 2
*      FUNCTION:    TO LINK TO ATTENTION ROUTINE
*         ENTRY:    MAIN LINE OF EXCP AND MAIN LINE OF IOS INTERRUPT
*                   SUPERVISOR
*          EXIT:    ATTENTION ROUTINE
         SPACE 3
INTATT   TM    CSWLOC+4,CSWCHE+CSWDVE                              IMP2
         BC    8+1,INT011B             LET ONLY D.E.'S THROUGH     IMP2
INTATT2  IC    ICREG,UCBATI(UCBREG)    GET INCREMENT TO ATN RTN    IMP2
         L     APBSRG,ATNTAB(ICREG)     GET ADDRESS OF ATN RTN
         L     WKREG1,XITFTR            SET EXIT EFFECTOR ADDR. FOR RTN
         BALR  LNKRG2,APBSRG            BRANCH TO ATTENTION ROUTINE
INTATT3  NI    UCBFL1(UCBREG),FF-UCBNRY-UCBITF-UCBBSY           US05348
         LH    TSTREG,UCBLTS(UCBREG)    GET RQE ADDRESS AGAIN-USED IN
*                                       ATTENTINON ROUTINE
         TM    UCBFL1(UCBREG),UCBPST+UCBDTR+UCBASK+UCBERR+UCBCUB US5348
         BC    7,INT013                YES- RETURN TO MAIN LINE    TPF1
         MVI   UCBLTS(UCBREG),ZERO      INDICATE DVE TO ERR RTN  BBBBBB
         BC    15,INT013                BACK TO MAIN LINE FLOW
         SPACE 3                                                   DAVV
         AIF   (&DAVV EQ 0).DAVV06                                 DAVV
*    DUMMY APPENDAGE FOR USERS WHO DO NOT REQUIRE APPENDAGES.      DAVV
         SPACE 1                                                   DAVV
SYSRTN   BCR   15,LNKRG2                RETURN ON REGISTER 14      DAVV
         SPACE 3                                                   DAVV
*    ATTENTION ROUTINE FOR DEVICES WHICH DO NOT REQUIRE ATTENTION  DAVV
*    ROUTINES.  THIS ROUTINE INITIATES THE SCHEDULING OF THE XSNT  DAVV
*    VOLUME VERIFICATION ROUTINE FOR 2311 AND 2314 UNSOLICITED     DAVV
*    DEVICE END INTERRUPTS.                                        DAVV
         SPACE 1                                                   DAVV
INTATN   TM    UCBTYP+2(UCBREG),DSKTYP  TEST FOR DIRECT ACCESS     DAVV
         BCR   8,LNKRG2                 NO, RETURN                 DAVV
         NI    UCBFL4(UCBREG),FF-UCBDAVV ZERO DAVV FLAGS        SA57136
         TM    SRTESTAT(UCBREG),SYSCON  SYSRES DEVICE              DAVV
         BCR   1,LNKRG2                 YES, RETURN                DAVV
         TM    UCBTYP+3(UCBREG),DRUM    TEST FOR DRUM            A30638
         BCR   1,LNKRG2                 YES, RETURN              A30638
         AIF   (NOT &D2321).DCEL005                               20201
         CLI   UCBTYP+3(UCBREG),UCB2321 TEST FOR 2321            BBBBBB
         BC    8,INTATN2                YES-NO TEST FOR VOL SER  BBBBBB
.DCEL005 CLI   UCBVOL1(UCBREG),ZERO     TEST IF VOL SER IN UCB   BBBBBB
         BCR   8,LNKRG2                 NO, RETURN                 DAVV
INTATN2  OI    UCBFL4(UCBREG),UCBVVR    SET DAVV RTN TO GET CTL  BBBBBB
         MVI   UCBRQESV(UCBREG),ZERO    INIT FIELD FOR DAVV RTN    DAVV
         BCR   15,LNKRG2                RETURN                     DAVV
         AGO   .DAVV07                                             DAVV
.DAVV06  ANOP                                                      DAVV
*    THIS IS THE DUMMY APPENDAGE AND ATTENTION ROUTINE WHICH IS    DAVV
*    REFERRED TO BY USERS WHO DO NOT REQUIRE APPENDAGES, AND BY    DAVV
*    DEVICES WHICH DO NOT REQUIRE ATTENTION ROUTINES.              DAVV
         SPACE 1                                                   DAVV
INTATN   DS    0H                                                  DAVV
SYSRTN   BCR   15,LNKRG2                RETURN ON REGISTER 14      DAVV
.DAVV07  EJECT                                                     DAVV
*              UNIT RECORD TRAP CODE MODULE
         SPACE 2
INTURT   DS    0C
         TM    UCBFL1(UCBREG),UCBBSY    RESTART POSSIBLE         BBBBBB
         BCR   8,LNKRG2                 YES-CONTINUE
* SELECTOR SUB CHANNELS HAVE 0C0 ADDRESSES- MULTIPLEXOR SHARED SUBCH.
*  HAVE 080 ADDRESSES. THE NEXT CODE TRIES TO CIRCUMVENT CH. RESTART
*   ON DEVICES WHICH ARE CONNECTED TO THE MULTIPLEXOR AND PRESENTS
*   CHE/DVE SEPERATELY.
         LR    WKREG1,UAREG             GET DEVICE ADDR          A47317
         SRL   WKREG1,8                 SEPARATE OFF CHAN ADDR   A47317
         SLL   WKREG1,2                 MULTIPLY CHAN BY 4       A47317
         LA    WKREG1,IECCST(WKREG1)    GET CHAN TBL ENTRY       A47317
         TM    LCTFLS(WKREG1),LCTSLBX   TEST FOR SEL OR BLK MPX  A47317
         BCR   1,LNKRG2                 YES, GO RESTART CHANNEL  A47317
         CLI   UCBUA(UCBREG),SHRDMPX    TEST FOR SHARED          A47317
         BCR   8+2,LNKRG2               YES, GO RESTART CHANNEL SA73324
         SPACE 1
*   BECAUSE OF CASCADING INTERRUPTS FROM THE MULTIPLEXOR CHANNEL AT
*   INT027, DO NOT CIRCUMVENT CHANNEL RESTART IF IMMEDIATE
*   OPERATION WAS PERFORMED AND TRAP CODE ENTERED AFTER SIO.
         SPACE 1
         TM    IOBCC(IOBREG),IOBCC3 ENTERED FRM IMMEDIATE OPERATION0588
         BCR   7,LNKRG2                 YES-TRY TO RESTART CHANNEL 0588
         IECLNK1 ,BALR,LNKRG1,LNKRG2,,,INT033D,&TBASE              TBSE
         SPACE 1
INT033D  BC    15,INT033                TO EXIT
         SPACE 2
         AIF   (&TAPE EQ 0).STIOS22
*              TAPE TRAPCODE MODULE
         SPACE 1
         AIF   (&VS EQ 0 AND &ASPN EQ 0).NOVSTAT                  21048
         AIF   (&ASPN EQ 0).NOASP                                 21048
INTASP   DS    0C                       ENTRY POINT FOR 3420      21048
.NOASP   ANOP                                                     21048
         AIF   (&VS EQ 0).NOVES                                   21048
INTTAT   DS    0C                  INTRY POINT FOR 2400 WITH VES  21048
         AIF   (&RWTAU EQ 0).NOVES     READ WRITE TAU?            21048
         TM    CSWLOC+4,CSWSMD+CSWBSY  IS CONTROL UNIT BUSY       21048
         BC    1,INTERR3A              YES-GO TEST FOR ERROR      21048
.NOVES   ANOP                                                     21048
***********************************************************************
*
*         STATISTICS UPDATE ROUTINE                               21048
*
*         USED FOR BOTH VES AND 3420                              21048
*
         TM    IOBFL1(IOBREG),IOBERR   IS ERR RTN IN CONTROL      21048
         BC    1,COMCOD                YES-DO NOT UPDATE CTR      21048
         L     WKREG1,UCBROR(0,UCBREG)  GET POINTER TO WRK AREA   21048
         LH    WKREG2,SIOCNT(0,WKREG1)  GET SIO COUNT             21048
         LA    WKREG2,ONE(0,WKREG2)     UPDATE COUNTER            21048
         STH   WKREG2,SIOCNT(0,WKREG1)   STORE COUNT              21048
***********************************************************************
         AIF   (&VS EQ 1).VES                                     21048
.NOVSTAT ANOP                                                     21048
INTTAT   DS    0C              INTRY POINT FOR 2400 W/O VES       21048
         AIF   (&RWTAU EQ 0).VES                                  21048
         TM    CSWLOC+4,CSWSMD+CSWBSY  IS CONTROL UNIT BUSY       21048
         BC    1,INTERR3A              YES-GO TEST FOR ERROR      21048
.VES     ANOP                                                     21048
COMCOD   L     WKREG1,DCBBLK(0,DCBREG)  GET BLOCK COUNT           21048
         AH    WKREG1,IOBBCI(IOBREG)     INC BLOCK COUNT          21048
         ST    WKREG1,DCBBLK(DCBREG)     STORE UPDATED BLOCK COUNT21048
         BCR   15,LNKRG2                                          21048
         SPACE 2
.STIOS22 ANOP
*              DISK TRAPCODE MODULE
         SPACE 1
INTDAT   TM    UCBFL1(UCBREG),UCBASK+UCBDTR SEEK END
         BCR   8+1,LNKRG2               NO-EXIT
         NI    UCBFL1(UCBREG),FF-UCBPST RESET POST FLAG          BBBBBB
         BC    15,INT026           TO RETURN FOR CH. S.            IMP1
         SPACE 2
         AIF   (NOT &D2305).ZU010                                 20201
*    TRAP CODE FOR 2305 DRUM                                      20201
         SPACE 1                                                  20201
INTZUT   L     WKREG1,UCBWKADB(UCBREG)  GET PTR TO UCB EXTENSION  20201
         L     WKREG1,EXPCHN+BASADR(WKREG1)  GET BASE             20201
         NI    UCBFL1(WKREG1),FF-UCBBSY MAKE IT AVAILABLE         20201
         BCR   15,LNKRG2                                          20201
.ZU010   SPACE 2                                                  20201
         AIF   (&TP EQ 0).STIOS24
*   THIS IS THE TELECOMMUNICATIONS TRAP MODULE
         SPACE 1
XCPTPT   BCR   15,LNKRG2                TO APPENDAGE INSPECTION
.STIOS24 AIF   (&GR EQ 0).GRH
         SPACE 3
*  GRAPHIC UNITS TRAP CODE
         SPACE 1
INTDNP   TM    UCBFL1(UCBREG),UCBBSY    UCBBSY DE/CE OR DE ALONE
         BCR   8,LNKRG2                 YES SKIP
         TM    UCBFL5(UCBREG),UCBNALOC  DOES OLTEP OWN DEVICE
         BCR   1,LNKRG2
         OI    IOBFL2(IOBREG),IOBSNB+IOBGRF SET CH. END ALONE
         BC    15,INT026           RETURN                          IMP2
.GRH     TITLE 'SENSE ROUTINE - INPUT/OUTPUT SUPERVISOR'
***********************************************************************
*                                                                     *
*                                                                     *
*                           SENSE SUBROUTINE
*                                                                     *
*    FUNCTION  SELECT PROPER DEVICE-DEPENDENT SENSE ENTRY POINT,      *
*              USING THE DEVICE TABLE INDEX IN THE UCB, CONSTRUCT THE *
*              APPROPRIATE CCW'S FOR EXECUTING THE SENSE OPERATION,   *
*              ISSUE THE SIO COMMAND, AND HANDLE THE RESULTING        *
*              CONDITIONS.                                            *
*                                                                     *
*    ENTRY PT  INTSEN, FROM THE INTERRUPT HANDLER AFTER UNIT CHECK    *
*              OR CHANNEL ERRORS.                                     *
*                                                                     *
*    EXITS     TO ABNORMAL END APPENDAGE IF NORMAL REQUEST            *
*              TO INT026, IF DEVICE IS BUSY, TP, OR SHARED FILE       *
*              TO INTERR, IF ERP IS IN CONTROL                        *
*              TO CCH IF CHANNEL ERRORS                               *
*                                                                     *
*    OPERATION CONSTRUCT THE DEVICE-DEPENDENT CHANNEL PROGRAM AND GO  *
*              TO THE COMMON CODE TO ISSUE SIO, AND IF NOT TP OR      *
*              RHA/R0, TIO.  HANDLE THE RESULTS AS FOLLOWS-           *
*                                                                     *
*        CSW STORED - C.C.=1 - EXAMINE STATUS                         *
*           CHANNEL ERRORS - GO TO SERR04                             *
         AIF   (&ASR EQ 0 OR &M65MP EQ 1).ASRCL05                 21111
*           CHANNEL LOGOUT PENDING CONDITION - SET DELAYED SENSE BIT  *
*                                              IN UCB-INCREMENT COUNT *
*                                              AND EXIT VIA LNKRG1    *
.ASRCL05 ANOP                                                     21111
*           UNIT CHECK - SET EQUIPMENT CHECK INDICATION IN FIRST      *
*                        TWO SENSE BYTES - X'10FE' - AND GO TO INTS09 *
*           ALL REMAINING STATUS - COPY FIRST TWO SENSE BYTES INTO    *
*           THE IOB, DEQUEUE THE RQE, AND ENTER THE ABNORMAL END      *
*           APPENDAGE. IF THE ERP IS IN CONTROL, HOWEVER, ENTER THE   *
*           ERP INTERFACE AT INTERR.                                  *
*                                                                     *
*           IF NO RQE WAS AVAILABLE, RETURN VIA LNKRG1                *
*                                                                     *
*        BUSY - C.C.=2                                                *
*              RE-ISSUE THE COMMAND                                   *
*                                                                     *
*        NON-OPERATIONAL - C.C.=3, OR C.C=0 AFTER TIO                 *
*              SET EQUIPMENT CHECK INDICATION, GO TO INTS09           *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 3                                                  20201
INTSEN   TM    UCBFL1(UCBREG),UCBBSY    IS UCB BUSY               20201
         BC    8,INTSEN3A               NO, CONTINUE WITH SENSE SA59828
         SPACE 1                                                  20201
*   SET FLAGS TO CAUSE RETURN TO SENSE ON DEVICE END              20201
         OI    IOBFL2(IOBREG),IOBSNB    SET SENSE FLAG IN IOB     20201
INTSEN2  OI    UCBCHA(UCBREG),UCBSMD   SET RETURN TO SENSE FLAG    IMP5
         SRL   UAREG,8                  ISOLATE CHANNEL ADDRESS SA59828
         AR    UAREG,UAREG              DOUBLE CHANNEL ADDR     SA59828
         STH   UCBREG,UCBSAVE(UAREG)    SET UP FOR FAST LOOKUP  SA59828
         BAL   WKREG1,IINTSENJ *        GO TEST IF CMD RETRY      ITH01
         B     IINTSEN2 *               NO, CONTINUE PROCESSING   ITH01
         L     WKREG1,UCBWKADB(0,UCBREG) * GET PTR TO EXTENSION   ITH01
         TM    RSBFLG1(WKREG1),RTRYSSS * IS RETRY CMD SK&SS       ITH01
         IECLNK1 ,BC,1,SELBSY,0,LNKRG1,,&TBASE * YES, BUSY EXIT   ITH01
IINTSEN2 DS    0H *                     CONNECT                   ITH01
         TM    UCBFL1(UCBREG),UCBPST    IS THERE AN RQE         US05348
         BO    INT026                   YES, EXIT, WAIT FOR INT US05348
         OI    UCBFL1(UCBREG),UCBBSY    IND INT EXPECTED        US05348
         BC    15,INT026                EXIT, WAIT FOR INTERRUPTSA59828
         SPACE 1                                                  20201
INTSEN3A DS    0H                                               SA61121
         AIF   (&ASR EQ 0).ASR6         CCH IN SYSTEM           SA61121
         SPACE 1                                                SA61121
*   INITIALIZE A COUNTER TO ALLOW THREE RETRIES OF THE SENSE    SA61121
*   OPERATION WHEN CHANNEL ERRORS OCCUR.                        SA61121
         LA    REG0,3                   INIT 3 INTO REG 0       SA61121
.ASR6    ANOP                                                   SA61121
         MVI   SNSCNT,ZERO              CLEAR RETRY COUNT       SA61121
INTSEN3  IC    ICREG,UCBDTI(UCBREG)     GET INDEX TO DEVICE TABLE 20201
         LH    LNKRG2,DEVTAB+DVTSEN(ICREG) GET PTR TO SENSE RTN   20201
         AIF   (&TBASE EQ 0).TBS233                               20201
         AR    LNKRG2,BASREG            ADJUST ADDRESS            20201
.TBS233  BCR   15,LNKRG2                GO TO SENSE ROUTINE       20201
         AIF   (&TP EQ 0).TP050                                   20201
         SPACE 3                                                  20201
*   SENSE ENTRY FOR TELEPROCESSING DEVICES                        20201
TPSEN    LA    LNKRG3,INTSEN2           SET RETURN ADDRESS A51138 20201
         BC    15,INTS00                JOIN COMMON CODE          20201
.TP050   SPACE 3                                                  20201
*    SENSE ENTRY FOR ALL NEW DEVICES                              20201
EXPSEN   BAL   WKREG1,IINTSENJ *        GO TEST IF CMD RETRY      ITH01
         B     IEXPSEN *                NO, GO SETUP FOR SENSE    ITH01
         L     WKREG1,UCBWKADB(0,UCBREG) * GET PTR TO EXTENSION   ITH01
         LA    WKREG2,UCBWKDCC(0,WKREG1) * POINT TO BLKMPX CP     ITH01
         L     WKREG2,SEEK2(0,WKREG2) * GET SEEK ARG ADDRESS      ITH01
         MVC   SSS1ARG(6),0(WKREG2) *   SETUP SEEK ADDRESS        ITH01
         MVC   SSS1ARG+6(1),RSBSECTR(WKREG1) SETUP SECTOR VALUE   ITH01
         L     WKREG1,RSBCAW(WKREG1) *  GET CAW FOR RETRY SIO     ITH01
         B     INTS03P *                GO DO RETRY SIO           ITH01
IEXPSEN  MVC   SYSSENSE+7(1),UCBSNS+2(UCBREG) * INIT BYTE COUNT   ITH01
         L     WKREG2,UCBSNS+2(UCBREG)  GET POINTER TO SENSE AREA 20201
         AIF   (&RESREL EQ 0 AND &TP EQ 0).TP090                  20201
         LA    LNKRG3,INTS05A           SETUP RETURN REG         A51138
.TP090   BC    15,INTS01                GO TO COMMON SENSE SUBROU 20201
         SPACE 3                                                  20201
*   ENTRY FOR MOST OTHER DEVICES, AND COMMON CODE                 20201
NORMSEN  DS    0H                       COMMON CODE               20201
         AIF   (&RESREL EQ 0 AND &TP EQ 0).TP060                  20201
         LA    LNKRG3,INTS05A           SETUP RETURN REG         A51138
.TP060   ANOP                                                     20201
INTS00   LA    WKREG2,UCBSNS(UCBREG)    PT AT UCB SENSE AREA      20201
         MVI   SYSSENSE+7,X'06'         SET BYTE COUNT IN CCW     20201
         TM    UCBTYP+2(UCBREG),DSKTYP+TAPE   TAPE OR DA DEVICE SA70856
         BC    5,INTS01                 YES, COUNT OK           SA70856
         TM    UCBTYP+2(UCBREG),GRTYPE  GRAPHICS               @SA76027
         BZ    INTSUR                   NO, INIT 2 BYTE SNS    @SA76027
         CLI   UCBTYP+3(UCBREG),X'02'   2250                   @SA76027
         BE    INTSGR                   YES, INIT 4 BYTE SNS   @SA76027
         CLI   UCBTYP+3(UCBREG),X'03'   2260                   @SA76027
         BNE   INTSUR                   NO, INIT 2 BYTE SNS    @SA76027
INTSGR   MVI   SYSSENSE+7,X'04'         SET BYTE COUNT IN CCW   SA70856
         B     INTS01                   CONTINUE                SA70856
INTSUR   MVI   SYSSENSE+7,X'02'         SET BYTE COUNT IN CCW   SA70856
INTS01   LA    WKREG1,SYSSENSE          FORM COMD ADDR FOR SIO    20201
INTS02   ST    WKREG2,0(WKREG1)         SET SENSE DATA ADDR IN CCW20201
         MVI   0(WKREG1),SENSOP         RESTORE CCW OP CODE       20201
INTS03   MVI   1(WKREG2),ZERO           CLEAR SECOND SENSE BYTE   20201
INTS03P  DS    0H
         ST    WKREG1,CAWLOC      STORE AND ZERO PROTECTION KEY   20201
         MVI   CATAPP,CATSNS .          SET CCH CODE FOR SIO SENSE  CCH
         LA    LNKRG2,INTS04            SET RETURN REGISTER     SA68848
         L     WKREG1,LPCNT             GET CHAN BUSY LIMIT     SA68848
         LR    LCHREG,WKREG1            GET DEV/CU BUSY LIMIT
         TM    UCBTYP+2(UCBREG),DSKTYP+TAPE   TAPE OR DA DEVICE
         BCR   5,LNKRG2                 YES, LEAVE HIGH LIMIT
         SRL   LCHREG,6                 LOWER LIMIT
INTS04   DS    0H                                                 20201
         SIO   0(UAREG)                 START SENSE OPERATION      TEST
         BC    4,INTS07C                CSW STORED-GO INSPECT   SA57297
         BC    2,INTS20                 DECREMENT LOOP COUNT    SA68848
         BC    1,INTS10                 EQP. MALFUNCTION          20201
         BAL   WKREG1,IINTSENJ *        GO TEST IF CMD RETRY      ITH01
         B     IINTS04 *                NO, PROCEED WITH SENSE OP ITH01
         L     WKREG1,UCBWKADB(0,UCBREG) * GET PTR TO EXTENSION   ITH01
         MVC   IOBCSW+1(7,IOBREG),RSBCSW+1(WKREG1) RESTORE IOBCSW ITH01
         TM    RSBFLG1(WKREG1),RTRYSSS * IS RETRY CMD SK&SS       ITH01
         BZ    IINTS04J *               NO, GO RESET BUSY         ITH01
         IECLNK1 ,LA,LNKRG1,IINT037,,,,&TBASE * SETUP RETURN      ITH01
         BAL   LNKRG3,XCP064 *          GO LOAD LCHREG            ITH01
         LA    REG0,IINT037 *           SETUP RETURN FOR PSIO     ITH01
         B     XCP162 *                 GO TO SK&SS TIO           ITH01
IINTS04J OI    UCBFL1(UCBREG),UCBBSY *  RESET BUSY                ITH01
         B     INT026 *                 GO TO CHANNEL SEARCH      ITH01
IINTS04  DS    0H *                     CONNECT                   ITH01
         AIF   (&RESREL EQ 0 AND &TP EQ 0).TP070                  20201
         BCR   15,LNKRG3                RETURN OR CONTINUE       A51138
.TP070   SPACE 1                                                  20201
INTS05A  LA    LNKRG2,INTS05            SET RETURN REGISTER     SA57297
         MVI   CATAPP,CATTIOSN .        SET CCH CODE FOR TIO SENSE  CCH
         L     WKREG1,LPCNT             GET CHAN BUSY LIMIT     SA68848
         LR    LCHREG,WKREG1            GET DEV/CU BUSY LIMIT
         TM    UCBTYP+2(UCBREG),DSKTYP+TAPE   TAPE OR DA DEVICE
         BCR   5,LNKRG2                 YES, LEAVE HIGH LIMIT
         SRL   LCHREG,6                 LOWER LIMIT
INTS05   DS    0H                                                 20201
         TIO   0(UAREG)                                            TEST
         BC    2,INTS20                 DECREMENT LOOP COUNT
         BC    8+1,INTS10               EQP. MALFUNCTION          20201
         BC    15,INTS07C               GO CHECK STATUS         SA57297
         SPACE 1
INTS07   LA    LNKRG2,INTSEN3           SET RETURN REGISTER     SA61121
         MVI   SNSCNT,ZERO              CLEAR RETRY COUNT       SA61121
         L     WKREG1,LPCNT             GET CHAN BUSY LIMIT     US05348
         LR    LCHREG,WKREG1            GET DEV/CU BUSY LIMIT   US05348
         AIF   (&ASR EQ 0).ASR47                                SA61121
         LA    REG0,3                   INIT 3 INTO REG 0       SA57297
         SPACE 1                                                SA57297
*    TEST CSW STATUS AND GO TO CCH IF CHANNEL OR INTERFACE CNTRL CHECK.
INTS07C  DS    0H .                                             SA57297
         AIF   (&M65MP EQ 1).ASRCL06    CHANNEL LOGOUT PENDING    21111
         TM    CSWLOC,CSWCLP            CHANNEL LOGOUT PENDING    21111
         BC    7,INTSLOG                YES-GO HANDLE LOGOUT PEND 21111
.ASRCL06 ANOP                                                     21111
         TM    CSWLOC+5,CSWCTK+CSWICK+CATCCH . CAT. ERRORS        20201
         L     APBSRG,IECPL2            GET TEMP BASE REG      @SA77644
         BC    7,SERR04-IECCPL02(APBSRG) GO TO CCH             @SA77644
         TM    CSWLOC+5,CSWCDK+CSWCCK   CHAN DATA & CHAN CHG CHK  19062
         BC    7,INTSEN3                YES, RE-TRY SENSE       SA61121
         AGO   .ASR83                                            20201
.ASR47   SPACE 1                                                  20201
*    CSW STORED TEST STATUS - TIO & SIO COMMON ROUTINES.          20201
INTS07C  TM    CSWLOC+5,CSWCDK+CSWCTK+CSWICK+CSWCCK CAT. ERRORS SA57297
         L     APBSRG,IECPL2            GET TEMP BASE REG      @SA77644
         BC    7,SERR04-IECCPL02(APBSRG) YES, GO TO SERR       @SA77644
.ASR83   ST    WKREG1,ITSAVWK1 *        SAVE WKREG1               ITH01
         BAL   WKREG1,IINTSENJ *        GO TEST IF CMD RETRY      ITH01
         B     IINTS07C *               NO, PROCEED WITH SENSE OP ITH01
         L     WKREG1,ITSAVWK1 *        RESTORE WKREG1            ITH01
         TM    CSWLOC+4,CSWUCK+CSWCHE * IS IT CHE OR UNIT CHK?    ITH01
         BZ    INTS15 *                 NO, RETRY THE SIO         ITH01
         L     WKREG1,UCBWKADB(0,UCBREG) * GET PTR TO EXTENSION   ITH01
         TM    RSBFLG1(WKREG1),RTRYFE * WAS RETRY CMD NOT FOUND?  ITH01
         BO    IINTS07J *               YES, GO SIMULATE ADDR     ITH01
         L     LNKRG2,CAWLOC *          GET CAW                   ITH01
         MVC   CSWLOC+6(2),6(LNKRG2) *  SIMULATE RESIDUAL COUNT   ITH01
         AL    LNKRG2,EIGHTF *          SIMULATE CSW ADDRESS      ITH01
         ST    LNKRG2,CSWLOC *          STORE                     ITH01
IINTS07K MVC   IOBCSW+1(7,IOBREG),RSBCSW+1(WKREG1) RESTORE IOBCSW ITH01
         TM    RSBFLG1(WKREG1),RTRYSSS * IS RETRY CMD SK&SS       ITH01
         BZ    INT008 *                 NO, GO EXAMINE STATUS     ITH01
         IECLNK1 ,LA,LNKRG1,IINT037,,,,&TBASE * SETUP RETURN      ITH01
         BAL   LNKRG3,XCP064 *          GO LOAD LCHREG            ITH01
         LA    REG0,IINT037 *           SETUP RETURN FROM PSIO    ITH01
         B     IXCP162A *               GO TO POST TIO INSPECTION ITH01
IINTS07J MVC   CSWLOC+1(3),IOBCSW+1(IOBREG) * SIMULATE CSW ADDR   ITH01
         B     IINTS07K *               GO CLEANUP AND EXIT       ITH01
IINTS07C L     WKREG1,ITSAVWK1 *        RESTORE WKREG1            ITH01
         TM    CSWLOC+4,CSWUCK *        UNIT CHECK ON SENSE       ITH01
         BC    1,INTS10                 YES, SET UP SENSE ERROR  A53284
INTS08   TM    CSWLOC+4,CSWBSY          BUSY STATUS              A53284
         BC    1,INTS15                 YES, RETRY THE SIO/TIO  SA57297
         NI    UCBFL5(UCBREG),FF-UCBUEX CLEAR UNIT EXEC STATUS   S22024
         TM    CSWLOC+4,CSWUEX          TEST FOR UNIT EXCEPTION  S22024
         BC    8,INTS09                 NO, DO NOT SET UEX FLAG  S22024
         OI    UCBFL5(UCBREG),UCBUEX    INDICATE UEX ON SENSE    S22024
INTS09   TM    UCBFL1(UCBREG),UCBPST    DE WITHOUT CE             20201
         IECLNK1 ,BCR,8,LNKRG1,,,,&TBASE
INTS02H  MVC   IOBSNS(2,IOBREG),ZERO(WKREG2) SAVE SENSE DATA      20201
         SPACE 1                                                  20201
*  ON UNIT CHECK ON DE FROM A STANDALONE SEEK THE RQE MUST BE DEQ.
*   BEFORE IT IS SENT TO THE ERP.
INTS02G  BAL   LNKRG2,XCPPDQ           GO DEQUEUE REQUEST          IMP3
INT031   NI    UCBFL1(UCBREG),UCBBSY    RESET ALL FLAGS BUT BUSY  20201
INT031B  LA    APBSRG,APGABE            ABNORMAL END APD. INCR.   20201
         BC    15,INT019                GO TO APPENDAGE INTERFACE 20201
         SPACE 3                                                  20201
*    RETRY FAILING SENSE TEN TIMES                               A53284
INTS10   IC    ICREG,SNSCNT             LOAD UC COUNTER          A53284
         LA    ICREG,1(ICREG)           INCREMENT COUNT          A53284
         STC   ICREG,SNSCNT             STORE UC COUNTER         A53284
         CLI   SNSCNT,TEN               TEST FOR TEN RETRIES     A53284
         BC    13,INTSEN3               NO, GO TRY AGAIN        SA61121
*    ON EQUIPMENT CHECK, SET FIRST TWO SENSE BYTES TO X'10FE'    A53284
INTS10A  MVI   ZERO(WKREG2),X'10'       YES-SET EQUIPMENT CHECK  A53284
         MVI   1(WKREG2),X'FE'          SET IND IN SECOND BYTE   A53284
INTS10B  LTR   TSTREG,TSTREG            DO WE HAVE AN RQE        A53284
         IECLNK1  ,BCR,8,LNKRG1,,,,&TBASE                        A53284
         CH    TSTREG,LTSVALSV          IS THE RQE VALID         A53284
         IECLNK1  ,BCR,7,LNKRG1,,,,&TBASE                        A53284
         STH   TSTREG,UCBLTS(UCBREG)    RESTORE RQE ADDRESS     SA60254
         MVI   CHMSK,RSTMSK             RESTART ALL CHANNELS    SA60254
         IECLNK1  ,LA,LNKRG1,INT027,,,,&TBASE                   SA60254
         BC    15,INTS09                GO POST REQUEST IN ERR  SA71481
         SPACE 1
*    THE FOLLOWING ROUTINE IS AN ATTEMPT TO KEEP THE SYSTEM
*    UP IF A DEVICE RESPONDS WITH UNIT CHECK AND THEN DEVICE
*    OR CONTROL UNIT BUSY WHEN PERFORMING A SENSE OPERATION.
*    THE SENSE WILL BE DUMMIED AS '10FC' (SIMILAR TO '10FE'
*    WHEN A UNIT CHECK IS RECEIVED PERFORMING A SENSE
*    OPERATION.)
         SPACE 1
INTS15   BCTR  LCHREG,LNKRG2            RE-SIO/TIO TIL CNT LIMIT
         TM    UCBTYP+2(UCBREG),DSKTYP+TAPE   TAPE OR DA
         BC    4,INTSLOG                AVOID CONTINGENT CONNEC
         MVI   ZERO(WKREG2),X'10'       DEV HUNG IN BUSY LOOP
         MVI   1(WKREG2),X'FC'          SET IND IN SENSE BYTES
         BC    15,INTS10B               HANDLE BSY LIKE UNIT CHK
         SPACE 1
*    THE FOLLOWING ROUTINE, UTILIZING THE LOGOUT PENDING ROUTINE,
*    LIMITS THE CHANNEL BUSY LOOP OF THE SENSE SUBROUTINE.  WHEN
*    THE LIMIT IS REACHED, THE CHANNEL LOGOUT PENDING ROUTINE IS
*    ENTERED.  THE CLP ROUTINE ALLOWS THE SYSTEM TO ENABLE, AFTER
*    WHICH THE SIO/TIO MAY BE RESUMED.  ALLOWING THE SYSTEM TO  SA68848
*    ENABLE MAY ALLOW THE SENSE OPERATION TO TERMINATE.         SA68848
         SPACE 1
INTS20   BCTR  WKREG1,LNKRG2            REISSUE SIO/TIO TIL CNT SA68848
         CLI   CATAPP,CATTIOSN          TIO IN PROC             SA71483
         BE    INTSEN2                  YES, CLEAR STAT VIA INT SA71483
*        B     INTSLOG                  ENABLE CPU BEFORE CONT  SA68848
         SPACE 2                                                  21111
*   ON CHANNEL LOGOUT PENDING CONDITION MARK UCB FOR DELAYED SENSE
*   INCREMENT DELAYED SENSE COUNT-IF EXCP ENTRANCE SET CHMSK TO FORCE
*   FRONT END ENTRANCE TO IOS TO GO TO CHANNEL RESTART WHERE IOS WILL
*   EVENTUALLY ENABLE ALLOWING CHANNEL TO LOGOUT.               SA62867
*   IF LOG PENDING STATUS WAS PRESENTED ON THE SIO FOR SENSE,   SA62867
*   SET BOTH DELAYED SENSE AND DELAYED HIO FLAGS TO INDICATE    SA62867
*   THE SENSE MAY BE RETRIED.                                   SA62867
         SPACE 1                                                SA62867
INTSLOG  DS    0H .                                               21111
         AIF   (NOT &D2305).ASRCL09                             SA62867
         LR    WKREG1,UCBREG .          SAVE UCBREG             SA62867
         L     WKREG2,AZUSRTN .         GET ADDR OF 2305 RTN    SA62867
         BALR  LNKRG3,WKREG2 .          GO GET BASE IF 2305     SA62867
         BC    7,INTSLG1 .              NOT 2305, CONTINUE      SA62867
         LR    WKREG2,UAREG .           COPY UAREG              SA62867
         N     WKREG2,ZEXPMSK .         CLEAR ALL BUT EXP BITS  SA62867
         SLL   WKREG2,6 .               GET OFFSET TO 2305 EXP  SA62867
         LA    UCBREG,0(WKREG2,UCBREG) .GET EXPOSURE UCB        SA62867
         CR    UCBREG,WKREG1 .          SAME UCB NOW            SA62867
         BC    8,INTSLG1 .              YEP, CONTINUE           SA62867
         STH   UCBREG,TSTUCB(TSTREG) .  SETUP RQE IN UCB        SA62867
         MVC   UCBFL1(1,UCBREG),UCBFL1(WKREG1) .MOVE UCBFL1     SA62867
         MVI   UCBFL1(WKREG1),ZERO .    CLEAR BASE UCBFL1       SA62867
.ASRCL09 ANOP                                                   SA62867
INTSLG1  OI    UCBCHA(UCBREG),UCBDSNS+UCBSMD WAIT FOR DELAY SENSE 21111
         CLI   CATAPP,CATSNS .          WAS LOG PENDING ON SIO  SA62867
         BC    7,INTSLG3 .              NO, DON'T INDICATE SIO  SA62867
         OI    UCBCHA(UCBREG),UCBDHIO . SIO MAY BE RETRIED      SA62867
INTSLG3  IC    ICREG,CLPCNT .           LOAD DELAY EVENT CNTR   SA62867
         LA    ICREG,ONE(ICREG) .       INCREMENT BY ONE          21111
         STC   ICREG,CLPCNT .           STORE COUNTER             21111
         TM    XCPBTE,XCPCNT+XCPERR .   EXCP ENTRANCE           SA62867
         IECLNK1 ,BCR,8+1,LNKRG1,,,,&TBASE                      SA62867
         MVI   CHMSK,RSTMSK .           SET FOR DISABLE EXCP USER 21111
         IECLNK1 ,BCR,15,LNKRG1,,,,&TBASE                       SA62867
         AIF   (&RESREL EQ 0).RR015                               20201
  TITLE 'SHARED FILE SUPPORT FOR SENSE - INPUT/OUTPUT SUPERVISOR'
*                  SHARED FILE SUPPORT FOR SENSE                  20201
*                                                                 20201
*    A RHA/RR0 CHANNEL PROGRAM IS CHAINED TO THE SENSE CHANNEL    20201
*    PROGRAM SO THAT THE HA AND R0 ARE AVAILABLE TO THE ERP       20201
*    DURING ERROR RECOVERY. FURTHERMORE, IF THE RESERVE COUNTER   20201
*    IS ZERO, THE SENSE COMMAND IS CHANGED TO A RELEASE, SO THAT  20201
*    THE DEVICE IS AVAILABLE TO THE OTHER CPU DURING THIS CPU'S   20201
*    ERROR RECOVERY PROCESS. THIS SUPPORT IS USED ONLY FOR        20201
*    2311S, 2314S AND 2321S (STAND SEEK DEVICES).
         SPACE 2                                                  20201
SDASEN   TM    UCBTYP+1(UCBREG),UCBRR   RES/REL CAPABILITY        20201
         BC    8,NORMSEN                NO, GO PREPARE FOR SENSE  20201
         TM    UCBFL1(UCBREG),UCBPST    IS THERE AN RQE TO USE    20201
         BC    8,NORMSEN                NO-BYPASS RHA/RR0         20201
         L     APBSRG,CCWRTNAD          GET CCW RTN ADDR
         BALR  WKREG1,APBSRG            GO GET CCW ADDR
         AIF   (&SSDA EQ 0 AND &SSTA EQ 0).MPX025
         BC    0,0                      NOP FILLER
.MPX025  ANOP
*    INITIALIZE SENSE/RELEASE, RHA AND RR0 CHANNEL PROGRAM
INTRR04  LR    WKREG1,WKREG3           PREPARE FOR CAW STORE       SDA1
         MVC   0(16,WKREG1),RRSNCCW     MOVE SKELETON TO CH AREA  20201
         SH    WKREG1,EIGHT            BACK UP TO SENSE           20201
INTRR005 MVI   CCWFLG(WKREG1),CCWCC+CCWSLI     SET  CCW FLAGS     20201
         AIF   (NOT &D2321).DCEL003                               20201
         L     WKREG2,UCBWKADA(0,UCBREG) GET PTR TO 2321 WORK AREA20201
         CLI   UCBTYP+3(UCBREG),UCB2321                            SDA1
         BC    8,INTRR01               DEVICE IS 2321              SDA1
.DCEL003 L     WKREG2,UCBWKADB(0,UCBREG) GET PTR TO ERP WKAREA    20201
INTRR01  LA    WKREG2,UCBRR0A(0,WKREG2) GET READ R0 OFFSET        20201
         ST    WKREG2,RR0ADR(WKREG1)   SET RR0 ADDRESS           A53742
         MVI   RR0ADR(WKREG1),RR0OP    SET UP READ R0 OP CODE    A53742
         LA    WKREG2,UCBRHA(WKREG2)   INCRE TO RHA AREA          20201
         ST    WKREG2,RHADR(WKREG1)    SET RHA ADR               A53742
         MVI   RHADR(WKREG1),HAOP       SET UP READ HA OP CODE   A53742
         MVC   1(4,WKREG2),UCBSKA+3(UCBREG) GET CCHH FOR ERROR    20201
         LA    WKREG2,UCBSNS(UCBREG)    GET ADDRESS OF UCB SENSE  20201
         MVI   7(WKREG1),X'06'          SET COUNT                 20201
         STH   WKREG2,2(WKREG1)         SET SENSE AREA ADDR IN CCWM5738
         MVI   0(WKREG1),SENSOP         SET CCW OP CODE TO SENSE A49373
         CLI   UCBRRCNT(UCBREG),ZERO    IS COUNTER ZERO          A49373
         BC    7,INTRR02                NO, DO NOT RELEASE       A49373
         MVI   0(WKREG1),RELEASE        CHANGE SENSE TO RELEASE  A49373
INTRR02  BAL   LNKRG3,INTS03            GO DO SENSE OPERATION    A51138
         BC    15,INTSEN2               GO TO WAIT FOR INTERRUPT  20201
.RR015   SPACE 3                                                  20201
         AIF   (&TP EQ 0 AND &RESREL EQ 0).RR010                  20201
         SPACE 4                                                  20201
*   STATUS MODIFIER ENTRANCE                                      20201
INTSEN4  LA    WKREG2,UCBSNS(UCBREG)    SETUP SENSE DATA PTR      20201
         TM    UCBFL5(UCBREG),UCBEXTSN  EXP SENSE DEVICE ?     @SA77172
         BZ    INTSEN4B                 NO, USE NORMAL SENSE   @SA77172
         L     WKREG2,UCBNBRSN(UCBREG)  GET PTR TO EXT. SENSE  @SA77172
INTSEN4B TM    UCBCHA(UCBREG),UCBDSNS . WAITING FOR DLY SENSE  @SA77172
         BO    INTSEN6 .                YES SETUP FOR STATUS CHECK21111
         AIF   (&TWOSW EQ 0).TW31300                             A31305
         LH    WKREG1,UCBCHA(UCBREG)    GET DEVICE ADDRESS       A31305
         N     WKREG1,ADDRMSK           CLEAR FLAGS              A31305
         CR    WKREG1,UAREG             EQUAL TO PATH STARTED    A31305
         BC    7,INT027                 NO, TRY TO RESTART CHANNEL31305
.TW31300 ANOP                                                    A31305
         NI    UCBCHA(UCBREG),FF-UCBSMD RESET PENDING SENSE     US05348
         AIF   (&ASR EQ 0).ASR85                                  SDA1
         MVI   CATAPP,CATREC          INDICATE SECOND ENTRY TO CCH IMP2
.ASR85   AIF   (&RESREL EQ 0).RR031                                SDA1
         TM    UCBTYP+2(UCBREG),DSKTYP  IS IT A DA DEVICE       SA71483
         BC    8,INTS07                 NO, GO INSPECT STATUS   SA71483
         TM    UCBTYP+1(UCBREG),UCBRR   IS IT A SHARED DEVICE   SA71483
         BC    8,INTS07                 NO, GO INSPECT STATUS   SA71483
         TM    UCBTYP+2(UCBREG),UCBRPS  IS IT AN RPS DEVICE     SA71483
         BC    1,INTS07                 YES, GO INSPECT STATUS  SA71483
         TM    CSWLOC+5,CSWCDK+CSWCTK+CSWICK+CSWCCK CAT. ERRORS SA56509
         BC    7,INTS07                 YES, GO SEE WHICH ERROR   20201
         TM    CSWLOC+4,CSWUCK          TEST FOR UNIT CHECK       20201
         BC    1,INTSEN4A .             YES, CHECK COMMAND       A36733
         MVC   UCBDVRES(1,UCBREG),UCBRRCNT(UCBREG) . IND DEV RES A36733
         B     INTS07 .                 NO UCK, CHECK OTHER STATUS05348
*    TEST IF UCK ON SENSE OR R-HA OR R-R0 COMMANDS.               20201
INTSEN4A L     APBSRG,CCWRTNAD          GET CCW RTN ADDR
         BALR  WKREG1,APBSRG            GO GET CCW ADDR
         AIF   (&SSDA EQ 0 AND &SSTA EQ 0).MPX025A
         BC    0,0                      NOP FILLER
.MPX025A ANOP
         L     WKREG1,CSWLOC            GET ADDR. OF CCW          20201
         LA    WKREG1,0(WKREG1)         GET ADDR. ONLY            20201
         SR    WKREG1,WKREG3            UNIT CHECK ON SENSE       20201
         LA    WKREG2,UCBSNS(UCBREG)    GET ADDR OF UCB SENSE     M5030
         BC    8,INTS10A                YES, HANDLE NORMAL      US05348
*    WHEN UCK ON HA OR R0, TEST IF THE SENSE IS TRACK COND. CHECK 20201
*    IF YES, SIMULATE THIS COND. WITH A D/C FOR POSSIBLE RECOVERY.20201
         TM    UCBSNS(UCBREG),X'02'     TRACK CONDITION CHECK     20201
         BC    8,INTSEN5                NO-AROUND                 20201
         OI    UCBSNS(UCBREG),X'08'     YES SET D/C               20201
INTSEN5  LA    WKREG1,RRSENSE2          ADDR. OF SENSE CCW        20201
         LA    LNKRG3,INTSEN2           SET FOR RETURN           A51138
         B     INTS03P                  GO SENSE
         AGO   .RR010                                             20201
.RR031   BC    15,INTS07                GO INSPECT STATUS         20201
.RR010   ANOP                                                   SA68848
         SPACE 2
*   ROUTINE HANDLES INTERRUPTS THAT ARE REALLY ENDING STATUS OF   21111
*   SENSE OPERATIONS BUT CAME AS INTERRUPT DUE TO LOG PENDING COND21111
INTSEN6  NI    UCBCHA(UCBREG),FF-UCBDSNS-UCBSMD PEND/DELAY SNS OFF21111
         TM    UCBCHA(UCBREG),UCBDHIO . WAS LOG PENDING ON SIO  SA62867
         BC    8,INTSENLT .             NO, GO HANDLE FOR TIO   SA62867
         NI    UCBCHA(UCBREG),FF-UCBDHIO .RESET SIO SWITCH      SA62867
         BC    15,INTSEN3A .            GO RETRY SENSE SIO      SA62867
INTSENLT LA    LNKRG2,INTS10A .         RETURN ADDR IF CHAN ERR SA62867
         TM    CSWLOC+5,CSWCTK+CSWICK+CATCCH .GONE TO CCH YET   SA62867
         BC    7,INTSEN6B .             YES, DON'T RESET RETURN SA62867
INTSEN6A LA    LNKRG2,INTS07 .          SET NORMAL RETURN       SA62867
INTSEN6B LA    WKREG2,UCBSNS(UCBREG) .  NORMAL SENSE POINTER      M0192
         TM    UCBFL5(UCBREG),UCBEXTSN .EXPANDED SENSE DEVICE     21111
         BCR   8,LNKRG2 .               INSPECT STATUS          SA62867
         L     WKREG2,UCBNBRSN(UCBREG) .GET PTR TO EXPAND SENSE   21111
         BCR   15,LNKRG2 .              INSPECT STATUS          SA62867
IINTSENJ CLC   UCBTYP+2(2,UCBREG),IT7330 * IS IT 7330?            ITH01
         BCR   7,WKREG1 *               NO, SENSE RETURN          ITH01
         TM    UCBFL1(UCBREG),UCBPST *  IS THERE AN RQE?          ITH01
         BCR   14,WKREG1 *              NO, SENSE RETURN          ITH01
         TM    IOBCSW+4(IOBREG),CSWRETRY * IS IT CMD RETRY STATUS?ITH01
         BCR   14,WKREG1 *              NO, SENSE RETURN          ITH01
         B     4(WKREG1) *              COMMAND RETRY RETURN      ITH01
         TITLE 'ERROR INTERFACES - INPUT/OUTPUT SUPERVISOR'
*        IOS INTERRUPT SUPERVISOR ERROR INTERFACE
         SPACE 2
*   FUNCTION   TO SCRUTINIZE ERRORS AND SCHEDULE PROPER               *
*              HANDLING ROUTINE                                       *
*   ENTRY      MAIN LINE OF IOS, 2311(DISK) RESIDENT ERROR ROUTINE    *
*   EXT REF    XTFKTR, ER2311                                         *
*   EXITS      IOS MAIN LINE, IOS EXCP, 2311 RESIDENT ERP             *
*
*   OPERATION       1. RETURN TO POST ROUTINE ON PERMANENT ERROR.
*                   2. CHECK IF IOS OR USER ERROR ROUTINES ARE USED
*                      (ALL OR SOME) - USER 'ALL' AND 'SOME', RETURN
*                      VIA EXIT EFFECTOR.
*                   3. CHECK MASK IF 'SOME' ERRORS CHECKED.
*                   4. ON 'ALL' ERRORS (IOS) - GO TO 2311 ERROR
*                      ROUTINE FOR DIRECT ACCESS - PASS RQE TO EXIT
*                      EFFECTOR FOR ALL OTHERS.
*                   5. IF 'NO' ERRORS CHECKED (IOS AND USER) - GO TO
*                      ABEOT IF PGM OR PROT CHECK.
*                   6. PURGE IF RELATED REQ HAS A PERMANENT ERROR.
         SPACE 2
         AIF   (&RWTAU EQ 0).RW150
INTERR3A NI    UCBFL1(UCBREG),FF-UCBPST ERP TO RESTART CCW       BBBBBB
.RW150   ANOP                                                    BBBBBB
INTERR   TM    IOBCOD(IOBREG),NOTPMF    PERMANENT ERROR
         BC    8,INT024                 YES - TO POST
INTERR3  NI    DCBFL(DCBREG),FF-DCBPER  RESET ERROR FLAG         BBBBBB
INT050   DS    0H                                                  DAVV
         AIF   (&DAVV EQ 0).DAVV08                                 DAVV
         TM    UCBTYP+2(UCBREG),DSKTYP  TEST FOR DIRECT ACCESS     DAVV
         BC    8,INT050A                NO, SKIP                   DAVV
         TM    UCBFL4(UCBREG),UCBVVR    TEST IF VOL VER RTN IN CTL DAVV
         AIF   (&DDR NE 0).DDR01                                SA68176
         BC    1,INTERR4A          YES, GO SCHEDULE RTN            DAVV
         AGO   .DAVV08                                          SA68176
.DDR01   ANOP                                                   SA68176
         BC    8,INT050A                NO, SKIP                SA68176
         C     TSTREG,ADDRRQE           IS THIS THE DDR RQE?    SA68176
         BC    7,INTERR4A               NO, GO TO SCHEDULE RTN  SA68176
.DAVV08  ANOP                                                      DAVV
INT050A  TM    DCBFL(DCBREG),DCBIEK     TEST FOR IOS ERROR KEY     DAVV
         BC    8,INT022                 USE IOS ERPS FOR ERRORS  BBBBBB
         AIF   (&TWOSW EQ 0).TWS160                              BBBBBB
         TM    IOBCC(IOBREG),IOBCC3     TEST IF CC=3 ON SIO      BBBBBB
         BC    1,INTERR4D               TO SCHEDULE PURGE        BBBBBB
.TWS160  ANOP                                                    BBBBBB
INT057   TM    IOBCOD(IOBREG),NOTPMF    PERMANENT ERROR          BBBBBB
         BC    8,INT058                 YES - BRANCH
         CLI   IOBCOD(IOBREG),IOBINT    INTERCEPTED IOB
         MVI   IOBCOD(IOBREG),INTCOD    PUT IN INTERCEPT CODE
         BC    8,INT058                 YES, LEAVE INTERCEPT CODE
         MVI   IOBCOD(IOBREG),PERMER   SET PERM ERROR CODE         IMP2
INT058   TM    IOBFL1(IOBREG),IOBURL    IS THE REQUEST UNRELATED
         BC    1,INT031B                YES, GO TO ABN APN       BBBBBB
         TM    DCBFL(DCBREG),DCBPER    PERM ERR SET BY PREV REQ    TPF1
         BC    1,INT031B                YES, GO TO ABN APN       BBBBBB
         NI    DCBFL(DCBREG),FF-DCBPER  RESET ERROR FLAG         BBBBBB
INTERR4D OI    DCBFL(DCBREG),DCBEX      SET DCB EXCEPTION FLAG   BBBBBB
         SPACE 1                                                   IMP3
*   FORCE EXIT EFFECTOR TO USE UCB ID BYTE TO SCHEDULE IGE0025E.   IMP3
         SPACE 1                                                   IMP3
INTERR4  LR    WKREG1,UCBREG            GET UCB ADDRESS            DAVV
         SH    WKREG1,SIX              POINT TO UCB ID BYTE        IMP3
         STH   WKREG1,TSTUCB(0,TSTREG)      IN THE RQE             IMP3
*  HANG RQE ON ERROR LCH. FOR TRANSIENT ERP AND WTO FUNCTION
INT059   DS    0H                                                 19021
         AIF   (&RMS EQ 0).RMS06                                  19021
         L     WKREG1,RMSFLAD           ADDR OF RMS FLAG         BBBBBB
         CH    TSTREG,TWO(WKREG1)       TEST IF RMS ERROR        BBBBBB
         BC    8,INT057                 YES, GO TO SET PERM ERR   19021
.RMS06   L     WKREG1,XITFTR            EXIT EFFECTOR EXTERN      19021
         OI    TSTUCB+1(TSTREG),ONE     INDICATE SIRB REQUEST    BBBBBB
         AIF   (&DDR EQ 0).DDRAEQ7                              SA58100
         C     TSTREG,ADDRRQE           IS THIS THE DDR RQE     SA58100
         BC    7,INT059T                NO, DONT SET UCBERR     SA58100
         OI    UCBFL1(UCBREG),UCBERR    TIE UCB TO DDR RQE      SA58100
         STH   TSTREG,UCBLTS(UCBREG)    TIE DDR RQE TO UCB      SA63107
.DDRAEQ7 ANOP                                                   SA58100
INT059T  LA    LNKRG2,INT026            SET RETURN ADDR         SA58100
         BCR   15,WKREG1                TO ENQ. ON ERROR LCH
         SPACE 1
*    DEFINITION OF EXIT EFFECTOR - IOS INTERFACE                 BBBBBB
*    INPUT     TSTREG,WKREG1(BASE ADDR),LNKRG2(RET-INT026)       BBBBBB
*    REGS CHANGED  NONE                                          BBBBBB
         SPACE 1                                                 BBBBBB
         AIF   (&DAVV EQ 0).DAVV09                                 DAVV
         SPACE 1                                                   DAVV
INTERR4B MVI   UCBRQESV(UCBREG),ZERO    RQE PURGED-RE-INIT FIELD   DAVV
INTERR4A BAL   LNKRG2,XCPPDQ            GO TO DEQUEUE RQE          DAVV
         STH   TSTREG,UCBLTS(0,UCBREG)  INIT UCBLTS FIELD          DAVV
         OI    UCBFL1(UCBREG),UCBERR    RESERVE UCB FOR THIS RQE   DAVV
         BC    15,INTERR4               GO TO SCHEDULE RTN         DAVV
.DAVV09  SPACE 1                                                   DAVV
INTERR5  MVC   IOBCSW+4(2,IOBREG),UCBLTS(UCBREG) COPY CSW STATUS   IMP5
         STH   TSTREG,UCBLTS(UCBREG)    RESTORE RQE TO UCB         IMP5
         MVI   IOBCOD(IOBREG),IOBINT    SET INTERCEPT              IMP5
         LA    WKREG2,UCBSNS(UCBREG)    INITIALIZE FOR SENSE MOVE 20201
         TM    UCBFL5(UCBREG),UCBEXTSN  THIS UCB HAVE PTR TO SENSE20201
         BC    8,INTS02H                NO, GO SAVE SENSE INFO    20201
         L     WKREG2,UCBSNADR-1(UCBREG) GET PTR TO SENSE AREA    20201
         BC    15,INTS02H               DEQUE RQST, SEND TO ERP    IMP5
         SPACE 1
*   SEND REQUEST TO IOS ERROR ROUTINE
INT022   TM    IOBFL1(IOBREG),IOBURL   IS REQUEST UNRELATED
         BC    1,INT055                YES- DO NOT SET FLAG
         OI    DCBFL(DCBREG),DCBEX     SET ERR ROUT IN CONTROL
INT055   TM    UCBTYP+2(UCBREG),DSKTYP  TEST FOR DIRECT ACCESS     TRCE
         BC    8,INT059                 NO-SKIP                    TRCE
         L     APBSRG,T311ERT           GET DISK ERROR ROUTINE ADDRESS
         BALR  LNKRG2,APBSRG            TO 2311 ERROR RTN
         SPACE 1
*    DEFINITION OF RESIDENT 2311 ERP - IOS INTERFACE             BBBBBB
*    INPUT     LNKRG2(RET ADDR),APBSRG(BASE ADDR)                BBBBBB
*    REGS CHANGED  NONE                                          BBBBBB
         SPACE 1
***********************************************************************
*                                                                     *
*                  2311 ERROR ROUTINE VECTOR TABLE                    *
*                                                                     *
         BC    15,INTER7                TO EXCP INSPECT
         BC    15,INTER8                CORRECTION PERM ERROR
         BC    15,INTER10               HANG REQUEST ON ERRLCH   A43707
         AIF   (&DDR EQ 0).DDR13                                  19022
         BC    15,INTER7                TO READ HA-R0             19022
**       BC    15,INTERDDR              TIE RQE TO UCB + HANG RQE 19022
*                                       ON THE ERR LCH FOR DDR    19022
*                                       OR PERM ERR FOR SYSRES DDR19022
         AGO   .DDR14                                             19022
.DDR13   ANOP                                                     19022
**       BC    15,INTER7                TO READ HA-R/
.DDR14   ANOP                                                     19022
*                                                                     *
***********************************************************************
         AIF   (&DDR EQ 0).DDR15                                  19022
*    SET UCBERR TO TIE UP UCB FOR DDR                             19022
INTERDDR OI    UCBFL1(UCBREG),UCBERR    TIE UCB TO THIS RQE       19022
         TM    SRTESTAT(UCBREG),SYSCON  TEST FOR SYSRES DEVICE    19022
         BC    1,INTER8                 YES, POST PERM ERR
         L     WKREG1,TOADDR            ADDR OF DDRSRTO          BBBBBB
         CH    UCBREG,0(WKREG1)         IS THIS DDR 'TO' DEVICE  BBBBBB
         BC    7,INT059                 NO, GO TO HANG ON ERRLCH  19022
         MVI   UCBFL1(UCBREG),ZERO      LET SYSRES DDR THRU       19022
         BC    15,INT059                HANG RQE ON ERR LCH       19022
         SPACE 1                                                  19022
.DDR15   ANOP                                                     19022
*   EXCP RETURN FROM DISK RESIDENT ERROR RTN.
INTER7   OI    UCBFL1(UCBREG),UCBERR    TIE-UP UCB FOR THIS REQUEST
INTER7A  TM    XCPBTE,XCPCNT+XCPERR     TEST FOR EXCP ENTRANCE
         BC    8+1,INTER7B              NO, BRANCH AROUND
INTER7D  OI    XCPBTE,XCPERR            YES - MAKE LOOK LIKE SVC 15
         MVN   IOPSWO+2(1),UCBCHA(UCBREG)
INTER7B  BAL   LNKRG3,XCP064            INITIALIZE LCH
INTER7C  DS 0H
         AIF   (&APR EQ 0).APR200
         L     APBSRG,ADEACT            ADDR OF APR RTN          BBBBBB
         LA    LNKRG2,INTER7E           RETURN ADDR              BBBBBB
         BC    15,INTAPR01-INTDEACT(APBSRG) TO TEST FOR APR      BBBBBB
.APR200  ANOP                                                    BBBBBB
INTER7E  L     APBSRG,ENQAD             ADDR OF XCPPNQ RTN       BBBBBB
         LA    LNKRG2,INTER7F           SET RETURN ADDR          BBBBBB
         BC    15,XCPPNQB-XCPPNQ(APBSRG)   TO ENQUEUE RQE        BBBBBB
INTER7F  XC    LTSVALSV(2),LTSVALSV     ZERO RQE SAVE AREA       BBBBBB
         AGO   .PGMCK19                                          BBBBBB
INTER7F  DS    0H                                                BBBBBB
.PGMCK19 BC    15,INT027                GO TO CHAN RESTART       BBBBBB
INTER8   TM    IOBFL1(IOBREG),IOBEX    IS IOBEX=1               US06042
         BO    INT057                                           US06042
         NI    DCBFL(DCBREG),FF-DCBPER  RESET ERR RTN IN CONTROLUS06042
         B     INT019B                  TO CHAN END APPENDAGE   US06042
INTER10  TM    SRTESTAT(UCBREG),SYSCON  SYSTEM RESIDENCE VOLUME  A43707
         BC    1,INT059                 YES,DO NOT PROTECT STAT  A43707
         OI    UCBFL1(UCBREG),UCBERR    TIE DEVICE TO THIS RQE   A43707
         BC    15,INT059                HANG REQUEST ON ERRLCH   A43707
         AIF   (&M65MP EQ 0).MPB028                                RL19
         TITLE 'SHOULDER TAP INTERFACE - INPUT/OUTPUT SUPERVISOR'
*****************************************************************  MP1A
*                                                                  MP1A
*              IOS SHOULDER TAP INTERFACE (RECEIVING CPU)          MP1A
*                                                                  MP1A
*FUNCTION                                                          MP1A
*   TO DETERMINE WHICH CHANNEL SHOULD BE RESTARTED IN ORDER TO     MP1A
*   SERVICE THE I/O REQUEST WHICH WAS ENQUEUED BY THE OTHER CPU.   MP1A
*                                                                  MP1A
*ENTRY POINT                                                       MP1A
*        IECISHTP                                                  MP1A
*   FROM THE EXTERNAL FIRST LEVEL INTERRUPT HANDLER WHEN IT        MP1A
*   DETERMINES THAT AN I/O SHOULDER TAP WAS RECEIVED.              MP1A
*                                                                  MP1A
*INPUT                                                             MP1A
*   THE SHOULDER TAP MASK (STMASK), WITH BIT SETTING TO INDICATE   MP1A
*   WHICH CHANNEL IS TO BE RESTARTED.                              MP1A
*                                                                  MP1A
*OUTPUT                                                            MP1A
*   THE CHANNEL ADDRESS IN THE I/O OLD PSW FOR CHANNEL RESTART.    MP1A
*                                                                  MP1A
*EXITS                                                             MP1A
*        INT027 - CHANNEL RESTART - WHEN A CHANNEL IS FOUND        MP1A
*   TO BE RESTARTED.                                               MP1A
*                                                                  MP1A
*        IEAQEXIO - EXTERNAL FLIH - WHEN A REQUEST IS FOUND FOR    MP1A
*   AN INVALID CHANNEL, AND WHEN ALL CHANNELS HAVE BEEN PROCESSED. MP1A
*                                                                  MP1A
*****************************************************************  MP1A
IECISHTP BALR  BASREG,0            ENTRY POINT FROM EXTERNAL FLIH  MP1A
         USING *,BASREG                                          BBBBBB
         DROP  BASREG2                  DON'T USE REG 5 HERE    SA58049
IECISTP1 S     BASREG,IECISTAD          ESTABLISH
         USING IOSBAS,BASREG            ADDRESSABILITY           BBBBBB
         USING IOSBAS+4094,BASREG2                              SA58049
         LA    BASREG2,4094(BASREG)     SET HIGH BASE REGISTER  SA58049
         MVI   XCPBTE,XCPST             SET ENTRANCE FLAGS      SA58049
         L     APBSRG,PREFIX2           GET OTHER PSA POINTER
         MVI   STMASK+3-IPLPSW(APBSRG),ZERO  CLR SHLDR TAP IND.
         SR    ICREG,ICREG              SET UP FOR CHAN RESTART  M0147
         LA    WKREG1,STCHMSKB          GET CPU B'S CHAN. MASK   A42119
         CLI   CPUID-IEAQFX00,CPUA      IS THIS CPU A            A42119
         BC    8,IECISTP2               YES, GO AHEAD            A42119
         LA    WKREG1,STCHMSKA          NO, GET A'S CHAN. MASK   A42119
IECISTP2 OC    CHMSK(1),ZERO(WKREG1)    SET UP CHAN. FOR RESTART A42119
         ST    ICREG,ZERO(WKREG1)       RESET CHANNEL INDICATOR  A42119
         BC    15,INT033                GO ATTEMPT CHANNEL RESTART
         SPACE 1
IECIOSTV L     LNKRG2,IECIOSTR     GO BACK TO EX-FLIH              MP1A
         BCR   15,LNKRG2                                           MP1A
         SPACE 1
IECISTAD DC    A(IECISTP1-IOSBAS)       ADDRESSING CONSTANT      BBBBBB
         SPACE 1                                                 BBBBBB
*****************************************************************  MP1A
.MPB028  EJECT                                                     RL19
         MEND
