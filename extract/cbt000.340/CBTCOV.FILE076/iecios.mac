         MACRO
         IECIOS &TAPE,&SMF,&P3211,&TP,&APR,&VP,&SKORD,&RFIFO,&RPR,     *
               &TWOSW,&VS,&PROTECT,&TRACE,&SSDA,&TCBTYP,&SSTA,&GR,     *
               &NOP1,&RWTAU,&RESJQ,&NOP2,&ASR,&M65MP,&TBASE,&ANR,      X
               &DAVV,&DDR,&M7CH,&ASPN,&FPTK,&OLTEP,&TSO,&CEXT,&RMS,    X
               &TCAM,&MFTATT,&NOP3,&ALTSHR
         GBLB  &D2311,&D2321,&D2301,&D2303,&D2305,&DML            20201
         GBLB  &R2311,&R2321,&R2301,&R2303,&R2305,&RML            20201
         GBLB  &IECITB                                            20201
         LCLB  &RESREL,&RPS,&TBYT                                 20201
&RESREL  SETB  (&R2321 OR &R2311)                                 20201
&RPS     SETB  (&D2305 OR &DML)                                   20201
&TBYT    SETB  (&IECITB)           SET IF 2 BYTE LOOKUP TABLE     20201
         TITLE 'POST ROUTINE INTERFACE - INPUT/OUTPUT SUPERVISOR'
*                        POST ROUTINE INTERFACE
         SPACE 1
*              THIS ROUTINE GETS THE EVENT COMPLETION CODE IN WKREG1
*              SHIFTS IT LEFT SO THAT IT IS NOW A 30 BIT CODE, LOCATES
*              THE TCB FROM THE TCBID IN THE REQUEST ELEMENT AND
*              BRANCHES TO THE POST ROUTINE
*              RETURN IS VIA LNKRG2.
        SPACE 1
XCPPST   L     WKREG2,IOBECB-1(IOBREG)  GET ECB ADDRESS
         LA    WKREG1,0(0,WKREG2)       CLEAR OUT HIGH BYTE
         XR    WKREG1,WKREG2            ZERO OUT LOW ADDR AND GET COMP
         L     APBSRG,POSTADR           GET POST ADDRESS
         SPACE 3
         AIF   (&TCBTYP LT 2).STIOS1X   NOT MVT, SKIP            A24739
XCPTCBFD L     WKREG3,TSTTCB(TSTREG)    LOAD TCB ADDRESS
         TM    TSTKEY(TSTREG),X'F0'     CHECK RQSTR'S PROTECT KEY M3588
*  CLEAR HIGH ORDER BYTE TO PREVENT ABEND LOOP
         LA    WKREG3,0(WKREG3)                                   M3588
         LA    WKREG2,0(WKREG2) .CLEAR HI-BYTE                    M0100
         BCR   8,APBSRG                 TO POST WITHOUT VAL CHK   M3588
         L     APBSRG,POSTADR1          LOAD POST ENTRY WITH VAL  M3588
         AGO   .STIOS2Y
.STIOS1X ANOP                           SYSTEM 1 CODING
*  THE ID BYTE IS USED TO BE COMPATABLE WITH THE PARTITION SYSTEM
XCPTCBFD IC    ICREG,TSTTCB(TSTREG)     GET TCB ID FROM RQE
         SLL   ICREG,2                  MULTIPLY BY 4             19054
         L     WKREG3,TCBTBL            GET TCB TABLE ADDR
         L     WKREG3,0(ICREG,WKREG3)   GET THIS TCB ADDR
         SR    ICREG,ICREG              CLEAR PRIOR CHAN RESTART  M1542
*  CLEAR HIGH ORDER BYTE TO PREVENT ABEND LOOP                     IMP4
         LA    WKREG3,0(WKREG3)                                    IMP4
         LA    WKREG2,0(WKREG2) .CLEAR HI-BYTE                    M0100
         AIF   (&PROTECT NE 1).STIOS2Y
         TM    DEBPRT(DEBREG),PPKEY     IS DEB KEY ZERO           M3588
         BC    8,4(APBSRG)              YES BYPASS VAL CHECK      M3588
.STIOS2Y ANOP
         BCR   15,APBSRG                TO POST ROUTINE          A24739
         SPACE 1
*    DEFINITION OF POST - IOS INTERFACE                          BBBBBB
*    INPUT     WKREG1(COMPLETION CODE),WKREG2(ECB ADDR),         BBBBBB
*              WKREG3(TCB ADDR),APBSRG(BASE ADDR)                BBBBBB
*    REGS CHANGED  ICREG,LCHREG                                  BBBBBB
         AIF   (&PROTECT EQ 0).STIOSH1                          SA68418
         TITLE 'VALIDITY CHECK ROUTINE - INPUT/OUTPUT SUPERVISOR'
*                                                                     *
*                       VALIDITY CHECK SUBROUTINE                     *
*                                                                     *
*  FUNCTION    TO VALIDITY CHECK USER'S IOB, ECB, DCB, DEB            *
*                                                                     *
*  EXITS       LNKRG2 - TO RETURN TO CALLER                           *
         AIF   (&TCBTYP LT 2).MFT01                             SA68418
*              XERX00 - TO ABEND TASK                                 *
.MFT01   ANOP                                                   SA68418
*                                                                     *
*  ENTRY POINT VALCHK                                                 *
*              VALCHK2                                                *
*              VALCHK3                                                *
*                                                                     *
*   THIS IS THE TEST FOR INVALID CONTROL BLOCKS                    VDTY
         SPACE 1                                                   VDTY
VALCHK   MVZ   PROTPSW+1(1),DEBPRT(DEBREG) SEPARATE PROTECT KEY  A24739
         LPSW  PROTPSW                  ENTER USER'S PROTECT KEY A24739
VALCHK2  NI    IOBFL1(IOBREG),FF        VERIFY ALL USER BLOCKS   A24739
         L     WKREG1,IOBECB-1(IOBREG)  GET ECB ADDR             A24739
         NI    0(WKREG1),FF             CHECK ECB                A24739
         NI    DCBFL(DCBREG),FF         CHECK DCB                A24739
         AIF   (&TCBTYP EQ 2).MVT01                             SA68418
         NI    DEBDCB(DEBREG),FF   CHECK DEB                    SA68418
.MVT01   ANOP                                                   SA68418
         LPSW  NOPRTPSW                 KEY FROM TCB             A24739
         AIF   (&TCBTYP EQ 2).MVT02                             SA68418
VALCHK3  BCR   15,LNKRG2           RETURN TO KEY ZERO           SA68418
         AGO   .STIOSH1                                         SA68418
.MVT02   ANOP                                                   SA68418
VALCHK3  LR    WKREG2,DEBREG            ADDR OF DEB              A24739
         IC    WKREG2,WORDMASK          CLEAR BITS 24-31         A24739
         SR    WKREG1,WKREG1                                     A24739
         ISK   WKREG1,WKREG2            GET STORAGE KEY OF DEB   A24739
         LTR   WKREG1,WKREG1            IS IT ZERO               A24739
         BCR   8,LNKRG2                 YES - RETURN OK          A24739
         L     APBSRG,IECPL2            GET TEMP BASE REG      @SA77644
         BC    15,XERX00-IECCPL02(APBSRG) GO ABEND THE TASK    @SA77644
.STIOSH1 ANOP
         TITLE 'ADDRESS CONSTANTS - INPUT/OUTPUT SUPERVISOR'
***********************************************************************
IECIXAVL DS    0F
NEXAVL   DC    AL2(TSTAR-IPLPSW)        NEXT AVAILABLE RQE ADDR
DUMMY    DC    XL2'FFFF'                DUMMY RQE
*    THE FOLLOWING ARE CONSTANTS AND ENTRIES TO IOS WHICH ARE    BBBBBB
*    LOCATED THROUGH THE CVT AND IECIXAVL.  EACH WORD MUST       BBBBBB
*    MAINTAIN ITS PRESENT OFFSET FROM LABEL IECIXAVL FOR         BBBBBB
*    EXTERNAL ROUTINES' USE.                                     BBBBBB
         AIF   (&TCBTYP LT 2).MVT020    NOT MVT, SKIP            A24739
POSTADR1 DC    V(IEA0PT02) .            +4         POST ENTRY VAL. CHK.
         AGO   .MVT021
.MVT020  ANOP
TCBTBL   DC    A(0) .                   +4         TCB TBL LOOKUP 19054
.MVT021  DC    A(IECIHIO) .             +8         ENTRY TO HIO ROUTINE
POSTADR  DC    A(IEA0PT01) .            FILL  POST RTN ENTRY PT    IMP5
         AIF   (&DDR EQ 0).DDR10                                  19022
ADDRAPN  DC    X'80',AL3(DDRAPNVT) .    +16      FLAG, VEC. TBL.  19022
         AGO   .DDR11                                             19022
.DDR10   ANOP                                                     19022
T311ERT  DC    A(ER2311) .              FILL  ERROR RTN ENTRY PT.  IMP5
.DDR11   ANOP                                                     19022
CHMSK    DC    A(0) .                   +20        CHAN RSTRT CONSTANT
         AIF   (&APR EQ 0).APR140
CEBCTR   DC    X'05' .                  +24 CEB COUNT FEILD         APR
CEBPTR   DC    AL3(CEBTAB) .            +25 CEB BLOCK POINTER       APR
         AGO   .NAPR140
.APR140  ANOP
DISMIS   DC    A(DISMISS) .             +24 FILL RTN TO SUPERVISOR
.NAPR140 ANOP
ZDEBAD   DC    A(ZDEBPRG) .             +28  PTR TO ZEUS RTN      20201
         AIF   (&TCBTYP NE 1).NOWTQP    IS THIS MFT             SA41451
         DC    A(WTQPRG) .              +32  WAIT QUEUE PURGE   SA41451
         AGO   .A325CWK
.NOWTQP  ANOP                                                   SA41451
SIRBAD   DC    V(SIRB) .                +32  ADDRESS OF SIRB
.A325CWK DC    3F'0' .                  +36  325C WORK AREA
***********************************************************************
ENQAD    DC    A(XCPPNQ)                ADDR OF ENQ RTN          BBBBBB
CCWRTNAD DC    A(CCWRTN)                CCW ROUTINE ADDRESS
LISTA    DC    A(LIST)                  UCB LIST ADDR           SA71362
IECILK2A DC    A(IECILK2)               UCB LIST ADDR           SA71362
         AIF   (&TCBTYP EQ 1).MFTREGS
GETMAIN  DC    V(RMBRANCH)              GETMAIN ADDRESS         SM00010
PCKREGS  DC    V(IEAPKSAV)              ADDR OF PGM CK REGS
         AGO   .RQEADDR
.MFTREGS ANOP
PCKREGS  DC    V(PISAV)                 ADDR OF PGM CK REGS
GETMAIN  DC    A(RMBRANCH)              ABEND SAVE AREA RTN ADDRSM00010
.RQEADDR ANOP
FSTRQE   DC    AL2(TSTAR-IPLPSW)        ADDR OF FIRST RQE       SA63707
LSTRQE   DC    AL2(LASTRQE-IPLPSW)      ADDR OF LAST RQE        SA63707
IECPL2   DC    A(IECCPL02)              ADCON FOR TEMP BASE    @SA77644
         AIF   (&TCBTYP GT 1).MVT030                             A24739
XCPSDA   DC    A(IEA0IO02+1)            PSEUDO DISABLE SWITCH    BBBBBB
         AGO   .MPB071
.MVT030  ANOP
AIEAHEAD DC    A(IEAHEAD)               ADDRESS OF HEAD TCB
AIEADS02 DC    A(IEA0DS02)              ADDRESS OF TASK SWITCH ROUTINE
         AIF   (&M65MP EQ 1).MPB071                                MP1A
AIEATCBP DC    A(IEATCBP)               PTR TO NEW TCB           BBBBBB
.MPB071  AIF   (&ASR EQ 0).ASR9         CHANNEL CHECK HANDLER
CATAPP   DC    V(IGFCAT)                CCH BASE REG AND START PT  CCH
         AGO   .ASR10                                           SA68848
.ASR9    ANOP                                                   SA68848
CATAPP   DC    X'00'                    SENSE SIO/TIO FLAG      SA68848
.ASR10   AIF   (&TRACE EQ 0).STIOSTC
TRACON   DC    V(TRACE)                 SIO TRACE ENTRY
.STIOSTC AIF   (&SMF EQ 0).SMF170                                  SMF2
SMFRTN   DC    V(IEASMFEX)              ADDRESS OF SMF ROUTINE   BBBBBB
.SMF170  ANOP                                                      SMF2
SVCXAD   DC    A(IEA0XE00)              SVC HANDLER EXIT
         AIF   (&DDR EQ 0).DDR12                                  19022
T311ERT  DC    A(ER2311) .              D/A RESIDENT ERR RTN      19022
.DDR12   AIF   (NOT &D2305).NOZUS1
AZUSRTN  DC    A(ZUSRTN)                RTN TO GET ZEUS BASE ADDR 20201
.NOZUS1  ANOP                                                   SA68848
ACLPRTN  DS    0C                                                 21111
CLPCNT   DC    X'00'                    CHAN LOGOUT PENDING CNTR  21111
         DC    AL3(CLPRTN)              CHANNEL LOGOUT PENDING RTN21111
IECDMS   DS    0C
         AIF   (&TCBTYP EQ 2).MVTERR                             A48653
         AIF   (&MFTATT EQ 1).MFTERR                             A48653
SVCDCB   DC    V(IEASVDCB)              SVCLIB DCB
         AGO   .ENDERR1                                          A48653
.MVTERR  AIF   (&RESREL EQ 0 AND &RML EQ 0).MFTERR                ITH01
ERRTCB   DC    V(IEAERTCB)              SYSTEM ERROR TCB ADDR    A48653
.MFTERR  ANOP                                                    A48653
MASTCB   DC    V(IEAMSTCB)              MASTER SCHED TCB ADDR    A48653
.ENDERR1 ANOP                                                    A48653
CURRTCB  DC    F'0'                     CURRECNT TCB ADDR       SA53199
         AIF   (&TCBTYP NE 1).NOSIRB    DON'T GEN SIRB VCON
SIRBAD   DC    V(SIRB)                  ADDRESS OF SIRB         SA53191
.NOSIRB  AIF   (&APR EQ 0).APR145
DISMIS   DC    A(DISMISS)               RETURN TO SUPERVISOR
.APR145  AIF   (&M65MP EQ 0).MPB072
IECIOSTR DC    V(IEAQEXIO)              RETURN IN EXFLIH FROM SH. TAP
STADDR   DC    A(SHOLDTAP)              SHOULDER TAP ENTRY POINT   MP1A
NEGDISP  DC    A(UCBMPANX)              NEGATIVE UCB DISPLACEMENT
.MPB072  ANOP                                                      MP1A
XITFTR   DC    V(IEA0EF00)              EXIT EFFECTOR
         AIF   (&RESJQ EQ 0).STIOSTT                             BBBBBB
JOBQENT  DC    V(IEAAJOBQ)              ENTRY POINT TO RES. JOB Q
.STIOSTT AIF   (&RMS EQ 0).RMS12                                  19021
RMSFLAD  DC    A(RMSFLAG)               ADDR OF RMS FLAG BYTE     20061
RMSAD    DC    A(INTRMS)                ADDR OF RMS ENTRY RTN     20061
RMSEXAD  DC    A(IECRMS)                ADDR FOR RMS EXIT RTN    BBBBBB
.RMS12   AIF   (&DDR EQ 0).DDR12A                                A24739
ADDRRQE  DC    A(DDRRQE)                ADDR OF THE DDR RQE      A24739
DDRSTAD  DC    A(DDRSTART)              ADDR OF DDRSTART         BBBBBB
TOADDR   DC    A(DDRSRTO)               ADDR OF DDRSRTO          BBBBBB
.DDR12A  AIF   (&DDR LT 2).DDR12B                                 M3374
ADDRRQES DC    A(DDRRQESR)              ADDR OF DDR SYSRES RQE    M3374
.DDR12B  AIF   (&APR EQ 0).APR065                                BBBBBB
ADEACT   DC    A(INTDEACT)              ADDR OF APR CEB DEACTIVATE BBBB
.APR065  TITLE 'SYSTEM TABLES - INPUT/OUTPUT SUPERVISOR'
*              SYSTEM APPENDAGE VECTOR TABLE
         SPACE 2
IECXAPG  DS    0F
         DC    A(SYSRTN)                END OF EXTENT
         DC    A(SYSRTN)                SIO
         DC    A(SYSRTN)                PCI INTERUPTION
         DC    A(SYSRTN)                CHANNEL END
         DC    A(SYSRTN)                EXCEPTIONAL CHANNEL END
         SPACE 3
*              ATTENTION TABLE
         SPACE 2
ATNTAB   DS    0F                       ATTENTION TABLE FOR UNIT EXITS
*   ALL UCB'S WITH ZERO ATTENTION INDEX WILL BRANCH TO DUMMY INSTRUCT
         DC    AL4(INTATN)              RETURN FOR IGNORE ATTNS
         DC    AL4(IEEBA1)              CONSOLE
         DC    A(0)                     SPARE SPOT
         DC    AL4(IEFDPOST)            SCHED POST ROUTINE
         AIF   (&GR EQ 2).GRJ                                     M2256
         DC    A(INTATN)
         AGO   .GRK
.GRJ     DC    V(IFFIOM)                GRAPHIC ATTENTION HANDLER
.GRK     AIF   (&TCAM EQ 0).NTCAM       TEST FOR TCAM             20002
         DC    A(IEDQATTN)              TP TCAM ATTN HANDLER      20002
         AGO   .TCAM                                              20002
.NTCAM   DC    A(INTATN)                RETURN                    20002
.TCAM    AIF   (&ASPN EQ 0 AND &ANR EQ 0 AND &P3211 EQ 0).ATTEND
*                                                                A44146
         AIF   (&ASPN EQ 0).N34XX
         DC    V(IECTATTN)              34XX ATTENTION HANDLER    21048
         AIF   (&ANR EQ 0 AND &P3211 EQ 0).ATTEND                 M2054
         AIF   (&ANR NE 2).NANR         IF NO ANR, DUMMY ATTN     M2054
         AGO   .ANR01
.N34XX   DC    AL4(INTATN)              RETURN TO IGNORE ATTNS    21003
         AIF   (&ANR NE 2).NANR         IF NO ANR, DUMMY ATTN     M2054
.ANR01   DC    V(IECTATEN)              ANR ATTENTION HANDLER     21003
         AGO   .PRT3200                 GO TEST FOR 3211         A44146
.NANR    DC    AL4(INTATN)              RETURN TO IGNORE ATTNS   A44146
.PRT3200 AIF   (&P3211 EQ 0).ATTEND
         DC    V(IECURATN)              3211 ATTENTION HANDLER   A44146
.ATTEND  SPACE 2
*              THE OLTEP VECTOR TABLE                             19026
IECOLTVT DS    0H                       DUMMY IF NO OLTEP         19026
         AIF   (&OLTEP EQ 0).GTF5                                 21014
IECOLTSW DC    2F'0'                    SWITCH, IECIOLTS, OLT QUE 19026
         SPACE 1                                                  21014
.GTF5    ANOP                                                     21014
* THIS SECTION CONTAINS OUT OF LINE BRANCHES FOR GENERALIZED      21014
*  TRACE FUNCTION                                                 21014
GTF01    HOOK  EID=IHLMIO1              TRACE FOR ZERO STAT INTS  21014
         AIF   (&TBASE NE 0).OLT050                               21014
         IECLNK1 ,BCR,15,LNKRG1,,,,&TBASE                         21014
         SPACE 1                                                  21014
.OLT050  AIF   (&TBASE EQ 0).TBS185                               19026
*                   SELECT VECTOR TABLE                            TBSE
         SPACE 2                                                   TBSE
*    THIS VECTOR TABLE IS USED WHEN THE OPTION FOR TWO BASE      BBBBBB
*    REGISTERS IS SPECIFIED.  IT ALLOWS FREEING OF 'LNKRG1 FOR   BBBBBB
*    USE AS THE SECOND BASE REGISTER.                            BBBBBB
         SPACE 2                                                   TBSE
SELECTV  L     LNKRG2,LNKRG1S           GENERAL RETURN VECTOR    BBBBBB
         BCR   15,LNKRG2                                           TBSE
         SPACE 1                                                   TBSE
SELTAGV  L     LNKRG2,LNKRG1S           TRY AGAIN RETURN VECTOR  BBBBBB
         BC    15,SELTAG(LNKRG2)                                   TBSE
         SPACE 1                                                   TBSE
SELBSYV  L     LNKRG2,LNKRG1S           BUSY RETURN VECTOR       BBBBBB
         BC    15,SELBSY(LNKRG2)                                   TBSE
         SPACE 1                                                   TBSE
SELNXTLV L     LNKRG2,LNKRG1S           SELECT NEXT LCH RETURN VECTOR
         BC    15,SELNLCH(LNKRG2)                                  TBSE
.TBS185  AIF   (&DDR EQ 0).DDRBB3A                               BBBBBB
         SPACE 2                                                 BBBBBB
*                DDR APPENDAGE VECTOR TABLE                      BBBBBB
         SPACE 1                                                 BBBBBB
DDRAPNVT DS    0F                                                BBBBBB
ADDRWTQ  DC    A(DDRWAITQ)              ADDR OF WAIT QUE FOR PURGE
         BC    15,DDRRTN1               RE-EXCP WITH CTRL OF UCB BBBBBB
         BC    15,DDRRTN2               RE-EXCP W/O CTRL         BBBBBB
         BC    15,DDRRTN3               POST DDR W/O CTRL OF UCB BBBBBB
         BC    15,DDRRTN4               POST DDR WITH CTRL       BBBBBB
         BC    15,DDRRTN5               RETRY USER AFTER SWAP    BBBBBB
         BC    15,DDRRTN6               SWAP UCB INFORMATION     BBBBBB
         BC    15,DDRRTN7               DDR REQ CANCELLED BY OP. BBBBBB
         AIF   (&DDR LT 2).DDRBB1A                               BBBBBB
         BC    15,DDRRTN8               CANCEL SYSRES DDR REQ.   BBBBBB
         BC    15,DDRRTN9               SYSRES RE-EXCP ON CC3    BBBBBB
.DDRBB1A SPACE 3                                                 BBBBBB
*                       DDR BRANCH TABLE                         BBBBBB
         SPACE 1                                                 BBBBBB
DDRRTN1  L     APBSRG,DDRSTAD           ADDR OF DDRSTART         BBBBBB
         BC    15,DDRXCPWC-DDRSTART(APBSRG) RE-EXCP WITH CTRL    BBBBBB
         SPACE 1                                                 BBBBBB
DDRRTN2  L     APBSRG,DDRSTAD           ADDR OF DDRSTART         BBBBBB
         BC    15,DDRXCPNC-DDRSTART(APBSRG) RE-EXCP W/O CTRL     BBBBBB
         SPACE 1                                                 BBBBBB
DDRRTN3  L     APBSRG,DDRSTAD           ADDR OF DDRSTART         BBBBBB
         BC    15,DDRPSTNC-DDRSTART(APBSRG) POST DDR W/O CTRL    BBBBBB
         SPACE 1                                                 BBBBBB
DDRRTN4  L     APBSRG,DDRSTAD           ADDR OF DDRSTART         BBBBBB
         BC    15,DDRPSTWC-DDRSTART(APBSRG) POST DDR WITH CTRL    M1870
         SPACE 1                                                 BBBBBB
DDRRTN5  L     APBSRG,DDRSTAD           ADDR OF DDRSTART         BBBBBB
         BC    15,DDRRETRY-DDRSTART(APBSRG) RETRY USER AFTER SWAP BBBBB
         SPACE 1                                                  BBBBB
DDRRTN6  L     APBSRG,DDRSTAD           ADDR OF DDRSTART         BBBBBB
         BC    15,DDRSWAP-DDRSTART(APBSRG) SWAP UCB INFORMATION  BBBBBB
         SPACE 1                                                 BBBBBB
DDRRTN7  L     APBSRG,DDRSTAD           ADDR OF DDRSTART         BBBBBB
         BC    15,DDRCANCL-DDRSTART(APBSRG) DDR REQ CANCELLED    BBBBBB
         AIF   (&DDR LT 2).DDRBB3A                               BBBBBB
         SPACE 1                                                 BBBBBB
DDRRTN8  L     APBSRG,DDRSTAD           ADDR OF DDRSTART         BBBBBB
         BC    15,DDRSRCAN-DDRSTART(APBSRG) CANCEL SYSRES DDR REQ BBBBB
         SPACE 1                                                  BBBBB
DDRRTN9  L     APBSRG,DDRSTAD           ADDR OF DDRSTART         BBBBBB
         BC    15,DDRXCPSR-DDRSTART(APBSRG) SYSRES RE-EXCP-CC3   BBBBBB
.DDRBB3A AIF   (&RESREL EQ 0 AND &RML EQ 0).RELSE2                ITH01
   TITLE 'SHARED FILE RELEASE CONTROL BLOCKS - INPUT/OUTPUT SUPERVISOR'
*    ONLY CERTAIN FIELDS IN THE IOB, DEB AND DCB ARE ESSENTIAL     IMP5
*    FOR EXECUTING A CHANNEL PROGRAM.  IOS USES THE FOLLOWING      IMP5
*    SKELETON CONTROL BLOCKS TO EXECUTE A RELEASE AFTER PURGE.     IMP5
*    THE IOB IS TRUNCATED AFTER 20 BYTES.  THE DCB CONSISTS ONLY   IMP5
*    OF A POINTER TO THE DEB.  THE DEB CONSISTS OF THE PURGE       IMP5
*    CHAIN FIELD, THE PTR TO THE DCB, THE PTR TO THE APPENDAGE     IMP5
*    TABLE, AND THE FILE MASK-UCB PTR FIELD.  THE ECB IS THE IOB   IMP5
*    PTR TO IT.                                                    IMP5
         SPACE 1                                                   IMP5
RRIOB    DS    0F                       ALIGN IOB                  IMP5
         DC    X'02'                    UNRELATED, RESTART FL OFF  IMP5
         DC    X'00'                    FLAGS 2 ARE 0              IMP5
         DC    2X'00'                   SENSE BYTES                IMP5
RRECB    DC    X'7F'                    CC FIELD FOR ECB       @SA78510
         DC    AL3(RRECB)               PTR TO ECB AND ECB ITSELF  IMP5
         DC    2F'0'                    CSW AREA                   IMP5
         DC    A(SYSNOP)                POINTER TO NOP             IMP5
         DC    A(RRDCB)                 PTR TO DCB IOB + 20    @SA78510
RRDCB    EQU   *-44                     SET UP DCB ORIGIN      @SA78510
RRDCBDEB DC    X'0C'                    DCBIFLAGS-NO ERP       @SA78510
         DC    AL3(RRDEB)               DEB PTR FOR DCB + 44   @SA78510
         DC    F'0'                     IOB ERR COUNT FOR DAVV @SA78510
RRDEB    EQU   *-20                     DEB ORIGIN             @SA78510
         DC    F'0'                     PURGE ECB, DEB + 20    @SA78510
         DC    A(RRDCB)                 DCB POINTER DEB + 24   @SA78510
         DC    A(IECXAPG)               AVT POINTER DEB + 28   @SA78510
RRDEBUCB DC    X'58'                    F.M.,NOSKS,NOWR,DEB+32 @SA78510
         DC    3X'00'                   UCB POINTER DEB +34    @SA78510
.RELSE2  TITLE 'SYSTEM CHANNEL PROGRAMS - INPUT/OUTPUT SUPERVISOR'
*   THIS CCW IS SHARED BY THE TAPE,UNIT RECORD AND SENSE MODULES
SYSIMD   CCW X'00',0,X'70',6            FILLED IN BY ROUTINE     BBBBBB
*   THIS COMMAND ALLOWS THE ERROR RTN TO HOLD UNIT CHECK UNTIL D.END
SYSNOP   CCW   NOOP,0,X'20',6           NOOP COMMAND               IMP5
SYSSENSE CCW   X'04',0,X'20',6          SENSE COMMAND
         AIF   (&TAPE EQ 0).NOTAPE                                 RL19
         AIF   (&RWTAU EQ 0).SXI08
TAPESM   CCW   0,0,X'20',1              SET MODE COMMAND
         AGO   .SXI09
.SXI08   ANOP
TAPESM   CCW   0,0,X'60',1              SET MODE COMMAND        SA54451
.SXI09   ANOP                                                   SA54451
         CCW   0,0,X'60',1              MODE SET IF CRC         SA54451
         AIF   (&RWTAU EQ 0).NOTAPE                             SA54451
TAPECCW  DC    F'0'                     TAPE CCW SAVE AREA      SA54451
.NOTAPE  ANOP                                                      RL19
DEVMSK   DC    X'0000000F'
         AIF   (&RESREL EQ 0).RR033                                SDA2
SEEKRR   CCW   RESERVE,0,X'70',1        RESERVE THE DEVICE         SDA2
.RR033   ANOP                                                      SDA2
SEEK1    CCW   X'07',0,X'20',6          STAND ALONE SEEK
         AIF   (&RESREL EQ 0).RR006A
RELCCW   CCW   RELEASE,0,X'30',6        RELEASE AFTER PURGE
RRSENSE2 CCW   X'04',0,X'30',1          SENSE CCW AFTER UC ON RH
.RR006A  ANOP                                                   SA54451
RRDXCCW  CCW   X'07',0,X'60',6          SEEK
         CCW   X'1F',0,X'60',1          SET FILE MASK
         AIF   (&RESREL EQ 0).RR006Z                            SA54451
RRSNCCW  CCW   X'1A',0,X'60',5          READ HOME ADDRESS
         CCW   X'16',0,X'20',4          READ R0
.RR006Z AIF (NOT &R2301 AND NOT &R2303 AND NOT &R2305 AND NOT &RML).RRZ
RRDRMCCW CCW   RESERVE,0,X'30',1        SHARED DRUM CCW           20201
.RRZ     AIF   (&M65MP EQ 0).MPB078                               20201
MPENB    DC    X'FE040000'                                         MP1A
         DC    A(INT033A+4)                                        MP1A
MPDSB    DC    X'00040000'                                         MP1A
         DC    A(INT033B+4)                                        MP1A
MPENBA   DC    X'FE040000'                                         MP1A
         DC    A(INT030B+4)                                        MP1A
MPDSBA   DC    X'00040000'                                         MP1A
         DC    A(INT030C+4)                                        MP1A
.MPB078  ANOP                                                  SA68418
         AIF   (&PROTECT EQ 0).MPB074                           SM00045
PROTPSW  DC    X'00040000'              DISABLED EXCEPT MACHINE  A24739
         DC    A(VALCHK2)                                        A24739
NOPRTPSW DC    X'00040000'                                       A24739
         DC    A(VALCHK3)                                        A24739
.MPB074  ANOP                                                   SM00045
         TITLE 'MISCELLANEOUS CONSTANTS - INPUT/OUTPUT SUPERVISOR'
         AIF   (&M65MP NE 1 AND &TWOSW NE 1 AND &APR NE 1).MPB073 M4164
PATHTST  TM    UCBCHM(UCBREG),X'00'     PATH STATUS
.MPB073  SPACE 3
         DS    0F
CUBMSK   DC    X'8FFFFFFF'              CUB AND CUE MASK
CHEDVE   DC    X'0000F2BF'              CE,DE,WLR,UEX MASK
ADDRMSK  DC    X'00000FFF'              UNIT ADDRESS MASK
PCIMSK   DC    X'0000FF3F'              PCI AND WLR MASK         BBBBBB
         AIF   (NOT &D2305).ZUSMSK                              SA62867
ZBASMSK  DC    X'00000FF8'              2305 UAREG BASE MASK    SA62867
ZEXPMSK  DC    X'00000007'              2305 UAREG EXP MASK     SA62867
.ZUSMSK  AIF   (&TWOSW EQ 0).TBS400                             SA62867
CHCUMASK DC    X'00000FF0'              CHANNEL/CONTROL UNIT MASK M5716
.TBS400  ANOP                                                   SA68848
HWMSK    DC    X'0000FFFF'                                       BBBBBB
LPCNT    EQU   HWMSK                    SIO/TIO LOOP COUNT      SA68848
         AIF   (&M65MP EQ 0).MPB075                              BBBBBB
STCHMSKA DC    F'0'                     CHANNEL RESTART CONSTANT A42119
STCHMSKB DC    F'0'                     FOR SHOULDER TAP INTERF. A42119
CPUIDMSK DC    X'00000008'              CLEAR ALL EXCEPT IOCPUID
.MPB075  AIF   (&TCBTYP LT 2).M7SIM                              A24739
WORDMASK DC    X'00000003'              FULL WORD BOUNDARY MASK  BBBBBB
.M7SIM   ANOP                                                  @SA78138
CHANBIT  DC    X'80000000'              CONSTANT FOR M7CH          I267
         AIF   (&TSO EQ 0).TSO015                              @SA78138
DUMPTCB  DC    X'80F5C000'              SVC   92 ABEND CODE       20001
.TSO015  ANOP
IOSABEND DC    C'IOSABEND'              SAVE AREA ID
ABCORE   DC    X'FD000080'              SUBPOOL AND LENGTH
DUMPCODE DC    X'80000000'              VARIABLE ABEND CODE       21016
CODEDUMP EQU   DUMPCODE+1               CODE POSITION             21016
         AIF   (&TCBTYP EQ 0).CPL0100                            A24739
DUMPCOD1 DC    X'800F1000'              CODE FOR INT PGM CHK     A24739
DUMPCOD2 DC    X'800F2000'              CODE FOR SVC PGM CHK     A24739
DUMP10F  DC    X'8010F000'              INVALID SVC 15 CODE     SA63707
DUMP600  DC    X'80600000'              INVALID LCH CODE
LCHWDSV  DC    F'0'                     LCH ADDR SAVE AREA
LCHERRAD DC    A(LCHERR)                WRONG LCH RTN ADDR
WKREG2S  DC    F'0'                     SAVE FOR BBCC COMPARE    BBBBBB
.CPL0100 AIF   (&TBASE EQ 0).TBS390                              A24739
LNKRG1S  DC    F'0'                     PSUEDO LNKRG1 FOR TBASE  BBBBBB
.TBS390  ANOP                                                    BBBBBB
EIGHTF   DC    F'8'                     CCW LENGTH                21111
EXTSAV DC F'0'                                                    20201
BLNKPTR  DC    F'0'                     BACK-LINK FOR CHAN SRCH  BBBBBB
BUSYSV   DC    2F'0'                    RETURN REG SAVE ON BUSY  BBBBBB
CLEARM   DC    AL1(IOBCDS+IOBURL)       CLEAR IOB FLAGS
         DC    AL1(IOBQSM)              QSAM FLAG
         DC    AL2(0)                   CLEAR IOB MASK
LTSVALSV DC    H'0'                     RQE SAVE AREA FOR PGM CK A24739
MAXUCBO  DC    AL2(PURGPTR-LIST)        MAX OFFSET IN UCB LIST
PRGFLD   DC    X'00'                    WORK AREA-USE NUMERIC PORTION
IDBYTE   DC    X'00'                    WORK AREA-USE ZONE PORTION
SIX      DC    AL2(6)                                              IMP3
EIGHT    DC    AL2(8)                   CCW INCREMENT
STARTCP  DC    AL2(20)                  START CCW FOR DTR.
         AIF   (&SSDA EQ 0 AND &SSTA EQ 0).MPX030               SA54451
STARTCP2 DC    AL2(24)                  LENGTH OF CCW STRING
.MPX030  AIF   (&RESREL EQ 0 OR &SSDA EQ 0).SNS1
STARTCP3 DC    AL2(32)                  LNGTH OF CCW'S FOR SSDA    IMP5
.SNS1    AIF   (&M65MP EQ 0).SNS347     MP SYSTEM               SA60510
MPTCHSW  DC    X'00'                    MP TCH SWITCH           SA60510
.SNS347  ANOP                                                   SA60510
SNSCNT   DC    X'00'                    SENSE RETRY COUNTER      A53284
XCPBTE   DC    X'00'                    FLAG BYTE FOR XCP AND INT
XCPENB   DC    X'FE'                    ENABLE MASK
         AIF   (&TWOSW EQ 0 AND &M65MP EQ 0).TWOS4                 IMP3
PATHMSK  DC    X'00'                    VARY MASK FROM TCH
.TWOS4   AIF   (&RESREL EQ 0).SXILA52
CLEAR    DC    AL3(RRSNCCW+16)          DUMMY CSW PTR
         DC    X'0C00000040'            SIM. CE/DE, SIO ACPT CC
.SXILA52 ANOP                           GET CAW FOR RETRY SIO     ITH01
SSS1     CCW   X'27',SSS1ARG,X'20',7 *  STAND ALONE SEEK & SS     ITH01
SSS1ARG  DC    XL7'00' *                SA SEEK & SS ARGUMENT     ITH01
         DC    X'00' *                  ALIGN                     ITH01
ITBLKMPX DC    V(SCXCPMON) *            ADDR OF CH PGM MONITOR    ITH01
XCP110AD DC    A(IXCP110A) *            ADDR OF 7330 RPS SETUP    ITH01
ITSAVWK1 DC    F'0' *                   SAVE AREA FOR WKREG1      ITH01
IT7330   DC    X'2009' *                UCBTYP FOR 7330           ITH01
         TITLE 'TEST CHANNEL AND CHANNEL SEARCH ROUTINES - INPUT/OUTPUTX
                SUPERVISOR'
***********************************************************************
*
*   THIS SECTION OF IOS CONTAINS THE FOLLOWING                     IMP4
*
         AIF   (&M65MP EQ 0).MPB085                                MP1A
*   THE DEVICE ACCESSABILITY ROUTINE                               MP1A
.MPB085  ANOP                                                      MP1A
*   THE TEST CHANNEL ROUTINES                                      IMP4
*   THE CHANNEL SEARCH ROUTINES                                    IMP4
*   THE DEVICE TABLE - PTRS TO DEVICE-DEPENDENT CODE FOR EACH DEVICE
*   THE CHANNEL TABLE - PTRS TO THE CHANNEL SEARCH CODE FOR EACH CHAN.
         AIF   (&M65MP EQ 1).MPB087
*   THE UCB SAVE TABLE - USED TO SAVE PTRS OF UCB'S STARTED LAST
.MPB087  ANOP
*   THE LCH WORD TABLE - THE LOGICAL CHANNEL WORDS FOR ALL CHANNELS
*   THE UCB LOOKUP TABLE - ADDRESSES OF THE UCB'S IN THE SYSTEM
         AIF   (&TBASE EQ 1).TBS330                                IMP4
*   THE UCB TABLE - THE UCB'S IN THE SYSTEM
*   THE RQE TABLE - THE RQE'S IN THE SYSTEM
.TBS330  ANOP                                                      IMP4
*   THE STATISTICS TABLE
*
* FUNCTIONS OF THE ROUTINES
         AIF   (&M65MP EQ 0).MPB090                                MP1A
*   THE DEVICE ACCESSABILITY ROUTINE - A BIT SETTING THAT HAD BEEN MP1A
*   PLACED IN THE NEGATIVE UCB DISPLACEMENT BY NIP IS TESTED TO    MP1A
*   DETERMINE IF THE EXECUTING CPU HAS ACCESS TO THE DEVICE.  IF   MP1A
*   IT DOES, THE TEST CHANNEL ROUTINE IS ENTERED. IF THE OTHER CPU MP1A
*   HAS ACCESS TO IT, THE SHOULDER TAP ROUTINE IS ENTERED. IF      MP1A
*   NEITHER CPU HAS ACCESS TO THE DEVICE, THE REQUEST IS ENQUEUED. MP1A
*
.MPB090  ANOP                                                      MP1A
*   THE TEST CHANNEL ROUTINE INITIALIZES UAREG WITH THE PROPER
*   CHANNEL ADDRESS AND EXECUTES A TCH INSTRUCTION.  IF THE CHANNEL
*   IS BUSY, THE REQUEST IS ENQUEUED, OR, IF OPTIONAL CHANNEL
*   PATHS EXIST, THE OPTIONAL CHANNEL PATHS ARE TESTED.  IF THE
*   CHANNEL IS NOT BUSY, CONTROL IS GIVEN TO THE DEVICE-
*   DEPENDENT SIO SUBROUTINE.
*
*   THE CHANNEL SEARCH ROUTINES GIVE CONTROL TO THE CHANNEL RESTART
*   SUBROUTINE TO RESTART A LOGICAL CHANNEL.
*
* PATH ROTATION
*   THE TEST CHANNEL ROUTINES ATTEMPT TO START A DEVICE ON THE MOST
*   LIKELY AVAILABLE PATH, IF THERE ARE TWO OR MORE PATHS TO A
*   DEVICE.  THE METHOD OF DETERMINING THE MOST LIKELY PATH IS
*   DEPENDENT ON THE SYSGENED SYSTEM CONFIGURATION.  THE
*   CONFIGURATION DETERMINES THE PHYSICAL CHANNEL/LOGICAL
*   CHANNEL RELATIONSHIP.  CHANNEL DISTRIBUTION IS AFFECTED ALSO,
*   AS CAN BE SEEN IN THE EXAMPLE.  LOGICAL CHANNEL 5 ALWAYS
*   ATTEMPTS TO START CHANNEL 6; LEAVING CHANNEL 5 AVAILABLE
*   FOR LOGICAL CHANNEL 4.
*   EXAMPLE:
*   LOGICAL CHANNEL 1 HAS PHYSICAL CHANNELS 1 AND 3.
*   LOGICAL CHANNEL 2 HAS PHYSICAL CHANNELS 2 AND 4.
*   LOGICAL CHANNEL 3 HAS PHYSICAL CHANNELS 1 AND 2.
*   LOGICAL CHANNEL 4 HAS PHYSICAL CHANNEL 5.
*   LOGICAL CHANNEL 5 HAS PHYSICAL CHANNELS 5 AND 6.
*        LOGICAL CHANNEL           FIRST PATH ATTEMPTED BY TCH
*               1                             CHAN 3
*               2                             CHAN 4
*               3                    ALTERNATELY CHAN 1 AND 2
*               5                             CHAN 6
*
***********************************************************************
         EJECT
         MEND
