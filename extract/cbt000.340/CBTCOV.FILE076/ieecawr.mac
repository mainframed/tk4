         MACRO
         IEECAWR &SIZE
         TITLE 'IEECMAWR   COMMUNICATIONS TASK WAIT AND ROUTER'
* WAIT AND ROUTER (AWR)
*
* THE FUNCTION OF THIS MODULE IS TO WAIT ON ALL COMMUNICATIONS ECB'S
* (SINGLE EVENT). IT RUNS AS A SUBTASK OF THE MASTER SCHEDULER TCB
* AND GIVES CONTROL TO THE ROUTER FUNCTION WHEN ANY OF THE ECB'S
* ARE POSTED.
* THE ROUTER CHECKS THE ECB'S IN A PRIORITY SCHEME AS FOLLOWS-
* RMS,EXTERNAL,ATTENTION,I/O, WTO/R, DOM, AND LOG INITIALIZATION.THESE
* ECB'S ARE CHECKED IN SUCH A WAY THAT WHENEVER AN ECB IS POSTED AND A
* SERVICE IS PERFORMED, THE ROUTINE RETURNS TO THE BEGINNING (WREXT),
* CHECKS THE RMS ECB AND CONTINUES FROM THERE.  WHEN ALL
* ECB'S HAVE BEEN CHECKED, AND NOTHING IS POSTED, THE ROUTER RETURNS
* TO THE WAIT MODULE.
*
* MODULE NAME- IEECMAWR
*
* ENTRY POINT - IEECMWRT
*
* INPUT- POSTING OF ECB'S
*
* OUTPUT- SERVICING POSTED ECB'S BY GIVING CONTROL TO APPROPRIATE
*         ROUTINES
*
* EXTERNAL ROUTINES -
*        IGCR107B FOR RMS INTERRUPT
*        IGCXL07B FOR EXTERNAL INTERRUPT
*        IEECMDSV FOR ATTENTION,I/O COMPLETE, AND OUTPUT
*        IEECMWSV FOR WTO/R SERVICE
*        IEECMDOM FOR DOM SERVICE
*        IGC0907B FOR SERVICE AFTER THE LOG HAS BEEN INITIALIZED
*
* EXITS- NONE
*
* ATTRIBUTES- REFRESHABLE, REENTRANT
*
         EJECT
         SPACE 5
IEECMAWR CSECT
*APARS                                                           A26818
*                                                                A26940
*OWNERSHIP    19372                                                 MCH
*MODULE CREATED FOR MCS - REL 18
         ENTRY IEECIRFC
         ENTRY IEECIRUP
         ENTRY IEECIR45
         ENTRY WRABXLE
         EXTRN IEECMDSV
         EXTRN IEECMWSV
         EXTRN IEECMDOM
         USING CVTDSECT,10
R0       EQU   0                   WORKREG
R1       EQU   1                   INIT-EIL BASE  ROUTER-PARAMETER REG
R2       EQU   2                   INIT-BASE DSECT REG  ROUTER-UCM BASE
R3       EQU   3                   INIT-UCM BASE  RTR-RESV FOR DEVSERV
R4       EQU   4                   INIT-WORKREG  ROUTER-MCS UCM BASE
R5       EQU   5                   INIT-WORKREG  ROUTER-INDEX REG
R6       EQU   6                   WORKREG
R7       EQU   7                   WORKREG
R8       EQU   8                   INIT-WORKREG  ROUTER-EIL BASE
R9       EQU   9                   BASE REG
R10      EQU   10                  RESERVED FOR WTOSERV
R11      EQU   11                  UCM ENTRY
R12      EQU   12                     BXLE
R13      EQU   13                   REGS
R14      EQU   14                  RETURN REG
R15      EQU   15                  BRANCH REG
ZEROBYTE EQU   X'00'
HEX40    EQU   X'40'
EIGHTY   EQU   X'80'
TWO      EQU   X'02'                                             A26818
FD       EQU   X'FD'                                             A26818
SWRTN    EQU   72
ZERO     EQU   0
ONE      EQU   1
FOUR     EQU   4
EIGHT    EQU   8
SIXTEEN  EQU   16
MASK255  EQU   255                 USE TO SWITCH BITS IN A MASK
TCBDAR   EQU   172                                                  DAR
DARREC   EQU   X'40'               SECONDARY RECURSION BIT          DAR
TCBPTR   EQU   8                                                    DAR
         USING BASE,R2
         USING UCMXECB,R3
         DS    0D
IEECIR45 DC    CL8'IEECMAWR'       THIS ROUTINE
         DC    AL2(100)                THIS SERIES OF DC'S
         DC    X'00C0'                 FORM THE COMMUNICATION TASK
         DC    A(IEECMWRT)             REQUEST BLOCK
         DC    XL4'00040000'       FLAG
         DC    A(IEECMWRT)         ENTRY PT TO THIS MODULE
         DC    A(0)                ZERO
         DC    V(IEACMTCB)         TCB
      EJECT
***********************************************************************
*                                                                     *
*        IEECMWRT GETS CONTROL DURING SYSTEM INITIALIZATION           *
*        IT SETS UP ENVIRONMENTAL REGISTERS FOR ALL COMMUNICATIONS    *
*        TASK RESIDENT ROUTINES, AND ISSUES A WAIT UNTIL              *
*        INITIALIZATION IS FINISHED.  IT THEN GIVES CONTROL           *
*        TO CTWSTART                                                  *
*                                                                     *
***********************************************************************
IEECMWRT EQU   *
         ENTRY IEECMAA0                                             MCH
         ENTRY IEECMAA1                                             MCH
IEECMAA0 EQU   *                                                    MCH
         BALR  R9,R0               SET UP BASE REGISTERS
         USING *,9
         L     R10,CVTPTR              LOAD CVT POINTER
         L     R3,CTWCUCM              LOAD UCM POINTER
         ST    R3,CVTCUCB              STORE UCM PTR TO CVT
         L     R2,CVTMSER              GET MSER PTR
         L     TCBPTR,ZERO(R10)    OBTAIN ADDRESS OF                DAR
         L     TCBPTR,FOUR(TCBPTR) CURRENT TCB                      DAR
         TM    TCBDAR(TCBPTR),DARREC DAR REINITIALIZED THIS TASK    DAR
         BO    CTWFIX              YES - BRANCH                     DAR
         LA    R1,BAIPL                SET TO WAIT ON ECB IN MS RES
*                                      DATA AREA FOR POST BY IEFSD569
         NI    ZERO(R1),ZEROBYTE   CLEAR WAIT-POST BYTE
         WAIT  ECB=(R1)
CTWFIX   LR    R2,R3                   RESTORE UCM PTR
         DROP  R3
         USING UCMXECB,R2
         NI    ASYN(TCBPTR),X'FE' .TURN OFF PROB. ASYN EXIT   BB A43808
*                                  BIT SO ROLL-DELETE WILL    BB A43808
*                                  WORK.                      BB A43808
ASYN     EQU   X'1D'                                          BB A43808
         L     R3,UCMVEA           INITIALIZE UCM ENTRY PTR
         LA    R5,UCMXECB          INITIALIZE
         LA    R4,FOUR             UCM
         SR    R5,R4               PREFIX
         L     R4,ZERO(R5)         BASE REG
         L     R8,UCMLSTP          INITIALIZE EIL PTR
         USING EIL,R8
         USING UCMLIST,R11
         USING UCMMCENT,R4
***********************************************************************
*                                                                     *
*        CTWSTART ISSUES A WAIT ON ALL COMMUNICATIONS ECB'S.          *
*        THIS BECOMES THE ENTRY POINT OF THE WAIT MODULE AFTER        *
*        IEECMWRT EXITS.                                              *
*                                                                     *
***********************************************************************
CTWSTART EQU   *
         LA    R1,EILSL                 POINT R1 TO ECB LIST
         LR    R6,R1
ECBLOOP  EQU   *
         L     R7,ZERO(R6)        ADDRESS THE ECB
         NI    ZERO(R7),WAITOFF   TURN WAIT BIT OFF           BE A44624
WAITOFF  EQU   X'7F'                                          BE A44624
         TM    ZERO(R6),EIGHTY    IS THIS THE LAST ECB
         BO    LASTECB            YES
         LA    R6,FOUR(R6)        NEXT ECB
         B     ECBLOOP            TEST NEXT ECB
LASTECB  EQU   *
CTWAIT   WAIT  ECBLIST=(1)
      EJECT
*
*                   ROUTER   MODULE
*
**********************************************************************
*                                                                     *
*        SERVICE EXTERNAL INTERRUPTS BY GOING TO IGCXL07B FOR         *
*        EVERY INTERRUPT                                              *
*                                                                     *
**********************************************************************
         SSM   DISABLE
WREXT    EQU   *                                                  20061
RMSCHK   EQU   *                                                  20061
         TM    UCMRECB,POST        RMS ECB POSTED                 20061
         BZ    WREXT1              NO-CONTINUE NORMAL PROCESSING  20061
         LA    R1,ADRMSNAM         GET ADDRESS OF RMS MODULE      20061
         L     R0,UCMRECB          SAVE ADDRESS IN PARAMETER REG  20061
         XC    UCMRECB(FOUR),UCMRECB CLEAR ECB                    20061
         SVC   SWRTN               ISSUE SVC 72 TO ROUTER         20061
         B     WREXT               CONTINUE SEARCH FOR POSTED ECB 20061
WREXT1   EQU   *                                                  20061
         CLI   UCMXCT,ZERO         HAS EXTERNAL ECB BEEN POSTED   20061
         BE    WRATTN              NO, CHECK ATTENTION ECB
         SR    R6,R6
         IC    R6,UCMXCT           GET NUMBER OF TIMES POSTED
         BCTR  R6,R0               DECREMENT BY ONE
         STC   R6,UCMXCT           STORE NEW COUNT OF UNSERVICED POSTS
         MVC   UCMXSA+EIGHT(R8),WREXTNAM  NAME OF EXT. SWITCH RTN
         MVI   UCMXSA+SIXTEEN,HEX04     INDICATE EXTERNAL INTERRUPT
         LA    R1,UCMXSA+EIGHT
         SVC   SWRTN               SVC 72 - GO TO SWITCH RTN
         B     WREXT               SERVICE ANY UNSERVICED EXTERNAL INT.
**********************************************************************
*                                                                     *
*        GO THROUGH CONSOLE ENTRIES,AND WHENEVER AN ATTENTION IS      *
*        PENDING, GO TO ROUTINE WRATSERV.                             *
*                                                                     *
**********************************************************************
WRATTN   XC    UCMXECB(FOUR),UCMXECB   CLEAR EXTERNAL ECB
         TM    UCMAECB,POST        HAS ATTENTION ECB BEEN POSTED
         BZ    WRIOCOMP            NO,CHECK I/O ECB'S
         TM    UCMSFLGS+ONE,TWO   WAS ATTN POSTED BY WTL         A26818
         BNO   WRALOOP1           NO - REGULAR ATTENTION         A26818
         LA    R1,EIGHT                                          A26818
         NI    UCMSFLGS+ONE,FD    TURN OFF FLAG                  A26818
         B     WRAWTLSV           GO TO DSV                      A26818
WRALOOP1 LM    R11,R13,UCMVEA     INITIALIZE BXLE UCM ENTRY REGS A26818
WRALOOP  TM    UCMSTS,UCMAF        IS ATTENTION PENDING FOR THIS ENTRY
         BNO   WRABXLE            NO,BRANCH TO GET NEXT ENTRY    A33847
         TM    UCMATR,ACTIVE      IS THIS DEVICE ACTIVE ?        A33847
         BO    WRATSERV            YES, BRANCH
         NI    UCMSTS,MASK255-ATTN NO-TURN OFF ATTENTION FLAG    A33847
WRABXLE  BXLE  R11,R12,WRALOOP     NO,GET NEXT ENTRY
         XC    UCMAECB(FOUR),UCMAECB    CLEAR ATTENTION ECB
**********************************************************************
*                                                                     *
*        GO THROUGH LIST OF I/O ECB'S, AND WHENEVER ONE IS POSTED     *
*        COMPLETE, GO TO ROUTINE WRIOSERV.                            *
*                                                                     *
**********************************************************************
WRIOCOMP LM    R11,R13,UCMVEA      GET BXLE ENTRY REGS
         SR    R6,R6
WRIOLOOP EQU   *
         L     R7,EILIOL(R6)       GET ADDRESS OF THIS ENTRY'S ECB
         TM    ZERO(R7),POST       IS IT POSTED COMPLETE
         BO    WRIOSERV            YES, BRANCH
WRIOBXLE LA    R6,FOUR(R6)         INCREMENT ECB PTR ADDRESS
         BXLE  R11,R12,WRIOLOOP    AND ENTRY ADDR TO DO NEXT ENTRY
         EJECT
**********************************************************************
*                                                                     *
*        IF WE ARE STILL PROCESSING CONSOLE OUTPUT QUEUES,            *
*        CONTINUE PROCESSING BY GOING TO IEECMDSV WITH OUTPUT CODE.   *
*        (REGISTER 3 POINTS TO NEXT CONSOLE ENTRY TO BE PROCESSED)    *
*                                                                     *
**********************************************************************
WRDVSERV C     R3,UCMVEA           ARE WE IN THE MIDDLE OF 'DEVSERV'
         BNE   WROUTPUT            YES                        BG A43719
         TM    UCMSFLGS+ONE,UCMSYSJ NO, IS THERE H.C. TO DO   BG A43179
         BZ    WRWTO               NO CHECK WTO/R ECB         BG A43719
         NI    UCMSFLGS+ONE,MASK255-UCMSYSJ TURN OFF H.C.FLAG BG A43719
WROUTPUT EQU   *                                              BG A43719
         L     R15,ADDVSV          YES, GO BACK TO 'DEVSERV'
         SR    R1,R1               CODE = 0 FOR OUTPUT
         BALR  R14,R15             GO TO 'DEVSERV'
         B     WREXT               RETURN TO CHECK EXTERNAL ECB
**********************************************************************
*                                                                     *
*        IF WE ARE NOT STILL PROCESSING CONSOLE OUTPUT QUEUES,        *
*        SERVICE A POSTED WTO/R ECB BY GOING TO IEECMWSV.             *
*        NOTE- WHEN IEECMWSV FINISHES QUEUEING, IT GOES TO IEECMDSV   *
*        TO START PROCESSING CONSOLE OUTPUT QUEUES. THIS GETS US      *
*        INTO THE PREVIOUS LOOP, AND THE WTO/R ECB IS NOT CHECKED     *
*        AGAIN UNTIL ALL CONSOLE QUEUES HAVE BEEN PROCESSED.          *
*                                                                     *
**********************************************************************
WRWTO    TM    UCMOECB,POST        IS WTO/R ECB POSTED
         BZ    WRCLNUP             NO,DO NEXT SERVICE
         XC    UCMOECB(FOUR),UCMOECB   YES,CLEAR ECB
         L     R15,ADWTOSV         GET ADDRESS OF WTO/R SERVICE
         BALR  R14,R15             GO TO WTO/R SERVICE
         B     WREXT               RETURN TO CHECK EXTERNAL ECB
**********************************************************************
*                                                                     *
*        IF THE SYSTEM WQE QUEUE NEEDS TO BE CONSOLIDATED,            *
*        GO TO IEECMDSV WITH CLEANUP CODE.                            *
*                                                                     *
**********************************************************************
WRCLNUP  TM    UCMSFLGS+ONE,CLEANUP IS CLEANUP SERVICE NEEDED
         BZ    WRDOM               NO,DO NEXT SERVICE
         LA    R1,EIGHT            YES, CODE=4 FOR CLEANUP
         L     R15,ADDVSV          GET ADDRESS OF DEQ RTN
         NI    UCMSFLGS+ONE,MASK255-CLEANUP  TURN OFF CLEANUP INDICATOR
         BALR  R14,R15             GO TO DEQ RTN
         B     WREXT               RETURN TO CHECK EXTERNAL ECB
         EJECT
**********************************************************************
*                                                                     *
*        IF THE DOM ECB HAS BEEN POSTED, GO TO THE DOM SERVICE RTN    *
*        (IEECMDOM).                                                  *
*                                                                     *
***********************************************************************
WRDOM    TM    UCMDECB,POST        IS DOM ECB POSTED
         BZ    WRLOG               NO, GO TO SYSLOG CHECKER ROUTINE MCS
         SR    R0,R0              CLEAR INPUT REG 0 FOR DOM      A26940
         L     R15,ADDOM           YES, GET ADDRESS OF DOM SERVICE
         BALR  R14,R15             GO TO DOM SERVICE
         B     WREXT               RETURN TO CHECK EXTERNAL ECB
**********************************************************************
*                                                                     *
*        IF AN ATTENTION OR I/O COMPLETE IS PENDING, SERVICE BY       *
*        GOING TO IEECMDSV.                                           *
*                                                                     *
***********************************************************************
WRIOSERV OI    UCMDEVC,IOCOMP      CODE=4 FOR ATTN OR I/O
WRATSERV LA    R1,FOUR             CODE=4 FOR ATTN OR I/O
WRAWTLSV EQU   *                                                 A26818
         L     R15,ADDVSV          GET ADDR OF DEVSERV
         BALR  R14,R15             GO TO DEVSERV
         B     WREXT               RETURN TO CHECK EXTERNAL ECB
***********************************************************************
*                                                                     *
*        IF THE LOG INITIALIZATION ECB HAS BEEN POSTED, GO TO SERVICE *
*        ROUTINE (IGC0907B) TO CHECK IF HARDCOPY IS SYSLOG            *
*                                                                     *
***********************************************************************
WRLOG    TM    UCMNPECB,POST       HAS LOG JUST BEEN INITIALIZED    MCS
         BZ    CTWSTART            NO, GO TO WAIT MODULE            MCS
         LA    R1,WTL4NIP          YES, GET ADDR OF SYSLOG SERVICE  MCS
         SVC   SWRTN               SVC72 - IS HARDCOPY SYSLOG       MCS
         XC    UCMNPECB(FOUR),UCMNPECB  CLEAR LOG ECB IN UCM        MCS
         B     WREXT               GO TO CHECK EXTERNAL ECB         MCS
         EJECT
CLEANUP  EQU   X'80'
IOCOMP   EQU   X'08'
POST     EQU   X'40'
ADRMSNAM DC    CL8'IGCR107B'       NAME OF RMS MODULE             20061
WREXTNAM DC    CL8'IGCXL07B'       NAME OF CONSOLE SWITCH ROUTINE
WTL4NIP  DC    CL8'IGC0907B'       NAME OF SYSLOG CHECKER ROUTINE   MCS
DISABLE  DC    X'00'
HEX04    EQU   X'04'
ACTIVE   EQU   X'10'                                             A33847
ATTN     EQU   X'80'                                             A33847
ADDVSV   DC    A(IEECMDSV)         DEVICE SUPPORT
ADWTOSV  DC    A(IEECMWSV)         WRITE SERVICE
ADDOM    DC    A(IEECMDOM)         DOM RTN
CTWCUCM  DC    V(IEECVUCM)         UCM
IEECMAA1 EQU   *                                                    MCH
         SPACE  3
*                                      BOUNDARY BOX DEFINITION
         DS    0D
IEECIRFC DC    A(0)                ZERO
         DC    A(IEECIRUP-IEECIRFC) BOUNDARY BOX SIZE
         DC    &SIZE.X'00'        INFORMATION STORAGE
         DS    0D
IEECIRUP EQU   *
         EJECT
EIL      DSECT
         IEECVMUG      EIL
         SPACE 3
UCM      DSECT
         IEECUCM
WQE      DSECT
         IEECVMUG    WQE
CVTDSECT DSECT
         CVT
         IEEBASEB
         MEND
