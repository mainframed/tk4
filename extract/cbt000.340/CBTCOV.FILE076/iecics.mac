         MACRO
   IECICS  &CHAN,&DATOT,&M65MP,&SLCHS,&ALTSW,&ALTSWA,&PRI,&RESREL,     *
               &TBASE,&SSDA,&MLCHPRI                             A36305
         GBLA  &SSCCW(16),&CCW(16)
         LCLA  &COUNT,&LCHNS(60),&LCOUNT,&COUNT2
         LCLA  &NUM,&NUM2
         LCLC  &LABEL,&LCHADR(30)
         AIF   (&DATOT EQ 0).LCH11
&NUM     SETA  1
         AIF   (&SSDA(&NUM) EQ 0).NOTC
&SSCCW(&CHAN+1)  SETA  1
.NOTC    ANOP
&NUM     SETA  &NUM+1
         AIF   (&SSDA(&NUM) EQ 0).NOTD
&SSCCW(&CHAN+1)  SETA  &SSCCW(&CHAN+1)+2
.NOTD    ANOP
&NUM     SETA  &NUM+1
         AIF   (&SSDA(&NUM) EQ 0).NOTE
&SSCCW(&CHAN+1)  SETA  &SSCCW(&CHAN+1)+4
.NOTE    ANOP
&NUM     SETA  &NUM+1
         AIF   (&SSDA(&NUM) EQ 0).NOTF
&SSCCW(&CHAN+1)  SETA  &SSCCW(&CHAN+1)+8
.NOTF    AIF   (&SSCCW(&CHAN+1) EQ 0).NORES
         AIF   (&RESREL EQ 0).NORES
&SSCCW(&CHAN+1)  SETA  &SSCCW(&CHAN+1)+16
.NORES   AIF   (&SSCCW(&CHAN+1) NE 0).LCH11
&CCW(&CHAN+1) SETA  1
         AIF   (&RESREL EQ 0).LCH11
&CCW(&CHAN+1)  SETA  &CCW(&CHAN+1)+16
.LCH11   ANOP                                                      RRSS
&NUM     SETA  0
&NUM2    SETA  0
&COUNT   SETA  0
&COUNT2  SETA  0
         SPACE 1
INTCS&CHAN    DS    0C
.*      THIS LOOP DETERMINES THE LCHS -HOW MANY AND WHAT NUMBERS
         AIF   (T'&SLCHS EQ 'O').MPXSRH  TEST FOR MPX CHAN
&COUNT2  SETA  1                        INITIALIZE COUNTER
         AGO   .LCHFND2                 SKIP FIRST UPDATE
.LCHFND  ANOP
&COUNT2  SETA  &COUNT2+2                UPDATE THRU SLCH LIST
.LCHFND2 ANOP
&COUNT   SETA  &COUNT+1                UPDATE COUNTER
         AIF   (&COUNT2+1  EQ  N'&SLCHS).MVCCODE  TEST END OF LIST
         AIF   (&SLCHS(&COUNT2+1)  EQ  0).LCH3   CHECK IF LCH ASSIGNED
&LCOUNT  SETA  &LCOUNT+1               UPDATE NUM OF LCHS THIS CHAN
&LCHNS(&LCOUNT) SETA &COUNT+&SLCHS(1)  SET PROPER LCH NUMBER
.LCH3   AIF   (&SLCHS(&COUNT2+2)  EQ  0).LCHFND   CHECK FO R DA SK DEV
         LA    LCHREG,LCH&LCHNS(&LCOUNT)   SET LCH FOR SEEK DEV
         BAL   LNKRG2,INT036C           TO START AT TOP OF QUEUE
*    VECTOR ENTRY FOR NEXT IN QUEUE
         BC    15,INTCIC                TO PICK UP NEXT RQE
*    VECTOR ENTRY FOR NEXT LCH
         AGO   .LCHFND                 RETURN TO FIND LOOP
.MVCCODE ANOP       THIS IS THE INITIALIZATION CODE FOR SEARCH
&COUNT   SETA  0                       RESET COUNTER
**       THIS CODE SETS THE SEARCH LOGIC FOR ALL I/O DATA TRANSFER
         AIF   (&LCOUNT LT 2).LCH4
         LH    WKREG1,INTCS&CHAN.A      SAVE FIRST ENTRY
&NUM     SETA  &LCOUNT*2-2
         MVC   INTCS&CHAN.A(&NUM),INTCS&CHAN.A+2  MOVE UP LIST
         STH   WKREG1,INTCS&CHAN.A+&NUM MAKE FIRST LAST
&NUM     SETA  0
.LCH4    ANOP
&COUNT   SETA  &COUNT+1
         AIF   (&PRI EQ 1 AND &LCOUNT GT 1).MVCLOOP               PRQ1
         AIF   (&COUNT GT &LCOUNT).LCH5  ALL DONE
         AIF   (&LCOUNT LT 2).LCH9
         LH    LCHREG,INTCS&CHAN.A+&NUM GET LCH ADDR.
         AIF   (&TBASE EQ 0).TBS010                                IMP3
         AR    LCHREG,BASREG            ADJUST ADDRESS           BBBBBB
.TBS010  ANOP                                                      IMP3
&NUM     SETA  &NUM+2                   INCR. LCH POINTER
&NUM2    SETA  &NUM2+1                  INCR LIST ID
&LCHADR(&NUM2)  SETC   'LCH'.'&LCHNS(&COUNT)'  SET ENTRY FOR DC LIST
         AGO   .LCH10
.LCH9    LA    LCHREG,LCH&LCHNS(&COUNT)  SET PROPER LCH ADDR
.LCH10   BAL   LNKRG2,INT034C           TEST FOR STARTABLE DEVICE
*    VECTOR ENTRY FOR NEXT IN QUEUE
         BC    15,INT034D               TO PICK-UP NEXT RQE
*    VECTOR ENTRY FOR NEXT LCH
         AGO   .LCH4                    CONTINUE WITH LCHS
.LCH5    ANOP                           LAST ENTRY IN SEARCH
         BC    15,INT033                EXIT FROM CHANNEL SEARCH
         SPACE 2                                                  PRQ1
         AIF   (&LCOUNT LT 2).LCHEND    NEED LIST?
&LABEL   SETC  'INTCS&CHAN.A'
&LABEL   DS    0C
&NUM     SETA  1
.LCHCON  DC    AL2(&LCHADR(&NUM)-IOSBASE)    LCH ADDRESS         BBBBBB
&NUM     SETA  &NUM+1
         AIF   (&NUM-1 NE &LCOUNT).LCHCON
         SPACE 2                                                  PRQ1
.LCHEND  AIF   (&MLCHPRI EQ 1).LASTCHK  IF PRIORITY QUEUING AND  A36305
.*   MULTIPLE LCHS WITH PRIORITY, GO GENERATE PRIORITY COMPARE.  A36305
         MEXIT                                                     PRQ1
.*   CODE FOR PRIORITY LCHS                                       PRQ1
.MVCLOOP ANOP
&COUNT   SETA  &COUNT+1                UPDATE COUNTER
         AIF   (&COUNT GT &LCOUNT).SRCODE  END OF LIST CHECK
         MVC   LCH&LCHNS(&COUNT)+LCHSRH(2),LCH&LCHNS(&COUNT)       PRQ1
&LCHADR(&COUNT) SETC   'LCH'.'&LCHNS(&COUNT)'                      PRQ1
         AGO   .MVCLOOP
.SRCODE  ANOP       THIS IS SEARCH CODE -PER CHANNEL
&COUNT   SETA  1                       RESET COUNTER
&LCHADR(&COUNT) SETC   'LCH'.'&LCHNS(&COUNT)'                     PRQ1
         LA    LCHREG,LCH&LCHNS(&COUNT)                           PRQ1
         LR    TSTREG,LCHREG                                      PRQ1
INTCS&CHAN.B  MVC   LCHSRH(2,LCHREG),TSTLNK(TSTREG)               PRQ1
         LH    LCHREG,INTCS&CHAN.A                                PRQ1
         AIF   (&TBASE EQ 0).TBS020                                IMP3
         AR    LCHREG,BASREG            ADJUST ADDRESS           BBBBBB
.TBS020  ANOP                                                      IMP3
         LH    TSTREG,LCHSRH(LCHREG)                              PRQ1
.SRCLOOP ANOP            COMPARE LOOP FOR MORE THAN ONE LCH
         AIF   (&COUNT EQ &LCOUNT).SRCOMN      TO COMMON CODE/PER CHAN
&COUNT   SETA  &COUNT+1                UPDATE COUNTER
&NUM     SETA &NUM+2                                              PRQ1
         LH    WKREG1,INTCS&CHAN.A+&NUM                           PRQ1
         AIF   (&TBASE EQ 0).TBS030
         AR    WKREG1,BASREG            ADJUST ADDRESS           BBBBBB
.TBS030  ANOP                                                      IMP3
         LH    WKREG2,LCHSRH(WKREG1)                              PRQ1
         AIF   (&ALTSW NE 1).SRCOMN    CHECK FOR ANY ALTERNATES
         BAL   LNKRG2,CSCHCMP          TO COMMON COMPARE ROUTINE
         AGO   .SRCLOOP                RETURN TO COMPARE FOR LAST
.SRCOMN  ANOP            THIS IS COMMON CODE FOR EACH SEARCH MODULE
         BAL   LNKRG2,INT034                                      PRQ1
         BC    15,INTCS&CHAN.B                                    PRQ1
         AGO   .LCH5                                              PRQ1
.LASTCHK ANOP                                                     PRQ1
&COUNT   SETA  N'&SLCHS                SET UP FOR COMPARE RTN TEST
         AIF   (&SLCHS(&COUNT) EQ 1).COCOMP  ALT PATH SWITCH ON
         MEXIT                         NO-EXIT FROM MACRO
.COCOMP  ANOP                   COMMON COMPARE CODE AND RQE UPDAAE
CSCHCMP  LTR   WKREG2,WKREG2           IS TEST REQUEST DUMMY
         BCR   4,LNKRG2                YES,RETURN
         LTR   TSTREG,TSTREG           IS FIRST REQUEST DUMMY
         BC    4,CSCHCMPA              YES, SWITCH RQE'S
         CLC   TSTPR(1,TSTREG),TSTPR(WKREG2)     TEST FOR HIGH
         BCR   10,LNKRG2               FIRST IS HIGH-RETURN
CSCHCMPA LR    TSTREG,WKREG2           SWITCH-TEST IS HIGHER
         LR    LCHREG,WKREG1           SWITCH CURRENT LCH WITH NEW
         BCR   15,LNKRG2               RETURN
         SPACE 2
         MEXIT                                                    PRQ1
.MPXSRH  ANOP           THIS IS MULTIPLEXOR SEARCH CODE
         AIF   (&DATOT EQ 0).LCH7       ANY DA ON MPLX
         LA    LCHREG,LCHTAB            MPLX LCH
         BAL   LNKRG2,INT036C           TO START AT TOP OF QUEUE
*    VECTOR ENTRY FOR NEXT IN QUEUE
         BC    15,INTCIC                TO PICK UP NEXT IN QUEUE
*    VECTOR ENTRY FOR NEXT LCH
.LCH7    ANOP
         LA    LCHREG,LCHTAB            INITILIZE TO 1ST LCHWORD
         BAL   LNKRG2,INT034C           TO START AT TOP OF QUEUE
*    VECTOR ENTRY FOR NEXT IN QUEUE
         BC    15,INT034D               TO PICK-UP NEXT DTR IN QUEUE
*    VECTOR ENTRY FOR NEXT LCH
         BC    15,INT033               RETURN WHEN RQE IS DUMMY
         EJECT
         MEND
