         MACRO
         SGIEF001 &ACNREQ,&PRGREQ
IEFCC    DSECT
         IEFACOMM
         IEFAJCTB
         IEFASCTB
         IEFASIOT
         IEFJFCBN
         EJECT
IEFJM    CSECT
***********************************************************************
*                                                                     *
*                        JOB CARD HEADER ROUTINE                      *
*                                                                     *
***********************************************************************
* FUNCTION-                                                           *
*        1. INITIALIZE JOB CONTROL TABLE VALUES.                      *
*        2. INITIALIZE ACCOUNT CONTROL TABLE.                         *
*        3. DRIVE SUBROUTINE TO START AND ASSIGN QUEUE ADDRESSES.     *
*        4. DRIVE SUBROUTINES TO PROCESS JOB CARD POSITIONAL AND      *
*           KEYWORD PARAMETERS.                                       *
* ENTRY POINT-                               FROM- CSECT IEFMF        *
*        CSECT IEFJM- TO PROCESS JOB CONTROL                          *
*                     STATEMENT                                       *
* INPUT-                                                              *
*        REGISTERS-                                                   *
*              INPR1 - STARTING ADDRESS OF THE JOBNAME ON THE         *
*                      STATEMENT                                      *
*              INPR2 - LENGTH IN BYTES OF THE JOBNAME                 *
*              INPR3 - STARTING ADDRESS OF THE STATEMENT              *
*                      PARAMETER LIST                                 *
*              INPR4 - LENGTH IN BYTES OF THE PARAMETER LIST          *
*                                                                     *
*        SWITCHES-                                                    *
*              INJMSWA-                                               *
*                   INJMANRD  BIT-0/ACCOUNT NUMBER RECEIVED           *
*                   INJMPNRD  BIT-2/PROGRAMMER NAME RECEIVED          *
*              INCMSWB-                                               *
*                   INCMCXP   BIT-3/CONTINUATION EXPECTED             *
*              INCMSWD-                                               *
*                   INCMNCON  BIT-7/NO CONTINUATION                   *
*              INAMSWA-                                               *
*                   INAMCONT  BIT-1/ACCOUNTING CONTINUATION           *
*                   INAMERBO  BIT-3/CONTINUATION ERROR IN BREAKOUT    *
*                   INAMCDCT  BIT-5/COND= FIELD CONTINUATION          *
*                   INAMCDER  BIT-6/COND= FIELD ERROR                 *
*                                                                     *
*                                                                     *
* OUTPUT-                                                             *
*        REGISTERS- NONE                                              *
*                                                                     *
*        SWITCHES-                                                    *
*              INJMSWA-                                               *
*                   INCMLVLR  BIT-5/MESSAGE LEVEL RECEIVED            *
*                                                                     *
*              INCMSWA-                                               *
*                   INCMDFSH  BIT-5/DATA FLUSH                        *
*                   INCMJFSH  BIT-6/JOB FLUSH                         *
*                                                                     *
* EXTERNAL REFERENCES                                                 *
*        1. V(IEFCOND)  COND= SUBROUTINE                              *
*        2. V(IEFAM)    ACCOUNTING INFORMATION SUBROUTINE             *
*        3. V(IEFMI)    Q-MANAGER LINKAGE SUBROUTINE                  *
*        4. V(IEFMF)    CONTROL ROUTINE VERB ID SUBROUTINE            *
*        5. V(IEFWMSG)  MESSAGE OUTPUT SUBROUTINE                     *
*        6. V(IEFBM)    BREAKOUT SUBROUTINE                           *
*        7. V(IEFN1)  TYPRUN= SUBROUTINE                              *
*        8. V(IEFNX)  MSGLEVEL= SUBROUTINE                            *
*        9. V(IEFNY)  PRTY= SUBROUTINE                                *
*       10. V(IEFNZ)  MSGCLASS= SUBROUTINE                            *
*                                                                     *
* EXIT-                                                               *
*        NORMAL-INJMNEJ4 (STATEMENT LABEL)                            *
*                                                                     *
*        ERROR- NONE                                                  *
*                                                                     *
* ERROR MESSAGES-                                                     *
*        1. IEF106I NO JOB NAME, JOB FAILED                           *
*        2. IEF107I JOB NAME OVER 8 CHARS.                            *
*        3. IEF110I KEYWORD NOT IN TABLE                              *
*        4. IEF111I KEYWORD NOT SUPPORTED                             *
*        5. IEF112I NO ACCOUNT NUMBER, JOB FAILED (SYSTEM OPTION)     *
*        6. IEF113I PROGRAMMER'S NAME MISSING, JOB FAILED (SYSTEM OPT)*
*                                                                     *
* TABLES/WORK AREAS-                                                  *
*        1. JOB CONTROL TABLE                                         *
*        2. ACCOUNT CONTROL TABLE                                     *
*        3. INTERPRETER WORK AREA                                     *
*                                                                     *
***********************************************************************
         USING IEFJCARD,INBSE
         TM    INJMSWA,X'02'
         BO    INJM9999
         STM   1,15,INJESAVE            SAVE REGISTERS
         BALR  INBSE,0
         USING *,INBSE                  SET UP BASE REGISTER
IEFJCARD EQU   *
         TM    INCMSWB,INCMCXP          IS CONTINUATION EXPECTED
         BC    INALZERO,INJMNCC1        NO
         NI    INCMSWB,INALL-INCMCXP    INDICATE CONT. RECEIVED
         TM    INAMSWA,INAMCONT         IS ACCOUNTING RTN EXPECT. CONT
         BC    INALLONE,INJMNCF5       YES
         TM    INAMSWA,INAMCDCT         IS COND= RTN EXPECTING CONT.
         BZ    INJMNCC4                 NO
         L     INTRY,IEFCONDV
         LA    INPR5,1                  CONSTANT OF 1
         OI    INJMSWA,X'02'            INDICATE FROM JOB
         B     INJMNEAA
INJMNCC4 TM    INCMSWD,INCMNCON         IS THIS A CONT. STMNT
         BO    INJMNEB2                 NO
         LA    INPR5,1
         BC    INALWAYS,INJMNDA1        YES
INJMNCC1 MVI   INJMSWA,X'00'            TURN OFF ALL SWITCHES
         MVI   JCTDSKAD,X'00'           CLEAR OUT JCT
         MVC   JCTDSKAD+1(179),JCTDSKAD
         LR    INWK3,INPR3              SAVE REG 3
         LA    INPR3,INJMJCT            JCT ADDRESS FOR ASSIGN & START
         SR    INPR5,INPR5              ZERO INPR5 TO INDICATE START
         L     INTRY,INJMQULK
         BALR INRTN,INTRY
         L     INWK1,INTTRSCT
         ST    INWK1,JCTSDKAD
         L     INWK1,INTTRSMB
         ST    INWK1,JCTSMBAD
         L     INWK1,INTTRACT
         ST    INWK1,JCTACTAD
         MVC   JCTDSKAD(3),INTTRJCT     DISK ADDRESS TO JC\
         STC   INPR5,JCTIDENT           JCT ID
         MVI   JCTJSRNO,X'01'           FILL IN JOB SERIAL NO
         LR    INPR3,INWK3              RESTORE REG 3
         LA    INPR5,1
         MVI   ACTDSKAD,X'00'           ZERO
         MVC   ACTDSKAD+1(179),ACTDSKAD   ACT
         MVI   ACTPRGNM,SSSBLANK        BLANK
         MVC   ACTPRGNM+1(19),ACTPRGNM   PROGRAMMERS NAME
         MVI   JCTJMGLV,X'10'           SET MESSAGE LEVEL TO 1
         STM   INPR1,INPR2,INAMMGSV     SAVE PARAMETER REGS
         SR    INPR1,INPR1
         IC    INPR1,DCBLRECL           LENGTH OF STATEMENT
         L     INPR2,INSTMT             STATEMENT ADDRESS
         L     INTRY,INJMMRCN
         BALR  INRTN,INTRY
         LM    INPR1,INPR2,INAMMGSV     RESTORE REGISTERS
         MVI   JCTJNAME,SSSBLANK        BLANK JOB NAME
         MVC   JCTJNAME+1(7),JCTJNAME     IN JCT
         LA    INPR5,1                  SET UP CONSTANT OF 1
         LTR   INPR2,INPR2              IS THERE A NAME ON STATEMENT
         BC    INNOTZRO,INJMNCF1        YES
         STM   INPR1,INPR2,INAMMGSV     SAVE REGS
         SR    INPR2,INPR2
         LA    INPR1,161                INTERNAL MESSAGE NBR 161
**  ERROR  **  NO JOB NAME
         L     INTRY,INJMMRCN
         BALR  INRTN,INTRY
         LM    INPR1,INPR2,INAMMGSV     RESTORE REGISTERS
         MVI   JCTJSTAT,X'04'           INDICATE JOB FAILED
         OI    INCMSWA,INCMDFSH         TURN ON DATA FLUSH SWITCH
         BC    INALWAYS,INJMNCJ1
INJMNCF1 CH    INPR2,IT8H               IS NAME OVER 8 CHARS
         BC    INNOTHI,INJMNCG1         NO
         STM   INPR1,INPR2,INAMMGSV     SAVE REGS
         SR    INPR2,INPR2              ZERO REG.
         LA    INPR1,162                INTERNAL MESSAGE NBR 162
**  ERROR  **  JOB NAME OVER 8 CHARACTERS
         L     INTRY,INJMMRCN
         BALR  INRTN,INTRY
         LM    INPR1,INPR2,INAMMGSV     RESTORE REGISTERS
         MVC   JCTJNAME(8),0(INPR1)     MOVE FIRST 8 CHARS TO JCT
         BC    INALWAYS,INJMNCH1
INJMMVNM MVC   JCTJNAME(1),0(INPR1)
INJMNCG1 SR    INPR2,INPR5              ADJUST NAME LENGTH FOR MOVE
         EX    INPR2,INJMMVNM           MOVE JOB NAME TO JCT
INJMNCH1 MVI   JCTJSTAT,X'00'           INDICATE IN JCT A GO SETUP JOB
         MVI   JCTJMGPO,C'A'            SET STANDARD MESSAGE CLASS
**  MOVE OTHER STANDARD VALUES TO JCT
INJMNCJ1 DS    0C
**  MOVE TERMINAL NAME TO JCT
         LTR   INPR4,INPR4              IS THERE A LIST
         BZ    INJMNEXX                 NO
INJMNCF4 LA    INPR5,2                  CONSTANT OF 2
         LA    INPR6,INBMLIST           DELIMITER LIST
         L     INTRY,INJMBOAD           BREAKOUT ADDRESS
         BALR  INRTN,INTRY
         LA    INPR5,1                  CONSTANT OF 1
         CLI   0(INPR1),SSSEQUAL        DID EQUAL STOP SCAN
         BE    INJMNDA1                 YES
INJMNCF5 EQU   *
         LA    INWK1,ACTJNFLD           ADDRESS OF NBR OF ACCT FIELDS
         L     INTRY,INJMADAC
         BALR  INRTN,INTRY
         TM    INAMSWA,INAMCONT         IS CONTINUATION XPECTD IN AC RN
         BO    INJMNEJ4                 YES
         CLI   0(INPR1),SSSEQUAL        DID = CAUSE RETURN
         BC    INNOTEQU,INJMNEA1        NO
         LA    INPR4,1(INPR4,INPR2)     RESTORE PROPER SCAN LENGTH
         LA    INPR5,1                 NO CHARS TO SCAN
INJMNDA1 LA    INPR6,INBMLIST           ADDRESS OF COMMA
         L     INTRY,INJMBOAD           BREAKOUT ADCON
         BALR  INRTN,INTRY
         TM    INCMSWB,INCMCXP          IS CONTINUATION EXPECTED
         BC    INALLONE,INJMNEJ3        YES
         TM    INAMSWA,INAMERBO         WAS THERE A CONT ERR IN BREAK
         BC    INALLONE,INJMNEB2        YES                         OUT
         LTR   INPR1,INPR1              DID END STOP SCAN
         BC    INNOTZRO,INJMNDBA        NO
         LA    INPR1,INAMBLNK           GET ADDRESS OF BLANK
         B     INJMNDBB
INJMNDBA LR    INPR4,INPR1              GET LENGTH OF FIELD
         SR    INPR4,INPR3                IN INPR4
INJMNDBB SR    INWK1,INWK1              ZERO REG
         IC    INWK1,INKEYTBL           NBR ENTRIES IN KEYWORD TABLE
         LA    INWK2,INKEYTBL+4         ADDRESS OF FIRST TABLE ENTRY
         SR    INWK3,INWK3              ZERO REG
INJMNDC1 IC    INWK3,11(INWK2)          NBR CHARS-1 IN TABLE ENTRY
         EX    INWK3,INKEYCMP           COMP. KEYWORD TO STATMENT FIELD
         BC    INEQZERO,INJMNDD3        THIS PATH IF EQUAL
         LA    INWK2,16(INWK2)          GET NEXT ENTRY
         BCT   INWK1,INJMNDC1           TRY AGAIN TIL ENTRIES EXHAUSTED
         STM   INPR1,INPR2,INAMMGSV     SAVE REGS
         SR    INPR2,INPR2              ZERO REG
         LA    INPR1,171                INTERNAL MESSAGE NBR
**  ERROR  **  KEYWORD NOT IN TABLE
         L     INTRY,INJMMRCN
         BALR  INRTN,INTRY
         MVI   JCTJSTAT,X'04'           FAIL JOB
         OI    INCMSWA,INCMDFSH         TURN ON DATA FLUSH
         LM    INPR1,INPR2,INAMMGSV     RESTORE REGISTERS
         BC    INALWAYS,INJMNEA1
INJMNDD3 TM    12(INWK2),INKEYSUP       IS KEYWORD SUPPORTED ON THIS
         BC    INALLONE,INJMNDE3        YES                    SYSTEM
         STM   INPR1,INPR2,INAMMGSV     SAVE REGS
         SR    INPR2,INPR2
         LA    INPR1,172                INTERNAL MESSAGE NBR 172
**  ERROR  **  KEYWORD NOT SUPPORTED ON THIS SYSTEM
         L     INTRY,INJMMRCN           MESSAGE ROUTINE ADDRESS
         BALR  INRTN,INTRY
         LM    INPR1,INPR2,INAMMGSV    RESTORE REGISTERS
         BC    INALWAYS,INJMNEA1
INKEYCMP CLC   0(1,INWK2),0(INPR3)
INJMNDE3 OI    INJMSWA,X'02'            INDICATE FROM JOB ROUTINE
         LA    INWK1,JCTJDPCD           FIRST DEPENDENCY
         L     INTRY,12(INWK2)          ADDRESS FO KEYWORD MODULE
INJMNEAA BALR  INRTN,INTRY
INJM9999 NI    INJMSWA,255-2
INJMNEA1 TM    INAMSWA,INAMCDCT         WAS CONT. FOUND IN COND= ROUTIN
         BO    INJMNEJ4                 YES
         CLI   0(INPR1),SSSBLANK        DID BLANK STOP SCAN
         BZ    INJMNEXX                 YES
         LR    INPR4,INPR2              SET UP PARAMETERS
         LA    INPR3,1(INPR1)             TO GET NEXT FIELD
         BC    INALWAYS,INJMNDA1
INJMNEB2 NI    INAMSWA,INALL-INAMERBO   TURN OFF SWITCH
         MVI   JCTJSTAT,X'04'          FAIL JOB
         OI    INCMSWA,INCMDFSH        TURN ON DATA FLUSH
         STM   INPR1,INPR2,INAMMGSV
         LA    INPR1,158
         SR    INPR2,INPR2
         L     INTRY,INJMMRCN
         BALR  INRTN,INTRY
         LM    INPR1,INPR2,INAMMGSV
         NI    INCMSWD,255-INCMNCON     NO
INJMNEXX EQU   *
         TM    INJMSWA,X'04'            WAS MESSAGE LEVEL RECEIVED
         BC    INALLONE,INJMNED2        YES
         NI    JCTJMGLV,X'0F'           SET MESSAGE LEVEL TO ZERO
         OI    INJMSWA,X'04'            INDICATE MESSAGE LEVEL RECEIVED
INJMNED2 EQU   *
         AIF   ('&ACNREQ' EQ 'A').ACCTNO
         AGO   .PROGTST
.ACCTNO  ANOP
         TM    INJMSWA,INJMANRD      HAS ACCOUNT NBR BEEN  RECEIVED
         BO    INJMNEE3              YES
         SR    INPR2,INPR2           ZERO REG
         LA    INPR1,173             INTERNAL MESSAGE NUMBER 173
** ERROR ** NO ACCOUNT NUMBER RECEIVED
         L     INTRY,INJMMRCN        MESSAGE ROUTINE VCON
         BALR  INRTN,INTRY
         B     INJMNEG3
INJMNEE3 EQU   *
.PROGTST ANOP
         AIF   ('&PRGREQ' EQ 'P').PROGCOD
         AGO   .END
.PROGCOD ANOP
         TM    INJMSWA,INJMPNRD      HAS PROGRMRS NAME BEEN RECIEVED
         BO    INJMNEJ3              YES
         SR    INPR2,INPR2           ZERO REG
         LA    INPR1,174             INTERNAL MESSAGE NUMBER 174
**  ERROR  **  PROGRAMMER'S NAME MISSING
         L     INTRY,INJMMRCN
         BALR  INRTN,INTRY
         AGO   .FINAL
.END     ANOP
         B     INJMNEJ3
.FINAL   ANOP
INJMNEG3 MVI   JCTJSTAT,X'04'           INDICATE JOB FAILED
         OI    INCMSWA,INCMDFSH         TURN ON DATA FLUSH SWITCH
         BC    INALWAYS,INJMNEJ4
INJMNEJ3 TM    INAMSWA,INAMCDER         WAS THERE AN ERROR IN COND RTN
         BC    INALZERO,INJMNEJ4        NO
         NI    INAMSWA,INALL-INAMCDER   TURN OFF  INDICATOR
         BC    INALWAYS,INJMNEG3
INJMNEJ4 LM    1,12,INJESAVE            RESTORE REGS
         LA    INWK2,20
         L     INTRY,IEFMFV
         BR    INTRY
IT8H     DC    XL2'08'
DCBLRECL DC    X'50'                   LENGTH OF STATEMENT
INAMBLNK DC    C' '
IEFCONDV DC    V(IEFCOND)
INJMADAC DC    V(IEFAM)
INJMQULK DC    V(IEFMI)
IEFMFV   DC    V(IEFMF)
INJMMRCN DC    V(IEFWMSG)
INJMBOAD DC    V(IEFBM)
***********************************************************************
**                                                                   **
**                            KEYWORD TABLE                          **
**                                                                   **
***********************************************************************
INKEYTBL DS    0C
         DC    X'05'          HEXIDECIMAL NBR OF ENTRIES IN THE TABLE
**                                   IN THIS SYSTEM
         DC    X'00'
         DS    2C
**                  EACH ENTRY WILL HAVE THE FOLLOWING FORMAT --
**                * BYTES 1-11     THE KEYWORD IN EBCDIC FORM
**                * BYTE  12       HEX NBR OF CHARS-1 IN KEYWORD
**                * BYTE  13
**                  BYTES 14-16    ADDRESS OF THE KEYWORD CODE MODULE
         DC    C'TYPRUN'
         DC    AL1(SSSEQUAL)
         DC    CL4' '
         DC    X'06'                    NBR OF CHARS-1 IN KEYWORD
         DC    X'80'               INDICATES SUPPORT BY THIS SYSTEM
         DC    VL3(IEFN1)
         DC    C'MSGLEVEL'
         DC    AL1(SSSEQUAL)
         DC    CL2' '
         DC    X'08'
         DC    X'80'
         DC    VL3(IEFNX)
         DC    C'PRTY'
         DC    AL1(SSSEQUAL)
         DC    CL6' '
         DC    X'04'
         DC    X'80'
         DC    VL3(IEFNY)
         DC    C'COND'
         DC    AL1(SSSEQUAL)
         DC    CL6' '
         DC    X'04'
         DC    X'80'
         DC    VL3(IEFCOND)
         DC    C'MSGCLASS'
         DC    AL1(SSSEQUAL)
         DC    CL2' '
         DC    X'08'
         DC    X'80'
         DC    VL3(IEFNZ)
*    SPECIAL CHARACTER DELIMITER LIST
INBMLIST DC    AL1(SSSCOMMA)            COMMA
         DC    AL1(SSSEQUAL)            EQUAL SIGN
         DC    AL1(SSSRPREN)            RIGHT PARENTHESIS
         DC    AL1(SSSLPREN)            LEFT PARENTHESIS
         DC    AL1(SSSBLANK)            BLANK
         DC    AL1(SSSQUOTE)            QUOTE
         MEND
