         MACRO
  IEAAIH &STORAGE,&SVHI,&SVLO,&SLMT,&SER,&EXT,&TRACE,&PROTECT,&FLOATPT,X
               &TRSVCTB,&MOD,&SIZER,&MACH,&HIARCHY,&TIMER,&SMF,        X
               &CHANLOG                                           19E01
     TITLE 'IEAAIH, FIRST LEVEL INTERRUPT HANDLERS'
.*2133006040,006120-006200,026440,026520-026600,028000             ISER
.*2133000600,000850,029200,031600,036600-036800                    MO17
.*0030,028800                                                      R85
.*0030,036760                                                      MCS
.*001096,001128,036500                                           A26704
.*  000480-000760,036620-036780,046410-046530,054020-054120       19018
.*037200,037600-038200,067800,068200-068800                    CH 21014
.* ABOVE CODE DELETED FOR GTF-ALL REGS ARE SAVED ON PGM CHK    CH 21014
***        STATUS  --  CHANGE LEVEL = 005           ***           19572
.*
.*    PARAMETERS  FOR  IEAAIH  --  SMF REQUIRES TTIME             19018
.*
.* IEAAPS
.* DEPENEDENT
.*
.*  YES        1    STORAGE             PRIM      PART
.*             2    HI IBM SVC          NUMBER
.*             3    LO USER SVC         NUMBER
.*             4    INDEX LOW RES       NUMBER
.*             5    SERR                SERP      MCH
.*             6    (EXT)               (Y,Y,N,N,N,N,N,N)
.*                  EXT(1)              DTIME OR TTIME           A26704
.*                  EXT(2)              ALTERNATE CONSOLE        A26704
.*  YES        7    TRACE               Y    N
.*             8    PROTECTION          Y    N
.*  YES        9    F.P. REGS           Y    N
.*             10   RES. TTR TABLE
.*                  FOR ALL SVC LOADS   Y    N
.*             11   MODEL               30   40   ETC
.*             12   SIZER
.*             13   MACHINE             32768
.*             14   LCS                 Y    N
.*  YES        15   TIMER               NOTIM     DTIME     TTIME
.*  YES        16   SMF                 SMF  NSF                  19018
.*             17   2880 CHANNEL        1    0                    19E01
.*
         SPACE
IEAAIH00 CSECT
         EXTRN IEACVT,IEAATA00
         EXTRN  SVE,DXA                                            0051
         ENTRY IEA0IO02,SVEX
         ENTRY DISMISS
         ENTRY SVF
         ENTRY SVCSAV
         ENTRY IEAAPK00                                           66784
         AIF   ('&TRSVCTB' EQ 'Y').HBBC
         ENTRY SVCPSW
.HBBC    ANOP
         ENTRY PDSAV
         ENTRY IORGSAV
         ENTRY PISAV,AIOS
         ENTRY IORGSW
         ENTRY IEA0XE00
         EXTRN IEA0AB00
         EXTRN IEA0PL00
         EXTRN SVCTBL
         EXTRN SVPRFX
         ENTRY SLMT
         ENTRY RESUMPSW
         AIF ('&STORAGE' EQ 'PRIM').AFTCMT                        21014
         ENTRY IHLCMT                                             21014
.AFTCMT  ANOP                                                     21014
         AIF   ('&TRACE' NE 'Y').HBAA
         EXTRN IEATRTBL,PSWNDX,PSWNDX2
.HBAA    AIF   ('&SER' EQ 'SERP' OR '&MOD' NE '65').HBAL          M2944
         EXTRN IGFN0000
.HBAL    ANOP
.HBAM AIF ('&STORAGE' NE 'PRIM').HBAQ
         ENTRY IEAHEAD,IEAPPTCB,IEAHPTCB
         EXTRN IEZPJSCB                                            I254
         ENTRY IEAMSBBX                                           19572
         AIF   ('&TIMER' NE  'TTIME').CONT4C1                     19572
         ENTRY IEATIBBX                                           19572
.CONT4C1 ANOP                                                     19572
.HBAQ   ANOP
         EJECT
XIRB     EQU   X'C0'
XLOD     EQU   X'20'               ROUTINE HAS BEEN LOADED.
XLODP    EQU   X'E0'
XNSVRB   EQU   X'10'               TRANSIENT AREA SVRB SIZE.
XSVRB   EQU   X'C0'               SVRB STAB INDICATOR.
XRBWT    EQU   X'80'               WAIT BIT.
XACTV    EQU   X'40'               ROUTINE IS ACTIVE.
XPRVD    EQU   X'20'               ROUTINE IS PRIVILEGED.
XRSCHD   EQU   X'10'               ROUTINE IS RESCHEDULABLE.
XIOSRB   EQU   X'08'               RB FOR IOS.
XIQE12   EQU   X'04'               IQE TYPE IS 12*.
XDYN     EQU   X'02'               RB PLACED IN CORE AWAY FROM ROUTINE.
XSIPRG   EQU   56                  PROGRAM NAME IN SIRB.
XIQETYP  EQU   X'C0'
         EJECT
***   RB DISPLACEMENT VALUES.   ***
         SPACE 1
XRBNM    EQU   0                   NAME FIELD OF PRB, TTR IN SVRB.
XRBSZ    EQU   8                   SIZE FIELD OF RB.
XSTAB    EQU   10                  STATUS + ATTRIBUTE BYTES.
XRBUSE   EQU   12                  USE COUNT, FOR LOADED PROGRAMS.
XRBEP    EQU   12                  ENTRY POINT OF ASSOCIATED PROGRAM.
XRBPSW   EQU   16                  PSW SAVE AREA, IN ALL RBS.
XRBQ     EQU   24                  SECONDARY QUEUENG FIELD.
XRBWAT   EQU   28                  RB WAIT COUNT FIELD.
XRBLNK   EQU   28                  RB LINK FIELD FOR READY RBS.
XRBREG   EQU   32                  REGISTER SAVE AREA.
         SPACE 2
***   TCB DISPLACEMENT VALUES   ***
*
TCBRBP   EQU   0                   READY QUEUE LIST ORIGIN
TCBPIE   EQU   4                   POINTER TO P.I.E.
TCBDEB   EQU   8
TCBTIO   EQU   12                  POINTER TO TASK IO TABLE.
TCBCMP   EQU   16                  COMPLETION CODE FIELD FOR ABEND.
TCBTRN   EQU   20                  FIELD FOR USE BY TESTRAN.
TCBMSS   EQU   24                  FIELD FOR USE BY MAIN STORAGE SUPVR.
TCBPKF   EQU   28                  PROTTCT KEY FIELD.
TCBFLGS   EQU   28                 FLAG BYTES, BEGIN AT OFFSET 29.
TCBWAT   EQU   33                  TCB WAIT BYTE, OF FLAG BYTE GROUP
TCBLMP   EQU   34                  LIMIT PRIORITY.
TCBDSP   EQU   35                  DISPATCHING PRIORITY.
TCBLLS   EQU   36                  ORIGIN OF LIST OF LOADED PROGRAMS.
TCBJLB   EQU   40                  JOB LIBRARY POINTER (DCB).
TCBJSE   EQU   44                  INACTIVE PROGRAM LIST ORIGIN.
TCBGRS   EQU   48                  GENERAL REGISTER SAVE AREA, 10-9.
TCBTCB   EQU   116
TCBTME   EQU   120
         SPACE
************************************************************      19572
*                    COMMON  EQUATES                       *      19572
************************************************************      19572
.* ADDITIONAL SHOULD BE ADDED AT THE END OF THIS LIST *.          19572
         SPACE
R0       EQU   0                   REG 0                          19572
R1       EQU   1                   REG 1                          19572
R2       EQU   2                   REG 2                          19572
R3       EQU   3                   REG 3                          19572
R4       EQU   4                   REG 4                          19572
R5       EQU   5                   REG 5                          19572
R6       EQU   6                   REG 6                          19572
R7       EQU   7                   REG 7                          19572
R8       EQU   8                   REG 8                          19572
R9       EQU   9                   REG 9                          19572
RA       EQU   10                  REG 10                         19572
RB       EQU   11                  REG 11                         19572
RC       EQU   12                  REG 12                         19572
RD       EQU   13                  REG 13                         19572
RE       EQU   14                  REG 14                         19572
RF       EQU   15                  REG 15                         19572
XG01     EQU   1
R14      EQU   14                  REG 14                         19572
Q0       EQU   0                   VALUE OF 0                     19572
Q1       EQU   1                   VALUE OF 1                     19572
Q2       EQU   2                   VALUE OF 2                     19572
Q3       EQU   3                   VALUE OF 3                     19572
Q4       EQU   4                   VALUE OF 4                     19572
Q5       EQU   5                   VALUE OF 5                     19572
CVTPTR   EQU   16                  ADDRESS OF CVT.                19572
XGBASE   EQU   2
XGTCB    EQU   9
XG2      EQU   2
XG9      EQU   9
XG10     EQU   10
VPRB     EQU   0
VPRBSAV  EQU   16
OLDPSW   EQU   24
IC       EQU   27
VREGSAV  EQU   48                 .REG SAVE AREA OFFSET IN TCB
LNKREG   EQU   2              . LINK REGISTER                     19018
         EJECT
IPLPSW   DS    0F
         USING IPLPSW,0
         ORG   IPLPSW+16
         DC    A(IEACVT)
         AIF   ('&TRACE' NE 'Y').HBAW
         DC    A(IEATRTBL)         POINTER TO TRACE TABLE POINTER
.HBAW  ANOP
         DC    A(0)           ZERO PTR WHEN NO TRACE TABLE EXIST
         ORG   IPLPSW+24
         SPACE 2
***  INTERRUPT OLD PSW AREA  ***
*
TEOPSW   DC    XL8'0'              TIMER/EXTERNAL OLD PSW AREA.
SVCOPSW  DC    XL8'0'              SVC OLD PSW AREA.
PIOPSW   DC    XL8'0'              PROGRAM INTERRUPT OLD PSW AREA.
MCOPSW   DC    XL8'0000FF0000000000'
IOOPSW   DC    XL8'0'              INPUT/OUTPUT OLD PSW AREA.
         SPACE
CSW      DS    1D                  CHANNEL STATUS WORD.
CAW      DS    1F                  CHANNEL ADDRESS WORD.
         SPACE
         DC    A(IEACVT)
         SPACE 2
         DC    X'62E08000'                                    BD A50059
         AIF   ('&TRACE' NE 'Y').NOTRCE                           19572
         DC    A(IEATRTBL)         POINTER TO TRACE TABLE         19572
         AGO   .NOTRC2                                            19572
.NOTRCE  ANOP                                                     19572
         DS    1F                                                 19572
.NOTRC2  ANOP                                                     19572
         SPACE 3
***  INTERRUPT NEW PSW AREA  ***
*
*                  EXTERNAL NEW PSW
TENPSW   DC    XL4'00040000'       TIMER/EXTERNAL NEW PSW.
         DC    A(IEAQEX00)
*
*                  SVC NEW PSW
SVCNPSW  DC    XL4'00040000'       SVC NEW PSW.
         DC    A(IEAASC00)
*
*                  PROGRAM NEW PSW
PINPSW   DC    XL4'00040000'       PROGRAM NEW PSW.
         DC    V(ITLPCSIM)                                         ITEL
*
*                  MACHINE NEW PSW
         AIF   ('&SER' EQ 'SERP' OR '&MOD' NE '65').HABN          M2944
         DC    A(0)
         DC    A(IGFN0000)
         AGO   .HABO
.HABN    ANOP
MCNPSW   DS    0D
         DC    X'00020000000000E2'  BASIC MC HANDLER,DISABLED WAIT
.HABO    ANOP
*
*                  IO NEW PSW
         DC    XL4'00040000'           I/O NEW PSW
         DC    A(IEAAIO00)
           EJECT
*        THE FOLLOWING MACRO CALLS THE DESIRED LEVEL OF SER        ISER
         IFBSRLOG   &MOD,&CHANLOG                                 19E01
SERLOGA  EQU   *                                                   R85
*
         ORG   IPLPSW+364                                          ISER
         LPSW  X'170'                   TO RESTART AT GOMANA
         DC    X'00040000'
         DC    V(IEANIP4)
         ORG   SERLOGA                                             R85
         EJECT
         AIF   ('&STORAGE' NE 'PRIM').HBAN2                        MO17
***  TASK CONTROL BLOCK  ***
         SPACE 1
IEAHEAD  DS    0F
IEAPPTCB DS    0F
IEAHPTCB DS    0F
PRBPTX   DC    F'0'                RB LIST ORIGIN
PIEPTX   DC    F'0'
DEBQPTX  DC    F'0'
TIOTPTX  DC    F'0'
COMMX    DC    F'0'
TTRNX    DC    F'0'
MSSQPTX  DC    A(IEAMSBBX)         MSS BOUNDARY BOX POINTER        MO17
     AIF  ('&PROTECT' EQ 'N').HABQ1
TCBPRTK  DC  X'10'
     AGO  .HABQ2
.HABQ1  ANOP
TCBPRTK  DC  X'00'
.HABQ2  ANOP
TCBFLAG  DC    XL2'00'
         AIF   ('&FLOATPT' NE 'Y').HBAAXX
         DC    X'08'
*              FLOATING POINT REGISTERS EXIST.
         AGO   .HBAX
.HBAAXX  ANOP
         DC    X'00'
.HBAX    ANOP
         DC    XL2'00'
TCBPRTY  DC    AL2(0)              TCB PRIORITY FIELD.
LODPTRX  DC    A(0)
JOBLIB   DC    A(0)                JOBLIB DCB POINTER.
INACTPLX DC    A(0)                INACTIVE PROGRAM LIST ORIGIN.
BREGSX   DS    8F
IORGSAV  DS    8F                  NORMAL IO INTERRUPT REG. SAVE AREA.
         DC    X'04000000'
TCBLNK   DC    A(0)                TCB LINK FIELD.
         AIF   ('&TIMER' NE 'TTIME').HBAN                        A26704
TIMPTR   DC    A(IEATIBBX)         POINTER TO TIMER ELEMENT.       MO17
         AGO   .HBAN1                                              MO17
.HBAN    ANOP                                                      MO17
TIMPTR   DC    A(0)                POINTER TO TIMER ELEMENT.       MO17
.HBAN1   ANOP                                                      MO17
         DC    A(0)                                               19572
.******* ABEND APPENDAGE ************************************     19572
         DC    2F'0'        ABEND APPENDAGE                       19572
.******* BOUNDARY BOXES *************************************     19572
         AIF   ('&HIARCHY' NE 'Y').NLCS                           19572
BNDBX0   DC    AL1(1)                                             19572
         AGO   .BNDBOX                                            19572
.NLCS    ANOP                                                     19572
BNDBX0   DC    AL1(0)                                             19572
.BNDBOX  ANOP                                                     19572
         DC    AL3(0)                                             19572
         DC    A(0)                                               19572
         DC    A(&MACH)                                           19572
         DC    A(0)                                               19572
         DC    A(0)                                               19572
         DC    A(0)                                               19572
         DC    A(0)                    TCBNSTAE                   19572
         DC    A(0)                                               19572
         DC    A(0)                    TCBUSER                    19572
         DC    A(0)                                               19572
         DC    A(0)                                               19572
         DC    AL1(0)              FLAG BYTE                       I254
         DC    AL3(IEZPJSCB)       WTP CONTROL BLOCK               I254
IEAMSBBX EQU   BNDBX0                                             19572
         AIF   ('&TIMER' NE  'TTIME').CONTIN5                     19572
TIMEL0   DS    0F                                                 19572
         DC    XL1'83'                                            19572
         DC    AL3(IEAHEAD)                                       19572
         DC    27F'0'                                             19572
IEATIBBX EQU   TIMEL0                                             19572
.CONTIN5 ANOP                                                     19572
         EJECT
.HBAN2   ANOP                                                      MO17
         AIF   ('&STORAGE' EQ 'PRIM').HBAO                         MO17
IORGSAV  DC    8F'0'    PROGRAM INT. SAVE AREA
PISAV    DC    16F'0'   PROGRAM INT. SAVE AREA
         AGO   .HBAP
.HBAO    AIF   ('&TRACE' NE 'Y').HBAT
PISAV    DC    8F'0'    PROGRAM INT. SAVE AREA
         AGO   .HBAP
.HBAT    ANOP
PISAV    DS    0F                  PROGRAM INTERRUPT SAVE AREA.
.HBAP    ANOP
PDSAV    DC    8F'0'    PSEUDO-DISABLE REG. SAVE AREA
         SPACE 1
ASVCTBL  DC    V(SVCTBL)
ASVPRFX  DC    V(SVPRFX)
** IEAAPS XSNT REFRESH ROUTINE DEPENDENT ON V(SVE,DXA) FOR LM INST.0051
SVEX     DC    A(SVE)                                              0051
         DC    A(DXA)                                              0051
SVF      EQU   SVEX-1
         AIF   ('&TRSVCTB' EQ 'Y').HBAX11
SVCPSW   EQU   *-4
         DC    A(0)
.HBAX11  ANOP
AIOS     DC    F'4096'
IOSORG   DC    A(IECINT)          I/O SUPERVISOR
ABTPLG   DC    V(IEA0PL00)
         AIF   ('&TRACE' NE 'Y').HBAB
TRACEIO  DC    A(PSWNDX2)
TRACESVC DC    A(PSWNDX)
.HBAB    ANOP
SLMT     DC    AL2(&SLMT)          LOW NON-RES. SVC INDEX FACTOR
         AIF ('&STORAGE' EQ 'PRIM').NOCMT                         21014
IHLCMT   CMT   DSECT=NO                                           21014
.NOCMT   ANOP                                                     21014
         EJECT
***  IO INTERRUPT HANDLER  ***
         SPACE 1
IEAAIO00 DS    0H
IORGSW   BC    0,IOS
         OI    IORGSW+1,X'F0'
         STM   2,9,IORGSAV         SAVE WORK REGISTERS.
         AIF   ('&STORAGE' EQ 'PRIM').HIAA
**     L     3,IEATCBP+4          GET CURRENT TCB ADDRESS.
        SPACE
        DC    AL4(IEATCBP+4)
        ORG   *-4
        DC    X'5830'
        ORG   *+2
        SPACE
         L     4,TCBRBP(0,3)       GET CURRENT RB
         MVC   XRBPSW(8,4),IOOPSW  SAVE OPSW IN CURRENT RB.
         AIF   ('&SMF' NE 'SMF').NOSMF1                           19018
         LR    9,4                      .SET UP FOR SYS WAIT      19018
         BAL   LNKREG,IEAQWAIT          .TO GATHER SYS WAIT TIME  19018
.NOSMF1  ANOP                                                     19018
         AGO   .HIAB
.HIAA    ANOP
         L     4,PRBPTX            GET CURRENT RB
.HIAB    ANOP
IEA0IO02     DS     0H
PDSBL   BC  0,PDON
         NI    XRBPSW+1(4),X'FD'   TURN OFF WAIT IN PSW.
         AIF   ('&STORAGE' EQ 'PRIM').HIAC
         STM   10,1,TCBGRS(3)
         AIF   ('&TIMER' NE 'TTIME').HIAD .                      A40380
         L     R7,JSTDEQU .        GET DEQ ROUTINE ADDRESS       A40380
         LR    RB,R3 .             SET UP TCB ADDRESS            A40380
         BALR  R3,R7 .             GO DEQUE JSTTQE               A40380
         B     JSTDEQU+4 .         BRANCH AROUND ADCON           A40380
JSTDEQU  DC    A(JSTDEQ) .         ROU IN APS TO DEQ JSTTQE      A40380
         AGO   .HIAD .                                           A40380
.HIAC    ANOP
         STM   10,1,BREGSX         SAVE REMAINING WORK REGISTERS IN TCB
.HIAD    ANOP
IOS      DS    0H
         AIF   ('&TRACE' NE 'Y').HBAC
         L     9,TRACEIO           TRACE IO INTERRUPT
         BALR  11,9
.HBAC    ANOP
         L     BASREG,AIOS         LOAD IOS' BASE ADDRESS.
         L     9,IOSORG
         BCR   15,9               TO I/O INTERRUPT SUPERVISOR
PDON     STM   10,1,PDSAV          SAVE REMAINING REGISTERS IN SPECIAL
         MVC   RESUMPSW(8),IOOPSW  SAVEX AREA &OPSW IN RESUME PSW LOC.
         B     IOS                 AND ENTER IOS.
DISMISS  NI    IORGSW+1,X'0F'      RESET ORIGINAL INTERRUPT CONDITION.
         LM    2,9,IORGSAV         RESTORE WORK REGISTERS.
         TM    PDSBL+1,240         Q. PSEUDO-DISABLE CONDITION NOT ON.
*       BZ     IEA0DS
         SPACE 1
         DC    AL4(IEA0DS)
         ORG   *-4
         DC    X'4780'
         ORG   *+2
         SPACE 1
         LM    10,1,PDSAV          RESTORE SPECIALLY SAVED REGISTERS.
         LPSW  RESUMPSW
         EJECT
* E X T E R N A L  F L I H
         SPACE
 AIF ('&EXT(1)' NE 'Y' AND '&EXT(2)' NE 'Y').HBAF
IEAQEX00 DS    0C
TEXFLIH  STM   XG2,XG9,IEAEXSAV    SAVE 8 REGS
         TM    PDSBL+1,X'F0'       Q. SYSTEM PSEUD/-DISABLE NOT ON.
         BC    8,TEXA
         STM   10,1,PDSAV          SAVE REGS IN P/D SAVE AREA.
         MVC   RESUMPSW(8),TEOPSW  SAVE EXT OPSW IN RESUME LOCATION.
         B     TEXB
TEXA     DS    0H
**      L     XGTCB,IEATCBP+4     .GET TCB ADDRESS.
         SPACE 1
         DC    AL4(IEATCBP+4)
         ORG   *-4
         DC    X'5890'
         ORG   *+2
         SPACE 1
         STM   XG10,XG01,VREGSAV(XGTCB) .SAVE REGS IN TCB
         L     XGTCB,VPRB(XGTCB)  .GET PRB ADDR
         MVC   VPRBSAV(8,XGTCB),OLDPSW .SAVE PSW IN PRB
         AIF   ('&SMF' NE 'SMF').NOSMF2                           19018
         BAL   LNKREG,IEAQWAIT          .TO GATHER SYS WAIT TIME  19018
.NOSMF2  ANOP                                                     19018
         NI    VPRBSAV+1(XGTCB),X'FD'  .SET WAIT BIT OFF IN PSW.
TEXB     DS    0H
         AIF ('&STORAGE' EQ 'PRIM').AFTHK1                        21014
*  ISSUE HOOK FOR EXTERNAL TRACE                                  21014
         HOOK  EID=IHLMEXT    .ISSUE HOOK FOR EXTERNAL TRACE      21014
.AFTHK1  ANOP                                                     21014
*
 AIF ('&EXT(2)' NE 'Y').HBAG
         TM    IC(0),X'40'
         BZ    *+10
         L     XGBASE,ASLKEY
         BALR  XGBASE,XGBASE
.HBAG AIF ('&EXT(1)' NE 'Y').HBAH
         TM    IC(0),X'80'
         BZ    *+10
         L     XGBASE,ASLTIM
         BALR  XGBASE,XGBASE
.HBAH    ANOP
         B     DISMISS
*
      AIF ('&EXT(2)' NE 'Y').HBAI
ASLKEY   DC    V(IEEBC1PE)
.HBAI AIF ('&EXT(1)' NE 'Y').HBAJ
ASLTIM   DC    V(IEA0TI00)
.HBAJ    ANOP
IEAEXSAV EQU   IORGSAV
         AGO   .HBAK
.HBAF    ANOP
IEAQEX00 LPSW  TEOPSW
.HBAK    ANOP
         EJECT
         AIF   ('&SMF' NE 'SMF').NOSMF3                           19018
***     SYSTEM WAIT TIME COLLECTION     ***                       19018
         SPACE 1                                                  19018
XRBWAIT  EQU   17                       .AMWP OFFSET IN PSW       19018
RBADDR   EQU   9                        .RB ADDRESS               19018
INTTIME  EQU   80                       .INTERVAL TIMER LOC       19018
TIMESAVE EQU   5                        .SYS WAIT TIME START      19018
TIMERACC EQU   6                        .ACC SYS WAIT TIME        19018
INTIMVAL EQU   7                        .VALUE IN TIMER           19018
IEAQWAIT DS    0H                                                 19018
         TM    XRBWAIT(RBADDR),X'02'    .Q. SYSTEM WAITING        19018
         BCR   8,LNKREG                 .NO. RET TO CALLER        19018
         L     INTIMVAL,INTTIME         .READ OUT TIMER           19018
         LM    TIMESAVE,TIMERACC,SYSWSAVE .LOAD SYS WAIT VALUES   19018
         LTR   TIMESAVE,TIMESAVE        .Q. SYS WAIT START TIME   19018
*                                       .VALUE ZERO               19018
         BCR   8,LNKREG                 .YES. RET TO CALLER       19018
         SR    TIMESAVE,INTIMVAL        .GET ELAPSED SYSTEM WAIT  19018
*                                       .TIME                     19018
         AR    TIMERACC,TIMESAVE        .INCR ACC SYS WAIT TIME   19018
         ST    TIMERACC,SYSWSAVE+4      .SAVE ACC SYS WAIT TIME   19018
* TO ENSURE CURRENT VALUE IN TIMER IS STORED IN SYS WAIT START    19018
* FIELD AFTER AN I/O OR CONSOLE INTERRUPT                         19018
         ST    INTIMVAL,SYSWSAVE                                  19018
         BR    LNKREG                   .RETURN TO CALLER         19018
         EJECT                                                    19018
.NOSMF3  ANOP                                                     19018
***     PROGRAM INTERRUPT HANDLER    ****
         SPACE 1
PEXIT    SVC   3
         SPACE 1
IEAAPK00 DS    0H
         STM   10,9,PISAV          SAVE ALL REGISTERS.
         ENTRY ENTRY2                                              ITEL
ENTRY2   EQU   *                                                   ITEL
         AIF ('&STORAGE' EQ 'PRIM').NOGTF                         21014
         L     RF,IEACON                .LINK TO IHLMCIH FOR      21014
         BALR  RE,RF                    .POSSIBLE HOOK PROCESSING 21014
*  ISSUE HOOK FOR PROGRAM TRACE                                   21014
         HOOK  EID=IHLMPI,TYPE=S   .ISSUE HOOK FOR PROGRAM TRACE
         LM    14,1,PISAV+X'10' .      RESTORE REGS 14,15,1      A54025
.NOGTF   ANOP                                                     21014
         LM    12,13,PIOPSW             SAVE OLD PSW IN REGISTERS
         L     11,ABTPLG           GET ABTERM ADDRESS.
         AIF   ('&STORAGE' EQ 'PRIM').HIAE
*       L      10,IEATCBP+4
         SPACE 1
         DC    AL4(IEATCBP+4)
         ORG   *-4
         DC    X'58A0'
         ORG   *+2
         SPACE 1
         AGO   .HIAF
.HIAE    ANOP
         LA    RA,IEAPPTCB         GET TCB ADDRESS.              A29120
.HIAF    ANOP
         TM    PIOPSW+1,1          Q. SUPR CAUSED PROG CHECK.    A29120
         AIF   ('&HIARCHY' NE 'Y').NOLCS1                        A29120
         BNZ   NOTSUPR             - NO -                        A29120
         L     R9,TCBMSS(RA)       GET ADDRESS OF BOUNDARY BOX.  A29120
         MVI   0(R9),1             REPLACE LCS FLAG.             A29120
         BR    11                  GO TO ABTERM                  A29120
NOTSUPR  EQU   *                                                 A29120
         AGO   .NOLCS2                                           A29120
.NOLCS1  ANOP                                                    A29120
         BCR   8,11                YES -- GO TO ABTERM           A29120
.NOLCS2  ANOP
         L     RA,TCBPIE(0,10)  Q. PROB PROG HAS NOT SPCIFIED    A29120
         LTR   10,10               INTERRUPT EXIT VIA SPIE
         BCR   12,11
         LR    11,10              SAVE PIE POINTER.                MTS0
         L     10,0(10)           GET PICA ADDRESS FROM PIE.       MTS0
         LTR   10,10               Q. SPIE EXIT IS IN PROCESS OR
         BC    12,ABGR01          NO PICA CURRENTLY IN EFFECT.     MTS0
         STM   12,2,4(11)         PLACE OPSW + REGS IN PIE.        MTS0
         LR    1,11               PLACE PIE PTR IN STANDARD REG.   MTS0
         L     11,ABTPLG          RECOVER ABTERM ADDRESS           MTS0
         MVI   0(1),X'80'
         LM    15,0,0(10)          Q. THIS INTRPT. TYPE IS NOT SELECTED
         SLL   0,0(12)             BY THE PICA MASK.
         LTR   0,0
         BCR   11,11
         ST    15,PIOPSW+4         SET UP ENTRY POINT PSW FOR EXIT.
         L     14,APIEEXIT         SET RETURN REGISTER,.           ITEL
         LM    10,13,PISAV         RESTORE WORK REGISTERS.
         LPSW  PIOPSW
ABGR01   EQU   *                                                   MTS0
         LR    1,11               PLACE PIE PTR IN STANDARD REG.   MTS0
         L     11,ABTPLG          RECOVER ABTERM ADDRESS.          MTS0
         BR    11                                                  MTS0
APIEXIT  DC    V(PIXT2)                                            ITEL
         AIF ('&STORAGE' EQ 'PRIM').NOGTF1                        21014
         SPACE
IEACON   DC    V(IHLMCIH)               .ADDR OF MONITER CALL IH  21014
IEACON2  DC    V(IHLBHOOK)              .ENTRY PT FOR HOOK PROCESS21014
.NOGTF1  ANOP                                                     21014
         EJECT
***  FIRST LEVEL SVC HANDLER  ***
         SPACE 1
IEAASC00 DS    0H
         STM   0,15,SVCSAV         SAVE REGISTERS IN TYPE I SAVE AREA.
         AIF ('&STORAGE' EQ 'PRIM').AFTHK2                        21014
*  ISSUE HOOK FOR SVC TRACE                                       21014
         HOOK  EID=IHLMSVC    .ISSUE HOOK FOR SVC TRACE           21014
.AFTHK2  ANOP                                                     21014
         AIF   ('&TRACE' NE 'Y').HBAD
         L     9,TRACESVC          TRACE SVC.
         BALR  11,9
.HBAD    ANOP
         LH    10,SVCOPSW+2            LOAD SVC CODE INTO REGISTER
SVV      CLI   SVCOPSW+3,&SVHI     Q. SVC CODE NOT IBM PROVIDED.
         BC    10,SVA
SVW      LM    8,9,ASVCTBL         GET SVC TABLE ADDRESS
         IC    10,0(9,10)          GET SVC INDEX FACTOR.
         LA    11,0(10,10)         DERIVE ADDRESS OF SVC TABLE ENTRY.
         AIF   ('&TRSVCTB' EQ 'Y').HBAY
         AR    11,10               FOR PROGRAM BEING CALLED.
         AGO   .HBAZ
.HBAY    ANOP
         ALR   11,11                   FOR PROGRAM BEING CALLED.
.HBAZ    ANOP
         BC    8,SVB               Q. NO TABLE ENTRY EXISTS FOR SVC.
         MVC   16(4),76                REFRESH CVT              A66090
SV2      BC    0,SV2                                            A66090
         L     3,16                CVT TABLEADDRESS.
**      L      4,IEATCBP+4         CURRENT TCB ADDRESS
         SPACE 1
         DC    AL4(IEATCBP+4)
         ORG   *-4
         DC    X'5840'
         ORG   *+2
         SPACE 1
         L     5,TCBRBP(0,4)       CURRENT RB ADDRESS.
         AIF   ('&TRSVCTB' EQ 'Y').HBBA
         CH    10,SLMT
         BC    10,SVD
         ALR   11,8
         MVC   SVCPSW+5(3),0(11)
         L     6,SVCPSW+4          ENTRY POINT
         TM    2(11),7
         BC    7,SVD
         OI    SVF+1,X'F0'         INDICATE TYPE I SVC IN PROGRESS.
         AGO   .HBBB
.HBBA    ANOP
         ALR   11,8                    GET SVC ADDRESS
         TM    3(11),7                 CHECK IF TYPE 1
         L     6,0(11)                 GET SVC ENTRY
         BC    7,SVD                   BRANCH IF NOT TYPE 1
         OI    SVF+1,X'F0'             INDICATE TYPE 1
.HBBB    ANOP
         BALR  14,6
         EJECT
         AIF   ('&STORAGE' EQ 'PRIM').HIAM
***  TYPE I SVC EXIT ROUTINE  ***
         SPACE 1
IEA0XE00 DS   0H
SVCXT    MVI   SVF+1,0                 RESET TYPE 1 INDICATOR
         LM    2,14,SVCSAV+8
**      CLC    IEATCBP+5(3),IEATCBP+1  Q. DISPATCHER SHOULD BE ENTERED.
         SPACE
         DS    2C                  Q. DISPATCHER SHOULD BE ENTERED.
         DC    AL4(IEATCBP+5)
         ORG   *-6
         DC    AL4(IEATCBP+1)
         ORG   *-4
         DC    X'D502'
         ORG   *+4
         SPACE
         BC    7,SVI
SVL      LPSW  SVCOPSW
SVI      CLI   SVCOPSW,255         Q. CALLER IS DISABLED.
         BC    4,SVL
         TM    PDSBL+1,X'F0'           TEST IF SYSTEM PSEUDO-DISABLED
         BC    8,SVI2                  RETURN TO CALLER          A73676
         CLI   SVCOPSW+3,1             WAIT                      A73676
         BC    7,SVL                   NO                        A73676
SVI2     EQU   *                                                 A73676
**      L      2,IEATCBP+4         GET TCB ADDRESS.
         SPACE
         DC    AL4(IEATCBP+4)
         ORG   *-4
         DC    X'5820'
         ORG   *+2
         SPACE
         STM   10,1,TCBGRS(2)      STORE REGISTERS.
         L     2,TCBRBP(2)         GET CURRENT RB ADDRESS.
         MVC   XRBPSW(8,2),SVCOPSW  SAVE RESUME PSW.
         L     2,SVCSAV+8
         AGO   .HIAN
.HIAM    ANOP
***  TYPE I SVC EXIT ROUTINE  ***
*
IEA0XE00 DS    0H                  TYPE I SVC EXIT ROUTINE.
SVCXT    MVI   SVF+1,0                 RESET TYPE 1 INDICATOR
         LM    2,14,SVCSAV+8
**      CLI    IEATCBP+2,0         Q. DISPATCHER IS TO BE ENTERED.
         SPACE 1
         DC    AL4(IEATCBP+2)
         ORG   *-4
         DC    X'9500'
         ORG   *+2
         SPACE 1
         BC    8,SVI
SVL      LPSW  SVCOPSW
SVI      CLI   SVCOPSW,255         Q. CALLER IS DISABLED.
         BC    4,SVL
         STM   10,1,BREGSX
         L     10,PRBPTX
         MVC   XRBPSW(8,10),SVCOPSW
.HIAN    ANOP
**      B      IEA0DS
         SPACE 1
         DC    AL4(IEA0DS)
         ORG   *-4
         DC    X'47F0'
         ORG   *+2
         SPACE 1
         EJECT
***  SVC SECOND LEVEL INTERRUPT HANDLER INTERFACE  ***
*
SVD      CH    10,SLMT                 CHECK IF TYPE 2 OR 3
         MVC   TCBGRS(64,4),SVCSAV     STORE REGS IN TCB
         L     12,SVEX
         BCR   15,12
         SPACE 3
SVA      DS    0H
         AIF   ('&SVLO' EQ '255').HBAE
***  NON-IBM SVC CODE INTERROGATION  ***
*
         CLI   SVCOPSW+3,&SVLO     Q. SVC CODE NOT VALID.
         BC    12,SVB
         LA    11,255
         XR    10,11
         LA    10,&SVHI.(0,10)
         B     SVW
         SPACE 3
.HBAE    ANOP
***   SVC ERROR CODE ROUTINE    ***
         SPACE 1
SVB      LH    1,SVCOPSW+2         PICK UP SVC CODE THAT IS IN ERROR.
         SLL   1,12
         O     1,KABEND   GENERATE ERROR CODE
         LM    8,11,SVCSAV+32      RESTORE WORK REGS
         SVC   13
SVCSAV   DS    8D
RESUMPSW DS    1D
         AIF   ('&SMF' NE 'SMF').NOSMF4                           19018
         TITLE '              SMF EXCP COUNTER ROUTINE'           19018
* ROUTINE NAME                                                    19018
         SPACE 1                                                  19018
*        IEASMFEX-  EXCP COUNTER ROUTINE                          19018
         SPACE 2                                                  19018
* FUNCTION                                                        19018
         SPACE 1                                                  19018
*             THE EXCP COUNTER ROUTINE COUNTS DIRECT 'SVC 0' EXCP'S
*        AND INDIRECT 'PCI INTERRUPT, CHANNEL END, ABNORMAL END'  19018
*        APPENDAGE EXCP RETURNS TO IOS.  EXCP COUNTING IS PERFORMED
*        ONLY FOR THE PROBLEM PROGRAM. COUNTING IS CONTROLLED BY  19018
*        A MASK OF THE PROBLEM PROGRAM TIOT. COUNTS ARE MAINTAINED
*        BY DATA SET/DEVICE LEVEL AND ARE CONTAINED IN THE TCT.   19018
*        EXCP COUNTS FOR OPEN/CLOSE/EOV FUNCTIONS ARE INCLUDED.   19018
         SPACE 2                                                  19018
* ENTRY POINT NAME                                                19018
         SPACE 1                                                  19018
*        IEASMFEX-  ENTRY TO EXCP COUNTER.                        19018
         SPACE 2                                                  19018
* ATTRIBUTES                                                      19018
         SPACE 1                                                  19018
*        .SUPERVISOR STATE                                        19018
*        .DISABLED FOR ALL MASKABLE INTERRUPTS EXCEPT MACHINE CHK 19018
*        .RESIDENT CODE                                           19018
*        .REENTRANT                                               19018
         SPACE 2                                                  19018
* APPLICABILITY                                                   19018
         SPACE 1                                                  19018
*        USE IN OPTION 2 SYSTEM                                   19018
         SPACE 2                                                  19018
* EXTERNAL REFERENCES                                             19018
         SPACE 1                                                  19018
*        .NONE                                                    19018
         SPACE 2                                                  19018
* SVC USAGE                                                       19018
         SPACE 1                                                  19018
*        NONE                                                     19018
         SPACE 2                                                  19018
* INPUT                                                           19018
         SPACE 1                                                  19018
*             THE FOLLOWING REGISTERS ARE PASSED ON ENTRY.        19018
*                  0 - TCB ADDRESS                                19018
*                  1 - RQE ADDRESS                                19018
*                  2 - IOB ADDRESS                                19018
*                  3 - DEB ADDRESS                                19018
*                  4 - DCB ADDRESS                                19018
*                  7 - UCB ADDRESS                                19018
*                 14 RETURN ADDRESS                               19018
*                 15 ENTRY POINT ADDRESS                          19018
*              REGISTERS 0 THROUGH 9 MUST NOT BE ALTERED AT EXIT. 19018
         SPACE 2                                                  19018
*        OUTPUT                                                   19018
         SPACE 1                                                  19018
*        NONE                                                     19018
         SPACE 2                                                  19018
* ENTRY POINTS                                                    19018
         SPACE 1                                                  19018
*        IEASMFEX-  ENTRY FROM IOS                                19018
*       .PSW-     .SUPERVISOR STATE                               19018
*                 .DISABLED EXCEPT FOR MACHINE CHECK              19018
*                 .SUPERVISOR PROTECT KEY                         19018
*       .REGISTERS 10 - 13 ARE IRREVA LENT.                       19018
*                 14 RETURN ADDRESS TO CALLER.                    19018
*                 15 ENTRY POINT ADDRESS                          19018
         SPACE 2                                                  19018
* EXIT POINTS                                                     19018
         SPACE 1                                                  19018
*        BRANCH ON REGISTER 14 BACK TO CALLER.                    19018
*       .PSW-     .SUPERVISOR STATE                               19018
*                 .DISABLED EXCEPT FOR MACHINE CHECK              19018
*                 .SUPERVISOR PROTECT KEY                         19018
*       .REGISTERS 0 - 9,14 TRANSPARENT OVER ROUTINE.             19018
*                  10 - 13,15 VOLATILE OVER ROUTINE.              19018
         SPACE 2                                                  19018
* ERROR EXIT POINTS                                               19018
         SPACE 1                                                  19018
*        NONE                                                     19018
         SPACE 2                                                  19018
* TABLES, CONTROL BLOCKS, AND POINTERS REFERENCED                 19018
         SPACE 1                                                  19018
*        1. TCBTCT   (TCB)  -NOT ALTERED                          19018
*        2. DCBTIOT  (DCB)  -NOT ALTERED                          19018
*        3. DCBOFLGS (DCB)  -NOT ALTERED                          19018
*        4. TCTUCBP  (TCT)  -NOT ALTERED                          19018
*        5. TCTDCBTD (TCT)  -NOT ALTERED                          19018
*        6. TCTSCTR  (TCT)  -NOT ALTERED                          19018
*        7. TCTIOTSD (TCT)  -NOT ALTERED                          19018
*        8. TCTDCTR  (TCT)  -ALTERED                              19018
*        9. TCTIOTBL (TCT)  -NOT ALTERED                          19018
         SPACE 2                                                  19018
* LIST ORIGINS, TABLES, CONTROL BLOCKS INCLUDED                   19018
         SPACE 1                                                  19018
*        NONE                                                     19018
         SPACE 2                                                  19018
* RESTRICTIONS ON USE OF ROUTINE                                  19018
         SPACE 1                                                  19018
*        NONE                                                     19018
         EJECT                                                    19018
*...TIMING CONTROL TABLE DISPLACEMENTS- TCT...                    19018
         SPACE 1                                                  19018
TCTUCBP  EQU   0             .DISPLACEMENT TO UCB ADDRESS         19018
TCTDCBTD EQU   0             .DISP TO DD ENTRY DISPLACEMENT       19018
TCTSCTR  EQU   2             .DISPLACEMENT TO NUMBER OF UCB'S     19018
TCTIOTSD EQU   2             .DISPLACEMENT TO SLOT                19018
TCTSW    EQU   3             .TCT OPTION SWITCHES                 19018
TCTDCTR  EQU   4             .DISPLACEMENT TO EXCP COUNTER        19018
TCTCOMIO EQU   8             .COMMON AREA FOR I/O TABLE           19018
TCTIOTBL EQU   12            .I/O TABLE ADDRESS                   19018
         SPACE 2                                                  19018
*...DCB DISPLACEMENTS...                                          19018
         SPACE 1                                                  19018
DCBTIOT  EQU   40            .DD ENTRY OFFSET                     19018
DCBOFLGS EQU   48            .OPEN/CLOSE FLAGS                    19018
         SPACE 2                                                  19018
*...DCB FLAGS...                                                  19018
         SPACE 1                                                  19018
OB       EQU   X'11'               .DCB OPENED/BUSY FLAGS         19018
         SPACE 2                                                  19018
* EXCP COUNTER.                                                   19018
XTCBPASS EQU   0                  .PASSED TCB POINTER             19018
ADDRIQE  EQU   1                        .IQE ADDR                 19028
XSCMMTBL EQU   3                                                  19028
XSEXTEFT EQU   5                                                  19028
IRBADDR  EQU   1                                                  19028
SMCAADR  EQU   9                        .SMCA ADDRESS             19028
EXITS    EQU   X'20'                    .MASKS FOR SMF EXITS      19028
XDCBPAS  EQU   4                   .PASSED DCB POINTER            19018
XUCBPTR  EQU   7                  .UCB POINTER                    19018
IQEADDR  EQU   8                                                  19028
XDCBPTR  EQU   10                  .REAL PP DCB POINTER           19018
XTIOTD   EQU   10                 .CONTAINS TIOT DD ENTRY DISP.   19018
XSYSTRTN EQU   10                       .ASYSNCH RTN              19028
XTCBPTR  EQU   11                 .TCB POINTER                    19018
XTCTPTR  EQU   11                 .TCT POINTER                    19018
XRWRK    EQU   12                 .WORK REGISTER                  19018
XTCBPIB  EQU   12                 .PIB  ADDRESS                   19018
XTCTSRC  EQU   13                 .LOOK UP TABLE SEARCH POINTER   19018
XRWRK2   EQU   13                       .WORK REGISTER            19028
XRTN     EQU   14                 .RETURN REGISTER                19018
XRWRK4   EQU   14                       .WORK REGISTER            19028
BASESMF  EQU   15                  .EXCP COUNTER ROUTINE BASE     19018
ENTLGTH  EQU   4                  .LENGTH OF LOOK UP ENTRY        19018
SLOTLNGH EQU   8                  .LENGTH OF SLOT FIELD           19018
TCBTCT   EQU   164                .TCT ADDRESS                    19018
C96      EQU   96                       .IQE ADDR OFFSET IN IRB   19028
IQEDCB   EQU   8                        .4 BYTE DCB ADDR          19028
IQEOUTLM EQU   4                        .4 BYTE OUTLIM ADDR       19028
C4       EQU   4                                                  19028
C1       EQU   1 .                                                20472
C2       EQU   2 .                                                20472
C7       EQU   7 .                                                20472
C8       EQU   8 .                                                20472
UCBTYPSM  EQU   18 .                                              M0761
DEC56    EQU   56 .                                               20472
CELLCTR  EQU   3 .                                                20472
CNT11    EQU   11 .                                               20472
DEC16    EQU   16 .                                               20472
DEBPTR   EQU   44 .                                              A28422
XS1      EQU   6                                                  19028
CVTSMCA  EQU   196                      .OFFSET TO SMCA           19028
CVTBTERM EQU   52                       .OFFSET TO ABTERM         19028
TCTDCB   EQU   32                       .4 BYTE DCB ADDR OFFSET   19028
VTCTINIT EQU   4                        .INIT TCB ADDR IN TCT     19028
C6       EQU   6 .                       WORK REG             BD A30900
ON       EQU   255 .                     SWITCH ON            BD A30900
OFF      EQU   0 .                       SWITCH OFF           BD A30900
TCBTIOT  EQU   12 .                      TIOT OFFSET IN TCB   BD A30900
         SPACE 2                                                  19018
         EXTRN IEATLEXT                                           19028
IEASMFEX CSECT                                                    19018
         SPACE 1                                                  19018
         USING *,BASESMF                                          19018
         LR    XTCBPTR,XTCBPASS .  LOAD TCB POINTER REG           20472
         L     XRWRK,TCBRBP(XTCBPTR) GET CURRENT RB               19018
         TM    XSTAB(XRWRK),XSVRB  IS RB AN SVRB                  19018
         BNO   SMFEXCPC            NO-CONTINUE PROCESSING         19018
         TM    XSTAB(XRWRK),X'E0'      LPRB                      A69325
         BC    1,SMFEXCPC              YES                       A69325
         TM    XSTAB+1(XRWRK),X'80' IS NEXT RB PTR THE TCB PTR    19018
         BCR   C1,XRTN .           YES-SKIP PROCESSING            20472
         L     XRWRK,XRBLNK(XRWRK) GET NEXT RB                    19018
         TM    XSTAB(XRWRK),X'E0'      LPRB                      A69325
         BC    1,SMF2                  YES                       A69325
         TM    XSTAB(XRWRK),XSVRB  IS THIS A PRB                  19018
         BCR   C7,XRTN .           NO-SKIP PROCESSING             20472
*  SVRB IS FIRST RB, PRB IS SECOND RB.  CHECK PRB RESUME PSW FOR  19018
*  OPEN/OPENJ/CLOSE/TCLOSE/EOV.                                   19018
SMF2     EQU   *                                                 A69325
         CLI   XRBPSW+3(XRWRK),X'15'  WAS SVC A STOW              19018
         BCR   C8,XRTN .           YES-SKIP PROCESSING            20472
         CLI   XRBPSW+3(XRWRK),X'37'  WAS SVC EOV                 19018
         BE    SMFEXCPB            YES-CONTINUE PROCESSING        19018
         CLI   XRBPSW+3(XRWRK),X'13'  WAS SVC LESS THAN OPEN      19018
         BCR   C4,XRTN .           YES-SKIP PROCESSING            20472
         CLI   XRBPSW+3(XRWRK),X'17' WAS SVC GREATER THAN TCLOSE  19018
         BCR   C2,XRTN .           YES-SKIP PROCESSING            20472
*  TEST IF THE DEB PASSED IS ON THE TCB DEB CHAIN. IF NOT THEN   A28422
*  THIS IS A WORK DCB AND NO COUNT TAKEN                         A28422
SMFEXCPB DS    0H .                                              A28422
         L     XRWRK,DEBPTR(0,XDCBPAS) .DEB ADDRESS FROM DCB     A28422
         LA    XRWRK,0(0,XRWRK) .  CLEAR HI BYTE                 A28422
         L     XRWRK2,0(XRWRK)         DEB TCB                   A69325
         LA    XRWRK2,0(XRWRK2)        HI BYTE                   A69325
         LTR   XRWRK2,XRWRK2           ZERO                      A69325
         BCR   8,XRTN                  YES RETURN TO IOS         A69325
SMFEXCPC LR    XDCBPTR,XDCBPAS                                    19018
SMFEXCPA L     XTCBPIB,TCBPIB(XTCBPTR)  TESET FOR A SYSTEM        19018
         LA    XTCBPIB,0(XTCBPIB)       TASK BY CHECKING FOR      19018
         LTR   XTCBPIB,XTCBPIB          A PIB. IF SYS TASK        19018
         BCR   C8,XRTN .           THEN NO EXCP COUNTING          20472
         L     XTCTPTR,TCBTCT(XTCBPTR)  GET TCT ADDRESS           19018
         LA    XTCTPTR,0(XTCTPTR)                                 19018
         LTR   XTCTPTR,XTCTPTR     IS THERE A TCT                 19018
         BCR   C8,XRTN .           BRANCH IF NO                   20472
         STM   R0,BASESMF,LSAVE      AVE CONTENTS OF ALL REGS     M3321
         L     XTCTPTR,TCTIOTBL(XTCTPTR)  GET I/O TABLE ADDRESS   19018
         LTR   XTCTPTR,XTCTPTR     IS THERE AN I/O TABLE          19018
         BCR   C8,XRTN .           BRANCH IF NO                   20472
         TM    DCBOFLGS(XDCBPTR),OB  IS DCB OPENED/BUSY           19018
         BCR   C8,XRTN .           BRANCH IF NO                   20472
         LH    XTIOTD,DCBTIOT(XDCBPTR)  GET TIOT DD OFFSET        19018
         SR    XRWRK,XRWRK                                        19018
         LA    XTCTSRC,TCTCOMIO(XTCTPTR)  LOAD SEARCH POINTER     19018
*  FIND RIGHT TCTIOT SLOT - A MATCH BETWEEN THIS DCB TIOT DD      19018
*  OFFSET AND A DCB TIOT DD OFFSET IN THE TCTIOT LOOKUP TABLE.    19018
SRCHSLOT CH    XRWRK,TCTDCBTD(XTCTSRC)   LAST SLOT                19018
         BCR   C8,XRTN .           BRANCH IF YES                  20472
         CH    XTIOTD,TCTDCBTD(XTCTSRC)  IS THIS THE SLOT         19018
         BE    SLOTFND             BRANCH IF YES                  19018
         LA    XTCTSRC,ENTLGTH(XTCTSRC)  STEP TO NEXT SLOT        19018
         B     SRCHSLOT                                           19018
*  COMPUTE THIS SLOT ADDRESS IN THE TCTIOT COUNTER TABLE.         19018
SLOTFND  AH    XTCTPTR,TCTIOTSD(XTCTSRC)  CALC SLOT ADDR.         19018
         ST    XTCTPTR,LSAVE1           .STORE CALC SLOT ADDR     M3321
         IC    XRWRK,TCTSCTR(XTCTPTR)  GET UCB COUNT THIS SLOT    19018
         LR    XRWRK2,XRWRK                 SAVE # UCB'S          19028
         LA    XTIOTD,0(0,XUCBPTR)     CLEAR FLAGS FROM UCB PTR   19018
*  FIND RIGHT TCTIOT COUNTER - A MATCH BETWEEN THIS UCB AND A UC  19018
*  IN THIS TCTIOT COUNTER SLOT.                                   19018
SRCHUCB  CH    XTIOTD,TCTUCBP(XTCTPTR)   THIS THE UCB             19018
         BE    UCBFND              BRANCH IF YES                  19018
         CLC   UCBTYPSM(C2,XTIOTD),D2321 .IS UCB FOR 2321 ?       M0761
         BNE   NOT2321 .           NO - BRANCH                    20472
         LA    XTIOTD,DEC56(XTIOTD) .POINT TO BIN 0               20472
         LA    CELLCTR,CNT11 .     INIT COUNT TO 11               20472
BINLOOP  BCT   CELLCTR,SRCHCELL .  LOOP CONTROL                   20472
NOT2321  DS    0H .                                               20472
         LA    XTCTPTR,SLOTLNGH(XTCTPTR)  STEP TO NEXT UCB        19018
         BCT   XRWRK,SRCHUCB       NOT ZERO-SEARCH NEXT UCB       19018
         LA    XTCTPTR,SLOTLNGH(XTCTPTR) STEP OVER OUTLIM FIELD  A30900
         TM    CONCSW,ON .               ALREADY INIT'ED      BD A30900
         BO    NOSET .                   YES                  BD A30900
         MVI   CONCSW,ON .               SET SWITCH ON        BD A30900
         LR    C6,XTCBPASS .             TCB ADDR             BD A30900
         L     C6,TCBTIOT(C6) .          TIOT ADDR            BD A30900
         AH    C6,DCBTIOT(C4) .          TIOT DD ENTRY ADDR   BD A30900
NOSET    EQU   * .                                            BD A30900
         IC    XRWRK,0(C6) .             LENGTH OF DD ENTRY   BD A30900
         AR    C6,XRWRK .                ADDR OF NEXT DD ENTRY   A30900
         CLI   0(C6),0 .                 Q. END OF TIOT       BD A30900
         BE    NOTEXCP .                 YES - RETURN         BD A30900
         CLI   4(C6),C' ' .              Q. IS NAME BLANK     BD A30900
         BE    SLOTFND+4 .               YES CHECK UCB MATCH  BD A30900
         B     NOTEXCP .                 ELSE RETURN TO IOS   BD A30900
SRCHCELL CH    XTIOTD,TCTUCBP(XTCTPTR) .DOES 2321 UCB MATCH SLOT  20472
         BE    UCBFND .            BRANCH IF WE GET MATCH         20472
         LA    XTIOTD,DEC16(XTIOTD) .IF NO MATCH - NEXT BIN       20472
         B     BINLOOP .           CHECK COUNTER                  20472
*  COUNTER FOUND - ADD ONE TO COUNTER.                            19018
UCBFND   LA    XRWRK,1             SET EXCP COUNTER               19018
         AL    XRWRK,TCTDCTR(XTCTPTR)  ADD IN COUNT               19018
         ST    XRWRK,TCTDCTR(XTCTPTR)  STORE NEW EXCP COUNT       19018
* THIS SECTION OF CODE FINDS THE TOTAL NUMBER OF EXCP'S           19028
* FOR EACH DEVICE PER DATA SET AND COMPARES THE TOTAL TO THE      19028
* EXCP OUTLIMIT VALUE PLACED IN THE TCTIOT BY OPEN.               19028
         SR    XRWRK,XRWRK                                        19028
         LA    XRWRK4,4            SET OFFSET TO EXCP FIELD       19028
         L     XTCTPTR,LSAVE1          RESTORE SLOT ADDRESS       M3321
SUMEXCP  AL    XRWRK,TCTUCBP(XRWRK4,XTCTPTR)  ADD EACH EXCP COUNT 19028
         LA    XRWRK4,8(XRWRK4)               STEP  TO NEXT  ONE  19028
         BCT   XRWRK2,SUMEXCP               BR IF NOT ALL TOTALED 19028
         LA    XRWRK2,4                                           M3321
         SR    XRWRK4,XRWRK2        SUBTRACT TO GET TRUE DISPL    19028
         SR    XRWRK2,XRWRK2                                      19028
         C     XRWRK2,TCTUCBP(XRWRK4,XTCTPTR) COMPARE OUTLIM TO 0 19028
         BE    NOTEXCP                        OUTLIM NOT SET, BR  19028
         BCTR  XRWRK,0             DECREMENT SUM BY ONE           M3321
         C     XRWRK,TCTUCBP(XRWRK4,XTCTPTR)  COMPARE SUM TO OUTLM19028
         BNE   NOTEXCP                  LIMIT NOT EXCEEDED, BR    19028
* THE OUTPUT LIMIT HAS BEEN EXCEEDED. CONTROL WILL NOW            19028
* BE PASSED TO THE STG2 EXIT EFFECTOR TO SCHEDULE IEATLEXT.       19028
* IEATLEXT SETS UP A BALR INTERFACE WITH IEFUSO.                  19028
         L     XSCMMTBL,16              ADDR OF CVT               M3321
         L     SMCAADR,CVTSMCA(XSCMMTBL)  LOAD SMCA ADDR          M3321
         TM    0(SMCAADR),EXITS        Q. EXITS ALLOWED           M3321
         BNO   OUTLMABN                NO ABTERM THE JOB          M3321
         LR    XTCBPTR,XTCBPASS        GET TCB ADDRESS            M3321
         L     PIBPTR,124(XTCBPTR)     POINTER TO PIB             M3321
         L     JSTQEADR,40(PIBPTR)      PTR TO PIB                M3321
         LA    IQEADDR,112(JSTQEADR)   POINTER TO TQEIQE          M3321
         LA    IQEADDR,16(IQEADDR)   POINTER TO SYSOUT IQE        M3321
         L     XDCBPTR,LSAVE+40                                   M3321
         L     XTCTPTR,164(XTCBPTR)    GET TCT ADDRESS            M3321
         ST    XDCBPTR,TCTDCB(XTCTPTR)  .STORE DCB ADDR IN TCT    M3321
         L     XRWRK,LSAVE1             .RESTORE SLOT ADDR        19028
         AR    XRWRK4,XRWRK       ADD TO GET OUTLM EFFECTIVE ADDR M3321
         ST    XRWRK4,IQEOUTLM(IQEADDR)    STORE OUTLM ADDR       M3321
         L     XSYSTRTN,IEATLEX                                   M3321
         OI    4(IQEADDR),X'40'           .TURN OUTLIM SWITCH ON  M3321
         LR    ADDRIQE,IQEADDR         PUT IQE ADDR IN REG 1      M3321
         LCR   ADDRIQE,ADDRIQE         INDICATE IQE               M3321
         L     XSEXTEFT,C4(XSCMMTBL)        STG2 EX EFF ADDR      M3321
         BALR  R14,XSEXTEFT            GO TO STG2                 M3321
NOTEXCP  LM    R0,BASESMF,LSAVE     RESTORE IOS REGS AND RETURN   M3321
*   BASE ADDR IS STILL VALID AFTER RESTORING REGS             BD A30900
         MVI   CONCSW,OFF .              SET SWITCH OFF       BD A30900
NOTEXCPA EQU   *                                                  M3321
         BR    XRTN                                               M3321
OUTLMABN L     R1,ABCODE2              SET OUTLIM ABEND CODE      M3321
         LR    XS1,BASESMF              SAVE BASE REG             M3321
         L     BASESMF,CVTBTERM(XSCMMTBL)  ABTERM ADDR FROM CVT   M3321
         BALR  R14,BASESMF              ABTERM THE TASK           M3321
         LR    BASESMF,XS1              RESTORE BASE REG          M3321
         B     NOTEXCP              RESTORE IOS REGS AND RETURN   M3321
         DS    0F                       .ALIGN TO FULL WD BDY     19028
LSAVE    DS    16F'0'             . LOCAL SAVEAREA                19028
LSAVE1   DC    F'0'                                               19028
ABCODE2  DC    X'80722000'                                        19028
D2321    DC    X'2005' .           UCB TYPE FOR 2321             A28422
CONCSW   DC    X'00' .                   CONCAT CHECK SWITCH  BD A30900
IEATLEX  DC    A(IEATLEXT)             ADDR IEATLEXT              19028
IEAAIH00 CSECT                                                    19018
.NOSMF4  ANOP                                                     19018
KABEND   DC    X'80F00000'             CONSTANT FOR ERROR CODE
.*LAST MACRO LABEL .HBBC
         MEND
