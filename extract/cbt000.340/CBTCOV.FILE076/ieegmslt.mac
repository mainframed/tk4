         MACRO
&NAME    IEEGMSLT &SECT,&SMBL=NO,&LABEL=IEEMSLT
         GBLC  &SCHDLR
         TITLE 'MASTER SCHEDULER RESIDENT CONTROL DATA AREA'
         AIF   ('&SECT' EQ 'CSECT').SECTC
&LABEL   DSECT
         AGO   .SECTX
.SECTC   ANOP
&LABEL   CSECT
.SECTX   ANOP
*                  =IEEMSLT= CORRESPONDS TO A TRANSFER VECTOR
*                            TABLE ENTRY.
*                  =IEEMSLTV=     SPECIFIES THE RELATIVE LOCATION IN
*                                 THE TRANSFER VECTOR TABLE OF THE
*                                 RESOLVED ADDRESS OF =IEEMSLT=.
*
*                            VERSION 5  4/30/65
*
*
*             ALL DATA AREA SYMBOLS BEGIN WITH =MS=.
*
*
         AIF   ('&SMBL' EQ 'NO').DATA
         SPACE
*
*                            =    GLOBAL SYMBOL DEFINITIONS     =
*                        *** REGISTER USAGE ***
*
ERRGPR   EQU   0        ERROR CODE DEVELOPMENT REGISTER.
M        EQU   8        ADDRESS OF RESIDENT MASTER DATA.
S        EQU   9        (DEFINE)  ADDRESS OF TRAILING DELIMITER.
Q        EQU   10       (DEFINE)  TERM CHARACTER COUNT.
P        EQU   11       (DEFINE)  ADDRESS OF LEADING TERM CHARACTER.
R        EQU   12       (DEFINE)  RETURN.
RETURN   EQU   14       MODULE RETURN.
BASE     EQU   15       MODULE BASE.
PASSREG0 EQU   0         PASSING REGISTER
PASSREG1 EQU   1         PASSING REGISTER
VRBTBL   EQU   3         VERB TABLE ORIGIN
NTRYSIZE EQU   4         VERB TABLE ENTRY SIZE
LASTNTRY EQU   5         VERB TABLE LAST ENTRY
WORKREG  EQU   6         WORK REGISTER
INDEXER  EQU   7         INDEXER,USUALLY CONTAINS 1
BASEREG1 EQU   8         BASE REGISTER FOR POINTING TO SWITCHES
INTPASSR EQU   9         ADDR OF INTERPRETER BUFFER
SCANREG  EQU   10        USED FOR VERB CHARACTER COUNT
BASEREG2 EQU   11        BASE REGISTER FOR POINTING TO COMMAND BUFFER
BASEREG  EQU   12        BASE REGISTER FOR THIS CSECT
TWLVSTAR EQU   1         12* ADDRESS REGISTER
XITAFCTR EQU   10        EXIT EFFECTOR ADDRESS REGISTER (IEAXFTTR)
TRBASADR EQU   13        TRANSFER BASE ADDRESS REGISTER
*
*                        *** USEFUL BIT PATTERNS ***
*
OFF      EQU   8         CONDITION
ON       EQU   1           CODES FOR
MIXED    EQU   4             TEST UNDER MASK
NEQ      EQU   7         NOT EQUAL CONDITION CODE
NE       EQU   7
EQ       EQU   8         EQUAL CONDITION CODE
E        EQU   8                  CC EQUAL.
NOP      EQU   0         NO OPERATION
BRANCH   EQU   15        UNCONDITIONAL BRANCH
Z        EQU   X'00'
ZEROS    EQU   X'00'     ALL ZEROS BYTE
F        EQU   X'FF'
ONES     EQU   X'FF'     ALL ONES BYTE
BLANK    EQU   C' '
COMMA    EQU   C','
EQUAL    EQU   C'='
SLASH    EQU   C'/'
*
*                        *** EQUATED SVC CODES ***
*
WAIT     EQU   X'01'     SVC CODE FOR WAIT MACRO
POST     EQU   X'02'     SVC CODE FOR POST MACRO
EXIT     EQU   X'03'     SVC CODE FOR EXIT MACRO
LINK     EQU   X'06'     SVC CODE FOR LINK MACRO
MGCR     EQU   X'22'     SVC CODE FOR MASTER GET COMMAND ROUTINE
         EJECT
*                       COMMAND EXECUTION MESSAGE CODES.
*                  MESSAGE CODE 1 - COMPOSITION ERRORS.
MSMSG001 EQU   1             CHARACTER COUNT.
MSMSG002 EQU   2             IMPROPER DELIMITER.
MSMSG003 EQU   3             UNIDENTIFIABLE KEY WORD.
MSMSG004 EQU   4             MISSING POSITIONAL PARAMETER.
MSMSG005 EQU   5             UNIDENTIFIABLE VERB.
MSMSG006 EQU   6             INVALID NUMERICS.
MSMSG007 EQU   7             SSS COMMAND REQUEST
MSMSG008 EQU   8             VARY
MSMSG009 EQU   9             MOUNT
MSMSG010 EQU   10                 VERIFY
MSMSG011 EQU   11                 DEFER
MSMSG012 EQU   12            REFERENCE
*                  OFFSETS FOR GENERAL ERROR CODE DEVELOPMENT PROCEDURE
ERRBSE   EQU   7             ERROR BASE COUNTER.
ERRCNT   EQU   0             CHARACTER COUNT.
ERRDEL   EQU   2             IMPROPER DELIMITER.
ERRKEY   EQU   4             UNIDENTIFIABLE KEY WORD.
ERRPOS   EQU   6             MISSING POSITIONAL PARAMETER.
ERRVRB   EQU   8             UNIDENTIFIABLE VERB.
ERRNUM   EQU   10            INVALID NUMERICS.
         EJECT
.DATA    ANOP
TVTADR   EQU   16        T.V. TABLE ADDRESS IN LOCATION 16
IEEMSLTV EQU   60        RELATIVE LOCATION OF IEEMSLT IN T.V.TABLE
IECLSTTV EQU   40       UCB 2-BYTE LOOK-UP TABLE.     CVT
IEESJQTV EQU   96       CANCEL SELECTED JOB QUEUE.    CVT
IEECUCBT EQU   100      CURRENT CONSOLE UCB.          CVT
IEFQMWR  EQU   148      CDA POINTER    CVT
EOB      EQU   X'E0'              END OF BUFFER MARK
*
          IEZBITS                                                 M0656
         SPACE
MSRESDNT DS    0D
MSBCTLH  DS    F        CONTROL PROGRAM V-TYPE HEADER
MSBFH    DS    F        V-TYPE BUFFER HEADER REGION.
MSCMD    DS    126C     COMMAND BUFFER.
MSEOB    DC    AL1(EOB) END OF BUFFER CHARACTER
MSCCTLB  DC    AL1(EOB) SECOND END OF BUFFER MARK
MSBSZ    EQU   126      BUFFER EXTENT.
         SPACE
&NAME    EQU   *
MS1BASEX EQU   *
         DC    XL20'0'
         DC    XL20'0'
         DC    XL8'0'        48 BYTES ZEROED.
         ORG   MS1BASEX
         SPACE
MSNTAL   DS    C                   INITIALIZATION BYTE.
MSNIP    EQU   BIT0               IPL SWITCH
*              BIT1     SYSOUT IPL.
*              BIT2     SYSOUT JOB START.
MSCURE34 EQU   BIT5          34 SECURITY BIT.
MSQNIP   EQU   BIT6          Q INITIALIZED.
MSPNIP   EQU   BIT7          PROCEDURE CATALOG INITIALIZED.
MSSSB    DS    C        SSS SYSTEM EXCLUSIVE     BYTE 2
MSCONFLG EQU   BIT0          CONSOLE FLAG
MSCANFLG EQU   BIT1          CANCEL FLAG FOR ABENDT
MSROLFLG EQU   BIT2          ROLL-OUT FLAG
MSSO     EQU   BIT3          SPINOFF FLAG(CANCEL)
MSBTN    EQU   MSSSB                   TIME NOTE BIT IS BIT 6
MSTN     EQU   2                       VALUE TO TURN ON TIME NOTE
MSSSDSN  EQU   BIT4                DISPLAY DATASET NAME            AABA
MSSSPACE EQU   BIT5                DISPLAY SPACE                   AABA
         SPACE
MSPFG    DS    C        PENDING FLAGS...
MSDATE   EQU   BIT0          IPL DATE
MSPNB    EQU   BIT1          PARTITION BUSY.
MSCMC    EQU   BIT2          COMMAND MOVE COMPLETED.
MSICR    EQU   BIT3                    INTERPRETER COMMAND RETURN.
MSSYN    EQU   BIT4          SYSTEM INPUT CONTROL PURGE REQUEST.
MSSYT    EQU   BIT5          SYSTEM OUTPUT CONTROL PURGE REQUEST.
MSBSP    EQU   BIT6          BLANK START PENDING(REQ=1,START BLANK=0)
MSCCS    EQU   BIT7          CONSOLE COMMAND SUPPRESSED.
         SPACE
MSECBFL  DS    C        ECB FLAGS...
MSEXT    EQU   BIT0          EXTERNAL INTERRUPT.
MSWTO    EQU   BIT1          WRITE TO OPERATOR.
MSWTL    EQU   BIT2          WRITE TO LOG.
MSATTN   EQU   BIT3          CONSOLE ATTENTION.
MSYSIN   EQU   BIT4          SYSTEM INPUT.
MSYSOUT  EQU   BIT5                SYSTEM OUTPUT.
MSMCR    EQU   BIT6          MASTER COMMAND ROUTINE
MSSUM    EQU   BIT7          SUMMARY BIT, VARY UCB SCAN REQUIRED.
         SPACE
MSSSA    DS    0C                  SSS RESIDENT SWITCHES.
MSTUS    DS    C        STATUS FLAGS...
MSINLSW  EQU   BIT0          MASTER INITIALIZATION SWITCH.
MSSSSIPL EQU   BIT0               IPL.
MSWRPEN  EQU   BIT1          WTO PENDING.
MSNUPSW  EQU   BIT2          CONSOLE USUAGE, PRINCIPAL OR ALTERNATE.
MSWRLOG  EQU   BIT3          LOG PURGE REQUEST.
MSREOF   EQU   BIT4                READER END OF FILE.
MSSRDR   EQU   BIT4                START READER.
MSNRP    EQU   BIT5                NEW READER PENDING.
MSNWP    EQU   BIT6                NEW WRITER PENDING.
MSYOUT   EQU   BIT6                NEW WRITER PENDING(MODIFY).
MSJNF    EQU   BIT7          JOB NOTIFICATION FLAG(1=YES).
         SPACE
MSFHF    DS    C        FETCH FLAGS...
MSNMF    EQU   BIT0          NAMED     FETCH
MSCSD    EQU   BIT1          CURRENT COMMAND EXECUTION SEQUENCE DEFER.
MSTTT    EQU   BIT2          TCB TREE TRACE  FETCH (LOCATE).
MSFAX    EQU   BIT3          AUXILARY FETCH GIVEN.
MSREPLYB EQU   BIT4          REPLY BIT TO REQUEST ATTENTION
MSPSDT   EQU   BIT5          PSEUDO-SYSOUT FLAG
MSQHR    EQU   BIT7          Q HOLD-RELEASE
         SPACE
MSVRB    DS    8C       COMMAND VERB.
MSPASS   DS    8C       VARIABLE COMMUNICATION FIELD
MSERM    DS    H        MESSAGE GENERATION CONTROL.
MSPBP    DS    F        P POINTER. POINTS TO CHAR. BEFORE LIST.
         SPACE
MSECB    DS    F        MASTER ECB.
MSSJQ    DS    F        ADDR OF ECB IN SJQ ENTRY OF JOB USING CONSOLE.
*                       CURRENT UCB POINTED AT FROM IEECUCB.
MSBOBECB DS    F        ECB FOR ALLOCATION INTERNAL USE.
*                                                UCB POINTERS.
MSUCBPR  DS    F        PRIMARY        UCB
MSUCBAL  DS    F        ALTERNATE      UCB
MSABL    DC    V(IEA0IO02)              PSEUDO-DISABLE SWITCH.
MSPPTCB  DC    V(IEAPPTCB)    PROBLEM PROGRAM TCB
         SPACE
         AIF   ('&SCHDLR' NE 'SSS').SPASS
MSTPTCB  DC    V(IEAHPTCB)    HIGHEST PRIORITY TCB
         AGO   .END
.SPASS   ANOP
MS2BASEX EQU   *
         DC    XL20'0'
         DC    XL20'0'
         DC    XL20'0'
         DC    XL20'0'
         DC    XL20'0'
         DC    XL8'0'        108 BYTES ZEROED.
         ORG   MS2BASEX
MSQAL    DS    C        QUEUE MANAGER ALTER OPERATION CODES...
MSQCL    EQU   1             CANCEL.
MSQCD    EQU   2             CANCEL WITH DUMP.
MSQHD    EQU   3             HOLD.
MSQRL    EQU   4             RELEASE.
MSQRS    EQU   5             RESET.
MSQDY    EQU   6             DISPLAY.
MSQSB    EQU   7             START(BLANK).
MSQSN    EQU   8             START(NON-BLANK).
MSQDT    EQU   9             DELETE.
MSPRY    DS    C        PRIORITY VALUE FOR RESET.
MSRPYWA DS     C        REPLY TABLE INTERLOCK BYTE.
         SPACE
MSQJBN   DS    8C       JOBNAME FOR QUEUE ALTER OPERATION.
MSABC    DS    C        TRANSIENT FLAGS.
*                                                FETCH ARGUMENTS.
MSECBRI  DS    F        ECB FOR READER/INTERPRETER.
MSECBAC  DS    F        ECB FOR ALLOCATION COMPLETION.
MSECBC   DS    F        COMPLETION ECB FOR PARTITON CONTROL.
         SPACE
*                                                CONTROL POINTERS.
MSPECB   DS    F        PARTITION CONTROL ECB POINTER.
MSCAR    DS    F        CURRENTLY ACTIVE COMMUNICATION REGION.
MSINN    DS    F        SYSTEM INPUT CONTROL CORE.
MSOUT    DS    F        SYSTEM OUTPUT CONTROL CORE.
MSICO    DS    F        INTERPRETER COMMAND ORIGIN.
MSICE    DS    F        INTERPRETER COMMAND EXTENT.
MSVRBPTR DS    F        WTO AND WTL SVC CHAIN FIELD.
MSRPYPTR DS    F        WTOR SVC CHAIN FIELD.
         SPACE
MSFAL    DS    D             AUXILARY (PRESCRIBED 2ND SUCCESSOR FETCH).
         SPACE
MSMRT    DS    44C      MASTER REPLY TABLE.
.END     ANOP
         MEND
