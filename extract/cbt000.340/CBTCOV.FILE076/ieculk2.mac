         MACRO
         IECULK2 &INDXVLU,&OCU,&NCU,&CHINDX,&NCHANSW,&NEGC,&NEG
         GBLB  &IECITB,&IECICTB(16),&IECIC2B                      M1791
         GBLA  &IECICTA(16),&CHAN                                 M1791
         LCLA  &COUNT,&COUNT1,&COUNT2                            A38114
&COUNT1  SETA  1                                                   TBYT
         AIF   (NOT &IECITB).NTB        TEST FOR 2 BYTE LOOKUP     TYBA
&COUNT1  SETA  2                                                   TBYT
         AIF   (&NCHANSW NE 1).NOUP                              A38114
&CHAN    SETA  &CHAN+1                                           A38114
.NOUP    ANOP                                                    A38114
         AIF   (NOT &IECICTB(&CHAN)).NTB FILL NEEDED?            A38114
         DC    XL&IECICTA(&CHAN).'0'   FILL                      A38114
&IECICTB(&CHAN) SETB  0                RESET GBL                 A38114
.NTB     ANOP                                                      TBYT
&COUNT   SETA  &OCU                     INITIALIXE OLD TO COUNTER
         AIF   (&NCHANSW EQ 1).CHKCIF   NEW CHANNEL-TO CHK INDX FACTOR
.FILUPA  AIF   (&COUNT+1 GE &NCU).INSRTIX  CHECK FOR FILLER NEEDED
         DC    AL&COUNT1.(0)            NORMAL FILL                TBYT
&COUNT   SETA  &COUNT+1                 UPDATE COUNTER
         AGO   .FILUPA                  RETURN TO LOOP
.CHKCIF  ANOP            CHECK FOR FILLER NEEDED ON CHANNEL SWITCH
         AIF   (&NEGC EQ 0).INSRTIX    TO INSERT INDEX            M5906
&COUNT   SETA  &CHINDX                 SET CNT TO LST CH INDX     M5906
.FILUPB  AIF   (&COUNT EQ 0).INSRTIX   END OF FILL TO INSRT INDX  M5906
         DC    AL&COUNT1.(0)            INDEX FILL                 TBYT
&COUNT   SETA  &COUNT-1                UPDATE COUNTER             M5906
         AGO   .FILUPB                  RETURN TO TEST FOR COMPLETE
.ZEROFIL ANOP
         DC    AL&COUNT1.(0)            CONTROL UNIT INDEX (SUB)   TBYT
         MEXIT
.INSRTIX ANOP          INSERT THE ACTUAL INDEX TO THE TABLE
         AIF   (&NEG EQ 1).ZEROFIL     TEST FOR MINUS INDEX
&COUNT   SETA  &INDXVLU/2              SET UP FINAL INDEX VALUR
         DC    AL&COUNT1.(&COUNT)       CONTROL UNIT INDEX         TBYT
.*2196000450,000600                                                TYBA
         MEND
