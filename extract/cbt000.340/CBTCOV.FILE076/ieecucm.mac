         MACRO
         IEECUCM &A,&B,&C,&D,&E,&G,&REPLY=1,&BUFFERS=1,&MCS=,&EXIT=,   *
               &MASTCON=,&ROUTCDE=,&HARDCDE=,&OLDWCDE=,&AUTH=,&ALTCON=,*
               &HARDCPY=(SYSLOG,NOCMDS,NOHCPY),&COMPCON=,&ALTCOMP=
         LCLA  &K,&SYSFLGS,&SYSFLG1,&XX,&SDSONE,&SDS5G           S21002
         LCLB  &OUT,&COMPOS                                      S21002
         LCLB  &F(4),&MC,&DISP(8)                                S21002
         LCLA  &N,&Z,&I,&M,&UCMESIZ,&FIXED                          MCS
         LCLC  &X,&Y
         LCLC  &P
         LCLA  &WQENUM,&RQENUM,&ZERONE                                P
         LCLC  &BUFTEK                                                P
         LCLA  &RK,&RK1,&RK2,&RK3,&RK4,&RK5,&RK6,&IT             S21003
         LCLB  &YES2740
.*A103500                                                      @SA76834
.*A338200                                                      @SA76542
.*
.*
.*
.*  OPERAND    DESCRIPTION
.*
.*    A        SUBLIST OF 3-BYTE UCB NAMES.
.*
.*    B        CORRESPONDING 2-BYTE NAME MODIFIERS. XX WOULD RESULT
.*              IN IGCXXONN AS THE PROCESSOR MODULE NAME.
.*
.*    C        CORRESPONDING 4-BYTE INITIAL ATTRIBUTES,
.*              GIVEN IN THE FORM WAXA.(AN N IN ANY
.*              POSITION IMPLIES LACK OF THAT ATTRIBUTE).
.*                  CHARACTER     MEANING
.*                     W           ENTRY SUPPORTS WTO.
.*                     A           ENTRY SUPPORTS ATTENTION
.*                     X           ENTRY SUBJECT TO EXTERNAL INTERRUPT
.*                     A           ENTRY ACTIVE AT IPL.
.*
.*             EXAMP.  NAXN   ALTERNATE CARD READER.
.*                     WANA   1052 ONLY CONSOLE.
.*                                                                    P
.*    D        BUFFER TECHNIQUE TO BE USED                            P
.*               F SPECIFIES FIXED NUMBER OF BUFFERS (AND WORK SPACE) P
.*               U SPECIFIES UNLIMITED                                P
.*                                                                    P
.*    E        DIFFERENTIATES BETWEEN CONSOLE ENTRIES FOR 2250,2260,
.*             AND 22OC. THESE DEVICES ARE SPECIFIED BY THE      S21003
.*             FOLLOWING PARAMETERS:                             S21003
.*                     A FOR 22OC                                S21003
.*                     B FOR 2250                                S21003
.*                     C FOR 2260                                S21003
.*                     D FOR ANR48                               S21003
.*                     E FOR ANR19                               S21003
.*
.*    G        CORRESPONDING 1-BYTE DESCRIPTIONS OF A CONSOLES   S21003
.*              INPUT/OUTPUT CAPABILITY.                         S21003
.*                 CHARACTER..MEANING                            S21003
.*                    F.......CONSOLE HAS FULL INPUT CAPABILITY  S21003
.*                            AND OUTPUTS BOTH IN-LINE MESSAGES  S21003
.*                            AND OUT-OF-LINE STATUS DISPLAYS.   S21003
.*                    M.......CONSOLE OUTPUTS ALL MESSAGES IN THES21003
.*                            MESSAGE STREAM. INPUT IS LIMITED TOS21003
.*                            LIGHT PEN ONLY (IF AVAILABLE).     S21003
.*                    S.......CONSOLE OUTPUTS STATUS DISPLAYS IN S21003
.*                            SPECIFICALLY DEFINED AREAS ONLY.   S21003
.*                            INPUT IS LIMITED TO LIGHT PEN ONLY S21003
.*                            (IF AVAILABLE).                    S21003
.*             NOTE: THE DEFAULT VALUE IS F.                     S21003
.*
.*    REPLY    /MAXIMUM REPLY ID TO BE ASSIGNED, LESS THAN 100.
.*             INTERPRET ALSO AS THE FIXED MODE REPLY QUEUE LIMIT
.*
.*
.*    BUFFERS  /MAXIMUM ALLOWABLE NUMBER OF IN-CORE BUFFERS WAITING.
.*
.*
.*    MCS       MULTIPLE CONSOLE SUPPORT IS INCLUDED.
.*
.*
.*    EXIT      EQUALS 'USEREXIT' WHEN USER DESIRED THAT HIS EXIT BE
.*               TAKEN PRIOR TO ROUTING OF EACH WTO/R FOR MCS.
.*
.*
.*    MASTCON   THE ADDRESS OF THE MASTER CONSOLE FOR MCS.
.*
.*
.*    ROUTCDE   A 4 DIGIT SUBLIST CORRESPONDING TO THE 16 POSSIBLE
.*               ROUTING CODES FOR EACH CONSOLE.
.*               EX: ROUTCDE=(FFFF,0001); WHERE THE 1ST CONSOLE HAS
.*               ALL ROUTE CODES AND THE 2ND HAS ONLY THE 16TH.
.*
.*
.*    HARDCDE   A 4 DIGIT GROUP CORRESPONDING TO THE 16 POSSIBLE
.*               ROUTING CODES FOR HARD COPY.
.*
.*
.*    OLDWCDE   A 4 DIGIT GROUP CORRESPONDING TO THE 16 POSSIBLE
.*               ROUTING CODES THAT CAN BE ASSIGNED TO OLD WTO/R-S.
.*
.*
.*    AUTH      A 2 DIGIT GROUP (THE LAST OF WHICH IS PRESENTLY 0)
.*               CORRESPONDING TO THE 4 PRESENT COMMAND CODES.
.*
.*
.*    HARDCPY   (SYSLOG, AN ADDRESS OR ZERO; CMDS, STCMDS,       S21002
.*               INCMDS OR NOCMDS; HCPY OR NOHCPY) DESIGNATING   S21002
.*               RESPECTIVELY: WHERE HARDCOPY WILL GO; WHETHER   S21002
.*               COMMANDS AND ALL STATUS DISPLAYS, COMMANDS AND  S21002
.*               STATIC STATUS DISPLAYS, COMMANDS AND INLINE     S21002
.*               RESPONSES OR NO COMMANDS AND RESPONSES WILL GO  S21002
.*               TO HARDCOPY; AND WHETHER HARDCOPY EXISTS.       S21002
.*
.*
.*    COMPCON   A 3 DIGIT ADDRESS SUBLIST FOR THE COMPOSITE CONSOLES
.*               CORRESPONDING TO THE RELATIVE CONSOLE ADDRESS IN
.*               THE 'A' PARAMETER: FFF IF NONE SPECIFIED         M1717
.*
.*
.*    ALTCON    A 3 DIGIT ADDRESS SUBLIST FOR THE ALTERNATE CONSOLES
.*               CORRESPONDING TO THE RELATIVE CONSOLE ADDRESS IN THE
.*               'A'PARAMETER: FFF IF NONE SPECIFIED              M1717
.*
.*
.*    ALTCOMP   A 3 DIGIT ADDRESS SUBLIST FOR THE COMPOSITE CONSOLES
.*               CORRESPONDING TO THE RELATIVE ALTERNATE CONSOLE
.*               ADDRESS IN THE ALTCON PARAMETER. FFF IF NONE     M1717
.*               SPECIFIED.                                       M1717
.*
         ACTR  6144                                            @SA76834
&RQENUM  SETA  &REPLY
&WQENUM  SETA  &BUFFERS
         AIF   (&RQENUM LT 100).RA                                68526
&RQENUM  SETA  99
.RA      ANOP
&N       SETA  N'&A
         SPACE
*                             UNIT CONTROL MODULE MAP (UCM).
         SPACE
&BUFTEK  SETC  '&D'
         AIF   ('&MCS' NE 'MCS').RADICE                            MCS
&MC      SETB  1                                                   MCS
.RADICE  ANOP                                                      MCS
         AIF   (&N LT 1).NOXTRN                                  MCS
UCM2EXT  DS    0D                  UCM EXTENTION                  M3883
UCM2WID  DC    H'0'                TJID OF TASK FOR UCMWQECB      M3883
UCM2RID  DC    H'0'                TJID OF TASK FOR UCMRQECB      M3883
         AIF   ('&BUFTEK' EQ 'F').MFTPOST                         21016
UCM2PST  DC    V(IEA0PT02)        MVTPOST BRANCH ENTRY            21016
         AGO   .AFTRMFT                                           21016
.MFTPOST ANOP
UCM2PST  DC    V(IEA0PT01)        MFT POST BRANCH ENTRY           21016
.AFTRMFT ANOP                                                     21016
         AIF   (NOT &MC).RADICE3                                 MCS
UCMPRFX  DS    0D                  MCS PREFIX AREA                M3883
UCMMCENT DC    A(UCMMCUCM)             PTR MASTER CONSOLE UCM NTRY MCS
         AGO   .MCENT
.NOMCUCM ANOP
UCM2     DSECT                     UCM EXTENTION                  M3883
UCM2EXT  DS    0D                  UCM EXTENTION                  M3883
UCM2WID  DC    H'0'                TJID OF TASK FOR UCMWQECB      M3883
UCM2RID  DC    H'0'                TJID OF TASK FOR UCMRQECB      M3883
UCM2PST  DC    F'0'    ENTRY PT TO POST WITH VALIDITY CHKING      21016
         SPACE
MCSUCM   DSECT                                                      MCS
UCMPRFX  DS    0D                  MCS PREFIX AREA                M3883
UCMMCENT DC    F'0'
.MCENT   ANOP
UCMSAVE0 DS    18F                     RESIDENT SAVE AREA          MCS
UCMDOME  DC    F'0'                    PTR 1ST DOM ELEMENT          MCS
         AIF   ('&EXIT' NE 'USEREXIT').NIXEXIT
UCMWTOX  DC    V(IEECVXIT)             PTR USER WTO/R EXIT RTN.    MCS
         AGO   .WASEXIT
.NIXEXIT ANOP
UCMWTOX  DC    F'0'
.WASEXIT ANOP
         AIF   (&N LT 1).NOT3
         AIF   ('&HARDCPY(3)' EQ 'NOHCPY').NOT1
&SYSFLGS SETA  &SYSFLGS+40
.NOT1    ANOP
         AIF   ('&HARDCPY(2)' EQ 'NOCMDS').NOT2                  S21002
         AIF   ('&HARDCPY(2)' NE 'STCMDS').NOT1A                 S21002
&SDSONE  SETA  &SDSONE+128                                       S21002
         AGO   .NOT2                                             S21002
.NOT1A   ANOP                                                    S21002
         AIF   ('&HARDCPY(2)' NE 'INCMDS').NOT1B                 S21002
&SDSONE  SETA  &SDSONE+64                                        S21002
         AGO   .NOT2                                             S21002
.NOT1B   ANOP                                                    S21002
&SYSFLGS SETA  &SYSFLGS+20
.NOT2    ANOP
         AIF   (&Z EQ &N).NOT2A                                     MCS
&Z       SETA  &Z+1                                                 MCS
         AIF   ('&B(&Z)' EQ '01' OR '&B(&Z)' EQ 'XX').NOT2
         AIF   ('&B(&Z)' EQ '21' OR '&B(&Z)' EQ '11').NOT2
&SYSFLGS SETA  &SYSFLGS+4                                           MCS
.NOT2A   ANOP                                                       MCS
&Z       SETA  0                                                    MCS
         AIF   ('&HARDCPY(1)' NE 'SYSLOG').NOT3
&SYSFLGS SETA  &SYSFLGS+2
.NOT3    ANOP
&Z       SETA  0                                                    MCS
.NOSET   AIF   (&Z EQ &N).NOT2B                                     MCS
&Z       SETA  &Z+1                                                 MCS
         AIF   ('&B(&Z)' NE '51' AND '&B(&Z)' NE '61').NOSET     S21003
&SYSFLG1 SETA  4                                                    MCS
         AGO   .NONE63                                              MCS
.NOT2B   ANOP
&SYSFLG1 SETA  0                                                    MCS
.NONE63  ANOP                                                       MCS
&Z       SETA  0                                                    MCS
UCMSFLGS DC    X'&SYSFLGS.'                                         MCS
         DC    X'&SYSFLG1.'                                         MCS
UCMSYSA EQU    128                     RESERVED
UCMSYSB  EQU   64                      HARD COPY SUPPORT REQUIRED   MCS
UCMSYSC  EQU   32                      COMMANDS TO HARD COPY        MCS
UCMSYSD  EQU   16                      CONSOLE SWITCH IS FOR MASTER MCS
UCMSYSE  EQU   8                       NO CONSOLES EXIST            MCS
UCMSYSF  EQU   4                       GRAPHIC CONSOLES EXIST       MCS
UCMSYSG  EQU   2                       HARD COPY DEVICE IS SYSLOG   MCS
UCMSYSH  EQU   1                       TIMER IS PRESENT & OPERATING MCS
UCMSYSI  EQU   X'80'               WQE CLEANUP NEEDED               MCS
UCMSYSJ   EQU   X'40'     HARD COPY TO BE DONE                      MCS
UCMSYSK  EQU   X'20'               NEW CONSOLE IS COMPOSITE         MCS
UCMSYSL  EQU   X'10'               OPEN BEING ISSUED TO RING ALARM  MCS
UCMSYSM   EQU   X'08'    FAILING CONSOLE IS COMPOSITE               MCS
UCMSYSN  EQU   X'04'               GRAPHIC CONSOLE PRESENT        19029
UCMSYSO  EQU   X'02'               USED BY WTL                    19772
         AIF   (&N LT 1).ZER1
UCMOWTOR DC    X'&OLDWCDE'
         AGO   .WHERE1
.ZER1    ANOP
UCMOWTOR DC    X'0000'
.WHERE1  ANOP
UCMCMID  DC    F'1'                    CURRENT MESSAGE ID NUMBER    MCS
         AIF   (&N LT 1 OR '&HARDCPY(1)' EQ 'SYSLOG' OR '&HARDCPY(3)' E*
               Q 'NOHCPY').SYSLOG
UCMHCUCM DC    A(UCMHARD)
         AGO   .DEVICE
.SYSLOG  ANOP
UCMHCUCM DC    F'0'
.DEVICE  ANOP
UCMXCT   DC    X'00'                   EXTERNAL REQUEST COUNT       MCS
         AIF   ('&EXIT' NE 'USEREXIT').OVER
UCMUEXIT DC    AL3(UCMEXIT)            PTR USER EXIT DATA           MCS
         AGO   .RAD333                                              MCS
.OVER    ANOP
UCMUEXIT DC    3X'00'                  NO USER EXIT DATA
.RAD333  ANOP                                                       MCS
         AIF   (&N LT 1 OR '&HARDCPY(3)' EQ 'NOHCPY').ZER2
UCMHRDRT DC    X'&HARDCDE'
         AGO   .WHERE2
.ZER2    ANOP
UCMHRDRT DC    X'0000'
.WHERE2  ANOP
         DC    X'0000'
UCMXSA   DC    A(UCM3WD)
         DS    1F
UCM3WD   DS    4F
UCMQRTN  DC    V(IEECMENQ)
UCMRUTCK DS    1F                      ROUTE CHECKING FIELD
UCMDOMRT DC    V(IEECMDOM)             DOM MOD ENTRY PT
         AIF   (&N LT 1).GETPTR                                     MCS
.GETZ    AIF   (&Z EQ &N).NO2740S                                A26944
&Z       SETA  &Z+1
         AIF   ('&B(&Z)' NE 'XX').GETZ
.GETPTR  ANOP                                                       MCS
UCMTPPTR DC    A(UCMTPSAV)             PTR TO 2740 SAVE AREA
&YES2740 SETB  1
         AGO   .OVERNO2
.NO2740S ANOP
UCMTPPTR DC    A(0)
.OVERNO2 ANOP
&Z       SETA  0
UCMNPECB DC    F'0'           POSTED WHEN NIP'S H.C. CAN BE WRITTEN MCS
UCMLOGAD DC    F'0'          ADDRESS OF LOG TCB                  A33710
UCMDTINT DC    F'3000'        DYNAMIC DISPLAY TIME INTERVAL      S21002
*                      THIS IS FOR STIMER BINTVL=UCMDTINT        S21002
*                      THIS DEFAULT IS 30 SECONDS.               S21002
UCMSDS1  DC    AL1(&SDSONE)   STATUS DISPLAY CONTROL BYTE 1      S21002
UCMSDS1A EQU   X'80'          STCMDS TO HARDCOPY                 S21002
UCMSDS1B EQU   X'40'          INCMDS TO HARDCOPY                 S21002
UCMSDS1C EQU   X'20'                                             S21002
UCMSDS1D EQU   X'10'                                             S21002
UCMSDS1E EQU   X'08'                                             S21002
UCMSDS1F EQU   X'04'                                             S21002
UCMSDS1G EQU   X'02'                                             S21002
UCMSDS1H EQU   X'01'                                             S21002
UCMSDS2  DS    XL3            RESERVED FOR SDS                   S21002
.RADICE3 ANOP                                                    S21002
         DS    0D                  ALIGNMENT FOR PREFIX PTRS     S21002
         DC    A(UCM2EXT)          PTR TO UCM EXTENSION          S21002
         AIF   (&N LT 1).PAC05                                   S21002
         AIF   (NOT &MC).PAC10     IF NO MCS THEN NO PREFIX      S21002
.PAC05   ANOP                                                    S21002
         DC    A(UCMPRFX)          PTR TO MCS PREFIX             S21002
         AGO   .PAC20                                            S21002
.PAC10   ANOP                                                    S21002
         DC    F'0'                NO PREFIX                     S21002
.PAC20   ANOP                                                    S21002
         SPACE 2
         AIF   (&N LT 1).NOLIMIT                                 MCS
         ENTRY IEECUCB
IEECUCB  EQU   *
         ENTRY IEECVUCM
IEECVUCM EQU *
.NOXTRN  ANOP
*                             FIXED ECBS.
UCMXECB  DC    F'0'               EXTERNAL INTERRUPT.
UCMAECB  DC    F'0'               ATTENTION INTERRUPT.
UCMOECB  DC    F'0'               WTO-WTOR REQUEST.
UCMDECB  EQU   *
UCMLECB  DC    F'0'               WTL REQUEST.
UCMRECB  DC    F'0'               RMS REQUEST ECB                 20061
         SPACE
*                             ECB LIST PTR (EIL).
         AIF ('&BUFTEK' EQ 'U' AND NOT &MC).DYNAEIL
UCMLSTP  DC  A(UCMEIL)
         AGO   .EILDONE
.DYNAEIL ANOP
UCMLSTP  DC    F'0'               EVENT INDICATION LIST PTR (EIL).
.EILDONE ANOP
         SPACE
*                            WTO/WTOR CONTROL FIELDS.
UCMWTOQ  DC    F'0'               WQE PTR.
UCMRPYQ  DC    F'0'               RQE PTR.
UCMRPYI  DC    3F'0'         REPLY ID ASSIGNMENT PATTERN.
         DC    X'00'
UCMRQLM  DC    AL1(&RQENUM)  ID ASSIGNMENT LIMIT
UCMWQLM  DC    AL2(&WQENUM)       BUFFER STACKING REQUIREMENTS
UCMRQECB DC    F'0'          REPLY REQUEST WAITING ECB.
UCMWQECB DC    F'0'          BUFFER REQUEST WAITING ECB.
UCMRQNR  DC    H'0'          CURRENT RQE COUNT.
UCMWQNR  DC    H'0'          CURRENT WQE COUNT.
UCMWQEND DC    F'0'          PTS TO LAST WQE, OR ZERO.
         AIF   ('&BUFTEK' EQ 'F').FIXED99                        S21002
UCMPXA   DC    V(IEECVTCB)       ADDR OF COMM. TASK TCB (U MODE ONLY)
         AGO   .FIXEDXX                                          S21002
.FIXED99 ANOP                                                    S21002
UCMPXA   DC    V(IEACMTCB)         COMM TASK TCB FOR FIXED MODE  S21002
.FIXEDXX ANOP                                                    S21002
UCMPXB   EQU   *
         AIF   ('&BUFTEK' EQ 'U').JUMP
&FIXED   SETA  1                                                    MCS
&ZERONE  SETA  &ZERONE+1                                              P
.JUMP    ANOP                                                         P
         AIF   (&MC).MCS2
         AGO   .RADICE2                                            MCS
.MCS2    ANOP                                                      MCS
&ZERONE  SETA  &ZERONE+2                                           MCS
.RADICE2 ANOP                                                      MCS
UCMMODE  DC    X'&ZERONE'               01 - FIXED BUFFER MODE        P
*                                       00 - UNLIMITED                P
*                                      02 - MCS IN SYSTEM          MCS
UCMAMFA  EQU   X'08'               ACCEPT MSTCONS FROM ANYONE       MCS
UCMOGCE  EQU   X'04'               ONLY GRAPHICS CONSOLES EXIST     MCS
UCMMCS   EQU   X'02'
UCMFIX   EQU   X'01'
UCMCORE  DC    X'00'
UCMMODEL DC    X'00'
UCMINCR  DC    X'00'
UCMFREE  EQU   X'80'
         SPACE
*                            VARIALBE ENTRY CONTROL FIELDS.
UCMVEA   DC    A(UCMLIST)         1ST ENTRY PTR. * SUITABLE FOR
UCMVEZ   DC    A(UCMESIZE)        ENTRY SIZE.    *   LM - BXLE
UCMVEL   DC    A(UCMEND)          LAST ENTRY PTR.* COMBINATION.
UCMSAVE3 DS    14F                SAVE FOR ED2
UCMSAVE4 DS    16F                SAVE FOR CRA
UCMR9SV  DS    F                  SAVE FOR ED2
         DS    0D                      ALLIGNMENT                   MCS
         SPACE 3
         AIF   ('&BUFTEK' EQ 'U' AND &MC).MCS4                     2445
         AIF   ('&BUFTEK' EQ 'U').JUMP2                            2445
*                                       THE NEXT TWO BXLE COMBINATIONSP
*                                       ARE USED ONLY IN FIXED MODE   P
UCMWEA   DC    A(UCMWQE)                SUITABLE FOR LM BXLE          P
UCMWEZ   DC    A(UCMWSIZ)               SEARCH OF WTO                 P
UCMWEL   DC    A(UCMWEND)               QUEUE ELEMENTS                P
         SPACE
UCMREA   DC    A(UCMRQE)                SUITABLE FOR LM BXLE          P
UCMREZ   DC    A(UCMRSIZ)               SEARCH OF REPLY               P
UCMREL   DC    A(UCMREND)               QUEUE ELEMENTS                P
         SPACE
UCMOPENX DS    58F                      WORK SPACE FOR OPEN-CLOSE
         SPACE
         AIF   (&MC).MCS4
UCMREAD  DS    CL144                    INPUT BUFFER                  P
UCMSAVE1 DS    18F                      SAVE AREAS USED BY            P
UCMSAVE2 DS    18F                      COMMUNICATION MODULES         P
UCMINDCB DS    34F                      SPACE FOR INPUT DCB
UCMOUDCB DS    29F                                OUTPUT              P
.MCS4    ANOP                                                      MCS
UCMEIL   DC    AL1(&N+7)          LENGTH OF MCS EIL               20061
         DC    X'00'                                                MCS
         DS    CL6                                                  MCS
         DC    A(UCMXECB)
         DC    A(UCMAECB)
         DC    A(UCMOECB)
         DC    A(UCMDECB)
         DC    A(UCMRECB)                                         20061
UCMRPYL  EQU   UCMEIL+1                                             MCS
         AIF   (&MC).MCS66
         DS    CL16
         AGO   .JUMP3
.MCS66   ANOP                                                       MCS
         DC    A(UCMLIST+&I*(72+144*&FIXED))                     S21002
&I       SETA  &I+1                                                 MCS
         AIF   (&I LT &N-1).MCS66
         DC    X'80'
         DC    AL3(UCMLIST+&I*(72+144*&FIXED))                   S21002
.JUMP3   ANOP                                                      MCS
         SPACE
.JUMP2   ANOP
         AIF   (&N GT 0).USERXIT
         AGO   .XITCODE                                            MCS
.USERXIT AIF   ('&EXIT' NE 'USEREXIT').NOEXIT                      MCS
.XITCODE ANOP                                                      MCS
**  USER EXIT WORK AREA  **
UCMEXIT  DS    0F                                                   MCS
UCMMSTXT DS    128C                MESSAGE TEXT                     MCS
UCMROUTC DS    1F                  ROUTE CODES                      MCS
UCMDESCD DS    1F                  DESCRIPTOR CODES                 MCS
UCMXTSAV DS    18F                     USER EXIT SAVE AREA          MCS
.NOEXIT  ANOP                                                      MCS
         AIF   (NOT &YES2740 AND &N GT 0).NIX2740                   MCS
UCMTPSAV DS    18F                     2740 SAVE AREA
.NIX2740 ANOP
*                            INDIVIDUAL ENTRY MAP.
*                            EACH ENTRY DEFINES CONSOLE UNIT SUPPORT.
         SPACE
UCMLIST  DS    0F            ENTRY ORIGIN.
UCMECB   DS    F                  I/O COMPLETION ECB.
UCMSBR   DS    F                  PROCESSOR PTR.
UCMDCB   DS    F                  CONTROL BLOCKS MODULE PTR.
UCMUCB   DS    F                  UCB PTR/UCB NAME.
UCMNAME  DS    CL8                PROCESSING MODULE NAME.
         SPACE
UCMSTS   DS    CL1           STATUS FLAGS.            TEST ORDER.
UCMAF    EQU   128                ATTENTION FLAG.     3
UCMPF    EQU   64                 OUTPUT(WTO) FLAG.   4
UCMBF    EQU   32                 BUSY FLAG.          2
UCMCF    EQU   16                 CLOSE FLAG.         1
UCMTA    EQU   8                  OPEN
UCMTB    EQU   4                  DEQ APPROPRIATE OUTPUT Q ENTRIES
UCMTD    EQU   2                   I/O RETRY FLAG              @SA76542
UCMTC    EQU   1             WORKING ON INLINE MLWTO             S21002
         SPACE
UCMATR   DS    CL1           ATTRIBUTE FLAGS.
UCMOF    EQU   128                WTO SUPPORT.
UCMIF    EQU   64                 ATTENTION SUPPORT.
UCMXF    EQU   32                 EXTERNAL SWITCH SENSITIVE.
UCMUF    EQU   16                 USE FLAG (0-INACTIVE,1-ACTIVE).
UCMLF    EQU   8                  LOAD FLAG.
UCMAT04   EQU   X'04'      DEVICE STATUS TO CHANGE                  MCS
         SPACE
UCMID    EQU   *                                                    MCS
UCMXA    DS    H
UCMXB    DS    F
         AIF   (&MC OR &N LT 1).MCS7
         AGO   .RAD10                                               MCS
.MCS7    ANOP
UCMRTCD  DC    X'0000'
         DC    X'0000'
UCMOUTQ  DC    F'0'                    PTR OUTPUT QUEUE
UCMAUTH  DC    X'00'
*        COMMAND GROUP 0 IS ALWAYS PRESENT FOR A CONSOLE         S21003
UCMAUTH1 EQU   X'80'         COMMAND GROUP 1 (SYS)               S21003
UCMAUTH2 EQU   X'40'         COMMAND GROUP 2 (I/O)               S21003
UCMAUTH3 EQU   X'20'         COMMAND GROUP 3 (CONS)              S21003
         DC    X'00'
UCMDISP  DS    CL2                     DISPOSITION FLAGS
UCMDISPA EQU   128                     MASTER CONSOLE               MCS
UCMDISPB EQU   64                      HARD COPY DEVICE/CONSOLE     MCS
UCMDISPC EQU   32                      GRAPHICS                     MCS
UCMDISPD EQU   16                      OUTPUT ONLY                  MCS
UCMDISPE EQU   8             CONSOLE HAS FULL I/O CAPABILITY     S21003
UCMDISPF EQU   4             CONSOLE IS MESSAGE STREAM ONLY      S21003
UCMDISPG EQU   2             CONSOLE IS STATUS DISPLAY ONLY      S21003
UCMDISPH EQU   1                                                    MCS
UCMALTEN DC    F'0'                                                 MCS
UCMOAOEN DC    F'0'                    PTR OUTPUT/ALTERNATE
*                                      OUTPUT UCM ENTRY
UCMWLAST DC    F'0'                    PTR LAST WQE IN INPUT Q
UCMCOMPC DC    F'0'                    PTR TO OTHER HALF OF
*                                      COMPOSITE CONSOLE
UCMMSG   DS    CL2                     MESSAGE TYPE FLAGS
UCMMSGA  EQU   128                     DISPLAY JOBNAMES REQUESTED   MCS
UCMMSGB  EQU   64                      DISPLAY STATUS REQUESTED     MCS
UCMMSGC  EQU   32            MONITOR ACTIVE                      S21002
UCMMSGD  EQU   16                      RESERVED                     MCS
UCMMSGE  EQU   8                       SHOW REQUESTED             19006
UCMMSGF  EQU   4                       MONITOR SESS               20034
UCMMSGG  EQU   2                       RESERVED                     MCS
UCMMSGH  EQU   1                       RESERVED                     MCS
UCMXOR   DC    X'00'                   USED TO XOR CHANGE BYTE-UCB  MCS
UCMDEVC  DS    CL1                     DEVICE CONTROL FLAGS
UCMDEVA  EQU   128                     FULL SCREEN ON GRAPHIC CONS  MCS
UCMDEVB  EQU   64                      PREPARE COMMAND ISSUED       MCS
UCMDEVCC EQU   32                      TESTED FOR CONSOLE SWITCH
UCMDEVD  EQU   16                      DOM ISSUED
UCMDEVE  EQU   8                       I/O COMPLETE
UCMDEVF  EQU   4                       MODIFIED DCM FOR DOM       20772
UCMDEVG  EQU   2                       HALT I/O ON 2740           20772
UCMDEVH  EQU   1                       RESERVED                     MCS
UCMMLAST DC    F'0'           PTR TO LAST MINOR WQE HANDLED      S21002
UCMRCT   EQU   *              TO LOAD RCT ADDRESS                S21002
UCMSDS5  DC    X'00'          STATUS DISPLAY CONTROL BYTE 5      S21002
UCMSDS5A EQU   X'80'         MLWTO LINE NEEDED TO KEEP WRITING   S21002
UCMSDS5B EQU   X'40'     INLINE OUTPUT PENDING                   S21002
UCMSDS5C EQU   X'20'     OUT-OF-LINE OUTPUT PENDING              S21002
UCMSDS5D EQU   X'10'     TRANSIENT DCM BLOCKED                   S21003
UCMSDS5E EQU   X'08'     TRANSIENT DCM LOCKED                    S21003
UCMSDS5F EQU   X'04'               FOR CRT, UCMMLAST VALID       S21002
UCMSDS5G EQU   X'02'       I/O HARDWARE IN OUTPUT-ONLY STATUS    S21002
UCMSDS5H EQU   X'01'                                             S21002
         DC    AL3(0)         POINTER TO ROUTING CONTROL TABLE   S21002
         AIF   ('&BUFTEK' EQ 'U').JUMP31                         S21002
UCMINPUT DS    CL144                                                MCS
.JUMP31  ANOP                                                       MCS
.RAD10   ANOP                                                       MCS
         SPACE
UCMESIZE EQU   *-UCMLIST     SIZE OF ENTRY IN BYTES.
&I       SETA  0                                                    MCS
.LOOP    ANOP                                                       MCS
         AIF   (&N LT 1).END
         AIF   (&M GE 1).NOORG
         EJECT
         ORG   UCMLIST
.NOORG   ANOP                                                       MCS
         SPACE
&M       SETA  &M+1
         AIF   (&M GT &N).END
         AIF   ('&MASTCON' EQ '&A(&M)'(1,3)).MCS8
         AGO   .RADICE7
.MCS8    ANOP
UCMMCUCM DS    0F
.RADICE7 ANOP
         AIF   ('&HARDCPY(1)' EQ '&A(&M)'(1,3)).MCS88
         AGO   .RAD77
.MCS88   ANOP
UCMHARD  DS    0F
.RAD77   ANOP
         AIF   (NOT &MC).NONE2                                      MCS
&Z       SETA  0
.BEGIN1  ANOP
         AIF   (&Z EQ &N).NONE
&Z       SETA  &Z+1
         AIF   ('&A(&M)'(1,3) NE '&ALTCON(&Z)'(1,3)).BEGIN1
UCMALT&M DS   0F
.NONE    ANOP
&Z       SETA  0
&COMPOS  SETB  0                                                 S21003
.BEGIN2  ANOP
         AIF   (&Z EQ &N).NONE1
&Z       SETA  &Z+1
         AIF   ('&A(&M)'(1,3) NE '&COMPCON(&Z)'(1,3)).BEGIN2
&COMPOS  SETB  1                                                 S21003
UCMCOM&M DS   0F
.NONE1   ANOP
&Z       SETA  0                                                    MCS
.BEGIN3  ANOP                                                       MCS
         AIF   (&Z EQ &N).NONE2                                     MCS
&Z       SETA  &Z+1                                                 MCS
         AIF   ('&A(&M)'(1,3) NE '&ALTCOMP(&Z)'(1,3)).BEGIN3        MCS
UCMALC&M DS    0F
.NONE2   ANOP                                                       MCS
&I       SETA  &I+1
         AIF   (&I GT &N).END
&X       SETC  '&A(&I)'(1,3)
&Y       SETC  'IGC&B(&I).07B'
&P       SETC  '&C(&I)'
&F(1)    SETB  ('&P'(1,1) EQ 'W')
&F(2)    SETB  ('&P'(2,1) EQ 'A')
&F(3)    SETB  ('&P'(3,1) EQ 'X')
&F(4)    SETB  ('&P'(4,1) EQ 'A')
&Z       SETA  128*&F(1)+64*&F(2)+32*&F(3)+16*&F(4)
*                       ENTRY.
         AIF   ('&B(&I)' NE 'XX').NOT2740                           MCS
         DC    F'0'                                                 MCS
         DC    V(IEEC2740)                                          MCS
&IT      SETA  &IT+1
         DC    V(DCB40&IT)
         AGO   .ITSDONE
.NOT2740 DC    3F'0'                                                MCS
.ITSDONE ANOP
         DC    CL3'&X'                 UNIT NAME.
         DC    CL1' '
         DC    CL8'&Y'                 PROCESSOR NAME.
         DC    XL1'0'
         DC    AL1(&Z)                 ATTRIBUTES
         AIF   ('&B(&I)' EQ '51').XB TEST FOR GRAPHICS SUPPORT    19029
         AIF   ('&B(&I)' EQ '61').XB   CRT WITH TRANS DCM        S21003
         DC    AL1(&I)                 UNIQUE ENTRY ID
         DC    XL5'0'                                               MCS
         AGO   .XBDUN                                              000A
.XB      ANOP                                                      000A
         DC    AL1(&I)             UNIQUE ENTRY ID                19029
         DC    X'00'                                              19029
         AIF   ('&E(&I)' EQ 'A').XB1 IS IT A 22OC                 19029
         AIF   ('&E(&I)' EQ 'B').XB2 IS IT A 2250                 19029
         AIF   ('&E(&I)' EQ 'C').XB3 IS IT A 2260                S21003
         AIF   ('&E(&I)' EQ 'D').XB4 IS IT AN ANR48              S21003
         AIF   ('&E(&I)' EQ 'E').XB5 IS IT AN ANR19              S21003
.XB1     ANOP                                                    S21003
&RK      SETA  &RK+1                                             S21003
         DC    V(IEECVD&RK)        DCM FOR 22OC                  S21003
         AGO   .XBDUN                                            S21003
.XB2     ANOP                                                    S21003
&RK1     SETA  &RK1+1                                            S21003
         DC    V(IEECVE&RK1)       DCM FOR 2250                  S21003
         AGO   .XBDUN                                            S21003
.XB3     ANOP                                                    S21003
&RK2     SETA  &RK2+1                                            S21003
         DC    V(IEECVF&RK2)        DCM FOR 2260                 S21003
         AGO   .XBDUN                                            S21003
.XB4     ANOP                                                    S21003
&RK3     SETA  &RK3+1                                            S21003
         DC    V(IEECVG&RK3)        DCM FOR ANR48                S21003
         AGO   .XBDUN                                            S21003
.XB5     ANOP                                                    S21003
&RK4     SETA  &RK4+1                                            S21003
         DC    V(IEECVH&RK4)        DCM FOR ANR19                S21003
.XBDUN   ANOP                                                      000A
         AIF   (&MC).MCS11
         AGO   .RAD12                                               MCS
.MCS11   ANOP                                                       MCS
&K       SETA  &K+1
         DC    X'&ROUTCDE(&K)'
         DC    X'0000'
         DC    F'0'                    PTR OUTPUT Q                 MCS
         DC    X'&AUTH(&K)'
         DC    X'00'
&DISP(1) SETB  0                                              BE  M2092
&DISP(2) SETB  0                                              BE  M2092
&DISP(3) SETB  0                                              BE  M2092
&DISP(4) SETB  0                                              BE  M2092
&DISP(5) SETB  0                                              BE  M2092
&DISP(6) SETB  0                                              BE  M2092
&DISP(7) SETB  0                                              BE  M2092
&DISP(8) SETB  0                                              BE  M2092
         AIF   ('&MASTCON' NE '&A(&M)').NO1
&DISP(1) SETB  1
.NO1     AIF   ('&HARDCPY(1)' NE '&A(&M)').NO2
&DISP(2) SETB  1
.NO2     AIF   ('&B(&K)' EQ '51').YES1                            19029
         AIF   ('&B(&K)' EQ '61').YES1    CRT WITH TDCM          S21003
         AGO   .NO3                                              S21003
.YES1    ANOP                                                    S21003
&DISP(3) SETB  1                                                 S21003
.*
.* AN OUTPUT-ONLY CRT CONSOLE IS ONE WHICH IS IN EITHER STATUS DISPLAY
.* OR MESSAGE STREAM MODE. A NON-CRT CONSOLE WILL HAVE &G(M) SET AS
.* 'F' OR NULL ON ENTRY TO THIS SECTION .
.*
.NO3     AIF   ('&G(&M)' EQ 'S' OR '&G(&M)' EQ 'M').NO4       BE  M2092
.*
.* IF A DEVICE IS PART OF A COMPOSITE CONSOLE, OR SUPPORTS ATTENTIONS
.* AND IS NOT IN STATUS DISPLAY OR MESSAGE STREAM MODE, IT MUST HAVE
.* FULL CAPABILITY AND IS NOT AN OUTPUT-ONLY CONSOLE.
.*
         AIF   (&COMPOS OR &F(2)).NO7                         BE  M2092
.NO4     ANOP                                                 BE  M2092
&DISP(4) SETB  1                                              BE  M2092
.*
.* A NON-CRT OUTPUT-ONLY CONSOLE IS ALWAYS MESSAGE STREAM MODE.
.*
         AIF   (NOT &DISP(3)).M                               BE  M2092
         AIF   ('&G(&M)' EQ 'S').NO6                          BE  M2092
.M       ANOP                                                    S21003
&DISP(6) SETB  1                                                 S21003
         AGO   .NO8                                              S21003
.NO6     ANOP                                                 BE  M2092
&DISP(7) SETB  1                                                 S21003
         AGO   .NO8                                              S21003
.NO7     ANOP                                                    S21003
&DISP(5) SETB  1                                                 S21003
.NO8     DC    B'&DISP(1).&DISP(2).&DISP(3).&DISP(4).&DISP(5).&DISP(6).X
               &DISP(7).&DISP(8)'
         DC    X'00'
&Z       SETA  0
         AIF   ('&ALTCON(&M)' EQ 'FFF').NOTIN                     M2956
.MORE    ANOP
&Z       SETA  &Z+1
         AIF   ('&A(&Z)' NE '&ALTCON(&M)').MORE                  S21003
         DC    A(UCMALT&Z)   PTR TO ALT                           M2956
         AIF   ('&ALTCOMP(&M)' EQ 'FFF').NOTCOMP                  M2956
.FINDOUT ANOP                                                     M2956
&XX      SETA  0
.FIND1   ANOP
&XX      SETA  &XX+1
         AIF   ('&ALTCOMP(&M)' NE '&A(&XX)').FIND1               S21003
         DC    A(UCMALC&XX)                                       M2956
         AIF   ('&ALTCON(&M)' EQ 'FFF').NOTCOMP                   M2956
         AGO   .ALTCOMP                                           M2956
.NOTIN   AIF   ('&ALTCOMP(&M)' EQ 'FFF').NOALT                    M2956
         AGO   .FINDOUT                                           M2956
.NOALT   ANOP                                                     M2956
         DC    F'0'          NO ALT CONSOLE                       M2956
.NOTCOMP ANOP                                                     M2956
         DC    F'0'          ALT NOT A COMPOSITE                  M2956
.ALTCOMP ANOP                                                       MCS
         DC    F'0'                    PTR LAST WQE
&Z       SETA  0
         AIF   ('&COMPCON(&M)' EQ 'FFF').NOMORE2                  M1717
.MORE1   ANOP
         AIF   (&Z EQ &N).NOMORE2                                   MCS
&Z       SETA  &Z+1
         AIF   ('&A(&Z)'(1,3) NE '&COMPCON(&M)'(1,3)).MORE1
.OUTPUT  DC    A(UCMCOM&Z)
         AGO   .NOMOR2
.NOMORE2 ANOP
         DC    A(0)
.NOMOR2  ANOP
         DC    F'0'                    MESSAGE,XOR,& DEVICE FIELDS
         DC    F'0'          PTR TO LAST MINOR HANDLED           S21002
&OUT     SETB  (&DISP(3) AND &F(2) AND NOT &DISP(5))             S21002
&SDS5G   SETA  &OUT*2                                            S21002
         DC    X'0&SDS5G'    SDS FLAGS                           S21002
         DC    AL3(0)        PTR TO ROUTING CONTROL TABLE        S21002
.RAD12   ANOP                                                       MCS
         AIF   ('&BUFTEK' EQ 'U' OR NOT &MC).JUMP51                 MCS
         DS    CL144                   INPUT AREA                   MCS
.JUMP51  ANOP                                                       MCS
         AIF   (&F(1) OR &F(2)).A
         AGO   .MSG
.A       ANOP
         AIF   (&F(3) OR &F(4)).B
.MSG     ANOP
         MNOTE *,'UNUSUAL ATTRIBUTES, ENTRY &I'
.B       ANOP
         AGO   .LOOP
.END     ANOP
UCMEND   EQU   *-UCMESIZE
         AIF   ('&BUFTEK' EQ 'U').NOLIMIT
         SPACE 2
UCMWQE   DS    0D
*        IEECVMUG WQE         WTO/WTOR TEXT QUEUING MAP               P
*WQEF    DS    0D
         AIF   (NOT &MC AND &N GT 0).OLD7
*WQEXTNT DS    0CL168                                               MCS
         AGO   .OVERME                                              MCS
.OLD7    ANOP                                                       MCS
*WQEXTNT DS    0CL144
.OVERME  ANOP                                                       MCS
*WQEUSE  EQU   *
*WQELKPTR EQU  *+1
*WQELKP  DS    F                       LINK PTR
*WQENBR  DS    F                       COUNT FIELD (CCW)
*WQETXT  DS    CL128                   TEXT
*WQETXTL EQU   *-1                     LAST TEXT BYTE
*WQEXA   DS    CL1                     DISPOSITION FLAGS          20021
*WQETJID1 DS   CL1                     TSO USER ID - 1ST BYTE     20021
*WQETJID2 DS   CL1                     TSO USER ID - 2ND BYTE     20021
*WQEAVAIL DS   C                       BUFFER STATUS
*                                       80-FREE 40-IN USE 20-RESERVED P
*WQEXB   DS    F
         AIF   (NOT &MC AND &N GT 0).OLD3
*WQERTCT DS    CL1                     ROUTED WQE COUNT             MCS
*WQESEQN DS    CL3                     ID SEQUENCE NUMBER           MCS
*WQEMCSF DS    CL2                     MCS FLAGS                    MCS
*WQEMSGTP DS   CL2                     MESSAGE TYPE FLAGS           MCS
*WQEROUT DS    CL2                     ROUTING CODES                MCS
*        DS    CL2                     RESERVED FOR ROUT EXPANSION  MCS
*WQEUCMID DS   CL1                     UNIQUE UCM ENTRY ID          MCS
*WQEPKE  DS    CL1                TCB KEY OF WTO ISSUER IN MCS
*         DS   CL2                     RESERVED
*WQEDESCD DS   CL2                     DESCRIPTOR CODES
*         DS                           FOR CODE EXPANSION
*WQETIME  DS   1F                      TIMER ELEMENT
.OLD3    ANOP
*WQEL    EQU   *
*WQESIZE EQU   L'WQEXTNT
         AIF   (NOT &MC AND &N GT 0).OLD5
         DS    CL168                                               MCS
         AGO   .OVERI                                              MCS
.OLD5    ANOP                                                       MCS
         DS    CL144
.OVERI   ANOP                                                      MCS
UCMWSIZ  EQU   *-UCMWQE
         SPACE
         ORG   UCMWQE
.SETWQE  AIF   (&WQENUM LE 0).SETRQE
&WQENUM  SETA  &WQENUM-1
         DS    CL139                   SPACE FOR A WTO
         DC    X'80'                   QUEUE
         DS    F                       ELEMENT - FIXED MODE
         AIF   (NOT &MC AND &N GT 0).OLD4
         DS    6F
.OLD4    ANOP
         SPACE
         AGO   .SETWQE                                                P
.SETRQE  ANOP                                                         P
UCMWEND  EQU   *-UCMWSIZ                                              P
         SPACE 2
UCMRQE   DS    0D
*        IEECVMUG RQE         REPLY QUEUE ELEMENT MAP                 P
*RQEF    DS    0D                      DOUBLE WD ALIGNMENT
*RQELKP  DS    F                       LINK PTR
*RQEID   DS    CL2                     ELEMWNT ID
*RQEXA   DS    C                        BUFFER STATUS
*RQEAVAIL DS   C                       BUFFER STATUS
*RQETJID1 DS   C                       TSO USER ID - 1ST BYTE     20021
*RQETCB  DS    CL3                     TCB PTR                    20021
*RQEWQE  EQU   *                       PTR ASSOCIATED WQE           MCS
*RQEXB   DS    F
         SPACE
*RQERPY  DS    0F                      MAPS WTOR PREFIX
*RQELNTH DS    C                       MAX LGTH OF REPLY
*RQEPTR  DS    CL3                     BUFFER PTR FOR REPLY
*RQETJID2 DS   CL1                     TSO USER ID - 2ND BYTE     20021
*RQEECB  DS    CL3                     ECB PTR                    20021
*RQEL    EQU   *
*RQESIZE EQU   RQEL-RQEF               BLOCK SIZE
         DS    6F
UCMRSIZ  EQU   *-UCMRQE                                               P
         SPACE
         ORG   UCMRQE                                                 P
.SETR    AIF   (&RQENUM LE 0).RQDONE                                  P
&RQENUM  SETA  &RQENUM-1                                              P
         DS    CL7                      SPACE FOR REPLY
         DC    X'80'                    QUEUE
         DS    CL16                     ELEMENT
         SPACE
         AGO   .SETR                                                  P
.RQDONE  ANOP                                                         P
UCMREND  EQU   *-UCMRSIZ                                              P
         SPACE
         AIF   (&N LT 1).NOMCUCM                                 MCS
.NOLIMIT ANOP                                                         P
         MEND
