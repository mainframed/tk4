         MACRO
         IEAQTR &TRACE,&M65MP                                        MP
         LCLA  &NUMB,&NUMB4
         LCLB  &QNOTMP,&QM65MP                                       MP
&QM65MP  SETB  ('&M65MP' EQ '1'  OR  '&M65MP' EQ 'M65MP')            MP
&QNOTMP  SETB  (NOT &QM65MP)                                         MP
&NUMB    SETA  32*(&TRACE+1)                                       9301
&NUMB4   SETA  4*(&TRACE+1)                                        9301
IEAQTRCE START 0
         AIF   (&QNOTMP).M65MP00                                     MP
         EJECT
         IEAQFX 65,MCH,M65MP                                         MP
         EJECT
IEAQTRCE CSECT                                                       MP
         USING IEAQFX00,0                                          MP65
.M65MP00 ANOP                                                        MP
         TITLE 'OPTION 4  TRACE ROUTINE'
*...T R A C E   R O U T I N E...
         SPACE 2
         TITLE '             COMMENTARY FOR TRACE ROUTINE'
* ROUTINE NAME
         SPACE
*        IEAQTRCE- OPTION 4 TRACE ROUTINE.
         SPACE 2
* FUNCTION
         SPACE
*        PROVIDES A TRACE FUNCTION FOR 6 EVENTS-
*        1. EXTERNAL INTERRUPTS.
*        2. I/O INTERRUPTS.
*        3. PROGRAM INTERRUPTS
*        4. SVC INTERRUPTS.
*        5. START I/O (FROM IOS)
*        6. DISPATCHER EXIT CONDITIONS
         SPACE
*        FORMATS FOR EACH EVENT FOLLOW THE COMMENTARY
         SPACE 2
* ENTRY POINT NAMES
         SPACE
*        1. TREX-  TRACE ENTRY FOR EXTERNAL INTERRUPTS.
*        2. TRIO-  TRACE ENTRY FOR I/O INTERRUPTS.
*        3. TRPI-  TRACE ENTRY FOR PROGRAM INTERRUPTS.
*        4. TRSVC- TRACE ENTRY FOR SVC INTERRUPTS.
*        5. TRSIO- TRACE ENTRY FOR START I/O.
*        6. TRDISP-TRACE ENTRY FOR DISPATCHER.
*        7. TRSAV- TRACE BUILD AREA.
*        8. IECXTRA- TRACE INHIBIT SWITCH.
         SPACE 2
* CONTAINED IN MODULE
         SPACE
*        IEAQTRCE- OPTION 4 TRACE ROUTINE.
         SPACE 2
* ATTRIBUTES
         SPACE
*        1. SUPERVISOR STATE.
*        2. DISABLED FOR ALL MASKABLE INTERRUPTS.
*        3. RESIDENT.
*        4. SERIALLY REUSABLE.
         SPACE 2
* APPLICABILITY
         SPACE
*        OPTION 4 SYSTEMS
         SPACE 2
* EXTERNAL REFERENCES
         SPACE
*        1. IEATCBP-   'NEW' AND 'OLD' TCB POINTERS
*        2. IEATPC-    TIMER PSEUDO CLOCKS
*        3. FUDGESTR-  TEMPORARY LOW CORE REGISTER SAVE AREA.
         SPACE 2
* SVC USAGE
         SPACE
*        NONE
         SPACE 2
* INPUT
         SPACE
*        1. EXTERNAL INTERRUPTS
*              .REGS 0,1,15
*              .EX OPSW
*              .IF TIMER INTERRUPT, 1ST WORD OF TQE TRACED.
*              .TIMER VALUE
*        2. I/O INTERRUPTS
*              .IO OPSW
*              .CSW
*              .TIMER VALUE
*        3. PROGRAM INTERRUPTS
*              .REGS 0,1,15
*              .PI OPSW
*              .CURRENT TCB
*              .TIMER VALUE
*        4. SVC INTERRUPT
*              .REGS 0,1,15
*              .SVC OPSW
*              .CURRENT TCB
*              .TIMER VALUE
*        5. START I/O
*              .REGS 1,6,10
*              .CAW
*              .CSW
*              .TIMER VALUE
*        6. DISPATCHER
*              .REGS 0,1,15
*              .PSW RECEIVING CONTROL.
*              .NEW TCB ADDRESS
*              .TIMER VALUE
         SPACE 2
* OUTPUT
         SPACE
*        ENTRY IN TRACE TABLE (SEE FORMATS)
         SPACE 2
* ENTRY POINTS
         SPACE
*        1. TREX-   TRACE ENTRY FOR EXTERNAL INTERRUPTS.
*          .THE PSW IS DISABLED FOR SYSTEM INTERRUPTS, IS IN SUPERVISOR
*           STATE, AND HAS THE SUPERVISOR PROTECTION KEY.
*          .REGISTERS 0,1 ARE AS AT TIME OF INTERRUPT.
*          .REGISTERS 2-9 ARE IRRELEVANT.
*          .REGISTER 10 CONTAINS THE ENTRY POINT ADDRESS OF THE TRACE
*           CODE.
*          .REGISTER 11 CONTAINS THE RETURN ADDRESS.
*          .REGISTERS 12-14 ARE IRRELEVANT.
*          .REGISTER 15 IS AS AT TIME OF INTERRUPT.
*        2. TRIO-   TRACE ENTRY FOR I/O INTERRUPTS.
*          .THE PSW IS DISABLED FOR SYSTEM INTERRUPTS, IS IN SUPERVISOR
*           STATE, AND HAS THE SUPERVISOR PROTECT KEY.
*          .REGISTERS 0-2 ARE AS AT TIME OF INTERRUPT.
*          .REGISTERS 3,4 ARE IRRELEVANT.
*          .REGISTERS 5-9 ARE AS AT TIME OF INTERRUPT.
*          .REGISTERS 10 CONTAINS THE ENTRY POINT ADDRESS OF THE TRACE
*           CODE.
*          .REGISTER 11 CONTAINS THE RETURN ADDRESS.
*          .REGISTERS 12-15 ARE AS AT TIME OF INTERRUPT.
*        3. TRPI-   TRACE ENTRY FOR PROGRAM INTERRUPTS.
*          .THE PSW IS DISABLED FOR SYSTEM INTERRUPTS, IS IN SUPERVISOR
*           STATE, AND HAS THE SUPERVISOR PROTECT KEY.
*          .REGISTERS 0-9,12-15 ARE AS AT TIME OF INTERRUPT.
*          .REGISTER 10 CONTAINS THE ENTRY POINT ADDRESS OF THE TRACE
*           CODE.
*          .REGISTER 11 CONTAINS THE RETURN ADDRESS.
*        4. TRSVC-  TRACE ENTRY FOR SVC INTERRUPTS.
*          .THE PSW IS DISABLED FOR SYSTEM INTERRUPTS, IS IN SUPERVISOR
*           STATE, AND HAS THE SUPERVISOR PROTECT KEY.
*          .REGISTERS 0-9,12-15 ARE AS AT TIME OF INTERRUPT.
*          .REGISTER 10 CONTAINS THE ENTRY POINT ADDRESS OF THE TRACE
*           CODE.
*          .REGISTER 11 CONTAINS THE RETURN ADDRESS.
*        5. TRSIO-  TRACE ENTRY FOR START I/O.
*          .THE PSW IS DISABLED FOR SYSTEM INTERRUPTS, IS IN SUPERVISOR
*           STATE, AND HAS THE SUPERVISOR PROTECT KEY.
*          .REGISTERS 0,2-5,7,8,11-15 CONTAIN IRRELEVANT DATA.
*          .REGISTER 1 CONTAINS THE 16* ADDRESS RELATED TO THE SIO.
*          .REGISTER 6 CONTAINS THE DEVICE ADDRESS.
*          .REGISTER 9 CONTAINS THE ENTRY POINT ADDRESS OF TRSIO.
*          .REGISTER 10 CONTAINS THE CONDITION CODE.
*        6. TRDISP- TRACE ENTRY FOR DISPATCHER.
*          .THE PSW IS DISABLED FOR SYSTEM INTERRUPTS, IS IN SUPERVISOR
*           STATE, AND HAS THE SUPERVISOR PROTECT KEY.
*          .REGISTERS 0-9,13,15 ARE IRRELEVANT.
*          .REGISTER 10 CONTAINS THE ENTRY POINT ADDRESS OF THE TRACE
*           CODE.
*          .REGISTER 11 CONTAINS THE RETURN ADDRESS.
*          .REGISTER 12 CONTAINS THE NEW RB ADDRESS.
*          .REGISTER 14 CONTAINS THE NEW TCB ADDRESS.
         SPACE 2
* EXIT POINTS
         SPACE
*        1. BRANCH BACK TO CALLER
*          .THE PSW IS DISABLED FOR SYSTEM INTERRUPTS, IS IN SUPERVISOR
*           STATE, AND HAS THE SUPERVISOR PROTECT KEY.
*          .REGISTERS 0-9,12-15 ARE TRANSPARENT OVER ROUTINE EXCEPT FOR
*           DISPATCHER TRACE WHERE 12 AND 13 ARE VOLATILE.
*          .REGISTER 10- TRACE CODE BASE REG (IRRELEVANT).
*          .REGISTER 11- RETURN ADDRESS.
         SPACE 2
* ERROR EXIT POINTS
         SPACE
*        NONE
         SPACE 2
* TABLES AND CONTROL BLOCKS REFERENCED
         SPACE
*        1. TRACE TABLE-           ALTERED
*        2. IEATCBP (NEW AND OLD)- NOT ALTERED
         SPACE 2
* LIST ORIGINS, TABLES, CONTROL BLOCKS INCLUDED
         SPACE
*        1. TRACE TABLE
         SPACE 2
* RESTRICTIONS
         SPACE
*        NONE
         SPACE 2
* NOTES
         SPACE
*        TO MINIMIZE SYSTEM DEGRADATION AND MAXIMIZE SYSTEM       21014
*        PERFORMANCE, THE GTF CODE (21014) SHOULD COME AS         21014
*        EARLY IN EACH ENTRY AS POSSIBLE.                         21014
         EJECT
* NOTE THAT THE IDENTIFIER IS IN THE PSW FOR EXTERNAL, I/O, SVC, AND
* PROGRAM INTERRUPT AND DISPATCHER TRACES. IT IS LOCATED IN BIT
* POSITION 4 (THE 5TH BIT FROM THE LEFT).
         SPACE 3
* EXTERNAL INTERRUPT
         SPACE
* .................................................................
* .               .       .       .       .       .       .       .
* .    EXTERNAL   .  REG  .  REG  .  REG  . RE-   . TIMER . TIMER .
* .     OLD PSW   .   15  .   0   .   1   . SERVED. INTRPT. VALUE .
* . IDENTIFIER= 1 .       .       .       .       . TQE   .       .
* .               .       .       .       .       . 1ST WD.       .
* .0.......4.......8.......C.......0.......4.......8.......C.......
         SPACE 3
* I/O INTERRUPT
         SPACE
* .................................................................
* .               .               .       .       .       .       .
* .      I/O      .      CSW      . RE-   . RE-   . RE-   . TIMER .
* .    OLD PSW    .               . SERVED. SERVED. SERVED. VALUE .
* . IDENTIFIER= 5 .               .       .       .       .       .
* .               .               .       .       .       .       .
* .0.......4.......8.......C.......0.......4.......8.......C.......
         SPACE 3
* PROGRAM INTERRUPT
         SPACE
* .................................................................
* .               .       .       .       .       .       .       .
* .      PI       .       .       .       .       .       .       .
* .    OLD PSW    .  REG  .  REG  .  REG  . RE-   .CURRENT. TIMER .
* . IDENTIFIER= 3 .   15  .   0   .   1   . SERVED.  TCB  . VALUE .
* .               .       .       .       .       .       .       .
* .0.......4.......8.......C.......0.......4.......8.......C.......
         EJECT
* SVC INTERRUPT
         SPACE
* .................................................................
* .               .       .       .       .       .       .       .
* .      SVC      .  REG  .  REG  .  REG  . RE-   .CURRENT. TIMER .
* .    OLD PSW    .   15  .   0   .   1   . SERVED.  TCB  . VALUE .
* . IDENTIFIER= 2 .       .       .       .       .       .       .
* .               .       .       .       .       .       .       .
* .0.......4.......8.......C.......0.......4.......8.......C.......
         SPACE 3
* START I/O    IDENTIFIER = 0
         SPACE
* .................................................................
* .   .   .       .               .               .       .       .
* . C . D .  CAW  .      CSW      .   RESERVED    . TCB   . TIMER .
* . C . E .       .               .               . FROM  . VALUE .
* .   . V .       .               .               . 16 *  .       .
* .   .   .       .               .               .       .       .
* .0.......4.......8.......C.......0.......4.......8.......C.......
         SPACE 3
* DISPATCHER
         SPACE
* .................................................................
* .               .       .       .       .       .       .       .
* . NEW PSW TO BE .  REG  .  REG  .  REG  . RE-   .  NEW  . TIMER .
* .    LOADED     .   15  .   0   .   1   . SERVED.  TCB  . VALUE .
* . IDENTIFIER= D . (NEW) . (NEW) . (NEW) .       .       .       .
* .               .       .       .       .       .       .       .
* .0.......4.......8.......C.......0.......4.......8.......C.......
         SPACE 3
*        ENTRY/EXTRN DEFINITION
         SPACE 1
         ENTRY TREX,TRPI,TRSVC,TRIO,TRDISP,TRSIO
         ENTRY TRACE
         ENTRY TRPTR                    CURRENT TRACE POINTER
         ENTRY IECXTRA            TRACE INHIBIT SWITCH             FLSF
         SPACE 3
*        REGISTER EQUATES
         SPACE 1
R1       EQU   1                       REG 1
RA       EQU   10                      .REG 10
RB       EQU   11                      .REG 11
RC       EQU   12                      .REG 12
RD       EQU   13                      .REG 13
RE       EQU   14                      .REG 14
RF       EQU   15                      .REG 15
XTRBASE  EQU   10
XRET     EQU   11
TRPTRG   EQU   12
XPIAFFLK EQU   14                      .CVTAFFLK IN LAST BYTE      MP65
XWKA     EQU   14
XWKB     EQU   15
         SPACE 3
*        COUNT-FIELD EQUATES.
C1       EQU   1                       .BYTE
C2       EQU   2                       .HALF WORD.
C3       EQU   3                       .ADDRESS OR THREE-BYTE TMR. MP65
C4       EQU   4                       .FULL WORD.
C8       EQU   8                       .DOUBLE WORD.
*        GENERAL EQUATES
         SPACE 1
PSW      EQU   0
REG      EQU   8
DEV      EQU   0
CAW      EQU   4
CSW      EQU   8
         AIF   (&QM65MP).NOTMP00                                     MP
VAR      EQU   20
TCB      EQU   24
TIMER    EQU   28
.NOTMP00 ANOP                                                        MP
         AIF   (&QNOTMP).M65MP01                                     MP
TCB      EQU   16            .(SIO) TCB FROM 16*                   MP65
STM      EQU   20            .(EXT) SHOULDER-TAP MASK              MP65
TCBA     EQU   20            .(DISP) NEWA (SVC,SSM,PI) OLDA        MP65
AFFLK    EQU   TCBA          .(SSM) CVTAFFLK BEFORE ACTION OF SSM  MP65
TCBB     EQU   TCBA+4        .(DISP) NEWB (SVC,SSM,PI) OLDB        MP65
TQE      EQU   24            .(EXT) TQEFLAGS/TQETCB (TIMER INT)    MP65
TMR      EQU   28            .VALUE OF ACTIVE TIMER                MP65
CPU      EQU   TMR+3         .CPUID                                MP65
FLGTRSSM EQU   X'40'                   .ID FOR SSM-TRACE ENTRIES.  MP65
.M65MP01 ANOP                                                        MP
         SPACE 3
         AIF   (&QM65MP).NOTMP01                                     MP
EXOPSW   EQU   X'18'
IOOPSW   EQU   X'38'
PIOPSW   EQU   X'28'
SVCOPSW  EQU   X'20'
.NOTMP01 ANOP                                                        MP
RBOPSW   EQU   16                       RESUME PSW IN RB
         SPACE 3
***  TRACE ENTRY FOR SVC INTERRUPTS
         SPACE 1
         USING *,XTRBASE
         SPACE 1
TRSVC    ST    XRET,TREGSV              SAVE RETURN
         BAL   XTRBASE,PRECOM           INITIAL PHASE
         USING *,XTRBASE
*    RETURN FROM COMMON ROUTINE.
         MVC   PSW(8,TRPTRG),SVCOPSW    TRACE SVC OLD PSW
         OI    PSW+2(TRPTRG),X'20'      FLAG TRACE AS SVC
         AIF   (&QM65MP).NOTMP02                                     MP
         LA    XTRBASE,TRPI+8           SET BASE FOR COMMON
         USING TRPI+8,XTRBASE
         BC    15,TRCOMM                GO TO COMMON TRACE
.NOTMP02 ANOP                                                        MP
         AIF   (&QNOTMP).M65MP02                                     MP
         B     POSTCOM                 .PREPARE TO RETURN.         MP65
.M65MP02 ANOP                                                        MP
         SPACE 3
***  TRACE ENTRY FOR I/O INTERRUPTS
         SPACE 1
         USING *,XTRBASE
         SPACE 1
TRIO     ST    XRET,TREGSV              SAVE RETURN
         BAL   XTRBASE,PRECOM           INITIAL PHASE
         USING *,XTRBASE
*    RETURN FROM COMMON ROUTINE.
         MVC   PSW(8,TRPTRG),IOOPSW     TRACE I/O OLD PSW
         OI    PSW+2(TRPTRG),X'50'      FALG TRACE AS IO
         MVC   CSW(8,TRPTRG),64         TRACE CSW
         BC    15,POSTCOM               GO TO CLEAN UP
         SPACE 3
***  TRACE ENTRY FOR SIO
         SPACE 1
TRACE    DS    0C
         USING *,9
TRSIO    STM   10,11,TREGSV-4           SAVE REGS
         BAL   XTRBASE,PRECOM           INITIAL PHASE
*    RETURN FROM COMMON ROUTINE
         L     10,TREGSV-4              RESTORE REG
         ST    6,DEV(TRPTRG)            STORE DEVICE
         STC   10,DEV(TRPTRG)           STORE COND CODE
         NI    DEV(TRPTRG),X'30'        CLEAR ILC BITS
         MVC   CAW(4,TRPTRG),72         TRACE CAW
         MVC   CSW(8,TRPTRG),64         TRACE CSW
         MVC   TCB(4,TRPTRG),12(1)      TRACE 16* TCB
         BC    15,POSTCOM               CLEAN UP AND RETURN
         SPACE 3
***  TRACE ENTRY FOR EXTERNAL INTERRUPT
         SPACE 1
TQEPTR   EQU   12                       TQE PTR OFFSET FROM SHPC
TQETCB   EQU   0
         SPACE 1
         USING *,XTRBASE
TREX     ST    XRET,TREGSV              SAVE RETURN
         BAL   XTRBASE,PRECOM           INITIAL PHASE
         USING *,XTRBASE
*    RETURN FROM COMMON ROUTINE.
         AIF   (&QM65MP).NOTMPEX                                     MP
         L     RF,TREGSV+16        .RESTORE REG 15 FOR TRACE  AB  M0447
         STM   15,1,REG(TRPTRG)         TRACE 15,0,1
.NOTMPEX ANOP                                                        MP
         MVC   PSW(8,TRPTRG),EXOPSW     TRACE EXTERNAL OLD PSW
         OI    PSW+2(TRPTRG),X'10'      FLAG TRACE AS EXTERNAL
         AIF   (&QNOTMP).M65MP03
         AIF   (&QNOTMP).M65MP03                                     MP
         L     RF,PREFIX2              .PREFIX FOR OTHER PSA       MP65
         MVC   STM(C4,TRPTRG),STMASK-IEAQFX00(RF) .STMASK OTH CPU. MP65
         XC    TQE(C4,TRPTRG),TQE(TRPTRG)   .(NON-TIMER CASE)      MP65
.M65MP03 ANOP                                                        MP
         TM    EXOPSW+3,X'80'           Q. IS IT TIMER INT
         BC    8,POSTCOM                NO, PREPARE TO EXIT
         L     XWKA,ASHPC               GET TIMER PSEUDO CLOCK ADDR
         L     XWKA,TQEPTR(XWKA)        GET TQE PTR VALUE
         AIF   (&QM65MP).NOTMP03                                     MP
         MVC   TCB(4,TRPTRG),TQETCB(XWKA)  TRACE TQE FLAG AND TCB
.NOTMP03 ANOP                                                        MP
         AIF   (&QNOTMP).M65MP04                                     MP
         MVC   TQE(C4,TRPTRG),TQETCB(XWKA)  .(TQEFLGS/TQETCB)      MP65
.M65MP04 ANOP                                                        MP
         BC    15,POSTCOM               TO CLEAN UP
         SPACE 3
***  TRACE ENTRY FOR DISPATCHER
         SPACE 1
         USING *,XTRBASE
         SPACE 1
TRDISP   ST    XRET,TREGSV              SAVE RETURN
         BAL   XTRBASE,PRECOM           INITIAL PHASE
         USING *,XTRBASE
*    RETURN FROM COMMON ROUTINE
         L     13,TREGSV+4              GET THE RB
         MVC   PSW(8,TRPTRG),RBOPSW(13) TRACE THE RESUME PSW
         OI    PSW+2(TRPTRG),X'D0'      FLAG TRACE AS DISPATCHER
         AIF   (&QM65MP).NOTMP04                                     MP
         ST    14,TCB(TRPTRG)           TRACE NEW TCB
.NOTMP04 ANOP
         AIF   (&QNOTMP).M65MP05
*    STORE NEXT CURRENT TCB'S (NEW'S) IN TCBA AND TCBB             MP65
         L     RD,PREFIX2                                          MP65
         CLC   CPUID(C1),CPUID-IEAQFX00(RD)      .Q WHICH CPU      MP65
         L     RE,IEATCBP              .LOAD NEW1                  MP65
         L     RD,IEATCBP-IEAQFX00(RD)  .AND NEW2                  MP65
         BL    TRDISP2                 .B IF CPUID=A               MP65
         STM   RD,RE,TCBA(TRPTRG)                                  MP65
         B     TRDISP3                                             MP65
TRDISP2  ST    RE,TCBA(TRPTRG)                                     MP65
         ST    RD,TCBB(TRPTRG)                                     MP65
TRDISP3  DS    0H                      .(END BRANCH ON CPUID)      MP65
.M65MP05 ANOP
         L     13,108(14)               GET REG 15 FROM NEW TASK
         LM    14,15,48(14)             GET REGS 081 FROM NEW TASK
         STM   13,15,REG(TRPTRG)        TRACE THE REGS
         BC    15,POSTCOM               DO CLEAN UP AND RETURN
         SPACE 3
         AIF   (&QNOTMP).M65MP06                                     MP
*** TRACE ENTRY FOR SSM INTERRUPTS                                 MP65
         ENTRY TRSSM                                               MP65
         SPACE 1                                                   MP65
         USING *,XTRBASE                                           MP65
         SPACE 1                                                   MP65
TRSSM    ST    XRET,TREGSV                                         MP65
         BAL   XTRBASE,PRECOM                                      MP65
         USING *,XTRBASE                                           MP65
*     RETURN FROM COMMON ROUTINE                                   MP65
         MVC   PSW(C8,TRPTRG),PIOPSW   .MOVE PIOPSW. TO TRT LINE.  MP65
         OI    PSW+2(TRPTRG),FLGTRSSM  .FLAG FOR SSM TRACE ENTRY.  MP65
         MVC   AFFLK(C1,TRPTRG),TREGSV15                           MP65
         B     POSTCOM                 .PREPARE TO RETURN.         MP65
         SPACE 3                                                   MP65
.M65MP06 ANOP                                                        MP
***  TRACE ENTRY FOR PROGRAM INTERRUPTS
         SPACE 1
         USING *,XTRBASE
         SPACE 1
TRPI     ST    XRET,TREGSV              SAVE RETURN
         BAL   XTRBASE,PRECOM           INITIAL PHASE
         USING *,XTRBASE
*    RETURN FROM COMMON ROUTINE
         MVC   PSW(8,TRPTRG),PIOPSW     TRACE PGM INT OLD PSW
         MVI   PSW+2(TRPTRG),X'30'      FLAG TRACE AS PI      AB A46031
         SPACE 3
         AIF   (&QM65MP).NOTMP05                                     MP
*    THIS ROUTINE COMMON TO SVC AND PI TRACE
         SPACE 1
TRCOMM   EQU   *
         L     RF,TREGSV+16        .RESTORE REG 15            AB  M0447
         STM   15,1,REG(TRPTRG)         TRACE REGS 15,0,1
         L     14,AIEATCBP              GET 'NEW/OLD'
         MVC   TCB(4,TRPTRG),4(14)      TRACE CURRENT
         BC    15,POSTCOM               TO CLEAN UP
         SPACE 3
.NOTMP05 ANOP                                                        MP
         AIF   (&QNOTMP).M65MP07                                     MP
         B     POSTCOM                 .PREPARE TO RETURN.         MP65
.M65MP07 ANOP                                                        MP
***  PRECOMMON
*    PERFORM FUNCTIONS APPLICABLE TO ALL ENTRIES
         SPACE 1
PRECOM   DS    0C
         BALR  XRET,0                   SET BASE VALUE
         USING *,XRET
         SPACE 1
POINT    DS    0C
*        MVZ   IECXTRA+1,X'20'          PICK UP SWITCH
IECXTRA  BC    0,NOTRACE                BRANCH IF SWITCH ON
         STM   12,15,TREGSV+4           SAVE REGISTERS
*  TO MINIMIZE SYSTEM DEGRADATION AND MAXIMIZE SYSTEM PERFORMANCE 21014
* THE GTF CODE SHOULD COME AS EARLY AS POSSIBLE                   21014
         L     XWKB,ADRCVT         GET ADDRESS OF THE CVT
*                                  USE INTERNAL POINTER
*                                  IN CASE X'10' HAS BEEN
*                                  CLOBBERED SINCE ABTERM
*                                  HAS NOT  RE-INITIALIZED
*                                  X'10'
         USING CVTDSECT,XWKB      GET CVT ADDRESSABILITY          21014
         TM    CVTGTF,CVTGTFS     IS GTF ACTIVE                   21014
         BO    POSTCOM            YES,IGNORE OLD TRACE,GO RETURN  21014
         L     TRPTRG,TRPTR       GET CURRENT ENTRY
         LA    TRPTRG,32(TRPTRG)        ADVANCE IT
         CL    TRPTRG,TREND             Q. ARE WE AT END OF TABLE
         BC    10,*+12                  YES, SKIP OVER
STR      ST    TRPTRG,TRPTR             UPDATE CURRENT
         BC    15,*+12                  SKIP OVER
         L     TRPTRG,TRBEG             RESET TO BEGINNING OF TABLE
         BC    15,STR                   BACK TO STORE IT
         AIF   (&QM65MP).NOTMP06                                     MP
         MVC   TIMER(4,TRPTRG),80       TRACE TIMER
.NOTMP06 ANOP                                                        MP
         AIF   (&QNOTMP).M65MP08                                     MP
         L    RF,TREGSV+16 .        RESTORE REG 15 FOR TRACE  AB  M0447
         STM   RF,R1,REG(TRPTRG)     .STORE REGS (15),(0),(1)      MP65
TRTIMER  L     RD,PREFTMRA             .UPPER THREE BYTES          MP65
         MVC   TMR(C3,TRPTRG),TIMER-IEAQFX00(RD) . OF ACTIVE TIMER MP65
TRCPUID  MVC   CPU(C1,TRPTRG),CPUID    .CPU ID TO UNUSED TMR BYTE  MP65
*    STORE CURRENT TCB'S (OLD'S) IN TCBA AND TCBB                  MP65
         L     RD,PREFIX2              .PREFIX FOR OTHER CPU.      MP65
         CLC   CPUID(C1),CPUID-IEAQFX00(RD)      .Q WHICH CPU      MP65
         BL    TRCPUID2                .B IF CPU-A.                MP65
         L     RE,IEATCBP+4-IEAQFX00(RD)                           MP65
         L     RF,IEATCBP+4                                        MP65
         B     TRCPUID3                                            MP65
TRCPUID2 L     RE,IEATCBP+4                                        MP65
         L     RF,IEATCBP+4-IEAQFX00(RD)    .'OLD' FOR OTH CPU.    MP65
TRCPUID3 STM   RE,RF,TCBA(TRPTRG)      .STORE 'OLD' POINTERS.      MP65
.M65MP08 ANOP                                                        MP
         BCR   15,XTRBASE               RETURN
         SPACE 3
***  POST COMMON
*    CEAN UP BEFORE RETURN
         SPACE 1
POSTCOM  BALR  XTRBASE,0                ESTABLISH ADDRESSING
         USING *,XTRBASE
         LM    11,15,TREGSV             RESTORE REGS
         BCR   15,XRET                  RETURN
         SPACE 1
         DROP  XTRBASE
         USING POINT,XRET
NOTRACE  LM    10,11,TREGSV-4           RESTORE REGS
         BCR   15,11                    RETURN WITHOUT TRACING
***  REGISTER SAVE AREA
         SPACE 1
         DC    1F'0'
TREGSV   DC    5F'0'
TREGSV15 EQU   TREGSV+15               .(SSM) SAVED AFFIN BYTE.    MP65
         AIF   (&QM65MP).NOTMP07                                     MP
         EXTRN IEATCBP
AIEATCBP DC    A(IEATCBP)
.NOTMP07 ANOP                                                        MP
         EXTRN IEATPC
ASHPC    DC    A(IEATPC)
TRPTR    DC    A(IEAQTRA)
TRBEG    DC    A(IEAQTRA)
TREND    DC    A(IEAQTRA+&NUMB)
         DC    3D'0'
ADRCVT   DC    A(IEACVT)           ADDRESS OF THE CVT
         EXTRN  IEACVT
IEAQTRA  DS    &NUMB4.D
CVTDSECT DSECT                                                    21014
         CVT
         MEND
