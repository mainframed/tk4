         MACRO
         IEATCB &STORAGE,&ROLLOUT,&TIMER,&FLOATPT,&PROTECT,&HIARCHY,   *
               &LOGTCB,&SIZER,&TYPE,&MACH,&SZ,&GRPTYP,&SIZER2,&SZ2,    *
               &TSIPT,&TSLST,&TSVAL,&IORMS,&SMF,&ATTACH,&JST
         LCLA  &PROTKEY,&TCBID,&ADD,&NO,&DISP,&PTR,&CT,&N
         LCLA  &SIZE(52),&SIZE2(52),&NUM,&PROKEY,&DBBXBIT          LC0A
         LCLA  &HIPR,&LOPR                                         I250
         LCLA  &COUNT,&COUNT1,&COUNT2                              MCS
         LCLC  &GRUPTYP(52)                                        MFT2
         LCLB  &EQUSW,&SZSW,&SWIT                                  MFT2
         LCLC  &RBNAME,&FREEQ,&LOWER,&LOWER2,&UPPER                LC0A
         LCLC  &PRIMARY,&PARTITN,&NOTIME,&DTIME,&TTIME
         LCLC  &SSS,&MTSRB,&ROLLRB,&SSINRB,&SSOUTRB,&ZERO
         LCLC  &MSFREEQ,&MSLOWER,&MSUPPER
         LCLC  &ROFREEQ,&ROLOWER,&ROUPPER
         LCLC  &INFREEQ,&INLOWER,&INUPPER
         LCLC  &OUTFREQ,&OUTLOWR,&OUTUPPR
         LCLC  &MCTRB,&MCTFQ,&MCTUP,&MCTLO,&FIRSTPT,&LASTPT        MTS0
         LCLA  &TCB                                                MTS0
         LCLA  &TYPFLG,&SW,&SW2,&N1,&N2                            MFT2
         LCLA  &T                                                 19018
.*A065605                                                      BD M0630
.*D057410-057440,061500                                        BD M0630
.*3187,030150,057410,057500                                        MCS
.*066900                                                        YA65737
&PRIMARY SETC  'PRIM'
&PARTITN SETC  'PART'
&NOTIME  SETC  'NOTIM'
&DTIME   SETC  'DTIME'
&TTIME   SETC  'TTIME'
&SSS     SETC  'SSS'
&CT      SETA  1                                                   MFT2
&MCTRB   SETC  'IEECIR50'                                          MFT2
&MCTFQ   SETC  'IEECMSFQ'                                          MFT2
&MCTUP   SETC  'IEECMSUP'                                          MFT2
&MCTLO   SETC   'IEECIR50'                                         MFT2
&DISP    SETA  255
&PTR     SETA  1
&ZERO    SETC  '0'
&LOWER2  SETC  '0'                                                 LC0A
&MTSRB   SETC  'IEECIR45'
&SSINRB  SETC  'SYSINRB'
&SSOUTRB SETC  'SYSOUTRB'
&MSFREEQ SETC  'IEECIRFC'
&MSLOWER SETC  'IEECIR45'
&MSUPPER SETC  'IEECIRUP'
&INFREEQ SETC  'INFREEQ'
&INLOWER SETC  'INLOWER'
&INUPPER SETC  'INUPPER'
&OUTFREQ SETC  'OUTFREEQ'
&OUTLOWR SETC  'OUTLOWER'
&OUTUPPR SETC  'OUTUPPER'
&FREEQ   SETC  '&MSFREEQ'
&LOWER   SETC  '&MSLOWER'
&UPPER   SETC  '&MSUPPER'
&RBNAME  SETC  '&MTSRB'
*
.* THIS MACRO GENERATES ALL TCBS FOR THE MULTIPROGRAMMING SYSTEM WITH A
.*       FIXED NUMBER OF TASKS (AT LEAST A COMMUNICATIONS TCB, A
.*       MASTER SCHEDULER TCB, AND TWO USER TCB'S).
.*                FOR THE PRIMARY SYSTEM AND THE MULTI-
.*       PROGRAMMING SYSTEM WITH A FIXED NUMBER OF TASKS, IT ALSO
.*       GENERATES THE BOUNDARY BOXES AND TCB ID TABLE, AND THE ABEND
.*       APPENDAGES AND TIMER ELEMENTS(WHEN APPLICABLE).
.*       THE SYSTEM TCB'S NEVER HAVE:
.*             1.FLOATING POINT REGISTER SAVE AREA
.*             2.ABEND APPENDAGES
.*       THE TCB FORMAT BELOW INDICATES WHICH FIELDS ARE ZEROED AND
.*       WHICH FIELDS ARE INITIALIZED TO SOME VALUE DURING SYSTEM
.*       GENERATION.
.*
.* TCB'S GENERATED FOR EACH PARAMETER:
.*
.*       STORAGE = PRIM  -  NO TCBS
.*               = PART - AT LEAST TWO USER TCB'S
.*
.*
.*
         AIF   ('&HIARCHY' NE 'Y').NOLC0                           LC0A
&DBBXBIT SETA  1                                                   LC0A
.NOLC0   ANOP                                                      LC0A
         AIF   ('&STORAGE' EQ '&PRIMARY').GO11                     MO17
         TITLE 'IEATCB,TCB BOUNDARY BOXES ABEND APP. TIMER ELEMTS(OPT)'
.STATUS  ANOP
* STATUS- CHANGE LEVEL 000
*        TCB FORMAT
*           (IN THE PRIMARY SYSTEM THE TCB
*           IS INCLUDED IN THE AIH MODULE)   NOTES-
*        * * * * * * * * * * * * * * *        1.SYSTEM TCB-PTR TO RESP
*        *  FLOATING PT REG SAVE     *           RB
*        *   (OPTIONAL 32 BYTES)     *          PROB PROG TCB - 0
*        * * * * * * * * * * * * * * *
*        *   TCBRBP    *   TCBPIE    *
*        *    NOTE1    *      0      *
*        * * * * * * * * * * * * * * *        3.TIMER OPTIONS - TCB+124
*        *   TCBDEB    *   TCBTIO    *          NO TIMER OPTIONS - TCB
*        *      0      *      0      *            +120
*        * * * * * * * * * * * * * * *
*        *   TCBMP     *   TCBTRN    *        4.TCBFLGS+0
*        *      0      *      0      *           NO PROT - 0
*        * * * * * * * * * * * * * * *           WITH PROT
*        *   TCBMSS    * PKF *TCBFLGS*             SYSTEM TCB - 0
*        *    NOTE3    *   NOTE4     *             HI PRI PP TCB - 1
*        * * * * * * * * * * * * * * *             LO PRI PP TCB - 2
*        *X'00 *LPM*DSP*   TCBLLS    *          TCBFLGS+1 - 0
*        *  FF'*NT5*NT6*      0      *          TCBFLGS+2
*        * * * * * * * * * * * * * * *             SYSTEM TCB - X'80'
*        *   TCBJLB    *   TCBJSE    *             PROB PROG TCB - 0
*        *      0      *      0      *          TCBFLGS+3
*        * * * * * * * * * * * * * * *             NO FLT PT-0
*        *         TCBGRS            *             FLT PT AND SYS TCB-0
*        *        64 BYTES           *             FLT PT AND PP TCB-
*        * * * * * * * * * * * * * * *              X'08'
*        *TCBID* TCBFSA*   TCBTCB    *        5.SSS AND SYS TCB'S - 0
*        *NOTE6*   0   *    NOTE7    *          PPS AND PROB PROG TCB'S
*        * * * * * * * * * * * * * * *            HI PRI PP TCB - X'E8'
*        *   TCBTME    *             *            LO PRI PP TCB - X'A8'
*        *    NOTE8    *    NOTE9    *        6.TCBIDF -
*        * * * * * * * * * * * * * * *            255 - 1ST TCB
*        *     MSS BOUNDARY BOX      *            254 - 2ND TCB
*        *         12 BYTES          *            253 - 3RD TCB
*        * * * * * * * * * * * * * * *            ETC.
*        *        TIMER ELEMENT      *       7.TCBTCB - PTR TO NEXT TCB
*        *          96 BYTES         *           UNLESS LAST(0)
*        * * * * * * * * * * * * * * *        8.TCBTME - PTR TO TIMER
*                                                 ELEMENT WHEN TIMER
*                                                 OPTION CHOSEN
*                                             9.PARTITIONED SYSTEM -
*                                                 SYSTEM TCB'S-NO FIELD
*                                                 PROBLM PROG TCB'S
*                                                 TO RESP SCHED.
*  NOTE THAT ALL TCB'S ARE GENERATED IN WAIT STATE (TCBFLGS+5 = X'FF')
.NOLOG1  ANOP                                                       MCS
IEATCB00 CSECT
.GO11    ANOP                                                      MFT2
&N1      SETA  &N1+1                                               MFT2
&SIZE(&N1)     SETA &SIZER(&N1)                                    MFT2
         AIF   ('&HIARCHY' NE 'Y').NOLC1                           LC0A
&SIZE2(&N1)    SETA &SIZER2(&N1)                                   LC0A
.NOLC1   ANOP                                                      LC0A
&GRUPTYP(&N1)  SETC '&TYPE(&N1)'                                   MFT2
         AIF   (&N1 NE N'&SIZER).GO11                              MFT2
&NUM     SETA  N'&SIZER                                            MFT2
         AIF   ('&STORAGE' NE '&PRIMARY').GO1
&FREEQ   SETC  '0'
&LOWER   SETC  '&SIZE(1)'                                          MFT2
         AIF   ('&HIARCHY' NE 'Y').NOLC2                           LC0A
&LOWER2  SETC  '&SIZE2(1)'                                         LC0A
.NOLC2   ANOP                                                      LC0A
&UPPER   SETC  '&MACH'
         DC    A(0)                                                MO17
         ENTRY IEAMSBBX
         AIF   ('&TIMER' NE '&TTIME').CONT4C1                      LC0A
         ENTRY IEATIBBX
         AGO   .CONT4C1                                            LC0A
.GO1     ANOP
         ENTRY IEAHEAD
         ENTRY IEAMSTCB                                            MFT2
         ENTRY IEACMTCB
.OVER2   ANOP                                                      MFT2
&NO      SETA  &NO+1                                              19372
         AGO   .TACPTCB
.TACPNOP ANOP
         AIF   ('&ATTACH' NE 'ATTACH').NOATT                       I250
*      *****   SYSTEM ERROR TCB  **************************        I250
*                                                                  I250
         ENTRY IEASETCB                                            I250
         EXTRN SYSERRRB
         EXTRN IEASEBGN
         EXTRN IEASEEND
TCB&NO   DC    A(SYSERRRB)                                         I250
         DC    5F'0'                                               I250
         DC    A(BNDBX&NO)                                         I250
         DC    XL1'00'   PROTECT KEY                               I250
         DC    XL1'00'                                             I250
         DC    XL2'8000'                                           I250
         DC    XL2'0000'                                           I250
         DC    XL1'00'                                             I250
         DC    AL1(&DISP)                                          I250
         DC    2F'0'                                               I250
         DC    A(TCB&NO)                                           I250
         DC    16F'0'                                              I250
         DC    AL1(&TCBID)                                         I250
         DC    AL3(0)                                              I250
&NO      SETA  &NO+1                                               I250
         DC    A(TCB&NO)                                           I250
&NO      SETA  &NO-1                                               I250
         DC    8F'0'                                               I250
         DC    AL1(&DISP)                                          I250
         DC    AL3(0)                                              I250
         DC    5F'0'                                               I250
         DC    2F'0'                                               I254
         DC    2F'0'                                          BD A58667
*   USED FOR QUEUEING SVRBS CONTENDING FOR XSNT AREA               I250
         DC    2F'0'                                               I250
*  ABEND APPENDAGES                                                I250
         DC    2F'0'                                               I250
BNDBX&NO DC    AL1(&DBBXBIT)
         DC    AL3(IEASEBGN)
         DC    A(IEASEBGN)
         DC    A(IEASEEND)
         DC    3F'0'
IEASETCB EQU   TCB&NO
&NO      SETA  &NO+1                                               I250
&TCBID   SETA  &TCBID+1                                            I250
&DISP    SETA  &DISP-1                                             I250
.NOATT   ANOP                                                      I250
         AIF   ('&LOGTCB' NE 'LOG').GO3A
         AGO   .BLDLOG
.GO3A    AIF   ('&IORMS' NE 'IORMS').GO4A                         M3767
         AGO   .RMSTCB
.GO4A    ANOP
         ENTRY IGFRMTCB,IORMSSVC                                  M3767
IGFRMTCB EQU   * .                                                M3767
IORMSSVC EQU   * .                                                M3767
.G04     ANOP                                                      MCS
&NO      SETA  &NO-1                                              19372
*
* COMMUNICATION TASK TCB **************************************** MFT2
*                                                                  MFT2
&TYPFLG  SETA  192                                                 MCS
         AGO   .ASBLTCB                                            MFT2
.RETURN0 ANOP                                                      MFT2
IEACMTCB EQU   TCB&NO                                              MCS
*                                                                  MFT2
* MASTER  SCHEDULER TASK **************************************** MMFT2
*                                                                  MFT2
&TYPFLG  SETA  0                                                   MCS
&ADD     SETA  &ADD+1                                              MFT2
&RBNAME  SETC  '&MCTRB'                                            MFT2
&FREEQ   SETC  '&MCTFQ'                                            MFT2
&UPPER   SETC  '&MCTUP'                                            MFT2
&LOWER   SETC  '&MCTLO'                                            MFT2
         AGO   .ASBLTCB                                            MFT2
.RETURN1 ANOP                                                      MFT2
IEAMSTCB EQU   TCB&NO                                              MFT2
&ADD     SETA  &ADD+1
         AIF   ('&SMF' NE 'SMF').NOSMF                            19018
*
* SMF TASK TCB
*
         ENTRY SMFTCBIT                                           19018
         EXTRN IEESMFFQ                                           19018
         EXTRN IEESMFWT                                           19018
         EXTRN IEESMFUP                                           19018
         EXTRN TLEXTIRB                                           19018
&NO      SETA  &NO+1
TCB&NO   DC    A(IEESMFWT)                                        19018
         DC    5F'0'
         DC    A(BNDBX&NO)
         DC    XL1'00'        .PROTECT KEY
         DC    XL1'00'
         DC    XL2'8000'
         DC    XL2'0000'                                          19018
         DC    XL1'00'
         DC    AL1(&DISP)
         DC    2F'0'                                               I250
         DC    A(TCB&NO)                                           I250
         DC    16F'0'                                              I250
         DC    AL1(&TCBID)
&TCBID   SETA  &TCBID+1
         DC    AL3(0)
&NO      SETA  &NO+1
         DC    A(TCB&NO)
&NO      SETA  &NO-1
         DC    8F'0'                                               I275
         DC    AL1(&DISP)                                          I275
         DC    AL3(0)                                              I275
         DC    5F'0'                                               I275
         DC    2F'0'                                               I254
         DC    2F'0'                                          BD A58667
*****   TRANSIENT AREA REQUEST QUEUE ********************          I275
         DC    F'0'   PTR TO NEXT REQUEST ON QUEUE                 I275
         DC    F'0'   PTR TO SVRB IN WAIT                          I275
* ABEND APPENDAGES
         DC    2F'0'
BNDBX&NO DC    AL1(&DBBXBIT)
         DC    AL3(IEESMFFQ)                                      19018
         DC    A(IEESMFWT)                                        19018
         DC    A(IEESMFUP)                                        19018
         DC    A(0)
         DC    A(0)
         DC    A(0)
SMFTCBIT EQU   TCB&NO                                             19018
&DISP    SETA  &DISP-1
.NOSMF   ANOP                                                     19018
.RETURN2 ANOP
*
********* HIGH PARTITION TCB (P/P IF NO OTHER SPECIFIED) **************
*
&N       SETA  1                                                *A*MFT2
.RETURN6 ANOP                                                      MFT2
&FREEQ   SETC  '0'                                                 MFT2
&LOWER   SETC  '&SIZE(1)'                                          MFT2
         AIF   ('&HIARCHY' NE 'Y').NOLC3                           LC0A
&LOWER2  SETC  '&SIZE2(1)'                                         LC0A
.NOLC3   ANOP                                                      LC0A
&UPPER   SETC  '&MACH'                                             MFT2
&ADD     SETA  &ADD+1                                              MFT2
&TYPFLG  SETA  192                                                 MFT2
         AIF   ('&GRUPTYP(&N)'(5,1) EQ 'W').RETURN3                MFT2
         AIF   ('&GRUPTYP(&N)'(5,1) EQ 'R').RETURN4                MFT2
.RETRN2B AIF   ('&PROTECT' EQ 'N').NOKEY                           MFT2
&PROKEY  SETA  &PROKEY+16                                          1372
&PROTKEY SETA  &PROKEY                                             MFT2
.NOKEY   ANOP                                                      MGT2
         AIF   (&ADD GT 3).RETURN5                                 MGT2
         AGO   .ASBLTCB
.RETURN5 ANOP                                                      MGT2
*
******** LOWER PROBLEM PROGRAM PARTITION TCB ********************* MFT2
*
&LOWER   SETC  '&SIZE(&CT)'                                        MFT2
         AIF   ('&HIARCHY' NE 'Y').ASBLTCB                         LC0A
&LOWER2  SETC  '&SIZE2(&CT)'                                       LC0A
         AGO   .ASBLTCB
.RETURN3 ANOP
*                                                                  MFT2
******** WRITER TCB FOR OPTION 2 SYSTEMS ******************************
*                                                                  MFT2
&PROTKEY SETA  0                                                   MFT2
&TYPFLG  SETA  128                                                 MFT2
         AIF   (&ADD EQ 3).OTHER                                   MGT2
&LOWER   SETC  '&SIZE(&CT)'                                        MFT2
         AIF   ('&HIARCHY' NE 'Y').OTHER                           LC0A
&LOWER2  SETC  '&SIZE2(&CT)'                                       LC0A
.OTHER   ANOP                                                      MFT2
         AGO   .ASBLTCB                                            MFT2
.RETURN4 ANOP
*                                                                  MFT2
* RESIDENT READER TCB FOR OPTION 2 SYSTEMS **********************  MFT2
*                                                                  MFT2
&PROTKEY SETA  0                                                   MFT2
&TYPFLG  SETA  64                                                  MFT2
&LOWER   SETC  '&SIZE(&CT)'                                        MFT2
         AIF   ('&HIARCHY' NE 'Y').ASBLTCB                         LC0A
&LOWER2  SETC  '&SIZE2(&CT)'                                       LC0A
.ASBLTCB ANOP
         AIF   (&ADD GE 3).NOEXTRN                                 MGT2
         EXTRN &RBNAME
         EXTRN &FREEQ
         EXTRN &UPPER
         AIF   (&ADD LE 1).NOEXTRN                                 MFT2
         EXTRN &LOWER
.NOEXTRN ANOP
         AIF   ('&FLOATPT' EQ 'Y' AND &ADD GE 3).FPRSAVE           MFT2
         AGO   .TCBSTRT
.FPRSAVE ANOP
         DC    4D'0'
.TCBSTRT ANOP
&NO      SETA  &NO+1
         AIF   (&ADD GE 3).ZERO                                    MFT2
TCB&NO   DC    A(&RBNAME)
         AGO   .CONTIN0
.ZERO    ANOP
TCB&NO   DC    A(0)
.CONTIN0 ANOP
         DC    5F'0'
         DC    A(BNDBX&NO)
.CONTIN1 ANOP
         DC    AL1(&PROKEY)   INSERT STORAGE KEY                   1372
.CONT1B  ANOP
         DC    XL1'00'
         AIF   (&ADD LT 3).CONTINB                                 MFT2
         AIF   (T'&TSVAL EQ 'O').TMSL1                             MTS0
         AIF   (&TCB GT &TSLST OR &TCB LT &TSIPT).TMSL1            MTS0
         AIF   ('&FIRSTPT' NE '').AGOGO                            MTS0
&FIRSTPT SETC  'TCB'.'&NO'                                         MTS0
&HIPR    SETA  &DISP                                               I250
.AGOGO   ANOP                                                      MTS0
&LASTPT  SETC  'TCB'.'&NO'                                         MTS0
         AIF   (&DISP LT 15).ZEROLW                                I250
&LOPR    SETA  &DISP-10                                            I250
         AGO   .LWPR                                               I250
.ZEROLW  ANOP                                                      I250
&LOPR    SETA  0                                                   I250
.LWPR    ANOP                                                      I250
         DC    XL1'01'            TIME SLICED TCB                  MTS0
         AGO   .TMSL2                                              MTS0
.TMSL1   ANOP                                                      MTS0
         DC    XL1'00'
.TMSL2   ANOP                                                      MTS0
&TCB     SETA  &TCB+1                                              MTS0
         AIF   ('&FLOATPT' EQ 'N').CONTINC
         DC    XL1'08'
         AGO   .CONTIND
.CONTINB ANOP
         DC    XL2'8000'
         AGO   .CONTIND
.CONTINC ANOP
         DC    XL1'00'
.CONTIND ANOP
         DC    XL2'00FF'
         DC    XL1'00'
         AIF   (&DISP LT 0).ZERODS1                                I250
         DC    AL1(&DISP)                                          I250
         AGO   .DONEDS                                             I250
.ZERODS1 ANOP                                                      I250
         DC    AL1(0)                                              I250
.DONEDS  ANOP                                                      I250
         DC    2F'0'                                               I250
         DC    A(TCB&NO)                                           I250
         DC    16F'0'                                              I250
         DC    AL1(&TCBID)                                         I250
         DC    AL3(0)
         AIF   (&ADD EQ &NUM+2 AND '&SZ' EQ 'LT27').ZEROTCB        MFT2
         AIF   (&ADD EQ &NUM+2 AND &SZSW EQ 1).ZEROTCB             MFT2
&NO      SETA  &NO+1
         DC    A(TCB&NO)
&NO      SETA  &NO-1
         AGO   .CONTIN3
.ZEROTCB ANOP
         DC    AL4(0)
.CONTIN3 AIF   ('&TIMER' EQ '&TTIME').CONTINX                      MFT2
         DC    A(0)
         AGO   .CONTIN4
.CONTINX ANOP
         DC    A(TIMEL&NO)
.CONTIN4 AIF   (&PTR NE 10).CONT4A                                 MFT2
&ZERO    SETC  ''                                                  MFT2
.CONT4A  ANOP                                                      MFT2
         DC    AL1(&TYPFLG)                                        MFT2
         AIF   (&ADD GE 3).CONT4C                                 MFT2
         DC    AL3(0)                                              MFT2
         AGO   .CONTAT                                             I250
.CONT4C  ANOP                                                      MFT2
         DC    AL3(IEFIEA&ZERO&PTR)                                MFT2
         EXTRN IEFIEA&ZERO&PTR                                     MFT2
&PTR     SETA  &PTR+1                                              MFT2
.CONTAT  ANOP                                                      I250
         DC    6F'0'                                               I250
         AIF   (&DISP LT 0).ZEROLP2                                I250
         DC    AL1(&DISP)                                          I250
         AIF   (&ADD GE 3).PRBPRTN                                 I250
&DISP    SETA  &DISP-1       SYS TCBS ONLY HAVE DIFF OF 1          I250
         AGO   .DONELP                                             I250
.PRBPRTN ANOP                                                      I250
&DISP    SETA  &DISP-11      PRB TCBS HAVE DIFF OF 11              I250
         AGO   .DONELP                                             I250
.ZEROLP2 ANOP                                                      I250
         DC    AL1(0)                                              I250
.DONELP  ANOP                                                      I250
         DC    AL3(0)                                              I250
         DC    5F'0'                                               I250
         DC    2F'0'                                               I254
         DC    2F'0'                                          BD A58667
*  USED FOR QUEUEING SVRBS CONTENDING FOR XSNT AREA                I275
         DC    2F'0' .  MUST BE -16 FROM BNDBX                     I275
.CONT4C1 ANOP                                                      LC0A
.******* ABEND APPENDAGE ******************************************LC0A
         DC    2F'0'        ABEND APPENDAGE                        LC0A
.CONT4D  ANOP                                                      MFT2
.******* BOUNDARY BOXES ***********************************************
BNDBX&NO DC    AL1(&DBBXBIT)                                       LC0A
         DC    AL3(&FREEQ)                                         LC0A
         DC    A(&LOWER)
         DC    A(&UPPER)
         DC    A(0)                                                LC0A
         DC    A(&LOWER2)                                          LC0A
         DC    A(0)                                                LC0A
         AIF   ('&STORAGE' EQ '&PRIMARY').CONTINE
         AGO   .CONTINF
.CONTINE ANOP
IEAMSBBX EQU   BNDBX&NO
.CONTINF ANOP
         AIF   ('&TIMER' NE '&TTIME').CONTIN5
.XXXX    ANOP
TIMEL&NO DS    0F
         DC    XL1'83'
         AIF   ('&STORAGE' NE '&PRIMARY').CONTING
         DC    AL3(IEAHEAD)
         AGO   .CONTINH
.CONTING ANOP
         DC    AL3(TCB&NO)
.CONTINH ANOP
         DC    27F'0'
         AIF   (&ADD EQ 1).CONTIN5                             BD M0630
         AIF   ('&JST' NE 'JST').NOJST                            19018
         AIF   (&ADD EQ 0).NOJST                                  19018
&T       SETA  &T+1                                               19018
JSTQE&T  DS    0F                                                 19018
         DC    XL1'83'                                            19018
         DC    AL3(TCB&NO)                                        19018
         DC    27F'0'                                             19018
         ENTRY JSTQE&T                                            19018
         AIF   ('&SMF' NE 'SMF').NOJST                            19018
         DC    F'0'                                               19018
         DC    A(JSTQE&T)                                         19018
         DC    A(TLEXTIRB)                                        19018
         DC    A(TCB&NO)                                          19018
         DC    2F'0'                                              19028
         DC    A(TLEXTIRB)                                        19028
         DC    A(TCB&NO)                                          19028
.NOJST   ANOP                                                     19018
         AIF   ('&STORAGE' NE '&PRIMARY').CONTIN5
IEATIBBX EQU   TIMEL&NO
.CONTIN5 ANOP
         AIF   ('&STORAGE' EQ '&PRIMARY').FINI1                 YA65737
&TCBID   SETA  &TCBID+1                                            I250
         AIF   (&ADD EQ 0).RETURN0                                 MFT2
         AIF   (&ADD EQ 1).RETURN1
&N       SETA  &N+1                                                MFT2
&CT      SETA  &CT+1                                               MFT2
         AIF   (&ADD GE &NUM+2).CONT5A                             MFT2
         AIF   (&ADD GE 3).RETURN6                                 MFT2
.CONT5A  ANOP
         AIF   ('&SZ' EQ 'LT27').CONT5AA                           MFT2
         AIF   (&SZSW).CONT5AA                                     MFT2
&SZSW    SETB  1                                                   MFT2
&SWIT    SETB  0                                                   MFT2
.LOOP    ANOP                                                      MFT2
&N1      SETA  &N1+1                                               MFT2
&N2      SETA  &N2+1                                               MFT2
&SIZE(&N1)     SETA &SZ(&N2)                                       MFT2
         AIF   ('&HIARCHY' NE 'Y').NOLC8                           LC0A
&SIZE2(&N1)    SETA &SZ2(&N2)                                      LC0A
.NOLC8   ANOP                                                      LC0A
&GRUPTYP(&N1)  SETC '&GRPTYP(&N2)'                                 MFT2
         AIF   (&N2 GE N'&SZ).CONT5AB                              MFT2
         AGO   .LOOP                                               MFT2
.CONT5AB ANOP                                                      MFT2
&NUM     SETA  &NUM+N'&SZ                                          MFT2
         AGO   .RETURN6                                            MFT2
.CONT5AA ANOP                                                      MFT2
IEAHEAD  EQU   TCB1
.CONT5AD ANOP                                                      MCS
         AIF   (T'&TSVAL EQ 'O').FINI1                             MCS
         ENTRY IEATSCE                                             MTS0
IEATSCE  DS    0D                                                  MTS0
         AIF   ('&ATTACH' EQ 'ATTACH').ATTSCE                      I250
         DC    XL1'00'       DISP.PRI.INITIALIZED TO NON-ZERO      MTS0
         DC    AL3(&FIRSTPT)     FIRST PARTITION                   MTS0
         DC    A(&LASTPT)     LAST PARTITION                       MTS0
         AGO   .TSCOM                                              I250
.ATTSCE  ANOP                                                      I250
         DC    AL1(&HIPR)                                          I250
         DC    AL3(&FIRSTPT)                                       I250
         DC    AL1(&LOPR)                                          I250
         DC    AL3(&LASTPT)                                        I250
.TSCOM   ANOP                                                      I250
         DC    A(&FIRSTPT)      NEXT FIELD                         MTS0
         DC    F'&TSVAL'     TIME-SLICE VALUE                      MTS0
         ORG   *-4                                                 MTS0
         DC    X'80'         END OF TSCE INDICATOR                 MTS0
         ORG   *+4                                                 MTS0
         ENTRY IEATSTQE                                            MTS0
IEATSTQE DS    0D                                                  MTS0
TSTQE    EQU   *                                                   MTS0
         DC    X'80'                                               MTS0
         DC    X'00000000'                                         MTS0
TQEFLNK  DC    X'000000'                                           MTS0
         DC    X'00'                                               MTS0
TQEBLNK  DC    X'000000'                                           MTS0
TQEVAL   DC    F'0'                                                MTS0
.FINI1   ANOP                                                      MTS0
         MEXIT
.RMSTCB  ANOP                                                     19022
*
*  I/O RMS  TCB *******************************************************
*
         ENTRY IGFRMTCB                                           19022
         EXTRN DDRDEB                                             19022
         EXTRN IGF201                                             19022
         EXTRN IGFDDR01                                           19022
         EXTRN IORMSWT                                            19022
         EXTRN IGFDDRSR                                           19022
         EXTRN IGFDDRFC                                           19022
         EXTRN IGFDDR11                                           19022
         EXTRN IGFDDRND                                           19022
IGFRMTCB DS    0F                                                 19022
TCB&NO   DC    A(IGFDDR11)                                        19022
         DC    F'0'                                               19022
IODEB    DC    A(DDRDEB)                                          19022
         DC    3F'0'                                              19022
         DC    A(BNDBXRMS)                                        19022
         DC    XL1'00'   PROTECT KEY                              19022
         DC    XL1'00'                                            19022
         DC    XL2'8000'                                          19022
         DC    XL2'0000'                                          19022
         DC    XL1'00'                                            19022
         DC    AL1(&DISP)                                         19022
         DC    2F'0'                                               I250
         DC    A(TCB&NO)                                           I250
         DC    F'0'                                                I250
IOR11    DC    A(IGF201)                                          19022
IOR12    DC    A(IGFDDR01)                                        19022
         DC    F'0'                                               19022
IOR14    DC    A(IORMSWT)                                         19022
IOR15    DC    A(IGFDDRSR)                                        19022
         DC    3F'0'                                              19022
IOR3     DC    A(16)                                              19022
IOR4     DC    A(IGFRMTCB)                                        19022
IOR5     DC    A(IGFDDR11)                                        19022
         DC    4F'0'                                              19022
         DC    AL1(&TCBID)
&TCBID   SETA  &TCBID+1
         DC    AL3(0)                                             19022
&NO      SETA  &NO+1                                              19022
         DC    A(TCB&NO)                                          19022
         DC    8F'0'                                               I275
         DC    AL1(&DISP)                                          I275
         DC    AL3(0)                                              I275
         DC    5F'0'                                               I275
         DC    2F'0'                                               I254
         DC    2F'0'                                          BD A58667
*****   TRANSIENT AREA REQUEST QUEUE ********************
         DC    F'0'   PTR TO NEXT REQUEST ON QUEUE                 I275
         DC    F'0'   PTR TO SVRB IN WAIT                          I275
         DC    2F'0'                                              19022
BNDBXRMS DC    AL1(&DBBXBIT)
         DC    AL3(IGFDDRFC)                                      19022
         DC    A(IGFDDR11)                                        19022
         DC    A(IGFDDRND)                                        19022
         DC    A(0)                                               19022
         DC    A(0)                                               19022
         DC    A(0)                                               19022
&DISP    SETA  &DISP-1                                            19022
         AGO   .G04                                               19022
.BLDLOG  ANOP                                                     19372
********                                                          19372
************************* LOG TCB                                 19372
********                                                          19372
         ENTRY IEALTCB
         EXTRN IEELWAIT                                           19372
         EXTRN IEELOGFC                                           19372
         EXTRN IEELOGUP                                           19372
TCB&NO   DC    A(IEELWAIT)  PRB FOR LOG                           19372
IEALTCB  EQU   TCB&NO                                             19372
         DC    5F'0'                                              19372
         DC    A(BNDBXL)                                          19372
         DC    XL1'00'  PROTECT KEY                               19372
         DC    XL1'00'                                            19372
         DC    XL2'8000'                                          19372
         DC    XL2'0000'                                          19372
         DC    XL1'00'                                            19372
         DC    AL1(&DISP)                                         19372
         DC    2F'0'                                               I250
         DC    A(TCB&NO)                                           I250
         DC    16F'0'                                              I250
         DC    AL1(&TCBID)
&TCBID   SETA  &TCBID+1
         DC    AL3(0)                                             19372
&NO      SETA  &NO+1                                              19372
         DC    A(TCB&NO)                                          19372
         DC    8F'0'                                               I275
         DC    AL1(&DISP)                                          I275
         DC    AL3(0)                                              I275
         DC    5F'0'                                               I275
         DC    2F'0'                                               I254
         DC    2F'0'                                          BD A58667
*****   TRANSIENT AREA REQUEST QUEUE ********************          I275
         DC    F'0'   PTR TO NEXT REQUEST ON QUEUE                 I275
         DC    F'0'   PTR TO SVRB IN WAIT                          I275
         DC    2F'0'                                              19372
BNDBXL   DC    AL1(&DBBXBIT)
         DC    AL3(IEELOGFC)                                      19372
         DC    A(IEELWAIT)                                        19372
         DC    A(IEELOGUP)                                        19372
         DC    A(0)                                               19372
         DC    A(0)                                               19372
         DC    A(0)                                               19372
&DISP    SETA  &DISP-1
         AGO   .GO3A                                              19372
.TACPTCB ANOP                                                      I275
*   *********TRANSIENT AREA LOADING TCB ************************   I275
*                                                                  I275
         ENTRY IEAXSNT                                             I275
         EXTRN IEAXSVRB                                            I275
         EXTRN IEARBFQ                                             I275
         EXTRN IEARBEND                                            I275
TCB&NO   DC    A(IEAXSVRB)                                         I275
         DC    5F'0'                                               I275
         DC    A(BNDBX&NO)                                         I275
         DC    XL1'00'   PROTECT KEY                               I275
         DC    XL1'00'                                             I275
         DC    XL2'8000'                                           I275
         DC    XL2'0000'      TCB IS DISPATCHABLE                  I275
         DC    XL1'00'                                             I275
         DC    AL1(&DISP)                                          I275
         DC    2F'0'                                               I250
         DC    A(TCB&NO)                                           I250
         DC    16F'0'                                              I250
         DC    AL1(&TCBID)
&TCBID   SETA  &TCBID+1
         DC    AL3(0)                                              I275
&NO      SETA  &NO+1                                               I275
         DC    A(TCB&NO)                                           I275
&NO      SETA  &NO-1                                               I275
         DC    8F'0'                                               I275
         DC    AL1(&DISP)                                          I275
         DC    AL3(0)                                              I275
         DC    5F'0'                                               I275
         DC    2F'0'                                               I254
         DC    2F'0'                                          BD A58667
         DC    2F'0'  XSNT AREA REQ QUEUE                          I275
         DC    2F'0'  ABEND APPENDAGES                             I275
BNDBX&NO DC    AL1(&DBBXBIT)                                       I275
         DC    AL3(IEARBFQ)                                        I275
         DC    A(IEAXSVRB)                                         I275
         DC    A(IEARBEND)                                         I275
         DC    A(0)                                                I275
         DC    A(&LOWER2)                                          I275
         DC    A(0)                                                I275
IEAXSNT  EQU   TCB&NO                                              I275
&NO      SETA  &NO+1                                               I275
&DISP    SETA  &DISP-1                                             I275
         AGO   .TACPNOP                                            I275
         MEND
