         MACRO
         IEAQFX &MOD,&SER,&M65MP,&CHANLOG,&GENCODE=NO             19E03
         LCLB  &M6                                                 MP1A
&M6      SETB  ('&M65MP' EQ '1' OR '&M65MP' EQ 'M65MP')            MP1A
*...FIXED, LOW CORE LOCATIONS.
         SPACE 2
.*3122000400                                                       MP1A
.*3122011640,011720-011800                                         ISER
.*007600                                                         A50059
         SPACE
         AIF   ('&GENCODE' EQ 'NO').NOGEN
IEAQFX00 CSECT
         ENTRY SVCOPSW                                             0539
         ENTRY PINPSW                                              0539
         ENTRY PIOPSW                                              0539
         ENTRY IEAPSW                                              0539
         ENTRY IECIERLC                                            0539
         ENTRY IEASCSAV                                            0539
         ENTRY IEAPKSAV                                            0539
         ENTRY IEASAV                                              0539
         ENTRY IHLCMT                                             21014
         AGO   .GOSTART                                            0539
.NOGEN   ANOP                                                      0539
IEAQFX00 DSECT                                                     0539
.GOSTART ANOP                                                      0539
         USING IPLPSW,0
         SPACE
.NOSTART ANOP
IPLPSW   DS    0F            .USE FOR BASE ADDRESSABILITY.
         DS    CL24
         SPACE 3
*...INTERRUPT OLD PSW'S.
         SPACE
EXOPSW   DS    1D            .TIMER/EXTERNAL OLD PSW.
SVCOPSW  DS    1D            .SVC OLD PSW.
PIOPSW   DS    1D            .PROGRAM CHECK OLD PSW.
MCOPSW   DC    X'0000FF0000000000'     .MACHINE CHECK OLD PSW.     1759
IOOPSW   DS    1D            .I/O OLD PSW.
         SPACE
         DS    CL12
         SPACE
         DC    V(IEACVT)     .ADDRESS OF CVT.
TIMER    DC    X'62E08000' SIX HR VALUE IN 13.02083 USEC UNITS.  A50059
         SPACE
         DC    V(TRPTR)      .ADDRESS OF TRACE ROUTINE.
         SPACE 3
*...INTERRUPT NEW PSW'S.
         SPACE
EXNPSW   DC    XL4'00040000' .TIMER/EXTERNAL NEW PSW. DISABLED EXCEPT
         DC    V(IEAQEX00)    FOR MACHINE CHECK.
         SPACE
SVCNPSW  DC    XL4'00040000' .SVC NEW PSW. DISABLED EXCEPT FOR MACHINE
         DC    V(IEAQSC00)    CHECK.
         SPACE
PINPSW   DC    XL4'00040000' .PROGRAM CHECK NEW PSW. DISABLED EXCEPT
         AIF   ('&GENCODE' EQ 'NO').ITLDSF                         ITEL
         DC    A(ITLPCSIM)  FOR MACHINE CHECK                      ITEL
         AGO   .NITLDSF                                            ITEL
.ITLDSF  DS    F            FOR MACHINE CHECK                      ITEL
.NITLDSF ANOP                                                      ITEL
         SPACE 2
         AIF   ('&SER' EQ 'SERP' OR '&MOD' NE '65').SER1          M2944
         AGO   .SETSER                                            M2944
.SER1    ANOP
MCNPSW   DC    XL4'00020000' .MACHINE CHECK NEW PSW. COMPLETLY DISABLED
         DC    XL4'000000E2'  WAIT PSW
         AGO   .AA1
.SETSER  ANOP
MCNPSW   DC    A(0)
         DC    V(IGFN0000)
.AA1     ANOP
         SPACE
IONPSW   DC    XL4'00040000' .I/O NEW PSW. DISABLED EXCEPT FOR MACHINE
         DC    V(IEAQIO00)    CHECK.
         SPACE 3
*      THE FOLLOWING MACRO DETERMINES THE LEVEL OF SER             ISER
         IFBSRLOG  &MOD,&CHANLOG                                  19E03
*                                                                  ISER
         AIF   (NOT &M6).AA2                                       MP1A
         DS    CL128         .GPR LOGOUT FOR M65 MULTIPROCESSOR.   MP1A
.AA2     ANOP                                                      MP1A
         SPACE
* LOW CORE AREA FOR LOADING PSW'S.
IEAPSW   DS    1D
         AIF   (&M6).ASYNBYP                                       MP1A
.ASYNMOV ANOP                                                      MP1A
*DEFINITION OF AEQS USED BY THE ASYNCHRONOUS EXIT EFFECTOR. THIS
*QUEUE IS REFERENCED BY THE EXIT EFFECTOR, SVC PURGE, AND BY IOS
*AND MUST BE DEFINED AS ORIGINATING BELOW AN 8K BOUNDARY.
         DS    1H                      RESERVED FOR GENERATION
IECIERLC DS    0H
AEQA     DC    AL2(0)      ADDR OF NEXT RQE TO BE DEQUED           0539
AEQB     DC    AL2(0)      ADDR OF LAST RQE QUEUED                 0539
AEQC     DC    AL2(0)      INITIAL VALUE OF AEQA                   0539
         SPACE
*        THE FOLLOWING ADCONS INITIALIZE THE QUEUE SO THAT
*THE THREE ELEMENTS OF THE QUEUE POINT TO THE FIRST ELEMENT
         ORG   AEQC-1                                              0539
         DC    AL3(AEQA)                                           0539
         ORG   AEQB-1                                              0539
         DC    AL3(AEQA)                                           0539
         ORG   AEQA-1                                              0539
         DC    AL3(AEQA)                                           0539
         ORG   AEQC+2         RESET COUNTER                        0539
         SPACE 2                                                   0539
*    THREE WORDS FOR QUEUE ORIGIN OF IQE-AEQ.  ABEND EXPECTS       0539
*   THIS QUEUE TO BE ADJACENT TO THE RQE-AEQ.                      0539
         DS    0F                                                  0539
AEQJ     DC    X'FF'     FLAG INDICATES EMPTY QUEUE                0539
         DC    AL3(AEQJ)     ADDR OF NEXT IQE TO BE DQD            0539
AEQK     DC    X'FF'                                               0539
         DC    AL3(AEQJ)     ADDR OF LAST IQE QUEUED               0539
         AIF   (&M6).ENDFX                                         MP1A
.ASYNBYP ANOP                                                      MP1A
* REG SAVE AREA FOR SVC FLIH.
IEASCSAV DS    16F
* REG SAVE AREA FOR PROGRAM CHECK INTERRUPT HANDLER.
IEAPKSAV DS    16F
         SPACE
* LOW CORE WORK AREA.
IEASAV   DS    3F
         AIF   (NOT &M6).ADDCMT                                   21014
         MPFX  &SER,&GENCODE .INSERT MULTIPROCESSING PREFIX AREA.  MP1A
.ADDCMT  ANOP                                                     21014
IHLCMT   CMT   DSECT=NO                                           21014
         AIF   (NOT &M6).ENDFX                                    21014
         ORG   IEAQFX00+4096 .PUT ASYNCH QUEUES OUTSIDE M65MP PSA. MP1A
         AGO   .ASYNMOV                                            MP1A
.ENDFX   ANOP                                                      MP1A
IEAEND   DS    0F                                                  0539
         SPACE
         ORG   IEAQFX00+X'16C'
*...LOCATION X'16C' IS BRANCHED TO FROM THE IPL PROGRAM.
         LPSW  *+4
         AIF   (NOT &M6).COREOK                                    MP1A
         DC    F'0'          .THIS PSW IS LOADED TO ENTER MPNIP.   MP1A
         AGO   .NIPAD                                              MP1A
.COREOK  ANOP                                                      MP1A
         DC    X'00040000'   .THIS PSW IS LOADED TO ENTER GOMAN
.NIPAD   ANOP                                                      MP1A
         DC    V(IEANIP4)
         ORG   IEAEND                                              0539
         MEND
