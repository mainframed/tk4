         MACRO
&NAME    HOOK  &EID=,&CLASS=,&ID=,&TYPE=
         GBLB  &IHLMHK
.*A000000-999999                                                 S21014
.* TEST TO SEE IF THE MAPPING IS ALREADY EXPANDED
         AIF   (&IHLMHK).XPNDED
         IHLMHOOK
.XPNDED  AIF   ('&TYPE' NE 'S').NOTBRHK
*
* SPECIAL BRANCH HOOK GENERATED BY THE HOOK MACRO
*
&NAME    L     15,IEACON2          GET RESIDENT ROUTINE BRANCH E.P.
         BALR  1,15                BRANCH HOOK
         AIF   ('&EID' EQ '').BR1
         DC    XL1'47',AL3(&EID)   NOP TO CARRY EID
         MEXIT
.BR1     DC    XL1'47',AL1(&CLASS),AL2(&ID)  NOP TO CARRY CLASS & ID
         MEXIT
.NOTBRHK AIF   ('&TYPE' EQ '').TYPEOK
         MNOTE 8,'IHL053  INVALID TYPE= KEYWORD.'
         MEXIT
.TYPEOK  ANOP
*
* MC INSTRUCTION GENERATED BY THE HOOK MACRO
* COMPATIBLE FOR MACHINES WITHOUT THE MC HARDWARE
*
         AIF   ('&CLASS' NE '' AND '&ID' NE '').CIHK
         AIF   ('&EID' NE '').EIDHK
         MNOTE 8,'IHL052  INSUFFICIENT KEYWORD PARAMETERS.'
         MEXIT
.CIHK    ANOP
&NAME    DC    XL1'AF',AL1(&CLASS),AL2(&ID)  MC INSTRUCTION
         DC    XL1'47',AL1(&CLASS),AL2(&ID)  NOP TO CARRY CLASS & ID
         MEXIT
.EIDHK   ANOP
&NAME    DC    XL1'AF',AL3(&EID)   MC INSTRUCTION
         DC    XL1'47',AL3(&EID)   NOP TO CARRY EID
         MEND
