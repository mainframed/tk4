         MACRO
         IECXTCH &LCHNO,&TYPE,&PCHA,&CUA,&M65MP,&TBASE,&APR,      19026X
               &LAST,&OLTEP,&SRDDR                                19025
.* TO REMOVE THE SIMULATE CODE FROM IOS, REMOVE THE STATEMENT    BBBBBB
.* FOLLOWING THESE MACRO COMMENTS AND INSERT IN ITS PLACE THE    BBBBBB
.* FOLLOWING STATEMENT -                                         BBBBBB
.*       GBLB  &IECI2SW,&IECIACU,&IECIOLT  GLOBALS               BBBBBB
         GBLB  &IECI2SW,&IECIACU,&IECITST,&IECIOLT  GLOBALS       19026
         GBLA  &IECIOPT(16)             FOR TEST CHANNEL           I267
         LCLB  &CUSW,&SECSW             BINARY SWITCHES            IMP5
         LCLA  &RCTR,&INDX,&PATHN(4),&PATHMSK,&CUNO,&X,&PATHS,&MOVE
         LCLA  &NUMCH,&NUMCH2,&NUM,&HICHAN(4),&SUMPTH,&LHCTR      M1186
         LCLA  &MOVE2,&MOVE3                                      M1186
         LCLC  &LABEL,&PCHNO,&A,&SUM,&NOPTH,&CHAN,&VARY,&CU        MCS1
.*                                                               BBBBBB
.*  DEFINITION OF INPUT PARAMETERS                                 IMP5
.*  LCHNO    LOGICAL CHANNEL NUMBER OR SUBLIST OF LCH/CHAN         IMP5
.*  TYPE     0=MULTIPLEXOR,1=SELECTOR                              IMP5
.*  PCHA     PHYSICAL CHANNEL ADDRESS                              IMP5
.*  CUA      CONTROL UNIT ADDRESS                                  IMP5
.*  M65MP    MULTIPROCESSING                                       IMP5
.*  TBASE    TWO BASE REGISTERS ARE NEEDED IN IOS                  IMP5
.*  APR      ALTERNATE PATH RETRY OPTION                           IMP5
.*  LAST     THIS IS THE LAST LOGICAL CHANNEL IN THE SYSTEM        IMP5
.*  OLTEP    ON-LINE TEST FOR THIS CHANNEL                        19026
.*  SRDDR    SYSRES DDR OPTION, DA ON THIS LCH                    19022
.*                                                                19022
         AIF   (N'&LCHNO EQ 1).MPROC    REAL MACRO CALL, SKIP     19022
.SAVE    ANOP
&NUM     SETA  &NUM+1
&IECIOPT(&NUM) SETA  &LCHNO(&NUM)
         AIF   (&NUM NE N'&LCHNO).SAVE
         SPACE 1                                                   ROTN
         MEXIT                                                     ROTN
.MPROC   AIF   (N'&PCHA LT 2).TCHLP4A LOW SPEED MPX, SKIP          IMP5
.OPTCH   ANOP
&NUM     SETA  &NUM+1
         AIF   (&PCHA(&NUM) EQ 0).LOOP
&PATHS   SETA  &PATHS+1
&HICHAN(1) SETA  &NUM                                             19022
&PATHN(&PATHS) SETA &NUM                SAVE CHAN NO. IN TABLE     ROTN
.LOOP    AIF   (N'&PCHA NE &NUM).OPTCH
         AIF   (&PATHS LT 2).CULBL
&HICHAN(1) SETA  &PATHN(1)                                        19022
&HICHAN(2) SETA  &PATHN(2)                                        19022
&HICHAN(3) SETA  &PATHN(3)                                        M1186
&HICHAN(4) SETA  &PATHN(4)                                        M1186
&CU      SETC  '0'
&RCTR    SETA  &PATHS                   SET NUMBER TO ROTATE =PTHS ROTN
         AGO   .SETLBL
.CULBL   ANOP
&X SETA &CUA(1)+&CUA(2)+&CUA(3)+&CUA(4)
         AIF   (&X LT 2).TCHLP4A
.* THERE ARE ALTERNATE C.U.'S IN THIS LOGICAL CHANNEL
&IECIACU SETB  1                        SET AL. C.U. GLOBAL        MCS1
&CUSW    SETB 1                         SET C.U. SWITCH            MCS1
&CHAN    SETC  '&HICHAN(1)'             PICK UP CHAN NUMBER       19022
.SETLBL  ANOP
&IECI2SW SETB  1                        SET OPT. CHAN. PATHS GLOBAL SSW
&LABEL   SETC  'XCPCH&LCHNO.B'
&LABEL   DS    0H                       ALTERNATE PATH TABLE
&NUM     SETA  0
.TCHLP1  ANOP
&NUM     SETA  &NUM+1
         AIF   (&PATHS LT 2).TCHLP2     NO OPT CHANS, SKIP         ROTN
         AIF   (&SECSW EQ 1).ROTN2                                 IMP5
         AIF   (&NUM EQ &PATHS OR &NUM LT &PATHS).ROTN6
&SECSW   SETB  1
&NUM     SETA  0
         AGO   .TCHLP1                                             IMP5
.ROTN6   ANOP                                                      IMP5
&INDX    SETA  &PATHN(&NUM)+1           PT AT POSITION IN SUBLIST  ROTN
         AIF   (&SRDDR EQ 1).ROTN6A                               19025
         AIF   (&IECIOPT(&INDX) GT 1).TCHLP1
&RCTR    SETA  &RCTR-1                  ONE LESS TO ROTATE         ROTN
.ROTN6A  ANOP                                                     19025
&PATHN(&NUM) SETA 0                     ZERO OUT ENTRY             ROTN
         AGO   .ROTN7                   GO SET UP TABLE ENTRY    BBBBBB
.**********************************************************************
.*
.*
.* THE FOLLOWING INSTRUCTIONS DETERMINE WHETHER PATHS SHOULD BE ROTATED
.* AND HOW THEY SHOULD BE ROTATED.  &RCTR IS A COUNTER OF THE NUMBER OF
.* PATHS TO BE ROTATED.  THE FOLLOWING TABLE DESCRIBES HOW &RCTR IS
.* CHANGED TO PROVIDE PROPER PATH ROTATION:
.*
.**********************************************************************
.* NO. PATHS  *  OLD &RCTR  *  NEW &RCTR  *  BACKUP START OF ROTATION *
.**********************************************************************
.*     2      *      0      *      2      *          NO               *
.*            *      1      *      1      *          NO               *
.*            *      2      *      2      *          NO               *
.**********************************************************************
.*     3      *      0      *      3      *          NO               *
.*            *      1      *      2      *          YES              *
.*            *      2      *      2      *          NO               *
.*            *      3      *      3      *          NO               *
.**********************************************************************
.*     4      *      0      *      4      *          NO               *
.*            *      1      *      3      *          YES              *
.*            *      2      *      2      *          NO               *
.*            *      3      *      3      *          NO               *
.*            *      4      *      4      *          NO               *
.**********************************************************************
.*
.* IF THIS ROUTINE TERMINATES WITH A VALUE OF 2 OR GREATER FOR &RCTR,
.* PATHS WILL BE ROTATED.  THE LAST PATHS ARE ROTATED UNLESS BACKUP OF
.* START OF ROTATION IS REQUESTED BY SETTING &MOVE TO A VALUE OF 1.
.*
.ROTN2   AIF   (&RCTR NE 0).ROTN2B      IF RCTR EQ ZERO, CHANGE SA70545
&RCTR    SETA  &PATHS                   RCTR TO NUMBER OF PATHS SA70545
         AGO   .ROTN3                                           SA70545
.ROTN2B  AIF   (&RCTR NE 1 OR &PATHS LT 3).ROTN3 IF 3 OR 4 PTHS SA70545
&RCTR    SETA  &PATHS-1          AND RCTR EQ 1, RCTR EQ PATHS-1 SA70545
&MOVE    SETA  1                        BACKUP START OF ROTAT'N SA70545
.ROTN3   AIF   (&RCTR LT 2).ROTN5       NONE LEFT TO ROTATE     SA70545
.*
.*
.**********************************************************************
&NUMCH2  SETA  &RCTR*2-2                NO. OF BYTES TO MOVE       ROTN
.ROTN5   AIF   (&NUM GT &PATHS).TCHLP2A                         SA70545
         AIF   (&PATHN(&NUM) EQ 0).ROTN4                           IMP5
&INDX    SETA  &PATHN(&NUM)+1           PT AT SUBLIST ENTRY        ROTN
.ROTN7   ANOP                           SET UP TABLE ENTRY       BBBBBB
&NUMCH   SETA  &NUMCH+1                 INCREASE PATH COUNT      BBBBBB
&VARY    SETC  '8421'(&NUM,1)           PICK UP PATH DESIGNATION BBBBBB
&CHAN    SETC  '0123456789ABCDEF'(&INDX,1)                         ROTN
         AGO   .TABLE                                              ROTN
.ROTN4   AIF   (&NUM NE &PATHS).TCHLP1
         AGO   .TCHLP2A                 COMPLETE TABLE             ROTN
.TCHLP2  AIF   (&NUM GT N'&CUA).TCHLP2A
         AIF   (&CUA(&NUM) EQ 0).TCHLP2A
&CU      SETC  'CDEF'(&NUM,1)
&NUMCH   SETA  &NUMCH+1
&VARY    SETC  '8421'(&NUMCH,1)
.TABLE   DC    X'0&CHAN.&CU.&VARY' CHAN ADR AND PATH DESIGNATION BBBBBB
&SUMPTH  SETA  &SUMPTH+&VARY
         AGO   .TCHLP1
.TCHLP2A AIF   (&NUM LT N'&CUA).TCHLP1
&NOPTH   SETC  '&NUMCH'
&SUM     SETC  '123456789ABCDEF'(&SUMPTH,1)
         DC    X'0&NOPTH.0&SUM'         NUMBER OF PATHS AND MASK SUM
.TCHLP4A ANOP
&LABEL   SETC  'XCPCH&LCHNO'           SET UP LABEL FOR ROUTINES   IMP5
&LABEL   DS    0H                       ENTER TEST CHANNEL CODE    IMP5
.*  IF ALTERNATE CHANNELS, GO TO ROTATE CODE                    SA70545
         AIF   (&CUSW EQ 0 AND &NUMCH GT 1).TCH2B               SA70545
.*  IF ALTERNATE CONTROL UNITS, PROVIDE FOR ROTATION            SA70545
         AIF   (&CUSW EQ 0 OR &NUMCH LT 2).MPX01                SA70545
&PATHS   SETA  &NUMCH                   PROVIDE FOR ROTATION    SA70545
&RCTR    SETA  &NUMCH                                           SA70545
&NUMCH2  SETA  &RCTR*2-2                NO. OF BYTES TO MOVE    SA70545
         AGO   .TCH2B                                           SA70545
.MPX01   ANOP                                                   SA70545
.*  THIS SECTION IS FOR LOGICAL CHANNELS WITH SINGLE PATHS         IMP5
         AIF   (&HICHAN(1) NE 0).MPX1
         AIF   (&M65MP EQ 0).MPX05      NO MPROC, SKIP            19022
         LA    UAREG,X'008'             SET UP FOR CHAN 0         19022
         AGO   .MPX2                                              19022
.MPX05   AIF   (N'&PCHA LT 2).MPX00  IF MPX BR                   SM5072
.MPX15   ANOP                                                    SM5072
&CHAN    SETC  '0'                      SET CHAN ZERO             M0162
         AGO   .MPX08                                             M0162
.MPX00   AIF   (&PCHA EQ 0).MPX15      IS THIS CHAN ZERO         SM5072
&CHAN    SETC  '123456789ABCDEF'(&PCHA,1)                       SA62871
         AGO   .MPX08                                            A40532
.MPX1    ANOP                                                      I267
&CHAN    SETC  '123456789ABCDEF'(&HICHAN(1),1)
         AIF   (&APR EQ 0).MPX08                                  19022
         AIF   (&M65MP EQ 0 AND &NUMCH LT 2).MPX08                19022
         LA    UAREG,X'&CHAN.08'        SET UP CHAN ADDRESS       19022
         AGO   .MPX2                                              19022
.MPX08   LA    UAREG,X'&CHAN.00'        SET UP CHANNEL ADDRESS    19022
.MPX2    AIF   (&M65MP EQ 0 OR &TYPE EQ 1).TCH2E                   IMP5
         LA    APBSRG,CHAN&HICHAN(1)    INITIALIZE CHAN TABLE PTR 19022
         AGO   .TCH2E                                              IMP5
.*  THIS SECTION IS FOR LOGICAL CHANNELS WITH MULTIPLE PATHS       IMP5
.TCH2B   AIF   (&RCTR LT 2).TCH1A
*   ROTATE  ENTRIES TO ATTEMPT TO START THE MOST LIKELY AVAILABLE PATH
&MOVE    SETA  (&PATHS-&RCTR-&MOVE)*2    NUMBER OF BYTES TO MOVE
.TCH2A   ANOP
&MOVE2   SETA  &MOVE+2
&MOVE3   SETA  &MOVE+&NUMCH2
         LH    WKREG1,&LABEL.B+&MOVE    SAVE FIRST ENTRY           IMP5
         MVC   &LABEL.B+&MOVE.(&NUMCH2),&LABEL.B+&MOVE2 MOVE IT UP IMP5
         STH   WKREG1,&LABEL.B+&MOVE3   PUT FIRST IN LAST SLOT     IMP5
.*  THE FOLLOWING ROUTINE DETERMINES IF THERE ARE FOUR PATHS AND
.*  ONLY THE LAST TWO ARE BEING ROTATED.  IF YES, THE FIRST TWO
.*  WILL BE ROTATED ALSO, BUT SEPARATELY FROM THE LAST TWO.     SA70545
         AIF   (&PATHS LT 4 OR &RCTR NE 2).TCH2D                SA70545
         AIF   (&MOVE EQ 0).TCH2D                               SA70545
*  ROTATE THE FIRST TWO PATHS SEPARATELY FROM THE LAST TWO PATHS
&MOVE    SETA  0                                                SA70545
         AGO   .TCH2A                                           SA70545
.TCH2D   ANOP                                                   SA70545
*                                                                  IMP5
&NUMCH2  SETA  0                                                   IMP5
.TCH1A   LH    UAREG,&LABEL.B+&NUMCH2   GET UNIT ADDRESS           IMP5
&LHCTR   SETA  &LHCTR+1                 INCREMENT LH COUNTER       IMP5
&PATHMSK  SETA  &NUMCH2+1                                          IMP5
         AIF   (&CUSW EQ 0).TCH2C       NO ALT. CU'S, SKIP        19022
         MVZ   UCBUA(1,UCBREG),&LABEL.B+&PATHMSK  PUT CU INTO UCB  IMP5
.TCH2C   AIF   (&M65MP EQ 0 OR &TYPE EQ 1).TCH2E                  19022
         LA    APBSRG,CHAN&HICHAN(1)    INITIALIZE CHAN TABLE PTR 19022
.** THIS RTN SETS UP THE BAL INSTRUCTION TO THE COMMON CODE  ******IMP5
.TCH2E   AIF   (&OLTEP EQ 0).TCH2F      NO OLTEP, SKIP            19026
&IECIOLT SETB  1                        SET OLTEP GLOBAL          19026
         BAL   LNKRG2,XCPTCH9           SEE IF OLTEP OWNS CHANNEL 19026
         AGO   .TCH3C                                             19026
.TCH2F   AIF   (&TYPE EQ 0).TCH3B       IF MPX, BRANCH            19026
         AIF   (&APR EQ 0).TCH3E        NO APR, BRANCH
         AIF   (&M65MP EQ 0 AND &NUMCH LT 2).TCH3E                19022
         BAL   LNKRG2,XCPTCH8           SEE IF PATH IS AVAIL       IMP5
         AGO   .TCH3C                                              IMP5
.TCH3E   BAL   LNKRG2,XCPTCH            GO TO COMMON CODE          IMP5
         AGO   .TCH3C
.TCH3B   AIF   (&APR EQ 0).TCH3G                                  19022
         AIF   (&M65MP EQ 0 AND &NUMCH LT 2).TCH3G                19022
         BAL   LNKRG2,XCPTCH7           SEE IF PATH IS AVAIL       IMP5
         AGO   .TCH3C
.TCH3G   BAL   REG0,XCPTCH3             GO TO COMMON CODE          IMP5
.TCH3C   ANOP                                                      IMP5
&NUMCH2  SETA  &NUMCH2+2                INCR. DOWN CHAN ADDR. LIST IMP5
         SPACE 1                                                   IMP5
.TCH3    AIF   (&LHCTR NE &NUMCH).TCH1A DIDN'T DO ENOUGH LH'S YET  IMP5
.TCH3A   AIF   (&M65MP EQ 0).TCH4                                  MCS1
&INDX    SETA  1                                                  M1186
&NUMCH2  SETA  0                                                  M1186
         AIF   (&NUMCH LT 2 OR &TYPE EQ 0).TCH3A2                 M1186
.TCH3A1  LH    UAREG,&LABEL.B+&NUMCH2    GET UNIT ADDRESS         M1186
.TCH3A2  ANOP                                                   SA56321
         BAL   LNKRG2,XCPTCH1            GO TEST FOR SHOULDER TAP M1186
         SPACE 1                                                  M1186
         AIF   (&INDX GE &NUMCH).TCH3A4                           M1186
&INDX    SETA  &INDX+1                                            M1186
&NUMCH2  SETA  &NUMCH2+2                                          M1186
         AIF   (&HICHAN(&INDX) NE 0).TCH3A1                       M1186
.TCH3A4  SPACE 1                                                  M1186
.TCH4    ANOP                                                      IMP5
* ALL POSSIBLE PATHS HAVE BEEN TRIED - RETURN                      IMP5
         AIF   (&M65MP NE 0).TCH17X      MP SYSTEM?              SM5267
         AIF   (&TYPE EQ 0).TCH4C      IF MPLXR, BRANCH            IMP5
         AIF   (&TBASE EQ 1).TCH4B                                 IMP5
         BC    15,SELNLCH(0,LNKRG1)     ENQUEUE OR GET NEXT LCH    IMP5
         AGO   .END                                                IMP5
.TCH4B   BC    15,SELNXTLV              ENQUE OR GET NEXT LCH      IMP5
         AGO   .END                                                IMP5
.TCH4C   AIF   (&TBASE EQ 1).TCH4D                                 IMP5
         BC    15,SELBSY(0,LNKRG1)      GET NEXT LCH OR ENQUE      IMP5
         AGO   .END                                                IMP5
.TCH4D   BC    15,SELBSYV               GET NEXT LCH OR ENQUE      IMP5
         AGO   .END                                             SA60510
.TCH17X  AIF   (&TYPE EQ 0).TCH4CX     IF MPLXR, BRANCH         SA60510
         AIF   (&TBASE EQ 1).TCH4BX                             SA60510
         LA    LNKRG2,SELNLCH(LNKRG1)   ENQUEUE OR GET NEXT LCH SA60510
         BC    15,XCPTCH17              GO CHECK FOR NO PATHS   SA60510
         AGO   .END                                             SA60510
.TCH4BX  LA    LNKRG2,SELNXTLV          ENQUE OR GET NEXT LCH   SA60510
         BC    15,XCPTCH17              GO CHECK FOR NO PATHS   SA60510
         AGO   .END                                             SA60510
.TCH4CX  AIF   (&TBASE EQ 1).TCH4DX                             SA60510
         LA    LNKRG2,SELBSY(LNKRG1)    GET NEXT LCH OR ENQUE   SA60510
         BC    15,XCPTCH17              GO CHECK FOR NO PATHS   SA60510
         AGO   .END                                             SA60510
.TCH4DX  LA    LNKRG2,SELBSYV           GET NEXT LCH OR ENQUE   SA60510
         BC    15,XCPTCH17              GO CHECK FOR NO PATHS   SA60510
.END     SPACE 2                                                   APR1
         AIF   (&LAST EQ 0).FINIS                                  IMP5
         SPACE 2                                                   IMP5
*                                                                  IMP5
*   THIS IS THE TEST CHANNEL COMMON ROUTINE.                       IMP5
*                                                                  IMP5
         AIF   (&IECIOLT EQ 0).OLT1                               19026
         AIF   (&APR EQ 0).OLT2                                   19026
&LABEL   SETC  'XCPTCH8'
         AGO   .OLT5                                              19026
.OLT2    ANOP                                                     19026
&LABEL   SETC  'XCPTCH'                                           19026
.OLT5    AIF   (&M65MP EQ 0).OLT9                               SA60510
XCPTCH19 OI    MPTCHSW,ONLNPTH          INDICATE ONLINE PATH    SA60510
         L     LNKRG2,TCHLNK2           RESTORE RETURN REG      SA60510
         BCR   15,LNKRG2                RETURN                  SA60510
TCHLNK2  DC    F'0'                     LNKRG2 SAVE AREA        SA60510
.OLT9    ANOP                                                   SA60510
XCPTCH9  DS    0H                       TCH-OLTEP ROUTINE         19026
         TM    IECOLTSW,OLTCHOPT        DOES OLTEP OWN A CHANNEL  19026
         BC    8,&LABEL                 NO, CONTINUE PROCESSING   19026
         AIF   (&M65MP EQ 0).OLT19      MP SYSTEM?              SA60510
         ST    LNKRG2,TCHLNK2           SAVE RETURN REG         SA60510
         LA    LNKRG2,XCPTCH19          SET NEW RETURN ADDR     SA60510
.OLT19   L     APBSRG,IECOLTSW          GET ADR OF OLTEP RTN    SA60510
         BAL   REG0,OLTCHN(0,APBSRG)    GO TO OLTEP RTN           19026
*                                                                 19026
*              DEFINITION OF TCH-OLTEP INTERFACE                  19026
*   INPUT      DEBREG, UAREG - CHAN ADR, REG0 - NORMAL RETURN     19026
*              LNKRG2 - TRY NEXT PATH RETURN, APBSRG--BASE ADR    19026
*   REGS CHANGED   WKREG1, WKREG2, WKREG3, LCHREG                 19026
*                                                                 19026
.OLT1    AIF   (&APR EQ 0).APR6                                   19026
XCPTCH8  DS    0H                                                 19022
         BAL   LNKRG3,XCPTCH2           SEE IF PATH IS AVAIL.     19022
.APR6    ANOP                                                     19022
XCPTCH   DS    0H                                                  IMP5
         TCH   0(UAREG)                 TEST CHANNEL               IMP5
         BCR   4+2,LNKRG2               RET IF INT PENDING OR BUSY IMP5
XCPTCH3A LR    REG0,LNKRG2              SAVE RETURN ADR FOR PSIO   IMP5
XCPTCH3  DS    0H                                                  IMP5
         AIF   (&IECI2SW EQ 0 OR &M65MP EQ 1).TWOS2A              19022
         STC   UAREG,PATHMSK           SAVE PATH INDICATOR         IMP5
         AIF   (&IECIACU EQ 0 OR &APR EQ 1).TWOS2A                19022
         NI    PATHMSK,X'0F'            CLEAR CU FIELD             SSSW
.TWOS2A  IC    UAREG,UCBUA(0,UCBREG)    SET UP U.A. FOR SIO        IMP5
         IC    ICREG,UCBDTI(UCBREG)    GET DEVICE TABLE INDEX      IMP5
         LH    WKREG1,DEVTAB+DVTSIO(ICREG)  GET SIO RTN VECTOR     IMP5
         AIF   (&TBASE EQ 0).TBS085                                IMP5
         AR    WKREG1,BASREG            ADJUST ADDRESS           BBBBBB
.TBS085  NI    UCBFL1(UCBREG),FF-UCBCUB CLEAR C.U. BSY           A27691
         BCR   15,WKREG1                TO SIO CODE AND RETURN   A27691
*                                      VIA SELECT VECTOR TABLE     IMP5
         SPACE 3                                                   IMP5
         AIF   (&APR EQ 0).MPB018                                 19022
*   DETERMINE IF THIS PATH TO THE DEVICE IS AVAILABLE, AND        19022
*   INITIALIZE THE PATH MASK IF IT IS.                            19022
         SPACE 1                                                  19022
XCPTCH2  DS    0H                       AVAILABILITY ROUTINE      19022
         AIF   (&M65MP EQ 0).MPB020                               19022
         SPACE 1                                                  19022
*   CHECK IF THE CHANNEL IS OFFLINE OR BUSY                       19022
         LR    WKREG1,UAREG             COPY CHANNEL ADDRESS      19022
         SRL   WKREG1,8                 ISOLATE CHAN NUMBER       19022
         AR    WKREG1,WKREG1            DOUBLE IT                19022
         TM    CHAN0-IPLPSW(WKREG1),CHRCF IS IT AVAILABLE         19022
         BCR   7,LNKRG2                 NO, TRY NEXT PATH         19022
.MPB020  LA    WKREG1,FOURPATH          SET UP ANDING MASK      SA60510
         NR    WKREG1,UAREG             GET PATH BITS             19022
         AIF   (&M65MP EQ 0).MPB030                               19022
         CLI   CPUID-IEAQFX00,CPUA      IS IT CPU-A               19022
         BC    8,XCPTCH2A               YES, LEAVE PATH BITS      19022
         SRL   WKREG1,2                 NO, SHIFT PATH BIT        19022
XCPTCH2A DS    0H                                                 19022
.MPB030  EX    WKREG1,PATHTST           IS PATH OFFLINE           19022
         BCR   7,LNKRG2                 YES, TRY NEXT PATH        19022
         AIF   (&IECIACU EQ 0 AND &M65MP EQ 0).MPB035             19022
         STC   WKREG1,PATHMSK           INIT PATH MASK            19022
         AIF   (&M65MP EQ 0).MPB035J    MP SYSTEM               SA60510
         OI    MPTCHSW,ONLNPTH          INDICATE PATH EXISTS    SA60510
.MPB035J AIF   (&M65MP EQ 1 OR &IECIACU EQ 0).MPB035            SA60510
         IC    UAREG,PATHMSK            COMBINE CH ADR WITH PTH   19022
.MPB035  BCR   15,LNKRG3                RETURN TO CALLER          19022
         SPACE 1                                                  19022
.MPB018  AIF   (&M65MP EQ 0).MPB040                               19022
*  INVESTIGATE THE POSSIBLITY OF STARTING THIS REQUEST ON THE OTHER CPU
XCPTCH1  DS    0H                                               SA56321
         LA    WKREG1,FOURPATH          SET UP ANDING MASK      SA60510
         NR    WKREG1,UAREG             GET PATH BITS           SA60510
         CLI   CPUID-IEAQFX00,CPUB      IS IT CPU-B             SA60510
         BC    8,XCPTCH2M               YES, LEAVE PATH BITS    SA60510
         SRL   WKREG1,2                 NO, SHIFT PATH BITS     SA60510
XCPTCH2M EX    WKREG1,PATHTST           IS PATH OFFLINE         SA60510
         BCR   7,LNKRG2                 YES, TRY NEXT PATH      SA60510
         LR    APBSRG,UAREG             COPY CUA                SA56321
         SRL   APBSRG,8                 ISOLATE CHANN ADDR      SA56321
         AR    APBSRG,APBSRG            2X ADDR = INDEX         SA56321
         LA    APBSRG,CHAN0-IEAQFX00(APBSRG)  GET CHAN OFFSET   SA56321
         A     APBSRG,PREFIX2           GET OTHER CPU'S PRFX    SA56321
XCPTCH1A TM    CHPTH(APBSRG),CHRCF      TEST IF CHAN IS OFFLINE SA60510
         BCR   7,LNKRG2                 RETURN IF OFFLINE       SA60510
         OI    MPTCHSW,ONLNPTH          INDICATE PATH EXISTS    SA60510
         TM    CHPTH(APBSRG),CHBSY      TEST IF CHAN IS BUSY    SA60510
         BCR   7,LNKRG2                 RETURN IF BUSY             IMP5
*                                                                  IMP5
*   THIS ROUTINE BRANCHES TO THE SHOULDER TAP ROUTINE AFTER        IMP5
*   SETTING THE SHOULDER TAP MASK TO INDICATE TO THE OTHER CPU     IMP5
*   WHICH CHANNEL IS INVOLVED IN THE I/O OPERATION.                IMP5
*                                                                  IMP5
         TM    XCPBTE,XCPST             TEST FOR TYPE OF ENTRANCE  IMP5
         BC    8,XCPSTGO                NOT FROM SHOULDER TAP      IMP5
         TM    UCBFL1(UCBREG),UCBCUB    TEST FOR CU BUSY           IMP5
         BC    1,SELBSYV           DON'T SHOULDER TAP - EXIT       IMP5
*                                                                  IMP5
XCPSTGO  LR    WKREG2,UAREG             COPY CUA
         SRL   WKREG2,8                 REMOVE UNIT ADDRESS
         SLL   WKREG2,2                 CHANNEL ADR X 4          BBBBBB
         LA    WKREG2,IECCST(WKREG2)    POINT AT CHAN TBL ENTRY  BBBBBB
         LA    WKREG1,STCHMSKA          GET CPU A'S CHAN. MSK    A42119
         CLI   CPUID-IEAQFX00,CPUA      IS THIS CPUA             A42119
         BC    8,XCPSTMSK               YES, GO AHEAD            A42119
         LA    WKREG1,STCHMSKB          NO, GET B'S CHAN. MASK   A42119
XCPSTMSK OC    ZERO(1,WKREG1),LCTMSK(WKREG2) FORCE CHAN. RESTRT  A42119
         MVI   STMASK+3,ONE             INDICATE SHOULDER TAP
         OI    CHPTH(APBSRG),CHBSY     MARK CHANNEL BUSY           IMP5
         L     APBSRG,STADDR       LOAD SHOULDER TAP ADDRESS       IMP5
         BALR  LNKRG2,APBSRG       TO SHOULDER TAP ROUTINE         IMP5
         BC    15,SELBSYV          TO ENQUEUE OR RESTART           IMP5
         SPACE 3                                                SA60510
XCPTCH17 TM    MPTCHSW,ONLNPTH          WAS A PATH AVAILABLE    SA60510
         BCR   7,LNKRG2                 YES, EXIT NORMALLY      SA60510
         OI    IOBCC(IOBREG),IOBCC3     FAKE CC=3 ON SIO        SA60510
         MVI   PATHMSK,PATHINOP         NO ONLINE PATHS         SA60510
         STH   TSTREG,UCBLTS(UCBREG)    TIE RQE TO UCB          SA73450
         BC    15,XCP054                GO TO PSIO CC=3 HANDLER SA60510
.MPB040  AIF   (&APR EQ 0).DONE                                   19022
         AIF   (&M65MP EQ 0 AND &IECIACU EQ 0).DONE               19022
         SPACE 1                                                  19022
XCPTCH7  DS    0H                                                19022
         AIF   (&M65MP EQ 0).MPB050
         A     APBSRG,PREFIX2           ADDRESS OTHER CPU PSA    19022
         NI    ZERO(APBSRG),FF-CHBSY    RESET OTHER CPU MPLX BSY 19022
.MPB050  BAL   LNKRG3,XCPTCH2           SEE IF PATH IS AVAIL     19022
         BC    15,XCPTCH3A              GO TO COMMON ROUTINE      19022
.DONE    EJECT
.FINIS   ANOP
         MEND                                                      IMP5
