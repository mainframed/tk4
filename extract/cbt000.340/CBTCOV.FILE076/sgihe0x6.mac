         MACRO
         SGIHE0X6
.*
.*       THIS MACRO GENERATES THE ASSEMBLY LANGUAGE CODE FOR IHELTTA
.*       AND ALSO PROVIDES THE STANDARD PRV DEFINITION.
.*
*
*        IHELTTA     TRANSFER VECTOR MODULE
*
IHELT1   CSECT
*
*        PRV DEFINITIONS FOR SHARED LIBRARY.
*
IHEQTT1  DXD   A
IHEQTT2  DXD   A
IHEQTT3  DXD   A
IHEQTT4  DXD   A
IHEQTT5  DXD   A
IHEQTV1  DXD   A
IHEQTV2  DXD   A
IHEQTV3  DXD   A
IHEQTV4  DXD   A
IHEQTV5  DXD   A
IHEQLPR  DXD   A
IHEQADC  DXD   2A
IHEQATV  DXD   A
IHEQCFL  DXD   2A
IHEQCTS  DXD   A
IHEQERR  DXD   A
IHEQEVT  DXD   2A
IHEQFOP  DXD   A
IHEQFVD  DXD   A
IHEQINV  DXD   A
IHEQLCA  DXD   A
IHEQLSA  DXD   A
IHEQLW0  DXD   A
IHEQLW1  DXD   A
IHEQLW2  DXD   A
IHEQLW3  DXD   A
IHEQLW4  DXD   A
IHEQLWE  DXD   A
IHEQLWF  DXD   A
IHEQRTC  DXD   A
IHEQSAR  DXD   A
IHEQSFC  DXD   A
IHEQSLA  DXD   A
IHEQSPR  DXD   A
IHEQTIC  DXD   A
IHEQVDA  DXD   A
IHEQXLV  DXD   2A
IHEQTCA  DXD   A
*
*
*
*
IHELT1   CSECT
         ENTRY IHELTT1
         ENTRY IHELTTA
         ENTRY IHELTTB
         ENTRY IHELTTC
*
*
IHELTTA  EQU   *
         STM   14,12,12(13)             SAVE REGISTERS.
         BALR  10,0                     ADDRESSABILITY.
         USING *,10
         GETMAIN R,LV=112               CORE FOR SSA.
         ST    1,8(13)                  OLD FWD CHAIN.
         ST    13,4(1)                  BACK CHAIN.
         SR    3,3
         ST    3,0(1)                   IDENTIFIER=0.
         ST    3,8(1)                   NEWFWD CHAIN=0.
         LR    13,1                     POINT R13 AT SAVE AREA.
         LM    4,8,VTT1                 STORE ADDR LTT1 TO LTT5
         STM   4,8,72(13)               IN DUMMY PRV
         LOAD  EP=IHELTVA               GET ADDR IHELTVA.
         LR    2,0
         LM    4,8,8(2)                 STORE ADDR LTV1 TO LTV5
         STM   4,8,92(13)               IN DUMMY PRV
         LA    12,72(13)                MAKE PR APPEAR TO
         SH    12,QTT1                  POINT AT PRV.
         L     1,4(13)
         L     1,24(1)                  R1=ADDR FROM INVOKER.
         LA    14,IHELTTC               PROG RETURN ADDR.
         L     15,VNTRY                 PROG ENTRY ADDR.
         BR    15                       GO TO PL/1 PROGRAM.
*
*
IHELTTB  EQU   *                        RETURN FROM SAP/TSA
         BALR  10,0                     ESTABLISH ADDRESSABILITY
         USING *,10
         LR    15,13                    SAVE CURRENT R13.
         L     6,*-*(12)                GET IHEQCTS SLOT IS...
         ORG   *-2                      ZERO FOR NON-TASKING.
         DC    QL2(IHEQCTS)             CNTLTASK SAVE AREA FOR TASKING
         LTR   6,6                      TEST FOR TASKING.
         BZ    IHENOTK                  SKIP IF NON-TASKING.
         L     15,4(6)                  LOAD TASKING SAVE AREA.
IHENOTK  EQU   *
         LR    6,12                     SET R6 TO POINT AT...
         AH    6,QTT1                   DUMMY PRV IN SAVE AREA.
         MVC   0(40,6),72(15)           MOVE A(TRANS VECTS) TO PRV.
         L     15,VMAIN                 GET REAL PROG ENTRY POINT.
         L     15,0(15)                 BRANCH TO REAL PROG
         BR    15
*
*
IHELTTC  EQU   *
         BALR  10,0                     ESTABLISH ADDRESSABILITY
         USING *,10
         LR    2,15                     RETURN FROM PL/1 PROG.
         LR    3,13                     COMPLETE.
         L     4,4(13)
         DELETE EP=IHELTVA              IN CASE LPA NOT IN USE
         LA    0,112
         LR    1,13
         FREEMAIN R,LV=(0),A=(1)        FREE SAVE AREA.
         LR    15,2                     RESTORE ALL REGISTERS.
         LR    13,4
         LM    0,12,20(13)
         L     14,12(13)
         BR    14                       TRANSFER OUT.
*
*        PROGRAM CONSTANTS
*
QTT1     DC    QL2(IHEQTT1)
VTT1     DC    V(IHELTT1)
VTT2     DC    V(IHELTT2)
VTT3     DC    V(IHELTT3)
VTT4     DC    V(IHELTT4)
VTT5     DC    V(IHELTT5)
VMAIN    DC    V(IHEMAIN)               A(MAIN PROC).
VNTRY    DC    V(IHENTRY)               REAL PL/1 ENTRY POINT.
*
*
IHELTT1  EQU   *
         MEND
