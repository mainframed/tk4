         MACRO
         IECTBL &CHANLST,&M65MP,&LCHTOT,&TYPE,&OLTEP
         GBLB  &D2305                                           SA56509
         LCLA  &INSERT,&LSTINDX,&COUNT,&CHMSK                   SA56509
.*   &CHANLST  LIST OF CHANNELS
.*   &M65MP    M65MP SYSTEM INDICATOR
.*   &LCHTOT   TOTAL LCH'S IN SYSTEM
.*   &TYPE     SUBLIST OF DEVICE TYPES IN SYSTEM
.*   &OLTEP    ON LINE DEVICE TEST SUPPORT INDICATOR
*   DEVICE TABLE
DEVTAB   DS    0F
         AIF   (&OLTEP EQ 0).TSTLOOP                              21051
         AIF   (NOT &D2305).TSTLOOP                               21051
**  SPECIAL ENTRY FOR OLTEP SPECIFIC EXPOSURE REQUESTS            21051
         DC    AL2(0)                                             21051
         DC    AL2(ZEUSOLT-IOSBASE)     SPECIAL OLTEP SIO CODE    21051
         DC    AL2(0)                                             21051
         DC    AL2(0)                                             21051
.TSTLOOP AIF   (&COUNT EQ N'&TYPE).END  LAST TYPE OUT
&COUNT   SETA  &COUNT+1                UPDATE COUNT OF TUPES
         AIF   ('&TYPE(&COUNT)' EQ '').TSTLOOP  TEST FOR BLANK TYPE
         AIF   ('&TYPE(&COUNT)'(1,1) EQ 'P').PRTYTST  TEST PRIOROTY Q
         AIF   ('&TYPE(&COUNT)'(1,1) EQ 'O').ORDDSK    TEST ORDERED Q
.*                 CHECK FOR FIFO ENQUE
    AIF   ('&TYPE(&COUNT)' EQ 'DA' OR '&TYPE(&COUNT)' EQ 'DS').DAWFIFO
         AIF   ('&TYPE(&COUNT)' EQ 'ML').DAWFIFO  TEST FOR MERLIN
         AIF   ('&TYPE(&COUNT)' EQ 'TA').TAWFIFO  TEST TA/FOFO
         AIF   ('&TYPE(&COUNT)' EQ 'TP').TPWFIFO  TEST TP/FIFO Q
         AIF   ('&TYPE(&COUNT)' EQ 'GR').GRWFIFO  TEST FOR GRAPHICS
    AIF   ('&TYPE(&COUNT)' EQ 'DR' OR '&TYPE(&COUNT)' EQ 'ZS').DRWFIFO
         AIF   ('&TYPE(&COUNT)' EQ 'AT').ASPFIFO ASPEN TYPE       21048
**  UNIT RECORD ENTRY WITH FIFO ENQUEING
         DC    AL2(XCPENQ-IOSBASE)      FIFO QUEUE FOR UNIT RECORD RL19
.URSIO   AIF   ('&TYPE(&COUNT)' EQ 'WR' OR '&TYPE(&COUNT)' EQ 'PWR').WR*
               SIO                      TEST FOR 3211 PRINTER    A44146
         DC    AL2(XIOUR-IOSBASE)       SIO CODE FOR UNIT RECORD
         AGO   .URTRAP
.WRSIO   DC    AL2(XIOWR-IOSBASE)       SIO CODE FOR 3211        A44146
.URTRAP  DC    AL2(INTURT-IOSBASE)      TRAP CODE FOR UNIT RECORD
    AIF  ('&TYPE(&COUNT)' EQ 'WR' OR '&TYPE(&COUNT)' EQ 'PWR').EXPSN
    AIF  ('&TYPE(&COUNT)' EQ 'NR' OR '&TYPE(&COUNT)' EQ 'PNR').EXPSN
         DC    AL2(NORMSEN-IOSBASE)     SENSE ENTRY
         AGO   .TSTLOOP           RETURN TO TYPE TST LOOP
.TAWFIFO ANOP
**   TAPE ENTRY FOR FIFO QUEUEING
         DC    AL2(XCPENQ-IOSBASE)      FIFO QUEUE FOR TAPE        RL19
.TASIO   DC    AL2(XIOTPE-IOSBASE)      SIO FOR TAPE
         DC    AL2(INTTAT-IOSBASE)      TRAP FOR TAPE              RL19
         DC    AL2(NORMSEN-IOSBASE)     SENSE ENTRY
         AGO   .TSTLOOP           RETURN TO TYPE TST LOOP
.ASPFIFO ANOP                                                     21048
**   ASPEN ENTRY WITH FIFO QUEUING                                21048
         DC    AL2(XCPENQ-IOSBASE)      FIFO QUEUE FOR ASPEN      21048
.ASPSIO  DC    AL2(XIOTPEA-IOSBASE)     SIO FOR ASPEN             21048
         DC    AL2(INTASP-IOSBASE)      TRAP FOR ASPEN            21048
         DC    AL2(EXPSEN-IOSBASE)      SENSE FOR ASPEN           21048
         AGO   .TSTLOOP                 RTN TO TYPE TST LOOP      21048
.DAWFIFO ANOP
**  DA OR ML ENTRY WITH FIFO QUEUEING
         DC    AL2(XCP100-IOSBASE)      FIFO QUEUEING FOR DA OR ML
         AIF   ('&TYPE(&COUNT)' EQ 'ML').DRPRIR   TEST FOR MERLIN
         DC    AL2(XIODSK-IOSBASE)      SIO FOR D.A.               RL19
         DC    AL2(INTDAT-IOSBASE)      TRAP FOR D.A.
.SNS     AIF   ('&TYPE(&COUNT)' EQ 'OML').EXPSN   TEST FOR MERLIN
    AIF  ('&TYPE(&COUNT)' EQ 'PML' OR '&TYPE(&COUNT)' EQ 'PZS').EXPSN
    AIF  ('&TYPE(&COUNT)' EQ 'ML' OR '&TYPE(&COUNT)' EQ 'ZS').EXPSN
         AIF   ('&TYPE(&COUNT)' EQ 'DS').SHAR     TEST FOR SHARED
         DC    AL2(NORMSEN-IOSBASE)     SENSE ENTRY
         AGO   .TSTLOOP           RETURN TO TYPE TST LOOP
.SHAR    DC    AL2(SDASEN-IOSBASE)      SHARED DA SENSE
         AGO   .TSTLOOP           RETURN TO TYPE TST LOOP
.TPWFIFO ANOP
**  TP SUPPORT ENTRY WITH FIFO QUEING
         DC    AL2(XCPENQ-IOSBASE)      FIFO QUEUEING FOR TP       RL19
         DC    AL2(XCP121-IOSBASE)      SIO FOR TP DEVICES         RL19
         DC    AL2(XCPTPT-IOSBASE)      TRAP FOR TP DEVICES        RL19
         DC    AL2(TPSEN-IOSBASE)       SENSE FOR TP
         AGO   .TSTLOOP           RETURN TO TYPE TST LOOP
.DRWFIFO ANOP
**  DR OR ZS SUPPORT ENTRY WITH FIFO QUEUEING
         DC    AL2(XCP100-IOSBASE)      FIFO QUEUEING FOR DR
         AIF   ('&TYPE(&COUNT)' NE 'ZS').DRPRIR   TEST IF NOT ZEUS
.ZUS     DC    AL2(ZEUSIO-IOSBASE)      SIO CODE FOR ZEUS
         DC    AL2(INTZUT-IOSBASE)           ZEUS TRAP CODE
         AGO   .SNS                     GET SENSE POINTER
.DRPRIR  DC    AL2(RPSSIO-IOSBASE)      SIO CODE FOR RPS OR ML
         DC    AL2(INTNOT-IOSBASE)      NO TRAP CODE ENTRY
         AGO   .SNS                     GET SENSE POINTER
.GRWFIFO ANOP
**  GRAPHICS SUPPORT ENTRY WITH FIFO QUEUING
         DC    AL2(XCPENQ-IOSBASE)      FIFO QUEUING FOR GRAPHICS  RL19
         DC    AL2(XIOUR-IOSBASE)       SIO FOR GRPAHIC DEVICES    RL19
         DC    AL2(INTDNP-IOSBASE)      TRAP FOR GRAPHIC DEVICES   RL19
         DC    AL2(NORMSEN-IOSBASE)     SENSE ENTRY
         AGO   .TSTLOOP            RETURN TO TYPE TST LOOP
.PRTYTST ANOP
    AIF   ('&TYPE(&COUNT)' EQ 'PDA' OR '&TYPE(&COUNT)' EQ 'PDS').DAWPR
         AIF   ('&TYPE(&COUNT)' EQ 'PML').DAWPR   TEST FOR MERLIN
         AIF   ('&TYPE(&COUNT)' EQ 'PTA').TAPEWPR  TEST TAPE/PRI.Q
         AIF   ('&TYPE(&COUNT)' EQ 'PTP').TPWPR  TEST T/T / PRIORITY Q
         AIF   ('&TYPE(&COUNT)' EQ 'PGR').GRWPR  TEST GR PRIORITY QUE.
    AIF   ('&TYPE(&COUNT)' EQ 'PDR' OR '&TYPE(&COUNT)' EQ 'PZS').DRWPR
         AIF   ('&TYPE(&COUNT)' EQ 'PAT').ASPPRI                  21048
**  UNIT RECORD EQUIPMENT POINTS TO ENTRY ONE WITH PRIORITY
         DC    AL2(XCPPRQ-IOSBASE)      PRIORITY Q'ING FOR U.R.    RL19
         AGO   .URSIO
.TAPEWPR ANOP
**  TAPE ENTRY FOR PRIORITY QUEING
         DC    AL2(XCPPRQ-IOSBASE)      PRIORITY QUE FOR TAPE      RL19
         AGO   .TASIO
.ASPPRI  ANOP                                                     21048
**   ASPEN ENTRY WITH PRIORITY QUEUING                            21048
         DC    AL2(XCPPRQ-IOSBASE)      PRIORITY QUEUE FOR ASPEN  21048
         AGO   .ASPSIO                                            21048
.DRWPR   ANOP
**  DR OR ZS ENTRY WITH PRIORITY QUEUEING
         AGO   .DRPRI
.DAWPR   ANOP
**  DA OR ML ENTRY WITH PRIORITY QUEUEING
.DRPRI   DC    AL2(XCP100A-IOSBASE)     PRIORITY SEEK Q'NG FOR DA
         AIF   ('&TYPE(&COUNT)' EQ 'PZS').ZUS     TEST FOR ZEUS
    AIF  ('&TYPE(&COUNT)' EQ 'PDR' OR '&TYPE(&COUNT)' EQ 'PML').DRPRIR
         DC    AL2(XIODSK-IOSBASE)      SIO FOR DIRECT ACCESS      RL19
         DC    AL2(INTDAT-IOSBASE)      TRAP FOR DIRECT ACCESS     RL19
         AIF   ('&TYPE(&COUNT)' EQ 'PDS').SHAR    TEST FOR SHARED
         DC    AL2(NORMSEN-IOSBASE)     SENSE ENTRY
         AGO   .TSTLOOP           RETURN TO TYPE TST LOOP
.TPWPR   ANOP
**  TP SUPPORT ENTRY WITH PRIORITY QUEING
         DC    AL2(XCPPRQ-IOSBASE)      PRIORITY QUE FOR TP        RL19
         DC    AL2(XCP121-IOSBASE)      SIO RTN FOR TP DEVICES     RL19
         DC    AL2(XCPTPT-IOSBASE)      TRAP FOR TP DEVICES        RL19
         DC    AL2(TPSEN-IOSBASE)       SENSE FOR TP
         AGO   .TSTLOOP           RETURN TO TYPE TST LOOP
.GRWPR   ANOP
**  GRAPHICS ENTRY WITH PRIORITY QUEUING
         DC    AL2(XCPPRQ-IOSBASE)      PRIORITY QUEUING FOR GRAPH RL19
         DC    AL2(XIOUR-IOSBASE)       SIO FOR GRAPHICS           RL19
         DC    AL2(INTDNP-IOSBASE)      TRAP FOR GRAPHICS          RL19
         DC    AL2(NORMSEN-IOSBASE)     SENSE ENTRY
         AGO   .TSTLOOP            RETURN TO TYPE TST LOOP
.ORDDSK  ANOP                                                      OSER
**   DA ENTRY WITH ORDERED SEEK QUEUEING
         DC    AL2(XCPSEQ-IOSBASE)      ORDERED SEEK Q FOR DA      RL19
         AIF   ('&TYPE(&COUNT)' EQ 'OML').DRPRIR  TEST FOR MERLIN
         DC    AL2(XIODSK-IOSBASE)      SIO FOR DIRECT ACCESS      RL19
         DC    AL2(INTDAT-IOSBASE)      TRAP FOR DIRECT ACCESS     RL19
         AIF   ('&TYPE(&COUNT)' EQ 'ODS').SHAR    TEST FOR SHARED
         DC    AL2(NORMSEN-IOSBASE)     SENSE ENTRY
         AGO   .TSTLOOP                 RETUN TO TEST LOOP
.EXPSN   DC    AL2(EXPSEN-IOSBASE)      EXPANDED SENSE ENTRY
         AGO   .TSTLOOP           RETURN TO TYPE TST LOOP
.END     ANOP
**   CHANNEL TABLE
IECCST   DS    0H
.CONTIN  AIF  (&LSTINDX EQ N'&CHANLST).ENDA
         AIF   (&CHMSK GT 1).CONTINA    TEST FOR INITIALIZATION
&CHMSK   SETA  256                      INITIALIZE MASK
.CONTINA ANOP
&CHMSK   SETA  &CHMSK/2                 SHIFT RIGHT 1
&LSTINDX SETA  &LSTINDX+1               INCREMENT INDEX
         AIF   (&CHANLST(&LSTINDX) EQ 0).OUT1     TEST FOR CHAN
&INSERT  SETA  &LSTINDX-1               SET CHAN CODE POINTER
         DC    AL2(INTCS&INSERT.-IOSBASE)    CHAN SEARCH CODE
         DC    AL1(&CHMSK)              MASK
         DC    AL1(&CHANLST(&LSTINDX))  CHANNEL TYPE
&INSERT  SETA  &LSTINDX                 GET HI-TOT FOR UCBSAVE
         AGO   .CONTIN                  CONTINUE
.OUT1    DC    AL2(INT033-IOSBASE)
         DC    AL1(&CHMSK)              MASK
         DC    X'00'                    FILLER
         AGO   .CONTIN                  CONTINUE
.ENDA    AIF   (N'&CHANLST LT 8).ENDB   MORE THAN 7 CHANNELS
&CHMSK   SETA  0
&COUNT   SETA  65536
&LSTINDX SETA  0
.RSTLOOP AIF   (&LSTINDX EQ 16).RSTMSK
&LSTINDX SETA  &LSTINDX+1
&COUNT   SETA  &COUNT/2
         AIF   (&CHANLST(&LSTINDX) EQ 0).RSTLOOP
&CHMSK   SETA  &CHMSK+&COUNT
         AGO   .RSTLOOP
.RSTMSK  ANOP
*        RESTART MASK FOR GREATER THAN 7 CHANNELS
RST7MSK  DC    AL2(&CHMSK)
.ENDB    AIF   (&M65MP EQ 1).NOSAVE     TEST FOR MP
UCBSAVE  DS    0H
         DC    &INSERT.Y(UCB1-IPLPSW)   INITIALIZE ADDRESS
.NOSAVE  ANOP
*   LCH TABLE
LCHTAB   DS    0H
IECILCH  DS    0C
&INSERT  SETA  1                        INITIALIZE NUMBER
.LCHST   AIF   (&INSERT GT &LCHTOT).LCHEND
**             CHANNEL LCHWORD
LCH&INSERT DC X'FFFF',XL4'0',AL2(XCPCH&INSERT.-IPLPSW)   LCHWORD
&INSERT  SETA  &INSERT+1                UPDATE INDEX
         AGO   .LCHST
.LCHEND  ANOP
         MEND
