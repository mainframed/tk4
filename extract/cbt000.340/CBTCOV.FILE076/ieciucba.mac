         MACRO
         IECIUCBA  &UCBNO,&TYPEA,&BMP,&SH,&TMSK,&EVAR,&EVAW,&LOG  20201
         GBLA  &IECIZS1,&IECIZCT                                  20201
         LCLA  &A                                                 20201
         LCLC  &C
 AIF  ('&TYPEA'(5,4) NE '2006' AND '&TYPEA'(5,4) NE '2007').ZSET2 20201
         AIF   (&IECIZCT  NE 0).ZSET    TEST FOR 1ST ZEUS UCB     20201
&IECIZS1 SETA  &UCBNO                   SET POINTER TO FIRST UCB  20201
.ZSET    ANOP                                                     20201
&IECIZCT   SETA  &IECIZCT+1             UPDATE COUNTER            20201
         AIF   (&IECIZCT  EQ 8).ZSET1   TEST FOR LAST             20201
&A       SETA  &UCBNO+1                 SET PTR TO NEXT UCB       20201
         AGO   .ZSET2                                             20201
.ZSET1   ANOP                                                     20201
&A       SETA  &IECIZS1+1               SET LAST TO POINT TO SECOND
.ZSET2   ANOP                                                     20201
&C       SETC  '&TYPEA'(3,1)
         AIF   ('&TYPEA'(5,2) NE '80').TSTUR TEST FOR TAPE        19023
         AIF   ('&TYPEA'(7,2) EQ '01').NOTASP 2400?               21048
         AIF   (&BMP EQ 0).NO3410        3410?                    21048
UCBS&UCBNO DC  XL9'0'                    3410 SENSE AREA          21048
         AGO   .NOTASP                                            21048
.NO3410  ANOP                                                     21048
UCBS&UCBNO DC   3D'0'                     3420 SENSE AREA         21048
.NOTASP  ANOP                                                     21048
UCBW&UCBNO DC  D'0'                     ROR WORK AREA             19023
         AIF   (&TMSK LT 64).CKASP      TEST FOR ESV,EVA          21048
         DC    X'0000',AL1(&EVAR),AL1(&EVAW),F'0' EVA/ESV WK AREA 19023
         AIF   (&TMSK LT 192).CKASP2     TEST FOR ESV             21048
         DC    D'0'                     ESV WORK AREA             19023
         AGO   .END                                               19023
.CKASP   ANOP                                                     21048
         AIF   ('&TYPEA'(7,2) EQ '01').END    2400?               21048
         DC    2D'0'                     34XX WORK AREA           21048
         AGO   .END                                               21048
.CKASP2  ANOP                                                     21048
         AIF   ('&TYPEA'(7,2) EQ '01').END  34XX?                 21048
         DC    D'0'                      34XX WORK AREA           21048
         AGO   .END                                               21048
.TSTUR   AIF   ('&TYPEA'(5,2) EQ '08').URWK  TST FOR UR           21088
         AGO   .DA                                                19017
.URWK    ANOP                                                     19017
UCBW&UCBNO DC  D'0'                     ERP WORK AREA             19017
 AIF ('&TYPEA'(7,2) NE '06' AND '&TYPEA'(7,2) NE '09' AND '&TYPEA'(7,2)X
               NE '0C' AND '&TYPEA'(7,2) NE '0B').T2495 TEST WR   M0417
         DC    2D'0'                    ERP WORK AREA             20202
         AGO   .END                                               20202
.T2495   AIF   ('&TYPEA'(5,4) NE '0818').END TEST FOR 2495        20202
         DC    3D'0'                    ERP WORK AREA             19016
         AGO   .END
.DA      AIF   ('&TYPEA'(5,4) NE '2009').DA1 TEST FOR MERLIN      20201
UCBS&UCBNO  DC 3D'0'                    SENSE AREA                20201
         CCW   X'07',0,X'60',6          3330 SEEK                 20201
         CCW   X'1F',*+5,X'60',1        3330 SET FILE MASK        20201
         CCW   X'08',0,X'00',0          3330 TIC TO USER CH PROG  20201
         AGO   .ZSWK                                              20201
.DA1 AIF  ('&TYPEA'(5,4) NE '2006' AND '&TYPEA'(5,4) NE '2007').NDABS
UCBS&UCBNO  DC  3D'0',F'0'              SENSE AREA                20201
         DC    A(UCB&IECIZS1)           PTR TO FIRST ZEUS UCB     20201
         DC    A(UCB&A)                 PTR TO NEXT IN RING       20201
         DC    A(CULOG)                 PTR TO LOGOUT AREA        20201
.ZSWK    ANOP                                                     20201
UCBW&UCBNO DC  15D'0'                   WORK AREA                 20201
* UCB WORK AREA EXTENSION FOR CMD RETRY AND RPS                   ITH01
         DC    2D'0' *                  EXTENSION                 ITH01
         AGO   .END                                               20201
.NDABS   AIF   ('&C'GE'0'AND'&C'LT'4' OR '&C'GE'8'AND'&C'LT'C').NOVRFL
UCBW&UCBNO DC  5D'0',10F'0'             ERP WORK AREA WITH TRK OVFLOW
         AGO   .END
.NOVRFL  ANOP
UCBW&UCBNO DC 5D'0'                     ERP WORK AREA
.END     AIF   (&LOG EQ 0).END1         TEST FOR LOG OUT AREA     20201
CULOG    DC    20D'0'                   LOG OUT AREA              20201
.END1    AIF   (&IECIZCT  LT 8).END2    TEST FOR LAST ZEUS        20201
&IECIZCT   SETA  0                      CLEAR COUNTER             20201
.END2    ANOP                                                     20201
         MEND
