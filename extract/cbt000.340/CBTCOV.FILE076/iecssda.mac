         MACRO
         IECSSDA   &SSDA,&SSTA,&M65MP
         GBLA  &SSCCW(16),&CCW(16)                              US03240
         LCLA  &WORK2,&WORK3,&WORK4,&INDX,&BYTE,&HIGH           US03240
         LCLC  &CHAR1,&CHAR2                                    US03240
         TITLE 'CCW LOOKUP ROUTINE AND CCWS - INPUT/OUTPUT SUPERVISOR'
*      THIS ROUTINE FINDS THE PROPER CCWS TO BE USED IN
*      BULIDING THE PREFIX TO A USERS CHANNEL PROGRAM FOR BOTH
*      TAPE AND DIRECT ACCESS DEVICES.   APBSRG IS USED AS THE
*      BASE REGISTER AND WKREG1 AS THE RETURN REGISTER.
         AIF   (&SSDA EQ 0 AND &SSTA EQ 0).SSDA005
*      IF THE CCWS ARE FOR A DEVICE ON A SUB-SELECTOR CHANNEL,
*      A CHECK IS MADE TO SEE IF THE CCWS ARE CURRENTLY IN USE.
*      IF THE CCWS ARE BUSY, THE RETURN IS MADE DIRECTLY TO
*      WKREG1.  IF FREE, THE RETURN IS TO WKREG1+4.
         SPACE 1
HISMPXSW EQU   X'01'                    SELECTOR SUB-CHANNEL    US03240
.SSDA005 SPACE 2                                                US03240
CCWRTN   DS    0H                       CCW FIND ROUTINE        US03240
         USING *,APBSRG                 ADDRESSABILITY          US03240
         LR    WKREG2,UAREG             COPY UNIT ADDRESS       US03240
         SRL   WKREG2,8                 ISOLATE CHANNEL ADDR    US03240
         AIF   (&SSDA EQ 0 AND &SSTA EQ 0).SSDA080  HS MPX IN SYSTEM
         AR    WKREG2,WKREG2            MULTIPLY BY 2           US03240
         LA    WKREG3,IECCCWA(WKREG2)   GET TABLE ADDRESS       US03240
         IC    ICREG,0(WKREG3)          GET CHANNEL INDEX       US03240
         TM    1(WKREG3),HISMPXSW       SUB-SELECTOR DEVICE     US03240
         BZ    XCPCCW60                 NO, HANDLE FOR SELECTOR US03240
         LR    WKREG2,UAREG             COPY UNIT ADDRESS       US03240
         SLL   WKREG2,24+2              ISOLATE SUB-CHANNEL     US03240
         SRL   WKREG2,24+2+4               INDEX                US03240
         AIF   (&M65MP EQ 0).SSDA010    MP65 SYSTEM             US03240
         SLL   WKREG2,2                 MULTIPLY BY 4           US03240
         CLI   CPUID-IEAQFX00,CPUA      IS THIS CPUA            US03240
         BE    XCPCCW10                 YES, USE FIRST CCWS     US03240
         LA    WKREG2,2(WKREG2)         NO, USE SECOND CCWS     US03240
         AGO   .SSDA020                                         US03240
.SSDA010 AR    WKREG2,WKREG2            MULTIPLY BY 2           US03240
.SSDA020 ANOP                                                   US03240
XCPCCW10 LA    WKREG3,IECCCWB(ICREG)    GET XCX ADDRESS         US03240
         LH    WKREG3,0(WKREG2,WKREG3)  GET CCW  POINTER        US03240
         LTR   WKREG3,WKREG3            VALID POINTER           US04550
         BC    8,LCHERR1                NO, GO HANDLE           US04550
         AR    WKREG3,APBSRG            ADJUST ADDR WITH BASE   US03240
         LH    WKREG2,SKTIC+4(WKREG3)   GET LAST CCW UCB        US03240
         AIF   (&M65MP EQ 0).SSDA030    MP65 STSTEM             US03240
         SH    WKREG2,MPNEGUCB          BACK UP TO UCB PREFIX   US03240
         TM    UCBFL3(WKREG2),UCBCPUA   LAST START ON CPU A     US03240
         LA    WKREG2,4(WKREG2)         ADJUST BACK TO UCB      US03240
         BO    XCPCCW20                 NO, CPU B LAST STARTED  US03240
         CLI   CPUID-IEAQFX00,CPUA      IS THIS CPU A           US03240
         BE    XCPCCW30                 YES, IS UCB ACTIVE      US03240
         B     XCPCCW50                 NO, CCWS ARE FREE       US03240
XCPCCW20 CLI   CPUID-IEAQFX00,CPUA      IS THIS CPU A           US03240
         BE    XCPCCW50                 YES, CCWS ARE FREE      US03240
.SSDA030 ANOP                                                   US03240
XCPCCW30 DS    0H                                               US03240
         AIF   (&SSDA EQ 0 OR &SSTA EQ 0).SSDA040  BOTH SSDA AND SSTAPE
         TM    UCBTYP+2(WKREG2),DSKTYP  IS THIS DIRECT ACCESS   US03240
         BO    XCPCCW40                 YES, CHECK DTR AND ASK  US03240
.SSDA040 AIF   (&SSTA EQ 0).SSDA050     ANY SSTAPE              US03240
         TM    UCBFL1(WKREG2),UCBPST    IS DEVICE ACTIVE        US03240
         BCR   1,WKREG1                 RETURN, CCWS ARE BUSY   US03240
         AIF   (&SSDA EQ 0).SSDA060     ANY SSDA                US03240
         B     XCPCCW50                 NO, CCWS ARE FREE       US03240
.SSDA050 ANOP                                                   US03240
XCPCCW40 TM    UCBFL1(WKREG2),UCBDTR+UCBASK  IS DEVICE ACTIVE   US03240
         BCR   1,WKREG1                 RETURN, CCWS ARE BUSY   US03240
.SSDA060 ANOP                                                   US03240
XCPCCW50 STH   UCBREG,SKTIC+4(WKREG3)   UPDATE LAST UCB ADDR    US03240
         B     4(WKREG1)                RETURN, CCWS ARE FREE   US03240
XCPCCW60 LA    WKREG3,IECCCWB(ICREG)    GET CCW POINTER ADDR    US03240
         AIF   (&M65MP EQ 0).SSDA070    MP65 SYSTEM             US03240
         CLI   CPUID-IEAQFX00,CPUA      IS THIS CPUA            US03240
         BE    XCPCCW70                 YES, USE FIRST CCWS     US03240
         LA    WKREG3,2(WKREG3)         NO, USE SECOND CCWS     US03240
.SSDA070 ANOP                                                   US03240
XCPCCW70 LH    WKREG3,0(WKREG3)         GET CCW ADDRESS         US03240
         LTR   WKREG3,WKREG3            VALID POINTER           US04550
         BC    8,LCHERR1                NO, GO HANDLE           US04550
         AR    WKREG3,APBSRG            ADJUST WITH BASE ADDR   US03240
         B     4(WKREG1)                RETURN, CCWS ARE FREE   US03240
         AIF   (&M65MP EQ 0).SSDA086                            US03240
MPNEGUCB DC    H'4'                     MP UCB PREFIX LENGTH    US03240
         AGO   .SSDA086                 BYPASS NO HS MPX CODE   US03240
.SSDA080 AIF   (&M65MP EQ 0).SSDA082    MP65 SYSTEM             US03240
         SLL   WKREG2,2                 MULTIPLY BY 4           US03240
         CLI   CPUID-IEAQFX00,CPUA      IS THIS CPUA            US03240
         BE    XCPCCW80                 YES, USE FIRST CCWS     US03240
         LA    WKREG2,2(WKREG2)         NO, USE SECOND CCWS     US03240
         AGO   .SSDA084                                         US03240
.SSDA082 AR    WKREG2,WKREG2            MULTIPLY BY 2           US03240
.SSDA084 ANOP                                                   US03240
XCPCCW80 LH    WKREG3,IECCCWA(WKREG2)   GET CCW ADDRESS         US03240
         LTR   WKREG3,WKREG3            VALID POINTER           US04550
         BC    8,LCHERR1                NO, GO HANDLE           US04550
         AR    WKREG3,APBSRG            ADJUST ADDR WITH BASE ADDR
         BR    WKREG1                   RETURN                  US03240
.SSDA086 ANOP
         DROP  APBSRG
         SPACE 2
&INDX    SETA  0
.SSDA090 ANOP
&INDX    SETA  &INDX+1
         AIF   (&INDX GT 16).SSDA110
         AIF   (&SSCCW(&INDX) NE 0).SSDA100
         AIF   (&CCW(&INDX) EQ 0).SSDA090
.SSDA100 ANOP
&HIGH    SETA  &INDX
         AGO   .SSDA090
.SSDA110 ANOP
&INDX    SETA  0
IECCCWA  DS    0H
         AIF   (&SSDA NE 0 OR &SSTA NE 0).SSDA120
.SSDA113 ANOP
&INDX    SETA  &INDX+1
         AIF   (&INDX GT &HIGH).SSDA270
         AIF   (&CCW(&INDX) EQ 0).SSDA117
&CHAR1   SETC  '0123456789ABCDEF'(&INDX,1)
&CHAR1   SETC  'CCW&CHAR1'
         AIF   (&M65MP EQ 0).SSDA115
         DC    AL2(&CHAR1.A-CCWRTN)
         DC    AL2(&CHAR1.B-CCWRTN)
         AGO   .SSDA113
.SSDA115 DC    AL2(&CHAR1-CCWRTN)
         AGO   .SSDA113
.SSDA117 DC    AL2(0)
         AIF   (&M65MP EQ 0).SSDA113
         DC    AL2(0)
         AGO   .SSDA113
.SSDA120 ANOP
&INDX    SETA  &INDX+1
         AIF   (&INDX GT &HIGH).SSDA150
&CHAR1   SETC  '0123456789ABCDEF'(&INDX,1)
&CHAR1   SETC  'CCWPTR&CHAR1'
         AIF   (&SSCCW(&INDX) EQ 0).SSDA130
         DC    AL1(&CHAR1.C-IECCCWB,1)
         AGO   .SSDA120
.SSDA130 AIF   (&CCW(&INDX) EQ 0).SSDA140
         DC    AL1(&CHAR1-IECCCWB,0)
         AGO   .SSDA120
.SSDA140 DC    AL2(0)
         AGO   .SSDA120
.SSDA150 ANOP
&INDX    SETA  0
&BYTE    SETA  0
IECCCWB  DS    0H
.SSDA160 ANOP
&INDX    SETA  &INDX+1
         AIF   (&INDX GT &HIGH).SSDA270
         AIF   (&SSCCW(&INDX) EQ 0).SSDA250
&CHAR1   SETC  '0123456789ABCDEF'(&INDX,1)
&CHAR1   SETC  'CCWPTR&CHAR1.C'
&WORK2   SETA  0
         AIF   (&SSCCW(&INDX)-&SSCCW(&INDX)/2*2 NE 0).SSDA170
&WORK2   SETA  2
         AIF   (&SSCCW(&INDX)/2-&SSCCW(&INDX)/4*2 NE 0).SSDA170
&WORK2   SETA  4
         AIF   (&SSCCW(&INDX)/4-&SSCCW(&INDX)/8*2 NE 0).SSDA170
&WORK2   SETA  6
.SSDA170 AIF   (&M65MP EQ 0).SSDA180
&WORK2   SETA  &WORK2*2
.SSDA180 AIF   (&WORK2 EQ &BYTE OR &WORK2 LT &BYTE).SSDA190
         DC    AL2(0)
&BYTE    SETA  &BYTE+2
         AGO   .SSDA180
.SSDA190 ANOP
&CHAR2   SETC  '&WORK2'
&CHAR1   EQU   *-&CHAR2
&WORK3   SETA  &SSCCW(&INDX)
         AIF   (&WORK3 LT 16).SSDA200
&WORK3   SETA  &WORK3-16
.SSDA200 ANOP
&WORK4   SETA  1
         AIF   (&WORK3-&WORK3/2*2 NE 0).SSDA210
&WORK4   SETA  2
         AIF   (&WORK3/2-&WORK3/4*2 NE 0).SSDA210
&WORK4   SETA  4
         AIF   (&WORK3/4-&WORK3/8*2 NE 0).SSDA210
&WORK4   SETA  8
.SSDA210 ANOP
&CHAR1   SETC  '0123456789ABCDEF'(&INDX,1)
&CHAR2   SETC  'CDXEXXXF'(&WORK4,1)
&CHAR1   SETC  'SSCCW&CHAR1&CHAR2'
         AIF   (&M65MP EQ 0).SSDA220
         DC    AL2(&CHAR1.A-CCWRTN)
         DC    AL2(&CHAR1.B-CCWRTN)
&BYTE    SETA  &BYTE+4
         AGO   .SSDA230
.SSDA220 DC    AL2(&CHAR1-CCWRTN)
&BYTE    SETA  &BYTE+2
.SSDA230 ANOP
&WORK3   SETA  &WORK3-&WORK4
         AIF   (&WORK3 EQ 0).SSDA160
.SSDA240 ANOP
&WORK4   SETA  &WORK4*2
         AIF   (&WORK3/&WORK4-&WORK3/(&WORK4*2)*2 NE 0).SSDA200
         DC    AL2(0)
&BYTE    SETA  &BYTE+2
         AIF   (&M65MP EQ 0).SSDA240
         DC    AL2(0)
&BYTE    SETA  &BYTE+2
         AGO   .SSDA240
.SSDA250 AIF   (&CCW(&INDX) EQ 0).SSDA160
&CHAR1   SETC  '0123456789ABCDEF'(&INDX,1)
&CHAR2   SETC  'CCWPTR&CHAR1'
&CHAR1   SETC  'CCW&CHAR1'
         AIF   (&M65MP EQ 0).SSDA260
&CHAR2   DC    AL2(&CHAR1.A-CCWRTN)
         DC    AL2(&CHAR1.B-CCWRTN)
&BYTE    SETA  &BYTE+4
         AGO   .SSDA160
.SSDA260 ANOP
&CHAR2   DC    AL2(&CHAR1-CCWRTN)
&BYTE    SETA  &BYTE+2
         AGO   .SSDA160
.SSDA270 ANOP
&INDX    SETA  0
.SSDA280 ANOP
&INDX    SETA  &INDX+1
         AIF   (&INDX GT &HIGH).SSDA360
         AIF   (&SSCCW(&INDX) EQ 0).SSDA330
&WORK3   SETA  &SSCCW(&INDX)
         AIF   (&SSCCW(&INDX) LT 16).SSDA290
&WORK3   SETA  &WORK3-16
.SSDA290 ANOP
&WORK4   SETA  1
         AIF   (&WORK3-&WORK3/2*2 NE 0).SSDA300
&WORK4   SETA  2
         AIF   (&WORK3/2-&WORK3/4*2 NE 0).SSDA300
&WORK4   SETA  4
         AIF   (&WORK3/4-&WORK3/8*2 NE 0).SSDA300
&WORK4   SETA  8
.SSDA300 ANOP
&CHAR1   SETC  '0123456789ABCDEF'(&INDX,1)
&CHAR2   SETC  'CDXEXXXF'(&WORK4,1)
&CHAR2   SETC  '&CHAR1&CHAR2'
&CHAR1   SETC  'SSCCW&CHAR2'
         AIF   (&SSCCW(&INDX) LT 16).SSDA310
         CCW   X'94',0,X'70',5
.SSDA310 AIF   (&M65MP EQ 0).SSDA320
&CHAR1.A CCW   X'07',0,X'60',6
         CCW   X'1F',*+4,X'60',1
         DC    X'08000000'
         DC    AL2(UCB1-IPLPSW,C'&CHAR2')
&CHAR1   SETC  '&CHAR1.B'
         AIF   (&SSCCW(&INDX) LT 16).SSDA320
         CCW   X'94',0,X'70',5
.SSDA320 ANOP
&CHAR1   CCW   X'07',0,X'60',6
         CCW   X'1F',*+4,X'60',1
         DC    X'08000000'
         DC    AL2(UCB1-IPLPSW,C'&CHAR2')
&WORK3   SETA  &WORK3-&WORK4
         AIF   (&WORK3 EQ 0).SSDA280
         AGO   .SSDA290
.SSDA330 AIF   (&CCW(&INDX) EQ 0).SSDA280
&CHAR1   SETC  '0123456789ABCDEF'(&INDX,1)                      SA70391
&CHAR2   SETC  'CH&CHAR1'
&CHAR1   SETC  'CCW&CHAR1'
         AIF   (&CCW(&INDX) LT 16).SSDA340
         CCW   X'94',0,X'70',5
.SSDA340 AIF   (&M65MP EQ 0).SSDA350
&CHAR1.A CCW   X'07',0,X'60',6
         CCW   X'1F',*+4,X'60',1
         DC    X'08000000',CL4'&CHAR2.A'
&CHAR1   SETC  '&CHAR1.B'
&CHAR2   SETC  '&CHAR2.B'
         AIF   (&CCW(&INDX) LT 16).SSDA350
         CCW   X'94',0,X'70',5
.SSDA350 ANOP
&CHAR1   CCW   X'07',0,X'60',6
         CCW   X'1F',*+4,X'60',1
         DC    X'08000000',CL4'&CHAR2'
         AGO   .SSDA280
.SSDA360 ANOP
         MEND
