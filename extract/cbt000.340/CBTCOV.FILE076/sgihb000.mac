         MACRO
         SGIHB000 &M,&R,&N
.* APARS FIXED:
.*  @SA72226 10/31/74
.*  @SA78088 03/02/77
.*             &M = 0 = THE SYSTEM IS 'MVT'.
.*                = 1 = THE SYSTEM IS 'MFT'.
.*             &R = 1 = ACCESS METHODS ARE RESIDENT (RAM).
.*             &N = THE NUMBER OF 2740'S IN THE 'MCS' SYSTEM.
         LCLC  &C,&D
.*             CHARACTERS USED TO GENERATE THE 2740 CONTROL BLOCKS.
         LCLA  &A
.*             COUNTER FOR THE NUMBER OF 2740 CONTROL BLOCKS
.*               (DURING GENERATION).
.*       ORGANIZATION OF THIS MACRO --
.*             .DSP000 - PROLOGUE, REGISTER EQUATES, INITIALIZATION.
.*             .DSP010 - OPEN A 2740 TERMINAL.
.*             .DSP020 - A READ OPERATION IS COMPLETE.
.*             .DSP030 - A WRITE OPERATION IS COMPLETE.
.*             .DSP040 - INITIATE A WRITE TO A 2740.
.*             .DSP050 - CLOSE A 2740,
.*             .DSP060 - INITIATE A READ TO A 2740.
.*             .DSP070 - HALT I/O ON A 2740.                     A33944
.*             .DSP080 - RETURN TO THE CALLER.
.*             .DSP090 - ERROR ROUTINES.
.*             .DSP100 - EQUATES AND CONSTANTS (GENERAL).
.*             .DSP110 - TRANSLATE TABLES-- 2740-EBCDIC,EBCDIC-2740.
.*             .DSP120 - 'DCB' DSECT.
.*             .DSP130 - 'DECB' DSECT.
.*             .DSP140 - 'TCB' DSECT.
.*             .DSP150 - 'DEB' DSECT.
.*             .DSP160 - 'UCB' DSECT.
.*             .DSP170 - 'UCM' DSECT.
.*             .DSP180 - 'WQE' DSECT.
.*             .DSP190 - 'DEVICE-I/O-MODULE ENTRY' DSECT.
.*             .DSP200 - 'OUTPUT-QUEUE ENTRY' DSECT.
.*             .DSP210 - 'EVENT-INDICATOR-LIST ENTRY' DSECT.
.*             .DSP220 - INPUT MESSAGE DSECT.
.*             .DSP230 - SAVEAREA DSECT.
.*             .DSP240 - BEGINNING OF THE 2740 CONTROL BLOCKS.
.*             .DSP500 - 2740 'DCB'.
.*             .DSP600 - 2740 'DECB'.
.*             .DSP700 - 2740 'IOB'.
.*             .DSP800 - 2740 'DEB'.
.*             .DSP900 - END OF THE MACRO.
.DSP000  ANOP
 TITLE 'IEEC2740 - ''DEVICE SUPPORT PROCESSOR'' FOR THE 2740 TERMINALS'
IEEC2740 CSECT
* FUNCTION-    PERFORM THE BASIC FUNCTIONS FOR A 2740 IN A
*               'MULTIPLE CONSOLE SUPPORT' ENVIRONMENT-
*                 - OPEN
*                 - WRITE
*                 - READ
*                 - CLOSE
*
* ENTRY POINT- IEEC2740, THE FIRST EXECUTABLE INSTRUCTION
*                         IN THE ROUTINE.
*
* INPUT
* (REGISTERS)- RUCM     - ADDRESS OF THE 'UCM' BASE.
*              RUCMP    - ADDRESS OF THE 'MCS-UCM PREFIX', IGNORED.
*              RUCME    - ADDRESS OF A 2740 'UCM' ENTRY.
*              RLINK    - RETURN ADDRESS.
*              RBRANCH  - ADDRESS OF IEEC2740.
*
* OUTPUT AND
*    RESULTS-  AT OPEN TIME -
*                A 'DEB' IS ADDED TO THE 'DEB CHAIN' OF THE
*                 COMMUNICATION TASK 'TCB'.
*                THE 'UCM' ENTRY AND ITS CONTROL BLOCKS (DEB,DCB,ETC)
*                 ARE INITIALIZED FOR THE 2740.
*                THE 'EIL' (EVENT INDICATOR LIST) IS MODIFIED
*                 TO POINT TO AN 'ECB' FOR THE NEW 2740.
         AIF   ('&M' EQ '1' AND '&R' EQ '0').DSP003
*                BTAM MODULES ARE LOADED (IGG019MA, MB, AND M0).
.DSP003  ANOP
*                THE LINE TO THE 2740 IS INITIALIZED USING THE
*                 BTAM 'LOPEN' ROUTINE.
*              AT WRITE TIME -
*                IF THE LINE TO THE 2740 HAS BEEN 'PREPARED' FOR
*                 A PREVIOUS 'READ', IT IS RESET USING 'HALT I/O'.
*                A MESSAGE IS FOUND ON THE OUTPUT QUEUE, TRANSLATED
*                 TO 2740 CODE, AND SENT TO THE 2740 USING THE
*                 BTAM ROUTINE 'IGG019MA'.
*              AT READ TIME -
*                THE LINE TO THE 2740 IS 'PREPARED' FOR INPUT.
*                 WHEN THE COMPLETE MESSAGE IS RECEIVED, IT IS
*                 TRANSLATED TO 'EBCDIC' AND SENT TO THE
*                 'MASTER SCHEDULER' (VIA SVC 34).
*              AT CLOSE TIME -
*                ALL OUTSTANDING MESSAGES ARE SENT.
*                ALL OUTSTANDING REPLIES ARE RECEIVED.
*                THE 'UCM' ENTRY AND ITS CONTROL BLOCKS ARE
*                 'DE-INITIALIZED'.
*                THE 'DEB' IS REMOVED FROM THE 'DEB CHAIN' OF THE
*                 COMMUNICATION TASK'S 'TCB'.
*                THE 'UCB' IS SET SO IT CAN BE ALLOCATED
*                 TO A PROBLEM PROGRAM IF REQUESTED.
         AIF   ('&M' EQ '1' AND '&R' EQ '0').DSP005
*                THE BTAM MODULES ARE DELETED.
.DSP005  ANOP
*
* EXTERNAL
*   ROUTINES-  IECTLOPN - 'LOPEN' - TO INITIALIZE EACH LINE.
*   (MACROS)   SVC 34 - TO PASS A MESSAGE FROM A 2740 TO THE
*                        'MASTER SCHEDULER'.
*              IOS COMMON HALT I/O ROUTINE                       A33944
*              IGG019MA - THE BTAM 'READ/WRITE ROUTINE' TO
*                          READ AND WRITE MESSAGES.
*              SVC 72 - TO CALL THE CONSOLE SWITCH ROUTINE.
         AIF   ('&M' EQ '1').DSP007
*              SVC 10 - 'GETMAIN' AND 'FREEMAIN' - TO DYNAMICALLY
*                        OBTAIN BUFFERS FOR READING FROM A 2740.
.DSP007  AIF   ('&M' EQ '1' AND '&R' EQ '0').DSP009
*              SVC 8 - LOAD   (FOR THE
*              SVC 9 - DELETE   BTAM MODULES)
.DSP009  ANOP
*              ASMTRTAB - TO GENERATE TRANSLATE TABLES FOR 2740
*                          INPUT AND OUTPUT.
*              DCBD - TO GENERATE A 'DCB' DSECT.
*              IECTDECB - TO GENERATE A 'DECB' DSECT.
*              IECTDEBX - TO GENERATE A 'DEB' DSECT.
*              IEECUCM - TO GENERATE A 'UCM' DSECT.
*              IEECVMUG - TO GENERATE A 'WQE' DSECT.
*
* EXIT-        VIA THE ADDRESS IN REGISTER 'RLINK'.
*
* SUBROUTINES- OPEN A 2740        - DSP010
*              READ IS COMPLETE   - DSP020
*              WRITE IS COMPLETE  - DSP030
*              INITIATE A WRITE   - DSP040
*              CLOSE A 2740       - DSP050
*              INITIATE A READ    - DSP060
*              HALT I/O ON A 2740 - DSP070                       A33944
*              EXIT               - DSP080
*              ERROR ROUTINES     - DSP090
*
* ATTRIBUTES-  THIS ROUTINE OPERATES UNDER THE COMMUNICATION TASK,
*               IN THE SUPERVISOR STATE, AND A PROTECTION KEY OF 0.
*               IT IS SERIALLY REUSABLE.
 TITLE 'IEEC2740 - REGISTER VALUES AND INITIALIZATION '
RZERO    EQU   0 USED AS A WORK REGISTER.
RPARM    EQU   1 ADDRESS OF A PARAMETER LIST.  ALSO A WORK REGISTER.
RUCM     EQU   2                  ADDRESS OF THE 'UCM' BASE.
RUCB     EQU   3                  ADDRESS OF THE 'UCB'.
RMSG     EQU   4                  ADDRESS OF THE CURRENT MESSAGE.
RBASE    EQU   5                  BASE FOR 'IEEC2740'.
RWQE     EQU   6                  ADDRESS OF THE CURRENT 'WQE'.
RDECB    EQU   7                  ADDRESS OF THE 'DECB'.
RUCMP    EQU   7 ADDRESS OF THE 'MCS-UCM PREFIX', IGNORED.
RDCB     EQU   8                  ADDRESS OF THE 'DCB'.
HIOUCBAD EQU   8                  UCB POINTER FOR HIO ROUTINE    A33944
HIORTREG EQU   9                  HIO ROUTINE ADDRESS            A33944
RTCB     EQU   9                  ADDRESS OF THE COMMUNICATION 'TCB'.
RDEB     EQU   10                 ADDRESS OF THE 'DEB'.
RUCME    EQU   11                 ADDRESS OF THE 'UCM' ENTRY.
RLNTH    EQU   12                 LENGTH OF AN INPUT MESSAGE.
RSAVE    EQU   13 ADDRESS OF THE CURRENT 'SAVE AREA'.
LOWREG   EQU   RSAVE-1 THE REGISTER JUST BELOW THE SAVE REGISTER.
HIGHREG  EQU   RSAVE+1 THE REGISTER JUST ABOVE THE SAVE REGISTER.
RLINK    EQU   14 ADDRESS OF THE RETURN POINT OF THE CALLER.
RWORK    EQU   14 USED AS A WORK REGISTER.
RCODE    EQU   15 VALUE OF THE RETURN CODE IS IN THIS REGISTER.
RBRANCH  EQU   15 REGISTER USED TO 'BRANCH' TO THIS ROUTINE.
RTURNREG EQU   15                 HIO RETURN REGISTER            A33944
         SPACE 3
         USING SAVEAREA,RSAVE     SAVE REGISTERS
         STM   HIGHREG,LOWREG,SAVEREGS
         LR    RBASE,RBRANCH      SET UP BASES FOR -
         USING IEEC2740,RBASE      IEEC2740,
         USING UCMECB,RUCME
         USING UCMXECB,RUCM
         L     RTCB,UCMPXA         TCB,
         USING TCB,RTCB
         L     RDECB,UCMECB        DECB,
         USING IECTDECB,RDECB
         L     RDCB,UCMDCB         DCB,
         USING IHADCB,RDCB
         L     RUCB,UCMUCB         UCB,
         USING UCB,RUCB
         L     RDEB,DCBDEBAD       DEB.
         USING DEB,RDEB
         LA    RPARM,DSPSAVE      GET A NEW SAVE AREA
         ST    RPARM,SAVELOW       AND CHAIN IT TO
         ST    RSAVE,SAVEHIGH-SAVEAREA(RZERO,RPARM)
         LR    RSAVE,RPARM         THE CURRENT ONE.
         USING WQE,RWQE
.DSP010  ANOP
 TITLE 'IEEC2740 - OPEN A 2740 TERMINAL'
DSP010   EQU   *                  IS THE 'OPEN' FUNCTION TO BE
         TM    UCMSTS,UCMOPEN      PERFORMED...
         BZ    DSP020              NO - CHECK FOR OTHER FUNCTIONS.
*                                  YES - 'OPEN' THE 2740 TERMINAL.
  SPACE 2
         TM    DCBOFLGS,OPEN      IS THIS 2740 ALREADY OPEN...
         BNZ   DSP019              YES - DON'T OPEN AGAIN.
*                                  NO - PROCEED WITH OPEN.
  SPACE  2
*                                 SET THE 'DECB' BASE.
         LA    RDECB,DECB401-DCB401(RDCB)
         ST    RDECB,UCMECB
.*                                     IS THE SYSTEM 'MFT'...
.*                                      YES - DON'T GET A BUFFER.
         AIF   ('&M' EQ '1').DSP010E
*                                 GET AN I/O BUFFER.
         GETMAIN R,LV=MSGLNTH,SP=SUBPOOL
         LA    RPARM,MESSAGE-MSG(RPARM)  = A (ACTUAL MESSAGE).
         AGO   .DSP010G
.DSP010E ANOP
         LA    RPARM,UCMINPUT+(MESSAGE-MSG)  = A (ACTUAL MESSAGE).
.DSP010G ANOP
         ST    RPARM,DECAREA      = A (MESSAGE) IN THE DECB.
         LR    RPARM,RUCME        CALCULATE THE ENTRY IN THE 'EIL'
         S     RPARM,UCMVEA        WHICH GOES WITH THIS 2740.
         SR    RZERO,RZERO
         D     RZERO,UCMVEZ
         SLL   RPARM,MULT4
         A     RPARM,UCMLSTP      PUT OUR 'ECB' PTR. IN THE 'EIL'.
         USING EIL,RPARM
         TM    EILIOL,X'80'        IS THIS THE LAST PTR?
         BNO   DSP011           - NO -
         ST    RDECB,EILIOL       - YES -   PUT THE
         OI    EILIOL,X'80'         LAST INDICATOR BACK IN.
         B     DSP012
DSP011   EQU   *       NOT THE LAST EIL ENTRY.
         ST    RDECB,EILIOL
DSP012   EQU   *
*                                 INITIALIZE THE 'DEB'.
         ST    RTCB,DEBTCBAD      SET THE 'DEB TCB ADDRESS'.
         L     RPARM,TCBDEB       PUT THE 'DEB' IN THE 'DEB CHAIN'.
         ST    RDEB,TCBDEB
         IC    RZERO,DEBAMLNG
         ST    RPARM,DEBDEBAD
         STC   RZERO,DEBAMLNG
         MVC   DEBPRIOR,TCBDSP    SET THE 'DEB PRIORITY'.
         MVZ   DEBPROTG,TCBPKE    SET THE 'DEB PROTECTION KEY'.
         ST    RUCB,DEBUCBAD      SET THE 'DEB UCB ADDRESS'.
.*                                     IS THE SYSTEM 'MFT' WITHOUT
.*                                      RESIDENT ACCESS METHODS...
.*                                      YES - DON'T 'LOAD'.
         AIF   ('&M' EQ '1' AND '&R' EQ '0').DSP015
  SPACE 2
.*                                      NO - (IT IS EITHER 'MFT' W/RAM
.*                                            OR 'MVT').
         LOAD  EPLOC=MA           LOAD THE BTAM MODULES.
         IC    RPARM,DCBOFLGS     SET THE 'DCB R/W ROUTINE'.
         ST    RZERO,DCBREAD
         STC   RPARM,DCBOFLGS
*                                 INITIALIZE THE APPENDAGE ADDRESSES.
         LOAD  EPLOC=MB           LOAD THE CHANNEL-END APPENDAGE.
         L     RPARM,DEBAPPAD
         USING APPADDRS,RPARM
         ST    RZERO,DEBCEA        A( CHANNEL END APPENDAGE )
         ST    RZERO,DEBXCEA       A( ABNORMAL END APPENDAGE )
         LOAD  EPLOC=M0           LOAD THE 2740 'DEVICE I/O MODULE'.
         AGO   .DSP016
.DSP015  ANOP
         L     RZERO,M0
.DSP016  ANOP
         L     RPARM,DCBREAD      PUT THE 'DEVICE I/O MODULE' IN THE
         LA    RPARM,DEVIODIR(RPARM)
         ST    RPARM,SAVE          'DEVICE I/O DIRECTORY' (IN THE
         USING IODIRECT,RPARM      BTAM R/W ROUTINE).
DSP015   EQU   *
         TM    IODEVTP,IOFULL     IS THE DIRECTORY FULL...
         BO    DSP097              YES, ERROR                    A33946
*                                 IS THIS DIRECTORY ENTRY USED...
         NC    IODEVTP(4),IODEVTP                                A41130
         BZ    DSP016              NO - USE IT.
*                                  YES - IS THE TERMINAL
         CLC   IODEVTP,DCBDEVTP      A 2740 WITH THE CHECKING FEATURE..
         BE    DSP017               YES - USE IT AS IT IS.
         LA    RPARM,IONEXT
         B     DSP015               NO - TRY THE NEXT ENTRY.
DSP016   EQU   *                  SET UP THIS ENTRY FOR 2740
         ST    RZERO,IODEVMOD      WITH THE CHECKING FEATURE.
         MVC   IODEVTP,DCBDEVTP
DSP017   EQU   *                  SAVE THE DISPLACEMENT TO THE
         S     RPARM,SAVE          ENTRY IN 'DCBDEVTP'.
         SRL   RPARM,DIVIDE4
         STC   RPARM,DCBDEVTP
         OI    UCMATR,UCMACTIV    NOTE - THE 2740 IS ACTIVE.
*                                 NOTE -
         NI    UCMDEVC,ONES-UCMPREP      'PREPARE' IS NOT IN PROGRESS.
*                                 MAKE 'LOPEN' THINK THAT THE 'DCB'
         OI    DCBOFLGS,OPEN       IS ALREADY 'OPEN'.
*                                 READY THE LINE FOR OPERATION
         MVI   DCBIFLGS,X'00'          ZERO IFLGS FOR LOPEN      A29572
         L     RPARM,DCBIOBAD          GET IOB ADDRESS           A32451
         LA    RPARM,IOBLNTH(RPARM)    ADJUST TO BEGINNING       A32451
         MVI   96(RPARM),X'00'     INDICATE NOT OPEN'S SIO       A56797
         NI    IOBINCAM(RPARM),X'7F'   CLEAR SAD/ENABLE FAIL      M3461
         TM    IOBINCAM(RPARM),INCAMBSY    IS IOB BUSY?          A41107
         BO    DSP097                  YES, SWITCH               A41107
         OI    IOBINCAM(RPARM),INCAMBSY TURN ON BUSY             A41107
         LA    RMSG,IOBCPA(RPARM)      ADDRESS OF CHANNEL PROGRAMA41107
         ST    RMSG,IOBSTART(RPARM)    INITIALIZE IOBSTART       A41107
         MVC   0(8,RMSG),DISABLE       MOVE IN DISABLE CCW       A41107
         LA    RMSG,8(RMSG)                                      A41107
         TM    UCBTYP+3,CU0103   IS CONTROL UNIT 2701 OR 2703    A41107
         BO    DSP017A                 IF SO, DONT ISSUE SAD     A41107
         SR    RLNTH,RLNTH             CLEAR IT                  A41107
         IC    RLNTH,UCBTYP+1          SAD CODE                  A41107
         N     RLNTH,ANDMASK           CLEAR UNUSED BITS         A41107
         IC    RLNTH,SADCODE(RLNTH)    GET SAD COMMAND CODE      A41107
         STC   RLNTH,0(RMSG)           STORE IN CCW              A41107
         MVC   1(7,RMSG),CCWCONST      REMAINDER OF CCW          A41107
         LA    RMSG,8(RMSG)                                      A41107
DSP017A  MVC   0(8,RMSG),ENABLE        MOVE IN ENABLE            A41107
         ST    RDECB,IOBECB(RPARM)     INCERT ECB ADDR IN IOB    A41107
         NI    DECSDECB,ONES-X'7E'     CLEAR ECB                 A46828
         EXCP  (1)                                               A41107
         STIMER WAIT,BINTVL=TIME       WAIT FIVE SECONDS         A41107
         LR    RWQE,RUCME              SAVE THIS UCME ADDRESS    A41107
         LM    RZERO,RPARM,UCMVEZ      BXLE REGS                 A41107
         L     RUCME,UCMVEA            FIRST UCME                A41107
DSP017A1 TM    UCMDISP,UCMDISPC        GRAPHICS CONSOLE          A41107
         BZ    DSP017B                 NO, CHECK NEXT ENTRY      A41107
* ALGORITHM TO FIND IEECVDCC, WHICH CONTAINS DIDOCS TIMER EXIT   A41107
         L     RLNTH,UCMXB             DCM POINTER               A41107
         L     RLNTH,0(RLNTH)          THESE POINTERS            A70060
         SH    RLNTH,HALF8             ARE IN DIDOCS             A70060
         L     RLNTH,0(RLNTH)          CONTROL BLOCKS            A70060
         L     RLNTH,8(RLNTH)          ADDRESS OF IEECVDCC       A70060
* IF DIDOCS WAS USING THE TIMER, WE WILL BALR TO HIS TIMER EXIT ROUTINE
         TM    DCCFLG(RLNTH),TIMERACT  WAS DIDOCS USING THE TIMERA41107
         BZ    DSP017C                 NO, CONTINUE OPEN         A41107
         LA    RBRANCH,EXIT(RLNTH)     DIDOCS TIMER EXIT ROUTINE A41107
         BALR  RLINK,RBRANCH           GO TO DIDOCS EXIT ROUTINE A41107
         B     DSP017C                                           A41107
DSP017B  BXLE  RUCME,RZERO,DSP017A1    TRY NEXT ENTRY            A41107
DSP017C  LR    RUCME,RWQE              RESTORE OUR UCME POINTER  A41107
*                                 NOTE -
         NI    UCMSTS,ONES-UCMBUSY       THE 2740 IS NOT BUSY.
         TM    DECSDECB,X'7E'          GOOD COMPLETION           A46828
         BNO   DSP097                  NO, SWITCH                A46828
DSP019   EQU   *                  NOTE - THE 'OPEN' FUNCTION HAS BEEN
         NI    UCMSTS,ONES-UCMOPEN PERFORMED.
         NI    UCMDEVC,ONES-UCMIOCMP       TURN EM OFF
         OI    UCMDEVC,HAIO+UCMPREP    INDICATE OPEN             A32449
         MVI   DECTYPE+1,ONES .        INDICATE BREAK            A33946
         B     DSP080              RETURN.....
.DSP020  ANOP
 TITLE 'IEEC2740 - I/O COMPLETION FOR A ''READ'''
DSP020   EQU   *
         L     RMSG,DECAREA       GET THE A ('OUR' MESSAGE AREA).
         SH    RMSG,HALF4
         USING MSG,RMSG
         TM    UCMDEVC,UCMIOCMP   HAS AN I/O OPERATION JUST COMPLETED..
         BZ    DSP040              NO - CHECK FOR OTHER FUNCTIONS.
*                                  YES - SEE IF THE I/O = READ OR
*                                        WRITE.
  SPACE 2
*                                 NOTE -
         NI    UCMSTS,ONES-UCMBUSY       THE 2740 IS NOT BUSY.
         NI    UCMDEVC,ONES-UCMIOCMP .  TURN OFF I/O COMPLETE    A32449
         TM    WORKFLAG,TRMRESET .     IOCMP ON RESET MESSAGE    A33946
         BO    DSP093 .                YES                       A33946
         TM    UCMDEVC,UCMPREP+HAIO .  IS IT I/O FROM A WRITE?   A32449
         BZ    DSP030 .                -- YES --                 A32449
         BO    DSP021                  IF LOPEN'S ENABLE WAS LASTA32449
*              OPERATION, SEE  IF OUTPUT PENDING                 A32449
         TM    UCMDEVC,UCMPREP .       IS IT I/O FROM READ?      A32449
         BO    DSP022 .                -- YES --                 A32449
DSP021   EQU   *                                                 A32449
         NI    UCMDEVC,ONES-HAIO-UCMPREP  TURN EM OFF            A32449
         B     DSP040 .                GO TO DA OTHER FUNCTIONS  A32449
         SPACE 2                                                 A32449
DSP022   EQU   *  -- A 'READ' OPERATION JUST COMPLETED --        A32449
         NI    UCMDEVC,ONES-UCMPREP      'PREPARE' IS NOT IN PROGRESS.
         TM    DECSDECB,X'7E'          GOOD COMPLETION           A46828
         BNO   DSP090                  NO, SWITCH                A46828
*                                  '7F' - ALL IS OK.
         NI    WORKFLAG,ONES-BIDHIT-RETRIED                      A33946
  SPACE 2
*                                 TRANSLATE THE INPUT MESSAGE FROM 2740
         TR    MESSAGE,IECTRC40    TO EBCDIC.
*                                 PUT AN 'EOB' AT THE END OF THE BUFFER
         MVI   MESSAGE+L'MESSAGE-ONE,EOB  JUST IN CASE ONE IS MISSING.
         LA    RPARM,MESSAGE      FIND THE MESSAGE LENGTH...
         LR    RZERO,RPARM
DSP024   EQU   *                  EDIT THE MSG (FOR 'BACKSPACE' AND
*                                   'CANCEL').
         CLI   ZERO(RPARM),EOT         EOT TAKEN AS CANCEL       A32450
         BE    DSP040                                            A32450
         CLI   ZERO(RPARM),BSP    IS THE CURRENT CHARACTER 'BSP'...
         BNE   DSP025              NO--CHECK FOR 'EOB'.
         CLI   ONE(RPARM),RTRN     YES--IS IT FOR THE CANCEL FCTN..
         BE    DSP040                  CHECK FOR OUTPUT          A32449
         LR    RWORK,RPARM          NO - 'BACKSPACE' THE MSG(2 SPACES).
         BCTR  RWORK,ZERO            *
         CR    RWORK,RZERO             HAVE WE BACKSPACED TO     A32448
*              BEGINNING OF MESSAGE AREA?                        A32448
         BL    DSP040                  YES, CANCEL MESSAGE       A32448
         SR    RPARM,RZERO        CALCULATE THE
         LNR   RPARM,RPARM         'REMAINING BUFFER LENGTH'.
         LA    RPARM,PARTLNTH(RPARM)
         EX    RPARM,DSP02MVC      (BACKSPACE 2 SPACES)
         LR    RPARM,RWORK        RESET THE 'SCAN' POINTER.
         B     DSP024             LOOK AT THE NEXT CHARACTER.
DSP02MVC MVC   ZERO(ONE,RWORK),TWO(RWORK)
DSP025   EQU   *
         CLI   ZERO(RPARM),EOB    ARE WE AT THE END OF MSG...
         BE    DSP026              YES - 'EOB' IS REACHED.
         CLI   ZERO(RPARM),RTRN    IS THE CHAR CARRIAGE RETURN
         BNE   DSP025F             NO.
         MVI   ZERO(RPARM),C' '       YES--CHANGE IT TO BLANK
DSP025F  EQU   *
         LA    RPARM,ONE(RPARM)    NO -
         B     DSP024             LOOK AT THE NEXT CHARACTER.
DSP026   EQU   *                  CALCULATE THE
         MVI   0(RPARM),BLANK          OF SIGNIFICANT            A29171
         SR    RPARM,RZERO         TOTAL LENGTH
         LA    RPARM,WORDLNTH(RPARM) OF THE MESSAGE.
         STH   RPARM,MESSLNTH     PREPARE THE MSG FOR THE
         MVC   MESSFLGS,MCSFLGS    MASTER SCHEDULER.
         SR    RZERO,RZERO        GIVE
         IC    RZERO,UCMID         THE MESSAGE
         LR    RPARM,RMSG          TO THE
         SVC   MASTSCH             MASTER SCHEDULER.
*                                 NOTE -
         B     DSP041             CONTINUE TERMINAL OPERATIONS.
.DSP030  ANOP
 TITLE 'IEEC2740 - I/O COMPLETION FOR A ''WRITE'''
DSP030   EQU   *  A 'WRITE' HAS JUST COMPLETED.
         L     RPARM,UCMWLAST      YES - ANALYZE THE COMPLETION.
         ST    RPARM,UCMXB         RESTORE UCMXB               @SA72226
         USING OUTQUE,RPARM
         TM    DECSDECB,X'7E'          GOOD COMPLETION           A46828
         BNO   DSP095                  NO, SWITCH                A46828
         NI    WORKFLAG,ONES-RETRIED-WREOT                       A33946
         CLI   DECTYPE+1,WRRESET       COMPLETE ON WR EOT?       A33946
         BNE   DSP035                  NO, SET WQE USED          A33946
         MVI   DECTYPE+1,ONES          SET BREAK                 A33946
         B     DSP041                  CONTINUE OPERATIONS       A33946
  SPACE 2
DSP035   EQU   *                  NOTE - THE BUFFER JUST WRITTEN IS
         TM    OUTQSTAT,MLWTO      WAS WRITE PERFORMED ON MLWTO   21002
         BO    DSP035A             YES                            21002
DSP0351  EQU   *                                                  21002
         OI    OUTQSTAT,WQEUSED    NO LONGER NEEDED.
*                                 NOTE - A WQE HAS JUST BEEN SET AS
         OI    UCMSTS,UCMTB      NO LONGER NEEDED
*                                 NOTE -
DSP0352  EQU   *                                                  21002
         CLI   DECRESPN+1,SPACE .      SPACE PRECEDING CIRCLE Y  A33945
*                                      INDICATES OPERATOR        A33945
*                                      REQUESTS A READ           A33945
         BNE   DSP041                  WRITE ANOTHER             A33945
         OI    WORKFLAG,BIDHIT     OPER WANTS IN                 A33945
         B     DSP060                                            A33945
DSP035A  EQU   *                                                  21002
         NC    UCMMLAST(FOUR),UCMMLAST   MINOR JUST WRITTEN       21002
         BNZ   DSP035C             YES                            21002
         L     RWQE,OUTQWQE       MAJOR POINTER                   21002
         TM    WMJMLTYP,WMJMLTYD   END INDICATOR IN MAJOR         21002
         BO    DSP035F             YES, SET UP FOR PURGE          21002
         TM    WMJMMLW,WMJMMLWF                                  A57895
         BO    DSP035G            EXIT                           A57895
DSP035B  EQU   *                                                  21002
         L     RWQE,OUTQWQE        MAJOR POINTER                 A60834
         TM    WMJMMLW,WMJMMLWH    ANY MINOR TEXT EXISTS          21002
         BO    DSP035G             NO                             21002
         NI    OUTQSTAT,ONES-WQEREORD  STARTING NEW MINOR CHAIN   21002
         MVC   UCMMLAST(FOUR),WMJMMIN   NEXT LINE TO BE WRITTEN   21002
         B     DSP0352            CHECK ATTN                      21002
DSP035C  EQU   *                                                  21002
         L     RWQE,UCMMLAST       POINT TO MINOR WRITTEN         21002
         SR    RLNTH,RLNTH         CLEAR REG                      21002
         IC    RLNTH,WMNMUC1       GET USE COUNT                  21002
         BCTR  RLNTH,RZERO         REDUCE BY ONE                  21002
         STC   RLNTH,WMNMUC1       STORE USE COUNT                21002
         LTR   RLNTH,RLNTH         USE COUNT                      21002
         BNZ   DSP035D             NO                             21002
         L     RLNTH,OUTQWQE      MAJOR POINTER                   21002
         OI    FOUR(RLNTH),MLWTOSR   TELL DEQ TO CHECK CHAIN      21002
         OI    UCMSTS,UCMTB        TELL DEQ TO CHECK MY QUEUE     21002
DSP035D  EQU   *                                                  21002
         TM    WMNMLT1,WMNMLT1D    END INDICATOR IN THIS LINE     21002
         BO    DSP035F             YES, PURGE                     21002
DSP035D1 EQU   *                                                  21002
         NC    WMNMNX1(THREE),WMNMNX1   NEXT LINE EXIST           21002
         BZ    DSP035G             NO                             21002
         L     RWORK,WMNMUC1       GET ADDR OF NEXT MINOR        A59270
         LA    RWORK,ZERO(RWORK)   CLEAR HIGH BYTE               A59270
         NC    FIVE(TWO,RWORK),FIVE(RWORK)  LINE TYPE FILLED IN  A59270
         BZ    DSP035G             NO, WAIT FOR IT               A59270
         MVC   UCMMLAST+ONE(THREE),WMNMNX1   NEXT LINE TO WRITE   21002
         B     DSP0352            CHECK ATTN                      21002
DSP035F  EQU   *                                                  21002
         NI    UCMSTS,ONES-UCMTC   NO LONGER WORKING              21002
         B     DSP0351             COMPLETE PURGE                 21002
DSP035G  EQU   *                                                  21002
         NI    UCMSTS,ONES-UCMPF   NO MORE OUTPUT UNTIL WE RECEIVE21002
*                                  ANOTHER MLWTO LINE             21002
         OI    UCMSDS5,UCMSDS5A    WAITING ON AN MLWTO            21002
         B     DSP060             PUT UP READ                     21002
DSP035H  EQU   *                                                  21002
         L     RPARM,UCMWLAST     CURRENT OUTQ ENTRY              21002
         NI    UCMSDS5,ONES-UCMSDS5A   TURN OFF WAITING BIT       21002
         NC    UCMMLAST(FOUR),UCMMLAST   WAS LAST WRITE FOR MINOR 21002
         BZ    DSP035I             NO                             21002
         TM    OUTQSTAT,WQEREORD   HAS MINOR CHAIN BEEN REORDERED 21002
         BO    DSP035B     YES, START WITH FIRST MINOR            21002
         L     RWQE,UCMMLAST       BASE WQE                       21002
         B     DSP035D1            GET NEW LINE PTR               21002
DSP035I  EQU   *                                                  21002
         L     RWQE,OUTQWQE       CURRENT WQE                     21002
         B     DSP035B             GET NEXT LINE                  21002
MOVETXT  EQU   *                                                  21002
         MVC   MESSAGE(ZERO),ZERO(RPARM)                          21002
.DSP040  ANOP
 TITLE 'IEEC2740 - THERE IS OUTPUT FOR A 2740'
DSP040   EQU   *
         TM    UCMSTS,UCMOUTPT    IS THERE OUTPUT PENDING...
         BZ    DSP050              NO - CHECK FOR OTHER FUNCTIONS.
  SPACE 2
         TM    UCMDEVC,UCMPREP     YES - IS 'PREPARE' IN PROGRESS...
         BO    DSP070 .                HALT PREPARE              A33944
DSP041   EQU   *                  GET SET TO ISSUE 'WRITE'.
         TM    UCMSDS5,UCMSDS5A    WERE WE WAITING ON AN MLWTO    21002
         BO    DSP035H             YES, CONTINUE WITH WRITE       21002
         TM    UCMSTS,UCMTC        WORKING ON MLWTO               21002
         BO    DSP049A             YES, WRITE LINE                21002
         L     RPARM,UCMOUTQ      IS THERE ANYTHING
         LTR   RPARM,RPARM         IN THE OUTPUT QUEUE...
         BZ    DSP050              NO - CHECK FOR 'CLOSE'.
DSP043   EQU   *
         TM    OUTQSTAT,WQEXISTS+WQEUSED
         BM    DSP046              YES - SET UP FOR 'WRITE'.
DSP044   EQU   *                    CHECK FOR END OF Q BLOCK.
         TM    OUTQSTAT,WQENEXT
         BO    DSP044A             END OF THIS Q BLOCK.
         BZ    DSP045              (THIS IS A NULL ENTRY)
         NI    UCMSTS,ONES-UCMOUTPT TURN OFF OUTPUT PENDING.
         B     DSP050              CHECK FOR 'CLOSE'...
DSP044A  EQU   *                   GO TO NEXT Q BLOCK..
         L     RPARM,OUTQWQE
         B     DSP043             TRY THE NEXT ENTRY.
DSP045   EQU   *
         LA    RPARM,OUTQNEXT
         B     DSP043             TRY THE NEXT ENTRY.
DSP046   EQU   *                  SELECT A MESSAGE FROM THE
         L     RWQE,OUTQWQE                OUTPUT QUEUE.
         TM    WQEXA,WQEPURGE     IS THIS MSG TO BE PURGED...
         BO    DSP049              YES - DON'T WRITE IT.
         ST    RPARM,UCMXB     SAVE THAT ENTRY POINTER FOR MSG.
         TM    OUTQSTAT,MLWTO      IS THIS ENTRY AN MLWTO         21002
         BO    DSP049A             YES, GO WRITE IT               21002
         MVC   MESSAGE,WQETXT     PUT THE MSG IN 'OUR' AREA.
         TR    MESSAGE,MODTABLE        TRANSLATE EBCDIC TO 2740  A29571
         L     RLNTH,WQENBR       PUT AT THE END OF THE MESSAGE-
DSP0461  EQU   *                                                  21002
         LA    RLNTH,0(RLNTH)                                    A35756
         LA    RPARM,130               MAX CHARS 130             A35756
         CR    RPARM,RLNTH             IF MSG LESS THAN 130      A35756
         BH    DSP046A                    BRANCH                 A35756
         LR    RLNTH,RPARM             MSG GREATER THAN 130      A35756
DSP046A  EQU   *                                                 A35756
         LA    RPARM,MESSAGE(RLNTH)    'RETURN-EOB'.
         MVC   ZERO(L'RETRNEOB,RPARM),RETRNEOB
*                                 'WRITE' THE MESSAGE TO THE 2740.
         TM    DECTYPE+1,RST .         IS BREAK NECESSARY?       A33946
         BO    DSP047A                 CHECK UCB                   00MC
         MVI   DECTYPE+1,WRCONT .      NO, WRITE CONTINUE        A33946
         B     DSP047C                                           A33946
DSP047A  CLI   UCBTYP,T2740B           DO WE HAVE BREAK?           00MC
         BE    DSP047B                 YES, SET UP WR BREAK        00MC
         MVI   DECTYPE+1,WRINIT        NO, SET UP WR INITIAL       00MC
         B     DSP047C                                             00MC
DSP047B  MVI   DECTYPE+1,WRBREAK .     WRITE BREAK               A33946
DSP047C  WRITE (RDECB),T,MF=E                                    A33946
         LTR   RCODE,RCODE .       CHECK RETURN CODE FROM        A33946
         BNZ   DSP097 .            READ/WRITE                    A33946
*                                  SWITCH IF BAD                 A33946
*                                  ZERO - 'WRITE' IS STARTED OK.
  SPACE 2
         OI    UCMSTS,UCMBUSY .        THE 2740 IS BUSY          A33946
         TM    WORKFLAG,TRMRESET+WREOT AVOID STORING WQE ADDR?   A33946
         BC    7,DSP080                YES, DIDN'T USE WQE       A33946
         L     RPARM,UCMXB       GET  D  C  M  ADDRESS
         ST    RPARM,UCMWLAST   SAVE CURRENT MSG. POINTER ENTRY
         XC    UCMXB(FOUR),UCMXB  ZERO DCM FIELD                 A68904
         B     DSP080             RETURN.
DSP049   EQU   *
         OI    UCMSTS,UCMTB            NOTE - WQE NO LONGER NEEDED
*                                 NOTE - THIS ENTRY IN THE OUTPUT
         OI    OUTQSTAT,WQEUSED         QUEUE IS NO-LONGER-NEEDED.
         B     DSP044              CHECK FOR END OF Q BLOCK
DSP049A  EQU   *                   COULD BE CONTINUATION OF       21002
*                                  PREVIOUS WRITE OR START OF     21002
*                                  NEW WRITE.                     21002
         SR    RLNTH,RLNTH        CLEAR IT                        21002
         TM    UCMSTS,UCMTC        WORKING ON MLWTO               21002
         BO    DSP049D             YES, OLD WRITE                 21002
         XC    UCMMLAST(FOUR),UCMMLAST ZERO LAST MINOR POINTER    21002
         OI    UCMSTS,UCMTC        NOW WORKING                    21002
         IC    RLNTH,WMJMTXTL+ONE  GET TEXT LENGTH                21002
         TM    OUTQSTAT,MLWTOHC    MLWTO FOR HARDCOPY             21002
         BO    DSP049C             YES, INCLUDE HARDCOPY TEXT     21002
         BCTR  RLNTH,RZERO         REDUCE FOR EXECUTE             21002
         LA    RPARM,WMJMTXT       POINT TO TEXT                  21002
DSP049B  EQU   *                                                  21002
         EX    RLNTH,MOVETXT       MOVE TEXT TO MESSAGE           21002
         LA    RLNTH,ONE(RLNTH)    RESTORE LENGTH                 21002
         TR    MESSAGE,MODTABLE    TRANSLATE EBCDIC TO 2740       21002
         B     DSP0461            COMPLETE SET UP                 21002
DSP049C  EQU   *                                                  21002
         LA    RLNTH,ELEVEN(RLNTH)   ADD TO LENGTH FOR HCTXT      21002
         LA    RPARM,WMJMTS        POINT TO TEXT                  21002
         B     DSP049B             COMPLETE SET UP                21002
DSP049D  EQU   *                                                  21002
         L     RPARM,UCMWLAST     CURRENT OUTQ ENTRY              21002
         L     RWQE,UCMMLAST       POINT MINOR HALF               21002
         NC    WMNMTL1(1),WMNMTL1  IS THE LENGTH ZERO            A55358
         BZ    DSP035C             YES, GO PURGE                 A55358
         IC    RLNTH,WMNMTL1       GET LENGTH                     21002
         TM    OUTQSTAT,MLWTOHC    MLWTO FOR HARDCOPY             21002
         BO    DSP049E             YES                            21002
         BCTR  RLNTH,RZERO         REDUCE FOR EXECUTE             21002
         LA    RPARM,WMNMTXT1      POINT TO TEXT                  21002
         B     DSP049B             COMPLETE SET UP                21002
DSP049E  EQU   *                                                  21002
         LA    RPARM,WMNMHCT1     TEXT POINTER                    21002
         LA    RLNTH,THREE(RLNTH)  ADD TO TEXT LENGTH FOR HCTXT   21002
         B     DSP049B             COMPLETE SET UP                21002
.DSP050  ANOP
 TITLE 'IEEC2740 - CLOSE A 2740 TERMINAL'
DSP050   EQU   *                  IS THE 'CLOSE' FUNCTION TO BE
         TM    UCMSTS,UCMCLOSE     PERFORMED...
         BZ    DSP060              NO - SET UP TO 'READ'.
         TM    UCMDEVC,UCMPREP .       HIO NECESSARY?            A33944
         BO    DSP070 .                YES                       A33944
         L     RMSG,DECAREA        GET ADDRESS
         SH    RMSG,HALF4          OF MESSAGE AREA..
         TM    UCBFL1,UCBPOST          OPERATION IN PROGRESS     A38559
         BO    DSP080                  YES, INTERRUPT EXPECTED   A38559
         L     RPARM,DCBIOBAD          IOB ADDRESS               A38559
         LA    RPARM,IOBLNTH(RPARM)    ADJUST TO BEGINNING       A38559
         TM    IOBFL1(RPARM),IOBSENSE  ERP IN CONTROL            A38559
         BO    DSP080                  YES, ALLOW TO FINISH      A38559
         L     RPARM,UCMOUTQ      ARE THERE ANY MSGS OR REPLIES
         LTR   RPARM,RPARM          OUTSTANDING...
         BZ    DSP054              NO - 'CLOSE' THE 2740.
DSP051   EQU   *
         TM    OUTQSTAT,WQEXISTS+WQEUSED
         BM    DSP046              YES -(MSG)- SET UP TO WRITE.
         TM    OUTQSTAT,WQENEXT
         BM    DSP054              NO - 'CLOSE' THE 2740.
*                                  (THIS IS A POINTER TO AN
         BO    DSP053               ADDITIONAL ENTRY).
*                                  (THIS IS A NULL ENTRY).
         LA    RPARM,OUTQNEXT
         B     DSP051             TRY THE NEXT ENTRY.
DSP053   EQU   *
         L     RPARM,OUTQWQE
         B     DSP051             TRY THE NEXT ENTRY.
DSP054   EQU   *                  'CLOSE' THE 2740.
*                                 NOTE -
         NI    UCMATR,ONES-UCMACTIV    THIS 2740 IS INACTIVE..    P4789
         XC    UCMECB,UCMECB      RESET THE 'UCM' ENTRY CNTL BLKS.
         LR    RPARM,RUCME        RESET THE 'ECB' PTR. IN THE 'EIL'.
         S     RPARM,UCMVEA
         SR    RZERO,RZERO
         D     RZERO,UCMVEZ
         SLL   RPARM,MULT4
         A     RPARM,UCMLSTP
         USING EIL,RPARM
         TM    EILIOL,X'80'        IS THIS THE LAST PTR?
         BNO   DSP055A       - NO -
         ST    RUCME,EILIOL       - YES -   PUT THE
         OI    EILIOL,X'80'         LAST INDICATOR BACK IN.
         B     DSP055B
DSP055A  EQU   *       NOT THE LAST EIL ENTRY.
         ST    RUCME,EILIOL
DSP055B  EQU   *
.*                                     IS THE SYSTEM 'MFT'...
.*                                      YES - DON'T FREE A BUFFER.
         AIF   ('&M' EQ '1').DSP055
*                                 FREE THE I/O BUFFER.
         L     RMSG,DECAREA                                      A43791
         LTR   RMSG,RMSG            MSG ALREADY FREED?           A66401
         BZ    DSP055E            DO ATTEMPT TO FREE             A66401
         SH    RMSG,HALF4                                        A43791
         FREEMAIN R,LV=MSGLNTH,SP=SUBPOOL,A=(RMSG)
.DSP055  ANOP
DSP055E  TM    SRTESTAT,X'40'     DEVICE GOING TO OFFLINE?       A66401
         BNO   DSP055C                 - NO -                     P4789
         MVI   SRTESTAT,X'00'          YES - ZERO UCB FIELD       P4789
         B     DSP055D                 CONTINUE -                 P4789
DSP055C  MVI   SRTESTAT,X'80'          TURN ONLINE BIT IN UCB..   P4789
DSP055D  EQU   *                                                  P4789
.*                                     IS THE SYSTEM 'MFT' WITHOUT
.*                                      RESIDENT ACCESS METHODS...
.*                                      YES - DON'T 'DELETE'.
         AIF   ('&M' EQ '1' AND '&R' EQ '0').DSP057
.*                                      NO - (IT IS 'MFT' W/'RAM' OR
.*                                            'MVT').
         DELETE EPLOC=MA          'DELETE' THE BTAM ROUTINES.
         DELETE EPLOC=MB
         DELETE EPLOC=M0
.DSP057  ANOP
         LA    RDEB,DEB           REMOVE THE 'DEB' FROM THE DEB CHAIN.
         LA    RPARM,((TCBDEB-TCB)-(DEBDEBAD-DEB))(RTCB)
         DROP  RDEB
         USING DEB,RPARM
DSP056   EQU   *
         LR    RZERO,RPARM
         L     RPARM,DEBDEBAD
         LA    RPARM,DEB
         CR    RPARM,RDEB         IS THIS 'OUR DEB'...
         BNE   DSP056              NO - TRY THE NEXT DEB.
         LR    RPARM,RZERO         YES - REMOVE IT.
         MVC   DEBDEBAD,(DEBDEBAD-DEB)(RDEB)
         DROP  RPARM
         USING DEB,RDEB
         MVI   DCBDEVTP,DEVTP40   RESET THE DCB 'DCBDEVTP' FIELD.
         NI    DCBOFLGS,ONES-OPEN NOTE - THE 'CLOSE' FUNCTION HAS
         NI    UCMSTS,ONES-UCMCLOSE-UCMBUSY   CLOSE PERFORMED,   A30884
*              BUSY OFF                                          A30884
         B     DSP076                                            A42504
.DSP060  ANOP
 TITLE 'IEEC2740 - PREPARE FOR INPUT FROM A 2740'
DSP060   EQU   *
         MVI   MESSAGE,X'01'           BLANK MESSAGE AREA PRIOR  A30758
         MVC   MESSAGE+1(139),MESSAGE     TO READ                A30758
         CLI   UCBTYP,T2740B           DO WE HAVE BREAK?           00MC
         BNE   DSP062                  NO, ISSUE RD INITIAL        00MC
         TM    DECTYPE+1,RST .         IS BREAK NECESSARY        A33946
         BO    DSP063 .                YES                       A33946
         TM    WORKFLAG,BIDHIT         OPERATOR WANTS IN         A33946
         BO    DSP062                  YES, DON'T USE BREAK      A33946
         MVI   DECTYPE+1,WRRESET       WRITE EOT OP TYPE         A33946
         OI    WORKFLAG,WREOT          WE ARE WRITING EOT        A33946
         B     DSP047C                 GO TO ISSUE WRITE         A33946
DSP062   EQU   *                                                 A33946
         MVI   DECTYPE+1,RDINIT        READ I ITIAL              A33946
         B     DSP064                                            A33946
DSP063   MVI   DECTYPE+1,RDBREAK       READ INITIAL BREAK        A33946
DSP064   READ  (RDECB),T,MF=E                                    A33946
         LTR   RCODE,RCODE .       CHECK RETURN CODE FROM        A33946
         BNZ   DSP097 .            READ/WRITE                    A33946
*                                  SWITCH IF BAD                 A33946
*                                  ZERO - 'PREPARE-READ' IS STARTED OK.
  SPACE 2
         OI    UCMSTS,UCMBUSY     NOTE - THE 2740 IS BUSY.
         OI    UCMDEVC,UCMPREP    NOTE - 'PREPARE' IS IN PROGRESS.
         TM    WORKFLAG,BIDHIT         OPERATOR WANTS IN?        A33945
         BZ    DSP080                  NO                        A33945
         MVI   DECRESPN,ZERO .         YES, BLOCK WRITE          A33945
         B     DSP080                                            A33945
.DSP070  ANOP                                                    A33944
 TITLE 'IEEC2740 - HALT I/O ROUTINE'                             A33944
DSP070   EQU   *                                                 A33944
         CLI   DECRESPN,ZERO  -YES-  DATA BEING RECEIVED..       A33944
         BE    DSP080    -YES-  RETURN..                         A33944
         CLI   UCBID,ONES .       VALID UCB?                     A33944
         BNE   DSP097 .           NO, SWITCH                     A33944
         TM    UCBFL1,UCBPOST .   HIO NECESSARY?                 A33944
         BZ    DSP079                  NO, PURGE                 A46828
         STM   RUCMP,RBRANCH,DSPSAVE+12 SAVE NON-TRANSPARENT REGSA33944
         IC    RUCMP,UCBFL1 .     SAVE UCB FLAGS                 A33944
         L     HIORTREG,SIXTEEN . GET CVT POINTER                A33944
         L     HIORTREG,IXAVL(HIORTREG) GET IOS CONSTANTS ADDR   A33944
         L     HIORTREG,HIORTN(HIORTREG) GET HIO ROUTINE ADDR    A33944
         LR    HIOUCBAD,RUCB .    PUT UCB POINTER IN REG 8       A33944
         BALR  RTURNREG,HIORTREG GO TO COMMON HIO ROUTINE        A33944
         STC   RUCMP,UCBFL1 .     RESTORE UCB FLAGS              A33944
         LM    RUCMP,RBRANCH,DSPSAVE+12 RESTORE THEM             A33944
         L     RPARM,DCBIOBAD   IOB ADDRESS                      A46845
         LA    RPARM,IOBLNTH(RPARM) ADJUST TO BEGINNING          A46845
         TM    12(RPARM),X'0C'  STATUS CE,DE                     A46845
         BNO   DSP075         NO,INTERRUPT PENDING               A46845
         L     RPARM,8(RPARM)   ENDING CCW                       A46845
         LA    RPARM,0(RPARM)   ZAP HIGH BYTE                    A46845
         SH    RPARM,HALF8    START OF CCW                       A46845
         CLI   0(RPARM),X'06'  IS CCW PREPARE                    A46845
         BNE   DSP079     NO,PURGE RQE                           A46845
DSP075   EQU   *                                                 A33944
*                 -- PREPARE HAS BEEN REMOVED --                 A33944
         NI    UCMDEVC,ONES-UCMPREP .   TURN OFF PREPARE         A33944
         OI    UCMSTS,UCMBUSY .        DEVICE IS BUSY            A33944
         OI    UCMDEVC,HAIO .          INDICATE HALT I/O IN PROC A33944
DSP076   EQU   *                                                 A42504
         L     RPARM,DCBIOBAD         GET IOB ADDRESS            A46828
         LA    RPARM,IOBLNTH(RPARM)    ADJUST TO BEGINNING       A46828
         MVI   IOBECB(RPARM),IOSPEC    INSERT HIO COMPLETION     A46828
         NI    IOBINCAM(RPARM),ONES-INCAMBSY  IOB BUSY BIT OFF   A30759
         NI    IOBFL1(RPARM),ONES-IOBSENSE-IOBEX  TURN OFF FLGS  A30759
         MVI   DECTYPE+1,ONES .        INDICATE BREAK            A33948
         B     DSP080                                            A46828
DSP079   EQU   *                                                 A46828
*  RQE IS ENQUEUED, BUT SIO HAS NOT BEEN ISSUED.  WE MUST PURGE. A46828
         LA    RPARM,36(RDEB)          PURGE PARM LIST           A46828
         ST    RDEB,0(RPARM)           DEB ADDRESS TO PARM LIST  A46828
         MVI   0(RPARM),X'E0'          PURGE OPTIONS             A46828
         ST    RTCB,4(RPARM)           TCB ADDRESS TO PARM LIST  A46828
         LA    RLNTH,DEBUSRPG          PURGE IOB CHAIN POINTER   A46828
         ST    RLNTH,8(RPARM)          STORE IT                  A46828
         SVC   16                                                A46828
         B     DSP075                                            A46828
.DSP080  ANOP
 TITLE 'IEEC2740 - RETURN ROUTINE'
DSP080   EQU   *                  RETURN
         L     RSAVE,SAVEHIGH
         LM    HIGHREG,LOWREG,SAVEREGS
RETURN   EQU   *
         BR    RLINK               VIA THE ADDRESS IN 'RLINK'.
.DSP090  ANOP
 TITLE 'IEEC2740 - ERROR ROUTINES'
DSP090 EQU     *                                                 A33946
         CLI   UCBTYP,T2740B           DO WE HAVE BREAK            00MC
         BNE   DSP097                  NO, SWITCH                  00MC
         TM    DECSENS0,TIMEOUT .      READ TIMEOUT              A33946
         BNO   DSP095 .            NO, CHECK RETRY               A33946
         MVC   MESSAGE(34),TOERMSG  MOVE IN TIMEOUT MESSAGE      A33946
         OI    WORKFLAG,TRMRESET                                 A33946
         B     DSP047B .           GO TO WRITE IT                A33946
DSP093   NI    WORKFLAG,ONES-TRMRESET  AFGER IOCMPLT ON WRITE    A33946
         TM    DECSDECB,X'7E'          GOOD COMPLETION           A46828
         BNO   DSP097                  NO, SWITCH                A46828
         B     DSP040                                            A33946
DSP095   EQU   *
         CLI   UCBTYP,T2740B           DO WE HAVE BREAK            00MC
         BNE   DSP097                  NO, SWITCH                  00MC
         TM    DECSENS0,RETRYMSK .  SHOULD WE RETRY AFTER        A33946
         BNZ   DSP097 .            THIS ERROR?                   A33946
         TM    WORKFLAG,RETRIED .  HAVE WE RETRIED               A33946
         BO    DSP097 .            YES, SWITCH                   A33946
         MVI   DECTYPE+1,ONES .    NO, SET BREAK                 A33946
         OI    WORKFLAG,RETRIED .   AND RETRY                    A33946
         OC    UCMMLAST,UCMMLAST    IS UCMMLAST ZERO           @SA78088
         BNZ   DSP040                                          @SA78088
         NI    UCMSTS,ONES-UCMTC    TURN OFF INLINE WTO        @SA78088
         B     DSP040                                            A33946
DSP097   EQU   *                  NOTE - THE CONSOLE-SWITCH ROUTINE
         OI    UCMDEVC,UCMCONSW    IS BEING PUT IN CONTROL.
         ST    RUCME,FAILCONS     PASS CONTROL TO THE CONSOLE-
         LA    RPARM,AXL07B            SWITCH ROUTINE
         SVC   SWCONS
         NI    UCMDEVC,ONES-UCMCONSW-UCMIOCMP-UCMPREP            A42504
*                                   IS NOT IN CONTROL.
         NI    UCMSTS,ONES-UCMOPEN-UCMBUSY  INSURE NO IMMEDIATE  A30884
*              REOPEN, BUSY OFF                                  A30884
         B     DSP054              CLOSE                         A43791
.DSP100  ANOP
 TITLE 'IEEC2740 - EQUATES AND CONSTANTS'
.*                                     IS THE SYSTEM 'MFT'
.*                                      WITHOUT 'RAM'...
.*                                       YES - GENERATE V-CONSTANTS.
         AIF   ('&M' EQ '1' AND '&R' EQ '0').DSP102
.*                                       NO - IT IS 'MVT' OR 'MFT'
.*                                        WITH 'RAM', GENERATE C-CONST.
MA       DC    CL8'IGG019MA'      NAME OF BTAM R/W ROUTINE.
MB       DC    CL8'IGG019MB'      NAME OF BTAM CEA ROUTINE.
M0       DC    CL8'IGG019M0'      NAME OF BTAM DEV. I/O ROUTINE.
         AGO   .DSP104
.DSP102  ANOP
M0       DC    V(IGG019M0)        ADDRESS OF BTAM DEV.I/O ROUTINE.
.DSP104  ANOP
HALF4    DC    H'4' HALF-WORD OF 4.
HALF8    DC    H'8'                                              A41107
ANDMASK  DC    F'3'                                              A41107
TIME     DC    F'500'                                            A41107
DISABLE  DC    X'2F00000060010001'                               A41107
ENABLE   DC    X'2700000020240001'                               A41107
CCWCONST DC    X'00000060240001'                                 A41107
SADCODE  DC    X'13171B1F'                                       A41107
IOBCPA   EQU   64                                                A41107
IOBSTART EQU   16                                                A41107
DCCFLG   EQU   12                                                A41107
EXIT     EQU   18                                                A41107
TIMERACT EQU   X'80'                                             A41107
CU0103   EQU   X'02'                                             A41107
DCMDCC   EQU   X'170'                                            A41107
MULT4    EQU   2 TO MULTIPLY BY 4, SHIFT BY 2.
DIVIDE4  EQU   2  TO DIVIDE BY 4, SHIFT BY 2.
WORDLNTH EQU   4 THE LENGTH OF A FULL-WORD IN BYTES.
*                   CONSTANTS USED TO SWITCH CONSOLES.
CONSWCON DC    A(AXL07B)  =A(LOCATION OF THE NAME OF THE ROUTINE
*                                 TO EXECUTE)
         DC    A(0)  NOT USED
AXL07B   DC    CL8'IGCXL07B'  =A(NAME OF THE ROUTINE TO BE EXECUTED)
FAILCONS DS    F    =A(UCM ENTRY FOR THE FAILING CONSOLE).
*
RETRNEOB DC    0XL10'0'                                          A35756
         DC    AL1(RTRN2740)                                     A35756
         DC    8AL1(IDLE)                                        A35756
         DC    AL1(EOB2740)                                      A35756
IDLE     EQU   X'5E'                                             A35756
RTRN2740 EQU   X'5B'                   2740 CARR RETN-NEW LINE   A29571
EOB2740  EQU   X'3D'                   2740 EOB                  A29571
RTRN     EQU   X'15'   CODE FOR 'CARRIAGE RETURN'
BSP      EQU   X'16' CODE FOR 'BACKSPACE'.
EOB      EQU   X'26' EOB CHARACTER
EOT      EQU   X'37' CODE FOR THE 'END-OF-TRANSMISSION' CHARACTER.
IOSPEC   EQU   X'48'               SPECIAL I/O COMPLETE CONDITION
ZERO     EQU   0                  THE VALUE 0.
ONE      EQU   1                  THE VALUE 1.
TWO      EQU   2                  THE VALUE 2.
ONES     EQU   X'FF' A ONE-BYTE VALUE OF ALL ONES
SUBPOOL  EQU   255 THE 'COMMUNICATION TASK' SUBPOOL.
MASTSCH  EQU   34 SVC CODE FOR 'CALL THE MASTER SCHEDULER'.
SWCONS   EQU   72 SVC CODE FOR THE 'CONSOLE SWITCH' FUNCTION.
DSPSAVE  DS    18F  THE 'DSP' SAVEAREA.
SAVE     DS    F    SAVEAREA FOR A WORK REGISTER.
SAVELNTH EQU   72                      THE LENGTH OF THE SAVEAREA.
*                                 SENSE BYTE 1
RETRYMSK EQU   X'88'                                               00MC
IOBFL1   EQU   0                                                 A30759
IOBINCAM EQU   28                                                A30759
INCAMBSY EQU   X'40'                                             A30759
IOBSENSE EQU   X'20'                                             A30759
IOBEX    EQU   X'04'                                             A30759
HAIO     EQU   X'02' .             UCMDEVC HALT I/O INDICATOR    A32449
BLANK    EQU   X'40' .             BLANK                         A29171
RST      EQU   X'80'                                             A33946
SPACE    EQU   X'01' .                 2740 TRANSCODE SPACE      A33945
WRINIT   EQU   X'02'                                               00MC
T2740B   EQU   X'59'                                               00MC
WRCONT   EQU   X'04' .                 WR CONTINUE OP TYPE       A33946
WRBREAK  EQU   X'16' .                 WR BREAK OP TYPE          A33946
WRRESET  EQU   X'0A'                   WRITE EOT OP TYPE         A33946
RDINIT   EQU   X'81'                   READ INITIAL OP TYPE      A33946
RDBREAK  EQU   X'95'                   READ BREAK OP TYPE        A33946
TIMEOUT  EQU   X'01' .             TIMEOUT BIT IN SENSE FIELD    A33946
IOBECB   EQU   4                                                 A33944
UCBPOST  EQU   X'20'              UCBFL1 POST BIT                A33944
SIXTEEN  EQU   16                 CVT LOCATION                   A33944
IXAVL    EQU   124                OFFSET IN CVT TO IOS TABLE     A33944
HIORTN   EQU   8      OFFSET IN IOS IXAVL TABLE TO HIO ROUTINE   A33944
ELEVEN   EQU   11             LENGTH AND DISPLACEMENT             21002
THREE    EQU   3              LENGTH AND DISPLACEMENT             21002
FOUR     EQU   4              LENGTH AND DISPLACEMENT             21002
FIVE     EQU   5                   DISP OF 5                     A52790
MLWTOSR  EQU   X'02'          FLAG IN MAJOR WQE, SERVICE CHAIN    21002
*                   I E E X X X X   T I M E O U T   -            A33946
TOERMSG  DC    XL18'F3EBEB020715F301A6F3C9EBCCA9A6014001'        A33946
*                   T E R M I N A L   R E S E T                  A33946
         DC    XL16'A6EBD2C9F3CAE2C601D2EBA5EBA65B3D'            A33946
         LTORG
.DSP110  ANOP
 TITLE 'IEEC2740 - TRANSLATE TABLES -2740 TO EBCDIC, EBCDIC TO 2740'
         ASMTRTAB RC40
MODTABLE EQU   *                                                 A29571
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F                A29571
         DC    X'01011601010101010101010101010101' 0             A29571
         DC    X'01010101010101010101010101010101' 1             A29571
         DC    X'01010101010101010101010101010101' 2             A29571
         DC    X'01010101010101010101010101010188' 3             A29571
         DC    X'01010101010101010101A0768493E1B7' 4             A29571
         DC    X'61010101010101010101D757909587F6' 5             A29571
         DC    X'4023010101010101010101378BC08EA3' 6             A29571
         DC    X'010101010101010101018816208D8296' 7             A29571
         DC    X'01626467686B6D6E7073010101010101' 8             A29571
         DC    X'01434546494A4C4F5152010101010101' 9             A29571
         DC    X'01012526292A2C2F3132010101010101' A             A29571
         DC    X'01010101010101010101010101010101' B             A29571
         DC    X'01E2E4E7E8EBEDEEF0F3010101010101' C             A29571
         DC    X'01C3C5C6C9CACCCFD1D2010101010101' D             A29571
         DC    X'0101A5A6A9AAACAFB1B2010101010101' E             A29571
         DC    X'15020407080B0D0E1013010101010101' F             A29571
.DSP120  ANOP
 TITLE 'IEEC2740 - DSECT FOR A ''DCB'''
*                                 'DCB' OPEN FLAGS
OPEN     EQU   X'10'              THE DCB IS 'OPEN'.
         DCBD  DSORG=BX
WORKFLAG EQU DCBTIOT                                             A33946
TRMRESET EQU   X'01'                                             A33946
BIDHIT   EQU   X'02'                                             A33945
RETRIED  EQU   X'04'                                             A33946
WREOT    EQU   X'08'                   WRITING EOT INDICATOR     A33946
.DSP130  ANOP
 TITLE 'IEEC2740 - DSECT FOR A ''DECB'''
*                                 'ECB' I/O COMPLETION CODES
IOCOMPLT EQU   X'7F'              I/O IS COMPLETE WITHOUT ERROR.
         IECTDECB
.DSP140  ANOP
 TITLE 'IEEC2740 - DSECT FOR A ''TCB'''
TCB      DSECT
         DS    2F
TCBDEB   DS    F    A( 1ST DEB IN THE DEB CHAIN )
         DS    4F
TCBPKE   DS    AL1  TCB PROTECTION KEY.
         DS    6X
TCBDSP   DS    AL1                TCB DISPATCHING PRIORITY.
.DSP150  ANOP
 TITLE 'IEEC2740 - DSECT FOR A ''DEB'''
DEB      DSECT
*                                 BASIC SECTION
DEBNMSUB DS    0XL1               NUMBER OF SUBROUTINES LOADED BY
*                                  THE OPEN EXECUTOR ROUTINES.
DEBTCBAD DS    A                  ADDRESS OF THE TCB FOR THIS DEB.
DEBAMLNG DS    0XL1               NUMBER OF BYTES IN THE ACCESS
*                                  METHOD DEPENDENT SECTION.
DEBDEBAD DS    A                  ADDRESS OF THE NEXT DEB IN THE TASK.
*                                  (ZEROES FOR THE LAST DEB IN CHAIN)
DEBOFLGS DS    0BL1               DATA SET STATUS FLAGS.
DEBIRBAD DS    A                  IRB ADDRESS FOR APPENDAGES EXITS.
DEBOPATB DS    0BL1               EOV DISPOSITION
DEBSYSPG DS    A                  ADDRESS OF THE 1ST IOB IN THE SYSTEM
*                                  PURGE CHAIN.
DEBNMEXT DS    0XL1               NUMBER OF EXTENTS IN DSCB AND DEB.
DEBUSRPG DS    A                  ADDRESS OF 1ST IOB IN THE USER
*                                  PURGE CHAIN.
DEBPRIOR DS    0XL1               DISPATCHING PRIORITY.
DEBECBAD DS    A                  USED TO LOCATE THE PURGE ECB.
DEBPROTG DS    0BL1               BTAM PROTECTION TAG.
DEBDEBID DS    0BL1               HEX 'F' TO IDENTIFY THIS AS A DEB.
DEBDCBAD DS    A                  ADDRESS OF THE DCB ASSOCIATED
*                                  WITH THIS DEB.
DEBEXSCL DS    0XL1               EXTENT SCALE- 4 FOR DIRECT ACCESS,
*                                  2 FOR NON-DIRECT ACCESS.
*                                  USED TO DETERMINE THE SIZE OF THE
*                                  DEVICE DEPENDENT SECTION.
DEBAPPAD DS    A                  ADDRESS OF THE I/O APPENDAGE
*                                  VECTOR TABLE.
*                                 DEVICE DEPENDENT SECTION.
DEBUCBAD DS    A                  ADDRESS OF THE UCB FOR THIS DEB.
APPADDRS DSECT
DEBEOEA  DS    A                  A( END-OF-EXTENT APPENDAGE )
DEBSIOA  DS    A                  A( START I/O APPENDAGE )
DEBPCIA  DS    A                  A( PCI APPENDAGE )
DEBCEA   DS    A                  A( CHANNEL END APPENDAGE )
DEBXCEA  DS    A                  A( ABNORMAL END APPENDAGE )
.DSP160  ANOP
 TITLE 'IEEC2740 - DSECT FOR A ''UCB'''
UCB      DSECT
         DS    X
         DS    X
UCBID    DS    X                  UCB IDENTIFICATION - FF        A33944
SRTESTAT DS    X                  UCB STATUS 'A' FLAGS.
         DS    X                                                 A33944
         DS    X                                                 A33944
UCBFL1   DS    X                  UCB FLAGS                      A33944
         DS    X                                                 A33944
         DS    F                                                 A33944
         DS    X
UCBNAME  DS    CL3                UCB NAME (IN EBCDIC).
UCBTYP   DS    F                                                   00MC
.DSP170  ANOP
 TITLE 'IEEC2740 - DSECT FOR A ''UCM'''
*                                 'UCM' STATUS FLAGS
UCMOPEN  EQU   X'08'              OPEN IS PENDING.
UCMCLOSE EQU   X'10'              CLOSE IS PENDING.
UCMBUSY  EQU   X'20'              THE UCM ENTRY (2740) IS BUSY.
UCMOUTPT EQU   X'40'              OUTPUT IS PENDING.
*                                 'UCM' ATTRIBUTE FLAGS
UCMACTIV EQU   X'10'              THE UCM ENTRY (2740) IS ACTIVE.
*                                 'UCM' DEVICE CONTROL FLAGS
UCMIOCMP EQU   X'08'              AN I/O OPERATION HAS COMPLETED.
UCMCONSW EQU   X'20'  CONSOLE SWITCH ROUTINE (SVC 72) HAS CONTROL.
UCMPREP  EQU   X'40'              PREPARE IS IN PROGRESS ON THIS LINE
UCM      DSECT
         IEECUCM
.DSP180  ANOP
 TITLE 'IEEC2740 - DSECT FOR A ''WQE'''
*                                 'WQE' DISPOSITIONS
WQE      DSECT
         IEECVMUG WQE
         ORG   WQE
         IEECVMUG WQEMAJ                                          21002
         ORG   WQE
         IEECVMUG WQEMIN                                          21002
         ORG
.DSP190  ANOP
 TITLE 'IEEC2740 - DSECT FOR A ''DEVICE-I/O-MODULE'' ENTRY'
DEVIODIR EQU   12 DISPLACEMENT OF THE 'DEVICE I/O DIRECTORY' FROM THE
*                 BEGINNING OF THE 'BTAM R/W ROUTINE-IGG019MA'.
IOFULL   EQU   X'FF' INDICATOR OF THE 'FULL' CONDITION OF THE
*                      DEVICE I/O DIRECTORY.
IODIRECT DSECT
IODEVTP  DS    0XL1  CODE SAVED HERE FROM 'DCBDEVTP'.
IODEVMOD DS    F                  A( DEVICE I/O MODULE ).
IONEXT   DS    0F                 NEXT DIRECTORY ENTRY.
.DSP200  ANOP
 TITLE 'IEEC2740 - DSECT FOR AN OUTPUT QUEUE ENTRY'
OUTQUE   DSECT
OUTQSTAT DS    0XL1               OUTPUT QUEUE DISPOSITIONS
WQENULL  EQU   X'00'              'OUTQWQE' = A NULL ENTRY.
WQEXISTS EQU   X'01'  'OUTQWQE' = A GOOD ENTRY.
WQEUSED  EQU   X'02'  'OUTQWQE' = A USED ENTRY.
WQENEXT  EQU   X'C0'              'OUTQWQE' = A( MORE ENTRIES )
MLWTO    EQU   X'04'         MLWTO ENTRY                          21002
MLWTOHC  EQU   X'08'         MLWTO FOR HARDCOPY ENTRY             21002
WQEREORD EQU   X'10'     MINORS HAVE BEEN REORDERED               21002
OUTQWQE  DS    F  A( WQE ) OR A( MORE OUTPUT QUEUE ENTRIES ).
OUTQNEXT DS    0F                 NEXT OUTPUT QUEUE ENTRY.
.DSP210  ANOP
 TITLE 'IEEC2740 - DSECT FOR AN ''EVENT INDICATOR LIST '' ENTRY'
EIL      DSECT
         IEECVMUG EIL
.DSP220  ANOP
 TITLE 'IEEC2740 - DSECT FOR AN INPUT MESSAGE'
MCSFLAGS EQU   0   MASTER SCHEDULER FLAGS FOR MCS SUPPORT.
MSGLNTH  EQU   144 THE LENGTH OF EACH INPUT MESSAGE.
PARTLNTH EQU   MSGLNTH-6 THE PARTIAL LENGTH USED IN EDITING A MSG.
MSG      DSECT
MESSLNTH DS    XL2                LENGTH OF THIS MESSAGE.
MESSFLGS DS    XL2                FLAGS FOR THE MASTER SCHEDULER.
MESSAGE  DS    CL(MSGLNTH-4)      THE MESSAGE CONTENT.
.DSP230  ANOP
 TITLE 'IEEC2740 - DSECT FOR A SAVEAREA'
SAVEAREA DSECT
SAVEWRD1 DS    F                  WORD 1 (RESERVED).
SAVEHIGH DS    F                  HIGH SAVEAREA ADDRESS.
SAVELOW  DS    F                  LOW SAVEAREA ADDRESS.
SAVEREGS DS    F                  REGISTERS (RSAVE+1 THROUGH RSAVE-1).
.DSP240  ANOP
 TITLE 'IEEC2740 - CONTROL BLOCKS FOR THE 2740 TERMINAL(S)'
IEEC2740 CSECT
DEVTP40  EQU   X'0C'  2740 CODE FOR 'DCBDEVTP'.                  A29573
MCSFLGS  DC    Y(MCSFLAGS*16+0)  VALUE TO BE INSERTED INTO 'MESSFLGS'.
IOBLNTH  EQU   88                      LENGTH OF AN IOB.
.*                                     IS THE SYSTEM 'MFT'
.*                                      WITHOUT 'RAM'...
&D       SETC  'IGG019MB'               YES - GENERATE ACTUAL LINKAGES.
         AIF   ('&M' EQ '1' AND '&R' EQ '0').DSP251
&D       SETC  '0'                      NO - LINKAGE IS SET AT EXECTIME
APP2740  DC    A(RETURN,RETURN,RETURN,0,0)  APPENDAGE VECTOR TABLE.
         DS    0D        ENSURE THE PROPER ALIGNMENT
         AGO   .DSP500
.DSP251  ANOP
APP2740  DC    A(RETURN,RETURN,RETURN),V(&D,&D) APPENDAGE VECTOR TABLE
         DS    0D                 ASSURE THE PROPER ALIGNMENT.
.DSP500  ANOP
&A       SETA  &A+1
&C       SETC  '&A'
 TITLE 'IEEC2740 - CONTROL BLOCKS FOR 2740 CONSOLE NUMBER &C.'
  SPACE 2
         ENTRY DCB40&C            CREATE AN ENTRY FOR EACH DCB.
*                                 'DCB'
         ORG   *-20               BACK UP TO SAVE UNUSED SPACE.
DCB40&C. DS    0F                 DCB MUST BEGIN ON A WORD BOUNDARY.
         ORG   *+20      MOVE FORWARD PAST THE UNUSED SPACE.
*                                      COMMON INTERFACE
*              DCBBUFNO,DCBBUFCB,DCBBUFL
         DC    AL1(0),AL3(0),AL2(0)                                   *
*              DCBDSORG = CX
         DC    X'1000'                                                *
*              DCBDEVTP,DCBIOBAD = A(IOB) - L'IOB
         DC    AL1(DEVTP40),AL3(IOB40&C.-IOBLNTH)                     *
*                                      FOUNDATION EXTENSION
*              DCBBFTEK,DCBERROP = (R,W,E)
         DC    AL1(0),X'06',AL2(0)                                    *
*              DCBEIOBX,DCBEXLST
         DC    AL1(IOBLNTH),AL3(0)                                    *
*                                      FOUNDATION SEGMENT (AFTER OPEN).
*              DCBTIOT,DCBMACRF,DCBIFLGS,DCBDEBAD
         DC    AL2(0),X'2020',AL1(0),AL3(DEB40&C.)                    *
*              DCBOFLGS,DCBREAD/DCBWRITE
.*                                     IS THE SYSTEM 'MFT'
.*                                      WITHOUT 'RAM'...
&D       SETC  'IGG019MA'               YES - GENERATE ACTUAL LINKAGE.
         AIF   ('&M' EQ '1' AND '&R' EQ '0').DSP591
&D       SETC  '0'                      NO - LINKAGE IS SET AT EXECTIME
         DC    AL1(0),AL3(0)
         SPACE 3
         AGO   .DSP600
.DSP591  ANOP
         DC    0AL1(0),V(&D)
         SPACE 3
.DSP600  ANOP
*                                 'DECB'
         READ DECB40&C.,T,DCB40&C.,,L'MESSAGE,,1,MF=L
         EJECT
.DSP700  ANOP
*                                 'IOB'
IOB40&C. DS    0D IOB MUST BEGIN ON A DOUBLE-WORD BOUNDARY.
*                                      STANDARD FIELDS
*              IOBFLAG1,IOBFLAG2,IOBSENS0,IOBSENS1,IOBECBPT
         DC    X'C2',AL1(0,0,0,0),AL3(DECB40&C.)                 A30772
*              IOBFLAG3,IOBCSW,IOBSIOCC,IOBSTART
         DC    AL1(0),AL3(IOB40&C.),XL4'00',AL1(0),AL3(CP40&C.)       *
*              IOBDCBPT,IOBRESTR,IOBINCAM,IOBERRCT
         DC    A(DCB40&C.,0),AL2(0,0)                                 *
*                                      BTAM EXTENSION SEGMENT
*              IOBUCBX,IOBWORK,IOBERCCW,IOBERINF
         DC    AL1(0),XL7'00',XL8'00',XL16'00'                        *
CP40&C.  DS    6D         CHANNEL PROGRAM AREA                   A33946
         SPACE 3
.DSP800  ANOP
*                                 'DEB'
*                                      PREFIX SECTION
*              DEBDCBMK,DEBLNGTH
         DC    A(0),AL1((DEBEND&C.-DEB40&C.)/8+1),AL3(0)              *
*                                      BASIC SECTION
*              DEBNMSUB,DEBTCBAD
DEB40&C. DC    AL1(0),AL3(0)                                          *
*              DEBAMLNG,DEBDEBAD
         DC    AL1(4),AL3(0)                                          *
*              DEBOFLGS,DEBIRBAD,DEBOPATB,DEBSYSPG
         DC    AL1(0),AL3(0),AL1(0),AL3(0)                            *
*              DEBNMEXT = 1,DEBUSRPG,DEBPRIOR,DEBECBAD
         DC    AL1(1),AL3(0),AL1(0),AL3(0)                            *
*              DEBPROTG/DEBDEBID,DEBDCBAD,DEBEXSCL = 2,DEBAPPAD
         DC    X'0F',AL3(DCB40&C.),AL1(2),AL3(APP2740)                *
*                                      DEVICE DEPENDENT SECTION
*              DEBUCBAD
         DC    A(0)                                                   *
PURGE&C  DC    3F'0'    PARM LIST FOR PURGE                      A46828
DEBEND&C. DS   0D  END OF CONTROL BLOCKS FOR THIS TERMINAL.
         AIF   ('&A' EQ '31').DSP900
         AIF   (&A NE &N).DSP500
.DSP900  ANOP
         MEND
