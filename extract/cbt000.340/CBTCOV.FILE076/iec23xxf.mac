         MACRO
   IEC23XXF  &D2314,&D2301,&D2302,&D2303,&D2321,&OVF,&CCH,&DDR,        X
               &SYSDDR,&D2305,&D3330,&LCS                        A35351
         LCLC  &A,&B,&C,&D,&E,&F,&G,&H,&I,&J,&K                  A32176
&A       SETC  '&D2314'                                          A32176
&B       SETC  '&D2301'                                          A32176
&C       SETC  '&D2302'                                          A32176
&D       SETC  '&D2303'                                          A32176
&E       SETC  '&D2321'                                          A32176
&F       SETC  '&OVF'                                            A32176
&G       SETC  '&CCH'                                            A32176
&H       SETC  '&DDR'                                            A32176
&I       SETC  '&SYSDDR'                                         A32176
&J       SETC  '&D2305'                                          S20201
&K       SETC  '&D3330'                                          S20201
IEC23XXF CSECT
*C127880-127970  LOGGING PRIORITY                              @SA79916
*A289414  ECF LOOP                                    US07447  @SA77654
*C191940  DEF/ALT TRACK POINTER                                @SA78184
*A040011,203400-203420,237610-237640                           @SA78184
*D307800-308702  SYSRES ALT PATH                               @SA78183
*A038701-038702,310610-310800                                  @SA78183
*D532900,533000         TRANSIENT FETCH REQ           US07309  @SA77717
*C128020,128030         CORRECTABLE ERROR             US07309  @SA77720
*A039922,290225,290225  TEST FOR NO-OP                         @SA76802
*C290249                                                       @SA76802
*C257560        SKIP RETRY                             US07309 @SA77511
*D257550        DELETE TEST FOR 2321                   US07309 @SA77511
*A310610        SET ERP ENTERED ON INT REQ                     @SA77193
*A308200-308600 TREAT FETCH AND OBR LIKE SYSRES                @SA77437
*A127892-127894,532900-532910                                  @SA76234
*A731720-732020                                                @SA75963
*A124700                                                       @SA75243
*A124300-124600,040300 RESET RESTART BIT IF CC=0       US06782 @SA76790
*D373500,374400        VER CORRECT SEEK                US06782 @SA76260
*A198500,262500-262784                                         @SA71611
*C191960                                                       @SA71611
*D198500,262500-262784   DELETE OS71611                        @SA74812
*C191960                 DELETE OS71611                        @SA74812
*A039920                                                       @SA64437
*C290227                                                       @SA64437
*A290220,290224,290319                                           A59754
*C290227,790340                                                  A59754
*A380810-381250,431100-431984,643600,643700,925620-925680      @SA72530
*A039520,260400,263300,290627-290740,422600,422700,425300      @SA73886
*C381250,D290579,290590                                        @SA73886
*D600300                                               US06782 @SA76342
*C576900,598500                                        US06782 @SA76342
*A577400,599000,599100                                 US06782 @SA76342
*A203000-203080,203400-203480,237630-237660,238100-238200      @SA78506
*                                                              @SA78214
*                                                              @SA78214
*                                                                M4204
*                                                               SA78212
*                                                               SA78212
*                                                               SA78212
*                                                               A32176
*D122400                                                        A62858
*C414900,731700                                                 A62858
*A376700,450500,730400,732200,732300                            A62858
*D338400-343800,379800,380700                                   A63226
*C458152                                                        A63226
*A338300,380210-381410,458256-458360                            A63226
*A072500,148200-148420,895200-895300                            SA63263
*D289908-289941                                                 SA66008
*A290561-290567                                                 SA66008
*A131900,132000,132320                                          SA60720
*C789960-790080                                                 SA65470
*A191844,191848                                                 SA67819
*C191860,191880,191900,191920,191940,191960                     SA67819
*D191970,191980                                                 SA67819
*A289571,289620,290165,290214,607100,608900,609000,662000       SA68579
*A662100,662200,663800                                          SA68579
*C289600,290194                                                 SA68579
*D289611,290205,627600-628300                                   SA68579
*A127901,127902                                                @SA80070
*C289966                                                       @SA80070
*D127885,127886,289967,317800-318200                           @SA80070
*
         ENTRY ER2311                   ENTRY TO RESIDENT ERROR ROUTINE
         EXTRN IFBDEB
*NON RE-ENTRANT LOCATIONS ERSAV,ERINC
TSTREG   EQU   1                        12* REG.
IOBRG    EQU   2                        IOB BASE REG
DEBREG   EQU   3                        DEB. REG.
DCBREG   EQU   4                        DCB BASE REG
UCBRG2   EQU   4                        UCB WK AR BASEREG
CCWREG   EQU   5                        ERR CCW REG
CVTREG   EQU   6                        CVT BASE REG
UCBREG   EQU   7                        UCB. REG.
SNSREG   EQU   8                        FLOATING SENSE REG       S20201
LNKRG2   EQU   9                        LINKAGE REG AND UT 4
LINKRG1  EQU   13
WORK     EQU   10
ERREG1   EQU   10                       UTILITY REG 1
RMSREG   EQU   10                       POINTER TO RMS TABLE
ERREG2   EQU   11                       UTILITY AND PARAM REG 2
ERCCWR   EQU   12                       ERP CCW PTR (UTIL 3)
ERREG3   EQU   12                       UTIL 3 (ERP CCW PTR)
DDRBASE  EQU   12                       BASE REG FOR DDRTN
ERRETR   EQU   14                       IOS RETURN
BASREG   EQU   15                       BAS REG
         USING *,15
         USING DEB,DEBREG
         USING IOB,IOBRG
         USING CVT,CVTREG
         USING UCB,UCBREG
         USING ERU,UCBRG2
         USING UCBSNS2,SNSREG                                    S20201
         SPACE 1
*MISCELLANEOS
IOCLBT   EQU   X'00'                    CLEARING BYTE
DEBST    EQU   6                        XTNT START LOC IN BLOCK  A32176
DEBEND   EQU   10                       XTNT END LOG IN BLOCK    A32176
DEBSCA   EQU   4                        SPLIT CYL ALLOC FLAG     A32176
ERNSKM   EQU   X'18'                    NO SEEK FILE MASK
HDSK     EQU   X'1B'                    SEEK HEAD BIT IN SEEK CMD
SERWRK   EQU   8                        SENSE INFORM.
STCDC    EQU   X'01'                    STAT INDICATOR FOR CDC
CVTLOC   EQU   16                       LOC OF CVT ADDR
CVTOPTA  EQU   X'B6'                    CVT OPTION BYTE        @SA78183
CVTNIP   EQU   X'10'                    NIP IS EXECUTING       @SA78183
OBR      EQU   X'80'                    OBR RECORD SAVED         A42863
STOP     EQU   X'08'                    DO NOT RESTART CHPGM BIT S20201
SEARCHA  EQU   X'39'                    SEARCH HA CMD CODE       S20201
FLTSNS   EQU   X'08'                    UCB HAS FLOATING SENSE   S20201
PCICODE  EQU   X'71'                    FETCH PCI RETRY CODE
SECTOR   EQU   X'23'                    SET SECTOR COMMAND CODE  S20201
DCHAIN   EQU   X'80'                    DATA CHAINING FLAG       S20201
SLI      EQU   X'20'                    SUPPRESS LENGTH FLAG     S20201
LNGCK    EQU   X'40'                    INCORR LENGTH IN CSW     S20201
C0       EQU   0                        GENERAL CONSTANT ZERO    S20201
EXPSNS   EQU   X'08'                    EXPANDED SENSE IND     @SA73886
USEDDR   EQU   X'10'                    USE DDR BIT
       AIF     (&D2321 EQ 0).NORES0                              S20201
* 2321 OPTION                                                    S20201
SWPSVN   EQU   X'70'                    SWEEP CNT MASK           S20201
SWPMASK  EQU   X'0413'                  SWEEP CYL/HD MASK        S20201
OPERABLE EQU   X'40'                    OPERABLE BIT IN UCB      A37849
*2321 END                                                        S20201
.NORES0  ANOP                                                    S20201
UPCNT    EQU   4                        LOOP CNT                 S20201
ONES     EQU   1                        ONES COND CODE           S20201
EQ       EQU   8                        EQUAL COND CODE          S20201
CNTFLD   EQU   X'10'                    COUNT FIELD OPERATION    S20201
SECFLD   EQU   X'20'                    READ SECTOR OPERATION  @SA64437
ONE      EQU   X'01'                    ONE
NOOP     EQU   X'03'                    NO-OP CCW CMD          @SA76802
ALTDEF   EQU   X'0B'                    ALT/DEF TRACK POINTER  @SA78184
         SPACE 1                                                 S20201
*   IOB COMPLETION CODES                                         S20201
INTRCPT  EQU   X'7E'                    IOB INTERCEPT CODE
IOBNORM  EQU   X'7F'                    NORMAL POST CODE
CC1OR3   EQU   X'10'                    SIO CC=1 OR 3          @AS76790
         SPACE 1
*SENSE BIT DEFINITIONS IN IOB OR UCB
IOBCMDR  EQU   X'80'                    COMMAND REJECT IN SENSE  S20201
OBRSNS   EQU   X'3C'                    OBR RECORD SENSE         A52407
EQUIPSNS EQU   X'10'                    EQUIPMENT CHECK          S20201
DATASNS  EQU   X'08'                    DATA CHECK SENSE         S20201
IOBOVRN  EQU   X'04'                    OVERRUN SENSE            S20201
IOBTKC   EQU   X'02'                    TRK COND SENSE           S20201
IOBSKCK  EQU   X'01'                    SEEK CHK SENSE           S20201
**                     BYTE ONE  SENSE DEFINITIONS               S20201
PERMSNS  EQU   X'80'                    PERMANENT ERROR SENSE    S20201
INVALTFM EQU   X'40'                    INVALID TRACK FORMAT     A52407
EOCSNS   EQU   X'20'                    END OF CYLINDER SENSE    S20201
SNSNRF   EQU   X'08'                    NRF IN SENSE             S20201
FPSNS    EQU   X'04'                    FILE PROTECT SENSE       S20201
IOBAMRK  EQU   X'02'                    SENSE MSSNG ADDR MRK     S20201
IOBOVF   EQU   X'01'                    SENSE OVERFLOW BIT       S20201
**                     BYTE TWO  SENSE DEFINITIONS               S20201
UCBUNS   EQU   X'80'                    UNSAFE SENSE BIT         S20201
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD0               S20201
* 2305 OR 3330 OPTION                                            S20201
ZUSLOG   EQU   X'80'                    ZEUS CNTL UNIT LOG FULL  S20201
ECFSNS   EQU   X'40'                    ECF DATA CHECK           S20201
LOGSNS   EQU   X'10'                    ENVIRONMNTL DATA PRESENT S20201
C26      EQU   26                       CONSTANT OF 26           S20201
RDLOG    EQU   X'24'                    READ BUFFERED LOG CMD CD S20201
CULENG   EQU   X'01'                    CONTROL UNIT DET LENGTH  S20201
*2305/3330 END                                                   S20201
.NEWD0   ANOP                                                    S20201
         SPACE 1
*   THESE FLAGS ARE IN IOB-FLAG 1
IOBERR   EQU   X'20'                    IOB ERROR FLAG
IOBEX    EQU   X'04'                    EXCEPT FLAG
IOBSRS   EQU   X'01'                    START/RESTART FLAG
         SPACE 2
*   THESE ARE FLAGS IN IOB-FLAG 2
IOBRHA   EQU   X'10'                    READ HOME ADDR. FLAG
IOBNET   EQU   X'08'                    NO TEST FOR OUT OF EXTENT
IOBPREV  EQU   X'04'                    SET TO INDICATE ERP WAS ENTERED
IOBOFLO  EQU   X'02'                    OVERFLOW INDICATOR       A33657
         SPACE 2
*   THESE FLAGS ARE IN IOB-FLAG 3
IOBIN1   EQU   X'80'                    INDICATOR ONE            S20201
IOBPIK   EQU   X'40'                    PICK FLAG                S20201
IOBNRF   EQU   X'20'                    NO RECORD FOUND FLAG
IOBBOE   EQU   X'10'                    BUS OUT ERROR CNT
IOBRSF   EQU   X'08'                    RESTORE FLAG
IOBMSG   EQU   X'04'                    MESSAGE TYPE
IOBSWP   EQU   X'02'                    SWEEP FLAG               S20201
IOBLOG   EQU   X'01'                    LOG OUT INDICATION
         SPACE 1
*        FLAGS AND COUNTS IN IOB ERROR BYTE 1. SYMBOL =IOBECT=
*        BIT   0-7                      DATA CHECK COUNT
         SPACE 1
*        FLAGS AND COUNTS IN IOB ERROR BYTE 2 . SYMBOL #IOBECT+1#
IOBWRT   EQU   X'80'                    INDICATOR TO STAT. UPDATE
*        BIT 0                          IOBWRT FOR STAT UPDATE
*        BIT 1-7                        OVERRUN,NRF,& SK CHK COUNT
         SPACE 2
*     FLAGS IN UCB
UCBSHRD  EQU   X'20'                    SHARED                  SA63263
UCBNRY   EQU   X'40'                    UCB NOT READY
UCBERR   EQU   X'01'                   ERROR RTN. IN CONTROL
UCBSYS   EQU   X'02'                    SYSRES FLAG
         SPACE 1
*        ASR BIT DEFINITION'S
CSWCDC   EQU   8                        CHAN DATA CHK            A32176
CHANERR  EQU   X'0F'                    CDC+CCC+ICC+CHAIN        S20201
TABLELGN EQU   8                        ERPIB TABLE ENTRY LENGTH A32176
ERPIBFLG EQU   4                        ERPIB SFTWRE FLGS OFFSET A32176
NORETRY  EQU   1                        CCH'S NO-RETRY FLG TO ERPA32176
CCHDELMT EQU   255                      CCH'S ERPIB TABLE DELIMITA32176
         SPACE
*              STATUS INDICATORS
CSWUCK   EQU   X'02'                    UNIT CHECK
         SPACE 2
*    CCW COMMANDS
CMDTIC   EQU   X'08'                    TIC COMMAND
CMDFSK   EQU   X'07'                    FULL SEEK  CMD CODE
CCWSK    EQU   X'0B'                    HEAD OR CYL SEEK INDICATE
SKIP     EQU   X'10'                    NO DATA XFER
SEARCH   EQU   X'21'                    SEARCH TYPE COMMAND     SA78212
         SPACE 1
*    FLAG BITS IN HOME ADDRESS
TRKBAD   EQU   X'02'                    BAD TRACK
         SPACE 1
*   THESE ARE COMPLETION CODE SETTINGS.
*    POINTERS TO THE DIRECT ACCESS CHARACTERISTICS TABLE
HEADNO   EQU   3                       INDEX TO NUMBER OF HEADS/CYL
         SPACE 1
*    DEVICE TYPE
DE2301   EQU   X'02'                    DRUM DEVICE TYPE
DE2302   EQU   X'04'                    2302 DEVICE
DE2303   EQU   X'03'                    2303 DRUM DEVICE CODE
DE2314   EQU   X'08'                    2314 DEVICE CODE
DE2321   EQU   X'05'                    2321 DEVICE TYPE
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD1               S20201
* 2305 OR 3330 OPTION                                            S20201
ZEUSA    EQU   X'06'                    ZEUSA DEVICE TYPE        S20201
ZEUSC    EQU   X'07'                    ZEUSC DEVICE TYPE        S20201
MERLIN   EQU   X'09'                    MERLIN DEVICE TYPE       S20201
*2305/3330 END                                                   S20201
DCC      EQU   X'10'                    DEVICE HAS RECORD READY  S20201
.NEWD1   ANOP                                                    S20201
         EJECT
*****************************************************************
* THE DASD ERP IS REQUIRED-
* A.TO REACT TO NESTED ERRORS IN THE SAME CHAN PRO AND SAME DEVICE
* C.TO REACT TO SEVERAL ERRORS ASYNCHRONOUSLY ON SEVERAL DEVICES
* B.TO REACT TO SEVERAL ERRORS ON THE SAME DEVICE ASYNCHRONOUSLY
* D.ALL OF THE ABOVE
* E.TO OBTAIN CERTAIN INFORMATION ABOUT CERTIAN ERRORS AND PASS
*   IT TO THE OPERATOR COMMUNICATION,STATISTICS AND ERR LOGGING
*   ROUTINES.
* SINCE THE ERP IS OPERATED ASYNCHRONOUSLY,IT IS CONTROLLED BY
* BIT SWITCHES WHICH ARE SET AT EXIT AND INTERROGATED UPON
* RE-ENTRY
ER2311   DS    0H
         STM   DCBREG,SNSREG,ERSAV      SAVE REGS 4- 8           S20201
         L     UCBRG2,UCBWK             UCB,UCB WK AR CONTI
         LA    SNSREG,UCBSNS1           SENSE REG                S20201
       AIF     (&D2321 EQ 0).NORES1                              S20201
* 2321 OPTION                                                    S20201
         CLI   UCBUTB,DE2321            2321 DEVICE              S20201
         BNE   NOT21                    NO, SKIP 2321 UPDATE     S20201
         MVI   UCBERP,X'01'             SET IGE0000A AS TRANS    S20201
         L     UCBRG2,UCBWK1            2321 DISJOINT WORK AREA  S20201
NOT21    DS    0H                       CONNECTOR                S20201
*2321 END                                                        S20201
.NORES1  ANOP                                                    S20201
         L     CVTREG,CVTLOC
         TM    IOBST,CC1OR3             SIO CC=1 OR 3          @SA76790
         BO    ERPINCTL                 YES, LEAVE IOBSRS ON   @SA76790
         NI    IOBFL1,X'FF'-IOBSRS      NO, RESET IOBSRS       @SA76790
ERPINCTL EQU   *                        CONNECTOR              @SA76790
         OI    IOBFL1,IOBERR+IOBEX      SET ERP IN CONTROL
         TM    IOBCSW+4,CSWUCK          TEST FOR UNIT CHK
         BO    ERSTUP                   YES,SKIP
         XC    IOBSNS(2),IOBSNS         NO,CLEAR RESID SENSE
ERSTUP   DS    0H                       CONNECTOR                S20201
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD3               S20201
* 2305 OR 3330 OPTION                                            S20201
         BAL   LNKRG2,MZTEST            CHECK FOR MERLIN/ZEUS    S20201
         BNE   ERSTUP2                  BRANCH IF NEITHER        S20201
         L     SNSREG,EXTSNS0           GET FLOATING SENSE AREA  S20201
         LA    SNSREG,0(SNSREG)         CLR HI-ORDER BYTE        S20201
         TM    IOBCSW+4,CSWUCK          TEST FOR UNIT CHK        A52407
         BO    ERSTUPX                  YES,SKIP                 A52407
         XC    USNS2(1),USNS2           NO CLEAR RESID SENSE     A51174
ERSTUPX  DS    0H                       CONNECTOR              @SA79916
         TM    USNS2,LOGSNS+ZUSLOG      NEW ENV DATA?          @SA79916
         BNZ   LOGSAVE                  YES, LOG IT            @SA79916
         TM    IOBSNS,OBRSNS            OBRABLE ERROR?         @SA79916
         BZ    ERNTRP                   NO, DONT LOG           @SA79916
         TM    FMSG,X'30'               NEW FMT 3 ?            @SA78211
         BO    LOGSAVE                  YES, SAVE IT           @SA78211
         TM    EXTSNS+7,X'60'           PREV FMT 6             @SA76234
         BO    ERNTRP                   YES, DON'T OVERLAY IT  @SA76234
         TM    EXTCSW,OBR               PREV OBR ENTRY?        @SA80070
         BO    ERNTRP                   YES, DO NOT LOG        @SA80070
TSTDCK   DS    0H                       CONNECTOR              @SA79916
         TM    UCBSNS,DATASNS           IS THIS A DATA CHECK     S20201
         BZ    LOGSAVE                  BRANCH IF NO             S20201
         TM    UCBSNS+2,X'40'           IS ERROR CORRECTABLE   @SA77720
         BO    ERNTRP                   YES, DON'T LOG         @SA77720
LOGSAVE  DS    0H                       CONNECTOR                S20201
         MVC   EXTSNS,UCBSNS            SAVE THE SENSE           S20201
         MVC   EXTCSW,IOBCSW            SAVE THE CSW             S20201
         MVI   EXTCSW,OBR               INDICATE A SAVED ENTRY   S20201
         OI    IOBFL3,IOBLOG            SET IOB LOG FLAG         MS0058
         B     ERNTRP                   BRANCH TO ERROR INTERP   S20201
*2305/3330 END                                                   S20201
ERSTUP2  DS    0H                       CONNECTOR                S20201
.NEWD3   ANOP                                                    S20201
         SPACE 1
*****************************************************************
         BAL   LNKRG2,ERSTPTR           PTR TO STAT ENTRY        S20201
         TM    IOBCSW+4,CSWUCK          UNIT CHECK?             SA60720
         BZ    SKIPMOV                  NO, SKIP                SA60720
         MVC   ERINC(3),UCBSNS          PUT SENSE INTO WORK LOCA
SKIPMOV  DS    0H                       CONNECTOR               SA60720
         AIF   (&D2301 EQ 0).LOGREG
         CLI   UCBUTB,DE2301            IS DEVICE 2301?          A36382
         BE    LOG2301                  YES,USE 2301 LOG MASK    A36382
.LOGREG  ANOP
         NC    ERINC+1(2),NORLOG        NO,USE NORMAL MASK       A36382
         AIF   (&D2301 EQ 0).LOGREG1
         B     COMLOG                   DO COMPOSIT              A36382
LOG2301  NC    ERINC+1(2),SPLOG         USE 2301 MASK            A36382
.LOGREG1 ANOP
COMLOG   OC    ERINC+1(1),ERINC+2       2ND=COMPOSITE OF 2ND+3RD A36382
         OC    SERWRK(2,ERREG2),ERINC   OR COMPOSITE INTO STAT
         TM    IOBCSW+5,CSWCDC          CHECK FOR CDC
         BZ    *+8                      NO,GO AROUND
         OI    SERWRK+1(ERREG2),STCDC   SET CDC STAT IND
ERNTRP   DS    0H
         CLI   IOBCOD,INTRCPT           INTERCEPTED REQUEST
         BNE   ERNTRPA                   NO, CONTINUE
         MVI   IOBCOD,IOBNORM           YES, RESET CODE AND -
         B     OVERRUN                  START REQUEST            A32176
ERNTRPA  DS    0H
         BAL     LNKRG2,CCWSUB          GET ERR CCW PTR
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD4               S20201
* 2305 OR 3330 OPTION                                            S20201
         BAL   LNKRG2,MZTEST            CHECK FOR MERLIN/ZEUS    S20201
*2305/3330 END                                                   S20201
.NEWD4   ANOP                                                    S20201
         LR    10,IOBRG                 GLITCH FOR INTRP
         LR    9,BASREG                 REG 9=RETURN REG
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD4A              S20201
* 2305 OR 3330 OPTION                                            S20201
         BE    ERNTMZ                   BRANCH IF MERLIN/ZEUS    S20201
*2305/3330 END                                                   S20201
.NEWD4A  ANOP                                                    S20201
         TM    UCBOFEAT,UCBSHRD         TEST FOR SHARED         SA63263
         BZ    ERNTNSHR                 BR IF NOT SHARED        SA63263
         NI    IOBFL1,X'FF'-IOBSRS      CLEAR RESTART           SA63263
ERNTNSHR DS    0H                       CONNECTER               SA63263
         L     BASREG,CVTNTRP           ADDR OF ERR INTERP
ERNT     BALR  12,BASREG                GO
*
         DC    X'1D',AL1(ERCCC-ERNT-2)       CHAN CTRL CHK       A32176
*
         DC    X'1E',AL1(ERICC-ERNT-4)       INTERFACE CTRL CHK  A32176
*
         DC    X'1C',AL1(TRY10C-ERNT-6)       CHAN DATA CHK      A53829
*
         DC    X'03',AL1(EREQCH-ERNT-8)      EQUIP CHK           A32176
*
         DC    X'0C',AL1(ERNRF-ERNT-10)      NO REC FND          A32176
*
         DC    X'07',AL1(ERSK-ERNT-12)       SEEK CHK            A32176
*
         DC    X'01',AL1(ERNREQ-ERNT-14)     INT REQ'D           A32176
*
         DC    X'02',AL1(ERBUSO-ERNT-16)     BUSOUT              A32176
*
         DC    X'04',AL1(ERDAT-ERNT-18)      DATA CHECK          A32176
*
         DC    X'05',AL1(EROVRN-ERNT-20)     OVERRUN             A32176
*
         DC    X'0E',AL1(ERMARK-ERNT-22)     MISSING ADDR MRK    A32176
*
         DC    X'00',AL1(ERCMDR-ERNT-24)     COMMAND REJECT      A32176
*
         DC    X'06',AL1(ERTCC-ERNT-26)      TRK COND CHK        A32176
*
         DC    X'09',AL1(ERTOVRN-ERNT-28)    TRK OVERRUN         A32176
*
         DC    X'0A',AL1(EREOC-ERNT-30)      END OF CYLINDER     A32176
*
         DC    X'0D',AL1(ERFPRT-ERNT-32)     FILE PROTECT        A32176
*
         DC    X'16',AL1(ERSEV-ERNT-34)      UNIT CHK- NO SNS    A32176
*
         DC    X'1F',AL1(TRY10C-ERNT-36)     CHAIN CHK           A53829
*
         DC    X'1A',AL1(ERCHP-ERNT-38)      CH PROG CHK         A32176
*
         DC    X'1B',AL1(ERPROT-ERNT-40)     CH PROT CHK         A32176
*
         DC    X'17',AL1(EREOF-ERNT-42)      UNIT EXCEPTION      A32176
*
         DC    X'19',AL1(ERLEN-ERNT-44)      WRONG LENGTH        A32176
*
         DC    X'2F',AL1(ERNOER-ERNT-46)     END OF TEST         A32176
*
*****************************************************************
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD5               S20201
* 2305 OR 3330 OPTION                                            S20201
**        MERLIN  ZEUS ERROR INTERPRETER TABLE       **          S20201
ERNTMZ   DS    0H                       CONNECTOR                S20201
         L     BASREG,CVTNTRP           ADDR OF ERR INTERP       S20201
ERNT2    BALR  12,BASREG                BRANCH TO INTERPRETER    S20201
*                                                                S20201
         DC    X'1D',AL1(ERCCC-ERNT2-2)      CHAN CTRL CHK       S20201
*                                                                S20201
         DC    X'1E',AL1(ERICC-ERNT2-4)      INTERFACE CTRL CHK  S20201
*                                                                S20201
         DC    X'1C',AL1(TRY10C-ERNT2-6)     CHAN DATA CHK       A53829
*                                                                S20201
         DC    X'1F',AL1(TRY10C-ERNT2-8)     CHAINING CHK        A53829
*                                                                S20201
         DC    X'05',AL1(TRY10A-ERNT2-10)    OVERRUN            SA67819
*                                                                S20201
         DC    X'08',AL1(ERSEV-ERNT2-12)     PERMANENT ERR      SA67819
*                                                                S20201
         DC    X'03',AL1(TRY10A-ERNT2-14)    EQUIP CHK          SA67819
*                                                                S20201
         DC    X'02',AL1(ERBUSO-ERNT2-16)    BUS OUT            SA67819
*                                                                S20201
         DC    X'01',AL1(ERNREQ-ERNT2-18)    INT REQ'D          SA67819
*                                                                S20201
         DC    X'00',AL1(ERCMDRJ-ERNT2-20)   COMMAND REJECT     SA67819
*                                                                S20201
         DC    X'0C',AL1(ERPERM-ERNT2-22)    NO RECORD FOUND   @SA74812
*                                                                S20201
         DC    X'09',AL1(ERPERM-ERNT2-24)    INVALID TRACK FORMATS20201
*                                                                S20201
         DC    X'04',AL1(ERECF-ERNT2-26)     DATA CHK            S20201
*                                                                S20201
         DC    X'0A',AL1(EREOC-ERNT2-28)     END OF CYLINDER     S20201
*                                                                S20201
         DC    X'0D',AL1(ERFPRT-ERNT2-30)    FILE PROTECT        S20201
*                                                                S20201
         DC    X'0F',AL1(EROV-ERNT2-32)      OVERFLOW ALONE      S20201
*                                                                S20201
         DC    X'16',AL1(ERSNS-ERNT2-34)     UNIT CHK            S20201
*                                                                S20201
         DC    X'1A',AL1(ERPERM-ERNT2-36)    CHAN PROG CHK       S20201
*                                                                S20201
         DC    X'1B',AL1(ERPERM-ERNT2-38)    CHAN PROT CHK       S20201
*                                                                S20201
         DC    X'17',AL1(EREOF-ERNT2-40)     UNIT EXCEPTION      S20201
*                                                                S20201
         DC    X'19',AL1(ERLEN-ERNT2-42)     INCORRECT LENGTH    S20201
*                                                                S20201
         DC    X'2F',AL1(ERNOER-ERNT2-44)    END TEST            S20201
*                                                                S20201
**************************************************************** S20201
*2305/3330 END                                                   S20201
.NEWD5   ANOP                                                    S20201
ERCCC    DS    0H                       CHAN CONTROL CHK RTN
ERICC    B     CHANNEL0                 CHANNEL ERROR RTN
ERNOER   B     ERNONE                   NO ERR  PROCESS
EREQCH   B     EQUIPMT                  EQUIPMENT CHECK RTN
ERSEV    B     MSGLOG                   NO SENSE AND A UNIT CK
ERBUSO   B     BUSOUT                   BUS OUT RTN
ERNRF    B     NORECF                   NO RECORD FOUND RTN
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD5B            @SA73886
.NEWD5B  ANOP                                                  @SA73886
ERTCC    B     TRACKCON                 TRACH CONDITION RTN
ERDAT    B     DATACK                   DATA CHECK RTN
ERMARK   B     MISSING                  MISSING ADDR MARK RTN
ERSK     B     SEEKCK                   SEEK CHECK RTN
TRY10C   B     CHANNEL2                 CDC OR CHAINING          A53829
* COMMAND REJECT IS A HARD USER ERROR WITH MSG BUT NO LOG        S20201
        AIF    (&D2305 EQ 0 AND &D3330 EQ 0).NEWD5C            @SA78506
* 3330 OPTION                                                  @SA78506
* COMMAND REJECT ON A 3330 AND SENSE BYTE 7 = 0B WILL BE       @SA78184
* LOGGED TO INDICATE AN IMPROPER ALT/DEF TRACK POINTER.        @SA78184
ERCMDRJ  B     ERCMDRJ1                 CMD REJ                @SA78184
* 3330   END                                                   @SA78506
.NEWD5C  ANOP                                                  @SA78506
ERCMDR   B     MSG                      GO SET UP FOR MSG        S20201
ERNREQ   B     INTERVEN                 INTERVENTION REQUIRED RTN
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD6               S20201
* 2305 OR 3330 OPTION                                            S20201
EROV     B     RSTOVF                   RESTART OVERFLOW CHPGM   S20201
ERECF    B     ECFDATA                  ECF ERROR                S20201
ERSNS    B     FLAGCK                   CKECK BYTE THREE OF SENS S20201
TRY10    B     RTRY10                   RETRY 10                 S20201
TRY10A   B     RTRY10A                  RETRY 10                  M5595
*2305/3330 END                                                   S20201
.NEWD6   ANOP                                                    S20201
         AIF   (&LCS EQ 0).NOLCS1
*  LCS OPTION                                                    A35351
EROVRN   B     OVERUNA                 CHANNEL OVRRUN RTN        A35351
*  LCS OPTION END                                                A35351
         AGO   .LCS1                                             A35351
.NOLCS1  ANOP                                                    A35351
EROVRN   B     OVERRUN                  CHANNEL OVERRUN RTN
.LCS1    ANOP                                                    A35351
EREOF    B     UNITEXC                  END OF FILE ROUTINE
ERLEN    B     LENGTH                   INCORRECT LENGTH RTN
EREOC    B     CYLINDER                 END OF CYLINDER RTN
ERFPRT   B     FILEPROT                 FILE PROTECT RTN
*****************************************************************
*  HARD USER ERROR CONDITIONS, NO MSG, NO LOG
ERPERM   DS    0H                       HARD ERRORS,             S20201
ERTOVRN  DS    0H                       TRACK OVERUN
ERCHP    DS    0H                       CHAN PROG CHK RTN
ERPROT   DS    0H                       CHAN PROT CHK
         B     ERHARD                   HARD USER ERROR
       AIF     (&CCH EQ 0).NASR2                                 A32176
* CCH OPTION
*****************************************************************
*CH CONTRL CHK AND INTERFACE CONTROL CHK ARE TREATED AS SEEK CHKS
*IF RETRIES ARE PERMITTED BY THE ERPIB'S. OTHERWISE IT IS A HARD
*ERROR AND WILL RESULT IN MSG AND LOG
CHANNEL0 DS    0H
         BAL   LINKRG1,ERASR0           CHK FOR ERPIB            A32176
         BC    9,CHANNEL3           NO ERPIB OR NOT RETRYABLE    A53829
*CCH END
         AGO   .NASR2A
.NASR2   ANOP
CHANNEL0 DS    0H                       CONNECTOR
.NASR2A  ANOP
CHANNEL2 TM    IOBCNT+1,10              CNT MAX?                 A53829
         BNO   CHANNEL4                 NO                       A53829
CHANNEL3 OI    IOBFL2,IOBRHA            IND CCHH IN MSG          A53829
         B     MSG                      GO TO SET FOR ERR MSG    A53829
CHANNEL4 BAL   LINKRG1,INCR10           RETRY PGM 10 TIMES       A53829
         B     RETRY                    RETRY CHANNEL PROGRAM
*
*****************************************************************
         AIF    (&D3330 EQ 0).NEWD61                           @SA78506
*EXPSNS OPTION                                                 @SA78506
ERCMDRJ1 DS    0H                                              @SA78184
         CLI   FMSG,ALTDEF              ALT TRACK POINTER BAD  @SA78184
         BE    MSGLOG                   YES, LOG AND MSG       @SA78184
         B     MSG                      NO, MSG                @SA78184
* EXPSNS END                                                   @SA78506
.NEWD61  ANOP                                                  @SA78506
*****************************************************************
*EQUIP CHK OR BUS OUT ERRORS ARE RETRIED ONCE,IF ERROR PERSISTS
*MSG AND LOG
EQUIPMT  TM    USNS2,UCBUNS             IS DEVICE UNSAFE         S20201
         BO    MSGLOGA                  YES, MSG AND LOG
*
BUSOUT   XI    IOBFL3,IOBBOE            SET/RESET ONE COUNT
         TM    IOBFL3,IOBBOE            TEST BUS OUT FLG ALONE   S20201
         BZ    MSGLOGA                  RETRY FAILED, MSG AND LOG
         B     RETRY                    RETRY THE ERROR ONCE
*
*
*****************************************************************
*NRF WITH MISSING ADDR MARK  IS TREATED AS SEEK CHK
*NRF ALONE RETRIED ONCE ON 2301 DEVICES
*NRF ALONE RETRIED ONCE ON 2301 AND 2321 DEVICES                 S20201
*NRF ALONE RESULTS IN A READ HA TO VERIFY CORRECT SEEK. IF NOT
*CORRECT SEEK THEN NRF TREATED AS SEEK CHK. IF SEEK CORRECT
*THEN ERROR ASSUMED TO BE USER ERROR,NO MSG NO LOG.
NORECF   TM    IOBSNS+1,IOBAMRK         IS THIS A MISSING ADDR MARK
         BO    MISSING                  BRANCH IF YES
         TM    IOBFL2,IOBRHA    ERP CHANNEL PROGRAM?             A42169
         BZ    NRFUSE                   NO                       A42169
         BAL   LINKRG1,INCR10           RETRY 10                 A42169
         B     RETRY                    RETRY 10                 A42169
NRFUSE   EQU   *                                                 A42169
       AIF     (&D2321 EQ 0).NORES2A                             S20201
* 2321 OPTION                                                    S20201
         B     NRFSET                   SKIP RETRY             @SA77511
*2321 END                                                        S20201
.NORES2A ANOP                                                    S20201
       AIF     (&D2301 EQ 0).ND2301A                             S20201
* 2301 OPTION                                                    S20201
         CLI   UCBUTB,DE2301            2301 DEVICE              S20201
         BNE   NRFSET                   NO, SKIP RTY             S20201
*2301 END                                                        S20201
.ND2301A ANOP                                                    S20201
       AIF     (&D2301 EQ 0 AND &D2321 EQ 0).N01N21              S20201
* 2301 OR 2321 OPTION                                            S20201
BITFLIP  DS    0H                       RETRY ONCE FOR 2301/2321 S20201
         XI    IOBFL3,IOBBOE            SET/RESET RTY CNT        S20201
         TM    IOBFL3,IOBBOE            TEST ISOLATED BIT        S20201
         BO    RETRY                    RETRY IF FIRST TIME      S20201
NRFSET   DS    0H                       CONNECTOR                S20201
*2301/2321 END                                                   S20201
.N01N21  ANOP                                                    S20201
         OI    IOBFL3,IOBNRF            SET NRF FLAG ON
         B     ERRHAB                   GO READ HOME ADDR
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD6A            @SA73886
.NEWD6A  ANOP                                                  @SA73886
*
*****************************************************************
*TRK COND RESULTS IN RHA RR0 TO GET THE ADDR OF THE ALT OR
*PRIME TRACK FROM R0. HA FLAG INDICATES PRIME OR ALT TRACK ID.
*SEE'ERNONE' FOR SECOND STAGE
TRACKCON B     ERRHAB                   GO READ HOME ADDR
*
*****************************************************************
* DATA CHK IS RETRIED 255 TIMES WITH
* RECALIBRATES EVERY 16TH TIME,MSG,LOG
DATACK   TM    4(CCWREG),SKIP           IS THIS A WRITE CHECK
         BZ    *+8                      BRANCH IF NO
         OI    IOBCNT+1,X'80'           SET WRITE INDICATOR
       AIF     (&D2321 EQ 0).NORES3                              S20201
* 2321 OPTION                                                    S20201
         CLI   UCBUTB,DE2321            2321 DEVICE              S20201
         BE    DATA21                   YES, GO TO 2321 SUPPORT  S20201
*2321 END                                                        S20201
.NORES3  ANOP                                                    S20201
INCR256  TM    IOBCNT,255               IS COUNTER FULLL
         BO    MSGLOG                   READ HA FOR MSG AND LOG IF FULL
         IC    WORK,IOBCNT              GET 256 RETRY COUNTER
         LA    WORK,1(WORK)             INCREMENT COUNT
         STC   WORK,IOBCNT              STORE UPDATED COUNT
         TM    IOBCNT,X'0F'             IS COUNT A MULTIPLE OF 16
         BNZ   RETRY                    BRANCH IF NO
*
         B     ERRECAL                  RECALIBRATE
*
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD7A              S20201
* 2305 OR 3330 OPTION                                            S20201
* 2305/3330 OVERRUN = RETRY 10 - THEN MSG BUT NO LOG             S20201
RTRY10   TM    IOBCNT+1,10              RETRY CNT = 10?          S20201
         BO    MSG                      YES, EXIT WITH MSG       S20201
RTRY10A  BAL   LINKRG1,INCR10           GO INCREMENT COUNT        M5595
         B     RETRY                    GO RETRY                 S20201
ECFDATA  TM    USNS2,X'40'              CORRECTABLE ERROR        S20201
         BZ    NOTCORR                  NOT ECF CORRECTABLE    @SA78124
         LH    ERREG2,ECCDISP           FETCH BACKWARDS DISP     S20201
         N     ERREG2,ZERO              CLEAR HI-ORDER 2 BYTES   S20201
         MVC   ECFCNT+2(2),6(CCWREG)    SAVE CCW CNT             S20201
       AIF     (&D2305 EQ 0 OR &D3330 EQ 0).D23XXB               S20201
* 2305 AND 3330 OPTION                                           S20201
         CLI   UCBUTB,MERLIN            MERLIN DEVICE            S20201
         BNE   DISPZ                    NO, SET ZEUS DISP        S20201
*2305 AND 3330 END                                               S20201
.D23XXB  ANOP                                                    S20201
       AIF     (&D3330 EQ 0).D23XXB1                             S20201
* 3330 OPTION                                                    S20201
         MVC   TOTALCNT+1(3),CLOCBYTE   FETCH RESTART DISP       S20201
         L     ERREG1,TOTALCNT          LOAD                     S20201
         SR    ERREG1,ERREG2            CALC FORWARD DISP        S20201
*3330 END                                                        S20201
.D23XXB1 ANOP                                                    S20201
       AIF     (&D2305 EQ 0 OR &D3330 EQ 0).D23XXB2              S20201
* 2305 AND 3330 OPTION                                           S20201
         B     ECCNT                    SKIP ZEUS CODE           S20201
*2305 AND 3330 END                                               S20201
.D23XXB2 ANOP                                                    S20201
       AIF     (&D2305 EQ 0).ZEUS2                               S20201
* 2305 OPTION                                                    S20201
* CALCULATE FORWARD DISPLACEMENT FOR ECC                         S20201
DISPZ    DS    0H                       CONNECTOR                S20201
         L     ERREG1,ECFCNT            FETCH CCW CNT            S20201
         SR    ERREG1,ERREG2            DECR BY ECC DISP         S20201
         LH    ERREG2,IOBCSW+6          FETCH RESID CNT          S20201
         N     ERREG2,ZERO              CLEAR HI-ORDER 2 BYTES   S20201
         SR    ERREG1,ERREG2            DECR BY RESID CNT        S20201
         BM    OVERRUN                  BRANCH IF DISP NEGATIVE  S20201
*2305 END                                                        S20201
.ZEUS2   ANOP                                                    S20201
*                                                                S20201
ECCNT    DS    0H                       CONNECTOR                S20201
         LA    ERREG2,ECCSYN            POINT AT ECC BYTE        S20201
         LA    ERREG3,3                 SET LOOP CNT             S20201
ECFLOOP1 DS    0H                       CONNECTOR                S20201
         CL    ERREG1,ECFCNT            ERROR IN THIS SEG        A51174
         BNL   ENDSEG                   NO                       S20201
         LTR   ERREG3,ERREG3           LOOP CNT ZERO?            A54490
         BZ    ENDSEG                   YES                      A54490
* CALCULATE ERROR ADDRES                                         S20201
         L     LNKRG2,0(CCWREG)         FETCH CCW ADDR           S20201
         LA    LNKRG2,0(LNKRG2)         CLEAR HI-ORDER BYTE      S20201
         AR    LNKRG2,ERREG1            ADD FORWARD DISPLACEMENT S20201
         CLI   0(ERREG2),0              IS ECC BYTE ZERO         S20201
         BE    ECFINCR                  YES, SKIP EXCL-OR        S20201
         TM    4(CCWREG),SKIP           SKIP FLAG ON             S20201
         BO    ECFINCR                  YES, SKIP EXCL-OR        S20201
* APPLY ECC BYTE TO CORE                                         S20201
         XC    0(1,LNKRG2),0(ERREG2)    DO IT                    S20201
ECFINCR  DS    0H                       INCR POINTERS            S20201
         LA    ERREG2,1(ERREG2)         POINT TO NEXT ECC BYTE   S20201
       AIF     (&D2305 EQ 0).ZEUS2A                              S20201
* 2305 OPTION                                                    S20201
         CLI   UCBUTB,ZEUSA             IS THIS A ZEUS ATHENS    S20201
         BNE   ECFINCR1                 NO, SKIP                 S20201
         LA    ERREG1,1(ERREG1)         ZEUSA GETS TWO INCREMENT S20201
ECFINCR1 DS    0H                       CONNECTOR                S20201
*2305 END                                                        S20201
.ZEUS2A  ANOP                                                    S20201
         LA    ERREG1,1(ERREG1)         INCR DISP                S20201
         BCT   ERREG3,ECFLOOP1          DECR LOOP CNT, LOOP      S20201
         B     LENCHK                   END OF LOOP            @SA77654
*                                                                S20201
ENDSEG   DS    0H                       CONNECTOR                S20201
       AIF     (&D3330 EQ 0).D23XXB4                             S20201
* 3330 OPTION                                                    S20201
         CLI   UCBUTB,MERLIN            MERLIN DEVICE            S20201
         BNE   ECFOVCK                  NO, SKIP                 S20201
         TM    4(CCWREG),DCHAIN         IS THIS DATA CHAINING    S20201
         BZ    LENCHK                   NO, CHK FOR INCOR LENGTH S20201
*                                                                S20201
         L     LNKRG2,TOTALCNT          FETCH                    S20201
         S     LNKRG2,ECFCNT            DECR BY CCW CNT          S20201
         ST    LNKRG2,TOTALCNT          RESTORE                  S20201
         S     ERREG1,ECFCNT            DECR DISP BY CCW CNT     S20201
         LA    CCWREG,8(CCWREG)         INCR TO NEXT CCW         S20201
* TEST IF CMD=TIC
         TM    0(CCWREG),CMDFSK                                  A45276
         BNE   SETECNT                  NO,UPDATE ECFCNT         S20201
         TM    0(CCWREG),CMDTIC                                 SA68579
         BZ    SETECNT                  NO, SKIP                SA68579
         L     CCWREG,0(CCWREG)         YES, POINT TO TIC ARG    S20201
SETECNT  DS    0H                       CONNECTOR                S20201
         MVC   ECFCNT+2(2),6(CCWREG)    UPDATE COUNT AREA        S20201
         B     ECFLOOP1                 LOOP                     S20201
*
LENCHK   DS    0H                       CHECK FOR INCORR LENGTH  S20201
         TM    SNS23,CULENG             INCORRECT LENG IN CU     S20201
         BZ    MER210                   BRANCH NO                S20201
         CLC   TOTALCNT(4),ECFCNT       COMPARE COUNTS           S20201
         BNE   MER210                   B IF INCORRECT LENG CALC S20201
*        IF NO INCORRECT LENGTH DETECTED IN CALC THEN EQUIP CK   S20201
         OI    USNS0,EQUIPSNS           INDICATE EQUIP CK        S20201
         B     ERSTUP                   TRY AGAIN AS EQUIP CK    S20201
MER210   EQU   *
         XC    IOBCSW+5(3),IOBCSW+5     CLEAR RESIDUL COUNT      A43747
         CLC   TOTALCNT(4),ECFCNT       COUNTS EQUAL             S20201
         BE    ECFOVCK                                           A43747
         L     ERREG1,ECFCNT            GET CCW COUNT             M1772
         S     ERREG1,TOTALCNT          SUBTRACT BYTE COUNT       M1772
         BM    NEGRES                   DIFFERENCE NEG           A43747
         ST    ERREG1,IOBCSW+4          STORE COMPUTED RESIDUL   A43747
NEGRES   EQU   *                                                 A43747
         TM    4(CCWREG),SLI            SUPPRESS LENGTH ON       S20201
         BO    ECFOVCK                  YES, BRANCH              S20201
         TM    IOBSNS+1,IOBOVF          OVERFLOW?                A51174
         BO    ECFOVCK                  YES                      A51174
         OI    IOBFL3+5,LNGCK           SET INCORR LENGTH        S20201
         B     CCWSET                   SET CH END DEV END       S20201
*                                                              @SA78124
NOTCORR  TM    IOBSNS+1,IOBOVF          OP INC=OVERFLOW RECORD @SA78124
         BZ    OVERRUN                  NO, RETRY TEN TIMES    @SA80070
         SLR   ERREG3,ERREG3            SET LOOP COUNT=0       @SA78124
         MVC   TOTALCNT+1(3),CLOCBYTE   GET FORWARD DISP       @SA78124
         B     ENDSEG                   BUILD RESTART CCW CHAIN@SA78124
*3330 END                                                        S20201
.D23XXB4 ANOP                                                    S20201
*                                                                S20201
ECFOVCK  BAL   ERREG3,ERXTPTR           GET POINTER TO EXTENT    S20201
         TM    0(ERREG2),X'01'          IS INHIBIT RETRY ON      S20201
         BZ    NOTPCI                   BRANCH IF NO             S20201
         TM    IOBFL1,STOP              SHOULD CHPGM BE RESTARTE S20201
         BZ    NOTPCI                   GO RESTART CHPGM         S20201
         MVI   IOBCOD,PCICODE           SET ERROR CORRECTED CODE S20201
         B     NOERROR                  DO NOT ATTEMPT RESTART   S20201
*                                                                S20201
NOTPCI   TM    4(CCWREG),X'40'          IS CMND CHAINING BIT ON  S20201
         BO    CMDCHN                   YES                      A47754
         TM    IOBSNS+1,IOBOVF          OVERFLOW?                A47754
         BO    OVFINC                   YES                      A47754
         B     CCWSET                                            A47754
CMDCHN   MVC   SECTVAL+1(1),ECCSCTR     GET REST RT S53TOR       S20201
         LA    ERREG2,8(CCWREG)         POINT AT NEXT CCW        S20201
* TEST IF CMD=TIC
         TM    0(ERREG2),CMDFSK                                  A51543
         BNE   NOTIC                    NO, SKIP                 S20201
         TM    0(ERREG2),CMDTIC                                 SA68579
         BZ    NOTIC                    NO, SKIP                SA68579
         L     ERREG2,0(ERREG2)         YES, POINT TO NEXT       S20201
NOTIC    TM    0(ERREG2),X'80'          RE MULTI TRK ON ?        A59754
         BZ    NOMT                     NO                       A59754
         OI    RDCNT,X'80'              CHANG OP TO MT           A59754
         CLI   0(ERREG2),NOOP           NO-OP CMD?             @SA76802
         BE    ENDCCW                   YES, BRANCH            @SA76802
NOMT     TM    0(ERREG2),CNTFLD+SECFLD  COUNT OR RD-SEC CMD    @SA64437
         BZ    ECFBLD                   NO, BRANCH               S20201
ENDCCW   MVI   ECFEND,ERREG1*16+UCBRG2  END CHAN PGM ON CCW 3  @SA76802
ECFBLD   LA    ERREG2,ECFPGM            GET ECF RESTART CH PGM   S20201
         ST    CCWREG,OFLOCCW5+4        SAVE CCW REG             A51509
         LA    CCWREG,8(CCWREG)         INCR TO NEXT CCW         S20201
         BAL   LINKRG1,EREP03           BUILD RESTART CHANNEL PG S20201
         L     CCWREG,OFLOCCW5+4        RESTORE CCW REG          A51509
         MVI   ECFEND,UCBRG2            RESTORE END TO CCW 4     S20201
         NI    RDCNT,X'7F'              RESTORE RD CNT OP        A59754
OVFINC   MVC   UCBSKA+6(1),ECCHEAD      GET CORRECT HEAD         A47754
         NI    UCBSKA+6,X'1F'           CLEAR HIGH ORDER BIT     S20201
         TM    IOBSNS+1,IOBOVF          IS THIS AN EOVEFLOW ERRO S20201
         BZ    NOUPD                    BRANCH IF NOT OVERFLOW   S20201
*                                                                S20201
         BAL   LNKRG2,ERCHUDT           INCREMENT TO NXT HEAD/CY S20201
*                                                                S20201
NOUPD    DS    0H                                                S20201
       AIF     (&OVF EQ 0).NOVF3A                                S20201
* OVERFLOW OPTION                                                S20201
         BAL   LINKRG1,OVFLCHK          CHECK FOR OVERFLOW       S20201
*OVERFLOW END                                                    S20201
.NOVF3A  ANOP                                                    S20201
         B     RESTART                                           S20201
*                                                                S20201
NOERROR  DS    0H                                                S20201
       AIF     (&OVF EQ 0).NOVF3B                                S20201
* OVERFLOW OPTION                                                S20201
         BAL   LINKRG1,OVFLCHK          CHECK FOR OVERFLOW       S20201
.NOVF3B  ANOP                                                    S20201
CCWSET   DS    0H                       CONNECTOR                S20201
         MVI   IOBCSW+4,X'0C'           SET NO ERROR             S20201
         LA    CCWREG,8(CCWREG)         POINT TO ERRCCW+8       SA66008
         IC    ERREG1,IOBFL3            SAVE FLAG 3             SA66008
         ST    CCWREG,IOBCSW            UPDATE CSW              SA66008
         STC   ERREG1,IOBFL3            RESTORE FLAG3           SA66008
         B     ERSOFT                   RETURN NO ERROR          S20201
RSTOVF   DS    0H                       CONNECTOR                S20201
         MVC   UCBSKA+6(1),ECCHEAD      PUT CORRECT HEAD IN SEEK S20201
         NI    UCBSKA+6,X'1F'           CLEAR PART OF HIGH HALF  S20201
        AIF    (&D2305 EQ 0 AND &D3330 EQ 0).NEWD18B           @SA73886
         BAL   LNKRG2,MZTEST            MERLIN FAMILY          @SA73886
         BE    OVFC                     YES, SKIP EXTENT       @SA73886
.NEWD18B ANOP                                                  @SA73886
         OI    IOBFL2,IOBNET            BYPASS EXTENT CK       @SA73886
         B     OVFC                     TO OVERFLOW HANDLING     S20201
*                                                                S20201
*2305/3330 END                                                   S20201
*OVERFLOW END                                                    S20201
.NEWD7A  ANOP                                                    S20201
*****************************************************************
*MISSING ADDR MARK AND SEEK CHK ARE RETRIED 10 TIMES WITH
* RECALIBRATES EACH TIME,MSG,LOG
MISSING  DS    0H
       AIF     (&D2321 EQ 0).NORES2                              S20201
* 2321 OPTION                                                    S20201
         CLI   UCBUTB,DE2321            2321 DEVICE              S20201
         BE    MAM21                    YES, GO TO 2321 SUPPORT  S20201
*2321 END                                                        S20201
.NORES2  ANOP                                                    S20201
SEEKCK   TM    IOBSNS,IOBCMDR           IS COMMAND REJECT ON
         BO    MSG                      BRANCH IF YES            S20201
       AIF     (&D2321 EQ 0).NORES4                              S20201
* 2321 OPTION                                                    S20201
         CLI   UCBUTB,DE2321            2321 DEVICE              S20201
         BE    SEEK21                   YES, GO TO 2321 SUPPORT  S20201
*2321 END                                                        S20201
.NORES4  ANOP                                                    S20201
         BAL   LINKRG1,INCR10           RETRY PGM 10 TIMES
         B     ERRECAL                  RECALIBRATE
*
*****************************************************************
*INTERVENTION REQUIRED-NON SYSRES DEVICE WTO,SYSRES DEVICE
* CLEAR FLAGS AND EXIT (RETRY),NO LOG
INTERVEN NI    IOBFL2,X'FF'-IOBRHA-IOBNET CLEAR FLAG 2
         NI    IOBFL3,IOBBOE            RESET ALL BUT COUNT
         TM    IOBST,X'30'              CC = 3?                   M1973
         BO    NORTY                    YES NO RETRY              M1973
         XI    IOBFL3,IOBBOE            TEST AND SET 1 COUNT
         BNZ   RETRY                    RETRY THE PROGRAM ONCE
*
NORTY    EQU   *                                                  M1973
         CLC   CVTDCB+1(3),IOBDCB+1     FETCH?                 @SA77437
         BE    SYSRES                   YES, TREAT AS SYSRES   @SA77437
         LA    DEBREG,0(DEBREG)         GET DEB ADDRESS        @SA77437
         CL    DEBREG,OBRDEB            OBR?                   @SA77437
         BE    SYSRES                   YES, TREAT AS SYSRES   @SA77437
         TM    UCBSTA,UCBSYS            SYSRES?                @SA78183
         BNO   ERPENT                   NO, EXIT WTO           @SA78183
         TM    IOBST,CC1OR3             SYSRES CC=3 ?          @SA78183
         BO    ERPENT                   YES, EXIT WTO          @SA78183
         TM    CVTOPTA(CVTREG),CVTNIP   NIP EXECUTING?         @SA78183
         BO    SYSRES                   YES, RETRY             @SA78183
ERPENT   EQU   *                                               @SA78183
         OI    IOBFL2,IOBPREV           INDICATE ERP ENTERED   @SA77193
         B     ERXCTL                   XCTL TO IGE00A AND WTO   A37849
*
SYSRES   EQU   *                                                 A41907
         OI    UCBFL1,UCBNRY            SET UCB NOT READY
         B     RETRY                    TAKE RETRY EXIT          A32176
*
*****************************************************************
*CHAIN CHK RETRY 10 TIMES
*OVER-RUN RETRY 10 TIMES
OVERRUN  BAL   LINKRG1,INCR10           RETRY PGM 10 TIMES
         B     RETRY                    TAKE RETRY EXIT          A32176
         AIF   (&LCS EQ 0).NOLCS2
*  LCS OPTION                                                    A35351
OVERUNA  BAL   LINKRG1,INCLCS          RETRY PGM 256 TIMES       A35351
         B     RETRY                   TAKE RETRY EXIT           A35351
*  LCS OPTION END                                                A35351
.NOLCS2  ANOP                                                    A35351
*
*****************************************************************
*UNIT EXCEPTION IF RETURN FROM RHA RR0 OF BAD TRK GO TO
* PHASE 2 OF TRK COND,OTHERWISE HARD USER ERROR,NO MSG,NO LOG
UNITEXC  TM    IOBFL2,IOBRHA            DID THIS OCCUR ON A READ HA/R0
         BO    ERNONE                   TREAT AS NO ERROR ON READ HA
*
*****************************************************************
*WRONG LENGTH HARD USER ERROR,NO MSG,NO LOG
* IF PREVIOUS ERROR GO TO STATUP AND CH END APPEND
* IF EOCYL,FILE PROT PREVIOUS GO TO CH END APPEND
* OTHERWISE HARD USER ERR-NO LOG,NO MSG
LENGTH   TM    IOBFL2,IOBPREV           HAS THE ERP BEEN PREV ENTERED
         BO    ERSOFT                   TREAT AS SOFT ERROR IF YES
         B     ERHARD                   HARD ERROR IF NO PREV ENTRY
*
*****************************************************************
*END OF CYLINDER-UPDATE UCB SEEK ADDRESS TO NEXT CYLINDER
*RESTART USER CH PRO AT ERR CCW,NO MSG,NO LOG
CYLINDER DS    0H                       FORCE CYL SW BY LGE HEAD S20201
         MVI   UCBSKA+6,X'F0'                                    S20201
         B     FPUPDATE                 GO TO UPDATE CODE       A63226
*****************************************************************
FILEPROT DS    0H                       FILE PROTECT RTN
*FILE PROTECT  UPDATE UCB SEEK ADDR TO DIFFERENT HEAD
* DETERMINE CAUSE
*  1.IN LINE SEEK
*  2.MULTI TRK READ OR SEARCH
*  3.LOGICAL END OF CYL ON DRUM
*  NO MSG NO LOG
         CLI   0(CCWREG),CMDFSK         CHECK IF 07 FULL SEEK
         BE    ERHARD                   HARD USER ERROR
         TM    0(CCWREG),CCWSK          TEST FOR CYL,HD SEEK
         BO    ERFPRT1                  YES GO MOVE SK TO UCB
       AIF     (&D2301 EQ 0 AND &D2303 EQ 0).ND0103              A32176
* 2301 OR 2303 OPTION
         BAL   ERREG3,ERXTPTR           GET PTR TO EXTENT
         TM    0(ERREG2),ERNSKM         TEST NO SEEK FILE MASK
         BO    FPUPDATE                 UPDATE SEEK ADDR         A32176
       AIF     (&D2301 EQ 0).ND2301B                             A32176
* 2301 OPTION
*                                       IF HEAD SEEKS ARE ALLOWED, THE
*                                       FILE PROTECT WAS CAUSED BY A
*                                       CYLINDER BOUNDRY. THE HEAD MUST
*                                       BE SET TO A MAXIMUM TO FORCE A
*                                       CYLINDER SWITCH BY THE ERCHUDT
*                                       ROUTINE
         CLI   UCBUTB,DE2301            IF THE DEVICE IS A 2301, THE
         BNE   *+12                     CCHH MUST BE INCREMENTED TO THE
         OI    UCBSKA+6,7               NEXT PROTECTION BOUNDRY WHICH
         B     FPUPDATE                 IS ON A HEAD ADDR DIVISABLE
*                                       BY 8
*2301 END
.ND2301B ANOP
         MVI   UCBSKA+6,X'F0'           EOC ON ALL OTHER DEVICES S20201
*2301/2303 END
.ND0103  ANOP
FPUPDATE BAL   LNKRG2,ERCHUDT           UPDATE HEAD AND CYL IF NECESS
SHACHAN  LA    ERREG2,ERPOCP1           REPOSITION CHANNEL PGM 1
OVFSHA   DS    0H
         NI    IOBFL2,X'FF'-IOBNET      CLEAR NO EXTENT TEST     A62858
       AIF     (&OVF EQ 0).NOVF2                                 A32176
* OVERFLOW OPTION
         BAL   LINKRG1,OVFLCHK          GO CHECK FOR OVERFLOW
*OVERFLOW END
.NOVF2   ANOP
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD10              S20201
* 2305 OR 3330 OPTION                                            S20201
         BAL   LNKRG2,MZTEST            CHECK FOR MERLIN/ZEUS    S20201
         BNE   NOSECT                   NO, SKIP SET SECTOR CP   S20201
SECTSET  DS    0H                       POINT AT SET SECTOR CP   S20201
         LA    ERREG2,EROCP3            CHAN PGM                 S20201
NOSECT   DS    0H                       CONNECTOR                S20201
*2305/3330 END                                                   S20201
.NEWD10  ANOP                                                    S20201
         CLC   UCBCCW4(8),0(CCWREG)     ERR IN ERP CCW4?        A63226
         BNE   ERFPBLD                  NO, SKIP                A63226
         L     CCWREG,UCBCSW-1          YES, PICK UP TIC ADDR @SA73886
ERFPBLD  DS    0H                       CONNECTOR               A63226
         B     EREP03A                  GO BLD CH PGM           A63226
         SPACE 1
ERFPRT1  L     ERREG1,0(CCWREG)         GET SEEK ARG
         MVC   UCBSKA+1(6),0(ERREG1)    MOVE SEEK ARG TO UCB
         TM    0(CCWREG),HDSK           IS THIS A SEEK HEAD
         LA    CCWREG,8(CCWREG)         UPDATE CCW PTR
         BZ    SHACHAN                  BRANCH IF SEEK CYLINDER
         LA    ERREG2,ERPOCP2           GET RHA CHANNEL PGM
         B     OVFSHA                   GO BUILD CHANNEL PGM
         SPACE  1
*****************************************************************
*ERP ENTERED WITH NO ERROR
*CAUSES
*A.RETURN FROM ERP INITIATED CHANNEL PROG
* 1.READ HOME ADDR,R0 - TRACK CONDITION(TRAVEL BETWEEN PRIME AND
*                       ALTERNATE TRACKS)
*                     - PERM ERROR TO OBTAIN INFORMATION FOR
*                       MESSAGE AND LOG
*                     - NO RECORD FOUND,TO VERIFY HARDWARE SEEK
* 2.RECALIBRATE , RECALIBRATE SEEK MECHANISM-WILL BE FOLLOWED
*                       BY A RETRY OF ORIGINAL SEQUENCE.
*B.SUCCESSFUL COMPLETION OF A USER INITIATED CH PROG
* 1.SUCCESSFUL RETRY,A RETRY OF USERS ENTIRE CH PRO
* 2.SUCCESSFUL RESTART,AN ERP GENERATED CHAN PRO POSITIONS
*                      THE DEVICE,VERIFIES THE POSITION AND
*                      THEN RECONNECTS THE USER CHAN PRO
*                     AT THE POINT CHAINING WAS BROKEN
*        EXAMPLES-TRACK CONDTION,END OF CYLINDER
* 3.SUCCESSFUL COMPLETION OF AN INTERUPTED CCW,THE ACTIONS
*                     DESCRIBED IN 2. ARE PERFORMED. IN
*                     ADDITION THE CCW AT WHICH ERROR OCCURRED
*                     IS       LOGICALLY COMPLETED BY THE ERP.
*        EXAMPLES-OVERFLOW INCOMPLETE AND FILE PROTECT (ON A
*                 SEEK CYL OR SEEK HEAD)
ERNONE   DS    0H                       NO ERROR
         TM    IOBFL2,IOBOFLO           OVERFLOW IND ON?         A33657
         BZ    *+8                      NO, SKIP                 A33657
         OI    IOBSNS+1,IOBOVF          YES, SET OFLO IN SNS     A33657
       AIF     (&D2321 EQ 0).NORES5                              S20201
* 2321 OPTION                                                    S20201
         CLI   UCBUTB,DE2321            2321 DEVICE              S20201
         BE    NOER21                   YES, GO TO 2321 SUPPORT  S20201
*2321 END                                                        S20201
.NORES5  ANOP                                                    S20201
* IF RETURN FROM RECALIBRATE (RSF=1) RETRY ORIG SEQUENCE
         TM    IOBFL3,IOBRSF            TEST IF RECALIBRATE
         BZ    ERRHA2                   NO,CHK FOR RHA
         BAL   LNKRG2,ERFLGS1           CLEAR FLAGS              A62858
         B     RETRY
         SPACE 1
* IF RETURN FROM A READ HOME ADDRESS (RHA=1,RSF=0) THEN
*   IF RESTORE ORIG ERROR SENSE AND CSW,SET UP CCWREG
ERRHA2   TM    IOBFL2,IOBRHA            CHK RHA FLAG
*BRANCH ON SUCCESSFUL RESTART ON TRK/CYL SWITCH,EOCYL,FILE PR,TCC
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD11              S20201
* 2305 OR 3330 OPTION                                            S20201
         BO    ERRHA3                   BRANCH IF RET FROM RHA   S20201
         BAL   LNKRG2,MZTEST            CHECK FOR MERLIN OR ZEUS S20201
         BNE   ERSOFT                   BRANCH IF NOT            S20201
         TM    EXTCSW,OBR               IS THERE AN OBR ENTRY    S20201
         BZ    ERSOFT                   BRANCH IF NO             S20201
         OI    IOBFL3,IOBLOG            MAKE A LOG ENTRY         S20201
         B     ERSOFT                   TO OBR                   S20201
ERRHA3   DS    0H                       CONNECTOR                S20201
         CLI   C0(CCWREG),RDLOG         IS THIS A READ LOG       S20201
         BNE   ERRHA4                   BRANCH IF NO             S20201
         NI    IOBFL2,X'FF'-IOBRHA      CLEAR RHA FLAG           S20201
         NI    IOBFL1,X'FF'-IOBSRS      CLEAR SRS               A57282
         TM    IOBFL3,IOBIN1            RESTART ADDR SAVED?     A57282
         BZ    RETRY                    NO, RETRY               A57282
         MVC   IOBRST,OFLOCCW2+4        YES, RESTORE RESTART    A57282
         NI    IOBFL3,X'FF'-IOBIN1      RESET IN1               A57282
         B     RESTART                  GO RESTART              A57282
ERRHA4   EQU   *                        CONNECTOR                S20201
*2305/3330 END                                                   S20201
         AGO   .NEWD11A                 SKIP IF 2305 OR 3330     S20201
.NEWD11  ANOP                                                    S20201
         BZ    ERSOFT                   STATUP SOFT ERR
.NEWD11A ANOP                                                    S20201
         NI    IOBFL2,X'FF'-IOBRHA      CLEAR RHA FLG
*RESTORE CONTEXT OF ORIG ERROR
         MVC   IOBSNS(2),UCBSSN         YES,RESTORE USER CONTEXT
         MVC   IOBCSW+1(7),UCBCSW       SENSE,CSW OF ORIG ERROR
SUBCALL  EQU   *
         BAL     LNKRG2,CCWSUB          GET ERR CCW PTR
*   IF MSG OR LOG=1 GO TO TRANSIENTS OBR,STATUP,SDR
         TM    IOBFL3,IOBMSG+IOBLOG     MSG OR LOG ON ERR
         BZ    ERNRF2                   CHECK NRF
         MVC   IOBRST(4),UCBRHA+1       YES MOVE CCHH TO IOB(LOG)
         B     ERHARD                   HARD ERR RETURN MSG,LOG
         SPACE 1
*   IF NRF=1 AND IF CC OF HOME ADDR (NOW IN UCB WK AR)=CC OF SEEK
*   ADDR.
*     IF YES PERM ERR ON USER
*     IF NO,THEN NRF REFLECTS A SEEK CHK,TREAT IT AS SUCH
ERNRF2   TM    IOBFL3,IOBNRF            CHECK NO REC FOUND
         BZ    ERTCC2                   NO,CHK TRK COND CHK
         CLC   UCBRHA+1(3),UCBSKA+3     HA CCH=CCH OF SEEK ARG   S20201
         BE    ERHARD                   YES, USER NO REC FND
         NI    IOBSNS+1,X'FF'-SNSNRF    CLEAR NRF IN IOBSNS
         OI    IOBSNS,IOBSKCK           NO,SET SEEK CHK
         OI    UCBSNS,IOBSKCK           SET UCBSNS FOR STAT
         B     ERSTUP                   BACK THRU ERP
         SPACE  1
*   IF RHA=1,MSG=LOG=NRF=0 THEN MUST BE TRACK CONDITION
*     R0 CONTAINS POINTER TO ALT OR PRIME
ERTCC2   MVC   UCBSKA+3(4),UCBRR0       BAD OR ALT TRK,SET SEEK
         TM    UCBRHA,TRKBAD            TEST BAD TRK
         BO    TCCBAD                   BRANCH IF CAUSED BY A BAD TRK
         BAL   LNKRG2,ERCHUDT           INCREMENT PAST ORIGIONAL BAD TK
*                                       IF THE TCC WAS CAUSED AT END
*                                       OF AN ALTERNATE TRACK
         NI    IOBFL2,X'FF'-IOBNET      CLEAR NO EXTENT TEST     A62858
         B     *+8                      SKIP FLAG SET FOR BAD TRACK
TCCBAD   OI    IOBFL2,IOBNET            SET NO EXTENT CK FOR ALT SEEK
TCCREPO  LA    ERREG2,ERPOCP1           ADDR OF REPOSITION CHANNEL PGM
       AIF     (&OVF EQ 0).NOVF3                                 A32176
* OVERFLOW OPTION
         BAL   LINKRG1,OVFLCHK          GO CHECK FOR OVERFLOW
*OVERFLOW END
.NOVF3   ANOP
         B     EREP03A                  BLD CHAN PGM AND RESTART
*****************************************************************
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD12              S20201
* 2305 OR 3330 OPTION                                            S20201
FLAGCK   DS    0H                       CONNECTOR                S20201
       AIF     (&D3330 EQ 0).D23XXC                              S20201
* 3330 OPTION                                                    S20201
         TM    USNS2,LOGSNS             IS THIS A MERLIN COUNTER S20201
         BZ    NOTMER                   BRANCH IF NO             S20201
PACKCNG  B     RETRY                    RESTART USERS CH PGM     S20201
*3330 END                                                        S20201
.D23XXC  ANOP                                                    S20201
*                                                                S20201
NOTMER   EQU   *
         AIF   (&D2305 EQ 0).NEWD12
         TM    USNS2,ZUSLOG             ZEUS LOG TO BE OFFLOADED
         BZ    MSGLOGB                  BRANCH IF NO            A63226
         OI    IOBFL2,IOBRHA            SET READ HA FLAG         S20201
         TM    IOBFL1,IOBSRS            ERP CH PGM?             A57282
         BZ    BLDRDLOG                 NO, SKIP RST SAVE       A57282
         MVC   OFLOCCW2+4(4),IOBRST     SAVE RESTART ADDR       A57282
         OI    IOBFL3,IOBIN1            IND RESTART SAVED       A57282
BLDRDLOG MVC   OFLOCCW5,LOGPGM          MOVE IN SKEL CH PGM     A57282
         LA    ERREG2,OFLOCCW5          ADDR OF RDLOG CH PGM    A57282
         ST    ERREG2,IOBRST            POINT TO IT             A57282
         LA    ERREG2,C26               INCR BY -                S20201
         A     ERREG2,CULOG              - HEADER LENGTH         S20201
         ST    ERREG2,OFLOCCW5          PUT BUFFER ADDR IN CCW1  A57282
         MVI   OFLOCCW5,RDLOG           REPLACE OVERLAID OP CODE A57282
         B     RESTART                  RESTART THE PROGRAM      S20201
         SPACE
MSGLOGB  DS    0H                       CONNECTOR               A63226
         MVC   EXTSNS,UCBSNS            SAVE SNS                A63226
         MVC   EXTCSW,IOBCSW            SAVE CSW                A63226
         MVI   EXTCSW,OBR               IND LOG NEEDED          A63226
         B     MSGLOG                   GO SET FOR MAG & LOG    A63226
**************************************************************** S20201
.NEWD12  ANOP                                                    S20201
MSGLOGA  OI    IOBFL2,IOBRHA            USE SEEK ADDR IN MSG
MSGLOG   OI    IOBFL3,IOBLOG
MSG      OI    IOBFL3,IOBMSG            SET MSG AND LOG FLAGS
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD13              S20201
* 2305 OR 3330 OPTION                                            S20201
         BAL   LNKRG2,MZTEST            CHECK FOR MERLIN OR ZEUS S20201
         BE    SEKCCH                   BRANCH IF MERLIN/ZEUS    S20201
*2305/3330 END                                                   S20201
.NEWD13  ANOP                                                    S20201
         TM    IOBFL2,IOBRHA            IS THIS A READ HA FOR A HA ERR
         BZ    ERRHAB                   BRANCH IF NO
SEKCCH   DS    0H                       CONNECTOR                S20201
         MVC   IOBRST(4),UCBSKA+3       USE LAST SEEK FOR CCHH
         B     ERHARD                   HARD ERROR
*****************************************************************
*SET UP TO READ HOME ADDRESS AND R0
ERRHAB   OI    IOBFL2,IOBRHA            SET READ HA FLAG
         LA    ERREG2,ERRHAP            GET READ HA CCW
         B     EREP03A                  BLD CHAN PGM AND RESTART
*
*****************************************************************
         SPACE 1
*****************************************************************
*RECALIBRATE IS ALWAYS FOLLOWED BY A RETRY OF USER STRING
ERRECAL  DS    0H                       BUILD RECALIBRATE CH PRO
         LA    ERREG2,ERRCLCP           ADDR RECAL SKELETON
RSFSET   OI    IOBFL3,IOBRSF            SET RECALIBRATE FLAG     S20201
       AIF     (&D2302 EQ 0).ND2302A                             A32176
* 2302 OPTION
         CLI   UCBUTB,DE2302            DEV=2302
         BNE   ERRECLB                  NO,GO COUNT AND BLD CH PR
         LA    ERREG2,ER2302A           SET UP HALF 2302 RECAL
         CLC   4(5,CCWREG),ERC2302+4    TEST IF SK CHK SK CYL 251
         BE    ERRECLA                  YES,EXCP HALF,NO COUN3
         LA    ERREG2,ERC2302           NO,SET UP FULL RECAL
*2302 END
.ND2302A ANOP
ERRECLB  DS    0H                       CONNECTOR
ERRECLA  DS    0H                       CONNECT
         OI    IOBFL2,IOBRHA            SET HA FLAG
*****************************************************************
EREP03A  LA    LINKRG1,RESTART          FORCE RETURN TO RESTART
EREP03   DS    0H                       FINISH CH PRO
         LA    ERCCWR,UCBCCW1           CH PRO (TO) ADDR
         BAL   LNKRG2,ERCPBLD           BUILD CH PRO
         BAL   LNKRG2,ERTICBD           BUILD TIC
         BR    LINKRG1                  RETURN TO CALLING ROUTINE
*****************************************************************
RETRY    TM    IOBFL2,IOBRHA            ERP CHANNEL PROGRAM
         BZ    *+8                      NO, SKIP SRS SET
RESTART  OI    IOBFL1,IOBSRS            SET RESTART FLAG ON
         LM    DCBREG,SNSREG,ERSAV      RESTORE REGS 4 -8        S20201
         OI    IOBFL2,IOBPREV           INDICATE ON RETURN THAT ERP
*                                       WAS ENTERED
         SR    9,9                      IOS EXPECTS ZERO IN THIS REG
         BR    ERRETR                   RETURN+0 RETRY OR RESTART CCW
*****************************************************************
         SPACE  1
*****************************************************************
*ERHARD HARD ERROR,DETERMINE IF MSG,LOG TO BE GIVEN
*  YES,CHK IF ERR ON ERROR TASK
*    YES,DO NOT GIVE MSG,LOG, TAKE (+4) EXIT
*    NO,GIVE MSG,LOG
*  NO,DO NOT GIVE MSG,LOG(+4) EXIT
ERHARD   NI    IOBFL1,X'FF'-IOBERR      HARD ERR,CLR ERR FLG
         BAL   LNKRG2,ERFLGS0           CLEAR FLAGS
       AIF     (&DDR EQ 0).NODDR                                 A32176
* DDR OPTION
       AIF     (&D2321 EQ 0).NORES5A                             S20201
* 2321 OPTION                                                    S20201
         CLI   UCBUTB,DE2321            2321                     S20201
         BE    LOGCHK                   YES, SKIP RES DDR RTN    A53829
*2321 END                                                        S20201
.NORES5A ANOP                                                    S20201
         USING RMSTABLE,RMSREG                                   A32176
         TM    UCBSTA,UCBSYS            IS THIS SYSRES
         BZ    DDRCHK                   NO, SKIP SYSRES CODE
       AIF     (&SYSDDR EQ 0).NOSYSR                             A32176
* SYSRES DDR OPTION
         CLC   CVTDCB+1(3),IOBDCB+1     FETCH ?
         BNE   LOGCHK                   NO, PERM ERR             A32176
         L     DDRBASE,SYSRPTR          GET SYSRES DDR POINTER
         BALR  LINKRG1,DDRBASE          TO SYSRES DDR CODE
         LTR   DDRBASE,DDRBASE          CHECK RETURN CODE
         BNZ   DDRTN                    TO DDRTN IF NOT ZERO
*SYSRES DDR END
.NOSYSR  ANOP
         B     LOGCHK                   PERM ERR                 A32176
DDRCHK   L     RMSREG,CVTLOG            GET LOGREC DCB POINTER
         L     RMSREG,0(RMSREG)         FIRST WORD IS RMS TABLE PTR
         TM    RMSTATUS,USEDDR          IS DDR TO BE USED
         BZ    LOGCHK                   NO, SKIP DDR CODE        A32176
         BAL   LNKRG2,ERSTPTR           LOGABLE ERR ?            A32176
         BE    ERHARD1                  NO                       A32176
         TM    IOBFL3,IOBLOG            LOGABLE & PERMANENT      A32176
         BZ    SETRHA                   NO                       A32176
DDRTN    OI    IOBFL2,IOBRHA            SET RHA                  A40851
         LM    DCBREG,SNSREG,ERSAV      RESTORE REGS 4-8         A40851
         SR    9,9                      IOS EXPECTS  ZERO IN 9
         B     16(ERRETR)               GO TO DDR ROUTINE
*DDR END
.NODDR   ANOP
*
LOGCHK   BAL   LNKRG2,ERSTPTR           LOGABLE ERROR
         BE    ERHARD1                  NO
SETRHA   BAL   LNKRG2,FETCHK            FETCH OR OBR = NO RETURN
         OI    IOBFL2,IOBRHA            SET RHA(INDICATE D/A WTO)
         B     ERXCTL                   XCTL TO WTO
         SPACE 1
*****************************************************************
*ERSOFT- DETERMINE IF STATUP SHOULD BE CALLED,IF NOT SOFT ERR
* EXIT WITHOUT STATUP,-IF STATUP SHOULD BE CALLED CHECK THAT
* ERROR NOT AGAINST ERROR TASK-IF ERROR TASK DO NOT GO TO STATUP.
ERSOFT   NI    IOBFL1,X'FF'-IOBEX-IOBERR SOFT ERROR
         BAL   LNKRG2,ERFLGS0           CLEAR FLAGS
         BAL   LNKRG2,ERSTPTR           CHECK IF LOGABLE ERROR
         BE    ERHARD1                  THE STATISTICS COUNTERS DO NOT
*                                       NEED TO BE UPDATED
         BAL   LNKRG2,FETCHK            FETCH OR OBR = NO RETURN
ERSOFT1  NC    IOBCNT(2),ERRCLR         CLR ERROR COUNTS         A36382
ERXCTL   LM    DCBREG,SNSREG,ERSAV      RESTORE REGS 4 -8        S20201
         SR    9,9                      IOS EXPECTS ZERO IN THIS REG
         B     8(ERRETR)                XCTL TO IGE00A & WTO/STATUP
*****************************************************************
         SPACE  1
*FETCHK  BYPASSES TRANSIENT ROUTINE FOR ERRORS ON FETCH OR OBR
FETCHK   CLC   CVTDCB+1(3),IOBDCB+1     FETCH
         BE    ERHARD1                  YES
         LA    DEBREG,0(DEBREG)         CLEAR HI ORDER BYTE
         CL    DEBREG,OBRDEB            OBR'S DEB
         BCR   7,LNKRG2                 NO, RETURN TO CALLER
ERHARD1  MVI   IOBFL3,IOCLBT            HARD ERR NO MSG NOR LOG
         LM    DCBREG,SNSREG,ERSAV      RESTORE REGS 4 -8        S20201
         SR    9,9                      IOS EXPECTS ZERO IN THIS REG
         B     4(ERRETR)                RETURN+4 PERM ERROR NO MSG/LOG
*****************************************************************
         SPACE 1
       AIF     (&CCH EQ 0).NASR3                                 A32176
* CCH OPTION
ERASR0   L     ERREG1,CVTLOG            GET LOGREC DCB
         L     ERREG1,0(ERREG1)         PICK UP CCH PARM PTR
         L     ERREG1,0(ERREG1)         PICK UP PARM
ERASR1   CLI   0(ERREG1),CCHDELMT       IS THIS LAST
         BCR   8,LINKRG1                RETURN IF NO ERPIB FOUND
         CLC   2(2,TSTREG),2(ERREG1)    PARM=THIS UCB
         BE    ERASR2                   YES
         LA    ERREG1,TABLELGN(ERREG1)  NO,GET NEXT
         B     ERASR1                   CONTINUE SCAN
         SPACE 1
ERASR2   TM    ERPIBFLG(ERREG1),NORETRY TEST IF RETRY NOT
         MVI   0(ERREG1),X'00'          CLEAR ERPIB AREA
         MVC   1(7,ERREG1),0(ERREG1)
         BO    ERASR3                   BRANCH IF NOT RETRYABLE
         LTR   LINKRG1,LINKRG1          SET COND CODE 2  RETRYABLE ERR
ERASR3   BR    LINKRG1                  RETURN
*CCH END
.NASR3   ANOP
*****************************************************************
         AIF   (&LCS EQ 0).NOLCS3
*  LCS OPTION                                                    A35351
INCLCS   TM    IOBCNT+1,255             COUNTER FULL?            A35351
         BO    MSGLOG                   YES MSG AND LOG          A35351
         B     *+8                      NO                       A35351
*  LCS OPTION END                                                A35351
.NOLCS3  ANOP                                                    A35351
INCR10   TM    IOBCNT+1,10              IS RETRY COUNT EQUAL TO 10
         BO    MSGLOG                   BRANCH IF COUNT IS 10
         IC    WORK,IOBCNT+1            GET TEN COUNTER
         LA    WORK,1(WORK)             INCREMENT COUNT
         STC   WORK,IOBCNT+1            STORE UPDATED COUNT
         BR    LINKRG1                  RETURN IF COUNTER NOT FULL
*****************************************************************
      AIF      (&OVF EQ 0).NOVFA                                 A32176
* OVERFLOW OPTION
OVFLCHK  TM    IOBSNS+1,IOBOVF          TEST FOR OVERFLOW
         BO    OVFC                     YES,OVERFLOW
         LA    ERCCWR,OFLOCCW1          GET ADD OF ERP CH PGM  @SA76342
         CR    ERCCWR,CCWREG            LAST SEG?              @SA76342
         BNE   RETURN7                  NO, RETURN               A32176
         ST    CCWREG,IOBRST            SET RESTART ADDR
         B     RESTART                  GO RESTART               A32176
RETURN7  BR    LINKRG1                  NO OVERFLOW RETURN TO CALLER
         SPACE 1
*****************************************************************
*OVERFLOW INCOMPLETE
* CAUSED BY
* 1.FILE PROTECT INTERUPT DURING OVERFLOW RECORD READ
* 2.END OF CYLINDER   *DITTO*
* 3.TRK COND CHK      *DITTO*
*RECOVERY-
* BUILD REPOSITION CHANNEL PROG (SEARCH ID REC 1 NEXT SEGMENT)
* BUILD CCW TO COMPLETE USERS INTERUPTED COMMAND
* BUILD TIC BACK TO USER'S  STRING AT ERRCCW +8
* THIS SECTION HANDLES RECOVERY FOR SEARCH KEY AND DATA(SCAN)
OVFC     TM    IOBSNS,IOBTKC            CHK IF TRK COND W/ OVERFL
         BZ    OVFA                     NO,SKIP NEXT TEST
         TM    UCBRHA,TRKBAD            TEST FOR BAD TRACK TO ALT
         BO    OVFB                     YES,USE HA AS SEARCH ARG
OVFA     MVC   UCBRHA+1(4),UCBSKA+3     USE SEEK ADDR FOR SRCHARG
OVFB     DS    0H
         OI    IOBFL2,IOBOFLO           SET OVERFLOW IND         A33657
         MVI   UCBRHA+5,X'01'           'R' OF CCHHR = 1
         LA    ERCCWR,OFLOCCW1          GET ADD OF ERP CH PGM  @SA76342
         LA    ERREG2,OFLOCCW3          GET ADD OF ERP CH PGM  @SA76342
         CR    ERREG2,CCWREG            ERP CH PGM?            @SA76342
         BE    EROVF3                   YES,FO OVFL CCW3 ONLY
         LA    ERREG2,OFLCP             OFLCP IN OVF AREA
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD14              S20201
* 2305 OR 3330 OPTION                                            S20201
         BAL   LNKRG2,MZTEST            CHECK FOR MERLIN OR ZEUS S20201
         BNE   BUILDIT                  BRANCH IF NO             S20201
         MVI   OFLEND4,ERREG1*16+UCBRG2 END CHANNEL PGM ON CCW 4 S20201
BUILDIT  BAL   LNKRG2,ERCPBLD           DO IT                    S20201
         MVI   OFLEND4,UCBRG2           RESTORE CHANNEL PGM      S20201
*2305/3330 END                                                   S20201
         AGO   .NEWD14A                 SKIP IF 2305 OR 3330     S20201
.NEWD14  ANOP                                                    S20201
         BAL   LNKRG2,ERCPBLD           DO IT
.NEWD14A ANOP                                                    S20201
         ST    CCWREG,OFLOCCW5+4        SAVE ERR CCW POINTER
         LA    ERREG1,2                 SET UP COUNTER
         LA    ERCCWR,OFLOCCW3          SET BLD REG
EROVF1   LA    CCWREG,8(CCWREG)         NEXT CCW
         LA    ERCCWR,8(ERCCWR)         UPDATE BLD REG
* TEST IF CMD=TIC
         TM    0(CCWREG),CMDFSK                                  A45276
         BNE   EROVF1A                  NO,CONSTRUCT TIC TO USER
         TM    0(CCWREG),CMDTIC                                 SA68579
         BZ    EROVF1A                  NO, SKIP                SA68579
         CLC   OFLOCCW5+5(3),1(CCWREG)  NXT CCW POINTS AT ERR CCW
         BE    EROVF2                   YES SKIP TIC BUILD
EROVF1A  DS    0H
         BAL   LNKRG2,ERTICBD           NO,BUILD TIC TO USER
EROVF2   DS    0H
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD15              S20201
* 2305 OR 3330 OPTION                                            S20201
         BAL   LNKRG2,MZTEST            CHECK FOR ZEUS           S20201
         BE    EROVF2A                  BRANCH IF YES            S20201
*2305/3330 END                                                   S20201
.NEWD15  ANOP                                                    S20201
         BCT   ERREG1,EROVF1            BACK TO SECOND POSITION
EROVF2A  L     CCWREG,OFLOCCW5+4        RESTORE CCWREG TO ERRCCW S20201
EROVF3   DS    0H                       SET UP RECOVER CCW
         LH    ERREG2,6(CCWREG)         GET ERRCCW COUNT
         LH    ERREG1,IOBCSW+6          LOAD RESID COUNT
         SLR   ERREG2,ERREG1            CCWCNT-RESIDUAL
         N     ERREG2,ZERO              CLEAR UPPER 2 BYTES
         AL    ERREG2,0(CCWREG)         ADD DATA ADDR+CNT-RESID
EROVF3A  ST    ERREG2,OFLOCCW3          SAVE NEW DATA ADDR       S20201
         MVC   OFLOCCW3(1),USNS5        SET CU GENERATED CMD     S20201
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD15A             S20201
* 2305 OR 3330 OPTION                                            S20201
         BAL   LNKRG2,MZTEST            CHECK FOR 2305/2330      S20201
         BNE   USECMD                   NO                       S20201
         MVC   OFLOCCW3(1),RSTCMD       SET CMD CODE             S20201
USECMD   DS    0H                       CONNECTOR                S20201
*2305/3330 END                                                   S20201
.NEWD15A ANOP                                                    S20201
         MVC   OFLOCCW3+4(1),4(CCWREG)  COPY FLGS FROM ERRCCW    A32176
         TM    0(CCWREG),SEARCH         SEARCH TYPE CMD?        SA78212
         BO    EROVF3B                  YES, SET CNT,SKIP =1    SA78212
         MVC   OFLOCCW3+6(2),IOBCSW+6   COUNT=RESID              A32176
         CLC   IOBCSW+6(2),ZERO         TEST RESID=0             A32176
         BNE   EROVF4                   NO, NORMAL               A32176
EROVF3B  DS    0H                                               SA78212
         MVI   OFLOCCW3+7,X'01'         SET CNT TO 1             A32176
         OI    OFLOCCW3+4,SKIP          SET SKIP BIT             A32176
EROVF4   DS    0H                       CONNECTOR                S20201
         LA    ERCCWR,OFLOCCW1          POINT AT OFLOCCW1        S20201
         ST    ERCCWR,IOBRST            STORE IN RESTART ADDR FLD
         B     RESTART                  GO RESTART               A32176
         SPACE 1
*****************************************************************
*OVERFLOW END
.NOVFA   ANOP
         SPACE 1
*ERCPBLD SETS UP A CHANNEL PROGRAM FOR EXECUTION
*ERREG2 IS THE SOURCE REG POINTING AT THE PROTOTYPE CH PRO
*ERCCWR IS THE TARGET (WHERE TO PUT IT)
*THE 5TH BYTE OF EACH PROTOTYPE CCW CONTAINS CONTROL INFORMATION
*  A.WHETHER TO RELOCATE THE CCW AND ITS ARGUMENT ADDR.
*  B.HOW TO RELOCATE IT
*  C.END OF CHANNEL PROGRAM
* ERROR SENSE AND CSW ARE SAVED,APPROPRIATE FLAGS AND ADDR.S SET
ERELOC   LR    ERREG1,0                 ERREG1=PROPER BASE REG
ERCPBLD  DS    0H                       BUILD CH PRO
         ST    ERCCWR,IOBRST            SAVE RESTART ADDR
         MVC   UCBSSN(2),IOBSNS         SAVE PREVIOUS SENSE      S20201
         MVC   UCBCSW(7),IOBCSW+1       SAVE CSW                 S20201
ERCPB2   IC    ERREG1,5(ERREG2)         PICK UP RELOCATE REG
         EX    ERREG1,ERELOC            LOAD PROPER BASE TO RELOC
         AL    ERREG1,0(ERREG2)         ADD DISPL AND CMDCODE
         ST    ERREG1,0(ERCCWR)         PUT
         MVC   4(4,ERCCWR),4(ERREG2)    MOVE 2ND HALF
         TM    5(ERREG2),ERREG1*16      TERMINATOR CHECK
         LA    ERREG2,8(ERREG2)         INCREMENT(FROM)LOC
         LA    ERCCWR,8(ERCCWR)         INCREMENT(TO)LOC
         BNO   ERCPB2                   CONTINUE BUILDING
         BR    LNKRG2                   EXIT
         SPACE  1
*****************************************************************
*ERTICBD BUILDS A TIC IN UCB WK AR CONNECTING ERP CH PRO WITH
*   REMAINDER OF USER CH PRO (IT RESOLVES TIC TO TIC PROBLEMS)
ERTICBD  DS    0H                       BUILD TIC TO USER CH PRO
         TM    IOBFL2,IOBRHA            TEST IF ERP FUNCTION
         BCR   ONES,LNKRG2              YES NO TIC TO USER
         MVC   0(4,ERCCWR),0(CCWREG)    MOVE IN USER CMD
* TEST IF CMD=TIC
         TM    0(CCWREG),CMDTIC                                 SA68579
         BZ    NOTIC1                   NO, SKIP                SA68579
         TM    0(CCWREG),CMDFSK                                  A45276
         BCR   EQ,LNKRG2                YES,EXIT
NOTIC1   DS    0H                       CONNECTOR               SA68579
         ST    CCWREG,0(ERCCWR)         STORE CCWREG
         MVI   0(ERCCWR),CMDTIC         SET TIC CMD
         BR    LNKRG2                   RETURN
         SPACE 1
*****************************************************************
*ERCHUDT UPDATES CYL AND HEAD ADDR IN UCB WITHIN LIMITS OF EITHER
*   DEB EXTENTS IF SPLIT CYL OR DEV EXTENTS IF NOT
ERCHUDT  DS    0H                       UPDATE SEEK ADDR CCHH
         L     ERREG2,CVTDTAB           DEVICE TABLE ADDR        S20201
         SR    ERREG3,ERREG3            CLEAR                    S20201
         IC    ERREG3,UCBUTB            DEVICE CHAR INDEX INDEX  S20201
         IC    ERREG3,0(ERREG3,ERREG2)  DEVICE CHAR INDEX        S20201
         LA    ERREG2,0(ERREG2,ERREG3)  POINT AT DEVICE CH&R ENT S20201
         MVC   TRKLIM,0(ERREG2)         MOVE CCHH CHAR'S TO TRKL S20201
       AIF     (&D3330 EQ 0).D23XXE                              S20201
* 3330 OPTION                                                    S20201
         CLI   UCBUTB,MERLIN            MERLIN DEVICE            S20201
         BNE   CNTSET                   NO                       S20201
         MVC   TRKLIM(2),FOXFOX         FORCE CYLS HIGH          S20201
CNTSET   DS    0H                       CONNECTOR                S20201
*3330 END                                                        S20201
.D23XXE  ANOP                                                    S20201
         LA    ERREG1,UPCNT             SET CNT                  S20201
         TM    DEBOPFL,DEBSCA           SPLIT CYL                S20201
         BZ    TRKINC                   NO, INCR TRK ADDR        S20201
         BAL   ERREG3,ERXTPTR           POINT AT EXTENT          S20201
         CLC   UCBSKA+6(1),DEBEND+3(ERREG2)  HD AT LIMIT         S20201
         BL    TRKINC                   NO, INCR HD              S20201
         MVC   UCBSKA+6(1),DEBST+3(ERREG2)   YES, SET HD TO STAR S20201
         BCTR  ERREG1,0                 DECREMENT CNT            S20201
TRKINC   DS    0H                       INCREMENT TRACK          S20201
         IC    ERREG2,UCBSKA+2(ERREG1)  FETCH                    S20201
         LA    ERREG2,1(ERREG2)         INCREMENT                S20201
         STC   ERREG2,UCBSKA+2(ERREG1)  STORE NEW SEEK ADDR      S20201
* ** STORE CHAR IN 'I' PORTION OF INST CMPRE ** *                S20201
         STC   ERREG2,CMPRE+1           SET UP COMPARE           S20201
         LA    ERREG2,TRKLIM-1(ERREG1)  ACCESS BASE              S20201
       AIF     (&D3330 EQ 0).D23XXF                              S20201
* 3330 OPTION                                                    S20201
         CLI   UCBUTB,MERLIN            MERLIN DEVICE            S20201
         BNE   CMPRE                    NO                       S20201
         CLI   CMPRE+1,0                BYTE OFLO                S20201
         BE    CNTDN                    YES, NEXT BYTE           S20201
         CLI   CMPRE+1,X'FF'            MAX FOR BYTE             S20201
         BE    CHUOUT                   YES                      S20201
*3330 END                                                        S20201
.D23XXF  ANOP                                                    S20201
CMPRE    DS    0H                                                S20201
         CLI   0(ERREG2),0              SEEK ADDR G/T DEVICE LIM S20201
         BH    CHUOUT                   NO, DONE                 S20201
         SR    ERREG2,ERREG2            CLEAR                    S20201
         STC   ERREG2,UCBSKA+2(ERREG1)  RESET TO ZERO            S20201
CNTDN    DS    0H                       DECREMENT COUNT          S20201
         BCT   ERREG1,TRKINC            NEXT FIELD               S20201
         MVC   UCBSKA+3(4),TRKLIM      DEVICE LIMIT EXCEDED      A39252
CHUOUT   DS    0H                       CHECK EXTENTS            S20201
         BAL   ERREG3,ERXTPTR           POINT AT EXTENT          S20201
         NI    IOBFL2,X'FF'-IOBNET      CLR NO-XTENT FLAG      @SA73886
         CLC   UCBSKA+3(4),DEBEND(ERREG2)    OUT OF EXTENT       S20201
         BH    CHUDONE                  YES, RTRN W/COND CODE    S20201
         SH    ERREG1,FOUR              SET COND CODE            S20201
CHUDONE  DS    0H                       UPDATE COMPLETE          S20201
         BR    LNKRG2                   RTRN W/COND CODE         S20201
*****************************************************************
*ERSTPTR COMPUTES LOCATION OF STATAB AND RETURNS POINTER (ERREG2)
*   TO CALLING ROUTINE. ALSO IT CHECKS IF STATAB WORK AREA OR IOB
*   ERR COUNTS =ZERO AND RETURNS THE CONDITION CODE AS INDICATOR
*   WHETHER TO GO TO STAT UPDATE.
ERSTPTR  DS    0H                       FORM PTR TO STATAB(ERREG2)
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD16              S20201
* 2305 OR 3330 OPTION                                            S20201
         ST    LNKRG2,RGSAV             SAVE REG                 S20201
         BAL   LNKRG2,MZTEST            CHECK FOR MERLIN OR ZEUS S20201
         L     LNKRG2,RGSAV             RESTORE REG              S20201
         BNE   ERSTPTRA                 NO, SKIP M/Z CODE        S20201
         TM    EXTCSW,OBR               IS THERE AN OBR ENTRY    S20201
         MVI   EXTCSW,0                 CLEAR LOG FLAG           S20201
         BCR   ONES,LNKRG2              YES,RETURN               S20201
         TM    IOBFL3,IOBMSG            MSG NEEDED?              A53829
         BR    LNKRG2                   RETURN WITH COND CODE    S20201
ERSTPTRA DS    0H                       CONNECTOR                S20201
*2305/3330 END                                                   S20201
.NEWD16  ANOP                                                    S20201
         L     ERREG1,STATAB            GET STATAB ADDR
         SR    ERREG2,ERREG2            CLEAR 2
         IC    ERREG2,UCBSTI            INDEX
ERSTPT1  CH    UCBREG,0(ERREG1)         CURRENT UCB IN RANGE
         BL    ERSTPT2                  YES,CONTINUE
         LA    ERREG1,2(ERREG1)         NO,INC STATAB LOOK-UP
         LA    ERREG2,256(ERREG2)          INC INDEX
         B     ERSTPT1                  GO TO NEXT
         SPACE  1
ERSTPT2  DS    0H
       AIF     (&D2314 EQ 0).ND2314M                             A32176
* 2314 OPTION
         CLI   UCBUTB,DE2314            DEV=2314
         BNE   ERSTPT3                  NO,TAKE NORM
         SR    ERREG1,ERREG1            YES,GET PHYSICAL DR ADDR
         TM    USNS4,X'0F'              NO DRIVE ASSIGNED        S20201
         BO    ERSTPT3                  YES,APPLY STAT TO 0
         IC    ERREG1,USNS4             GET DRIVE NO.            S20201
         ST    LNKRG2,RGSAV             SAVE REG                 A52329
         LA    LNKRG2,15                GET CLEAR BYTE           A52329
         NR    ERREG1,LNKRG2            CLEAR HIGH 2 BITS        A52329
         L     LNKRG2,RGSAV             RESTORE REG              A52329
         AR    ERREG2,ERREG1            ADD TO BASE INDEX
*2314 END
.ND2314M ANOP
ERSTPT3  MH    ERREG2,ERRC10            INDEX*10 (10 BYTES/ENTRY)
         A     ERREG2,STATAB            ADD STATAB BASE
         CLC   SERWRK(2,ERREG2),ZERO    TEST FOR DEVICE ERR
         BCR   7,LNKRG2                 EXIT UNEQUAL
         CLC   IOBCNT(2),ZERO           TEST FOR CH OR DEV ERR
         BR    LNKRG2                   RETURN
         SPACE 1
*****************************************************************
*CCWSUB LOADS CCWREG WITH ADDR OF ERR CCW
CCWSUB   L     CCWREG,IOBCSW            GET CCW ADDR FROM CSW
         SL    CCWREG,ERRCN1            CCWREG-8
         BR    LNKRG2                   RETURN
         SPACE  1
*****************************************************************
*ERFLGS0 CLEARS ALL IOBFLAGS EXCEPT ERR,EX,MSG,LOG FOR FINAL EXIT
*ERFLGS1 DOES NOT RESET IOBPREV, IOBOFLO OR IOBNET               A62858
ERFLGS0  DS    0H                       CLEAR FLAGS FOR FINAL EXIT
         NI    IOBFL2,X'FF'-IOBNET-IOBPREV-IOBOFLO     FLG2      A62858
         LA    ERREG2,UCBCCW1+8         GET LOW LIMIT OF WORK  @SA75963
         CLR   CCWREG,ERREG2            WITHIN WORK AREA?      @SA75963
         BL    ERFLGS1                  NO-USE NEW CSW ADDR    @SA75963
         LA    ERREG2,OFLOCCW5+8        GET HI LIMIT OF WORK   @SA75963
         CLR   CCWREG,ERREG2            WITHIN WORK AREA?      @SA75963
         BH    ERFLGS1                  NO-USE NEW CSW ADDR    @SA75963
         MVC   IOBSNS(2),UCBSSN         NO-USE NORM SNS        @SA75963
         MVC   IOBCSW+1(7),UCBCSW       NO-USE NORM CSW        @SA75963
         MVC   IOBCSW+5(3),CSWLOC+5     USE CSW COUNT          @SA75963
CSWLOC   EQU   64                                              @SA75963
         MVI   IOBCSW+4,X'0C'           SET CE,DE              @SA75963
ERFLGS1  DS    0H                       CONNECTER                A62858
         NI    IOBFL2,X'FF'-IOBRHA      FLG2                     A62858
         NI    IOBFL3,IOBMSG+IOBLOG     F3,CLEAR ALL BUT MSG+LOG
         NI    IOBFL1,X'FF'-IOBSRS      CLEAR RESTART FLAG       MS0058
         BR    LNKRG2                   RETURN
         SPACE 1
*****************************************************************
*ERXTPTR-POINT TO CURRENT EXTENT IN DEB
ERXTPTR  DS    0H                       FORM PTR TO CURRENT EXTNT
         SR    ERREG2,ERREG2            CLEAR IC REG
         IC    ERREG2,IOBDAM            GET EXTENT NO.
         SLL   ERREG2,4                 MULT BY 16
         LA    ERREG2,DEBMDB(ERREG2)    FILE MSK ADDR
         BR    ERREG3                   RETURN
         SPACE  1
       AIF     (&D2321 EQ 0).NORES7                              S20201
* 2321 OPTION                                                    S20201
**************************************************************** S20201
*RESIDENT SUPPORT PECULIAR TO 2321 DEVICE                        S20201
         SPACE 1                                                 S20201
*DATA CHECK AND MISSING ADDR MKR                                 S20201
DATA21   DS    0H                       2321 DATA CHECK          S20201
MAM21    DS    0H                       2321 MISS ADDR MK        S20201
         CLI   IOBCNT,226               IS ERR CNT MAXIMUM       S20201
         BNL   MSGLOG                   YES, RHA FOR MSG         S20201
         CLI   IOBCNT,8                 IS CNT LESS THAN EIGHT   S20201
         BL    INCR                     YES, RETRY               S20201
         TM    IOBCNT,X'1F'             IS CNT MULT OF 32        S20201
         BZ    ADJSEK                   YES, SEEK TO ADJACENT ST S20201
         TM    IOBCNT,1                 IS CNT ODD               S20201
         BO    INCR                     YES, RETRY               S20201
         SPACE 1                                                 S20201
*SET UP FOR HEAD SHAKE                                           S20201
         LA    ERREG2,ERPSHAK           SET FROM ADDR            S20201
         MVC   UCBSEK1,UCBSKA+1         SET SEK1                 S20201
         MVC   UCBSEK2,UCBSEK1          SET SEK2                 S20201
         SR    ERREG1,ERREG1            ZERO                     S20201
         STH   ERREG1,UCBSEK1+4         SET CYL/HD TO ZERO       S20201
         LA    ERREG1,SWPMASK           FETCH CYL/HD MASK        S20201
         STH   ERREG1,UCBSEK2+4         SET CYL/HD TO 4/19       S20201
         OI    IOBFL3,IOBSWP            SET SWEEP FLAG           S20201
         B     ERRECLA                  BUILD CHAN PGM AND REST& S20201
         SPACE 1                                                 S20201
*SET UP FOR SEEK TO ADJACENT STRIP                               S20201
ADJSEK   MVC   UCBSEK2,UCBSKA+1         FETCH SEEK ADDR          S20201
         SR    ERREG1,ERREG1            ZERO                     S20201
         IC    ERREG1,UCBSEK2+3         FETCH STRIP ADDR         S20201
         CLI   UCBSEK2+3,0              IS IT ZERO               S20201
         BNE   *+8                      NO, SKIP NEXT INST       S20201
         LA    ERREG1,2                 YES, FORCE RESULT OF ONE S20201
         BCTR  ERREG1,0                 DECREMENT                S20201
         STC   ERREG1,UCBSEK2+3         STORE ADJACENT STRIP ADDRS20201
         OI    IOBFL3,IOBPIK            SET PIK FLAG             S20201
         LA    ERREG2,ERPSEEK           POINT AT CHAN PGM        S20201
         B     ERRECLA                  GO BUILD CP AND RESTART  S20201
*INCREMENT COUNT AND RETURN                                      S20201
INCR     DS    0H                       CONNECTOR                S20201
         IC    ERREG1,IOBCNT            FETCH CNT                S20201
         LA    ERREG1,1(ERREG1)         INCR                     S20201
         STC   ERREG1,IOBCNT            REPLACE                  S20201
FLGOFF   BAL   LNKRG2,ERFLGS0           CLEAR FLAGS              S20201
         B     RETRY                    RETRY                    S20201
         SPACE 1                                                 S20201
*SEEK CHECK                                                      S20201
SEEK21   DS    0H                       2321 SEEK CHECK          S20201
         TM    IOBSNS+1,IOBAMRK         MISS ADDR MARK           S20201
         BO    MSGLOGA                  YES, PERM ERR            S20201
         BAL   LINKRG1,INCR10           NO, UPDATE CNT           S20201
         MVC   UCBSEK2,UCBSKA+1         FETCH SEEK ADDR          S20201
         XC    UCBSEK2+2(3),SKMSK       CHANGE S/C, STP, CYL     S20201
         CLI   UCBSEK2+4,5              CYL TO HIGH              S20201
         BL    SEKPT                    NO, SKIP ALTER           S20201
         MVI   UCBSEK2+4,0              YES, CYL = 0             S20201
SEKPT    LA    ERREG2,ERPSEEK           POINT TO SEEK CCW        S20201
         B     RSFSET                   RSF = 1                  S20201
*NO ERROR                                                        S20201
NOER21   DS    0H                       2321 NO ERROR            S20201
         TM    IOBFL3,IOBPIK            PIK FLAG ON              S20201
         BO    INCR                     YES                      S20201
         TM    IOBFL3,IOBRSF            RESTORE FLAG ON          S20201
         BO    FLGOFF                   YES                      S20201
         TM    IOBFL3,IOBSWP            SWEEP FLAG ON            S20201
         BZ    ERRHA2                   NO, GO CHECK RHA         S20201
         TM    IOBCNT+1,SWPSVN          SWP CNT END IN SEVEN     S20201
         BO    SHKDN                    YES, RESET AND RETRY     S20201
         IC    ERREG1,IOBCNT+1          NO, INCR CNT AND RETRY   S20201
         LA    ERREG1,16(ERREG1)        INCR                     S20201
         STC   ERREG1,IOBCNT+1          REPLACE                  S20201
         B     RESTART                  RESTART                  S20201
SHKDN    NI    IOBCNT+1,X'FF'-SWPSVN    RESET SWEEP CNTR         S20201
         B     INCR                     GO RETRY                 S20201
*2321 END                                                        S20201
.NORES7  ANOP                                                    S20201
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD18              S20201
* 2305 OR 3330 OPTION                                            S20201
         SPACE 1                                                 S20201
** ** ** ** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** **  S20201
*                                                                S20201
*         THIS ROUTINE CHECKS FOR MERLIN, ZEUS-A OR ZEUS-C       S20201
*                                                                S20201
MZTEST   CLI   UCBUTB,MERLIN            IS THIS A MERLIN         S20201
         BE    MZRET                    BRANCH IF YES            S20201
ZEUSTST  CLI   UCBUTB,ZEUSA             IS THIS A ZEUS ATHENS    S20201
         BE    MZRET                    BRANCH IF YES            S20201
         CLI   UCBUTB,ZEUSC             IS THIS A ZEUS CORINTH   S20201
MZRET    BR    LNKRG2                   RETURN WITH CONDITION CO S20201
*                                                                S20201
** ** ** ** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** **  S20201
*2305/3330 END                                                   S20201
.NEWD18  ANOP                                                    S20201
         EJECT
*****************************************************************
*CCW1    SHA   UCBSKA+3,CC+SLI,3        VERIFY CORRECT SEEK (CCH)
ERPOCP1  DS    0F                       REPOSITION CH PRO SHA
         DC    X'39'                    OP                       A32176
         DC    AL3(UCBSKA+3-UCB)        ARG                      A32176
         DC    X'60'                    CC+SLI                   A32176
         DC    AL1(UCBREG)              RELOCATE INFO FOR ERCPBLD
         DC    X'0004'                  LENGTH                   A32176
*CCW2    TIC   *-8
         DC    X'08'                    OP                       A32176
         DC    AL3(UCBCCW1-ERU)         ARG                      A32176
         DC    X'60'                    CC+SLI                   A32176
         DC    AL1(UCBRG2)              RELOCATE INFO FOR ERCPBLD
         DC    X'0000'                  LENGTH                   A32176
ERPOCP2  DS    0F                       REPOSITION CH PRO RHA
*CCW3,1  RHA   UCBRHA,SLI+SKIP+CC,5
         DC    X'1A'                    OP                       A32176
         DC    AL3(UCBRHA-ERU)          ARG                      A32176
         DC    X'70'                    CC+SLI+SKIP              A32176
         DC    AL1(ERREG1*16+UCBRG2)    RELOCATE INFO AND END FLG
         DC    X'0005'                  LENGTH                   A32176
         SPACE 1
*****************************************************************
*CCW1    RHA   UCBRHA,CC+SLI,5
ERRHAP   DC    X'1A'                    OP                       A32176
         DC    AL3(UCBRHA-ERU)          ARG                      A32176
         DC    X'60'                    CC+SLI                   A32176
         DC    AL1(UCBRG2)              RELOCATE INFO FOR ERCPBLD
         DC    X'0005'                  LENGTH                   A32176
*CCW2    RR0   UCBRR0,SLI,4
         DC    X'16'                    OP                       A32176
         DC    AL3(UCBRR0-ERU)          ARG                      A32176
         DC    X'20'                    SLI                      A32176
         DC    AL1(ERREG1*16+UCBRG2)    RELOCATE INFO AND END FLG
         DC    X'0004'                  LENGTH                   A32176
         SPACE 1
*****************************************************************
*CCW1    RECA  0,SLI,1
ERRCLCP  DS    0F                       RECALIBRATE CH PRO
         DC    X'13'                    OP                       A32176
         DC    AL3(0)                   NO ARG                   A32176
         DC    X'60'                    CC+SLI                   A32176
         DC    AL1(UCBRG2)              RELOCATE IN ERU
         DC    X'0001'                  LENGTH                   A32176
         SPACE 1
*  CCW2  NOP   TO KEEP RQE CONNECTED THROUGH THE SEEK
         DC    X'03'                    OP                       A32176
         DC    AL3(0)                   NO ARG                   A32176
         DC    X'20'                    SLI                      A32176
         DC    AL1(ERREG1*16+UCBRG2)    RELOCATE INFO AND END FLG
FOUR     DC    X'0004'                  SHARE LENGTH AS DC       S20201
       AIF     (&D2305 EQ 0 ).ZEUS6                              S20201
* 2305 OPTION                                                    S20201
** ** ** ** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** **  S20201
* CCW1   OFFLOAD ZEUS CONTROL UNIT LOG                           S20201
LOGPGM   DS    0F                       ALIGN                    S20201
         DC    X'24'                    CONTROL UNIT OFFLOAD CMD S20201
         DC    AL3(0)                   ADDR SUPPLIED BY PROGRAM S20201
         DC    X'20'                    SUPPRESS LENGTH ONLY     S20201
         DC    AL1(ERREG1*16+UCBRG2)    RELOCATE INFO AND END FL S20201
         DC    X'0080'                  LENGTH                   S20201
*                                                                S20201
*2305 END                                                        S20201
.ZEUS6   ANOP                                                    S20201
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD20              S20201
* 2305 OR 3330 OPTION                                            S20201
** ** ** ** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** **  S20201
EROCP3   DS    0F                       RPS RESTART CP           S20201
*CCW1    SET SECTOR ZERO                                         S20201
         DC    X'23'                    SET SECTOR               S20201
         DC    AL3(UCBCCW1+6-ERU)       GET ZEROS                S20201
         DC    X'60'                    CC+SLI                   S20201
         DC    AL1(UCBRG2)              BUILD CCW IN WORK AREA   S20201
         DC    X'0001'                  ONE BYTE SPECIFING SECTO S20201
*CCW2    RHA   UCBRHA,CC+SLI+SKIP,5                              S20201
         DC    X'1A'                    RHA                      S20201
         DC    AL3(UCBRHA-ERU)          ARG                      S20201
         DC    X'70'                    CC+SLI+SKIP              S20201
         DC    AL1(ERREG1*16+UCBRG2)    RELOCATE INFO + END FLG  S20201
         DC    X'0005'                  LENGTH                   S20201
         SPACE 1                                                 S20201
ECFPGM   DS    0F                       ECF CHAN PGM             S20201
* CCW1   RHA   UCBRHA,CC+SLI+SKIP,5                             SA65470
         DC    X'1A'                    RHA                     SA65470
         DC    AL3(UCBRHA-ERU)          ARG                     SA65470
         DC    X'70'                    CC+SLI+SKIP             SA65470
         DC    AL1(UCBRG2)              RELOCATE INFO           SA65470
         DC    X'0005'                  LENGTH                  SA65470
*CCW2    SEARCH ID EQUAL                                         S20201
         DC    X'31'                    SEARCH ID EQUAL          S20201
         DC    AL3(EXCCHHR-UCBSNS2)     CCHHR IN SENSE BYTES     S20201
         DC    X'60'                    CC+SLI                   S20201
         DC    AL1(SNSREG)              SENSE AREA               S20201
         DC    X'0005'                  LENGTH                   S20201
*CCW3    TIC *-8   ALSO CONTAINS SECTOR VALUE                    S20201
         DC    X'08'                    TIC                      S20201
         DC    AL3(UCBCCW2-ERU)         *-8                      S20201
         DC    X'60'                    CC+SLI                   S20201
ECFEND   DC    AL1(UCBRG2)              RELOCATION INFO AND END  S20201
SECTVAL  DC    X'0000'                  SECTOR                   S20201
*CCW4    READ COUNT  USED IF TIC TO READ DATA CCW FOLLOWS        S20201
RDCNT    DC    X'12'                    READ COUNT               A59754
         DC    AL3(UCBCCW4-ERU)         ANY ADDRESS OK           S20201
         DC    X'70'                    NO DATA TRANSFER         S20201
         DC    AL1(ERREG1*16+UCBRG2)    END FLAGS                S20201
         DC    X'0001'                  LENGTH                   S20201
MZOFLCP  DS    0F                       FIRST OVFL CCW           S20201
*CCW0    SET SECTOR CCW                                          S20201
         DC    X'23'                    SET SECTOR               S20201
         DC    AL3(OFLOCCW2+7-ERU)      TIC CONTAINS VALUE       S20201
         DC    X'60'                    CC+SLI                   S20201
         DC    AL1(UCBRG2)              RELOCATE INFO            S20201
         DC    X'0001'                  LENGTH                   S20201
*                                                                S20201
*2305/3330 END                                                   S20201
.NEWD20  ANOP                                                    S20201
         SPACE 1
       AIF     (&OVF EQ 0).NOVFB                                 A32176
* OVERFLOW OPTION
*****************************************************************
OFLCP    DS    0F
*OFLCCW1  SIDEQ UCBRHA+1,CC+SLI,05
         DC    X'31'                    OP                       A32176
         DC    AL3(UCBRHA+1-ERU)        ARG                      A32176
         DC    X'60'                    CC+SLI                   A32176
         DC    AL1(UCBRG2)              RELOCATE  INFO           A32176
         DC    X'0005'                  LENGTH                   A32176
*OFLCCW2  TIC  *-8,CC
         DC    X'08'                    OP                       A32176
         DC    AL3(OFLOCCW1-ERU)        ARG                      A32176
         DC    X'60'                    CC+SLI                   A32176
         DC    AL1(UCBRG2)              RELOCATE INFO            A32176
OFSECT   DC    X'0000'                  SECTOR VALUE             S20201
*OFLCCW3  X    (ERRCCW DATA ADDR+CCWCNT-RESID),ERRCCWFLAGS,RESID FROM C
         DC    X'00'                    OP FROM SENSE BYTE 5     A32176
         DC    AL3(0)                   ARG CALCULATED BY PGM    A32176
         DC    X'00'                    FLGS FROM ERR CCW        A32176
         DC    AL1(UCBRG2)              RELOCATE INFO            A32176
         DC    X'0001'                  LENGTH SET FROM RESID    A32176
*OFLOCCW4  TIC  *-8
         DC    X'08'                    OP                       A32176
         DC    AL3(OFLOCCW3-ERU)        ARG                      A32176
         DC    X'60'                    CC+SLI                   A32176
OFLEND4  DC    AL1(UCBRG2)              RELOCATE INFO OF END     S20201
         DC    X'0001'                  LENGTH                   A32176
*OFLOCCW5  TIC *-16
         DC    X'08'                    OP                       A32176
         DC    AL3(OFLOCCW3-ERU)        ARG                      A32176
         DC    X'20'                    SLI                      A32176
         DC    AL1(ERREG1*16+UCBRG2)    RELOCATE INFO + END FLG  A32176
         DC    X'0000'                  LENGTH                   A32176
         SPACE 1
*OVERFLOW END
.NOVFB   ANOP
       AIF     (&D2302 EQ 0).ND2302B                             A32176
* 2302 OPTION
*****************************************************************
*RECALIBRATE FOR 2302
ERC2302  DS    0F                       SEEK TO CYL 251          A32176
*CCW1    SK    ERD02SK,CC+SL1,6
         DC    X'07'                    OP                       A32176
         DC    AL3(ERD02SK-ER2311)      ARG                      A32176
         DC    X'60'                    CC+SLI                   A32176
         DC    AL1(BASREG)              RELOCATE INFO            A32176
         DC    X'0006'                  LENGTH                   A32176
*CCW2    SK    ERD02SK+4,CC+SLI,6
ER2302A  DC    X'07'                    SEEK TO CYL 0            A32176
         DC    AL3(ERD02SK+4-ER2311)    ARG                      A32176
         DC    X'60'                    CC+SLI                   A32176
         DC    AL1(BASREG)              RELOCATE INFO            A32176
         DC    X'0006'                  LENGTH                   A32176
*CCW3    NOP   0,SLI,1                  NOP TO KEEP RQE CONNECTED
         DC    X'03'                    OP                       A32176
         DC    AL3(0)                   ARG                      A32176
         DC    X'20'                    SLI                      A32176
         DC    AL1(ERREG1*16+UCBRG2)    RELOCATE INFO + END FLG  A32176
         DC    X'0001'                  LENGTH                   A32176
         SPACE  1
ERD02SK  DS    0C                       2302 SEEK ARG            A32176
         DC    X'000000FB000000000000'                           A32176
         SPACE 1                                                 A32176
*2302 END
.ND2302B ANOP
       AIF     (&D2321 EQ 0).NORES8                              S20201
* 2321 OPTION                                                    S20201
**************************************************************** S20201
*2321 CCWS                                                       S20201
ERPSHAK  DS    0F                       &L975                    S2020
*CCW1    SK    UCBSEK1,CC,6             SEEK VIA ARG IN UCBSEK1  S20201
         DC    X'07'                    SEEK                     S20201
         DC    AL3(UCBSEK1-UCB)        ARG                       S20201
         DC    X'40'                    CC                       S20201
         DC    AL1(UCBREG)              RELOCATE INFO FOR ERCPBLDS20201
         DC    X'0006'                  LENGTH                   S20201
*CCW2,1  SK    UCBSEK2,CC,6             SEEK VIA ARG IN UCBSEK2  S20201
ERPSEEK  DC    X'07'                    SEEK                     S20201
         DC    AL3(UCBSEK2-UCB)         ARG                      S20201
         DC    X'40'                    CC                       S20201
         DC    AL1(UCBREG)              RELOC&T5 INFO FOR ERCPBL S20201
         DC    X'0006'                  LENGTH                   S20201
*CCW3,2  NOP   0,SLI,1                                           S20201
         DC    X'03'                    NOP                      S20201
         DC    AL3(0)                   ARG=N/A                 S20201
         DC    X'20'                    SLI                      S20201
         DC    AL1(ERREG1*16+UCBRG2)    RELOCATE INFO AND END FL S20201
         DC    X'0001'                  LENGTH                   S20201
**************************************************************** S20201
SKMSK    DC    X'010101'                SEEK MASK                S20201
*2321 END                                                        S20201
.NORES8  ANOP                                                    S20201
*****************************************************************
ERSAV    DS    5F                       SAVE AREA REGS 4-8       S20201
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD21              S20201
* 2305 OR 3330 OPTION                                            S20201
ECFDISP  DC    F'0'                     TEMP SAVE                S20201
TOTALCNT DC    F'0'                     TEMP STORAGE             S20201
ECFCNT   DC    F'0'                     TEMP STORAGE             S20201
*2305/3330 END                                                   S20201
.NEWD21  ANOP                                                    S20201
RGSAV    DS    F                        TEMP SAVE                A51174
ZERO     DC    X'0000FFFF'              HI ORDER ZERO MASK       A32176
         ORG   ZERO+2                   ALIGN                    S20201
FOXFOX   DS    2C                       CONSTANT X'FFFF'         S20201
ERRCN1   DC    F'8'                     CONSTANT 8
TRKLIM   DS    F                        DEVICE TABLE WORK AREA   S20201
ERRC10   DC    H'10'                    TEN
OBRDEB   DC    A(IFBDEB)
NORLOG   DC    X'02B8'                  MASK FOR NORMAL LOG      A36382
         AIF   (&D2301 EQ 0).LOGREG2
SPLOG    DC    X'4800'                  MASK FOR 2301 LOG        A36382
.LOGREG2 ANOP
ERRCLR   DC    X'0080'                  MASK TO CLEAR ERR CNT    A36382
ERINC    DS    H                        ERROR INCREMENT
         DS    C                        AND SENSE SHUFFLE
*MACRO ASSEMBLY IDENTIFIER
         DC    C'IEC23XXF &A&B&C&D&E&F&G&H&I&J&K '
         DC    CL50'IEC23XXF 10-04-77 US08113 PATCH   '        @SA79916
       AIF     (&SYSDDR EQ 0).NOSYSR2                            A32176
* SYSRES DDR OPTION
SYSRPTR  DC    V(IGFDDR05)
*SYSRES DDR END
.NOSYSR2 ANOP
         EJECT
*CVT DEFINITION
CVT      DSECT
CVTTCB   DS    A                        PTR TO (NXT,CURR)TCB
         ORG   CVT+44                   ALIGN                    A32176
CVTXCTL  DS    A                        ADDR OF XCTL RTN         A32176
         ORG   CVT+64                   ALIGN                    A32176
CVTDTAB  DS    A                        DEVICE CHARACTERISTICS TB
CVTNTRP  DS    A                        ERR INTERPRETER
CVTWTO   DS    A                        WRITE TO OPERATOR
         ORG   CVT+84                   ALIGN                    A32176
CVTDCB   DS    A                        SYS1-LOGREC DCB          A32176
         ORG   CVT+112                  ALIGN                    A32176
STATAB   DS    A                        CVT LOC OF STATAB
CVTLOG   DS    A                        LOG RTN ADDRESS
       AIF     (&DDR EQ 0).NODDRA                                A32176
* DDR OPTION
         EJECT
RMSTABLE DSECT
         ORG   RMSTABLE+20              ALIGN                    A32176
RMSTATUS DS    C                        DO/ DO NOT USE DDR
*DDR END
.NODDRA  ANOP                                                    A32176
         EJECT
DEB      DSECT
DEBTCB   DS    A                        TCB PTR                  A32176
         DS    A                        ALIGN                    A32176
DEBOPFL  DS    C                        OPEN FLAGS               A32176
         ORG   *-1                      ALIGN                    A32176
DEBIRB   DS    A                        IRB PTR                  A32176
         DS    3A                       ALIGN                    A32176
DEBDCB   DS    A                        ASSOCIATED DCB ADDR      A32176
         DS    A                        ALIGN                    A32176
DEBMDB   DS    C                        FILE MASK                A32176
         EJECT
UCB      DSECT
SRTEJBNR DS    C                        JOB INFO                 S20201
UCBFL5   DS    C                        FLAG #5                @SA73886
UCBID    DS    C                        ALWAYS FF                S20201
UCBSTA   DS    C                        UCBSTATUS
         DS    H                        ALIGN                    A32176
UCBFL1   DS    X                        FLAG1                    A32176
         DS    C                        ALIGN                    A32176
UCBERP   DS    C                        TRANSIENT ERP ADDR
UCBSTI   DS    X                        STATISTICS TABLE INDEX   A32176
         ORG   UCB+17                   ALIGN                   SA63263
UCBOFEAT DS    X                        OPT. FEATURES           SA63263
         ORG   UCB+19                   ALIGN                    A32176
UCBUTB   DS    X                        DEVICE TYPE              A32176
         ORG   UCB+22                   ALIGN                    A32176
UCBSNS1  DS      XL6                    SENSE                    S20201
         ORG   UCBSNS1+2                ALIGN                    S20201
EXTSNS0  DS    F                        EXTENDED SENSE POINTER   S20201
UCBVOL   DS    CL6                      VOLID OF PACK            S20201
UCBSTAT  DS    XL2                      STATUS BYTE B            S20201
       AIF     (&D2321 EQ 0).NORES9                              S20201
* 2321 OPTION                                                    S20201
         ORG   UCBSNS1+6                ALIGN                    S20201
UCBSEK1  DS    CL6                      2321 ERP SKA-1           S20201
UCBSEK2  DS    CL6                      2321 ERP SKA-2           S20201
*2321 END                                                        S20201
.NORES9  ANOP                                                    S20201
         ORG   UCB+48                   ALIGN                    A32176
UCBSKA   DS    XL8                      SEEK ADDR MBBCCHHR       A32176
         ORG   UCBSKA+3                 ALIGN                    S20201
UCBCCHH  DS    CL4                      SEEK ARG                 S20201
         ORG   UCB+60                   ALIGN                    A32176
UCBWK    DS    A                        DISJOINT UCB WK AR PTR
       AIF     (&D2321 EQ 0).NORES10                             S20201
* 2321 OPTION                                                    S20201
         ORG   UCB+216                  ALIGN                    S20201
UCBWK1   DS    A                        2321 WK AREA PTR         S20201
*2321 END                                                        S20201
.NORES10 ANOP                                                    S20201
         EJECT
UCBSNS2  DSECT                          UCB SENSE BYTES          S20201
UCBSNS   DS    XL6                      STANDARD SIX BYTES       S20201
         ORG   UCBSNS2                  ALIGN                    S20201
USNS0    DS    C                        SENSE BYTE 0             S20201
USNS1    DS    C                        SENSE BYTE 1             S20201
USNS2    DS    C                        SENSE BYTE 2             S20201
USNS3    DS    C                        SENSE BYTE 3             S20201
USNS4    DS    C                        SENSE BYTE 4             S20201
USNS5    DS    C                        SENSE BYTE 5             S20201
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD21A             S20201
* 2305 OR 3330 OPTION                                            S20201
*2305-2330 SENSE BYTES                                           S20201
         ORG   UCBSNS2+3                ALIGN                    S20201
RSTCMD   DS    C                        RESTART CMD              S20201
         DS    C                        ALIGN                    S20201
CURCYL   DS    C                        CURRENT CYL              S20201
ECCHEAD  DS    C                        CURRENT HEAD             S20201
FMSG     DS    C                        FORMAT/MSG             @SA78184
EXCCHHR  DS    CL5                      CCHHR                    S20201
ECCSCTR  DS    C                        SECTOR FOR ERROR RECORD  S20201
         ORG   UCBSNS2+15               ALIGN                    S20201
CLOCBYTE DS    CL3                      RESTART DISP             S20201
ECCDISP  DS    CL2                      DISPLACEMENT             S20201
ECCSYN   DS    CL3                      ECC BYTES IN SENSE       S20201
*2305/3330 END                                                   S20201
SNS23    DS    C                        SENSE BYTE 23            S20201
.NEWD21A ANOP                                                    S20201
         EJECT                                                   S20201
ERU      DSECT                          WK AREA                  S20201
         ORG   ERU+92                   ALIGN                    S20201
MAINUCB  DS    F                        POINTER TO MAIN UCB      S20201
         DS    F                        ALIGN                    S20201
CULOG    DS    F                        PTR TO CONTROL UNIT LOG  S20201
UCBCCW1  DS     D                       ERP CONSTRUCTED CH PROGS
UCBCCW2  DS     D                       FOR TRK COND,END OF CYL,
UCBCCW3  DS     D                       FILE PROT,PERM ERR ETC
UCBCCW4  DS     D
UCBCCW5  DS     D
         ORG   UCBCCW3+4                ALIGN                    A32176
UCBRR0   DS    CL4                      R0 READ IN BUFFER
UCBRHA   DS    CL5                      HA READ IN BUFFER
UCBRNO   DS    CL1                      REC NO ATTACHED FOR SEARCH
         ORG   UCBCCW4+7                ALIGN                    A32176
UCBSSN   DS    CL2                      SAVE AREA SENSE ORIG ERROR
UCBCSW   DS    CL7                      SAVE AREA CSW ORIG ERROR
* OVERFLOW OPTION                                                S20201
* OVER FLOW ERP WORK AREA
       AIF     (&D2305 EQ 0 AND &D3330 EQ 0).NEWD22              S20201
* 2305 OR 3330 OPTION                                            S20201
         ORG   UCBCCW5                  MERLIN/ZEUS OVRFLW WK AR S20201
OFLOCCW0 DS    D                        OVERFLOW SET SECTOR CCW  S20201
*2305/3330 END                                                   S20201
.NEWD22  ANOP                                                    S20201
         ORG   UCBCCW5+8                ALIGN                    S20201
OFLOCCW1 DS    D                        ERP CONSTRUCTED CH PROGS
OFLOCCW2 DS    D                        FOR OVER FLOW INCOMPLETE
OFLOCCW3 DS    D                        AT TRK COND,END OF CYL,OR
OFLOCCW4 DS    D                        FILE PROTECT             A32176
OFLOCCW5 DS    D
EXTCSW   DS    D                        EXTENDED CSW SAVE AREA   S20201
EXTSNS   DS    CL24                     EXTENDED SENSE SAVE AREA S20201
*2305/3330 END                                                   S20201
.NEWD23  ANOP                                                    S20201
*OVERFLOW END                                                    S20201
         EJECT
IOB      DSECT
IOBFL1   DS    CL1       FLAGS1,IOB-ERROR,EXCEPTION,RESTART
IOBFL2   DS    CL1       FLAGS2,READ HA,NO EXTNT CHK,CC-HH UPDATE
IOBSNS   DS    CL2       FIRST 2 SENSE BYTES
IOBCOD   DS    CL1       ECB CODE
         DS    AL3       ALIGN                                   A32176
IOBCSW   DS    D         CSW
         ORG   IOBCSW    ALIGN                                   A32176
IOBFL3   DS    CL1       TRK COND,NO REC FND,BUS OUT,RESTORE CMD
         ORG   IOBCSW+8  ALIGN                                   A32176
IOBST    DS    A         CH PGM START ADDR                       A32176
IOBDCB   DS    A         DCB ADDR                                A32176
IOBRST   DS    A         CH PGM RESTART ADDR                     A32176
         ORG   IOB+30    ALIGN                                   A32176
IOBCNT   DS    H         ERROR RETRY CNT                         A32176
IOBDAM   DS    CL8       MBBCCHH OF USER REQUEST
         MEND
