         MACRO
         IECIUCB &UCBNO,&DEVAD,&TYPE,&DEVTAB,&ERRTAB,&STATAB,&LCHN,    X
               &DEVNDX,&TYPEA,&ACHMSK,&SYSRES,&SQOPT,&MP,&GR,&TMSK,    X
               &ACU,&OFF,&DEVADE,&SHR
         LCLC  &DEVADDR                                         SA62675
         LCLA  &COUNT
         AIF   (&UCBNO NE 1).NOTITLE
         TITLE 'UNIT CONTROL BLOCKS - INPUT/OUTPUT SUPERVISOR'
.NOTITLE ANOP
         AIF   (&MP EQ 0).NMP           TEST FOR MULTI-PROC       19023
         AIF   ('&TYPE' EQ 'TP' OR '&TYPE' EQ 'GR').TPGR          M4743
         DC    AL1(&ACU),AL3(0)         MP FLAG AND FILL          19022
         AGO   .NMP                     BRANCH AROUND             M4773
.TPGR    DC    F'0'                     MP FILL                   M4773
.NMP     ANOP                                                     19023
&DEVADDR SETC  '&DEVADE'                 SET CAR ADDRESS          20201
         AIF   ('&TYPE' NE 'TP' AND '&TYPE' NE 'GR').ST GRAPHIC OR TP?
&DEVADDR SETC  '&DEVAD'                SET TP CAR ADDRESS         20201
.ST      ANOP
.*           **  THIS IS THE CODING FOR STANDARD PORTION OF ALL UCBS **
UCB&UCBNO  DC  AL2(&ACHMSK),X'FF'       JOBNO,ALLOC MSK, ID       20201
         AIF   ('&TYPE' EQ 'TP' OR '&TYPE' EQ 'GR').ONLINE
         AIF   (&OFF EQ 0).ONLINE       TEST FOR ONLINE           20201
         DC    X'&SYSRES'               STAT 'A'                  20201
         AGO   .NORM                                              20201
.ONLINE  DC    X'8&SYSRES'              STAT 'A'                  20201
.NORM    AIF   ('&TYPE' EQ 'TP' OR '&TYPE' EQ 'GR').NORM2        A52670
         AIF   ('&TYPE' NE 'AT' OR &SHR EQ 0).NORM2  3400 SHARABLE?
         DC    X'0&DEVAD',XL1'40',AL1(&DEVTAB),XL1'&ERRTAB'
         AGO   .ATATN
.NORM2   DC    X'0&DEVAD',AL2(&DEVTAB),XL1'&ERRTAB'    FLGS,UA,DEV,ERR
.******************************************************************UNC8
         AIF   ('&TYPE' NE 'SUD').NOTSUD                          *UNC8
         DC    AL1(&STATAB,&LCHN),XL2'0100'  STAT,LCH,ATN,CK      *UNC8
         DC    CL3'&DEVADDR',XL4'&TYPEA',F'0'                     *UNC8
         AGO   .END                                               *UNC8
.NOTSUD  ANOP                                                     *UNC8
.******************************************************************UNC8
         AIF   ('&TYPEA'(5,4) EQ '1004' AND '&TYPE' EQ 'GR').GR1053
         AIF   ('&TYPE' EQ 'AT').ATATN                            21048
         AIF   ('&TYPEA'(5,4) EQ '1009' OR '&TYPEA'(5,4) EQ '100A' OR  X
               '&TYPEA'(5,4) EQ '100B').ANRATN     ANR?           21003
         AIF   ('&TYPEA'(5,4) EQ '100C').GR1053
         AIF   ('&TYPE' EQ 'GR').REG    IF GRAPHICS, BRANCH
         AIF   ('&TYPEA'(5,4) EQ '0809').UR3211 3211 ATTN INDEX  A44146
.GR1053  DC    AL1(&STATAB),AL1(&LCHN),XL2'0' STAT,LCH,ATN,CHNMSK
         AGO   .ADDR
.ATATN   DC    AL1(&STATAB),AL1(&LCHN),XL2'1800'  STAT,LCH,ATN    21048
         AGO   .ADDR
.ANRATN  DC    AL1(&STATAB),AL1(&LCHN),XL2'1C00' STAT,LCH,ATN,CK  21003
         AGO   .ADDR                                              21003
.UR3211  DC    AL1(&STATAB),AL1(&LCHN),XL2'2000' STAT,LCH,ATN    A44146
         AGO   .ADDR
.REG     DC    AL1(&STATAB),AL1(&LCHN),XL2'1000' STAT,LCH,ATN,CHMSK
.ADDR    DC    CL3'&DEVADDR',XL4'&TYPEA',F'0' UA,DEVTYPE,RQE,SNSE 20201
.*   TEST FOR UCS ON PRINTER
         AIF   ('&TYPE' NE 'WR' AND '&TYPE' NE 'NR').NWTR   NEW UR
         DC    X'06',AL3(UCBW&UCBNO)    MASK,SENSE PTR            20202
         DC    A(UCBW&UCBNO+8)          WORK AREA PTR             20202
         AGO   .END                                               20202
.NWTR    AIF   ('&TYPE' NE 'UR').NOUCS  TEST FOR UR               20202
         AIF   ('&TYPEA'(6,3) NE '808').NOUCS     TEST FOR NOT PRINTER
         AIF   ('&TYPEA'(3,1) NE '8').NOUCS  TEST FOR NO UCS FEATURE
.*   ADD 2 FULL WORDS FOR UCS ON PRINTER
         DC    X'02',AL3(UCB&UCBNO+22)  COUNT,SENSE AREA PTR      20202
         AGO   .URWK                                              20202
.NOUCS AIF ('&TYPEA'(5,4) EQ '0823' OR '&TYPEA'(5,4) EQ '0830').URWK
         AIF   ('&TYPEA'(5,4) EQ '0818' OR '&TYPEA'(5,4) EQ '081A' OR  C
               '&TYPEA'(5,4) EQ '081B' OR '&TYPEA'(5,4) EQ '081C' OR   C
               '&TYPEA'(5,4) EQ '0820' OR '&TYPEA'(5,4) EQ '0822').URWK
         AGO   .NOWRK                                             19017
.URWK    DC    A(UCBW&UCBNO)            UR WORK AREA POINTER      19017
         AGO   .END                                               19017
.NOWRK   AIF   ('&TYPE' NE 'GR').TADACK TEST FOR GRAPHICS         19017
  AIF  ('&TYPEA'(7,2) NE '09' AND '&TYPEA'(7,2) NE '0A' AND '&TYPEA'(7,X
               2) NE '0B' AND '&TYPEA'(7,2) NE '0C').NOTANR     SA59372
         DC    XL2'&TMSK',XL2'0'       ADDITIONAL OPTIONS BYTE    21003
         DC    3F'0'                   IRB ADD,IOB ADD,D.I.       21003
         AGO   .END
.NOTANR  ANOP                                                     21003
         AIF   ('&TYPEA'(8,1) EQ '7' OR '&TYPEA'(8,1) EQ '8').END M2872
         DC    3F'0'                    SENSE,DUC,GCB
        AIF ('&TYPEA'(7,2) EQ '03' OR '&TYPEA'(7,2) EQ '04').SDA0 73767
         DC    XL1'&DEVNDX'             DEVICE INDEX
         AIF   (&GR EQ 1).GRF           TEST FOR GAM               GRAF
         DC    XL3'00'                  NO GAM SUPPORT             GRAF
         AGO   .END                                                GRAF
.GRF     AIF   ('&TYPEA'(1,2) EQ '31').MOD1  CHECK FOR 2250-1
         DC    VL3(IFFB&ACU)           BUFFER TABLE NAME        SA62675
         AGO   .END
.MOD1    DC    VL3(IFFB&DEVAD)          BUFFER TABLE NAME
         AGO   .END
.SDA0    DC    F'0'                     2260 OR 1053               IMP5
         AGO   .END
.TADACK  AIF   ('&TYPE' EQ 'UR' OR '&TYPE' EQ 'TP').END
         AIF   ('&TYPE' NE 'DUM').ADD1  CHECK FOR DUMMY
         DC    2F'0'     SENSE PADDING AND USER PTR TO EXTENSION   IMP5
         AGO   .END
.ADD1    AIF   ('&TYPEA'(5,2) NE '80').ADD2 BR IF NOT TAPE        21048
.*             THIS IS THE TAPE UCB                                IMP5
         AIF   ('&TYPEA'(5,4) EQ '8001').NOASP  CHECK FOR 2400    21048
         AIF   (&SQOPT EQ 1).C34XX                                21048
         DC    X'18',AL3(UCBS&UCBNO)  MASK AND SENSE PTR FOR 3420 21048
         AGO   .REST
.C34XX   DC    X'9',AL3(UCBS&UCBNO)      MASK AND PTR FOR 34XX    21048
         AGO   .REST                                              21048
.NOASP   DC    F'0'                     NORMAL SENSE
.REST    AIF   ('&TYPE' NE 'AT' OR &SHR EQ 1).REST2  3400 SHARABLE?
         DC    XL6'0',XL2'8000',H'0',H'0',2F'0'   VOLSER
         AGO   .REST3
.REST2   DC    XL6'0',XL2'0',H'0',H'0',2F'0'   VOLSER
.REST3   ANOP
*              STATAB & DMCT, FILE SEQ CT, FILE SEQ NO, DS SERNO  19023
         DC    AL1(&TMSK),AL3(UCBW&UCBNO)    MASK AND WK AREA PTR
         AGO   .END                     END OF TAPE UCB            IMP5
.ADD2    AIF   ('&TYPE' NE 'ML' AND '&TYPE' NE 'ZS').NDABS        20201
         DC    X'18',AL3(UCBS&UCBNO)    MASK & SENSE PTR          20201
         AGO   .DABS                                              20201
.NDABS   DC    F'0'                     SENSE                     20201
.DABS    DC    XL6'0',XL2'0',F'0',XL2'0',XL4'0'   VOLSER          20201
*              STATB & DMCT, VTOC ADR, RRCTS, DAVV WK AREA
         AIF   ('&TYPEA'(8,1) EQ '5').A2321  BRANCH FOR DATA CELL
         DC    XL2'0',XL8'0',XL1'0',XL3'0'
*              OSQ WKAR, LAST SEEK ADDRESS, USER CT, DA ECB ADR
         AGO   .WKAR                    PICK UP WORK AREA PTR
.A2321   DC    XL2'0',XL8'0'            OSQ WKAREA, LAST SEEK ADDRESS
&COUNT   SETA  128                      DEVICE ADDRESS / TYPE A FIELD
.LOOPH   DC    F'&COUNT',3F'0'              SUBCELL
&COUNT   SETA  &COUNT+65536
         AIF   (&COUNT NE (10*65536+128)).LOOPH     LAST SUBCELL
.WKAR    DC    A(UCBW&UCBNO.-104)       ERP WORK AREA ADDRESS
.END     SPACE 1
         MEND
