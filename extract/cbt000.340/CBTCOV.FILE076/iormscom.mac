         MACRO
         IORMSCOM   &CCH,&DDR,&SYSRES,&NSL,&ALTCUA,&SECT=D
         LCLC  &ALTRES
*
* RELEASE 21.8 PTFS
*
         AIF   ('&SECT' EQ 'C').CSCT
IORMS    DSECT
         AGO   .CCHPTAB
.CSCT    ANOP
         TITLE 'IGF201'
IGF201   CSECT
&ALTRES  SETC  '&ALTCUA'
         AIF   ('&CCH' EQ '1').CCH
         DS    5F                       CCH AREA
         AGO   .NOCCH1
.CCH     ANOP
         ENTRY CCHPTTAB
.NOCCH1  ANOP
         AIF   ('&DDR' NE '1').CCHPTAB
         ENTRY DDRIOB
         ENTRY DDRDEB
.CCHPTAB ANOP
         AIF   ('&CCH' NE '1' AND '&SECT' EQ 'C').NOCCH2
         DS    0F
CCHPTTAB DC    V(ERPIBFD)               ADDR OF CCH ERPIB FIELD
         DC    F'0'                     RESERVED FOR CCH
         DC    V(IGFCAT)                CCH ENTRY POINT AND BASE VALUE
         DC    V(CHRADTAB)
         DC    F'0'                     2880 LOGOUT POINTER
.NOCCH2  ANOP
         AIF   ('&SECT' EQ 'D').DRDSCT1
         AIF   ('&DDR' NE '1').END
.DDR1    ANOP
         AIF   ('&SYSRES' EQ '1' AND '&NSL' EQ '1').SYSNSL
         AIF   ('&SYSRES' EQ '1').SYS
         AIF   ('&NSL' EQ '1').NSL
*
***      DDRFLGSA
*
DDRFLGSA DC    X'10'                    FLAG FIELD
         AGO   .CONT1
.SYSNSL  ANOP
DDRFLGSA DC    X'92'                    FLAG FIELD
         AGO   .CONT1
.SYS     ANOP
DDRFLGSA DC    X'90'                    FLAG FIELD
         AGO   .CONT1
.NSL     ANOP
DDRFLGSA DC    X'12'                    FLAG FIELD
         AGO   .CONT1
.DRDSCT1 ANOP
DDRFLGSA DS    CL1                      FLAG FIELD
.CONT1   ANOP
SYSRESOP EQU   X'80'                    SYSRES OPTION
DDRBUSY  EQU   X'20'                    DDR PRESENTLY PROCESSING A RQST
DDRON    EQU   X'10'                    SYS-INITIATED DDR ALLOWED
VALIDATE EQU   X'08'                    'TO-DEV' TO BE VALIDATED
NSLUSER  EQU   X'04'                    USER NSL VERIFICATION
NSLAVAIL EQU   X'02'                    NSL VERIFICATION RTN AVAILABLE
OPINIT   EQU   X'01'                    OPERATOR INITIATED DDR
         ORG   *-1                      SET LABEL ON FULL WORD BOUNDARY
SYSRESAD DS    CL1
         AIF   ('&SYSRES' NE '1').NOSR1
         DC    VL3(IGFDDRSR)            POINTER TO DDR SYSRES ROUTINE
         AGO   .SYS1
.NOSR1   ANOP
         DC    AL3(0)                   RESERVED FOR DDR SYSRES
.SYS1    ANOP
*
***      DDRFLGSB
*
DDRFLGSB DC    X'00'                    FLAG FIELD
SWAPMSG  EQU   X'80'                    SWAP MSG BIT
INVALID  EQU   X'40'                    INVALID MSG BIT
SYSRES   EQU   X'20'                    SYSRES WARNING MSG BIT
DDREND   EQU   X'10'                    DDR TERMINATED MSG BIT
PROCEED  EQU   X'08'                    PROCEED MSG BIT
SHRDASD  EQU   X'04'                    TO INDICATE SHARED DASD
NOTOUCB  EQU   X'02'                    TO INDICATE 'TO' UCB NOT FOUND
FIRSTIO  EQU   X'01'                    TO INDICATE FIRST IO FOR CONTRL
         ORG   *-1                      SET LABEL ON FULL WORD BOUNDARY
RMSTCB   DS    CL1
         DC    VL3(IGFRMTCB)            I/O RMS TACB POINTER
*
***      DDRFLGSC
*
DDRFLGSC DC    X'00'                    FLAG FIELD
TERMNATE EQU   X'80'                    TO INDICATE TERMINATION OF DDR
STAE     EQU   X'40'                    TO INDICATE STAE PROCESSING
CANCEL   EQU   X'20'                    DDR CANCELED MSG BIT
ERROR    EQU   X'10'                    ERROR MSG BIT
SWAPUCB  EQU   X'08'                    TO INDICATE SWAP UCB INFO
REPOSITN EQU   X'04'                    TO INDICATE REPOSITION TAPE
IONOCTRL EQU   X'02'                    TO RE-EXCP WITHOUT CONTROL
ERPINPRO EQU   X'01'                    ERP IN PROCESS MSG BIT
         ORG   *-1                      SET LABEL ON FULL WORD BOUNDARY
IOSVT    DS    CL1
         DC    VL3(DDRAPNVT)            POINTER TO IOS VECTOR TABLE
*
***      DDRFLGSD
*
DDRFLGSD DC    X'00'                    FLAG FIELD
SYSRESBR EQU   X'80'                    TO INDICATE BRANCH TO SYSRES
DDRMSG   EQU   X'40'                    WHEN OFF & SYSRESBR ON, MSG 511
DUMPTAPE EQU   X'20'                    TO INDICATE DAR DUMP TAPE
READ     EQU   X'10'                    TO READ A RECORD
STDLABEL EQU   X'08'                    TO INDICATE STD LABEL TAPE
UNLABELD EQU   X'04'                    TO INDICATE UNLABELED TAPE
FORSPACE EQU   X'02'                    TO FOWARD SPACE FILE
IOCONTRL EQU   X'01'                    TO RE-EXCP WITH CONTROL
         ORG   *-1                      SET LABEL ON FULL WORD BOUNDARY
USRTCB   DS    CL1
         DC    3X'00'
CNSLID   DC    X'00'                    CONSOLE ID FIELD
         ORG   *-1                      SET LABEL ON FULL WORD BOUNDARY
USRDCB   DS    CL1
         DC    3X'00'
DDRFMUCB DC    H'0'
DDRTOUCB DC    H'0'
         AIF   ('&SECT' EQ 'C' AND '&SYSRES' EQ '1').SKIP1
ALTCUASR DS    F
         AGO  .SKIP2
.SKIP1   ANOP
ALTCUASR DC    X'0'
         DC    C'&ALTRES'
.SKIP2   ANOP
         DC    VL4(DDRRQE)              POINTER TO DDR RQE
         ORG   *-4                      SET COUNTER BACK
USRRQE   DC    H'0'                     SAVE AREA FOR USER'S RQE
OURRQE   DS    0C
         ORG   *+2                      RESET COUNTER
BLKCNT   DC    F'0'                     BLOCK COUNTER FOR TAPE
FILECNT  DC    H'0'                     DDR FILE COUNTER
*
***      DDRFLGSE
*
DDRFLGSE DC    X'00'                    FLAG FIELD                20201
RETRY    EQU   X'80'                    TO RETRY USER
WRONGVOL EQU   X'40'                    WRONG VOLUME MSG BIT
LABLPROC EQU   X'20'                    TO INDICATE TAPE LABEL PROCESS
READFROM EQU   X'10'                    TO READ MERLIN BUFFER
FMERROR  EQU   X'08'                    ERROR READING 'FROM' MERLIN BUF
TOERROR  EQU   X'04'                    ERROR READING 'TO' MERLIN BUFER
RECRDING EQU   X'02'                    TO RECORD MERLIN BUFFER
READTO   EQU   X'01'                    'TO' DEVICE BUFFER WAS READ
*
***      DDRFLGSF
*
DDRFLGSF DC    X'00'                    FLAG FIELD
COMPLETE EQU   X'80'                    COMPLETE MSG BIT
REENTRY  EQU   X'40'                    TO INDICATE ABNORM APPDGE RENTD
REINIT   EQU   X'20'                    TO RE-INITIALIZE DDR BLOCK CNT
LABELERR EQU   X'10'                    TO INDICATE ERROR IN LABEL PROC
DDRCNTRL EQU   X'08'                    TO INDICATE CONTROL OF UCB
OPCANCEL EQU   X'04'                    TO INDICATE CANCEL OPER. SWAP
NEGCOUNT EQU   X'02'                    BLK COUNT TO BE INIT TO -1
*
DDRECB   DC    X'80'              DDR RESIDENT ROUTINE WAITS FOR
         DC    VL3(DDRRB)               THIS ECB TO BE POSTED
IOSECB   DC    F'0'                     IOS POSTS THIS TO RETURN TO DDR
ALLOCECB DC    X'40'                    ALLOCATION WAITS ON THIS ECB
         DC    3X'00'                        TO PREVENT CONCURRENT OPNS
ALLOCTCB DC    F'0'                     SAVE AREA FOR ALLOCATION
REPLYBUF DC    F'0'                     REPLY BUFFER FOR DDR MSG'S
COREADDR DC  F'0'                       PTR TO OBTAINED STORAGE
ALLOTJID DC    X'0000'                  TJID SAVE AREA FILLED BY ALLOC
*
***      DDRFLGSG
*
DDRFLGSG DC    X'00'                    FLAG FIELD
WTQPURGD EQU   X'80'                    WAIT QUEUE ENTRY PURGED
ALLOCENQ EQU   X'40'                    TASK MARKED NON-DSPCH BY DDR
*                                       IS ENQED ON IEEWQE
NEGBLKCT EQU   X'20'                    USER HAS NEG. BLOCK COUNT
DVCEND   EQU   X'10'                    DEVICE END PRESENT IN USER
*                                       CSW STATUS
RETRIED  EQU   X'08'               USER RQE HAS BEEN RETRIED
MSGDOM   EQU   X'04'                   MSG NEEDS DOMING        @AS77454
RET508   EQU   X'02'                   RETURN FROM 508E        @SA77454
*
         DC    X'00'                    SPARE
ID511A   DC    F'0'                      DOM MSG ID FIELD      @SA72584
         AIF   ('&SYSRES' NE '1').NOSR2
DDRCOUNT DC    V(IGFDDR05)
         AGO   .SYS2
.NOSR2   ANOP
DDRCOUNT DC    AL4(0)                   RESERVED FOR DDR SYSRES
.SYS2    ANOP
         SPACE 2
*
**
***      THE FOLLOWING CODE UP TO AND INCLUDING THE POINTER TO THE
***      DDR DEB MUST REMAIN UNCHANGED SO THAT THE CORRECT RELATIONSHIP
***      BETWEEN THE DCB BLOCKCOUNT AND DCB DEB POINTER IS MAINTAINED.
**
*
         SPACE 2
         CNOP  4,8
DDRDCB   EQU   *-12
         DC    F'0'                     BLOCK COUNT
DDRWORK  DS    7F
         DC    X'00'
         DC    VL3(DDRDEB)
XCTLLIST DS    4F                       XCTL LIST BUILD FOR NSLREPOS
         SPACE 3
         DS    0D
DDRIOB   EQU   *
         DC    X'02'               IOBFLAG1
         DC    X'00'               IOBFLAG2
         DC    X'00'               SENSE 0
         DC    X'00'               SENSE 1
         DS    CL1                 COMPLETION CODE
         DC    AL3(IOSECB)         ECB ADDRESS
         DC    X'00'               IOBFLAG3
         DS    7CL1                IOBCSW
         DC    X'00'               SIO CONDITION CODE
         DC    AL3(DDRWORK)        CHANNEL PROGRAM POINTER
         DC    X'00'
         DC    AL3(DDRDCB)         DCB POINTER
         DC    F'0'
         DC    XL2'0040'
         DC    H'0'
         DC    X'00'               M EXTENT
         DS    7CL1          SEEK ADDRESS
APPVTAB  EQU   *
         DC    A(BRET)             END OF EXTENT APPENDAGE
         DC    A(BRET)             SIO APPENDAGE
         DC    A(BRET)             PCI APPENDAGE
DDRSYSCE DC    V(IGFDDR02)              CHANNEL END APPENDAGE PTR
DDRSYSAE DC    V(IGFDDR03)              ABNORMAL END APPENDAGE PTR
DDRDEB   EQU   *
         DC    X'00'               NMSUB
         DC    VL3(IGFRMTCB)
         DC    X'00'               AM LENGTH
         DC    AL3(0)              NEXT DEB ADDRES
         DC    X'00'               OPEN FLAGS
         DC    AL3(0)              IRB ADDRESS
         DC    X'00'               OPEN FILE TYPES
         DC    AL3(0)              SYSTEM PIRGE CHAIN
         DC    X'00'               NMEXT
         DC    AL3(0)              USER PURGE CHAIN
         DC    X'FF'               PRIORITY
         DC    AL3(0)              PURGE LIST ADDRESS
         DC    X'0F'               PROT TAG, DEB ID
         DC    AL3(DDRDCB)         DCB ADDRESS
         DC    X'02'               EXTENT SCALE
         DC    AL3(APPVTAB)        APP VECTOR TABLE ADDRESS
         DC    X'00'               MODE SET OR FILE MASK BYTE
         DC    AL3(0)              UCB POINTER
         DC    3F'0'               DIRECT ACCESS TRACK LOCATION
BRET     BCR   15,14               RETURN NORMALLY INSTRUCTION
ID502E   DC    F'0'                    502E DOM ID             @AS77454
*
***      ERROR MESSAGE IGF503I                                 @AS77454
*
MSG8     WTO   'IGF503I ERROR ON XXX, SELECT NEW DEVICE',ROUTCDE=(1,3),X
               DESC=4,MF=L                                     @AS77454
*
***      PROCEED MESSAGE IGF502E                               @AS77454
*
MSG9     WTO   'IGF502E PROCEED WITH SWAP XXX TO YYY',ROUTCDE=(1,3,4),DX
               ESC=3,MF=L                                      @AS77454
*
***      CAUTION MESSAGE IGF514I                               @AS77454
*
MSG10    WTO   'IGF514I ///// CAUTION; ENSURE NO HEAD TO DISK PACK INTEX
               RFERENCE/////',ROUTCDE=(1,3,4),DESC=4,MF=L      @AS77454
.END       ANOP
     MEND
