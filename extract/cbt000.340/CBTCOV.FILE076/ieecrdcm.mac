         MACRO
         IEECRDCM    &DEV=,&T=,&A=,&KEY=,&DSECT=
         LCLA        &N,&S,&E
         LCLC  &TABLE,&C,&D,&F,&G
.*
.* OPERAND DESCRIPTION
.*
.*   DEV    DEVICE TYPE  2250, 2260, ANR19, ANR48, OR 85CRT
.*
.*    T     INDEX NUMBER FOR ASSOCIATED DCM AREA
.*
.*   A      NUMBER OF STATUS DISPLAY SUPPORT AREAS REQUESTED
.*
         AIF   ('&DSECT' EQ '').NODEC
&S       SETA   1
&E       SETA   1
&D       SETC   '1'
         AGO   .POS
.NODEC   ANOP
         AIF   ('&A'  EQ  '').NEG
&S       SETA    N'&A-1
&E       SETA  1
&D       SETC  '&A(&E)'
         AGO   .POS
.NEG     ANOP
&S       SETA  0
.POS     ANOP
&N       SETA  1
&F       SETC  '0'
&TABLE   SETC  'ABCDEFGHIJK'
         AIF   (&S  EQ  0).NUN
&C       SETC  '&TABLE'(&N,1)           COMPUTE AREA ID
&F       SETC  '&TABLE'(&S,1)
.NUN     ANOP
DCMADTRN DC    V(IEECV&T)          POINTER TO TRANSIENT AREA
DCMINDEX DC    FL1'0'              DISPLAY CONSOLE INDEX NUMBER
DCMRFLGS DC    X'00'               FLAGS
DCMREAD  EQU   X'80'               DCM READ IN PROCESS
DCMTYPE  EQU   X'40'               CONSOLE USES TRANS DCM
DCMWRITE EQU   X'20'               DCM WRITE IN PROCESS
DCMDOM   EQU   X'10'               DOM MUST BE TRIED
DCMNIPP  EQU   X'08'               DCM WAS USED BY NIP
DCMMOD   EQU   X'04'               REWRITE FOR PRIOR DOM      MC A63438
DCMFRSRD EQU   X'02'               FIRST READ OF DCM
DCMSRCH  EQU   X'01'               PROC ZERO ROUTING FLAG
DCMLEN   DC    H'0'                LENGTH OF TRANS DCM
DCMADKP  DC    A(0)                ADDRESS OF ROUTED K COMMAND
*                                       PARAMETER LIST
         AIF   ('&F' EQ '0').GGG
DCMTOPAR DC    C'&F'               TOP DISPLAY AREA DEFINED
         AGO   .HHH
.GGG     ANOP
DCMTOPAR DC    X'00'
.HHH     ANOP
DCMTOPDS DC    X'00'               TOP DISPLAY ON SCREEN
DCMRESTA DC    X'00'               SPECIAL FLAGS FOR ATTN AND OPEN
DCMATTN  EQU   X'80'               ATTENTION STATUS HAS BEEN SAVED
DCMOPNSV EQU   X'40'               OPEN STATUS HAS BEEN SAVED
DCMDEVTY DC    X'00'               DEVICE TYPE FLAGS
DCMTY60  EQU   X'80'               DEVICE IS A 2260
DCMTY50  EQU   X'40'               DEVICE IS A 2250
         AIF   (&S EQ 0).NONE      IF NO SDS AREAS REQUESTED
DCMADSDS DC    A(DCMACB)           POINTER TO FIRST SDS SUPPORT AREA
         AGO   .RMS
.NONE    ANOP
DCMADSDS DC    F'0'                POINTER TO FIRST SDS SUPPORT AREA
.RMS     ANOP
         AIF   ('&DEV' EQ '').EEE
         AIF   ('&DEV' NE 'A').AAA
&G       SETC  '3066'
         AGO   .XXX
.AAA     ANOP
         AIF   ('&DEV' NE 'B').BBB
&G       SETC  '2250'
         AGO   .XXX
.BBB     ANOP
         AIF   ('&DEV' NE 'C').CCC
&G       SETC  '2260'
         AGO   .XXX
.CCC     ANOP
         AIF   ('&DEV' NE 'D').DDD
&G       SETC  'AN19'
         AGO   .XXX
.DDD     ANOP
         AIF   ('&DEV' NE 'E').EEE
&G       SETC  'AN48'
.XXX     ANOP
DCMRMS   DC    X'03'               NUMBER OF CCWS TO WRITE
DCMADRMS DC    VL3(SUB&G)          POINTER TO RMS CCWS
         AGO   .YYY
.EEE     ANOP
DCMRMS   DC    X'00'
DCMADRMS DC   XL3'00'
.YYY     ANOP
DCMWLAST DC    F'0'                PT CON Q ENTRY LAST OUT (O-O-L)
DCMRMSAL DC    H'0'                NUMBER LINES IN MSG AREA
         DC    H'0'                RESERVED
DCMMSGSV DC    F'0'                POINTER TO SAVED NIP MESSAGES
         AIF   ('&KEY' EQ '').MARA
DCMADPFK DC    V(&KEY)             POINTER TO RESIDENT PFK AREA
         AGO   .MARB
.MARA    ANOP
DCMADPFK DC    F'0'                POINTER TO RESIDENT PFK AREA
.MARB    ANOP
DCMINTVL DC    H'0'                INTERVAL FOR THIS DCM
DCMTMCTR DC    H'0'                TIME COUNTER FOR THIS DCM
DCMR2FLG DC    X'00'               TIMER FLAGS
DCMRXSFL EQU   X'80'               FULL SCREEN FLAG
DCMRXUNV EQU   X'40'               UNVIEWABLE MESSAGE DISPLAYED
DCMRXTMR EQU   X'20'               TIMER FLAG
DCMRXRLL EQU   X'10'               READY TO ROLL
DCMRXDEL EQU   X'08'               PENDING DELETE REQUEST
DCMRXTIM EQU   X'02'               TIMER ELAPSED FOR THIS DISPLAY
DCMRXDCM EQU   X'01'               T DCM IS IN CORE
DCMR3FLG DC    X'00'               MISC FLAGS
DCMSTSWT EQU   X'80'               CHANGING STATUS OF OUTPUT ONLY CON
DCMKVIP  EQU   X'40'               ENTRY FOR K VARY COMMAND
DCMCLPR  EQU   X'20'               CLOSE IN PROCESS
DCMRXSCN EQU   X'10'               ASY ERROR MESSAGE ON SCREEN
         DC    XL2'0'              RESERVED
         AIF   (&S EQ 0).END       IF NO SDS AREAS, DEPART
         SPACE
* SCREEN AREA CONTROL BLOCK - SACB
         SPACE
DCMPACB  EQU   *                   SACB PREFIX
DCMPLN   DC    FL2'&D'                  SYSGEN LENGTH OF AREA
DCMPLNPR DC    H'04'               LENGTH OF SACB PREFIX-IN BYTES
DCMPRSZ  EQU   *-DCMPACB           SIZE OF SACB PREFIX IN BYTES
         SPACE
DCMACB   EQU   *                   SCREEN AREA CONTROL BLOCK
         AIF   (&N EQ &S).ZERO     BRANCH IF LAST AREA
DCMACBNX DC    A(*+DCMACBSZ+DCMPRSZ) POINTER TO NEXT SACB
         AGO   .AID
.ZERO    ANOP
DCMACBNX DC    A(0)                POINTER TO NEXT SACB
.AID     ANOP
DCMAID   DC    C'&C'               AREA ID
DCMASACB DC    X'80'               SACB FLAGS
DCMAUSE  EQU   X'80'               SACB IN USE-AREA DEFINED HERE
DCMAGM   EQU   X'40'               GETMAINED SACB
DCMALN   DC    FL2'&D'                  LENGTH OF AREA
DCMATOP  DC    X'00'               TOP ROW OF AREA
DCMAROW  DC    X'00'               ROW TO BE WRITTEN NEXT
DCMAFR   DC    H'00'               FRAME ON SCREEN
DCMAMJWQ DC    A(0)                POINTER TO CON Q ENTRY FOR MAJOR
DCMAMIN  DC    A(0)                POINTER TO MINOR WQE
DCMATIME DC    F'0'                TIME CONTROL LINE WAS WRITTEN
DCMAMT   DC    XL2'00'             MESSAGE TYPE FLAGS
DCMAMTC  EQU   X'20'               MONITOR ACTIVE
DCMAFLG1 DC    X'00'               AREA FLAGS1
DCMAHRD  EQU   X'80'               MONITOR DISPLAY ON THE WAY MC A63496
DCMADISP EQU   X'40'               DISPLAY IN AREA
DCMADEND EQU   X'20'               END OF DISPLAY ON SCREEN
DCMAFRPR EQU   X'10'               FRAMING IN PROGRESS
DCMAFULL EQU   X'08'               FRAME FULL
DCMABL   EQU   X'04'               BLANKING TO BE DONE
DCMAFLG2 DC    X'00'               AREA FLAGS 2
DCMALMIN EQU   X'80'               SAVED POINTER TO LAST MINOR OUTPUT
DCMAWCON EQU   X'40'               WRITE CONTROL LINE
DCMARCON EQU   X'20'               REWRITE CONTROL LINE
DCMAMJFR EQU   X'10'               MAJOR WQE HAS BEEN FOUND
DCMADFLG DC    X'00'               DISPLAY FLAGS
DCMADD   EQU   X'80'               DYNAMIC DISPLAY
DCMAHOLD EQU   X'40'               DYNAMIC DISPLAY IN HOLD MODE
DCMACSIB EQU   X'20'               DYNAM DISP WITH CONT LINE IN SIB
         DC    3X'0'               RESERVED
*                                  NOTE: WHEN THE SACB IS
*                                       REINITIALIZED, FIELDS
*                                       DCMAROW THROUGH THE END
*                                       (INCLUDING THE RESERVED
*                                       FIELD) ARE ZERO'D
DCMACBND EQU   *                   END OF SACB
DCMACBSZ EQU   *-DCMACB            SIZE OF SACB IN BYTES
.NEXT    AIF   (&N EQ &S).END      BRANCH IF LAST AREA IS DONE
&N       SETA  &N+1
&E       SETA  &E+1
&C       SETC  '&TABLE'(&N,1)           COMPUTE AREA ID
&D       SETC  '&A(&E)'
         SPACE
* SCREEN AREA CONTROL BLOCK - SACB
*                                  SACB PREFIX
         DC    FL2'&D'                  SYSGEN LENGTH OF AREA
         DC    H'04'               LENGTH OF SACB PREFIX-IN BYTES
*                                  SACB
         AIF   (&N EQ &S).Z        BRANCH IF LAST AREA
         DC    A(*+DCMACBSZ+DCMPRSZ) POINTER TO NEXT SACB
         AGO   .ID
.Z       ANOP
         DC    A(0)                POINTER TO NEXT SACB
.ID      ANOP
         DC    C'&C'               AREA ID
         DC    X'80'               SACB FLAGS
         DC    FL2'&D'                  LENGTH OF AREA
         DC    X'00'               TOP ROW OF AREA
         DC    X'00'               ROW TO BE WRITTEN NEXT
         DC    H'00'               FRAME ON SCREEN
         DC    A(0)                POINTER TO CON Q ENTRY FOR MAJOR
         DC    A(0)                POINTER TO MINOR WQE
         DC    F'0'                TIME CONTROL LINE WAS WRITTEN
         DC    XL2'00'             MESSAGE TYPE FLAGS
         DC    X'00'               AREA FLAGS1
         DC    X'00'               AREA FLAGS2
         DC    X'00'               DISPLAY FLAGS
         DC    3X'00'              RESERVED
         AGO   .NEXT               RETURN TO GENERATE NEXT AREA
.END     MEND
