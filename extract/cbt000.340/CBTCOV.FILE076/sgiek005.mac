         MACRO
         SGIEK005   &UTAB,&OBER#,&ONRD#,&ONPRNT#,&ONNPH#,&OBER,&ONRD,  *
               &ONNPH
.* SGIEK005 BUILDS THE UNIT ASSIGNMENT TABLE FOR FORTRAN E/H/G.
         LCLA  &COUNT,&BL,&LRECL
         LCLC  &RECFM
.*
         TITLE 'IHCUATBL UNIT ASSIGNMENT TABLE'
*
* STATUS.  CHANGE LEVEL 002,17MAR69  RELEASE 18
*
*FUNCTION/OPERATION.   THIS MODULE IS THE UNIT TABLE GENERATION AND
*   FORTRAN OBJECT TIME ERROR MESSAGE WRITER SPECIFICATION MODULE.
*   IT GENERATES A SPECIFIED OR IMPLIED NUMBER OF UNIT TABLE BLOCKS
*   AND ASSIGNS A SPECIFIED OR IMPLIED UNIT AS THE OBJECT TIME ERROR
*   MESSAGE WRITER.
*
*ENTRY POINT.   THIS MODULE IS ACTIVATED DURING ASSEMBLY BY ITS MACRO
*   STATEMENT.
*   DATA PASSED BY STATEMENT PARAMETERS COMPRISES --
*   1.  NUMBER OF UNIT BLOCKS,  8 THROUGH 99.
*         &UTAB=NN    DEFAULT VALUE=08
*   2.  UNIT NUMBER FOR OBJECT TIME ERROR MESSAGE WRITER.
*         &OBJERR=NN   DEFAULT VALUE=06.
*   3.  UNIT NUMBER FOR ONLINE READ.
*         &ONRD#=NN    DEFAULT VALUE=05. H AND G ONLY.
*   4.  UNIT NUMBER FOR ONLINE PRINT.
*         &ONPRNT#     IDENTICAL TO ONLINE PUNCH. H AND G ONLY.
*   5.  UNIT NUMBER FOR ONLINE PUNCH.
*         &ONNPH#=NN   DEFAULT VALUE=07. H AND G ONLY.
*
*INPUT.   N/A
*
*OUTPUT.   N/A
*
*EXTERNAL ROUTINES.   N/A
*
*EXITS.   CONCLUSION OF MACRO FUNCTION ALLOWS FORTRAN LOAD MODULE
*   EXECUTION OF INPUT/OUTPUT STATEMENTS.
*
*TABLES/WORK AREAS.    A TABLE OF UNIT BLOCKS 1 THROUGH N (&UTAB)
*   STARTS WITH THE NAME 'UB1'.
*   EACH ENTRY IN THE TABLE CONTAINS FOUR WORDS, TWO OF WHICH ARE
*   INITIALIZED AT ONE. THE FIRST OF THESE IS FOR THE ADDRESS OF
*   THE DCB FIELD, AND THE SECOND IS FOR THE DIRECT ACCESS DATA
*   SET PARAMETER LIST.
*
*ATTRIBUTES.   N/A
*
*NOTES.
*   THE FOLLOWING UNIT ASSIGNMENT TABLE PROVIDES FOR (UTABLE) NUMBER
*   OF UNITS.
*    THE RECORD FORMAT FIELD -RECFM- MAY CONTAIN THE FOLLOWING-
*        RECFM=11000000 UNDEFINED
*        RECFN=10000000  FIXED
*        RECFM=11000100  UNDEFINED,ASA CARRIAGE CONTROL CHARACTER
*
IHCUATBL START 0
         DC    X'0000'             RESERVED
ENDT     DC    AL2(END-UTENTRY)
         DC    AL1(&OBER#)        OBJERR DATA SET REFERENCE NUMBER
         DC    AL1(&ONRD#)        READ DSRN.
         DC    AL1(&ONPRNT#)      PRINT DSRN.
         DC    AL1(&ONNPH#)       PUNCH DSRN.
         SPACE 2
UTENTRY  EQU   *
.GENRATE ANOP
&COUNT   SETA  &COUNT+1
&BL      SETA  800
&RECFM   SETC  '11000000'
&LRECL   SETA  32768
         MNOTE *,'UNIT &COUNT'
         AIF   (&COUNT EQ &ONRD OR &COUNT EQ &ONNPH).ALTER1
         AIF   (&COUNT EQ &OBER).ALTER2
.GEN1    ANOP
UB&COUNT DC    F'1'               ADDRESS OF DCB FIELD
         DC    B'01110000'         DEFAULT OPTIONS
         DC    X'00'               UNUSED DEFAULT FIELD
         DC    AL2(&BL)            BLKSIZE
         DC    B'&RECFM'           RECFM
         DC    AL1(2)              BUFNO
         DC    AL2(&LRECL)
         DC    F'1'               DIRECT ACCESS DATA SET PARM LIST
         SPACE 1
         AIF   (&COUNT EQ &UTAB).MEND
         AGO   .GENRATE
.ALTER1  ANOP
&BL      SETA  80
&RECFM   SETC  '10000000'
&LRECL   SETA  80
         AGO   .GEN1
.ALTER2  ANOP
&BL      SETA  133
&RECFM   SETC  '11000100'
         AGO   .GEN1
.MEND    ANOP
END      EQU   *
         MEND
