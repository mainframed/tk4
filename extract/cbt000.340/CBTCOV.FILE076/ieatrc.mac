         MACRO
         IEATRC &TRCENT,&STORAGE
         LCLA  &TRCENTA
.*1720,000800-000900,015600,018400,020600-021400                   1947
.*009800-011800                                                  A27771
IEATRC   CSECT
         AIF   ('&STORAGE' EQ 'PRIM').TRC005
         ENTRY  TRSTOPER
.TRC005  ANOP
         ENTRY IEATRTBL,PSWNDX,PSWNDX2,TRACE,IECXTRA
         EXTRN IEATCBP
SVCOPSW  EQU   32
IOOPSW   EQU   56
CAWLOC   EQU   72
CSWLOC   EQU   64
UAREG    EQU   6
WKREG1   EQU   10
WKREG2   EQU   11
TRBASE   EQU   9
WKREG8  EQU    8
         AIF   ('&STORAGE' EQ 'PRIM').AFTGTF1                     21014
CVTGTFST EQU   236                 GTF FLAGS DISPLACEMT IN CVT    21014
GTFACTIV EQU   X'C0'               FLAGS FOR GTF ACTIVE           21014
*  TO MINIMIZE SYSTEM DEGRADATION AND MAXIMIZE SYSTEM PERFORMANCE 21014
* THE GTF CODE SHOULD COME AS EARLY AS POSSIBLE                   21014
.AFTGTF1 ANOP                                                     21014
*                        ENTRY FOR RECORDING SVC INTERRUPTS
         USING *,TRBASE
         AIF   ('&STORAGE' NE 'PRIM').AFTGTF2                     21014
PSWNDX   CLI   20,0                IS TRACE STOPPED
         BCR   7,WKREG2            YES.RETURN
         AGO   .CONTSVC                                           21014
.AFTGTF2 ANOP                                                     21014
PSWNDX   ST    WKREG1,SAVE10                                   AB M1989
         L     WKREG1,CVTPTR            GET CVT ADDR              21014
         TM    CVTGTFST(WKREG1),GTFACTIV    IS GTF ACTIVE         21014
         BC    1,RESTORE           YES, RETURN                 AB M1989
         CLI   20,0                IS TRACE STOPPED               21014
         BC    7,RESTORE           YES, RETURN                 AB M1989
.CONTSVC ANOP                                                     21014
         MVC   TRSAV(8),SVCOPSW    MOVE SVC OLD PSW INTO SAVE AREA
         OI    TRSAV+2,X'10'       SET CODE IN PSW FIELD
         STM   0,1,TRSAV+8              MOVE REG 0 AND 1 TO SAVE AREA
         LA    TRBASE,TRACE             SET UP ADDRESSABILITY
         BC    15,TRA-TRACE(0,TRBASE)
         SPACE 2
*                        ENTRY FOR RECORDING I/O INTERRUPTS
         SPACE 1
         USING *,TRBASE
         AIF   ('&STORAGE' NE 'PRIM').AFTGTF3                     21014
PSWNDX2  CLI   20,0                IS TRACE STOPPED
         BCR   7,WKREG2            YES.RETURN
         AGO   .CONTIO                                            21014
.AFTGTF3 ANOP                                                     21014
PSWNDX2  ST    WKREG1,SAVE10                                   AB M1989
         L     WKREG1,CVTPTR            GET CVT ADDR              21014
         TM    CVTGTFST(WKREG1),GTFACTIV    IS GTF ACTIVE         21014
         BC    1,RESTORE           YES, RETURN                 AB M1989
         CLI   20,0                IS TRACE STOPPED               21014
         BC    7,RESTORE           YES, RETURN                 AB M1989
.CONTIO  ANOP                                                     21014
         MVC   TRSAV(8),IOOPSW     MOVE I/O OLD PSW INTO SAVE AREA
         BAL   TRBASE,TR0               SET UP ADDRESSABILITY AND BRAN
         SPACE 2
*                        ENTRY FOR RECORDING SIO
         SPACE 1
         USING *,TRBASE
         AIF   ('&STORAGE' NE 'PRIM').AFTGTF4                     21014
TRACE    CLI   20,0                IS TRACE STOPPED
         BC    7,RESTORE           YES, RETURN                 AB M1989
         AGO   .CONTSIO                                           21014
.AFTGTF4 ANOP                                                     21014
TRACE    ST    WKREG1,SAVE10                                   AB M1989
         L     WKREG1,CVTPTR            GET CVT PTR               21014
         TM    CVTGTFST(WKREG1),GTFACTIV    IS GTF ACTIVE         21014
         BC    1,RESTORE           YES, RETURN                 AB M1989
         CLI   20,0                IS TRACE STOPPED               21014
         BC    7,RESTORE           YES, RETURN                 AB M1989
.CONTSIO ANOP
         ST    UAREG,TRSAV         STORE UNIT ADDRESS INTO SAVE AREA
         MVC   TRSAV(1),SAVE10+3   SAVE SIO COND CODE          AB M1989
         NI    TRSAV,X'30'              AND OUT ALL BUT CONDITION CODE
         MVC   TRSAV+4(4),CAWLOC        MOVE CAW INTO SAVE AREA
TR0      MVC   TRSAV+8(8),CSWLOC       MOVE CSW TO SAVE AREA
IECXTRA  DS    0C                       EXTERNAL REFERENCE TO THIS PT
TRA      DS    0C
         AIF   ('&STORAGE' EQ 'PRIM').SKSTOP                      20048
         ENTRY IEATRCST                                           20048
IEATRCST BCR   0,WKREG2                 SET BY SVC DUMP           20048
.SKSTOP  ANOP                                                     20048
         L     WKREG1,TRPTR             GET LAST POINTER
         LA    WKREG1,16(WKREG1)        INCREMENT BY 16
         CL    WKREG1,TREND             COMPARE TO END OF TABLE
         BNH   TRC                      NOT AT END YET,ENTER     A33058
         NI    TREST+3,X'F0'            KEEP TRACE ON DOUBLE-DOUBLE
         NI    TREND+3,X'F0'            ADJUST END POINTER       A33058
         L     WKREG1,TREST             LOAD ADDRESS OF TABLE START
TRC      MVC   0(16,WKREG1),TRSAV       MOVE SAVE AREA TO TRACE TABLE
         ST    WKREG1,TRPTR             UPDATE LAST POINTER
         BCR   15,WKREG2                RETURN
RESTORE  EQU   *                                              AB  M2385
         BALR  WKREG1,0                                       AB  M2385
         USING *,WKREG1                                       AB  M2385
         L     WKREG1,SAVE10                                  AB  M2385
         BCR   15,WKREG2                                      AB  M2385
         AIF   ('&STORAGE' EQ 'PRIM').TRC004
TCBFLGS  EQU   28
TCBTIOT  EQU   12
TCBCMP   EQU   16
TCBREGIN EQU   0
TCBREG   EQU   12
WKREG3   EQU   13                                                  1947
BASREG   EQU   15
TRSTOPER DS    0H
         BALR  BASREG,0            ESTABLISH ADDRESSABILITY
         USING *,BASREG
         STM   WKREG1,WKREG3,SAVIT     SAVE WORK REGS              1947
         LR    TCBREG,TCBREGIN   PLACE TCB ADDR IN REG 12
         TM    TCBFLGS+2(TCBREG),X'20'  ALREADY TESTED
         BC    7,RET               YES.RETURN
         TM    TCBCMP(TCBREG),X'80'    DUMP REQUISTED
         BC    8,RET               NO.RRETURN
         L     WKREG1,TCBTIOT(TCBREG)  GET TIOT ADDRESS
         LTR   WKREG1,WKREG1       IS ONE PRESENT
         BC    8,RET                   NO RETURN
         LA    WKREG1,24(WKREG1)   GO AROUND JOB ENTRY
         L     WKREG3,24(TCBREG)         GET ADR OF BOUNDARY BOX     MO
         NC    4(8,WKREG3),4(WKREG3)    Q. PARTITION ALL IN H1     3981
         BNZ   COMP                     NO-BRANCH                  3981
         LA    WKREG3,12(WKREG3)        GET PTR TO LCS BB          3981
COMP     EQU   *                                                   3981
         L     WKREG3,8(WKREG3)        GET ADDR OF UPPER BOUNDRY   1947
         SH    WKREG3,NINETEEN SUB SIZE OF VALID SYSABEND ENTRY    1947
         OI    TCBFLGS+2(TCBREG),X'20' SET TRACE STOP RTN ENTERED  1947
TEST     CR    WKREG1,WKREG3 COMPARE ADDR TO UPPER BOUNDRY         1947
         BH    RET   UPPER BOUNDRY REACHED NO SYSABEND CARD FOUND  1947
         CLI   0(WKREG1),0             IS THIS THE END OF THE TIOT 1947
         BC    8,RET                   YES.RETURN
         CLC   SYSABEND(8),4(WKREG1)  IS THERE A SYSABEND CARD
         BC    8,STOPIT            YES STOP TRACE
         SR    WKREG2,WKREG2       CLEAR WORK REGISTER
         IC    WKREG2,0(WKREG1)    GET LENTH OF ENTRY
         AR    WKREG1,WKREG2       ADD LENTH T/ LAST ENTRY
         BC   15,TEST             GOTEST THIS ONE
STOPIT   IC    WKREG2,20           GET CTR FOR TRACE
         LA    WKREG2,1(WKREG2)    ADD ONE TO IT
         STC   WKREG2,20           STORE IT BACK
         B     *+8           BRANCH AROUND RESET OF INDICATOR      1947
RET      DS    0H
         NI    TCBFLGS+2(TCBREG),X'DF' RESET TRACE STOP ENTERED    1947
         LM    WKREG1,WKREG3,SAVIT     RESTORE REGS                1947
         BCR   15,14                   RETURN TO CALLER            1947
SAVE10   DC   F'0'                                             AB M1989
SAVIT    DS    4F                                                  1947
NINETEEN DC    X'0019'
SYSABEND DC    CL8'SYSABEND'
         DROP  BASREG
.TRC004  ANOP
         SPACE 3
&TRCENTA SETA  &TRCENT
IEATRTBL DS    0F
TRPTR    DC    A(END)                   LAST ENTRY MODE IN TRACE
TREST    DC    A(TRAREA)                BEGINNING OF TABLE
TREND    DC    A(END-16)                LAST ENTRY PTR           A33058
TRSAV    DC    XL16'00'                 SAVE AREA
CVTPTR   DC    V(IEACVT)           @ OF CVT, TO PREVENT PGM   AB A69885
*                                       CHK LOOP BETWEEN      AB A69885
*                                       TRACE AND PGM CHK     AB A69885
         SPACE 3
         DS    1D                       PAD FOR ALLIGNMENT
TRAREA   DS    0D
.TRC000  ANOP
         AIF   ((&TRCENTA-32) LE 0).TRC001
         DC    32XL16'0'
&TRCENTA SETA  &TRCENTA-32
         AGO   .TRC000
.TRC001  ANOP
         DC    &TRCENTA.XL16'00'
END      EQU   *                                                 A33058
         MEND
