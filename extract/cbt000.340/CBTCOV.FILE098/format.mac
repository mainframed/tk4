FORMAT   TITLE 'QUEUE COMMAND - JQE AND JOE FORMAT ROUTINES'      ONL01
FORMAT   QSTART ,                  START FORMAT CSECT ASSEMBLY    ONL01
******************************************************************BFG04
* BFG CHANGES:                                                    BFG04
*      (1) BFG04 - IF A SYSLOG DATASET, DISPLAY FIRST TIME STAMP  BFG04
*                  THIS MOD WAS ORIGINALLY FROM PWF               BFG04
*      (2) BFG08 - ADD HEADING FOR DESTINATION COLUMN             BFG08
*      (3) BFG12 - DISPLAY NJE JQES IF IN SP ($XMIT & $RECEIVE)   BFG12
*                  ALSO FORMAT NETWORK AND DUMP JOES              BFG12
*      (4) BFG18 - FIX BUGS FOR XA FORMATING AND VERSION 2        BFG18
*      (5) BFG22 - FIX LINE COUNTS FOR 10 MILLION AND GREATER     BFG22
*      (6) BFG25 - ADD CODE FROM BILL BALMER (EG&G) TO DISPLAY    BFG25
*                  NUMBER OF LINES LEFT TO PRINT ON ACTIVE JOBS   BFG25
******************************************************************BFG04
***********************************************************************
* RNB CHANGES:                                                        *
*      (1) RNB17 - WHEN FORMATTING JOES, CHECK FOR JOE ON PSO AND     *
*                  INDICATE 'EXT-WTR' IF SO. ALSO, DON'T INDICATE ON  *
*                  ON PRINT/PUNCH UNLESS SHOWS ACTIVE IN JOEFLAG1.    *
*                  ALSO, FIX BUG IN GETTING TO CHECKPOINT JOE FOR     *
*                  RECORDS LEFT, AND FIX RECORDS LEFT FOR SP2.        *
*                  ALSO, IF JOE NOT BUSY, BUT CHKPT JOE VALID, PRINT  *
*                  NUMBER OF RECORDS LEFT, NOT TOTAL RECORDS.         *
*                  08 OCT 82 - FIX FOR REMOTE LINECNT NOT DECREMENTING*
*      (2) RNB18 - DISTINGUISH BETWEEN JOES WITH REMOTE ROUTING AND   *
*                  THOSE WITH SPECIAL LOCAL ROUTING (DEFINED BY DESTID*
*                  STATEMENTS WITH DEST=UNNN).                        *
*      (3) RNB19 - WHEN LISTING JQE'S, DON'T ASSUME INPUT QUEUE, BUT  *
*                  USE THE JQETYPE INSTEAD. ALSO, SPECIALLY HANDLE    *
*                  CONVERSION AND DUMP QUEUES, AND AWAITING OUTPUT.   *
*      (4) RNB20 - WHEN LISTING THINGS, DISTINGUISH BETWEEN NORMAL    *
*                  HOLD, HOLD ALL, AND DUPLICATE HOLD. ALSO, FOR      *
*                  JOES, INDICATE SELECT=NO IF APPLICABLE.            *
*      (5) RNB21 - FIX SETDEVIC SUBROUTINE FOR SP2.                   *
*      (6) RNB25 - ALLOW 'COUNT' PARM TO HO TO INDICATE TOTAL LINE    *
*                  COUNT DESIRED FOR JOBS ON HELD OUTPUT QUEUE        *
*      (7) RNB28 - FIX FOR REMOTE PRINTER LINECOUNTS.                 *
***********************************************************************
         USING QDISPLAY,R10   BASE REG FOR DISPLAY WORK AREA
         L     R10,QVDSPL     LOAD BASE REG
         USING JQE,R9         BASE REG FOR JQE DSECT
         USING JOE,R8         BASE REG FOR JOE DSECT
         USING WORK,R13       BASE FOR LOCAL WORK AREA
***********************************************************************
*                                                                     *
*   INPUT TO THIS MODULE -                                            *
*     R1 = 0 INDICATES PRINT JQE                                      *
*     R1 = 4 INDICATES PRINT JOE                                      *
*     R9 CONTAINS JQE ADDRESS                                         *
*     R8 CONTAINS JOE ADDRESS                                         *
*                                                                     *
***********************************************************************
*                                                                     *
*   BRANCH TO PROPER ROUTINE                                          *
*                                                                     *
***********************************************************************
         ST    R1,JTYPE       SAVE ENTRY ARGS                 PWF BFG04
         STM   R8,R9,AJOEJQE  SAVE BLOCK ADDRS                PWF BFG04
         MVC   FCLEAR,=CL80' ' CLEAR THE PRINT AREA
         MVC   FQUEUE,QCLASS  CLASS NAME
         MVC   QDHLINE,HEADING REPORT HEADING
         CLI   QSUBNAME,C'X'  IS THE REQUEST FOR A HEX DUMP?
         BE    NOTBUSY        YES. SKIP.
*        CLC   =X'01000100',JQEPRTRT IS THE PRINT/PUNCH FOR LOCAL?
*        BE    LOCAL          YES. SKIP THIS ROUTINE.
*        MVC   FREMOTE,=C'RJE' INDICATE THIS JOB IS REMOTE
         TM    JQEFLAG1,JQE1BUSY IS THE JOB EXECUTING?
         BZ    NOTBUSY        NO. SKIP THIS ROUTINE.
         IC    R15,JQEFLAG1   GET SYSTEM NUMBER
         N     R15,=F'7'      ZERO OUT UNWANTED BITS
         SLL   R15,3          MULTIPLY BY 8
         LA    R15,QSYSID(R15) OBTAIN SYSTEM ID
         MVC   FSYSID,0(R15)  MOVE SYSTEM ID TO DISPLAY
NOTBUSY  CLI   QCLASS,32           SKIP SERVICE ROUTINE           BFG12
         BH    CHEKJQE              IF UNDEFINED                  CL005
         SR    R15,R15             COMPUTE BRANCH INDEX           CL005
         IC    R15,QCLASS          QUEUE TYPE                     CL005
         B     *+4(R15)            BR BASED ON QUEUE TYPE         CL005
         B     LISTTSO             0  TSO QUEUE                   CL005
         B     LISTSTC             4  STC QUEUE                   CL005
         B     LISTHO              8  HELD OUTPUT QUEUE           CL005
         B     LISTDM              12 DUMP QUEUE                  CL005
         B     LISTCN              16 CONVERSION QUEUE            CL005
         B     LISTAOUT            20 AWAITING OUTPUT QUEUE       CL005
         B     LISTSTUP            24 AWAITING SETUP QUEUE        CL005
         B     LISTXMIT            28 TRANSMISSION QUEUE          BFG12
         B     LISTRECV            32 SYSOUT RECEIVER QUEUE       BFG12
CHEKJQE  LTR   R1,R1          IS REQUEST FOR JQE OR JOE?          CL005
         BZ    LISTJQE        JQE.
***********************************************************************
*                                                                     *
*   FORMAT JOE                                                        *
*                                                                     *
***********************************************************************
LISTJOE  MVC   FQNAME,=C'OUTPUT' MOVE IN NAME OF QUEUE
*                                                                 BFG12
**       SEARCH (Q20) WILL SET QCLASS TO '>' IF DOING THE         BFG12
**       NETWORK JOE QUEUE AND TO '<' IF DOING THE DUMP           BFG12
**       JOE QUEUE.  THIS IS SO WE CAN PUT NICE HEADING           BFG12
**       ON LINE                                                  BFG12
*                                                                 BFG12
         CLI   QCLASS,C'>'    IS THIS A NETWORK JOE?              BFG12
         BNE   DMPJOE         NO. TRY DUMP.                       BFG12
         MVC   FQNAME,=C'NETWRK'                                  BFG12
         MVC   FQUEUE,JOECURCL SET TRUE CLASS                     BFG12
         B     GOODJOE        ALL NOW OK.                         BFG12
DMPJOE   CLI   QCLASS,C'<'    IS THIS A DUMP JOE?                 BFG12
         BNE   GOODJOE        NO. SO OK.                          BFG12
         MVC   FQNAME,=C'DUMP  '                                  BFG12
         MVC   FQUEUE,JOECURCL SET TRUE CLASS                     BFG12
GOODJOE  DS    0H                                                 BFG12
         CLI   QSUBNAME,C'X'  IS THE REQUEST FOR A HEX DUMP?
         BE    HEXO           YES. DO IT.
         TM    JOEFLAG2,JOE2SLEC   IS THIS JOE SELECTABLE?        RNB20
         BZ    LSTJOEAA            /YES - DON'T FLAG IT           RNB20
         MVC   FHOLD,=C' S=N'      /NO  - SAY SO                  RNB20
LSTJOEAA EQU   *                                                  RNB20
         L     R0,JOERECCT    NUMBER OF PRINT LINES
         S     R0,JOEWRECN    LESS THOSE PROCESSED SO FAR         BFG25
         TM    JOEFLAG1,JOE1CKV CHECKPOINT AREA VALID??           BFG25
         BZ    NOCHK          IF NOT DONT BOTHER TO READ          BFG25
         TM    JOEFLAG1,JOE1PUN+JOE1PRT IS IT BUSY ON DEVICE      BFG25
         BZ    NOCHK          IF NOT BUSY DONT BOTHER CKPT        BFG25
         MVC   QCTRAK,JOECPADR GET CHECKPOINT JOE ADDR            BFG25
         L     R3,QCBLKA      POINT AT BUFFER BLOCK               BFG25
         LR    R1,R3          SET FOR CALL                        BFG25
         L     R15,=V(READSPC)                                    BFG25
         BALR  R14,R15        GET CKPT JOE                        BFG25
         USING IAZCHK,R3      SETUP ADDRESSABILITY                BFG25
         CLC   CHKID(4),=C'CHK ' VALIDITY CHECK IT                BFG25
         BNE   NOCHK          OUT IF NOT VALID                    BFG25
         S     R0,CHKREC      LESS THOSE IN CHECKPOINT            BFG25
NOCHK    DS    0H                                                 BFG25
         BAL   R7,FMTLINE     GO AND FORMAT NUMBER OF LINES       BFG22
         TM    JOEFLAG1,JOE1BUSY IS JOE ACTIVE?                   RNB17
         BZ    LSTJOE$$          /NO  - DON'T SET DEVICE ID       RNB17
*                                /YES -                           RNB17
         CLI   JOEDEVID,X'0F'    IS JOB ON PSO (EXTERNAL WRITER)? RNB17
         BNE   LSTJOE$           /NO  - GO DO REAL DEVICE         RNB17
         MVC   FPRINT,=CL8'EXT-WTR'  /YES - SAY SO                RNB17
         B     LSTJOE$$                                           RNB17
LSTJOE$  TM    JOEDEVID,X'40'           JOB ON NJE DEVICE?        RNB17
         BO    LOCAL                     /YES - SKIP DEVICE ID    RNB17
         LA    R3,JOEDEVID    A(OUTPUT DEVICE DESCRIPTOR            FCI
         BAL   R7,SETDEVIC    SET THE OUTPUT DEVICE                 FCI
LSTJOE$$ EQU   *                                                  RNB17
         TM    JOEFLAG1,JOE1CKV IS CKPT JOE VALID?                RNB17
         BO    LSTJOE##         /YES - USE RECORDS PRINTED        RNB17
         TM    JOEFLAG1,JOE1BUSY /NO - IS JOE BUSY?               RNB17
         BZ    LOCAL            IF NOT, GO DO LOCAL STUFF         RNB17
         B     LIST             IF YES, SKIP LOCAL STUFF, GO LIST RNB17
LSTJOE## EQU   *                                                  RNB17
         L     R0,JOERECCT    RESTORE R0 FOR REMOTES              RNB28
         LR    R3,R8            SAVE WORK JOE ADDRESS             RNB17
         B     LIST                NO CKPT FOR NOW                UF046
LOCAL    DS    0H                                                 UF047
         SR    R1,R1               CLEAR REG                      UF047
         ICM   R1,B'0111',JOECHARB OFFSET TO CHAR JOE             UF047
         BZ    LOCAL1              NONE, SKIP THIS                UF047
         A     R1,QCJOTA           GET JOE ADDRESS                UF047
         PUSH  USING                                              UF047
         DROP  R8                  DROP OLD JOE ADDRESSING        UF047
         USING JOE,R1              TEMP USING                     UF047
         TM    JOEWTRID,X'FF'-X'40'  WAS THERE A WTRID?           UF047
         BZ    LOCAL1              NO, SKIP                       UF047
         MVC   FLINES+10(3),=C' W='  SET PREFIX                   UF047
         MVC   FLINES+13(8),JOEWTRID SET WRITER ID                UF047
         CLI   QCLASS,C'>'         IS THIS NETWORK QUEUE          BFG12
         BNE   LOCAL1              IF NOT THEN SKIP               BFG12
         MVC   FLINES+10(3),=C' U='  SET PREFIX OF USERID         BFG12
         POP   USING                                              UF047
LOCAL1   DS    0H                                                 UF047
         LA    R15,FREMOTE    ADDRESS TO PUT TEXT
         LA    R1,JOEROUT     POINT TO ROUTING INFO                FCI*
***********************************************************************
*RMTORLCL SUBROUTINE - DETERMINE REMOTE OR LOCAL DESTINATION       FCI*
* R1 POINTS TO PRTRT/PUNRT, R15 TO ASSEMBLY POINTER                FCI*
***********************************************************************
         SPACE 1                                                  BFG12
******************************************************************BFG12
*  IN SP THERE ARE 4 DISTINCT CASES FOR THE NODE AND RMT NUMBERS: BFG12
*      NODE    RMT        DESIRED OUTPUT                          BFG12
*       0      ^0          LCLI       (SPECIAL LOCAL ROUTE)       BFG12
*    =QCNODE   ^0          RMTI       (A REMOTE ON THIS NODE)     BFG12
*      ^0       -          NI.RI      (ANOTHER NODE AND RMT)      BFG12
*    =QCNODE    0        **BLANKS**   (NORMAL LOCAL ROUTE)        BFG12
******************************************************************BFG12
RMTORLCL DS    0H                                                 BFG12
         CLC   =H'0',0(R1)    IS THIS SPECIAL LOCAL               BFG12
         BNE   RMTORLC1       NO. KEEP CHECKING                   BFG12
         MVC   0(L'LCL,R15),LCL YES. SET ID                       BFG12
         LA    R15,L'LCL(R15) BUMP POINTER                        BFG12
         B     RMTORLC9       GO FORMAT NUMBER                    BFG12
RMTORLC1 CLC   QCNODE,0(R1)   IS IT OUR OWN NODE                  BFG12
         BNE   RMTORLC2       NO. MUST BE OTHER NODE              BFG12
         CLC   =H'0',2(R1)    IS REMOTE NUMBER PRESENT            BFG12
         BE    LIST           NO. SO DONE.                        BFG12
         MVC   0(L'RMT,R15),RMT YES. SET ID                       BFG12
         LA    R15,L'RMT(R15) BUMP POINTER                        BFG12
         B     RMTORLC9       GO FORMAT NUMBER                    BFG12
RMTORLC2 LH    R14,0(,R1)     GET NODE NUMBER                     BFG12
         MVI   0(R15),C'N'    SET NODE ID                         BFG12
         LA    R15,1(,R15)    BUMP POINTER                        BFG12
         CLC   =H'0',2(R1)    IS A REMOTE NUMBER PRESENT ALSO     BFG12
         BE    RMTORLCA       GO AND FORMAT IT AS NODE ONLY       BFG12
         LR    R2,R1          SAVE REGISTER                       BFG12
         CVD   R14,DOUBLEWD   RMT NUMBER TO DECIMAL               BFG12
         BAL   R3,FITINUM1    CONVERT NUMBER TO CHARACTER         BFG12
         MVI   0(R15),C'R'    SET FOR REMOTE                      BFG12
         LA    R15,1(,R15)    FORMAT IT                           BFG12
         LR    R1,R2          RESTORE R1                          BFG12
RMTORLC9 LH    R14,2(,R1)     GET REMOTE NUMBER                   BFG12
RMTORLCA CVD   R14,DOUBLEWD   RMT NUMBER TO DECIMAL               BFG12
         LA    R3,LIST        SET RETURN POINT                    BFG12
         B     FITINUM1       FIT THE NUMBER IN RMT MESSAGE       BFG12
         SPACE 2
***********************************************************************
* FITINUM SUBROUTINE - CONVERT BIN NUMBER TO NICE FORMAT           FCI*
*                                                                  FCI*
***********************************************************************
FITINUM  CVD   R1,DOUBLEWD    CONVERT TO PACKED DECIMAL            FCI*
         LA    R3,LIST        SET RETURN POINT                    BFG12
FITINUM1 MVC   NUMBER(L'NORMAL),NORMAL INITIALIZE THE EDIT FORMAT  FCI*
         LA    R1,NUMBER+SIGNORM IN CASE OF ZEROES                 FCI*
         EDMK  NUMBER(L'NORMAL),DOUBLEWD+2 CONVERT TO EBCDIC       FCI*
         LA    R14,NUMBER+L'NORMAL-1 A(END OF CONVERTED NUMBER)    FCI*
         SLR   R14,R1         LENGTH OF THE CONVERTED NUMBER - 1   FCI*
         EX    R14,MVNUMBER   PUT THE NUMBER IN THE MSG            FCI*
         LA    R15,1(R14,R15) A(NEXT SPOT IN MSG)                  FCI*
         BR    R3             RETURN TO OUR CALLER                BFG12
         SPACE 3                                                   FCI*
MVNUMBER MVC   0(0,R15),0(R1) TO BE EXECUTED                       FCI*
         SPACE 2                                                   FCI*
NORMAL   DC    X'402020202020202020202120' EDIT MASK               FCI*
SIGNORM  EQU   11             OFFSET TO LAST DIGIT                 FCI*
RMT      DC    C'RMT'                                              FCI*
LCL      DC    C'LCL'                                             RNB18
***********************************************************************
*                                                                     *
*        FORMAT NUMBER OF LINES IN A REASONABLE FORMAT                *
*                                                                     *
***********************************************************************
FMTLINE  DS    0H                                                 BFG22
         CVD   R0,CONVERT     MAKE DECIMAL                        BFG22
         MVC   FLINES,ED8     PREPARE FOR EDIT                    BFG22
         C     R0,=F'9999999' SEE IF MAX ALLOWED                  BFG22
         BH    FMTLNE1        IF BIGGER THEN HANDLE               BFG22
         ED    FLINES,CONVERT+4 EDIT NUMBER OF LINES              BFG22
         BR    R7             EXIT                                BFG22
FMTLNE1  DS    0H                                                 BFG22
         ED    FLINES,CONVERT+1 EDIT NUMBER OF MILLIONS OF LINES  BFG22
         MVI   FLINES+8,C'M'  INDICATE MILLIONS                   BFG22
         BR    R7                                                 BFG22
***********************************************************************
*                                                                     *
*   FORMAT JQE                                                        *
*                                                                     *
***********************************************************************
LISTTSO  MVC   FQNAME(8),=C'TSO USER' NAME OF QUEUE
         B     LIST           CONTINUE
LISTSTC  MVC   FQNAME(8),=C'SYSTEM Q' NAME OF QUEUE
         B     LIST           CONTINUE
LISTHO   MVC   FQNAME(8),=C'HELD OUT' NAME OF QUEUE
         B     LIST           CONTINUE
LISTDM   MVC   FQNAME(4),=C'DUMP'     NAME OF QUEUE               RNB19
         B     LIST                                               RNB19
LISTAOUT MVC   FQNAME(6),=C'AW OUT'   NAME OF QUEUE               RNB19
         B     LISTCN1                GO DO SYSAFF                RNB19
LISTSTUP MVC   FQNAME(5),=C'SETUP'    NAME OF QUEUE               CL005
         B     LIST                                               CL005
LISTXMIT MVC   FQNAME(5),=C'XMIT '    NAME OF QUEUE               BFG12
         LH    R15,JQEXEQND   GET NODE NUMBER                     BFG12
         B     LISTRCXM                CONTINUE                   BFG12
LISTRECV MVC   FQNAME(5),=C'RECV '    NAME OF QUEUE               BFG12
         LH    R15,JQEINPND   GET NODE NUMBER                     BFG12
LISTRCXM DS    0H                                                 BFG12
         LTR   R15,R15        IS THERE A NODE                     BFG12
         BZ    LIST           IF NOT SKIP                         BFG12
         CVD   R15,DOUBLEWD   MAKE DECIMAL                        BFG12
         MVI   FREMOTE,C'N'   INDICATE A NODE                     BFG12
         MVC   FREMOTE+2(L'NDIGS),NDIGS SETUP EDIT MASK           BFG12
         EDMK  FREMOTE+1(L'NDIGS+1),DOUBLEWD+5 CONVERT TO PRINT   BFG12
         MVC   FREMOTE+1(L'NDIGS),0(R1) ADJUST FOR BLANKS         BFG12
         MVI   FREMOTE+1+L'NDIGS,C' ' BLANK LAST ONE              BFG12
         B     LIST                    CONTINUE                   BFG12
NDIGS    DC    X'2020202020'  5 DIGIT NODE NAME                   BFG12
LISTCN   MVC   FQNAME(6),=C'CONVRT'   NAME OF QUEUE               RNB19
         MVC   FQUEUE,JQEJCLAS        JOB CLASS                   RNB19
LISTCN1  TM    JQEFLAG1,JQE1BUSY      IS JOB CONVERTING?          RNB19
         BNZ   LIST                   /YES - GO LIST IT           RNB19
*                                     /NO  - PUT SYSAFF IN        RNB19
         TM    JQEFLAG2,JQE2SAF       CHECK SYSTEM AFFINITY       RNB19
         BO    LIST                   LIST IT IF NO SPECIAL AFF   RNB19
         LA    R15,QSYSID+8           GET FIRST SID               RNB19
         TM    JQEFLAG2,X'01'         IS THIS IT?                 RNB19
         BO    LISTCN2                /YES -                      RNB19
         LA    R15,8(,R15)            /NO  - CHECK REST           RNB19
         TM    JQEFLAG2,X'02'                                     RNB19
         BO    LISTCN2                                            RNB19
         LA    R15,8(,R15)                                        RNB19
         TM    JQEFLAG2,X'04'                                     RNB19
         BO    LISTCN2                                            RNB19
         LA    R15,8(,R15)                                        RNB19
         TM    JQEFLAG2,X'08'                                     RNB19
         BO    LISTCN2                                            RNB19
         LA    R15,8(,R15)                                        RNB19
         TM    JQEFLAG2,X'10'                                     RNB19
         BO    LISTCN2                                            RNB19
         LA    R15,8(,R15)                                        RNB19
         TM    JQEFLAG2,X'20'                                     RNB19
         BO    LISTCN2                                            RNB19
         LA    R15,8(,R15)                                        RNB19
LISTCN2  MVC   FREMOTE(4),0(R15)      MOVE SYSTEM ID TO DISPLAY   RNB19
         B     LIST                                               RNB19
*LISTJQE  MVC   FQNAME,=C' INPUT' NAME OF QUEUE                   RNB19
LISTJQE  MVC   FQNAME(5),=CL5'XEQ'  ASSUME XEQ QUEUE              RNB19
         TM    JQETYPE,$XEQ         IS IT XEQ QUEUE?              RNB19
         BO    LIST                 /YES - GO LIST IT             RNB19
         MVC   FQNAME(5),=C'INPUT'  ELSE ASSUME INPUT, ETC.       RNB19
         TM    JQETYPE,$INPUT                                     RNB19
         BO    LIST                                               RNB19
         MVC   FQNAME(6),=C'OUTPUT'                               RNB19
         TM    JQETYPE,$OUTPUT+$HARDCPY                           RNB19
         BNZ   LIST                                               RNB19
         MVC   FQNAME(5),=C'PURGE'                                RNB19
         TM    JQETYPE,$PURGE                                     RNB19
         BO    LIST                                               RNB19
         MVC   FQNAME(8),=CL8'????????'                           RNB19
LIST     CLI   QSUBNAME,C'X'  IS THE REQUEST FOR A HEX DUMP?
         BE    HEX            YES. DO IT.
         MVC   FCOUNT,ED5     PREPARE FOR EDIT
         ED    FCOUNT,QCOUNT  EDIT THE POSITION IN QUEUE
         MVC   FNAME,JQEJNAME MOVE IN JOBNAME
         MVC   FJOBNO,ED5     PREPARE FOR EDIT
         LH    R0,JQEJOBNO    LOAD HASP JOBNUMBER
         CVD   R0,CONVERT     CONVERT TO DECIMAL
         ED    FJOBNO,CONVERT+5 EDIT HASP JOBNUMBER
         SR    R0,R0          ZERO OUT REGISTER
         IC    R0,JQEPRIO     LOAD JQE PRIORITY
         SRL   R0,4           DIVIDE BY 16
         CVD   R0,CONVERT     CONVERT TO DECIMAL
         MVC   FPRIO,ED3      PREPARE FOR EDIT
         ED    FPRIO,CONVERT+6 EDIT JQE PRIORITY
         TM    JQEFLAG1,JQE1HLDA+JQE1HLD1+JQE1HLD2
         BZ    NOHOLD         NO.
         MVC   FHOLD,=C'HELD' INDICATE JOB HELD
         TM    JQEFLAG1,JQE1HLD1  SPECIFIC HOLD?                  RNB20
         BO    NOHOLD             /YES - GO DISPLAY               RNB20
         MVC   FHOLD,=C'DUP '     ASSUME DUPLICATE                RNB20
         TM    JQEFLAG1,JQE1HLD2  IS IT DUPLICATE HOLD?           RNB20
         BO    NOHOLD             /YES - GO DISPLAY               RNB20
         MVC   FHOLD,=C'$HA '     ELSE MUST BE FROM $HA           RNB20
NOHOLD   DS    0H
         CLC   JQEJNAME,=CL8'SYSLOG' ?/SYSLOG JOE             PWF BFG04
         BNE   NOHOLD1        NO. BYPASS SYSLOG PROCESSING    PWF BFG04
         LM    R8,R9,AJOEJQE  R8 - A(JOE)  R9 - A(JQE)        PWF BFG04
         CLC   JTYPE,=F'0'    ?/JOE OR JQE BEING PROCESSED    PWF BFG04
         BE    SLOGJQE        JQE. GO PROCESS                 PWF BFG04
SLOGJOE  MVC   QCTRAK,JOEIOTTR TRACK ADDR OF SPIN IOT         PWF BFG04
         L     R1,QCIOTA      IOAREA FOR SPIN IOT             PWF BFG04
         LR    R3,R1          A(IOAREA)                       PWF BFG04
         AL    R1,=A(IOTSTART-IOT) ADJUST FOR HEADER          PWF BFG04
         L     R15,=V(READSPC)                                PWF BFG04
         BALR  R14,R15        GO TO ROUTINE                   PWF BFG04
         USING IOT,R3         SETUP ADDRESSABILITY            PWF BFG04
         MVC   QPJOBID,IOTJBKEY SAVE JOB KEY                      BFG18
         A     R3,IOTPDDB          OFFSET TO FIRST PDDB IN IOT    BFG18
         USING PDB,R3         PDDB ADDRESSABILITY             PWF BFG04
         MVC   QCTRAK,PDBMTTR TRACK DS BUFFER                 PWF BFG04
         LR    R3,R1          A(DS BUFFER)                    PWF BFG04
         B     SLOGCMN        BRANCH TO COMMON PROCESSING     PWF BFG04
SLOGJQE  MVC   QCTRAK,JQETRAK A(JCT MTTR)                     PWF BFG04
         L     R3,QCJCTA      A(IOAREA)                       PWF BFG04
         LR    R1,R3          A(DS BUFFER)                    PWF BFG04
         AL    R1,=A(JCTSTART-JCT)                            PWF BFG04
         L     R15,=V(READSPC)                                PWF BFG04
         BALR  R14,R15                                        PWF BFG04
         USING JCT,R3         JCT ADDRESSABILITY              PWF BFG04
         MVC   QPDSID,JCTPDDBK ID OF CURRENT PDDB             PWF BFG04
         MVC   QPJOBID,JCTJBKEY  SAVE FOR LATER CHECKS  PWF BFG04 BFG14
         MVC   QCODEH(2),=H'3' TELL LISTDS CALLER IS FORMAT   PWF BFG04
         MVC   QCTRAK,JCTIOT  A(IOT)                          PWF BFG04
         OC    QCTRAK,QCTRAK  ANY TRACK                           BFG18
         BZ    SLOGXIT        SKIP IF NOT                         BFG18
         DROP  R3                                             PWF BFG04
         L     R1,QCIOTA      IOT IOAREA                      PWF BFG04
         LR    R3,R1          A(DS BUFFER)                    PWF BFG04
         L     R15,=V(READSPC)                                PWF BFG04
         BALR  R14,R15        GO TO ROUTINE                   PWF BFG04
         L     R15,=V(LISTDS)                                 PWF BFG04
         BALR  R14,R15        GO TO ROUTINE                   PWF BFG04
         XC    QCODEH(2),QCODEH RESET THE CODE                PWF BFG04
SLOGCMN  L     R15,=V(READSPC)                                PWF BFG04
         BALR  R14,R15        GO TO ROUTINE                   PWF BFG04
         CLC   QPJOBID,4(R3)  VALIDATE DATA BLOCK FOR THIS SLOG   BFG18
         BNE   SLOGXIT        IF NOT THEN EXIT                    BFG18
         LA    R3,10(R3)      BYPASS RECORD HEADER            PWF BFG04
NEXTREC  CLI   0(R3),X'FF'    IS LENGTH BYTE FF?              PWF BFG04
         BE    SLOGXIT        YES. END SEARCH                 PWF BFG04
         TM    1(R3),X'10'    IS THIS A SPAN RECORD           PWF BFG04
         BO    SPAN           YES. SKIP IT                    PWF BFG04
         SR    R4,R4          ZERO OUT REG                    PWF BFG04
         IC    R4,0(R3)       INSERT LENGTH                   PWF BFG04
         LR    R5,R4          SAVE RECORD LENGTH              PWF BFG04
         LR    R2,R3          SAVE RECORD LOCATION            PWF BFG04
         TM    1(R3),X'80'    IS CARRIAGE CONTROL SPECIFIED   PWF BFG04
         BZ    NOCCTL         NO. CONTINUE                    PWF BFG04
         LA    R2,1(R2)       SKIP OVER CC                    PWF BFG04
NOCCTL   TM    1(R3),X'08'    IS THIS RECORD TO BE IGNORED    PWF BFG04
         LR    R3,R2          UPDATE RECORD POINTER           PWF BFG04
         BNZ   SKIPREC        YES. SKIP IT.                   PWF BFG04
         CLI   10(R3),C'.'    IS THIS THE TIME STAMP          PWF BFG04
         BNE   SKIPREC1       NO. GET NEXT RECORD       PWF BFG04 BFG18
         CLI   13(R3),C'.'    YES. BUT ARE YOU SURE           PWF BFG04
         BNE   SKIPREC1       NO. GET NEXT RECORD       PWF BFG04 BFG18
         CLC   8(8,R3),=C'00.00.00' IS THIS A DUMMY TIME STAMPPWF BFG04
         BE    SKIPREC        YES. GET NEXT RECORD            PWF BFG04
         MVC   FREMOTE,8(R3)  SAVE TIME STAMP IN DISPLAY      PWF BFG04
SLOGXIT  MVC   QPDSID,=X'FFFF' CLEAR DATASET FLAG         PWF BFG04 SBG
         B     NOHOLD1        CONTINUE WITH NORMAL PROCESSING PWF BFG04
SKIPREC1 CLI   27(R3),C':'    IS THIS XA TIME STAMP               BFG18
         BNE   SKIPREC        NO. GET NEXT RECORD                 BFG18
         CLI   30(R3),C':'    IT WAS BUT ARE WE SURE              BFG18
         BNE   SKIPREC        NO. SO SKIP                         BFG18
         MVC   FREMOTE,25(R3) SAVE TIME STAMP IN DISPLAY          BFG18
         B     SLOGXIT        EXIT                                BFG18
SKIPREC  LA    R3,3(R4,R3)    INCREMENT TO NEXT RECORD        PWF BFG04
         B     NEXTREC                                        PWF BFG04
SPAN     LH    R4,2(R3)       LENGTH OF SEGMENT               PWF BFG04
         TM    1(R3),X'08'    IS THIS THE FIRST SEGMENT       PWF BFG04
         BO    SPANFRST       YES. USE HEADER LENGTH OF 6     PWF BFG04
         LA    R3,4(R4,R3)    UPDATE RECORD POSITION          PWF BFG04
         B     NEXTREC        PROCESS NEXT RECORD             PWF BFG04
SPANFRST LA    R3,6(R4,R3)    UPDATE RECORD POSITION          PWF BFG04
         B     NEXTREC        PROCESS NEXT RECORD             PWF BFG04
NOHOLD1  DS    0H                                                 BFG04
         CLC   QCODEH,=H'28'  IS THIS THE HO COMMAND?             RNB25
         BNE   LIST2          /NO  - DO NORMAL LISTING            RNB25
*                             /YES -                              RNB25
         CLC   =C'COUNT',QPARM1  DOES USER WANT LINE COUNTS?      RNB25
         BNE   LIST2          /NO  - DO NORMAL LISTING            RNB25
*                             /YES - GET JCT AND FORMAT LINE CNT  RNB25
         MVC   QCTRAK,JQETRAK DISK ADDR OF JCT                    RNB25
         L     R3,QCJCTA      ADDR OF IOAREA FOR JCT              RNB25
         LR    R1,R3          PARM FOR READSPC                    RNB25
         AL    R1,=A(JCTSTART-JCT)  ADJUST FOR BUFFER PREFIX      ONL16
         L     R15,=V(READSPC)  ROUTINE TO READ HASPACE           RNB25
         BALR  R14,R15          GO DO IT                          RNB25
         USING JCT,R3         BASE FOR JCT                        ONL16
         SPACE 1                                                  ONL03
         CLC   JCTID,=CL4'JCT'  WAS A JCT READ FROM SPOOL ?       ONL03
         BNE   LIST2          SKIP IF NO                          ONL03
         CLC   JCTJNAME,JQEJNAME  DOES JOBNAME MATCH ?            ONL03
         BNE   LIST2          SKIP IF NO                          ONL03
         LA    R1,JQE         ADDR OF JQE                         ONL03
         SL    R1,QCJQTA      COMPUTE THE JQE OFFSET              ONL03
         CL    R1,JCTJQE      DOES OFFSET MATCH THE JCT'S ?       ONL03
         BNE   LIST2          SKIP IF NO                          ONL03
         SPACE 1                                                  ONL03
         L     R0,JCTLINES    GET TOTAL LINES GENERATED BY JOB    RNB25
         DROP  R3                                                 RNB25
         SPACE 1                                                  ONL03
         BAL   R7,FMTLINE     GO AND FORMAT NUMBER OF LINES       BFG22
LIST2    EQU   *                                                  RNB25
         LA    R1,QDMSG       ADDR OF MESSAGE AREA
         ST    R1,QDMSGA      STORE MESSAGE ADDR
         MVC   QDMLNG,=H'80'  LENGTH OF DISPLAY LINE
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE
         BALR  R14,R15        DISPLAY THE LINE
STOP     QSTOP
***********************************************************************
*                                                                     *
*   TAKE HEX DUMP OF JOE                                              *
*                                                                     *
***********************************************************************
HEXO     UNPK  FHEX1,0(8,R8)  UNPK FIRST PART OF JOE INTO HEX
         UNPK  FHEX2,7(8,R8)  SECOND
         UNPK  FHEX3,14(8,R8) THIRD
         UNPK  FHEX4,21(8,R8) FOURTH
         UNPK  FHEX5,28(5,R8) FIFTH
         MVI   FHEXOC,C' '    CLEAR LAST BYTE
         TR    FHEXO,TABLE    CHANGE TO PRINTABLE HEX
         LA    R1,QDMSG       ADDR OF MESSAGE AREA
         ST    R1,QDMSGA      STORE MESSAGE ADDR
         MVC   QDMLNG,=H'80'  LENGTH OF DISPLAY LINE
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE
         BALR  R14,R15        DISPLAY THE LINE
***********************************************************************
*                                                                     *
*   TAKE HEX DUMP OF JQE                                              *
*                                                                     *
***********************************************************************
HEX      UNPK  FHEX1,0(8,R9)  UNPK FIRST PART OF JOE INTO HEX
         UNPK  FHEX2,7(8,R9)  SECOND
         UNPK  FHEX3,14(8,R9) THIRD
         UNPK  FHEX4,21(8,R9) FOURTH
         MVC   FHEXC,QBLANK   CLEAR LAST BYTES
         TR    FHEX,TABLE     CHANGE TO PRINTABLE HEX
         B     NOHOLD         CALL DISPLAY
***********************************************************************
* SETDEVIC SUBROUTINE - GET DEVICE DATA (R3) POINTS TO DEVID       FCI*
*                                                                  FCI*
***********************************************************************
SETDEVIC ST    R7,SETDHOLD       SAVE LINK ADDRESS                 FCI*
         MVC   FPRINT(9),=CL9' '
         TM    0(R3),HIGHBIT REMOTE DEVICE?                        FCI*
         BO    RMTDEV         YES => OUTPUT IT                     FCI*
         SR    R1,R1          FOR THE INSERT CHARACTER             FCI*
         IC    R1,0(R3)         DEVICE TYPE                        FCI*
         SRL   R1,4           RIGHT JUSTIFIED                      FCI*
         MH    R1,DEVTYPEL    TYPE * LENGTH OF A DEVICE ENTRY      FCI*
         LA    R1,DEVTABLE(R1) A(DEVICE TYPE)                      FCI*
         MVC   FPRINT,1(R1)   PUT IN THE DEVICE TYPE
         CLI   0(R3),0          INTERNAL READER?                   FCI*
         BE    SETDRTN        YES => GIVE THE USER THE INFO        FCI*
         SR    R15,R15        FOR THE INSERT CHARACTER             FCI*
         ICM   R15,3,1(R3)      DEVICE NUMBER  (SP2)              RNB21
         CVD   R15,DOUBLEWD   IN PACKED DECIMAL                    FCI*
         IC    R15,0(R1)      OFFSET TO WHERE THE DEV # GOES       FCI*
         LA    R15,FPRINT(R15) A(WHERE THE DEV # GOES)
         MVC   1(L'DIGITS3,R15),DIGITS3 SET UP THE EDIT OF 3 DIGITSKMT*
         EDMK  0(L'DIGITS3+1,R15),DOUBLEWD+6 DEV # IN EBCDIC       FCI*
         MVC   0(L'DIGITS3+1,R15),0(R1) ADJUST FOR BLANKS          FCI*
*                                                                  FCI*
         B     SETDRTN        GO EXIT
*                                                                  FCI*
RMTDEV   SR    R0,R0          FOR THE INSERT CHARACTER             FCI*
         IC    R0,2(R3)         REMOTE NUMBER                     RNB21
         CVD   R0,DOUBLEWD    IN PACKED DECIMAL                    FCI*
         MVI   FPRINT,C'R'    INDICATE A REMOTE DEVICE             FCI*
         MVC   FPRINT+2(L'THREEPT),THREEPT SET UP THE EDIT MASK    FCI*
         EDMK  FPRINT+1(L'THREEPT),DOUBLEWD+6 CONVERT TO EBCDIC    FCI*
         MVC   FPRINT+1(L'THREEPT),0(R1) ADJUST FOR BLANKS         FCI*
         LA    R1,FPRINT+1    A(SPOT JUST BEFORE POSSIBLE SEP)     FCI*
FINDPT   LA    R1,1(R1)       A(NEXT BYTE)                         FCI*
         CLI   0(R1),C'.'     FOUND THE SEPARATOR?                 FCI*
         BNE   FINDPT         NO => KEEP LOOKING                   FCI*
         SR    R15,R15        FOR THE INSERT CHARACTER             FCI*
         IC    R15,0(R3)        DEVICE TYPE INDICATOR              FCI*
         SRL   R15,3          RIGHT JUSTIFIED                      FCI*
         LA    R15,RMTDEVS-HIGHBIT/8(R15) A(DEVICE TYPE)           FCI*
         MVC   1(2,R1),0(R15) PUT IN THE DEVICE TYPE               FCI*
         MVC   3(1,R1),0(R3) PUT IN THE DEVICE NUMBER              FCI*
         OI    3(R1),C'0'     MAKE THE DEVICE NUMBER PRINTABLE     FCI*
         B     SETDRTN
         EJECT ,                                                   FCI*
SETDRTN  L     R7,SETDHOLD    GET RETURN ADDRESS                   FCI*
         BR    R7             RETURN TO OUR CALLER                 FCI*
         SPACE 5                                                   FCI*
DIGITS3  DC    X'202020'                                           FCI*
THREEPT  DC    X'2020204B'                                         FCI*
         DS    0H                                                  FCI*
DEVTABLE DC    AL1(0),CL8'INTRDR',AL1(6),CL8'READER'               FCI*
         DC    AL1(7),CL8'PRINTER',AL1(5),CL8'PUNCH'               FCI*
DEVTYPEL DC    AL2((*-DEVTABLE)/4)                                 FCI*
RMTDEVS  DC    C'**',C'RD',C'PR',C'PU'                             FCI*
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
ED8      DC    X'4020202020202120'
ED5      DC    X'402020202021'
ED3      DC    X'40202021'
HEADING  DC    C'  QUEUE  POSITION JOBNAME    JOB#  PRIORITY  LINES'
         DC    CL40'   EXECUTING     DESTINATION'                 BFG08
HIGHBIT  EQU   X'80'
TABLE    EQU   *-240
         DC    C'0123456789ABCDEF' TRANSLATE TO PRINTABLE HEX
WORK     DSECT
         DS    CL80
CONVERT  DS    D
SETDHOLD DS    F
DOUBLEWD DS    D
NUMBER   DS    CL16
JTYPE    DS    F                                                  BFG04
AJOEJQE  DS    2F                                                 BFG04
FORMAT   CSECT
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
FORMAT   CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
        $HASPEQU
         $JQE
         $JOE
         $PDDB ,                                              PWF BFG04
         $TAB  ,                                              PWF BFG04
         IFGRPL
        $BUFFER ,                                                 ONL16
        $JCT   ,                                                  RNB25
         $IOT  ,                                              PWF BFG04
        $CHK   ,                                                  BFG25
         QCOMMON
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
         ORG   QDMSG
FCLEAR   DS    0CL80          FORMAT FOR QUEUE RECORDS
FQNAME   DS    CL6            NAME OF QUEUE
         DS    C
FQUEUE   DS    C              CLASS NAME
         DS    CL2
FCOUNT   DS    CL6            POSITION IN QUEUE
         DS    CL2
FNAME    DS    CL8            JOBNAME
         DS    CL2
FJOBNO   DS    CL6            JES2 JOB NUMBER
         DS    CL2
FPRIO    DS    CL4            JOB PRIORITY
         DS    CL2
FLINES   DS    CL8            NUMBER OF OUTPUT LINES
         DS    CL3
FSYSID   DS    CL8            SYSTEM ID
         DS    CL3
FHOLD    DS    CL4            JOB HOLD STATUS
         DS    CL1
FREMOTE  DS    CL8            REMOTE
         ORG   FSYSID
FPRINT   DS    CL8            PRINTING
         ORG   FCOUNT
FHEX     DS    0CL56          LENGTH OF JQE HEX DUMP
FHEXO    DS    0CL64          LENGTH OF JOE HEX DUMP
FHEX1    DS    0CL15
         DS    CL14
FHEX2    DS    0CL15
         DS    CL14
FHEX3    DS    0CL15
         DS    CL14
FHEX4    DS    0CL15
         DS    CL14
FHEXC    DS    0CL9           CLEAR LAST BYTES FOR JQE
FHEX5    DS    0CL9
         DS    CL8
FHEXOC   DS    C              CLEAR LAST BYTE FOR JOE
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
