************************************************************************
*
*           THE CANDID TSO CP SCANS THE VTOCS OF ALL PUBLIC AND STORAGE
*         PACKS SPECIFIED IN VATLST00 MEMBER OF PARMLIB AND PRODUCES IN
*         THE DATASET CALLED 'SYS1.VTOC.SCRATCH' A LIST OF DATASETS
*         THOUGHT TO BE ELIGIBLE SCRATCHING. THE CRITERIA USED FOR
*         SELECTION ARE :
*
*         (A)   DATASET MORE THAN 3 DAYS OLD (SU 60 DATE LAST REFERENCED
*         (B)   FIRST 3 CHARACTERS OF 2ND LEVEL QUALIFIER ARE 'SPF'
*         (C)   THE FIRST 4 CHARACTERS OF THE 3RD LEVEL QUALIFIER ARE 'L
*         (D)   THE 3RD LEVEL QUALIFIER IS PANLIST
*         (E)   THE 3RD LEVEL QUALIFIER IS TERM
*         (F)   THE 3RD LEVEL QUALIFIER IS LINKLIST
*         (G)   THE 3RD LEVEL QUALIFIER IS OUTLIST
*
*            THE COMMAND IS :
*
*                  CANDID
*
*         PREREQUISITES :
*                        CANDID DYNAMICALLY ALLOCATES 'SYS1.PARMLIB' AND
*        'SYS1.VTOC.SCRATCH'. BOTH THESE DATASETS MUST PRE-EXIST.
*         RESULTS WILL ALSO BE WRITTEN TO THE SCREEN BY TPUT
*
*            THE RESULT OF CANDID CAN BE EDITED TO ENSURE THAT ALL DATAS
*              LISTED ARE REDUNDANT, THEN 'SCRATCH' CAN BE RUN USING CAN
*              OUTPUT AS INPUT.
*
*
CANDID   TITLE 'CREATE CANDIDATE LIST OF DATASETS FOR SCRATCHING'
***********************************************************************
**********      INITIALISATION                             ************
***********************************************************************
         SPACE 2
CANDID   CSECT
         USING *,R12
         PRINT NOGEN
         B     24(R15)
         DC    CL8'CANDID'
         DC    CL4'&SYSTIME'
         DC    CL8'&SYSDATE'
         STM   R14,R12,8(R13)
         LR    R12,R15
         ST    R13,SV13
         LA    R13,SVREGS
         B     START
         EJECT
***********************************************************************
**********      CONSTANTS & STORAGE                        ************
***********************************************************************
         SPACE 2
         REGEQU
SVREGS   DS    9D
WORK     DS    D
SV13     DS    F
RETCODE  DC    F'0'
DSCBCNT  DC    F'0'                DSCB'S PER TRACK
KPCYL    DS    F
KPTRK    DS    F
KPTOT    DS    F
SPDATE   DS    PL3
CCDATE   DC    CL5' '              JULIAN DATE
CP1      DC    P'1'
CP3      DC    P'3'
CP638    DC    P'638'
CCSCR    DC    C'SCRATCH '
BUFFER   DC    CL121' '
QUAL1    DC    CL8' '
QUAL2    DC    CL8' '
QUAL3    DC    CL8' '
QUAL4    DC    CL8' '
CCSYS    DC    C'SYS'
CCVSAM   DC    C'VSAM'
CCLIST   DC    C'LIST'
CCPAN    DC    C'PANLIST'
CCSPF    DC    C'SPF'
CCLINK   DC    C'LINKLIST'
CCTERM   DC    C'TERM'
CCRA000  DC    C'RA000'
CCOUT    DC    C'OUTLIST'
VOLID1   DC    CL2' '
MSG1     DC    C'ERROR DDD READING VTOC ON DDDDDD  CCHHR = CCCCHHHHRR '
MSG2     DC    CL80'ALLOCATION ERROR INFO=0000 ERROR=0000 RC=00 '
MSG3     DC    C' PROCESSING VTOC ON DDDDDD'
MSG4     DC    C'FREESPACE   CYLS = DDD  TRACKS = DDDDD TOTAL = DDDDD '
DSNAME4  DC    44X'04'             FORMAT 4 DSCB NAME
CCHHR    DC    CL5'CCHHR'
CCHHRMAX DC    CL5'CCHHR'
VOLUME   DC    CL6'VOLUME'
         DS    0H                  AREA TO HALFWORD BNDRY
AREA     DC    CL140' '
TBCHAR   DC    C'0123456789ABCDEF'
VOLTAB   DC    50CL6' '
ENDVOL   EQU   *
SEARCH   CAMLST SEARCH,DSNAME4,VOLUME,AREA
SEEK     CAMLST SEEK,CCHHR,VOLUME,AREA
         EJECT
***********************************************************************
**********      DYNAMIC ALLOCATION BLOCKS                  ************
***********************************************************************
         SPACE 2
RBPTR    DS    0F                 POINTER TO REQUEST BLOCK
         DC    X'80'
         DC    AL3(RB)
*
RB       DC    X'14012000'        LENGTH,VERB,FLAGS
RBERR    DC    XL2'0'             ERROR REASON CODE
RBINFO   DC    XL2'0'             INFORMATION REASON CODE
RBTXT    DC    A(TEXT),2F'0'      ADDRESS OF TXT UNITS,RES AND FLAGS
*
TEXT     DC    A(TDDNAME)           POINTER TO DSNAME TEXT
         DC    A(TDSNAME)           POINTER TO DSNAME TEXT
         DC    A(TMEMBER)           POINTER TO MEMBER TEXT
         DC    A(TSTATUS)           POINTER TO STATUS TEXT
         DC    A(TDISP)             POINTER TO DISP TEXT
         DC    X'80',AL3(TUNALLOC)  POINTER TO UNALLOC TEXT
TDDNAME  DC    AL2(1),AL2(1),AL2(L'DDNAME) DDNAME
DDNAME   DC    C'INPUT'
TDSNAME  DC    AL2(2),AL2(1),AL2(L'DSNAME) DSNAME
DSNAME   DC    C'SYS1.PARMLIB'
TMEMBER  DC    AL2(3),AL2(1),AL2(L'MEMNAME) MEMBER NAME
MEMNAME  DC    C'VATLST00'
TSTATUS  DC    AL2(4),AL2(1),AL2(1),AL1(8) STATUS SHR
TDISP    DC    AL2(5),AL2(1),AL2(1),AL1(8) DISP KEEP
TUNALLOC DC    AL2(28),AL2(0)              UNALLOC AT CLOSE
TEXT1    DC    A(UDDNAME)           POINTER TO DSNAME TEXT
         DC    A(UDSNAME)           POINTER TO DSNAME TEXT
         DC    A(TSTATUS)           POINTER TO STATUS TEXT
         DC    A(TDISP)             POINTER TO DISP TEXT
         DC    X'80',AL3(TUNALLOC)  POINTER TO UNALLOC TEXT
UDDNAME  DC    AL2(1),AL2(1),AL2(L'EDNAME) DDNAME
EDNAME   DC    C'OUTPUT'
UDSNAME  DC    AL2(2),AL2(1),AL2(L'ESNAME) DSNAME
ESNAME   DC    C'SYS1.VTOC.SCRATCH'
*
INPUT    DCB   DDNAME=INPUT,DSORG=PS,EODAD=EOFIN,MACRF=(GM),           X
               LRECL=80,BLKSIZE=80,RECFM=F
OUTPUT   DCB   DDNAME=OUTPUT,DSORG=PS,MACRF=(PM)
         EJECT
***********************************************************************
**********       PROCESSING                                 ***********
***********************************************************************
START    DS    0H
         LA    1,RBPTR             ALLOCATE VATLST
         SVC   99                  DYNAMICALLY ALLOCATE
         LTR   15,15               WAS ALLOC OK ?
         BNZ   ERROR2              NO   TELL ABOUT THE ERROR
         LA    R1,TEXT1            SET UP FOR OUTPUT FILE
         ST    R1,RBTXT
         LA    1,RBPTR             ALLOCATE OUTPUT
         SVC   99                  DYNAMICALLY ALLOCATE
         LTR   15,15               WAS ALLOC OK ?
         BNZ   ERROR2              NO   TELL ABOUT THE ERROR
         TIME  DEC
         STCM  R1,7,SPDATE         SAVE JULIAN DATE
         SP    SPDATE+1(2),CP3     BACK 3 DAYS PLS
         CP    SPDATE+1(2),CP1     LAST YEAR NOW ?
         BNL   DATESET             NO
         STCM  R1,7,SPDATE         RESAVE JULIAN DATE
         SP    SPDATE,CP638        3 DAYS INTO LAST YEAR THEN
DATESET  DS    0H
         OI    SPDATE+2,X'0F'      PUT F SIGN BACK
         UNPK  CCDATE,SPDATE
         WRTERM CCDATE,5
         OPEN  (INPUT,(INPUT),OUTPUT,(OUTPUT))
         LA    R2,VOLTAB           ADDRESS VOLUME TABLE
         LA    R3,((ENDVOL-VOLTAB)/L'VOLTAB) LOOP CONTROL
         EJECT
***********************************************************************
**********       READ THE VATLST TO BUILD VOLUME TABLE      ***********
***********************************************************************
         SPACE 2
READ     DS    0H
         GET   INPUT,BUFFER
         MVC   0(L'VOLTAB,R2),BUFFER         PICK UP VOLID
         CLI   BUFFER,C'*'         IS THIS A COMMENT ?
         BE    READ                YES
         CLI   BUFFER+9,C'2'       IS IT A PRIVATE PACK ?
         BE    READ                YES  ONLY PUBLIC AND STORAGE WANTED
         LA    R2,L'VOLTAB(,R2)    BUMP TO NEXT ENTRY
         BCT   R3,READ
EOFIN    DS    0H
         CLOSE INPUT               CLOSE PARMLIB AND DEALLOCATE
         B     GETVOL1
         EJECT
***********************************************************************
**********       SELECT NEXT VOLUME FROM THE VOLUME TABLE   ***********
***********************************************************************
         SPACE 2
GETVOL   DS    0H
         LM    R5,R7,KPCYL         PICK UP COUNTERS
*
         CVD   R5,WORK
         UNPK  MSG4+19(3),WORK+6(2)
         OI    MSG4+21,C'0'
         CVD   R6,WORK
         UNPK  MSG4+33(5),WORK+5(3)
         OI    MSG4+37,C'0'
         CVD   R7,WORK
         UNPK  MSG4+47(5),WORK+5(3)
         OI    MSG4+51,C'0'
         WRTERM MSG4,L'MSG4
GETVOL1  DS    0H
         LA    R2,VOLTAB
GETVOLP1 DS    0H
         CLI   0(R2),C' '          ALL DONE ?
         BE    END                 YES
         CLI   0(R2),C'*'          HAS THIS VOLUME BEEN DONE
         BNE   SELECT
         LA    R2,L'VOLTAB(R2)     BUMP THRU TABLE
         B     GETVOLP1
*
         EJECT
**********************************************************************{
**********       READ THE FORMAT 4 DSCB                     ***********
***********************************************************************
         SPACE 2
SELECT   DS    0H
         XC    KPCYL,KPCYL         CLEAR CYL COUNTER
         XC    KPTRK,KPTRK         CLEAR TRK COUNTER
         XC    KPTOT,KPTOT         CLEAR TOT COUNTER
         MVC   VOLUME(L'VOLTAB),0(R2) PLUG THE VOLID INTO THE MSG
         MVC   MSG3+20(L'VOLTAB),0(R2) PLUG THE VOLID INTO THE OBTAIN
         WRTERM MSG3,L'MSG3
         OBTAIN SEARCH             READ FORMAT 4 LABEL
         MVI   0(R2),C'*'          SET VOLUME USED INDICATOR
         SR    R2,R2
         IC    R2,AREA+30
         ST    R2,DSCBCNT          SAVE NUMBER OF DSCBS PER TRACK
         MVC   CCHHRMAX,AREA+1     SAVE THE ADDRESS OF LAST DSCB
         MVC   CCHHR,AREA+96       SAVE THE ADDRESS OF 1ST DSCB
         B     FORMAT1
         EJECT
***********************************************************************
**********       READ THE FORMAT 1 DSCBS                    ***********
***********************************************************************
         SPACE 2
FORMAT1  DS    0H
         SR    R2,R2               CLEAR A REG
         IC    R2,CCHHR+4          PICK UP RECORD OF LAST DSCB READ
         LA    R2,1(,R2)           BUMP TO NEXT
         STC   R2,CCHHR+4          SAVE NEW RECORD NUMBER
         C     R2,DSCBCNT          IS THIS TOO HIGH FOR THIS TRACK ?
         BNH   READFMT1            NO READ THE FORMAT 1 DSCB
         LA    R2,1                SET RECORD TO FIRST ON A TRACK
         STC   R2,CCHHR+4          SAVE NEW RECORD NUMBER
         ICM   R2,3,CCHHR+2        PICK UP TRACK NUMBER
         LA    R2,1(,R2)           BUMP TO NEXT TRACK
         STCM  R2,3,CCHHR+2        PUT IN THE NEXT TRACK NUMBER
         CH    R2,=H'30'           NEW CYLINDER REQD ?
         BL    READFMT1            NO
         SR    R2,R2               SET TRACK TO ZERO
         STCM  R2,3,CCHHR+2        PUT IN THE NEXT TRACK NUMBER
         ICM   R2,3,CCHHR          PICK UP CYLINDER NUMBER
         LA    R2,1(,R2)           BUMP TO NEXT CYLINDER
         STCM  R2,3,CCHHR          SET NEW CYLINDER
READFMT1 DS    0H
         CLC   CCHHR,CCHHRMAX      IS THIS THE LIMIT ?
         BH    GETVOL              YES GET NEXT VOLUME
         OBTAIN SEEK
         LTR   R15,R15
         BNZ   ERROR1              SOME SORT OF ERROR
         CLI   AREA+44,C'1'        IS THIS A FORMAT 1 ?
         BNE   FORMAT5             NO
         CLI   AREA,C' '           ANY DATASET NAME ?
         BNH   FORMAT1             NO
         CLI   AREA+75,X'00'       IS DATE LAST REF 0 ?
         BE    FORMAT1             YES   JUST A NEW ONE
         CLC   AREA(3),CCSYS       SYSTEM DATASET ?
         BNE   NOTSYS
         CLC   AREA+17(5),CCRA000  IS IT A TEMPORARY DATASET ?
*                                  (NOTE THAT ALL TEMP DATASETS HAVE
*                                  A 3RD LEVEL QUALIFIER OF RA000)..
         BE    WRITE               YES DATASET CAN BE SCRATCHED
*
NOTSYS   DS    0H
         CLI   AREA+82,X'02'       IS IT PARTITIONED ?
         BE    FORMAT1             YES
         CLI   AREA+82,X'20'       IS IT DA ?
         BE    FORMAT1             YES
         SR    R2,R2
         IC    R2,AREA+75          PICK UP YY OF DATE LAST REF
         MH    R2,=H'10'           PRE-SHIFT THE EASY WAY
         CVD   R2,WORK
         CLC   WORK+6(1),SPDATE    SAME YEAR ?
         BL    CHKSAM              NO  LAST YEAR
         BH    FORMAT1             NO  NEXT YEAR ????
         ZAP   WORK,SPDATE+1(2)    PICK UP DDD
         CVB   R2,WORK
         CH    R2,AREA+76
         BL    FORMAT1             NOT 3 DAYS WITHOUT REFERENCE
CHKSAM   DS    0H
         XC    QUAL1(32),QUAL1
         LA    R2,QUAL1            SET INDEX
         LA    R3,44
         LA    R4,AREA             ADDRESS DSNAME IN DSCB
         LA    R5,QUAL1            INDEX
         LA    R6,QUAL4            END OF QUALS
         B     LOOP
         EJECT
***********************************************************************
**********       PROCESS FORMAT 5 DSCBS                     ***********
***********************************************************************
         SPACE 2
FORMAT5  DS    0H
         CLI   AREA+44,C'5'        IS IT A FORMAT 5 ?
         BNE   FORMAT1             NO BYPASS
         LA    R4,AREA+4           ADDRESS 1ST AVALIABLE EXTENT
         LA    R5,26
FMT5LP1  DS    0H
         LA    R6,AREA+44          SET POINTER TO IDENT
         CR    R6,R4               IS THIS THE IDENT
         BNE   FMT5LP2
         LA    R4,1(R4)            BUMP PAST FORMAT IDENT
FMT5LP2  DS    0H
         LH    R6,2(R4)
         A     R6,KPCYL            ACCUMULATE FREE CYLS
         ST    R6,KPCYL
         LH    R6,2(R4)
         MH    R6,=H'30'           CYLS X TRKS/CYL
         SR    R7,R7
         IC    R7,4(R4)            PICK UP FREE TRACKS
         AR    R6,R7               ACCUMULATE TOTAL
         A     R6,KPTOT
         ST    R6,KPTOT SAVE NEW TOTAL
         A     R7,KPTRK            ACCUM FREE TRACKS
         ST    R7,KPTRK
         LA    R4,5(R4)            BUMP TO NEXT
         BCT   R5,FMT5LP1
         B     FORMAT1             ALL DONE
         EJECT
***********************************************************************
**********       CHECK DATASET QUALIFIERS                   ***********
***********************************************************************
         SPACE 2
LOOP     DS    0H
         CLI   0(R4),C' '          ALL DONE ?
         BE    DONE
         CLI   0(R4),C'.'          SEPARATOR ?
         BNE   MVCNEXT             NO
         LA    R5,8(R5)
         LR    R2,R5
         CR    R2,R6               ALL DONE ?
         BH    DONE
         LA    R4,1(R4)            BUMP INPUT POINTER
         BCTR  R3,0
         B     LOOP
MVCNEXT  DS    0H
         MVC   0(1,R2),0(R4)
         LA    R4,1(R4)            BUMP INPUT POINTER
         LA    R2,1(R2)            BUMP OUTPUT POINTER
         BCT   R3,LOOP
DONE     DS    0H
         CLC   QUAL2(4),CCVSAM     VSAM DATASET ?
         BE    FORMAT1             YES
*
CHKSPF   DS    0H
         CLC   QUAL2(3),CCSPF      IS THIS AN SPF DATASET ?
         BE    WRITE
CHKLIST  DS    0H
         CLC   QUAL3(4),CCLIST     IS IT A LISTXXXX
         BE    WRITE
         CLC   QUAL3(7),CCPAN      IS IT A PANLIST ?
         BE    WRITE
         CLC   QUAL3(8),CCLINK     IS IT A LINKLIST ?
         BE    WRITE
         CLC   QUAL3(7),CCOUT      IS IT A OUTLIST ?
         BE    WRITE
         CLC   QUAL3(4),CCTERM     IS IT A TERM ?
         BE    WRITE
         B     FORMAT1
         EJECT
***********************************************************************
**********       CREATE OUTPUT                              ***********
***********************************************************************
         SPACE 2
WRITE    DS    0H
         MVI   BUFFER,C' '
         MVC   BUFFER+1(L'BUFFER-1),BUFFER
         MVC   BUFFER(44),AREA
         MVC   BUFFER+50(6),VOLUME
         SR    R1,R1
         IC    R1,AREA+53
         CVD   R1,WORK
         UNPK  BUFFER+60(2),WORK+6(2) DD OF CREATION DATE
         OI    BUFFER+61,C'0'
         MVI   BUFFER+62,C':'
         ICM   R1,3,AREA+54
         CVD   R1,WORK
         UNPK  BUFFER+63(3),WORK+5(3) YYY OF CREATION DATE
         OI    BUFFER+65,C'0'
         WRTERM CCSCR,79
         PUT   OUTPUT,CCSCR        CREATE OUTPUT
         B     FORMAT1
         EJECT
***********************************************************************
**********      ERROR READING DSCB1                        ************
***********************************************************************
         SPACE 2
ERROR1   DS    0H
         CH    R15,=H'20'          IS IT THE END OF THE VTOC ?
         BE    GETVOL
         MVC   MSG1+26(6),VOLUME   PUT IN VOLID
         CVD   R15,WORK
         UNPK  MSG1+6(3),WORK+6(2)
         OI    MSG1+8,C'0'
         UNPK  MSG1+42(11),CCHHR(6)
         TR    MSG1+42(10),TBCHAR-240
         MVI   MSG1+52,C' '
         WRTERM MSG1,L'MSG1
         B     GETVOL
         EJECT
***********************************************************************
**********      DYNAMIC ALLOCATION ERROR                   ************
***********************************************************************
         SPACE 2
ERROR2   DS    0H
         UNPK  MSG2+22(5),RBERR(3)
         TR    MSG2+22(4),TBCHAR-240
         MVI   MSG2+26,C' '
         UNPK  MSG2+33(5),RBINFO(3)
         TR    MSG2+33(4),TBCHAR-240
         MVI   MSG2+37,C' '
         STC   R15,WORK
         UNPK  MSG2+41(3),WORK(2)
         TR    MSG2+41(2),TBCHAR-240
         MVI   MSG2+43,C' '
         WRTERM MSG2,79
         B     END
         EJECT
***********************************************************************
**********      TERMINATION                                ************
***********************************************************************
         SPACE 2
END      DS    0H
         CLOSE OUTPUT
         L     R13,SV13
         MVC   12(4,R13),RETCODE   SET UP RETCODE
         LM    R14,R12,8(R13)
         BR    R14
         LTORG
         END   ,
/*
