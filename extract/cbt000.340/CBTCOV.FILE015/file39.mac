//TKSK8    JOB (S009),KANWAR,MSGCLASS=X,CLASS=2,NOTIFY=TKSK
//B EXEC ASSEMC,PRELINK='SYSPROG.KSK',NAME=ENQ
//SYSIN DD *
**********************************************************************
* THIS MODULE IS CALLED BY ENQCP.
*
*      CAUTION :   THIS PRGRAM USES SVC 235 TO  AUTHORISE ITSELF.
*                  ALSO WE USE INHOUSE START AND END MACROS.
*                  REPLACE THESE WITH YOUR OWN.
*      ENQCP IS A TSO COMMAND PROCESSOR.
*      INPUT TO ENQCP IS :   MAJORNAME/MINORNAME WHEN THE CP IS
*                               INVOKED.
*                            MAJORNAME MUST BE SPECIFIED.
*                            MAJORNAME OF '?' WILL LIST ANY MINOR
*                            NAME FOR WHICH WAIT COUNT IS = OR > 1.
*                            MINORNAME CAN BE '*'.
*                            '/' MUST ALSO BE SPECIFIED WHEN
*                            INVOKED AS A CP.
*                          ENQCP THEN PREPARES A PARMLIST TO PASS
*                          TO  ENQ.
*
*       AT ENTRY TO ENQ R1 POINTS TO A PARAMETER LIST.
*                +0    AN 8 BYTE FIELD CONTAINING THE MAJOR NAME.
*                +8    A TWO BYTE FIELD CONTAING THE LENGTH OF MINOR
*                        NAME LESS ONE.
*                +10   THE MINOR NAME UPTO 255 BYTES LONG.
*                IF NO MAJOR NAME IS PASSED THEN WE PROMPT FOR ONE.
*                WE PROMPT FOR A FIELD UP TO 16 BYTES LONG.
*                FIRST 8 CHARACTERS ARE THE MAJOR NAME.
*                NEXT  8 CHARACTERS ARE THE MINOR NAME.
*                IMBEDED BLANKS MUST BE SPECIFIED.
*                WE HAVE AN EXCLUDE LIST IN THIS PROGRAM,
*                IF WE WANT THAT EXCLUDE LIST NULLIFIED,
*                THEN MINORNAME SHOULD BE SPECIFIED AS '*'.
*
*    OUTPUT  :   TPUTS TO THE TERMINAL
*                JOBNAME, ASID, ENQ MAJORNAME, SHR OR EXC DISPOSITION,
*                MINORNAME UPTO 52 CHARACTERS LONG.
*
*                WRITTEN AT MERCATILE MUTUAL INSURANCE, SYDNEY,
*                   FOR MVS SP R1.3.
*                TESTED ON A 4341 GROUP2  UP.
*                IT WORKS FINE FOR US. WE NEEDED SOMETHING EQUAL TO
*                #N COMMAND FROM SHARE.
*                THE IDEA WAS BORROWED FROM #N, FOR WHICH WE ARE
*                GRATEFUL.
***********************************************************************
ENQ       PMSTA
          MVC    QNAME,0(R1)      MOVE AND SAVE MAJORNAME
          MVC    INC,10(R1)       SAVE INPUT PARM
          MVC    INCLEN,8(R1)     MOVE INCLUDE FOR MINORNAME
          GETMAIN EC,LV=32768,A=AREAAD
          LTR    R15,R15          DID WE GET THE CORE ?
          BZ     GETOK             BRANCH ON YES.
          TPUT   ERR1,ERR1LEN     ELSE ABANDON RUN.
          B      EOJ
GETOK     EQU    *
          LA     R9,LISTEXC      DEFAULT LIST OF EXCLUDES FOR MINRNAMES
          LA     R10,LISTEND
          LA     R8,8
          LR     R11,R9
          CLI    QNAME,C' '      WAS A MAJORNAME SUPPLIED.
          BNE    AGAIN              BRANCH ON YES
          TPUT   LINE2,LINE2LEN  ELSE PROMPT FOR ONE.
          TGET   QNAME,16
          MVI    INCLEN,X'FF'    INDICATE WE USE OUR EXCLUDE LIST.
          OC     QNAME(16),BLANKS   CHANGE TO UPPER CASE.
          CLI    INC,C' '        IS A MINOR NAME TO BE INCLUDED,
          BE     AGAIN           BRANCH ON NO.
          CLI    INC,C'*'        ARE ALL MINOR NAMES REQD ?
          BNE    LOOP1           BRANCH ON NO.
          MVI    LOOP2+1,X'F0'   INCLUDE ALL MINOR NAMES.
          B      AGAIN
LOOP1     EQU    *
          CLC    INC,0(R11)         DO WE INCLUDE THIS
          BNE    *+8
          MVI    0(R11),X'FF'       NULLIFY THIS EXCLUDE.
          AR     R11,R8
          CR     R11,R10
          BL     LOOP1
AGAIN     EQU    *
          USING  RIB,R2
          USING  RIBVAR,R3
          USING  RIBE,R4
          LA     R0,1
          SVC    235
          L      R2,AREAAD
          CLI    QNAME,C'*'
          BE     MAJONLY
          CLI    QNAME,C'?'
          BE     WAITONLY
          GQSCAN AREA=((2),32768),REQLIM=MAX,SCOPE=ALL,                X
               RESNAME=QNAME,REQCNT=1,TOKEN=TOKEN
COMEBACK  EQU    *
          ST     R15,GQCODE
          SR     R0,R0
          SVC    235
          TPUT   LINE3,LINE3L
          TPUT   LINE4,LINE4L
          L      R15,GQCODE
          B      HER(R15)
HER       EQU    *
          B      HER2
          B      NOTFOUND
          B      HER2
          B      NOTFOUND
          B      NOTFOUND
HER2      EQU    *
          L      R5,RIBNRIBE    NO. OF RIBE'S WITH THIS RESOURCE.
          LA     R3,RIBEND      ADDRESS OF RIBVAR
          LR     R4,R3
          AH     R4,RIBVLEN    ADDRESS OF IST RIBE
*
*    EXCLUDE TEMPORARY DSNAMES
*
          CLC    RIBRNAME(3),=C'SYS'
          BNE    WRITE4             NO, CONTINUE
          CLC    RIBRNAME+8(2),=C'.T'
          BNE    WRITE4             NO, CONTINUE
          CLI    RIBRNAME+16,C'.'
          BNE    WRITE4             NO, CONTINUE
          MVC    DWD(6),=6C'0'      SET NUMERIC FIELD
          MVZ    DWD(5),RIBRNAME+3
          CLC    DWD(6),=6C'0'      STILL NUMERIC
          BNE    WRITE4             NO, NOT SYSTEM NAME
          MVZ    DWD(6),RIBRNAME+10 MOVE NUMERIC ZONES
          CLC    DWD(6),=6C'0'      STILL NUMERIC
          BE     BYPASS             NO, SYSTEM NAME
WRITE4   EQU     *
          SR     R6,R6
          IC     R6,RIBRNMLN      LENGTH OF RNAME.
          C      R6,F55
          BL     *+8
          L      R6,F55
          MVC    RESNAME(56),BLANKS
          BCTR   R6,R0
          EX     R6,MOVE
          MVC    MAJNAME,RIBQNAME
RIBELOOP  EQU    *
          MVC    JOBNAME,RIBEJBNM
          MVC    SHREXC,EXC
          TM     RIBERFLG,RIBETYPE      0=EXC, 1=SHR
          BZ     AOKSHR
          MVC    SHREXC,SHR
AOKSHR    EQU    *
          LR     R11,R9
          CLI    INC,C'*'
          BE     WRITE2
WRITE1    EQU    *
          CLI    INC,C' '
          BE     LOOP2
          CLI    INCLEN,X'FF'
          BE     LOOP2
          LH     R1,INCLEN
          EX     R1,COMPARE
          BNE    BYPASS
          B      WRITE2
LOOP2     NOP    WRITE2
          CLC    RESNAME(8),0(R11)         DO WE EXCLUDE THIS
          BE     BYPASS
          AR     R11,R8
          CR     R11,R10
          BL     LOOP2
WRITE2    EQU    *
          LH     R1,RIBEASID
          CVD    R1,DWD
          OI     DWD+7,X'0F'
          UNPK   DWD(3),DWD+6(2)
          MVC    ASID,DWD+1
          TPUT   LINE1,ENDLINE
BYPASS    EQU    *
          LA     R4,RIBEEND          POINT TO NEXT RIBE FOR THIS RIB.
          BCT    R5,RIBELOOP         LOOP UNTIL ALL RIBES DUN FOR RIB.
          LR     R2,R4               POINT TO NEXT RIB
          L      R5,RIBNRIBE      ANY RIBES ?
          LTR    R5,R5               FOR THIS RIB.
          BNZ    HER2             BRANCH ON YES.
          L      R15,GQCODE       ELSE ALL RIBS DONE FOR THIS GQSCAN.
          B      HERE(R15)
HERE      EQU    *
          B      THEEND
          B      NOTFOUND
          B      AGAIN
          B      NOTFOUND
          B      NOTFOUND
NOTFOUND  EQU    *
          TPUT   ERR2,ERR2LEN
THEEND    EQU    *
          FREEMAIN E,LV=32768,A=AREAAD
EOJ       EQU    *
          PMEND  RC=(R15)
MAJONLY   EQU    *
          GQSCAN AREA=((2),32768),REQLIM=MAX,SCOPE=ALL,                X
               SYSNAME=0,REQCNT=1,TOKEN=TOKEN
          B      COMEBACK
WAITONLY  EQU    *
          GQSCAN AREA=((2),32768),REQLIM=MAX,SCOPE=ALL,                X
               SYSNAME=0,WAITCNT=1,TOKEN=TOKEN
          B      COMEBACK
MOVE      MVC    RESNAME(1),RIBRNAME
COMPARE   CLC    RIBRNAME(1),INC
DWD       DC     D'0'
GQCODE    DC     F'0'
F55       DC     F'52'
AREAAD    DS     F
AREALEN   DC     F'32768'
TOKEN     DC     F'0'
INCLEN    DC     X'FF00'
ERR1      DC     C'GETMAIN ERROR  RUN ABANDONED.'
ERR1LEN   EQU    *-ERR1
ERR2      DC     C'SPECIFIED MAJOR NAME NOT FOUND'
ERR2LEN   EQU    *-ERR2
LINE1     EQU    *
          DC     CL79' '
          ORG    LINE1
JOBNAME   DS     CL8
          DS     C
ASID      DS     CL2
          DS     C
MAJNAME   DS     CL8
          DS     C
SHREXC    DS     CL3
          DS     C
RESNAME   EQU    *
          ORG    LINE1+79
ENDLINE   EQU    *-LINE1
BLANKS    DC     CL80' '
LINE2     DC     C' PLEASE SPECIFY MAJOR NAME REQD.'
LINE2LEN  EQU    *-LINE2
LINE3     EQU    *
          DC     CL79' '
          ORG    LINE3
          DC     CL8'JOB/USER'
          DS     C
          DC     CL2'AS'
          DS     C
          DC     CL8'MAJNAME'
          DS     C
          DC     CL3'MOD'
          DS     C
          DC     C'MINOR NAME '
          ORG    LINE3+79
LINE3L    EQU    *-LINE3
LINE4     EQU    *
          DC     CL79' '
          ORG    LINE4
          DC     CL8'--------'
          DS     C
          DC     CL2'--'
          DS     C
          DC     CL8'-------'
          DS     C
          DC     CL3'---'
          DS     C
          DC     C'-----------'
          ORG    LINE4+79
LINE4L    EQU    *-LINE4
QNAME     DC     CL8' '
INC       DC     CL256' '
SHR       DC     C'SHR'
EXC       DC     C'EXC'
LISTEXC   DC     CL8'SYS1.PRO'
          DC     CL8'SYS1.TS.'
          DC     CL8'SYS1.UAD'
          DC     CL8'SYS1.BRO'
          DC     CL8'SYS1.ISP'
          DC     CL8'SYS1.ISF'
          DC     CL8'SYS1.HAS'
          DC     CL8'IPO1.CMD'
LISTEND   EQU    *
          EJECT
          ISGRIB
          END
