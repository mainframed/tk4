 HASPMOD3         THIS IS THE SOURCE IN SMP FORMAT OF A MOD ORIGINALLY
                  OBTAINED FROM SOUTH BRITISH UNITED (AUST) AND
                  MODIFIED BY AGC. IT ALLOWS ENFORCING OF JOB TIME
                  LIMITS FOR CERTAIN JOB CLASSES.COMMENTS IN THE CODE
                  ARE SELF-EXPLANATORY.NOTE THAT SYS PROGS ARE ALLOWED
                  TO BYPASS THESE LIMITS (GOODONYERMATE !!!)
++USERMOD(LM00015).
++VER(Z038) FMID(EJE1102) PRE(UZ29341,UZ29992)
  /* MODIFY CODE TO ADD HASPXE11 MACRO TO HASPXEQ MODULE
     IT CONTAINS CODE TO ENFORCE JOB TIME LIMITS FOR SOME
     JOB CLASSES */.
++SRCUPD(HASPXEQ).
./ CHANGE NAME=HASPXEQ
*        BZ    XJCLEXIT  RETURN IF UNSUCCESSFUL                 LM00015
         BZ    XJCLTIME  CHECK CLASS TIMING                     LM00015
*        BNE   XJCLEXIT  RETURN IF NONE                         LM00015
         BNE   XJCLTIME  CHECK CLASS TIMING                     LM00015
*        B     XJCLEXIT  THEN RETURN                            LM00015
         COPY  HASPXE11  CLASS TIMING CODE HERE                 LM00015
++MAC(HASPXE11) ASSEM(HASPXEQ) DISTLIB(HASPSRC) DISTMOD(AOSH3)
 DISTSRC(HASPSRC).
         EJECT                                                  LM00015
****************************************************************LM00015
*  ENFORCE JOB CLASS TIMING THUS                                LM00015
*    CLASSES D,E,F,J    LE 1 MIN. CPU TIME                      LM00015
****************************************************************LM00015
         SPACE 1                                                LM00015
         CLC   JCTJNAME(4),=C'ADPZ' IS IT TECH SUPPORT JOB?     LM00015
         BE    XJCLEXIT      YES - EXIT TO ORIGINAL CODE        LM00015
XJCLTIME DS    0H            SEE IF RESTRICTED CLASS            LM00015
         LA    WA,XCLASTAB-2 USED. POINT TO CLASS TABLE         LM00015
XCLASRCH LA    WA,2(WA)                                         LM00015
         CLI   0(WA),C'$'    Q. LAST ENTRY                      LM00015
         BE    XJCLEXIT      A. YES, NOT RESTRICTED CLASS       LM00015
         CLC   JCTJCLAS,0(WA) SEARCH FOR MATCHING ENTRY         LM00015
         BNE   XCLASRCH      KEEP LOOKING                       LM00015
         MVC   XCLSTIME,1(WA) SAVE TIME LIMIT TO USE            LM00015
         ICM   R15,B'1000',=AL1(TIMEJK)                         LM00015
         LA    WE,STRJKEY    USE XINTKEY TO SEE IF USER         LM00015
         BAL   WD,XINTKEY    HAS SPECIFIED TIME=.               LM00015
         BZ    XTIMEADD      NO, SO ADD ONE FOR HIM             LM00015
         SPACE 1                                                LM00015
XTIMEFND DS    0H            ENSURE THAT USERS TIME=            LM00015
         SLR   WD,WD         PARM. DOESNT EXCEED LIMITS         LM00015
         IC    WD,2(,WE)                                        LM00015
         BCTR  WD,0                                             LM00015
         BCTR  WD,0                                             LM00015
         CLI   2(WE),X'01'   CHECK HOW USER USED TIME=          LM00015
         BL    XJCLEXIT      TIME=(,N) OR TIME=(,NN)            LM00015
         BE    XTIMESOK      TIME=(N, OR TIME=N                 LM00015
*                            TIME=(XXXN, OR TIME=XXXN           LM00015
         EX    WD,XCLCMIN    ENSURE ALL X'S = C'0''S            LM00015
         BE    XTIMESOK                                         LM00015
         EX    WD,XMVCMIN    FORCE TO C'0'                      LM00015
         LA    WA,2+2(WD,WE)                                    LM00015
         B     XTMEMSET      GO SET DEFAULT TIME                LM00015
         SPACE 1                                                LM00015
XCLCMIN  CLC   3(*-*,WE),=C'000' THIS INSTR IS EX ECUTED        LM00015
XMVCMIN  MVC   3(*-*,WE),=C'000' THIS INSTR IS EX ECUTED        LM00015
         SPACE 1                                                LM00015
XTIMESOK DS    0H            VFY N OF TIME=N/TIME=(N,           LM00015
         LA    WA,2+2(WD,WE)                                    LM00015
         CLC   XCLSTIME,0(WA) VALIDATE N                        LM00015
         BH    XJCLEXIT      N LT CLASS TIME LIMIT              LM00015
         BE    XTMESCHK      N EQ CLASS TIME LIMIT              LM00015
XTMEMSET MVC   0(1,WA),XCLSTIME FORCE DEFAULT TIME              LM00015
         SPACE 3                                                LM00015
****************************************************************LM00015
*   CHECK AND ZAP SECONDS IF PRESENT ONLY IF TIME = LIMIT     * LM00015
****************************************************************LM00015
         SPACE 1                                                LM00015
XTMESCHK CLI   1(WE),X'01'   CHECK IF SECS PRESENT              LM00015
         BE    XJCLEXIT      NOT PRESENT                        LM00015
         CLI   1(WA),X'01'                                      LM00015
         BL    XJCLEXIT      DUE TO JCL SYNTAX CHECKING         LM00015
*                            THIS SHOULDNT BRANCH               LM00015
         BE    XSETSCS1      GO ZAP 1 DIGIT TO ZERO             LM00015
         MVI   3(WA),C'0'    ZAP 2ND SECS DIGIT                 LM00015
XSETSCS1 MVI   2(WA),C'0'    ZAP 1ST SECS DIGIT                 LM00015
         B     XJCLEXIT                                         LM00015
         EJECT                                                  LM00015
XTIMEADD DS    0H            ADD A TIME= TO THE                 LM00015
         LA    WA,0(,R1)     INTERNAL TEXT JOB CARD             LM00015
         AH    WA,0(,R1)                                        LM00015
         BCTR  WA,0          ADDRESS LAST BYTE IN RECORD        LM00015
         MVC   4(1,WA),0(WA) MOVE IT OVER 5 BYTES               LM00015
         MVC   0(3,WA),=AL1(TIMEJK,1,1)                         LM00015
         MVC   3(1,WA),XCLSTIME                                 LM00015
         LH    WA,0(,R1)     TIME=N IS NOW IN PLACE             LM00015
         LA    WA,4(,WA)     SO ADD 4 TO THE INTERNAL           LM00015
         STH   WA,0(,R1)     TEXT RECORDS RDW                   LM00015
         B     XJCLEXIT      NORMAL EXIT                        LM00015
         SPACE 5                                                LM00015
XCLASTAB DS    0XL20                                            LM00015
         DC    C'D1'                                            LM00015
         DC    C'E1'                                            LM00015
         DC    C'F1'                                            LM00015
         DC    C'J1'                                            LM00015
         DC    6C'$$'        SPACE FOR 6 MOR CLASSES            LM00015
XCLASLST DC    C'$'          DUMMY LAST ENTRY - MUST BE PRESENT LM00015
XCLSTIME DS    C                                                LM00015
