READCVOL IS A USER-WRITTEN UTILITY PROGRAM DESIGNED TO ASSIST IN
RESOLVING OS/CVOL PROBLEMS. THE PROGRAM RUNS UNDER TSO & PROVIDES AN
IN-FLIGHT OS/CVOL DEBUGGING TOOL. THE OUTPUT PRODUCED IS DISPLAYED ON
THE TERMINAL USED & ANY RETURN CODES FROM CATALOG MANAGEMENT ARE
DISPLAYED ON THE TOP RIGHT-HAND CORNER OF THE SCREEN.
WHEN READCVOL IS INITIATED THE FIRST RESPONSE SHOULD BE TO ESTABLISH
THE VOLSER OF THE OS/CVOL. (AMASPZAP OF READCVOL + X'B2E' TO DESIRED
DEFAULT CAN ELIMINATE THIS RESPONSE.) FROM THAT POINT THE CATALOG CAN
BE ACCESSED BY INDEX, DSNAME OR TTR. THE REFERENCED CATALOGED BLOCK
IS THEN DISPLAYED UPON THE TERMINAL. AT ANY TIME THE CVOL VOLSER CAN
BE CHANGED AS CAN THE WAY OF READING THE CATALOG RECORDS.
 ANYONE WISHING TO HAVE MORE INFORMATION ON HOW TO USE READCVOL PLEASE
        CONTACT PETER PASSANT OR ROB DAVIDS AT THE BANK OF N.S.W.
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
READCVOL TITLE 'BANK OF NSW  READCVOL COMMAND PROCESSOR'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*   SYSTEM  : TSO COMMAND PROCESSOR TO LOOK AT OS CVOL CATALOGS
*                                                                     *
*   MODULE  : READCVOL                                                *
*                                                                     *
*   PROGRAMMER : R.E. DAVIDS, BANK OF NSW, SYDNEY, AUSTRALIA          *
*   PHONE   : (02) 2 0230  X 5294                                     *
*   DATE    : 08 JUL 1981.                                            *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*             * * * MODIFICATION HISTORY * * *                        *
*                                                                     *
*  MOD NO.   DATE   PROGRAMMER       SHORT EXPLANATION.               *
*  1.                                                                 *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* REGISTER USAGE:                                                     *
*                                                                     *
*                                                                     *
* R0   : WORK & MACRO USE            R1   : WORK & MACRO USE          *
* R2   :                             R3   :                           *
* R4   :                             R5   :                           *
* R6   :                             R7   :                           *
* R8   :                             R9   :                           *
* R10  :                             R11  :MODULE BASE REGISTER.      *
* R12  :                             R13  :SAVE AREA ADDRESS          *
* R14  :RETURN ADDRESS               R15  :ENTRY ADDRESS              *
*                                          RETURN CODES               *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* OUTPUT RETURN CODES :                                               *
*                                                                     *
* R15  :  0  - NORMAL RETURN
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* MODULES CALLING THIS MODULE:    READCVOL TSO COMMAND                *
*                                                                     *
* ENTRY ADDRESS:                  READCVOL                            *
*                                                                     *
* MODULES CALLED BY THIS MODULE: NIL                                  *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                    * * *  SUBROUTINES * * *                         *
* NONE                                                                *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                    * * *  ATTRIBUTES  * * *                         *
*                                                                     *
*     NO SPECIAL ATTRIBUTES                                           *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* PURPOSE:                                                            *
*     THE PURPOSE OF THIS TSO COMMAND PROCESSOR IS TO ALLOW A         *
*     QUALIFIED SYSTEMS PROGRAMMER TO INSPECT AN OS CVOL CATALOG      *
*     TO DETERMINE WHERE IT IS IN ERROR AND TO GIVE HIM THE           *
*     NECESSARY INFORMATION TO CORRECT IT BY MEANS OF A SUPERZAP.     *
*                                                                     *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* METHOD :                                                            *
*     THIS PROGRAM READS THE OS CVOL CATALOG USING THE LOCATE MACRO   *
*     AND PUTS OUT TO THE TERMINAL THE HEXADECIMAL REPRESENTATION     *
*     OF THE CATALOG BLOCK SELECTED.  A BLOCK MAY BE SELECTED BY      *
*     TTR OR DATASET NAME AND THE USER CAN EASILY FOLLOW CHAINS       *
*     WITHIN THE CATALOG BY TYPING IN SUCCESSIVE TTRS.                *
*                                                                     *
*     NO DD STATEMENTS ARE REQUIRE SINCE INPUT IS VIA TERMINAL (TGET) *
*     AND THE LOCATE MACRO AND OUTPUT IS VIA TPUT MACRO. THIS PGM     *
*     CANNOT BE RUN IN BATCH UNLESS IT RUNS UNDER THE TMP.            *
*                                                                     *
*     THE USE OF THE AECATLG PROGRAM (OBTAINABLE FROM AUSTRALIAN      *
*     ATOMIC ENERGY COMMISSION) TO REORGANIZE AND STRAIGHTEN CATALOGS *
*     IN CONJUNCTION WITH THIS PROGRAM IS RECOMMENDED.                *
*                                                                     *
*     THIS PROGRAM IS PERFECTLY SAFE IN THAT IT ONLY READS AND        *
*     DOES NOT ATTEMPT UPDATES.                                       *
*     IF A TTR IS SELECTED OUTSIDE THE RANGE OF THE CATALOG DATASET   *
*     AN ERROR MESSAGE IS PUT ON THE OPERATORS CONSOLE TO THE EFFECT  *
*     THAT AN I/O ERROR HAS OCCURRED IN THE CATALOG (AS WELL AS THE   *
*     RC=28 RETURN CODE TO READCVOL.  THIS MESSAGE CAN BE IGNORED     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* WARNING:                                                            *
*     THIS PROGRAM WILL RETURN ERROR MESSAGES DEPENDING ON THE RETURN *
*     CODES FROM THE LOCATE MACRO.  SEE MANUAL DESCRIBING LOCATE AND  *
*     ERROR MESSAGES BELOW.                                           *
*     HOWEVER ONE ERROR HAS BEEN IN CATALOG MANAGEMENT SINCE IT WAS   *
*     DESIGNED AND CAUSES THIS PROGRAM TO GIVE INCORRECT OUTPUT.      *
*     IF A TTR IS ENTERED WHOSE BLOCK COUNT IS ONE LARGER THAN THE    *
*     TRACK CAPACITY OF THE DISK: IE  TTR=XXXX25 FOR 3350             *
*                                     TTR=XXXX1D FOR 3330             *
*     THEN INSTEAD OF RETURNING RC=28 LOCATE AND THIS PROGRAM WILL    *
*     RETURN A CODE OF ZERO AND DATA THAT REPRESENTS THE FIRST BLOCK  *
*     OF THE NEXT TRACK.                                              *
*     REFER TO APAR OZ54204.  THIS ERROR IS NOT LIKELY TO BE FIXED.   *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* USE    :                                                            *
*     THIS PROGRAM CAN BE USED UNDER MVS AND TSO OR UNDER VS1 AND TONE*
*     TO USE ENTER:                                                   *
*                        READCVOL            (NO ARGUMENTS)           *
*               OR                                                    *
*                        CALL LIBRARY(READCVOL)'   (IF NOT INSTALLED  *
*                                     IN A LINKLIST LIBRARY)'         *
*                                                                     *
*     SUBCOMMANDS:                                                    *
*                                                                     *
*               VOL=VVVVVV     THE VOLUME ON WHICH CVOL CATALOG       *
*                              (SYSCTLG DATASET) RESIDES. DEFAULT IS  *
*                              HARD CODED AS "SYSRES" AND MAY BE CHAN-*
*                              GED TO MOST COMMONLY USED BY INSTALLN. *
*               XXXXXX         TTR OF DESIRED CATALOG BLOCK IN HEX    *
*                              LEADING ZEROES REQUIRED. MUST START    *
*                              WITH NUMERIC                           *
*               DDDDDDDD.DDD   DATASET NAME. UP TO 44 CHAR            *
*                              MUST START WITH ALPHABETIC CHAR.       *
*                              MAY CONSIST OF ONE OR MORE INDEX LEVELS*
*                              ONLY                                   *
*                                                                     *
*               END       )    RETURN TO TSO                          *
*               E         )                                           *
*                                                                     *
*               T              TEST  DEVEOPMENT USE ONLY              *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* MACROS USED:                                                        *
*              $SAVER     LINK CONTROL MANAGEMENT                     *
*              $HEXDMP    FORMAT 3 LINE HEXADECIMAL O/P               *
*              $MVBIN     CONVERT BINARY TO CHAR FOR O/P              *
*              $HEXPACK   CONVERT AND VALIDATE HEXADECIMAL I/P        *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         MACRO
&LAB     $SAVER &AREA
         GBLA  &$S
         LCLB  &B1,&B9
         LCLC  &A,&A1,&A2,&A3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                   * SAVER MACRO *
*                   STANDARD MODULE ENTRY LINKAGE SEQUENCE
*                   BANK OF N.S.W.      AUTHOR: R.E.D.  DATE: 9 NOV 71
*              $SAVER  ENHANCEMENTS FOR RE-ENTRANT MODULES 14/7/75 RED
&$S      SETA  &$S+1
&A       SETC  '$SAVE'.'&$S'
&A1      SETC  '$SA'.'&$S'
&A2      SETC  '$RET'.'&$S'
&A3      SETC  '$RETCC'.'&$S'
         AIF   (T'&AREA EQ 'O').BB      OPERAND OMITTED?
&B1      SETB  1
&A       SETC  '&AREA'
         AIF   ('&A' NE '$RENT').BB
&B9      SETB  1
.BB      ANOP
&LAB     SAVE  (14,12),T,*
         BALR  RB,0                     LOAD BASE REGISTER
         USING *,RB                     TELL ASSEMBLER
         AIF   (NOT &B9).B8
         LR    R3,R1                    SAVE PARM  ->
*        GETMAIN R,LV=72                GET LO SAVE AREA
         GETMAIN R,LV=72                GET LO SAVE AREA
         ST    RS,4(R1)                 STO HI S.A. ADDRESS IN LO S.A.
         XC    8(4,R1),8(R1)            ZERO LO SA ADDR IN LO SA
         ST    R1,8(RS)                 STO LO S.A. ADDRESS IN HI S.A.
         LR    RS,R1                    LOAD LO SA  ADDRESS IN SAVE REG
         B     &A1                      BYPASS RETURN CODING
*                   RETURN  CODING  -USER BRANCHES HERE TO RETURN
&A3      SLR   R15,R15                  SET RETURN CODE=0
&A2      LR    R1,RS                    ADDRESS OF LO S.A. TB RELEASED
         L     RS,4(R1)                 RESTORE HI SA ADDR TO SAVE REG
*        FREEMAIN R,LV=72,A=(1)         RELEASE STORAGE
         FREEMAIN R,LV=72,A=(1)         RELEASE STORAGE
         L     R14,12(RS)               RESTORE  REG 14
         LM    R0,R12,20(RS)            RESTORE  REG 0 - 12
         MVI   12(R13),X'FF'            SET RETURN FLAG
         BR    R14                      RETURN
&A1      LR    R1,R3                    RESTORE PARM ->
         SPACE 5
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         MEXIT
.B8      ANOP
         ST    RS,&A+4                  STO HI SA ADDRESS IN THIS S.A.
         LR    R3,RS                    TEMP SAVE HI S.A. ADDRESS
         AIF   (&B1).B2
         CNOP  0,4                      ENSURE BOUNDARY ALIGNMENT S.A.
         BAL   RS,&A1                   LOAD LO SA ADDRESS IN SAVE REG
&A       DC    3F'0'
         DS    15F
         AGO   .B3
.B2      ANOP
         LA    RS,&A                    LOAD LO SA ADDRESS IN SAVE REG
         B     &A1
.B3      ANOP
&A3      SLR   R15,R15                  SET RETURN CODE=0
&A2      L     RS,&A+4
         L     R14,12(RS)               RESTORE RETURN ADDRESS
         LM    R0,R12,20(RS)            RESTORE REGS
         MVI   12(RS),X'FF'            SET RETURN INDICATOR
         BR    R14
&A1      ST    RS,8(R3)                 STO LO SA ADDRESS HI S.A.
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 5
         MEND
* * * * * * * * * * * * * *  $HEXDMP  MACRO * * * * * * * * * * * * * *
         MACRO
&NAME    $HEXDMP   &AREA,&L=75,&H=YES
*/*$ ASSEMBLER MACRO TPUTS A 3 LINE VERTICAL HEX LINE TO TERMINAL
*/*$E.   $HEXDMP   &AREA,&L=75,&H=YES    (FROMAREA,O/P LENGTH,HEADING)
         GBLB      &$HEXDSW
         AIF       (T'&NAME EQ 'O').BYPL
&NAME    EQU       *
.BYPL    AIF       ('&H' NE 'YES').NOHEAD
         TPUT      $HEXDMPH,L'$HEXDMPH
.NOHEAD  ANOP
         MVC       $HEXDMPA,$HEXDMPA-1
         MVC       $HEXDMPA(&L),&AREA
         TR        $HEXDMPA(&L),TRTAB1
         TPUT      $HEXDMPA,&L
         MVC       $HEXDMPA(&L),&AREA
         TR        $HEXDMPA(&L),TRTAB2
         TPUT      $HEXDMPA,&L
         MVC       $HEXDMPA(&L),&AREA
         TR        $HEXDMPA(&L),TRTAB3
         TPUT      $HEXDMPA,&L
         AIF       (&$HEXDSW).END
         B         $HEXDMPL
*             THE FOLLOWING TABLE CONTAINS LOWER CASE LETTERS
TRTAB1   DC        C'................................' 00
         DC        C'................................' 20
         DC        C' .........Ö.<(+×&&.........!$*);^' 40
         DC        C'-/.........,%_>?..........:#@''="' 60
         DC        C'.abcdefghi.......jklmnopqr......' 80
         DC        C'..stuvwxyz......................' A0
         DC        C'.ABCDEFGHI.......JKLMNOPQR......' C0
         DC        C'..STUVWXYZ......0123456789......' E0
TRTAB2   DC        16CL1'0'
         DC        16CL1'1'
         DC        16CL1'2'
         DC        16CL1'3'
         DC        16CL1'4'
         DC        16CL1'5'
         DC        16CL1'6'
         DC        16CL1'7'
         DC        16CL1'8'
         DC        16CL1'9'
         DC        16CL1'A'
         DC        16CL1'B'
         DC        16CL1'C'
         DC        16CL1'D'
         DC        16CL1'E'
         DC        16CL1'F'
TRTAB3   DC        16CL16'0123456789ABCDEF'
$HEXDMPA DC        C' '
$HEXDMP  DS        CL256
$HEXDMPH DC       CL20' '      HEADING
$HEXDMPL NOPR     R0
&$HEXDSW SETB     1
.END     MEND
* * * * * * * * * * * * $MVBIN MACRO* * * * * * * * * * * * * * * * * *
         MACRO
&NAME    $MVBIN &A,&B,&L=5,&T=,&Z=
*/*$ ASSEMBLER MACRO CONVERTS FROM BINARY TO CHARACTERS
*/*$     $MVBIN &A,&B,&L=5,&T=,&Z=
*/*$      ARGS: TO,FROM,L=TOLENGTH,T=H,Z=Y
         GBLB  &$MVBSW
         LCLA  &L2,&L3,&L4
         LCLC  &TT
&L4      SETA  &L                  DESIRED LENGTH OF O/P FIELD
         AIF   (&L NE &L/2*2).ODD
&L4      SETA  &L+1                MAKE IT ODD
         MNOTE 4,'L SHOULD BE ODD. HAVE INCREASED IT BY 1.'
.ODD     ANOP
&TT      SETC  '&T'
&L2      SETA  &L4/2+1
&L3      SETA  8-&L2
         AIF   (T'&B EQ 'O' OR T'&A EQ 'O').NOK2
         AGO   .OK2
.NOK2    MNOTE 4,'$MVBIN FIRST OR SECOND POSITIONAL ARG OMITTED'
         MEXIT
.OK2     ANOP
         AIF   (T'&T EQ 'O' OR '&T' EQ 'H').OK
         MNOTE 4,'$MVBIN T=H FOR HALFWORD IS ONLY OPTION'
&TT      SETC  ' '
.OK      ANOP
&NAME    SR    R0,R0
         L&TT  R0,&B
         CVD   R0,$MVBWORK
         IC    R0,&A-1        SAVE CHARACTER
         MVC   &A-1(&L4+1),=X'40202020202020202020'
         MVI   &A+&L4-2,X'21'            SIGNIFICANCE START
         ED    &A-1(&L4+1),$MVBWORK+&L3
         STC   R0,&A-1        RESTORE IT
         AIF   (&$MVBSW).ENDIT
         B     $MVBWORK+8
$MVBWORK DC    D'0'
&$MVBSW   SETB  1
.ENDIT   ANOP
         MEND
* * * * * * * * * * * * * $HEXPACK MACRO  * * * * * * * * * * * * * * *
         MACRO
&NAME    $HEXPACK  &OUT,&IN,&L=8
*/*$ ASSEMBLER MACRO CHECKS I/P VALID HEX CHARS AND PACKS
*/*$     $HEXPACK  &OUT,&IN,&L=8     (TO,FROM,LENGTH OF I/P)
         GBLB      &$HEXPSW
         LCLA      &L1
&L1      SETA      &L/2+1
&NAME    TRT       &IN.(&L),$HEXTB1      IS IT VALID HEX
         BZ        *+12                 NO LEAVE O/P AS ZERO
         LA        R15,4                SET R15=0 FOR BAD HEX
         B         *+20                 BYPASS CONVERSION
         TR        &IN.(&L),$HEXTB2-C'A' TRANSLATE
         PACK      &OUT.(&L1),&IN.(&L+1)
         MVI       &OUT+&L1-1,X'00'     ZERO LAST BYTE
         AIF       (&$HEXPSW).END
         B         $HEXPCKL
$HEXTB1  DC        193XL1'04'         INVALID
         DC        6X'00'               A-F VALID
         DC        41X'04'            INVALID
         DC        10X'00'            0-9 VALID
         DC        6X'04'             INVALID
$HEXTB2  DC        X'FAFBFCFDFEFF'      A-F
         DC        41X'00'              INVALID
         DC        C'0123456789'
         DC        6X'00'               INVALID
$HEXPCKL NOPR     R0
&$HEXPSW SETB     1
.END     MEND
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                   STANDARD REGISTER EQUATES
*                   BANK OF N.S.W.      AUTHOR: R.E.D.  DATE: 9 NOV 71
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13                       SAVE AREA PTR
R14      EQU   14
R15      EQU   15
RA       EQU   10
RB       EQU   11                       MAIN BASE REG
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15                       CALLED MODULE ADDRESS
RP       EQU   0                        PARAMETER PASSING REGISTER
RP1      EQU   1                        PARAMETER PASSING REGISTER
RW       EQU   10                       WORK AREA OR COMMON AREA PTR
RL       EQU   12                       SUBROUTINE LINK REG
RS       EQU   13                       SAVE AREA POINTER
RR       EQU   14                       MODULE LINKAGE RETURN ADDRESS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
READCVOL CSECT
         $SAVER
NEWGET   MVC       MSG2V,CVOL        MARK CURRENT CVOL ON MSG
         TPUT      MSG1,L'MSG1
         MVC       DSN,=CL44' '        BLANK OUT RECEIVING AREA
         TGET      DSN,44
         LTR       R1,R1            LENGTH OF INPUT
         BNZ       RDC7
         TPUT      MSG9,L'MSG9
         B         NEWGET
RDC7     OC        DSN,=CL44' '       TO UPPER CASE
         CLC       DSN(4),=C'END '
         BE        $RET1
         CLC       DSN(3),=C'E  '     END
         BE        $RET1
         CLC       DSN(2),=C'T '       TEST STATUS
         BNE       RDC8
         OI        FLAG,1              MARK TEST STATUS
         B         NEWGET
RDC8     CLC       DSN(4),=C'VOL='     SPECIFY CVOL CATALOG VOLUME
         BNE       RDC2
         MVC       CVOL,DSN+4
         B         NEWGET
RDC2     CLI       DSN,C'0'           IF FIRST CHAR NUM THEN ASSUME TTR
         BE        TRYTTR
         MVC       MSG2T,DASH         DASH OUT TTR FIELD IN HEADING
         MVC       MSG2D,DSN          ENTER PART OF DSN
         LOCATE    LST
TESTRC   ST        R15,SAV15
         ST        R0,SAV0
         C         R15,=F'4'          IS IT WRONG VOL ERROR
         BNE       RDC3A
         TPUT      MSG6,MSG6END-MSG6
         TM        FLAG,1             IS IT TEST MODE
         BO        RDC3               YES PUT OUT ANYWAY
         B         NEWGET
RDC3A    C         R15,=F'20'         RC20?
         BNE       RDC3B
         TPUT      MSG20,L'MSG20
         B         NEWGET
RDC3B    C         R15,=F'24'         RC24?
         BNE       RDC3C
         TPUT      MSG24,L'MSG24
         B         NEWGET
RDC3C    C         R15,=F'28'         RC28?
         BNE       RDC3
         TPUT      MSG28,L'MSG28
         B         NEWGET
RDC3     $MVBIN    MSG2RC,SAV15,L=3
         MVC       MSG8R0,=C'---'
         CLI       SAV15+3,0        IF RC=0 NOT INTERESTED IN R0
         BE        RD3Z
         $MVBIN    MSG8R0,SAV0,L=3
RD3Z     STLINENO  LINE=1            LETS START AT TOP OF SCREEN
         TPUT      MSG2,79
         TPUT      MSG8,MSG8END-MSG8     HEX POINTERS
         $HEXDMP   A1,L=64,H=NO
         $HEXDMP   A2,L=64
         $HEXDMP   A3,L=64
         $HEXDMP   A4,L=73
         TPUT      MSG7,MSG7END-MSG7     HEX POINTERS
         CLI       SAV15+3,8
         BNE       RD3X
         TPUT      MSG08,MSG08END-MSG08  ENTRY NOT FOUND MSG FOR RC 8
RD3X     CLI       SAV15+3,16
         BNE       RD3Y
         TPUT      MSG16,MSG16END-MSG16
RD3Y     CLI       SAV15+3,12
         BNE       NEWGET
         TPUT      MSG12,L'MSG12
         B         NEWGET
TRYTTR   MVC       TTRC,DSN                SAVE CHAR FORM OF TTR
         $HEXPACK  TTR,DSN,L=6
         C         R15,=F'4'
         BNE       TTROK
         TPUT      MSG3,L'MSG3
         B         NEWGET
TTROK    MVC       MSG2D,DASH              DASH OUT DSN ) IN HEADING
         MVC       MSG2T,TTRC              INSERT TTR   )
         LOCATE    LSTT
         B         TESTRC
LST      CAMLST    NAME,DSN,CVOL,AREA
LSTT     CAMLST    BLOCK,TTR,CVOL,AREA
DSN      DC        CL44' '
TTR      DC        F'0'
TTRC     DC        CL6' '                 SAVE CHARACTER FORM OF TTR
CVOL     DC        C'SYSRES'
         DS        0D
AREA     DS        CL270'B4'
EAREA    DC        F'0'
         ORG       AREA
A1       DS        CL64
A2       DS        CL64
A3       DS        CL64
A4       DS        CL64
A5       DS        CL84
         ORG
SAV15    DC        F'0'
SAV0     DC        F'0'
FLAG     DC        X'00'                 01 = TEST STATUS
MSG1     DC        C'ENTER DSNAME, TTR, END OR VOL=..:'
MSG3     DC        C'TTR NOT VALID HEX. 6 HEX DIGITS REQUIRED'
MSG6     DC        C'ERROR VOLUME NOT MOUNTED OR DOES NOT HAVE CATALOG'
         DC        C' RETURN CODE= 4 FROM LOCATE'
MSG6END  EQU       *
MSG7     DC        C'C.......×.......D.......×.......E.......×.......'
         DC        C'F.......×.......100 <- HEX'
MSG7END  EQU       *
MSG8     DC        C'0.......×.......1.......×.......2.......×.......'
         DC        C'3.......×.......40 <-HEX;R0:'
MSG8R0   DC        C'---'
MSG8END  EQU       *
MSG9     DC        C'ERROR NOTHING ENTERED. ENTER END TO TERMINATE'
MSG08    DC        C'ENTRY NOT FOUND; OR GDG ALIAS FOUND; OR'
         DC        C' IF R0=56 USER NOT AUTHORIZED'
MSG08END EQU       *
MSG12    DC        C'INDEX OR ALIAS FOUND'
MSG16    DC        C'YOU SPECIFIED TOO MANY INDEX LEVELS. DS FOUND'
         DC        C' AT R0 LEVELS'
MSG16END EQU       *
MSG28    DC        C'TTR ADDRESS OUTSIDE SYSCTLG DATA SET EXTENTS'
MSG24    DC        C'PERMANENT I/O ERROR'
MSG20    DC        C'SYNTAX ERROR IN NAME'
MSG2     DC        CL14'READCVOL--TTR:'
MSG2T    DC        CL6'------'
         DC        CL6'--DSN:'
MSG2D    DC        CL32'---------------------------------'
         DC        CL7'--CVOL:'
MSG2V    DC        CL6'------'
         DC        CL5'--RC:'
MSG2RC   DC        CL3'---'
DASH     DC        79C'-'
         LTORG
         END    READCVOL
