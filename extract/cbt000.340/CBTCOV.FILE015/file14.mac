         MACRO
&L       LOC   &T,&P
         LCLA  &A,&B
&A       SETA  N'&P
&B       SETA  K'&T-2
         AIF   ('&T' EQ '').ERR
         AIF   ('&P' EQ '').ERR
         AIF   (&B LE 0).ERR
&L       LA    R1,XIT&SYSNDX
         ST    R1,EXITADDR
         BAL   R14,INITLINE
         AIF   (&B LE 20).MVC
&B       SETA  20
.MVC     MVC   TITLE(&B),=C&T
&B       SETA  0
         L     R1,X'&P(1)'
.LOOP    ANOP
&B       SETA  &B+1
         AIF   (&B EQ &A).ENDGEN
         L     R1,X'&P(&B+1)'(R1)
         AGO   .LOOP
.ENDGEN  ANOP
         BAL   R9,ADCON
XIT&SYSNDX BAL R9,PUTLINE
         BAL   R8,SPACE
         MEXIT
.ERR     MNOTE 12,'MISSING PARAMETERS'
         MEND
         SPACE 3
         PRINT NOGEN
         SPACE
SSLOC    CSECT
         SAVE  (14,12),,*
         BALR  R10,0
         USING *,R10
         LA    R3,SAV
         ST    R13,4(R3)
         ST    R3,8(R13)
         LR    R13,R3
         SPACE 2
         OPEN  (OUT,(OUTPUT))
         SPACE 2
         SPIE  PIC,(4,5)           TRAP OC4,OC5
         ST    R1,APICA            STORE ADDR PREV PICA
         SPACE 3
         LOC   'CVT',10
         LOC   'CSD',(10,294)
         LOC   'LCCA0',(10,300,0)
         LOC   'LCCA1',(10,300,4)
         LOC   'PCCA0',(10,2FC,0)
         LOC   'PCCA1',(10,2FC,4)
         LOC   'PSA0',(10,2FC,0,18)
         LOC   'PSA1',(10,2FC,4,18)
         LOC   'CST',(10,41C)
         LOC   'CAT (CH SET0)',(10,41C,0)
         LOC   'CAT (CH SET1)',(10,41C,20)
         LOC   'SCD S (CH SET0)',(10,41C,8)
         LOC   'SCD S (CH SET1)',(10,41C,28)
         LOC   'LPA DIRECTORY',(10,168)
         LOC   'GDA',(10,230)
         LOC   'PRIVATE AREA LO',(10,230,10)
         LOC   'PRIVATE AREA SIZE',(10,230,14)
         LOC   'PVT',(10,164)
         LOC   'PFT',(10,164,C)
         LOC   'ASMVT',(10,2C0)
         LOC   'PART',(10,2C0,8)
         LOC   'LOGREC BUFFER',(10,23C,20)
         LOC   'TRACE TABLE',(54)
         SPACE 3
         L     R1,APICA            PREV PICA
         SPIE  MF=(E,(1))          RESTORE
         SPACE
         CLOSE OUT
         L     R13,4(R13)
         RETURN (14,12),RC=0
         SPACE 2
PIC      MVC   8(4,R1),EXITADDR    CONTINUE ADDR INTO PIE
         BR    R14
         SPACE 2
INITLINE MVC   LINE,LINE-1
         MVC   TITLE,FILL
         MVC   ADDR,NOTF
         BR    R14
         SPACE
SPACE    MVC   LINE,LINE-1
         BAL   R9,PUTLINE
         BR    R8
         SPACE 2
PUTLINE  PUT   OUT,LINE
         BR    R9
         SPACE 2
ABEND    ABEND 1,DUMP              OC4 OR 5
         SPACE 2
ADCON    ST    R1,WORK
         BAL   R14,HCON
         STCM  R1,3,ADDR+6
         IC    R1,WORK+2
         BAL   R14,HCON
         STCM  R1,3,ADDR+4
         IC    R1,WORK+1
         BAL   R14,HCON
         STCM  R1,3,ADDR+2
         IC    R1,WORK
         BAL   R14,HCON
         STCM  R1,3,ADDR
         BR    R9
         SPACE 2
HCON     DS    0H                  CONVERTS THE LO ORDER BYTE OF R1
*                                  TO A 2 BYTE PRINTABLE HEX VALUE
*                                  IN THE 2 LO BYTES OF R1
         N     R1,=F'255'          IGNORE TOP 3 BYTES
         LR    R15,R1              COPY BYTE TO R15
         N     R1,=F'15'           CLEAR 4 HI BITS OF LO BYTE
         IC    R1,HTAB(R1)         LO NYBBLE CHARACTER
         SRL   R15,4               THROW OUT 4 LO BITS
         LA    R15,HTAB(R15)       POINT TO HI NYBBLE CHARACTER
         ICM   R1,2,0(R15)         AND PUT IT INTO R1
         BR    R14
         SPACE  2
HTAB     DC    C'0123456789ABCDEF' FOR CONV TO PRINTABLE HEX
         SPACE 3
SAV      DS    18F
APICA    DS    F
WORK     DS    F
EXITADDR DC    A(ABEND)
         DC    C' '
LINE     DS    0CL80
         DS    C
TITLE    DS    CL20
         DS    C
ADDR     DS    CL8
         DS    CL50
         SPACE
         DC    53C' '
FILL     DC    20C'-'
NOTF     DC    CL8'NOT AV.'
         SPACE 2
OUT      DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=F,BLKSIZE=80
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
