* *           THE FREESPAC PROGRAM IS INVOKED BY A STARTED
TASK AND READS THE VTOCS OF ALL PACKS ON THE VATLST00.
ONLY THE FORMAT5 CHAIN IS SCANNED AND FREESPACE IS
CALCULATED. OUTPUT IS TO SYSOUT  CLASS M . INFORMATION GIVEN
IS THE AMOUNT OF SPACE AVAILABLE IN TRACK AND
CYLINDER UNITS AS WELL SIZE OF THE LARGEST SINGLE
EXTENT AVAILABLE.

FREESPAC TITLE 'CALCULATE AND PRINT FREESPACE FROM VTOCS  B SAWARD'
***********************************************************************
**********      INITIALISATION
************
***********************************************************************
         SPACE 2
FREESPAC CSECT
         USING *,R12
         PRINT NOGEN
         B     24(R15)
         DC    CL8'FREESPAC'
         DC    CL4'&SYSTIME'
         DC    CL8'&SYSDATE'
         STM   R14,R12,8(R13)
         LR    R12,R15
         ST    R13,SV13
         LA    R13,SVREGS
         B     START
         EJECT
***********************************************************************
**********      CONSTANTS & STORAGE                        ************
***********************************************************************
         SPACE 2
         REGEQU
SVREGS   DS    9D
WORK     DS    D
SV13     DS    F
RETCODE  DC    F'0'
KPCYL    DS    F
KPTRK    DS    F
KPTOT    DS    F
MAXCYL   DS    F
MAXTRK   DS    F
BUFFER   DC    CL121' '
MSG1     DC    CL80'ERROR DDD READING VTOC ON DDDDDD  CCHHR = CCCCHHHHRX
               R  '
MSG2     DC    CL80'ALLOCATION ERROR INFO=0000 ERROR=0000 RC=00 '
MSG4     DC    CL80'FREESPACE ON VOLUME DDDDDD '
MSG4A    DC    CL80'TRACKS =  DDDDD     LARGEST = DDDDD'
MSG4B    DC    CL80'CYLINDERS = DDD     LARGEST =   DDD'
MSG4C    DC    CL80'******************************************'
MSG5     DC    CL80'UNABLE TO LOCATE DSCB5 CHAIN ON VOLUME DDDDDD '
DSNAME4  DC    44X'04'             FORMAT 4 DSCB NAME
CCHHR    DC    CL5'CCHHR'
CXZERO   DC    XL5'0'
VOLUME   DC    CL6'VOLUME'
         DS    0H                  AREA TO HALFWORD BNDRY
AREA     DC    CL140' '
TBCHAR   DC    C'0123456789ABCDEF'
VOLTAB   DC    50CL6' '
ENDVOL   EQU   *
SEARCH   CAMLST SEARCH,DSNAME4,VOLUME,AREA
SEEK     CAMLST SEEK,CCHHR,VOLUME,AREA
         EJECT
***********************************************************************
**********      DYNAMIC ALLOCATION BLOCKS                  ************
***********************************************************************
         SPACE 2
RBPTR    DS    0F                 POINTER TO REQUEST BLOCK
         DC    X'80'
         DC    AL3(RB)
*
RB       DC    X'14012000'        LENGTH,VERB,FLAGS
RBERR    DC    XL2'0'             ERROR REASON CODE
RBINFO   DC    XL2'0'             INFORMATION REASON CODE
RBTXT    DC    A(TEXT),2F'0'      ADDRESS OF TXT UNITS,RES AND FLAGS
*
TEXT     DC    A(TDDNAME)           POINTER TO DSNAME TEXT
         DC    A(TDSNAME)           POINTER TO DSNAME TEXT
         DC    A(TMEMBER)           POINTER TO MEMBER TEXT
         DC    A(TSTATUS)           POINTER TO STATUS TEXT
         DC    A(TDISP)             POINTER TO DISP TEXT
         DC    X'80',AL3(TUNALLOC)  POINTER TO UNALLOC TEXT
TDDNAME  DC    AL2(1),AL2(1),AL2(L'DDNAME) DDNAME
DDNAME   DC    C'INPUT'
TDSNAME  DC    AL2(2),AL2(1),AL2(L'DSNAME) DSNAME
DSNAME   DC    C'SYS1.PARMLIB'
TMEMBER  DC    AL2(3),AL2(1),AL2(L'MEMNAME) MEMBER NAME
MEMNAME  DC    C'VATLST00'
TSTATUS  DC    AL2(4),AL2(1),AL2(1),AL1(8) STATUS SHR
TDISP    DC    AL2(5),AL2(1),AL2(1),AL1(8) DISP KEEP
TUNALLOC DC    AL2(28),AL2(0)              UNALLOC AT CLOSE
TEXT1    DC    A(UDDNAME)           POINTER TO DSNAME TEXT
         DC    A(SYSOUT)            POINTER TO SYSOUT TEXT
         DC    A(FREE)              POINTER TO FREE TEXT
         DC    X'80',AL3(TUNALLOC)  POINTER TO UNALLOC TEXT
UDDNAME  DC    AL2(1),AL2(1),AL2(L'EDNAME) DDNAME
EDNAME   DC    C'OUTPUT'
SYSOUT   DC    AL2(24),AL2(1),AL2(1),C'M'     SYSOUT=M
FREE     DC    AL2(89),AL2(0)                 SYSOUT HOLD QUEUE
INPUT    DCB   DDNAME=INPUT,DSORG=PS,EODAD=EOFIN,MACRF=(GM),           X
               LRECL=80,BLKSIZE=80,RECFM=F
OUTPUT   DCB   DDNAME=OUTPUT,DSORG=PS,MACRF=(PM),LRECL=121,RECFM=FB,   X
               BLKSIZE=1210
         EJECT
***********************************************************************
**********       PROCESSING                                 ***********
***********************************************************************
START    DS    0H
         LA    1,RBPTR             ALLOCATE VATLST
         SVC   99                  DYNAMICALLY ALLOCATE
         LTR   15,15               WAS ALLOC OK ?
         BNZ   ERROR2              NO   TELL ABOUT THE ERROR
         LA    R1,TEXT1            SET UP FOR OUTPUT FILE
         ST    R1,RBTXT
         LA    1,RBPTR             ALLOCATE OUTPUT
         SVC   99                  DYNAMICALLY ALLOCATE
         LTR   15,15               WAS ALLOC OK ?
         BNZ   ERROR2              NO   TELL ABOUT THE ERROR
         OPEN  (INPUT,(INPUT),OUTPUT,(OUTPUT))
         LA    R2,VOLTAB           ADDRESS VOLUME TABLE
         LA    R3,((ENDVOL-VOLTAB)/L'VOLTAB) LOOP CONTROL
         EJECT
***********************************************************************
**********       READ THE VATLST TO BUILD VOLUME TABLE      ***********
***********************************************************************
         SPACE 2
READ     DS    0H
         GET   INPUT,BUFFER
         MVC   0(L'VOLTAB,R2),BUFFER         PICK UP VOLID
         CLI   BUFFER,C'*'         IS THIS A COMMENT ?
         BE    READ                YES
         LA    R2,L'VOLTAB(,R2)    BUMP TO NEXT ENTRY
         BCT   R3,READ
EOFIN    DS    0H
         CLOSE INPUT               CLOSE PARMLIB AND DEALLOCATE
         B     GETVOL1
         EJECT
***********************************************************************
**********       SELECT NEXT VOLUME FROM THE VOLUME TABLE   ***********
***********************************************************************
         SPACE 2
GETVOL   DS    0H
         LM    R5,R9,KPCYL         PICK UP COUNTERS
         CVD   R5,WORK
         UNPK  MSG4B+12(3),WORK+6(2)
         OI    MSG4B+14,C'0'
         CVD   R7,WORK
         UNPK  MSG4A+10(5),WORK+5(3)
         OI    MSG4A+14,C'0'
         CVD   R8,WORK
         UNPK  MSG4B+32(3),WORK+6(2)
         OI    MSG4B+34,C'0'
         CVD   R9,WORK
         UNPK  MSG4A+30(5),WORK+5(3)
         OI    MSG4A+34,C'0'
         MVC   BUFFER(80),MSG4
         PUT   OUTPUT,BUFFER
         MVC   BUFFER(80),MSG4A
         PUT   OUTPUT,BUFFER
         MVC   BUFFER(80),MSG4B
         PUT   OUTPUT,BUFFER
         MVC   BUFFER(80),MSG4C
         PUT   OUTPUT,BUFFER
GETVOL1  DS    0H
         LA    R2,VOLTAB
GETVOLP1 DS    0H
         CLI   0(R2),C' '          ALL DONE ?
         BE    END                 YES
         CLI   0(R2),C'*'          HAS THIS VOLUME BEEN DONE
         BNE   SELECT
         LA    R2,L'VOLTAB(R2)     BUMP THRU TABLE
         B     GETVOLP1
*
         EJECT
**********************************************************************{
**********       READ THE FORMAT 4 DSCB                     ***********
***********************************************************************
         SPACE 2
SELECT   DS    0H
         XC    KPCYL(20),KPCYL     CLEAR COUNTERS
         MVC   VOLUME(L'VOLTAB),0(R2) PLUG THE VOLID INTO THE SEARCH
         MVC   MSG4+20(6),0(R2)    VOLID TO MSG
         OBTAIN SEARCH             READ FORMAT 4 LABEL
         MVI   0(R2),C'*'          SET VOLUME USED INDICATOR
         MVC   CCHHR,AREA+96       SAVE THE ADDRESS OF 1ST DSCB
         EJECT
***********************************************************************
**********       READ THE FIRST DSCB AFTER THE FORMAT4      ***********
***********************************************************************
         SPACE 2
FORM1ST  DS    0H
         SR    R2,R2               CLEAR A REG
         IC    R2,CCHHR+4          PICK UP RECORD OF FORMAT 4 DSCB
         LA    R2,1(,R2)           BUMP TO NEXT
         STC   R2,CCHHR+4          SAVE NEW RECORD NUMBER
RDFMT5   DS    0H
         OBTAIN SEEK               THIS SHOULD BE THE 1ST FORMAT5 DSCB
         LTR   R15,R15
         BNZ   ERROR1              SOME SORT OF ERROR
         CLI   AREA+44,C'5'        IS THIS A FORMAT 5 ?
         BE    FORMAT5             YES
         MVC   MSG5+39(6),VOLUME   PUT VOLID TO MESSAGE
         MVC   BUFFER(80),MSG5
         PUT   OUTPUT,BUFFER
         B     GETVOL              PROCESS NEXT VOLUME
         EJECT
***********************************************************************
**********       PROCESS FORMAT 5 DSCBS                     ***********
***********************************************************************
         SPACE 2
FORMAT5  DS    0H
         MVC   CCHHR,AREA+135      SET UP FOR NEXT IN CHAIN
         LA    R4,AREA+4           ADDRESS 1ST AVALIABLE EXTENT
         LA    R5,26
FMT5LP1  DS    0H
         LA    R6,AREA+44          SET POINTER TO IDENT
         CR    R4,R6               AT THE IDENT ?
         BNE   FMT5LP2
         LA    R4,1(R4)            BUMP PAST FORMAT IDENT
FMT5LP2  DS    0H
         LH    R6,2(R4)
         C     R6,MAXCYL           IS THIS BIGGEST ?
         BNH   FMT5LP3             NO
         ST    R6,MAXCYL
FMT5LP3  DS    0H
         A     R6,KPCYL            ACCUMULATE FREE CYLS
         ST    R6,KPCYL
         LH    R6,2(R4)
         MH    R6,=H'30'           CYLS X TRKS/CYL
         SR    R7,R7
         IC    R7,4(R4)            PICK UP FREE TRACKS
         AR    R6,R7               ACCUMULATE TOTAL
         C     R6,MAXTRK           IS THIS BIGGEST ?
         BNH   FMT5LP4             NO
         ST    R6,MAXTRK
FMT5LP4  DS    0H
         A     R6,KPTOT
         ST    R6,KPTOT            SAVE NEW TOTAL
         A     R7,KPTRK            ACCUM FREE TRACKS
         ST    R7,KPTRK
         LA    R4,5(R4)            BUMP TO NEXT
         BCT   R5,FMT5LP1
         CLC   CCHHR,CXZERO        ALL DONE
         BE    GETVOL              YES
         B     RDFMT5              READ THE NEXT FORMAT5
         EJECT
***********************************************************************
**********      ERROR READING DSCB                         ************
***********************************************************************
         SPACE 2
ERROR1   DS    0H
         CH    R15,=H'20'          IS IT THE END OF THE VTOC ?
         BE    GETVOL
         MVC   MSG1+26(6),VOLUME   PUT IN VOLID
         CVD   R15,WORK
         UNPK  MSG1+6(3),WORK+6(2)
         OI    MSG1+8,C'0'
         UNPK  MSG1+42(11),CCHHR(6)
         TR    MSG1+42(10),TBCHAR-240
         MVI   MSG1+52,C' '
         MVC   BUFFER(80),MSG1
         PUT   OUTPUT,BUFFER
         B     GETVOL              PROCESS NEXT
         EJECT
***********************************************************************
**********      DYNAMIC ALLOCATION ERROR                   ************
***********************************************************************
         SPACE 2
ERROR2   DS    0H
         UNPK  MSG2+22(5),RBERR(3)
         TR    MSG2+22(4),TBCHAR-240
         MVI   MSG2+26,C' '
         UNPK  MSG2+33(5),RBINFO(3)
         TR    MSG2+33(4),TBCHAR-240
         MVI   MSG2+37,C' '
         STC   R15,WORK
         UNPK  MSG2+41(3),WORK(2)
         TR    MSG2+41(2),TBCHAR-240
         MVI   MSG2+43,C' '
         MVC   BUFFER(80),MSG2
         PUT   OUTPUT,BUFFER
         EJECT
***********************************************************************
**********      TERMINATION                                ************
***********************************************************************
         SPACE 2
END      DS    0H
         CLOSE OUTPUT
         L     R13,SV13
         MVC   12(4,R13),RETCODE   SET UP RETCODE
         LM    R14,R12,8(R13)
         BR    R14
         LTORG
         END   ,
/*
