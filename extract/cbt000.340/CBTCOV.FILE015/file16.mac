DISK TITLE 'DISK - ONLINE DISK MONITOR   W.TEALE'
*
*            O N L I N E  3 3 5 0  D A S D   M O N I T O R
*            *********************************************
*
*
*
*        SCANS THE UCB LISTS IN THE NUCLEUS FOR ALL DEVICES
*       THAT ARE DASD 3350 ONLINE AND MOUNTED. EBCDIC CUA/VOLID
*       ARE DISPLAYED ALONG WITH THE NUMBER OF SIO'S / SECOND.
*         SHOULD THE ADDRESS SPACE BE SWAPPED OUT, A ROUTINE
*       TO CALCULATE THE SIO RATE / SECOND IS BRANCHED TO.
*
*         A GRAPH IS DISPLAYED AGAINST THE DEVICE FOR ITS
*       SIO/SEC COUNT.IF THE SIO COUNT > 100 THEN ">>>" ARE
*       DISPLAYED ...... ALT/PA1 TO TERMINATE
*
*
*        The CHANNEL addr is LAST PATH TRIED (probably successfully)
*        If the average SIO's are > 50 then more highlighting is done +
*        ALARMS are sounded (asteroids all over again !)
*
*        The 'RQP' are the   (1)   no. of requests  (R)
*                            (2)   queue length     (Q)
*                            (3)   CPU issueing SIO (MP only) (P)
*
*        TSO serv. rtns are not used due to slackness.
*        Further slackness can be seen in the amount of STATEMENTS
*        that have been COMMENTed out. No provision was made for FUTURE
*        CAPACITY so if you have more than 40 drives you'll have to
*        butcher the code to overcome any SCREEN spill.
*        Several (many actually) screen adjustments are in the code
*        and had to be made as we added more dasds,leaving out COSMETIC
*        stuff ,killing some READABILITY to allow for more SCREEN dasd.
*
*
*        I use this as a C.P. but it can be invoked from CALL , passing
*        a SAMPLE time interval between 0 and 99 or DEFAULT to 10.
*
*
*                      ie DISK 3  ... or CALL mylib(DISK) '3'
*
*
*
DISKMON  CSECT
         USING *,12
         USING PSA,0
         PRINT NOGEN
R1314    STM   14,12,12(13)
         LR    12,15
         ST    1,R1SAVE             ADDR CPPL
         STAX  STAXEXIT,REPLACE=NO,USADDR=IND,MF=(E,STAXLIST)
         LTR   15,15
         BC    7,BUGGARD1
         BC    15,START1
STAXLIST STAX  ,MF=L              AREA FOR HIS PARMS
STAXEXIT EQU   *                  TERMINATE GRACEFULLY !!
         L     2,8(,1)            USERADDR IN STAXLIST (IE IND)
         OI    0(2),1             INDICATE ALT/PA1 HIT
         BCR   15,14              RETURN ADDR
BUGGARD1 EQU   *
         TPUT  XX,80
         BC    15,EXIT1
         DS    0D
XX       DC    CL80'**** ERROR IN PROCESSING STAX .. SEE W.T'
IND      DC    X'0'               INTERUPT INDICATOR
WORK     DS    D
FLAGS    DC    X'0'               STATUS INDICATORS
*              BIT 7 - UCBTBYTI OF X'30' HAS BEEN FOUND ... DISK
*              BIT 6 - DO NOT INITIALIZE SEARCH TABLE
*              BIT 5 - AVERAGE SIO'S > SAMPLE SIO S (HITS)
*              BIT 4 - >= 50 SIOS ...
*              BIT 3 - NO HITS .. NO GRAPH
*              BIT 2 - TIME PARM SEARCH CHECK INDICATOR
*              BIT 1 - PARM IS NOT DEFAULT
*              BIT 0 - HIGHLITE IF AVG > 4 AND HITS > 0
*
FLAGS2   DC    X'0'               STATUS INDICATORS
*              BIT 7 - HIGHLITE @@@@@@@@@ THESE IF AV > 50 !!!
SVADD1   DS    H                  UCB H/W ADDR FOR > COMPARE
R1SAVE   DS    F
START1   DS    0H
         L     1,548              AOLD
         L     1,176(,1)
         CLC   0(2,1),=C'MN'
         BC    8,OK
         CLC   0(2,1),=C'SM'
         BC    8,OK
         TPUT  BAD1,80
         BC    15,EXIT1
BAD1     DC    CL80'IEA703I 806-4  MODULE DISK ACCESSED'
OK       EQU   *
         GETMAIN R,LV=8000        SCREEN BUFFER
         LTR   15,15              OK ??
         BC    7,GETERR           NOPE
         ST    1,MEMADDR          SAVE ADDR OF MEM HERE
         LR    15,1
         AH    15,=H'6000'        DASD TABLE
         ST    15,DASDMAT
         BC    15,START
MEMADDR  DC    F'0'               SAVE ADDR OF MEMORY HERE
GETERR   EQU   *
         TPUT  GETER1,80
         BC    15,EXIT
         DS    0D
GETER1   DC    CL80'***** TOO BAD .. ERROR IN GETMAIN  TOUGH EH ?'
START    DS    0H
*              GET TIME INTERVAL PARM
         L     1,R1SAVE            ADDR OF CPPL
         L     1,0(,1)            BUFFER FROM CPPL
         CLC   2(2,1),=X'FFFF'    HAILED BY 'ENQ' ??
         BC    7,$$$#
         MVC   DELAY1(4),=F'100'  1 SEC SAMPLE
         BC    15,NOPARM
$$$#     CLI   2(1),C'0'          NUMERIC ??
         BC    4,CP               NO MUST BE CRAP OR CMD PROC
         LH    2,0(,1)            LENGTH
         CH    2,=H'2'            L'PARM > 2 (IE 99 SECS ) ?
         BC    2,NOPARM           YES,TPUT THAT DEFAULT IS THE GO
         LTR   2,2                ANY PARMS ??
         BC    8,NOPARM           NOPE
         BC    15,VALIDATE        GET THE GOODIES.
CP       EQU   *
         LH    3,0(,1)            LENGTH OF CP BUFFER USED
         LH    2,2(,1)            GET OFFSET TO DATA PAST COMMAND
         SR    3,2                HAVE WE GOT A PARM IN CPPL ?
         CH    3,=H'5'            DIFF B/W CP NAME AND PARM LENG +1
         BC    4,NOPARM           IF LEN$(0(1)-2(1)) <=4 THEN NO   PARM
         LA    2,2(,2)            DEC LENGTH TO OBTAIN OFFSET
         STC   2,*+7              OFFSET TO COMPARE TO
         CLC   0(2,1),=C'  '      NOPARMS ???
         BC    8,NOPARM
         LA    1,0(2,1)           POINT TO PARMS
VALIDATE EQU   *
         LR    2,1                SAVE 1 .. MIGHT NEED HIM LATER
         LA    4,DIGIT            POINT TO TABLE
         LA    3,L'DIGIT          LENGTH OF TABLE
         CLC   2(1,2),0(4)        IS THIS CHAR 0-9 OR BLANK ?
         BC    7,*+24             NO  CHECK AGAINST NEXT TABLE ENTRY
         TM    FLAGS,32           BEEN HERE B4 ????
         BC    1,*+28             SAVE THE TIME PARM FOR "STIMER"
         OI    FLAGS,32           COMPARE SUCCESSFUL ..
         LA    2,1(2)             INC BASE 2
         BC    15,*-34            CHECK ITS MATE
         LA    4,1(4)             BUMP TABLE POINTER
         BCT   3,*-34             OH WELL TRY AGAIN
         BC    15,NOPARM          THIS CHAR NOT VALID
         TM    3(1),128           IS THIS BIT 0 ON ??
         BC    1,*+14             YES MUST BE CHAR NUMERIC..OK
         MVC   3(1,1),2(1)        SHIFT HIM ACROSS 1 BYTE
         MVI   2(1),C'0'          GIVE HIM HI-ORD 0
         PACK  DBLWRD+6(2),2(2,1) SET IT UP FOR BINARY
         ED    PARMSAVE(4),DBLWRD+6 SAVE IN DEC FROM FOR TPUT
         CVB   2,DBLWRD           MAKE IT HEX
         MH    2,=H'100'          STIMER NEEDS 100/TH SECS
         ST    2,DELAY1           SAVE IT FOR STIMER
         OI    FLAGS,64           TIME IS NOT DEFAULT
NOPARM   EQU   *
         TIME  BIN                GET SESSION START TIME
         ST    0,STARTIME         SAVE IT TO RECORD TOTAL TIME IN USE
         L     2,DASDMAT          INITALIZ TABLE POINTER
         ST    2,TBPOINT
TOP      EQU   *
         XC    HIAVGE,HIAVGE      CLEAR THIS
*              CLEAR VIDEO RAM
         L     4,MEMADDR          ADDR OF VIDEO BUFFER
         L     5,=F'4000'          BUFFER LENGTH
         LA    6,0                2ND OP ADDR.. 0000
         LA    7,1                LENGTH
         ICM   6,8,=C' '          WITH BLANKS
         MVCL  4,6                CLEAR VIDEO BUFFER WITH BLANKS
         XR    7,7                CLEAR 7
         NI    FLAGS,X'FE'        SET DISK FOUND BIT OFF
         XC    SVADD1,SVADD1      RESET
         L     8,MEMADDR          8 POINTS TO SCREEN BUFFER
         MVC   0(5,8),=X'C311404013'
         LA    8,1(,8)            BANDAID
         MVC   19(2,8),=X'1DE8'
         MVC   21(L'LN1,8),LN1 MOVE IN TITLE
         MVC   68(2,8),=X'1D40'
         LA    8,2(8)
         MVC   100(13,8),=C'TOT  HIT  AVG'
*        MVC   260(13,8),=C'TOT  HIT  AVG'
*        MVC   187(8,8),=C'---  ***'
*        MVC   347(8,8),=C'---  ***'
         MVC   164(3,8),=C'RQP'
*        MVC   244(3,8),=C'RQP'
*        MVC   51(22,8),=C'AVERAGE SIO PER SECOND'
*        MVC   209(22,8),=C'AVERAGE SIO PER SECOND'
*        MVC   404(3,8),=C'RQP'
         TIME  BIN                CURRENT TIME
*
*        TOTAL SESSION TIME 2,3,4
*
         XR    2,2                CLEAR THIS
         LR    3,0                GET CURRENT TIME IN BINARY
         S     3,STARTIME         TIME DIFFERENCE
         ST    3,TOTTIME          SESSION TIME IN BINARY
         D     2,=F'360000'       GET HRS
         CVD   3,DBLWRD           SAVE HOURS IN DECIMAL HH-
         MVC   HOURS(2),DBLWRD+6
         LH    7,HOURS
         SRL   7,4                PUSH DECIMAL SIGN OUT THE END
         STH   7,HOURS
         MVC   HHMMSSTH(1),HOURS+1 PUT DECIMAL INTERGER OF HOURS
         SRDL  2,32               GET REMAINDER (IE MINS)
         D     2,=F'6000'         GET MINS
         CVD   3,DBLWRD           SAVE MINS IN DECIMAL-MM-
         MVC   HOURS(2),DBLWRD+6
         LH    7,HOURS
         SRL   7,4                PUSH DECIMAL SIGN OUT THE END
         STH   7,HOURS
         MVC   HHMMSSTH+1(1),HOURS+1   PUT DECIMAL INTERGER OF MINS
         SRDL  2,32               GET REMAINDER (IE SECS)
         D     2,=F'100'          GET SECONDS
         CVD   3,DBLWRD           SAVE SECS  IN DECIMAL -SS
         MVC   HOURS(2),DBLWRD+6
         LH    7,HOURS
         SRL   7,4                PUSH DECIMAL SIGN OUT THE END
         STH   7,HOURS
         MVC   HHMMSSTH+2(1),HOURS+1   PUT DECIMAL INTERGER SECS
         MVC   OLDTIME,SAVETIME   SAVE OLD  TIME
*        MVC   296(20,8),=C'Elapsed session time'
*        MVC   318(9,8),=X'F021207A20207A2020'
*        ED    318(9,8),HHMMSSTH
*        MVI   318(8),C' '      SET TO ' '
*        MVC   316(2,8),=X'1DE8'
*        MVC   327(2,8),=X'1D40'
*
*        MVC   136(20,8),=C'Elapsed session time'
*        MVC   158(9,8),=X'F021207A20207A2020'
*        ED    158(9,8),HHMMSSTH
*        MVI   158(8),C' '      SET TO ' '
*        MVC   156(2,8),=X'1DE8'
*        MVC   167(2,8),=X'1D40'
*
         MVC   126(20,8),=C'Elapsed session time'
         MVC   148(9,8),=X'F021207A20207A2020'
         ED    148(9,8),HHMMSSTH
         MVI   148(8),C' '      SET TO ' '
         MVC   146(2,8),=X'1DE8'
         MVC   157(2,8),=X'1D40'
         ST    0,SAVETIME         GET NEW TIME (HUNDREDTHS SECS)
         LR    7,0                PUT CURRENT TIME IN 7
         S     7,OLDTIME          GET TIME TAKEN FOR SAMPLE PERIOD
         C     7,=F'100'          SAMPLE TIME < 1 SEC ?
         BC    2,*+8              NO .. GET AROUND NSI
         L     7,=F'100'          SAMPLE TIME IS NOW AT LEAST 1 SEC
         ST    7,TIMEDIFF         SAVE THE DIFFERENCE
         TIME
         ST    0,DECTIME
         MVC   70(4,8),=C'Time'
         MVC   76(9,8),=X'4020207A20207A2020'
         ED    76(9,8),DECTIME
         MVC   75(2,8),=X'1DE8'
         MVC   85(2,8),=X'1D40'
*        MVI   103(8),C'*'
*        MVC   104(L'LN1-1,8),103(8)     UNDER LINE TITLE
         XR    4,4
         L     2,FLCCVT           CVT
         USING CVT,2
         L     3,CVTILK2          POINT TO LIST ADDR (CVTILK2)
         DROP  2
         USING UCBCMSEG,4         IEFUCBOB UCB MASK
         LA    8,248(,8)          GET ADDR OF 6TH LINE OF SCREEN (484)
*        LA    8,408(,8)          GET ADDR OF 6TH LINE OF SCREEN (484)
         S     8,=F'80'           another bandaid
UCBSRCH  EQU   *
         LH    4,0(,3)            GET HWD ADDR
         LTR   4,4
         BC    8,LAB2             DONT WANT X'00000000'
         CH    4,SVADD1           OPTIONAL CHANNEL ?????
         BC    13,END             YEP .. THAT'LL DO THEN
         STH   4,SVADD1           SAVE DEV ADDR FOR COMPARE
         CLI   UCBTBYT1,X'30'     IS IT DASD ?
         BNE   LAB1               NO  TRY AGAIN;GET NEXT UCBADDR
         CLI   UCBTBYT4,X'0B'     ONLINE     ?
         BNE   LAB2               NO  TRY AGAIN;GET NEXT UCBADDR
         TM    UCBSTAT,X'80'      IS IT ONLINE ???
         BNO   LAB2               NO  GET NEXT UCB
         OI    FLAGS,X'01'        FOUND DISK LIST !
         BAL   10,RTN1            GET INFO
         B     LAB2
         DS    0D
#TAB     DC    C'0123456789ABCDEF'
RAM1     DS    D                  A CONVENIENT TRANSIENT AREA
LAB1     EQU   *
         TM    FLAGS,1            GONE PAST THE DISKS ?
         BO    END                YES .. BAIL OUT
LAB2     EQU   *
         LA    3,2(,3)            GET NEXT 1/2/WRD UCB ENTRY
         B     UCBSRCH            GET ANOTHER
RTN1     EQU   *
         L     2,UCBEXTPT         POINT TO UCB EXTENSION
         USING UCBCMEXT,2
         UNPK  DWD(4),UCBCHAN(3)
         STM   1,2,RAM1
         TR    DWD(4),#TAB-240
         LM    1,2,RAM1
         MVC   0(3,8),DWD        PUT IT IN
         MVC   3(2,8),=X'1D40'
         MVC   5(L'UCBVOLI,8),UCBVOLI SET UP CHR VOLID L(6)
         TM    FLAGS,X'02'        FIRST TIME THRU ?
         BC    1,LAB4            NO .. BEEN BEFORE..DO SIO COMPARE
*        SET UP DEVCNT TABLE WITH ADDRESSES AND CURRENT SIO COUNT
         L     6,TBPOINT          CURRENT TABLE POINTER
*
*      UCBNAME INSTEAD OF UCBUA AS SOME UA ARE DER SAME !
*
         MVC   1(3,6),UCBNAME     CHAR CHNUA NAME (SEE ABOVE)
         MVC   6(2,6),UCBMFCNT    INITIAL SIO COUNT
         MVC   10(2,6),UCBMFCNT     AND HERE TOO ....
         LA    6,12(6)            BUMP TABLE POINTER
         ST    6,TBPOINT          SAVE IT FOR NEXT DEVICE
         XC    DBLWRD,DBLWRD      CLEAR THIS
         MVI   DBLWRD+7,X'0C'                 TO  DEC ZERO
         BC    15,LAB9            SIO IS "0" FOR 1ST TIME THRU
LAB4     EQU   *
         L     6,DASDMAT          ADDRESS DASD MATRIX
LOOP1    CLC   2(2,6),=C'20'      < 20 NOT A DISK IN OUR PLACE
         BL    END                END OF TABLE (SOME ERROR)
         CLC   1(3,6),UCBNAME     IS THIS OUR UNIT/ADDR ?
         BC    8,LAB9             YEP ..! COMPARE THE I/O COUNT
         LA    6,12(6)            OH WELL .. TRY AGAIN
         BC    15,LOOP1
*
*        CALC ACTUAL SIOS FOR PREVIOUS SAMP INTV IN 7
*              (7 GETS CLOBBERED AT 242 )
*
LAB9     DS    0D
         LH    7,UCBMFCNT         GET THE CURRENT I/O COUNT
         LH    9,6(6)             GET  PREVIOUS I/O   COUNT
         STH   7,6(6)             STOR CURRENT SIO COUNT FOR INTERVAL
         CR    7,9                IS 7 < 9 (WRAPPED ROUND UCBMFCNT?)
         BC    11,*+8             NO .. DONT ADJUST
         A     7,=F'65536'        ADJUST BY 64K
         SR    7,9                FIND THE DIFFERENCE
         LPR   7,7                MAKE ME +
         MH    7,=H'100'          K FOR 100 SIOS
         ST    6,REGSAVE1         PUSH 6
         XR    6,6                CLEAR FOR DIVIDE
         D     6,TIMEDIFF         AMOUNT OF SIOS FOR LAST SEC IN 7
         L     6,REGSAVE1         POP 6
         CVD   7,DBLWRD           DECIMAL HITS COUNT FOR SECOND
         ST    7,HITRATE          SAVE HITS FOR LAST SEC
         MVC   23(4,8),=X'40202120' SIO EDIT PATT (HITS)
         TM    FLAGS,2            1ST TIME THRU ??
         BC    1,*+14             NO BYPASS
         XC    DBLWRD,DBLWRD      CLEAR THIS
         MVI   DBLWRD+7,X'0C'                 TO  DEC ZERO
         ED    23(4,8),DBLWRD+6   HITS FROM LAST LAST SEC
         LTR   7,7                IS 7 <= 0 ?
         BC    2,*+10             NO ITS OK..
         XR    7,7                YES MAKE IT 0
         OI    FLAGS,16           SET BIT 3 ON (HITRATE = 0)
         C     7,=F'50'           IS HIT RATE > 50 ?
         BC    4,*+8              NO ... DONT SET BIT
         OI    FLAGS,8            SET BIT 4 ON (>= 50 SIOS)
*
*        TOTAL SIOS SINCE SESSION STARTED
*
         STM   4,5,REGSAVE1       PUSH 4 TO 5
         LH    4,UCBMFCNT         DEVICE SIOS COUNT SINCE NIP
         CH    4,10(6)            UCBMFCNT WRAPPED AROUND ?
         BC    11,*+8             NO DONT ADJUST
         A     4,=F'65536'        ADJUST BY 64K
         SH    4,10(6)            SIOS THIS SESSION IN 4
         LPR   4,4                MAKE +VE
         TM    FLAGS,2            BIT 0 OFF (2ND   TIME THRU ?)
         BC    1,*+18             PUT 4 VAL IN
         XC    DBLWRD,DBLWRD      CLEAR THIS
         OI    DBLWRD+7,X'0C'     DEC ZERO
         BC    15,*+8             GET AROUND THIS
         CVD   4,DBLWRD           DECIMAL SIO COUNT FOR THIS SESSION
         MVC   14(6,8),=X'402020202120' SIO EDIT PATT
         ED    14(6,8),DBLWRD+5   SIO SESSION CNT TO SCREEN BUFFER
*
*                CALCULATE SIOS/SEC
*
*              SIO DIFFERENCE IN 7
*
*
         TM    FLAGS,2            BIT 0 OFF (2ND   TIME THRU ?)
         BC    1,*+12             NO .. BYPASS NEXT JP
         LM    4,5,REGSAVE1       POP  4 TO 5
         BC    15,LAB5            THIS IS FIRST TIME THRU
         SRDL  4,32               CLEAR 4 AND PUT INTO 5
         MH    5,=H'100'          SIOS * K TO DIV WITH HUNDRETHS SECS
         D     4,TOTTIME          GET SIO RATE/SEC
         C     5,=F'0'            QUOTENT=0 ?
         BC    2,*+16             NO MUST BE > ABS(1)
         C     4,=F'0'            IS THERE A REMAINDER ? (> 0)
         BC    8,*+8              NO ... SIOS MUST BE 0
         OI    FLAGS,4             0 > AVERAGE < 1
         LR    7,5                7 HAS SIO/SEC
         TM    FLAGS,2            1ST TIME THRU ??
         BC    8,*+18             YES .. AVERAGE IS ZERO
*        BC    8,*+26             YES .. AVERAGE IS ZERO
*        CH    7,=H'4'            AV SIO => 4 (MIKES R.O.T) ??
*        BC    4,*+18             NO DONT' HI-LITE
         L     4,HITRATE          IF HIT RATE > 0 (WAS IT HIT ??)
         LTR   4,4                WELL ??
         BC    13,*+8             DON'T  HI-LITE
         OI    FLAGS,128          HILITE AVG > 4 AND HIT > 0
         LM    4,5,REGSAVE1       POP  4 TO 5
         TM    FLAGS,2            1ST TIME THRU ??
         BC    8,LAB5             YES NO HIGHTLITE CHECK HERE..
         CVD   7,DBLWRD           DECIMAL SIO COUNT/SEC
         TM    FLAGS,128          WAS I HIT ???
         BC    8,LAB5             NO ... NOT IN COMPARE HUNT !!
         C     7,HIAVGE           IS THIS THE HIGHEST AVG SO FAR ??
         BC    13,LAB5            NO ..KEEP GOING
         STM   7,8,HIAVGE         SAVE THIS VALUE FOR GRAPH HILITE
LAB5     EQU   *
         MVC   30(4,8),=X'40202120' SIO EDIT PATT
         ED    30(4,8),DBLWRD+6   AVERAGE SIO    TO SCREEN BUFFER
         TM    FLAGS,2            1ST TIME THRU ??
         BC    8,*+16             BYPASS THE NEXT LOT
         TM    FLAGS,4            IS THE SIO AVE IN HERE 0<AV>1 ?
         BC    8,*+8              NO DONT PUT ">" IN
         MVI   33(8),C'>'         IND  0 < AVERAGE > 1
         TM    FLAGS,2            FIRST TIME THRU ?
         BC    8,LAB7             YES .. NO GRAPH PLZ
         C     7,=F'50'           SIO > 50  ?
         BC    13,LAB8            NO NORMAL GRAPH
         S     7,=F'49'           SET UP FOR ">>>"
         L     11,MEMADDR
         MVI   0(11),C'G'
         MVC   39(50,8),ASTAS     BLOCK MOVE ASTAS
         MVC   58(22,8),=C'GREATER THAN 50  SIO S'
         OI    FLAGS2,1           HILITE THIS GRAPH
         BCTR  7,0                1 FOR MOVE
         EX    7,MOVE3            SIO GRAPH > 50
         SLR   6,6                ZERO 6,IS THIS IS SO THEN NO HITS
         BC    15,LAB7            BRANCH AROUND
LAB8     EQU   *
         L     6,HITRATE          GET HITS FOR THE DEVICE
         C     6,=F'1'            HITS < 1 ???
         BC    4,*+8              YES .. NO ASTA
         MVI   39(8),C'-'
         BCTR  6,0                DECR FOR LENGTH
         BCTR  7,0
         TM    FLAGS,8            >= 50 HITS ??
         BC    1,LAB7-24          YES SET UP DSPLY LIKE THIS
         CR    7,6                IS AV/SEC > HIT RATE ?
         BC    2,LAB10            YES .. BYPASS
         LTR   6,6                6 <   0 ?
         BC    4,*+8              YES NO HIT GRAPH
         EX    6,MOVE1            PUT IN ----------
         LTR   7,7                IS 7 <   0 ?
         BC    4,LAB7             YES.... NO GRAPH FOR THIS
         EX    7,MOVE2            AVERAGE SIOS/SEC (******)
         BC    15,LAB7
LAB10    EQU   *
         LTR   7,7                IS 7 <   0 ?
         BC    4,*+8              YES.... NO GRAPH FOR THIS
         EX    7,MOVE2            AVERAGE SIOS/SEC (******)
         LTR   6,6                6 <= 0 ?
         BC    4,LAB7             YES.... NO GRAPH FOR THIS
         EX    6,MOVE1            PUT IN ----------
         BC    15,LAB7
         L     11,MEMADDR
         MVI   0(11),C'G'
         MVC   39(50,8),PLUS      PUT '++++++++++++'
         MVC   75(7,8),=C' >= 50 '
         EX    7,MOVE2            AVERAGE SIOS/SEC (******)
LAB7     EQU   *
         TM    FLAGS,4            SIO 0 > AV < 1 ??
         BC    8,*+8              NO * REQD
         MVI   39(8),C'*'         PUT IN *
         MVC   11(4,8),=X'1D401D40'
         MVC   20(4,8),=X'1D401D40'
         MVC   27(4,8),=X'1D401D40'
         MVC   34(5,8),=X'1D404F1D40' FS "×" FS
         MVC   88(2,8),=X'1D40'
         TM    FLAGS,128          IS HIGHLITE REQD?
         BC    8,*+20             NO ...JP AROUND
         MVI   4(8),X'E8'
         MVI   14(8),X'E8'
         MVI   23(8),X'E8'
         MVI   30(8),X'E8'
         MVC   87(1,8),UCBCPU     WHO ISSUED EXCP ?
         MVC   86(1,8),UCBCNT     QUEUE LENGTH  (LESS THAN 9)
         OC    85(3,8),=3C'0'     ASCII
         CLI   86(8),240
         BC    7,*+8
         MVI   86(8),64
         MVC   85(1,8),UCBSQC       # RESERVES (LESS THAN 9)
         CLI   85(8),240
         BC    7,*+8
         MVI   85(8),64
         TM    UCBFLC,UCBTICBT    CHAN END /DEV END /MOUNT PENDING ???
         BC    8,*+22
         MVC   55(27,8),=C'!!! CHN/DEV END PENDING !!!'
         MVI   38(8),X'E8'
         L     11,MEMADDR
         MVI   0(11),C'G'
         TM    FLAGS2,1           IS HIGHLITE REQD? (@@@@@)
         BC    8,*+8              NO ...JP AROUND
         MVI   38(8),X'E8'
         LA    8,90(,8)           NEXT LINE (90 BYTES LONG ...)
         NI    FLAGS,X'63'        RESET BIT 0,3,4,5
         NI    FLAGS2,X'FE'        RESET BIT 7
         BCR   15,10              RETURN
DWD      DS    D
         SPACE 2
ERRO1    DS    0H
ERRO2    EQU   *
END      DS    0H
*        MVI   28(8),C'×'          Y-AXIS FOR SPACE 1
*        LA    8,80(,8)           NEXT LINE (NO SF CHARS HERE)
*        MVI   28(8),C'×'          Y-AXIS FOR SPACE 1
         MVI   30(8),C'='          X-AXIS "________"
         MVC   31(50,8),30(8)     DRAW X-AXIS
*        LA    8,80(,8)            NEXT LINE
*        LH    3,=H'5'            6 POINTS (0 - 100)
*        MVI   28(8),C'×'          Y-AXIS FOR SPACE 1
*        LA    2,39(,8)
*        MVI   0(2),C'×'
*        LA    2,10(,2)           SPACE 10  .. PUT IN '×'
*        BCT   3,*-8              DO 6 TIMES
*        LA    8,80(,8)            NEXT LINE
         LA    8,1(8)
         MVI   29(8),C'0'
         MVC   38(2,8),=C'10'     SET
         MVC   48(2,8),=C'20'        UP
         MVC   58(2,8),=C'30'           DOMAIN
         MVC   68(2,8),=C'40'                  SCALE
         MVC   78(2,8),=C'50'
*        LA    8,80(,8)            SPACE A LINE (NO SF)
*        LA    8,80(,8)            NEXT LINE
         BCTR  8,0                 another of many BANDAIDS
         MVC   0(18,8),=C'Sample interval is'
         TM    FLAGS,64           WAS A TIME PARM PASSED ?
         BC    1,LAB12            YES .. NO DEFAULT MSG
         BCTR  8,0                DEC 8 * 1
         MVC   0(2,8),=X'1DE8'
         LA    8,1(,8)            INC 8 * 1
         MVC   1(7,8),=C'DEFAULT'
         MVC   8(2,8),=X'1D40'    LO-LITE
         MVC   10(11,8),=C'interval is'
         MVC   21(6,8),=X'1DE8F1F01D60'
         MVC   27(6,8),=C'second'
         BC    15,A0000
LAB12    EQU   *
         MVC   18(2,8),=X'1DE8'    HIGHLITE INTV
         MVC   20(2,8),PARMSAVE+2 CHAR DSPLY OF TIME
         MVC   22(2,8),=X'1D60'    LOWLITE INTV
         MVC   24(6,8),=C'second'
*        MVC   51(22,8),=C'AVERAGE SIO PER SECOND'
A0000    TM    FLAGS,2            1ST TIME?
         BC    8,*+18             DON'T BOTHER WITH THIS..
         L     2,HIAVGE+4         GET ADDR OF GRAPH TO HILITE
         OR    2,2                X'00' ..?? HE WONT LIKE THAT..
         BC    8,*+8
         MVI   38(2),X'E8'
         OI    FLAGS,X'02'        BEEN THRU HERE NOW ..
         TCLEARQ OUTPUT           CLEAR 3270 BUFFER
         LA    8,81(8)
*        LA    8,80(8)
         L     1,MEMADDR
         LR    0,8
         SR    0,1
         ICM   1,8,=X'13'
         SVC   93
         STIMER WAIT,BINTVL=DELAY1
         TM    IND,1              ALT/PA1 ???
         BC    8,TOP              NO ..
EXIT     EQU   *
         FREEMAIN R,LV=8000,A=MEMADDR   RETURN THIS RAM
EXIT1    EQU   *
         LM    14,12,12(13)
         BCR   15,14
         LTORG
MOVE1    MVC   39(0,8),HITS
MOVE2    MVC   39(0,8),ASTAS
MOVE3    MVC   39(0,8),ARROWS
*** WAS .... ONE SEC .... SE B.C.S  (10 SECS TO CUT SWAPPING)
DELAY1   DC    F'1000'           WAIT 1 SEC FOR TPUT (1 SEC SAMPLE)
LN1      DC    C'ONLINE  3350  DISK  ACTIVITY ANALYSIS'
         DS    0D
DECTIME  DS    PL8                MOVE TIME FROM 0
DASDMAT  DS     F   POINTER TO   100 DEVICES,SIO COUNT,TOT SESS SIO
*       1         4         8          12         16
*           00        UA      INIT SIO    TOT SIO
*
TBPOINT  DS    F                  ADDR OF CURRENT TABLE POINTER
REGSAVE1 DS    3F                 PUSH REGS
SAVETIME DC    F'0'               SAVE NEW TIME IN HUNDREDTHS
OLDTIME  DS    F                  SAVE OLD TIME IN "   "   "
TIMEDIFF DS    F                  DIFF IN OLD AND NEW TIME
SAMPTIME DS    F                  SAMPLE TIME IN SECS FOR DIVIDE
STARTIME DS    F                  START OF SESSION IN BINARY
TOTTIME  DS    F                  CURRENT TIME - START TIME
HHMMSSTH DS    F                  SAVE FIELD SET UP FOR EDIT
HITRATE  DS    F                  HITS/SEC FOR LAST SAMPLE
PARMSAVE DC    X'40202120'        TIME PARM FROM USER
SAMPCNT  DC    F'0'               SAMPLES TAKEN
SIOCOUNT DC    F'0'               SESSION SIO COUNT
ASTAS    DC    50C'*'             AVERAGE SIO/SEC
ARROWS   DC    50C'>'             GREATER THAN 50 SIOS PER SEC
HITS     DC    50C'-'             HIT RATE DURING PREV SEC
PLUS     DC    50C'@'             HITS >= 50
DIGIT    DS    0CL12
         DC    C'0123456789 '     CHECKOUT THE TIME INTV
         DC    X'00'
HOURS    DS    H                  SESSION TIME IN DEC FROM MY CALC
MINS     DS    H                  SESSION TIME IN DEC FROM MY CALC
SECS     DS    H                  SESSION TIME IN DEC FROM MY CALC
         DS    0D
DBLWRD   DS    D                  CVD DOUBLE WRD
HIAVGE   DS    D                  ADDR AND COUNT OF HI-AVE FOR HI-LITE
DUM      EQU   *                  PUT ALL CONSTS B4 DIS...
         CVT   DSECT=YES
         IHAPSA
         IEFUCBOB
         PRINT ON
         END   ,
/*
