 ***********************************************************************
 ***********************************************************************
                             LOGWTR                                   1

            LOGWTR IS A SUITE OF PROGRAMS WHICH PRODUCE SYSOUT CLASS M O
          PRINTERS. IT IS REALLY A MODIFIED VERSION OF THE SUPPLIED EXTE
          WRITER 'IEFSD087'. THE MODIFICATION BEARS NO RESEMBLANCE TO TH
          ORIGINAL MODULE. THE COMPONENTS WHICH MAKE UP LOGWTR ARE :
                         IEFSD087,IEFSD87A,LOGINIT,LOGDSK
                               LOGTSK AND LOGPRT
          THEIR FUNCTIONS ARE EXPANDED LATER.
          LOGWTR IS SET UP AS A STARTED TASK WHICH INVOKES THE IBM MODUL
          IASXWR00 WHICH ESTABLISHES THE ENVIRONMENT FOR LOGWTR AND CALL
          IEFSD087 WHENEVER CLASS 'M' SYSOUT IS READY. LOGWTR READS THE
          SYSOUT DATA USING SAM AND WRITES TO THE 328X PRINTER DIRECTLY
          USING EXCP. NO ACCESS IS MADE TO TCAM OR VTAM.

         EXTERNAL WRITERS ARE USED TO PROCESS JES SYSOUT SPOOL FILES TO
         OUTPUT DEVICE I.E TAPE, PRINTER ETC.
         CURRENTLY TWO WRITERS ARE IN USE -
         1) LOGWTR - PRODUCES SYSOUT CLASS 'M' ON 3270 PRINTERS.
         2) FCHWTR - PRODUCES SYSOUT CLASS 'F' ON TAPE FOR LATER PRODUCT
                   - MICROFICHE.

         THE EXTERNAL WRITER IS AN IBM SUPPLIED COMPONENT. IT CONSISTS O
         SERIES OF MODULES. MODULE IEFSD087 ALLOWS USERS TO INTRODUCE TH
         ENHANCEMENTS.

         FOLLOWING IS A DESCRIPTION OF THE MODULES WE HAVE INTRODUCED TO
         SUPPLEMENT THE IBM MODULES -

         IEFSD087 -
         REPLACES THE IBM VERSION OF IEFSD087. ITS FUNCTION IS TO
         DETERMINE WHICH OF THE EXTERNAL WRITERS ARE REQUIRED I.E. LOGWT
         FCHWTR OR THE BASIC IBM EXTERNAL WRITER.
         DETERMINATION OF THE EXTERNAL WRITER REQUIRED IS CARRIED OUT BY
         SCANNING THE JCL WHICH INVOKED THE WRITER FOR A PARTICULAR DD C
         FOLLOWS -
         1) DD 'CONPRT' - INDICATES THE WRITER 'LOGWTR' IS REQUIRED. CON
                        - PASSED TO IEFSD87A.
         2) DD 'FCHTAP' - INDICATES THE WRITER 'FCHWTR' IS REQUIRED. CON
                        - PASSED TO IEFSD87C.
         IF NEITHER DD CARD IS PRESENT THEN CONTROL IS PASSED TO IEFSD87

         IEFSD87A -
         MAIN PROCESSING MODULE FOR LOGWTR. IT READS THE CLASS M SYSOUT
         DATA AND FORMATS THE PRINT BUFFERS FOR THE 328X PRINTER. TO LIG
         THE PRINT LOAD IT PERFORMS SOME EDITING TO ELIMINATE UNNECESSAR
         PRINTING. EDITING COMPRISES SUPPRESSION OF COMMENT STATEMENTS,
         STRIPPING SEQUENCE NUMBERS FROM JCL, SUPPRESSION OF UN-TRANSLAT
         JCL AND SUPPRESSION OF SUPERFLUOUS MESSAGES. THE MODULE ALSO CO
         JOB ACCOUNTING INFORMATION FOR EACH STEP AND CALLS LOGDSK TO RE
         THE STATISTICS. IF THE NUMBER OF LINES IN OUTPUT FOR A GROUP OF
         PRINTING FOR SYSMSG,JESMSG OR JESJCL EXCEEDS 100 LINES LOGTSK I
         ATTACHED TO ALLOW THE OPERATOR TO CANCEL THE OUTPUT. THIS ACTIO
         NECESSARY FOR CANCELLATION OF LARGE DUMPS AND SYSPRINT DATA WHI
         MANAGE TO CREEP INTO CLASS M SYSOUT.

         IEFSD87B -
         MAIN PROCESSING MODULE FOR THE IBM SUPPLIED WRITER. IT WAS RENA
         IEFSD087 (SEE ABOVE).

         IEFSD87C -
         MAIN PROCESSING MODULE FOR FCHWTR.USED TO CREATE TAPES FOR PROC
         THROUGH A COM MACHINE. IT READS FILES HELD ON SPOOL CLASS 'F' (
         HOLD QUEUE). THESE FILES HAVE BEEN CREATED BY JOBS, PROGRAMS ET
         THEIR OUTPUT IS REQUIRED ON MICROFICHE.
         EACH OF THESE SPOOL FILES HAS SYSTEM MAINTAINED INFORMATION -
          A) JOBNAME (TAKEN FROM JOBNAME FIELD OF THE JOB CARD IN THE JC
             TO CREATE THE SPOOL FILE).
          B) JOBID (THE SYSTEM JOBNUMBER GIVEN TO THE JOB).

IEFSD087 TITLE 'IEFSD087 - DETERMINE EXTERNAL WRITER REQUIRED AND PASS X
               CONTROL     -    R.LAMB'
         SPACE 5
*
*              THIS PROGRAM IS CALLED BY IASXWR00 - THE EXTERNAL
*              WRITER INITIATING PROGRAM.
*
*              IT REPLACES THE IBM SUPPLIED PROGRAM IEFSD087,WHICH
*              HAS BEEN RENAMED TO IEFSD87B.
*
*              THE PURPOSE OF THIS PROGRAM IS TO DETERMINE WHICH
*              OF THE FOLLOWING EXTERNAL WRITERS IS REQUIRED -
*
*                A) IEFSD87A - BANK WRITTEN PROGRAM THAT DIRECTS
*                            - OUTPUT TO A 3278 ................
*
*                B) IEFSD87B - IBM SUPPLIED THAT DIRECTS OUTPUT
*                            - TO TAPE OR DISK ................
*
*                B) IEFSD87C - BANK WRITTEN PROGRAM THAT DIRECTS
*                            - OUTPUT TO TAPE FOR FICHE PROCESSING.
*
*                  NOTE : ALL THESE MODULES PLUS THIS MODULE
*                       : RESIDE IN 'SYS1.PLINKLIB' ........
*
*              DETERMINATION OF WRITER THAT IS REQUIRED IS DONE
*              BY SCANNING THE TIOT (TASK I/O TABLE).
*
*                A) IF A DD CARD WITH THE NAME 'CONPRT' IS PRESENT
*                   CONTROL IS PASSED TO IEFSD87A.
*
*                B) IF A DD CARD WITH THE NAME 'FCHTAP' IS PRESENT
*                   CONTROL IS PASSED TO IEFSD87C.
*
*                C) IF NEITHER IS PRESENT CONTROL IS THEN PASSED TO
*                   IEFSD87B.
*
*              NOTE : 'CONPRT' IS THE NAME ON THE DD CARD SPECIFYING
*                   : THE OUTPUT PRINTER DEVICE. REFER TO MEMBER
*                   : 'LOGWTR' IN 'USER1.PROCLIB'...................
*
*              NOTE : 'FCHTAP' IS THE NAME ON THE A DUMMY DD CARD.
*                   : REFER TO MEMBER 'FCHPRT' IN 'USER1.PROCLIB'.
*
         EJECT
***********************************************************************
**********      INITIALISATION                             ************
***********************************************************************
         SPACE 2
IEFSD087 CSECT
         SAVE  (14,12)           * SAVE REGISTERS
         LR    R12,R15           * SET BASE REG
         USING IEFSD087,R12
         LA    R0,80             * GET 80 BYTES OF STORAGE
         GETMAIN R,LV=(0)        ** TO USE FOR REG SAVE/WORK AREA
         LR    R9,R1             *
         USING DSECT1,R9         * ADDRESS DSECT AREA
         ST    R13,4(R9)         * FORWARD CHAIN ADDRESS
         ST    R9,8(R13)         * BACKWARD CHAIN ADDRESS
         LR    R13,R9            * ADDRESS REG SAVE AREA
         B     START
         EJECT
***********************************************************************
**********      CONSTANTS & STORAGE & DSECTS               ************
***********************************************************************
         SPACE 2
         REGEQU
CONPRT   DC    CL6'CONPRT'
FCHTAP   DC    CL6'FCHTAP'
IEFSD87A DC    CL8'IEFSD87A'
IEFSD87B DC    CL8'IEFSD87B'
IEFSD87C DC    CL8'IEFSD87C'
*
DSECT1   DSECT
SVAREA   DS    9D
TIOTADR  DS    F
LOADNAME DS    F
         EJECT
***********************************************************************
**********  GET THE TIOT ADDRESS AND SEE IF EITHER THE      ***********
**********    CONPRT OR FCHTAP DD CARDS WERE USED.          ***********
***********************************************************************
         SPACE 1
IEFSD087 CSECT
START    DS    0H
         LA    R2,TIOTADR
         EXTRACT (2),'S',FIELDS=(TIOT)    * ADDR THE TIOT
         L     R2,TIOTADR                 * POINT TO
         LA    R2,24(R2)                  **  FIRST DD
M1A1     DS    0H
         CLI   0(R2),X'00'                * LAST DD ENTRY ?
         BE    M1C1                       * YES .. BR
         CLC   CONPRT,4(R2)               * IS DD NAME CONPRT ?
         BE    M1B1                       * YES .. BR
         CLC   FCHTAP,4(R2)               * IS DD NAME FCHTAP ?
         BE    M1B2                       * YES .. BR
         SR    R3,R3                      * CLEAR A REGISTER
         IC    R3,0(R2)                   * LOAD LENGTH OF THIS ENTRY
         AR    R2,R3                      * POINT TO NEXT ENTRY
         B     M1A1
         EJECT
***********************************************************************
**********  OUTPUT TO A 3278 (IEFSD87A) .........           ***********
***********************************************************************
         SPACE 1
M1B1     DS    0H
         LA    R2,IEFSD87A         * ADDR 'IEFSD87A' CONSTANT
         B     M1D1
***********************************************************************
**********  OUTPUT TO TAPE (IEFSD87C) FOR LATER             ***********
**********           FICHE PROCESSING ....                  ***********
***********************************************************************
         SPACE 1
M1B2     DS    0H
         LA    R2,IEFSD87C         * ADDR 'IEFSD87C' CONSTANT
         B     M1D1
***********************************************************************
**********  OUTPUT TO DISK/TAPE (IEFSD87B) .........        ***********
***********************************************************************
         SPACE 1
M1C1     DS    0H
         LA    R2,IEFSD87B         * ADDR 'IEFSD87B' CONSTANT
***********************************************************************
**********  COMMON PROCESSING FOR BOTH DRIVERS .....        ***********
***********************************************************************
         SPACE 1
M1D1     DS    0H
         ST    R2,LOADNAME      * SAVE ADDR FOR LATER DELETE
         LOAD  EPLOC=(2)        * LOAD THE REQ'D DRIVER
         LR    R15,R0           * LOAD LINK ADDR (R0 HAS LOADNAME ADDR)
*
         L     R2,4(R13)        * ADDR CALLING MODS REG SAVE AREA
         LM    R10,R11,60(R2)   * RESTORE CALLING MODS REGISTERS
         LM    R0,R8,20(R2)     ** (BUT NOT R9 OR R12,THESE ARE OUR
*                               *** BASE AND DSECT REGISTERS ......)
         BALR  R14,R15          * GOTO REQD DRIVER ............
***********************************************************************
**********      TERMINATION                                ************
***********************************************************************
         SPACE 1
END      DS    0H
         L     R2,LOADNAME       * ADDR THE DRIVER WE LOADED
         DELETE EPLOC=(2)        * NOW DELETE IT ..
         L     R13,4(R13)        * ADDR CALLING MODS REG SAVE AREA
         L     R9,8(R13)         * ADDR THIS MODS REG SAVE AREA
         FREEMAIN R,LV=80,A=(9)  * FREE REG SAVE/WORK AREA
         LM    R14,R12,12(R13)   * RESTORE CALLING MODS REGISTERS
         SR    R15,R15           * ZERO RETURN CODE
         BR    R14               * BYE BYE ........
         LTORG
         END   ,
/*
//SM0002A   JOB  OHOT,'IEFSD87A$9TH FLR',
//             MSGCLASS=X,NOTIFY=SM0002,CLASS=S,PERFORM=13
//JOBCAT  DD   DSN=MASTCAT.SY1RES,DISP=SHR
//        DD   DSN=USERCAT.TSOPK2,DISP=SHR
//*MAIN SYSTEM=JGLOBAL
//STEP1  EXEC  ASMLKED,MOD='SM.TEST.LINKLIB'
//ASM.SYSPRINT DD SYSOUT=X
//ASM.SYSIN    DD *
LOGWTR   TITLE 'PRINT CONSOLE LOGS TO 3286 PRINTER   B SAWARD'
***********************************************************************
*                                                                     *
*             LOGWTR-SELECT CLASS M SPOOL FILES AND PRINT TO          *
*             3286 PRINTER                                            *
*             NOTE - THIS MODULE IS CALLED BY IEFSD087.REFER          *
*                  - MEMBER IN THIS DATASET ................          *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*             INITIALISATION ..............................           *
*                                                                     *
***********************************************************************
         SPACE 3
IEFSD87A CSECT ,
         SAVE  (14,12)
         BALR  R9,0
         USING *,R9
         LR    R10,R1              ADDR PARAMETER LIST
         USING PARMLST,R10
         LA    R0,112              GETMAIN 112 BYTES
         GETMAIN R,LV=(0)
         LR    R11,R1              ADDR SAVE AREA DSECT
         USING SVAREA,R11
         ST    R13,OLDR13          SAVE ADDR OF RETURN REGS
         LA    R13,SAVE            ADDR SAVE AREA
         B     BEGIN               BEGIN PROCESSING
         EJECT
***********************************************************************
*                                                                     *
*             CONSTANTS AND STORAGE AREAS  ................           *
*                                                                     *
***********************************************************************
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         DS    0F
CFINERR  DC    A(SYNAD)
CFINEOF  DC    A(EOFAD)
*
HEAD     DC    CL6'++**++'
REPLY    DC    CL5' '
WTORECB  DC    F'0'
DKPARM   DC    X'80',AL3(0)
FENCE    DC    X'FFFFFFFF'
CCOMM1   DC    C'//* '
CCOMM2   DC    C'          *** '
CCJCL1   DC    C'//'
CCJCL2   DC    C'     //'
CCJCL3   DC    C'     XX'
CCIAT    DC    C'IAT6140 '
CCIEF    DC    C'IEF237I '
CCIEF142 DC    C'IEF142I '
CCIEF373 DC    C'IEF373I '
CCIEF374 DC    C'IEF374I '
CCIEF472 DC    C'IEF472I '
CCIEF236 DC    C'IEF236I '
CCZERO   DC    C'0000'
CF0      DC    F'0'
CH8      DC    H'8'
CH121    DC    H'121'
CX0      DC    XL35'00'
CCRETRY  DC    C'RETRY'
CCSTOP   DC    C'STOP'
CCDONE   DC    C'DONE'
CCOND    DC    C'COND CODE '
CF100    DC    F'100'
CF4096   DC    F'4096'
CF232    DC    F'232'
CF3864   DC    F'3864'
*
ENDMSG   DS    0CL50
         DC    X'C95C155C155C155C1540'
         DC    CL30'OUTPUT TERMINATED BY OPERATOR'
         DC    X'155C155C155C155C5C19'
         ORG   ,
RESMSG   DS    0CL50
         DC    X'C95C155C155C155C1540'
         DC    CL30'RESTARTED AFTER INTERVENTION '
         DC    X'155C155C155C155C5C19'
         ORG   ,
*
TBTR     DS    0CL256
         DC    X'40404040404040404040404040404040'  00-0F
         DC    X'40404040404040404040404040404040'  10-1F
         DC    X'40404040404040404040404040404040'  20-2F
         DC    X'40404040404040404040404040404040'  30-3F
         DC    X'404040404040404040404A4B4C4D4E4F'  40-4F Ö.<(+×&
         DC    X'504040404040404040405A5B5C5D5E5F'  50-5F & !$*);
         DC    X'60614040404040404040406B6C6D6E6F'  60-6F -/ ,%_>?
         DC    X'404040404040404040407A7B7C7D7E7F'  70-7F :#@'="
         DC    X'40818283848586878889404040404040'  80-8F ABCDEFGHI
         DC    X'40919293949596979899404040404040'  90-9F JKLMNOPQR
         DC    X'4040A2A3A4A5A6A7A8A9404040404040'  A0-AF  STUVWXYZ
         DC    X'40404040404040404040404040404040'  B0-BF
         DC    X'40C1C2C3C4C5C6C7C8C9404040404040'  C0-CF ABCDEFGHI
         DC    X'40D1D2D3D4D5D6D7D8D9404040404040'  D0-DF JKLMNOPQR
         DC    X'4040E2E3E4E5E6E7E8E9404040404040'  E0-EF  STUVWXYZ
         DC    X'F0F1F2F3F4F5F6F7F8F9404040404040'  F0-FF 0123456789
TBCHAR   DC    C'0123456789ABCDEF'
         EJECT
***********************************************************************
*                                                                     *
*             I/O CONTROL BLOCK CONSTANTS   (DCB IOB CCW)             *
*                                                                     *
***********************************************************************
         SPACE 3
*
CONDCB   DCB   DDNAME=CONPRT,                                          *
               MACRF=(E)
*
CONCCW   CCW   X'05',0,X'20',1920
CANCCW   CCW   X'05',ENDMSG,X'20',50
RESCCW   CCW   X'05',RESMSG,X'20',50
*
CONIOB   DS    0CL40
         DC    AL1(0),AL1(0)       IOB FLAGS 1 & 2
         DC    AL1(0),AL1(0)       IOB SENSE 0 & 1
         DC    AL1(0),AL3(0)       ECBCC & ECB ADDRESS
         DC    AL1(0),XL7'0'       IOB FLAG 3 & IOBCSW
         DC    AL1(0),AL3(0)       SIO CC & CCW ADDRESS
         DC    AL1(0),AL3(0)       RES & DCB ADDRESS
         DC    XL16'0'
*
         EJECT
***********************************************************************
*                                                                     *
*             DSECTS  .....................................           *
*                                                                     *
***********************************************************************
         SPACE 3
LOGCOM   DSECT ,
DCONDCB  DS    CL52                DCB AREA
DCONCCW  DS    D                   CCW AREA
DCONIOB  DS    CL40                IOB AREA
DCONECB  DS    F                   ECB AREA
*
SWTASK   DS    X'00'
SWEOF    DS    X'00'
*
SAVCCW   DS    D                   RECORD COUNTER
TSKECB   DS    F
TSAVE    DS    5F
SSAVE    DS    5F
CFRECD   DS    F                   RECORD COUNTER
IO1      DS    CL1920              PRINTER BUFFER
IO2      DS    CL121
IO3LEN   DS    H
IO3      DS    CL35                DISK RECORD
ENDWK    EQU   *
*
PARMLST  DSECT ,
PARM1    DS    F
PARM2    DS    F                   ADDR OF DCB OF OPENED OUTPUT DATASET
PARM3    DS    F                   ADDR OF DCB OF INPUT DATASET
PARM4    DS    F
PARM5    DS    F
*
SVAREA   DSECT ,
WORK     DS    D
OLDR13   DS    F
SAVE     DS    18F
*
         EJECT
         PRINT OFF
         DCBD  DSORG=PS
         PRINT ON
*
         EJECT
***********************************************************************
*                                                                     *
*             START PROCESSING  ...........................           *
*                                                                     *
***********************************************************************
         SPACE 3
IEFSD87A CSECT ,
BEGIN    DS    0H
         BAL   R6,CLEANUP
         L     R6,PARM3            ADDR OF INPUT DCB FROM PARMLIST
         USING IHADCB,R6 SET ADDRESSABILITY FOR DCB
         OPEN  ((6),INPUT)         OPEN THE INPUT FILE
         MVC   DCBSYNAD+1(3),CFINERR+1   INPUT ERR ADDR
         MVC   DCBEODAD+1(3),CFINEOF+1   EOF ADDR
         LA    R0,(ENDWK-LOGCOM)   LENGTH OF STORAGE NEEDED
         GETMAIN R,LV=(0)
         LR    R12,R1              LOAD ADDR OF COM DSECT
         USING LOGCOM,R12
         DROP  R6
         EJECT
***********************************************************************
*                                                                     *
*             SET UP CONSTANTS TO DSECT WORKAREAS  ........           *
*                                                                     *
***********************************************************************
         SPACE 3
         MVC   DCONDCB+40(12),CONDCB+40
         MVC   DCONIOB(40),CONIOB
         MVC   DCONCCW(12),CONCCW
         LA    R5,IO1              ADDRESS OUTPUT AREA
         STCM  R5,7,DCONCCW+1      SET UP DATA ADDRESS IN CCW
         MVI   IO1,X'C9'           WRITE CONTROL CHARACTER
         LA    R5,DCONECB          PICK UP ECB ADDR
         STCM  R5,7,DCONIOB+5      SAVE IN IOB
         LA    R5,DCONCCW          PICK UP CCW ADDR
         STCM  R5,7,DCONIOB+17     SAVE IN IOB
         LA    R5,DCONDCB          PICK UP DCB ADDR
         STCM  R5,7,DCONIOB+21     SAVE IN IOB
         LA    R5,DCONDCB
         OPEN  ((5),OUTPUT)
         XC    SWTASK(2),SWTASK    CLEAR SWITCHES
         LA    R5,35               LENGTH OF IO3
         STH   R5,IO3LEN           SAVE IN DSECT
         XC    IO3,IO3             CLEAR ACCOUNT AREA
         XC    CFRECD,CFRECD       CLEAR RECORD COUNTER
         XC    TSKECB(5),TSKECB    CLEAR TASK PARAMETERS
         LA    R5,IO1+1            ADDRESS OUTPUT IOAREA
         ST    R5,SSAVE            SAVE IT
         LA    R5,IO1+L'IO1-125    LAST ENTRY IN OUTPUT BUFFER
         ST    R5,SSAVE+4          SAVE IT
         EJECT
***********************************************************************
*                                                                     *
*             READ THE INPUT FILE    ......................           *
*                                                                     *
***********************************************************************
         SPACE 3
GET      DS    0H
         CLC   TSKTCB,FENCE        IS CANCEL REQD ?
         BNE   CANCEL              YES
         L     R1,PARM3            ADDRESS INPUT DCB
         GET   (1)                 GET INPUT RECORD
         L     R8,CFRECD           PICK UP RECORD COUNT
         LA    R8,1(R8)            UP THE RECORD COUNT BY ONE
         ST    R8,CFRECD           SAVE THE RECORD COUNT
         LR    R8,R1               ADDRESS THE RECORD READ
         BAL   R6,TASK             CHECK FOR SUBTASK ATTACH
         CLC   0(4,R8),CCOMM1      IS IT A COMMENT ?
         BE    GET                 YES   GET NEXT RECORD
         CLC   0(14,R8),CCOMM2     IS IT A COMMENT ?
         BE    GET                 YES   GET NEXT RECORD
         CLC   0(8,R8),CCIEF       IS IT AN ALLOCATION MSG ?
         BE    GET                 YES   GET NEXT RECORD
         CLC   0(2,R8),CCJCL1      IS IT JCL
         BNE   NOTJCL              YES- WILL PRINT LATER
         CLI   2(R8),C'*'          IS IT JES3 JCL ??
         BNE   GET                 NO  BYPASS
NOTJCL   DS    0H
         LA    R3,72(,R8)          POINT TO SEQ NOS
         LA    R3,10(R3)           POINT TO TRANSLATED SEQ NOS
         CLC   5(7,R8),CCJCL2      IS IT TRANSLATED JCL ?
         BE    CHECKSEQ            YES
         CLC   5(7,R8),CCJCL3      IS IT JCL FROM A PROC ??
         BNE   CHECKHD             NO
CHECKSEQ DS    0H
         LA    R6,8                SET LOOP VALUE
SEQLOOP  DS    0H
         CLI   0(R3),C'0'          IS IT LT 0
         BL    CHECKHD             NOT A SEQ NO
         CLI   0(R3),C'9'          IS IT GT 9
         BH    CHECKHD             NOT A SEQ NO
         LA    R3,1(R3)            BUMP INDEX
         BCT   R6,SEQLOOP          CHECK FOR 8 DIGITS
         SH    R3,CH8              SET POINTER BACK AT SEQ FIELD
         XC    0(8,R3),0(R3)       CLEAR THE SEQ NO
CHECKHD  DS    0H
         CLC   1(8,R8),CCIAT       IS HEADING REQUIRED
         BE    HEADING             YES
         CLC   CFRECD,CF0          ANY RECORDS READ ?
         BE    SPACE1              NO
FORMAT   DS    0H
         BAL   R7,CHKACCT          CHECK FOR JOB ACCOUNTING
         L     R3,PARM3            PICK UP DCB ADDRESS
         LH    R3,82(R3)           PICK UP LRECL
         CH    R3,CH121            IS IT TOO LONG
         BNH   CLRIO2
         LH    R3,CH121            TRUNCATE
CLRIO2   DS    0H
         BCTR  R3,0                GET EXECUTE LENGTH
         XC    IO2,IO2             CLEAR THE INPUT AREA
         EX    R3,MVC01
         LA    R2,IO2+L'IO2-1      ADDRESS THE END OF THE RECORD
         LA    R3,121              LOOP CONTROL
LENCHK   DS    0H
         CLI   0(R2),X'40'         SEARCH BACK FOR A BLANK
         BE    NXTBYT
         CLI   0(R2),X'00'         OR A NULL
         BNE   PUTCON
NXTBYT   DS    0H
         BCTR  R2,0                BACK UP THE POINTER
         BCT   R3,LENCHK           REDUCE THE LENGTH BY 1 AND LOOP
         LA    R3,1                IF NOTHING THERE,PRETENT THAT 1 BYTE
PUTCON   DS    0H
         BAL   R7,PUT              PUT THIS RECORD TO THE OUTPUT
         B     GET
MVC01    MVC   IO2(0),0(R8)      MOVE DATA TO OUR IOAREA
         EJECT
***********************************************************************
*                                                                     *
*             FORMAT HEADING      .........................           *
*                                                                     *
***********************************************************************
         SPACE 3
HEADING  DS    0H
         XC    IO2(20),IO2        CLEAR 20 BYTES
         TIME  DEC
         SRL   R0,4
         STM   R0,R1,WORK
         OI    WORK+3,X'0F'        PUT IN THE SIGN
         UNPK  IO2+1(5),WORK+5(3)  DAY YYDDD
         UNPK  IO2+7(6),WORK(4)    TIME HHMMSS
         MVC   IO2+14(6),HEAD      HEAD STRING
         MVC   IO2+20(60),IO2      REPLICATE
         LA    R3,1                AND
         BAL   R7,PUT              PRINT
         BAL   R7,PUT              SEPARATOR
         BAL   R7,PUT              LINES
         LA    R3,80
         BAL   R7,PUT
SPACE1   DS    0H
         LA    R3,1
         BAL   R7,PUT
         B     FORMAT
*
         EJECT
***********************************************************************
*                                                                     *
*             END OF FILE ROUTINE   .......................           *
*                                                                     *
***********************************************************************
         SPACE 3
EOFAD    DS    0H
         CLC   CFRECD,CF0          WERE ANY RECDS READ
         BE    END                 NO
         MVI   SWEOF,X'FF'
         BAL   R7,PUT              WRITE SHORT BLOCK
         BAL   R7,WRDISK           WRITE DISK IF ANY
         B     END
SYNAD    DS    0H             ADCONS FOR EXIT ROUTINES
         WTO   'LOGWTR   ERROR READING JES3 SPOOL  LOGWTR TERMINATING'
         WTO   '******   ***** ******* **** *****  ****** ***********'
         ABEND 30,DUMP
         EJECT
***********************************************************************
*                                                                     *
*             WRITE TO 3286   .............................           *
*                                                                     *
***********************************************************************
         SPACE 3
PUT      DS    0H
         STM   R4,R8,TSAVE         SAVE REGISTERS
         LM    R4,R8,SSAVE         PICK UP POINTERS
         CLI   SWEOF,X'FF'         IS IT EOF ?
         BNE   BLKLNE              YES   JUST PUT OUT WHAT WE HAVE
         LR    R6,R4               SET END OF BUFFER TO NEXT INPUT
         BCTR  R6,0                BCK UP OVER THE NL CHARACTER
         B     WRTBLK              FORCE SHORT BLOCK OUT
BLKLNE   DS    0H
         LR    R7,R3               STOR LEN IN REG7
         BCTR  R7,0                REDUCE BY 1 FOR EXECUTE
         EX    R7,TRANSL           TRANSLATE UNPRINTABLE CHARS
         EX    R7,MVCBLK           MOVE RECD TO BUFFER
         LA    R7,1(,R3)           SET LENGTH OF NEW DATA
         LA    R6,0(R4,R7)         ADDRESS NEXT AVAILABLE POSITION
         MVI   0(R6),X'15'         PUT IN NL CHARACTER
         LA    R4,1(R6)            SET NEXT INPUT POSITION POINTER
         CR    R4,R5               ANY ROOM LEFT ?
         STM   R4,R8,SSAVE         SAVE THE POINTERS
         BL    PUTEND              YES
WRTBLK   DS    0H
         MVI   IO1,X'C9'           BUFFER CONTROL CHAR
         MVI   0(R6),X'19'         EM CHAR
         LA    R4,IO1+1
         LA    R5,1919             REINIT REGS
         SR    R6,R4               CALCULATE LENGTH FOR CCW
         LA    R6,2(R6)            ADJUST FOR WCC AND EM
         STH   R6,DCONCCW+6        PLUG LENGTH TO CCW FOR SIO
EXCP     DS    0H
         XC    DCONECB,DCONECB     CLEAR THE ECB
         XC    DCONIOB+4(1),DCONIOB+4  CLEAR ECB
         EXCP  DCONIOB             START UP THE PRINTER
         WAIT  ECB=DCONECB         WAIT FOR I/O TO COMPLETE
         CLI   DCONIOB+4,X'7F'     WAS ALL OK ?
         BNE   IOERROR             NO   INVESTIGATE
         STM   R4,R8,SSAVE         SAVE THE POINTERS
         LA    R4,IO1              ADDRESS OUTPUT IOAREA
         LA    R5,1920             LENGTH OF IO1
         LA    R6,CX0              FILLER
         LA    R7,1                LENGTH OF FILLER
         ICM   R7,8,CX0            PAD = FILLER
         MVCL  R4,R6               CLEAR THE OUTPUT IOAREA TO NULLS
PUTEND   DS    0H
         LM    R4,R8,TSAVE         RESTORE THE REGS WE PINCHED
         BR    R7                  RETURN
*
TRANSL   TR    IO2(0),TBTR         TRANSLATE UNPRINTABLE
MVCBLK   MVC   0(0,R4),IO2         MOVE RECD TO BUFFER
*
         EJECT
***********************************************************************
*                                                                     *
*             HANDLE IO ERROR CONDITIONS                              *
*                                                                     *
***********************************************************************
         SPACE 3
IOERROR  DS    0H
         CLI   DCONIOB+12,X'02'     WAS IT A UNIT CHECK ?
         BE    INTREQ               YES INTERV REQ
         CLI   DCONIOB+4,X'48'      IS IT OK ?
         BE    RESCHP               RESTART CHANNEL PROGRAM
         UNPK  IOWTORM+35(3),DCONIOB+4(2)   PICK UP COMP CODE
         TR    IOWTORM+35(2),TBCHAR-240
         MVI   IOWTORM+37,C'.'
         UNPK  IOWTORM+45(5),DCONIOB+12(3)  PICK UP CSW
         TR    IOWTORM+45(4),TBCHAR-240
         MVI   IOWTORM+49,C'.'
         XC    WTORECB,WTORECB     CLEAR THE ECB
         LA    R7,REPLY
         LA    R8,WTORECB
IOWTORM  EQU   IOWTOR+16
IOWTOR   WTOR  '*** 3286 ERROR   COMPLETION CODE = XX. CSW = XXXX.     X
               REFER SYSTEMS PROGRAMMER (RETRY STOP)',(7),5,(8)
IOWTORW  WAIT  ECB=WTORECB
         CLC   REPLY(5),CCRETRY    IS IT RETRY ?
         BE    EXCP
         CLC   REPLY(4),CCSTOP     IS IT ALL OVER ?
         BNE   IOWTORR             INVALID REPLY   GIVE IT ALL AGAIN
         ABEND 20,DUMP             YES
IOWTORR  DS    0H
         XC    WTORECB,WTORECB     CLEAR THE ECB
         LA    R7,REPLY
         LA    R8,WTORECB
         WTOR  'INVALID REPLY     REPLY RETRY OR STOP',(7),5,(8)
         B     IOWTORW
INTREQ   DS    0H
         XC    WTORECB,WTORECB     CLEAR THE ECB
         LA    R7,REPLY
         LA    R8,WTORECB
         WTOR  'LOGWTR    INTERVENTION REQUIRED ON PRINTER (RETRY STOP)X
               ',(7),5,(8)
         B     IOWTORW
RESCHP   DS    0H
         LA    R1,RESCCW          ADDRESS RESTART CCW
         ST    R1,DCONIOB+4       SET UP RESTART MSG
         EXCP  DCONIOB            PUT OUT CANCELLATION MSG
         WAIT  ECB=DCONECB
         CLI   DCONIOB+4,X'7F'    ALL OK ?
         BNE   RESABEND           NO   CRASH HERE
         LA    R1,DCONCCW         RESTORE CURRENT CCW
         ST    R1,DCONIOB+4       RESTORE CURRENT CCW
         MVI   DCONIOB,X'00'      RESET FLAGS
         MVI   DCONIOB+33,X'00'   RESET FLAGS
         B     EXCP
RESABEND DS    0H
         WTO   'UNABLE TO RESTART AFTER INTERVENTION'
         WTO   'TASK TERMINATING   CHECK PRINTER AND RESTART LOGWTR'
         ABEND 20,DUMP   ALL OVER NOW
         EJECT
***********************************************************************
*                                                                     *
*             ATTACH A CANCEL SUBTASK                                 *
*                                                                     *
***********************************************************************
         SPACE 3
TASK     DS    0H
         CLC   CFRECD,CF100    LINES EXCEEDED ?
         BLR   R6
         CLI   SWTASK,X'00'     IS A TASK ALREADY ACTIVE
         BNER  R6               YES
         MVI   SWTASK,X'FF'     INDICATE TASK ATTACHED
         ATTACH EP=LOGTSK,ETXR=TSKEXIT
         STCM  R1,B'0111',TSKTCB+5
         LTR   R15,R15
         BZR   R6               ALL OK   RETURN
         WTO   'ATTACH OF CANCEL TASK FAILED   LOGWTR TERMINATING'
         WTO   '****** ** ****** **** ******   ****** ***********'
         B     END1
*
TSKEXIT  DS    0D
         BAL   R2,12(R15)        ADDRESS SAVEAREA
TSKTCB   DC    X'FFFFFFFF',X'00000000'  TCB ADDRESS
         STCM  R1,B'1111',0(R2) SAVE TCB ADDRESS
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*             CHECK FOR JOB ACCOUNTING INFORMATION ........           *
*                                                                     *
***********************************************************************
         SPACE 3
CHKACCT  DS    0H
         CLC   CCIEF236,0(R8)      IS IT MSG IEF236I ?
         BE    IEF236              YES EXTRACT JOBNAME
         CLC   CCIEF142,0(R8)      IS IT MSG IEF142I ?
         BE    IEF142              YES EXTRACT & C CODE
         CLC   CCIEF373,0(R8)      IS IT MSG IEF373I ?
         BE    IEF373              YES EXTRACT START TIME AND STEP
         CLC   CCIEF374,0(R8)      IS IT MSG IEF374I ?
         BE    IEF374              YES EXTRACT STOP  TIME
         CLC   CCIEF472,0(R8)      IS IT MSG IEF472I ?
         BE    IEF472              YES EXTRACT  C CODE
         BR    R7                  RETURN
IEF236   DS    0H
         LA    R0,8                MAXIMUM JOBNAME
         LA    R2,19(,R8)          POINTER TO INPUT
         LA    R1,IO3              POINTER TO OUTPUT
IEF236LP DS    0H
         MVC   0(1,R1),0(R2)       1 CHARACTER AT A TIME
         LA    R1,1(,R1)           BUMP OUTPUT INDEX
         LA    R2,1(,R2)           BUMP INPUT INDEX
         CLI   0(R2),C' '          ALL OVER ?
         BER   R7                  YES
         CLI   0(R2),X'00'         ALL OVER ?
         BER   R7                  YES
         BCT   R0,IEF236LP         MAX OF 8 CHARS
         BR    R7
IEF472   DS    0H
         LA    R0,30               SET LOOP CONTROL
         LA    R1,28(R8)           START OF SEARCH
IEF472A  DS    0H
         CLI   0(R1),C'='          FIND =
         BE    IEF472B             FOUND
         LA    R1,1(,R1)           BUMP INDEX
         BCT   R0,IEF472A          CONTINUE SEARCH
         BR    R7                  NOT FOUND  ASSUME ZERO
IEF472B  DS    0H
         MVC   IO3+16(4),1(R1)     PICK UP COND CODE
         CLC   1(3,R1),CCZERO      IS SYSTEM CODE ZERO ?
         BNER  R7                  NO USE IT
         MVC   IO3+16(4),10(R1)    PICK UP USER CODE
         BR    R7
IEF142   DS    0H
         LA    R0,30               SET LOOP CONTROL
         LA    R1,28(R8)           START OF SEARCH
IEF142A  DS    0H
         CLC   CCOND,0(R1)         FIND 'COND CODE'
         BE    IEF142B             FOUND
         LA    R1,1(,R1)           BUMP INDEX
         BCT   R0,IEF142A          CONTINUE SEARCH
         BR    R7                  NOT FOUND  ASSUME ZERO
IEF142B  DS    0H
         MVC   IO3+16(4),10(R1)    PICK UP COND CODE
         BR    R7
IEF373   DS    0H
         MVC   IO3+8(8),14(R8)     PICK UP STEPNAME
         MVC   IO3+20(5),30(R8)    PICK UP DATE
         MVC   IO3+25(4),36(R8)    PICK UP START TIME
         BR    R7
IEF374   DS    0H
         MVC   IO3+29(4),36(R8)    PICK UP STOP TIME
         B     WRDISK              ALL DETAILS SO WRITE DISK
         EJECT
***********************************************************************
*                                                                     *
*             WRITE JOB ACCOUNTING DETAILS TO DISK ........           *
*                                                                     *
***********************************************************************
         SPACE 3
WRDISK   DS    0H
         CLC   IO3,CX0             ANY DETAILS ?
         BER   R7                  NO   NO CALL REQUIRED
         LA    R2,IO3LEN
         STCM  R2,7,DKPARM+1
         LA    R1,DKPARM
         LINK  EP=LOGDSK,ERRET=WRDKERR
         XC    IO3,IO3             CLEAR THE RECORD AREA
         BR    R7               ALL OK   RETURN
WRDKERR  DS    0H
         WTO   'LOGWTR   JOB ACCOUNTING FAILURE'
         BR    R7
         EJECT
***********************************************************************
*                                                                     *
*             CLEANUP MESS LEFT BY IASXWR00   FREE 4K STORAGE FROM DQE*
*                                                                     *
***********************************************************************
         SPACE 3
CLEANUP  DS    0H
         L     R1,X'10'            PICK UP CVT POINTER
         L     R1,0(R1)            PICK UP TCB POINTER
         L     R1,4(R1)            POINT TO CURRENT TCB
         L     R1,24(R1)           POINT TO SPQE
FINDSP0  DS    0H
         LTR   R1,R1               ANY THERE ?
         BZR   R6                  NO
         CLI   11(R1),X'00'        SUB POOL ZERO ?
         BE    ZEROF
         L     R1,4(R1)            PICK UP NEXT SPQE POINTER
         B     FINDSP0
ZEROF    DS    0H
         TM    8(R1),X'60'         IS THIS THE DQE POINTER ?
         BO    DQEF
         L     R1,4(R1)            PICK UP NEXT SPQE
         LTR   R1,R1               ANY THERE ?
         BZR   R6
DQEF     DS    0H                  AT LAST   THE 1ST DQE IS FOUND
         LA    R5,TBQE1
         LR    R12,R1
LOOP     DS    0H
         L     R12,4(R12)          PICK UP POINTER TO NEXT
         LTR   R12,R12
         BZR   R6
         CLC   12(4,R12),CF4096
         BNE   LOOP
         L     R7,0(R12)           PICK UP FQE PTR
         CLC   4(4,R7),CF232       IS THIS A ROGUE BLOCK
         BNE   LOOP                NO
         MVC   0(4,R5),8(R7)       SAVE ADDRESS OF FREE STORAGE
         LA    R5,4(,R5)           KEEP COUNT
         C     R5,TBQEND           ALL DONE
         BL    LOOP
         LA    R5,TBQE1
         LA    R12,3
LOOP1    DS    0H
         L     R1,0(R5)            ADDRESS TO BE FREED
         L     R0,CF3864           LENGTH TO BE FREED
         FREEMAIN R,LV=(0),A=(1)   LET IT GO
         MVC   0(4,R5),CCDONE
         LA    R5,4(R5)
         BCT   R12,LOOP1
         BR    R6
TBQE1    DC    F'0'
TBQE2    DC    F'0'
TBQE3    DC    F'0'
TBQE4    DC    F'0'
TBQE5    DC    F'0'
TBQE6    DC    F'0'
TBQEND   DC    A(TBQEND)
         EJECT
***********************************************************************
*                                                                     *
*             JOB TERMINATION      ........................           *
*                                                                     *
***********************************************************************
         SPACE 3
CANCEL   DS    0H
         MVC   DCONCCW(8),CANCCW  SET UP CANCEL MSG
         EXCP  DCONIOB            PUT OUT CANCELLATION MSG
         WAIT  ECB=DCONECB
END      DS    0H
         CLI   SWTASK,X'00'       WAS A CANCEL TASK CREATED ?
         BE    END1               NO
         DETACH TSKTCB+4          DETACH CANCEL SUBTASK
END1     DS    0H
         LA    R2,DCONDCB          ADDRESS OUTPUT DCB FOR CLOSE
         L     R3,PARM3            ADDRESS INPUT DCB FOR CLOSE
         CLOSE ((2),,(3))             CLOSE THE FILES
         L     R13,OLDR13
         LA    R0,(ENDWK-LOGCOM)   LENGTH OF STORAGE NEEDED
         LR    R1,R12
         FREEMAIN R,LV=(0),A=(1)
         LA    R0,112
         LR    R1,R11
         FREEMAIN R,LV=(0),A=(1)
         L     R14,12(R13)         RETURN TO CALLER
         LM    R0,R12,20(R13)
         SR    R15,R15
         BR    R14
         END   ,
/*
//LKED.SYSPRINT DD SYSOUT=X
//LKED.SYSIN   DD  *
   ENTRY IEFSD87A
   SETCODE AC(1)
   NAME IEFSD87A(R)
IEFSD87C TITLE 'IEFSD87C - READ SPOOLED PRINT LINES AND OUTPUT TO TAPE X
               FOR FICHE PROCESSING     -    R.LAMB'
         SPACE 5
*
*              THIS IS A BANK WRITTEN PROGRAM THAT FUNCTIONS
*              AS ONE OF THE MODULES THAT COMPRISE AN EXTERNAL
*              WRITER. IT IS CALLED BY MODULE 'IEFSD087' (ALSO
*              A BANK WRITTEN PROGRAM) TO PROCESS SPOOLED PRINT
*              LINES AND OUTPUT THEM TO TAPE FOR LATER FICHE
*              PROCESSING.
*
*              THE MAIN FUNCTION OF THE PROGRAM IS TO PROVIDE A
*              MEANS OF SEPARATING EACH SPOOLED JOB SO THAT WHEN
*              THE TAPE IS PROCESSED THROUGH THE 'COM' MACHINE A
*              SKIP TO A NEW FICHE WILL BE EFFECTED AT -
*              A) THE START OF EACH JOB AND/OR
*              B) AS DIRECTED BY INPUT RECORDS.
*
*              REFER TO MEMBER 'FCHWTR' IN 'SM00.BANK.DOC' FOR A
*              DETAILED DESCRIPTION OF THE PROGRAM.
*
*              FOR DETAILED INFORMATION RELATING TO EXTERNAL WRITERS
*              REFER TO 'OS/VS2 MVS SYSTEM PROGRAMMING LIBRARY : JOB
*              MANAGEMENT' MANUAL.
*
         EJECT
***********************************************************************
**********      INITIALISATION                             ************
***********************************************************************
         SPACE 2
IEFSD87C CSECT
         PRINT NOGEN
         SAVE  (14,12)             * SAVE REGISTERS
         LR    R12,R15             * SET BASE REG
         USING IEFSD87C,R12
         LA    R11,4095(R12)       * LOAD SECOND
         LA    R11,1(R11)          ** BASE REGISTER
         LR    R2,R1               * SAVE R1 CONTENTS (HAS ADDRESS OF
*                                  ** 12 BYTE PARMLIST CONTAINING
*                                  *** ADDRESSES OF INPUT/OUTPUT DCBS)
         LA    R0,80               * GETMAIN REG SAVE AREA
         GETMAIN R,LV=(0)          *
         ST    R13,4(R1)           * FORWARD CHAIN ADDRESS
         ST    R1,8(R13)           * BACKWARD CHAIN ADDRESS
         LR    R13,R1              * ADDRESS REG SAVE AREA
         L     R0,GETSIZE          * GETMAIN THE REQUIRED AMOUNT
         GETMAIN R,LV=(0)          ** OF STORAGE FOR USE AS WORK AREA
         LR    R9,R1               *
         USING STARTGET,R9         * ADDRESS DSECT AREA
         ST    R13,SVR13           * STORE THE ADDRESS OF OUR REG
*                                  ** SAVE AREA ....
         B     START
         EJECT
***********************************************************************
**********      CONSTANTS & STORAGE & DSECTS               ************
***********************************************************************
         SPACE 2
R0       EQU   0                   *
R1       EQU   1                   *
R2       EQU   2                   * WORK REGISTER
R3       EQU   3                   * WORK REGISTER
R4       EQU   4                   * JOBNUM DCB REGISTER
R5       EQU   5                   * INPUT DCB AND DCB DSECT REGISTER
R6       EQU   6                   * LOOP REGISTER
R7       EQU   7                   * LINK AND SAVE REGISTER
R8       EQU   8                   * GETMAIN AREA POINTER REGISTER
R9       EQU   9                   * DSECT REGISTER FOR STORAGE FIELDS
R10      EQU   10                  *
R11      EQU   11                  * BASE REGISTER
R12      EQU   12                  * BASE REGISTER
R13      EQU   13                  * SAVE REGISTER
R14      EQU   14                  * RETURN REGISTER
R15      EQU   15                  * ENTRY REGISTER
*
BLANKS   DC    CL40' '
NEWFCH   DC    C'NEWFCH'
JESMSG   DC    C'JESMSG'
JESJCL   DC    C'JESJCL'
DCBJNUMI DC    A(JOBNUMI)
DCBJNUMO DC    A(JOBNUMO)
RECYES   DC    C'RECYES'
RECNO    DC    C'RECNO'
NOTED    DC    C'NOTED'
GETSIZE  DC    A(ENDGET-STARTGET)
*
* CARE .  THE FOLLOWING TBQ ENTRIES MUST REMAIN TOGETHER
*
TBQE1    DC    F'0',F'0'
TBQE2    DC    F'0',F'0'
TBQE3    DC    F'0',F'0'
TBQE4    DC    F'0',F'0'
TBQE5    DC    F'0',F'0'
TBQE6    DC    F'0',F'0'
TBQEND   DC    A(TBQEND)
*
         EJECT
JOBNUMI  DCB   BLKSIZE=1330,                                           X
               DDNAME=JOBNUMI,                                         X
               DSORG=PS,                                               X
               LRECL=133,                                              X
               MACRF=GL,                                               X
               SYNAD=ERRNUMI,                                          X
               RECFM=FB
*
JOBNUMO  DCB   BLKSIZE=1330,                                           X
               DDNAME=JOBNUMO,                                         X
               DSORG=PS,                                               X
               LRECL=133,                                              X
               MACRF=PL,                                               X
               SYNAD=ERRNUMO,                                          X
               RECFM=FB
         EJECT
*
STARTGET DSECT                     * START OF GETMAIN AREA
JOBNAME  DS    CL8
JOBID    DS    CL8
DSNAME   DS    CL44
DCBOUT   DS    F
DCBIN    DS    F
SVGETADR DS    F
LINENUM  DS    F                   * NUMBER OF LINES PRINTED COUNTER
SVR7     DS    F
SVR13    DS    F
WTORECB  DS    F
REPLY    DS    CL5
REC1     DS    CL133
REC2     DS    CL133
JNUMREC  DS    CL133
*
FCHTITL  DS    0CL133              * FICHE TITLE IS BUILT HERE
FCHTCC   DS    CL1                 * CONTROL CHAR BYTE
FCHNEW   DS    CL6
         DS    CL1
FCHTJN   DS    CL8                 * JOB NAME FIELD
         DS    CL1
FCHTL1   DS    CL39                * LINE 1 FIELD
         DS    CL1
FCHTL2   DS    CL39                * LINE 2 FIELD
         DS    CL37                * END OF FCHTITL
*
SWNOSPAC DS    XL1                 * SET TO X'FF' WHEN A
*                                  ** SKIP TO CH1 IS EFFECTED.
*
ENDGET   EQU   *                   * END OF GETMAIN AREA
         EJECT
***********************************************************************
**********  FOLOWING ARE MAPPING MACROS ......              ***********
***********************************************************************
         DCBD  DSORG=PS
         IEFJSSOB (SO)
         IHASPQE
         IHAPSA
         CVT   DSECT=YES
         IKJTCB
***********************************************************************
**********  START OF PROGRAM PROCESSING .........           ***********
***********************************************************************
         SPACE 1
IEFSD87C CSECT
START    DS    0H
         MVC   DCBOUT,4(R2)        * SAVE ADDR OF OPENED OUTPUT DCB
         MVC   DCBIN,8(R2)         * SAVE ADDRESS OF INPUT DCB
         L     R2,DCBOUT           * STORE ERROR HANDLING ADDRESS
         USING IHADCB,R2           ** FOR THE TAPE OUTPUT FILE
         MVC   DCBSYNAD+1(3),=AL3(ERROUT)
         DROP  R2
*
*   ON ENTRY TO THIS PROGRAM R6 POINTS TO 'PARMLIST' (SEE ANY
*   OF THE IBM WRITTEN 'EXTERNAL WRITER' MODULES.
*   MANIPULATING R6 GIVES US THE ADDRESS OF THE SUB SYSTEM OPTION
*   BLOCK (SSOB) FOR THIS FILE.
*
         LA    R6,168(R6)          * POINT TO 'PARMWORK'
         L     R6,0(R6)            *
         L     R6,0(R6)            *
         USING SSOB,R6
         MVC   JOBNAME,SSSOJOBN    * SAVE JOBNAME
         MVC   JOBID,SSSOJOBI      * SAVE JOBID
         MVC   DSNAME,SSSODSN      * SAVE SYSOUT DATA SET NAME
         DROP  R6
         MVI   SWNOSPAC,X'00'      * INITIALIZE SWITCH
         MVI   FCHTITL,X'40'       * INITIALIZE FICHE TITLE LINE
         MVC   FCHTITL+1(L'FCHTITL-1),FCHTITL
         LA    R6,0
         ST    R6,LINENUM          * INITIALIZE LINE COUNTER
*
         L     R5,DCBIN            * OPEN THE INPUT FILE
         OPEN  ((5),INPUT)
         USING IHADCB,R5           * GIVE DCBD ADDRESSABILITY
         EJECT
***********************************************************************
**********   START PROCESSING THE 'JOBNUM' FILE ......     ************
***********************************************************************
*
M1A1     DS    0H
         L     R4,DCBJNUMI         * OPEN THE JOB NUMBER STORED FILE
         OPEN  ((4),INPUT)         *
         GET   (4)                 * GET THE FIRST RECORD
         MVC   JNUMREC,0(R1)       * STORE THE RECORD
         CLC   JNUMREC(8),JOBID    * JOBID OF THIS FILE EQUAL
*                                  ** TO JOBID OF PREVIOUS FILE
         BE    M1C1                * BRANCH IF YES
         EJECT
***********************************************************************
**********   PROCESS FILE WHOSE JOBID IS NOT THE SAME      ************
**********   AS THE PREVIOUS INPUT FILE .............      ************
***********************************************************************
*
M1B1     DS    0H
         CLC   JNUMREC+9(6),RECYES * ANY STORED RECORDS ?
         BNE   M1F1                * NO - BRANCH
         BAL   R7,A1A1             * GO GET/PUT STORED RECS
         B     M1F1                * BRANCH TO PROCESS INPUT FILE
*
         EJECT
***********************************************************************
**********   PROCESS FILE WHOSE JOBID IS THE SAME AS THE   ************
**********   PREVIOUS INPUT FILE .............             ************
***********************************************************************
*
M1C1     DS    0H
         CLC   JNUMREC+9(6),RECYES * ANY STORED RECS
         BNE   M1F1                * BRANCH IF NOT
         CLC   DSNAME+18(6),JESJCL * IS INPUT A 'JESJCL' FILE
         BE    M1D1                * BRANCH IF YES
*
M1C2     DS    0H
         BAL   R7,A1A1             * GO GET/PUT THE STORED RECS
         TM    DCBRECFM,X'06'      * DOES INPUT FILE HAVE CNTRL CHARS
         BNZ   M1D3A               * BRANCH IF YES
         BAL   R7,NEWPAGE          * GO SKIP TO A NEW PAGE
         B     M1D3A
         EJECT
***********************************************************************
**********    PROCESSING OF SAME JOBID WITH STORED RECORDS ************
**********    AND INPUT IS A 'JESJCL' FILE ............... ************
***********************************************************************
         SPACE 1
*
* GET THE SECOND JOBNUM RECORD. THIS WILL BE A FICHTITL RECORD
* (PREVIOUSLY FORMATTED) AND IS MOVED TO THE FICHTITL WORK AREA.
* A BRANCH IS THEN TAKEN TO READ THE FIRST 20 RECORDS OF THE JESJCL
* FILE. ANY NEWFCH INFO IN THE 20 RECORDS WILL BE MOVED TO THE
* FICHTITL AREA.
*
M1D1     DS    0H
         GET   (4)                 * GET 2ND JOBNUM RECORD
         MVC   FCHTITL,0(R1)       * MOVE IT TO THE FICHTITL AREA
         LA    R2,M1D1END          * LOAD THE FIRST EOF ROUTINE
         STCM  R2,B'0111',DCBEODA  * STORE ADDRESS IN INPUT DCB
         BAL   R7,B1B1             * GO TO READ FIRST 20
*                                  ** RECORDS FOR NEWFCH INFO
         BAL   R7,PUTFCHT          * GO PRINT FICHE LINE
         GET   (4)                 * SKIP JOBNUM RECS 3 AND 4. THEY ARE
         GET   (4)                 ** OLD NEWFCH RECS NO LONGER WANTED.
         BAL   R7,A1A1             * GO GET/PUT JOBNUM RECS
*
M1D1A    DS    0H
*
* PRINT THE STORED JESJCL RECORDS
*
         BAL   R7,NEWPAGE          * GO SKIP TO A NEW PAGE
         L     R8,SVGETADR         * ADDR START OF GETMAIN AREA
         LA    R6,20               * LOAD LOOP REGISTER
*
M1D2     DS    0H
         CLC   BLANKS,0(R8)        * ANY MORE RECS TO PRINT ?
         BE    M1D3                * NO - BRANCH
         BAL   R7,PUTPRT           * GO PRINT THE RECORD
         LA    R8,133(R8)          * UPDATE POINTER REG
         BCT   R6,M1D2             *
         EJECT
***********************************************************************
**********      PROCESS THE REST OF THE INPUT FILE ...     ************
***********************************************************************
         SPACE 1
*
M1D3     DS    0H
         L     R8,SVGETADR         * ADDR AND FREE THE AREA WE USED
         FREEMAIN R,LV=2660,A=(8)  ** TO PROCESS THE FIRST 20 RECORDS
*
M1D3A    DS    0H
         GETMAIN R,LV=399          * STORAGE FOR THREE 133 BYTE RECS
         ST    R1,SVGETADR         * SAVE THE GETMAIN AREA ADDR
         L     R8,SVGETADR         * ADDR THE GETMAINED AREA
         LA    R2,M1D3END          * LOAD NORMAL EOF ADDR
         STCM  R2,B'0111',DCBEODA  * STORE THE ADDR IN THE INPUT DCB
*
M1D4     DS    0H
         LA    R6,3                * LOAD LOOP REGISTER
*
M1D5     DS    0H                  * MAKE THE
         MVI   0(R8),X'40'         ** GETMAIN
         MVC   1(132,R8),0(R8)     *** AREA
         LA    R8,133(R8)          **** ALL
         BCT   R6,M1D5             ***** BLANKS
*
M1D6     DS    0H
         L     R8,SVGETADR         * ADDR THE GETMAINED AREA
         BAL   R7,GETREC           * GET A RECORD
         LTR   R3,R3               * IS IT A NEWFCH RECORD ?
         BZ    M1D7                * NO - BRANCH
         LA    R8,133(R8)          * UPDATE POINTER
         BAL   R7,GETREC           * READ ANOTHER 2 RECORDS.
         LA    R8,133(R8)          ** NEWFCH CARDS NORMALLY
         BAL   R7,GETREC           *** COME IN GROUPS OF THREE
         BAL   R7,PUTFCHT          * GO PRINT NEWFCH TITLE
         LA    R6,3                * LOAD LOOP REGISTER
         L     R8,SVGETADR         * ADDR THE GETMAINED AREA
*
M1D6A    DS    0H
         CLC   5(R8),NEWFCH        * IS STORED REC A NEWFCH REC ?
         BNE   M1D7                * NO BRANCH TO PUT PRINT
         CLC   DSNAME+18(6),JESJCL * IS INPUT A 'JESJCL' FILE
         BNE   M1D6B               * NO -BRANCH (WE ONLY PRINT NEWFCH
*                                  ** RECS RECEIVED IN A JESJCL FILE).
         BAL   R7,PUTPRT           * PRINT THE 1ST STORED RECORD
*
M1D6B    DS    0H
         LA    R8,133(R8)          * UPDATE POINTER
         BCT   R6,M1D6A            * GO PROCESS NEXT REC
         B     M1D8                * UPDATE THE POINTER
*
M1D7     DS    0H
         BAL   R7,PUTPRT           * GOTO PRINT THE RECORD
*
M1D8     DS    0H
         L     R8,SVGETADR         * ADDR THE GETMAINED AREA
         B     M1D4                *
         EJECT
***********************************************************************
**********    PROCESSING OF AN INPUT (JESMSG) FILE WHERE   ************
**********    THERE ARE NO STORED RECS ................... ************
***********************************************************************
         SPACE 1
*
M1F1     DS    0H
         CLC   DSNAME+18(6),JESMSG * IS INPUT A 'JESMSG' FILE
         BNE   M1G1                * NO - BRANCH
         CLOSE ((4))               * CLOSE JOBNUM FILE
         L     R4,DCBJNUMO
         OPEN  ((4),OUTPUT)        * OPEN JOBNUM FOR OUTPUT
         LA    R2,M1F1END          * LOAD EOF ADDR
         STCM  R2,B'0111',DCBEODA  * STORE IT IN DCB
         MVC   JNUMREC(8),JOBID    * FORMAT REC1 TO SHOW
         MVC   JNUMREC+9(6),RECYES ** RECORDS = YES
         PUT   (4)                 * PRINT IT
         MVC   0(133,R1),JNUMREC   * MOVE RECORD TO OUTPUT
         L     R2,DCBJNUMO         * ADDR THE JOBNUM DCB
         ST    R2,DCBOUT           * CHANGE DCBOUT SO THAT RECS
*                                  ** WILL BE PUT TO JOBNUM
         MVI   FCHTITL,X'40'
         MVC   FCHTITL+1(132),FCHTITL
         BAL   R7,PUTFCHT          * PRINT A NEWFCH LINE
*                                  ** WITH DEFAULT VALUES
         LA    R0,133              * GETMAIN RECORD WORK AREA
         GETMAIN R,LV=(0)
         LR    R8,R1               * ADDR THE GETMAIN AREA
*
M1F2     DS    0H
         MVI   0(R8),X'40'         * BLANK THE
         MVC   1(132,R8),0(R8)     ** RECORD AREA
         BAL   R7,GETREC           * GET AN INPUT REC
         BAL   R7,PUTPRT           * PUT THE RECORD
         B     M1F2                * REPEAT TILL EOF
         EJECT
***********************************************************************
**********    PROCESSING OF AN INPUT (JESJCL) FILE WHERE   ************
**********    THERE ARE NO STORED RECS ................... ************
***********************************************************************
         SPACE 1
*
M1G1     DS    0H
         CLC   DSNAME+18(6),JESJCL * IS INPUT A 'JESJCL' FILE
         BNE   M1H1                * NO - BRANCH
         LA    R2,M1G1END          * ADDR EOF ROUTINE
         STCM  R2,B'0111',DCBEODA  * STORE IN INPUT DCB
         BAL   R7,B1B1             * READ FIRST 20 RECS FOR NEWFCH
         BAL   R7,PUTFCHT          * BRANCH TO WRITE NEWFCH
         B     M1D1A               *
         EJECT
***********************************************************************
**********    PROCESSING OF AN INPUT FILE (NEITHER JESMSG  ************
**********    OR JESJCL) WHERE THERE ARE NO STORED RECS..  ************
***********************************************************************
         SPACE 1
*
M1H1     DS    0H
         CLC   JNUMREC(8),JOBID    * SAME JOBIDS
         BNE   M1H2                * NO - BR
         BAL   R7,NEWPAGE          * GO SKIP TO A NEW PAGE
         B     M1D3A
*
M1H2     DS    0H
         BAL   R7,PUTFCHT          * GO PR NEWFCH WITH DEFAULTS
         B     M1D3A
         EJECT
***********************************************************************
**********    READ THE JOBNUM FILE AND PUT THE RECORDS ... ************
***********************************************************************
         SPACE 1
*
A1A1     DS    0H
         STCM  R7,B'0111',JOBNUMI+33 * MAKE RETURN ADDRESS THE
*                                   ** EOF ADDRESS FOR JOBNUM
         L     R2,DCBOUT            * INDICATE OUTPUT DCB REQ'D
*
A1A2     DS    0H
         GET   (4)                 * GET NEXT RECORD
         LR    R8,R1               *
         PUT   (2)                 * PUT PRINT
         MVC   0(133,R1),0(R8)     * MOVE THE RECORD TO OUTPUT
         B     A1A2                * REPEAT TILL EOF
         EJECT
***********************************************************************
**********    SKIP TO NEW PAGE SUBROUTINE ....             ************
***********************************************************************
         SPACE 1
*
NEWPAGE  DS    0H
         L     R2,DCBOUT            * ADDRESS OUTPUT DCB
         PUT   (2)                  * PUT PRINT
         MVI   0(R1),X'8B'          * FORCE SKIP TO NEW PAGE
         LA    R2,0                 * SET LINE COUNTER
         ST    R2,LINENUM           ** TO ZERO
         MVI   SWNOSPAC,X'FF'      * SET SKIP TO CH1 DONE SWITCH
         BR    R7
         EJECT
***********************************************************************
**********    READ FIRST TWENTY RECORDS OF AN INPUT 'JESJCL' **********
**********    AND SCAN FOR ANY NEWFCH INFO ......            **********
***********************************************************************
         SPACE 1
*
B1B1     DS    0H
         ST    R7,SVR7             * SAVE LINK REGISTER
         GETMAIN R,LV=2660         * STORAGE FOR TWENTY 133 BYTE RECS
         ST    R1,SVGETADR         * SAVE THE GETMAIN AREA ADDR
         L     R8,SVGETADR         * ADDR THE GETMAINED AREA
         LA    R6,20               * LOAD LOOP REGISTER
*
B1B2     DS    0H                  * MAKE THE
         MVI   0(R8),X'40'         ** GETMAIN
         MVC   1(132,R8),0(R8)     *** AREA
         LA    R8,133(R8)          **** ALL
         BCT   R6,B1B2             ***** BLANKS
*
         L     R8,SVGETADR         * ADDR THE GETMAINED AREA
         LA    R6,20               * LOAD A LOOP REGISTER
*
B1B3     DS    0H
         BAL   R7,GETREC           * GO READ A RECORD
         LTR   R3,R3               * WAS A NEWFCH RECORD READ
         BZ    B1B4                * BR IF NOT
         LA    R3,2                * AS WE HAVE GOT A NEWFCH REC WE
         CR    R6,R3               ** NEED ONLY PROCESS ANOTHER 2 RECS.
         BL    B1B4                *** FIRST ENSURE THAT THERE IS ROOM
*                                  **** FOR 2 MORE RECS TO BE STORED.
*                                  ***** REMEMBER THAT NEWFCH RECS
*                                  ****** USUALLY COME IN THREES ....
         LA    R6,3                * SET COUNTER FOR 2 MORE RECS.
*
B1B4     DS    0H
         LA    R8,133(R8)          * UPDATE GETMAIN REG
         BCT   R6,B1B3
         L     R7,SVR7             * RESTORE LINK REGISTER
         BR    R7                  * RETURN
         EJECT
***********************************************************************
**********    END OF FILE ROUTINE USED WHEN THERE ARE LESS   **********
**********    THAN TWENTY RECORDS IN AN INPUT 'JESJCL' FILE  **********
***********************************************************************
         SPACE 1
*
M1D1END  DS    0H
         BAL   R7,PUTFCHT          * GO PRINT FICHE LINE
         GET   (4)                 * SKIP JOBNUM RECS 3 AND 4. THEY ARE
         GET   (4)                 ** OLD NEWFCH RECS NO LONGER WANTED.
         BAL   R7,A1A1             * GO GET/PUT JOBNUMRECS
*
M1D1END0 DS    0H
*
* PRINT THE STORED JESJCL RECORDS
*
         L     R8,SVGETADR         * ADDR START OF GETMAIN AREA
         LA    R6,20               * LOAD LOOP REGISTER
*
M1D1END1 DS    0H
         CLC   BLANKS,0(R8)        * ANY MORE RECS TO PRINT ?
         BE    M1D1END2            * NO - BRANCH
         BAL   R7,PUTPRT           * GO PRINT THE RECORD
         LA    R8,133(R8)          * UPDATE POINTER REG
         BCT   R6,M1D1END1         *
*
M1D1END2 DS    0H
         L     R8,SVGETADR         * ADDR AND FREE THE AREA WE USED
         FREEMAIN R,LV=2660,A=(8)  ** TO PROCESS THE FIRST 20 RECORDS
*
M1D1END3 DS    0H
         MVC   JNUMREC(8),JOBID    * MOVE THIS JOBS NUMBER IN
         MVC   JNUMREC+9(5),RECNO  * INDICATE NO RECORDS
         CLOSE ((4))
         L     R4,DCBJNUMO
         OPEN  ((4),OUTPUT)
         PUT   (4)
         MVC   0(133,R1),JNUMREC   *
         CLOSE ((4))
         B     END
         EJECT
***********************************************************************
**********    END OF FILE ROUTINE FOR THE INPUT FILE .....   **********
***********************************************************************
         SPACE 1
*
M1D3END  DS    0H
         L     R6,SVGETADR      * ADDR START OF GETMAIN AREA
         CR    R8,R6            * ANY STORED RECORDS
         BE    M1D3END4         * NO - BRANCH
         BAL   R7,PUTFCHT       * GO PRINT NEWFCH TITL1
         L     R8,SVGETADR      * ADDR START OF GETMAIN AREA
         LA    R6,3             * LOAD LOOP
*
M1D3END1 DS    0H
         CLC   BLANKS,0(R8)        * ANY DATA ?
         BNE   M1D3END4            * NO - BRANCH
         CLC   4(R8),NEWFCH        * IS STORED REC A NEWFCH REC ?
         BNE   M1D3END3            * NO BRANCH TO PUT PRINT
         CLC   DSNAME+18(6),JESJCL * IS INPUT A 'JESJCL' FILE
         BNE   M1D3END2            * NO -BRANCH (WE ONLY PRINT NEWFCH
         BAL   R7,PUTPRT           * PRINT THE 1ST STORED RECORD
*                                  ** RECS RECEIVED IN A JESJCL FILE).
M1D3END2 DS    0H
         LA    R8,133(R8)          * UPDATE POINTER
         BCT   R6,M1D6A            * GO PROCESS NEXT REC
         B     M1D3END4            *
*
M1D3END3 DS    0H
         BAL   R7,PUTPRT           * GOTO PRINT THE RECORD
*
M1D3END4 DS    0H
         L     R8,SVGETADR       * ADDR AND FREE THE AREA WE USED
         FREEMAIN R,LV=399,A=(8) ** TO PROCESS THE INPUT FILE .....
         B     M1D1END3
         EJECT
***********************************************************************
**********      END OF FILE ROUTINE FOR AN INPUT JESMSG FILE **********
***********************************************************************
         SPACE 1
*
M1F1END  DS    0H
         CLOSE ((4))              * CLOSE THE JOBNUM FILE
         FREEMAIN R,LV=133,A=(8)  * FREE GETMAINED AREA
         B     END                * GO TO FINISH
         SPACE 5
***********************************************************************
**********      END OF FILE ROUTINE FOR AN INPUT JESJCL FILE **********
***********************************************************************
         SPACE 1
*
M1G1END  DS    0H
         BAL   R7,PUTFCHT         * PRINT A NEW FICHE LINE
         B     M1D1END0           *
         EJECT
***********************************************************************
**********      PRINT THE OUTPUT RECORDS SUBROUTINE        ************
***********************************************************************
         SPACE 1
*
PUTPRT   DS    0H
         TM    DCBRECFM,X'02'      * DOES INPUT FILE HAVE
*                                  ** MACHINE CONTROL CHARACTERS ?
         BNO   PUTP1               * NO - BRANCH
         L     R2,DCBOUT           * ADDR THE OUTPUT DCB
         PUT   (2)                 * DO THE PUT
         MVC   0(133,R1),0(R8)     * MOVE THE PRINT LINE TO THE
*                                  ** ADDRESS RETURNED IN REG 1
         BR    R7                  * RETURN
*
PUTP1    DS    0H
         MVI   REC1,X'40'          * BLANK OUT
         MVC   REC1+1(132),REC1    ** THE RECORD
         MVC   REC2,REC1           *** WORK AREAS
         MVC   REC1,0(R8)        * MOVE THE RECORD TO WORK AREA
         TM    DCBRECFM,X'04'    * DOES INPUT FILE HAVE
*                                ** ASA CONTROL CHARACTERS ?
         BO    PUTP1B            * YES - BRANCH
*
* NOTE - THIS MEANS THE INPUT FILE HAS NO CONTROL CHARACTERS.
*      - THE COM MACHINE CANNOT HANDLE SUCH A FILE PROPERLY,
*      - OVERPRINTING WILL OCCUR IF NEW PAGES ARE NOT SKIPPED
*      - TO. TO OVERCOME THIS THE INPUT RECORD IS REFORMATTED
*      - SO THAT THE FIRST BYTE WILL CONTAIN A CONTROL CHARACTER.
*      - ALSO A COUNT OF EACH RECORD IS KEPT AND A SKIP TO CH1 IS DONE
*      - AFTER 60 OF THESE LINES ARE RECEIVED CONSECUTIVELY.
*
         MVC   REC2+1(132),REC1
         MVC   REC1,REC2
         L     R2,LINENUM        * LOAD LINE NUMBER COUNTER
         LA    R3,1              *
         AR    R2,R3             * INCREMENT BY ONE
         LA    R3,60             *
         CR    R2,R3             * MORE THAN 60 LINES PRINTED ?
         BH    PUTP5             * YES - BRANCH TO FORCE
*                                ** SKIP TO A NEW PAGE
         ST    R2,LINENUM        * STORE LINE COUNT
         CLI   SWNOSPAC,X'FF'    * IF A SKIP TO CH1 HAS
         BNE   PUTP1C            ** JUST BEEN EFFECTED DO
         MVI   SWNOSPAC,X'00'    *** NOT DO A SPACE BEFORE
         MVI   REC1,X'01'        **** PRINTING.
         B     PUTP7
*
*
* NOTE - THE FOLLOWING CODE SCANS EACH RECORDS ASA CONTROL CHARACTER
*      - AND REPLACES IT WITH A CORRESPONDING MCC (MACHINE CONTROL
*      - CHARACTER). THE COM MACHINE MUST RECEIVE ALL ITS INPUT (THIS
*      - PROGRAMS OUTPUT) IN THE SAME FORMAT (INPUT TO THIS PROGARM
*      - CAN VARY).
*
PUTP1B   DS    0H
         CLI   REC1,X'40'        * IS THIS A SPACE 1 CC ?
         BNE   PUTP2             * NO - BRANCH
PUTP1C   DS    0H
         MVI   REC1,X'0B'        * REPLACE WITH MCC
         B     PUTP7             * BR TO PUT PRINT
*
PUTP2    DS    0H
         CLI   REC1,X'F0'        *
         BNE   PUTP3             *
         MVI   REC1,X'13'        * REPLACE WITH MCC
         B     PUTP6             *
*
PUTP3    DS    0H
         CLI   REC1,X'60'        *
         BNE   PUTP4             *
         MVI   REC1,X'1B'        * REPLACE WITH MCC
         B     PUTP6             *
*
PUTP4    DS    0H
         CLI   REC1,X'4E'        *
         BNE   PUTP5             *
         MVI   REC1,X'01'        * REPLACE WITH MCC
         B     PUTP6             *
*
PUTP5    DS    0H
         MVI   REC1,X'8B'        * REPLACE WITH MCC
*
PUTP6    DS    0H
         LA    R2,0              * ZERO LINE COUNTER
         ST    R2,LINENUM        * AND STORE .......
*
PUTP7    DS    0H
         L     R2,DCBOUT           * ADDR THE OUTPUT DCB
         PUT   (2)                 * DO THE PUT
         MVC   0(133,R1),REC1      * MOVE THE PRINT LINE TO THE
*                                  ** AREA AS ADDRESSED BY REG1
*
*        NOTE - WE ARE REPLACING ASA CONTROL CHARACTERS WITH MACHINE
*               CONTROL CHARACTERS. ASA CC CAUSE A PRINTER TO SPACE/
*               SKIP BEFORE PRINTING. TO SIMULATE THIS WITH MACHINE CC
*               WE MUST FIRST USE AN IMMEDIATE MCC TO SKIP OR SPACE AND
*               THEN ISSUE A SECOND PUT TO WRITE THE ACTUAL RECORD.
*               REMEMBER THAT IT IS NOT POSSIBLE TO DO A SPACE/SKIP AND
*               THEN PRINT USING MACHINE CONTROL CHARACTERS.
*               THE SIMULATION IS ALSO DONE FOR A NEWFCH PRINT LINE SO
*               THAT IF OUTPUT IS TO A REAL PRINTER THEN THESE RECORDS
*               WILL ALSO PRINT.
*
*
PUTP8    DS     0H
         CLI    REC1,X'01'         * DID WE JUST DO A WRITE NO SPACE ?
         BER    R7                 * YES - RETURN
         MVI    REC1,X'01'         * FORCE A WRITE WITHOUT SPACE
         L      R2,DCBOUT          * PUT THE RECORD
         PUT    (2)
         MVC    0(133,R1),REC1
         BR     R7
         EJECT
***********************************************************************
**********      GET A RECORD SUBROUTINE  ..................   *********
***********************************************************************
         SPACE 1
GETREC   DS    0H
         GET   (5)                 * GET A RECORD
         LH    R2,DCBLRECL         * LOAD RECORD LENGTH
         BCTR  R2,0                * REDUCE BY 1 FOR EXECUTE INSTR
         EX    R2,MOVEREC          *
         B     NEWFCH1
*
MOVEREC  DS    0H
         MVC   0(0,R8),0(R1)       * MOVE THE RECORD TO THE
*                                  ** GETMAINED AREA .....
*
NEWFCH1  DS    0H
         CLC   NEWFCH(6),4(8)      * A NEWFCH CARD ?
         BE    NEWFCH2             * YES - BRANCH
         LA    R3,0                * INDICATE NOT A NEWFCH REC
         BR    R7                  * RETURN
*
NEWFCH2  DS    0H
         CLI   10(R8),X'F1'        * A NEWFCH1 CARD ?
         BNE   NEWFCH3
         MVC   FCHTJN,12(R8)       * CARD1 INFO TO FICHE TITLE LINE
         LA    R3,1                * INDICATE NEWFCH REC READ
         BR    R7                  * RETURN
*
NEWFCH3  DS    0H
         CLI   10(R8),X'F2'        * A NEWFCH2 CARD ?
         BNE   NEWFCH4
         MVC   FCHTL1,12(R8)       * CARD2 INFO TO FICHE TITLE LINE
         LA    R3,1                * INDICATE NEWFCH REC READ
         BR    R7                  * RETURN
*
NEWFCH4  DS    0H
         CLI   10(R8),X'F3'        * A NEWFCH3 CARD ?
         BNE   NEWFCH5
         MVC   FCHTL2,12(R8)       * CARD3 INFO TO FCHE TITLE LINE
         LA    R3,1                * INDICATE NEWFCH REC READ
         BR    R7                  * RETURN
*
NEWFCH5  DS    0H
         LA    R3,0                * NOT A VALID NEWFCH CARD. IGNORE IT
         BR    R7                  * RETURN
         EJECT
***********************************************************************
**********    PRINT A FICHE LINE SUBROUTINE ....           ************
***********************************************************************
         SPACE 1
PUTFCHT  DS    0H
         CLC   FCHTJN,BLANKS       * JOB AREA FILLED ?
         BNE   PUTF1               * YES - BRANCH
         MVC   FCHTJN,JOBNAME      * MOVE THE SSOB JOB NAME IN
*
PUTF1    DS    0H
         CLC   FCHTL1(3),=CL3'...' * IS ANY DATA REQ'D IN THIS FIELD
         BNE   PUTF1A              * YES - BRANCH
         MVI   FCHTL1,X'40'                   * NO - BLANK OUT
         MVC   FCHTL1+1(L'FCHTL1-1),FCHTL1    ** THE FIELD
         B     PUTF2
*
PUTF1A   DS    0H
         CLC   FCHTL1,BLANKS       * LINE 1 AREA FILLED ?
         BNE   PUTF2               * YES - BRANCH
         MVC   FCHTL1(8),JOBID     * MOVE THE SSOB JOB ID IN
*
PUTF2    DS    0H
         CLC   FCHTL2(3),=CL3'...' * IS ANY DATA REQ'D IN THIS FIELD
         BNE   PUTF2A              * YES - BRANCH
         MVI   FCHTL2,X'40'                   * NO - BLANK OUT
         MVC   FCHTL2+1(L'FCHTL2-1),FCHTL2    ** THE FIELD
         B     PUTF3
*
PUTF2A   DS    0H
         CLC   FCHTL2,BLANKS       * LINE 2 AREA FILLED ?
         BNE   PUTF3               * YES - BRANCH
         MVC   FCHTL2,DSNAME       * MOVE THE SSOB DSNAME IN
*                                  ** NOTE - ONLY THE FIRST 39
*                                  *** BYTES ARE MOVED AS FCHTL2
*                                  **** CAN ONLY HOLD THAT MANY..
PUTF3    DS    0H
         MVI   FCHTCC,X'8B'        * FORMAT A SKIP TO A NEW PAGE
         MVC   FCHNEW,NEWFCH       * INSERT 'NEWFCH' INTO FICHTITL
*
PUTF4    DS    0H
         L     R2,DCBOUT           * ADDR THE OUTPUT DCB
         PUT   (2)                 * DO THE PUT
         MVC   0(133,R1),FCHTITL   * MOVE THE PRINT LINE TO THE
*                                  ** ADDRESS AS RETURNED IN REG1
         MVI   FCHTITL,X'09'       * FORCE A WRITE WITHOUT SPACE
         PUT   (2)                 * DO THE PUT
         MVC   0(133,R1),FCHTITL   * MOVE THE PRINT LINE
         MVI   FCHTITL,X'8B'       * SKIP TO A NEW PAGE
         PUT   (2)                 * DO THE PUT
         MVC   0(133,R1),FCHTITL   * MOVE THE PRINT LINE TO THE
*                                  ** ADDRESS AS RETURNED IN REG1
*
PUTF6    DS    0H
         LA    R2,0                * SET LINE COUNTER TO INDICATE
         ST    R2,LINENUM          ** START OF NEW PAGE    .......
*
         MVI   FCHTITL,X'40'          * BLANK OUT THE
         MVC   FCHTITL+1(132),FCHTITL ** FCHTITL AREA
         MVI   SWNOSPAC,X'FF'      * SET SKIP TO CH1 DONE SWITCH
         BR    R7                  * RETURN
         EJECT
***********************************************************************
**********      I/O ERROR ROUTINES                         ************
***********************************************************************
         SPACE 1
*
ERRNUMI  DS    0H
         L     R13,SVR13            * ADDRESS REG SAVE AREA (CLOBBERED
*                                   ** WHEN SYNAD IS TAKEN AT PUT)
         XC    WTORECB,WTORECB      * CLEAR THE ECB
         MVC   REPLY(5),BLANKS      * CLEAR REPLY AREA
         LA    R2,REPLY
         LA    R3,WTORECB
         WTOR  '**FCHWTR-1-ERROR READING INPUT. **',(2),5,(3)
         WAIT  ECB=WTORECB
         CLC   REPLY(5),NOTED       * VALID REPLY ?
         BE    ERREND
         WTO   '**FCHWTR-4-INVALID REPLY. **'
         B     ERRNUMI              * ELSE LOG THE MESSAGE AGAIN
*
ERRNUMO  DS    0H
         L     R13,SVR13            * ADDRESS REG SAVE AREA (CLOBBERED
*                                   ** WHEN SYNAD IS TAKEN AT PUT)
         XC    WTORECB,WTORECB      * CLEAR THE ECB
         MVC   REPLY(5),BLANKS      * CLEAR REPLY AREA
         LA    R2,REPLY
         LA    R3,WTORECB
         WTOR  '**FCHWTR-2-ERROR WRITING OUTPUT. **',(2),5,(3)
         WAIT  ECB=WTORECB
         CLC   REPLY(5),NOTED       * VALID REPLY ?
         BE    ERREND
         WTO   '**FCHWTR-4-INVALID REPLY. **'
         B     ERRNUMO              * ELSE LOG THE MESSAGE AGAIN
*
ERROUT   DS    0H
         L     R13,SVR13            * ADDRESS REG SAVE AREA (CLOBBERED
*                                   ** WHEN SYNAD IS TAKEN AT PUT)
         XC    WTORECB,WTORECB      * CLEAR THE ECB
         MVC   REPLY(5),BLANKS      * CLEAR REPLY AREA
         LA    R2,REPLY
         LA    R3,WTORECB
         WTOR  '**FCHWTR-3-ERROR WRITING TAPE OUTPUT. **',(2),5,(3)
         WAIT  ECB=WTORECB
         CLC   REPLY(5),NOTED       * VALID REPLY ?
         BE    ERREND               * BR IF YES
         WTO   '**FCHWTR-4-INVALID REPLY. **'
         B     ERROUT               * ELSE LOG THE MESSAGE AGAIN
*
ERREND   DS    0H
         CLOSE ((5))             * CLOSE THE INPUT FILE
         L     R2,GETSIZE        * LOAD SIZE OF GETMAIN WORK AREA
         FREEMAIN R,LV=(2),A=(9)
         L     R13,4(R13)        * ADDR CALLING MODS REG SAVE AREA
         L     R9,8(R13)         * ADDR THIS MODS REG SAVE AREA
         FREEMAIN R,LV=80,A=(9)  * FREE REG SAVE/WORK AREA
         RETURN (14,12),,RC=8    * INDICATE OUTPUT ERROR
         EJECT
***********************************************************************
**********      TERMINATION                                ************
***********************************************************************
         SPACE 1
END      DS    0H
         CLOSE ((5))             * CLOSE THE INPUT FILE
         L     R2,GETSIZE        * LOAD SIZE OF GETMAIN WORK AREA
         FREEMAIN R,LV=(2),A=(9)
         L     R13,4(R13)        * ADDR CALLING MODS REG SAVE AREA
         L     R9,8(R13)         * ADDR THIS MODS REG SAVE AREA
         FREEMAIN R,LV=80,A=(9)  * FREE REG SAVE/WORK AREA
         EJECT
***********************************************************************
** THE FOLLOWING CODE IS USED TO FREE GETMAINED AREAS REQUESTED  ******
** BY IASXWR00 BUT NOT FREED.                                    ******
***********************************************************************
*
CLEANUP  DS    0H
         USING PSA,R0
         L     R1,FLCCVT           * PICK UP CVT POINTER
         DROP  R0
         USING CVT,R1
         L     R1,CVTTCBP          * PICK UP TCB POINTER
         L     R1,4(R1)            * POINT TO CURRENT TCB
         DROP  R1
         USING TCB,R1
         L     R1,TCBMSS           * POINT TO SPQE
         DROP  R1
         USING SPQESECT,R1
*
FINDDQE  DS    0H
         LTR   R1,R1               * ANY THERE ?
         BZ    CLUPEND             * NO
         TM    SPQEFLGS,LASTSPQE+SPQEOWN * IS THIS THE DQE POINTER ?
         BO    DQEF
         L     R1,SPDQEPTR         * PICK UP NEXT SPQE
         B     FINDDQE
*
DQEF     DS    0H
         CLI   SPQEID,X'00'        * IS IT SUBPOOL ZERO
         BNE   CLUPEND             * NO
         LA    R2,TBQE1
         LR    R3,R1
*
LOOP     DS    0H
         L     R3,4(R3)            * PICK UP POINTER TO NEXT DQE
         LTR   R3,R3               * ANY THERE
         BZ    CLUPEND             * NO
         L     R7,0(R3)            * PICK UP FQE PTR
         MVC   0(4,R2),8(R7)       * SAVE ADDRESS OF FREE STORAGE
         L     R5,12(R3)           * PICK UP DQE LENGTH
         L     R8,4(R7)            * PICK UP FQE LENGTH
         SR    R5,R8               * GIVES THE LENGTH FOR FREEMAIN
         ST    R5,4(R2)
         LA    R2,8(R2)
         C     R2,TBQEND           * END OF TABLE
         BL    LOOP
         LA    R2,TBQE4
         LA    R3,3
*
LOOP1    DS    0H
         L     R1,0(R2)            ADDRESS TO BE FREED
         L     R0,4(R2)            LENGTH TO BE FREED
         FREEMAIN R,LV=(0),A=(1)   LET IT GO
         LA    R2,8(R2)
         BCT   R3,LOOP1
*
CLUPEND  DS    0H
         RETURN (14,12),,RC=0
         LTORG
         END   ,
