//SM0007A JOB OHOT,'G WICKS$9TH FLR',NOTIFY=SM0007,MSGCLASS=X,CLASS=V
//JOBCAT  DD DSN=USERCAT.TSOPK1,DISP=SHR
//STEP1   EXEC ASMLKED
//ASM.SYSPRINT DD SYSOUT=X
//ASM.SYSIN DD *
      EJECT
UJV      TITLE 'IEFUJV  -  JOB VALIDATION EXIT  -  HEANEY'
         SPACE 10
*     CAUTION !!!
*     THIS EXIT USES THE 'USER-COMMUNICATION FIELD' OF THE COMMON EXIT
*     PARAMETER AREA POINTED TO BY R2. THE FIRST FOUR BITS OF THE
*     BYTE AT DISPLACEMENT +32 FROM THE START OF THE PARAMETER AREA
*     ARE USED FOR INDICATORS TO THE CALLING ROUTINE. THE TERMINATION
*     EXIT (IEFACTRT) USES THE REMAINING 28 BITS OF THIS WORD.
*     USE CAUTION ON ANY AMENDMENTS TO THIS EXIT AS ONLY ONE BIT,
*     I.E.'10' REMAINS UNUSED.
         EJECT
***********************************************************************
*                                                                     *
*     LOAD BASE (R12), LOAD PARM ADDRS (R2,R3,R4)                     *
*                                                                     *
***********************************************************************
         SPACE 3
IEFUJV   CSECT
         USING  *,R15BASE
         B      M1B1
         DC     CL8'IEFUJV'
M1B1     DS     0H
         STM    R14,R12,12(R13SAVE)
         DROP   R15BASE
         USING  IEFUJV,R12
         LR     R12,R15BASE
         LM     R2INFO,R4CODE,0(R1PARL)      STORE PARMS
         EJECT
***********************************************************************
*                                                                     *
*     TEST FOR JOB CARD - LOCATE JOB ACCT DETAILS (R5)                *
*                                                                     *
***********************************************************************
         SPACE 3
         TM     0(R4CODE),X'01'              IS IT JOB CARD
         BZ     M3B1                         NO - WAS MSGC=M REQD
         CLC    0(3,R3JCL),=C'// '           CONT. JOB CARD
         BE     M2B1                         YES
         LA     R5PTR,2(R3JCL)              3RD BYTE OF JOB CARD
M1B3     DS     0H
         LA     R5PTR,1(R5PTR)
         CLI    0(R5PTR),X'40'               SCAN FOR END OF
         BNE    M1B3                         JOBNAME
M1B4     DS     0H
         LA     R5PTR,1(R5PTR)               SCAN FOR JOB STATEMENT
         CLI    0(R5PTR),X'40'               STATEMENT
         BE     M1B4
         CLC    0(4,R5PTR),=C'JOB '          FOUND
         BNE    M1B2                         NO - RETURN
         MVI    32(R2INFO),X'00'             REINIT USER INDICATORS
         LA     R5PTR,3(R5PTR)               ADDR NEXT BLANK STRING
M1B5     DS     0H
         LA     R5PTR,1(R5PTR)
         CLI    0(R5PTR),X'40'               SCAN FOR ACCT. INFO
         BE     M1B5
         CLI    0(R5PTR),X'6B'               IS IT COMMA
         BE     M1B2 NO JOB ACCT
         CLI    0(R5PTR),X'7D'               IS IT APOSTROPHE
         BE     M1B6
         CLI    0(R5PTR),X'4D'              IS IT BRACKET
         BNE    M1C1
         EJECT
***********************************************************************
*                                                                     *
*     R5 POINTS TO ACCT FIELD - ENSURE FOUR CHARS LONG                *
*                             - VALIDATE AGAINST TABLES               *
*                                                                     *
***********************************************************************
         SPACE 3
M1B6     DS     0H
         LA     R5PTR,1(R5PTR)              SKIP LEADING BRACKET
M1C1     DS     0H INVALID IF NOT 4 CHARS
         CLI    4(R5PTR),X'6B'              COMMA
         BE     M1C1A
         CLI    4(R5PTR),X'7D'               APOSTROPHE
         BE     M1C1A
         CLI    4(R5PTR),X'5D'               BRACKET
         BNE    M1D1                         INVALID
M1C1A    DS     0H
         LOAD   EP=STJAP                     GET THE ACCT INFO TABLE
         LR     R7WKTYP,R0                   LOAD THE TABLE ADDRESS
M1C1B    DS     0H
         L      R6LPCTL,0(R7WKTYP)           NUMBER OF TABLE ENTRIES
         LA     R7WKTYP,12(R7WKTYP)          ADDRESS THE SECT ID'S TBL
M1C2     DS     0H
         CLC    0(2,R5PTR),0(R7WKTYP)        DOES SECTION ID MATCH
         BE     M1C3                         YES - TEST APPLICATION ID
         LA     R7WKTYP,2(R7WKTYP)           BUMP TO NEXT ENTRY
         BCT    R6LPCTL,M1C2                 KEEP LOOKING
         B      M1D1                         INVALID WORK TYPE
M1C3     DS     0H
         LR     R7APPCD,R0                   RELOAD THE TABLES ADDRESS
         L      R6LPCTL,8(R7APPCD)           NUMBER OF TABLE ENTRIES
         L      R7APPCD,4(R7APPCD)           ADDRESS THE APPLN ID TBL
M1C4     DS     0H
         CLC    2(2,R5PTR),0(R7APPCD)        DOES APPLICATION ID MATCH
         BE     M1C5                         YES - ALL IS WELL
         LA     R7APPCD,2(R7APPCD)           BUMP TO NEXT ENTRY
         BCT    R6LPCTL,M1C4                 KEEP LOOKING
         B      M1D1                         INVALID APPLICATION CODE
         EJECT
***********************************************************************
*                                                                     *
*     IF ACCT EQ LVXX INDICATE MSGCLASS=M REQUIRED                    *
*                     EXIT IF NOT LIVE JOB                            *
*                     SCAN REMAINDER OF CARD FOR MSGCLASS=M           *
*                                                                     *
***********************************************************************
         SPACE 3
M1C5     DS    0H
         CLC   0(2,R5PTR),=C'LV'   IS IT A LIVE JOB
         BNE   M1B2                NO
         OI    32(R2INFO),X'80'    INDICATE LIVE JOB IN COMM FLD
         LA    R5PTR,3(R5PTR)      ADDR NEXT PARAM ON JOB CARD
M1C7     DS    0H
         LA    R7CONT,71(R3JCL)         ADDR CONTIN BYTE 72
M1C6     DS    0H
         LA    R5PTR,1(R5PTR)     NEXT BYTE
         CR    R5PTR,R7CONT        IS IT 72
         BE    M2C2                YES
         CLC   0(10,R5PTR),=C'MSGCLASS=M'  REQD FIELD FOUND
         BNE   M1C6                NO - TRY NEXT BYTE
         NI    32(R2INFO),X'FF'-X'40' INDICATE MSGC=M FOUND
         B     M1B2                VALID FIELD FOUND
M2C2     DS    0H
         OI    32(R2INFO),X'40'    INDICATE MSGC=M NOT YET FOUND
         B     M1B2                RETURN
         EJECT
***********************************************************************
*                                                                     *
*     PROCESS CONTINUATION JOB CARD                                   *
*                                                                     *
***********************************************************************
         SPACE 3
M2B1     DS    0H
         TM    32(R2INFO),X'40'         MSGC=M REQD FOR THIS JOB
         BNO   M1B2                     NO
         LA    R5PTR,2(R3JCL)           ADDR 3RD BYTE
M2B3     DS    0H
         LA    R5PTR,1(R5PTR)           LOCATE NXT NON BLANK BYTE
         CLI   0(R5PTR),X'40'
         BE    M2B3
         BCTR  R5PTR,0               ADJUST PNTR THEN CONTINUE PROCESS
         B     M1C7                  AS PER JOB CARD.
         EJECT
***********************************************************************
*                                                                     *
*     PROCESS NON JOB CARD JCL  - IF AFTER PROCESSING ALL JOB CARDS   *
*             FOR LIVE WORK AND MSGCLASS=M WAS NOT FOUND CANCEL.      *
*                                                                     *
***********************************************************************
         SPACE 3
M3B1     DS    0H
         TM    32(R2INFO),X'40'         IS MSGC=M REQD BUT NOT
*                                           PRESENT FOR THIS JOB
         BO    M1D3                     YES - CANCEL JOB
         B     M1B2 ******** GOTO PCF CHECKING ********
*
         EJECT
***********************************************************************
*                                                                     *
*     MESSAGE LOGGING ROUTINE  -   JOB ACCT PROCESSING                *
*                                                                     *
***********************************************************************
         SPACE 3
M1D1     DS     0H
         OI     32(R2INFO),X'20'   INDICATE JOB ACCT INVALD
M1D3     DS     0H
         LA    R7LEN,128           LENGTH FOR GETMAIN
         GETMAIN R,LV=(R7LEN)
         LR    R10MSGDM,R1ADDR     STORE ADDR OF VIRT STORAGE
         USING MSGDUM,R10MSGDM     ADDR MSG AREA DSECT
         TM    32(R2INFO),X'20'    TEST USER AREA FOR REASON CODE
         BNO   F1B5
         MVC   MSG2(30),MSGTXT2    INVALID JOB ACCOUNTING
         WTO   'JOB ACCOUNTING INVALID',ROUTCDE=(11)
         B     F1B8
F1B5     DS    0H
         TM    32(R2INFO),X'40'
         BNO   F1B6
         MVC   MSG2(30),MSGTXT3    INVALID MSGCLASS FOR LIVE WORK
         WTO   'INVALID MESSAGE CLASS FOR LIVE WORK',ROUTCDE=(11)
F1B8     DS    0H
         MVC   WTOLEN,MSGLEN
         MVC   MSG1(13),MSGTXT1    SET UP SYSOUT MSG
         MVC   JOB(7),0(R2INFO)    JOBNAME
         MVC   MSG3(6),MSGTXT4
         WTO   MF=(E,(R10MSGDM))
         LR    R1ADDR,R10MSGDM
         FREEMAIN R,LV=128,A=(1)
* GOTO F1B6 TO CANCEL JOB
         EJECT
***********************************************************************
*                                                                     *
*     SET APPROPRIATE RETURN CODE  -  M1B2 - CONTINUE PROCESSING      *
*                                     F1B6 - CANCEL                   *
*                     AND RETURN TO CALLING MOD.                      *
*                                                                     *
***********************************************************************
         SPACE 3
F1B6     DS     0H
         NI     32(R2INFO),X'FF'-X'F0'       REINIT USER INDICATORS
         LA     R2,4                         RET CODE - CANCEL
         B      M1D2                         RETURN TO CALLING MOD
M1B2     DS     0H
         SR     R2,R2                        RET CODE - OK
M1D2     DS     0H
         ST     R2,16(R13SAVE)
         LM     R14,R12,12(R13SAVE)
         BR     R14
         EJECT
***********************************************************************
*                                                                     *
*     CONSTANTS, DATA AREAS AND TABLES                                *
*                                                                     *
***********************************************************************
         SPACE 3
*
R0       EQU    0
R1PARL   EQU    1
R1ADDR   EQU    1
R2INFO   EQU    2
R2       EQU    2
R3JCL    EQU    3
R4CODE   EQU    4
R5PTR    EQU    5
R6LPCTL  EQU    6
R7LEN    EQU    7
R7WKTYP  EQU    7
R7CONT   EQU    7
R7APPCD  EQU    7
R10MSGDM EQU    10
R12      EQU    12
R13SAVE  EQU    13
R14      EQU    14
R15BASE  EQU    15
*
MSGLEN   DC    AL2(ENDMSG-MSG1)    LEN OF SYSOUT MSG
         DC    XL2'0'
MSGTXT1  DC    C'** JOBNAME = '
MSGTXT2  DC    CL30'JOB ACCOUNTING INVALID'
MSGTXT3  DC    CL30'INVALID MSGCLASS FOR LIVE WORK'
MSGTXT4  DC    CL6'  ****'
*
MSGDUM   DSECT
WTOLEN   DS    F
MSG1     DS    13C            MSG TEXT
JOB      DS    8C                  JOBNAME
MSG2     DS    30C                      TEXT
MSG3     DS    6C                             MSG TEXT
ENDMSG   EQU   *
IEFUJV   CSECT
*
         LTORG
         END   ,
//LKED.SYSLMOD DD DSN=SM0007.PDS.LOAD,DISP=SHR,UNIT=3350,VOL=SER=TSOPK1
//LKED.SYSPRINT DD SYSOUT=X
//LKED.SYSIN DD *
  SETCODE AC(1)
  ENTRY IEFUJV
  NAME IEFUJV(R)
/*
