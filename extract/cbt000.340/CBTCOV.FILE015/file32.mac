 ***********************************************************************
 ***********************************************************************
                             SCRATCH COMMAND


            THE SCRATCH COMMAND DELETES DATASETS REGARDLESS OF WHETHER T
          DATASET HAS A CATALOG ENTRY. IF THERE IS A CATALOG ENTRY IN TH
          USER'S STEPCAT THE CATALOG ENTRY IS ALSO DELETED. FOR THIS REA
          IT IS RECOMMENDED THAT ONLY USERS WITH CONCATENATIONS OF ALL U
          ISSUE SCRATCH
          OF THE COMMAND IS :

                   SCRATCH DATA.SET.NAME VOLUME

          WHERE :   DATA.SET.NAME IS THE FULLY QUALIFIED DATASET NAME WI
                    APOSTROPHIES.
                    VOLUME THE THE VOLUME SERIAL NUMBER OF THE PACK CONT
                    THE DATASET.

             THE RESULT OF THE SCRATCH WILL BE WRITTEN TO SYSPRINT SO EN
               THAT SYSPRINT HAS BEEN ALLOCATED IF THE COMMAND IS RUN FR
               THE FOREGROUND. FOR MULTIPLE DATASET DELETIONS THE COMMAN
               MAY BE RUN FROM THE TSO FOREGROUND OR BACKGROUND BY CREAT
               A SYSIN DATASET CONTAINING ONE CARD ENTRIES WITH THE
               DATA.SET.NAME AND VOLUME AS PREVIOUSLY SPECIFIED. SYSPRIN
               SHOULD BE ALLOCATED AND THE COMMAND INVOKED BY

                   CALL 'SM00.LINKLIB(SCRATCH)'


          OR    //         EXEC PGM=SCRATCH
                //STEPLIB  DD   DSN=SM00.LINKLIB,DISP=SHR


            NOTE: THE COMMAND USES THE MVS MACROS SCRATCH,CATALOG AND CA
                  REFERENCE IS TO :


               OS/VS2 SYSTEM PROGRAMMING LIBRARY : DATA MANAGEMENT GC26-


//SM0004S JOB OSOT,XXXX,
//        MSGCLASS=X,CLASS=V,NOTIFY=SM0004              REL00060
//JOBCAT  DD   DSN=USERCAT.TSOPK1,DISP=SHR
//STEP1   EXEC ASMLKED,MOD='SM.TEST.LINKLIB'
//ASM.SYSPRINT DD SYSOUT=X
//ASM.SYSIN  DD *
SCRATCH  TITLE 'SCRATCH A DATASET AND UNCATALOG      B SAWARD'
***********************************************************************
**********      INITIALISATION                             ************
***********************************************************************
         SPACE 2
SCRATCH  CSECT
         USING *,R12
         PRINT NOGEN
         B     24(R15)
         DC    CL8'SCRATCH'
         DC    CL4'&SYSTIME'
         DC    CL8'&SYSDATE'
         STM   R14,R12,8(R13)
         LR    R12,R15
         ST    R13,SV13
         LA    R13,SVREGS
         B     START
         EJECT
***********************************************************************
**********      CONSTANTS & STORAGE                        ************
***********************************************************************
         SPACE 2
         REGEQU
SVREGS   DS    9D
WORK     DS    D
SV13     DS    F
RETCODE  DC    F'0'
*
VOL      DC    H'1'         *  NUMBER OF VOLUMES * MUST
         DC    X'3050200B'  *  DEVT   3350       * REMAIN
VOLID    DC    CL6'VOLID0'  *  VOLUME            * TOGETHER
STATUS   DC    H'0'         *  STATUS            * .
*
CCEND    DC    CL4'END'
DSNAME   DS    CL44' '
BLANKS   DC    CL121' '
CC0      DC    8C'0'
BUFFER   DC    CL80' '
PRINT    DC    CL121' '
EMSG1    DC    CL10'INPUT ERR '
EMSG2    DC    CL20'VOLID INVALID'
EMSG3    DC    CL20'UNKNOWN ERROR'
DELET    DC    CL10'DELETED'
UCAT     DC    CL24'DELETED && UNCATALOGGED'
SWPARM   DC    X'00'
*
SCRMSG   DC    A(0,SCRMSG1,SCRMSG2,SCRMSG3,SCRMSG4)
         DC    A(SCRMSG5,SCRMSG6,SCRMSG7,SCRMSG8,SCRMSG9)
SCRMSG1  DC    CL20'DATASET NOT FOUND   '
SCRMSG2  DC    CL20'PASSWORD NEEDED     '
SCRMSG3  DC    CL20'UNEXPIRED FILE      '
SCRMSG4  DC    CL20'PERM I/O ERROR      '
SCRMSG5  DC    CL20'MOUNT REQUIRED      '
SCRMSG6  DC    CL20'UNABLE TO MOUNT VOL '
SCRMSG7  DC    CL20'DATASET IN USE      '
SCRMSG8  DC    CL20'RACF DEFINED DATASET'
SCRMSG9  DC    CL20'RACF ERROR          '
*
INPUT    DCB   DDNAME=SYSIN,EODAD=ENDIN,MACRF=GM,DSORG=PS
OUTPUT   DCB   DDNAME=SYSPRINT,LRECL=121,MACRF=PM,DSORG=PS
DELETE   CAMLST SCRATCH,DSNAME,,VOL,,OVRD
UNCAT    CAMLST UNCAT,DSNAME
*
         EJECT
***********************************************************************
**********       PROCESSING                                 ***********
***********************************************************************
START    DS    0H
         L     R2,0(R1)            PICK UP PARMLIST POINTER
         LH    R3,0(R2)            PICK UP LENGTH OF PARMLIST
         LTR   R3,R3               ANY PARMS
         BZ    OPEN
         LA    R2,2(,R2)           POINTER TO PARMS DATA
         CLI   0(R2),X'00'         IS THIS A COMMAND ?
         BNE   MPARM
FPARM    DS    0H
         CLI   0(R2),C' '          FIND START OF DATASET NAME
         BE    MPARM
         LA    R2,1(,R2)
         BCT   R3,FPARM
         B     ERROR1
MPARM    DS    0H
         BCTR  R3,0
         EX    R3,EXMVC0
         MVI   SWPARM,X'FF'
         B     CHECK
OPEN     DS    0H
         OPEN  INPUT
         OPEN  (OUTPUT,OUTPUT)
READ     DS    0H
         GET   INPUT,BUFFER
CHECK    DS    0H
         CLC   CCEND,BUFFER
         BE    ENDIN
         LA    R4,BUFFER
         LA    R5,80
FDSNAME  DS    0H
         CLI   0(R4),C' '      FIND THE FIRST NON BLANK
         BNE   MDSNAME         MOVE DATASET NAME
         LA    R4,1(,R4)       UPDATE POINTER
         BCT   R5,FDSNAME
         B     ERROR1
MDSNAME  DS    0H
         LR    R2,R4           SAVE DATASET NAME POINTER
FDSEND   DS    0H
         CLI   1(R4),C' '      FIND END OF DATASET NAME
         BE    MDSNAME1
         LA    R4,1(,R4)       UPDATE POINTER
         BCT   R5,FDSEND
         B     ERROR1
MDSNAME1 DS    0H
         LR    R1,R4
         SR    R1,R2               CALCULATE LENGTH OF DATASET NAME
         CH    R1,=H'43'           CHECK LENGTH
         BH    ERROR1
         MVC   DSNAME,BLANKS
         EX    R1,EXOC1            SETUP DATASET NAME
         LA    R4,1(R4)            UPDATE POINTER TO 1ST CHAR PAST NAME
FVOLID   DS    0H
         CLI   0(R4),C' '
         BNE   MVOLID
         LA    R4,1(,R4)           UPDATE POINTER
         BCT   R5,FVOLID
         B     ERROR1
MVOLID   DS    0H
         MVC   VOLID,0(R4)         MOVE IN VOLUME INFO
         OC    VOLID,BLANKS        ALL TO UPPER CASE
         SCRATCH DELETE        DELETE THE DATASET
         LTR   R15,R15
         BNZ   ERROR2
         MVC   PRINT(10),DELET
         CATALOG UNCAT         DELETE THE CATALOG POINTER
         LTR   R15,R15
         BNZ   DISPLAY
         MVC   PRINT(24),UCAT
         B     DISPLAY
EXMVC0   MVC   BUFFER(0),0(R2)     MOVE PARAMETERS TO BUFFER
EXOC1    OC    DSNAME(0),0(R2)     MOVE DATASET NAME
         EJECT
***********************************************************************
**********      ERROR ROUTINE                              ************
***********************************************************************
         SPACE 2
ERROR1   DS    0H
         LA    R15,80
         SR    R15,R5              SET R15 TO POINT TO ERROR
         MVC   PRINT(10),EMSG1
         CVD   R15,WORK
         UNPK  PRINT+10(8),WORK+3(5)
         OC    PRINT+10(8),CC0     ALL TO NUMERALS
         B     DISPLAY
         SPACE 2
ERROR2   DS    0H
         CH    R15,=H'4'
         BNE   ERROR2A
         MVC   PRINT(20),EMSG2
         B     DISPLAY
ERROR2A  DS    0H
         CH    R15,=H'8'
         BE    ERROR2B
         MVC   PRINT(20),EMSG3
         B     DISPLAY
ERROR2B  DS    0H
         LH    R15,STATUS
         SLL   R15,2               INDEX BY 4
         L     R2,SCRMSG(R15)      GET POINTER TO ERROR MSG
         MVC   PRINT(20),0(R2)     MOVE IN THE ERROR MESSAGE
         B     DISPLAY
         SPACE 2
         EJECT
***********************************************************************
**********      DISPLAY RESULT OF SCRATCH                  ************
***********************************************************************
         SPACE 2
DISPLAY  DS    0H
         CLI   SWPARM,X'FF'        WAS INPUT FROM PARMLIST
         BNE   DISP2               ALL DONE
         MVC   PRINT+22(80),BUFFER
         TPUT  PRINT,79
         B     END
DISP2    DS    0H
         MVC   PRINT+25(80),BUFFER
         PUT   OUTPUT,PRINT
         MVC   PRINT,BLANKS        CLEAR OUTPUT BUFFER
         B     READ
         EJECT
***********************************************************************
**********      TERMINATION                                ************
***********************************************************************
         SPACE 2
ENDIN    DS    0H
         CLOSE INPUT
         CLOSE OUTPUT
END      DS    0H
         L     R13,SV13
         MVC   12(4,R13),RETCODE   SET UP RETCODE
         LM    R14,R12,8(R13)
         BR    R14
         LTORG
         END   ,
/*
//LKED.SYSPRINT DD SYSOUT=X
//LKED.DD1 DD DSN=SM.TEST.LINKLIB,DISP=SHR
//LKED.SYSIN DD *
  NAME SCRATCH(R)
/*
