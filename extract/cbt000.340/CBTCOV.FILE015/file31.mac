//SM0004A   JOB   OSOT,SAWARD,MSGCLASS=X,NOTIFY=SM0004,CLASS=V
//JOBCAT    DD    DSN=USERCAT.TSOPK1,DISP=SHR
//ASMIUC    EXEC  PGM=IEV90,PARM='OBJECT,NODECK,RENT'
//SYSPRINT  DD    SYSOUT=X
//SYSLIB    DD    DSN=PCS.TEST.MACRO.MACLIB,DISP=SHR,DCB=BLKSIZE=12960
//          DD    DSN=SYS1.AMODGEN,DISP=SHR
//          DD    DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1    DD    DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100))
//SYSUT2    DD    DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3    DD    DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSLIN    DD    DSN=&&OBJIUC,UNIT=SYSDA,SPACE=(80,(200,50)),
//          DISP=(MOD,PASS)
//SYSPUNCH  DD    DUMMY
//SYSIN     DD    *
ERBMFIUC TITLE 'INITIALISE RMF EXITS      B SAWARD'
***********************************************************************
**                                                                   **
**   THIS PROGRAM IS THE RMF EXIT INITIALISER                        **
**   REFER RMF REFERENCE AND USERS GUIDE                             **
**         CHAPTER 3 'PROCEDURES FOR ADDING USER FUNCTIONS'          **
**                                                                   **
**     IT'S FUNCTION IS TO ATTACH A TASK WHICH PERIODICALLY          **
**     SCANS THE ASCB'S FOR DOMAIN 120 OR GREATER TO TERMINATE       **
**                                                                   **
***********************************************************************
***********************************************************************
**********      INITIALISATION                             ************
***********************************************************************
         SPACE 2
ERBMFIUC CSECT
         USING *,R10
         B     24(R15)
         DC    CL8'ERBMFIUC'
         DC    CL4'&SYSTIME'
         DC    CL8'&SYSDATE'
         STM   R14,R12,8(R13)
         LR    R10,R15             SET BASE REG
         L     R5,0(,R1)           PICK UP THE PARMLIST POINTER
         GETMAIN R,LV=72           GET A SAVEAREA
         ST    R13,4(R1)           SAVE THE OLD SAVEAREA POINTER
         ST    R1,8(R1)            SAVE THIS SAVEAREA POINTER
         ST    R1,4(,R5)           FREE STORAGE ADDRESS FOR ERBMFTUR
         LR    R13,R1              SET SAVEAREA POINTER
         B     START
         EJECT
***********************************************************************
**********      CONSTANTS                                  ************
***********************************************************************
         SPACE 2
         REGEQU
         EJECT
***********************************************************************
**********       PROCESSING                                 ***********
***********************************************************************
START    DS    0H
         GETMAIN R,LV=8
         LTR   R15,R15             PICK UP RETURN CODE
         BNZ   ERROR1              BAIL OUT
         XC    0(8,R1),0(R1)       CLEAR THE STORAGE
         MVC   4(4,R1),=C'ECB.'    SET ECB IDENT
         ST    R1,0(,R5)           STORAGE ADDRESS FOR ERBMFTUR
         LR    R4,R1               SET PARAM ADDR FOR ATTACH
         ATTACH EP=ERBMFTSK,PARAM=(4)   ATTACH THE TASK
         WAIT  ECB=(4)             WAIT FOR ERBMFTSK TO ASSUME CONTROL
         B     END
         EJECT
***********************************************************************
**********      TERMINATION                                ************
***********************************************************************
         SPACE 2
ERROR1   DS    0H
         WTO   'ERROR IN GETMAIN  ERBMFIUC TERMINATING  ...'
         B     END
         EJECT
***********************************************************************
**********      TERMINATION                                ************
***********************************************************************
         SPACE 2
END      DS    0H
         LR    R2,R13              PICK UP SAVEAREA POINTER
         L     R13,4(R2)           RESTORE R13
         LM    R14,R12,8(R13)
         BR    R14
         LTORG
         EJECT
         PRINT OFF
         END   ,
/*
//ASMTUR    EXEC  PGM=IEV90,PARM='OBJECT,NODECK,RENT'
//SYSPRINT  DD    SYSOUT=X
//SYSLIB    DD    DSN=PCS.TEST.MACRO.MACLIB,DISP=SHR,DCB=BLKSIZE=12960
//          DD    DSN=SYS1.AMODGEN,DISP=SHR
//          DD    DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1    DD    DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100))
//SYSUT2    DD    DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3    DD    DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSLIN    DD    DSN=&&OBJTUR,UNIT=SYSDA,SPACE=(80,(200,50)),
//          DISP=(MOD,PASS)
//SYSPUNCH  DD    DUMMY
//SYSIN     DD    *
ERBMFTUR TITLE 'TERMINATE RMF EXITS      B SAWARD'
***********************************************************************
**                                                                   **
**   THIS PROGRAM IS THE RMF EXIT TERMINATOR                         **
**   REFER RMF REFERENCE AND USERS GUIDE                             **
**         CHAPTER 3 'PROCEDURES FOR ADDING USER FUNCTIONS'          **
**                                                                   **
**     IT'S FUNCTION IS TO END THE TASK ERBMFTSK WHICH WAS ATTACHED  **
**     BY ERBMFIUC                                                   **
**                                                                   **
***********************************************************************
          SPACE 3
***********************************************************************
**********      INITIALISATION                             ************
***********************************************************************
ERBMFTUR CSECT
         USING *,R10
         B     24(R15)
         DC    CL8'ERBMFTUR'
         DC    CL4'&SYSTIME'
         DC    CL8'&SYSDATE'
         STM   R14,R12,8(R13)
         LR    R10,R15             SET BASE REG
         L     R1,0(R1)            GET PARMLIST
         L     R2,0(R1)            PICK UP ECB POINTER
         L     R1,4(R1)            GET SAVEAREA POINTER
         ST    R13,4(R1)           SAVE OLD SAVEAREA POINTER
         LR    R13,R1              SET SAVEAREA POINTER
         ST    R13,8(R13)          SAVEAREA POINTER
         B     START
         EJECT
***********************************************************************
**********      CONSTANTS                                  ************
***********************************************************************
         SPACE 2
         REGEQU
         EJECT
***********************************************************************
**********       PROCESSING                                 ***********
***********************************************************************
START    DS    0H
         XC    0(4,R2),0(R2)       CLEAR THE ECB
         MVC   4(4,R2),=C'END.'    TELL ERBMFTSK TO TERMINATE
         WAIT  ECB=(2)             WAIT FOR THE TASK TO TERMINATE
         SPACE 2
END      DS    0H
         LR    R5,R13              GET ADDRESS OF SAVEAREA
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(5)    FREE THE SAVEAREA
         LM    R14,R12,8(R13)
         BR    R14
         LTORG
         PRINT OFF
         END   ,
/*
//LKED     EXEC   PGM=IEWL,PARM=(RENT,REUS,REFR,LIST,MAP),
//         COND=(0,NE)
//UNCHANGE DD   DSN=SYS1.LINKLIB,DISP=SHR,VOL=SER=SY2RES,UNIT=3350
//OBJIUC   DD   DSN=&&OBJIUC,DISP=(OLD,DELETE)
//OBJTUR   DD   DSN=&&OBJTUR,DISP=(OLD,DELETE)
//SYSLMOD  DD   DISP=SHR,DSN=SYS1.LINKLIB,VOL=SER=SY2RES,UNIT=3350
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSPRINT DD   SYSOUT=X
//SYSLIN   DD  *
   INCLUDE OBJIUC
   SETCODE AC(1)
   ENTRY ERBMFIUC
   NAME ERBMFIUC(R)
   INCLUDE UNCHANGE(ERBMFDUC)
   ENTRY ERBMFDUC
   NAME ERBMFDUC(R)
   INCLUDE UNCHANGE(ERBMFRUR)
   ENTRY ERBMFRUR
   NAME ERBMFRUR(R)
   INCLUDE OBJTUR
   SETCODE AC(1)
   ENTRY ERBMFTUR
   NAME ERBMFTUR(R)
   INCLUDE UNCHANGE(ERBMFITR)
   ENTRY ERBMFITR
   NAME ERBMFITR(R)
   INCLUDE UNCHANGE(ERBMFPUS)
   ENTRY ERBMFPUS
   NAME ERBMFPUS(R)
/*
