         PRINT NOGEN
         SPACE 4
***********************************************************************
*                                                                     *
*        TSO SUBMIT EXIT.                                             *
*        ***************                                              *
*                                                                     *
*                                                                     *
*   1)   APPLIES TO TESTING JOBS ONLY. (1ST CHAR OF JOBNAME 'X')      *
*                                                                     *
*   2)   FINDS JOBCLASS FROM 'CLASS=' OPERAND OR USES THE DEFAULT     *
*        AT LABEL DEFCLASS IF NO CLASS CODED ON JOB STATEMENT.        *
*                                                                     *
*   3)   FINDS AND REMOVES 'TIME=' OPERAND IF ONE IS CODED.           *
*                                                                     *
*   4)   ADDS A CONTINUATION OF THE JOB STATEMENT WITH A TIME         *
*        OPERAND LIMITED BY THE TIME AS SPECIFIED IN THE TABLE        *
*        AT LABEL TIMETABL (DEPENDING ON JOB CLASS).                  *
*                                                                     *
*   5)   TRIES TO ALLOW FOR ALL THE STRANGE WAYS IN WHICH JOB         *
*        STATEMENTS CAN BE CODED, AND THE FACT THAT THEY MAY          *
*        HAVE SYNTAX ERRORS.                                          *
*                                                                     *
*   6)   TO BE USED IN CONJUNCTION WITH MODIFIED VERSION OF IEFUTL    *
*        SMF EXIT WHICH EXTENDS THE TIME IF NOT RUNNING ON 3033.      *
*                                                                     *
***********************************************************************
         EJECT
IKJEFF10 CSECT
         SAVE  (14,12),,*
         LR    R12,R15            BASE
         USING IKJEFF10,R12
         SPACE
         L     R11,0(R1)          KEEP PARM POINTER
         USING IEEXITL,R11        PARM LIST
         L     R10,IESUBCTP       A(CONTROL SWITCHES)
         USING IESUBCTD,R10
         L     RCARD,IECARDP      A(CARD IMAGE)
         L     R7,IEEXITWD        USER FIELD
         LTR   R7,R7              IF ZERO THIS IS FIRST TIME
         BNZ   NOTFIRST
         SPACE 2
         GETMAIN R,LV=WORKALEN    GET WORK/SAVEAREA
         LR    R7,R1
         USING WORKA,R7           TEMP BASE FOR WORKA
         SPACE 2
         ST    R7,IEEXITWD        KEEP FOR SUBS CALLS
         BAL   R8,INITWF          INIT WORK AREAS
         SPACE
*        SET TO TAKE EXIT FOR ALL CARDS
         MVI   IETAKEEX,IETJOB+IETEXEC+IETDD+IETCMD+IETNULL+IETJES
         SPACE 2
NOTFIRST ST    R7,8(R13)          CHAIN
         ST    R13,4(R7)
         LR    R13,R7
         DROP  R7                 DROP TEMP BASE
         USING WORKA,R13
         EJECT
***********************************************************************
*                                                                     *
*        STORE CARD IMAGES UNTIL WE HAVE ALL THE JOB STATEMENT OPERANDS
*                                                                     *
***********************************************************************
         SPACE 2
         LTR   RCARD,RCARD        IF THIS IS AN INSERT CALL
         BZ    CLEANUP            GO GET CARD IMAGE
         SPACE
         LR    R1,RCARD           POINT TO CARD IMAGE TO BE STORED
         BAL   R8,STORECRD        STORE CARD IMAGE
         LTR   R15,R15            IF NO MORE SPACE, GET OUT - (THE
         BNZ   CLEANUP            CURRENT CARD HAS BEEN STORED)
         USING CARDSD,RCARD
         SPACE
         TM    IESTMTYP,IESJOB    IF WE GET NON-JOB STMT.
         BNO   CLEANUP            EXPECTED CONTINUATION MISSING
         SPACE
         TM    IESTMTYP,IESCONTN  IF THIS IS A CONTINUATION
         BO    ISCONTJ            THEN THERE IS NO JOBNAME
         SPACE
         CLC   TESTCLAS,CSJOBNAM  //JOBNAME - LOOK AT 1ST CHAR
         BNE   CLEANUP            IGNORE NON-TESTING JOBS
         SPACE
ISCONTJ  EQU   *
         TM    IESTMTYP,IESOPCON  IF OPERANDS ARE NOT CONTINUED
         BNO   PROCESS            THEN PROCESS THE STATEMENT
         SPACE
         LA    R15,IECONTIN       ELSE GET THE REST OF THE JOB STMT
         SPACE 2
RETURN   L     R13,4(R13)
         RETURN (14,12),RC=(15)
         EJECT
CLEANUP  DS    0H                 RETURN ANY STORED CARDS, FREE WORK
*                                 AREA, STOP TAKING CALLS.
         SPACE
         L     RCARD,REMPTR       'REMOVE' POINTER
         C     RCARD,CARDPTR      IF AT END    (NO CARDS STORED)
         BE    NOMORES            THEN FINISHED
         SPACE
         LA    R15,CSCARD         A(CARD IMAGE)
         ST    R15,IECARDP        TO RETURN
         LA    RCARD,CSEND        NEXT STORED CARD AREA
         SPACE
         ST    RCARD,REMPTR       KEEP FOR NEXT TIME
         LA    R15,IERETURN       TO INSERT
         B     RETURN
         SPACE 2
NOMORES  MVI   IETAKEEX,IETJOB    START OVER IF MORE THAN 1 JOB.
         XC    IEEXITWD,IEEXITWD  WORKAREA TO BE FREED - WILL OBTAIN
*                                 ANOTHER AREA IF ENTERED AGAIN.
         LR    R2,R13             WORKAREA
         L     R13,4(R13)         PREVIOUS SA.
         FREEMAIN R,LV=WORKALEN,A=(R2)
         SPACE
         RETURN (14,12),RC=IECONTIN FINAL RETURN - CONTINUE WITH SUB.
         EJECT
***********************************************************************
*                                                                     *
*        WE NOW HAVE ALL THE JOB CARD OPERANDS - PROCESS THEM:        *
*                                                                     *
*        1) FIND THE JOB CLASS                                        *
*        2) FIND AND REMOVE TIME OPERAND (IF CODED)                   *
*        3) ADD A CONTINUATION OF THE JOB CARD WITH A TIME OPERAND    *
*           WHICH LIMITS THE TIME, DEPENDING ON JOB CLASS             *
*                                                                     *
*        WE WILL ONLY GET HERE IF WE WERE PROCESSING A TEST JOB.      *
*                                                                     *
***********************************************************************
         SPACE 2
PROCESS  DS    0H
         SPACE
         LA    RCARD,CARDSTOR     ADDR OF 1ST STORED CARD
         ST    RCARD,PROCPTR      KEEP IT
         SPACE 2
PROCARD  EQU   *                  PROCESS CARD
         SR    R2,R2
         ICM   R2,1,CSOPER        IF THERE IS AN OPERAND ON THIS CARD
         BNZ   LOOKJ              THEN LOOK AT IT
         SPACE 2
PROCNEXT L     RCARD,PROCPTR      A(STORED CARD)
         LA    RCARD,CSEND        NEXT STORED CARD
         ST    RCARD,PROCPTR      KEEP IT
         C     RCARD,CARDPTR      IF THERE ARE MORE
         BNE   PROCARD            THEN PROCESS THEM
         SPACE
         B     CLASSFND           NO CLASS CODED - USE DEFAULT
         SPACE 3
LOOKJ    BAL   R14,INITSCAN       SETUP R2,R3,STATUS FOR SCAN
         B     HAVEOP             LOOK AT 1ST OP ON THIS CARD
         SPACE 2
INITSCAN LA    R2,CSCARD-1(R2)    A(OPERAND)   (R2 WAS OPERAND COL)
         LA    R3,CSCONTC         A(CONTINUE COLUMN)
         SR    R3,R2              MAX LENGTH OF OPERANDS + COMMENTS
*        AT THIS POINT R2 SHOULD HAVE POINTER TO OPERANDS, R3 SHOULD
*        HAVE LENGTH.
         MVI   PSTATUS,PSOPERF    WE HAVE OPERAND,NOT END OF OPERANDS
         BR    R14
         SPACE 2
SCANCARD TM    PSTATUS,PSENDOP    IF ALREADY AT END OF OPERANDS
         BO    PROCNEXT           THEN GET OUT OF SCAN
         BAL   R14,GETOPER        FIND OPERAND, UPDATE R2,R3
         TM    PSTATUS,PSABAND    IF THERE IS SYNTAX ERROR
         BO    CLEANUP            THEN GET OUT
         SPACE
PROCOP   TM    PSTATUS,PSOPERF    IF NO OPERAND FOUND
         BNO   PROCNEXT           THEN STOP SCAN FOR THIS CARD
         SPACE
HAVEOP   CH    R3,=AL2(L'CLASEQIL) COULD THIS BE CLASS OP
         BL    SCANCARD           TOO SHORT
         CLC   CLASEQIL,0(R2)     IF THIS IS NOT 'CLASS='
         BNE   SCANCARD           THEN TRY FOR 'TIME='
         SPACE
         OI    PSTATUS2,PSCLSFND  SET IND CLASS OPERAND FOUND
         CLI   L'CLASEQIL(R2),COMMA CLASS=, ?
         BE    CLASSFND           LEAVE DEFAULT IN 'CLASSAV'
         MVC   CLASSAV,L'CLASEQIL(R2) KEEP CLASS
         SPACE 3
CLASSFND EQU   *                  NOW LOOK FOR TIME OPERAND
         SPACE
         LA    RCARD,CARDSTOR     ADDR OF 1ST STORED CARD
         ST    RCARD,PROCPTR      KEEP IT
         SR    R1,R1
         STH   R1,QTCOUNT         RE-INIT FOR SCAN
         STH   R1,PARCOUNT
         SPACE 2
PROCARD2 EQU   *                  PROCESS CARD
         SR    R2,R2
         ICM   R2,1,CSOPER        IF THERE IS AN OPERAND ON THIS CARD
         BNZ   LOOKJ2             THEN LOOK AT IT
         SPACE 2
PROCNEX2 L     RCARD,PROCPTR      A(STORED CARD)
         LA    RCARD,CSEND        NEXT STORED CARD
         ST    RCARD,PROCPTR      KEEP IT
         C     RCARD,CARDPTR      IF THERE ARE MORE
         BNE   PROCARD2           THEN PROCESS THEM
         SPACE 2
******** END OF CARDS
         B     TIMEFND            END OF CARDS - TIME OP NOT FOUND
         SPACE 3
LOOKJ2   BAL   R14,INITSCAN       SETUP FOR SCAN
         B     HAVEOP2            LOOK AT 1ST OP ON THIS CARD
         SPACE 2
SCANCAR2 TM    PSTATUS,PSENDOP    IF ALREADY AT END OF OPERANDS
         BO    PROCNEX2           THEN GET OUT OF SCAN
         BAL   R14,GETOPER        FIND OPERAND, UPDATE R2,R3
         TM    PSTATUS,PSABAND    IF THERE IS SYNTAX ERROR
         BO    CLEANUP            THEN GET OUT
         SPACE
PROCOP2  TM    PSTATUS,PSOPERF    IF NO OPERAND FOUND
         BNO   PROCNEX2           THEN STOP SCAN FOR THIS CARD
         SPACE
HAVEOP2  CH    R3,=AL2(L'TIMEEQIL) COULD THIS BE TIME OP
         BL    SCANCAR2           TOO SHORT
         CLC   TIMEEQIL,0(R2)     IF NOT 'TIME='
         BNE   SCANCAR2           THEN TRY NEXT OPERAND
         SPACE 2
*        TIME OPERAND FOUND
         STM   R2,R3,REMTO        KEEP POSITION,REMAINING LENGTH
         BAL   R14,GETOPER        FIND NEXT
         TM    PSTATUS,PSABAND    IF SYNTAX ERROR
         BO    CLEANUP            THEN GET OUT
         SPACE
         TM    PSTATUS,PSSPLIT    IF TIME OPERAND IS SPLIT
         BO    CLEANUP            FORGET IT FOR NOW
         SPACE
         STM   R2,R3,REMFROM      END FOR REMOVING OPERAND
         BCTR  R2,0               EG    TIME=10
*                                              ×
         CLI   0(R2),COMMA        EG    TIME=10,
*                                               ×
         BE    TIMEF1             DONT MOVE ","
         SPACE
         LA    R2,1(R2)           IT WAS NOT FOLLOWED BY ","
         SPACE
TIMEF1   L     R1,REMTO           POINT TO 'T'
         LA    R1,L'TIMEEQIL(R1)  DROP 'TIME='
         LR    R15,R2             END
         SR    R15,R1             LENGTH
         LTR   R15,R15            IF NULL
         BZ    DECTIME            THEN NOTHING TO MOVE
         BCTR  R15,0              FOR EX
         EX    R15,MOVETIM        PUT INTO TIMEFLD
         SPACE
DECTIME  BAL   R8,GETIM           DECODE IT
         SPACE
         LTR   R15,R15            IF TIME OPERAND INVALID
         BNZ   CLEANUP            THEN DONT TOUCH IT
         SPACE
         OI    PSTATUS2,PSTIMFND  TIME OPERAND FOUND
         SPACE 2
*        REMOVE TIME OPERAND
         LM    R4,R5,REMTO        POINT TO 'T' & GET REMAINING LENGTH.
         BCTR  R4,0               BACKUP TO SEE IF THIS IS THE FIRST
         LA    R5,1(R5)           OPERAND ON THIS CARD
         CLI   0(R4),COMMA        LOOK FOR "," IN FRONT OF "TIME"
         BE    REMOVE1            B IF TIME IS NOT 1ST OPERAND ON CARD
         SPACE
         LA    R4,1(R4)           TIME WAS 1ST OPERAND - BACK TO "T"
         BCTR  R5,0               CORRECT LENGTH
         LM    R6,R7,REMFROM      END
         BCTR  R6,0               BACKUP TO SEE IF THER ARE MORE
         LA    R7,1(R7)           OPERANDS AFTER TIME OPERAND
         CLI   0(R6),COMMA
         BNE   ONLYTIME           ONLY TIME OPERAND, NO MORE OPERANDS
         CLI   1(R6),SPACE        ONLY TIME OP ON THIS CARD, OPS CONT.
         BE    ONLYCONT
         SPACE
*        TIME OPERAND WAS 1ST OP. ON CARD, THERE ARE MORE
*        OPERANDS ON THE CARD.
         LA    R6,1(R6)           BACK TO NEXT OPERAND
         BCTR  R7,0               CORRECT LENGTH
         B     REMOVE2            GO MOVE REMAINING OPERANDS UP.
         SPACE
REMOVE1  LM    R6,R7,REMFROM      ADDR OF NEXT OP OR END
         BCTR  R6,0               BACKUP TO CHECK
         LA    R7,1(R7)           IF THERE ARE MORE
         CLI   0(R6),COMMA
         BE    REMOVE2            B IF THERE ARE FOLLOWING OPERANDS
         LA    R6,1(R6)           NO FOLLOWING OPERANDS
         BCTR  R7,0               POINT BEYOND TIME OPERAND
         SPACE
REMOVE2  ICM   R7,8,=C' '         PAD
         MVCL  R4,R6              TIME OPERAND REMOVED
         SPACE 2
***********************************************************************
TIMEFND  EQU   *                  EITHER TIME OPERAND WAS NOT
*                                 CODED, OR IT HAS NOW BEEN REMOVED.
*
*                                 FIND LAST CARD WITH OPERANDS, ADD
*                                 A ',' TO INDICATED A CONTINUATION
*                                 AND THEN INSERT CARD WITH TIME
*                                 OPERAND.
*
*                                 THERE THE POSSIBILITY THAT THE ','
*                                 WILL NOT FIT ON THE CARD - AN
*                                 OPERAND MAY HAVE TO BE REMOVED AND
*                                 INCLUDED ON THE INSERTED CARD.
         SPACE
         L     RCARD,CARDPTR      NEXT AVAILABLE SPACE
         SH    RCARD,=AL2(CSEND-CARDSD) ADDR OF CARD WITH LAST OPERAND
         SPACE
         MVC   CONTBYTE,CSCONTC   KEEP CONTINUATION COL.
         SR    R2,R2
         IC    R2,CSOPER          GET OPERAND COL
         BAL   R14,INITSCAN       SETUP FOR SCAN
HAVEOP3  STM   R2,R3,REMTO        KEEP IN CASE OPERAND MUST BE MOVED
         BAL   R14,GETOPER        BUMP UP BY ONE OPERAND
         TM    PSTATUS,PSABAND    IF SYNTAX ERROR
         BO    CLEANUP            THEN GIVE UP
         TM    PSTATUS,PSENDOP    KEEP GOING
         BNO   HAVEOP3            TILL END OF OPERANDS IS FOUND
         SPACE
*        CHECK IF THERE IS SPACE TO ADD THE COMMA
         TM    PSTATUS,PSENDCD    IF AT END OF CARD
         BO    NOSPCE             THEN NO SPACE FOR COMMA
         SPACE
INSRTIM  MVC   0(R2,2),=C', '     OPERANDS CONTINUED
         LA    RCARD,CSEND        NEXT CARD SLOT
         MVI   CSCARD,SPACE       CLEAR CARD TO BE INSERTED
         MVC   CSCARD+1(L'CSCARD-1),CSCARD
         MVC   CSCONTC,CONTBYTE   IN CASE OF CONTINUED COMMENT
         BAL   R8,SETUPTIM        TIME=OPERAND BUILT
         MVI   0(R4),SPACE        OPERANDS NOT CONTINUED
         LA    RCARD,CSEND        NEXT SPACE
         ST    RCARD,CARDPTR      CARD NOW 'INSERTED'
         B     CLEANUP            GO RETURN STORED CARDS
         SPACE
NOSPCE   DS    0H                 NO SPACE TO ADD ','
         SPACE
         CLC   =C'//  ',CSCARD    1ST SEE IF WE CAN MOVE OPERANDS BACK
         BNE   NOSPCE2            IE IS THERE A 'SPARE' SPACE
         MVC   CSCARD+2(68),CSCARD+3 MOVE OPERANDS BACK
         BCTR  R2,0               BACKUP R2 TO NEW END
         B     INSRTIM            GO ADD CONTINUATION
         SPACE
NOSPCE2  DS    0H                 MOVE LAST OPERAND ONTO NEXT CARD
         LA    RCARD,CSEND        ADDR FOR NEXT CARD
         MVC   CSCARD(3),=C'// '  INIT
         MVC   CSCARD+3(L'CSCARD-3),CSCARD+2
         SPACE
         LM    R4,R5,REMTO        ADDR,LENGTH OF LAST OPERAND
         LA    R2,CSCARD+3        START OF AREA TO MOVE TO
         LA    R2,0(R2,R5)        PAST END
         BCTR  R5,0               LEN FOR EX
         EX    R5,MOVEOPR         COPY OPERAND ONTO NEXT CARD
*                                 NOW REMOVE OPERAND
         LA    R5,1(R5)           REAL LENGTH AGAIN
         SR    R7,R7              'FROM' LENGTH ZERO
         ICM   R7,8,=C' '         PAD TO CLEAR IT TO SPACES
         MVCL  R4,R6              GONE
         SPACE
         B     INSRTIM            GO ADD TIME CARD
         SPACE 2
MOVEOPR  MVC   CSCARD+3(1),0(R4)  EXECUTED
         SPACE  2
MOVETIM  MVC   TIMEFLD(1),0(R1)   EXECUTED
         SPACE 3
ONLYTIME EQU   *                  ONLY TIME OPERAND - REPLACE IT
         BAL   R8,SETUPTIM        OVERLAY OPERAND
         MVI   0(R4),SPACE        NO MORE OPERANDS
         B     CLEANUP
         SPACE  2
ONLYCONT EQU   *                  AS ABOVE BUT THE OPERANDS CONTINUE
         BAL   R8,SETUPTIM        ON THE NEXT CARD
         MVI   0(R4),COMMA        'CONTINUED'
         B     CLEANUP
         EJECT
SETUPTIM DS    0H                 BUILD TIME CARD:
*
*        // TIME=MMMM
*  OR    // TIME=(MMMM,SS)
*                012345678
*                ×
*               R4
*
*        RETURNS WITH R4 POINTING JUST BEYOND TIME OPERAND
         SPACE
         MVC   CSCARD(3),=C'// '
         MVC   CSCARD+3(L'TIMEEQIL),TIMEEQIL   // TIME=
         LA    R4,CSCARD+3+L'TIMEEQIL  PAST '='
         SPACE
         LA    R15,TIMETABL       FIND TIME TABLE ENTRY FOR
FINDCLT  CLI   0(R15),255         THIS CLASS
         BE    ENDTTAB            X'FF' ON END OF TABLE
         CLC   CLASSAV,0(R15)
         BE    HAVETCL
         LA    R15,5(R15)         EACH ENTRY 5 BYTES
         B     FINDCLT            LOOK AT NEXT
         SPACE
ENDTTAB  LA    R15,DEFTIMET       DEFAULT ENTRY
HAVETCL  ICM   R7,15,1(R15)       GET NUMBER OF SECONDS FROM ENTRY
         TM    PSTATUS2,PSTIMFND  IF TIME WAS NOT CODED
         BNO   PUTTIN             THEN USE MAXIMUM FOR THIS CLASS
         L     R1,TOTSECS         ELSE CHECK TIME CODED:
         LTR   R1,R1              USE TIME CODED IF ITIS
         BZ    PUTTIN             NOT ZERO AND
         CR    R1,R7              IS LESS THAN
         BL    USECODED           MAX ALLOWED.
         SPACE
PUTTIN   SR    R6,R6              CLEAR FOR DIVIDE
         D     R6,=F'60'          CONV TO MINS,SECS
         LTR   R6,R6              IF WHOLE NUMBER OF MINUTES
         BZ    ONLYMINS           LEAVE OUT '(' ')' ETC
         MVI   0(R4),LP           TIME=(
         CVD   R7,DUB
         UNPK  1(4,R4),DUB+5(3)   TIME=(MMM*
         OI    4(R4),C'0'         TIME=(MMMM
         MVI   5(R4),COMMA        TIME=(MMMM,
         CVD   R6,DUB
         UNPK  6(2,R4),DUB+6(2)   TIME=(MMMM,S*
         OI    7(R4),C'0'         TIME=(MMMM,SS
         MVI   8(R4),RP           TIME=(MMMM,SS)
         LA    R4,9(R4)           RETURN POINTER TO NEXT BYTE
         BR    R8                 RETURN
         SPACE
USECODED LR    R7,R1              TIME CODED WAS LESS AND WHO
         B     PUTTIN             ARE WE TO ARGUE
         SPACE
ONLYMINS CVD   R7,DUB
         UNPK  0(4,R4),DUB+5(3)   TIME=MMM*
         OI    3(R4),C'0'         TIME=MMMM
         LA    R4,4(R4)           NEXT BYTE
         BR    R8
         EJECT
***********************************************************************
*                                                                     *
*        CONVERT TIME IN FIELD 'TIMEFLD' TO SECONDS IN 'TOTSECS'      *
*                                                                     *
*        RETURNS R15=0 IF OK, R15=4 IF SYNTAX ERROR.                  *
*        HAS TO ALLOW FOR VARIOUS STRANGE FORMS OF THE TIME OPERAND   *
*        WHICH ARE ACCEPTED BY THE OPERATING SYSTEM.                  *
*                                                                     *
*        EXAMPLES OF VALID TIME OPERANDS:                             *
*        TIME=,TIME=(),TIME=((),()),TIME=((1))                        *
*                                                                     *
***********************************************************************
         SPACE 2
GETIM    DS    0H
         SPACE
         BAL   R14,FILLDEF        FILL 0'S INTO OMITTED SUB-PARMS
         SPACE
         LA    R4,TIMEFLD         START
         BAL   R14,GETNUM         MAY BE SIMPLY TIME=MMMM
         LTR   R7,R7              IF A NUMBER WAS FOUND
         BNZ   GTONEN             THEN EXAMINE IT
*                                 ELSE IT IS NOT A NUMBER:
         CLI   0(R4),LP           IF IT IS '('
         BE    GTOKSOFR           THEN CONTINUE
*                                 ELSE IT IS INVALID TIME OPERAND
ABANDONT LA    R15,4              SET 'INVALID' CODE
         BR    R8                 AND RETURN
         SPACE
GTOKSOFR SR    R1,R1              PAR. COUNT
GTSKLP   CLI   0(R4),LP           IF NOT '('
         BNE   TINNER             THEN EXPECT NUMBER OF MINUTES
         LA    R1,1(R1)           ELSE COUNT ONE MORE '('
         LA    R4,1(R4)           NEXT CHAR
         B     GTSKLP             WILL TERMINATE WITH 'FDUMMY'
*                                 CHARACTER IF OPERAND IS ALL LP'S
         SPACE
TINNER   BAL   R14,GETNUM         NUMBER OF MINUTES EXPECTED ((MMMM...
         LTR   R7,R7              IF NOT NUMERIC
         BZ    ABANDONT           THEN GET OUT WITH ERROR
         CH    R7,=H'4'           MAX DIGITS FOR MINUTES
         BH    ABANDONT           GET OUT IF TOO MANY DIGITS
         BCTR  R7,0               LEN FOR EX
         EX    R7,PACKT           PUT
         CVB   R15,DUB            INTO
         STH   R15,TMINS          MINUTES FIELD
         SPACE
         CLI   0(R4),COMMA        COULD BE (1,30) ETC
         BE    GTCHECKC           OK
         CLI   0(R4),RP           COULD BE ((1),30) ETC
         BNE   ABANDONT           INVALID IF NOT
GTSKRP   CLI   0(R4),RP           GET PAST ')' S
         BNE   GTENDM
         BCTR  R1,0               TO CHECK MATCHED ()'S
         LA    R4,1(R4)           NEXT CHARACTER
         B     GTSKRP             KEEP GOING TILL NO MORE ')' S
         SPACE
GTENDM   CLI   0(R4),FDUMMY       WAS IT SOMETHING LIKE ((1))
         BNE   GTCHECKC           IF NOT TRY FOR ((1),...
*                                 ELSE WE ARE AT END OF OPERAND,
         LTR   R1,R1              SAME NUMBER OF '(' AS ')' ?
         BZ    CONVERTT           IF SO GO CONVERT TO SECONDS
         B     ABANDONT           ELSE RETURN ERROR CODE
         SPACE
GTCHECKC DS    0H                 SHOULD BE (1,30) OR ((1),30) ETC
         CLI   0(R4),COMMA        IF NOT IT IS INVALID
         BNE   ABANDONT
         LA    R4,1(R4)           PAST ',';
         SPACE
         BAL   R14,GETNUM         TRY FOR ...,SS)
         LTR   R7,R7              IF NUMBER FOUND
         BNZ   GTSECS             GO LOOK AT IT
         SPACE
         CLI   0(R4),LP           TRY FOR ...,(1)) ETC
         BNE   ABANDONT           NOPE, GIVE UP
         SPACE
GTSKLPA  CLI   0(R4),LP           GET PAST '(' S
         BNE   TINNERA
         LA    R1,1(R1)           COUNT '(' S
         LA    R4,1(R4)           NEXT CHAR
         B     GTSKLPA
         SPACE
TINNERA  BAL   R14,GETNUM         SHOULD HAVE MM OR M
         LTR   R7,R7              IF NUMBER FOUND
         BNZ   GTSECS             GO LOOK AT IT
         B     ABANDONT           ELSE GET OUT
         SPACE
GTATEND  CLI   0(R4),FDUMMY       IF NOT AT END OF OPERAND
         BNE   ABANDONT           THEN GET OUT
         LTR   R1,R1              ELSE CHECK FOR MATCHED '(' ')'
         BNZ   ABANDONT           ERROR IF NOT MATCHED
         B     CONVERTT           IF OK GO CONVERT TO SECONDS
         SPACE
GTSECS   CLI   0(R4),RP           EG ...1) OR ...1))
         BNE   ABANDONT           GIVE UP IF NOT
         CH    R7,=H'2'           MAX OF 2 DIGITS
         BH    ABANDONT           GET OUT IF TOO MANY
         BCTR  R7,0               EX LEN
         EX    R7,PACKT           PUT
         CVB   R15,DUB            INTO
         STH   R15,TSECS          SECONDS FIELD
         SPACE
GTSKRPA  CLI   0(R4),RP           GET PAST ')' S
         BNE   GTATEND
         BCTR  R1,0               TO CHECK MATCHING '(' ')'
         LA    R4,1(R4)           NEXT CHAR
         B     GTSKRPA
         SPACE 2
GTONEN   CLI   0(R4),FDUMMY       GET OUT IF SOMETHING
         BNE   ABANDONT           TIME=NNN) ETC
         CH    R7,=H'4'           MAX OF 4 DIGITS FOR MINS
         BH    ABANDONT           GET OUT IF TOO MANY
         BCTR  R7,0               EX LEN
         EX    R7,PACKT           PUT
         CVB   R15,DUB            INTO
         STH   R15,TMINS          MINUTES FIELD
         SPACE
CONVERTT DS    0H                 CONVERT MINS,SECS TO SECS
         LH    R1,TMINS           MINUTES
         CH    R1,MAXMINS         CHECK RANGE
         BH    ABANDONT
         MH    R1,=H'60'          =>SECS
         LH    R15,TSECS          SECONDS
         CH    R15,MAXSECS        CHECK RANGE
         BH    ABANDONT
         AR    R1,R15             TOTAL
         ST    R1,TOTSECS
         SPACE
         SR    R15,R15            OK
         BR    R8                 RETURN
         SPACE 2
PACKT    PACK  DUB,0(1,R6)        EXECUTED
         SPACE 3
GETNUM   DS    0H                 CHECK IF FIELD POINTED TO BY R4
*                                 IS NUMERIC. PUTS ADDR OF START OF
*                                 NUMBER IN R6 AND LENGTH IN R7.
*                                 IF 1ST CHAR POINTED TO BY R4 IS NOT
*                                 NUMERIC R7 WILL BE 0. ADVANCES R4
*                                 TO 1ST NON-NUMERIC CHAR.
         LR    R6,R4              START
         SR    R7,R7              LENGTH
NUMCH    CLI   0(R4),C'0'
         BLR   R14
         CLI   0(R4),C'9'
         BHR   R14
         LA    R4,1(R4)
         LA    R7,1(R7)
         B     NUMCH
         SPACE 2
FILLDEF  DS    0H                 PUT 0'S INTO OMITTED SUB-OPERANDS
         CLI   TIMEFLD,FDUMMY     NOTHING AT ALL?
         BNE   FHAVTIM
         MVI   TIMEFLD,C'0'
         BR    R14
         SPACE
FHAVTIM  LA    R1,TIMEFLD         START
FHAVTIMX CLI   0(R1),FDUMMY       END
         BER   R14
         CLC   =C',)',0(R1)       EG (1,)
         BE    FINS0
         CLC   =C'()',0(R1)       EG TIME=(),...
         BE    FINS0
         CLC   =C'(,',0(R1)       EG (,30)
         BE    FINS0
         LA    R1,1(R1)
         B     FHAVTIMX
         SPACE
FINS0    LA    R15,TIMEFLD+L'TIMEFLD-2 2ND LAST CHAR
         SPACE
FEXPAND  MVC   1(1,R15),0(R15)    MAKE SPACE FOR 0
         BCTR  R15,0              BACKUP
         CR    R1,R15             TO WHERE WE ARE
         BNE   FEXPAND
         MVI   1(R1),C'0'
         B     FHAVTIMX           LOOK FOR MORE
         EJECT
GETOPER  DS    0H                 ON ENTRY R2 IS CURRENT POSITION
*        WITHIN CARD, R3 IS REMAINING LENGTH.
*        LOCATES NEXT OPERAND ON CARD,UPDATING R2,R3 AND SETS FLAGS
*        IN PSTATUS.
*        TAKES INTO ACCOUNT OPERANDS IN QUOTES (EG PROGRAMMER NAME)
*        AND OPERANDS IN PARENTHESES. (EG ACCOUNTING INFO)
         SPACE
         MVI   PSTATUS,0          HAVE NOT FOUND ANYTHING YET
         SPACE
         TM    QTCOUNT+1,X'01'    IF QUOTE COUNT IS ODD WE ARE
         BO    INQOT              WITHIN QUOTED STRING.
         SPACE
         CLI   0(R2),RP           CHECK
         BE    RIGHTP             FOR
         CLI   0(R2),LP           ()
         BE    LEFTP
         SPACE
         CLI   0(R2),SPACE        END OF OPERANDS?
         BE    FSPACE
         SPACE
         LH    R1,PARCOUNT        IGNORE ',' S
         LTR   R1,R1              THAT ARE
         BNZ   INQOT              WITHIN ( )
         SPACE
         CLI   0(R2),COMMA
         BE    FCOMMA
         SPACE
INQOT    CLI   0(R2),QUOTE
         BNE   NEXCHR
         LH    R1,QTCOUNT
         LA    R1,1(R1)
         STH   R1,QTCOUNT
         SPACE
NEXCHR   LA    R2,1(R2)
         SH    R3,=H'1'           LENGTH REMAINING
         BNZ   GETOPER            KEEP GOING
         OI    PSTATUS,PSENDCD+PSENDOP END OF CARD, NO MORE OPERANDS
         BR    R14
         SPACE
LEFTP    LH    R1,PARCOUNT        ONE MORE
         LA    R1,1(R1)           '('
         STH   R1,PARCOUNT
         B     NEXCHR
         SPACE
RIGHTP   LH    R1,PARCOUNT        ONE MORE
         SH    R1,=H'1'           ')'
         BM    ABANDON1           TOO MANY ) NOT PRECEEDED BY (
         STH   R1,PARCOUNT
         B     NEXCHR
         SPACE
FCOMMA   LA    R2,1(R2)           PAST ','
         SH    R3,=H'1'           LES LENGTH OF ','
         BZ    CATEND             COMMA AT END OF CARD
         CLI   0(R2),SPACE        ARE THERE MORE OPERANDS ON THIS CARD
         BE    COPCON             NO
         OI    PSTATUS,PSOPERF    OPERAND FOUND
         BR    R14                RETURN
         SPACE
CATEND   OI    PSTATUS,PSENDCD    END OF CARD, OPERANDS CONTINUED
         BR    R14
         SPACE
COPCON   BR    R14                PSOPERF=PSENDOP=PSENDC=0
         SPACE
FSPACE   LH    R1,PARCOUNT        CHECK FOR CONTINUED OPERAND -
         LTR   R1,R1              OPERAND SPLIT OVER 2 CARDS
         BNZ   MAYBSPLT
         OI    PSTATUS,PSENDOP    END OF OPERANDS
         BR    R14
         SPACE
MAYBSPLT LR    R1,R1
         BCTR  R1,0
         CLI   0(R1),COMMA
         BNE   ABANDON1           SPLIT BUT NO ',' ON END
         OI    PSTATUS,PSSPLIT
         BR    R14
         SPACE
ABANDON1 OI    PSTATUS,PSABAND    GIVE UP
         BR    R14
         EJECT
STORECRD DS    0H                 STORE CARD IMAGE POINTED TO BY
*                                 R1 IN CARDSTOR, RETURN R15 NOT ZERO
*                                 WHEN AREA FULL
         L     RCARD,CARDPTR      NEXT SPACE
         MVC   CSOPER,IEOPRAND    OPERAND COL
         MVC   CSFLAG,IESTMTYP    FLAGS
         MVC   CSCARD,0(R1)       CARD IMAGE
         SPACE
         SR    R1,R1              'DELETE' THIS CARD -
         ST    R1,IECARDP         WILL BE INSERTED AGAIN LATER
         SPACE
         LA    R1,CSEND           NEXT SPACE
         ST    R1,CARDPTR         TO INSERT
         SPACE
         LA    R15,ENDSTOR        IF WE ARE AT
         CR    R1,R15             END OF AREA
         BER   R8                 RETURN WITH R15 NOT ZERO
         SPACE
         SR    R15,R15            ELSE R15=0 INDICATES
         BR    R8                 THAT THERE IS STILL SPACE FOR MORE
         EJECT
*        INIT WORK FIELDS
         DROP  R13
         USING WORKA,R7
INITWF   MVI   TIMEFLD,FDUMMY     'DUMMY CHARS' SO THAT END OF OPERAND
         MVC   TIMEFLD+1(L'TIMEFLD-1),TIMEFLD   CAN BE FOUND.
         SR    R1,R1
         SPACE
         ST    R1,TOTSECS         *
         STH   R1,TMINS           **** TIME FIELDS
         STH   R1,TSECS           *
         SPACE
         STH   R1,QTCOUNT         QUOTE COUNT
         STH   R1,PARCOUNT        PAR. COUNT
         SPACE
         MVI   PSTATUS2,0         INIT STATUS
         SPACE
         MVC   CLASSAV,DEFCLASS   DEFAULT CLASS
         SPACE
         LA    R1,CARDSTOR        JOBCARD STORE AREA
         ST    R1,CARDPTR         INSERT POINTER
         ST    R1,REMPTR          REMOVE POINTER
         SPACE
         BR    R8
         EJECT
MAXMINS  DC    H'1440'            MAX VALUE FOR MINUTES
MAXSECS  DC    H'59'              MAX VALUE FOR SECONDS
DEFCLASS DC    C'A'               DEFAULT CLASS
TIMEEQIL DC    C'TIME='
CLASEQIL DC    C'CLASS='
         SPACE 4
TIMETABL EQU   *                  TABLE OF TIMES FOR DIFFERENT CLASSES
*                                 EACH ENTRY 5 BYTES:
*                                 C'CLASS',XL4(SECONDS)
         DC    C'A',AL4(5*60)     5  MIN (ALLOW FOR RESETTING TO I)
         DC    C'H',AL4(1*60)     1  MIN
         DC    C'I',AL4(5*60)     5  MIN
         DC    C'C',AL4(30*60)    30 MIN
         DC    C'J',AL4(30*60)    30 MIN
         DC    5X'FF'             END MARKED BY 'FF'
         DC    5X'FF'             SOME SPARE ENTRIES
         DC    5X'FF'
         DC    5X'FF'
         DC    5X'FF'
DEFTIMET DC    C'*',AL4(1439*60)  TIME IF CLASS NOT FOUND IN TABLE
         SPACE
*        THE ABOVE TABLE ALSO EXISTS IN MODULE IEFUTL, ANY CHANGES
*        SHOULD BE MADE THERE TOO.
         SPACE 3
TESTCLAS DC    C'X'               1ST CHAR FOR TESTING JOBS
         EJECT
         LTORG
         SPACE 3
WORKA    DSECT
SAVEAREA DS    18F
         SPACE
DUB      DS    D                  WORK DOUBLEWORD
TOTSECS  DS    F                  TIME IN SECONDS
PROCPTR  DS    A                  CURRENT STORED CARD TO PROCESS
REMTO    DS    2F                 ADDR,LEN TO MOVE TO TO REMOVE TIME
*                                 OPERAND.
REMFROM  DS    2F                 ADDR,LEN TO MOVE FROM TO  "     "
*                                 TIME OPERAND (WITHIN 'TIMEFLD')
         SPACE
CARDPTR  DS    A                  ADDR OF NEXT AREA TO STORE CARD
REMPTR   DS    A                  ADDR OF NEXT CARD TO REMOVE FROM
*                                 CARD STORE AREA.
         SPACE
TMINS    DS    H                  NUMBER OF MINUTES FROM TIME OPERAND
TSECS    DS    H                  NUMBER OF SECONDS FROM TIME OPERAND
QTCOUNT  DS    H                  QUOTE COUNT
PARCOUNT DS    H                  +1 FOR EACH '('  -1 FOR EACH ')'
TIMEFLD  DS    CL80               TIME OPERAND (LESS 'TIME=')
CLASSAV  DS    C                  JOB CLASS (IF PSCLSFND SET)
CONTBYTE DS    C                  CONTINUE CARD COLUMN FROM LAST CARD
         SPACE
PSTATUS  DS    X                  PARSE STATUS
PSOPERF  EQU   X'80'              OPERAND FOUND
PSENDOP  EQU   X'40'              END OF OPERANDS FOUND
PSENDCD  EQU   X'20'              MMD OF CARD REACHED
PSSPLIT  EQU   X'10'              OPERAND SPLIT (CONTINUED)
PSABAND  EQU   X'08'              PARSE ABANDONNED - SYNTAX ERROR
         SPACE
PSTATUS2 DS    X                  GENERAL STATUS
PSTIMFND EQU   X'80'              TIME OPERAND FOUND + REMOVED
PSCLSFND EQU   X'40'              CLASS OPERAND FOUND
         SPACE 2
CARDSTOR DS    12CL(CSEND-CARDSD) AREA FOR JOB STATEMENT
ENDSTOR  DS    2CL(CSEND-CARDSD)  ALLOW FOR INSERTED CARDS
         SPACE
*        ADD ANY EXTRA WORK AREAS REQUIRED HERE.
         SPACE
WORKALEN EQU   *-WORKA
         SPACE  3
FDUMMY   EQU   X'B1'              USED TO MARK END OF TIME OPERAND
COMMA    EQU   C','
SPACE    EQU   C' '
LP       EQU   C'('
RP       EQU   C')'
QUOTE    EQU   C''''
         SPACE 3
CARDSD   DSECT                    CARD IMAGE STORE AREA - USED TO
*                                 STORE JOB STATEMENT (AND
*                                 CONTINUATIONS)
CSOPER   DS    X                  OPERAND COLUMN (IEOPRAND)
CSFLAG   DS    X                  FLAGS FROM IESTMTYP
CSCARD   DS    0CL80              CARD IMAGE
CSSSLASH DS    CL2                //
CSJOBNAM DS    CL8                JOBNAME
CSTMT    DS    CL61               REST OF STATEMENT
CSCONTC  DS    C                  CONTINUATION COLUMN
CSSEQ    DS    CL8                SEQ NO
CSEND    EQU   *                  END - NEXT ENTRY BEGINS HERE
         SPACE 3
         IKJEFFIE IETYPE=SUBMIT
R0       EQU   0
R1       EQU   1                  WORK
R2       EQU   2                  WORK/SCAN POINTER
R3       EQU   3                  WORK/NUMBER OF CHARS REMAINIG
R4       EQU   4                  WORK
R5       EQU   5                  WORK
R6       EQU   6                  WORK
R7       EQU   7                  WORK
R8       EQU   8                  BAL
RCARD    EQU   9                  CARD IMAGE BASE
R10      EQU   10                 CONTROL SWITCHES BASE (IESUBCTP)
R11      EQU   11                 PARM LIST POINTER
R12      EQU   12                 BASE
R13      EQU   13                 SAVEAREA/WORKAREA BASE
R14      EQU   14                 LO LEVEL BAL
R15      EQU   15                 WORK/RETURN CODE
         END
