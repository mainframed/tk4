*                     SMF EXIT TO CANCEL JOBS WHICH EXCEED RESOURCE LIMI
*                     **************************************************
*           THE SMF JOB/STEP TERMINATION EXIT HAS BEEN INVOKED TO CANCEL
*         WHICH EXCEED PREDETERMINED LEVELS OF SERVICE UNITS IN THE SHOR
*         CLASSES. AT THE TIME OF WRITING THE SERVICE UNIT LEVEL WAS 500
*         THE SHORT BATCH CLASSES WERE CLASSES 'A' AND 'V'.
*
*         THE EXIT IS IEFACTRT WHICH IS ONE OF THE CSECTS OF THE MODULE
*         THE MODULE HAS MANY ALIASES SO TAKE CARE WHEN RELINKING IT AND
*         SURE TO USE THE LINKEDIT JCL WHICH IS WRAPPED AROUND THE SOURC
*
*         TESTING WAS ACCOMPLISHED BY LINKING IEFW21SD INTO
*         'SM.TEST.LINKLIB' AND THEN CREATING AN IEALPAXX MEMBER, THE RE
*         IS THE TEST VERSION IN THE MLPA AND THE ORIGINAL STILL INTACT
*         'SYS1.LPALIB' FOR FALLBACK IF NEEDED - CARE MUST BE EXERCISED
*         IEFACTRT IS A CRITICAL FUNCTION WHEN ACTIVE AND ANY ERRORS WIL
*         REQUIRE FALLBACK.
//SM0004A  JOB   OSOT,SAWARD,MSGCLASS=X,NOTIFY=SM0004,CLASS=V
//JOBCAT   DD    DSN=USERCAT.TSOPK1,DISP=SHR
//ASM      EXEC  PGM=IEV90,PARM='OBJECT,NODECK,RENT'
//SYSPRINT DD    SYSOUT=X
//SYSLIB   DD    DSN=PCS.TEST.MACRO.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD    DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100))
//SYSUT2   DD    DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3   DD    DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSLIN   DD    DSN=&&OBJSET,UNIT=SYSDA,SPACE=(80,(200,50)),
//         DISP=(MOD,PASS)
//SYSPUNCH DD    DUMMY
//SYSIN    DD    *
IEFACTRT TITLE 'IEFACTRT  SMF JOB/STEP TERMINATION EXIT      B SAWARD'
*
*
*
***********************************************************************
**                                                                   **
**  NOTE NOTE NOTE                                                   **
**  ---- ---- ----                                                   **
**                                                                   **
**                                                                   **
**   THIS EXIT USES THE USER COMMUNICATION FIELD IN THE COMMON EXIT  **
**   PARAMETER AREA AS THE STEP SERVICE ACCUMULATOR. REFER GC28-0706 **
**   FOR A DESCRIPTION OF THE COMMON EXIT PARAMETER AREA (P 53) AND  **
**   OFFSET 32(DEC) WITHIN THAT AREA. IF IN FUTURE THE USER          **
**   COMMUNICATION FIELD IS REQUIRED THE LOGIC OF THIS EXIT SHOULD   **
**   BE ALTERED ACCORDINGLY. THE EXIT IEFUJV USES THE FIRST 2 BITS   **
**   OF THE ACCUMULATOR FOR SWITCHES.                                **
**                                                                   **
**   IEFACTRT                                                        **
**   --------                                                        **
**                                                                   **
**   SMF JOB/STEP TERMINATION EXIT                                   **
**                                                                   **
**   THE FUNCTION OF THIS EXIT IS TO ACCUMULATE AND REPORT CUMULATIVE**
**   SERVICE UNITS FOR JOB STEPS AND IF THE ACCUMULATION EXCEEDS     **
**   50,000 FOR JOBS WITHIN THE 'SHORT BATCH' CLASSES (NAMELY A & V) **
**   THEN THE JOB IS TO BE TERMINATED.                               **
**                                                                   **
**                                                                   **
***********************************************************************
         EJECT
***********************************************************************
**********      INITIALISATION                             ************
***********************************************************************
         PRINT NOGEN
IEFACTRT CSECT
         USING *,R11
         B     24(R15)
         DC    CL8'IEFACTRT'
         DC    CL4'&SYSTIME'
         DC    CL8'&SYSDATE'
         STM   R14,R12,12(R13)
         LR    R11,R15
         LR    R9,R1               SAVE POSSIBLE PARM PTR
         LR    R8,R0               SAVE ENTRY REASON CODE
         GETMAIN R,LV=200          GET SOME STORAGE TO PLAY WITH
         LTR   R15,R15
         BNZ   ERROR1              SOMETHING WRONG HERE
         ST    R13,4(R1)           FORWARD CHAIN
         ST    R1,8(R13)           BACKWARD CHAIN
         LR    R13,R1              SET SAVEAREA PTR
         LR    R10,R1              SET POINTER TO FREE STORAGE
         USING FREESTOR,R10        ADDRESS FREESTOR
         B     START
         EJECT
***********************************************************************
**********      CONSTANTS                                  ************
***********************************************************************
         SPACE 2
         REGEQU
*
MSG1     DC    CL80'IEFACT1 - TOTAL SERVICE UNITS FOR COMPLETED STEPS =X
                DDD,DDD,DDD '
MSG2     DC    CL80'IEFACT2 - TOTAL SERVICE UNITS EXCEED 50,000 - ANY FX
               URTHER STEPS WILL BE FLUSHED'
*
ED1      DC    X'402020206B2020206B202120'
*
         EJECT
***********************************************************************
**********       PROCESSING                                 ***********
***********************************************************************
START    DS    0H
         L     R2,0(R9)            SET POINTER TO C E P A
         L     R3,36(R9)           PICK UP RECORD POINTER
         CH    R8,=H'12'           IS REASON STEP TERMINATION ?
         BE    CHECKSU             YES   CHECK HIS SERVICE UNITS
         CH    R8,=H'20'           IS IT POSSIBLE STEP TERMINATION ?
         BNE   END                 NO   EXIT NOW
CHECKSU  DS    0H
         CLI   5(R3),X'04'         IS IT REALLY STEP TERMINATION ?
         BNE   END                 NO   EXIT NOW
         SR    R4,R4               CLEAR R4
         L     R5,32(R2)           PICK UP STEP SERVICE TO DATE
         SLDL  R4,4                IEFUJV SWITCHES TO R4
         STC   R4,WORK             SAVE IEFUJV SWITCHES
         SRL   R5,4                LEAVE ORIGINAL 3 AND A HALF BYTES
         LH    R4,104(R3)          PICK UP OFFSET IN RECORD TO RELOC
         LA    R4,28(R4,R3)        SET POINTER TO SMF4SST - 4 (RDW)
         A     R5,4(R4)            ACCUMULATE
         IC    R4,WORK             PICK UP IEFUJV SWITCHES
         SLL   R4,28               SHIFT INTO BITS 0-3
         CVD   R5,WORK             GRAB THE NEW TOTAL
         OR    R4,R5               RECOMBINE IEFUJV SWITCHES AND ACCUM
         ST    R4,32(R2)           AND RETURN THE NEW TOTAL
         MVC   MSGAREA(80),MSG1    SET UP THE MESSAGE
         MVC   MSGAREA+51(12),ED1  SET UP FOR EDIT
         ED    MSGAREA+51(12),WORK+3  SERV UNITS TO MESSAGE
         BAL   R7,WRITEMSG         WRITE THE MESSAGE TO SYSMSG
         CLI   31(R2),C'A'         IS THIS A CLASS A JOB ?
         BE    QUOTA               YES  CHECK THE QUOTA
         CLI   31(R2),C'V'         IS THIS A CLASS V JOB ?
         BNE   END                 NO   EXIT NOW
QUOTA    DS    0H
         CP    WORK,=P'50000'      IS HE ABOVE HIS QUOTA ?
         BNH   END
         MVC   MSGAREA(80),MSG2    SET UP THE MESSAGE
         BAL   R7,WRITEMSG         WRITE THE MESSAGE TO SYSMSG
         L     R5,4(R13)           ADDRESS PREV SAVEAREA
         MVC   16(4,R5),=F'4'      SET RETURN CODE TO CANCEL IN R15
         B     END
         EJECT
***********************************************************************
**********      MESSAGE WRITER ROUTINE                     ************
**********      PREFORMAT MESSAGE IN WORKAREA AT R10       ************
***********************************************************************
         SPACE 2
WRITEMSG DS    0H
         LA    R1,MSGAREA          SET POINTER FOR WTO
         ST    R1,36(R12)          SET UP MSG ADDRESS
         MVC   42(2,R12),=H'80'    MSG LENGTH
         L     R15,=V(IEFYS)
         BALR  R14,R15             CALL IEFYS TO LOG MSG
         BR    R7
         EJECT
***********************************************************************
**********      ERROR ROUTINE                              ************
***********************************************************************
         SPACE 2
ERROR1   DS    0H
*        ERROR IN GETMAIN
         ABEND 100
         EJECT
***********************************************************************
**********      TERMINATION                                ************
***********************************************************************
         SPACE 2
END      DS    0H
         L     R13,4(R10)          PICK UP THE SAVEAREA ADDRESS
         FREEMAIN R,LV=200,A=(10)
         LM    R14,R12,12(R13)
         SR    R1,R1               SET ZERO RETURN CODE
         BR    R14
         LTORG
         EJECT
***********************************************************************
**********       FREE STORAGE DSECT                         ***********
***********************************************************************
FREESTOR DSECT ,
SAVEAREA DS    9D                  REGISTER SAVEAREA
WORK     DS    D                   WORKAREA
MSGAREA  DS    CL80
         DS    CL20                UNUSED
         END   ,
/*
//LKED     EXEC PGM=IEWL,REGION=768K,
//         PARM='RENT,REUS,REFR,LIST,MAP,SIZE=(300K,100K)',
//         COND=(8,LT,ASM)
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLMOD  DD   DISP=SHR,DSN=SYS1.LPALIB
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSLIB   DD   DSN=SYS1.LPALIB,DISP=SHR
//SYSPRINT DD   SYSOUT=X
//SYSIN    DD   *
     INCLUDE SYSLIB(IEFW21SD)
     ORDER IEFDB400,IEFDB401,IEFAB4F7(P)
     ORDER IEFAB4F6,IEFDB4FF,IEFDB4FC
     ORDER IEFDB4FA,IEFDB410(P),IEFDB412
     ORDER IEFDB411,IEFAB4DC,IEFDB417
     ORDER IEFAB4E9,IEFAB445,IEFDB413,IEFDB414
     ORDER IEFDB418,IEFDB4FE,IEFDB4F9
     ORDER IEFDB4F8,IEFDB4FD,IEFDB480,IEFDB481
     ORDER IEFDB4A0,IEFDB4A1,IEFDB470
     ORDER IEFDB450,IEFDB460,IEFDB490,IEFBB401
     ORDER IEFAB4FE,IEFBB402,IEFBB404
     ORDER IEFBB4M3,IEFAB451,IEFAB452,IEFAB453
     ORDER IEFAB470,IEFAB454,IEFAB457
     ORDER IEFAB464,IEFAB459,IEFAB421,IEFAB427
     ORDER IEFAB431,IEFAB423,IEFAB424
     ORDER IEFAB425,IEFAB426,IEFAB430,IEFAB433
     ORDER IEFAB436(P),IEFAB440(P)
     ORDER IEFAB4F0,IEFAB434(P),IEFAB428
     ORDER IEFAB4FC,IEFAB435(P),IEFAB441
     ORDER IEFAB442,IEFAB432,IEFAB490
     ORDER IEFAB4F3,IEFAB4FD,IEFAB471(P)
     ORDER IEFAB4FA(P),IEFAB473(P),IEFAB4M5
     ORDER IEFAB4F9,IEFAB4F8,IEFAB475
     ORDER IEFAB476,IEFAB480,IEFAB4F2(P)
     ORDER IEFAB492,IEFAB493,IEFAB494(P)
     ORDER IEFAB495,IEFBB410(P),IEFBB412,IEFBB414
     ORDER IEFBB416,IEFAB4EC,IEFAB4A0
     ORDER IEFAB4A2(P),IEFAB4A4(P),IEFAB4A6,IEFAB4A8
     ALIAS IGC0009I,IEFAB4DC,IEFBB410,IEFAB49C,IEFAB4F4
     ALIAS IEFAB445,IEFAB4F5,IEFAB4UV,IEFAB4EC
     ENTRY IEFBB401
     NAME IEFW21SD(R)
