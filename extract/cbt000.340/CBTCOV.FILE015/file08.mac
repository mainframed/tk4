         TITLE 'IEESQ03D -   #Q  ENQUEUE ANALYZER'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       IEESQ03D -       #Q  ENQUEUE ANALYZER                     ***
***                                                                 ***
***       VERSION -        1.0                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS 3.8 SP2/3                            ***
***                                                                 ***
***       UPDATED -        27 OCT 1981  - BNSW -                    ***
***                                                                 ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
IEESQ03D CSECT
         LR    R12,R15            SET ADDRESSABILITY
         USING IEESQ03D,R12
         L     R0,SIZE
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         USING XSA,R2
         LR    R10,R13             LOAD SAVE AREA ADDR
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L
ESTAEBYP MVC   ESTAELST(16),ESTAEOVL
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         ST    R14,SAVRET          PUT RETURN ADDRESS IN UNUSED SLOT
         ST    R2,XSASAVE          SAVE XSA ADDR
         MVI   FOUND,X'00'
         MVC   MAJLINE(4),=X'00360100'
         MVC   MINLINE(4),=X'00360100'
         MVC   JOBLINE(4),=X'00360100'
         MVC   MAJLINE+4(8),=CL8'MAJOR='
         MVC   MINLINE+4(8),=CL8'MINOR='
         MVC   MAJOR(44),=CL44' '
         MVC   MINOR(44),=CL44' '
         XC    TOKEN,TOKEN        SETUP FOR 1ST GQSCAN
         MVC   GQSCANL(GQSCANE-GQSCANP),GQSCANP SET UP LIST FORM
         SPACE
NXTGQ    GQSCAN AREA=(GQAREA,L'GQAREA),TOKEN=TOKEN,MF=(E,GQSCANL),     *
               SCOPE=ALL,WAITCNT=1
         ST    R15,GQRET          KEEP RC
         CH    R15,=H'8'
         BH    BADRET             GRS ERROR OR BAD SYSNAME
         CH    R15,=H'4'
         BE    NOTFOUND           NONE FOUND
         SPACE
         USING RIB,R3
         USING RIBE,R4
         USING RIBVAR,R15
         LR    R7,R0              L'RIB, L'RIBE
         N     R7,=X'0000FFFF'    L'RIBE
         LR    R5,R1              NUMBER OF RIBS
         LA    R3,GQAREA          A(1ST RIB)
         SPACE 1
GETMAJ   MVC   MAJOR(8),RIBQNAME  SAVE MAJOR QCB
         LA    R15,RIBEND         1ST MINOR
         SPACE 1
         CLC   =C'SYS',RIBRNAME   SYSTEM NAME
         BNE   CHKQEL             NO, CONTINUE
         CLC   =C'.T',RIBRNAME+8  SYSTEM NAME
         BNE   CHKQEL             NO, CONTINUE
         CLI   RIBRNAME+16,C'.'   SYSTEM NAME
         BNE   CHKQEL             NO, CONTINUE
         MVC   NUMCHK(6),=6C'0'   SET NUMERIC FIELD
         MVZ   NUMCHK(5),RIBRNAME+3  MOVE NUMERIC ZONES
         CLC   NUMCHK(6),=6C'0'   STILL NUMERIC
         BNE   CHKQEL             NO, NOT SYSTEM NAME
         MVZ   NUMCHK(6),RIBRNAME+10  MOVE NUMERIC ZONES
         CLC   NUMCHK(6),=6C'0'   STILL NUMERIC
         BNE   CHKQEL             NO, NOT SYSTEM NAME
         SPACE 1
         B     NEXTMAJ
         EJECT
MINMOVE  MVC   MINOR(0),RIBRNAME
         SPACE 3
CHKQEL   SR    R1,R1
         IC    R1,RIBRNMLN        MINOR NAME LENGTH
         BCTR  R1,0
         CH    R1,=H'43'          MAX
         BNH   *+8
         LH    R1,=H'43'          FORCE MAX
         MVC   MINOR(44),=CL44' '
         EX    R1,MINMOVE         MOVE MINOR NAME
         CLI   MINOR,C'9'         VALID CHAR
         BH    CHKQEL1
         CLI   MINOR,C'A'         VALID CHAR
         BNL   CHKQEL2
         SPACE 1
CHKQEL1  MVC   MINOR(44),=CL44'*---UNKNOWN---*'
         SPACE 1
CHKQEL2  LA    R4,RIBVAR          START OF VARIABLE PORTIOON
         AH    R4,RIBVLEN         A(1ST RIBE)
         L     R6,RIBNRIBE        NUMBER OF RIBE'S RETURNED
         LTR   R6,R6
         BZ    NEXTMAJ            IF NONE GET NEXT RIB
         SPACE 1
*        WRITE 1ST 2 LINES
         LA    R1,MAJLINE
         SR    R0,R0
         IC    R0,XAU
         SVC   35                 WRITE MAJOR LINE
         SPACE
         LA    R1,MINLINE
         SR    R0,R0
         IC    R0,XAU
         SVC   35                 WRITE MINOR LINE
         MVI   FOUND,X'FF'        INDICATE OUTPUT
         SPACE 3
NEXTRIBE MVC   JOBNAME+8(44),=CL44' '
         MVC   JOBNAME(8),RIBEJBNM JOBNAME
         SPACE 1
         MVC   EXCLSHR(4),=CL4'SHR'
         TM    RIBERFLG,RIBETYPE  SHARED
         BO    ISSHR              YES
         MVC   EXCLSHR(4),=CL4'EXCL'
         SPACE 1
ISSHR    MVC   RESERVE(8),=CL8' '
         TM    RIBERFLG,RIBERESV  RESERVED
         BNO   CHKQEL9            NO
         MVC   RESERVE(8),=CL8'RESERVED'
         SPACE 1
CHKQEL9  MVI   OWAT,C'W'          WAITING
         TM    RIBESFLG,RIBESTAT  IS IT WAITING
         BNO   CHKQELA
         MVI   OWAT,C'O'          OWNER
CHKQELA  LA    R1,JOBLINE
         SR    R0,R0
         IC    R0,XAU
         SVC   35                 IDENTIFY JOB
         SPACE
*        KEEP GOING TILL ALL RIBE INFO PRINTED
         AR    R4,R7              +LENGTH OF RIBE
         BCT   R6,NEXTRIBE        REPEAT FOR ALL RIBE'S
         SPACE 3
NEXTMAJ  LR    R3,R4              NEXT RIB
         BCT   R5,GETMAJ          REPEAT FOR ALL IN GQAREA
         SPACE
         CLC   GQRET,=F'8'        IF THERE ARE MORE
         BE    NXTGQ              GO ISSUE GQSCAN AGAIN
         SPACE 3
NOTFOUND CLI   FOUND,X'00'        IF SOME MESSAGES WERE ISSUED
         BNE   EXIT               THEN GET OUT
         LA    R1,NFNDMSG         ELSE NO ENQ CONFLICTS WERE FOUND
         SR    R0,R0
         IC    R0,XAU
         SVC   35                 'NO CONFLICTS'
         SPACE
EXIT     L     R0,SIZE
         L     R14,SAVRET
         LR    R1,R13
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         SPACE 3
BADRET   ABEND 1                  LET STAE RTN ISSUE MESSAGE
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
         L     R2,=A(STAERTRY)     LOAD RETRY ADDR
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(IEESQ03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         B     EXIT
         EJECT
         LTORG
         SPACE 3
GQSCANP  GQSCAN MF=L              TO INIT LIST FORM
GQSCANE  EQU   *                  TO GET LENGTH OF LIST FORM
         SPACE 3
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORK)
NFNDMSG  DC    X'00300100'
         DC    CL48'NO ENQ CONFLICTS EXIST'
         SPACE 3
STAEMSG  DC    X'00360100'
         DC    CL50'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         SPACE 3
WORK     DSECT
         DS    18F                REQD FOR GQSCAN
TOKEN    DS    F                  GQSCAN TOKEN
GQSCANL  GQSCAN MF=L
GQRET    DS    F                  GQSCAN RETURN CODE
SAVRET   DS    F
XSASAVE  DS    F
FOUND    DS    X
NUMCHK   DS    CL6
         SPACE 1
         DS    0F
MAJLINE  DS    0CL56
         DS    CL12
MAJOR    DS    CL44
         SPACE 1
         DS    0F
MINLINE  DS    0CL56
         DS    CL12
MINOR    DS    CL44
         SPACE 1
         DS    0F
JOBLINE  DS    0CL56
         DS    CL4
JOBNAME  DS    CL8
         DS    CL4
EXCLSHR  DS    CL4
         DS    CL2
OWAT     DS    C
         DS    CL2
RESERVE  DS    CL8
         DS    CL24
         SPACE 3
ESTAELST DS    4F
GQAREA   DS    XL1024             GQSCAN RETURN AREA
WORKEND  DS    0D
         EJECT
DSXSA    DSECT
XSA      DS    0D
XAP      DS    F                  PTR TO XCTL NAME
XAD      DS    F                  DCB PTR
XAX      DS    D                  XCTL NAME
XAE      DS    0C                 ERROR CODE
XAR      DS    F                  PTR TO PARM LIST (REG1)
XAN      DS    0C                 VERB INDEX
XAL      DS    F                  PTR TO LIST POSITION
XAV      DS    D                  VERB
XAS      DS    D                  TEMP SAVE-AREA
XAU      DS    C                  UCM ENTRY INDICATOR
         DS    C                  RESERVED
XAJ      DS    H                  TJID ENTRY INDICATOR
         DS    F                  RESERVED
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
         ISGRIB
         END
