 ***********************************************************************
 ***********************************************************************
                      RMF EXIT TO CANCEL JOBS WHICH EXCEED RESOURCE LIMI
                      **************************************************
            THE RMF EXIT PROCESSING HAS BEEN INVOKED TO MONITOR THE ASCB
          AND SEARCH FOR ADDRESS SPACES IN DOMAINS 120-128. IF AN ADDRES
          IS FOUND IN DOMAIN 120-128 THE RECOVERY/TERMINATION MANAGER IS
          TO CANCEL THE ADDRESS SPACE.

          THE FUNCTION CAN BE ENABLED BY THE FOLLOWING COMMAND

            MODIFY RMF.IDENT,F ZZ,EXITS


          THE FUNCTION CAN BE DISABLED BY THE FOLLOWING COMMAND

            MODIFY RMF.IDENT,F ZZ,NOEXITS

            NB.  THE FUNCTION MAY TAKE UP TO 1 MINUTE TO TERMINATE.



          THE FOLLOWING COMMAND WILL DISPLAY WHETHER THE FUNCTION IS CUR
          ENABLED OR DISABLED

            MODIFY RMF.IDENT,D ALL

          THE RESULTING DISPLAY SHOWS WHETHER EXITS OR NOEXITS PROCESSIN
          BEING DONE.

         THE MODIFICATION IS AS FOLLOWS
         ERBMFIUC.    THIS IS THE RMF INITIALISATION EXIT. IT'S FUNCTION
                      ATTACH THE PROCESSING TASK ERBMFTSK. THE ADDRESS O
                      ECB FOR ERBMFTSK IS STORED IN THE FIRST USER FULLW
                      USE BY THE TERMINATION EXIT. ERBMFIUC IS CALLED AT
                      START OF EXIT PROCESSING TO ESTABLISH THE EXIT ENV
                      THE 2ND FULLWORD IS THE ADDRESS OF THE SAVEAREA AN
                      PASSED TO ERBMFTUR.

         ERBMFTUR.    THIS IS THE RMF TERMINATION EXIT. IT'S FUNCTION IS
                      END ERBMFTSK AT THE END OF EXIT PROCESSING. THE AD
                      OF THE ECB FOR ERBMFTSK IS PASSED BY ERBMFIUC IN T
                      USER FULLWORD.

         ERBMFTSK.    THIS IS THE PROCESSING TASK WHICH SCANS THE ASCB C
                      LOOKING FOR JOBS IN DOMAINS 120-128. IF A JOB IS F
                      IT IS CANCELLED VIA THE SVC 34 INTERFACE.
                      THIS TASK RUNS IN SUPERVISOR STATE AND
                      KEY ZERO. EXTREME CARE SHOULD THEREFORE BE EXERCIS
                      THIS TASK IS TO BE MODIFIED.

         REFERENCE MANUAL:  OS/VS2 MVS  RESOURCE MEASUREMENT FACILITY (R
                            REFERENCE AND USER'S GUIDE   SC28-0922

//SM0004A   JOB   OSOT,SAWARD,MSGCLASS=X,NOTIFY=SM0004,CLASS=V
//JOBCAT    DD    DSN=USERCAT.TSOPK2,DISP=SHR
//ASMTSK    EXEC  PGM=IEV90,PARM='OBJECT,NODECK'
//SYSPRINT  DD    SYSOUT=X
//SYSLIB    DD    DSN=PCS.TEST.MACRO.MACLIB,DISP=SHR,DCB=BLKSIZE=12960
//          DD    DSN=SYS1.AMODGEN,DISP=SHR
//          DD    DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1    DD    DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100))
//SYSUT2    DD    DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3    DD    DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSLIN    DD    DSN=&&OBJTSK,UNIT=SYSDA,SPACE=(80,(200,50)),
//          DISP=(MOD,PASS)
//SYSPUNCH  DD    DUMMY
//SYSIN     DD    *
ERBMFTSK TITLE 'RMF TIMER TASK            B SAWARD'
***********************************************************************
**                                                                   **
**   THIS PROGRAM IS A TASK ATTACHED BY ERBMFIUC.                    **
**   IT'S FUNCTION IS TO PERIODICALLY SCAN THE ASCB CHAIN AND        **
**   IF IT FINDS AN ASCB WITH A DOMAIN GT 120 THE ASCB WILL          **
**   BE TERMINATED.                                                  **
***********************************************************************
          EJECT
***********************************************************************
**********      INITIALISATION                             ************
***********************************************************************
         SPACE 2
ERBMFTSK CSECT
         USING PSA,R0
         USING CVTMAP,R3
         USING ASVT,R4
         USING ASXB,R5
         USING OUCB,R6
         USING ASCB,R7
         USING *,R12
         B     24(R15)
         DC    CL8'ERBMFTSK'
         DC    CL4'&SYSTIME'
         DC    CL8'&SYSDATE'
         STM   R14,R12,8(R13)
         LR    R12,R15             SET BASE REG
         LR    R9,R4               PICK UP THE ECB POINTER
         ST    R13,SVR13
         LA    R13,SAVEAREA        SET SAVEAREA POINTER FOR CALLRTM
         B     START
         EJECT
***********************************************************************
**********      CONSTANTS                                  ************
***********************************************************************
         SPACE 2
         REGEQU
WAITTIME DC    F'6000'
SAVEAREA DC    9D'0'
SVR13    DC    F'0'
MSG      DS    0D
MSGL     DC    X'00360000'
MSGD     DC    CL50'JOB XXXXXXXX FOUND IN DOMAIN XXX  BEING CANCELLED'
CANCEL   DC    Y(20),Y(0),CL16'CANCEL XXXXXXXX'
OLDKEY   DC    C' '
         EJECT
***********************************************************************
**********      EXTERNAL STORAGE DESCRIPTIONS              ************
***********************************************************************
         SPACE 2
PARMSECT DSECT
ECB1     DS    F
ECBIDENT DS    CL4
         EJECT
***********************************************************************
**********       INITIALISATION                             ***********
***********************************************************************
ERBMFTSK CSECT
START    DS    0H
         MODESET KEY=ZERO,MODE=SUP
         POST  (9)                 TELL ERBMFIUC WE HAVE CONTROL
         USING PARMSECT,R9         ADDRESS THE EXTERNAL AREA
LOOP     DS    0H
         CLC   ECBIDENT,=C'END.'   IS END REQUIRED ?
         BE    END                 YES
         EJECT
***********************************************************************
**********       SCAN THE ASCB CHAIN FOR DOMAIN 120-128     ***********
***********************************************************************
         SPACE 3
ASCBSCAN DS    0H
         L     R3,FLCCVT           PICK UP CVT ADDRESS
         L     R4,CVTASVT          ADDRESS THE ADDRESS SPACE VECTOR
         LA    R4,ASVTENTY-4       ADDRESS THE FIRST ASCB-4
ASCBNEXT DS    0H
         LA    R4,4(R4)            BUMP TO NEXT ASVT ENTRY
         L     R7,0(R4)            LOAD ADDRESS OF ASCB
         LA    R7,0(R7)            ADDRESS ONLY PLS
         LTR   R7,R7               ANY ADDRESS ?
         BZ    WAIT                NO   ALL DONE
         TM    0(R4),X'80'         IS ASCB AVAILABLE ?
         BO    ASCBNEXT            NO
         L     R6,ASCBOUCB         PICK UP THE SRM BLOCK ADDRESS
         CLI   OUCBDMN,X'78'       IS DOMAIN LESS THAN 120
         BL    ASCBNEXT            YES
ASCBKILL DS    0H
         L     R2,ASCBJBNI         PICK UP THE JOBNAME POINTER
         LTR   R2,R2               ANY GOODIES ?
         BNZ   KILLA1
         L     R2,ASCBJBNS         TRY THIS
KILLA1   DS    0H
         MVC   MSGD+4(8),0(R2)     PICK UP JOBNAME FOR MESSAGE
         MVC   CANCEL+11(8),0(R2)  JOBNAME TO CANCEL COMMAND
         SR    R2,R2               CLEAR R2
         IC    R2,OUCBDMN          PICK UP THE DOMAIN
         CVD   R2,SAVEAREA
         UNPK  MSGD+29(3),SAVEAREA+6(2)
         OI    MSGD+31,C'0'
         LA    R1,MSGL
         SVC   35                  WTO
         SR    R0,R0               SET FOR SVC 34
         LA    R1,CANCEL
         SVC   34                  ISSUE CANCEL COMMAND
         B     ASCBNEXT            YES
         EJECT
***********************************************************************
**********      SET TIMER FOR 1 MINUTE AND WAIT            ************
***********************************************************************
         SPACE 2
WAIT     DS    0H
         STIMER WAIT,BINTVL=WAITTIME
         B     LOOP
         EJECT
***********************************************************************
**********      FREE THE ECB AND EXIT                      ************
***********************************************************************
         SPACE 2
END      DS    0H
         POST  (9)                 TELL TERMINATOR WE ARE GOING
         L     R13,SVR13           RESTORE R13
         LM    R14,R12,8(R13)      RESTORE REGS
         SR    R15,R15             ZERO THE RETURN CODE
         BR    R14
         LTORG
         PRINT OFF
         IRAOUCB DSECT=YES
         IHAASCB DSECT=YES
         IHAASXB DSECT=YES
         IHAPSA  DSECT=YES
         CVT     DSECT=YES
         IHAASVT DSECT=YES
         END   ,
/*
//LKED     EXEC   PGM=IEWL,PARM=(REUS,REFR,LIST,MAP),
//         COND=(8,LT)
//OBJTSK   DD   DSN=&&OBJTSK,DISP=(OLD,DELETE)
//SYSLMOD  DD   DISP=SHR,DSN=SYS1.PLINKLIB,UNIT=3350,VOL=SER=SY1RES
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSPRINT DD   SYSOUT=X
//SYSLIN   DD  *
   INCLUDE OBJTSK
   SETCODE AC(1)
   ENTRY ERBMFTSK
   NAME ERBMFTSK(R)
/*
