         MACRO
&LABEL   QSORT &START=OMITTED,                                         *
               &LEN=1,                                                 *
               &LAST=OMITTED,                                          *
               &DISP=0,                                                *
               &SRTLEN=1,                                              *
               &ORD=AS
* COMMONWEALTH BANKING CORPORATION MACRO- QSORT -1.0
         AIF   ('&START' EQ 'OMITTED').ERR1
         AIF   ('&LAST' EQ 'OMITTED').ERR1
         AIF   (T'&START EQ 'N').QS001
         AIF   (T'&START NE 'U').ERR2
.QS001   ANOP
         AIF   (T'&LAST EQ 'N').QS004
         AIF   (T'&LAST NE 'U').ERR2
.QS004   ANOP
         AIF   (T'&LEN NE 'N').ERR2
         AIF   (&LEN GT 256).ERR2
         AIF   (T'&DISP NE 'N').ERR2
         AIF   (T'&SRTLEN NE 'N').ERR2
         AIF   (&SRTLEN LT 1).ERR2
         AIF   (&SRTLEN GT 256).ERR2
         AIF   (&DISP LT 0).ERR2
         AIF   ('&ORD' EQ 'AS').QS002
         AIF   ('&ORD' NE 'DS').ERR2
.QS002   ANOP
         AIF   (&SRTLEN+&DISP GT &LEN).ERR3
         AIF   (&DISP+&LEN GT 4095).ERR4
         AGO   .CODE
.ERR2    ANOP
         MNOTE 9,'INVALID OPERAND-MACRO GENERATION TERMINATED'
         MEXIT
.ERR1    ANOP
         MNOTE 9,'REQD PARAMETER MISSING-MACRO GENERATION TERMINATED'
         MEXIT
.ERR3    ANOP
         MNOTE 9,'DISP+SRTLEN IS GT REC LEN-MACRO GENERATION TERMINATEDX
               '
         MEXIT
.ERR4    ANOP
         MNOTE 9,'DISPLACEMENT+LENGTH GT ALLOWABLE MAXIMUM-MACRO GENERAX
               TION TERMINATED'
.CODE    ANOP
         ST    &START,SVRG&SYSNDX  SAVE HIS REG1
         ST    &LAST,SVRG&SYSNDX+8 SAVE HIS 2ND REG
         STM   QSR1&SYSNDX,QSR5&SYSNDX,SVRG&SYSNDX+12
         LM    QSR1&SYSNDX,QSR3&SYSNDX,SVRG&SYSNDX
         SR    QSR3&SYSNDX,QSR2&SYSNDX  ADJ END OF SORT PTR
         ST    QSR1&SYSNDX,QSV1&SYSNDX  SAVE SORT AREA POINTER
QSL1&SYSNDX DS    0H
         CR    QSR1&SYSNDX,QSR3&SYSNDX  ANYTHING TO SORT?
         BH    QSND&SYSNDX    NO-EXIT
QSL2&SYSNDX DS    0H
         CLC   &DISP.(&SRTLEN,QSR1&SYSNDX),&LEN+&DISP.(QSR1&SYSNDX)
         AIF   ('&ORD' EQ 'AS').QS005
         BNL   QSL3&SYSNDX
         AGO   .QS006
.QS005   ANOP
         BNH   QSL3&SYSNDX
.QS006   ANOP
         AIF   (&LEN LT 257).QS007
         LA    QSR4&SYSNDX,&LEN-255
         ST    QSR1&SYSNDX,QSV1&SYSNDX+4
         LA    QSR5&SYSNDX,255
QSL6&SYSNDX DS    0H
         EX    QSX1&SYSNDX,QSR5&SYSNDX
         EX    QSX2&SYSNDX,QSR5&SYSNDX
         EX    QSX1&SYSNDX,QSR5&SYSNDX
         LA    QSR1&SYSNDX,256(QSR1&SYSNDX)
         S     QSR4&SYSNDX,QSC1&SYSNDX
         BP    QSL6&SYSNDX
         LCR   QSR4&SYSNDX.QSR4&SYSNDX
         AGO   .QS008
.QS007   ANOP
         LA    QSR4&SYSNDX,&LEN-1
.QS008   ANOP
         EX    QSR4&SYSNDX,QSX1&SYSNDX
         EX    QSR4&SYSNDX,QSX2&SYSNDX
         EX    QSR4&SYSNDX,QSX1&SYSNDX
         AIF   (&LEN LT 257).QS009
         L     QSR1&SYSNDX,QSV1&SYSNDX+4
.QS009   ANOP
QSL3&SYSNDX DS    0H
         BXLE  QSR1&SYSNDX,QSR2&SYSNDX,QSL2&SYSNDX
         SR    QSR3&SYSNDX,QSR2&SYSNDX
         L     QSR1&SYSNDX,QSV1&SYSNDX  RELOAD START PTR
         B     QSL1&SYSNDX    LOOP THRU
QSR1&SYSNDX EQU   1
QSR2&SYSNDX EQU   2
QSR3&SYSNDX EQU   3
QSR4&SYSNDX EQU   4
QSR5&SYSNDX EQU   5
SVRG&SYSNDX DC    F'0'
         DC    A(&LEN)
         DC    F'0'
         DC    5F'0'
QSV1&SYSNDX DC    2F'0'
QSX1&SYSNDX XC    0(0,QSR1&SYSNDX),&LEN.(QSR1&SYSNDX)
QSX2&SYSNDX XC    &LEN.(0,QSR1&SYSNDX),0(QSR1&SYSNDX)
QSND&SYSNDX DS    0H
         LM    QSR1&SYSNDX,QSR5&SYSNDX,SVRG&SYSNDX+12  RESTORE REGS
         MEND
