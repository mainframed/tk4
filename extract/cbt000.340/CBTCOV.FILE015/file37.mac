         MACRO
&NAME    PMSTA &BASE=R12,&GENSUB=NO,&LBLTYP=80,&ID=YES,                -
               &PROGRAM=,&SAVEA=YES,&SYSTEM=OS
.*             THE FOLLOWING GLOBAL SYMBOLS ARE USED BY PM SERIES
.*              MACROS AS AT 01/02/78.
         GBLA  &PMID
.*             &PMID IS A GLOBAL IDENTIFICATION FIELD USED BY ALL
.*              PM SERIES MACROS. IT IS USED IN THE FORMAT PM&PMID AND
.*              IS INCREMENTED BY ONE FOR EACH NEW IDENTIFICATION
.*              GENERATED.
         GBLA  &PMENDID
.*             &PMENDID IS A GLOBAL IDENTIFIER OF THE PMEND GENERATED
.*              SAVE AREA.
         GBLA  &PMPARID
.*             &PMPARID IS A GLOBAL IDENTIFIER OF THE PMPAR GENERATED
.*              SAVE AREA.
         GBLA  &PMSTAID
.*             &PMSTAID IS A GLOBAL IDENTIFIER OF THE OCCURENCE OF
.*              PMSTA AND PMSTR.
         GBLA  &PMSTARE
.*             &PMSTARE IS A GLOBAL IDENTIFIER OF THE PMSTA GENERATED
.*              RETURN ADDRESS.
         GBLA  &PMSTASA
.*             &PMSTASA IS A GLOBAL IDENTIFIER OF THE PMSTA GENERATED
.*              SAVE AREA.
         GBLC  &PMSTASY
.*             &PMSTASY IS A GLOBAL IDENTIFIER OF THE TYPE OF OPERATING
.*              SYSTEM THAT WILL BE USED TO EXECUTE THE PROGRAM
.*              GENERATED BY THESE MACOS.
         LCLA  &A,&B,&C,&D,&E,&F,&G,&H,&I,&J,&K,&L,&M,&N
         LCLC  &O,&P,&Q,&R,&S,&T,&U,&V,&W,&X,&Y,&Z
         AIF   ('&GENSUB' EQ 'YES').ZGENSUB
&X       SETC  '&NAME'
         AIF   ('&X' NE '').NAMEOK
&PMID    SETA  &PMID+1
&X       SETC  'PM&PMID'
.NAMEOK  ANOP
&PMENDID SETA  0
&PMSTAID SETA  &PMSTAID+1
&PMSTARE SETA  &PMID+1
&PMSTASA SETA  &PMID+2
&PMSTASY SETC '&SYSTEM'
         AIF   ('&PMSTASY' NE 'DOS').SYSOK
         AIF   ('&PROGRAM' EQ '' AND &PMSTAID EQ 1).SYSOK
&PMSTASY SETC  'DOSSUB'
.SYSOK   ANOP
&PMID    SETA  &PMID+3
&Y       SETC  'PM&PMID'
&PMID    SETA  &PMID+1
         AIF   ('&PMSTASY' EQ 'OS').NOPUNCH
         AIF   (&PMSTAID NE 1).NOPUNCH
         PUNCH ' CATALR &NAME'
.NOPUNCH ANOP
&X       CSECT
         AIF   ('&BASE(1)' EQ '').BASED
         AIF   ('&PMSTASY' NE 'DOS').BASEB
         BALR  &BASE(1),0
         BCTR  &BASE(1),0
         BCTR  &BASE(1),0
         AGO   .BASEC
.BASEB   ANOP
         SAVE  (14,12)
         USING &X,R15
         AIF   ('&SAVEA' EQ 'NO').SAVEANA
         ST    R13,PM&PMSTASA+4
         LA    R14,PM&PMSTASA
         ST    R14,8(R13)
         L     R14,12(R13)
.SAVEANA AIF   ('&BASE(1)' EQ 'R15' OR 'BASE(1)' EQ '15').BASED
         LR    &BASE(1),R15
         DROP  R15
.BASEC   ANOP
         USING &X,&BASE(1)
.BASED   ANOP
         MNOTE 'R0 IS RESERVED FOR IBM.'
         MNOTE 'R1 IS RESERVED FOR THE ADDRESSES OF PARAMETER LISTS.'
         AIF   ('&BASE(1)' EQ '').EQU
         MNOTE '&BASE(1) IS THE BASE REGISTER.'
.BASEL   ANOP
&A       SETA  &A+1
         AIF   ('&BASE(&A+1)' EQ '').BASELE
         LA    &BASE(&A+1),2048(&BASE(&A))
         LA    &BASE(&A+1),2048(&BASE(&A+1))
         USING &X+X'&A.000',&BASE(&A+1)
         MNOTE '&BASE(&A+1) IS A BASE REGISTER AT &X+X''&A.000''.'
         AIF   (&A+1 LE N'&BASE).BASEL
.BASELE  AIF   ('&SAVEA' EQ 'NO').SAVEANB
         LA    R13,PM&PMSTASA
.SAVEANB B     &Y
         AIF   ('&PMSTASY' NE 'DOS').LBLNO
         AIF   ('&LBLTYP' EQ '').LBLNO
         ORG   &X+&LBLTYP
.LBLNO   ANOP
PM&PMSTARE DS  0H
         AIF   ('&PMSTASY' EQ 'DOS').DOSRET
         AIF   ('&SAVEA' EQ 'NO').SAVEANC
         L     R13,PM&PMSTASA+4
.SAVEANC RETURN (14,12)
.DOSRET  ANOP
         AIF   ('&PMSTASY' NE 'DOS').EQU
         EOJ
.EQU     ANOP
         AIF   (&PMSTAID NE 1).EQUNO
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.EQUNO   ANOP
         AIF   ('&ID' EQ 'NO').NAMENO
         DS    0D
         DC    CL8'&X'
.NAMENO  ANOP
         AIF   ('&BASE(1)' EQ '').NO1415
         AIF   ('&SAVEA' EQ 'NO').SAVEAND
PM&PMSTASA DS  9D
.SAVEAND ANOP
&Y       DS    0H
         MNOTE 'R13 CONTAINS THE ADDRESS OF THE SAVE AREA.'
         AIF   ('&BASE(1)' EQ 'R15' OR '&BASE(1)' EQ '15').NO1415
         MNOTE 'R14 IS THE LINK ADDRESS REGISTER.'
         MNOTE 'R15 IS THE CALL ADDRESS REGISTER.'
.NO1415  ANOP
         EJECT
         MEXIT
.ZGENSUB ANOP
         MEND
