 ***********************************************************************
 ***********************************************************************
                                         PEEK COMMAND



                    THIS COMMAND ALLOWS SPECIFIED USERS  TO PEEK AT
               SYS1.UADS TO EXAMINE TSO USERIDS AND CHANGE ANY OF THEIR
               ATTRIBUTES.  ANY OTHER USER TRYING TO USE THIS COMMAND GE
               MESSAGE 'IKJ56500I COMMAND PEEK NOT FOUND'.  THIS IS BUIL
               THE COMMAND BECAUSE SYS1.UADS IS DYNAMICALLY ALLOCATED.

                    FORMAT OF THE COMMAND IS -  PEEK  OR  PEEK USERID

               THIS WILL ALLOW YOU TO LOOK AT AND/OR CHANGE THE FOLLOWIN
                               PASSWORD
                               ACCOUNTING INFORMATION
                               LOGON PROCEDURE
                               MAXIMUM REGION SIZE
                               PCF LEVEL
                               SYSTEM ATTRIBUTES
                               DATE USER LAST LOGGED ON
                               DATE USER LAST CHANGED HIS PASSWORD
                               GENERIC UNIT NAME
                               USER'S NAME
                               DISTRIBUTION CODE
                               PHONE NUMBER
                               INDICATOR BYTE

               MULTIPLE FIELDS CAN BE CHANGED FOR ONE USER IN ONE USE OF
               COMMAND, AND MULTIPLE USERS CAN LIKEWISE BE CHANGED.
//SM0005A JOB   OSOT,SEABERG,
//         MSGCLASS=X,NOTIFY=SM0005,CLASS=V
//JOBCAT  DD   DSN=USERCAT.TSOPK1,DISP=SHR
//STEP1  EXEC  ASMLKED,MOD='SM.TEST.LINKLIB'
//ASM.SYSPRINT DD SYSOUT=X
//ASM.SYSIN    DD *
PEEK     TITLE 'PEEK AT TSO USERS           G SEABERG'
         EJECT
***********************************************************************
**********      INITIALISATION                             ************
***********************************************************************
         SPACE 2
PEEK     CSECT
         USING *,R12
         USING *+4096,R11
         USING *+8192,R10
         STM   R14,R12,8(R13)
         LR    R12,R15
         LA    R11,4095(R12)
         LA    R11,1(R11)
         LA    R10,4095(R11)
         LA    R10,1(R10)
         ST    R13,SV13
         LA    R13,SVREGS
         B     START
         EJECT
***********************************************************************
**********      CONSTANTS & STORAGE                        ************
***********************************************************************
         SPACE 2
         REGEQU
SVREGS   DS    9D
SV13     DS    F
SFR7     DS    F
SFR9     DS    F
RETCODE  DC    F'0'
USNO     DC    PL2'0'
WORK     DS    D
WORK1    DS    D
WORK2    DS    D
ALLBUF   DC    CL70' '
SIZEHALF DS    H
PASSWORD DS    CL8
WR1      DC    C' 1. PASSWORD              2. JOB ACCOUNTING INFO.   3.*
                LOGON PROCEDURE'
WR2      DC    C' 4. MAXIMUM REGION SIZE   5. PCF LEVEL              6.*
                SYSTEM ATTRIBUTES'
WR3      DC    C' 7. DATE LAST LOGGED ON   8. DATE PWD LAST CHANGED  9.*
                GENERIC UNIT NAME'
WR4      DC    C'10. USER''S NAME          11. DISTRIBUTION CODE     12*
               . PHONE NUMBER     '
WR5      DC    C'13. INDICATOR BYTE       14. ALL OF THE ABOVE'
ATTRIBDA DC    CL30' '
BUDBUF   DC    CL80' '
UNITNAME DS    CL8
NOM      DS    CL8
CODE     DS    CL8
NUMBER   DS    CL8
SWALL    DC    X'00'
SWCHG    DC    X'00'
BUFAD    DS    F
RBPTR    DS    0F                 POINTER TO REQUEST BLOCK
         DC    X'80'
         DC    AL3(RB)
*
RB       DC    X'14012000'        LENGTH,VERB,FLAGS
RBERR    DC    XL2'0'             ERROR REASON CODE
RBINFO   DC    XL2'0'             INFORMATION REASON CODE
RBTXT    DC    A(TEXT),2F'0'      ADDRESS OF TXT UNITS,RES AND FLAGS
*
TEXT     DC    A(TDDNAME)           POINTER TO DSNAME TEXT
         DC    A(TDSNAME)           POINTER TO DSNAME TEXT
         DC    A(TMEMBER)           POINTER TO MEMBER TEXT
         DC    A(TSTATUS)           POINTER TO STATUS TEXT
         DC    A(TDISP)             POINTER TO DISP TEXT
         DC    A(TUNALLOC)          POINTER TO UNALLOC TEXT
         DC    X'80',AL3(TPWD)      POINTER TO PWD TEXT
TDDNAME  DC    AL2(1),AL2(1),AL2(L'DDNAME) DDNAME
DDNAME   DC    C'INFILE'
TMEMBER  DC    AL2(3),AL2(1),AL2(L'MEMBER) MEMBER
MEMBER   DC    CL7'TSO0010'
TDSNAME  DC    AL2(2),AL2(1),AL2(L'DSNAME) DSNAME
DSNAME   DC    C'SYS1.UADS'
TSTATUS  DC    AL2(4),AL2(1),AL2(1),AL1(8) STATUS SHR
TDISP    DC    AL2(5),AL2(1),AL2(1),AL1(8) DISP KEEP
TUNALLOC DC    AL2(28),AL2(0)              UNALLOC AT CLOSE
TPWD     DC    AL2(80),AL2(1),AL2(L'PWD)   PASSWORD
PWD      DC    C'XXXX'
INFILE   DCB   DDNAME=INFILE,EODAD=ERROR1,MACRF=(GL,PL),               *
               RECFM=F,LRECL=800,BLKSIZE=800,DSORG=PS,BUFNO=1
TBCHAR   DC    C'0123456789ABCDEF'
TBPCF    DC    X'0A0B0C0D0E0F0000000000000000000000000000000000'
         DC    X'0000000000000000000000000000000000000000000000'
         DC    X'0000010203040506070809'
CLEARBUF DC    X'40114040'            WCC + SBA
         DC    X'1DC8'                HI INTENSITY
         DC    C'ENTER PASSWORD ===>'
         DC    X'1D4C'                NON DISPLAY
         DC    X'13'                  IC
         DC    C'        '            INPUT AREA
         DC    X'1DC8'                HI INTENSITY
         DC    X'3C5D7F40'            RTA BLANKS
ENDBUF   EQU   *
CCBLANK  DC    CL4' '
INPUT    DC    CL200' '
ANSWER   DC    CL200' '
START    DS    0H
         L     R7,8(R1)           PICK UP PSCB
         L     R1,0(R1)            PICK UP ANY PARAMETERS
         LH    R2,0(R1)            GET LENGTH
         LA    R3,2(R1)            ADDRESS PARAMETERS
         LH    R4,2(R1)            ADDRESS PARAMETERS
         CLC   =C'PEEK',4(R1)      IS IT TSO COMMAND
         BNE   CALL                NO - MUST BE CALL
         CLC   0(2,R7),=C'SM'     IS IT US
         BE    CONT                YES
         CLC   0(6,R7),=C'BACKUP' IS IT US
         BE    CONT                YES
         WRTERM 'IKJ56500I COMMAND PEEK NOT FOUND'
         B     END
CONT     DS    0H
         CH    R4,=H'4'            IS THERE A PARAMETER
         BNE   NUP                 NO
         WRTERM 'WHO AT?'
         RDTERM ANSWER,LENGTH=6
         LA    R3,ANSWER
         B     DYN
NUP      DS    0H
         SR    R2,R4               REDUCE LGTH BY OFFSET
         SH    R2,=H'4'            REDUCE LGTH BY COMMAND NAME
         CH    R2,=H'6'            IS USERID LGTH OKAY
         BNE   PARMERR             NO - NO SUCH USER
         LA    R3,2(R3)            START OF PARMS
         AR    R3,R4               ADDRESS USERID
         B     DYN
CALL     DS    0H
         CH    R2,=H'6'            IS USERID LGTH OKAY
         BNE   PARMERR             NO - NO SUCH USER
DYN      DS    0H
         LA    R0,ENDBUF-CLEARBUF  LENGTH
         LA    R1,CLEARBUF         PASSWORD PROMPT
         TPUT  (1),(0),FULLSCR
         RDTERM INPUT,LENGTH=4
         OC    INPUT(4),CCBLANK
         MVC   PWD,INPUT
USER     DS    0H
         OC    0(6,R3),=CL6' '     SET USERID TO UPPER CASE
         MVC   MEMBER(6),0(R3)     SHIFT IN FOR DYNALLOC
*
         LA    R1,RBPTR
         SVC   99                  DYNAMICALLY ALLOCATE
         LTR   R15,R15             WAS ALLOC OK ?
         BNZ   PARMERR             NO - INVALID USERID
         OPEN  (INFILE,(UPDAT))
         GET   INFILE
         ST    R1,BUFAD            SAVE THE INPUT BUFFER ADDRESS
         LA    R2,BUFFER           ADDRESS BUFFER
         L     R3,=F'800'
         LR    R4,R1
         LR    R5,R3
         MVCL  R2,R4               MOVE THE DATA INTO OUR BUFFER
         MVI   ANSWER,X'40'
         MVC   ANSWER+1(79),ANSWER
         CLEAR
         WRTERM '                     ENTER NUMBER OF ITEM REQUIRED    *
               '
ASK      DS    0H
         WRTERM ' '
         WRTERM WR1,L'WR1
         WRTERM WR2,L'WR2
         WRTERM WR3,L'WR3
         WRTERM WR4,L'WR4
         WRTERM WR5,L'WR5
         WRTERM ' '
         RDTERM ANSWER,LENGTH=2
         WRTERM ' '
         LA    R7,BUFFER
         L     R8,24(R7)
         LA    R9,0(R8,R7)
         ST    R7,SFR7
         ST    R9,SFR9
         OC    ANSWER(80),=CL80' '
         CLI   ANSWER+1,X'40'
         BNE   TEN
         CLC   =C'1',ANSWER
         BE    PASSWD
         CLC   =C'2',ANSWER
         BE    ACCT
         CLC   =C'3',ANSWER
         BE    PROC
         CLC   =C'4',ANSWER
         BE    SIZE
         CLC   =C'5',ANSWER
         BE    PCF
         CLC   =C'6',ANSWER
         BE    ATTRIB
         CLC   =C'7',ANSWER
         BE    ACCT
         CLC   =C'8',ANSWER
         BE    ACCT
         CLC   =C'9',ANSWER
         BE    UNIT
         B     ASKERR
TEN      DS    0H
         CLC   ANSWER+2(1),=X'40'
         BNE   ASKERR
         CLC   =C'10',ANSWER
         BE    ACCT
         CLC   =C'11',ANSWER
         BE    ACCT
         CLC   =C'12',ANSWER
         BE    ACCT
         CLC   =C'13',ANSWER
         BE    ACCT
         CLC   =C'14',ANSWER
         BNE   ASKERR
         MVI   SWALL,X'FF'
         B     PASSWD
ASKERR   DS    0H
         WRTERM 'NO SUCH NUMBER'
         WRTERM ' '
         B     ASK
PASSWD   DS    0H
         L     R7,SFR7
         L     R9,SFR9
         L     R9,8(R9)
         LA    R6,0(R9,R7)
         MVC   PASSWORD,4(R6)
         CLI   SWALL,X'FF'
         BNE   PASSWD1
         MVC   ALLBUF(8),WR1+4
         MVC   ALLBUF+30(8),PASSWORD
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ACCT
PASSWD1  DS    0H
         WRTERM PASSWORD,8
         BAL   R7,UPDATE
         MVC   4(8,R6),ANSWER
         MVC   ALLBUF(18),=C'NEW PASSWORD IS   '
         MVC   ALLBUF+18(8),ANSWER
         WRTERM ' '
         WRTERM ALLBUF,26
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
ACCT     DS    0H
         L     R7,SFR7
         L     R9,SFR9
         L     R8,4(R9)
         LA    R9,0(R8,R7)
         CLC   8(4,R9),=X'00000000'
         BNE   ACCTCONT
         WRTERM 'ERROR IN ACCOUNTING FIELD IN UADS - CHECK'
         B     CLOSE
ACCTCONT DS    0H
         L     R8,8(R9)
         LA    R6,45(R8,R7)
         CLI   SWALL,X'FF'
         BE    ACCT1
         CLC   =C'2',ANSWER
         BE    ACCT1
         CLC   =C'7',ANSWER
         BE    LOGDATE
         CLC   =C'8',ANSWER
         BE    PWDDATE
         CLC   =C'10',ANSWER
         BE    NAME
         CLC   =C'11',ANSWER
         BE    DIST
         CLC   =C'12',ANSWER
         BE    PHONE
         B     INDIC
ACCT1    DS    0H
         MVC   WORK(4),0(R6)
         CLI   SWALL,X'FF'
         BNE   ACCT2
         MVC   ALLBUF(20),WR1+29
         MVC   ALLBUF+30(4),WORK
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     LOGDATE
ACCT2    DS    0H
         WRTERM WORK,4
ACCTBAL  BAL   R7,UPDATE
         CLI   ANSWER+4,X'40'
         BNE   ACCTERR
         LA    R2,4
         LA    R3,ANSWER
ACCTCHK  DS    0H
         CLI   0(R3),C'A'
         BL    ACCTERR
         CLI   0(R3),C'Z'
         BH    ACCTERR
         LA    R3,1(R3)
         BCT   R2,ACCTCHK
         MVC   0(4,R6),ANSWER
         MVC   ALLBUF(25),=C'NEW ACCOUNTING CODE IS   '
         MVC   ALLBUF+25(4),ANSWER
         WRTERM ' '
         WRTERM ALLBUF,29
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
ACCTERR  DS    0H
         WRTERM ' '
         WRTERM 'INVALID JOB ACCOUNTING INFORMATION'
         B     ACCTBAL
LOGDATE  DS    0H
         LA    R5,4(R6)
         CLC   0(4,R5),=C'DATE'
         BNE   LOGNEXT
         WRTERM 'THIS USER HAS NOT LOGGED ON SINCE IT WAS CREATED'
         CLI   SWALL,X'FF'
         BE    PWDDATE
         B     ENDATA
LOGNEXT  DS    0H
         UNPK  WORK(7),0(4,R5)
         CLI   SWALL,X'FF'
         BNE   LOG2
         MVC   ALLBUF(19),WR3+4
         MVC   ALLBUF+30(5),WORK+2
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     PWDDATE
LOG2     DS    0H
         WRTERM WORK+2,5
LOGBAL   DS    0H
         BAL   R7,UPDATE
         CLI   ANSWER+5,X'40'
         BNE   LOGERR
         LA    R2,5
         LA    R3,ANSWER
LOGCHK   DS    0H
         CLI   0(R3),X'F0'
         BL    LOGERR
         CLI   0(R3),X'F9'
         BH    LOGERR
         LA    R3,1(R3)
         BCT   R2,LOGCHK
         PACK  WORK(4),ANSWER(5)
         MVC   0(4,R5),WORK
         MVC   ALLBUF(29),=C'NEW DATE LAST LOGGED ON IS   '
         MVC   ALLBUF+29(5),ANSWER
         WRTERM ' '
         WRTERM ALLBUF,34
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
LOGERR   DS    0H
         WRTERM ' '
         WRTERM 'INVALID DATE'
         B     LOGBAL
PWDDATE  DS    0H
         LA    R5,8(R6)
         CLC   0(4,R5),=C'PASS'
         BNE   DPWDNEX
         WRTERM 'THIS USER HAS NOT CHANGED PASSWORD SINCE IT WAS CREATE*
               D'
         CLI   SWALL,X'FF'
         BE    NAME
         B     ENDATA
DPWDNEX  DS    0H
         UNPK  WORK(7),0(4,R5)
         CLI   SWALL,X'FF'
         BNE   DPWDNEX2
         MVC   ALLBUF(21),WR3+29
         MVC   ALLBUF+30(5),WORK+2
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     NAME
DPWDNEX2 DS    0H
         WRTERM WORK+2,5
DPWDBAL  DS    0H
         BAL   R7,UPDATE
         CLI   ANSWER+5,X'40'
         BNE   DPWDERR
         LA    R2,5
         LA    R3,ANSWER
DPWDCHK  DS    0H
         CLI   0(R3),X'F0'
         BL    DPWDERR
         CLI   0(R3),X'F9'
         BH    DPWDERR
         LA    R3,1(R3)
         BCT   R2,DPWDCHK
         PACK  WORK(4),ANSWER(5)
         MVC   0(4,R5),WORK
         MVC   ALLBUF(36),=C'NEW DATE PASSWORD LAST CHANGED IS   '
         MVC   ALLBUF+36(5),ANSWER
         WRTERM ' '
         WRTERM ALLBUF,41
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
DPWDERR  DS    0H
         WRTERM ' '
         WRTERM 'INVALID DATE'
         B     DPWDBAL
NAME     DS    0H
         LA    R5,12(R6)
         MVC   NOM,0(R5)
         LA    R2,NOM
         LA    R3,8
NAM1     DS    0H
         CLI   0(R2),C':'
         BNE   NAM2
         MVI   0(R2),X'40'
NAM2     DS    0H
         LA    R2,1(R2)
         BCT   R3,NAM1
         CLI   SWALL,X'FF'
         BNE   NAM3
         MVC   ALLBUF(11),WR4+4
         MVC   ALLBUF+30(8),NOM
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     DIST
NAM3     DS    0H
         WRTERM NOM,8
NAM4     DS    0H
         BAL   R7,UPDATE
         CLI   ANSWER+8,X'40'
         BNE   NAMERR
         MVC   NOM,ANSWER
         LA    R2,ANSWER
         LA    R3,8
NAM5     DS    0H
         CLI   0(R2),X'40'
         BNE   NAM6
         MVI   0(R2),C':'
NAM6     DS    0H
         LA    R2,1(R2)
         BCT   R3,NAM5
         MVC   0(8,R5),ANSWER
         MVC   ALLBUF(14),=C'NEW NAME IS   '
         MVC   ALLBUF+14(8),NOM
         WRTERM ' '
         WRTERM ALLBUF,22
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
NAMERR   DS    0H
         WRTERM ' '
         WRTERM 'MAXIMUM EIGHT CHARACTERS FOR NAME'
         B     NAM4
DIST     DS    0H
         LA    R5,20(R6)
         MVC   CODE,0(R5)
         LA    R2,CODE
         LA    R3,8
DIS1     DS    0H
         CLI   0(R2),C':'
         BNE   DIS2
         MVI   0(R2),X'40'
DIS2     DS    0H
         LA    R2,1(R2)
         BCT   R3,DIS1
         CLI   SWALL,X'FF'
         BNE   DIS3
         MVC   ALLBUF(17),WR4+29
         MVC   ALLBUF+30(8),CODE
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     PHONE
DIS3     DS    0H
         WRTERM CODE,8
DIS4     DS    0H
         BAL   R7,UPDATE
         CLI   ANSWER+8,X'40'
         BNE   DISERR
         LA    R2,ANSWER
         MVC   NOM,ANSWER
         LA    R3,8
DIS5     DS    0H
         CLI   0(R2),X'40'
         BNE   DIS6
         MVI   0(R2),C':'
DIS6     DS    0H
         LA    R2,1(R2)
         BCT   R3,DIS5
         MVC   0(8,R5),ANSWER
         MVC   ALLBUF(27),=C'NEW DISTRIBUTION CODE IS   '
         MVC   ALLBUF+27(8),NOM
         WRTERM ' '
         WRTERM ALLBUF,35
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
DISERR   DS    0H
         WRTERM ' '
         WRTERM 'MAXIMUM EIGHT CHARACTERS FOR DISTRIBUTION CODE'
         B     NAM4
PHONE    DS    0H
         LA    R5,28(R6)
         MVC   NUMBER,0(R5)
         LA    R2,NUMBER
         LA    R3,8
PHO1     DS    0H
         CLI   0(R2),C':'
         BNE   PHO2
         MVI   0(R2),X'40'
PHO2     DS    0H
         LA    R2,1(R2)
         BCT   R3,PHO1
         CLI   SWALL,X'FF'
         BNE   PHO3
         MVC   ALLBUF(12),WR4+55
         MVC   ALLBUF+30(8),NUMBER
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     INDIC
PHO3     DS    0H
         WRTERM NUMBER,8
PHO4     DS    0H
         BAL   R7,UPDATE
         CLI   ANSWER+8,X'40'
         BNE   PHOERR
         MVC   NOM,ANSWER
         LA    R2,ANSWER
         LA    R3,8
PHO5     DS    0H
         CLI   0(R2),X'40'
         BNE   PHO6
         MVI   0(R2),C':'
PHO6     DS    0H
         LA    R2,1(R2)
         BCT   R3,PHO5
         MVC   0(8,R5),ANSWER
         MVC   ALLBUF(22),=C'NEW PHONE NUMBER IS   '
         MVC   ALLBUF+22(8),NOM
         WRTERM ' '
         WRTERM ALLBUF,30
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
PHOERR   DS    0H
         WRTERM ' '
         WRTERM 'MAXIMUM EIGHT CHARACTERS FOR PHONE NUMBER'
         B     PHO4
INDIC    DS    0H
         LA    R5,36(R6)
         CLI   SWALL,X'FF'
         BNE   IND1
         MVC   ALLBUF(14),WR5+4
         MVC   ALLBUF+30(1),0(R5)
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     PROC
IND1     DS    0H
         MVC   WORK(1),0(R5)
         WRTERM WORK,1
IND2     DS    0H
         BAL   R7,UPDATE
         CLI   ANSWER,X'F0'
         BL    IND3
         CLI   ANSWER,X'F3'
         BNH   IND4
IND3     DS    0H
         WRTERM ' '
         WRTERM 'MUST BE EITHER 0-NO CHANGE, 1-NAME, 2-DIST. CODE, 3-PH*
               ONE NO.'
         B     IND2
IND4     DS    0H
         MVC   0(1,R5),ANSWER
         MVC   ALLBUF(31),=C'INDICATOR BYTE IS NOW SET TO   '
         MVC   ALLBUF+31(1),ANSWER
         WRTERM ' '
         WRTERM ALLBUF,32
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
PROC     DS    0H
         L     R7,SFR7
         L     R9,SFR9
         L     R8,4(R9)
         LA    R9,0(R8,R7)
         L     R8,4(R9)
         LA    R9,0(R8,R7)
         L     R8,8(R9)
         LA    R6,4(R8,R7)
         MVC   WORK(8),0(R6)
         CLI   SWALL,X'FF'
         BNE   PROC2
         MVC   ALLBUF(15),WR1+55
         MVC   ALLBUF+30(8),WORK
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     SIZE
PROC2    DS    0H
         WRTERM WORK,8
         BAL   R7,UPDATE
         MVC   0(8,R6),ANSWER
         MVC   ALLBUF(25),=C'NEW LOGON PROCEDURE IS   '
         MVC   ALLBUF+25(8),ANSWER
         WRTERM ' '
         WRTERM ALLBUF,33
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
SIZE     DS    0H
         MVC   SIZEHALF,BUFFER+14
         LH    R2,SIZEHALF
         CVD   R2,WORK
         OI    WORK+7,X'0F'
         UNPK  WORK1(5),WORK+5(3)
         CLI   SWALL,X'FF'
         BNE   SIZE2
         MVC   ALLBUF(19),WR2+4
         MVC   ALLBUF+30(4),WORK1+1
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     PCF
SIZE2    DS    0H
         WRTERM WORK1+1,4
SIZECHK  DS    0H
         BAL   R7,UPDATE
         CLI   ANSWER+4,X'40'
         BNE   SIZERR
         CLI   ANSWER+3,X'40'
         BNE   SIZEFOUR
         LA    R2,3
         B     SIZEGT
SIZEFOUR DS    0H
         LA    R2,4
SIZEGT   DS    0H
         LA    R3,ANSWER
SIZEMO   DS    0H
         CLI   0(R3),X'F0'
         BL    SIZERR
         CLI   0(R3),X'F9'
         BH    SIZERR
         LA    R3,1(R3)
         BCT   R2,SIZEMO
         CLI   ANSWER+3,X'40'
         BNE   SIZEON
         PACK  WORK,ANSWER(3)
         B     SIZELT
SIZEON   DS    0H
         PACK  WORK,ANSWER(4)
SIZELT   DS    0H
         CVB   R2,WORK
         STH   R2,SIZEHALF
         MVC   BUFFER+14(2),SIZEHALF
         MVC   ALLBUF(29),=C'NEW MAXIMUM REGION SIZE IS   '
         MVC   ALLBUF+29(4),ANSWER
         WRTERM ' '
         WRTERM ALLBUF,33
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
SIZERR   DS    0H
         WRTERM ' '
         WRTERM 'INVALID REGION SIZE'
         B     SIZECHK
PCF      DS    0H
         UNPK  WORK2+1(5),BUFFER+18(3)
         TR    WORK2+1(4),TBCHAR-240
         CLI   SWALL,X'FF'
         BNE   PCF2
         MVC   ALLBUF(9),WR2+29
         MVC   ALLBUF+30(4),WORK2+1
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ATTRIB
PCF2     DS    0H
         WRTERM WORK2+1,4
PCFBAL   BAL   R7,UPDATE
         MVC   NOM(4),ANSWER
         CLI   ANSWER+4,X'40'
         BNE   PCFERR
         LA    R2,3
         LA    R3,ANSWER
PCFCHK   DS    0H
         CLI   0(R3),X'F0'
         BL    PCFERR
         CLI   0(R3),X'F9'
         BH    PCFERR
         LA    R3,1(R3)
         BCT   R2,PCFCHK
         CLI   0(R3),X'F0'
         BL    PCFCHK1
         CLI   0(R3),X'F9'
         BH    PCFERR
         B     PCFWORK
PCFCHK1  DS    0H
         CLI   0(R3),X'C1'
         BL    PCFERR
         CLI   0(R3),X'C6'
         BH    PCFERR
PCFWORK  DS    0H
         PACK  WORK2(2),ANSWER(3)
         NI    WORK2+1,X'F0'
         TR    ANSWER+3(1),TBPCF-193
         OC    WORK2+1(1),ANSWER+3
         MVC   BUFFER+18(2),WORK2
         MVC   ALLBUF(19),=C'NEW PCF LEVEL IS   '
         MVC   ALLBUF+19(4),NOM
         WRTERM ' '
         WRTERM ALLBUF,23
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
PCFERR   DS    0H
         WRTERM ' '
         WRTERM 'INVALID PCF LEVEL'
         B     PCFBAL
ATTRIB   DS    0H
         LA    R2,ATTRIBDA
         TM    BUFFER+16,X'80'
         BNO   ATTRIBA
         MVC   0(5,R2),=C'OPER,'
         LA    R2,5(R2)
         B     ATTRIBB
ATTRIBA  DS    0H
         MVC   0(7,R2),=C'NOOPER,'
         LA    R2,7(R2)
ATTRIBB  DS    0H
         TM    BUFFER+16,X'40'
         BNO   ATTRIBC
         MVC   0(8,R2),=C'ACCOUNT,'
         LA    R2,8(R2)
         B     ATTRIBD
ATTRIBC  DS    0H
         MVC   0(10,R2),=C'NOACCOUNT,'
         LA    R2,10(R2)
ATTRIBD  DS    0H
         TM    BUFFER+16,X'20'
         BNO   ATTRIBE
         MVC   0(4,R2),=C'JCL,'
         LA    R2,4(R2)
         B     ATTRIBF
ATTRIBE  DS    0H
         MVC   0(6,R2),=C'NOJCL,'
         LA    R2,6(R2)
ATTRIBF  DS    0H
         TM    BUFFER+16,X'10'
         BNO   ATTRIBG
         MVC   0(5,R2),=C'MOUNT'
         B     ATTRIBH
ATTRIBG  DS    0H
         MVC   0(7,R2),=C'NOMOUNT'
ATTRIBH  DS    0H
         CLI   SWALL,X'FF'
         BNE   ATTRIB2
         MVC   ALLBUF(17),WR2+55
         MVC   ALLBUF+30(30),ATTRIBDA
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     UNIT
ATTRIB2  DS    0H
         WRTERM ATTRIBDA,30
ATTRBAL  DS    0H
         BAL   R7,UPDATE
         LA    R2,ANSWER
         CLC   =C'OPER,',0(R2)
         BNE   ATTRIBI
         OI    BUFFER+16,X'80'
         LA    R2,5(R2)
         B     ATTRIBJ
ATTRIBI  DS    0H
         CLC   =C'NOOPER,',0(R2)
         BNE   ATTRERR
         LA    R2,7(R2)
         NI    BUFFER+16,X'FF'-X'80'
ATTRIBJ  DS    0H
         CLC   =C'ACCOUNT,',0(R2)
         BNE   ATTRIBK
         OI    BUFFER+16,X'40'
         LA    R2,8(R2)
         B     ATTRIBL
ATTRIBK  DS    0H
         CLC   =C'NOACCOUNT,',0(R2)
         BNE   ATTRERR
         NI    BUFFER+16,X'FF'-X'40'
         LA    R2,10(R2)
ATTRIBL  DS    0H
         CLC   =C'JCL,',0(R2)
         BNE   ATTRIBM
         OI    BUFFER+16,X'20'
         LA    R2,4(R2)
         B     ATTRIBN
ATTRIBM  DS    0H
         CLC   =C'NOJCL,',0(R2)
         BNE   ATTRERR
         NI    BUFFER+16,X'FF'-X'20'
         LA    R2,6(R2)
ATTRIBN  DS    0H
         CLC   =C'MOUNT',0(R2)
         BNE   ATTRIBO
         OI    BUFFER+16,X'10'
         B     ATTRIBP
ATTRIBO  DS    0H
         CLC   =C'NOMOUNT',0(R2)
         BNE   ATTRERR
         NI    BUFFER+16,X'FF'-X'10'
ATTRIBP  DS    0H
         MVC   ALLBUF(28),=C'NEW SYSTEM ATTRIBUTES ARE   '
         MVC   ALLBUF+28(30),ANSWER
         WRTERM ' '
         WRTERM ALLBUF,58
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
ATTRERR  DS    0H
         WRTERM ' '
         WRTERM 'INVALID SYSTEM ATTRIBUTES'
         WRTERM ' '
         B     ATTRBAL
UNIT     DS    0H
         L     R7,SFR7
         L     R9,SFR9
         L     R8,4(R9)
         LA    R9,0(R8,R7)
         L     R8,4(R9)
         LA    R9,0(R8,R7)
         L     R8,8(R9)
         LA    R7,0(R8,R7)
         LA    R6,16(R7)
         MVC   UNITNAME,0(R6)
         CLI   SWALL,X'FF'
         BNE   UNIT2
         MVC   ALLBUF(17),WR3+55
         MVC   ALLBUF+30(8),UNITNAME
         WRTERM ALLBUF,70
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
UNIT2    DS    0H
         WRTERM UNITNAME,8
         BAL   R7,UPDATE
         MVC   0(8,R6),ANSWER
         MVC   ALLBUF(19),=C'NEW UNIT NAME IS   '
         MVC   UNITNAME,ANSWER
         MVC   ALLBUF+19(8),UNITNAME
         WRTERM ' '
         WRTERM ALLBUF,27
         MVI   ALLBUF,X'40'
         MVC   ALLBUF+1(69),ALLBUF
         B     ENDATA
UPDATE   DS    0H
         MVI   ANSWER,X'40'
         MVC   ANSWER+1(79),ANSWER
         WRTERM ' '
         WRTERM 'TYPE NEW DATA OR PRESS ENTER'
         WRTERM ' '
         RDTERM ANSWER,LENGTH=8
         OC    ANSWER(80),=CL80' '
         CLI   ANSWER,X'40'
         BE    ENDATA
         MVI   SWCHG,X'FF'
         BR    R7
ENDATA   DS    0H
         MVI   SWALL,X'00'
         WRTERM ' '
         WRTERM 'DO YOU WANT TO SEE/CHANGE ANY OTHER FIELDS?  TYPE ''Y'*
               ' OR PRESS ENTER'
         WRTERM ' '
         MVI   ANSWER,X'40'
         MVC   ANSWER+1(79),ANSWER
         RDTERM ANSWER,LENGTH=1
         OI    ANSWER,X'40'
         CLI   ANSWER,C'Y'
         BNE   ENDATA2
         CLEAR
         B     ASK
ENDATA2  CLI   SWCHG,X'FF'
         BNE   CLOSE
WRITE    DS    0H
         LA    R2,BUFFER
         L     R3,=F'800'
         L     R4,BUFAD
         LR    R5,R3
         MVCL  R4,R2
         PUTX  INFILE
         MVI   SWCHG,X'00'
CLOSE    DS    0H
         CLOSE INFILE
         CLEAR
         WRTERM 'ENTER NEXT USERID OR PRESS ENTER'
         WRTERM ' '
         MVI   ANSWER,X'40'
         MVC   ANSWER+1(79),ANSWER
         RDTERM ANSWER,LENGTH=6
         OC    ANSWER,=CL80' '
         CLI   ANSWER,X'40'
         BE    END
         LA    R3,ANSWER
         B     USER
         EJECT
***********************************************************************
**********      ERROR ROUTINE                              ************
***********************************************************************
         SPACE 2
ERROR1   DS    0H
         WRTERM 'UNABLE TO DYNAMICALLY ALLOCATE THIS USER'
         B     END
PARMERR  DS    0H
         WRTERM 'INVALID USERID'
         EJECT
***********************************************************************
**********      TERMINATION                                ************
***********************************************************************
         SPACE 2
END      DS    0H
         L     R13,SV13
         MVC   12(4,R13),RETCODE   SET UP RETCODE
         LM    R14,R12,8(R13)
         BR    R14
         LTORG
BUFFER   DC    800X'40'
         END   ,
/*
//LKED.SYSPRINT DD SYSOUT=X
//LKED.SYSIN   DD  *
   ENTRY PEEK
   NAME PEEK(R)
/*
//
