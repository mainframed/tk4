 ***********************************************************************
 ***********************************************************************
                                     PASSWRD  COMMAND



                    THIS COMMAND ALLOWS THE PASSWORD DATASET TO BE SCANN
               ALL OR SELECTED PASSWORDS. IF NO PARAMETER IS SPECIFIED T
               OF THE PASSWORD DATASETS WILL BE DISPLAYED AT THE TERMINA
               PARAMETER IS SPECIFIED ALL DATASETS PREFIXED BY THAT CHAR
               STRING WILL BE DISPLAYED FROM BOTH PASSWORD DATASETS.


                    FORMAT OF THE COMMAND IS -  PASSWRD   OR PASSWRD STR

               WHERE STRING IS AN OPTIONAL SEARCH VALUE PREFIX.

               E.G.TO SCAN FOR ALL SYS1. DATASET PASSWORDS

                         PASSWRD SYS1.


                   TO SCAN FOR PASSWORD FOR SYS1.LPALIB

                         PASSWRD SYS1.LPALIB




1                  COMMONWEALTH BANKING CORPORATION
//SM0004A  JOB   OSOT,SAWARD,
//         MSGCLASS=X,NOTIFY=SM0004,CLASS=V
//JOBCAT   DD    DSN=USERCAT.TSOPK1,DISP=SHR
//ASM      EXEC  PGM=IEV90,PARM='OBJECT,NODECK'
//SYSPRINT DD    SYSOUT=X
//SYSLIB   DD    DSN=PCS.TEST.MACRO.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD    DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100))
//SYSUT2   DD    DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3   DD    DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSLIN   DD    DSN=&&OBJSET,UNIT=SYSDA,SPACE=(80,(200,50)),
//         DISP=(MOD,PASS)
//SYSPUNCH DD    DUMMY
//SYSIN    DD    *
PASSWRD  TITLE 'PASSWRD  DISPLAY CONTENTS OF PASSWORD DATASET B SAWARD'
*
*
*           THIS PROGRAM DISPLAYS THE CONTENTS OF THE PASSWORD DATASET
*
*
***********************************************************************
**********      INITIALISATION                             ************
***********************************************************************
         PRINT NOGEN
PASSWRD  CSECT
         USING *,R12
         B     24(R15)
         DC    CL8'PASSWRD'
         DC    CL4'&SYSTIME'
         DC    CL8'&SYSDATE'
         STM   R14,R12,8(R13)
         LR    R12,R15
         ST    R13,SV13
         LA    R13,SVREGS
         B     START
         EJECT
***********************************************************************
**********      CONSTANTS & STORAGE                        ************
***********************************************************************
         SPACE 2
         REGEQU
SVREGS   DS    9D
SV13     DS    F
RETCODE  DC    F'0'
WORK     DS    D
MSG1     DC    CL70'ERROR READING DISK '
MSG2     DC    CL80'ALLOCATION ERROR INFO=0000 ERROR=0000 RC=00 '
MSG3     DC    CL40'CONTENTS OF SY1RES PASSWORD DATASET'
DSKDCB   DCB   DDNAME=PSWDISK,MACRF=(R),SYNAD=ERROR1,                  X
               DSORG=PS,EODAD=EOF,KEYLEN=52
CLEARBUF DC    X'40114040'            WCC + SBA
         DC    X'1DC8'                HI INTENSITY
         DC    C'ENTER PASSWORD ===>'
         DC    X'1D4C'                NON DISPLAY
         DC    X'13'                  IC
         DC    C'        '            INPUT AREA
         DC    X'1DC8'                HI INTENSITY
         DC    X'3C5D7F40'            RTA BLANKS
ENDBUF   EQU   *
CCBLANK  DC    CL80' '
INPUT    DC    CL200' '
         EJECT
***********************************************************************
**********      DYNAMIC ALLOCATION BLOCKS                  ************
***********************************************************************
         SPACE 2
RBPTR    DS    0F                 POINTER TO REQUEST BLOCK
         DC    X'80'
         DC    AL3(RB)
*
RB       DC    X'14012000'        LENGTH,VERB,FLAGS
RBERR    DC    XL2'0'             ERROR REASON CODE
RBINFO   DC    XL2'0'             INFORMATION REASON CODE
RBTXT    DC    A(TEXT),2F'0'      ADDRESS OF TXT UNITS,RES AND FLAGS
*
TEXT     DC    A(TDDNAME)           POINTER TO DSNAME TEXT
         DC    A(TDSNAME)           POINTER TO DSNAME TEXT
         DC    A(TSTATUS)           POINTER TO STATUS TEXT
         DC    A(TDISP)             POINTER TO DISP TEXT
         DC    A(TVOL)              POINTER TO VOL TEXT
         DC    A(TUNIT)             POINTER TO UNIT TEXT
         DC    A(TPSWD)             POINTER TO UNIT TEXT
         DC    X'80',AL3(TUNALLOC)  POINTER TO UNALLOC TEXT
TDDNAME  DC    AL2(1),AL2(1),AL2(L'DDNAME) DDNAME
DDNAME   DC    C'PSWDISK'
TDSNAME  DC    AL2(2),AL2(1),AL2(L'DSNAME) DSNAME
DSNAME   DC    C'PASSWORD'
TSTATUS  DC    AL2(4),AL2(1),AL2(1),AL1(8) STATUS SHR
TDISP    DC    AL2(5),AL2(1),AL2(1),AL1(8) DISP KEEP
TVOL     DC    AL2(16),AL2(1),AL2(6),C'SY1RES' VOLUME
TUNIT    DC    AL2(21),AL2(1),AL2(4),C'3350'   UNIT
TPSWD    DC    AL2(80),AL2(1),AL2(L'TPASS)     PASSWORD FOR PASSWORD
TPASS    DC    CL8' '                          PASSWORD FOR PASSWORD
TUNALLOC DC    AL2(28),AL2(0)              UNALLOC AT CLOSE
*
         EJECT
***********************************************************************
**********       PROCESSING                                 ***********
***********************************************************************
START    DS    0H
         L     R2,0(R1)            ADDRESS THE PARAMETER LIST LENGTH
         LH    R3,0(R2)            PICK THE PARAMETER LIST LENGTH
         LA    R4,2(R2)            SET PARAMETER POINTER
         CLI   2(R2),X'00'         IS IT A TSO COMMAND ?
         BNE   RESTART             NO
         STFSMODE ON,INITIAL=YES
         LA    R4,2(R4)            ADJUST FOR TSO COMMAND
         SH    R3,=H'4'
         SH    R3,2(R2)            ADJUST FOR TSO COMMAND
         AH    R4,2(R2)            SET TO REAL PARMS
         LTR   R5,R3
         BZ    RESTART             NO PARMS
         EX    R5,EXOC             PARMS TO UPPER CASE
RESTART  DS    0H
         LA    R0,ENDBUF-CLEARBUF  LENGTH
         LA    R1,CLEARBUF         PASSWORD PROMPT
         TPUT  (1),(0),FULLSCR
         RDTERM INPUT,LENGTH=20
         OC    INPUT(8),CCBLANK
         MVC   TPASS,INPUT
REALLOC  DS    0H
         LA    1,RBPTR
         SVC   99                  DYNAMICALLY ALLOCATE
         LTR   15,15               WAS ALLOC OK ?
         BNZ   ERROR2              NO   TELL ABOUT THE ERROR
         OPEN  DSKDCB
         MVC   MSG3+12(6),TVOL+6   PUT IN VOLID
         WRTERM MSG3,40
GET      DS    0H
         READ  DSKDECB,SF,DSKDCB,INPUT
         CHECK DSKDECB
         LTR   R5,R3
         BZ    DOALL
         BCTR  R5,0
         EX    R5,EXCLC
         BNE   GET
DOALL    DS    0H
         WRTERM INPUT,52           DISPLAY DSNAME AND PASSWORD
         B     GET
EXCLC    CLC   INPUT(0),0(R4)      SAME DSNAME ?
EXOC     OC    0(0,R4),CCBLANK     FORCE PARMS TO UPPER CASE
         EJECT
***********************************************************************
**********      ERROR ROUTINE                              ************
***********************************************************************
         SPACE 2
ERROR1   DS    0H
*        WRTERM MSG1,70
         B     END
         EJECT
***********************************************************************
**********      DYNAMIC ALLOCATION ERROR                   ************
***********************************************************************
         SPACE 2
ERROR2   DS    0H
         UNPK  MSG2+22(5),RBERR(3)
         TR    MSG2+22(4),TBCHAR-240
         MVI   MSG2+26,C' '
         UNPK  MSG2+33(5),RBINFO(3)
         TR    MSG2+33(4),TBCHAR-240
         MVI   MSG2+37,C' '
         STC   R15,WORK
         UNPK  MSG2+41(3),WORK(2)
         TR    MSG2+41(2),TBCHAR-240
         MVI   MSG2+43,C' '
         WRTERM MSG2,79
         B     END1
         EJECT
***********************************************************************
**********      TERMINATION                                ************
***********************************************************************
         SPACE 2
EOF      DS    0H
END      DS    0H
         CLOSE DSKDCB              ALL DONE
END1     DS    0H
         STFSMODE OFF
         L     R13,SV13
         MVC   12(4,R13),RETCODE   SET UP RETCODE
         LM    R14,R12,8(R13)
         BR    R14
         LTORG
TBCHAR   DC    C'0123456789ABCDEF'
         END   ,
/*
//LKED     EXEC   PGM=IEWL,PARM=(LIST,MAP),
//         COND=(8,LT,ASM)
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLMOD  DD   DISP=SHR,DSN=SM.TEST.LINKLIB
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSLIB   DD   DSN=PCS.TEST.AUTOCALL.LINKLIB,DISP=SHR
//SYSPRINT DD   SYSOUT=X
//SYSIN    DD   *
   SETCODE AC(1)
   NAME PASSWRD(R)
