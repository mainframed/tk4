//SM0001A   JOB   OSOT,SAWARD,MSGCLASS=X,NOTIFY=SM0001
//JOBCAT    DD    DSN=USERCAT.TSOPK2,DISP=SHR
//*MAIN CLASS=S,SYSTEM=SY1
//ASM       EXEC  PGM=IFOX00,PARM='OBJ,NODECK,RENT'
//SYSPRINT  DD    SYSOUT=X
//SYSLIB    DD    DSN=SYS1.MACLIB,DISP=SHR
//          DD    DSN=SYS1.AMODGEN,DISP=SHR
//          DD    DSN=TEST.MACRO.MACLIB,DISP=SHR
//SYSUT1    DD    DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100))
//SYSUT2    DD    DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3    DD    DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSGO     DD    DSN=&&OBJSET,UNIT=SYSDA,SPACE=(80,(200,50)),
//          DISP=(MOD,PASS)
//SYSPUNCH  DD    SYSOUT=B
//SYSIN     DD    *
IKJEFF53 TITLE 'OUTPUT,STATUS,CANCEL EXIT           B SAWARD'
*        THIS IS THE ASSEMBLER SOURCE CODE FOR THE
*        TSO OUTPUT, STATUS AND CANCEL EXIT ROUTINE
*        CANCEL EXIT ROUTINE.
*
*                VALIDITY CHECKS JOBNAME ON A CANCEL, OUTPUT OR       *
*                STATUS FIB (FOREGROUND INITIATED BACKGROUND)         *
*                COMMAND.  ALLOWS FOR STATUS WITH JOBNAME 'TSO'       *
*                                                                     *
*                OPERATION =                                          *
*                   STATUS:  IF JOBNAME IS NOT 'TSO' RETURN TO        *
*                   CALLER SINCE ANY JOBNAME WILL BE ALLOWED.         *
*                   IF JOBNAME IS 'TSO' PUT OUT LIST OF USERIDS       *
*                   WITH ADDRESS SPACE ID AND TERMINAL ADDRESS        *
*                   OR SYMBOLIC TERMINAL NAME (TSO/VTAM).             *
*                   RETURN TO CALLER WITH RETURN CODE OF 12 TO        *
*                   SKIP JOBNAME SEARCH FOR 'TSO'.                    *
*                                                                     *
*                   OUTPUT:  FIRST CHECK IF THE USER HAS PCF LEVEL 3  *
*                   AUTHORITY.  IF YES, ALLOW THE USER TO OUTPUT      *
*                   ANY JOBNAME.  IF THE USER DOES NOT HAVE PCF LVL 3 *
*                   AUTHORITY COMPARE THE USERID WITH THE JOBNAME     *
*                   PASSED.  IF THE JOBNAME IS NOT THE USERID OR      *
*                   DOES NOT START WITH THE USERID, THE JOBNAME       *
*                   IS REJECTED BY FIRST RETURNING TO THE CALLER      *
*                   AN ERROR MESSAGE AND A RETURN CODE REQUESTING     *
*                   THAT THE MESSAGE BE ISSUED VIA THE PUTLINE        *
*                   MECHANISM.  WHEN REENTERED FOR THE JOBNAME,       *
*                   THE EXIT ISSUES A RETURN CODE REJECTING THE       *
*                   JOBNAME.                                          *
*                                                                     *
*                   CANCEL:  FIRST CHECK IF THE USER HAS PCF LEVEL 3  *
*                   AUTHORITY.  IF YES, ALLOW THE USER TO CANCEL      *
*                   ANY JOBNAME (THE MVS SYSTEM WILL PREVENT THE      *
*                   USER FROM CANCELLING STARTED TASKS AND TSO        *
*                   SESSIONS).  IF THE USER DOES NOT HAVE PCF LVL 3   *
*                   AUTHORITY COMPARE THE USERID WITH THE JOBNAME     *
*                   PASSED.  IF THE JOBNAME IS NOT THE USERID PLUS    *
*                   AT LEAST ONE CHARACTER, THE JOBNAME IS REJECTED   *
*                   BY FIRST RETURNING TO THE CALLER AN ERROR         *
*                   MESSAGE AND A RETURN CODE REQUESTING THAT THE     *
*                   THAT THE MESSAGE BE ISSUED VIA THE PUTLINE        *
*                   MECHANISM.  WHEN REENTERED FOR THE JOBNAME,       *
*                   THE EXIT ISSUES A RETURN CODE REJECTING THE       *
*                   JOBNAME.                                          *
***********************************************************************
         EJECT
IKJEFF53 CSECT
         SAVE  (14,12),,IKJEFF53.IPO.&SYSTIME..&SYSDATE SAVE REGISTERS
*                                      WITH CSECT IDENTIFIER
         LR    R12,R15                 BRANCH AND LOAD REGISTER 12
         USING IKJEFF53,R12            CSECT ADDRESSABILITY
         L     R0,SIZDATD              GETMAIN SIZE
         GETMAIN R,LV=(0)              GET AREA FROM SUBPOOL 0
         LR    R11,R1                  ADDRESS AREA
         USING DATD,R11                ESTABLISH ADDRESSABILITY
         USING REJMSG,R10              ESTABLISH REGISTER 10 AS BASE
*                                      FOR REJECT MESSAGE DSECT
         ST    R13,SAVEAREA+4          CHAIN SAVEAREA
         LM    R0,R1,20(R13)           RELOAD REGISTERS 0 AND 1
         ST    R11,8(,R13)             SAVE ADDRESS OF CURRENT SAVEAREA
         LR    R13,R11                 SET SAVEAREA POINTER
         LR    R4,R1                   SET PARAMETER LIST POINTER
         USING IEPARML,R4              ESTABLISH ADDRESSABILITY
         LA    R5,IECONTIN             INITIALIZE RETURN CODE
         L     R6,IECODEP              LOAD COMMAND CODE POINTER
         CLI   0(R6),IECANCEL          SEE IF COMMAND IS CANCEL
         BE    PROCESS                 YES....CONTINUE PROCESSING
         CLI   0(R6),IEOUTPUT          SEE IF COMMAND IS OUTPUT
         BE    PROCESS                 YES....CONTINUE PROCESSING
         CLI   0(R6),IESTATUS          SEE IF COMMAND IS STATUS
         BE    PROCESS                 YES....CONTINUE PROCESSING
         B     ENDPROC                 NO.....DO NO PROCESSING
         SPACE 2
***********************************************************************
*                                                                     *
*              CONSTANTS AND STORAGE AREAS                            *
*                                                                     *
***********************************************************************
         SPACE 2
         REGEQU
ASVTENTY EQU   528
ASCBTSB  EQU   60
ASCBJBNS EQU   176
*
         DS    0F
SIZDATD  DC    AL1(0)
         DC    AL3(ENDDATD-DATD)
REJTEXT1 DC    CL66'REJECTED - JOBNAME MUST BE YOUR USERID PLUS AT LEASC
               T ONE CHARACTER'
REJTEXT2 DC    CL69'REJECTED - JOBNAME MUST BE YOUR USERID OR MUST STARC
               T WITH YOUR USERID'
HEADER   DC    CL79'USERID   ASID   LINE   USERID   ASID   LINE   USERIX
               D   ASID   LINE '
EDMSK    DC    XL8'4020202020202120'
USERS    DC    CL20' USERS ARE LOGGED ON'
TRT      DC    CL16'0123456789ABCDEF'
TSOWORD  DC    CL3'TSO'
ASCBWORD DC    CL4'ASCB'
STRTWORD DC    CL8'STARTING'
DISCON   DC    CL4'DISC'
GETMINFO DS    0F                      SUBPOOL AND LENGTH FOR GETMAIN
*                                      OF MESSAGE AREA
GETMSP   DC    AL1(0)
GETMLEN  DC    AL3(84)
*
DATA     DS    0H
JOBNCOMP CLC   0(0,R6),0(R1)
MOVEJOBN MVC   REJSLOT(0),0(R1)
HALF84   DC    H'84'
HALF2    DC    H'2'
HALF3    DC    H'3'
JOBWORD  DC    CL4'JOB '
BLANK    EQU   C' '
*
DATD     DSECT
         DS    0F
SAVEAREA DS    18F
EXTLIST  EXTRACT MF=L
PSCBADDR DS    F
PSCBATR1 EQU   16
PSCBCTRL EQU   X'80'
PACK     DS    D
CHAR     DS    CL8
LINE     DS    CL79
ENDLINE  DS    F
         DS    0D
ENDDATD  EQU   *
*
REJMSG   DSECT                         DSECT FOR RETURN MESSAGE
REJMLEN  DS    H
REJJOB   DS    CL4
REJSLOT  DS    CL9
*
         SPACE 2
***********************************************************************
*                                                                     *
*        SEE IF SECOND TIME ENTRY FOR A JOB -                         *
*           MESSAGE POINTER NOT ZERO                                  *
*                                                                     *
***********************************************************************
         SPACE 2
IKJEFF53 CSECT
PROCESS  DS    0H
         L     R6,IEMSGP               LOAD MESSAGE POINTER
         LTR   R6,R6                   2ND TIME THROUGH ?
         BNZ   FREEMESS                YES....GO FREE MESSAGE BUFFER
         EJECT
***********************************************************************
*                                                                     *
*        DETERMINE IF THIS IS FOR CANCEL                              *
*           NO.....GO TO OUTPUT ROUTINE                               *
*           YES....IF USER HAS OPERATOR AUTHORITY THEN O.K.           *
*              ELSE CHECK IF JOBNAME IS VALID FOR CANCEL              *
*              CANCEL NEEDS JOBNAME EQUAL TO USERID                   *
*              PLUS AT LEAST ONE CHARACTER                            *
*                                                                     *
***********************************************************************
         SPACE 2
VALIDITY DS    0H
         L     R6,IECODEP              LOAD COMMAND POINTER
         CLI   0(R6),IECANCEL          SEE IF COMMAND IS CANCEL
         BNE   OUTPUT                  NO.....GO SEE IF OUTPUT COMMAND
         EXTRACT PSCBADDR,'S',FIELDS=PSB,MF=(E,EXTLIST) GET PSCB ADDR
         L     R1,PSCBADDR             LOAD POINTER TO PSCB
         IC    R1,18(R1)               PICK UP USERDATA BITS
         SLL   R1,28                   DROP COMMAND ACCESS LEVEL
         SRL   R1,28                   LEAVE DATASET ACCESS LEVEL
         CH    R1,HALF3                CHECK PCF LEVEL
         BNH   ENDPROC                 LE 3...GO TO END OF PROCEDURE
*                                      O.K. TO CANCEL ANY JOBNAME
         L     R15,IEIDLENP            LOAD USERID LENGTH POINTER
         SLR   R14,R14                 ZERO REGISTER 14
         IC    R14,0(,R15)             INSERT USERID LENGTH INTO
*                                      REGISTER 14 AND LOAD
         L     R6,IENAMELP             LOAD JOBNAME LENGTH POINTER
         CH    R14,0(,R6)              SEE IF USERID LENGTH IS
*                                      SHORTER THAN JOBNAME LENGTH
         BNL   BADJOBN                 NO.....GO BUILD INVALID
*                                      JOBNAME MESSAGE
         L     R6,IEUSRIDP             LOAD USERID POINTER
         L     R1,IEJOBNMP             LOAD JOBNAME POINTER
         BCTR  R14,0                   DECREMENT R14 FOR
         EX    R14,JOBNCOMP            EXECUTE OF COMPARE
         BNE   BADJOBN                 NO.....GO BUILD INVALID JOBNAME
*                                      MESSAGE
         B     ENDPROC                 GO ACCEPT JOBNAME
         EJECT
***********************************************************************
*                                                                     *
*        SEE IF THIS IS OUTPUT COMMAND                                *
*           NO.....GO TO STATUS ROUTINE                               *
*           YES....IF USER HAS OPERATOR AUTHORITY THEN O.K.           *
*              ELSE CHECK IF JOBNAME IS VALID FOR OUTPUT              *
*              OUTPUT NEEDS JOBNAME EQUAL TO USERID OR                *
*              EQUAL TO USERID PLUS AT LEAST ONE CHARACTER            *
*                                                                     *
***********************************************************************
         SPACE 2
OUTPUT   DS    0H
         CLI   0(R6),IEOUTPUT          SEE IF COMMAND IS OUTPUT
         BNE   STATUS                  NO.....GO SEE IF STATUS COMMAND
         EXTRACT PSCBADDR,'S',FIELDS=PSB,MF=(E,EXTLIST) GET PSCB ADDR
         L     R1,PSCBADDR             LOAD POINTER TO PSCB
         IC    R1,18(R1)               PICK UP USERDATA BITS
         SLL   R1,28                   DROP COMMAND ACCESS LEVEL
         SRL   R1,28                   DROP VOLUME INFO AND LEAVE
*                                      DATASET ACCESS LEVEL
         CH    R1,HALF3                CHECK PCF LEVEL
         BNH   ENDPROC                 LE 3...GO TO END OF PROCEDURE
*                                      O.K. TO OUTPUT ANY JOBNAME
         L     R15,IEIDLENP            LOAD USERID LENGTH POINTER
         SLR   R14,R14                 ZERO REGISTER 14
         IC    R14,0(,R15)             INSERT USERID LENGTH INTO
*                                      REGISTER 14
         L     R6,IENAMELP             LOAD JOBNAME LENGTH POINTER
         CH    R14,0(,R6)              SEE IF USERID LENGTH
*                                      IS EQUAL TO OR SHORTER
*                                      THAN JOBNAME LENGTH
         BH    BADJOBN                 NO.....GO BUILD INVALID
*                                      JOBNAME MESSAGE
         L     R6,IEUSRIDP             LOAD USERID POINTER
         L     R1,IEJOBNMP             LOAD JOBNAME POINTER
         BCTR  R14,0                   DECREMENT R14 FOR
         EX    R14,JOBNCOMP            EXECUTE OF COMPARE
         BE    ENDPROC                 EQUAL..RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*        REJECT THIS INVALID JOBNAME AND BUILD INVALID JOBNAME        *
*           MESSAGE                                                   *
*                                                                     *
***********************************************************************
         SPACE 2
BADJOBN  DS    0H                      BUILD BAD JOBNAME MESSAGE
         L     R0,GETMINFO             SUBPOOL 0, LENGTH DECIMAL 84
         GETMAIN R,LV=(0)              GET MESSAGE BUFFER
         ST    R1,IEMSGP               STORE POINTER TO MESSAGE BUFFER
*                                      INTO PARAMETER LIST
         LR    R10,R1                  LOAD REGISTER 10 WITH ADDRESS OF
*                                      GETMAINED AREA
         MVI   REJMSG,BLANK            INITIALIZE BUFFER WITH BLANKS
         MVC   REJMSG+1(83),REJMSG
         MVC   REJMLEN(2),HALF84       PUT IN MAXIMUM MESSAGE LENGTH
         MVC   REJJOB(4),JOBWORD       PUT IN WORD 'JOB'
         L     R1,IENAMELP             LOAD JOBNAME LENGTH POINTER
         LH    R15,0(,R1)              LOAD JOBNAME LENGTH
         LR    R14,R15                 LOAD REGISTER 14 WITH LENGTH
         BCTR  R14,0                   DECREMENT REGISTER 14 FOR MOVE
         L     R1,IEJOBNMP             LOAD JOBNAME POINTER
         EX    R14,MOVEJOBN            INSERT THE BAD JOBNAME
         AH    R15,HALF2               INDEX PAST BLANK AFTER JOBNAME
         LR    R5,R15                  LOAD REGISTER 5 AS TEMPORARY
*                                      SAVE AREA FOR REGISTER 15
         EJECT
***********************************************************************
*                                                                     *
*        DIFFERENT MESSAGE TEXTS FOR CANCEL AND OUTPUT                *
*                                                                     *
***********************************************************************
         SPACE 2
         L     R1,IECODEP              LOAD COMMAND POINTER
         CLI   0(R1),IECANCEL          SEE IF COMMAND IS CANCEL
         BNE   OUTPMESS                NO.....GO TO OUTPUT MESSAGE
         ALR   R10,R5                  OFFSET MESSAGE IN BUFFER
         MVC   REJSLOT-1(66),REJTEXT1  MOVE IN TEXT FOR CANCEL
         B     SETRC                   GO SET RETURN CODE
OUTPMESS DS    0H
         ALR   R10,R5                  OFFSET MESSAGE IN BUFFER
         MVC   REJSLOT-1(69),REJTEXT2  MOVE IN TEXT FOR OUTPUT
SETRC    DS    0H
         LA    R5,IEMSG                SET RETURN CODE TO PROMPT
         B     ENDPROC                 GO TO RETURN
         EJECT
***********************************************************************
*                                                                     *
*        SEE IF THIS IS STATUS COMMAND                                *
*           NO.....GO TO END OF PROCEDURE                             *
*           YES....CHECK IF NAME IS 'TSO'                             *
*              NO.....GO TO END OF PROCEDURE                          *
*              YES....PUT OUT USERIDS, ASIDS, AND ADDRESSES           *
*                                                                     *
***********************************************************************
         SPACE 2
STATUS   DS    0H
         CLI   0(R6),IESTATUS          SEE IF COMMAND IS STATUS
         BNE   ENDPROC                 NO.....GO TO END OF PROCEDURE
         L     R1,IEJOBNMP             LOAD POINTER TO JOBNAME
         CLC   TSOWORD,0(R1)           SEE IF JOBNAME IS 'TSO'
         BNE   ENDPROC                 NO.....GO TO END OF PROCEDURE
         L     R1,IENAMELP             LOAD POINTER TO JOBNAME LENGTH
         CLC   HALF3,0(R1)             SEE IF JOBNAME LENGTH IS 3
         BNE   ENDPROC                 NO.....GO TO END OF PROCEDURE
         L     R15,CVTPTR              LOAD POINTER TO CVT
         USING CVTMAP,R15              ESTABLISH ADDRESSABILITY TO CVT
         L     R6,CVTASVT              LOAD POINTER TO ASVT
         DROP  R15                     DROP ADDRESSABILITY TO CVT
         LA    R6,ASVTENTY-4(R6)       LOAD ADDRESS OF FIRST ASVT ENTRY
*                                      MINUS 4
         LA    R3,0                    ZERO OUT USER COUNTER
         TPUT  HEADER,L'HEADER         PUT OUT HEADER LINE
         MVI   LINE,C' '               BLANK OUT THE OUTPUT LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R5,LINE+50              SET LINE END
         ST    R5,ENDLINE              SET INDEX END
         LA    R5,LINE                 SET LINE INDEX
         EJECT
***********************************************************************
*                                                                     *
*        GO DOWN THE ADDRESS SPACE VECTOR TABLE                       *
*           SEE IF ADDRESS SPACE CONTROL BLOCK IS FOR A TSO USER      *
*              YES....GET ADDRESS SPACE ID, USERID, AND PHYSICAL      *
*                     TERMINAL ADDRESS AND OUTPUT LINE                *
*              NO.....GO CHECK NEXT ASCB                              *
*                                                                     *
***********************************************************************
         SPACE 2
ASCBNEXT DS    0H
         LA    R6,4(R6)                LOAD ADDRESS OF NEXT ASVT ENTRY
         L     R7,0(R6)                LOAD ADDRESS OF ACSB
         LA    R7,0(R7)                ZERO HIGH ORDER BIT
         LTR   R7,R7                   SEE IF ADDRESS IS ZERO
         BZ    LASTASCB                YES....GO HANDLE LAST ASCB
         TM    0(R6),X'80'             SEE IF ASID IS AVAILABLE
         BO    ASCBNEXT                NO....GO ON TO NEXT ASVT ENTRY
         CLC   0(4,R7),ASCBWORD        ENSURE CONTROL BLOCK IS VALID
         BNE   ASCBNEXT                NO.....GO ON TO NEXT ASVT ENTRY
         L     R8,ASCBTSB(R7)          LOAD ADDRESS OF TSB
         LTR   R8,R8                   SEE IF ADDRESS ZERO - NOT TSO
         BZ    ASCBNEXT                ZERO...GO ON TO NEXT ASVT ENTRY
         MODESET EXTKEY=ZERO,SAVEKEY=(2)  MODESET AND SAVE OLD KEY
         TM    25(R8),X'01'            SEE IF THIS IS A TSO/VTAM TSB
         BO    GETTSBX                 YES....GO GET TSB EXTENSION
         LH    R8,82(R8)               GET BINARY LINE ADDRESS
         STH   R8,PACK                 STORE ADDRESS IN WORKAREA
         UNPK  PACK+3(5),PACK(3)       UNPACK THE ADDRESS
         MVC   17(3,R5),PACK+4         MOVE UNPACKED ADDRESS INTO LINE
         TR    17(3,R5),TRT-240        TRANSLATE TO EBCDIC
         B     MODEREST                GO MODESET AND RESTORE OLD KEY
GETTSBX  DS    0H
         L     R9,96(R8)               GET ADDRESS OF TSB EXTENSION
         TM    32(R9),X'10'            SEE IF TERMINAL AWAITING RECON
         BNO   GETSYM                  NO.....GO GET SYMBOLIC NAME
         MVC   16(4,R5),DISCON         MOVE IN DISCON'D
         B     MODEREST                GO MODESET AND RESTORE OLD KEY
GETSYM   DS    0H
         MVC   15(5,R5),104(R8)        MOVE IN TERMINAL SYMBOLIC NAME
MODEREST DS    0H
         MODESET KEYADDR=(2)           MODESET AND RESTORE OLD KEY
         L     R8,ASCBJBNS(R7)         LOAD POINTER TO JOBNAME FIELD
         LTR   R8,R8                   SEE IF POINTER IS ZERO
         BZ    STARTING                YES....GO MOVE IN 'STARTING'
         MVC   0(8,R5),0(R8)           MOVE USERID TO PRINT LINE
         B     EDITASID                GO EDIT ASID TO EBCDIC
STARTING DS    0H
         MVC   0(8,R5),STRTWORD        MOVE 'STARTING' TO PRINT LINE
EDITASID DS    0H
         LH    R8,36(R7)               LOAD ADDRESS SPACE ID
         CVD   R8,PACK                 CONVERT TO DECIMAL
         MVC   CHAR,EDMSK              MOVE EDIT MASK TO CHAR
         ED    CHAR,PACK+4             EDIT TO ASID TO EBCDIC
         MVC   10(2,R5),CHAR+6         MOVE TWO CHARS TO PRINT LINE
         LA    R3,1(R3)                ADD 1 TO USER COUNT
         MVI   20(R5),C','             PUT IN A COMMA
         LA    R5,23(,R5)              BUMP INDEX
         C     R5,ENDLINE              IS THIS A FULL LINE ?
         BL    ASCBNEXT                NO
         LA    R1,LINE                 LOAD POINTER TO LINE
         TPUT  (R1),L'LINE             PUT OUT OUTPUT LINE
         MVI   LINE,C' '               BLANK OUT THE OUTPUT LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R5,LINE                 RESET INDEX
         B     ASCBNEXT                GO ON TO NEXT ASVT ENTRY
         SPACE 2
***********************************************************************
*                                                                     *
*        AT END OF ASVT CHAIN SO PUT OUT TSO USER COUNT               *
*           AND SET RETURN CODE FOR USERID REJECT                     *
*           TO AVOID SEARCH FOR JOBNAME 'TSO'                         *
*                                                                     *
***********************************************************************
         SPACE 2
LASTASCB DS    0H
         LA    R1,LINE                 LOAD POINTER TO OUTPUT LINE
         TPUT  (R1),L'LINE             PUT OUT LAST INCOMPLETE LINE
         MVI   LINE,C' '               BLANK OUT THE OUTPUT LINE
         MVC   LINE+1(L'LINE-1),LINE
         CVD   R3,PACK                 CONVERT USER COUNT TO DECIMAL
         MVC   CHAR,EDMSK              MOVE EDIT MASK TO CHAR
         ED    CHAR,PACK+4             CONVERT DECIMAL COUNT TO EBCDIC
         MVC   LINE(4),CHAR+4          MOVE IN COUNT
         MVC   LINE+4(L'USERS),USERS   MOVE IN MESSAGE
         LA    R1,LINE                 LOAD POINTER TO OUTPUT LINE
         TPUT  (R1),L'LINE             PUT OUT COUNT MESSAGE
         LA    R5,12                   TREAT AS IF JOBNAME REJECTED
         B     ENDPROC                 GO TO END OF PROCEDURE
         SPACE 2
***********************************************************************
*                                                                     *
*        SECOND TIME ENTRY (FREE MESSAGE BUFFER)                      *
*                                                                     *
***********************************************************************
         SPACE 2
FREEMESS DS    0H
         L     R1,IEMSGP               LOAD POINTER TO GETMAINED AREA
         L     R0,GETMINFO             LOAD SUBPOOL NUMBER AND LENGTH
*                                      OF GETMAINED AREA
         FREEMAIN  R,LV=(0),A=(1)      FREE MESSAGE BUFFER
         SLR   R6,R6                   ZERO OUT MESSAGE POINTER ENTRY
         ST    R6,IEMSGP               IN PARMLIST
         LA    R5,12                   SET RETURN CODE TO 12 - DELETE
*                                      ENTRY
         SPACE 2
***********************************************************************
*                                                                     *
*        RETURN TO CALLER (STATUS, CANCEL, OR OUTPUT COMMAND)         *
*                                                                     *
***********************************************************************
         SPACE 2
ENDPROC  DS    0H
         L     R13,4(,R13)             RESTORE REGISTER 13
         L     R0,SIZDATD              LOAD REGISTER 0 WITH SIZE OF
*                                      GETMAINED AREA
         LR    R1,R11                  LOAD REGISTER 1 WITH ADDRESS OF
*                                      GETMAINED AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED AREA
         LR    R15,R5                  LOAD REGISTER 15 WITH RETURN
*                                      CODE
         L     R14,12(,R13)            LOAD REGISTER 14 WITH RETURN
*                                      ADDRESS
         LM    R0,R12,20(R13)          RESTORE REGISTERS
         BR    R14                     RETURN VIA REGISTER 14
         EJECT
COPYBOOK DSECT
         IKJEFFIE IETYPE=OUTPUT
         CVT
         END   ,
/*
//LKED     EXEC   PGM=IEWL,PARM=(RENT,REUS,REFR),COND=(8,LT,ASM)
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLMOD  DD   DISP=SHR,VOL=SER=SY2RES,UNIT=3350,
//         DSN=SYS1.LINKLIB
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSLIB   DD   DSN=TEST.AUTOCALL.LINKLIB,DISP=SHR
//SYSPRINT DD SYSOUT=X
//SYSIN    DD  *
   SETCODE AC(1)
   NAME IKJEFF53(R)
