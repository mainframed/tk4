//RPBTAPE  JOB ('********/61100000',B1),R.P.BACKSTROM,
//             MSGCLASS=A,NOTIFY=RPB,
//             CLASS=0,TIME=(,30)
/*ROUTE  PRINT VIEW
/*JOBPARM  L=2
//*
//TAPETEST PROC TAPE=TAPE,ID=SCRTCH,DEN=4
//*
//*   TAPETEST -- PROCEDURE FOR TESTING TAPES BY WRITING NUMBERED
//*               RECORDS FROM LOAD POINT TO END OF TAPE AND
//*               THEN READING THEM BACKWARDS TO LOAD POINT.
//*
//*               DEN=3 IS FOR 1600 BPI TAPE DRIVES.
//*               DEN=4 IS FOR 6250 BPI DRIVES.
//*
//*   AUTHOR:  R.P. BACKSTROM,
//*            AUSTRALIAN ATOMIC ENERGY COMMISSION,
//*            LUCAS HEIGHTS RESEARCH LABORATORIES,
//*            SUTHERLAND,
//*            N.S.W. 2232,
//*            AUSTRALIA.
//*                                             AUGUST 1982.
//*
//TPTEST  EXEC PGM=TAPETST&DEN,PARM='&ID',REGION=40K
//STEPLIB  DD  DSN=RPB.LINKLIB,DISP=SHR
//SYSPRINT DD  SYSOUT=A
//SYSUT2   DD  UNIT=(&TAPE,,DEFER),VOL=SER=&ID,LABEL=(,BLP),
//             DCB=DEN=&DEN
//       PEND
//*
//TEMP0  EXEC  PGM=IEBGENER
//SYSPRINT DD  DUMMY
//SYSIN    DD  DUMMY
//SYSUT2   DD  DSN=&&TEMP0,UNIT=SYSDA,DISP=(,PASS),
//             DCB=(RECFM=FB,BLKSIZE=2560,LRECL=80),
//             SPACE=(TRK,(1,1))
//SYSUT1 DD *
         MACRO
&NAME    STBR  &N=1
         LCLA  &L,&A,&B,&C,&R,&R1
         LCLA  &C1,&C2
         LCLC  &CH,&CSECT,&SEP,&DT1,&DT2,&DT3
.*
&SEP     SETC  ' - '
&C1      SETA  &N
&CH      SETC  '&NAME'
         AIF   ('&CH' NE '').N1
&CH      SETC  'ASM'
.N1      ANOP
&CSECT   SETC  '&CH'
.*
&DT1     SETC  '&SYSDATE'(4,2)         MM/DD/YY --> DD/MM/YY
&DT2     SETC  '&SYSDATE'(1,2)
&DT3     SETC  '&SYSDATE'(7,2)
&CH      SETC  '&CH.&SEP.&DT1./&DT2./&DT3.&SEP.&SYSTIME'
.*
&C2      SETA  K'&CH
&L       SETA  (&C2/4)*4+3
&C       SETA  &L+5
&B       SETA  &C+4
&A       SETA  &B+90
.*
&CSECT   CSECT
         B     &A.(0,15)
         DC    AL1(&C2.),CL&L.'&CH'
         DC    18A(0)
         LA    15,0
         L     13,4(0,13)
         L     14,12(0,13)
         LM    0,12,20(13)
         MVI   12(13),255
         BR    14
         STM   14,12,12(13)
         ST    13,&B.(0,15)
         LR    12,13
         LA    13,&C.(0,15)
         ST    13,8(0,12)
.*
&R       SETA  13
.X       USING &CSECT+&C,&R
         AIF   (&C1 EQ 1).Y
&R1      SETA  &R-1
         LA    &R1,2048(0,&R)
         LA    &R1,2048(0,&R1)
&C       SETA  &C+4096
&R       SETA  &R1
&C1      SETA  &C1-1
         AGO   .X
.Y       MEXIT
         MEND
         EJECT
*
*     TAPETEST IS A PROGRAM DESIGNED TO TEST MAGNETIC TAPES BY
*   WRITING RECORDS FROM LOAD POINT TO THE END-OF-TAPE REFLECTIVE
*   MARKER AND THEN VERIFYING THEM BY READING BACKWARDS UNTIL LOAD
*   POINT IS REACHED.
*
*     EACH RECORD CONSISTS OF A 4-BYTE RECORD COUNTER FOLLOWED BY
*   SUFFICIENT X'FF' BYTES TO COMPLETE THE DESIRED RECORD SIZE.
*
*     THE PROGRAM USES EXCP TO READ AND WRITE TO THE TAPE UNITS,
*   AND SO MAY 'HOG' THE TAPE CHANNEL.  AN OPERATOR REPLY IS THERE-
*   FORE PROVIDED TO 'P'AUSE AND 'R'ESUME TESTING, OR TO PRINT OUT
*   THE NUMBER OF FEET OF TAPE TESTED AND TERMINATE THE PROGRAM.
*
*     THE DENSITY AND LENGTH OF RECORDS WRITTEN ARE SPECIFIED AT
*   ASSEMBLY TIME, AND TYPICAL VALUES ARE AS FOLLOWS:
*
*       DEN=3 (1600 BPI)  IRG=3   LEN=12
*       DEN=4 (6250 BPI)  IRG=3   LEN=4
*
*   WHERE THE INTER-RECORD GAP IS SPECIFIED IN 10THS OF INCHES
*   AND THE LENGTH (LEN) IS THE TOTAL RECORD LENGTH IN INCHES
*   INCLUDING THE IRG VALUE.
*
*     AUTHOR:  R.P. BACKSTROM,
*              AUSTRALIAN ATOMIC ENERGY COMMISSION,
*              LUCAS HEIGHTS RESEARCH LABORATORIES,
*              SUTHERLAND,
*              N.S.W. 2232,
*              AUSTRALIA.
*                                               AUGUST 1982.
*
         SPACE
/*
//TEMP1  EXEC  PGM=IEBGENER
//SYSPRINT DD  DUMMY
//SYSIN    DD  DUMMY
//SYSUT2   DD  DSN=&&TEMP1,UNIT=SYSDA,DISP=(,PASS),
//             DCB=(RECFM=FB,BLKSIZE=2560,LRECL=80),
//             SPACE=(TRK,(1,1))
//SYSUT1 DD *
TAPETST3 STBR  ,                       DEN=3 (8TH LETTER MUST BE DEN)
         SPACE
IRG      EQU   3                       INTER-RECORD GAP = .3 INCHES
ERG      EQU   7                       ERASE GAP LENGTH IN 1/2 INS.
LEN      EQU   12                      LENGTH OF RECORD (IN INCHES)
*                                      INCLUDING INTER-RECORD GAP
DEN      EQU   1600                    1600 BYTES PER INCH
/*
//TEMP2  EXEC  PGM=IEBGENER
//SYSPRINT DD  DUMMY
//SYSIN    DD  DUMMY
//SYSUT2   DD  DSN=&&TEMP2,UNIT=SYSDA,DISP=(,PASS),
//             DCB=(RECFM=FB,BLKSIZE=2560,LRECL=80),
//             SPACE=(TRK,(1,1))
//SYSUT1 DD *
TAPETST4 STBR  ,                       DEN=4 (8TH LETTER MUST BE DEN)
         SPACE
IRG      EQU   3                       INTER-RECORD GAP = .3 INCHES
ERG      EQU   7                       ERASE GAP LENGTH IN 1/2 INS.
LEN      EQU   4                       LENGTH OF RECORD (IN INCHES)
*                                      INCLUDING INTER-RECORD GAP
DEN      EQU   6250                    6250 BYTES PER INCH
/*
//*
//TEMP3  EXEC  PGM=IEBGENER
//SYSPRINT DD  DUMMY
//SYSIN    DD  DUMMY
//SYSUT2   DD  DSN=&&TEMP3,UNIT=SYSDA,DISP=(,PASS),
//             DCB=(RECFM=FB,BLKSIZE=2560,LRECL=80),
//             SPACE=(TRK,(5,5))
//SYSUT1 DD *
         SPACE
LR       EQU   DEN*(10*LEN-IRG)/10     LRECL
LRECL    EQU   ((LR+7)/8)*8            ROUND UP TO DOUBLE-WORD
ERR      EQU   100                     MAXIMUM ERROR COUNT
*                                      SEE ALSO TP5L
LINK     EQU   1                       CHANNEL FOR LINK TAPES AT AAEC
*                                      SOFTWARE-SIMULATED CHANNEL,
*                                      DENSITY SET TO 800 BPI AND
*                                      NO READ-BACKWARDS PHASE.
         PRINT NOGEN
         L     1,0(0,1)                LOAD
         LH    2,0(0,1)                AND TEST
         LTR   2,2                     PARMFIELD
         BZ    TP30                    LENGTH
         CH    2,TP4A+2                EXTRACT
         BL    TP10                    LEADING
         LA    2,5                     SIX
         B     TP20                    CHARACTERS
TP10     MVI   TP15L,C' '              CLEAR
         MVC   TP15L+1(5),TP15L        VOLSER
         BCTR  2,0                     FIELD
TP20     EX    2,TP1X                  SET UP NEW ID
TP30     TIME  DEC                     READ THE TIME
         STM   0,1,TP1W                STORE TIME AND DATE
         MVI   TP1L,C'1'               NEW PAGE
         MVI   TP1L+1,C' '             CLEAR
         MVC   TP1L+2(119),TP1L+1      LINE
         MVC   TP1L+1(L'TP11L),TP11L   SET UP TIME, DATE, TAPE, ID
         L     15,4(0,13)              FIND
         L     15,16(0,15)             ENTRY POINT
         MVC   TP1L+1+L'TP11L-12(1),12(15)  ADD DENSITY CODE
         SR    14,14                   LOAD
         IC    14,12(0,15)             DENSITY BYTE
         SLL   14,2                    MULTIPLY BY 4
         LA    14,TP12L-4*C'0'(14)     SELECT
         MVC   TP1L+1+L'TP11L-9(4),0(14)  DENSITY
         UNPK  TP2W(7),TP1W(4)         UNPACK
         MVC   TP1L+6(2),TP2W          SET
         MVC   TP1L+9(2),TP2W+2        UP
         MVC   TP1L+12(2),TP2W+4       TIME
         UNPK  TP2W(5),TP1W+5(3)       UNPACK
         MVC   TP1L+22(2),TP2W         SET UP
         MVC   TP1L+25(3),TP2W+2       DATE
         GETMAIN VU,LA=TP1A,A=TP1A+8   REQUEST BUFFER AREA
         LA    15,4                    LOAD RETURN CODE
         CLC   TP1A(4),TP1A+12         EXIT ON
         BH    76(0,13)                GETMAIN ERROR
         OPEN  (TP1D,OUTPUT)           OPEN SYSPRINT
         LA    15,8                    TEST
         TM    TP1D+48,X'10'           SUCCESS
         BNO   76(0,13)                ERROR EXIT
         L     1,TP1A+8                SET
         ST    1,TP1CC                 BUFFER
         MVI   TP1CC,1                 ADDRESS
         ST    1,TP6CC                 IN OUTPUT
         MVI   TP6CC,1                 COMMANDS
         A     1,TP1A+12               SET END
         BCTR  1,0                     OF BUFFER
         ST    1,TP3CC                 ADDRESS
         MVI   TP3CC,12                IN READ
         ST    1,TP7CC                 BACKWARDS
         MVI   TP7CC,12                COMMANDS
         L     1,TP1A+8                SET
         LA    2,1                     REGISTERS
         LCR   2,2                     TO
         LR    3,2                     MINUS 1
         L     4,TP1A+12               LOAD DOUBLE
         SRL   4,3                     WORD COUNT
TP40     STM   2,3,0(1)                SET WHOLE
         LA    1,8(0,1)                BUFFER
         BCT   4,TP40                  TO -1
         L     1,16                    LOAD CVT POINTER
         L     1,0(0,1)                LOAD TCB POINTER
         L     1,4(0,1)                CURRENT TCB ADDRESS
         L     1,12(0,1)               TIOT ADDRESS
         LA    1,24(0,1)               SKIP STEPNAMES
         LA    2,0                     CLEAR REGISTER 2
TP50     CLC   TP2D+40(8),4(1)         COMPARE DDNAMES
         BE    TP70                    BRANCH IF EQUAL
         IC    2,0(0,1)                LOAD LENGTH OF ENTRY
         LTR   2,2                     TEST FOR END
         BZ    TP60                    OF TIOT
         AR    1,2                     INCREMENT POINTER
         B     TP50                    REPEAT TEST
TP60     MVI   TP1L,C' '               CLEAR
         MVC   TP1L+1(120),TP1L        LINE
         MVI   TP1L,C'0'               DOUBLE SPACE
         MVC   TP1L+1(22),TP2L         SYSUT2
         PUT   TP1D,TP1L               DD CARD MISSING
         B     TP320                   EXIT
TP70     L     1,16(0,1)               LOAD UCB ADDRESS
         MVC   TP80+13(3),13(1)        EXTRACT
         MVC   TPM20+25(3),13(1)       UNIT
         MVC   TPF10+17(3),13(1)       NUMBER
         MVC   TPM40+25(3),13(1)       SET IN MESSAGES
         MVC   TP6B(2),4(1)            AND UNIT IN HEX
         NI    TP6B,X'07'              CLEAR TOP 5 BITS
         TM    18(1),X'80'             TEST FOR
         BO    TP90                    TAPE DEVICE
TP80     WTO   'UNIT XXX NOT TAPE DEVICE - TAPETEST TERMINATED',       X
               ROUTCDE=3,DESC=4
         MVI   TP1L,C' '               CLEAR
         MVC   TP1L+1(120),TP1L        LINE
         MVI   TP1L,C'0'               DOUBLE SPACE
         MVC   TP1L+1(21),TP4L         INCORRECT
         PUT   TP1D,TP1L               DEVICE TYPE ON SYSUT2
         B     TP320                   EXIT
TP90     CLI   TP6B,LINK               TEST FOR
         BNE   TP100                   LINK TAPE
         LA    1,800                   CHANGE
         STH   1,TP1CC+6               BYTE
         STH   1,TP6CC+6               COUNTS
         LA    1,6                     CHANGE RECORD LENGTH
         ST    1,TP2A                  TO 6 HALF INCHES
TP100    LA    9,1                     INITIALISE RECORD COUNT
         LA    10,ERR+1                SET MAXIMUM ERROR COUNT
         LA    11,1                    INITIALISE LENGTH
         MVC   TP1L+36(3),TP80+13      EXTRACT TAPE UNIT
         MVC   TP1L+45(6),TP15L        AND VOLSER
         PUT   TP1D,TP1L               PRINT THE LINE
         MVI   TP1L,C'0'               DOUBLE SPACE
         OI    TP2D+49,X'0C'           PREVENT ERROR RETRY
         OPEN  TP2D                    OPEN TAPE DCB
         TM    TP2D+48,X'10'           TEST
         BO    TP110                   OPEN
         MVI   TP1L+1,C' '             CLEAR
         MVC   TP1L+2(119),TP1L+1      LINE
         MVC   TP1L+1(24),TP9L         OPEN UNSUCCESSFUL
         B     TP300                   EXIT
TP110    BAL   14,TPM                  TEST
         B     TP330                 0 REPLY
         LA    1,TP1CC               4 SET
         ST    1,TP3B                  WRITE CAW
         L     1,TP1A+8                SET RECORD COUNT
         ST    9,0(0,1)                IN 1ST WORD
         EXCP  TP1B                    WRITE RECORD
         WAIT  ECB=TP4B                WAIT FOR I/O
         CLI   TP4B,X'7F'              TEST FOR
         BNE   TP130                   ERRORS
TP120    LA    9,1(0,9)                INCREMENT RECORD COUNT
         A     11,TP2A                 INCREMENT LENGTH COUNTER
         B     TP110                   CONTINUE
TP130    TM    TP2B+4,1                END OF
         BO    TP220                   TAPE ?
         BAL   14,TPF                  CHECK FOR
         B     TP110                 0 FILE PROTECT
         TM    TP1B+2,X'40'          4 RETRY IF
         BNO   TP140                   INTERVENTION REQUIRED
         BAL   14,TPW                  WAIT FOR A FEW SECONDS
         B     TP110                   RETRY
TP140    L     1,TP3A                  TEST FOR
         LTR   1,1                     DOM
         BZ    TP150                   OUTSTANDING
         DOM   MSG=(1)                 DELETE MESSAGE
         SR    1,1                     CLEAR
         ST    1,TP3A                  DOM ID
TP150    MVI   TP1L+1,C' '             CLEAR
         MVC   TP1L+2(119),TP1L+1      LINE
         MVC   TP1L+1(L'TP6L),TP6L     SET MESSAGE IN LINE
         LA    1,TP1L+37               POINT TO NUMBER
         LR    7,9                     SET UP RECORD COUNT
         BAL   14,TPN                  SET IN LINE
         LA    1,TP1L+43               POINT TO NUMBER
         LA    6,0                     DIVIDE
         LR    7,11                    HALF INCH COUNT
         D     6,TP5A                  BY 24
         LA    7,1(0,7)                ROUND UP
         BAL   14,TPN                  CONVERT TO DECIMAL
         BCTR  1,0                     RESET
         MVI   0(1),C'('               BRACKET
         LA    8,9                     SET RETRY COUNTER
         BCT   10,TP170                TEST TOTAL ERROR COUNT
TP160    MVI   TP1L,C'0'               DOUBLE SPACE
         MVI   TP1L+1,C' '             CLEAR
         MVC   TP1L+2(119),TP1L+1      LINE
         MVC   TP1L+1(L'TP5L),TP5L     ERROR COUNT
         B     TP300                   EXCEEDED
TP170    BAL   14,TPM                  TEST
         B     TP330                   REPLY
         LA    1,TP2CC                 SET RETRY
         ST    1,TP3B                  CAW
         LCR   7,8                     INCLUDE
         LA    7,10(0,7)               RETRY NUMBER
         L     1,TP1A+8                AS 2ND WORD
         ST    7,4(0,1)                OF RECORD
         EXCP  TP1B                    REWRITE RECORD
         WAIT  ECB=TP4B                WAIT FOR I/O
         CLI   TP4B,X'7F'              TEST FOR
         BE    TP200                   NO ERROR
         BAL   14,TPF                  CHECK FOR
         B     TP170                 0 FILE PROTECT
         TM    TP1B+2,X'40'          4 RETRY IF
         BNO   TP180                   INTERVENTION REQUIRED
         BAL   14,TPW                  WAIT FOR A FEW SECONDS
         B     TP170                   RETRY
TP180    L     1,TP3A                  TEST
         LTR   1,1                     DOM
         BZ    TP190                   OUTSTANDING
         DOM   MSG=(1)                 DOM
         SR    1,1                     CLEAR
         ST    1,TP3A                  DOM ID
TP190    LA    11,ERG(0,11)            ADD ERASE GAP LENGTH
         BCT   8,TP170                 RETRY
         B     TP210                   BRANCH
TP200    MVC   TP1L+1(9),TP13L         SET UP TEMPORARY
         MVC   TP1L+55(17),TP14L       AND RETRY MSG
         LCR   7,8                     AND
         LA    7,250(0,7)              CALCULATE
         STC   7,TP1L+74               NUMBER
TP210    PUT   TP1D,TP1L               PRINT ERROR MESSAGE
         MVI   TP1L,C' '               SINGLE SPACE
         L     1,TP1A+8                RESET RECORD
         MVC   4(4,1),8(1)             WORD TO -1
         B     TP120                   CONTINUE
TP220    MVI   TP1L,C'0'               DOUBLE SPACING
         MVI   TP1L+1,C' '             CLEAR
         MVC   TP1L+2(119),TP1L+1      LINE
         MVC   TP1L+1(L'TP7L),TP7L     SET
         LA    6,0                     UP
         LR    7,11                    TAPE
         D     6,TP5A                  LENGTH
         LA    7,1(0,7)                MESSAGE
         LA    1,TP1L+13               AT END
         BAL   14,TPN                  OF TAPE
         CLI   TP6B,LINK               TEST FOR
         BE    TP300                   LINK TAPE
         PUT   TP1D,TP1L               PRINT THE LINE
         LA    10,ERR+1                SET MAXIMUM ERROR COUNT
         B     TP240                   READ BACKWARDS
TP230    S     11,TP2A                 SUBTRACT RECORD + IRG LENGTH
TP240    BAL   14,TPM                  TEST
         B     TP330                   REPLY
         LA    1,TP3CC                 SET READ
         ST    1,TP3B                  BACKWARDS CAW
         EXCP  TP1B                    READ BACKWARDS
         WAIT  ECB=TP4B                WAIT FOR I/O
         TM    TP1B+3,8                TEST FOR
         BO    TP310                   LOAD POINT
         CLI   TP4B,X'7F'              TEST FOR
         BE    TP230                   ERRORS
         TM    TP1B+2,X'40'            RETRY IF
         BNO   TP250                   INTERVENTION REQUIRED
         BAL   14,TPW                  WAIT FOR A FEW SECONDS
         B     TP240                   RETRY
TP250    MVI   TP1L+1,C' '             CLEAR
         MVC   TP1L+2(119),TP1L+1      LINE
         MVC   TP1L+1(L'TP6L),TP6L     SET MESSAGE
         MVC   TP1L+11(5),TP8L         IN LINE
         LA    1,TP1L+37               POINT TO NUMBER
         L     7,TP1A+8                LOAD
         L     7,0(0,7)                RECORD COUNT
         BAL   14,TPN                  SET IN LINE
         LA    8,9                     SET RETRY COUNTER
         LA    1,TP1L+43               POINT TO NUMBER
         LA    6,0                     DIVIDE
         LR    7,11                    HALF INCH
         D     6,TP5A                  COUNT BY 24
         LA    7,1(0,7)                ROUND UP
         BAL   14,TPN                  SET IN PLACE
         BCTR  1,0                     RESET
         MVI   0(1),C'('               BRACKET
         BCT   10,TP260                TEST TOTAL ERROR COUNT
         B     TP160                   EXIT IF REACHED
TP260    BAL   14,TPM                  TEST
         B     TP330                   REPLY
         LA    1,TP4CC                 SET RETRY
         ST    1,TP3B                  READ BACKWARDS CAW
         EXCP  TP1B                    REREAD RECORD
         WAIT  ECB=TP4B                WAIT FOR I/O
         TM    TP1B+3,8                TEST FOR
         BO    TP310                   LOAD POINT
         CLI   TP4B,X'7F'              TEST FOR
         BE    TP280                   NO ERROR
         TM    TP1B+2,X'40'            BRANCH IF NO
         BNO   TP270                   INTERVENTION REQUIRED
         BAL   14,TPW                  WAIT FOR A FEW SECONDS
         B     TP260                   RETRY
TP270    BCT   8,TP260                 RETRY
         B     TP290                   BRANCH
TP280    MVC   TP1L+1(9),TP13L         SET UP TEMPORARY
         MVC   TP1L+55(17),TP14L       AND RETRY MSG
         LCR   7,8                     SET UP
         LA    7,250(0,7)              RETRY
         STC   7,TP1L+74               COUNT
TP290    PUT   TP1D,TP1L               PRINT THE LINE
         MVI   TP1L,C' '               SINGLE SPACE
         B     TP240                   CONTINUE
TP300    PUT   TP1D,TP1L               PRINT THE LINE
TP310    CLOSE (TP2D,REWIND)           CLOSE SYSUT2
TP320    MVI   TP1L,C'0'               DOUBLE SPACE
         MVI   TP1L+1,C' '             CLEAR
         MVC   TP1L+2(119),TP1L+1      LINE
         MVC   TP1L+1(13),TP11L        SET UP TIME MESSAGE
         TIME  DEC                     READ THE TIME
         ST    0,TP1W                  STORE VALUE
         UNPK  TP2W(7),TP1W(4)         UNPACK
         MVC   TP1L+6(2),TP2W          SET
         MVC   TP1L+9(2),TP2W+2        UP
         MVC   TP1L+12(2),TP2W+4       TIME
         PUT   TP1D,TP1L               PRINT THE LINE
         CLOSE TP1D                    CLOSE SYSPRINT
         B     72(0,13)                EXIT
TP330    LA    1,TP5CC                 WRITE
         ST    1,TP3B                  TAPE MARK
         EXCP  TP1B                    WHEN CANCELLED
         WAIT  ECB=TP4B                BY OPERATOR
         MVI   TP1L,C'0'               DOUBLE SPACE
         MVI   TP1L+1,C' '             CLEAR
         MVC   TP1L+2(119),TP1L+1      LINE
         MVC   TP1L+1(L'TP3L),TP3L     SET UP MESSAGE
         L     7,TP1A+8                LOAD CURRENT
         L     7,0(0,7)                RECORD COUNT
         M     6,TP2A                  MULTIPLY BY 1/2 IN PER RECORD
         D     6,TP5A                  DIVIDE BY 1/2 IN PER FOOT
         LA    7,1(0,7)                ROUND UP
         LA    1,TP1L+25               POINT TO NUMBER
         BAL   14,TPN                  CONVERT TO DECIMAL
         B     TP300                   PRINT AND EXIT
*
*   TEST FOR FILE PROTECT
*
TPF      TM    TP1B+2,X'40'            EXIT IF NOT
         BO    4(0,14)                 YET RELOADED
         TM    TP1B+3,2                TEST FOR
         BNO   4(0,14)                 FILE PROTECT
         ST    14,TP6A                 SAVE RETURN ADDRESS
TPF10    WTO   'TAPETEST:XXX SHOULD NOT BE FILE PROTECTED',            X
               ROUTCDE=3,DESC=2
         ST    1,TP3A                  STORE DOM ID
         LA    1,TP8CC                 ISSUE
         ST    1,TP3B                  REWIND
         EXCP  TP1B                    AND
         WAIT  ECB=TP4B                UNLOAD
         L     14,TP6A                 LOAD RETURN ADDRESS
         BR    14                      EXIT
*
*   TEST OPERATOR REPLY
*
TPM      TM    TP5B,X'40'              TEST FOR
         BNO   4(0,14)                 REPLY
         ST    14,TP6A                 SAVE RETURN ADDRESS
         WAIT  ECB=TP5B                WAIT
         OI    TP10L,C' '              TRANSLATE TO UPPER CASE
         CLI   TP10L,C'C'              IS IT
         BCR   8,14                    CANCEL ?
         CLI   TP10L,C'P'              IS IT
         BE    TPM30                   PAUSE ?
TPM10    SR    1,1                     CLEAR
         ST    1,TP5B                  WTOR ECB
TPM20    WTOR  'TAPETEST:XXX ''C''ANCEL OR ''P''AUSE',                 X
               TP10L,1,TP5B,ROUTCDE=3,DESC=3
         L     14,TP6A                 RESTORE RETURN ADDRESS
         B     4(0,14)                 AND RETURN
TPM30    SR    1,1                     CLEAR
         ST    1,TP7B                  PAUSE ECB
TPM40    WTOR  'TAPETEST:XXX ''C''ANCEL OR ''R''ESUME',                X
               TP10L,1,TP7B,ROUTCDE=3,DESC=3
         WAIT  ECB=TP7B                WAIT
         L     14,TP6A                 LOAD RETURN ADDRESS
         OI    TP10L,C' '              ENSURE UPPER CASE
         CLI   TP10L,C'C'              IS IT
         BCR   8,14                    CANCEL ?
         CLI   TP10L,C'R'              IS IT
         BE    TPM10                   RESUME ?
         B     TPM30                   REPEAT QUESTION
*
*   CONVERT TO DECIMAL
*
TPN      CVD   7,TP1W                  CONVERT TO
         UNPK  TP2W(7),TP1W+5(4)       DECIMAL
         MVC   0(5,1),TP2W             SET IN LINE
TPN10    CLI   0(1),C'0'               TEST FOR
         BCR   7,14                    LEADING
         MVI   0(1),C' '               ZERO
         LA    1,1(0,1)                CHANGE
         B     TPN10                   TO BLANK
*
*   WAIT FOR A FEW SECONDS
*
TPW      STIMER WAIT,DINTVL=TP3W       WAIT FOR A FEW SECONDS
         BR    14                      RETURN
         SPACE
TP1A     DC    A(LRECL,LRECL)          MIN, MAX GETMAIN AREA
         DC    A(0,0)                  ADDRESS, LENGTH
TP2A     DC    A(LEN+LEN)              RECORD + IRG IN HALF INCHES
TP3A     DC    A(0)                    DOM ID FOR FILE PROTECT MSG
TP4A     DC    A(6)                    MAX VOLSER LENGTH
TP5A     DC    A(24)                   HALF INCH COUNT PER FOOT
TP6A     DC    A(0)                    RETURN ADDRESS
         SPACE
*
*   IOB
*
TP1B     DC    0A(0),X'42',AL3(0)      IOB
         DC    A(TP4B)                 ECB ADDRESS
TP2B     DC    A(0,0)                  CSW
TP3B     DC    A(0)                    CAW
         DC    A(TP2D)                 DCB ADDRESS
         DC    A(0)                    RESTART ADDRESS
         DC    AL2(1,0)                BLOCK COUNT INCREMENT
*
TP4B     DC    A(0)                    EXCP ECB
TP5B     DC    X'40000000'             WTOR ECB (INITIALLY POSTED)
TP6B     DC    A(0)                    TAPE UNIT IN HEX (BYTES 0,1)
TP7B     DC    A(0)                    PAUSE WTOR ECB
         SPACE
TP1CC    CCW   1,0,X'20',LRECL         WRITE
TP2CC    CCW   X'27',0,X'60',1         BACKSPACE RECORD
         CCW   X'17',0,X'60',1         ERASE GAP
TP6CC    CCW   1,0,X'20',LRECL         WRITE
TP3CC    CCW   12,0,0,LRECL            READ BACKWARDS
TP4CC    CCW   X'37',0,X'60',1         FORWARD SPACE
TP7CC    CCW   12,0,0,LRECL            READ BACKWARDS
TP5CC    CCW   X'1F',0,X'20',1         WRITE TAPE MARK
TP8CC    CCW   X'0F',0,X'20',1         REWIND/UNLOAD
         SPACE
         PRINT NOGEN
         SPACE
TP1D     DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,EXLST=TP1E,           X
               RECFM=FBA,LRECL=121
TP2D     DCB   DSORG=PS,MACRF=E,DDNAME=SYSUT2,IOBAD=TP1B
         SPACE
TP1E     DC    0A(0),X'85',AL3(*+3)    SYSPRINT OPEN EXIT
         LH    0,62(0,1)               TEST
         LTR   0,0                     BLKSIZE
         BNZR  14                      RETURN IF SET
         LA    0,1210                  SET TO 1210
         STH   0,62(0,1)               IN DCB
         BR    14                      RETURN
         SPACE
TP1L     DS    121C
TP2L     DC    C'SYSUT2 DD CARD MISSING'
TP3L     DC    C'CANCELLED BY OPERATOR AT XXXX FEET'
TP4L     DC    C'INCORRECT DEVICE TYPE'
TP5L     DC    C'ERROR COUNT EXCEEDS 100'
TP6L     DC    C'PERMANENT WRITE ERROR AT RECORD NO. XXXXX (XXXX FEET)'
TP7L     DC    C'TAPE LENGTH: XXXX FEET'
TP8L     DC    C'READ '
TP9L     DC    C'SYSUT2 OPEN UNSUCCESSFUL'
TP10L    DC    C' '
TP11L    DC    C'TIME=HH.MM.SS   DATE=YY.DDD   TAPE=UUU   ID=SCRTCH   DX
               EN=X (NNNN BPI)'
         SPACE
TP12L    DC    C' 200'                 DEN=0  200 BPI
         DC    C' 556'                 DEN=1  556 BPI
         DC    C' 800'                 DEN=2  800 BPI
         DC    C'1600'                 DEN=3 1600 BPI
         DC    C'6250'                 DEN=4 6250 BPI
         DC    C'DEN5'                 DEN=5
         DC    C'DEN6'                 DEN=6
         DC    C'DEN7'                 DEN=7
         DC    C'DEN8'                 DEN=8
         DC    C'DEN9'                 DEN=9
         SPACE
TP13L    DC    C'TEMPORARY'
TP14L    DC    C'- NO OF RETRIES ='
TP15L    DC    C'SCRTCH'
         SPACE
TP1W     DS    D
TP2W     DS    D
TP3W     DC    C'00000500'             5 SECOND INTERVAL
         SPACE
TP1X     MVC   TP15L(0),2(1)           SET ID IN PLACE
         SPACE
         END
/*
//TP3    EXEC  ASMFCL,PARM.ASM='NODECK,LOAD,TERM',
//             PARM.LKED='LIST,MAP',COND.LKED=(0,NE)
//ASM.SYSIN DD DSN=&&TEMP0,DISP=(OLD,PASS)
//          DD DSN=&&TEMP1,DISP=(OLD,DELETE)
//          DD DSN=&&TEMP3,DISP=(OLD,PASS)
//LKED.SYSLMOD DD DSN=RPB.LINKLIB(TAPETST3),DISP=SHR
//*
//TP4    EXEC  ASMFCL,PARM.ASM='NODECK,LOAD,TERM',
//             PARM.LKED='LIST,MAP',COND.LKED=(0,NE)
//ASM.SYSIN DD DSN=&&TEMP0,DISP=(OLD,DELETE)
//          DD DSN=&&TEMP2,DISP=(OLD,DELETE)
//          DD DSN=&&TEMP3,DISP=(OLD,DELETE)
//LKED.SYSLMOD DD DSN=RPB.LINKLIB(TAPETST4),DISP=SHR
//*
//TST3   EXEC  TAPETEST,ID=RPBTST,DEN=3,COND=(0,NE)
//*
//TST4   EXEC  TAPETEST,ID=RPBSCR,DEN=4,COND=(0,NE)
