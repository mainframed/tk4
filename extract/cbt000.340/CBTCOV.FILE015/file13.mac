         PRINT NOGEN
         SPACE
SSCSA    CSECT
         SAVE  (14,12),,*
         BALR  R10,0
         USING *,R10
         LA    R3,SAV
         ST    R13,4(R3)
         ST    R3,8(R13)
         LR    R13,R3
         SPACE 2
         OPEN  (OUT,(OUTPUT))
         SPACE 2
         SPIE  PIC,(4,5)           TRAP OC4,OC5
         ST    R1,APICA            STORE ADDR PREV PICA
         SPACE 3
         BAL   R8,TSTAMP           PUT OUT DATE/TIME
         SPACE 2
         L     R2,16               CVT
         L     R2,X'230'(R2)       GDA
         L     R3,8(R2)            CSA PQE
         SR    R5,R5               TOT
GETFBQE  L     R4,0(R3)            A(1ST FBQE)
INLIN    CR    R3,R4               IF NO MORE
         BE    GETOUT              THEN GET OUT
         BAL   R14,INITLINE
         MVC   TITLE(13),=C'CSA FBQE SIZE'
         L     R1,8(R4)            SIZE
         AR    R5,R1               TOT
         BAL   R9,ADCON
         BAL   R9,PUTLINE
         L     R4,0(R4)            NEXT FBQE
         B     INLIN
         SPACE 2
GETOUT   EQU   *
         L     R3,8(R3)            NEXT PQE
         LTR   R3,R3
         BNZ   GETFBQE
         SPACE
         BAL   R8,SPACE
         BAL   R14,INITLINE
         MVC   TITLE(18),=C'CSA FREE BLOCK TOT'
         LR    R1,R5
         BAL   R9,ADCON
         BAL   R9,PUTLINE
         SPACE 2
         BAL   R8,SPACE
         BAL   R14,INITLINE
         MVC   TITLE(19),=C'SQA SPACE AVAILABLE'
         L     R1,X'1C'(R2)        SQA SPACE FROM GDA
         BAL   R9,ADCON
         BAL   R9,PUTLINE
         SPACE 3
         L     R2,16               CVT
         SPACE
         L     R3,360(R2)          LPA DIRECTORY
         LA    R3,0(R3)            ENSURE HI BYTE CLEAR
         ST    R3,KEYEND           STOP WHEN WE RUN INTO LPA
         SPACE
         L     R3,356(R2)          A(PVT)
         L     R3,12(R3)           A(PAGE FRAME TABLE)
         ST    R3,APFT             KEEP IT
         L     R3,416(R2)          START OF CSA
         SPACE
         L     R2,0(R2)            TCBWORDS
         L     R2,12(R2)           ASCB
         L     R2,52(R2)           RSMHD
         L     R2,0(R2)            VSA SEG TAB
         ST    R2,ASEG             KEEP IT
         SPACE
KEYLOOP  EQU   *
         LA    R1,0(R3)            COPY ADDR OF PAGE
         SRL   R1,16               SEG NO TO LO BYTE
         SLL   R1,2                *4
         A     R1,ASEG             ADDR OF SEG TAB ENTRY
         L     R15,0(R1)           RA(PT) XXRBNDDD
         SRL   R15,1               SEG INVALID BIT OUT
         SLL   R15,1
         C     R15,0(R1)
         BNE   KEYNEXT             IF SEG INVALID GET OUT
         LR    R1,R15              XXRBNDDD
         N     R15,=X'00000FFF'    GET RID OF THE REST  00000DDD
         N     R1,=X'00FFF000'     00RBN000
         SRL   R1,8                0000RBN0
         A     R1,APFT             ADDR OF PFT ENTRY
         SR    R14,R14
         ICM   R14,6,2(R1)         00VBN000
         OR    R15,R14             00VBNDDD    ADDR OF PAGE TABLE
         N     R15,=X'FFFFFFF8'    GET RID OF GARBAGE BITS
         LA    R14,16              NUMBER OF PAGES
         LR    R4,R15              A(PAGE TAB)
         LA    R15,16*2(R15)       A(XPTE)
PERSEG   SR    R2,R2
         IC    R2,0(R15)           P KEY
         SRL   R2,4                FRCX OUT
         SLL   R2,2                *4
         TM    1(R4),X'01'         GETMAINED?
         BNO   NOKEYAD
         L     R1,KEYTAB(R2)
         LA    R1,1(R1)            COUNT NUMBER OF PAGES WITH THIS
         ST    R1,KEYTAB(R2)       PROT KEY
         SPACE
NOKEYAD  LA    R4,2(R4)            NEXT IN PAGE TAB
         LA    R15,12(R15)         NEXT ENT IN XPTE
         BCT   R14,PERSEG
         SPACE
KEYNEXT  A     R3,=A(16*4096)      TO NEXT ENTRY IN SEG TAB
*                                  (64K SEGMENTS)
         C     R3,KEYEND           HI ADDR
         BL    KEYLOOP
         SPACE 2
         LA    R2,KEYTAB
         LA    R3,16               NUMBER OF ENTRIES
         SPACE
         BAL   R8,SPACE
KEYPR    BAL   R14,INITLINE
         MVC   TITLE(3),=C'KEY'
         LA    R14,16
         SR    R14,R3
         CVD   R14,DUB
         UNPK  TITLE+3(3),DUB+6(2)
         OI    TITLE+5,X'F0'
         L     R1,0(R2)            COUNT
         BAL   R9,ADCON
         BAL   R9,PUTLINE
         LA    R2,4(R2)            NEXT COUNTER
         BCT   R3,KEYPR
         SPACE 3
         L     R1,APICA            PREV PICA
         SPIE  MF=(E,(1))          RESTORE
         SPACE
         CLOSE OUT
         L     R13,4(R13)
         RETURN (14,12),RC=0
         SPACE 2
PIC      MVC   8(4,R1),EXITADDR    CONTINUE ADDR INTO PIE
         BR    R14
         SPACE 2
INITLINE MVC   LINE,LINE-1
         MVC   TITLE,FILL
         BR    R14
         SPACE
SPACE    MVC   LINE,LINE-1
         BAL   R9,PUTLINE
         BR    R8
         SPACE 2
PUTLINE  PUT   OUT,LINE
         BR    R9
         SPACE 2
TSTAMP   BAL   R14,INITLINE
         MVC   TITLE(L'TSC),TSC
         TIME
         SRL   R1,4                000YYDDD
         ST    R1,DUB              000YYDDD
         ST    R0,DUB+4            000YYDDDHHMMSSTH
         MVC   ADDR(TPATL),TPAT
         ED    ADDR(TPATL),DUB+1
         BAL   R9,PUTLINE
         B     SPACE
         SPACE 2
ABEND    ABEND 1,DUMP              OC4 OR 5
         SPACE 2
ADCON    ST    R1,WORK
         BAL   R14,HCON
         STCM  R1,3,ADDR+6
         IC    R1,WORK+2
         BAL   R14,HCON
         STCM  R1,3,ADDR+4
         IC    R1,WORK+1
         BAL   R14,HCON
         STCM  R1,3,ADDR+2
         IC    R1,WORK
         BAL   R14,HCON
         STCM  R1,3,ADDR
         BR    R9
         SPACE 2
HCON     DS    0H                  CONVERTS THE LO ORDER BYTE OF R1
*                                  TO A 2 BYTE PRINTABLE HEX VALUE
*                                  IN THE 2 LO BYTES OF R1
         N     R1,=F'255'          IGNORE TOP 3 BYTES
         LR    R15,R1              COPY BYTE TO R15
         N     R1,=F'15'           CLEAR 4 HI BITS OF LO BYTE
         IC    R1,HTAB(R1)         LO NYBBLE CHARACTER
         SRL   R15,4               THROW OUT 4 LO BITS
         LA    R15,HTAB(R15)       POINT TO HI NYBBLE CHARACTER
         ICM   R1,2,0(R15)         AND PUT IT INTO R1
         BR    R14
         SPACE  2
HTAB     DC    C'0123456789ABCDEF' FOR CONV TO PRINTABLE HEX
         SPACE 3
DUB      DS    D
SAV      DS    18F
APICA    DS    F
WORK     DS    F
ASEG     DS    A                   ADDR OF SEG TAB
APFT     DS    A                   ADDR OF PAGE FRAME TAB
KEYEND   DS    A                   STOP POINT
KEYTAB   DC    16F'0'
         SPACE
EXITADDR DC    A(ABEND)
         DC    C' '
LINE     DS    0CL80
         DS    C
TITLE    DS    CL20
         DS    C
ADDR     DS    CL8
         DS    CL50
         SPACE
         DC    53C' '
FILL     DC    20C'-'
NOTF     DC    CL8'NOT AV.'
TSC      DC    C'DATE/TIME'
TPAT     DC    X'40202120'
         DC    C':'
         DC    X'202020404020204B20204B2020'
TPATL    EQU   *-TPAT
         SPACE 2
OUT      DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=F,BLKSIZE=80
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
