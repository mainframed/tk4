CMNER41  $PROLOG
***********************************************************************
*        PROGRAM - CMNER41                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS A REENTRANT ROUTINE WHICH AUTHORIZES ITSELF*
*               BUILDS A RALT COMMAND AND BRANCHES TO ISPCAT. ISPCAT  *
*               IS A SPF SUBROUTINE WHICH ATTACHES AND EXECUTES TSO   *
*               COMMANDS.                                             *
*                                                                     *
*        PANELS USED - CMNER41                                        *
*                                                                     *
*        ERROR MESSAGES - CMNER410 - CMNER414                         *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         GETMAIN R,LV=DYNWKLN
         LR    R8,R1               COPY ADDR OF GOTTEN STORAGE
         LA    R9,DYNWKLN          LENGTH OF DYNAMIC AREA
         LR    R3,R9               COPY LENGTH FOR MOVE
         LA    R2,DYNWK            BEGINNING ADDR OF MOVE
         MVCL  R8,R2               MOVE INTO GOTTEN AREA
         USING DYNWK,R8
         LR    R8,R1               RESTORE TO BEGININNING
         BAL   R10,SETUP
C41000   BAL   R10,FILL
         MVC   CMNEBRES,CMNERRES   STORE RESOURCE
         MVC   CMNEBCLS,CMNERCLS       AND CLASS
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG05,)  DISPLAY 1ST PANEL
C41005   CH    R15,=H'8'           END ENTERED? ? ?
         BE    C41999              YES--
         LTR   R15,R15             CHECK RETURN
         BZ    C41010              OKAY
         ABEND 1,DUMP
         SPACE 5
C41010   CLC   CMNEBRES,CMNERRES   MAKE SURE RESOURCE AND
         BNE   C41000
         CLC   CMNEBCLS,CMNERCLS       CLASS WERE'NT CHANGED
         BNE   C41000
         MVI   CMD+5,C' '          CLEAR COMMAND BUFFER
         MVC   CMD+6(249),CMD+5
         MVC   CMD+5(8),CMNERCLS
         MVC   CMD+14(8),CMNERRES
         LA    R3,CMD+23           ADVANCE PAST RESOURCE
         MVC   0(6,R3),=C'OWNER('  MOVE IN KEYWORD
         MVC   6(L'CMNEROWN,R3),CMNEROWN MOVE IN OWNER
         TRT   6(L'CMNEROWN+1,R3),BLANKTAB SEARCH FOR 1ST BLANK
         BNZ   C41011
         ABEND 3,DUMP
C41011   MVI   0(R1),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R1)            ADVANCE
         MVC   0(5,R3),=C'UACC('   MOVE IN KEYWORD
         MVC   5(L'CMNERUAC,R3),CMNERUAC
         TRT   5(L'CMNERUAC+1,R3),BLANKTAB
         BNZ   C41012
         ABEND 3,DUMP
C41012   MVI   0(R1),C')'
         LA    R3,2(R1)            ADVANCE
         MVC   0(6,R3),=C'AUDIT('  MOVE IN KEYWORD
         MVC   6(L'CMNERAUD,R3),CMNERAUD MOVE IN AUDIT
         TRT   6(L'CMNERAUD+1,R3),BLANKTAB SEARCH FOR 1ST BLANK
         BNZ   C41013
         ABEND 3,DUMP
C41013   MVI   0(R1),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R1)            ADVANCE
         CLC   CMNERDAT,=CL30' '   WAS INST DATA ENTERED
         BE    C41014              NO--
         MVC   0(6,R3),=C'DATA(''' YES-- MOVE IN KEYWORD
         MVC   6(L'CMNERDAT,R3),CMNERDAT MOVE IN DATA
         LA    R3,L'CMNERDAT+6(R3) ADVANCE
         MVC   0(2,R3),=C''')'     MOVE IN CLOSING QUOTE AND PAREN
         LA    R3,3(R3)
C41014   CLC   CMNERAPP,=CL30' '   WAS APPL DATA ENTERED
         BE    C41020              NO
         MVC   0(10,R3),=C'APPLDATA(''' YES-- MOVE IN KEYWORD
         MVC   10(L'CMNERAPP,R3),CMNERAPP MOVE IN APPL DATA
         LA    R3,L'CMNERAPP+10(R3) ADVANCE
         MVC   0(2,R3),=C''')'     MOVE IN CLOSING QUOTE AND PAREN
         LA    R3,3(R3)
*
*
C41020   L     R2,X'21C'           TCB OLD
         L     R2,X'70'(R2)        ADDR OF 1ST SAVE AREA
         L     R2,X'18'(R2)        R1 IN 1ST SAVE AREA PTS TO PARM
         L     R2,0(R2)            FIRST PARM IS TLD
         CLC   =C'TLD',0(R2)       BE SURE
         BE    C41030
         ABEND 2,DUMP
C41030   ST    R2,ISPCATCL         STORE IN PARM LIST
         LA    R4,ISPCATRC         RELOCATE
         ST    R4,ISPCATCL+8               RETURN CODE
         LA    R4,CMDBUF           RELOCATE
         ST    R4,ISPCATCL+4               COMMAND BUFFER
         LOAD  EP=ISPSUBS
         LR    R3,R0               COPY EP OF ISPSUBS
         LA    R1,=C'AUTH'
         SVC   231                 MAKE AUTHORIZED
         L     R15,X'2C'(R3)       EP OF ISPCAT
         LA    R1,ISPCATCL
         BALR  R14,R15             GO TO IT
         L     R4,ISPCATRC         GET RETURN
         SR    R1,R1               REMOVE AUTHORIZATION
         SVC   231
         LTR   R4,R4               CHECK IT
         BZ    C41035
         ST    R4,CMNERRC
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG02,)
         B     C41040
C41035   CALL  ISPLINK,(VDISPLAY,PANEL,MSG01,)
C41040   CH    R15,=H'8'           CHECK FOR END KEY
         BE    C41999              YES--
         LTR   R15,R15             CHECK FOR ZERO
         BZ    C41050              YES--
         ABEND 1,DUMP
C41050   CLC   CMNERRES,=CL8' '
         BE    C41060              ISSUE ERROR MESSAGE
         CLC   CMNERCLS,=CL8' '
         BE    C41060              ISSUE ERROR MESSAGE
         CLC   CMNEBRES,CMNERRES   DID RESOURCE CHANGE ?
         BNE   C41000              YES, THEN GET INFO FOR NEW ONE
         CLC   CMNEBCLS,CMNERCLS   DID CLASS CHANGE ?
         BNE   C41000              YES, THEN GET INFO FOR NEW ONE
         B     C41010              OTHERISE MORE MODS TO THIS ONE
C41060   CALL  ISPLINK,(VDISPLAY,PANEL,MSG03,)
         B     C41040
C41999   FREEMAIN R,LV=DYNWKLN,A=(8)
         $EPILOG
         TITLE '***** SETUP ROUTINE *****'
SETUP    CALL  ISPLINK,(,LIST1,CMNERRES),MF=(E,ISPLNK1)
         CALL  ISPLINK,(,LIST2,CMNERRC),MF=(E,ISPLNK3)
         CALL  ISPLINK,(,LIST3,CMNERDAT),MF=(E,ISPLNK4)
         CALL  ISPLINK,(,LIST1),MF=(E,ISPLNK2)
         BR    R10
         TITLE '***** FILL VARIABLES WITH RACF DATA *****'
FILL     EQU   *
         MVI   RESLN,L'CMNERRES    SET MAX LENGTH
         TRT   CMNERRES,BLANKTAB   SEARCH FOR BLANK
         BZ    FILL10              NOT FOUND THEN MUST BE MAX LENGTH
         LA    R15,CMNERRES
         SR    R1,R15              COMPUTE LENGTH
         STC   R1,RESLN            STORE IT
FILL10   LA    R1,=C'AUTH'         MAKE AUTHORIZED
         SVC   231
         ICHEINTY LOCATE,ENTRY=RESLN,WKAREA=WK1,CLASS=CMNERCLS,        X
               MF=(E,TY1)
         LR    R14,R15             STORE RETURN
         SR    R1,R1
         SVC   231                 UNAUTHORIZE
         LTR   R14,R14
         BZ    FILL30              OKAY
         CH    R14,=H'12'          CHECK FOR NOT FOUND
         BE    FILL20              ISSUE MESSAGE
         ABEND 5,DUMP              OTHERISE SAY BYE
FILL20   CALL  ISPLINK,(VDISPLAY,PANEL,MSG04,,)
         CH    R15,=H'8'           CHECK RETURN
         BE    C41999
         LTR   R15,R15
         BZ    FILL                TRY AGAIN
         ABEND 1,DUMP
FILL30   MVC   CMNEROWN,WOWN       MOVE IN OWNER
         TM    WUACC,X'80'
         BNO   FILL32              FILL UACC FIELD
         MVC   CMNERUAC,=CL8'ALTER'
         B     FILL40
FILL32   TM    WUACC,X'40'
         BNO   FILL33              FILL UACC FIELD
         MVC   CMNERUAC,=CL8'CONTROL'
         B     FILL40
FILL33   TM    WUACC,X'20'
         BNO   FILL34              FILL UACC FIELD
         MVC   CMNERUAC,=CL8'UPDATE'
         B     FILL40
FILL34   TM    WUACC,X'10'
         BNO   FILL35              FILL UACC FIELD
         MVC   CMNERUAC,=CL8'READ'
         B     FILL40
FILL35   MVC   CMNERUAC,=CL8'NONE'
FILL40   TM    WAUDIT,X'80'        FILL IN AUDIT FIELD
         BNO   FILL42
         MVC   CMNERAUD,=CL8'ALL'
         B     FILL50
FILL42   TM    WAUDIT,X'40'        FILL IN AUDIT FIELD
         BNO   FILL44
         MVC   CMNERAUD,=CL8'SUCCESS '
         B     FILL50
FILL44   TM    WAUDIT,X'20'        FILL IN AUDIT FIELD
         BNO   FILL46
         MVC   CMNERAUD,=CL8'FAILURES'
         B     FILL50
FILL46   MVC   CMNERAUD,=CL8'NONE'
FILL50   XR    R15,R15             CLEAR REG
         IC    R15,WINST           GET LENGTH OF INST DATA
         CH    R15,=H'30'          CHECK FOR LESS THAN MAX
         BL    FILL60              YES
         LA    R15,30              SET MAX
FILL60   MVC   CMNERDAT,=CL30' ' CLEAR INST DATA FIELD
         LTR   R15,R15             IS LENGTH ZERO
         BZ    FILL70              THEN, NO INST DATA
         BCTR  R15,0               REDUCE FOR MOVE
         EX    R15,MOVDAT1         MOVE IT
FILL70   LA    R15,WIDATL          GET BEGINNING ADDR
         LH    R14,0(R15)          GET LENGTH
         LA    R15,4(R14,R15)      POINT TO APPL DATA
         XR    R14,R14             CLEAR REG
         IC    R14,0(R15)          GET LENGTH OF APPL
         MVC   CMNERAPP,=CL30' '
         LTR   R14,R14             ZERO? ? ?
         BZ    FILL99              YES, THEN, DONE
         CH    R14,=H'30'          CHECK FOR MAX
         BL    FILL80              NOT GREATER
         LA    R14,30              SET MAX
FILL80   BCTR  R14,0               REDUCE FOR MOVE
         EX    R14,MOVDAT2
FILL99   BR    R10                 RETURN
         TITLE '***** CONTANTS *****'
*
*        DIALOG MANAGER SERVICES
*
VGET     DC    CL8'VGET'
VDEF     DC    CL8'VDEFINE'
VDISPLAY DC    CL8'DISPLAY'
*
*        DIALOG MANAGER LISTS
*
LIST1    DC    C'(CMNERRES CMNERCLS CMNEROWN CMNERAUD CMNERUAC)'
LIST2    DC    C'(CMNERRC)'
LIST3    DC    C'(CMNERDAT CMNERAPP)'
*
*        DIALOG MANAGER LENGTHS
*
LNG4     DC    F'4'
LNG8     DC    F'8'
LNG30    DC    F'30'
*
*        MISCELANEOUS VARIABLSE
*
FIXED    DC    CL8'FIXED'
CHAR     DC    CL8'CHAR'
SHARED   DC    CL8'SHARED'
PANEL    DC    CL8'CMNER41'
MSG01    DC    CL8'CMNER410'       COMMAND SUCCESSFUL
MSG02    DC    CL8'CMNER411'       COMMAND FAILED
MSG03    DC    CL8'CMNER412'       ENTER RESOURCE/CLASS
MSG04    DC    CL8'CMNER413'       NOT FOUND
MSG05    DC    CL8'CMNER414'       ENTER CHANGES
MOVDAT1  MVC   CMNERDAT(0),WINST+1
MOVDAT2  MVC   CMNERAPP(0),1(R15)
BLANKTAB DC    64X'00'
         DC    X'01'
         DC    191X'00'
*
*
*
ACT1     ICHEACTN FIELD=OWNER,MF=L
ACT2     ICHEACTN FIELD=UACC,MF=L
ACT3     ICHEACTN FIELD=AUDIT,MF=L
ACT4     ICHEACTN FIELD=INSTDATA,MF=L
ACT5     ICHEACTN FIELD=APPLDATA,MF=L
         LTORG
         TITLE '***** DYNAMIC AREAS *****'
         DS    0D
DYNWK    EQU   *
ISPCATRC DS    A(0)
ISPCATCL DS    3A(0)
*
*        DIALOG MANAGER VARIABLES
*
RESLN    DS    X
CMNERRES DS    CL8' '
CMNERCLS DS    CL8' '
CMNEROWN DS    CL8' '
CMNERAUD DS    CL8' '
CMNERUAC DS    CL8' '
CMNERRC  DS    F'0'
CMNERDAT DS    CL30' '
CMNERAPP DS    CL30' '
CMNEBRES DC    CL8' '
CMNEBCLS DC    CL8' '
ISPLNK1  CALL   ,(VDEF,LIST1,CMNERRES,CHAR,LNG8),MF=L
ISPLNK2  CALL  ,(VGET,LIST1,SHARED),MF=L
ISPLNK3  CALL   ,(VDEF,LIST1,CMNERRC,FIXED,LNG4),MF=L
ISPLNK4  CALL   ,(VDEF,LIST3,CMNERDAT,CHAR,LNG30),MF=L
CMDBUF   DC    H'255'
CMDOFF   DC    H'5'
CMD      DC    CL255'RALT '
*
*
*
TY1      ICHEINTY LOCATE,TYPE='GEN',MF=L,ACTIONS=(ACT1,ACT2,ACT3,ACT4, X
               ACT5)
WK1      DC    A(WK1LN)
         DS    6F
         DS    H
WOWN     DS    CL8
         DS    XL2
WUACC    DS    X
         DS    XL2
WAUDIT   DS    X
WIDATL   DS    XL2
WINST    DS    XL255
         DS    XL2
         DS    XL255
         DS    0F
WK1LN    EQU   *-WK1
DYNWKLN  EQU   *-DYNWK
         END
