 PUNCH ' INCLUDE ISP(ISPLINK) '
 PUNCH ' ENTRY CMNER30 '
CMNER30  $PROLOG
         SPACE 5
***********************************************************************
*        PROGRAM - CMNER30                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS NON REENTRANT, NON APF AUTHORIZED MODULE   *
*               WHICH CONTROLS DISPLAY OF THE INTIAL 'DELETE' SCREEN  *
*               AND CONTROLS THE SELECTION OF ADD SUB FUNCTIONS       *
*                                                                     *
*        PANELS - CMNER30                                             *
*                                                                     *
*        MESSAGES - CMNER300                                          *
*                   CMNER301                                          *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         BAL   R10,SETUP           DEFINE DIALOG VARIABLES
C30000   CALL  ISPLINK,(VDISPLAY,PANEL,,CURSOR),VL
C30005   CH    R15,=H'8'           WAS END KEY ENTERED ? ? ?
         BE    C30999              YES, THEN GET OUT
         LTR   R15,R15             CHECK FOR ZERO
         BZ    C30010
         ABEND 1,DUMP
C30010   CALL ISPLINK,(VPUT,LIST5,SHARED)
         CLC   CMNERUID,=CL4' '    WAS USERID ENTERED
         BE    C30020              NO GO CHECK GROUP
         CALL  ISPLINK,(VSELECT,CMNERBL,CMNER22B)
         B     C30000
C30020   CLC   CMNERGRP,=CL4' '    WAS GROUP ENTERED
         BE    C30030              NO GO CHECK DSN
         CALL  ISPLINK,(VSELECT,CMNERBL,CMNER23B)
         B     C30000
C30030   CLC   CMNERDSN,=CL44' '   WAS DSN ENTERED
         BE    C30040              NO, GO CHECK GENERAL RESOURCE
         CALL  ISPLINK,(VSELECT,CMNERBL,CMNER24B)
         B     C30000
C30040   CLC   CMNERRES,=CL8' '    WAS RESOURCE ENTERED
         BE    C30060              NO, THEN ISSUE MESSAGE
         CLC   CMNERCLS,=CL8' '    WAS CLASS ENTERED
         BNE   C30050              YES, THE SELECT GENERAL RESOURCE ADD
         CALL ISPLINK,(VDISPLAY,PANEL,MSG01,CURSOR)
         B     C30005
C30050   CALL  ISPLINK,(VSELECT,CMNERBL,CMNER21B)
         B     C30000
C30060   CALL  ISPLINK,(VDISPLAY,PANEL,MSG02,CURSOR)
         B     C30000
C30999   CALL  ISPLINK,(VDELETE,LIST5)
         $EPILOG
         TITLE '***** SETUP ROUTINE *****'
SETUP    EQU   *
         CALL  ISPLINK,(VDEF,LIST1,CMNERDSN,CHAR,LNG44)
         CALL  ISPLINK,(VDEF,LIST2,CMNERUID,CHAR,LNG4)
         CALL  ISPLINK,(VDEF,LIST3,CMNERRES,CHAR,LNG8)
         CALL  ISPLINK,(VDEF,LIST4,CMNERVOL,CHAR,LNG6)
         BR    R10
         TITLE '***** CONSTANTS *****'
*
*  DIALOG MANAGER SERVICES
*
VDELETE  DC    CL8'VDELETE'
VDEF     DC    CL8'VDEFINE'
VPUT     DC    CL8'VPUT'
VDISPLAY DC    CL8'DISPLAY'
VSELECT  DC    CL8'SELECT'
*
*  LENGTHS
*
LNG4     DC    F'4'
LNG6     DC    F'6'
LNG8     DC    F'8'
LNG44    DC    F'44'
*
*  DIALOG MANAGER VARIABLE LISTS
*
LIST1    DC    C'(CMNERDSN)'
LIST2    DC    C'(CMNERUID CMNERGRP)'
LIST3    DC    C'(CMNERRES CMNERCLS)'
LIST4    DC    C'(CMNERVOL)'
LIST5    DC    C'(CMNERDSN '
         DC    C'CMNERUID '
         DC    C'CMNERGRP '
         DC    C'CMNERRES '
         DC    C'CMNERCLS '
         DC    C'CMNERVOL)'
*
*  DIALOG MANAGER VARIABLES
*
CMNERDSN DC    CL44' '
CMNERUID DC    CL4' '
CMNERGRP DC    CL4' '
CMNERRES DC    CL8' '
CMNERCLS DC    CL8' '
CMNERVOL DC    CL6' '
*
*  DIALOG MANAGER MISCELLANEOUS FIELDS
*
SHARED   DC    CL8'SHARED'
CHAR     DC    CL8'CHAR'
PANEL    DC    CL8'CMNER30'
CURSOR   DC    CL8'CMNERUID'
MSG01    DC    CL8'CMNER300'
MSG02    DC    CL8'CMNER301'
CMNERBL  DC    F'12'
CMNER21B DC    C'PGM(CMNER31)'
CMNER22B DC    C'PGM(CMNER32)'
CMNER23B DC    C'PGM(CMNER33)'
CMNER24B DC    C'PGM(CMNER34)'
         END
