*          DATA SET P50299     AT LEVEL 004 AS OF 05/29/81
*          DATA SET P50299     AT LEVEL 003 AS OF 05/28/81
*          DATA SET P50299     AT LEVEL 002 AS OF 05/28/81
*          DATA SET P50299     AT LEVEL 001 AS OF 05/20/81
 PUNCH ' INCLUDE ISP(ISPLINK) '
 PUNCH ' INCLUDE SYSLMOD(P50215) '
 PUNCH ' ENTRY P50299 '
P50299   CSECT
 TITLE '**** SPF/RACF LIST GENERALIZED RESOURCE     &SYSDATE &SYSTIME'
*
*
*
*
*   P50299 LIST PERTINENT RACF GENERALIZED RESOURCE DATA
*
*
*
*
         EJECT
         PRIME
         REGISTER
         L     R2,0(R1)
         ST    R2,GENERAL
         L     R2,4(R1)           CLASS ADDRESS
         ST    R2,CLASS
         SPACE 3
*
*        DEFINE PROGRAM VARIABLES TO DIALOG MANAGER
*
         CALL  ISPLINK,(VDEF,NAMES,OWNER,CHAR,LNG),VL
         LTR   R15,R15
         BZ    DEFNAME
         ABEND 10,DUMP
VDEF     DC    CL8'VDEFINE'
NAMES    DC    C'(OWNER NUMU)'
CHAR     DC    CL8'CHAR'
LNG      DC    F'4'
DEFNAME  EQU   *
         CALL  ISPLINK,(VDEF,NAMEL,ONAME,CHAR,LNG20),VL
         LTR   R15,R15
         BZ    DEFDATES
         ABEND 10,DUMP
NAMEL    DC    C'(ONAME)'
LNG20    DC    F'20'
DEFDATES EQU   *
         CALL  ISPLINK,(VDEF,NAMEI,INSTALL,CHAR,LNG40),VL
         LTR   R15,R15
         BZ    DEFDATE
         ABEND 10,DUMP
NAMEI    DC    C'(INSTALL)'
LNG40    DC    F'40'
DEFDATE  EQU   *
         CALL  ISPLINK,(VDEF,DEFINEL,DEFINED,CHAR,LNG12),VL
         LTR   R15,R15
         BZ    DEFATTR
         ABEND 10,DUMP
DEFINEL  DC    C'(DEFINED REF UPDT)'
LNG12    DC    F'12'
DEFATTR  EQU   *
         CALL  ISPLINK,(VDEF,UACCL,UACC,CHAR,LNG10),VL
         LTR   R15,R15
         BZ    DEFINT
         ABEND 10,DUMP
UACCL    DC    C'(UACC AUDIT)'
LNG10    DC    F'10'
         SPACE 3
DEFINT   EQU   *
         CALL  ISPLINK,(VDEF,USERL1,USER1,CHAR,LNG17),VL
         CALL  ISPLINK,(VDEF,USERL2,USER25,CHAR,LNG17),VL
         LTR   R15,R15
         BZ    DEFCNT
         ABEND 10,DUMP
USERL1   DC    C'(USER1 USER2 USER3 USER4 USER5 USER6 USER7 USER8 USER9X
                USER10 USER11 USER12 USER13 USER14 USER15 USER16 USER17X
                USER18 USER19 USER20 USER21 USER22 USER23 USER24)'
USERL2   DC    C'(USER25 USER26 USER27 USER28 USER29 USER30 USER31 USERX
               32 USER33 USER34 USER35 USER36 USER37 USER38 USER39 USERX
               40 USER41 USER42 USER43 USER44)'
LNG17    DC    F'17'
DEFCNT   EQU   *
         CALL  ISPLINK,(VDEF,COUNT1,ALTER,CHAR,LNG6),VL
         LTR   R15,R15
         BZ    RACFINQ
         ABEND 10,DUMP
COUNT1   DC    C'(ALTER CTL UPDATE READ)'
LNG6     DC    F'6'
*
*        OBTAIN RACF INFORMATION ABOUT THIS USER
*
RACFINQ  EQU   *
         L     R2,GENERAL          RESOURCE NAME
         MVC   GENL+1(8),0(R2)
         SR    R3,R3
         LA    R4,8                MAX
GENLOOP  EQU   *
         CLI   0(R2),C' '          END
         BE    MOVEGEN
         LA    R3,1(R3)            UPCOUNT
         LA    R2,1(R2)
         BCT   R4,GENLOOP
MOVEGEN  EQU   *
         LTR   R3,R3
         BNZ   STC3
         LA    R3,1                MIN
STC3     EQU   *
         STC   R3,GENL
         L     R2,CLASS            CLASS
         MVC   CLASSL(8),0(R2)
         LA    R1,AUTH
         SVC   231
         ICHEINTY LOCATE,MF=(E,LOCATE),ENTRY=GENL
         LTR   R15,R15
         BZ    DISPL
         CH    R15,=H'12'          NOT FOUND ? ? ? ?
         BE    DISPLNF
         ABEND 91,DUMP
DISPLNF  EQU   *
         CALL  ISPLINK,(DISPLAY,GROUP,R2991,CURSOR),VL
         CH    R15,=H'8'           PF3
         BE    RETURN
         LTR   R15,R15
         BZ    USERCHNG
         ABEND 51,DUMP
R2991    DC    CL8'RL991A'
         DS    0H
DISPL    EQU   *
         SR    R1,R1
         SVC   231
         LA    R2,WORK+X'1C'       FIRST FIELD
         LH    R3,0(R2)            LENGTH OF DEFINED
         LTR   R3,R3
         BZ    NODEF
         CLI   2(R2),X'00'         INVALID DATE
         BE    NODEF
         MVC   DEFPACK+1(3),2(R2)
         CALL  P50215,(DEFPACK,DEFINED),VL
NODEF    EQU   *
         LA    R2,2(R3,R2)         NEXT FIELD(OWNER)
         LH    R3,0(R2)
         BCTR  R3,0
         EX    R3,MVCOWNER
         LA    R2,3(R3,R2)         NEXT FIELD(LAST REF)
         LH    R3,0(R2)
         LTR   R3,R3
         BZ    CHKLCHNG
         CLI   2(R2),X'FF'
         BE    CHKLCHNG
         MVC   DEFPACK+1(3),2(R2)
         CALL  P50215,(DEFPACK,REF),VL
CHKLCHNG EQU   *
         LA    R2,2(R3,R2)         NEXT FIELD(LAST CHG)
         LH    R3,0(R2)
         LTR   R3,R3
         BZ    NXTALT
         CLI   2(R2),X'FF'
         BE    NXTALT
         MVC   DEFPACK+1(3),2(R2)
         CALL  P50215,(DEFPACK,UPDT),VL
NXTALT   EQU   *
         LA    R2,2(R3,R2)         NEXT FIELD(ACSALTR)
         LH    R15,2(R2)
         CVD   R15,DBLWRD
         ED    EDIT,DBLWRD+5
         MVC   ALTER,EDIT+1
         LA    R2,4(R2)            NEXT FIELD(ACSCNTL)
         LH    R15,2(R2)
         CVD   R15,DBLWRD
         MVC   EDIT,=X'4020206B202020'
         ED    EDIT,DBLWRD+5
         MVC   CTL,EDIT+1
         LA    R2,4(R2)            NEXT FIELD(ACSUPDT)
         LH    R15,2(R2)
         CVD   R15,DBLWRD
         MVC   EDIT,=X'4020206B202020'
         ED    EDIT,DBLWRD+5
         MVC   UPDATE,EDIT+1
         LA    R2,4(R2)            NEXT FIELD(ACSREAD)
         LH    R15,2(R2)
         CVD   R15,DBLWRD
         MVC   EDIT,=X'4020206B202020'
         ED    EDIT,DBLWRD+5
         MVC   READ,EDIT+1
         LA    R2,4(R2)            NEXT FIELD(UACC)
         TM    2(R2),X'80'         ALTER ACCESS
         BNO   CHKCTL
         MVC   UACC,=CL10'ALTER'
CHKCTL   EQU   *
         TM    2(R2),X'40'         CONTROL
         BNO   CHKUPT
         MVC   UACC,=CL10'CONTROL'
CHKUPT   EQU   *
         TM    2(R2),X'20'
         BNO   CHKREAD
         MVC   UACC,=CL10'UPDATE'
CHKREAD  EQU   *
         TM    2(R2),X'10'         READ
         BNO   CHKNONE
         MVC   UACC,=CL10'READ'
CHKNONE  EQU   *
         TM    2(R2),X'01'         NONE
         BNO   CHKNXT
         MVC   UACC,=CL10'NONE'
CHKNXT   EQU   *
         LA    R2,3(R2)            NEXT FIELD(AUDIT)
         TM    2(R2),X'80'         ALL
         BNO   AUDSUC
         MVC   AUDIT,=CL10'ALL'
AUDSUC   EQU   *
         TM    2(R2),X'40'         SUCCESS
         BNO   AUFAIL
         MVC   AUDIT,=CL10'SUCCESS'
AUFAIL   EQU   *
         TM    2(R2),X'20'         FAILURES
         BNO   AUNONE
         MVC   AUDIT,=CL10'FAILURES'
AUNONE   EQU   *
         TM    2(R2),X'10'         NONE
         BNO   CHKUSER
         MVC   AUDIT,=CL10'NONE'
CHKUSER  EQU   *
         LA    R2,3(R2)            NEXT FIELD(INSTALL)
         LH    R3,0(R2)            LENGTH
         LR    R15,R3
         BCTR  R15,0
         CH    R15,=H'40'
         BL    OKMVCI
         LA    R15,39              MAX
OKMVCI   EQU   *
         EX    R15,MVCI
         LA    R2,2(R3,R2)         NEXT FIELD(ACTLST)
         LH    R15,2(R2)
         LTR   R15,R15
         BZ    CHKOWN
         CVD   R15,DBLWRD
         MVC   EDIT+1(6),=X'402020202020'
         ED    EDIT+1(6),DBLWRD+5
         MVC   NUMU,EDIT+3
         CH    R15,=H'45'          MAX
         BL    OKNU
         LA    R15,44
OKNU     EQU   *
         LA    R2,4(R2)            NEXT FIELD(USERID)
         LH    R3,0(R2)            LENGTH OF USER FIELD
         LA    R5,2(R3,R2)         NEXT FIELD(ACCESS)
         LH    R3,0(R5)            LENGTH OF ACCESS
         LA    R6,4(R3,R5)         NEXT FIELD(COUNT)
         LA    R5,2(R5)            SKIP ACCESS LENGTH
         LA    R4,USER1            FIRST USER FIELD
*
*        R2 = USERID
*        R3 = LENGTH
*        R4 = USER OUTPUT
*        R5 = 1 BYTE ACCESS FIELD
*        R6 = 2 BYTE COUNT FIELD
*
ULOOP    EQU   *
         MVC   0(4,R4),2(R2)       MOVE USER ID
         MVI   4(R4),C'/'
         TM    0(R5),X'80'         ALTER
         BNO   USCTL
         MVC   5(6,R4),=CL6'ALTER'
USCTL    EQU   *
         TM    0(R5),X'40'         CONTROL
         BNO   USUPDT
         MVC   5(6,R4),=CL6'CTL'
USUPDT   EQU   *
         TM    0(R5),X'20'         UPDT
         BNO   USREAD
         MVC   5(6,R4),=CL6'UPDATE'
USREAD   EQU   *
         TM    0(R5),X'10'         READ
         BNO   USNONE
         MVC   5(6,R4),=CL6'READ'
USNONE   EQU   *
         TM    0(R5),X'01'         NONE
         BNO   USACC
         MVC   5(6,R4),=CL6'NONE'
USACC    EQU   *
         MVI   11(R4),C'/'
         LH    R14,0(R6)           NUMBER OF ACCESS
         CVD   R14,DBLWRD
         MVC   EDIT+1(6),=X'402020202120'
         ED    EDIT+1(6),DBLWRD+5
         MVC   12(5,R4),EDIT+2
         LA    R2,8(R2)            NEXT USER
         LA    R4,17(R4)           NEXT OUTPUT AREA
         LA    R5,1(R5)            NEXT ACCESS
         LA    R6,2(R6)            NEXT COUNT
         BCT   R15,ULOOP
CHKOWN   EQU   *
         MVC   USERL+1(4),OWNER
         LA    R1,AUTH
         SVC   231
         ICHEINTY MF=(E,LOCATEO),ENTRY=USERL
         LTR   R15,R15
         BZ    MOVEOWN
         CH    R15,=H'12'          NOT FOUND ? ? ? ?
         BE    DISPLNO
         ABEND 91,DUMP
DISPLNO  EQU   *
         CALL  ISPLINK,(DISPLAY,GROUP,R2992,CURSOR),VL
         CH    R15,=H'8'           PF3
         BE    RETURN
         LTR   R15,R15
         BZ    USERCHNG
         ABEND 51,DUMP
R2992    DC    CL8'RL991B'
         DS    0H
MOVEOWN  EQU   *
         SR    R1,R1
         SVC   231
         LH    R3,WORK+X'1C'
         BCTR  R3,0
         EX    R3,MVCOWN
DISP     EQU   *
         CALL  ISPLINK,(DISPLAY,GROUP,,CURSOR),VL
         CH    R15,=H'8'           PF3 ? ? ? ?
         BE    RETURN
         LTR   R15,R15
         BZ    USERCHNG
         ABEND 53,DUMP
CURSOR   DC    CL8'GENERL'
DISPLAY  DC    CL8'DISPLAY'
GROUP    DC    CL8'RACFGNRL'       MENU NAME
USERCHNG EQU   *
         LA    R4,OWNER
         LR    R6,R4
         LA    R5,LDATA            LENGTH
         SR    R7,R7
         O     R7,=X'40000000'     BLANK PAD
         MVCL  R4,R6               CLEAR OUTPUT AREAS
         L     R15,GENERAL
         SR    R14,R14
         IC    R14,GENL
         EX    R14,CLCGEN
         BNE   RACFINQ
         LA    R1,AUTH
         SVC   231
         ICHEINTY NEXT,MF=(E,LOCATE),ENTRY=GENL
         LTR   R15,R15
         BZ    CHNGGEN
         CH    R15,=H'12'          NOT FOUND ? ? ? ?
         BE    DISPLNF
         ABEND 91,DUMP
CHNGGEN  EQU   *
         SR    R14,R14
         IC    R14,GENL            LENGTH
         L     R15,GENERAL
         MVC   0(8,R15),=CL8' '
         BCTR  R14,0
         EX    R14,MVCGEN
         B     DISPL
RETURN   EQU   *
         CALL  ISPLINK,(VDELETE,USERL1),VL
         CALL  ISPLINK,(VDELETE,USERL2),VL
         CALL  ISPLINK,(VDELETE,VARIAB3),VL
         LTR   R15,R15
         BZ    TERME
         ABEND 99,DUMP
VDELETE  DC    CL8'VDELETE'
VARIAB3  DC    C'(ALTER CTL UPDATE READ UACC AUDIT ONAME OWNER NUMU INSX
               TALL)'
         DS    0H
TERME    EQU   *
         TERME
         EJECT
MVCOWN   MVC   ONAME(0),WORK+X'1E'
MVCOWNER MVC   OWNER(0),2(R2)
MVCGEN   MVC   0(0,R15),GENL+1
MVCI     MVC   INSTALL(0),2(R2)
CLCGEN   CLC   0(0,R15),GENL+1
GENERAL  DC    F'0'                ADDRESS OF GEN RESOURCE
CLASS    DC    F'0'                ADDRESS OF CLASS
DBLWRD   DC    D'0'
AUTH     DC    C'AUTH'
CLASSL   DC    CL8' '
GENL     DC    X'08'
         DC    CL8' '
USERL    DC    X'04'
         DC    CL4' '
OWNER    DC    CL4' '
NUMU     DC    CL4' '
ONAME    DC    CL20' '
INSTALL  DC    CL40' '
DEFINED  DC    CL12' '
REF      DC    CL12' '
UPDT     DC    CL12' '
UACC     DC    CL10' '
AUDIT    DC    CL10' '
USER1    DC    24CL17' '
USER25   DC    20CL17' '
ALTER    DC    CL6' '
CTL      DC    CL6' '
UPDATE   DC    CL6' '
READ     DC    CL6' '
LDATA    EQU   *-OWNER
EDIT     DC    X'4020206B202020'
DEFPACK  DC    PL4'0'
         EJECT
*
*        RACF MACROS
*
LOCATEO  ICHEINTY LOCATE,MF=L,TYPE='USR',WKAREA=WORK,                  X
               ACTIONS=ACTLSTO
LOCATE   ICHEINTY LOCATE,MF=L,TYPE='GEN',WKAREA=WORK,                  X
               CLASS=CLASSL,                                           X
               ACTIONS=(ACTLST1,                                       X
               ACTLST2,                                                X
               ACTLST3,                                                X
               ACTLST4,                                                X
               ACTLST5,                                                X
               ACTLST5A,                                               X
               ACTLST5B,                                               X
               ACTLST6,                                                X
               ACTLST7,                                                X
               ACTLST8,                                                X
               ACTLST8A,                                               X
               ACTLST9,                                                X
               ACTLSTA,                                                X
               ACTLSTB,                                                X
               ACTLSTC)
ACTLST1  ICHEACTN MF=L,FIELD=DEFDATE
ACTLST2  ICHEACTN MF=L,FIELD=OWNER
ACTLST3  ICHEACTN MF=L,FIELD=LREFDAT
ACTLST4  ICHEACTN MF=L,FIELD=LCHGDAT
ACTLST5  ICHEACTN MF=L,FIELD=ACSALTR
ACTLST5A ICHEACTN MF=L,FIELD=ACSCNTL
ACTLST5B ICHEACTN MF=L,FIELD=ACSUPDT
ACTLST6  ICHEACTN MF=L,FIELD=ACSREAD
ACTLST7  ICHEACTN MF=L,FIELD=UACC
ACTLST8  ICHEACTN MF=L,FIELD=AUDIT
ACTLST8A ICHEACTN MF=L,FIELD=INSTDATA
ACTLST9  ICHEACTN MF=L,FIELD=ACLCNT
ACTLSTA  ICHEACTN MF=L,FIELD=USERID
ACTLSTB  ICHEACTN MF=L,FIELD=USERACS
ACTLSTC  ICHEACTN MF=L,FIELD=ACSCNT
ACTLSTO  ICHEACTN MF=L,FIELD=PGMRNAME
         LTORG
WORK     DC    F'400'
         DC    400X'00'
         END
