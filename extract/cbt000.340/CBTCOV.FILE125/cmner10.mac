 PUNCH ' INCLUDE ISP(ISPLINK) '
 PUNCH ' ENTRY CMNER10 '
CMNER10  CSECT
 TITLE '**** SPF/RACF LIST COMMAND                  &SYSDATE &SYSTIME'
***********************************************************************
*        PROGRAM - CMNER10                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS NON REENTRANT MODULE WHICH ACTS AS A       *
*               CONTROLLER FOR THE SPF/RACF LIST FUNCTIONS. IT        *
*               DISPLAYS A SELECTION MENU AND SELECTS THE PROPER SUB- *
*               FUNCTIONS                                             *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         PRIME
         REGISTER
         SPACE 3
*
*        DEFINE PROGRAM VARIABLES TO DIALOG MANAGER
*
         CALL  ISPLINK,(VDEF,NAMES,MUSER,CHAR,LNG),VL
         LTR   R15,R15
         BZ    DEFC
         ABEND 10,DUMP
VDEF     DC    CL8'VDEFINE'
NAMES    DC    C'(CMNERUID CMNERGRP)'
CHAR     DC    CL8'CHAR'
LNG      DC    F'4'
         SPACE 3
*
*        DEFINE PROGRAM CLASS AND RESOURCE VARIABLES
*
DEFC     EQU   *
         CALL  ISPLINK,(VDEF,CNAME,MCLASS,CHAR,LNG8),VL
         LTR   R15,R15
         BZ    DEFDS
         ABEND 10,DUMP
CNAME    DC    C'(CMNERCLS CMNERRES)'
LNG8     DC    F'8'
         SPACE 3
*
*        DEFINE PROGRAM DATA SET VARIABLE
*
DEFDS    EQU   *
         CALL  ISPLINK,(VDEF,DNAME,DSNM,CHAR,LNG44),VL
         LTR   R15,R15
         BZ    DEFVOL
         ABEND 10,DUMP
DNAME    DC    C'(CMNERDSN)'
LNG44    DC    F'44'
         SPACE 3
*
*        DEFINE PROGRAM VOLUME VARIABLE
*
DEFVOL   EQU   *
         CALL  ISPLINK,(VDEF,VVOL,CMNERVOL,CHAR,LNG6),VL
         LTR   R15,R15
         BZ    REDISPL
         ABEND 10,DUMP
VVOL     DC    C'(CMNERVOL)'
LNG6     DC    F'6'
CMNERVOL DC    CL6' '
*
*        CALL SPF DIALOG MANAGER TO DISPLAY LIST MENU
*
REDISPL  EQU   *
         CALL  ISPLINK,(DISPLAY,LIST,,CURSOR),VL
         CH    R15,=H'8'           PF3 ? ? ? ?
         BE    RETURN
         LTR   R15,R15
         BZ    DPARMS
         ABEND 1,DUMP
CURSOR   DC    CL8'CMNERUID'
DISPLAY  DC    CL8'DISPLAY'
LIST     DC    CL8'CMNER10'        MENU NAME
         SPACE 3
*
*        SELECT LIST USER
*
DPARMS   EQU   *
         CLC   MUSER,=CL4' '       USER ENTERED ? ? ? ? ?
         BE    TRYGRP              YES
         CALL  ISPLINK,(VPUT,NAMES),VL
         CALL  ISPLINK,(SELECT,SELBL,SELBUF),VL
         B     REDISPL
VPUT     DC    CL8'VPUT'
SELECT   DC    CL8'SELECT'
SELBL    DC    F'12'
SELBUF   DC    C'PGM(CMNER11)'
         SPACE 3
*
*        LINK TO LIST GROUP
*
TRYGRP   EQU   *
         CLC   MGROUP,=CL4' '      GROUP ENTERED ? ? ? ? ?
         BE    TRYGNL              NO
         CALL  ISPLINK,(VPUT,GRPVARS),VL
         CALL  ISPLINK,(SELECT,GRPBL,GRPBUF),VL
         MVC   CURSOR,=CL8'CMNERGRP'
         B     REDISPL
GRPVARS  DC    C'(CMNERGRP)'
GRPBL    DC    F'12'
GRPBUF   DC    C'PGM(CMNER14)'
         SPACE 3
*
*        LINK TO GENERALIZED RESOURCE DISPLAY
*
TRYGNL   EQU   *
         CLC   MGENERL(8),=CL8' '  RESOURCE SPECIFIED
         BE    CHKDSN              CHECK FOR DATA SET NAME
         CALL  ISPLINK,(VPUT,RESVARS),VL
         CALL  ISPLINK,(SELECT,RESBL,RESBUF),VL
         MVC   CURSOR,=CL8'CMNERRES'
         B     REDISPL
RESVARS  DC    C'(CMNERRES CMNERCLS)'
RESBUF   DC    C'PGM(CMNER13)'
RESBL    DC    F'12'
         SPACE 3
*
*        LINK TO DATA SET DISPLAY
*
CHKDSN   EQU   *
         CLI   DSNM,C' '           DATA SET NAME SPECIFIED
         BE    REDISPL             NO
         CALL  ISPLINK,(VPUT,DSNVARS),VL
         CALL  ISPLINK,(SELECT,DSNBL,DSNBUF),VL
         MVC   CURSOR,=CL8'CMNERDSN'
         B     REDISPL
DSNBUF   DC    C'PGM(CMNER12)'
DSNBL    DC    F'12'
DSNVARS  DC    C'(CMNERDSN CMNERVOL)'
         DS    0D
RETURN   EQU   *
         TERME
         EJECT
MUSER    DC    CL4' '
MGROUP   DC    CL4' '
MCLASS   DC    CL8' '
MGENERL  DC    CL8' '
DSNM     DC    CL44' '
         EJECT
         END
