 PUNCH ' INCLUDE ISP(ISPLINK) '
 PUNCH ' ENTRY CMNER50 '
         TITLE '***** PERMIT/CONNECT FUNTION *****'
CMNER50  $PROLOG
         SPACE 5
***********************************************************************
*        PROGRAM - CMNER50                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS NON REENTRANT, NON APF AUTHORIZED MODULE   *
*               WHICH CONTROLS DISPLAY OF THE INTIAL 'PERMIT' SCREEN  *
*               AND CONTROLS THE SELECTION OF ADD SUB FUNCTIONS       *
*                                                                     *
*        PANELS - CMNER50                                             *
*                                                                     *
*        MESSAGES - CMNER500                                          *
*                   CMNER501                                          *
*                   CMNER502                                          *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         BAL   R10,SETUP           DEFINE DIALOG VARIABLES
C50000   CALL  ISPLINK,(VDISPLAY,PANEL,,CURSOR),VL
C50005   CH    R15,=H'8'           WAS END KEY ENTERED ? ? ?
         BE    C50999              YES, THEN GET OUT
         LTR   R15,R15             CHECK FOR ZERO
         BZ    C50010
         ABEND 1,DUMP
C50010   CALL ISPLINK,(VPUT,LIST5,SHARED)
         CLC   CMNERGRP,=CL4' '    WAS GROUP ENTERED
         BE    C50020              NO GO CHECK DSN
         CALL  ISPLINK,(VSELECT,CMNERBL,CMNER53B)
         B     C50000
C50020   CLC   CMNERDSN,=CL44' '   WAS DSN ENTERED
         BE    C50030              NO, GO CHECK GENERAL RESOURCE
         CLC   CMNERVOL,=CL6' '    WAS VOLUME SPECIFIED
         BNE   C50028              YES, THEN PROCEED TO DISPLAY
         LOCATE CAMLIST
         LTR   R15,R15             CHECK RETURN
         BZ    C50026              GOOD
         ST    R15,CMNERRC
         MVC   CURSOR,=CL8'CMNERVOL'
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG03,CURSOR)
         B     C50005
C50026   MVC   CMNERVOL,CATWRK+6
         CALL  ISPLINK,(VPUT,LIST4,SHARED)
C50028   CALL  ISPLINK,(VSELECT,CMNERBL,CMNER52B)
         B     C50000
C50030   CLC   CMNERRES,=CL8' '    WAS RESOURCE ENTERED
         BE    C50050              NO, THEN ISSUE MESSAGE
         CLC   CMNERCLS,=CL8' '    WAS CLASS ENTERED
         BNE   C50040              YES, THE SELECT GENERAL RESOURCE ADD
         CALL ISPLINK,(VDISPLAY,PANEL,MSG01,CURSOR)
         B     C50005
C50040   CALL  ISPLINK,(VSELECT,CMNERBL,CMNER51B)
         B     C50000
C50050   CALL  ISPLINK,(VDISPLAY,PANEL,MSG02,CURSOR)
         B     C50000
C50999   CALL  ISPLINK,(VDELETE,LIST5)
         $EPILOG
         TITLE '***** SETUP ROUTINE *****'
SETUP    EQU   *
         CALL  ISPLINK,(VDEF,LIST1,CMNERDSN,CHAR,LNG44)
         CALL  ISPLINK,(VDEF,LIST2,CMNERGRP,CHAR,LNG8)
         CALL  ISPLINK,(VDEF,LIST3,CMNERRES,CHAR,LNG8)
         CALL  ISPLINK,(VDEF,LIST4,CMNERVOL,CHAR,LNG6)
         CALL  ISPLINK,(VDEF,LIST6,CMNERRC,FIXED,LNG4)
         BR    R10
         TITLE '***** CONSTANTS *****'
*
*  DIALOG MANAGER SERVICES
*
VDELETE  DC    CL8'VDELETE'
VDEF     DC    CL8'VDEFINE'
VPUT     DC    CL8'VPUT'
VDISPLAY DC    CL8'DISPLAY'
VSELECT  DC    CL8'SELECT'
*
*  LENGTHS
*
LNG4     DC    F'4'
LNG6     DC    F'6'
LNG8     DC    F'8'
LNG44    DC    F'44'
*
*  DIALOG MANAGER VARIABLE LISTS
*
LIST1    DC    C'(CMNERDSN)'
LIST2    DC    C'(CMNERGRP)'
LIST3    DC    C'(CMNERRES CMNERCLS)'
LIST4    DC    C'(CMNERVOL)'
LIST5    DC    C'(CMNERDSN '
         DC    C'CMNERGRP '
         DC    C'CMNERRES '
         DC    C'CMNERCLS '
         DC    C'CMNERVOL)'
LIST6    DC    C'(CMNERRC)'
*
*  DIALOG MANAGER VARIABLES
*
CMNERDSN DC    CL44' '
CMNERGRP DC    CL8' '
CMNERRES DC    CL8' '
CMNERCLS DC    CL8' '
CMNERVOL DC    CL6' '
CMNERRC  DC    F'0'
*
*  DIALOG MANAGER MISCELLANEOUS FIELDS
*
SHARED   DC    CL8'SHARED'
CHAR     DC    CL8'CHAR'
FIXED    DC    CL8'FIXED'
PANEL    DC    CL8'CMNER50'
CURSOR   DC    CL8'CMNERGRP'
MSG01    DC    CL8'CMNER500'
MSG02    DC    CL8'CMNER501'
MSG03    DC    CL8'CMNER502'    LOCATE FAILED
CMNERBL  DC    F'12'
CMNER51B DC    C'PGM(CMNER51)'
CMNER52B DC    C'PGM(CMNER52)'
CMNER53B DC    C'PGM(CMNER53)'
CAMLIST  CAMLST NAME,CMNERDSN,,CATWRK
         DS    0D
CATWRK   DS    CL265
         END
