*          DATA SET P50298     AT LEVEL 003 AS OF 05/28/81
*          DATA SET P50298     AT LEVEL 002 AS OF 05/20/81
*          DATA SET P50298     AT LEVEL 001 AS OF 05/19/81
 PUNCH ' INCLUDE ISP(ISPLINK) '
 PUNCH ' INCLUDE SYSLMOD(P50215) '
 PUNCH ' ENTRY P50298 '
P50298   CSECT
 TITLE '**** SPF/RACF LIST GROUP COMMAND            &SYSDATE &SYSTIME'
*
*
*
*
*   P50298 LIST PERTINENT RACF GROUP DATA
*
*
*
*
         EJECT
         PRIME
         REGISTER
         L     R1,0(R1)
         ST    R1,GRPADDR
         MVC   GRPID,0(R1)        SAVE GROUP ID
         SPACE 3
*
*        DEFINE PROGRAM VARIABLES TO DIALOG MANAGER
*
         CALL  ISPLINK,(VDEF,NAMES,GROUPID,CHAR,LNG),VL
         LTR   R15,R15
         BZ    DEFNAME
         ABEND 10,DUMP
VDEF     DC    CL8'VDEFINE'
NAMES    DC    C'(GROUPID OWNER SUB1 SUB2 SUB3 SUB4 SUB5 SUPGRP NUMGRP X
               NUMU)'
CHAR     DC    CL8'CHAR'
LNG      DC    F'4'
DEFNAME  EQU   *
         CALL  ISPLINK,(VDEF,NAMEL,NAME,CHAR,LNG20),VL
         LTR   R15,R15
         BZ    DEFDATES
         ABEND 10,DUMP
NAMEL    DC    C'(NAME ONAME)'
LNG20    DC    F'20'
DEFDATES EQU   *
         CALL  ISPLINK,(VDEF,DEFINEL,DEFINED,CHAR,LNG12),VL
         LTR   R15,R15
         BZ    DEFATTR
         ABEND 10,DUMP
DEFINEL  DC    C'(DEFINED)'
LNG12    DC    F'12'
DEFATTR  EQU   *
         CALL  ISPLINK,(VDEF,UACCL,UACC,CHAR,LNG10),VL
         LTR   R15,R15
         BZ    DEFINT
         ABEND 10,DUMP
UACCL    DC    C'(UACC)'
LNG10    DC    F'10'
         SPACE 3
DEFINT   EQU   *
         CALL  ISPLINK,(VDEF,USERL1,USER1,CHAR,LNG11),VL
         CALL  ISPLINK,(VDEF,USERL2,USER25,CHAR,LNG11),VL
         CALL  ISPLINK,(VDEF,USERL3,USER48,CHAR,LNG11),VL
         LTR   R15,R15
         BZ    RACFINQ
         ABEND 10,DUMP
USERL1   DC    C'(USER1 USER2 USER3 USER4 USER5 USER6 USER7 USER8 USER9X
                USER10 USER11 USER12 USER13 USER14 USER15 USER16 USER17X
                USER18 USER19 USER20 USER21 USER22 USER23 USER24)'
USERL2   DC    C'(USER25 USER26 USER27 USER28 USER29 USER30 USER31 USERX
               32 USER33 USER34 USER35 USER36 USER37 USER38 USER39 USERX
               40 USER41 USER42 USER43 USER44 USER45 USER46 USER47)'
USERL3   DC    C'(USER48 USER49 USER50 USER51 USER52 USER53 USER54 USERX
               55)'
LNG11    DC    F'11'
*
*        OBTAIN RACF INFORMATION ABOUT THIS USER
*
RACFINQ  EQU   *
         LA    R1,AUTH
         SVC   231
         ICHEINTY LOCATE,MF=(E,LOCATE),ENTRY=GRPL
         LTR   R15,R15
         BZ    DISPL
         CH    R15,=H'12'          NOT FOUND ? ? ? ?
         BE    DISPLNF
         ABEND 91,DUMP
DISPLNF  EQU   *
         CALL  ISPLINK,(DISPLAY,GROUP,R2981,CURSOR),VL
         CH    R15,=H'8'           PF3
         BE    RETURN
         LTR   R15,R15
         BZ    USERCHNG
         ABEND 51,DUMP
R2981    DC    CL8'RL981A'
         DS    0H
DISPL    EQU   *
         SR    R1,R1
         SVC   231
         MVC   GROUPID,GRPID
         LA    R2,WORK+X'1C'       FIRST PARAMETER(SUPGROUP)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCSUP
         LA    R2,3(R3,R2)         NEXT FIELD(DEFINED DATE)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCDEF
         CALL  P50215,(DEFPACK,DEFINED),VL
         LA    R2,3(R3,R2)         NEXT FIELD(OWNER)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCOWN
         LA    R2,3(R3,R2)         NEXT FIELD(UACC)
         MVC   UACC,=CL10'NONE'    DEFAULT
         TM    2(R2),X'80'         ALTER
         BNO   CHKCTL
         MVC   UACC,=CL10'ALTER'
CHKCTL   EQU   *
         TM    2(R2),X'40'         CONTROL
         BNO   CHKUPT
         MVC   UACC,=CL10'CONTROL'
CHKUPT   EQU   *
         TM    2(R2),X'20'         UPDATE
         BNO   CHKNONE
         MVC   UACC,=CL10'UPDATE'
CHKNONE  EQU   *
         TM    2(R2),X'01'         NONE
         BNO   SUBCNT
         MVC   UACC,=CL10'NONE'
SUBCNT   EQU   *
         LA    R2,3(R2)            NEXT FIELD(SUBGRP COUNT)
         LH    R15,2(R2)           COUNT
         CVD   R15,DBLWRD
         MVC   NUMGRP,=X'40202020'
         ED    NUMGRP,DBLWRD+6
         LA    R2,4(R2)            NEXT FIELD (SUBGROUP)
         LTR   R15,R15
         BNZ   CHKLIM
         LA    R2,2(R2)            SKIP LENGTH
         B     CHKUSERS
CHKLIM   EQU   *
         LH    R3,0(R2)            SUBGROUP LENGTH
         LA    R6,2(R3,R2)         SAVE ADDRESS OF USER
         CH    R15,=H'6'
         BL    OKGRP
         LA    R15,5               LIMIT TO FIVE
OKGRP    EQU   *
         LA    R5,SUB1
SUBGRPL  EQU   *
         MVC   0(4,R5),2(R2)
         LA    R5,4(R5)            NEXT DISPLAY
         LA    R2,8(R2)            NEXT SUB-GROUP
         BCT   R15,SUBGRPL
         LR    R2,R6               RESTORE USER ADDR IN CASE < 5
CHKUSERS EQU   *
         LH    R15,2(R2)           NUMBER OF USERS
         CVD   R15,DBLWRD
         MVC   NUMU,=X'40202020'
         ED    NUMU,DBLWRD+6
         LA    R2,4(R2)            NEXT FIELD(USERID)
         LTR   R15,R15
         BZ    CHKOWN
         CH    R15,=H'56'
         BL    OKUSER
         LA    R15,55              LIMIT TO 55
OKUSER   EQU   *
         LH    R3,0(R2)
         LA    R5,USER1
         LA    R6,4(R3,R2)
USERLOOP EQU   *
         MVC   0(4,R5),2(R2)
         MVI   4(R5),C'/'
         TM    0(R6),X'80'         USER AUTHORITY
         BNO   CHKUC
         MVC   5(6,R5),=CL6'JOIN'
CHKUC    EQU   *
         TM    0(R6),X'40'         CONTROL ? ? ? ?
         BNO   CHKUUPT
         MVC   5(6,R5),=CL6'CONN'
CHKUUPT  EQU   *
         TM    0(R6),X'20'         UPDATE ? ? ? ?
         BNO   CHKUREAD
         MVC   5(6,R5),=CL6'CREATE'
CHKUREAD EQU   *
         TM    0(R6),X'10'         READ ? ? ? ?
         BNO   NXTU
         MVC   5(6,R5),=CL6'USE'
NXTU     EQU   *
         LA    R5,11(R5)
         LA    R2,8(R2)
         LA    R6,1(R6)            NEXT AUTHORITY
         BCT   R15,USERLOOP
CHKOWN   EQU   *
         MVC   OWNERL+1(4),OWNER
         LA    R1,AUTH
         SVC   231
         ICHEINTY MF=(E,LOCATEO),ENTRY=OWNERL
         LTR   R15,R15
         BZ    MOVEOWN
         CH    R15,=H'12'          NOT FOUND ? ? ? ?
         BE    DISPLNO
         ABEND 91,DUMP
DISPLNO  EQU   *
         CALL  ISPLINK,(DISPLAY,GROUP,R2982,CURSOR),VL
         CH    R15,=H'8'           PF3
         BE    RETURN
         LTR   R15,R15
         BZ    USERCHNG
         ABEND 51,DUMP
R2982    DC    CL8'RL981B'
         DS    0H
MOVEOWN  EQU   *
         SR    R1,R1
         SVC   231
         LH    R3,WORK+X'1C'
         BCTR  R3,0
         EX    R3,MVCOWNM
DISP     EQU   *
         CALL  ISPLINK,(DISPLAY,GROUP,,CURSOR),VL
         CH    R15,=H'8'           PF3 ? ? ? ?
         BE    RETURN
         LTR   R15,R15
         BZ    USERCHNG
         ABEND 53,DUMP
CURSOR   DC    CL8'GROUPID'
DISPLAY  DC    CL8'DISPLAY'
GROUP    DC    CL8'RACFGRP'        MENU NAME
USERCHNG EQU   *
         LA    R4,GROUPID+4
         LR    R6,R4
         LA    R5,LDATA-4          LENGTH
         SR    R7,R7
         O     R7,=X'40000000'     BLANK PAD
         MVCL  R4,R6               CLEAR OUTPUT AREAS
         CLC   GRPID(4),GROUPID    GROUP CHANGE ? ? ? ?
         BE    NEXTGRP
         MVC   GRPID(4),GROUPID    CHANGE ID
         B     RACFINQ
NEXTGRP  EQU   *
         LA    R1,AUTH
         SVC   231
         ICHEINTY NEXT,MF=(E,LOCATE),ENTRY=GRPL
         LTR   R15,R15
         BZ    CHNGRP
         CH    R15,=H'12'          NOT FOUND ? ? ? ?
         BE    DISPLNF
         ABEND 91,DUMP
CHNGRP   EQU   *
         L     R15,GRPADDR
         MVC   0(4,R15),GRPID
         B     DISPL
RETURN   EQU   *
         CALL  ISPLINK,(VDELETE,VARIAB1),VL
         CALL  ISPLINK,(VDELETE,VARIAB2),VL
         CALL  ISPLINK,(VDELETE,VARIAB3),VL
         LTR   R15,R15
         BZ    TERME
         ABEND 99,DUMP
VDELETE  DC    CL8'VDELETE'
VARIAB1  DC    C'(USER1 USER2 USER3 USER4 USER5 USER6 USER7 USER8 USER9X
                USER10 USER11 USER12 USER13 USER14 USER15 USER16 USER17X
                USER18 USER19 USER20 USER21 USER22 USER23 USER24)'
VARIAB2  DC    C'(USER25 USER26 USER27 USER28 USER29 USER30 USER31 USERX
               32 USER33 USER34 USER35 USER36 USER37 USER38 USER39 USERX
               40 USER41 USER42 USER43 USER44 USER45 USER46 USER47)'
VARIAB3  DC    C'(USER48 USER49 USER50 USER51 USER52 USER53 USER54 USERX
               55 UACC NAME ONAME GROUPID OWNER SUB1 SUB2 SUB3 SUB4 SUBX
               5 SUPGRP NUMGRP NUMU)'
         DS    0H
TERME    EQU   *
         TERME
         EJECT
DBLWRD   DC    D'0'
GRPADDR  DC    F'0'
AUTH     DC    C'AUTH'
MVCSUP   MVC   SUPGRP(0),2(R2)
MVCDEF   MVC   DEFPACK+1(0),2(R2)
MVCOWN   MVC   OWNER(0),2(R2)
MVCOWNM  MVC   ONAME(0),WORK+X'1E'
OWNERL   DC    X'04'
         DC    CL4' '
USERL    DC    X'04'
         DC    CL4' '
GRPL     DC    X'04'               GROUP LENGTH
GRPID    DC    CL4' '
GROUPID  DC    CL4' '
OWNER    DC    CL4' '
SUB1     DC    CL4' '
         DC    CL4' '
         DC    CL4' '
         DC    CL4' '
         DC    CL4' '
SUPGRP   DC    CL4' '
NUMGRP   DC    CL4' '
NUMU     DC    CL4' '
NAME     DC    CL20' '
ONAME    DC    CL20' '
UACC     DC    CL10' '
USER1    DC    24CL11' '
USER25   DC    23CL11' '
USER48   DC    18CL11' '
DEFINED  DC    CL12' '
LDATA    EQU   *-GROUPID
DEFPACK  DC    PL4'0'
         EJECT
*
*        RACF MACROS
*
LOCATEO  ICHEINTY LOCATE,MF=L,TYPE='USR',WKAREA=WORK,                  X
               ACTIONS=ACTLSTO
LOCATE   ICHEINTY LOCATE,MF=L,TYPE='GRP',WKAREA=WORK,                  X
               ACTIONS=(ACTLST1,                                       X
               ACTLST2,                                                X
               ACTLST3,                                                X
               ACTLST4,                                                X
               ACTLST5,                                                X
               ACTLST5A,                                               X
               ACTLST5B,                                               X
               ACTLST6,                                                X
               ACTLST7)
ACTLST1  ICHEACTN MF=L,FIELD=SUPGROUP
ACTLST2  ICHEACTN MF=L,FIELD=AUTHDATE
ACTLST3  ICHEACTN MF=L,FIELD=AUTHOR
ACTLST4  ICHEACTN MF=L,FIELD=UACC
ACTLST5  ICHEACTN MF=L,FIELD=SUBGRPCT
ACTLST5A ICHEACTN MF=L,FIELD=SUBGRPNM
ACTLST5B ICHEACTN MF=L,FIELD=ACLCNT
ACTLST6  ICHEACTN MF=L,FIELD=USERID
ACTLST7  ICHEACTN MF=L,FIELD=USERACS
ACTLSTO  ICHEACTN MF=L,FIELD=PGMRNAME
WORK     DC    F'400'
         DC    400X'00'
         END
