 PUNCH ' INCLUDE ISP(ISPLINK) '
 PUNCH ' INCLUDE ISP(P50215) '
 PUNCH ' ENTRY CMNER15 '
CMNER15  CSECT
***********************************************************************
*                                                                     *
*        PROGRAM - CMNER15                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS A REENTRANT ROUTINE WHICH AUTHORIZES ITSELF   *
*               AND FILLS ALL DIALOG MANAGER VARIABLES ASSOCAIATED    *
*               WITH A USER.                                          *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         PRIME
         REGISTER
         EJECT
         GETMAIN R,LV=DYNWKLN      GET SOME STORAGE
         LR    R8,R1
         LA    R2,DYNWK
         LA    R9,DYNWKLN          LENGTH OF DYNAMIC WORKAREA
         LR    R3,R9               COPY IT
         MVCL  R8,R2               MOVE FROM PROGRAM TO DYNAMIC AREA
         LR    R8,R1               RESTORE POINTER TO DYNAMIC AREA
         USING DYNWK,R8            SET ADDRESSIBILITY
         CALL  ISPLINK,(,,RETCD,,),MF=(E,ISPLNK1)
         CALL  ISPLINK,(,,USERID,,),MF=(E,ISPLNK2)
         CALL  ISPLINK,(,VGUID),MF=(E,ISPLNKB)
         CALL  ISPLINK,(,,NEXTSW,,),MF=(E,ISPLNK9)
         ESTAE ABENDRT
         CALL  ISPLINK,(,VNXTSW),MF=(E,ISPLNKA)
         GETMAIN R,LV=400
         ST    R1,WORK2
         LA    R2,=A(400)
         ST    R2,0(R1)            LENGTH OF WORK AREA
         SPACE 3
*
*        DEFINE PROGRAM VARIABLES TO DIALOG MANAGER
*
DEFNAMEX EQU   *
         CALL  ISPLINK,(,,MGROUP,,),MF=(E,ISPLNK3)
         LTR   R15,R15
         BZ    DEFNAME
         ABEND 41,DUMP
DEFNAME  EQU   *
         CALL  ISPLINK,(,,NAME,,),MF=(E,ISPLNK4)
         LTR   R15,R15
         BZ    DEFDATES
         ABEND 51,DUMP
DEFDATES EQU   *
         CALL  ISPLINK,(,,DEFINED,,),MF=(E,ISPLNK5)
         LTR   R15,R15
         BZ    DEFATTR
         ABEND 61,DUMP
DEFATTR  EQU   *
         CALL  ISPLINK,(,,ATTR1,,),MF=(E,ISPLNK6)
         LTR   R15,R15
         BZ    DEFINT
         ABEND 71,DUMP
         SPACE 3
DEFINT   EQU   *
         CALL  ISPLINK,(,,DAY,,),MF=(E,ISPLNK7)
         LTR   R15,R15
         BZ    RACFINQ
         ABEND 81,DUMP
LNG3     DC    F'3'
*
*        OBTAIN RACF INFORMATION ABOUT THIS USER
*
RACFINQ  EQU   *
         LA    R1,AUTH
         SVC   231
         TM    NEXTSW,X'01'        Q. NEXT?
         BZ    RACFINQ2            A. NO.
         ICHEINTY NEXT,MF=(E,LOCATE),ENTRY=USERL,WKAREA=WORK
         B     RACFINQ3
RACFINQ2 ICHEINTY LOCATE,MF=(E,LOCATE),ENTRY=USERL,WKAREA=WORK
RACFINQ3 LTR   R15,R15
         BZ    DISPL
         CH    R15,=H'12'          NOT FOUND ? ? ? ?
         BE    DISPLNF
         ABEND 91,DUMP
DISPLNF  EQU   *
         ST    R15,RETCD
         CALL  ISPLINK,(VPUT,VRETCD),MF=(E,ISPLNK8)
         B     RETURN1
         DS    0H
DISPL    EQU   *
         SR    R1,R1
         SVC   231
         LA    R2,WORK+X'1C'       LENGTH OF FIRST FIELD (GROUP)
         LH    R3,0(R2)
         BCTR  R3,0
         EX    R3,MVCGRP
         LA    R2,3(R3,R2)         NEXT FIELD (OWNER)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCOWNER
         LA    R2,3(R3,R2)         NEXT FIELD(NAME)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCNAME
         LA    R2,3(R3,R2)         NEXT FIELD(DEFINED)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCDEF
         CALL  P50215,(DEFPACK,DEFINED),MF=(E,P50215L)
         LA    R2,3(R3,R2)         NEXT FIELD(LAST USED)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCDEF
         MVC   LDATE,DEFINED       MOVE IN DEFAULT
         MVC   WORKDAT,DEFPACK+1   MOVE IN WORK DATE
         NI    WORKDAT+2,X'F0'     REMOVE SIGN
         TRT   WORKDAT,PACKTAB     CHECK FOR NUMERICS
         BNZ   L0010
         MVC   WORKDAT,DEFPACK+1   MOVE IN AGAIN
         NI    WORKDAT+2,X'0F'     REMOVE HIGH ORDER NUMERIC
         CLI   WORKDAT+2,X'0C'     CHECK FOR PLUS
         BE    L0005               YES,OKAY
         CLI   WORKDAT+2,X'0F'     CHECK FOR UNSIGNED
         BNE   L0010               NO, THEN NO GOOD
L0005    CALL  P50215,(DEFPACK,LDATE),MF=(E,P50215L)
L0010    LA    R2,3(R3,R2)         NEXT FIELD(LCHNG)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCDEF
         MVC   LCHNG,DEFINED       MOVE IN DEFAULT
         MVC   WORKDAT,DEFPACK+1   MOVE IN WORK DATE
         NI    WORKDAT+2,X'F0'     REMOVE SIGN
         TRT   WORKDAT,PACKTAB     CHECK FOR NUMERICS
         BNZ   L0010
         MVC   WORKDAT,DEFPACK+1   MOVE IN AGAIN
         NI    WORKDAT+2,X'0F'     REMOVE HIGH ORDER NUMERIC
         CLI   WORKDAT+2,X'0C'     CHECK FOR PLUS
         BE    L0015               YES,OKAY
         CLI   WORKDAT+2,X'0F'     CHECK FOR UNSIGNED
         BNE   L0020               NO, THEN NO GOOD
L0015    CALL  P50215,(DEFPACK,LCHNG),MF=(E,P50215L)
L0020    LA    R2,3(R3,R2)         NEXT FIELD(PASSINT)
         SR    R15,R15
         IC    R15,2(R2)           PASSWORD INTERVAL
         CVD   R15,DBLWRD
         MVC   DAYE(4),=X'40202020'
         ED    DAYE,DBLWRD+6
         MVC   DAY,DAYE+1
         LA    R2,3(R2)            NEXT FIELD(ATTR1)
         LA    R5,ATTR1            FIRST ATTRIBUTE
         MVI   ATTR1,C' '          CLEAR
         MVC   ATTR1+1(59),ATTR1
         TM    2(R2),X'80'         ADSP ? ? ? ?
         BNO   NATTR2
         MVC   0(10,R5),=CL10'ADSP'
         LA    R5,10(R5)           NEXT ATTRIBUTE
NATTR2   EQU   *
         LA    R2,3(R2)            ATTR2
         TM    2(R2),X'80'         SPECIAL ? ? ? ?
         BNO   NATTR3
         MVC   0(10,R5),=CL10'SPECIAL'
         LA    R5,10(R5)
NATTR3   EQU   *
         LA    R2,3(R2)            ATTR 3
         TM    2(R2),X'80'         OPERATIONS
         BNO   NATTR4
         MVC   0(10,R5),=CL10'OPERATIONS'
         LA    R5,10(R5)
NATTR4   EQU   *
         LA    R2,3(R2)            ATTR4
         TM    2(R2),X'80'         REVOKE ? ? ? ?
         BNO   NATTR5
         MVC   0(10,R5),=CL10'REVOKED'
         LA    R5,10(R5)
NATTR5   EQU   *
         LA    R2,3(R2)            ATTR5
         TM    2(R2),X'80'         GRPACC ? ? ? ?
         BNO   NATTR6
         MVC   0(10,R5),=CL10'GRPACC'
         LA    R5,10(R5)
NATTR6   EQU   *
         LA    R2,3(R2)            ATTR 6
         TM    2(R2),X'80'         AUDITOR ? ? ? ?
         BNO   NUMCONS
         MVC   0(10,R5),=CL10'AUDITOR'
NUMCONS  EQU   *
         LA    R2,3(R2)            NEXT FIELD(NUMBER OF CONNECTS)
         LH    R6,2(R2)            NUMBER OF CONNECTS
         LTR   R6,R6               ANY CONNECTS ? ? ? ?
         BZ    OWNAME              NO
         CVD   R6,DBLWRD
         MVC   DAYE,=X'40202020'
         ED    DAYE,DBLWRD+6
         MVC   CON(3),DAYE+1
         LA    R2,4(R2)            FIRST CONNECT
         LA    R5,GRP1             FIRST DISPLAY
         CH    R6,=H'6'            LIMIT TO 5
         BL    OK5
         LA    R6,5
OK5      EQU   *
         LH    R3,0(R2)            CONNECT LENGTH
         MVC   0(4,R5),2(R2)
         LA    R5,4(R5)            NEXT DISPLAY
         LA    R2,8(R2)         NEXT CONNECT
         BCT   R6,OK5
OWNAME   EQU   *
         MVC   OWNERL+1(4),MOWNER
         LA    R1,AUTH
         SVC   231
         L     R2,WORK2
         ICHEINTY MF=(E,LOCATEO),ENTRY=OWNERL,WKAREA=(R2)
         LTR   R15,R15
         BZ    MOVEONM
         CH    R15,=H'12'          NOT FOUND ? ? ? ?
         BE    DISPLNFO
         ABEND 92,DUMP
DISPLNFO EQU   *
         MVC   ONAME,=CL20'**NOT FOUND**'
         BE    RETURN
MOVEONM  EQU   *
         SR    R1,R1
         SVC   231
         L     R2,WORK2
         LA    R2,X'1C'(R2)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCONAME
DISP     EQU   *
RETURN   EQU   *
         ST    R15,RETCD
         CALL  ISPLINK,(VPUT,VARIAB),VL
         LTR   R15,R15
         BZ    RETURN1
         ABEND 99,DUMP
         DS    0H
RETURN1  SR    R1,R1               INSURE NOT AUTHORIZED ON EXIT
         SVC   231
TERME    EQU   *
         L     R1,WORK2
         FREEMAIN R,LV=400,A=(1)
         FREEMAIN R,LV=DYNWKLN,A=(8)
         TERME
         SPACE 3
*
*        CHECK USERS AUTHORITY
*
         EJECT
*   CONSTANT SECTION
*
*  DIALOG MANAGER SERVICES
*
VCOPY    DC    CL8'VCOPY'
VGET     DC    CL8'VGET'
VPUT     DC    CL8'VPUT'
VDEF     DC    CL8'VDEFINE'
VDELETE  DC    CL8'VDELETE'
DISPLAY  DC    CL8'DISPLAY'
*
*  VARIABLE LENGTHS
*
LNG1     DC    F'1'
LNG      DC    F'4'
LNG8     DC    F'8'
LNG10    DC    F'10'
LNG12    DC    F'12'
LNG20    DC    F'20'
LNG21    DC    F'21'
*
*  VARIABLE NAMES
*
VNXTSW   DC    C'(CMNERNXT)'
INTRL    DC    C'(CMNERINT)'
VRETCD   DC    C'(CMNERRC)'
VGUID    DC    C'(CMNERUID)'
DEFINEL  DC    C'(CMNERDEF CMNERLUD CMNERLCH)'
NAMES    DC    C'(CMNERGRP '
         DC    C'CMNEROWN '
         DC    C'CMNERGP1 '
         DC    C'CMNERGP2 '
         DC    C'CMNERGP3 '
         DC    C'CMNERGP4 '
         DC    C'CMNERGP5 '
         DC    C'CMNERCON)'
NAMEL    DC    C'(CMNERNAM CMNERONM)'
ATTRL    DC    C'(CMNERAT1 '
         DC    C'CMNERAT2 '
         DC    C'CMNERAT3 '
         DC    C'CMNERAT4 '
         DC    C'CMNERAT5 '
         DC    C'CMNERAT6)'
VARIAB   DC    C'(CMNERGRP '
         DC    C'CMNEROWN '
         DC    C'CMNERNAM '
         DC    C'CMNERONM '
         DC    C'CMNERDEF '
         DC    C'CMNERLUD '
         DC    C'CMNERLCH '
         DC    C'CMNERAT1 '
         DC    C'CMNERAT2 '
         DC    C'CMNERAT3 '
         DC    C'CMNERAT4 '
         DC    C'CMNERAT5 '
         DC    C'CMNERAT6 '
         DC    C'CMNERINT '
         DC    C'CMNERGP1 '
         DC    C'CMNERGP2 '
         DC    C'CMNERGP3 '
         DC    C'CMNERGP4 '
         DC    C'CMNERGP5 '
         DC    C'CMNERCON '
         DC    C'CMNERUID '
         DC    C'CMNERRC)'
VUNAME   DC    CL8'CMNERUID'
*
*  VARIABLE TYPES
*
FIXED    DC    CL8'FIXED'
CHAR     DC    CL8'CHAR'
SHARED   DC    CL8'SHARED'
*
*  SERVICE MODES
*
VLMODE   DC    CL8'LOCATE'
VMMODE   DC    CL8'MOVE'
*
*  MISCELLANEOUS CONTSANT FIELDS
*
MVCCON   MVC   0(0,R5),2(R2)
MVCGRP   MVC   MGROUP(0),2(R2)
MVCOWNER MVC   MOWNER(0),2(R2)
CLCOWNER CLC   WHO(0),2(R2)
MVCNAME  MVC   NAME(0),2(R2)
MVCONAME MVC   ONAME(0),2(R2)
MVCDEF   MVC   DEFPACK+1(0),2(R2)
AUTH     DC    C'AUTH'
PACKTAB  DC    10X'00'             00-09
         DC    6X'01'              0A-0F
         DC    10X'00'             10-19
         DC    6X'01'              1A-1F
         DC    10X'00'             20-29
         DC    6X'01'              2A-2F
         DC    10X'00'             30-39
         DC    6X'01'              3A-3F
         DC    10X'00'             40-49
         DC    6X'01'              4A-4F
         DC    10X'00'             50-59
         DC    6X'01'              5A-5F
         DC    10X'00'             60-69
         DC    6X'01'              6A-6F
         DC    10X'00'             70-79
         DC    6X'01'              7A-7F
         DC    10X'00'             80-89
         DC    6X'01'              8A-8F
         DC    10X'00'             90-99
         DC    6X'01'              9A-9F
         DC    96X'01'             A0-FF
         LTORG
         EJECT
*
*        RACF MACROS
*
ACTLST1  ICHEACTN MF=L,FIELD=DFLTGRP
ACTLST2  ICHEACTN MF=L,FIELD=AUTHOR
ACTLST3  ICHEACTN MF=L,FIELD=PGMRNAME
ACTLST4  ICHEACTN MF=L,FIELD=AUTHDATE
ACTLST5  ICHEACTN MF=L,FIELD=LJDATE
ACTLST5A ICHEACTN MF=L,FIELD=PASSDATE
ACTLST5B ICHEACTN MF=L,FIELD=PASSINT
ACTLST6  ICHEACTN MF=L,FIELD=FLAG1
ACTLST7  ICHEACTN MF=L,FIELD=FLAG2
ACTLST8  ICHEACTN MF=L,FIELD=FLAG3
ACTLST9  ICHEACTN MF=L,FIELD=FLAG4
ACTLSTA  ICHEACTN MF=L,FIELD=FLAG5
ACTLSTB  ICHEACTN MF=L,FIELD=FLAG6
ACTLSTC  ICHEACTN MF=L,FIELD=CONGRPCT
ACTLSTD  ICHEACTN MF=L,FIELD=CONGRPNM
         LTORG
         EJECT
*
*  AREA TO BE DYNAMICALLY ACQUIRED
*
DYNWK    EQU   *
DBLWRD   DC    D'0'
DAYE     DC    X'40202020'
USERADDR DC    F'0'
RETCD    DS    F
WORK2    DC    F'0'
TIOT     DC    F'0'
WHOL     DC    X'04'
WHO      DC    CL8' '
OWNERL   DC    X'04'
         DC    CL4' '
USERL    DC    X'04'               USER LENGTH
USERID   DC    CL8' '
MGROUP   DC    CL4' '
MOWNER   DC    CL4' '
GRP1     DC    CL4' '
GRP2     DC    CL4' '
GRP3     DC    CL4' '
GRP4     DC    CL4' '
GRP5     DC    CL4' '
CON      DC    CL4' '
WHOX     DC    CL4' '
NAME     DC    CL20' '
ONAME    DC    CL20' '
DEFINED  DC    CL12' '             DATE DEFINED
LDATE    DC    CL12' '             DATE LAST USED
LCHNG    DC    CL12' '             DATE LAST CHANGED
ATTR1    DC    CL10' '             ATTRIBUTES
         DC    CL10' '
         DC    CL10' '
         DC    CL10' '
         DC    CL10' '
         DC    CL10' '
DAY      DC    CL3' '
MESSAGE  DC    CL21' '
LDATA    EQU   *-MGROUP
DEFPACK  DC    PL4'0'              DATE DEFINED PACKED
WORKDAT  DC    CL3' '
NEXTSW   DC    X'00'
WORK     DC    F'300'
         DC    300X'00'
LOCATEO  ICHEINTY LOCATE,MF=L,TYPE='USR',WKAREA=WORK,                  X
               ACTIONS=(ACTLST3,ACTLST7)
LOCATE   ICHEINTY LOCATE,MF=L,TYPE='USR',WKAREA=WORK,                  X
               ACTIONS=(ACTLST1,                                       X
               ACTLST2,                                                X
               ACTLST3,                                                X
               ACTLST4,                                                X
               ACTLST5,                                                X
               ACTLST5A,                                               X
               ACTLST5B,                                               X
               ACTLST6,                                                X
               ACTLST7,                                                X
               ACTLST8,                                                X
               ACTLST9,                                                X
               ACTLSTA,                                                X
               ACTLSTB,                                                X
               ACTLSTC,                                                X
               ACTLSTD)
ISPLNK1  CALL  ,(VDEF,VRETCD,RETCD,FIXED,LNG),MF=L
ISPLNK2  CALL  ,(VDEF,VGUID,USERID,CHAR,LNG),VL,MF=L
ISPLNK3  CALL  ,(VDEF,NAMES,MGROUP,CHAR,LNG),VL,MF=L
ISPLNK4  CALL  ,(VDEF,NAMEL,NAME,CHAR,LNG20),VL,MF=L
ISPLNK5  CALL  ,(VDEF,DEFINEL,DEFINED,CHAR,LNG12),VL,MF=L
ISPLNK6  CALL  ,(VDEF,ATTRL,ATTR1,CHAR,LNG10),VL,MF=L
ISPLNK7  CALL  ,(VDEF,INTRL,DAY,CHAR,LNG3),VL,MF=L
ISPLNK8  CALL  ,(VPUT,VRETCD,SHARED),MF=L
ISPLNK9  CALL  ,(VDEF,VNXTSW,NEXTSW,FIXED,LNG1),VL,MF=L
ISPLNKA  CALL  ,(VGET,VNXTSW,),MF=L
ISPLNKB  CALL  ,(VGET,VGUID,),MF=L
P50215L  CALL  ,(DEFPACK,DEFINED),MF=L
DYNWKLN  EQU  *-DYNWK
         LTORG
ABENDRT  EQU  *
         LR    R12,R15
         DROP  R8
         USING ABENDRT,R12
         LR    R2,R1
         LR    R10,R14
         LA    R1,=C'AUTH'
         SVC   231
         SDUMP HDR='TS14 SPF LUSER DUMP',SDATA=(LPA,CSA)
         SR    R1,R1
         SVC   231
         SETRP WKAREA=(R2),RC=0
         BR    R10
         IHASDWA
         END
