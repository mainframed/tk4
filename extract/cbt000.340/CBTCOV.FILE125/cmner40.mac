 PUNCH ' INCLUDE ISP(ISPLINK) '
 PUNCH ' ENTRY CMNER40 '
CMNER40  $PROLOG
         SPACE 5
***********************************************************************
*        PROGRAM - CMNER40                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS NON REENTRANT, NON APF AUTHORIZED MODULE   *
*               WHICH CONTROLS DISPLAY OF THE INTIAL 'MODIFY' SCREEN  *
*               AND CONTROLS THE SELECTION OF ADD SUB FUNCTIONS       *
*                                                                     *
*        PANELS - CMNER40                                             *
*                                                                     *
*        MESSAGES - CMNER400                                          *
*                   CMNER401                                          *
*                   CMNER402                                          *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         BAL   R10,SETUP           DEFINE DIALOG VARIABLES
C40000   CALL  ISPLINK,(VDISPLAY,PANEL,,CURSOR),VL
C40005   CH    R15,=H'8'           WAS END KEY ENTERED ? ? ?
         BE    C40999              YES, THEN GET OUT
         LTR   R15,R15             CHECK FOR ZERO
         BZ    C40010
         ABEND 1,DUMP
C40010   CALL ISPLINK,(VPUT,LIST5,SHARED)
         CLC   CMNERUID,=CL4' '    WAS USERID ENTERED
         BE    C40020              NO GO CHECK GROUP
         CALL  ISPLINK,(VSELECT,CMNERBL,CMNER42B)
         B     C40000
C40020   CLC   CMNERGRP,=CL4' '    WAS GROUP ENTERED
         BE    C40030              NO GO CHECK DSN
         CALL  ISPLINK,(VSELECT,CMNERBL,CMNER43B)
         B     C40000
C40030   CLC   CMNERDSN,=CL44' '   WAS DSN ENTERED
         BE    C40040              NO, GO CHECK GENERAL RESOURCE
         CLC   CMNERVOL,=CL6' '    WAS VOLUME SPECIFIED
         BNE   C40038              YES, THEN PROCEED TO DISPLAY
         LOCATE CAMLIST
         LTR   R15,R15             CHECK RETURN
         BZ    C40036              GOOD
         ST    R15,CMNERRC
         MVC   CURSOR,=CL8'CMNERVOL'
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG03,CURSOR)
         B     C40005
C40036   MVC   CMNERVOL,CATWRK+6
         CALL  ISPLINK,(VPUT,LIST4,SHARED)
C40038   CALL  ISPLINK,(VSELECT,CMNERBL,CMNER44B)
         B     C40000
C40040   CLC   CMNERRES,=CL8' '    WAS RESOURCE ENTERED
         BE    C40060              NO, THEN ISSUE MESSAGE
         CLC   CMNERCLS,=CL8' '    WAS CLASS ENTERED
         BNE   C40050              YES, THE SELECT GENERAL RESOURCE ADD
         CALL ISPLINK,(VDISPLAY,PANEL,MSG01,CURSOR)
         B     C40005
C40050   CALL  ISPLINK,(VSELECT,CMNERBL,CMNER41B)
         B     C40000
C40060   CALL  ISPLINK,(VDISPLAY,PANEL,MSG02,CURSOR)
         B     C40000
C40999   CALL  ISPLINK,(VDELETE,LIST5)
         $EPILOG
         TITLE '***** SETUP ROUTINE *****'
SETUP    EQU   *
         CALL  ISPLINK,(VDEF,LIST1,CMNERDSN,CHAR,LNG44)
         CALL  ISPLINK,(VDEF,LIST2,CMNERUID,CHAR,LNG4)
         CALL  ISPLINK,(VDEF,LIST3,CMNERRES,CHAR,LNG8)
         CALL  ISPLINK,(VDEF,LIST4,CMNERVOL,CHAR,LNG6)
         CALL  ISPLINK,(VDEF,LIST6,CMNERRC,FIXED,LNG4)
         BR    R10
         TITLE '***** CONSTANTS *****'
*
*  DIALOG MANAGER SERVICES
*
VDELETE  DC    CL8'VDELETE'
VDEF     DC    CL8'VDEFINE'
VPUT     DC    CL8'VPUT'
VDISPLAY DC    CL8'DISPLAY'
VSELECT  DC    CL8'SELECT'
*
*  LENGTHS
*
LNG4     DC    F'4'
LNG6     DC    F'6'
LNG8     DC    F'8'
LNG44    DC    F'44'
*
*  DIALOG MANAGER VARIABLE LISTS
*
LIST1    DC    C'(CMNERDSN)'
LIST2    DC    C'(CMNERUID CMNERGRP)'
LIST3    DC    C'(CMNERRES CMNERCLS)'
LIST4    DC    C'(CMNERVOL)'
LIST5    DC    C'(CMNERDSN '
         DC    C'CMNERUID '
         DC    C'CMNERGRP '
         DC    C'CMNERRES '
         DC    C'CMNERCLS '
         DC    C'CMNERVOL)'
LIST6    DC    C'(CMNERRC)'
*
*  DIALOG MANAGER VARIABLES
*
CMNERDSN DC    CL44' '
CMNERUID DC    CL4' '
CMNERGRP DC    CL4' '
CMNERRES DC    CL8' '
CMNERCLS DC    CL8' '
CMNERVOL DC    CL6' '
CMNERRC  DC    F'0'
*
*  DIALOG MANAGER MISCELLANEOUS FIELDS
*
SHARED   DC    CL8'SHARED'
CHAR     DC    CL8'CHAR'
FIXED    DC    CL8'FIXED'
PANEL    DC    CL8'CMNER40'
CURSOR   DC    CL8'CMNERUID'
MSG01    DC    CL8'CMNER400'
MSG02    DC    CL8'CMNER401'
MSG03    DC    CL8'CMNER402'    ENTER VOLUME
CMNERBL  DC    F'12'
CMNER41B DC    C'PGM(CMNER41)'
CMNER42B DC    C'PGM(CMNER42)'
CMNER43B DC    C'PGM(CMNER43)'
CMNER44B DC    C'PGM(CMNER44)'
CAMLIST  CAMLST NAME,CMNERDSN,,CATWRK
         DS    0D
CATWRK   DS    CL265
         END
