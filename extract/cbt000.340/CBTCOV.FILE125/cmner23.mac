CMNER23  $PROLOG
***********************************************************************
*        PROGRAM - CMNER23                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS A REENTRANT ROUTINE WHICH AUTHORIZES ITSELF*
*               BUILDS A AG   COMMAND AND BRANCHES TO ISPCAT. ISPCAT  *
*               IS A SPF SUBROUTINE WHICH ATTACHES AND EXECUTES TSO   *
*               COMMANDS.                                             *
*                                                                     *
*        PANELS USED - CMNER23                                        *
*                                                                     *
*        ERROR MESSAGES - CMNER230 - CMNER233                         *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         GETMAIN R,LV=DYNWKLN
         LR    R8,R1               COPY ADDR OF GOTTEN STORAGE
         LA    R9,DYNWKLN          LENGTH OF DYNAMIC AREA
         LR    R3,R9               COPY LENGTH FOR MOVE
         LA    R2,DYNWK            BEGINNING ADDR OF MOVE
         MVCL  R8,R2               MOVE INTO GOTTEN AREA
         USING DYNWK,R8
         LR    R8,R1               RESTORE TO BEGININNING
         BAL   R10,SETUP
         CALL  ISPLINK,(VDISPLAY,PANEL,,CURSOR)  DISPLAY 1ST PANEL
C23015   MVI   CMD+3,C' '          CLEAR COMMAND BUFFER
         MVC   CMD+4(250),CMD+3
         CH    R15,=H'8'           WAS END KEY ENTERED
         BE    C23999              YES, THEN GET OUT
         LTR   R15,R15             CHECK FOR GOOD RETURN
         BZ    C23020              YES--
         ABEND 1,DUMP
C23020   CLC   CMNEBGRP,CMNERGRP   DID GROUP CHANGE    ?
         BNE   C23030              YES, THEN OKAY
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG01,CURSOR)
         B     C23015              NO, DISPLAY ERROR AND TRY AGAIN
C23030   LA    R3,CMD+3            GET 1ST AVAILABLE BYTE IN COMMAND
         MVC   0(8,R3),CMNERGRP    MOVE IN CLASS
         TRT   0(9,R3),BLANKTAB    SEARCH FOR BLANK AFTER CLASS
         BNZ   C23040              FOUND IT
         ABEND 2,DUMP              NOT FOUND
C23040   LA    R3,1(R1)            ADVANCE BEYOND 1ST BLANK
         MVC   0(6,R3),=C'OWNER('  MOVE IN OWNER PARAMETER
         MVC   6(L'CMNEROWN,R3),CMNEROWN    MOVE IN OWNER FROM SCREEN
         TRT   0(L'CMNEROWN+7,R3),BLANKTAB  CHECK FOR FIRST BLANK
         BNZ   C23050              FOUND IT
         ABEND 2,DUMP
C23050   MVI   0(R1),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R1)            ADVANCE BEYOND
         MVC   0(9,R3),=C'SUPGROUP('  MOVE IN SUPERIOR GROUP KEYWORD
         MVC   9(L'CMNERSUG,R3),CMNERSUG    MOVE IN SUPERIOR GROUP
         TRT   0(L'CMNERSUG+10,R3),BLANKTAB CHECK FOR FIRST BLANK
         BNZ   C23070              FOUND IT
         ABEND 2,DUMP
C23070   MVI   0(R1),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R1)            ADVANCE BEYOND
         CLC   CMNERNOT,=C'YES'    CHECK TERMUACC
         BE    C23080              DEFAULT--
         MVC   0(10,R3),=C'NOTERMUACC'
         SPACE 5
C23080   EQU   *
*
*   BEGIN BUILDING THE CALL LIST TO ISPCAT
*
         L     R2,X'21C'           TCBOLD
         L     R2,X'70'(R2)        ADDR OF 1ST SAVE AREA
         L     R2,X'18'(R2)        R1 IN 1ST SAVE ARE PTS TO PARM
         L     R2,0(R2)            FIRST PARM IS TLD
         CLC   =C'TLD',0(R2)       CHECK EYE CATCHER
         BE    C23110              GOOD
         ABEND 3,DUMP
C23110   ST    R2,ISPCATCL         STORE ADDR OF TLD
         LOAD  EP=ISPSUBS
         LR    R3,R0               STORE EP OF ISPSUBS
         LA    R1,=C'AUTH'         MAKE AUTHORIZED
         SVC   231
         LA    R4,ISPCATRC         RELOCATE
         ST    R4,ISPCATCL+8           IN DYNAMIC AREA
         LA    R4,CMDBUF
         ST    R4,ISPCATCL+4
         L     R15,X'2C'(R3)       GET ADDR OF ISPCAT ENTRY POINT
         LA    R1,ISPCATCL         GET PARAMETER LIST
         BALR  R14,R15             AND GO DO IT
         L     R4,ISPCATRC         GET RETURN CODE
         LTR   R4,R4               CHECK IT
         BZ    C23120              GOOD
         ST    R4,CMNERRC
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG02,CURSOR)
         B     C23015
C23120   MVC   CMNEBGRP,CMNERGRP    STORE BEFORE COPY
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG03,CURSOR)
         B     C23015
         SPACE 5
C23999   SR    R1,R1
         SVC   231                 REMOVE AUTHORIZATION
         FREEMAIN R,LV=DYNWKLN,A=(R8)
         $EPILOG
         TITLE '***** SETUP ROUTINE *****'
SETUP    EQU   *
         CALL  ISPLINK,(,,CMNERGRP,,),MF=(E,ISPLNK1)
         CALL  ISPLINK,(,,CMNERNOT,,),MF=(E,ISPLNK2)
         CALL  ISPLINK,(,,CMNERRC,,),MF=(E,ISPLNK3)
         CALL  ISPLINK,(,LIST1,),MF=(E,ISPLNK4)
         L     R4,544              ADDR OF ASCB
         L     R4,108(R4)          ADDR OF ASXB
         L     R4,200(R4)          ADDR OF ACEE
         USING ACEE,R4             SET UP ADDRESSIBILITY
         MVC   CMNEROWN,ACEEUSRI   MOVE IN DEFAULT OWNER
         MVC   CMNERSUG,ACEEGRPN
         DROP  R4
         MVC   CMNERNOT,=C'YES'
         BR    R10
         TITLE '***** CONTANTS *****'
*
*        DIALOG MANAGER SERVICES
*
VGET     DC    CL8'VGET'
VDEF     DC    CL8'VDEFINE'
VDISPLAY DC    CL8'DISPLAY'
*
*        DIALOG MANAGER LISTS
*
LIST1    DC    C'(CMNERGRP CMNEROWN CMNERSUG)'
LIST2    DC    C'(CMNERNOT)'
LIST3    DC    C'(CMNERRC)'
*
*        DIALOG MANAGER LENGTHS
*
LNG3     DC    F'3'
LNG4     DC    F'4'
LNG8     DC    F'8'
*
*        DIALOG MANAGER MISCELLANEOUS FIELDS
*
FIXED    DC    CL8'FIXED'
SHARED   DC    CL8'SHARED'
CHAR     DC    CL8'CHAR'
PANEL    DC    CL8'CMNER23'
MSG01    DC    CL8'CMNER230'
MSG02    DC    CL8'CMNER231'
MSG03    DC    CL8'CMNER232'
CURSOR   DC    CL8'CMNERGRP'
BLANKTAB DC    64X'00'
         DC    X'01'
         DC    191X'00'
         LTORG
         TITLE '***** DYNAMIC AREAS *****'
DYNWK    EQU   *
ISPCATRC DC    A(0)
ISPCATCL CALL  ,(,CMDBUF,ISPCATRC),MF=L
ISPLNK1  CALL   ,(VDEF,LIST1,CMNERGRP,CHAR,LNG8),MF=L
ISPLNK2  CALL   ,(VDEF,LIST2,CMNERNOT,CHAR,LNG3),MF=L
ISPLNK3  CALL  ,(VDEF,LIST3,CMNERRC,FIXED,LNG4),MF=L
ISPLNK4  CALL  ,(VGET,LIST1,SHARED),MF=L
CMDBUF   DC    H'255'
CMDOFF   DC    H'3'
CMD      DC    CL255'AG '
CMNEBGRP DC    CL8' '
*
*        DIALOG MANAGER VARIABES
*
CMNERGRP DS    CL8
CMNEROWN DS    CL8
CMNERSUG DC    CL8' '
CMNERNOT DC    CL3'YES'
CMNERRC  DS    F
         SPACE 5
DYNWKLN  EQU   *-DYNWK
         EJECT
         IHAACEE
         END
