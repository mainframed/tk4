CMNER12  $PROLOG
***********************************************************************
*                                                                     *
*        PROGRAM - CMNER12                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS A NON REENTRANT ROUTINE WHICH DISPLAYS ALL *
*               RACF INFORMATION ABOUT DATASETS. IT DOES NOT REQUIRE  *
*               APF AUTHORIZATION.                                    *
*                                                                     *
*        PANELS USED - CMNER12                                        *
*                                                                     *
*        ERROR MESSAGES - CMNER120 - CMNER123                         *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         BAL   R10,SETUP
P30010   CALL  ISPLINK,(VGET,LIST1,SHARED)
         CLC   CMNERVOL,=CL6' '    WAS VOLUME SPECIFIED
         BNE   P30030              YES, THEN DON'T READ CATALOG
         LOCATE CAMLIST            GET VOLUME FROM CATALOG
         LTR   R15,R15             CHECK RETURN
         BZ    P30020              OKAY
         ST    R15,CMNERRC         IF NOT GOOD, STORE RETURN AND
*                                  DISPLAY
         CALL ISPLINK,(VDISPLAY,CMNER01,MSG01,)
         B     P30099              AND GET OUT
P30020   MVC   CMNERVOL,WORK+6     MOVE IN VOLUME
         CALL  ISPLINK,(VPUT,VVOL,SHARED)
P30030   CALL  ISPLINK,(VPUT,VNXTSW,SHARED)
         CALL  ISPLINK,(VSELECT,LNG12,P50300)
         CALL  ISPLINK,(VGET,LIST1,SHARED)
         L     R8,CMNERRC          GET RETURN CODE FROM FILL MODULE
         LTR   R8,R8
         BZ    P30035              IF ZERO CONTINUE
*                                  OTHERWISE DISPLAY AN ERROR MSG
         ST    R8,CMNERRC
         CALL  ISPLINK,(VDISPLAY,CMNER01,MSG02,)
         B     P30099              AND GET OUT
P30035   BAL   R10,CHKAUTH         CHECK AUTHORIZATION
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   P30099              IF NOT ZERO, THEN GET OUT
         CALL ISPLINK,(VTBDISPL,TABLE,CMNER01,)
P30036   C     R15,=F'8'           WAS END KEY ENTERED? ? ?
         BE    P30099              YES, THEN GET OUT
         LTR   R15,R15             CHECK FOR GOOD RETURN
         BZ    P30040              YES, CONTINUE
         ABEND 3,DUMP              NO, SAY BYE
P30040   EQU   *
         MVI   CMNERNXT,X'00'      SET DEFAULT NEXT SWITCH
         CLC   CMNEROPT,=CL8'NEXT' IS IT A REQUEST FOR THE NEXT DATASET
         BE    P30050              YES--
         CALL  ISPLINK,(VTBDISPL,TABLE,CMNER01,MSG04)
         B     P30036
P30050   MVI   CMNERNXT,X'01'      YES, THEN SET NEXT SWITCH
         CALL  ISPLINK,(VPUT,VNXTSW,SHARED)
         CALL  ISPLINK,(VTBEND,TABLE)    REMOVE OLD TABLE
         CALL  ISPLINK,(VTBCRET,TABLE,,LIST2,NOWRITE,)
         B     P30010
P30099   CALL  ISPLINK,(VTBEND,TABLE)
         $EPILOG
         TITLE '***** CHECK AUTHORIZATION ROUTINE *****'
CHKAUTH  L     R4,544              ADDR OF ASCB
         L     R4,108(R4)          ADDR OF ASXB
         L     R4,200(R4)          ADDR OF ACEE
         USING ACEE,R4             SET UP ADDRESSIBILITY
         LA    R15,0               SET DEFAULT RETURN
         TM    ACEEFLG1,ACEESPEC   IS USER SPECIAL
         BOR   R10                 YES, THEN RETURN
         XR    R3,R3               CLEAR R3
         IC    R3,ACEEUSRL         GET LENGTH OF USER
         BCTR  R3,0                REDUCE FOR COMPARE
         EX    R3,CHK1             CHECK IF 1ST LEVEL = USERID
         BER   R10                 YES, THEN RETURN
         EX    R3,CHK2             CHECK IF USERID = OWNERID
         BER   R10                 YES, THEN RETURN
*
*                                  CHECK FOR AT LEAST READ ACCESS
         RACHECK ENTITY=CMNERDSN,VOLSER=CMNERVOL,CLASS='DATASET',      X
               ATTR=READ
         LTR   R15,R15             CHECK RETURN FROM RACHECK
         BZR   R10                 GOOD, THEN RETURN
*
*                                  IF NO GOOD, DISPLAY AUTHORIZATION
*                                  FAILED MSG
         CALL  ISPLINK,(VDISPLAY,CMNER01,MSG03,)
         BR    R10
         TITLE '***** SETUP ROUTINE *****'
SETUP    EQU   *
*
*  THIS ROUTINE PERFORMS ALL INTIAL DIALOG MANAGER SERVICES
*
         CALL  ISPLINK,(VTBCRET,TABLE,,LIST2,NOWRITE,)
         CALL  ISPLINK,(VDEF,VOPT,CMNEROPT,CHAR,LNG8)
         CALL  ISPLINK,(VDEF,VDSN,CMNERDSN,CHAR,LNG44)
         CALL  ISPLINK,(VDEF,VVOL,CMNERVOL,CHAR,LNG6)
         CALL  ISPLINK,(VDEF,VNXTSW,CMNERNXT,FIXED,LNG1)
         CALL  ISPLINK,(VDEF,VRETCD,CMNERRC,FIXED,LNG4)
         CALL  ISPLINK,(VDEF,VOWN,CMNEROWN,CHAR,LNG8)
         CALL  ISPLINK,(VGET,LIST3,SHARED)
         BR    R10
         TITLE '***** CONSTANTS *****'
*
*  DIALOG MANAGER SERVICES
*
VGET     DC    CL8'VGET'
VPUT     DC    CL8'VPUT'
VDEF     DC    CL8'VDEFINE'
VCOPY    DC    CL8'VCOPY'
VTBDISPL DC    CL8'TBDISPL'
VTBCRET  DC    CL8'TBCREATE'
VTBEND   DC    CL8'TBEND'
VSELECT  DC    CL8'SELECT'
VDISPLAY DC    CL8'DISPLAY'
*
*  DIALOG MANGER LENGTHS
*
LNG1     DC    F'1'
LNG2     DC    F'2'
LNG4     DC    F'4'
LNG6     DC    F'6'
LNG8     DC    F'8'
LNG11    DC    F'11'
LNG12    DC    F'12'
LNG44    DC    F'44'
*
*  DIALOG MANAGER LISTS
*
VOPT     DC    C'(CMNEROPT)'
VDSN     DC    C'(CMNERDSN)'
VVOL     DC    C'(CMNERVOL)'
VNXTSW   DC    C'(CMNERNXT)'
VRETCD   DC    C'(CMNERRC)'
VOWN     DC    C'(CMNEROWN)'
LIST1    DC    C'(CMNERDSN '
         DC    C'CMNERRC '
         DC    C'CMNERVOL '
         DC    C'CMNEROWN '
         DC    C'CMNERUAC '
         DC    C'CMNERCDT '
         DC    C'CMNERLUP)'
LIST2    DC    C'(CMNERTUI '
         DC    C'CMNERTAU '
         DC    C'CMNERTAC)'
LIST3    DC    C'(CMNERDSN '
         DC    C'CMNERVOL)'
*
*  DIALOG MANAGER VARIABLES
*
CMNEROPT DC    CL8' '
CMNERDSN DS    CL44
CMNERVOL DC    CL6' '
CMNERRC  DS    F
CMNERNXT DC    X'00'
CMNEROWN DS    CL8
DSNADDR  DS    A
*
*  DIALOG MANAGER PANELS, FUNCTIONS AND MESSAGES
*
CMNER01  DC    CL8'CMNER12'
P50300   DC    C'PGM(CMNER16)'
MSG01    DC    CL8'CMNER120'
MSG02    DC    CL8'CMNER121'
MSG03    DC    CL8'CMNER122'
MSG04    DC    CL8'CMNER123'
*
*  DIALOG MANAGER MISCELLANEOUS FIELDS
*
FIXED    DC    CL8'FIXED'
CHAR     DC    CL8'CHAR'
SHARED   DC    CL8'SHARED'
NOWRITE  DC    CL8'NOWRITE'
TABLE    DC    CL8'CMNERTDS'
*
*  MISCELLANEOUS FIELDS
*
CAMLIST  CAMLST NAME,CMNERDSN,,WORK
WORK     DS    0D
         DS    CL265
         DS    0F
CHK1     CLC   CMNERDSN(0),ACEEUSRI
CHK2     CLC   CMNEROWN(0),ACEEUSRI
         IHAACEE
         END
