 PUNCH ' INCLUDE ISP(ISPLINK) '
 PUNCH ' INCLUDE ISP(P50215) '
 PUNCH ' ENTRY CMNER11 '
CMNER11  $PROLOG
***********************************************************************
*                                                                     *
*        PROGRAM - CMNER11                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - DISPLAY PANELS FOR THE LIST USER FUNCTION FOR THE  *
*               MILLER SPF/RACF INTERFACE. THIS MODULE IS NOT RE-     *
*               ENTRANT AND ISSUES NO RACF MANAGER MACROS.            *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         CALL ISPLINK,(VDEF,VRETCD,RETCD,FIXED,LNG4)
         LTR   R15,R15             CHECK RETURN
         BZ    L0005
         ABEND 10,DUMP
L0005    CALL ISPLINK,(VDEF,VNXTSW,NEXTSW,FIXED,LNG1),VL
         LTR   R15,R15
         BZ    L0010
         ABEND 11,DUMP
L0010    MVC   RETLN,LNG8
         CALL  ISPLINK,(VCOPY,VUNAME,RETLN,USERID,VMODE),VL
         LTR   R15,R15
         BZ    L0020
         ST    R15,RETCD           STORE RETURN CODE TO SHOW
         CALL  ISPLINK,(VDISPLAY,PUSER,RL97001,CURSOR),VL
         B     DONE
L0020    CLC   USERID,=CL8' '      CHECK FOR BEGININNING
         BNE   L0021               NO--
         OI    NEXTSW,X'01'        SET NEXT SWITCH
L0021    CALL  ISPLINK,(VPUT,VNXTSW),VL  PUT IT IN SHARED POOL
         CALL  ISPLINK,(VSELECT,LNG12,P50297)
         LTR   R15,R15
         BZ    L0022
         ABEND 22,DUMP
L0022    CALL  ISPLINK,(VGET,NAMELIST,SHARED),VL
         L     R15,RETCD           GET RETURN CODE FROM P50297
         C     R15,=F'12'          WAS USER FOUND? ?
         BNE   L0023               YES--
         CALL  ISPLINK,(VDISPLAY,PUSER,RL97002,CURSOR),VL
         B     DONE
L0023    MVC   RETLN,LNG8
         CALL  ISPLINK,(VCOPY,VUNAME,RETLN,USERID,VMODE),VL
         LTR   R15,R15
         BZ    L0027
         ST    R15,RETCD           STORE RETURN CODE TO SHOW
         CALL  ISPLINK,(VDISPLAY,PUSER,RL97001,CURSOR),VL
         B     DONE
L0027    BAL   R10,CKAUTH          CHECK AUTHORITY TO LIST
         LTR   R15,R15             SHOULD BE ZERO
         BNZ   DONE
         CALL  ISPLINK,(VDISPLAY,PUSER,,CURSOR),VL
         LTR   R15,R15
         BZ    L0030
         C     R15,=F'8'           CHECK FOR END KEY
         BE    DONE                YES, THEN GET OUT
         ST    R15,RETCD           STORE IT
         CALL  ISPLINK,(VDISPLAY,PUSER,RL97004,CURSOR),VL
         B     DONE
L0030    OI    NEXTSW,X'01'
         CALL  ISPLINK,(VCOPY,VUNAME,RETLN,USERADDR,VLOCATE),VL
         L     R2,USERADDR
         CLC   USERID,0(R2)        CHECK IF USER CHANGED
         BE    L0020               NO, THEN GET NEXT
         MVI   NEXTSW,X'00'
         CALL  ISPLINK,(VREPLACE,VUNAME,LNG4,(R2))
         LTR   R15,R15
         BZ    L0040
         ST    R15,RETCD
         CALL  ISPLINK,(VDISPLAY,PUSER,RL97005,CURSOR)
         B     DONE
L0040    CALL  ISPLINK,(VPUT,VPUNAME),VL
         B     L0020
DONE     MVI   NEXTSW,X'00'
         $EPILOG 0
         EJECT
*  CHECK AUTHORIZATION SUBROUTINE
CKAUTH   EQU   *
         L     R4,544              GET ADDR OF ASCB
         L     R4,108(R4)          GET ADDR OF ASXB
         L     R4,200(R4)          GET ADDR OF ACEE
         USING ACEE,R4
         LA    R15,0               SET DEFAULT RETURN
         TM    ACEEFLG1,ACEESPEC   AM I SPECIAL
         BOR   R10                 YES, THEN OKAY
         XR    R8,R8               CLEAR R8
         IC    R8,ACEEUSRL         GET USER LENGTH
         BCTR  R8,0                REDUCE FOR COMPARE
         EX    R8,CLCUSR           IS IT MY ID
         BER   R10                 YES, THEN OKAY
         CALL ISPLINK,(VCOPY,VOWNER,RETLN,OWNADDR,VLOCATE)
         LTR   R15,R15
         BZ    CHKAUTH1
         ABEND 42,DUMP
CHKAUTH1 L     R4,OWNADDR         GET ADDR OF OWNER
         LA    R15,0
         CLC   ACEEUSER(4),0(R4)   AM I THE OWNER? ?
         BER   R10                 YES, THEN OKAY
         CALL ISPLINK,(VDISPLAY,PUSER,RL97003,CURSOR),VL
         LTR   R15,R15
         BZ    CHKAUTH2
         ABEND 52,DUMP
CHKAUTH2 LA    R15,4
         BR    R10
         EJECT
*  VARIABLE LENGTHS
LNG1     DC    F'1'
LNG4     DC    F'4'
LNG8     DC    F'8'
LNG11    DC    F'11'
LNG12    DC    F'12'
RETLN    DC    F'0'
*  DIALOG MANAGER SERVICES
VDEF     DC    CL8'VDEFINE'
VPUT     DC    CL8'VPUT'
VGET     DC    CL8'VGET'
VREPLACE DC    CL8'VREPLACE'
VLOCATE  DC    CL8'LOCATE'
VCOPY    DC    CL8'VCOPY'
VDISPLAY DC    CL8'DISPLAY'
VSELECT  DC    CL8'SELECT'
VMODE    DC    CL8'MOVE'
SHARED   DC    CL8'SHARED'
*  VARIABLE NAMES
VRETCD   DC    C'(CMNERRC)'
VUNAME   DC    CL8'CMNERUID'
VNXTSW   DC    C'(CMNERNXT)'
VPUNAME  DC    C'(CMNERUID)'
NAMELIST DC    C'(CMNERINT '
         DC    C'CMNERRC '
         DC    C'CMNERUID '
         DC    C'CMNERDEF '
         DC    C'CMNERLUD '
         DC    C'CMNERLCH '
         DC    C'CMNERGRP '
         DC    C'CMNEROWN '
         DC    C'CMNERGP1 '
         DC    C'CMNERGP2 '
         DC    C'CMNERGP3 '
         DC    C'CMNERGP4 '
         DC    C'CMNERGP5 '
         DC    C'CMNERCON '
         DC    C'CMNERNAM '
         DC    C'CMNERONM '
         DC    C'CMNERAT1 '
         DC    C'CMNERAT2 '
         DC    C'CMNERAT3 '
         DC    C'CMNERAT4 '
         DC    C'CMNERAT5 '
         DC    C'CMNERAT6)'
*  VARIABLES
RETCD    DS    F
USERID   DS    CL8
USERADDR DS    A
*  VARIABLE TYPES
FIXED    DC    CL8'FIXED'
CHAR     DC    CL8'CHAR'
*  MESSAGES
RL97001  DC    CL8'CMNER110'
RL97002  DC    CL8'CMNER112'
RL97003  DC    CL8'CMNER113'
RL97004  DC    CL8'CMNER114'
RL97005  DC    CL8'CMNER115'
NEXTSW   DC    X'00'
*  PANELS
PUSER    DC    CL8'CMNER11 '
CURSOR   DC    CL8'CMNERUID'
P50297   DC    C'PGM(CMNER15)'
CLCUSR   CLC   USERID(0),ACEEUSRI
VOWNER   DC    CL8'CMNEROWN'
OWNADDR  DS    A
         IHAACEE
         END
