         TITLE '***** CONNECT GROUP *****'
CMNER53  $PROLOG R12,R6
***********************************************************************
*                                                                     *
*        PROGRAM - CMNER53                                            *
*        DATE WRITTEN - AUGUST 1981                                   *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS A REENTRANT WHICH ALLOWS MODIFICATION OF   *
*               ACCESS LISTS FOR GENERAL RESOURCES. IT BUILDS A PERMIT*
*               COMMAND AND BRANCHES TO SPF SUBROUTINE ISPCAT         *
*                                                                     *
*        PANELS USED - CMNER53                                        *
*                      CMNER54                                        *
*                      CMNER55                                        *
*                      CMNER56                                        *
*                                                                     *
*        ERROR MESSAGES - CMNER530 - CMNER539                         *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         GETMAIN R,LV=DYNWKLN
         LR    R8,R1               COPY ADDR OF GOTTEN STORAGE
         LA    R9,DYNWKLN          LENGTH OF DYNAMIC AREA
         LR    R3,R9               COPY LENGTH FOR MOVE
         LA    R2,DYNWK            BEGINNING ADDR OF MOVE
         MVCL  R8,R2               MOVE INTO GOTTEN AREA
         USING DYNWK,R8
         LR    R8,R1               RESTORE TO BEGININNING
         BAL   R10,SETUP
P53010   CALL  ISPLINK,(VGET,LIST1,SHARED)
P53030   CALL  ISPLINK,(VPUT,VNXTSW,SHARED)
         CALL  ISPLINK,(VSELECT,LNG12,CMNER17)
         CALL  ISPLINK,(VGET,LIST1,SHARED)
         L     R15,CMNERRC         GET RETURN CODE FROM FILL MODULE
         LTR   R15,R15
         BZ    P53035              IF ZERO CONTINUE
*                                  OTHERWISE DISPLAY AN ERROR MSG
         ST    R15,CMNERRC
         CALL  ISPLINK,(VDISPLAY,CMNER01,MSG01,)
         B     P53099              AND GET OUT
P53035   BAL   R10,CHKAUTH         CHECK AUTHORIZATION
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   P53099              IF NOT ZERO, THEN GET OUT
         XR    R11,R11             CLEAR MSG REG
         BAL   R10,DISPSUB         GO DISPLAY SOMETHING
         SPACE 5
P53040   EQU   *                   CHECK SELECTION CRITERA
         CLI   CMNERSEL,C' '       WAS A SELECTION MADE
         BE    P53150              YES--GO TO COMMAND PROCESSING
         CLI   CMNERSEL,0          NULL OR BLANK MEAN NO SELECTION MADE
         BE    P53150
         CLC   CMNERTUI,=CL8' '    CHECK IF VALID SELECT LINE
         BNE   P53145              OKAY
         LA    R11,MSG04           NO, ISSUE MESSAGE
         BAL   R10,DISPSUB
         B     P53040
P53145   MVI   CMD+3,C' '          CLEAR COMMAND BUFFER
         MVC   CMD+4(251),CMD+3
         XR    R2,R2               CLEAR R2
         TRT   CMNERSEL,COMTAB     CHECK WHICH OPTION
         BNZ   P53050              FOUND IT
         LA    R11,MSG06
         BAL   R10,DISPSUB
         B     P53040
P53050   B     P53050(R2)
         B     P53060              (4 -> C)
         B     P53090              (8 -> D)
P53060   MVC   CMNERUID,CMNERTUI   SET UP FIELDS FOR DISPLAY
         MVC   CMNERGAU,CMNERTAC
         MVC   CMNERUAC,CMNERTUA
         MVC   CMNERGAC,=C'NO '    SET DEFAULT ATTRIBUTES
         MVC   CMNERADS,=C'NO '
         MVC   CMNERSPC,=C'NO '
         MVC   CMNEROPN,=C'NO '
         MVC   CMNERREV,=C'NO '
         BAL   R10,FILLATT         NOW, GO FILL ATTRIBUTES FOR DISPLAY
         CALL  ISPLINK,(VTBQUERY,TABLE,,,VROWNP,,,VCRPNP)
         CALL  ISPLINK,(VTBDEL,TABLE)  DELETE CURRENT ROW
         LTR   R15,R15             CHECK RETURN
         BZ    P53062              GOOD
         ABEND 13,DUMP
P53062   EQU   *
         L     R7,CMNERCRP         GET CURRENT CRP
P53063   C     R7,CMNERROW         IF SAME AS CURRENT NUM OF ROWNS THEN
*                                  WE ARE ON THE LAST ROW
         BE    P53070              AND THERE CAN'T BE ANY MORE
*                                  ATTRIBUTE ROWS
         CALL  ISPLINK,(VTBSKIP,TABLE,LNG1) IF NOT ON LAST, SKIP AHEAD
         LTR   R15,R15             CHECK RETURN
         BZ    P53065              GOOD
         ABEND 10,DUMP
P53065   CLC   CMNERTUI,=CL8' '    CHECK IF THIS IS NEXT USER
         BNE   P53068              YES, THEN GO AND BACK UP ONE
         BAL   R10,FILLATT         NO, THEN MUST BE ATTRIBUTE ROW
         LA    R7,1(R7)            ADVANCE CRP COUNTER
         CALL  ISPLINK,(VTBDEL,TABLE) DELETE THIS ROW
         LTR   R15,R15             CHECK RETURN
         BZ    P53063              GOOD, THEN CHECK NEXT ROW
         ABEND 10,DUMP             NO GOOD, SAY BYE
P53068   CALL  ISPLINK,(VTBSKIP,TABLE,NEG1)   BACK UP 1 ROW
         LTR   R15,R15             CHECK RETURN
         BZ    P53070              OKAY
         CH    R15,=H'8'           ARE WE AT TOP
         BE    P53070              YES, THEN OKAY
         ABEND 10,DUMP             NO, SAY BYE
P53070   CALL  ISPLINK,(VDISPLAY,CMNER02,,) DISPLAY CONNECT PANEL
         LTR   R15,R15
         BZ    P53072              IF GOOD CONTINUE
         CH    R15,=H'8'           END KEY ? ?
         BE    P53099              YES
         ABEND 11,DUMP             OTHERWISE-----------
P53072   BAL   R10,BLDCON          GO BUILD CONNECT COMMAND
         MVC   CMNERTMS,=CL10'**CHANGED' MOVE IN DEFAULT MESSAGE
         B     P53120              GO ISSUE COMMAND
         SPACE 5
P53090   EQU   *                   PROCESS REMOVE COMMAND
         MVC   CMD(2),=C'RE'       MOVE IN COMMAND
         MVC   CMD+3(L'CMNERUID),CMNERUID  MOVE IN USER ID
         LA    R3,L'CMNERUID+4(R3) ADVANCE TO FIRST BLANK
         MVC   0(6,R3),=C'GROUP('  MOVE IN KEYWORD
         MVC   6(L'CMNERGRP,R3),CMNERGRP MOVE IN GROUP
         TRT   6(L'CMNERGRP+1,R3),BLANKTAB SEARCH FOR FIRST BLANK
         MVI   0(R1),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R1)            ADVANCE TO NEXT BLANK
         CLC   CMNEROWN,=CL8' '    WAS OWNER ENTERED
         BE    P53100              NO, THEN BYPASS IT
         MVC   0(6,R3),=C'OWNER('  MOVE IN KEYWORD
         MVC   6(L'CMNEROWN,R3),CMNEROWN MOVE IN OWNERE
         TRT   6(L'CMNEROWN+1,R3),BLANKTAB SEARCH FOR 1ST BLANK
         MVI   0(R1),C')'
P53100   MVC   CMNERTMS,=C'***DELETED'
P53120   BAL   R10,ISSUE           GO ISSUE COMMAND
         CLC   ISPCATRC,=F'0'      CHECK FOR GOOD RETURN
         BE    P53140              YES-
         MVC   CMNERTMS,=CL10'**FAILED'
P53140   CALL  ISPLINK,(VTBPUT,TABLE,)
         SPACE 5
P53150   EQU   *                   BEGIN
*                                       PROCESSING
*                                            COMMAND
*                                                LINE
*
         OC    CMNEROPT,=CL20' '
         CLC   CMNEROPT(4),=C'NEXT'
         BE    P53190              YES
         CLC   CMNEROPT(2),=C'A '  IS THIS AN ADD
         BE    P53160
P53152   MVI   CMNERNXT,X'00'      SET DEFAULT NEXT SWITCH
         LA    R11,MSG02
         BAL   R10,DISPSUB
         B     P53040
P53160   EQU   *                   ADD NEW USER TO ACCESS LIST
         MVI   CMD+3,C' '          CLEAR COMMAND BUFFER
         MVC   CMD+4(251),CMD+3
         CALL  ISPLINK,(VTBSKIP,TABLE,NEG1) BACK UP ONE
         LTR   R15,R15             CHECK RETURN
         BZ    P53170              OKAY
         CH    R15,=H'8'           AT TOP OF TABLE
         BE    P53170              YES, THEN OKAY
         ABEND 12,DUMP             OTHERWISE, SAY BYE
P53170   MVC   CMNERGAC,=CL3'NO'   SET DEFAULTS
         MVC   CMNERADS,=CL3'NO'
         MVC   CMNERSPC,=CL3'NO'
         MVC   CMNEROPN,=CL3'NO'
         MVC   CMNERREV,=CL3'NO'
         MVC   CMNERUAC,=CL8'READ'
         MVC   CMNERUID,=CL8' '
         MVC   CMNERGAU,=CL8'USE'
         CALL  ISPLINK,(VDISPLAY,CMNER04,,)
         LTR   R15,R15             CHECK RETURN
         BZ    P53180
         CH    R15,=H'8'           WAS END KEY ENTERED? ?
         BE    P53099
         ABEND 10,DUMP
P53180   BAL   R10,BLDCON          GO BUILD CONNECT COMMAND
         BAL   R10,ISSUE           NOW, ISSUE IT
         LA    R11,MSG08
         MVC   CMNERTMS,=CL10'**FAILED' SET MESSAGES
         CLC   ISPCATRC,=F'0'      CHECK RETURN
         BNE   P51185              GO DISPLAY
         LA    R11,MSG09
         MVC   CMNERTMS,=CL10'**ADDED'
P51185   CALL  ISPLINK,(VTBPUT,TABLE,)
         BAL   R10,DISPSUB
         B     P53040
         SPACE
P53190   CLI   CMNERSEL,C' '       WAS ANYTHING SELECTED
         BE    P53195              NO, THEN NEXT OKAY
         CLI   CMNERSEL,0
         BNE   P53152
P53195   MVI   CMNERNXT,X'01'
         CALL  ISPLINK,(VTBEND,TABLE)   REMOVE OLD TABLE
         CALL  ISPLINK,(VTBCRET,TABLE,,LIST2,NOWRITE,)
         B     P53030
P53099   CALL  ISPLINK,(VTBEND,TABLE)
         $EPILOG
         TITLE '***** BUILD CONNECT COMMAND *****'
BLDCON   MVC   CMD(2),=C'CO'       MOVE IN COMMAND
         MVC   CMD+3(8),CMNERUID   MOVE IN USERID
         LA    R3,L'CMNERUID+4(R3) ADVANCE TO FIRST BLANK
         MVC   0(6,R3),=C'GROUP('  MOVE IN KEYWORD
         MVC   6(L'CMNERGRP,R3),CMNERGRP
         TRT   6(L'CMNERGRP+1,R3),BLANKTAB
         MVI   0(R1),C')'
         LA    R3,2(R1)            ADVANCE PAST GROUP
         CLC   CMNERGAU,=CL8' '    CHECK FOR GROUP AUTHORITY
         BE    BLDCON10            NOT ENTERED
         MVC   0(10,R3),=C'AUTHORITY(' KEYWORD
         MVC   10(L'CMNERGAU,R3),CMNERGAU MOVE IN GROUP AUTHORITY
         TRT   10(L'CMNERGAU+1,R3),BLANKTAB CHECK FOR FIRST BLANK
         MVI   0(R1),C')'
         LA    R3,2(R1)
BLDCON10 CLC   CMNERUAC,=CL8' '    CHECK FOR UACC
         BE    BLDCON20            NOT ENTERED
         MVC   0(5,R3),=C'UACC('
         MVC   5(L'CMNERUAC,R3),CMNERUAC
         TRT   5(L'CMNERUAC+1,R3),BLANKTAB
         MVI   0(R1),C')'
         LA    R3,2(R1)
BLDCON20 XR    R4,R4               CLEAR COUNT REG
         MVC   CMNERTUI,CMNERUID   SET UP FOR TABLE ADD
         MVC   CMNERTAC,CMNERGAU
         MVC   CMNERTUA,CMNERUAC
         CLC   CMNERGAC,=C'YES'    CHECK ATTRIBUTES
         BE    BLDCON25
         MVC   0(8,R3),=C'NOGRPACC'
         LA    R3,9(R3)
         B     BLDCON30
BLDCON25 MVC   0(6,R3),=C'GRPACC'
         MVC   CMNERTAT,=CL12'GRPACC'
         LA    R3,7(R3)
         BAL   R5,ADDAROW
BLDCON30 EQU   *
         CLC   CMNERSPC,=C'YES'    CHECK ATTRIBUTES
         BE    BLDCON35
         MVC   0(9,R3),=C'NOSPECIAL'
         LA    R3,10(R3)
         B     BLDCON40
BLDCON35 MVC   0(7,R3),=C'SPECIAL'
         MVC   CMNERTAT,=CL12'SPECIAL'
         LA    R3,8(R3)
         BAL   R5,ADDAROW
BLDCON40 EQU   *
         CLC   CMNERADS,=C'YES'    CHECK ATTRIBUTES
         BE    BLDCON45
         MVC   0(6,R3),=C'NOADSP'
         LA    R3,7(R3)
         B     BLDCON50
BLDCON45 MVC   0(4,R3),=C'ADSP'
         MVC   CMNERTAT,=CL12'ADSP'
         LA    R3,5(R3)
         BAL   R5,ADDAROW
BLDCON50 EQU   *
         CLC   CMNEROPN,=C'YES'    CHECK ATTRIBUTES
         BE    BLDCON55
         MVC   0(12,R3),=C'NOOPERATIONS'
         LA    R3,13(R3)
         B     BLDCON60
BLDCON55 MVC   0(10,R3),=C'OPERATIONS'
         MVC   CMNERTAT,=CL12'OPERATIONS'
         LA    R3,11(R3)
         BAL   R5,ADDAROW
BLDCON60 EQU   *
         CLC   CMNERREV,=C'YES'    CHECK ATTRIBUTES
         BE    BLDCON65
         MVC   0(6,R3),=C'RESUME'
         LA    R3,7(R3)
         B     BLDCON70
BLDCON65 MVC   0(6,R3),=C'REVOKE'
         MVC   CMNERTAT,=CL12'REVOKE'
         LA    R3,7(R3)
         BAL   R5,ADDAROW
BLDCON70 LTR   R4,R4               CHECK IF ANY ROWS ADDED
         BNZ   BLDCON80            YES, THEN CHECK IF IN RIGHT POSITION
         BAL   R10,ADDAROW
BLDCON80 BCTR  R4,R4               REDUCE FOR CURRENT ROW
         LTR   R4,R4               CHECK IF AT ZERO
         BZ    BLDCON99            YES, THEN WE'RE IN POSITION
BLDCON85 CALL  ISPLINK,(VTBSKIP,TABLE,NEG1,)  BACK UP TABLE
         LTR   R15,R15
         BZ    BLDCON86            ONLY ZERO IS ACCEPTABLE HERE
         ABEND 15,DUMP
BLDCON86 BCT   R4,BLDCON85         LOOP UNTIL WE'RE AT THE FIRST ROW
*                                  FOR THE USER BEING ADDED/CHANGED
BLDCON99 BR    R10                 AND RETURN
         TITLE '***** MISC SUBROUTINES *****'
FILLATT  EQU   *                   FILL ATTRIBUTE VARIABLES
         CLC   CMNERTAT,=CL12'GRPACC'
         BNE   FILLATT1
         MVC   CMNERGAC,=CL3'YES'
         B     FILLATT9
FILLATT1 CLC   CMNERTAT,=CL12'SPECIAL'
         BNE   FILLATT2
         MVC   CMNERSPC,=CL3'YES'
         B     FILLATT9
FILLATT2 CLC   CMNERTAT,=CL12'ADSP'
         BNE   FILLATT3
         MVC   CMNERADS,=CL3'YES'
         B     FILLATT9
FILLATT3 CLC   CMNERTAT,=CL12'OPERATIONS'
         BNE   FILLATT4
         MVC   CMNEROPN,=CL3'YES'
         B     FILLATT9
FILLATT4 CLC   CMNERTAT,=CL12'REVOKE'
         BNE   FILLATT9
         MVC   CMNERREV,=CL3'YES'
FILLATT9 BR    R10
         SPACE 5
ADDAROW  EQU   *
         MVC   CMNERTMS,=CL10' '   CLEAR MESSAGE
         CALL  ISPLINK,(VTBADD,TABLE,)
         LA    R4,1(R4)            ADD ONE TO ROW COUNT
         MVI   CMNERSEL,C' '       CLEAR OUT ROW VARIABLES
         MVC   CMNERTUI,=CL8' '
         MVC   CMNERTAC,=CL8' '
         MVC   CMNERTUA,=CL8' '
         BR    R5
         TITLE '***** CHECK AUTHORIZATION ROUTINE *****'
CHKAUTH  L     R4,544              ADDR OF ASCB
         L     R4,108(R4)          ADDR OF ASXB
         L     R4,200(R4)          ADDR OF ACEE
         USING ACEE,R4             SET UP ADDRESSIBILITY
         LA    R15,0               SET DEFAULT RETURN
         TM    ACEEFLG1,ACEESPEC   IS USER SPECIAL
         BOR   R10                 YES, THEN RETURN
         XR    R3,R3               CLEAR R3
         IC    R3,ACEEUSRL         GET LENGTH OF USER
         BCTR  R3,0                REDUCE FOR COMPARE
         EX    R3,CHK1             CHECK IF USERID = OWNERID
         BER   R10                 YES, THEN RETURN
*
*                                  CHECK FOR AT LEAST JOIN OR CONNECT
         TM    CMNERMAU,X'01'
         BOR   R10                 GOOD, THEN RETURN
*
*                                  IF NO GOOD, DISPLAY AUTHORIZATION
*                                  FAILED MSG
         CALL  ISPLINK,(VDISPLAY,CMNER01,MSG03,)
         LA    R15,8
         BR    R10
CHK1     CLC   CMNEROWN(0),ACEEUSRI
         DROP  R4
         TITLE '***** ISSUE TSO COMMAND (ISPCAT) *****'
ISSUE    EQU   *
         L     R2,X'21C'           TCB OLD
         L     R2,X'70'(R2)        ADDR OF 1ST SAVE AREA
         L     R2,X'18'(R2)        R1 IN 1ST SAVE AREA PTS TO PARM
         L     R2,0(R2)            FIRST PARM IS TLD
         CLC   =C'TLD',0(R2)       BE SURE
         BE    ISSUE10
         ABEND 5,DUMP
ISSUE10  ST    R2,ISPCATCL         STORE IN PARM LIST
         LA    R4,ISPCATRC         RELOCATE
         ST    R4,ISPCATCL+8               RETURN CODE
         LA    R4,CMDBUF           RELOCATE
         ST    R4,ISPCATCL+4               COMMAND BUFFER
         LOAD  EP=ISPSUBS
         LR    R3,R0               COPY EP OF ISPSUBS
         LA    R1,=C'AUTH'
         SVC   231                 MAKE AUTHORIZED
         L     R15,X'2C'(R3)       EP OF ISPCAT
         LA    R1,ISPCATCL
         BALR  R14,R15             GO TO IT
         MVC   CMDBUF(2),=H'255'
         MVC   CMDBUF+2(2),=H'3'   REINITIALIZE
         SR    R1,R1               REMOVE AUTHORIZATION
         SVC   231
         BR    R10
         TITLE '***** DISPLAY SUBROUTINE *****'
DISPSUB  EQU   *
*        ON ENTRY
*              R10 -> RETURN
*              R11 -> MESSAGE
*
         MVI   CMNERSEL,C' '      CLEAR OUT INPUT FIELDS
         MVC   CMNEROPT,=CL20' '
         CALL  ISPLINK,(VTBQUERY,TABLE,,,VROWNP,,,) GET NUM OF ROWS
         ICM   R15,15,CMNERROW
         BNZ   DISP10              NO ROWS IN TABLE
         CALL  ISPLINK,(,,(R11)),MF=(E,ISPLNKA)
         B     DISP90              GO CHECK RETURN
DISP10   CALL  ISPLINK,(,,,(R11)),MF=(E,ISPLNKB)
DISP90   CH    R15,=H'8'           END KEY ENTERED
         BE    P53099              YES
         CH    R15,=H'4'           TOO MANY ROWS
         BE    DISP95              YES, DISPLAY ERROR
         CH    R15,=H'0'           OKAY
         BE    DISP99              YES, RETURN
DISP95   MVI   CMNERSEL,C' '       CLEAR OUT FIELDS
         MVC   CMNEROPT,=CL20' '
         CALL  ISPLINK,(VTBDISPL,TABLE,CMNER01,MSG05)
         B     DISP90              GO CHECK RETURN
DISP99   XC    CMNERSEL,=C' '
         BR    R10
         TITLE '***** SETUP ROUTINE *****'
SETUP    EQU   *
*
*  THIS ROUTINE PERFORMS ALL INTIAL DIALOG MANAGER SERVICES
*
         CALL  ISPLINK,(VTBCRET,TABLE,,LIST2,NOWRITE,)
         CALL  ISPLINK,(,,CMNEROPT,,),MF=(E,ISPLNK1)
         CALL  ISPLINK,(,,CMNERGRP,,),MF=(E,ISPLNK2)
         CALL  ISPLINK,(,,CMNERNXT,,),MF=(E,ISPLNK3)
         CALL  ISPLINK,(,,CMNERRC,,),MF=(E,ISPLNK4)
         CALL  ISPLINK,(,,CMNEROWN,,),MF=(E,ISPLNK5)
         CALL  ISPLINK,(,,CMNERSEL,,),MF=(E,ISPLNK6)
         CALL  ISPLINK,(,,CMNERGAU,,),MF=(E,ISPLNK7)
         CALL  ISPLINK,(,,CMNERTMS,,),MF=(E,ISPLNK8)
         CALL  ISPLINK,(,,CMNERROW,,),MF=(E,ISPLNK9)
         CALL  ISPLINK,(,,CMNERMAU,,),MF=(E,ISPLNKC)
         CALL  ISPLINK,(,,CMNERTAT,,),MF=(E,ISPLNKD)
         BR    R10
         TITLE '***** CONSTANTS *****'
*
*  DIALOG MANAGER SERVICES
*
VGET     DC    CL8'VGET'
VPUT     DC    CL8'VPUT'
VDEF     DC    CL8'VDEFINE'
VCOPY    DC    CL8'VCOPY'
VTBDISPL DC    CL8'TBDISPL'
VTBCRET  DC    CL8'TBCREATE'
VTBEND   DC    CL8'TBEND'
VSELECT  DC    CL8'SELECT'
VDISPLAY DC    CL8'DISPLAY'
VTBQUERY DC    CL8'TBQUERY'
VTBTOP   DC    CL8'TBTOP'
VTBPUT   DC    CL8'TBPUT'
VTBGET   DC    CL8'TBGET'
VTBSCAN  DC    CL8'TBSCAN'
VTBADD   DC    CL8'TBADD'
VTBMOD   DC    CL8'TBMOD'
VTBSKIP  DC    CL8'TBSKIP'
VTBDEL   DC    CL8'TBDELETE'
*
*  DIALOG MANGER LENGTHS
*
LNG1     DC    F'1'
LNG2     DC    F'2'
LNG4     DC    F'4'
LNG7     DC    F'7'
LNG8     DC    F'8'
LNG10    DC    F'10'
LNG11    DC    F'11'
LNG12    DC    F'12'
LNG20    DC    F'20'
NEG1     DC    F'-1'
NEG15    DC    F'-15'
*
*  DIALOG MANAGER LISTS
*
VTAT     DC    C'(CMNERTAT)'
VMAU     DC    C'(CMNERMAU)'
VSEL     DC    C'(CMNERSEL)'
VROW     DC    C'(CMNERROW CMNERCRP)'
VOPT     DC    C'(CMNEROPT)'
VRES     DC    C'(CMNERGRP CMNERUID CMNERTUI)'
VNXTSW   DC    C'(CMNERNXT)'
VRETCD   DC    C'(CMNERRC)'
VOWN     DC    C'(CMNEROWN)'
VMSG     DC    C'(CMNERTMS)'
VGAU     DC    C'(CMNERGAU CMNERUAC CMNERTAC CMNERTUA)'
LIST1    DC    C'(CMNERGRP '
         DC    C'CMNERRC '
         DC    C'CMNEROWN '
         DC    C'CMNERMAU)'
LIST2    DC    C'(CMNERSEL '
         DC    C'CMNERTUI '
         DC    C'CMNERTAC '
         DC    C'CMNERTCT '
         DC    C'CMNERTUA '
         DC    C'CMNERTAT '
         DC    C'CMNERTMS)'
*
*  DIALOG MANAGER PANELS, FUNCTIONS AND MESSAGES
*
CMNER01  DC    CL8'CMNER53'
CMNER02  DC    CL8'CMNER54'
CMNER03  DC    CL8'CMNER55'
CMNER04  DC    CL8'CMNER56'
CMNER17  DC    C'PGM(CMNER17)'
MSG01    DC    CL8'CMNER530'       RACF ERROR
MSG02    DC    CL8'CMNER531'
MSG03    DC    CL8'CMNER532'       AUTHORIZATION ERROR
MSG04    DC    CL8'CMNER533'       SELECT USERID
MSG05    DC    CL8'CMNER534'       TOO MANY LINES MODIFIED
MSG06    DC    CL8'CMNER535'       INVALID SELECT CODE
MSG07    DC    CL8'CMNER536'       INVALID ADD
MSG08    DC    CL8'CMNER537'       ADD FAILED
MSG09    DC    CL8'CMNER538'       COMMAND COMPLETE
*
*  DIALOG MANAGER MISCELLANEOUS FIELDS
*
VROWNP   DC    CL8'CMNERROW'
VCRPNP   DC    CL8'CMNERCRP'
FIXED    DC    CL8'FIXED'
CHAR     DC    CL8'CHAR'
SHARED   DC    CL8'SHARED'
NOWRITE  DC    CL8'NOWRITE'
TABLE    DC    CL8'CMNERTGP'
*
*  MISCELLANEOUS FIELDS
*
COMTAB   DC    193X'00'            00-C0
         DC    X'00'               A
         DC    X'00'
         DC    X'04'               C
         DC    X'08'               D
         DC    16X'00'
         DC    X'00'               N
         DC    3X'00'
         DC    X'00'               R
         DC    10X'00'
         DC    X'00'               U
         DC    27X'00'
BLANKTAB DC    64X'00'
         DC    X'01'
         DC    191X'00'
         LTORG
         TITLE '***** DYNAMIC AREAS *****'
DYNWK    EQU   *
*
*  DIALOG MANAGER VARIABLES
*
CMNERSEL DC    C' '
CMNEROPT DC    CL20' '
CMNERGRP DS    CL8
CMNERUID DC    CL8' '
CMNERTUI DC    CL8' '
CMNERGAU DC    CL8' '
CMNERUAC DC    CL8' '
CMNERTAC DC    CL8' '
CMNERTUA DC    CL8' '
CMNERTAT DS    CL12' '
CMNERMAU DS    X
CMNERTMS DC    CL10' '
CMNERNXT DC    X'00'
CMNEROWN DS    CL8
DSNADDR  DS    A
CMNERROW DC    F'0'
CMNERCRP DC    F'0'
CMNERRC  DS    F
CMNERGAC DS    CL3
CMNERADS DS    CL3
CMNERSPC DS    CL3
CMNEROPN DS    CL3
CMNERREV DS    CL3
*
*  CALL LISTS
*
ISPLNK1  CALL  ,(VDEF,VOPT,CMNEROPT,CHAR,LNG20),MF=L
ISPLNK2  CALL  ,(VDEF,VRES,CMNERGRP,CHAR,LNG8),MF=L
ISPLNK3  CALL  ,(VDEF,VNXTSW,CMNERNXT,FIXED,LNG1),MF=L
ISPLNK4  CALL  ,(VDEF,VRETCD,CMNERRC,FIXED,LNG4),MF=L
ISPLNK5  CALL  ,(VDEF,VOWN,CMNEROWN,CHAR,LNG8),MF=L
ISPLNK6  CALL  ,(VDEF,VSEL,CMNERSEL,CHAR,LNG1),MF=L
ISPLNK7  CALL  ,(VDEF,VGAU,CMNERGAU,CHAR,LNG8),MF=L
ISPLNK8  CALL  ,(VDEF,VMSG,CMNERTMS,CHAR,LNG10),MF=L
ISPLNK9  CALL  ,(VDEF,VROW,CMNERROW,FIXED,LNG4),MF=L
ISPLNKA  CALL  ,(VDISPLAY,CMNER01,,),MF=L
ISPLNKB  CALL  ,(VTBDISPL,TABLE,CMNER01,),MF=L
ISPLNKC  CALL  ,(VDEF,VMAU,CMNERMAU,FIXED,LNG1),MF=L
ISPLNKD  CALL  ,(VDEF,VTAT,CMNERTAT,CHAR,LNG12),MF=L
*
*
ISPCATRC DC    A(0)
ISPCATCL DC    3A(0)
CMDBUF   DC    H'255'
         DC    H'3'
CMD      DC    CL255'PE '
DYNWKLN  EQU   *-DYNWK
         IHAACEE
         END
