         TITLE '***** CHANGE USER FUNCTION ******'
CMNER42  $PROLOG
***********************************************************************
*        PROGRAM - CMNER42                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS A REENTRANT ROUTINE WHICH AUTHORIZES ITSELF*
*               BUILDS A ALU  COMMAND AND BRANCHES TO ISPCAT. ISPCAT  *
*               IS A SPF SUBROUTINE WHICH ATTACHES AND EXECUTES TSO   *
*               COMMANDS.                                             *
*                                                                     *
*        PANELS USED - CMNER42                                        *
*                                                                     *
*        ERROR MESSAGES - CMNER420 - CMNER426                         *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         GETMAIN R,LV=DYNWKLN
         LR    R8,R1               COPY ADDR OF GOTTEN STORAGE
         LA    R9,DYNWKLN          LENGTH OF DYNAMIC AREA
         LR    R3,R9               COPY LENGTH FOR MOVE
         LA    R2,DYNWK            BEGINNING ADDR OF MOVE
         MVCL  R8,R2               MOVE INTO GOTTEN AREA
         USING DYNWK,R8
         LR    R8,R1               RESTORE TO BEGININNING
         BAL   R10,SETUP
C42000   BAL   R10,FILL
         MVC   CMNEBUID,CMNERUID   STORE USER ID
         MVC   CMNEBCL1,CMNERCL1   SAVE CLASS AUTHORIZATIONS
         MVC   CMNEBCL2,CMNERCL2
         MVC   CMNEBCL3,CMNERCL3
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG05,)  DISPLAY 1ST PANEL
C42005   CH    R15,=H'8'           END ENTERED? ? ?
         BE    C42999              YES--
         LTR   R15,R15             CHECK RETURN
         BZ    C42010              OKAY
         ABEND 1,DUMP
         SPACE 5
C42010   CLC   CMNEBUID,CMNERUID   MAKE SURE USIERID WASN'T CHANGED
         BNE   C42000
         MVC   CMDBUF(2),=H'355'   SET/RESET LENGTH
         MVC   CMDBUF+2(2),=H'4'   AND OFFSET
         MVI   CMD+4,C' '          CLEAR COMMAND BUFFER
         MVC   CMD+5(250),CMD+4
         MVI   CMD2,C' '           CLEAR SECOND PART OF COMMAND
         MVC   CMD2+1(99),CMD2
         MVC   CMD+4(8),CMNERUID
         LA    R3,CMD+13           ADVANCE PAST RESOURCE
         CLC   CMNERPAS,=CL8' '    WAS PASSWORD ENTERED
         BE    C42020              NO, THEN BYPASS IT
         MVC   0(9,R3),=C'PASSWORD(' MOVE IN KEYWORD
         MVC   9(L'CMNERPAS,R3),CMNERPAS  MOVE IN NEW PASSWORD
         TRT   9(L'CMNERPAS+1,R3),BLANKTAB  SEARCH FOR 1ST BLANK
         BNZ   C42015              FOUND IT
         ABEND 2,DUMP
C42015   MVI   0(R1),C')'          CLOSING PAREN
         LA    R3,2(R1)            ADVANCE
C42020   CLC   CMNERNAM,=CL20' '   IS THERE A NAME
         BNE   C42025              YES,
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG06,)
         B     C42005
C42025   MVC   0(5,R3),=C'NAME('   MOVE IN KEYWORD
         MVC   5(20,R3),CMNERNAM   MOVE IN NAME
         MVI   25(R3),C')'         MOVE IN CLOSING PAREN
         LA    R3,27(R3)           ADVANCE POINTER
         MVC   0(6,R3),=C'OWNER('  MOVE IN KEYWORD
         MVC   6(L'CMNEROWN,R3),CMNEROWN MOVE IN OWNER
         TRT   6(L'CMNEROWN+1,R3),BLANKTAB SEARCH FOR 1ST BLANK
         BNZ   C42030
         ABEND 3,DUMP
C42030   MVI   0(R1),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R1)            ADVANCE
         CLC   CMNERDFG,=CL8' '    WAS DEFAULT GROUP ENTERED
         BE    C42040              NO
         MVC   0(8,R3),=C'DFLTGRP(' MOVE IN KEYWORD
         MVC   8(L'CMNERDFG,R3),CMNERDFG
         TRT   8(L'CMNERDFG+1,R3),BLANKTAB
         BNZ   C42035              OK
         ABEND 2,DUMP
C42035   MVI   0(R1),C')'
         LA    R3,2(R1)            ADVANCE POINTER
C42040   CLC   CMNERGAU,=CL8' '
         BE    C42050              NO GROUP AUTHORITY ENTERED
         MVC   0(10,R3),=C'AUTHORITY(' MOVE IN KEYWORD
         MVC   10(L'CMNERGAU,R3),CMNERGAU
         TRT   10(L'CMNERGAU+1,R3),BLANKTAB SEARCH FOR FIRST BLANK
         BNZ   C42045              FOUND IT
         ABEND 2,DUMP
C42045   MVI   0(R1),C')'
         LA    R3,2(R1)
C42050   CLC   CMNERDAT,=CL30' '   WAS INST DATA ENTERED
         BE    C42060              NO--
         MVC   0(6,R3),=C'DATA(''' YES-- MOVE IN KEYWORD
         MVC   6(L'CMNERDAT,R3),CMNERDAT MOVE IN DATA
         LA    R3,L'CMNERDAT+6(R3) ADVANCE
         MVC   0(2,R3),=C''')'     MOVE IN CLOSING QUOTE AND PAREN
         LA    R3,3(R3)
C42060   EQU   *
         CLC   CMNERGAC,=CL3' '    WAS GRPACC ENTERED
         BE    C42070              NO--
         CLC   CMNERGAC,=C'YES'    YES
         BNE   C42065
         MVC   0(6,R3),=C'GRPACC'  MOVE IN KEYWORD
         LA    R3,7(R3)            ADVANCE POINTER
         B     C42070
C42065   MVC   0(8,R3),=C'NOGRPACC'
         LA    R3,9(R3)
C42070   CLC   CMNERADS,=CL3' '    WAS ADSP ENTERED
         BE    C42080              NO--
         CLC   CMNERADS,=C'YES'    YES
         BNE   C42075              OR NO
         MVC   0(4,R3),=C'ADSP'
         LA    R3,5(R3)
         B     C42080
C42075   MVC   0(6,R3),=C'NOADSP'
         LA    R3,7(R3)
C42080   CLC   CMNERSPC,=CL3' '
         BE    C42090              SPECIAL? ?
         CLC   CMNERSPC,=C'YES'    YES
         BNE   C42085              OR NO
         MVC   0(7,R3),=C'SPECIAL'
         LA    R3,8(R3)
         B     C42090
C42085   MVC   0(9,R3),=C'NOSPECIAL'
         LA    R3,10(R3)
C42090   CLC   CMNEROPN,=CL3' '    OPERATIONS
         BE    C42100
         CLC   CMNEROPN,=C'YES'    YES
         BNE   C42095              OR NO
         MVC   0(10,R3),=C'OPERATIONS'
         LA    R3,11(R3)
         B     C42100
C42095   MVC   0(12,R3),=C'NOOPERATIONS'
         LA    R3,13(R3)
C42100   CLC   CMNERAUD,=CL3' '    AUDITOR
         BE    C42110
         CLC   CMNERAUD,=C'YES'    YES
         BNE   C42105              OR NO
         MVC   0(7,R3),=C'AUDITOR'
         LA    R3,8(R3)
         B     C42110
C42105   MVC   0(9,R3),=C'NOAUDITOR'
         LA    R3,10(R3)
C42110   CLC   CMNERREV,=CL3' '    REVOKE/RESUME
         BE    C42120
         CLC   CMNERREV,=C'YES'
         BNE   C42115
         MVC   0(6,R3),=C'REVOKE'
         LA    R3,7(R3)
         B     C42120
C42115   MVC   0(6,R3),=C'RESUME'
         LA    R3,7(R3)
C42120   EQU   *
         MVC   NCLS,=CL26' '       BLANK OUT NCLAUTH AREA
         MVC   CLS,=CL26' '        BLANK OUT CLAUTH AREA
         LA    R6,NCLS             NCLS POINTERS
         LA    R15,0               NCLS LENGTH COUNT
         LA    R5,CLS              CLS POINTER
         LA    R14,0               CLS LENGTH COUNT
         CLC   CMNERCL1,CMNEBCL1   DID CLASS 1 CHANGE ? ?
         BE    C42130              NO, THEN CHECK CLASS 2
         CLC   CMNEBCL1,=CL8' '    WAS PREVIOUS CLASS 1, BLANK ? ? ?
         BE    C42125              YES, THEN CHECK CURRENT
         MVC   NCLS(8),CMNEBCL1    NO, THEN SET FOR NCLAUTH
         LA    R6,9(R6)            ADVANCE POINTER
         LA    R15,9(R15)          ADVANCE COUNT
C42125   CLC   CMNERCL1,=CL8' '    IS CURRENT CLASS 1, BLANK ? ? ?
         BE    C42130              YES, THEN CHECK CLASS 2
         MVC   CLS(8),CMNERCL1     NO, THEN SET FOR CLAUTH
         LA    R5,9(R5)            ADVANCE POINTER
         LA    R14,9(R14)          ADVANCE COUNT
C42130   EQU   *
         CLC   CMNEBCL2,CMNERCL2   DID CLASS 2 CHANGE
         BE    C42140              NO, THEN CHECK CLASS 3
         CLC   CMNEBCL2,=CL8' '    WAS PREVIOUS CLASS 2, BLANK ? ? ?
         BE    C42135              YES, THEN CHECK CURRENT
         MVC   0(8,R6),CMNEBCL2    NO, THEN SET FOR NCLAUTH
         LA    R6,9(R6)            ADVANCE POINTER
         LA    R15,9(R15)          ADVANCE COUNT
C42135   CLC   CMNERCL2,=CL8' '    IS CURRENT CLASS 2, BLANK ? ? ?
         BE    C42140              YES, THEN CHECK CLASS 3
         MVC   0(8,R5),CMNERCL2     NO, THEN SET FOR CLAUTH
         LA    R5,9(R5)            ADVANCE POINTER
         LA    R14,9(R14)          ADVANCE COUNT
C42140   EQU   *
         CLC   CMNEBCL3,CMNERCL3   DID CLASS 3 CHANGE
         BE    C42150              NO, THEN BUILD PARAMETER
         CLC   CMNEBCL3,=CL8' '    WAS PREVIOUS CLASS 3, BLANK ? ? ?
         BE    C42145              YES, THEN CHECK CURRENT
         MVC   0(8,R6),CMNEBCL3    NO, THEN SET FOR NCLAUTH
         LA    R15,8(R15)          ADVANCE COUNT
C42145   CLC   CMNERCL3,=CL8' '    IS CURRENT CLASS 3, BLANK ? ? ?
         BE    C42150              YES, THEN BUILD REST OF COMMAND
         MVC   0(8,R5),CMNERCL3     NO, THEN SET FOR CLAUTH
         LA    R14,8(R14)          ADVANCE COUNT
C42150   EQU   *
         LTR   R14,R14             ANY CLAUTH
         BZ    C42160              NO-- THEN TRY NOCLAUTH
         MVC   0(7,R3),=C'CLAUTH(' MOVE IN KEYWORD
         MVC   7(L'CLS,R3),CLS     MOVE IN CLASSES
         LA    R3,7(R3,R14)        ADVANCE POINTER
         MVI   0(R3),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R3)            ADVANCE TO NEXT AVAILABLE
C42160   EQU   *
         LTR   R15,R15             ANY NOCLAUTH
         BZ    C42170              NO-- PREPARE TO ISSUE COMMAND
         MVC   0(9,R3),=C'NOCLAUTH(' MOVE IN KEYWORD
         MVC   9(L'NCLS,R3),NCLS   MOVE IN CLASSES
         LA    R3,9(R3,R15)        ADVANCE POINTER
         MVI   0(R3),C')'          MOVE IN CLOSING PAREN
C42170   MVC   CMNEBCL1,CMNERCL1   MOVE IN CLASSES FOR NEXT TIME
         MVC   CMNEBCL2,CMNERCL2
         MVC   CMNEBCL3,CMNERCL3
C42200   L     R2,X'21C'           TCB OLD
         L     R2,X'70'(R2)        ADDR OF 1ST SAVE AREA
         L     R2,X'18'(R2)        R1 IN 1ST SAVE AREA PTS TO PARM
         L     R2,0(R2)            FIRST PARM IS TLD
         CLC   =C'TLD',0(R2)       BE SURE
         BE    C42210
         ABEND 2,DUMP
C42210   ST    R2,ISPCATCL         STORE IN PARM LIST
         LA    R4,ISPCATRC         RELOCATE
         ST    R4,ISPCATCL+8               RETURN CODE
         LA    R4,CMDBUF           RELOCATE
         ST    R4,ISPCATCL+4               COMMAND BUFFER
         LOAD  EP=ISPSUBS
         LR    R3,R0               COPY EP OF ISPSUBS
         LA    R1,=C'AUTH'
         SVC   231                 MAKE AUTHORIZED
         L     R15,X'2C'(R3)       EP OF ISPCAT
         LA    R1,ISPCATCL
         BALR  R14,R15             GO TO IT
         L     R4,ISPCATRC         GET RETURN
         SR    R1,R1               REMOVE AUTHORIZATION
         SVC   231
         LTR   R4,R4               CHECK IT
         BZ    C42220
         ST    R4,CMNERRC
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG02,)
         B     C42230
C42220   CALL  ISPLINK,(VDISPLAY,PANEL,MSG01,)
C42230   CH    R15,=H'8'           CHECK FOR END KEY
         BE    C42999              YES--
         LTR   R15,R15             CHECK FOR ZERO
         BZ    C42240              YES--
         ABEND 1,DUMP
C42240   CLC   CMNERUID,=CL8' '
         BE    C42250              ISSUE ERROR MESSAGE
         CLC   CMNEBUID,CMNERUID   DID USERID CHANGE ?
         BNE   C42000              YES, THEN GET INFO FOR NEW ONE
         B     C42010              OTHERISE MORE MODS TO THIS ONE
C42250   CALL  ISPLINK,(VDISPLAY,PANEL,MSG03,)
         B     C42230
C42999   FREEMAIN R,LV=DYNWKLN,A=(8)
         $EPILOG
         TITLE '***** SETUP ROUTINE *****'
SETUP    CALL  ISPLINK,(,LIST4,CMNERUID),MF=(E,ISPLNK1)
         CALL  ISPLINK,(,LIST2,CMNERRC),MF=(E,ISPLNK3)
         CALL  ISPLINK,(,LIST3,CMNERDAT),MF=(E,ISPLNK4)
         CALL  ISPLINK,(,LIST5,CMNERNAM),MF=(E,ISPLNK5)
         CALL  ISPLINK,(,LIST6,CMNERGAC),MF=(E,ISPLNK6)
         CALL  ISPLINK,(,LIST1),MF=(E,ISPLNK2)
         BR    R10
         TITLE '***** FILL VARIABLES WITH RACF DATA *****'
FILL     EQU   *
         MVC   CMNERCL1,=CL8' '
         MVC   CMNERCL2,=CL8' '    CLEAR OUT CLASSES
         MVC   CMNERCL3,=CL8' '
         MVI   UIDLN,L'CMNERUID    SET MAX LENGTH
         TRT   CMNERUID,BLANKTAB   SEARCH FOR BLANK
         BZ    FILL10              NOT FOUND THEN MUST BE MAX LENGTH
         LA    R15,CMNERUID
         SR    R1,R15              COMPUTE LENGTH
         STC   R1,UIDLN            STORE IT
FILL10   LA    R1,=C'AUTH'         MAKE AUTHORIZED
         SVC   231
         ICHEINTY LOCATE,ENTRY=UIDLN,WKAREA=WK1,                       X
               MF=(E,TY1)
         LR    R14,R15             STORE RETURN
         SR    R1,R1
         SVC   231                 UNAUTHORIZE
         LTR   R14,R14
         BZ    FILL30              OKAY
         CH    R14,=H'12'          CHECK FOR NOT FOUND
         BE    FILL20              ISSUE MESSAGE
         ABEND 5,DUMP              OTHERISE SAY BYE
FILL20   CALL  ISPLINK,(VDISPLAY,PANEL,MSG04,,)
         CH    R15,=H'8'           CHECK RETURN
         BE    C42999
         LTR   R15,R15
         BZ    FILL                TRY AGAIN
         ABEND 1,DUMP
FILL30   EQU   *
         L     R15,544             ASCB
         L     R15,108(R15)        ADDR OF ASXB
         L     R15,200(R15)        ACEE
         USING ACEE,R15
         TM    ACEEFLG1,ACEESPEC   AM I SPECIAL ? ?
         BO    FILL31              YES, THEN I CAN DO ANYTHING
         XR    R14,R14             CLEAR REG
         IC    R14,ACEEUSRL        GET LENGTH OF USER
         BCTR  R14,0               REDUCE FOR COMPARE
         EX    R14,MYCHK           IS IT MY ID ? ?
         BE    FILL31              YES, THEN I CAN AT LEAST LIST IT
         EX    R14,OWNCHK          AM I THE OWNERE? ? ?
         BE    FILL31              YES, THEN I CAN LIST AND MODIFY
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG07,)
         B     C42999              ON A SECURITY VIOLATION, SAY BYE
MYCHK    CLC   CMNERUID(0),ACEEUSRI
OWNCHK   CLC   WOWN(0),ACEEUSRI
         DROP  R15
FILL31   MVC   CMNEROWN,WOWN       MOVE IN OWNER
         MVC   CMNERPAS,=CL8' '    CLEAR PASSWORD
         MVC   CMNERNAM,WPGMR      MOVE IN PROGRAMMER NAME
         MVC   CMNERDFG,WDFLTGRP   MOVE IN DEFAULT GROUP
         MVC   CMNERGAC,=C'NO '    GRPACC
         TM    WFLAG5,X'80'
         BZ    FILL32
         MVC   CMNERGAC,=C'YES'
FILL32   MVC   CMNERADS,=C'NO '    ADSP
         TM    WFLAG1,X'80'
         BZ    FILL34
         MVC   CMNERADS,=C'YES'
FILL34   MVC   CMNERSPC,=C'NO '    SPECIAL
         TM    WFLAG2,X'80'
         BZ    FILL36
         MVC   CMNERSPC,=C'YES'
FILL36   MVC   CMNEROPN,=C'NO '    OPERATIONS
         TM    WFLAG3,X'80'
         BZ    FILL38
         MVC   CMNEROPN,=C'YES'
FILL38   MVC   CMNERAUD,=C'NO '    AUDITOR
         TM    WFLAG6,X'80'
         BZ    FILL40
         MVC   CMNERAUD,=C'YES'
FILL40   MVC   CMNERREV,=C'NO '    REVOKED
         TM    WFLAG4,X'80'
         BZ    FILL42
         MVC   CMNERREV,=C'YES'
FILL42   LA    R15,WCLCT           GET CLASS COUNT
         LH    R15,0(R15)
         LTR   R15,R15             ARE THERE ANY ? ?
         BZ    FILL50              NO--
         MVC   CMNERCL1,WCLS       MOVE IN CLASS 1
         BCT   R15,*+8             CONTINUE IF MORE
         B     FILL50
         MVC   CMNERCL2,WCLS+8     MOVE IN CLASS 2
         BCT   R15,*+8             CONTINUE IF MORE
         B     FILL50
         MVC   CMNERCL3,WCLS+16    MOVE IN CLASS 3
FILL50   LA    R14,WCLSLN          GET LENGTH OF CLASSES
         LH    R14,0(R14)
         LA    R14,WCLS(R14)       ADVANCE TO INSTALATION DATA
         XR    R15,R15             CLEAR R15
         IC    R15,2(R14)          GET LENGTH OF USER DATA
         CH    R15,=H'30'          CHECK FOR LESS THAN MAX
         BL    FILL60              YES
         LA    R15,30              SET MAX
FILL60   MVC   CMNERDAT,=CL30' ' CLEAR INST DATA FIELD
         LTR   R15,R15             IS LENGTH ZERO
         BZ    FILL70              THEN, NO INST DATA
         BCTR  R15,0               REDUCE FOR MOVE
         EX    R15,MOVDAT1         MOVE IT
         CNOP  0,4
FILL70   ICHEACTN TESTS=(TST1),MF=(E,ACT13)   RELOCATE RACF MACROS
         ICHETEST FLDATA=(8,CMNERUID),MF=(E,TST1)
         MVC   DFLTGRP,CMNERDFG
         MVI   DFLTLEN,L'DFLTGRP   MOVE IN MAX LEN
         TRT   DFLTGRP,BLANKTAB    FIND FIRST BLANK
         BZ    FILL72              NOT FOUND, THEN MUST BE MAX
         LA    R15,DFLTGRP         GET BEGININNING
         SR    R1,R15              COMPUTE LENGTH
         STC   R1,DFLTLEN          STORE IT
FILL72   LA    R1,=C'AUTH'         MAKE AUTHORIZED
         SVC   231
         ICHEINTY LOCATE,ENTRY=DFLTLEN,WKAREA=WK2,ACTIONS=(ACT13),     X
               OPTIONS=(FLDEF),MF=(E,TY2)
         LR    R14,R15             STORE RETURN
         SR    R1,R1               UNAUTHORIZE
         SVC   231
         LTR   R14,R14
         BZ    FILL80
         ABEND 6,DUMP
FILL80   TM    W2ACS,X'80'         FILL IN GROUP AUTHORITY
         BNO   FILL82
         MVC   CMNERGAU,=CL8'JOIN'
         B     FILL99
FILL82   TM    W2ACS,X'40'
         BNO   FILL84
         MVC   CMNERGAU,=CL8'CONNECT'
         B     FILL99
FILL84   TM    W2ACS,X'20'
         BNO   FILL86
         MVC   CMNERGAU,=CL8'CREATE'
         B     FILL99
FILL86   MVC   CMNERGAU,=CL8'USE'
FILL99   BR    R10                 RETURN
         TITLE '***** CONTANTS *****'
*
*        DIALOG MANAGER SERVICES
*
VGET     DC    CL8'VGET'
VDEF     DC    CL8'VDEFINE'
VDISPLAY DC    CL8'DISPLAY'
*
*        DIALOG MANAGER LISTS
*
LIST1    DC    C'(CMNERUID)'
LIST2    DC    C'(CMNERRC)'
LIST3    DC    C'(CMNERDAT)'
LIST4    DC    C'(CMNERUID '
         DC    C'CMNERDFG '
         DC    C'CMNERGAU '
         DC    C'CMNEROWN '
         DC    C'CMNERCL1 '
         DC    C'CMNERCL2 '
         DC    C'CMNERCL3 '
         DC    C'CMNERPAS)'
LIST5    DC    C'(CMNERNAM)'
LIST6    DC    C'(CMNERGAC '
         DC    C'CMNERADS '
         DC    C'CMNERSPC '
         DC    C'CMNEROPN '
         DC    C'CMNERAUD '
         DC    C'CMNERREV)'
*
*        DIALOG MANAGER LENGTHS
*
LNG3     DC    F'3'
LNG4     DC    F'4'
LNG8     DC    F'8'
LNG20    DC    F'20'
LNG30    DC    F'30'
*
*        MISCELANEOUS VARIABLSE
*
FIXED    DC    CL8'FIXED'
CHAR     DC    CL8'CHAR'
SHARED   DC    CL8'SHARED'
PANEL    DC    CL8'CMNER42'
MSG01    DC    CL8'CMNER420'       COMMAND SUCCESSFUL
MSG02    DC    CL8'CMNER421'       COMMAND FAILED
MSG03    DC    CL8'CMNER422'       ENTER USERID
MSG04    DC    CL8'CMNER423'       NOT FOUND
MSG05    DC    CL8'CMNER424'       ENTER CHANGES
MSG06    DC    CL8'CMNER425'       NAME REQUIRED
MSG07    DC    CL8'CMNER426'       NOT AUTHORIZED
MOVDAT1  MVC   CMNERDAT(0),3(R14)
BLANKTAB DC    64X'00'
         DC    X'01'
         DC    191X'00'
*
*
*
ACT1     ICHEACTN FIELD=AUTHOR,MF=L
ACT2     ICHEACTN FIELD=PGMRNAME,MF=L
ACT3     ICHEACTN FIELD=DFLTGRP,MF=L
ACT4     ICHEACTN FIELD=FLAG1,MF=L
ACT5     ICHEACTN FIELD=FLAG2,MF=L
ACT6     ICHEACTN FIELD=FLAG3,MF=L
ACT7     ICHEACTN FIELD=FLAG4,MF=L
ACT8     ICHEACTN FIELD=FLAG5,MF=L
ACT9     ICHEACTN FIELD=FLAG6,MF=L
ACT10    ICHEACTN FIELD=CLCNT,MF=L
ACT11    ICHEACTN FIELD=CLNAME,MF=L
ACT12    ICHEACTN FIELD=INSTDATA,MF=L
         LTORG
         TITLE '***** DYNAMIC AREAS *****'
         DS    0D
DYNWK    EQU   *
ISPCATRC DS    A(0)
ISPCATCL DS    3A(0)
NCLS     DS    CL26
CLS      DS    CL26
*
*        DIALOG MANAGER VARIABLES
*
UIDLN    DS    X
CMNERUID DS    CL8' '
CMNERDFG DS    CL8' '
CMNERGAU DS    CL8' '
CMNEROWN DS    CL8' '
CMNERCL1 DS    CL8
CMNERCL2 DS    CL8
CMNERCL3 DS    CL8
CMNERPAS DS    CL8
CMNERRC  DS    F'0'
CMNERDAT DS    CL30' '
CMNERNAM DS    CL20' '
CMNERGAC DS    CL3
CMNERADS DS    CL3
CMNERSPC DS    CL3
CMNEROPN DS    CL3
CMNERAUD DS    CL3
CMNERREV DS    CL3
CMNEBUID DC    CL8' '
CMNEBCL1 DC    CL8' '
CMNEBCL2 DC    CL8' '
CMNEBCL3 DC    CL8' '
DFLTLEN  DC    X'00'
DFLTGRP  DC    CL8' '
*
*   LISTS
*
ISPLNK1  CALL   ,(VDEF,LIST1,CMNERUID,CHAR,LNG8),MF=L
ISPLNK2  CALL  ,(VGET,LIST1,SHARED),MF=L
ISPLNK3  CALL   ,(VDEF,LIST1,CMNERRC,FIXED,LNG4),MF=L
ISPLNK4  CALL   ,(VDEF,LIST3,CMNERDAT,CHAR,LNG30),MF=L
ISPLNK5  CALL   ,(VDEF,LIST5,CMNERNAM,CHAR,LNG20),MF=L
ISPLNK6  CALL   ,(VDEF,LIST6,CMNERGAC,CHAR,LNG3),MF=L
CMDBUF   DC    H'355'
CMDOFF   DC    H'4'
CMD      DC    CL255'ALU '
CMD2     DC    CL100' '
*
*
*
TY1      ICHEINTY LOCATE,TYPE='USR',MF=L,ACTIONS=(ACT1,ACT2,ACT3,ACT4, X
               ACT5,ACT6,ACT7,ACT8,ACT9,ACT10,ACT11,ACT12)
TY2      ICHEINTY LOCATE,TYPE='GRP',MF=L,ACTIONS=(ACT13),WKAREA=WK2
TST1     ICHETEST MF=L,FIELD=USERID,COND=EQ
ACT13    ICHEACTN MF=L,FIELD=ACL,TESTS=(TST1)
WK1      DC    A(WK1LN)
         DS    6F
         DS    H
WOWN     DS    CL8
         DS    XL2
WPGMR    DS    XL20
         DS    XL2
WDFLTGRP DS    XL8
         DS    XL2
WFLAG1   DS    X
         DS    XL2
WFLAG2   DS    X
         DS    XL2
WFLAG3   DS    X
         DS    XL2
WFLAG4   DS    X
         DS    XL2
WFLAG5   DS    X
         DS    XL2
WFLAG6   DS    X
         DS    XL2
WCLCT    DS    XL2
WCLSLN   DS    XL2
WCLS     DS    20XL8
         DS    XL2
WINST    DS    XL255
         DS    XL2
         DS    XL255
         DS    0F
WK1LN    EQU   *-WK1
WK2      DC    A(WK2LN)
         DS    6F
         DS    H
         DS    CL8
W2ACS    DS    X
         DS    XL2
         DS    0D
WK2LN    EQU   *-WK2
DYNWKLN  EQU   *-DYNWK
         IHAACEE
         END
