CMNER44  $PROLOG
***********************************************************************
*        PROGRAM - CMNER44                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS A REENTRANT ROUTINE WHICH AUTHORIZES ITSELF*
*               BUILDS A ALD  COMMAND AND BRANCHES TO ISPCAT. ISPCAT  *
*               IS A SPF SUBROUTINE WHICH ATTACHES AND EXECUTES TSO   *
*               COMMANDS.                                             *
*                                                                     *
*        PANELS USED - CMNER44                                        *
*                                                                     *
*        ERROR MESSAGES - CMNER440 - CMNER446                         *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         GETMAIN R,LV=DYNWKLN
         LR    R8,R1               COPY ADDR OF GOTTEN STORAGE
         LA    R9,DYNWKLN          LENGTH OF DYNAMIC AREA
         LR    R3,R9               COPY LENGTH FOR MOVE
         LA    R2,DYNWK            BEGINNING ADDR OF MOVE
         MVCL  R8,R2               MOVE INTO GOTTEN AREA
         USING DYNWK,R8
         LR    R8,R1               RESTORE TO BEGININNING
         BAL   R10,SETUP
C44000   CLC   CMNERVOL,=CL6' '    WAS VOLUME PROVIDED
         BNE   C44002              YES, GO AHEAD WITH RACF STUFF
         LA    R1,CMNERDSN         NO, DO LOCATE
         ST    R1,CAMLIST+4        RELOCATE PARAMETERS
         LA    R1,CATWRK
         ST    R1,CAMLIST+12
         LOCATE CAMLIST
         LTR   R15,R15             WAS IT GOOD
         BZ    C44001              YES
         ST    R15,CMNERRC         STORE RETURN
         MVC   CURSOR,=C'CMNERVOL' POISITION CURSOR
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG06,)
         CH    R15,=H'8'           CHECK FOR END KEY
         BE    C44999              YES, THEN GET OUT
         CH    R15,=H'0'           OK, THEN MAKE SURE VOLUME IS THERE
         BE    C44000
         ABEND 5,DUMP              ANYTHING ELSE, SAY BYE
C44002   BAL   R10,FILL
         MVC   CMNEBDSN,CMNERDSN   STORE DATASET
         MVC   CMNEBVOL,CMNERVOL   STORE VOLUME
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG05,)  DISPLAY 1ST PANEL
C44005   CH    R15,=H'8'           END ENTERED? ? ?
         BE    C44999              YES--
         LTR   R15,R15             CHECK RETURN
         BZ    C44010              OKAY
         ABEND 1,DUMP
         SPACE 5
C44010   CLC   CMNEBDSN,CMNERDSN   MAKE SURE DATASET  AND
         BNE   C44000
         CLC   CMNERVOL,CMNEBVOL   VOLUME HAVE NOT CHANGED
         BNE   C44000
         MVI   CMD+4,C' '          CLEAR COMMAND BUFFER
         MVC   CMD+5(250),CMD+4
         MVI   CMD+4,C''''         BEGINING QUOTE
         MVC   CMD+5(L'CMNERDSN),CMNERDSN MOVE IN DATASET NAME
         TRT   CMD+5(L'CMNERDSN+1),BLANKTAB FIND FIRST BLANK
         MVI   0(R1),C''''         MOVE IN CLOSING BLANK
         LA    R3,2(R1)            POISITION R3
         MVC   0(6,R3),=C'OWNER('  MOVE IN KEYWORD
         MVC   6(L'CMNEROWN,R3),CMNEROWN MOVE IN OWNER
         TRT   6(L'CMNEROWN+1,R3),BLANKTAB SEARCH FOR 1ST BLANK
         BNZ   C44011
         ABEND 3,DUMP
C44011   MVI   0(R1),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R1)            ADVANCE
         MVC   0(5,R3),=C'UACC('   MOVE IN KEYWORD
         MVC   5(L'CMNERUAC,R3),CMNERUAC
         TRT   5(L'CMNERUAC+1,R3),BLANKTAB
         BNZ   C44012
         ABEND 3,DUMP
C44012   MVI   0(R1),C')'
         LA    R3,2(R1)            ADVANCE
         MVC   0(6,R3),=C'AUDIT('  MOVE IN KEYWORD
         MVC   6(L'CMNERAUD,R3),CMNERAUD MOVE IN AUDIT
         TRT   6(L'CMNERAUD+1,R3),BLANKTAB SEARCH FOR 1ST BLANK
         BNZ   C44013
         ABEND 3,DUMP
C44013   MVI   0(R1),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R1)            ADVANCE
         MVC   0(7,R3),=C'VOLUME(' MOVE IN KEYWORD
         MVC   7(L'CMNERVOL,R3),CMNERVOL MOVE IN VOLUME
         TRT   7(L'CMNERVOL+1,R3),BLANKTAB SEARCH FOR FIRST BLANK
         BNZ   C44014              FOUND IT
         ABEND 3,DUMP
C44014   MVI   0(R1),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R1)            ADVANCE R3
         CLC   CMNERUNI,=CL8' '    IS UNIT BLANK
         BE    C44016              YES, GO CHECK SET OPTIONS
         MVC   0(5,R3),=C'UNIT('   MOVE IN KEYWORD
         MVC   5(L'CMNERUNI,R3),CMNERUNI MOVE IN UNIT
         TRT   5(L'CMNERUNI+1,R3),BLANKTAB SEARCH FOR BLANK
         BNZ   C44015              FOUND IT
         ABEND 3,DUMP
C44015   MVI   0(R1),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R1)            ADVANCE R3
C44016   CLC   CMNERSET,=CL3'YES'  CHECK SET OPTION
         BE    C44020              DEFAULT DESIRED
         MVC   0(5,R3),=C'NOSET'
*
*
C44020   L     R2,X'21C'           TCB OLD
         L     R2,X'70'(R2)        ADDR OF 1ST SAVE AREA
         L     R2,X'18'(R2)        R1 IN 1ST SAVE AREA PTS TO PARM
         L     R2,0(R2)            FIRST PARM IS TLD
         CLC   =C'TLD',0(R2)       BE SURE
         BE    C44030
         ABEND 2,DUMP
C44030   ST    R2,ISPCATCL         STORE IN PARM LIST
         LA    R4,ISPCATRC         RELOCATE
         ST    R4,ISPCATCL+8               RETURN CODE
         LA    R4,CMDBUF           RELOCATE
         ST    R4,ISPCATCL+4               COMMAND BUFFER
         LOAD  EP=ISPSUBS
         LR    R3,R0               COPY EP OF ISPSUBS
         LA    R1,=C'AUTH'
         SVC   231                 MAKE AUTHORIZED
         L     R15,X'2C'(R3)       EP OF ISPCAT
         LA    R1,ISPCATCL
         BALR  R14,R15             GO TO IT
         L     R4,ISPCATRC         GET RETURN
         SR    R1,R1               REMOVE AUTHORIZATION
         SVC   231
         MVC   CMDBUF(2),=H'255'   SET UP FOR NEXT TIME
         MVC   CMDBUF+2(2),=H'4'
         LTR   R4,R4               CHECK IT
         BZ    C44035
         ST    R4,CMNERRC
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG02,)
         B     C44040
C44035   CALL  ISPLINK,(VDISPLAY,PANEL,MSG01,)
C44040   CH    R15,=H'8'           CHECK FOR END KEY
         BE    C44999              YES--
         LTR   R15,R15             CHECK FOR ZERO
         BZ    C44050              YES--
         ABEND 1,DUMP
C44050   CLC   CMNERDSN,=CL44' '
         BE    C44060              ISSUE ERROR MESSAGE
         CLC   CMNEBDSN,CMNERDSN   DID DATASET  CHANGE ?
         BNE   C44000              YES, THEN GET INFO FOR NEW ONE
         CLC   CMNEBVOL,CMNERVOL   DID VOLUME CHANGE ?
         BNE   C44000              YES, THEN GET INFO FOR A NEW ONE
         B     C44010              OTHERISE MORE MODS TO THIS ONE
C44060   CALL  ISPLINK,(VDISPLAY,PANEL,MSG03,)
         B     C44040
C44999   FREEMAIN R,LV=DYNWKLN,A=(8)
         $EPILOG
         TITLE '***** SETUP ROUTINE *****'
SETUP    CALL  ISPLINK,(,LIST1,CMNERDSN),MF=(E,ISPLNK1)
         CALL  ISPLINK,(,LIST2,CMNEROWN),MF=(E,ISPLNK2)
         CALL  ISPLINK,(,LIST3,CMNERRC),MF=(E,ISPLNK3)
         CALL  ISPLINK,(,LIST4,CMNERVOL),MF=(E,ISPLNK4)
         CALL  ISPLINK,(,LIST5,CMNERSET),MF=(E,ISPLNK5)
         CALL  ISPLINK,(,LIST6),MF=(E,ISPLNK6)
         BR    R10
         TITLE '***** FILL VARIABLES WITH RACF DATA *****'
FILL     EQU   *
         MVI   RESLN,L'CMNERDSN    SET MAX LENGTH
         TRT   CMNERDSN,BLANKTAB   SEARCH FOR BLANK
         BZ    FILL10              NOT FOUND THEN MUST BE MAX LENGTH
         LA    R15,CMNERDSN
         SR    R1,R15              COMPUTE LENGTH
         STC   R1,RESLN            STORE IT
FILL10   LA    R1,=C'AUTH'         MAKE AUTHORIZED
         SVC   231
         ICHEINTY LOCATE,ENTRY=DSNLN,WKAREA=WK1,VOLUME=CMNERVOL,       X
               MF=(E,TY1)
         LR    R14,R15             STORE RETURN
         SR    R1,R1
         SVC   231                 UNAUTHORIZE
         LTR   R14,R14
         BZ    FILL30              OKAY
         CH    R14,=H'12'          CHECK FOR NOT FOUND
         BE    FILL20              ISSUE MESSAGE
         ABEND 5,DUMP              OTHERISE SAY BYE
FILL20   CALL  ISPLINK,(VDISPLAY,PANEL,MSG04,,)
         CH    R15,=H'8'           CHECK RETURN
         BE    C44999
         LTR   R15,R15
         BZ    FILL                TRY AGAIN
         ABEND 1,DUMP
FILL30   L     R15,544             ADD OR ASCB
         L     R15,108(R15)        ADDR OF ASXB
         L     R15,200(R15)        ADDR OF ACEE
         USING ACEE,R15
         TM    ACEEFLG1,ACEESPEC   AM I SPECIAL ? ? ?
         BO    FILL30X             YES, THEN OKAY
         XR    R14,R14             CLEAR REG
         IC    R14,ACEEUSRL        GET LENGTH OF USER
         BCTR  R14,0               REDUCE FOR MOVE
         EX    R14,CHK1            IS USERID = OWNERID ? ?
         BE    FILL30X             YES, THEN CONTINUE
         RACHECK MF=(E,RCHK1),ENTITY=CMNERDSN,VOLUME=CMNERVOL
         LTR   R15,R15             CHECK RETURN
         BZ    FILL30X
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG07,)
         B     C44999              AFTER A VIOLATION, GET OUT
FILL30X  MVC   CMNEROWN,WOWN       MOVE IN OWNER
         TM    WUACC,X'80'
         BNO   FILL32              FILL UACC FIELD
         MVC   CMNERUAC,=CL8'ALTER'
         B     FILL40
FILL32   TM    WUACC,X'40'
         BNO   FILL33              FILL UACC FIELD
         MVC   CMNERUAC,=CL8'CONTROL'
         B     FILL40
FILL33   TM    WUACC,X'20'
         BNO   FILL34              FILL UACC FIELD
         MVC   CMNERUAC,=CL8'UPDATE'
         B     FILL40
FILL34   TM    WUACC,X'10'
         BNO   FILL35              FILL UACC FIELD
         MVC   CMNERUAC,=CL8'READ'
         B     FILL40
FILL35   MVC   CMNERUAC,=CL8'NONE'
FILL40   TM    WAUDIT,X'80'        FILL IN AUDIT FIELD
         BNO   FILL42
         MVC   CMNERAUD,=CL8'ALL'
         B     FILL50
FILL42   TM    WAUDIT,X'40'        FILL IN AUDIT FIELD
         BNO   FILL44
         MVC   CMNERAUD,=CL8'SUCCESS '
         B     FILL50
FILL44   TM    WAUDIT,X'20'        FILL IN AUDIT FIELD
         BNO   FILL46
         MVC   CMNERAUD,=CL8'FAILURES'
         B     FILL50
FILL46   MVC   CMNERAUD,=CL8'NONE'
FILL50   MVC   CMNERUNI,=CL8' '    CLEAR OUT UNIT
         CLC   WUNTYX,=8X'FF'      ANYTHING IN CHARACTER FIELD
         BE    FILL60              NO- THEN WILL HAVE TO TRANSALATE
         MVC   CMNERUNI,WUNTYX     YES, THEN MOVE IT IN AND GET OUT
         B     FILL99
FILL60   CLC   WUNTY,=4X'FF'       ANYTHIN IN UCBTYP FIELD
         BE    FILL99              NO- THEN GET OUT
         CLI   WUNTY+2,X'20'       IS THIS A DISK ? ? ?
         BE    FILL70              YES, THEN GO PROCESS TYPE
         CLI   WUNTY+2,X'80'       IS THIS A TAPE ? ? ?
         BNE   FILL99              NO, THEN GIVE UP
         CLI   WUNTY+3,X'01'       IS THIS A 2400 TYPE
         BNE   FILL62              NO, THEN CHECK NEXT
         MVC   CMNERUNI,=CL8'2400'
         B     FILL99
FILL62   CLI   WUNTY+3,X'03'       IS THIS A 3400 TYPE ? ? ?
         BNE   FILL99              NO, THEN GIVE UP
         MVC   CMNERUNI,=CL8'3400'
         B     FILL99
FILL70   CLI   WUNTY+3,X'09'       IS THIS A 3330 MODEL 1 OR 2
         BNE   FILL72              NO, THEN CHECK FURTHER
         MVC   CMNERUNI,=CL8'3330'
         B     FILL99
FILL72   CLI   WUNTY+3,X'0B'       IS THIS A 3350? ? ?
         BNE   FILL73              NO, THEN CHECK FURTH
         MVC   CMNERUNI,=CL8'3350'
         B     FILL99
FILL73   CLI   WUNTY+3,X'0D'       IS THIS A 3330 MOD 11 ? ? ?
         BNE   FILL99              NO, THEN GIVE UP (WE DON'T HAVE ANY
*                                  OTHER TYPES)
         MVC   CMNERUNI,=CL8'3330-11'
FILL99   MVC   CMNERSET,=C'YES'
         BR    R10                 RETURN
CHK1     CLC   ACEEUSRI(0),WOWN
         DROP  R15
         TITLE '***** CONTANTS *****'
*
*        DIALOG MANAGER SERVICES
*
VGET     DC    CL8'VGET'
VDEF     DC    CL8'VDEFINE'
VDISPLAY DC    CL8'DISPLAY'
*
*        DIALOG MANAGER LISTS
*
LIST1    DC    C'(CMNERDSN)'
LIST2    DC    C'(CMNEROWN CMNERAUD CMNERUAC CMNERUNI)'
LIST3    DC    C'(CMNERRC)'
LIST4    DC    C'(CMNERVOL)'
LIST5    DC    C'(CMNERSET)'
LIST6    DC    C'(CMNERDSN CMNERVOL)'
*
*        DIALOG MANAGER LENGTHS
*
LNG3     DC    F'3'
LNG4     DC    F'4'
LNG6     DC    F'6'
LNG8     DC    F'8'
LNG44    DC    F'44'
*
*        MISCELANEOUS VARIABLSE
*
FIXED    DC    CL8'FIXED'
CHAR     DC    CL8'CHAR'
SHARED   DC    CL8'SHARED'
PANEL    DC    CL8'CMNER44'
MSG01    DC    CL8'CMNER440'       COMMAND SUCCESSFUL
MSG02    DC    CL8'CMNER441'       COMMAND FAILED
MSG03    DC    CL8'CMNER442'       ENTER DATASET
MSG04    DC    CL8'CMNER443'       NOT FOUND
MSG05    DC    CL8'CMNER444'       ENTER CHANGES
MSG06    DC    CL8'CMNER445'       ENTER VOLUME
MSG07    DC    CL8'CMNER447'       AUTHOIZATION FAILED
BLANKTAB DC    64X'00'
         DC    X'01'
         DC    191X'00'
*
*
*
ACT1     ICHEACTN FIELD=AUTHOR,MF=L
ACT2     ICHEACTN FIELD=UNIVACS,MF=L
ACT3     ICHEACTN FIELD=AUDIT,MF=L
ACT4     ICHEACTN FIELD=DEVTYP,MF=L
ACT5     ICHEACTN FIELD=DEVTYPX,MF=L
         LTORG
         TITLE '***** DYNAMIC AREAS *****'
         DS    0D
DYNWK    EQU   *
ISPCATRC DS    A(0)
ISPCATCL DS    3A(0)
*
*        DIALOG MANAGER VARIABLES
*
RESLN    DS    X
CMNERDSN DS    CL44' '
CMNEROWN DS    CL8' '
CMNERAUD DS    CL8' '
CMNERUAC DS    CL8' '
CMNERUNI DS    CL8' '
CMNERVOL DS    CL6' '
CMNERRC  DS    F'0'
CMNERSET DS    CL3' '
CMNEBDSN DC    CL44' '
CMNEBVOL DC    CL6' '
ISPLNK1  CALL   ,(VDEF,LIST1,CMNERDSN,CHAR,LNG44),MF=L
ISPLNK2  CALL   ,(VDEF,LIST2,CMNEROWN,CHAR,LNG8),MF=L
ISPLNK3  CALL   ,(VDEF,LIST3,CMNERRC,FIXED,LNG4),MF=L
ISPLNK4  CALL   ,(VDEF,LIST4,CMNERVOL,CHAR,LNG6),MF=L
ISPLNK5  CALL   ,(VDEF,LIST5,CMNERSET,CHAR,LNG3),MF=L
ISPLNK6  CALL  ,(VGET,LIST6,SHARED),MF=L
CMDBUF   DC    H'255'
CMDOFF   DC    H'4'
CMD      DC    CL255'ASD '
*
*
*
RCHK1    RACHECK ENTITY=CMNERDSN,CLASS=DATASET,ATTR=READ,MF=L
TY1      ICHEINTY LOCATE,TYPE='DS',MF=L,ACTIONS=(ACT1,ACT2,ACT3,ACT4,  X
               ACT5)
WK1      DC    A(WK1LN)
         DS    6F
         DS    H
WOWN     DS    CL8
         DS    XL2
WUACC    DS    X
         DS    XL2
WAUDIT   DS    X
         DS    XL2
WUNTY    DS    XL4
         DS    XL2
WUNTYX   DS    XL8
         DS    0F
WK1LN    EQU   *-WK1
CAMLIST  CAMLST NAME,CMNERDSN,,CATWRK
         DS    0D
CATWRK   DS    CL265
DYNWKLN  EQU   *-DYNWK
         IHAACEE
         END
