CMNER13  $PROLOG
***********************************************************************
*                                                                     *
*        PROGRAM - CMNER13                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS A NON REENTRANT ROUTINE WHICH DISPLAYS ALL *
*               RACF INFORMATION ABOUT GENERAL RESOURCES. IT DOES NOT *
*               REQUIRE APF AUTHORIZATION.                            *
*                                                                     *
*        PANELS USED - CMNER13                                        *
*                                                                     *
*        ERROR MESSAGES - CMNER130 - CMNER132                         *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         BAL   R10,SETUP
P30010   CALL  ISPLINK,(VGET,LIST1,SHARED)
P30030   CALL  ISPLINK,(VPUT,VNXTSW,SHARED)
         CALL  ISPLINK,(VSELECT,LNG12,CMNER18)
         CALL  ISPLINK,(VGET,LIST1,SHARED)
         L     R8,CMNERRC          GET RETURN CODE FROM FILL MODULE
         LTR   R8,R8
         BZ    P30035              IF ZERO CONTINUE
*                                  OTHERWISE DISPLAY AN ERROR MSG
         ST    R8,CMNERRC
         CALL  ISPLINK,(VDISPLAY,CMNER01,MSG01,)
         B     P30099              AND GET OUT
P30035   BAL   R10,CHKAUTH         CHECK AUTHORIZATION
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   P30099              IF NOT ZERO, THEN GET OUT
         CALL  ISPLINK,(VDEF,VROW,CMNERROW,FIXED,LNG4)
         CALL  ISPLINK,(VTBQUERY,TABLE,,,VROWNP,,,)
         LTR   R15,R15
         BZ    P30035A
         ABEND 5,DUMP
P30035A  L     R15,CMNERROW        CHECK IF TABLE EMPTY
         CH    R15,=H'0'
         BNE   P30035B             NO, THEN CAN USE TABLE DISPLAY
         CALL ISPLINK,(VDISPLAY,CMNER01,,) OTHERWISE JUST USE DISPLAY
         B     P30036
P30035B  CALL ISPLINK,(VTBDISPL,TABLE,CMNER01,)
P30036   C     R15,=F'8'           WAS END KEY ENTERED? ? ?
         BE    P30099              YES, THEN GET OUT
         LTR   R15,R15             CHECK FOR GOOD RETURN
         BZ    P30040              YES, CONTINUE
         ABEND 3,DUMP              NO, SAY BYE
P30040   EQU   *
         MVI   CMNERNXT,X'00'      SET DEFAULT NEXT SWITCH
         OC    CMNEROPT,=CL4' '    TRANSLATE TO UPPER CASE
         CLC   CMNEROPT,=CL8'NEXT' IS IT A REQUEST FOR THE NEXT DATASET
         BE    P30050              YES--
         ICM   R15,15,CMNERROW        GET NUM OF ROWS
         BNZ   P30045              ONLY USE TABLE SERVICE IF ROWS THERE
         CALL  ISPLINK,(VDISPLAY,CMNER01,MSG02,) OTHERWISE JUST DISPLAY
         B     P30036
P30045   CALL  ISPLINK,(VTBDISPL,TABLE,CMNER01,MSG02)
         B     P30036
P30050   MVI   CMNERNXT,X'01'      YES, THEN SET NEXT SWITCH
         CALL  ISPLINK,(VPUT,VNXTSW,SHARED)
         CALL  ISPLINK,(VTBEND,TABLE)    REMOVE OLD TABLE
         CALL  ISPLINK,(VTBCRET,TABLE,,LIST2,NOWRITE,)
         B     P30010
P30099   CALL  ISPLINK,(VTBEND,TABLE)
         $EPILOG
         TITLE '***** CHECK AUTHORIZATION ROUTINE *****'
CHKAUTH  L     R4,544              ADDR OF ASCB
         L     R4,108(R4)          ADDR OF ASXB
         L     R4,200(R4)          ADDR OF ACEE
         USING ACEE,R4             SET UP ADDRESSIBILITY
         LA    R15,0               SET DEFAULT RETURN
         TM    ACEEFLG1,ACEESPEC   IS USER SPECIAL
         BOR   R10                 YES, THEN RETURN
         XR    R3,R3               CLEAR R3
         IC    R3,ACEEUSRL         GET LENGTH OF USER
         BCTR  R3,0                REDUCE FOR COMPARE
         EX    R3,CHK1             CHECK IF USERID = OWNERID
         BER   R10                 YES, THEN RETURN
*
*                                  CHECK FOR AT LEAST READ ACCESS
         RACHECK ENTITY=CMNERRES,CLASS=CMNERCLS,                       X
               ATTR=READ
         LTR   R15,R15             CHECK RETURN FROM RACHECK
         BZR   R10                 GOOD, THEN RETURN
*
*                                  IF NO GOOD, DISPLAY AUTHORIZATION
*                                  FAILED MSG
         CALL  ISPLINK,(VDISPLAY,CMNER01,MSG03,)
         BR    R10
         TITLE '***** SETUP ROUTINE *****'
SETUP    EQU   *
*
*  THIS ROUTINE PERFORMS ALL INTIAL DIALOG MANAGER SERVICES
*
         CALL  ISPLINK,(VTBCRET,TABLE,,LIST2,NOWRITE,)
         CALL  ISPLINK,(VDEF,VOPT,CMNEROPT,CHAR,LNG8)
         CALL  ISPLINK,(VDEF,VRES,CMNERRES,CHAR,LNG8)
         CALL  ISPLINK,(VDEF,VNXTSW,CMNERNXT,FIXED,LNG1)
         CALL  ISPLINK,(VDEF,VRETCD,CMNERRC,FIXED,LNG4)
         CALL  ISPLINK,(VDEF,VOWN,CMNEROWN,CHAR,LNG8)
         BR    R10
         TITLE '***** CONSTANTS *****'
*
*  DIALOG MANAGER SERVICES
*
VGET     DC    CL8'VGET'
VPUT     DC    CL8'VPUT'
VDEF     DC    CL8'VDEFINE'
VCOPY    DC    CL8'VCOPY'
VTBDISPL DC    CL8'TBDISPL'
VTBCRET  DC    CL8'TBCREATE'
VTBEND   DC    CL8'TBEND'
VSELECT  DC    CL8'SELECT'
VDISPLAY DC    CL8'DISPLAY'
VTBQUERY DC    CL8'TBQUERY'
*
*  DIALOG MANGER LENGTHS
*
LNG1     DC    F'1'
LNG2     DC    F'2'
LNG4     DC    F'4'
LNG8     DC    F'8'
LNG11    DC    F'11'
LNG12    DC    F'12'
*
*  DIALOG MANAGER LISTS
*
VROW     DC    C'(CMNERROW)'
VOPT     DC    C'(CMNEROPT)'
VRES     DC    C'(CMNERRES CMNERCLS)'
VNXTSW   DC    C'(CMNERNXT)'
VRETCD   DC    C'(CMNERRC)'
VOWN     DC    C'(CMNEROWN)'
LIST1    DC    C'(CMNERRES '
         DC    C'CMNERCLS '
         DC    C'CMNERRC '
         DC    C'CMNEROWN '
         DC    C'CMNERUAC '
         DC    C'CMNERCDT '
         DC    C'CMNERLUP)'
LIST2    DC    C'(CMNERTUI '
         DC    C'CMNERTAU '
         DC    C'CMNERTAC)'
*
*  DIALOG MANAGER VARIABLES
*
CMNEROPT DC    CL8' '
CMNERRES DS    CL8
CMNERCLS DC    CL8' '
CMNERRC  DS    F
CMNERNXT DC    X'00'
CMNEROWN DS    CL8
DSNADDR  DS    A
CMNERROW DC    F'0'
*
*  DIALOG MANAGER PANELS, FUNCTIONS AND MESSAGES
*
CMNER01  DC    CL8'CMNER13'
CMNER18  DC    C'PGM(CMNER18)'
MSG01    DC    CL8'CMNER130'
MSG02    DC    CL8'CMNER131'
MSG03    DC    CL8'CMNER132'
*
*  DIALOG MANAGER MISCELLANEOUS FIELDS
*
VROWNP   DC    CL8'CMNERROW'
FIXED    DC    CL8'FIXED'
CHAR     DC    CL8'CHAR'
SHARED   DC    CL8'SHARED'
NOWRITE  DC    CL8'NOWRITE'
TABLE    DC    CL8'CMNERTRS'
*
*  MISCELLANEOUS FIELDS
*
CHK1     CLC   CMNEROWN(0),ACEEUSRI
         IHAACEE
         END
