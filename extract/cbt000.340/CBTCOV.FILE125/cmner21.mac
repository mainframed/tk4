CMNER21  $PROLOG
***********************************************************************
*        PROGRAM - CMNER21                                            *
*        DATE WRITTEN - JULY 1981                                     *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS A REENTRANT ROUTINE WHICH AUTHORIZES ITSELF*
*               BUILDS A RDEF COMMAND AND BRANCHES TO ISPCAT. ISPCAT  *
*               IS A SPF SUBROUTINE WHICH ATTACHES AND EXECUTES TSO   *
*               COMMANDS.                                             *
*                                                                     *
*        PANELS USED - CMNER21                                        *
*                                                                     *
*        ERROR MESSAGES - CMNER210 - CMNER213                         *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         GETMAIN R,LV=DYNWKLN
         LR    R8,R1               COPY ADDR OF GOTTEN STORAGE
         LA    R9,DYNWKLN          LENGTH OF DYNAMIC AREA
         LR    R3,R9               COPY LENGTH FOR MOVE
         LA    R2,DYNWK            BEGINNING ADDR OF MOVE
         MVCL  R8,R2               MOVE INTO GOTTEN AREA
         USING DYNWK,R8
         LR    R8,R1               RESTORE TO BEGININNING
         BAL   R10,SETUP
         CALL  ISPLINK,(VDISPLAY,PANEL,,CURSOR)  DISPLAY 1ST PANEL
C21015   MVI   CMD+5,C' '          CLEAR COMMAND BUFFER
         MVC   CMD+6(249),CMD+5
         CH    R15,=H'8'           WAS END KEY ENTERED
         BE    C21999              YES, THEN GET OUT
         LTR   R15,R15             CHECK FOR GOOD RETURN
         BZ    C21020              YES--
         ABEND 1,DUMP
C21020   CLC   CMNEBRES,CMNERRES   DID RESOURCE CHANGE ?
         BNE   C21030              YES, THEN OKAY
         CLC   CMNEBCLS,CMNERCLS   DID CLASS CHANGE ?
         BNE   C21030              YES, THEN OKAY
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG01,CURSOR)
         B     C21015              NO, DISPLAY ERROR AND TRY AGAIN
C21030   LA    R3,CMD+5            GET 1ST AVAILABLE BYTE IN COMMAND
         MVC   0(8,R3),CMNERCLS    MOVE IN CLASS
         TRT   0(9,R3),BLANKTAB    SEARCH FOR BLANK AFTER CLASS
         BNZ   C21040              FOUND IT
         ABEND 2,DUMP              NOT FOUND
C21040   LA    R3,1(R1)            ADVANCE BEYOND 1ST BLANK
         MVC   0(8,R3),CMNERRES    MOVE IN RESOURCE NAME
         TRT   0(9,R3),BLANKTAB    SEARCH FOR BLANK AFTER RESOURCE
         BNZ   C21050              FOUND IT
         ABEND 2,DUMP
C21050   LA    R3,1(R1)            ADVANCE BEYOND 1ST BLANK
         MVC   0(6,R3),=C'OWNER('  MOVE IN OWNER PARAMETER
         MVC   6(4,R3),CMNEROWN    MOVE IN OWNER FROM SCREEN
         TRT   0(11,R3),BLANKTAB   SEARCH FOR FIRST BLANK AFTER OWNER
         BNZ   C21060              FOUND IT
         ABEND 2,DUMP
C21060   MVI   0(R1),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R1)            ADVANCE BEYOND
         MVC   0(6,R3),=C'AUDIT('  MOVE IN AUDIT KEYWORD
         MVC   6(8,R3),CMNERAUD    MOVE IN AUDIT FROM SCREEN
         TRT   0(15,R3),BLANKTAB   SEARCH FOR FIRST BLANK AFTER AUDIT
         BNZ   C21070              FOUND IT
         ABEND 2,DUMP
C21070   MVI   0(R1),C')'          MOVE IN CLOSING PAREN
         LA    R3,2(R1)            ADVANCE BEYOND
         MVC   0(5,R3),=C'UACC('   MOVE IN UACC KEYWORD
         MVC   5(8,R3),CMNERUAC    MOVE IN UACC FROM SCREEN
         TRT   0(14,R3),BLANKTAB   SEARCH FOR FIRST BLANK AFTER AUDIT
         BNZ   C21080              FOUND IT
         ABEND 2,DUMP
C21080   MVI   0(R1),C')'          MOVE CLOSING PAREN
         LA    R3,2(R1)            ADVANCE BEYON
         CLC   CMNERDAT,=CL30' '   WAS DATA ENTERED? ? ?
         BE    C21090              NO, THEN BYPASS
         MVC   0(6,R3),=C'DATA(''' MOVE IN KEYWORD
         MVC   6(30,R3),CMNERDAT   MOVE IN DATA
         TRT   0(37,R3),BLANKTAB   SEARCH FOR FIRST BLANK AFTER DATA
         BNZ   C21085              FOUND IT
         ABEND 2,DUMP
C21085   MVC   0(2,R1),=C''')'     MOVE IN CLOSING QUOTE AND PAREN
         LA    R3,3(R1)            ADVANCE BEYOND
C21090   CLC   CMNERAPP,=CL30' '   WAS APPLDATA ENTERED
         BE    C21100              NO, CONTINUE
         MVC   0(10,R3),=C'APPLDATA('''  MOVE IN KEYWORD
         MVC   10(30,R3),CMNERAPP  MOVE IN DATA
         TRT   0(41,R3),BLANKTAB   SEARCH FOR FIRST BLANK AFTER DATA
         BNZ   C21095              FOUND IT
         ABEND 2,DUMP
C21095   MVC   0(2,R1),=C')'''     MOVE IN CLOSING QUOTE AND PAREN
         SPACE 5
*
*   BEGIN BUILDING THE CALL LIST TO ISPCAT
*
C21100   L     R2,X'21C'           TCBOLD
         L     R2,X'70'(R2)        ADDR OF 1ST SAVE AREA
         L     R2,X'18'(R2)        R1 IN 1ST SAVE ARE PTS TO PARM
         L     R2,0(R2)            FIRST PARM IS TLD
         CLC   =C'TLD',0(R2)       CHECK EYE CATCHER
         BE    C21110              GOOD
         ABEND 3,DUMP
C21110   ST    R2,ISPCATCL         STORE ADDR OF TLD
         LOAD  EP=ISPSUBS
         LR    R3,R0               STORE EP OF ISPSUBS
         LA    R1,=C'AUTH'         MAKE AUTHORIZED
         SVC   231
         LA    R4,ISPCATRC         RELOCATE
         ST    R4,ISPCATCL+8           IN DYNAMIC AREA
         LA    R4,CMDBUF
         ST    R4,ISPCATCL+4
         L     R15,X'2C'(R3)       GET ADDR OF ISPCAT ENTRY POINT
         LA    R1,ISPCATCL         GET PARAMETER LIST
         BALR  R14,R15             AND GO DO IT
         L     R4,ISPCATRC         GET RETURN CODE
         LTR   R4,R4               CHECK IT
         BZ    C21120              GOOD
         ST    R4,CMNERRC
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG02,CURSOR)
         B     C21015
C21120   MVC   CMNEBRES,CMNERRES    STORE BEFORE COPY
         MVC   CMNEBCLS,CMNERCLS
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG03,CURSOR)
         B     C21015
         SPACE 5
C21999   SR    R1,R1
         SVC   231                 REMOVE AUTHORIZATION
         FREEMAIN R,LV=DYNWKLN,A=(R8)
         $EPILOG
         TITLE '***** SETUP ROUTINE *****'
SETUP    EQU   *
         CALL  ISPLINK,(,,CMNERRES,,),MF=(E,ISPLNK1)
         CALL  ISPLINK,(,,CMNEROWN,,),MF=(E,ISPLNK2)
         CALL  ISPLINK,(,,CMNERAUD,,),MF=(E,ISPLNK3)
         CALL  ISPLINK,(,,CMNERDAT,,),MF=(E,ISPLNK4)
         CALL  ISPLINK,(,,CMNERRC,,),MF=(E,ISPLNK5)
         CALL  ISPLINK,(,LIST1,),MF=(E,ISPLNK6)
         L     R4,544              ADDR OF ASCB
         L     R4,108(R4)          ADDR OF ASXB
         L     R4,200(R4)          ADDR OF ACEE
         USING ACEE,R4             SET UP ADDRESSIBILITY
         MVC   CMNEROWN,ACEEUSRI   MOVE IN DEFAULT OWNER
         DROP  R4
         BR    R10
         TITLE '***** CONTANTS *****'
*
*        DIALOG MANAGER SERVICES
*
VGET     DC    CL8'VGET'
VDEF     DC    CL8'VDEFINE'
VDISPLAY DC    CL8'DISPLAY'
*
*        DIALOG MANAGER LISTS
*
LIST1    DC    C'(CMNERRES CMNERCLS)'
LIST2    DC    C'(CMNEROWN)'
LIST3    DC    C'(CMNERAUD CMNERUAC)'
LIST4    DC    C'(CMNERDAT CMNERAPP)'
LIST5    DC    C'(CMNERRC)'
*
*        DIALOG MANAGER LENGTHS
*
LNG4     DC    F'4'
LNG8     DC    F'8'
LNG30    DC    F'30'
*
*        DIALOG MANAGER MISCELLANEOUS FIELDS
*
FIXED    DC    CL8'FIXED'
SHARED   DC    CL8'SHARED'
CHAR     DC    CL8'CHAR'
PANEL    DC    CL8'CMNER21'
MSG01    DC    CL8'CMNER210'
MSG02    DC    CL8'CMNER211'
MSG03    DC    CL8'CMNER212'
CURSOR   DC    CL8'CMNERRES'
BLANKTAB DC    64X'00'
         DC    X'01'
         DC    191X'00'
         LTORG
         TITLE '***** DYNAMIC AREAS *****'
DYNWK    EQU   *
ISPCATRC DC    A(0)
ISPCATCL CALL  ,(,CMDBUF,ISPCATRC),MF=L
ISPLNK1  CALL   ,(VDEF,LIST1,CMNERRES,CHAR,LNG8),MF=L
ISPLNK2  CALL   ,(VDEF,LIST2,CMNEROWN,CHAR,LNG4),MF=L
ISPLNK3  CALL  ,(VDEF,LIST3,CMNERAUD,CHAR,LNG8),MF=L
ISPLNK4  CALL  ,(VDEF,LIST4,CMNERDAT,CHAR,LNG30),MF=L
ISPLNK5  CALL  ,(VDEF,LIST5,CMNERRC,FIXED,LNG4),MF=L
ISPLNK6  CALL  ,(VGET,LIST1,SHARED),MF=L
CMDBUF   DC    H'255'
CMDOFF   DC    H'5'
CMD      DC    CL255'RDEF'
CMNEBRES DC    CL8' '
CMNEBCLS DC    CL8' '
*
*        DIALOG MANAGER VARIABES
*
CMNERRES DS    CL8
CMNERCLS DS    CL8
CMNEROWN DS    CL4
CMNERAUD DC    CL8'FAILURES'
CMNERUAC DC    CL8'NONE'
CMNERDAT DC    CL30' '
CMNERAPP DC    CL30' '
CMNERRC  DS    F
         SPACE 5
DYNWKLN  EQU   *-DYNWK
         EJECT
         IHAACEE
         END
