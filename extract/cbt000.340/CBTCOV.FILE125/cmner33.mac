         TITLE '***** DELETE GROUP *****'
CMNER33  $PROLOG
***********************************************************************
*        PROGRAM - CMNER33                                            *
*        DATE WRITTEN - AUGUST 1981                                   *
*        AUTHOR  - R. A. CLAUER                                       *
*        PURPOSE - THIS IS A REENTRANT ROUTINE WHICH AUTHORIZES ITSELF*
*               BUILDS A DG   COMMAND AND BRANCHES TO ISPCAT. ISPCAT  *
*               IS A SPF SUBROUTINE WHICH ATTACHES AND EXECUTES TSO   *
*               COMMANDS.                                             *
*                                                                     *
*        PANELS USED - CMNER33                                        *
*                                                                     *
*        ERROR MESSAGES - CMNER330 - CMNER333                         *
*                                                                     *
*        CHANGE LOG -                                                 *
*          MM/DD/YY -                                                 *
*                                                                     *
***********************************************************************
         EJECT
         GETMAIN R,LV=DYNWKLN
         LR    R8,R1               COPY ADDR OF GOTTEN STORAGE
         LA    R9,DYNWKLN          LENGTH OF DYNAMIC AREA
         LR    R3,R9               COPY LENGTH FOR MOVE
         LA    R2,DYNWK            BEGINNING ADDR OF MOVE
         MVCL  R8,R2               MOVE INTO GOTTEN AREA
         USING DYNWK,R8
         LR    R8,R1               RESTORE TO BEGININNING
         CALL  ISPLINK,(,LIST1,CMNERGRP),MF=(E,ISPLNK1)
         CALL  ISPLINK,(,LIST2,CMNERRC),MF=(E,ISPLNK3)
         CALL  ISPLINK,(,LIST1),MF=(E,ISPLNK2)
         CALL  ISPLINK,(VDISPLAY,PANEL,,)  DISPLAY 1ST PANEL
C33005   CH    R15,=H'8'           END ENTERED? ? ?
         BE    C33999              YES--
         LTR   R15,R15             CHECK RETURN
         BZ    C33010              OKAY
         ABEND 1,DUMP
C33010   MVI   CMD+3,C' '          CLEAR COMMAND BUFFER
         MVC   CMD+4(251),CMD+3
         CLC   CMNERGRP,=CL8' '    WAS GROUP ENTERED
         BE    C33030              NO, THEN ERROR
         MVC   CMD+3(L'CMNERGRP),CMNERGRP
         L     R2,X'21C'           TCB OLD
         L     R2,X'70'(R2)        ADDR OF 1ST SAVE AREA
         L     R2,X'18'(R2)        R1 IN 1ST SAVE AREA PTS TO PARM
         L     R2,0(R2)            FIRST PARM IS TLD
         CLC   =C'TLD',0(R2)       BE SURE
         BE    C33020
         ABEND 2,DUMP
C33020   ST    R2,ISPCATCL         STORE IN PARM LIST
         LA    R4,ISPCATRC         RELOCATE
         ST    R4,ISPCATCL+8               RETURN CODE
         LA    R4,CMDBUF           RELOCATE
         ST    R4,ISPCATCL+4               COMMAND BUFFER
         LOAD  EP=ISPSUBS
         LR    R3,R0               COPY EP OF ISPSUBS
         LA    R1,=C'AUTH'
         SVC   231                 MAKE AUTHORIZED
         L     R15,X'2C'(R3)       EP OF ISPCAT
         LA    R1,ISPCATCL
         BALR  R14,R15             GO TO IT
         L     R4,ISPCATRC         GET RETURN
         SR    R1,R1               REMOVE AUTHORIZATION
         SVC   231
         LTR   R4,R4               CHECK IT
         BZ    C33025
         ST    R4,CMNERRC
         CALL  ISPLINK,(VDISPLAY,PANEL,MSG02,)
         B     C33999
C33025   CALL  ISPLINK,(VDISPLAY,PANEL,MSG01,)
         B     C33999
C33030   CALL  ISPLINK,(VDISPLAY,PANEL,MSG03,)
         B     C33005
C33999   FREEMAIN R,LV=DYNWKLN,A=(8)
         $EPILOG
         TITLE '***** CONTANTS *****'
*
*        DIALOG MANAGER SERVICES
*
VGET     DC    CL8'VGET'
VDEF     DC    CL8'VDEFINE'
VDISPLAY DC    CL8'DISPLAY'
*
*        DIALOG MANAGER LISTS
*
LIST1    DC    C'(CMNERGRP)'
LIST2    DC    C'(CMNERRC)'
*
*        DIALOG MANAGER LENGTHS
*
LNG4     DC    F'4'
LNG8     DC    F'8'
*
*        MISCELANEOUS VARIABLSE
*
FIXED    DC    CL8'FIXED'
CHAR     DC    CL8'CHAR'
SHARED   DC    CL8'SHARED'
PANEL    DC    CL8'CMNER33'
MSG01    DC    CL8'CMNER330'
MSG02    DC    CL8'CMNER331'
MSG03    DC    CL8'CMNER332'
         LTORG
         TITLE '***** DYNAMIC AREAS *****'
         DS    0D
DYNWK    EQU   *
ISPCATRC DS    A(0)
ISPCATCL DS    3A(0)
*
*        DIALOG MANAGER VARIABLES
*
CMNERGRP DS    CL8' '
CMNERRC  DS    F'0'
ISPLNK1  CALL   ,(VDEF,LIST1,CMNERGRP,CHAR,LNG8),MF=L
ISPLNK2  CALL  ,(VGET,LIST1,SHARED),MF=L
ISPLNK3  CALL   ,(VDEF,LIST1,CMNERRC,FIXED,LNG4),MF=L
CMDBUF   DC    H'255'
CMDOFF   DC    H'3'
CMD      DC    CL255'DG '
DYNWKLN  EQU   *-DYNWK
         END
