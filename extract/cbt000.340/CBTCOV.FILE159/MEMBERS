00014720/*                                                                 */
00014730/* THIS HANDY CLIST IS USED TO GENERATE A LIST OF MEMBER NAMES     */
00014740/* OF ANY CATALOGUED PARTITIONED DATASET IN THE SEQUENTIAL         */
00014750/* DATASET OF YOUR CHOICE. THESE MAY THEN BE EDITED AND USED AS    */
00014760/* IEBCOPY CONTROL CARDS, INPUT FOR ANOTHER CLIST, ETC.            */
00014770/* NOTE: THE RECEIVING DATASET MUST EXIST BEFORE EXECUTION         */
00014780/*                                                                 */
00014790CONTROL NOFLUSH NOMSG
00014800FREE F(SYSPRINT SYSIN DD TARGET)
00014810FREE ATTR(A B)
00014820ATTR A RECFM(F B) LRECL(121) BL(1210)
00014830ATTR B RECFM(F B) LRECL(80) BL(1600)
00014840ALLOC F(SYSPRINT) NEW SP(1 1) TRACKS  USING(A) UNIT(SYSVIO)
00014850ALLOC F(SYSIN) NEW SP(1 1) TRACKS  USING(B) UNIT(SYSVIO)
00014860WRITENR ENTER NAME OF PDS:
00014870READ
00014880SET &PDSNAME EQ &SYSDVAL
00014890IF &SUBSTR(1,&PDSNAME) NE &STR(') THEN SET &PDSNAME EQ &SYSPREF..&PDSNAME
00014900ELSE SET &PDSNAME EQ &SUBSTR(2:&LENGTH(&PDSNAME)-1,&PDSNAME)
00014910WRITENR ENTER VOLUME THAT &SYSDVAL RESIDES ON:
00014920READ &PDSVOL
00014930WRITENR ENTER DEVICE TYPE OF &PDSVOL - (3330-1 OR 3350):
00014940READ &DEVTYPE
00014950SET &S EQ &STR( LISTPDS DSNAME=&PDSNAME,VOL=&DEVTYPE=&PDSVOL)
00014960ALLOC F(DD) DA('&PDSNAME') SH UNIT(SYSALLDA) VOL(&PDSVOL)
00014970WRITENR ENTER TARGET DATASET NAME:
00014980READ
00014990SET &FRED EQ &SYSDVAL
00015000ALLOC F(TARGET) DA(&SYSDVAL) SH
00015010OPENFILE SYSIN OUTPUT
00015020SET &SYSIN EQ &STR(&S)
00015030PUTFILE SYSIN
00015040CLOSFILE SYSIN
00015050CALL 'SYS1.LINKLIB(IEHLIST)'
00015060ERROR +
00015070DO
00015080  CLOSFILE SYSPRINT
00015090  CLOSFILE TARGET
00015100  FREE F(SYSPRINT SYSIN DD TARGET)
00015110  FREE ATTR(A B)
00015120  ALLOC F(SYSPRINT) DA(*)
00015130  ALLOC F(SYSIN) DA(*)
00015140  WRITE
00015150  WRITE YOU MAY NOW EDIT '&FRED'.
00015160  EXIT
00015170END
00015180OPENFILE SYSPRINT
00015190OPENFILE TARGET OUTPUT
00015200GET: GETFILE SYSPRINT
00015210IF &SUBSTR(2:11,&SYSPRINT) NE &STR(          ) THEN GOTO GET
00015220IF &SUBSTR(12,&SYSPRINT) EQ &STR( ) THEN GOTO GET
00015230SET &SYSPRINT EQ &SUBSTR(12:21,&SYSPRINT)
00015240SET &TARGET EQ &SYSPRINT
00015250PUTFILE TARGET
00015260GOTO GET
