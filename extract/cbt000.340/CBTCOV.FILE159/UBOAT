00021890/*                                                                 */
00021900/* EXERCISE IN 3-DIMENSIONAL BINARY SEARCH.                        */
00021910/*                                                                 */
00021920/* NOTE: REQUIRES "RANDOM" TO BE INSTALLED IN SYS2.CMDPROC         */
00021930/*       OR EQUIVALENT                                             */
00021940/*                                                                 */
00021950SET &N EQ 7 /* OPTIMUM VALUE */
00021960WRITE YOU ARE THE CAPTAIN OF THE DESTROYER USS KLUDGE.
00021970WRITE AN ENEMY SUB HAS BEEN REPORTED IN YOUR AREA.
00021980WRITE YOUR MISSION: DESTROY IT BEFORE IT DESTROYS YOU.
00021990WRITE SPECIFY DEPTH CHARGE EXPLOSION POINT WITH A TRIO OF NUMBERS
00022000WRITE SEPARATED BY BLANKS OR COMMAS. E.G. 51,0,99
00022010WRITE THE FIRST NUMBER REPRESENTS COORDINATES SOUTH-NORTH 0-99.
00022020WRITE THE SECOND NUMBER REPRESENTS COORDINATES WEST-EAST 0-99.
00022030WRITE THE THIRD NUMBER REPRESENTS THE DEPTH. 0 = SURFACE, 99 = BOTTOM.
00022040WRITE YOU ARE SEARCHING A 100 X 100 X 100 CUBE.
00022050GOODLUCK: WRITE GOOD LUCK.
00022060WRITE
00022070RANDOM 2
00022080SET &A EQ &LASTCC
00022090RANDOM 2
00022100SET &B EQ &LASTCC
00022110RANDOM 2
00022120SET &C EQ &LASTCC
00022130SET &I EQ 1
00022140DO WHILE &I LE &N
00022150WRITENR SHOT #&I.. =>&STR( )
00022160AGAIN: +
00022170READ &X,&Y,&Z
00022180IF &X-&A EQ 0 AND &Y-&B EQ 0 AND &Z-&C EQ 0 THEN +
00022190DO
00022200WRITE BOOM! YOU FOUND IT IN &I TRIES.
00022210REPLAY: WRITE ANOTHER GAME? Y/N
00022220READ &YN
00022230IF &YN EQ Y THEN GOTO GOODLUCK
00022240EXIT
00022250END
00022260SET &P EQ &STR()
00022270SET &Q EQ &STR()
00022280SET &R EQ &STR( AND )
00022290SET &S EQ &STR()
00022300IF &X GT &A THEN SET &P EQ NORTH
00022310IF &X LT &A THEN SET &P EQ SOUTH
00022320IF &Y GT &B THEN SET &Q EQ EAST
00022330IF &Y LT &B THEN SET &Q EQ WEST
00022340IF (&P EQ &STR() AND &Q EQ &STR()) THEN SET &R EQ &STR()
00022350IF &Z GT &C THEN SET &S EQ TOO LOW.
00022360IF &Z LT &C THEN SET &S EQ TOO HIGH.
00022370IF &Z EQ &C THEN SET &S EQ DEPTH OK.
00022380WRITE SONAR REPORTS SHOT WAS &P&Q&R&S
00022390SET &I EQ &I+1
00022400END
00022410WRITE YOU HAVE BEEN TORPEDOED! ABANDON SHIP!
00022420WRITE THE SUBMARINE WAS AT &A, &B, &C..
00022430GOTO REPLAY
