00015830/*                                                                 */
00015840/* THIS HAS BEEN INCLUDED MAINLY AS AN EXAMPLE OF A JCL GENERATOR. */
00015850/*                                                                 */
00015860/* IT WAS USED TO BUILD JCL FOR A 3330-1 TO 3350 DATASET MIGRATION.*/
00015870/*                                                                 */
00015880/* PLEASE NOTE THE FOLLOWING:                                      */
00015890/*                                                                 */
00015900/* 1) IEHMAP FROM THE CBT MODS TAPE IS THE DRIVING PROGRAM.        */
00015910/*    (IEHLIST COULD BE USED AS WELL, BUT DOES NOT YIELD           */
00015920/*    SPACE ALLOCATION INFORMATION.)                               */
00015930/* 2) NOTE THE TECHNIQUE USED FOR READING SYSPRINT AS AN INPUT     */
00015940/*    DATASET.                                                     */
00015950/* 3) NOTE IEHMAP CONTROL CARD GENERATION TECHNIQUE.               */
00015960/* 4) NOTE "DSECT" OF SYSPRINT INPUT RECORD. GENERATED BY          */
00015970/*    MAPREC CLIST. (SUBSEQUENTLY MODIFIED.)                       */
00015980/* 5) NOTE NODELIST SELECTION.                                     */
00015990/*                                                                 */
00016000CONTROL NOMSG
00016010SET &VOL EQ TSO001
00016020SET &TARGET EQ TSO000
00016030CONTROL NOFLUSH
00016040/*
00016050/*       GENERATE VIO IEHMAP LISTING OF &VOL
00016060/*
00016070FREE F(DSNAME1 SYSIN SYSPRINT) ATTR(MAPIN MAPOUT)
00016080ATTR MAPIN LRECL(80) BLKSIZE(3120) RECFM(F B)
00016090ATTR MAPOUT LRECL(137) BLKSIZE(3509) RECFM(V B A)
00016100ALLOC F(SYSIN) UNIT(SYSVIO) USING(MAPIN) SPACE(1) TRACKS
00016110ALLOC F(SYSPRINT) UNIT(SYSVIO) USING(MAPOUT) SPACE(1) CYLINDERS
00016120ALLOC F(DSNAME1) NEW SPACE(0) TRACKS UNIT(SYSALLDA) VOL(&VOL)
00016130OPENFILE SYSIN OUTPUT
00016140SET &SYSIN EQ &STR( MAP VOL=&VOL)
00016150PUTFILE SYSIN
00016160CLOSFILE SYSIN
00016170CALL 'SYS2.LINKLIB(IEHMAP)' 'TSO'
00016180/*
00016190/* BUILD "DSECT" OF IEHMAP OUTPUT  -  TO BE USED AS SUBSEQUENT INPUT.
00016200/*
00016210SET DSNAME EQ &&SUBSTR(2:45,&&SYSPRINT)
00016220SET SERIAL EQ &&SUBSTR(47:52,&&SYSPRINT)
00016230SET EXPDT EQ &&SUBSTR(69:76,&&SYSPRINT)
00016240SET DSO EQ &&SUBSTR(79:80,&&SYSPRINT)
00016250SET RECFM EQ &&SUBSTR(83:85,&&SYSPRINT)
00016260SET BLKSIZE EQ &&SUBSTR(89:93,&&SYSPRINT)
00016270SET LRECL EQ &&SUBSTR(95:99,&&SYSPRINT)
00016280SET TRKAL EQ &&SUBSTR(109:113,&&SYSPRINT)
00016290SET TYP EQ &&SUBSTR(131:131,&&SYSPRINT)
00016300/*
00016310/* OPEN INPUT FILE
00016320/*
00016330OPENFILE SYSPRINT
00016340/*
00016350/* FIRE UP OUTPUT FILES
00016360/*
00016370FREE F(OUT1 OUT2 OUT3)
00016380ALLOC F(OUT1) DA(EXEC.CNTL) SH
00016390ALLOC F(OUT2) DA(CATLG.CNTL) SH
00016400OPENFILE OUT1 OUTPUT
00016410OPENFILE OUT2 OUTPUT
00016420SET &OUT1 EQ &STR(//MOVEMOUT JOB 780780,SYS,NOTIFY=NORTHRO,CLASS=1
00016430PUTFILE OUT1
00016440SET &OUT1 EQ &STR(/*JOBPARM PROCLIB=PROCTEST)
00016450PUTFILE OUT1
00016460SET &OUT2 EQ &STR(//CATLG    JOB 780780,SYS,NOTIFY=NORTHRO,CLASS=1
00016470PUTFILE OUT2
00016480SET &OUT2 EQ &STR(//         EXEC PGM=IEHPROGM)
00016490PUTFILE OUT2
00016500SET &OUT2 EQ &STR(//SYSPRINT DD SYSOUT=A)
00016510PUTFILE OUT2
00016520SET &OUT2 EQ &STR(//&VOL   DD  UNIT=3330-1,VOL=SER=&VOL,DISP=OLD)
00016530PUTFILE OUT2
00016540SET &OUT2 EQ &STR(//&TARGET   DD  UNIT=3350,VOL=SER=&TARGET,DISP=OLD)
00016550PUTFILE OUT2
00016560SET &OUT2 EQ &STR(//SYSIN    DD  *)
00016570PUTFILE OUT2
00016580/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
00016590/*
00016600/* BUILD LIST OF NODES TO BE INCLUDED.
00016610/*
00016620/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
00016630SET &IP EQ 1
00016640WRITE NODES TO BE INCLUDED MUST BE ENTERED IN ALPHABETICAL ORDER.
00016650INC: WRITE ENTER NODE TO BE INCLUDED OR NULL LINE TO TERMINATE.
00016660  READ
00016670  IF &LENGTH(&SYSDVAL) EQ 0 THEN GOTO INCDONE
00016680  SET I&IP EQ &SYSDVAL
00016690  SET &IP EQ &IP+1
00016700  SET &SYSDVAL EQ &STR()
00016710GOTO INC
00016720INCDONE: +
00016730SET &IC EQ 1
00016740SET &NODE EQ &&I&IC
00016750/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
00016760/*                      MAIN LOOP
00016770/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
00016780ERROR +
00016790DO
00016800  WRITE &LASTCC
00016810  EXIT
00016820END
00016830GET: GETFILE SYSPRINT  /*  I.E. VIO IEHMAP LISTING.  */
00016840IF &LENGTH(&SYSPRINT) LT 52 THEN GOTO GET /* STRIP GARBAGE LINES. */
00016850/*
00016860/*  DISCOVER DEVICE TYPE AND BUILD LATENCY VALUES. */
00016870/*
00016880IF &SUBSTR(1:18,&DSNAME) EQ &STR(DEVICE DESCRIPTION) THEN +
00016890DO
00016900  IF &SUBSTR(27:32,&DSNAME) EQ &STR(3330-1) THEN +
00016910  DO
00016920    SET &TRKLENGTH EQ 13165
00016930    SET &CN EQ 35
00016940  END
00016950  ELSE +
00016960  DO
00016970    SET &TRKLENGTH EQ 19254
00016980    SET &CN EQ 185
00016990  END
00017000END
00017010IF &SERIAL NE &VOL THEN GOTO GET  /* USE VOLUME CONSTANT AS SENTINEL */
00017020IF &DSO EQ PO THEN SET &PROC EQ COPY /* PROC IN USER.PROCLIB. */
00017030ELSE SET &PROC EQ GENER              /* DITTO. */
00017040SET &DS EQ &DSNAME                   /* TO STRIP RIGHT BLANKS. */
00017050SET &DS EQ &DS                       /* RIGHT BLANKS NOW STRIPPED */
00017060/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
00017070/*
00017080/* ENSURE JCL IS CREATED ONLY FOR MEMBERS IN NODE LIST
00017090/*
00017100/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
00017110IF &NODE GT &SUBSTR(1:&LENGTH(&NODE),&DS) THEN GOTO GET
00017120IF &NODE LT &SUBSTR(1:&LENGTH(&NODE),&DS) THEN +
00017130DO
00017140  SET &IC EQ &IC+1
00017150  IF &IC GE &IP THEN EXIT /* NORMAL EXIT */
00017160  SET &NODE EQ &&I&IC
00017170  IF &NODE GT &SUBSTR(1:&LENGTH(&NODE),&DS) THEN GOTO GET
00017180  IF &NODE LT &SUBSTR(1:&LENGTH(&NODE),&DS) THEN +
00017190  DO
00017200    WRITE NODE LIST OUT OF SYNCH WITH IEHMAP
00017210    WRITE CURRENT NODE: &NODE
00017220    WRITE CURRENT DSNAME: &DS
00017230    EXIT
00017240  END
00017250END
00017260/*
00017270/*  FOLLOWING TWO DO GROUPS STRIP LEFT BLANKS FROM &TRKAL & &BLKSIZE.
00017280/*
00017290SET &N EQ 1
00017300DO WHILE &SUBSTR(&N,&TRKAL) EQ &STR( )
00017310  SET &N EQ &N+1
00017320END
00017330SET &ALLOC EQ &STR()
00017340SET &TRK EQ &SUBSTR(&N:5,&TRKAL)
00017350SET &P EQ 1
00017360DO WHILE &SUBSTR(&P,&BLKSIZE) EQ &STR( )
00017370  SET &P EQ &P+1
00017380END
00017390SET &BLK EQ &SUBSTR(&P:5,&BLKSIZE)
00017400/*
00017410/*  NEXT STATEMENT BUILDS ALLOCATION VALUE IN BLOCKS.
00017420/*  NOTE: NO SECONDARIES ALLOCATED.
00017430/*
00017440SET &PRIMARY EQ &EVAL((&TRKLENGTH/(&BLK+&CN))*&TRK)
00017450SET &DIR EQ &STR()                   /* ASSUME SEQUENTIAL */
00017460IF &DSO EQ PO THEN SET &DIR EQ 69    /* ARBITRARY VALUE   */
00017470/*
00017480/*  EXPIRATION DATE WILL BE EITHER 00000 OR THE FIRST DAY OF THE YEAR.
00017490/*
00017500IF &SUBSTR(4,&EXPDT) EQ &STR(.) THEN SET &EXP EQ &STR(00000)
00017510ELSE SET &EXP EQ &SUBSTR(7:8,&EXPDT)001
00017520/*
00017530/* BUILD IEBCOPY/IEBGENER EXEC CARDS.
00017540/*
00017550SET &OUT1 EQ &STR(//         EXEC &PROC,DSNAME='&DS',)
00017560PUTFILE OUT1
00017570SET &OUT1 EQ &STR(//             SPACE='(&BLK,(&PRIMARY,,&DIR))',)
00017580PUTFILE OUT1
00017590SET &OUT1 EQ &STR(//             EXPDT=&STR(&EXP),TARGET=&TARGET)
00017600PUTFILE OUT1
00017610/*
00017620/* BUILD IEHPROGM CONTROL CARDS FOR UNCATCAT JOB
00017630/*
00017640SET &OUT2 EQ &STR( CATLG   DSNAME=&DS,VOL=3350=&TARGET)
00017650PUTFILE OUT2
00017660GOTO GET  /* END OF LOOP. EXIT IS VIA ERROR RETURN CODE 400 (NORMAL).
