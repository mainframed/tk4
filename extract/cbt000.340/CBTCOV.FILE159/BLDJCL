00005310PROC 0 DEBUG
00005320/* )STARTHELP
00005330/* )F FUNCTION -
00005340/*    THE BLDJCL COMMAND IS A CLIST WHOSE FUNCTION IS TO
00005350/*    TRANSCRIBE NEW MEMBERS INTO PRODUCTION JCL LIBRARIES.
00005360/*    BLDJCL SHOULD BE USED TO UPDATE "RDC" AND "SYS2" LIBRARIES
00005370/*    IN ORDER TO AVOID THE ENQUEUE AND RESERVE PROBLEMS NORMALLY
00005380/*    ENGENDERED BY FOREGROUND COMMANDS SUCH AS EDIT.
00005390/*
00005400/* )X SYNTAX -  'BLDJCL'
00005410/* )O OPERANDS - YOU WILL BE PROMPTED FOR THE FOLLOWING:
00005420/*
00005430/*      * ENTER SOURCE 'DATASET' OR 'DATASET(MEMBER)'
00005440/*                -ENTER THE NAME OF THE DATASET TO BE TRANSCRIBED.
00005450/*                 IF IT IS A PDS, INCLUDE (MEMBERNAME).
00005460/*      * ENTER TARGET 'DATASET(MEMBER)' OR NULL TO UPDATE
00005470/*               'SYS2.JCLLIB(MEMBER)'.
00005480/*                -ENTER THE NAME OF THE TARGET DATASET;
00005490/*                 INCLUDE (MEMBERNAME)  -OR-
00005500/*                 DEPRESS ENTER TO UPDATE THE DEFAULT LIBRARY.
00005510/*
00005520/*             MISCELLANEOUS MESSAGES -
00005530/*
00005540/*      *  DATASET NAMES WITHOUT QUOTES WILL BE PREFIXED WITH "LOGONID".
00005550/*                 -INFORMATIONAL MESSAGE.
00005560/*      *  SEQUENTIAL DATASET SPECIFIED OR ERROR SPECIFYING MEMBER.
00005570/*                 -INFORMATIONAL MESSAGE. IMPLIES MISSING OR UNBALANCED
00005580/*                   PARENTHESES.
00005590/*      *  PLEASE ENTER MEMBER NAME IN TARGET DATASET:
00005600/*                 -OCCURS IF SEQUENTIAL SOURCE DATASET SPECIFIED.
00005610/*                  ENTER MEMBER NAME.
00005620/*      *  COMMAND "BLDJCL" INTENDED FOR   L I B R A R Y  USE ONLY.
00005630/*         PLEASE SPECIFY TARGET IN THE FORM: 'DSNAME(MEMBER)'.
00005640/*                 -NO TARGET MEMBER NAME WAS SPECIFIED. RE-ENTER
00005650/*                  TARGET DATASET NAME AND MEMBER NAME.
00005660/*      *  SPECIFY VOLUME-SERIAL NUMBER OR NULL IF CATALOGUED:
00005670/*                 -ALLOWS USER TO BYPASS CATALOG  BY SPECIFYING
00005680/*                  VOLUME SERIAL NUMBER. ENTER EITHER VOLUME-SERIAL
00005690/*                  NUMBER OR NULL LINE TO ACCEPT CATALOG DEFAULT.
00005700/*      *  SOURCE DATASET IS: DSNAME - TARGET DATASET IS: DSNAME
00005710/*         IS THIS CORRECT? Y/N
00005720/*                 -FINAL VERIFICATION MESSAGE. REPLY "Y" TO ALLOW
00005730/*                  UPDATE OF TARGET DATASET. REPLY "N" TO RESPECIFY.
00005740/*      *  UPDATE PROCEEDING.
00005750/*                 -INFORMATIONAL MESSAGE. OCCURS AFTER RESPONDING "Y"
00005760/*                  TO PREVIOUS MESSAGE.
00005770/*
00005780/* )ENDHELP
00005790CONTROL NOFLUSH NOMSG
00005800ATTN EXIT
00005810IF &DEBUG NE &STR() THEN CONTROL CONLIST LIST SYMLIST
00005820SET &TARG EQ &SUBSTR(4:&LENGTH(&SYSICMD),&SYSICMD)
00005830REDO: +
00005840WRITE DATASET NAMES WITHOUT QUOTES WILL BE PREFIXED WITH &SYSUID..
00005850WRITENR ENTER SOURCE 'DATASET' OR 'DATASET(MEMBER)'
00005860READ
00005870SET &SOURCE EQ &STR(&SYSDVAL)
00005880SET &N EQ &LENGTH(&SOURCE)
00005890DO WHILE &N GT 1
00005900  IF &SUBSTR(&N,&SOURCE) EQ ) THEN +
00005910  DO
00005920    SET &M EQ &N
00005930    SET &N EQ &N-1
00005940    DO WHILE &SUBSTR(&N,&SOURCE) NE &STR((
00005950      SET &N EQ &N-1
00005960    END
00005970    SET &MEMBER EQ &SUBSTR(&N+1:&M-1,&SOURCE)
00005980    GOTO GETARG
00005990  END
00006000  SET &N EQ &N-1
00006010END
00006020WRITE SEQUENTIAL SOURCE DATASET SPECIFIED OR ERROR SPECIFYING MEMBER.
00006030WRITENR PLEASE ENTER MEMBER NAME IN TARGET DATASET:
00006040READ &MEMBER
00006050GETARG: WRITE ENTER TARGET 'DATASET(MEMBER)' OR NULL TO UPDATE 'SYS2.&TARG.LIB(&MEMBER)'
00006060RESPEC: READ
00006070SET &X EQ N
00006080IF &SYSDVAL EQ &STR() THEN +
00006090DO
00006100  SET &TARG EQ 'SYS2.&TARG.LIB(&MEMBER)'
00006110  SET &X EQ Y
00006120END
00006130/* ENSURE MEMBER OF TARGET DATASET IS SPECIFIED.*/
00006140ELSE +
00006150DO
00006160  DO
00006170    SET &TARG EQ &STR(&SYSDVAL)
00006180    SET &N EQ &LENGTH(&TARG)
00006190    DO WHILE &N GT 1
00006200      IF &SUBSTR(&N,&TARG) EQ ) THEN +
00006210      DO
00006220        /* RIGHT PAREN FOUND, LOOK FOR LEFT PAREN. */
00006230        SET &N EQ &N-1
00006240        DO WHILE &SUBSTR(&N,&TARG) NE &STR((
00006250          SET &N EQ &N-1
00006260        END
00006270        GOTO MEMFOUND
00006280      END
00006290      SET &N EQ &N-1
00006300    END
00006310  END
00006320WRITE COMMAND "&SYSICMD" INTENDED FOR   L I B R A R Y  UPDATE ONLY.
00006330WRITE PLEASE SPECIFY TARGET IN THE FORM: 'DSNAME(MEMBER)'.
00006340GOTO RESPEC
00006350END
00006360MEMFOUND: +
00006370SET &VOL EQ &STR()
00006380SET &UNIT EQ &STR()
00006390IF &X NE Y THEN +
00006400DO
00006410  WRITENR SPECIFY VOLUME-SERIAL NUMBER OR NULL IF CATALOGUED:
00006420  READ &VOL
00006430  IF &VOL EQ &STR() THEN GOTO FINAL
00006440  SET &VOL EQ &STR(VOLUME(&VOL))
00006450  SET &UNIT EQ UNIT(SYSALLDA)
00006460END
00006470FINAL: +
00006480WRITE SOURCE DATASET IS: &SOURCE
00006490WRITE TARGET DATASET IS: &TARG &VOL
00006500WRITENR IS THIS CORRECT? Y/N
00006510READ &YN
00006520IF &YN NE Y THEN GOTO REDO
00006530ATTN GOTO CLEANUP
00006540WRITE UPDATE PROCEEDING.
00006550%UMSG
00006560FREE F(SYSIN SYSPRINT SYSUT1 SYSUT2)
00006570ALLOC F(SYSUT2) DA(&TARG) SH &VOL &UNIT
00006580ALLOC F(SYSUT1) DA(&SOURCE) SH
00006590FREE F(SYSIN SYSPRINT)
00006600ALLOC F(SYSIN) DUMMY
00006610ALLOC F(SYSPRINT) DUMMY
00006620CALL 'SYS1.LINKLIB(IEBGENER)'
00006630CLEANUP: FREE F(SYSUT1 SYSUT2 SYSIN SYSPRINT)
00006640ALLOC F(SYSIN) DA(*)
00006650ALLOC F(SYSPRINT) DA(*)
