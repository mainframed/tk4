00013070PROC 0 DATE()
00013080GLOBAL MONTH DAY YEAR JULIAN SHOP WEEKDAY
00013090/*                                                                   */
00013100/* THE FOLLOWING CLIST PERFORMS THESE FUNCTIONS:                     */
00013110/*                                                                   */
00013120/* TAKES ANY VALID GREGORIAN DATE IN THIS CENTURY AND                */
00013130/* CALCULATES THE JULIAN DATE, THE DAY OF THE WEEK, AND              */
00013140/* A 'SHOP DATE' IE- THE NUMBER OF DAYS ELAPSED BETWEEN              */
00013150/* THE INPUT DATE AND JAN. 1, 1900. THUS IF YOU FEED                 */
00013160/* THIS CLIST 2 DATES, THE DIFFERENCE BETWEEN THE TWO                */
00013170/* SHOP DATES EQUALS THE NUMBER OF ELAPSED DAYS BETWEEN              */
00013180/* THE TWO.                                                          */
00013190/*                                                                   */
00013200/* THE FORMATS OF THE GLOBAL VARIABLES ARE AS FOLLOWS:               */
00013210/*                                                                   */
00013220/* INPUT - MONTH=MM, DAY=DD, YEAR=YY                                 */
00013230/*         FOR EXAMPLE: 01 01 76                                     */
00013240/*         NOTE - 1 1 76 WILL NOT WORK.                              */
00013250/*                                                                   */
00013260/* OUTPUT - JULIAN=YYDDD, IE 99365 FOR 12/31/99                      */
00013270/* SHOP   - DD..DD, IE, 36523 FOR THE SAME DATE                      */
00013280/* WEEKDAY - MONDAY, TUESDAY, WEDNESDAY, ETC.                        */
00013290/*                                                                   */
00013300ERROR +
00013310DO
00013320  SET &LASTCC EQ 4
00013330  EXIT
00013340END
00013350IF &DATE EQ SYSDATE THEN -
00013360DO
00013370  SET &MONTH EQ &SUBSTR(1:2,&SYSDATE)
00013380  SET &DAY   EQ &SUBSTR(4:5,&SYSDATE)
00013390  SET &YEAR  EQ &SUBSTR(7:8,&SYSDATE)
00013400END
00013410SET &JULIAN = &SUBSTR(&EVAL((&MONTH*3)-2):&EVAL(&MONTH*3),000031059090120151181212243273304334)
00013420SET &JULIAN = &JULIAN + &DAY
00013430IF &YEAR//4 = 0 AND &MONTH GT 2 THEN SET &JULIAN = &JULIAN + 1
00013440SET &SHOP = &JULIAN
00013450SET &L = &LENGTH(&JULIAN)
00013460SET &JULIAN = &STR(X00&JULIAN)
00013470SET &JULIAN = &STR(&YEAR)&STR(&SUBSTR(&EVAL(&L+1):&EVAL(&LENGTH(&JULIAN)),&JULIAN))
00013480SET &SHOP = &EVAL(&YEAR*365) + &EVAL((&YEAR-1)/4) + &SHOP - 1
00013490SET &WEEKDAY = &SUBSTR(&EVAL(((&SHOP//7)*6)+1):&EVAL(((&SHOP//7)*6)+6),   MON  TUESWEDNES THURS   FRI SATUR   SUN)
00013500SET &WEEKDAY = &SUBSTR(&EVAL(6-&LENGTH(&WEEKDAY)+1):6,&WEEKDAY)DAY
