         MACRO
&NAME    VGET &VAR,                     NAMELIST                       *
               &POOL,                   VARIABLE POOL                  *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFVGET,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLB  &SPFASIS,&SPFSHARED,&SPFPROFILE
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&VAR' EQ '').ERR1
         AIF   ('&POOL' NE '' AND '&POOL' NE 'ASIS' AND                *
               '&POOL' NE 'SHARED' AND  '&POOL' NE 'PROFILE').ERR2
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFVGET).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&VAR' EQ '').CNT1
         AIF   ('&VAR'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&POOL' EQ 'ASIS' AND NOT &SPFASIS).GENLIT
         AIF   ('&POOL' EQ 'PROFILE' AND NOT &SPFPROFILE).GENLIT
         AIF   ('&POOL' EQ 'SHARED' AND NOT &SPFSHARED).GENLIT
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFVGET).BYP20
VGET     DC    CL8'VGET'                ISPF KEYWORD
&SPFVGET SETB 1
.BYP20   ANOP
         AIF   ('&VAR' EQ '').BYP30
         AIF   ('&VAR'(1,1) NE '''').BYP30
SPFV&NDX DC    C&VAR                    GENERATED VAR LIST
.BYP30   ANOP
         AIF   ('&POOL' EQ '').BYP90
         AIF   ('&POOL' NE 'ASIS' OR &SPFASIS).BYP82
ASIS     DC    CL8'ASIS'                ISPF KEYWORD
&SPFASIS SETB  1
.BYP82   ANOP
         AIF   ('&POOL' NE 'PROFILE' OR &SPFPROFILE).BYP84
PROFILE  DC    CL8'PROFILE'             ISPF KEYWORD
&SPFPROFILE SETB 1
.BYP84   ANOP
         AIF   ('&POOL' NE 'SHARED' OR  &SPFSHARED).BYP90
SHARED   DC    CL8'SHARED'              ISPF KEYWORD
&SPFSHARED SETB 1
.BYP90   ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,VGET                  GET ADDRESS OF VGET KEYWORD
.*
         AIF   ('&VAR' EQ '').BYP1000
         AIF   ('&VAR'(1,1) EQ '''').BYP1010
         LA    15,&VAR                  GET ADDRESS OF VAR NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR VAR
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFV&NDX              GET ADDRESS OF GENERATED VAR
.BYP1050 ANOP
.*
         AIF   ('&POOL' EQ '').BYP1100
         LA    0,&POOL                  GET POOL TYPE
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR PANEL
.BYP1150 ANOP
.*
         STM   14,0,&TEMP               STORE PARAMETERS INTO LIST
         OI    &TEMP+8,X'80'            MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 3).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 3).BYPVARS
&SPFVARS SETA  3
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(VGET)                  VGET LITERAL
.*
         AIF   ('&VAR' EQ '').BYP2000
         AIF   ('&VAR'(1,1) EQ '''').BYP2010
         DC    A(&VAR)                  ADDRESS OF VAR NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR VAR NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFV&NDX)              ADDRESS OF GENERATED VAR
.BYP2050 ANOP
.*
         AIF   ('&POOL' EQ '').BYP2100
         DC    A(&POOL+X'80000000')     POOL TYPE
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR PANEL NAME
.BYP2150 ANOP
.*
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'THE NAME LIST PARAMETER IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID POOL SPECIFIED - FOUND ''&POOL'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
