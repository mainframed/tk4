         MACRO
&NAME    ISPF  &FUNC,                   FUNCTION REQUESTED             *
               &LIST=SPFPARMS,          FOR ALL FUNCTIONS              *
               &COUNT=,                 COUNT OF PARM ADDRESSES        *
               &MINIMUM=,               MINIMUM COUNT OF PARMS         *
               &ISPLINK=(YES,ISPLINK),  GENERATE ISPLINK FULLWORD      *
               &ISPEXEC=(NO),           GENERATE ISPEXEC FULLWORD      *
               &FULL=NO,                GENERATE UNSTRING WORK AREA    *
               &LEVEL=2,                LEVEL OF MACROS IN USE         *
               &LASTCC=                 GENERATE EQUATE IN PARMS
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFLOAD,&SPFDELETE,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         GBLC  &SPFLVL
         LCLC  &NDX,&TEMP
         LCLA  &CNT,&LMT
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       EDIT  GENERAL PARAMETERS
.**********************************************************************
.*
         AIF   ('&COUNT' NE '' AND                                     *
               T'&COUNT NE 'N').ERROR5
         AIF   ('&MINIMUM' NE '' AND                                   *
               T'&MINIMUM NE 'N').ERROR5A
         AIF   ('&ISPLINK(1)' NE 'YES' AND                             *
               '&ISPLINK(1)' NE 'NO').ERROR6
         AIF   ('&ISPEXEC(1)' NE 'YES' AND                             *
               '&ISPEXEC(1)' NE 'NO').ERROR7
         AIF   ('&FULL' NE 'YES' AND                                   *
               '&FULL' NE 'NO').ERROR8
         AIF   ('&MINIMUM' EQ '' OR &MINIMUM LE &SPFVARS).CONT11
&SPFVARS SETA  &MINIMUM
.CONT11  ANOP
.*
.**********************************************************************
.*       EDIT FUNCTION
.**********************************************************************
.*
         AIF   ('&FUNC' EQ 'PARMS').PARMS
         AIF   ('&FUNC' EQ 'LOAD').LOAD
         AIF   ('&FUNC' EQ 'DELETE').DEL
         AIF   ('&FUNC' EQ 'DEBUG').DEBUG
         AIF   ('&FUNC' EQ 'DEBUGOFF').DEBUGOFF
         AIF   ('&FUNC' EQ 'SUPPRESS').SUPRESS
         AIF   ('&FUNC' EQ 'RESET').RESET
         AIF   ('&FUNC' EQ 'SET').SET
         AGO   .ERROR1
.*
.**********************************************************************
.*       ISPF PARMS FUNCTION
.**********************************************************************
.*
.PARMS   ANOP
         AIF   (&SPFLOAD AND &SPFDELETE).PARMSG
         MNOTE 4,'        NO ISPF LOAD AND DELETE HAS BEEN DONE'
.*
.**********************************************************************
.*       GENERATE ISPF PARMS AREA
.**********************************************************************
.*
.PARMSG  AIF   ('&NAME' EQ '').BYP0
&NAME    DS    0H
.BYP0    AIF   ('&LIST' EQ '').BYP1
         AIF   ('&COUNT' NE '').PARMSD
         AIF   (&SPFVARS EQ 0).DONTGEN
&LIST    DS    &SPFVARS.F               ISPF PARAMETER LIST
.DONTGEN ANOP
&SPFVARC SETA  &SPFVARS
&SPFVAR  SETB  1
         AGO   .BYP1
.PARMSD  ANOP
         AIF   (&COUNT LT &SPFVARS).ERRVAR
&LIST    DS    &COUNT.F                 ISPF PARAMETER LIST
&SPFVARC SETA  &COUNT
&SPFVAR  SETB  1
.BYP1    ANOP
         AIF   ('&ISPLINK(1)' EQ 'NO').BYP2
&TEMP    SETC  '&ISPLINK(2)'
         AIF   ('&TEMP' NE '').CONT1
&TEMP    SETC  'ISPLINK'
.CONT1   ANOP
&TEMP    DS    A                        CONTAINS ISPLINK ADDRESS
.BYP2    ANOP
         AIF   ('&ISPEXEC(1)' EQ 'NO').BYP3
&TEMP    SETC  '&ISPEXEC(2)'
         AIF   ('&TEMP' NE '').CONT2
&TEMP    SETC  'ISPEXEC'
.CONT2   ANOP
&TEMP    DS    A                        CONTAINS ISPEXEC ADDRESS
.BYP3    ANOP
         AIF   ('&FULL' EQ 'NO').BYP4
SPFCMD   DS    CL80                     COMMAND RETURNED FROM UNSTRING
SPFCMDP  DS    19CL80                   PARMS RETURNED FROM UNSTRING
SPFCOUNT DS    F                        NUMBER OF PARMS RETURNED
.BYP4    ANOP
         AIF   ('&LASTCC' EQ '').BYP5
&LASTCC  EQU   15                       EQUATE REG 15 TO LASTCC
.BYP5    ANOP
         MEXIT
.*
.**********************************************************************
.*       ISPF LOAD FUNCTION
.**********************************************************************
.*
.LOAD    ANOP
.*
.**********************************************************************
.*       GENERATE ISPF LOAD AREA
.**********************************************************************
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
&SPFLOAD SETB  1
         AIF   ('&ISPLINK(1)' NE 'YES').BYP11
         LOAD  EP=ISPLINK               LOAD ISPLINK
         AIF   ('&ISPLINK(2)' EQ '').BYP11
         ST    0,&ISPLINK(2)            STORE ISPLINK ADDRESS
.BYP11   ANOP
         AIF   ('&ISPEXEC(1)' NE 'YES').BYP12
         LOAD  EP=ISPEXEC               LOAD ISPEXEC
         AIF   ('&ISPEXEC(2)' EQ '').BYP12
         ST    0,&ISPEXEC(2)            STORE ISPEXEC ADDRESS
.BYP12   ANOP
         MEXIT
.*
.**********************************************************************
.*       ISPF DELETE FUNCTION
.**********************************************************************
.*
.DEL     ANOP
.*
.**********************************************************************
.*       GENERATE ISPF LOAD AREA
.**********************************************************************
.*
         AIF   ('&NAME' EQ '').BYP20
&NAME    DS    0H
.BYP20   ANOP
&SPFDELETE SETB  1
         AIF   ('&ISPLINK(1)' NE 'YES').BYP21
         DELETE EP=ISPLINK              DEL ISPLINK
.BYP21   ANOP
         AIF   ('&ISPEXEC(1)' NE 'YES').BYP22
         DELETE EP=ISPEXEC              LOAD ISPLINK
.BYP22   ANOP
         MEXIT
.*
.**********************************************************************
.*       ISPF DEBUG FUNCTION
.**********************************************************************
.*
.DEBUG   ANOP
&SPFDEBUG SETB 1
         MEXIT
.*
.**********************************************************************
.*       ISPF DEBUGOFF FUNCTION
.**********************************************************************
.*
.DEBUGOFF ANOP
&SPFDEBUG SETB 0
         MEXIT
.*
.**********************************************************************
.*       ISPF SUPRESS AND GENERATE CODE
.**********************************************************************
.*
.SUPRESS ANOP
&CNT     SETA  1
&LMT     SETA  N'&LIST
.SUPLOOP ANOP
         AIF   (&CNT GT &LMT).SUPEXIT
         ISPFSUPP &LIST(&CNT)
&CNT     SETA  &CNT+1
         AGO   .SUPLOOP
.SUPEXIT ANOP
         MEXIT
.*
.**********************************************************************
.*       ISPF RESET CODE
.**********************************************************************
.*
.RESET   ANOP
&CNT     SETA  1
&LMT     SETA  N'&LIST
.SUPLOO2 ANOP
         AIF   (&CNT GT &LMT).SUPEXI2
         ISPFREST &LIST(&CNT)
&CNT     SETA  &CNT+1
         AGO   .SUPLOO2
.SUPEXI2 ANOP
         MEXIT
.*
.**********************************************************************
.*       ISPF SET LEVEL CODE
.**********************************************************************
.*
.SET     ANOP
&SPFLVL  SETC  '&LEVEL'
         MEXIT
.*
.**********************************************************************
.*       ERRORS ISSUED FROM MACRO
.**********************************************************************
.*
.ERROR1  ANOP
         MNOTE 16,'INVALID ISPF FUNCTION - FOUND ''&FUNC'''
         MEXIT
.ERROR5  ANOP
         MNOTE 16,'COUNT= VALUE MUST BE NUMERIC - FOUND ''&COUNT'''
         MEXIT
.ERROR5A ANOP
         MNOTE 16,'MINIMUM= VALUE MUST BE NUMERIC - FOUND ''&MINIMUM'''
         MEXIT
.ERROR6  ANOP
         MNOTE 16,'ISPLINK= VALUE MUST BE YES OR NO - FOUND ''&ISPLINK(*
               1)'''
         MEXIT
.ERROR7  ANOP
         MNOTE 16,'ISPEXEC= VALUE MUST BE YES OR NO - FOUND ''&ISPEXEC(*
               1)'''
         MEXIT
.ERROR8  ANOP
         MNOTE 16,'FULL= VALUE MUST BE YES OR NO - FOUND ''&FULL'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'COUNT= COUNT IS NOT LARGE ENOUGH FOR LARGEST LIST'
         MEXIT
         MEND
