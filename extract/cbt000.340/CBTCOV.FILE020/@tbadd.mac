         MACRO
&NAME    @TBADD &TABLE,                 TABLE NAME                     *
               &SAVE,                   SAVE NAME-LIST                 *
               &ORDER,                  ORDER KEYWORD                  *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBADD,&SPFDEBUG,&SPFVAR,&SPFORDER,&SPFNULLC
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
         AIF   ('&ORDER' NE '' AND                                     *
               '&ORDER' NE 'ORDER').ERR1
         AIF   ('&TABLE' EQ '').ERR3
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERR2
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
         AIF   ('&MF(2)' NE '').BYP15
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP17
.BYP15   ANOP
&TEMP    SETC  '&MF(2)'
.BYP17   ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBADD).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    AIF   ('&ORDER' NE '' AND NOT &SPFORDER).GENLIT
         AIF   ('&SAVE' EQ '').CNT2
         AIF   ('&SAVE'(1,1) EQ '''').GENLIT
.CNT2    AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBADD).BYP20
TBADD    DC    CL8'TBADD'               ISPF KEYWORD
&SPFTBADD SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP40
         AIF   ('&TABLE'(1,1) NE '''').BYP40
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP40   ANOP
         AIF   ('&SAVE' EQ '').BYP50
         AIF   ('&SAVE'(1,1) NE '''').BYP50
SPFS&NDX DC    C&SAVE                   GENERATED NAME-LIST
.BYP50   ANOP
         AIF   ('&ORDER' EQ '').BYP60
         AIF   (&SPFORDER).BYP60
ORDER    DC    CL8'ORDER'               ISPF KEYWORD
&SPFORDER SETB  1
.BYP60   ANOP
         AIF   (&SPFNULLC).BYP70
SPFNULLC DC    CL8' '                   ISPF DUMMY PARM
&SPFNULLC SETB 1
.BYP70   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBADD                 GET ADDRESS OF TBADD KEYWORD
         AIF   ('&TABLE' EQ '').BYP100
         AIF   ('&TABLE'(1,1) EQ '''').BYP110
         LA    15,&TABLE                GET ADDRESS OF TABLE
         AGO   .BYP120
.BYP110  ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
         AGO   .BYP120
.BYP100  ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
.BYP120  ANOP
         AIF   ('&SAVE' EQ '').BYP140
         AIF   ('&SAVE'(1,1) EQ '''').BYP130
         LA    0,&SAVE                  GET ADDRESS OF NAME-LIST
         AGO   .BYP150
.BYP130  ANOP
         LA    0,SPFS&NDX               GET ADDRESS OF GENERATED SAVE
         AGO   .BYP150
.BYP140  ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR NAME-LIST
.BYP150  ANOP
         AIF   ('&ORDER' EQ '').BYP170
         LA    1,ORDER                  GET ADDRESS OF ORDER
         AGO   .BYP180
.BYP170  ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR ORDER
.BYP180  ANOP
         STM   14,1,&TEMP               STORE PARAMETERS
         OI    &TEMP+12,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 4).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 4).BYPVARS
&SPFVARS SETA  4
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBADD)                 BROWSE LITERAL
         AIF   ('&TABLE' EQ '').BYP200
         AIF   ('&TABLE'(1,1) EQ '''').BYP210
         DC    A(&TABLE)                ADDRESS OF TABLE
         AGO   .BYP220
.BYP210  ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
         AGO   .BYP220
.BYP200  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE
.BYP220  ANOP
         AIF   ('&SAVE' EQ '').BYP240
         AIF   ('&SAVE'(1,1) EQ '''').BYP230
         DC    A(&SAVE)                 ADDRESS OF NAME LIST
         AGO   .BYP250
.BYP230  ANOP
         DC    A(SPFS&NDX)              ADDRESS OF GENERATED NAME LIST
         AGO   .BYP250
.BYP240  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR NAME LIST
.BYP250  ANOP
         AIF   ('&ORDER' EQ '').BYP270
         DC    A(ORDER+X'80000000')     ADDRESS OF ORDER KEYWORD
         AGO   .BYP300
.BYP270  ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR ORDER
.BYP300  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR1    ANOP
         MNOTE 16,'EXPECTED ORDER KEYWORD - FOUND ''&ORDER'''
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR3    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
