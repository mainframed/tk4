         MACRO
&LBL     @TBCREAT &TABLE,               TABLE NAME                     *
               &KEYS,                   KEYS NAME LIST                 *
               &NAME,                   NAMES NAME LIST                *
               &WRITE,                  WRITE NOWRITE KEYWORD          *
               &REP,                    REPLACE KEYWORD                *
               &LIB,                    LIBRARY                        *
               &SHARE,                  SHARE STATUS                   *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBCREATE,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLB  &SPFREPLACE,&SPFSHARE,&SPFWRITE,&SPFNOWRITE
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&LBL' EQ '').BYP10
&LBL     DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&TABLE' EQ '').ERR1
         AIF   ('&REP' NE '' AND '&REP' NE 'REPLACE').ERR2
         AIF   ('&SHARE' NE '' AND '&SHARE' NE 'SHARE').ERR3
         AIF   ('&WRITE' NE '' AND '&WRITE' NE 'WRITE' AND             *
               '&WRITE' NE 'NOWRITE').ERR4
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBCREATE).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&KEYS' EQ '').CNT2
         AIF   ('&KEYS'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&NAME' EQ '').CNT3
         AIF   ('&NAME'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&WRITE' EQ 'WRITE' AND NOT &SPFWRITE).GENLIT
         AIF   ('&WRITE' EQ 'NOWRITE' AND NOT &SPFNOWRITE).GENLIT
.CNT4    ANOP
         AIF   ('&REP' NE '' AND NOT &SPFREPLACE).GENLIT
         AIF   ('&LIB' EQ '').CNT6
         AIF   ('&LIB'(1,1) EQ '''').GENLIT
.CNT6    ANOP
         AIF   ('&SHARE' NE '' AND NOT &SPFSHARE).GENLIT
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBCREATE).BYP20
TBCREATE DC    CL8'TBCREATE'            ISPF KEYWORD
&SPFTBCREATE SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP30
         AIF   ('&TABLE'(1,1) NE '''').BYP30
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP30   ANOP
         AIF   ('&KEYS' EQ '').BYP40
         AIF   ('&KEYS'(1,1) NE '''').BYP40
SPFK&NDX DC    C&KEYS                   GENERATED KEYS NAME LIST
.BYP40   ANOP
         AIF   ('&NAME' EQ '').BYP50
         AIF   ('&NAME'(1,1) NE '''').BYP50
SPFN&NDX DC    C&NAME                   GENERATED NAMELIST
.BYP50   ANOP
         AIF   ('&WRITE' EQ '').BYP60
         AIF   ('&WRITE' NE 'WRITE' OR &SPFWRITE).BYP55
WRITE    DC    CL8'WRITE'               ISPF KEYWORD
&SPFWRITE SETB 1
.BYP55   ANOP
         AIF   ('&WRITE' NE 'NOWRITE' OR &SPFNOWRITE).BYP60
NOWRITE  DC    CL8'NOWRITE'             ISPF KEYWORD
&SPFNOWRITE SETB 1
.BYP60   ANOP
         AIF   ('&REP' EQ '').BYP70
         AIF   (&SPFREPLACE).BYP70
REPLACE  DC    CL8'REPLACE'             GENERATED REPLACE KEYWORD
.BYP70   ANOP
         AIF   ('&LIB' EQ '').BYP80
         AIF   ('&LIB'(1,1) NE '''').BYP80
SPFL&NDX DC    CL8&LIB                  GENERATED LIBRARY
.BYP80   ANOP
         AIF   ('&SHARE' EQ '').BYP90
         AIF   (&SPFSHARE).BYP90
SHARE    DC    CL8'SHARE'               GENERATED SHARE KEYWORD
.BYP90   ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBCREATE              GET ADDRESS OF TBCREATE KEYWORD
.*
         AIF   ('&TABLE' EQ '').BYP1000
         AIF   ('&TABLE'(1,1) EQ '''').BYP1010
         LA    15,&TABLE                GET ADDRESS OF TABLE NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
.BYP1050 ANOP
.*
         AIF   ('&KEYS' EQ '').BYP1100
         AIF   ('&KEYS'(1,1) EQ '''').BYP1110
         LA    0,&KEYS                  GET ADDRESS OF KEYS NAME
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR KEYS
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFK&NDX               GET ADDRESS OF GENERATED KEYS
.BYP1150 ANOP
.*
         AIF   ('&NAME' EQ '').BYP1200
         AIF   ('&NAME'(1,1) EQ '''').BYP1210
         LA    1,&NAME                  GET ADDRESS OF NAME NAME
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR NAME
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFN&NDX               GET ADDRESS OF GENERATED NAME
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&WRITE' EQ '').BYP1300
         LA    14,&WRITE                GET ADDRESS OF WRITE NAME
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR WRITE
.BYP1350 ANOP
.*
         AIF   ('&REP' EQ '').BYP1400
         LA    15,REPLACE               GET ADDRESS OF REPLACE KEYWORD
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR REPLACE
.BYP1450 ANOP
.*
         AIF   ('&LIB' EQ '').BYP1500
         AIF   ('&LIB'(1,1) EQ '''').BYP1510
         LA    0,&LIB                   GET ADDRESS OF LIB NAME
         AGO   .BYP1550
.BYP1500 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR LIB
         AGO   .BYP1550
.BYP1510 ANOP
         LA    0,SPFL&NDX               GET ADDRESS OF GENERATED LIB
.BYP1550 ANOP
.*
         AIF   ('&SHARE' EQ '').BYP1600
         LA    1,SHARE                  GET ADDRESS OF SHARE NAME
         AGO   .BYP1650
.BYP1600 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR SHARE
         AGO   .BYP1650
.BYP1650 ANOP
.*
         STM   14,1,&TEMP+16            STORE PARAMETERS INTO LIST
         OI    &TEMP+28,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 8).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 8).BYPVARS
&SPFVARS SETA  8
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBCREATE)              TBCREATE LITERAL
.*
         AIF   ('&TABLE' EQ '').BYP2000
         AIF   ('&TABLE'(1,1) EQ '''').BYP2010
         DC    A(&TABLE)                ADDRESS OF TABLE NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
.BYP2050 ANOP
.*
         AIF   ('&KEYS' EQ '').BYP2100
         AIF   ('&KEYS'(1,1) EQ '''').BYP2110
         DC    A(&KEYS)                 ADDRESS OF KEYS NAME
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR KEYS NAME
         AGO   .BYP2150
.BYP2110 ANOP
         DC    A(SPFK&NDX)              ADDRESS OF GENERATED KEYS
.BYP2150 ANOP
.*
         AIF   ('&NAME' EQ '').BYP2200
         AIF   ('&NAME'(1,1) EQ '''').BYP2210
         DC    A(&NAME)                 ADDRESS OF NAME LIST
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR NAME
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFN&NDX)              ADDRESS OF GENERATED NAME
.BYP2250 ANOP
.*
         AIF   ('&WRITE' EQ '').BYP2300
         DC    A(&WRITE)                ADDRESS OF WRITE KEYWORD
         AGO   .BYP2350
.BYP2300 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR WRITE
.BYP2350 ANOP
.*
         AIF   ('&REP' EQ '').BYP2400
         DC    A(REPLACE)               ADDRESS OF REPLACE KEYWORD
         AGO   .BYP2450
.BYP2400 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR REP
.BYP2450 ANOP
.*
         AIF   ('&LIB' EQ '').BYP2500
         AIF   ('&LIB'(1,1) EQ '''').BYP2510
         DC    A(&LIB)                  ADDRESS OF LIB NAME
         AGO   .BYP2550
.BYP2500 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR LIB
         AGO   .BYP2550
.BYP2510 ANOP
         DC    A(SPFL&NDX)              ADDRESS OF GENERATED LIB
.BYP2550 ANOP
.*
         AIF   ('&SHARE' EQ '').BYP2800
         DC    A(SHARE+X'80000000')     ADDRESS OF SHARE KEYWORD
         AGO   .BYP2850
.BYP2800 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR SHARE
.BYP2850 ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'REPLACE KEYWORD EXPECTED - FOUND ''&REP'''
         MEXIT
.ERR3    ANOP
         MNOTE 16,'SHARE KEYWORD EXPECTED - FOUND ''&SHARE'''
         MEXIT
.ERR4    ANOP
         MNOTE 16,'WRITE/NOWRITE KEYWORD EXPECTED - FOUND ''&WRITE'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
