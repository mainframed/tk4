         MACRO
&LABEL   LMMFIND &DATAID=,              DATAID NAME                    *
               &MEMBER=,                VARIABLE FOR MEMBER            *
               &LOCK=,                  LOCK OPTION                    *
               &LRECL=,                 VARIABLE FOR LRECL             *
               &RECFM=,                 VARIABLE FOR RECFM             *
               &GROUP=,                 VARIABLE FOR GROUP             *
               &STATS=,                 VARIABLE FOR STATS             *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFLMMFIND              GLOBAL FOR LMMFIND LITERAL
         GBLB  &SPFNULLC                DUMMY PARAMTER FOR CALL
         GBLB  &SPFDEBUG                DEBUG MODE
         GBLB  &SPFVAR                  NUMBER OF REQUIRED FULLWORDS
         GBLA  &SPFVARS                 NUMBER OF FULLWORDS
         GBLA  &SPFVARC                 PREVIOUS FULLWORDS USED
         LCLC  &NDX                     USED IN PLACE OF SYSNDX
         LCLC  &TEMP                    USED AS TEMPORARY VARIABLE
.*
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&LABEL' EQ '').BYP10
&LABEL   DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&DATAID' EQ '').ERR1
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFLMMFIND).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&DATAID'(1,1) EQ '''').GENLIT
.*
         AIF   ('&LOCK' EQ '').CNT1
         AIF   ('&LOCK'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&MEMBER' EQ '').CNT2
         AIF   ('&MEMBER'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&LRECL' EQ '').CNT3
         AIF   ('&LRECL'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&RECFM' EQ '').CNT4
         AIF   ('&RECFM'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AIF   ('&GROUP' EQ '').CNT5
         AIF   ('&GROUP'(1,1) EQ '''').GENLIT
.CNT5    ANOP
         AIF   ('&STATS' EQ '').CNT6
         AIF   ('&STATS'(1,1) EQ '''').GENLIT
.CNT6    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFNULLC).BYP001
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP001  ANOP
.*
.*
         AIF   (&SPFLMMFIND).BYP20
LMMFIND  DC    CL8'LMMFIND'             ISPF KEYWORD
&SPFLMMFIND SETB 1
.BYP20   ANOP
.*
.*
         AIF   ('&DATAID' EQ '').BYP30
         AIF   ('&DATAID'(1,1) NE '''').BYP30
SPFZ&NDX DC    CL8&DATAID               GENERATED DATAID NAME
.BYP30   ANOP
.*
.*
         AIF   ('&LOCK' EQ '').BYP40
         AIF   ('&LOCK'(1,1) NE '''').BYP40
SPFB&NDX DC    CL8&LOCK                 LOCK OPTION
.BYP40   ANOP
.*
.*
         AIF   ('&MEMBER' EQ '').BYP50
         AIF   ('&MEMBER'(1,1) NE '''').BYP50
SPFA&NDX DC    CL8&MEMBER               MEMBER NAME
.BYP50   ANOP
.*
.*
         AIF   ('&LRECL' EQ '').BYP60
         AIF   ('&LRECL'(1,1) NE '''').BYP60
SPFC&NDX DC    CL8&LRECL                LRECL VARIABLE NAME
.BYP60   ANOP
         AIF   ('&RECFM' EQ '').BYP70
         AIF   ('&RECFM'(1,1) NE '''').BYP70
SPFD&NDX DC    CL8&RECFM                RECFM VARIABLE NAME
.BYP70   ANOP
         AIF   ('&GROUP' EQ '').BYP80
         AIF   ('&GROUP'(1,1) NE '''').BYP80
SPFE&NDX DC    CL8&GROUP                GROUP VARIABLE NAME
.BYP80   ANOP
         AIF   ('&STATS' EQ '').BYP90
         AIF   ('&STATS'(1,1) NE '''').BYP90
SPFF&NDX DC    CL8&STATS                STATS OPTION
.BYP90   ANOP
.*
.*
.*
.*
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
.*
.*
         LA    14,LMMFIND               GET ADDRESS OF LMMFIND KEYWORD
.*
.*
         AIF   ('&DATAID'(1,1) EQ '''').BYP1010
         LA    15,&DATAID               GET ADDRESS OF DATAID NAME
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFZ&NDX              GET ADDRESS OF GENERATED DATAID
.BYP1050 ANOP
.*
.*
         AIF   ('&MEMBER' EQ '').BYP1200
         AIF   ('&MEMBER'(1,1) EQ '''').BYP1210
         LA    0,&MEMBER                GET ADDRESS OF MEMBER VARIABLE
         AGO   .BYP1250
.BYP1200 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR MEMBER
         AGO   .BYP1250
.BYP1210 ANOP
         LA    0,SPFA&NDX               GET ADDRESS OF GENNED MEMBER
.BYP1250 ANOP
.*
.*
         AIF   ('&LOCK' EQ '').BYP1100
         AIF   ('&LOCK'(1,1) EQ '''').BYP1110
         LA    1,&LOCK                  GET ADDRESS OF &LOCK
         AGO   .BYP1150
.BYP1100 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR LOCK
         AGO   .BYP1150
.BYP1110 ANOP
         LA    1,SPFB&NDX               GENNED LOCK
.BYP1150 ANOP
.*
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
.*
.*
.*
         AIF   ('&LRECL' EQ '').BYP1300
         AIF   ('&LRECL'(1,1) EQ '''').BYP1310
         LA    14,&LRECL                GET ADDRESS OF LRECL VARIABLE
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR LRECL
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFC&NDX              GET ADDRESS OF GENNED LRECL
.BYP1350 ANOP
.*
.*
         AIF   ('&RECFM' EQ '').BYP1400
         AIF   ('&RECFM'(1,1) EQ '''').BYP1410
         LA    15,&RECFM                GET ADDRESS OF RECFM VARIABLE
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR RECFM
         AGO   .BYP1450
.BYP1410 ANOP
         LA    15,SPFD&NDX              GET ADDRESS OF GENNED RECFM
.BYP1450 ANOP
.*
.*
         AIF   ('&GROUP' EQ '').BYP1500
         AIF   ('&GROUP'(1,1) EQ '''').BYP1510
         LA    0,&GROUP                 GET ADDRESS OF GROUP VARIABLE
         AGO   .BYP1550
.BYP1500 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR GROUP
         AGO   .BYP1550
.BYP1510 ANOP
         LA    0,SPFE&NDX               GET ADDRESS OF GENNED GROUP
.BYP1550 ANOP
.*
.*
         AIF   ('&STATS' EQ '').BYP1600
         AIF   ('&STATS'(1,1) EQ '''').BYP1610
         LA    1,&STATS                 GET ADDRESS OF STATS KEYWORD
         AGO   .BYP1650
.BYP1600 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR STATS
         AGO   .BYP1650
.BYP1610 ANOP
         LA    1,SPFF&NDX               GET ADDRESS OF GENNED STATS
.BYP1650 ANOP
.*
.*
         STM   14,1,&TEMP+16            STORE PARAMETERS INTO LIST
.*
.*
.*
         OI    &TEMP+28,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 8).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 8).BYPVARS
&SPFVARS SETA  8
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
.*
.*
         DC    A(LMMFIND)               GET ADDRESS OF LMMFIND KEYWORD
.*
.*
         AIF   ('&DATAID'(1,1) EQ '''').BY1010
         DC    A(&DATAID)               GET ADDRESS OF DATAID NAME
         AGO   .BY1050
.BY1010 ANOP
         DC    A(SPFZ&NDX)              GET ADDRESS OF GENERATED DATAID
.BY1050 ANOP
.*
.*
         AIF   ('&MEMBER' EQ '').BY1200
         AIF   ('&MEMBER'(1,1) EQ '''').BY1210
         DC    A(&MEMBER)               GET ADDRESS OF MEMBER VARIABLE
         AGO   .BY1250
.BY1200 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR MEMBER
         AGO   .BY1250
.BY1210 ANOP
         DC    A(SPFA&NDX)              GET ADDRESS OF GENNED MEMBER
.BY1250 ANOP
.*
.*
         AIF   ('&LOCK' EQ '').BY1100
         AIF   ('&LOCK'(1,1) EQ '''').BY1110
         DC    A(&LOCK)                 GET ADDRESS OF &LOCK
         AGO   .BY1150
.BY1100 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR LOCK
         AGO   .BY1150
.BY1110 ANOP
         DC    A(SPFB&NDX)              GENNED LOCK
.BY1150 ANOP
.*
.*
         AIF   ('&LRECL' EQ '').BY1300
         AIF   ('&LRECL'(1,1) EQ '''').BY1310
         DC    A(&LRECL)                GET ADDRESS OF LRECL VARIABLE
         AGO   .BY1350
.BY1300 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR LRECL
         AGO   .BY1350
.BY1310 ANOP
         DC    A(SPFC&NDX)              GET ADDRESS OF GENNED LRECL
.BY1350 ANOP
.*
.*
         AIF   ('&RECFM' EQ '').BY1400
         AIF   ('&RECFM'(1,1) EQ '''').BY1410
         DC    A(&RECFM)                GET ADDRESS OF RECFM VARIABLE
         AGO   .BY1450
.BY1400 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR RECFM
         AGO   .BY1450
.BY1410 ANOP
         DC    A(SPFD&NDX)              GET ADDRESS OF GENNED RECFM
.BY1450 ANOP
.*
.*
         AIF   ('&GROUP' EQ '').BY1500
         AIF   ('&GROUP'(1,1) EQ '''').BY1510
         DC    A(&GROUP)                GET ADDRESS OF GROUP VARIABLE
         AGO   .BY1550
.BY1500 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR GROUP
         AGO   .BY1550
.BY1510 ANOP
         DC    A(SPFE&NDX)              GET ADDRESS OF GENNED GROUP
.BY1550 ANOP
.*
.*
         AIF   ('&STATS' EQ '').BY1600
         AIF   ('&STATS'(1,1) EQ '''').BY1610
         DC    A(&STATS+X'80000000')    GET ADDRESS OF STATS VARIABLE
         AGO   .BY1650
.BY1600 ANOP
         DC    A(SPFNULLC+X'80000000')  GET DUMMY PARM FOR STATS
         AGO   .BY1650
.BY1610 ANOP
         DC    A(SPFF&NDX+X'80000000')  GET ADDRESS OF GENNED STATS
.BY1650 ANOP
.*
.*
SPFE&NDX DS    0H
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'DATAID NAME IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
