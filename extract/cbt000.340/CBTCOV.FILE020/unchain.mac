         MACRO
&NAME    UNCHAIN &ANCHOR,&SCOPE=
         LCLC  &C
&C       SETC  '&ANCHOR'
         AIF   ('&ANCHOR'(1,1) NE '(').GO
&C       SETC  '0&ANCHOR'
.GO      ANOP
&NAME    LM    0,1,&C         LOAD POINTER TO OLD 1ST ELEMENT
         LTR   1,1            IF NO ELEMENTS,
         AIF   (T'&SCOPE EQ 'O').SCOPE1
         AIF   ('&SCOPE' NE 'ALL').ERRSCOP
         BZ    *+18           THEN SKIP UPDATE
         SR    15,15          INDICATE NO ELEMENTS IN QUEUE
         AGO   .SCOPEX
.SCOPE1  BZ    *+20           THEN SKIP UPDATE
         L     15,0(0,1)      OLD 2ND ELEMENT BECOMES NEW 1ST ELEMENT
.SCOPEX  LR    14,0           CHANGE COUNT FIELD IN ORDER TO
         BCTR  14,0           SERIALIZE WITH OTHER UNCHAINS
         CDS   0,14,&C        POINT CHAIN TO NEW 1ST ELEMENT
         AIF   ('&SCOPE' EQ 'ALL').ALL
         BNE   *-18           IF CONTENTION, RETRY
         MEXIT
.ALL     BNE   *-16           IF CONTENTION, RETRY
         MEXIT
.ERRSCOP MNOTE 8,'SCOPE=&SCOPE INVALID. SCOPE IGNORED.'
         AGO   .SCOPE1
         MEND
