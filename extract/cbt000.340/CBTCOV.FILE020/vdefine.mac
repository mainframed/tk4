         MACRO
&LBL     VDEFINE &NAME,                 NAME LIST                      *
               &VAR,                    POINTER TO STORAGE             *
               &FOR,                    FORMAT OF DATA                 *
               &LEN,                    LENGTH OF FIELD                *
               &OPT,                    OPTIONS LIST                   *
               &USER,                   USER DATA FOR EXIT             *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFVDEFINE,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLB  &SPFCHAR,&SPFFIXED,&SPFBIT,&SPFHEX,&SPFUSER
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&LBL' EQ '').BYP10
&LBL     DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&NAME' EQ '').ERR1
         AIF   ('&VAR' EQ '').ERR2
         AIF   ('&VAR'(1,1) EQ '''').ERR3
         AIF   ('&FOR' NE '' AND                                       *
               '&FOR' NE 'CHAR' AND                                    *
               '&FOR' NE 'BIT' AND                                     *
               '&FOR' NE 'FIXED' AND                                   *
               '&FOR' NE 'USER' AND                                    *
               '&FOR' NE 'HEX').ERR4
         AIF   ('&LEN' EQ '').ERR5
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFVDEFINE).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&NAME' EQ '').CNT1
         AIF   ('&NAME'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&FOR' EQ 'CHAR' AND NOT &SPFCHAR).GENLIT
         AIF   ('&FOR' EQ 'BIT' AND NOT &SPFBIT).GENLIT
         AIF   ('&FOR' EQ 'FIXED' AND NOT &SPFFIXED).GENLIT
         AIF   ('&FOR' EQ 'USER' AND NOT &SPFUSER).GENLIT
         AIF   ('&FOR' EQ 'HEX' AND NOT &SPFHEX).GENLIT
         AIF   ('&LEN' EQ '').CNT3
         AIF   (T'LEN EQ 'N').GENLIT
.CNT3    ANOP
         AIF   ('&OPT' EQ '').CNT4
         AIF   ('&OPT'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFVDEFINE).BYP20
VDEFINE  DC    CL8'VDEFINE'             ISPF KEYWORD
&SPFVDEFINE SETB 1
.BYP20   ANOP
         AIF   ('&NAME' EQ '').BYP30
         AIF   ('&NAME'(1,1) NE '''').BYP30
SPFN&NDX DC    C&NAME                   GENERATED NAME LIST
.BYP30   ANOP
         AIF   ('&FOR' EQ 'CHAR' AND &SPFCHAR).BYP40
         AIF   ('&FOR' EQ 'FIXED' AND &SPFFIXED).BYP40
         AIF   ('&FOR' EQ 'BIT' AND &SPFBIT).BYP40
         AIF   ('&FOR' EQ 'USER' AND &SPFUSER).BYP40
         AIF   ('&FOR' EQ 'HEX' AND &SPFHEX).BYP40
&FOR     DC    CL8'&FOR'                ISPF KEYWORD
         AIF   ('&FOR' NE 'CHAR').BYP32
&SPFCHAR SETB  1
.BYP32   ANOP
         AIF   ('&FOR' NE 'FIXED').BYP33
&SPFFIXED SETB  1
.BYP33   ANOP
         AIF   ('&FOR' NE 'CHAR').BYP34
&SPFCHAR SETB  1
.BYP34   ANOP
         AIF   ('&FOR' NE 'BIT').BYP35
&SPFBIT SETB  1
.BYP35   ANOP
         AIF   ('&FOR' NE 'USER').BYP36
&SPFUSER SETB  1
.BYP36   ANOP
         AIF   ('&FOR' NE 'HEX').BYP40
&SPFHEX  SETB  1
.BYP40   ANOP
         AIF   ('&LEN' EQ '').BYP50
         AIF   (T'&LEN NE 'N').BYP50
SPFL&NDX DC    F'&LEN'                  GENERATED LENGTH
.BYP50   ANOP
         AIF   ('&OPT' EQ '').BYP60
         AIF   ('&OPT'(1,1) NE '''').BYP60
SPFO&NDX DC    C&OPT                    GENERATED OPTION LIST
.BYP60   ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,VDEFINE               GET ADDRESS OF VDEFINE KEYWORD
.*
         AIF   ('&NAME' EQ '').BYP1000
         AIF   ('&NAME'(1,1) EQ '''').BYP1010
         LA    15,&NAME                 GET ADDRESS OF NAMELIST
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR NAMELIST
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFN&NDX              GET ADDRESS OF GENERATED NAME
.BYP1050 ANOP
.*
         LA    0,&VAR                   GET ADDRESS OF STORAGE
.*
         LA    1,&FOR                   GET ADDRESS OF FORMAT
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   (T'&LEN EQ 'N').BYP1310
         LA    14,&LEN                  GET ADDRESS OF LENGTH
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFL&NDX              GET ADDRESS OF GENERATED LEN
.BYP1350 ANOP
.*
         AIF   ('&OPT' EQ '').BYP1400
         AIF   ('&OPT'(1,1) EQ '''').BYP1410
         LA    15,&OPT                  GET ADDRESS OF OPTION LIST
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR OPTION
         AGO   .BYP1450
.BYP1410 ANOP
         LA    15,SPFO&NDX              GET ADDRESS OF GENERATED OPT
.BYP1450 ANOP
.*
         AIF   ('&USER' EQ '').BYP1500
         LA    0,&USER                  GET ADDRESS OF USER DATA
         AGO   .BYP1550
.BYP1500 ANOP
         LA    0,SPFNULLC               DUMMY USER DATA
.BYP1550 ANOP
.*
         STM   14,0,&TEMP+16            STORE PARAMETERS INTO LIST
         OI    &TEMP+24,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 7).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 7).BYPVARS
&SPFVARS SETA  7
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(VDEFINE)               VDEFINE LITERAL
.*
         AIF   ('&NAME' EQ '').BYP2000
         AIF   ('&NAME'(1,1) EQ '''').BYP2010
         DC    A(&NAME)                 ADDRESS OF NAMELIST
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR NAMELIST
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFN&NDX)              ADDRESS OF GENERATED NAMELIST
.BYP2050 ANOP
.*
         DC    A(&VAR)                  ADDRESS OF STORAGE AREA
.*
         DC    A(&FOR)                  ADDRESS OF FORMAT TYPE
.*
         AIF   (T'&LEN EQ 'N').BYP2310
         DC    A(&LEN)                  ADDRESS OF LENGTH
         AGO   .BYP2350
.BYP2310 ANOP
         DC    A(SPFL&NDX)              ADDRESS OF GENERATED LENGTH
.BYP2350 ANOP
.*
         AIF   ('&OPT' EQ '').BYP2400
         AIF   ('&OPT'(1,1) EQ '''').BYP2410
         DC    A(&OPT)                  ADDRESS OF OPT NAME
         AGO   .BYP2450
.BYP2400 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR OPT
         AGO   .BYP2450
.BYP2410 ANOP
         DC    A(SPFO&NDX)              ADDRESS OF GENERATED OPT
.BYP2450 ANOP
.*
         AIF   ('&USER' EQ '').BYP2800
         DC    A(&USER+X'80000000')     ADDRESS OF ROWID NAME
         AGO   .BYP2850
.BYP2800 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR ROWID
.BYP2850 ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'NAME LIST IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'AN ADDRESS POINTING TO STORAGE MAPPING VARIABLES IS *
               REQUIRED'
         MEXIT
.ERR3    ANOP
         MNOTE 16,'AN ADDRESS POINTING TO STORAGE MAPPING VARIABLES IS *
               REQUIRED - MUST NOT BE A LITERAL'
         MEXIT
.ERR4    ANOP
         MNOTE 16,'INVALID FORMAT SPECIFIED'
         MEXIT
.ERR5    ANOP
         MNOTE 16,'LENGTH FIELD IS REQUIRED'
         MEXIT
.ERR6    ANOP
         MNOTE 16,'USER DATA FIELD MUST NOT BE A LITERAL'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
