         MACRO
&NAME    FTERASE &MEM,                  MEMBER NAME (POSITIONAL)       *
               &P2,                                                    *
               &LIBRARY=,               LIBRARY NAME                   *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFFTERASE,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
         GBLC  &SPFLVL
         AIF   ('&SPFLVL' NE '1').NL1
&NAME    @FTERASE &MEM,&P2,LOAD=&LOAD,MF=&MF
         MEXIT
.NL1     ANOP
         AIF   ('&P2' NE '').LVLERR
         AGO   .NL2
.LVLERR  ANOP
         MNOTE 12,'POSITIONAL PARAMETER FOUND - NOT ALLOWED'
         MEXIT
.NL2     ANOP
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
         AIF   ('&MEM' EQ '').ERR1
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERR2
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
         AIF   ('&MF(2)' NE '').BYP15
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP17
.BYP15   ANOP
&TEMP    SETC  '&MF(2)'
.BYP17   ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFFTERASE).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&MEM' EQ '').CNT1
         AIF   ('&MEM'(1,1) EQ '''').GENLIT
.CNT1    AIF   ('&LIBRARY' EQ '').CNT2
         AIF   ('&LIBRARY'(1,1) EQ '''').GENLIT
.CNT2    AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFFTERASE).BYP20
FTERASE  DC    CL8'FTERASE'             ISPF KEYWORD
&SPFFTERASE SETB 1
.BYP20   ANOP
         AIF   ('&MEM' EQ '').BYP40
         AIF   ('&MEM'(1,1) NE '''').BYP40
SPFM&NDX DC    CL8&MEM                  GENERATED NAME
.BYP40   ANOP
         AIF   ('&LIBRARY' EQ '').BYP50
         AIF   ('&LIBRARY'(1,1) NE '''').BYP50
SPFL&NDX DC    CL8&LIBRARY              GENERATED LIBRARY
.BYP50   ANOP
         AIF   (&SPFNULLC).BYP70
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP70   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,FTERASE               GET ADDRESS OF FTERASE KEYWORD
         AIF   ('&MEM' EQ '').BYP100
         AIF   ('&MEM'(1,1) EQ '''').BYP110
         LA    15,&MEM                  GET ADDRESS OF MEMBER
         AGO   .BYP120
.BYP110  ANOP
         LA    15,SPFM&NDX              GET ADDRESS OF GENERATED MEMBER
         AGO   .BYP120
.BYP100  ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR MEMBER
.BYP120  ANOP
         AIF   ('&LIBRARY' EQ '').BYP140
         AIF   ('&LIBRARY'(1,1) NE '''').BYP130
         LA    0,SPFL&NDX               GET ADDRESS OF GENERATED LIB
         AGO   .BYP150
.BYP130  ANOP
         LA    0,&LIBRARY               GET ADDRESS OF LIBRARY
         AGO   .BYP150
.BYP140  ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR LIBRARY
.BYP150  ANOP
         STM   14,0,&TEMP               STORE PARAMETERS
         OI    &TEMP+8,X'80'            MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 3).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 3).BYPVARS
&SPFVARS SETA  3
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(FTERASE)               BROWSE LITERAL
         AIF   ('&MEM' EQ '').BYP200
         AIF   ('&MEM'(1,1) EQ '''').BYP210
         DC    A(&MEM)                  ADDRESS OF MEMBER
         AGO   .BYP220
.BYP210  ANOP
         DC    A(SPFM&NDX)              ADDRESS OF GENERATED MEMBER
         AGO   .BYP220
.BYP200  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR MEMBER
.BYP220  ANOP
         AIF   ('&LIBRARY' EQ '').BYP240
         AIF   ('&LIBRARY'(1,1) NE '''').BYP230
         DC    A(SPFL&NDX+X'80000000')  ADDRESS OF GENERATED LIBRARY
         AGO   .BYP250
.BYP230  ANOP
         DC    A(&LIBRARY+X'80000000')  ADDRESS OF LIBRARY
         AGO   .BYP250
.BYP240  ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR LIBRARY
.BYP250  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR1    ANOP
         MNOTE 16,'MEMBER NAME REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
