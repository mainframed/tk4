         MACRO
&NAME    EDREC &TYPE,                   TYPE OF RECOVERY               *
               &PASS,                   PASSWORD                       *
               &DATA,                   DATA ID                        *
               &CMD,                    COMMAND NAME                   *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFEDREC,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&TYPE' EQ '').ERR1
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFEDREC).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TYPE' EQ '').CNT1
         AIF   ('&TYPE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&PASS' EQ '').CNT2
         AIF   ('&PASS'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&DATA' EQ '').CNT3
         AIF   ('&DATA'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&CMD' EQ '').CNT4
         AIF   ('&CMD'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFEDREC).BYP20
EDREC    DC    CL8'EDREC'               ISPF KEYWORD
&SPFEDREC SETB 1
.BYP20   ANOP
         AIF   ('&TYPE' EQ '').BYP30
         AIF   ('&TYPE'(1,1) NE '''').BYP30
SPFT&NDX DC    CL8&TYPE                 GENERATED RECOVERY TYPE
.BYP30   ANOP
         AIF   ('&PASS' EQ '').BYP40
         AIF   ('&PASS'(1,1) NE '''').BYP40
SPFP&NDX DC    CL8&PASS                 GENERATED PASSWORD
.BYP40   ANOP
         AIF   ('&DATA' EQ '').BYP50
         AIF   ('&DATA'(1,1) NE '''').BYP50
SPFD&NDX DC    CL8&DATA                 GENERATED DATA ID
.BYP50   ANOP
         AIF   ('&CMD' EQ '').BYP60
         AIF   ('&CMD'(1,1) NE '''').BYP60
SPFC&NDX DC    CL8&CMD                  GENERATED COMMAND NAME
.BYP60   ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,EDREC                 GET ADDRESS OF EDREC KEYWORD
.*
         AIF   ('&TYPE' EQ '').BYP1000
         AIF   ('&TYPE'(1,1) EQ '''').BYP1010
         LA    15,&TYPE                 GET ADDRESS OF TYPE NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TYPE
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TYPE
.BYP1050 ANOP
.*
         AIF   ('&PASS' EQ '').BYP1100
         AIF   ('&PASS'(1,1) EQ '''').BYP1110
         LA    0,&PASS                  GET ADDRESS OF PASSWORD
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR PASSWORD
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFP&NDX               GET ADDRESS OF GENERATED PASS
.BYP1150 ANOP
.*
         AIF   ('&DATA' EQ '').BYP1200
         AIF   ('&DATA'(1,1) EQ '''').BYP1210
         LA    1,&DATA                  GET ADDRESS OF DATA NAME
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR DATA
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFD&NDX               GET ADDRESS OF GENERATED DATA
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&CMD' EQ '').BYP1300
         AIF   ('&CMD'(1,1) EQ '''').BYP1310
         LA    14,&CMD                  GET ADDRESS OF CMD NAME
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR CMD
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFC&NDX              GET ADDRESS OF GENERATED CMD
.BYP1350 ANOP
         ST    14,&TEMP+16              STORE PARAMETERS INTO LIST
         OI    &TEMP+16,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 5).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 5).BYPVARS
&SPFVARS SETA  5
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(EDREC)                 EDREC LITERAL
.*
         AIF   ('&TYPE' EQ '').BYP2000
         AIF   ('&TYPE'(1,1) EQ '''').BYP2010
         DC    A(&TYPE)                 ADDRESS OF TYPE NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TYPE NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TYPE
.BYP2050 ANOP
.*
         AIF   ('&PASS' EQ '').BYP2100
         AIF   ('&PASS'(1,1) EQ '''').BYP2110
         DC    A(&PASS)                 ADDRESS OF PASS NAME
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR PASS NAME
         AGO   .BYP2150
.BYP2110 ANOP
         DC    A(SPFP&NDX)              ADDRESS OF GENERATED PASS
.BYP2150 ANOP
.*
         AIF   ('&DATA' EQ '').BYP2200
         AIF   ('&DATA'(1,1) EQ '''').BYP2210
         DC    A(&DATA)                 ADDRESS OF DATA NAME
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR DATA
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFD&NDX)              ADDRESS OF GENERATED DATA
.BYP2250 ANOP
.*
         AIF   ('&CMD' EQ '').BYP2800
         AIF   ('&CMD'(1,1) EQ '''').BYP2810
         DC    A(&CMD+X'80000000')      ADDRESS OF CMD NAME
         AGO   .BYP2850
.BYP2800 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR CMD
         AGO   .BYP2850
.BYP2810 ANOP
         DC    A(SPFC&NDX+X'80000000')  ADDRESS OF GENERATED CMD
.BYP2850 ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'TYPE OF EDIT RECOVERY IS MISSING'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
