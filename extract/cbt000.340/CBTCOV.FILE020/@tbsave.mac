         MACRO
&NAME    @TBSAVE &TABLE,                TABLE NAME                     *
               &NEWCOPY,                NEWCOPY OR REPLCOPY            *
               &ALT,                    ALTERNATE NAME FOR SAVE        *
               &PER,                    PERCENT                        *
               &LIB,                    LIBRARY                        *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBSAVE,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLB  &SPFNEWCOPY,&SPFREPLCOPY
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&TABLE' EQ '').ERR1
         AIF   ('&NEWCOPY' NE '' AND '&NEWCOPY' NE 'NEWCOPY' AND       *
               '&NEWCOPY' NE 'REPLCOPY').ERR2
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBSAVE).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&NEWCOPY' EQ 'NEWCOPY' AND NOT &SPFNEWCOPY).GENLIT
         AIF   ('&NEWCOPY' EQ 'REPLCOPY' AND NOT &SPFREPLCOPY).GENLIT
         AIF   ('&ALT' EQ '').CNT3
         AIF   ('&ALT'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   (T'&PER EQ 'N').GENLIT
         AIF   ('&LIB' EQ '').CNT5
         AIF   ('&LIB'(1,1) EQ '''').GENLIT
.CNT5    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBSAVE).BYP20
TBSAVE   DC    CL8'TBSAVE'              ISPF KEYWORD
&SPFTBSAVE SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP30
         AIF   ('&TABLE'(1,1) NE '''').BYP30
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP30   ANOP
         AIF   ('&NEWCOPY' EQ '').BYP40
         AIF   ('&NEWCOPY' NE 'NEWCOPY' OR &SPFNEWCOPY).BYP35
NEWCOPY  DC    CL8'NEWCOPY'             ISPF KEYWORD
&SPFNEWCOPY SETB 1
.BYP35   ANOP
         AIF   ('&NEWCOPY' NE 'REPLCOPY' OR &SPFREPLCOPY).BYP40
REPLCOPY DC    CL8'REPLCOPY'            ISPF KEYWORD
&SPFREPLCOPY SETB 1
.BYP40   ANOP
         AIF   ('&ALT' EQ '').BYP50
         AIF   ('&ALT'(1,1) NE '''').BYP50
SPFA&NDX DC    CL8&ALT                  GENERATED ALT NAME
.BYP50   ANOP
         AIF   (T'&PER NE 'N').BYP60
SPFP&NDX DC    F'&PER'                  GENERATED PERCENTAGE
.BYP60   ANOP
         AIF   ('&LIB' EQ '').BYP70
         AIF   ('&LIB'(1,1) NE '''').BYP70
SPFL&NDX DC    CL8&LIB                  GENERATED LIBRARY
.BYP70   ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBSAVE                GET ADDRESS OF TBSAVE KEYWORD
.*
         AIF   ('&TABLE' EQ '').BYP1000
         AIF   ('&TABLE'(1,1) EQ '''').BYP1010
         LA    15,&TABLE                GET ADDRESS OF TABLE NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
.BYP1050 ANOP
.*
         AIF   ('&NEWCOPY' EQ '').BYP1100
         LA    0,&NEWCOPY               GET ADDRESS OF COPY NAME
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR COPY
.BYP1150 ANOP
.*
         AIF   ('&ALT' EQ '').BYP1200
         AIF   ('&ALT'(1,1) EQ '''').BYP1210
         LA    1,&ALT                   GET ADDRESS OF ALT NAME
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR ALT
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFA&NDX               GET ADDRESS OF GENERATED ALT
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&PER' EQ '').BYP1300
         AIF   (T'&PER EQ 'N').BYP1310
         LA    14,&PER                  GET ADDRESS OF PERCENT
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR PERCENT
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFP&NDX              GET ADDRESS OF GENERATED PERCT
.BYP1350 ANOP
.*
         AIF   ('&LIB' EQ '').BYP1400
         AIF   ('&LIB'(1,1) EQ '''').BYP1410
         LA    15,&LIB                  GET ADDRESS OF LIB NAME
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR LIB
         AGO   .BYP1450
.BYP1410 ANOP
         LA    15,SPFL&NDX              GET ADDRESS OF GENERATED LIB
.BYP1450 ANOP
.*
         STM   14,15,&TEMP+16           STORE PARAMETERS INTO LIST
         OI    &TEMP+20,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 6).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 6).BYPVARS
&SPFVARS SETA  6
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBSAVE)                TBSAVE LITERAL
.*
         AIF   ('&TABLE' EQ '').BYP2000
         AIF   ('&TABLE'(1,1) EQ '''').BYP2010
         DC    A(&TABLE)                ADDRESS OF TABLE NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
.BYP2050 ANOP
.*
         AIF   ('&NEWCOPY' EQ '').BYP2100
         DC    A(&NEWCOPY)              ADDRESS OF COPY NAME
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR COPY NAME
.BYP2150 ANOP
.*
         AIF   ('&ALT' EQ '').BYP2200
         AIF   ('&ALT'(1,1) EQ '''').BYP2210
         DC    A(&ALT)                  ADDRESS OF ALT NAME
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR ALT
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFA&NDX)              ADDRESS OF GENERATED ALT
.BYP2250 ANOP
.*
         AIF   ('&PER' EQ '').BYP2300
         AIF   (T'&PER EQ 'N').BYP2310
         DC    A(&PER)                  ADDRESS OF PERCENT
         AGO   .BYP2350
.BYP2300 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR PERCENT
         AGO   .BYP2350
.BYP2310 ANOP
         DC    A(SPFP&NDX)              ADDRESS OF GENERATED PERCENT
.BYP2350 ANOP
.*
         AIF   ('&LIB' EQ '').BYP2400
         AIF   ('&LIB'(1,1) EQ '''').BYP2410
         DC    A(&LIB+X'80000000')      ADDRESS OF LIB NAME
         AGO   .BYP2450
.BYP2400 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR LIB
         AGO   .BYP2450
.BYP2410 ANOP
         DC    A(SPFL&NDX+X'80000000')  ADDRESS OF GENERATED LIB
.BYP2450 ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'NEWCOPY/REPLCOPY EXPECTED - FOUND ''&NEWCOPY'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
