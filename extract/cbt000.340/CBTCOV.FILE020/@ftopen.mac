         MACRO
&NAME    @FTOPEN &TEMPL,                TEMP KEYWORD                   *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFFTOPEN,&SPFDEBUG,&SPFVAR,&SPFTEMP
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEM
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
         AIF   ('&TEMPL' NE '' AND                                     *
               '&TEMPL' NE 'TEMP').ERR1
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERR2
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
         AIF   ('&MF(2)' NE '').BYP15
&TEM     SETC  'SPFPARMS'
         AGO   .BYP17
.BYP15   ANOP
&TEM     SETC  '&MF(2)'
.BYP17   ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFFTOPEN).GENLIT
         AIF   ('&TEMPL' NE '' AND NOT &SPFTEMP).GENLIT
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFFTOPEN).BYP20
FTOPEN   DC    CL8'FTOPEN'              ISPF KEYWORD
&SPFFTOPEN SETB 1
.BYP20   ANOP
         AIF   ('&TEMPL' EQ '').BYP60
         AIF   (&SPFTEMP).BYP60
TEMP     DC    CL8'TEMP'                ISPF KEYWORD
&SPFTEMP SETB  1
.BYP60   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,FTOPEN                GET ADDRESS OF FTOPEN KEYWORD
         AIF   ('&TEMPL' EQ '').BYP170
         LA    15,TEMP                  GET ADDRESS OF TEMP
         AGO   .BYP180
.BYP170  ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TEMP
.BYP180  ANOP
         STM   14,15,&TEM               STORE PARAMETERS
         OI    &TEM+4,X'80'             MARK LAST PARM
         LA    1,&TEM                   GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 2).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 2).BYPVARS
&SPFVARS SETA  2
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(FTOPEN)                BROWSE LITERAL
         AIF   ('&TEMPL' EQ '').BYP270
         DC    A(TEMP+X'80000000')      ADDRESS OF TEMP KEYWORD
         AGO   .BYP300
.BYP270  ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR TEMP
.BYP300  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR1    ANOP
         MNOTE 16,'INVALID VALUE FOR TEMP PARM - FOUND ''&TEMPL'''
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
