         MACRO
         DDDD  &ROLE
         GBLA  &H,&N,&CONCATN
         GBLB  &RTDDN,&FREEB,&DDDFAIL,&DDDD
&DDDD    SETB  1
         AIF   ('&ROLE' EQ 'STORAGE').CODEX
         DC    Y(X'8032'-X'10000')     CONTROL BITS FOR DAIRFAIL
ALOROUT  STCM  0,B'0011',ALOBLOCK+1    SET VERB AND FLAGS1
         AIF   ('&ROLE' EQ '').COMMON
         AIF   ('&ROLE' NE 'CODE').ERRROLE
         MVI   ALOBLOCK,20             SET BLOCK LENGTH = 20
         XC    ALOBLOCK+3(17),ALOBLOCK+3  PRESET BLOCK TO ZEROS
         LA    0,ALOBLOCK+20           POINTER TO TEXT UNIT ADDRESSES
         ST    0,ALOBLOCK+8            SET INTO BLOCK
         LA    0,ALOBLOCK              ADDRESS OF REQUEST BLOCK
         ST    0,ALOBLOCK-20           POINTER FOR SVC
         OI    ALOBLOCK-20,X'80'       SET END OF POINTER
.COMMON  LA    1,ALOBLOCK-20           POINT TO POINTER
         STM   14,1,12(13)             SAVE REGS IN CASE OF ERROR
         DYNALLOC ,                    DYNALLOC SVC
         LTR   15,15                   IF RETURN CODE IS ZERO,
         BZR   14                      RETURN TO MAINSTREAM
         ST    15,20(,13)              SAVE RETURN CODE IN REG 0 AREA
         LM    15,1,16(13)             LOAD EXIT ADDR, RC, AND POINTER
         ST    0,ALOBLOCK-4            RETURN CODE FOR DAIRFAIL
         BR    15                      GO TO EXIT RTN
         AIF   (NOT &DDDFAIL).EXITX
ALODDERR DS    0H
         AIF   (NOT &FREEB).FREEX
         CLC   ALOBLOCK+4(2),=X'0438'  "NOT FREED, IS NOT ALLOCATED"
         BER   14                      IS A PERMISSABLE ERROR
         CLC   ALOBLOCK+4(2),=X'0440'  "NOT FREED, IS NOT ALLOCATED"
         BER   14                      IS A PERMISSABLE ERROR
.FREEX   ANOP
ALODFAIL DS    0H
         L     15,16                   ADDRESS CVT
         AIF   ('&ROLE' EQ '').PARAMX
         LA    14,ALOBLOCK-4           RETURN CODE IN FULLWORD
         LA    15,976(,15)             CVTEFF02 POINTS TO IKJEFF02
         LA    0,ALOROUT-2             BITS TO CONTROL IKJEFF18
         STM   14,0,ALOBLOCK-20+4
         AGO   .STOREX
.PARAMX  LA    15,976(,15)             CVTEFF02 POINTS TO IKJEFF02
         ST    15,ALOBLOCK-20+8
.STOREX  LINK  EP=IKJEFF18,            INVOKE DAIRFAIL TO ISSUE WTP    *
               MF=(E,ALOBLOCK-20)
         L     1,=A(X'80000000'+99)    ABEND CODE IS USER 99,DUMP
         ABEND (1)                     ABEND
.EXITX   AIF   ('&ROLE' EQ 'CODE').MEXIT
.CODEX   ANOP
&H       SETA  &H/2-2
         DS    &H.H                    SPACE FOR SOME TEXT UNITS
         DC    A(*-X'7FFFFFEC')        ADDRESS OF ALOBLOCK
         DC    A(ALOBLOCK-4)           ADDRESS OF SVC RC FOR DAIRFAIL
         DC    A(0)                    ADDRESS OF FULLWORD -> IKJEFF02
         DC    A(ALOROUT-2)        ADDRESS OF CONTROL BITS FOR DAIRFAIL
         DS    F                   RETURN CODE FROM SVC 99 FOR DAIRFAIL
ALOBLOCK DC    X'14002000'             LENGTH, VERB, FLAGS1
         DC    A(0,*+12,0,0)   ERROR CODES, TU LIST ADDR, RESV, FLAGS2
&N       SETA  &N/4-4
         DS    &N.A                    TU ADDRESS LIST
         AIF   (NOT &RTDDN).ARTDDNX
ALORTDDN DC    Y(X'55',1,8),CL8' '     TU FOR RETURN OF DDNAME
DDDNAME  EQU   ALORTDDN+6,8,C'C'
.ARTDDNX AIF   (&CONCATN EQ 0).MEXIT
ALOCONCA DC    Y(1,0),&CONCATN.XL10'0' TU FOR DDNAMES TO CONCATENATE
.MEXIT   MEXIT
.ERRROLE MNOTE 12,'&ROLE IS INVALID. USE CODE OR STORAGE.'
         MEND
