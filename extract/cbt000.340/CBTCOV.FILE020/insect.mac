         MACRO
&N       INSECT &B1,&B2,&B3,&B4,&WS=,&WSNAME=,&SAVE=,&ENTRY=,          X
               &ENTREG=15,&EQU=R,&EQUATES=YES,&RC=RTRNCD
         GBLC  &EQC,&RCC,&SAVEC
         LCLA  &B1A,&B2A,&B3A,&B4A,&WSA
         LCLC  &B1C,&B2C,&B3C,&B4C,&WSC,&ERROP,&ERRGO,&ERRSW
&B1C     SETC  '&B1'
&B2C     SETC  '&B2'
&B3C     SETC  '&B3'
&B4C     SETC  '&B4'
&WSC     SETC  '&WS'
&EQC     SETC  '&EQU'
&RCC     SETC  '&RC'
&SAVEC   SETC  '&SAVE'
&ERRSW   SETC  'N'
.*
.A0      ANOP
         AIF   ('&N' NE '').A01
         SPACE 2
         MNOTE 16,'***** NAME FIELD MUST BE CODED *****'
         SPACE 2
.*       MEXIT
.A01     ANOP
         AIF   (N'&SYSLIST LE 4).A02
         SPACE 2
         MNOTE 16,'***** TOO MANY BASE REGS CODED *****'
         SPACE 2
.*       MEXIT
.A02     ANOP
&N       CSECT
         AIF   ('&ENTRY' EQ '').A021
         ENTRY &ENTRY
.A021    ANOP
         AIF   ('&EQUATES' EQ 'YES').A03
&EQC     SETC  ''
         AGO   .A04
.A03     ANOP
&EQC.0   EQU   0
&EQC.1   EQU   1
&EQC.2   EQU   2
&EQC.3   EQU   3
&EQC.4   EQU   4
&EQC.5   EQU   5
&EQC.6   EQU   6
&EQC.7   EQU   7
&EQC.8   EQU   8
&EQC.9   EQU   9
&EQC.10  EQU   10
&EQC.11  EQU   11
&EQC.12  EQU   12
&EQC.13  EQU   13
&EQC.14  EQU   14
&EQC.15  EQU   15
.A04     ANOP
.*
.* IF NO BASE REGISTERS ARE SPECIFIED, THE DEFAULT IS R13.
.* IF ANY ARE SPECIFIED, THE FIRST ONE CANNOT BE OMITTED.
.*
         AIF   (T'&B1 NE 'O').A05
         AIF   (N'&SYSLIST EQ 0).BASE13
         SPACE 2
         MNOTE 16,'***** FIRST BASE REG NOT SPECIFIED *****'
         SPACE 2
.*       MEXIT
.A05     ANOP
         AIF   ('&SAVE' NE '').A06
&SAVEC   SETC  'SECTSAVE'           USE DEFAULT LABEL FOR SAVEAREA
         AGO   .A1
.A06     ANOP
         AIF   ('&SAVE' EQ 'D').A1
&SAVEC   SETC  '&SAVE'              USE USER LABEL FOR SAVEAREA
         AGO   .A1
.*
.A1      ANOP
.*
.* THE FOLLOWING VALIDATION IS PERFORMED FOR EACH REGISTER OPERAND,
.* INCLUDING THE ONE FOR WORKING STORAGE, IF IT IS CODED:
.*
.*  IF THE OPERAND BEGINS WITH 'R'
.*    IF IT CONTAINS FEWER THAN 2 CHARACTERS OR MORE THAN 3
.*      PROCESS ERROR
.*    ELSE
.*      SET THE CORRESPONDING LOCAL TO CHAR 2 OF THE OPERAND
.*      IF THE OPERAND CONTAINS MORE THAN 2 CHARACTERS
.*        SET THE LOCAL TO CHARS 2 AND 3
.*  ELSE
.*    IF IT CONTAINS MORE THAN 2 CHARACTERS
.*      PROCESS ERROR
.*    ELSE
.*      SET THE CORRESPONDING LOCAL EQUAL TO CHAR 1
.*      IF IT CONTAINS MORE THAN ONE CHARACTER
.*        SET THE LOCAL TO BOTH CHARS
.*  IF THE RESULTING VALUE IS EQUAL TO THAT OF ANY PREVIOUS LOCAL
.*    ERROR IT
.*  SET AN ARITHMETIC LOCAL WITH THE VALUE OF THE CHARACTER LOCAL
.*  IF THE ARITHMETIC LOCAL VALUE IS LESS THAN 3 OR GREATER THAN 12
.*    ERROR IT
.*
.* ERROP IS THE VALUE OF THE CURRENT OPERAND, AND ERRGO IS THE
.* LABEL FOR THE NEXT SEGMENT OF LOGIC.  IF AN ERROR IS ENCOUNTERED
.* IN THIS SECTION, AN MNOTE WILL BE ISSUED DISPLAYING ERROP, AND
.* AN AGO IS TAKEN TO ERRGO.
.*
&ERROP   SETC  '&B1'
&ERRGO   SETC  'A2'
         AIF   ('&B1'(1,1) NE 'R').A13
         AIF   (K'&B1 LT 2 OR K'&B1 GT 3).AERR1
&B1C     SETC  '&B1'(2,1)
         AIF   (K'&B1 EQ 2).A15
&B1C     SETC  '&B1'(2,2)
         AGO   .A15
.A13     ANOP
         AIF   (K'&B1 GT 2).AERR1
&B1C     SETC  '&B1'(1,1)
         AIF   (K'&B1 EQ 1).A15
&B1C     SETC  '&B1'(1,2)
         AGO   .A15
.A15     ANOP
&B1A     SETA  &B1C
         AIF   (&B1A LT 3 OR &B1A GT 12).AERR2
         AGO   .A2
.*
.A2      ANOP
         AIF   (T'&B2 EQ 'O').A7
&ERROP   SETC  '&B2'
&ERRGO   SETC  'A3'
         AIF   ('&B2'(1,1)  NE 'R').A23
         AIF   (K'&B2 LT 2 OR K'&B2 GT 3).AERR1
&B2C     SETC  '&B2'(2,1)
         AIF   (K'&B2 EQ 2).A25
&B2C     SETC  '&B2'(2,2)
         AGO   .A25
.A23     ANOP
         AIF   (K'&B2 GT 2).AERR1
&B2C     SETC  '&B2'(1,1)
         AIF   (K'&B2 EQ 1).A25
&B2C     SETC  '&B2'(1,2)
         AGO   .A25
.A25     ANOP
&B2A     SETA  &B2C
         AIF   (&B2A LT 3 OR &B2A GT 12).AERR2
         AIF   ('&B2C' EQ '&B1C').AERR3
         AGO   .A3
.*
.A3      ANOP
         AIF   (T'&B3 EQ 'O').A7
&ERROP   SETC  '&B3'
&ERRGO   SETC  'A4'
         AIF   ('&B3'(1,1) NE 'R').A33
         AIF   (K'&B3 LT 2 OR K'&B3 GT 3).AERR1
&B3C     SETC  '&B3'(2,1)
         AIF   (K'&B3 EQ 2).A35
&B3C     SETC  '&B3'(2,2)
         AGO   .A35
.A33     ANOP
         AIF   (K'&B3 GT 2).AERR1
&B3C     SETC  '&B3'(1,1)
         AIF   (K'&B3 EQ 1).A35
&B3C     SETC  '&B3'(1,2)
         AGO   .A35
.A35     ANOP
&B3A     SETA  &B3C
         AIF   (&B3A LT 3 OR &B3A GT 12).AERR2
         AIF   ('&B3C' EQ '&B1C' OR '&B3C' EQ '&B2C').AERR3
         AGO   .A4
.*
.A4      ANOP
         AIF   (T'&B4 EQ 'O').A7
&ERROP   SETC  '&B4'
&ERRGO   SETC  'A7'
         AIF   ('&B4'(1,1) NE 'R').A43
         AIF   (K'&B4 LT 2 OR K'&B4 GT 3).AERR1
&B4C     SETC  '&B4'(2,1)
         AIF   (K'&B4 EQ 2).A45
&B4C     SETC  '&B4'(2,2)
         AGO   .A45
.A43     ANOP
         AIF   (K'&B4 GT 2).AERR1
&B4C     SETC  '&B4'(1,1)
         AIF   (K'&B4 EQ 1).A45
&B4C     SETC  '&B4'(1,2)
         AGO   .A45
.A45     ANOP
&B4A     SETA  &B4C
         AIF   (&B4A LT 3 OR &B4A GT 12).AERR2
         AIF   ('&B4C' EQ '&B1C').AERR3
         AIF   ('&B4C' EQ '&B2C' OR '&B4C' EQ '&B3C').AERR3
         AGO   .A7
.*
.A7      ANOP
         AIF   (T'&WS EQ 'O').B1
&ERROP   SETC  '&WS'
&ERRGO   SETC  'B1'
         AIF   (T'&WSNAME NE 'O').A71
         SPACE 2
         MNOTE 16,'***** WSNAME OPERAND MISSING *****'
         SPACE 2
.*       MEXIT
.A71     ANOP
         AIF   ('&WS'(1,1) NE 'R').A73
         AIF   (K'&WS LT 2 OR K'&WS GT 3).AERR1
&WSC     SETC  '&WS'(2,1)
         AIF   (K'&WS EQ 2).A75
&WSC     SETC  '&WS'(2,2)
         AGO   .A75
.A73     ANOP
         AIF   (K'&WS GT 2).AERR1
&WSC     SETC  '&WS'(1,1)
         AIF   (K'&WS EQ 1).A75
&WSC     SETC  '&WS'(1,2)
         AGO   .A75
.A75     ANOP
&WSA     SETA  &WSC
         AIF   (&WSA LT 3 OR &WSA GT 12).AERR2
         AIF   ('&WSC' EQ '&B1C' OR '&WSC' EQ '&B2C').AERR3
         AIF   ('&WSC' EQ '&B3C' OR '&WSC' EQ '&B4C').AERR3
         AGO   .B1
.*
.AERR1   SPACE 2
         MNOTE 16,'***** &ERROP IS AN INVALID REG SPECIFICATION *****'
         SPACE 2
         AGO   .AERRX
.*
.AERR2   SPACE 2
         MNOTE 16,'***** &ERROP CANNOT BE USED AS A BASE REG *****'
         SPACE 2
         AGO   .AERRX
.*
.AERR3   SPACE 2
         MNOTE 16,'***** &ERROP WAS CODED TWICE AS A BASE REG *****'
         SPACE 2
         AGO   .AERRX
.*
.AERRX   SPACE 2
&ERRSW   SETC  'Y'
         AIF   ('&ERRGO' EQ 'A2').A2
         AIF   ('&ERRGO' EQ 'A3').A3
         AIF   ('&ERRGO' EQ 'A4').A4
         AIF   ('&ERRGO' EQ 'A7').A7
         AIF   ('&ERRGO' EQ 'B1').B1
.*
.BASE13  ANOP
.*
.* NO BASE REGISTER SPECIFIED; DEFAULTING TO R13.
.*
***
***  NOTE:  R13 IS BEING USED AS A BASE REGISTER FOR THIS CSECT.
***         HENCE, THE BASE ADDRESS WILL BE ESTABLISHED AT THE
***         LOCATION OF SECTSAVE RATHER THAN AT THE BEGINNING
***         OF THE CSECT.  DISPLACEMENT VALUES IN INSTRUCTION
***         OPERANDS WILL BE LESS THAN THE LOCATION COUNTER
***         VALUES FOR THEIR SYMBOLS BY AN AMOUNT EQUAL TO THE
***         VALUE OF THE LOCATION COUNTER AT SECTSAVE.
***
         USING *,&EQC.&ENTREG
         STM   &EQC.14,&EQC.12,12(&EQC.13) SAVE CALLER'S REGISTERS
         B     SECT01                      BYPASS ID/SAVEAREA
         SPACE
         DC    CL8'&N'                     CSECT NAME
         DC    CL8'&SYSDATE'               COMPILE DATE
         DC    CL8'&SYSTIME'               COMPILE TIME
SECTSAVE DC    18F'0'                      SAVEAREA
         SPACE
SECT01   DS    0H
         LA    &EQC.14,SECTSAVE            LSA POINTER
         ST    &EQC.14,8(&EQC.13)          STORE LSA POINTER IN HSA
         ST    &EQC.13,4(&EQC.14)          STORE HSA POINTER IN LSA
         LA    &EQC.13,0(&EQC.14)          POINT R13 TO LSA
         DROP  &EQC.&ENTREG
         USING SECTSAVE,&EQC.13            SPECIFY LSA AS PROGRAM BASE
         SPACE 2
         MEXIT
.*
.B1      ANOP
         AIF   ('&ERRSW' EQ 'Y').BX
         AIF   (T'&B2 NE 'O').B11
         USING *,&EQC.&B1C
         AGO   .B15
.B11     ANOP
         AIF   (T'&B3 NE 'O').B12
         USING *,&EQC.&B1C,&EQC.&B2C
         AGO   .B15
.B12     ANOP
         AIF   (T'&B4 NE 'O').B13
         USING *,&EQC.&B1C,&EQC.&B2C,&EQC.&B3C
         AGO   .B15
.B13     ANOP
         USING *,&EQC.&B1C,&EQC.&B2C,&EQC.&B3C,&EQC.&B4C
         AGO   .B15
.B15     ANOP
         CNOP  0,4
         STM   &EQC.14,&EQC.12,12(&EQC.13) SAVE CALLER'S REGISTERS
         LR    &EQC.&B1C,&EQC.&ENTREG      INIT MY BASE REGISTER
         B     SECT01
         SPACE
         DC    CL8'&N'                     CSECT NAME
         DC    CL8'&SYSDATE'               COMPILE DATE
         DC    CL8'&SYSTIME'               COMPILE TIME
&SAVEC   DC    18F'0'                      SAVEAREA
         SPACE
SECT01   DS    0H
         AIF   (T'&B2 EQ 'O').B2
         LA    &EQC.&B2C,4095(&EQC.&B1C)
         LA    &EQC.&B2C,1(&EQC.&B2C)      INIT 2ND BASE REG
         AIF   (T'&B3 EQ 'O').B2
         LA    &EQC.&B3C,4095(&EQC.&B2C)
         LA    &EQC.&B3C,1(&EQC.&B3C)      INIT 3RD BASE REG
         AIF   (T'&B4 EQ 'O').B2
         LA    &EQC.&B4C,4095(&EQC.&B3C)
         LA    &EQC.&B4C,1(&EQC.&B4C)      INIT 4TH BASE REG
.B2      ANOP
         AIF   (T'&WS EQ 'O').B22
         LA    &EQC.&WSC,&WSNAME           INIT W/S BASE REG
         USING &WSNAME,&EQC.&WSC           ESTABLISH W/S BASE ADDRESS
.B22     ANOP
         AIF   ('&SAVE' EQ 'D').C1         DYNAMIC S/A REQUEST
         SPACE
         LA    &EQC.14,&SAVEC              ADDRESS OF MY SAVE AREA
         ST    &EQC.14,8(&EQC.13)          STORE LSA POINTER IN HSA
         ST    &EQC.13,4(&EQC.14)          STORE HSA POINTER IN LSA
         LR    &EQC.13,&EQC.14             POINT R13 TO LSA
.BX      SPACE 2
         MEXIT
.*
.C1      ANOP
&SAVEC   SETC  'DYNAM'
***
***  NOTE:  DYNAMIC SAVE AREA WILL BE USED.
***
         LA    &EQC.0,72                   SUBPOOL 0, LENGTH 72
         GETMAIN R,LV=(&EQC.0)             REQUEST VIRTUAL STORAGE
         ST    &EQC.1,8(&EQC.13)           LSA POINTS TO GETMAIN AREA
         ST    &EQC.13,4(&EQC.1)           SET HSA POINTER
         LM    &EQC.0,&EQC.1,20(&EQC.13)   RESTORE R0, R1
         L     &EQC.13,8(&EQC.13)          POINT R13 TO LSA
         SPACE 2
         MEND
