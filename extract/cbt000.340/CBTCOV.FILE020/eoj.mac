         MACRO
&NAME    EOJ   &RC=0,&LOG=NO
         GBLA  &SIDX
         GBLB  &SREG
         GBLB  &IDS(255)
         GBLB  &CRENT(255)        ON IF CODE=RENT FROM SETREG
         GBLC  &IDP(255)
         GBLC  &POOL(255)         SUBPOOL FROM SETREG
         GBLC  &LV(255)         LENGTH FROM SETREG
         LCLC  &IDE
         LCLC  &RCR,&NAM,&SID
         LCLB  &RTYPE
&NAM     SETC  '&NAME'
&SID     SETC  '&SIDX'
         AIF   (&SREG).OK
         MNOTE *,'SHAME ON YOU USING EOJ WITHOUT SETREG'
&NAM     L     13,4(13)           GET OLD REGISTER SAVE AREA
         AGO   .RET
.OK      ANOP
&NAM     DC    0H'0'
         AIF   ('&RC' EQ '').RCNR1
         AIF   ('&RC'(1,1) NE '(').RCNR1
&RTYPE   SETB  1
.RCNR1   ANOP
&IDS(&SIDX) SETB (&IDS(&SIDX) AND '&LOG' EQ 'YES')
         AIF   (NOT &IDS(&SIDX)).NOLOG SOMEBODY DOESNT WANT TO LOG
&IDE     SETC  'END   '.'&IDP(&SIDX)'
         WTO   '&IDE',ROUTCDE=11  LOG THE PROGRAM OFF
.NOLOG   ANOP
         AIF   (NOT &CRENT(&SIDX)).NORENT
         LR    1,13               SAVE MY SAVEAREA ADDRESS
         L     13,4(13)           GET BACK SAVE AREA POINTER
         AIF   ('&RC' EQ '').FREE1
         AIF   (NOT &RTYPE).FREE1
         ST    &RC(1),16(,13) SAVE RET CODE IN R15 PLACE
.FREE1   ANOP
         L     0,$SPLV&SID        GET SP+LV
         FREEMAIN R,LV=(0),A=(1)  DO FREEMAIN
         AGO   .RET
.NORENT  ANOP
         L     13,4(13)           BACK SAVEAREA POINTER
.RET     ANOP
.RETTEST AIF   (&RTYPE).R9
         AIF   ('&RC' EQ '').RETEQ0 *   MESSAGE, ASSUME ZERO
         AIF   (T'&RC NE 'N').REGLD1 *  RETURN NUMBER?
         AIF   (&RC EQ 0).R0       *    RC=0
         AGO   .REGLA                   * LOAD ADDRESS ON RC
.RETEQ0  MNOTE *,'*** WARNING *** RC=0 (ZERO) ASSUMED '
         AGO   .R0
.REGLD1  L     15,&RC .                 LOAD SAVED RETURN CODE
         AGO   .BR14
.REGLA   LA    15,&RC .                 LOAD REQUESTED RETURN CODE
         AGO   .BR14
.R9      AIF   (&CRENT(&SIDX)).R0
         ST    &RC(1),16(,13)           SAVE RETURN CODE
.R0      XC    8(4,13),8(13) .          CLEAR FWD CHAIN PTR
         MVI   12(13),X'FF'             SAY WE RETURNED
         LM    14,12,12(13) .           RELOAD CALLERS' REGS
         AIF   (&RTYPE).R00
         XR    15,15 .                  CLEAR RETURN CODE
.R00     BR    14 .                     RETURN TO CALLER
         AGO   .END
.BR14    XC    8(4,13),8(13) .          CLEAR FWD CHAIN PTR
         MVI   12(13),X'FF'             SAY WE RETURNED
         LM    0,12,20(13) .            LOAD SAVED REGS
         L     14,12(0,13) .            AND RETURN
         BR    14 .                     RETURN TO CALLER
.END     ANOP
.* NOW DECREMENT SIDX BACK TO PREVIOUS LEVEL
         AIF   (&SIDX EQ 1).RET2  *NEVER DECR LESS THAN 1*
&SIDX    SETA  &SIDX-1
.RET2    ANOP
         MEND
