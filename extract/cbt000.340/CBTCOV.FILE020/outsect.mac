         MACRO
&N       OUTSECT
         GBLC  &EQC,&RCC,&SAVEC
.*
         L     &EQC.15,&RCC            RETRIEVE RETURN CODE
         L     &EQC.13,4(&EQC.13)      POINT TO CALLER'S SAVEAREA
         ST    &EQC.15,16(&EQC.13)     STORE IN REG 15 SLOT
.*
         AIF   ('&SAVEC' NE 'DYNAM').T1
.*
.* FREE DYNAMIC SAVE AREA
.*
***
*** FREEMAIN ISSUED FOR DYNAMICALLY OBTAINED SAVEAREA.
***
         L     &EQC.1,8(&EQC.13)       FREEMAIN ADDR FROM LSA PTR
         LA    &EQC.0,72               LENGTH VALUE TO R0
         FREEMAIN  R,LV=(&EQC.0),A=(&EQC.1)
         SPACE 2
.T1      ANOP
         LM    &EQC.14,&EQC.12,12(&EQC.13)   RESTORE CALLER'S REGS
         BR    &EQC.14                       RETURN
         SPACE 2
&RCC     DC    F'0'                    RETURN CODE
         MEND
