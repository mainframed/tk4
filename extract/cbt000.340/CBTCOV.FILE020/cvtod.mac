         MACRO
&LABEL   CVTOD &OPTION
         AIF   ('&OPTION' EQ  '').GENALL
         AIF   ('&OPTION' EQ  'DATA').DATONLY
.GENALL  ANOP
************************************************************
************************************************************
** CVTOD - CONVERT TOD TO INTERNAL/EXTERNAL FORMAT        **
**   ENTRY:  R1  - ADDR OF TOD TO CONVERT                 **
**                                                        **
**                                                        **
**   EXIT:   R1  - ADDRESS OF OUTPUT DATA                 **
**           R0  - LEN OF OUTPUT                          **
**                                                        **
**   LABEL:    OUTDAY = DAY OF WEEK    LEN=3              **
**             OUTJ   = JULIAN DATE    LEN=6              **
**             OUTDM  = GREGORIAN DATE LEN=8              **
**             OUTTH  = TIME OF DAY    LEN=5              **
**                                                        **
**    OUTPUT FORMAT: 'DAY YY.DDD MM/DD/YY HH:MM -'        **
**                                                        **
**     ASSUMES ZERO VALUE FOR TOD CLOCK IS ----           **
**                   '01/01/00 (000) 00:00:00.000000'     **
**                                                        **
**  THIS  CODE  IS FROM UCLA PROGRAM DUMPINFO ACQUIRED    **
**  VIA  THE CBT TAPE FILE 149 01/24/84....               **
**  AND  MODIFIED BY L. DYCK AS A MACRO                   **
**                                                        **
************************************************************
************************************************************
         SPACE 3
&LABEL   STM   R0,R15,CVSAVE            SAVE CALLERS REGS
         MVI   OUT,C' '                 INIT OUTPUT AREA TO BLANK
         MVC   OUT+1(OUTLEN-1),OUT
         LM    R2,R3,0(R1)              GET ORIG TOD VALUE
         SRDL  R2,12                    ALIGN MICROSECONDS
         D     R2,=A(60*1000000)        MIN TO R3, USEC TO R2
         CVD   R2,DWD                   USEC TO DECIMAL
         SR    R2,R2                    CLEAR FOR DIVIDE
         D     R2,=A(60)                HOUR TO R3, MIN TO R2
         CVD   R2,DWD                   MINUTES TO DEC
         UNPK  OUTTM(2),DWD+6(2)        MIN TO OUTPUT AREA
         OI    OUTTM+1,C'0'             FIX ZONE
         SR    R2,R2                    CLEAR FOR DIVIDE
         D     R2,=A(24)                DAYS TO R3, HOURS TO R2
*************************
*   CALC DAY OF WEEK    *
*************************
         SR    R9,R9
         LR    R9,R3                    TOTAL # DAYS IN R9
         SR    R8,R8                    CLEAR R8 FOR DIVIDE
         D     R8,=F'7'                 DIVIDE BY 7 TO GET REMAINDER
         LA    R6,DAYTBL                ADDR OF DAY TABLE IN R6
         LR    R9,R8                    MOVE REMAINDER FOR MULTIPLY
         M     R8,=F'3'                 MULTIPLY REMAINDER BY 3
         AR    R6,R9                    ADD TO TABLE ADDR
         MVC   OUTDAY(3),0(R6)          MOVE DAY INTO OUTPUT
**********************************
*   CALC YEAR, HOURS & MINUTES   *
**********************************
         CVD   R2,DWD                   HOURS TO DEC
         UNPK  OUTTH(2),DWD+6(2)         HOURS TO OUTPUT AREA
         OI    OUTTH+1,C'0'              FIX ZONE
         MVI   OUTTH+2,C':'             COLON BETWEEN HOUR:MIN
         C     R3,=A(365)               AFTER 1900?
         BNH   *+8                      BIF NOT AFTER 1900
         A     R3,=A(1)                 ELSE PRETEND 1900 LEAP YEAR
         SLL   R3,2                     DAYS * 4
         SR    R2,R2                    CLEAR FOR DIVIDE
         D     R2,=A((365*4)+1)         YEAR TO R3, DAY TO R2
         CVD   R3,DWD                   YEAR TO DEC
         UNPK  OUTJ(2),DWD+6(2)         YEAR TO JULIAN DATE OUTPUT
         UNPK  OUTDY(2),DWD+6(2)        YEAR TO OUTPUT AREA
         OI    OUTJ+1,C'0'              FIX ZONE
         OI    OUTDY+1,X'F0'            FIX ZONE
         SRL   R2,2                     DAYS * 4 TO DAYS
         A     R2,=A(1)                 + 1 TO GET DAYS ORIG 1
         CVD   R2,DWD                   JULIAN DAY TO DEC
         UNPK  OUTJ+3(3),DWD+6(2)       JULIAN DAY TO OUTPUT AREA
         OI    OUTJ+5,X'F0'             FIX ZONE
         MVI   OUTJ+2,C'.'              . IN JULIAN DATE
*****************************************************
*****************************************************
**                                                 **
**       CONVERT JULIAN TO GREGORIAN DATE          **
**                                                 **
**       R2 - JULIAN DAY IN BIN                    **
**       R3 - YEAR IN BIN                          **
**                                                 **
*****************************************************
*****************************************************
GRDATE   DS    0H
         LA    R5,CVTOD#T1-2            ASSUME NON-LEAP YEAR
         N     R3,=F'3'                 TEST FOR LEAP YEAR
         BNZ   CVTOD$NL                 BIF NOT LEAP YEAR
         LA    R5,CVTOD#T2-2            ELSE FEB HAS 29 DAYS
CVTOD$NL LR    R4,R5                    COPY TABLE ADDR
CVTOD$L1 LA    R4,2(,R4)                POINT TO NEXT ENTRY
         SH    R2,0(,R4)                DAYS LEFT AFTER MONTH
         BP    CVTOD$L1                 BIF AFTER THIS MONTH
         AH    R2,0(,R4)                GET DAY IN MONTH
         CVD   R2,DWD                   DAY IN MONTH TO DEC
         UNPK  OUTDD(2),DWD+6(2)        DAY TO OUTPUT AREA
         OI    OUTDD+1,C'0'             FIX ZONE
         MVI   OUTDD+2,C'/'             SLASH AFTER DAY
         SR    R4,R5                    GET 2*MONTH ORIG 0
         SRL   R4,1                     GET MONTH
         CVD   R4,DWD                   MONTH TO DEC
         UNPK  OUTDM(2),DWD+6(2)        MONTH TO OUTPUT AREA
         OI    OUTDM+1,C'0'             FIX ZONE
         MVI   OUTDM+2,C'/'             SLASH AFTER MONTH
         LA    R1,OUT                   -> OUTPUT AREA
         LA    R0,OUTLEN
         STM   R0,R1,CVSAVE
         LM    R0,R15,CVSAVE
         B     CVTODX                  BRANCH AROUND DATA
         SPACE 1
*---            DAY TABLE
DAYTBL   DC    CL3'MON'
         DC    CL3'TUE'
         DC    CL3'WED'
         DC    CL3'THR'
         DC    CL3'FRI'
         DC    CL3'SAT'
         DC    CL3'SUN'
         SPACE
*---            NON - LEAP YEAR TABLE OF DAYS / MONTH
CVTOD#T1 DC    H'31'       1 -          JAN
         DC    H'28'       2 -          FEB
         DC    H'31'       3 -          MARCH
         DC    H'30'       4 -          APRIL
         DC    H'31'       5 -          MAY
         DC    H'30'       6 -          JUNE
         DC    H'31'       7 -          JULY
         DC    H'31'       8 -          AUG
         DC    H'30'       9 -          SEPT
         DC    H'31'      10 -          OCT
         DC    H'30'      11 -          NOV
         DC    H'31'      12 -          DEC
         SPACE 1
*---              LEAP YEAR TABLE OF DAYS / MONTH
CVTOD#T2 DC    H'31'       1 -          JAN
         DC    H'29'       2 -          FEB
         DC    H'31'       3 -          MARCH
         DC    H'30'       4 -          APRIL
         DC    H'31'       5 -          MAY
         DC    H'30'       6 -          JUNE
         DC    H'31'       7 -          JULY
         DC    H'31'       8 -          AUG
         DC    H'30'       9 -          SEPT
         DC    H'31'      10 -          OCT
         DC    H'30'      11 -          NOV
         DC    H'31'      12 -          DEC
         DC    H'32767'   13 -          13TH MONTH (LARGE AND LAST)
         AIF   ('&OPTION' EQ  'NOGEN').NOGEN
.DATONLY ANOP
CVSAVE   DS    16F                     REGISTER SAVE AREA
DWD      DS    D
OUT      DS    0F
OUTDAY   DC    C'DAY '                          DAY OF WEEK
OUTJ     DC    C'YY.DDD '                       JULIAN DATE
OUTDM    DC    C'MM/'                           MONTH
OUTDD    DC    C'DD/'                           DAY
OUTDY    DC    C'YY '                           YEAR
OUTTH    DC    C'HH:'                           HOURS
OUTTM    DC    C'MM'                            MINUTES
OUTLEN   EQU   *-OUT                            AREA LENGTH
.NOGEN   ANOP
         AIF   ('&OPTION' EQ  'DATA').EXIT
CVTODX   DS    0H
.EXIT    ANOP
         MEND
