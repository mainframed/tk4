*          DATA SET P50269     AT LEVEL 001 AS OF 02/17/81
         MACRO
         SMFWTO
         TS    *+1            FOR ONE TIME OPERATION (MLPA ONLY)
         BNZ   SMF&SYSNDX    SKIP WTO AFTER FIRST EXECUTION
         TIME
         ST    1,FULL&SYSNDX
         ED    EDIT&SYSNDX,FULL&SYSNDX+1
         MVC   W&SYSNDX+8(5),EDIT&SYSNDX+1
         WTO '******************************************************', X
               ROUTCDE=(9)
W&SYSNDX WTO '      &SYSECT ACTIVE - LAST COMPILE &SYSDATE &SYSTIME',  X
               ROUTCDE=(9)
         WTO '******************************************************', X
               ROUTCDE=(9)
         B     SMF&SYSNDX
FULL&SYSNDX DC F'0'
EDIT&SYSNDX DC X'402020202020'
         CNOP  0,4
SMF&SYSNDX EQU *
         MEND
         TITLE 'ICHRIX02 - RACINIT POST PROCESSING EXIT'
         REGISTER
ICHRIX02 CSECT
         SPACE 5
***********************************************************************
*                                                                     *
*        MODULE: ICHRIX02 (P50269 ON PANVALET)                        *
*                                                                     *
*        PURPOSE: TO ISSUE A WARNING MESSAGE WHEN A PASSWORD IS       *
*                 WITHIN THREE DAYS OF EXPIRING. THIS ROUTINE MUST    *
*                 BE REENTRANT AND USES THE RACF MANAGER SVC TO       *
*                 RETRIEVE THE LAST PASSWORD CHANGE DATE.             *
*                                                                     *
***********************************************************************
         SPACE 5
         STM   R14,R12,12(R13)     STORE REGS ON ENTRY
         LR    R2,R1               SAVE ADDR OF PARM LIST
         LR    R12,R15             ESTABLISH NEW BASE
         USING ICHRIX02,R12
         GETMAIN R,LV=WORKSIZE     GET SOME STORAGE
         MVC   0(WORKSIZE,R1),WORKAREA
         ST    R13,4(R1)           CHAIN BACK
         ST    R1,8(R13)           CHAIN FORWARD
         LR    R13,R1              SET UP NEW SAVE AREA
         USING WORKAREA,R13
         SMFWTO
         EJECT
         L     R3,4(R2)            GET ADDRESS OF FLAG
         TM    0(R3),X'C0'         IS THIS A CREATE
         BNZ   EXIT                NO, GET OUT
         L     R3,60(R2)           GET ADDRESS OF RETURN CODE
         ICM   R3,15,0(R3)         DID RACINIT WORK??
         BNZ   EXIT                NO, DON'T BOTHER TO CHECK DATE
         L     R3,8(R2)            GET USER ID ADDR
         CLC   1(4,R3),=C'    '    WAS A USER SPECIFIED
         BE    EXIT                NO, THEN MUST BE BYPASS FROM PRE-
*                                  PROCESSING EXIT
         MVC   USERID+1(4),1(R3)   MOVE IT IN WORK AREA
         ICHEINTY MF=(E,TYMAC),ENTRY=USERID,WKAREA=LOCWORK,            X
               OPTIONS=(FLDEF)
         XR    R5,R5               CLEAR R5
         IC    R5,CHGINT+2         GET CHANGE INTERVAL
         TIME  DEC                 GET CURRENT DATE
         XC    CURDT,CURDT         CLEAR DOUBLE WORD WORK AREA
         ST    R1,CURDT+4          STORE CURRENT DATE
         NI    LPSDT,X'00'         GET RID OF YEAR
         NI    CURDT+5,X'00'
         SP    CURDT,LPSDT         SUBTRACT LAST CHG DATE FROM CURRENT
         CP    CURDT,ZERO          CHECK IF CROSSED YEAR BOUNDARY
         BC    11,ICHR0010         NO, CONTINUE
         AP    CURDT,=P'365'       YES, ADD 365
ICHR0010 CVB   R4,CURDT            CONVERT NO OF DAYS SINC LAST CHG
         SR    R4,R5               SUBTRACT TO GET NO OF DAYS BEFORE
*                                  YOU EXIPIRE
         CH    R5,=H'3'            COMPARE AGAINST 3
         BH    EXIT                IF GREATER, THEN GET OUT
         CVD   R5,CURDT            CONVERT TO DECIMAL
         UNPK  WTO+39(1),CURDT+7(1)  CONVERT TO CHARACTER
         OI    WTO+39,X'F0'        FIX UP SIGN
         L     R1,16               GET CVT ADDR
         L     R1,0(R1)            GET TCB PTR
         L     R1,12(R1)           GET ASCB PTR
         ICM   R1,15,X'3C'(R1)     GET TSCB PTR
         BNZ   ICHR0020            IT TSO ISSUE A TPUT
         WTO   MF=(E,WTO)          OTHERWISE MAKE IT A WTO
         B     EXIT                BYPASS TPUT
ICHR0020 LA    R5,WTO+4            GET BEGINNING OF MESSAGE
         LA    R0,WTOLEN           GET LENGTH
         BAL   R1,*+8              BRANCH AROUND MASK
         DC    X'40000000'         MASK FOR TPUT
         L     R1,0(R1)            SET MASK
         OR    R1,R5               ADDRESS OF DATA
         LA    R15,USERID+1        ADDRESS OF USERID
         SVC   93                  TPUT
EXIT     LR    R5,R13              PRESERVE ADDR OF WORK AREA
         L     R13,WORKAREA+4      GET ADDR OF PREV SAVE
         FREEMAIN R,LV=WORKSIZE,A=(R5)
         LM    R14,R12,12(R13)
         XR    R15,R15             CLEAR RETURN REG
         BR    R14
         LTORG
         EJECT
WORKAREA DS    18F
WTO      WTO   'ICHMBC01-W PASSWORD WILL EXPIRE IN X DAYS',MF=L,       X
               ROUTCDE=11,DESC=6
WTOLEN   EQU   *-WTO-8
TYMAC    ICHEINTY LOCATE,TYPE='USR',ENTRY=USERID,ACTIONS=(ACT1,ACT2),  X
               WKAREA=LOCWORK,MF=L
ACT1     ICHEACTN MF=L,FIELD=PASSDATE
ACT2     ICHEACTN MF=L,FIELD=PASSINT
LOCWORK  DC    F'36'
         DS    CL26
LPSDT    DS    PL3
CHGINT   DS    CL7
CURDT    DS    D
USERID   DC    X'04',CL8' '
ZERO     DC    PL1'0'
WORKSIZE EQU   *-WORKAREA
         PRINT NOGEN
         CVT   DSECT=YES
         ICHPRCVT
         END
