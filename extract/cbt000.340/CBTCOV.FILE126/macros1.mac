*
         MACRO
&NAME    PRIME &REG=12,&SAVE=,&ID=,&LV=0
.* PRIME WILL SAVE REGISTERS, CREATE A NEW SAVE AREA, CHAIN THE NEW
.* SAVE AREA TO THE CALLERS SAVE AREA, ESTABLISH ADDRESSABILITY,
.* OPTIONALLY OBTAIN ADDITIONAL STORAGE AND IN GENERAL PERFORM THE
.* NECESSARY ENTRY LINKAGE.
.* IF THE USER DOES NOT SPECIFY WHAT REGISTER HE REQUIRES FOR A BASE,
.* TWELVE IS USED.  NOTE..IF REG= IS SPECIFIED TO BE 0, 1, 13, 14, OR
.* 15  A WARNING MESSAGE IS ISSUED.
.* IF THE USER SPECIFIES A SAVE AREA VIA THE SAVE= OPERAND THEN PRIME
.* WILL CHAIN THE USERS SAVE AREA TO THE CALLERS SAVE AREA. IF THE
.* USER DOES NOT SPECIFY SAVE,THEN PRIME WILL OBTAIN A NEW SAVE AREA
.* VIA THE GETMAIN SVC.  AN ADDITIONAL AMOUNT OF STORAGE MAY BE
.* OBTAINED AT THIS TIME BY SPECIFIEING THE LV=, OPERAND.  IF LV IS NOT
.* SPECIFIED IN REGISTER NOTATION, 4023 IS THE MAXIMUM VALUE.
.* IF THE USER SPECIFIES SAVE=NONE, PRIME WILL NOT OBTAIN A NEW AREA.
.* REG 13 WILL POINT TO THE NEW 18 WORD SAVE AREA FOLLOWED BY THE EXTRA
.* STORAGE AREA.
         LCLA  &A,&B
         LCLC  &E,&F,&G,&H
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP
         AIF   ('&ID' EQ '').CONT4
         AIF   ('&ID' EQ '*').SPEC
&A       SETA  ((K'&ID+2)/2)*2+4
         BAL   15,&A.(0,15)             GET PGM MASK AND BRANCH AROUND
         USING *,&REG
&A       SETA  K'&ID
         DC    AL1(&A)
.CONTB   AIF   (&A GT 32).SPLIT
.CONTAA  AIF   (&A GT 8).BRAK
&E       SETC  '&ID'(&B+1,&A)
         DC    CL&A'&E'
         AGO   .CONT1
.BRAK    ANOP
&E       SETC  '&ID'(&B+1,8)
         DC    CL8'&E'
&B       SETA  &B+8
&A       SETA  &A-8
         AGO   .CONTAA
.SPLIT   ANOP
&E       SETC  '&ID'(&B+1,8)
&F       SETC  '&ID'(&B+9,8)
&G       SETC  '&ID'(&B+17,8)
&H       SETC  '&ID'(&B+25,8)
         DC    CL32'&E.&F.&G.&H'
&B       SETA  &B+32
&A       SETA  &A-32
         AGO   .CONTB
.SPEC    AIF   ('&NAME' EQ '').CSECTN
&E       SETC  '&NAME'
&A       SETA  1
.CONTQ   AIF   ('&E'(1,&A) EQ '&E').LVE
&A       SETA  &A+1
         AGO   .CONTQ
.LVE     ANOP
&B       SETA  ((&A+2)/2)*2+4
         BAL   15,&B.(0,15)             GET PGM MASK AND BRANCH AROUND
         USING *,&REG
         DC    AL1(&A)
         DC    CL&A'&E'
         AGO   .CONT1
.CSECTN  AIF   ('&SYSECT' EQ '').E4
&E       SETC  '&SYSECT'
&A       SETA  1
         AGO   .CONTQ
.E4      IHBERMAC 78,360
.CONT4   ANOP
         BALR  15,0                     R15 NOW CONTAINS PGM MASK
         USING *,&REG
.CONT1   ANOP
         AIF   ('&REG' EQ '0' OR '&REG' EQ '1' OR '&REG' EQ '13').ERR1
         AIF   ('&REG' EQ '14' OR '&REG' EQ '15').ERR1
.CONT3   ANOP
         DS    0H
         STM   14,12,12(13)             SAVE REGS IN CALLERS AREA
         LR    &REG,15                  SET TRUE BASE EQUAL EP
         AIF ('&SAVE' EQ 'NONE').A
         AIF   ('&SAVE' EQ '').C
         AIF   ('&SAVE'(1,1) EQ '(').F
         LA    1,&SAVE                  SET R1 EQ USERS SAVE ADDRESS
         ST    1,8(0,13)                CHAIN FORWARD
         AGO   .D
.F       ANOP
         AIF   ('&SAVE' EQ '(1)').D
         LR    1,&SAVE(1)               PICK UP USERS SAVE AREA
         AGO   .D
.C       ANOP
         AIF   ('&LV'(1,1) EQ '(').E
*        AIF   ('&LV' GT '4023').ERR2
         LA    0,&LV.+72(0,0)           PARAMETER FOR GETMAIN
         AGO   .CONT2
.E       ANOP
         AIF   ('&LV' EQ '(0)').CONT2
         LA    0,72(&LV(1).,0)          PARAMETER FOR GETMAIN
.CONT2   ANOP
         BAL   1,*+4                    INDICATE GETMAIN
         SVC   10                       ISSUE SVC 10
         ST    0,0(0,1)                 SAVE LENGTH IN 1ST WORD
         ST    1,8(0,13)                CHAIN FORWARD
         MVI   8(13),X'DC'              INDICATE GETMAIN
.D       ANOP
         XC    4(68,1),4(1)             CLEAR AREA
         ST    13,4(0,1)                CHAIN BACKWARD
         LM    0,1,20(13)               RESET R0 AND R1
         L     13,8(0,13)               SET SAVE AREA REGISTER
.A       ANOP
         MEXIT
.ERR1    MNOTE 'ILLEGAL BASE REGISTER SPECIFIED'
         AGO   .CONT3
.ERR2    MNOTE 'LV GT 4023, LV REQUEST NOT HONORED'
         LA    0,72(0,0)                PARAMETER FOR GETMAIN
         AGO   .CONT2
         MEND
         MACRO
         REGISTER
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEND
         MACRO
&NAME    TERME &XCTL,&LV=72,&SP=0,&RC=(15),&LVA=,&LIST=NO
.******************************************************************
.*
         MNOTE ' ***** TERME MACRO 08/25/76 TOM CAIN'
         AIF   ( '&LIST' EQ 'NO' ).NOLIST
*
*NAME    TERME &XCTL,&LV=72,&SP=0,&RC=(15),&LVA=,&LIST=NO
*
*   IF XCTL IS SPECIFIED CONTROL WILL NOT BE RETURNED AND
*   INSTRUCTIONS WILL BE GENERATED WHICH WILL ALLOW THE XCTL MACRO TO
*   BE GIVEN IMMEDIATELY FOLLOWING THE TERME MACRO.
*
*   ALL OPERANDS ARE OPTIONAL.
*
*   LV=  IS THE LENGTH OF THE AREA TO FREE (SAME AS FROM PRIME)
*   LV=0 WILL NOT GENERATE SAVE AREA CHAINING
*   LVA= IS USED BY THE LINKAGE MACRO TO INDICATE THAT FREEMAIN
*        SHOULD NOT BE GENERATED OR THAT FREEMAIN LV=(0) WILL BE
*        USED WITH LVA= SPECIFIED THE FULLWORD WITH SP= AND LV= VALUES
*
*   SP=  IS THE SUB POOL OF THE AREA (SAME AS PRIME)
*
*   RC=  IS THE SOURCE OF THE RETURN CODE VALUE.
*        IT CAN BE A REGISTER (2-12,15) OR THE SYMBOLIC NAME OF A
*        FULLWORD OR AN IMMEDIATE VALUE (0 TO 4095).
*        DEFAULTS TO RC=(15).
*
* REGISTERS 0, 1, AND 15 WILL NOT BE RESTORED
*
.**********************************************************************
.NOLIST  AIF   ( T'&NAME EQ 'O' ).NOLABEL
&NAME    DS    0H
.NOLABEL AIF   ( T'&RC EQ 'O' ).RCSET
         AIF   ( '&RC'(1,1) EQ '(' ).RCREG
         AIF   ( T'&RC EQ 'N' ).RCNUM
         L     15,&RC              SET RETURN CODE
         AGO   .RCSET
.RCNUM   LA    15,&RC
         AGO   .RCSET
.RCREG   AIF   ( '&RC'(2,3) EQ 'R15' OR '&RC'(2,2) EQ '15' ).RCSET
         LR    15,&RC(1)
.RCSET   AIF   ( '&LV' NE '0' ).HIGHSV
         ST    15,16(0,13)         STORE RETURN CODE
         AGO   .RESTORE
.HIGHSV  L     13,4(0,13)          GET HIGHER SA PTR
         STM   15,1,16(13)         TEMP FOR 15 TO 1
         AIF   ( '&LVA' EQ 'NOFREEMAIN' ).RESTORE
         XI    8(13),X'DC'         Q. FREE SAVE AREA ????
         BNZ   T&SYSNDX               NO
         L     1,8(0,13)              YES
         AIF   ( T'&LVA EQ 'O' ).FREEM
         L     0,&LVA
.FREEM   FREEMAIN R,LV=&LV,A=(1),SP=&SP
.RESTORE ANOP
T&SYSNDX LM    14,12,12(13)        RESTORE REGS
         XC    8(4,13),8(13)       REMOVE SA LINKAGE
         SPM   14
         AIF   ( '&XCTL' EQ 'XCTL' ).XCTL
         BR    14
         MEXIT
.XCTL    BALR  15,0
         USING *,15
         MEND
