*          DATA SET P50230     AT LEVEL 001 AS OF 12/01/80
RTH      TITLE  'TMS RACF EXIT - REPLACES IGC0BXXX'
*          DATA SET RHRTXB1    AT LEVEL 010 AS OF 03/29/78
         COPY  TMSSVCNO
         EJECT
IGC0B&XA CSECT  ,
         TMSVCEQU SVC=X
         EJECT
***********************************************************************
*                                                                     *
*  THIS MODULE REPLACES THE STANDARD TMS SECURITY MODULE TO IMPLEMENT *
*  RACF TAPE PROTECTION                                               *
*        RETURN CODES:                                                *
*        04 - INPUT WITH TMC FLAGGED AS PROTECTED AND NO RACF PROFILE *
*        08 - ACCESS FOR INPUT TAPE DENIED                            *
*        0C - INVALID RETURN CODE FROM RACHECK(>=12)                  *
*        10 - ALLOCATION TO DATA SET AFTER SCRATCH                    *
*        14 - RACDEF RETURN CODE OF 8                                 *
*        18 - ILLEGAL RETURN CODE FROM RACDEF (>8)                    *
*        1C - OUTPUT DATA SET WITH PROTECT=YES, AND ALREADY DEFINED   *
*             TO RACF, BUT NOT FLAGGED IN TMC                         *
*        20 - RACHECK FAILED ON OUTPUT TAPE                           *
*        24 - RETURN CODE 12 FROM OUTPUT RACHECK                      *
*        28 - 98000 TAPE                                              *
*        2C - UNAUTHORIZED USE OF NL TAPE                             *
*                                                                     *
*       ---------THIS MODULE IS P50230 ON PANVALET----------          *
*                                                                     *
***********************************************************************
ENTRY    BALR  R3,0
         USING *,R3
         USING SVCXWORK,R6
         USING IEC,R4                                             40095
         USING TMVT,R10
         LA    R13,SAVEAREA       POINT TO SAVE AREA
         ST    R2,SAVER2          SAVE RETURN ADDR FOR DSNBS      40095
         MVI   SWITCH,0           CLEAR THE SWITCH
         GETMAIN R,LV=SECWRKL      GET WORK AREA FOR THIS MODULE
         USING SECWRK,R1
         ST    R5,WRKR5            SAVE R5
         ST    R9,WRKR9            SAVE R9
         DROP  R1
         USING SECWRK,R5
         LR    R5,R1               ESTABLISH ADDRESSABILITY
         CLC   JFCBXPDT,=X'620000' IS IT A 98000 TAPE
         BE    ABND40              YES, GET OUT
         CLC   TMFLAG1,TMDELET     IS IT OUT OF AREA
         BO    ABND40              YES, ABEND AND GET OUT
******************************************************************
*   CHECK TO SEE IF IT IS A TEMPORARY DS AND BYPASS IF SO
******************************************************************
         TM    TMFLAG2,TMTEMPDS   TEMPORARY DATASET?
         BO    EXIT                YES - EXIT
******************************************************************
*   CHECK TO SEE IF IT IS OUTPUT
******************************************************************
         TM    0(R7),OUTPUT       Q. FILE OPENED FOR OUTPUT
         BO    PROUTPUT           A. YES - GO PROCESS OUTPUT
         EJECT
         TITLE 'IGC0BXXX - PROCESS INPUT TAPES'
******************************************************************
*
*   PROCESS INPUT TAPES
*
******************************************************************
*   THE PURPOSE OF THIS CODE IS TO FAIL JOBS ALLOCATING TO A DATA
*   SET AFTER IT WAS SCRATCHED, PROFILE DELETED, THEN MANUALLY
*   TAKEN OUT OF SCRATCH STATUS.
******************************************************************
*   CHECK FOR READ=NO AND IF SO FAIL THE TAPE
******************************************************************
         TM    TMDSSIF,X'C0'      PASSWORD PROTECTED?
         BO    IRACHECK           YES
         TM    TMDSSIF,X'80'      PASSWORD BIT AND NO READ BIT SET
         BO    ABND16             YES - FAIL IT
         B     EXIT
******************************************************************
*   SET UP AND ISSUE RACHECK
******************************************************************
IRACHECK MVC   RCPARM(RCPATL),RCPAT   MOVE PATTERN RACHECK
         RACHECK ENTITY=TMDSN,INSTLN=TMSKEY,VOLSER=TMVOLSER,           X
               ATTR=READ,MF=(E,RCPARM)
         B     *+4(R15)           TEST RETURN CODE
         B     EXIT                0 OK
         B     ABND04              4 NO PROFILE FAIL IT
         B     ABND08              8 FAIL IT
         B     ABND12              12 NOT USED
         EJECT
         TITLE 'IGC0BXXX - OUTPUT TAPE PROCESSING'
*
******************************************************************
*
*   PROCESS OUTPUT TAPES
*
******************************************************************
PROUTPUT OI    SWITCH,SWNOPASS    INDICATE NO PASSWORD ON OUTPUT
         TM    JFCBLTYP,JFCNL      IS TAPE NL??
         BNO   TEMPOUT             NO --
         MVC   NLRCHK(NLPATL),NLPAT YES, MOVE IN PATTERN
         RACHECK MF=(E,NLRCHK)    CHECK FOR AUTHORIZATION FOR NL
         LTR   R15,R15             CHECK RETURN
         BNZ   ABND44              NOT ZERO, ZONK IT
         B     EXIT                OTHERWISE EXIT
TEMPOUT  EQU   *
         CLC   =C'SYS7',JFCBDSNM  TEMPORARY FILE?                 03278
         BE    EXIT                YES                            03278
         CLC   =C'SYS8',JFCBDSNM  TEMPORARY FILE?                 03278
         BE    EXIT                YES                            03278
         TM    XFLAG1,VOLSWTCH     EOV??
         BNO   PROTCHK             NO-- IS PROTECT SPECIFIED
         L     R11,WRKR5           GET ADDR OF RB
         PRINT NOGEN
         L     R11,SVRB2(R11)      GET ADDR OF DCB
         USING IHADCB,R11
         L     R11,DCBDEBAD        GET ADDR OF DEB
         DROP  R11
         S     R11,=F'8'           DEB-8 = ADDR OF DEB EXTENSION
         L     R11,0(R11)          GET ADDR OF DEB EXTENSION
         USING  DEBXTN,R11
         TM    DEBXFLG1,DEBXDSSI   WAS FIRST VOLUME PROTECTED
         DROP  R11
         BNO   ORACHECK            NO - SEE IF IT IS ALREADY PROTECTED
         NI    SWITCH,255-SWNOPASS
         MVC   RDEFADD(RDPATAL),RDPATA MOVE PATTERN FOR ADDVOL
         RACDEF ENTITY=TMDSN,VOLSER=TMVOLSER,TYPE=ADDVOL,              X
               OLDVOL=JFCBVOLS,INSTLN=TMSKEY,                          X
               MF=(E,RDEFADD)
         B     RDFD150
         PRINT GEN
PROTCHK  TM    JFCFLGS1,JFCBPROT  CHECK FOR PROTECT=YES SPECIFIED
         BNO   ORACHECK           NO, CHECK OUTPUT TAPE
******************************************************************
*   SET UP AND ISSUE RACDEF
******************************************************************
         NI    SWITCH,255-SWNOPASS
         MVC   RDPARM(RDPATL),RDPAT   MOVE RACDEF PATTERN
         MVC   MODEL,JFCBDSNM      MOVE DSN TO WORK AREA
RDFD050  EQU   *
RDFD100  RACDEF ENTITY=TMDSN,TYPE=DEFINE,INSTLN=TMSKEY,                X
               VOLSER=TMVOLSER,                                        X
               MF=(E,RDPARM)
RDFD150  B     *+4(R15)               TEST RETURN CODE
         B     SETPSWD                 0 - OK
         B     RDFD175                 4 (PREV DEFINED) DO RACHECK
         B     ABND20                  8 FAIL IT
         B     ABND24                  12 FAIL IT (SHOULDN'T HAPPEN)
         B     ABND24                  16 FAIL IT (SHOULDN'T HAPPEN)
RDFD175  TM    TMDSSIF,X'C0'       IS TAPE PROTECTED
         BNO   ABND28              NO, FAIL IT
         B     RDFD200             CHECK AUTHORIZATION
******************************************************************
*   SET UP AND ISSUE RACHECK FOR OUTPUT
******************************************************************
ORACHECK TM    TMDSSIF,X'C0'       IS IT PASSWORD PROTECTED
         BNO   EXIT                NO, GET OUT
RDFD200  MVC   RCPARM(RCPATL),RCPAT   MOVE RACHECK PATTERN
         RACHECK ENTITY=TMDSN,INSTLN=TMSKEY,ATTR=UPDATE,               X
               MF=(E,RCPARM),VOLSER=TMVOLSER
         B     *+4(R15)               TEST RETURN CODE
         B     RCOK                    0 - OK
         B     EXIT                    4 (NO ENTRY) OK
         B     ABND32                  8 FAIL
         B     ABND36                  12 UNUSED
*
******************************************************************
*   RACHECK WAS OK TURN ON PASSWORD PROTECT BIT IN TMC
******************************************************************
RCOK     TM    SWITCH,SWNOPASS         NO PASSWORD SW ON?
         BO    EXIT                     YES - SKIP TMDSSIF SET
SETPSWD  LA    R1,PASSWORD             SET PASSWORD TO MASTER
         BAL   R2,TMPSWRD              CONVERT IT
         ST    R1,SVCXTEMP
         MVC   TMDSSPW,SVCXTEMP        POST THE PASSWORD
         MVI   TMDSSIF,X'C0'           INDICATE PASSWORD AND READ=YES
         B     EXIT                    DONE
*
         EJECT
         TITLE 'IGC0BXXX - DEFINE SUBROUTINES'
*
*
TMPSWRD  TMSPSWRD
         BR    R2
*
*                                                                 40095
CALLSVCY DS    0H                                                 40095
         LA    R1,TMSPARM         POINT TO PARAMETER LIST         40095
         SVC   SVCYYY             SVC Y                           40095
         BR    R2                 AND RETURN                      40095
*                                                                 40095
ABND04   LA    R15,4
         B     SECABND
ABND08   LA    R15,8
         B     SECABND
ABND12   LA    R15,12
         B     SECABND
ABND16   LA    R15,16
         B     SECABND
ABND20   LA    R15,20
         B     SECABND
ABND24   LA    R15,24
         B     SECABND
ABND28   LA    R15,28
         B     SECABND
ABND32   LA    R15,32
         B     SECABND
ABND36   LA    R15,36
         B     SECABND
ABND40   LA    R15,40
         B     SECABND
ABND44   LA    R15,44
*        B     SECABND
SECABND  EQU   *
         LR    R11,R5              STORE R5
         L     R5,WRKR5            RESTORE R5 TO RB
         ST    R15,SVCTEMP+8(R6)   STORE ABEND CODE
         UNPK  SVCTEMP(3,R6),SVCTEMP+11(2,R6)
         TR    SVCTEMP(2,R6),@HEXTABL-240
         MVC   SVCXCTL+11(2,R6),SVCTEMP(R6)
         LA    R1,6
         B     ABENDMSG
*
*
         EJECT
         TITLE 'IGC0BXXX - EXIT TO NEXT LOAD MODULE'
EXIT     DS    0H
         TM    TMDSSIF,X'C0'       CHECK IF PASSWORD PROTECTED
         BO    EXIT1               YES --
         MVI   TMDSSIF,X'00'       NO -- CLEAR IT
EXIT1    EQU   *
         LR    R1,R5               ADDR OF STORAGE TO FREE
         L     R9,WRKR9            RESTORE R9
         L     R5,WRKR5            RESTORE R5
         FREEMAIN R,LV=SECWRKL,A=(R1)
         LA    R11,IGC0FXXX       POINT TO MODULE NAME            12199
         TM    XFLAG1,VOLSWTCH    Q. EOV PROCESSING?              12199
         BO    XCTL               A. YES                          12199
         LA    R11,IGC0EXXX       POINT TO MODULE NAME            40189
         CLC   JFCBFLSQ,=H'01'    Q. SECONDARY DATA SET           40095
         BNH   XCTL               A. LOW,EQUAL - NO               40095
         TM    JFCBLTYP,X'63'     Q. LABEL TYPE SL OR NL          04101
         BZ    XCTL                GO TO F IF ZERO                04101
         LA    R11,IGC02XXX       POINT TO MODULE NAME            04095
         L     R2,SAVER2          LOAD RETURN ADDR FOR DSNBS      04095
*
XCTL     DS    0H
         MVC   SVCXXCTL+11(2),0(R11)  COMPLETE LOAD NAME          40095
         LA    R1,SVCXXCTL        POINT TO PARAMETER LIST
         LR    R15,R1             POINT TO XCTL PARAMETER LIST
         XCTL  MF=(E,(1)),SF=(E,(15))
         EJECT
         TITLE 'IGC0BXXX - CONSTANT SECTION'
IGC02XXX DC    C'02'              DSNB PROCESSING                 40095
IGC0EXXX DC    C'0E'              FINAL PROCESSING                40189
IGC0FXXX DC    C'0F'              EOV PROCESSING                  12199
MOVMOD   MVC   MODEL(0),JFCBDSNM
TMSKEY   DC    CL7'**TMS**'
MODVOL   DC    CL6'MODEL '
*
PASSWORD DC    CL8'(TMS)321'
         PRINT DATA
PATCH    DC    64S(*)
***********************************************************************
*    PATTERN FOR RACHECK FOR NL TAPES
***********************************************************************
NLPAT    RACHECK CLASS='TIMS',ATTR=READ,ENTITY=NLRESRC,MF=L
NLPATL   EQU    *-NLPAT
NLRESRC  DC    CL8'NLTAPE'
*
***********************************************************************
*    PATTERN FOR RACHECK FOR TAPEVOL
***********************************************************************
RCPAT    RACHECK CLASS='DATASET',MF=L
RCPATL   EQU   *-RCPAT
*
***********************************************************************
*    PATTERN FOR RACDEF -- DEFINE
***********************************************************************
RDPAT    RACDEF  CLASS='DATASET',TYPE=DEFINE,MF=L
RDPATL   EQU   *-RDPAT
*
***********************************************************************
*    PATTERN FOR RACDEF -- ADDVOL
***********************************************************************
RDPATA   RACDEF CLASS='DATASET',TYPE=ADDVOL,MF=L
RDPATAL  EQU   *-RDPATA
*
***********************************************************************
*    EQUATES
***********************************************************************
CVTPTR   EQU   16
CVTTCBP  EQU   0
TCBJSTCB EQU   124                JOB STEP TCB POINTER
TCBJSCB  EQU   180                POINTER TO JSCB
JSCBSWT1 EQU   243                JSCB SWITCH 1
JSCBPASS EQU   X'80'
CURRTCB  EQU   0
CURRASCB EQU   12
ASCBASXB EQU   X'6C'
ASXBSENV EQU   X'C8'
         LTORG ,
         TITLE 'IGC0BXXX - DEFINE DSECTS'
*
SECWRK   DSECT
MODEL    DS    CL44                MODEL FOR RACDEF
WRKR5    DS    A
WRKR9    DS    A
         DS    0F                  ALIGN ON FULLWORD BOUNDARY
NLRCHK   RACHECK CLASS='TIMS',MF=L
RDEFADD  RACDEF MF=L,CLASS='DATASET',TYPE=ADDVOL
GTFREGS  DS    16F
GTFU001  GTRACE MF=L
SECWRKL  EQU   *-SECWRK
SVCXWORK DSECT ,
         SVCXWORK
*
**                 DEFINE PASSWORD WORK AREA   (REDEFINES IOAREA2)
*
         ORG   IOAREA2
*
DWORD    DS    0D
*                                                                 40095
SAVER2   DS    A                  RETURN ADDR FOR DSNBS           40095
*
SWITCH   DS    X                  SWITCH
SWNOPASS EQU   1                  INDICATE NO PASSWORD REQUIRED
*
         SPACE 2
RCPARM   RACHECK CLASS='DATASET',MF=L
         ORG   RCPARM
RDPARM   RACDEF  CLASS='DATASET',MF=L
         ORG   ,
         SPACE 2
WORKEND  EQU   *-IOAREA2          CANNOT EXCEED 210 BYTES (X'0D2')
         ORG   ,
         SPACE 3
IEC      DSECT ,                                                  40095
         TMSIEC ,                                                 40095
         SPACE 2                                                  40095
TMVT     DSECT ,
         TMVT  TYPE=DSECT,TMVTEXT=NO
         SPACE 2
         PRINT NOGEN
TIOT     DSECT ,
         IEFTIOT1
         SPACE 2
DCB      DCBD  DSORG=(QS),DEVD=(DA)
         SPACE 2
         PRINT GEN
         IEZDEB LIST=YES
         SPACE 2
         IHAACEE ,
         END
