*          DATA SET P01820     AT LEVEL 001 AS OF 01/15/81
P01820   START 0
*
*        THIS ROUTINE WILL RETRIEVE JOB INFORMATION FROM THE
*              OPERATING SYSTEM AND RETURN IT TO THE CALLER.
*              A STANDARD EXTRACT MACRO IS USED.  THE FORMAT
*              OF THE AREA RETURNED TO THE CALLER WILL BE
*
*                   OSJOBLEN - FULLWORD BINARY, 4 BYTES
*                   OSJOBNM  - CHARACTER, 8 BYTES
*                   JBSTPLEN - FULL WORD BINARY, 4 BYTES
*                   JBSTPNM  - CHARACTER, 8BYTES
*                   PSTPLEN  - FULL WORD BINARY, 4 BYTES
*                   PSTPNM   - CHARACTER, 8 BYTES
*
*                        WHERE
*
*                   OSJOBLEN, JBSTPLEN, PSTPLEN ARE THE NUMBER OF
*                        NON-BLANK CHARACTERS IN OSJOBNM, JBSTPNM,
*                        AND PSTPNM, RESPECTIVELY.
*
*                        AND
*
*                   OSJOBNM - OS JOB NAME, LEFT JUSTIFIED
*                   JBSTPNM - JOB STEP NAME, LEFT JUSTIFIED
*                   PSTPNM  - CATALOGED PROC STEP NAME, LEFT JUSTIFIED
*
         EJECT
*
*        REGISTER EQUATES
*
         STREG
         EJECT
*
*        ESTABLISH STANDARD LINKAGE
*
         SAVE  (14,12),,*
         BALR  R2,R0               R2 - BASE REGISTER
         USING *,R2
         LA    R3,SAVEAREA         CHAIN SAVE AREAS
         ST    R13,4(R3)
         ST    R3,8(R13)
         LR    R13,R3
         ST    R1,SAVE1
         B     DOEXTRCT
         SPACE 3
SAVEAREA DC    18F'0'
SAVE1    DC    F'0'
         EJECT
*
*        ISSUE EXTRACT MACRO FOR OS JOB DATA
*
         DS    0F
DOEXTRCT EXTRACT ATIOT,,FIELDS=TIOT
         B     PROCTIOT
         SPACE 3
         DS    0F
ATIOT    DC    F'0'
         EJECT
*
*        PROCESS THE TIOT DATA
*
         DS    0F
PROCTIOT L     R3,ATIOT
         MVC   OSJOBNM(8),0(R3)
         MVC   JBSTPNM(8),8(R3)
         MVC   PSTPNM(8),16(R3)
         LA    R1,OSJOBNM          LENGTH OF OS JOB NAME
         BAL   R12,DELIMIT
         ST    R11,OSJOBLEN
         LA    R1,JBSTPNM          LENGTH OF JOB STEP NAME
         BAL   R12,DELIMIT
         ST    R11,JBSTPLEN
         LA    R1,PSTPNM           LENGTH OF PROC STEP NAME
         BAL   R12,DELIMIT
         ST    R11,PSTPLEN
         B     RETNDATA
         SPACE 3
         DS    0F
OSJOBLEN DC    F'0'
JBSTPLEN DC    F'0'
PSTPLEN  DC    F'0'
OSJOBNM  DC    CL9' '
JBSTPNM  DC    CL9' '
PSTPNM   DC    CL9' '
         EJECT
*
*        RETURN OS JOB DATA TO CALLER
*
         DS    0F
RETNDATA L     R3,SAVE1
         L     R4,0(R3)            R4 - ADDRESS OF USER DATA AREA
         MVC   0(4,R4),OSJOBLEN
         MVC   4(8,R4),OSJOBNM
         MVC   12(4,R4),JBSTPLEN
         MVC   16(8,R4),JBSTPNM
         MVC   24(4,R4),PSTPLEN
         MVC   28(8,R4),PSTPNM
*
         L     R13,SAVEAREA+4
         SR    R15,R15
         RETURN (14,12),,RC=(15)
         EJECT
*
*        PICK UP A CHARACTER STRING TO NEXT DELIMITER (BLANK OR COMMA)
*              INPUTS
*                   R1  - ADDRESS OF FIRST CHARACTER
*                   R12 - RETURN ADDRESS
*              OUTPUTS
*                   R10 - ADDRESS OF DELIMITED STRING
*                   R11 - LENGTH OF STRING
*
         DS    0F
DELIMIT  LA    R11,0
DNEXT    LA    R10,0(R1,R11)
         CLI   0(R10),C' '
         BE    DFOUND
         CLI   0(R10),C','
         BE    DFOUND
         LA    R11,1(R11)
         B     DNEXT
DFOUND   SR    R10,R1              COMPUTE LENGTH
         LR    R11,R10
         LR    R10,R1              SET DELIMITED STRING ADDRESS
         BR    R12
*
         END   P01820
