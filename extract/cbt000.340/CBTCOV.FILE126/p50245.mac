*          DATA SET P50245     AT LEVEL 015 AS OF 08/06/80
*          DATA SET P50245     AT LEVEL 014 AS OF 08/06/80
*          DATA SET P50245     AT LEVEL 008 AS OF 08/05/80
P50245   CSECT
 TITLE '****** ALTER LAST CHANGED DATE FOR RACF USER &SYSTIME &SYSDATE'
         PRIME
         REGISTER
         EJECT
         SPACE  10
***********************************************************************
*                                                                     *
*  THIS PROGRAM IS A TSO COMMAND PROCESSOR TO CHANGE THE DATE LAST    *
*  CHANGED FIELD FOR A USER TO 99365. THIS EFFECTIVELY MAKES THE      *
*  USERS PASSWORD NON-EXPIRABLE.                                      *
*                                                                     *
*                      F O R M A T                                    *
*                                                                     *
*                      ALUD USERID                                    *
*                                                                     *
*             USERID IS THE RACF USER ID YOU WANT ALTERED.            *
*                                                                     *
*                                                                     *
*  THE ONLY RESTRICTION FOR USE OF THIS COMMAND PROCESSOR IS YOU MUST *
*  AUTHORIZED TO USE SVC 231.                                         *
*                                                                     *
***********************************************************************
         EJECT
         LR    R2,R1               CPPL
         USING CPPL,R2
         LA    R4,IOPLAREA            IOPL ADDRESS
         USING IOPL,R4
         MVC   IOPLUPT,CPPLUPT        SET UP IOPL FOR PUTLINES
         MVC   IOPLECT,CPPLECT
         MVC   IOPLECB(4),=XL4'00'
         DROP  R4
 TITLE '***** SCAN INPUT BUFFER                  &SYSTIME &SYSDATE'
         LA    R10,36              CSPL LENGTH
         GETMAIN R,LV=(10),SP=1
         LR    R3,R1
         USING CSPL,R3
         MVC   CSPLUPT,CPPLUPT        USER PROFILE
         MVC   CSPLECT,CPPLECT        ENVIRONMENT
         LA    R15,ECB
         ST    R15,CSPLECB
         LA    R15,CSPLFLW            FLAG
         ST    R15,CSPLFLG
         LA    R15,CSPLOAR
         ST    R15,CSPLOA
         MVC   CSPLCBUF,CPPLCBUF      BUFFER
         XC    CSPLFLW,CSPLFLW
         OI    CSPLFLW,X'80'          NO SYNTAX CHECK
         L     R15,CSPLCBUF
         XC    2(2,R15),2(R15)        CLEAR OFFSET BY TMP
         CALLTSSR EP=IKJSCAN,MF=(E,CSPL)
         LTR   R15,R15
         BZ    SCANOK
         ABEND 1,DUMP
 TITLE '***** PARSE INPUT BUFFER                 &SYSTIME &SYSDATE'
SCANOK   EQU   *
         LA    R0,28                  LENGTH OF PPL
         GETMAIN R,LV=(0)
         LR    R4,R1
         USING PPL,R4
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         LA    R15,ECB
         ST    R15,PPLECB
         LA    R15,ANSWER
         ST    R15,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         SR    R1,R1
         ST    R1,PPLUWA
         L     R1,=A(COMMAND)
         ST    R1,PPLPCL
         LR    R1,R4                  PPL
         DROP  R2,R3,R4
         CALLTSSR EP=IKJPARS
         LTR   R15,R15
         BZ    CHKANSW
         SR    R14,R14
         ST    R14,GFCBPTR
         ST    R15,GFRCODE            STORE RETURN CODE TO ANALYZE
         ST    R14,GF02PTR
         MVC   GFCALLID,=X'0015'      PARSE
         ST    R2,GFCPPLP
         LA    R1,GFCPARM
         LINK  EP=IKJEFF19
         B     EXIT
CHKANSW  EQU   *
         CLI   ANSWER,X'FF'           PARSE OK ? ? ? ?
         BNE   OKPARSE
         ABEND 2,DUMP
OKPARSE  EQU   *
         L     R2,ANSWER
         USING IKJPARMD,R2
         LA    R15,ID
         TM    6(R15),X'80'           WAS USER ID SUPPLIED
         BO    MOVID
         PUTLINE PARM=WRITETRM,                                        X
               OUTPUT=(MSG1,TERM,SINGLE,DATA),                         X
               MF=(E,IOPLAREA)
         B     EXIT
MOVID    EQU   *
         LH    R14,4(R15)             SIZE
         STC   R14,USERL
         BCTR  R14,0
         L     R15,0(R15)
         EX    R14,MVCUSER
         LA    R15,DATE
         TM    6(R15),X'80'           WAS USER ID SUPPLIED
         BO    MOVDATE
         PUTLINE PARM=WRITETRM,                                        X
               OUTPUT=(MSG2,TERM,SINGLE,DATA),                         X
               MF=(E,IOPLAREA)
         B     EXIT
MOVDATE  EQU   *
         LH    R14,4(R15)             SIZE
         CH    R14,=H'5'              MUST BE FIVE. . . .
         BE    PACKDT
         PUTLINE PARM=WRITETRM,                                        X
               OUTPUT=(MSG3,TERM,SINGLE,DATA),                         X
               MF=(E,IOPLAREA)
         B     EXIT
PACKDT   EQU   *
         BCTR  R14,0
         L     R15,0(R15)
         EX    R14,MVCDATE
         PACK  NEWLDATE,LDATE
 TITLE '***** RACF INTERFACE VIA SVC 132         &SYSTIME &SYSDATE'
         LA    R1,AUTH
         SVC   231
         ICHEINTY MF=(E,ALTER),ENTRY=USERL
         LTR   R15,R15
         BZ    OKMSG
         ABEND 3,DUMP
OKMSG    EQU   *
         SR    R1,R1
         SVC   231
         PUTLINE PARM=WRITETRM,                                        X
               OUTPUT=(MSG4,TERM,SINGLE,DATA),                         X
               MF=(E,IOPLAREA)
 TITLE '***** EXIT                               &SYSTIME &SYSDATE'
EXIT     TERME
         LTORG
 TITLE '***** CONSTANTS AND WORK AREAS           &SYSTIME &SYSDATE'
AUTH     DC    CL4'AUTH'
MVCUSER  MVC   USER(0),0(R15)
MVCDATE  MVC   LDATE(0),0(R15)
WRITETRM PUTLINE MF=L
IOPLAREA DC    4F'0'                  IOPL
         DS    0F
MSG1     DC    AL2(ENDMSG1-MSG1)
         DC    AL2(0)
         DC    C'USER ID MUST BE SUPPLIED'
ENDMSG1  EQU   *
         DS    0F
MSG2     DC    AL2(ENDMSG2-MSG2)
         DC    AL2(0)
         DC    C'DATE LAST CHANGED MUST BE SUPPLIED'
ENDMSG2  EQU   *
         DS    0F
MSG3     DC    AL2(ENDMSG3-MSG3)
         DC    AL2(0)
         DC    C'DATE LAST CHANGED MUST BE FIVE CHARACTERS'
ENDMSG3  EQU   *
         DS    0F
MSG4     DC    AL2(ENDMSG4-MSG4)
         DC    AL2(0)
         DC    C'USER ID HAS BEEN MODIFIED'
ENDMSG4  EQU   *
ECB      DC    F'0'
USERL    DC    C' '
USER     DC    CL8' '
NEWLDATE DC    PL3'0'                 NEW DATE
LDATE    DC    CL5' '
ANSWER   DC    F'0'
 TITLE '***** RACF MACROS MF=L                   &SYSTIME &SYSDATE'
ACTION   ICHEACTN MF=L,FIELD=PASSDATE,                                 X
               FLDATA=(3,NEWLDATE)
         SPACE 3
ALTER    ICHEINTY ALTERI,MF=L,TYPE='USR',ACTIONS=ACTION
 TITLE '******* PARSE PARAMETER LIST FOR ALUD ******'
*
*        PARSE PARAMETER LIST FOR ALUD
*
         PRINT NOGEN
COMMAND  IKJPARM
USERID   IKJKEYWD DEFAULT='USERID'
         IKJNAME 'USERID',                                             X
               SUBFLD=SUBID
NEWDATE  IKJKEYWD DEFAULT='DATE(99365)'
         IKJNAME 'DATE',                                               X
               SUBFLD=SUBDATE
SUBID    IKJSUBF
ID       IKJIDENT 'USERID',                                            X
               MAXLNTH=4,                                              X
               FIRST=ALPHA,                                            X
               OTHER=ALPHANUM,                                         X
               HELP=('USERID IS FOUR CHARACTERS LONG')
SUBDATE  IKJSUBF
DATE     IKJIDENT 'DATE',                                              X
               MAXLNTH=5,                                              X
               FIRST=NUMERIC,                                          X
               OTHER=NUMERIC,                                          X
               HELP=('DATE LAST CHANGED IS JULIAN YYDDD')
         IKJENDP
         PRINT GEN
 TITLE '***** GENRAL FAIL CONTROL BLOCK   ******'
*
*        GENERAL FAIL CONTROL BLOCK
*
         IKJEFFGF
GFCPARM  DC    A(GFPARMS)
 TITLE '******* COMMAND PARAMETER LIST (CPPL) ******'
*
*        COMMAND PARAMETER LIST
*
         IKJCPPL
 TITLE '******* COMMAND SCAN PARAMTER LIST    ******'
*
*        COMMAND SCAN PARAMETER LIST
*
         IKJCSPL
CSPLFLW  DS    F                      FLAG
CSPLOAR  DS    2F                     OUTPUT AREA
 TITLE '******* PARSE PARAMETER LIST          ******'
*
*        PARSE PARAMETER LIST
*
         IKJPPL
 TITLE '***** IOPL FOR PUTLINE     *****'
*
*        DSECT FOR INPUT OUTPUT PARAMETER LIST (PUTLINE)
*
         IKJIOPL
 TITLE '***** CVT                   *****'
*
*        CVT
*
         CVT
         END
