*          DATA SET P50013     AT LEVEL 005 AS OF 10/30/80
IGC0023A CSECT
***********************************************************************
*
*     THIS USER SVC (231 DECIMAL/E7 HEX) WILL PROVIDE MODESET
*     AUTHORIZATION UPON INVOCATION.  IT IS THEN THE USER'S
*     RESPONSIBILITY TO ISSUE A MODESET MACRO IN ORDER TO
*     GAIN SUPERVISOR STATE OR KEY ZERO.  REGISTER ONE MUST
*     POINT TO THE CHARACTERS 'AUTH' FOR MODESET CAPABILITY,
*     AND CAN POINT TO ANYTHING ELSE TO RELINQUISH THE
*     MODESET CAPABILITY.
*
***********************************************************************
         USING *,6                 ENTRY BASE IS REG 6
         L     R9,180(R4)          GET POINTER TO JSCB
         CLC   0(4,R1),=CL4'AUTH'  SET REQUEST
         BNE   RESET               NO, RESET USER AUTH
         L     R15,16              GET ADDR OF CVT
         USING CVT,R15
         L     R15,CVTGDA          GET ADDR OF GDA
         USING GDA,R15
         L     R7,PASTRT           GET BEGINNING OF PRIVATE AREA
         USING RBBASIC,R8          GET ADDRESSIBILITY TO RB
         LR    R8,R5               COPY ADDR OF CURRENT RB
         L     R8,RBLINK           GET CALLER'S RB
         L     R11,RBCDE           GET CALLER'S CDE
         USING CDENTRY,R11         SET UP ADDRESSIBILITY
         CLI   RBOPSW+3,X'E7'      CHECK INTERUPT CODE
         BE    *+6
         DC    X'0700'
         L     R8,RBOPSW+4         GET INVOKER'S ADDRESS
         DROP  R8
         CR    R8,R7               IS INVOCATION FROM BELOW PRIVATE??
         BL    SET                 YES-- DO IT
         A     R7,PASIZE           GET END OF PRIVATE AREA
         CR    R8,R7               IS INVOCATION FROM ABOVE PRIVATE??
         BH    SET                 YES-- DO IT
         GETMAIN R,LV=RCHKLEN      GET SOME STORAGE
         LR    R10,R1              COPY ADDR
         MVC   0(RCHKLEN,R10),RCHKPAT  MOVE IN PATTERN FOR RACHECK
         LA    R11,CDNAME          GET NAME OF INVOKER
         DROP  R11
         RACHECK MF=(E,(R10)),ENTITY=((R11)) CHECK FOR AUTHORIZATION
         LTR   R8,R15              COPY RETURN CODE
         C     R8,=F'4'            WAS RESOURCE FOUND? ?
         BNE   FREEIT              YES- PROCEED
         USING RCHKPAT,R10
         MVC   NOTFND+17(8),0(R11) NO-- MOVE INTO MESSAGE
         WTO   MF=(E,NOTFND)
         LA    R8,0                NOP THIS INSTRUCTION TO ENFORCE
*                                  SVC231 USEAGE
FREEIT   FREEMAIN R,LV=RCHKLEN,A=(R10) GET RID OF PARM LIST
         LTR   R8,R8
         BNZ   *+8                 DON'T AUTHORIZE UNLESS RACHECK OK
SET      OI    236(R9),X'01'       PROVIDE AUTHORIZATION
         BR    R14                 RETURN
RESET    NI    236(R9),X'FE'       RESET AUTH BIT
         BR    R14                 RETURN TO SYSTEM
RCHKPAT  RACHECK MF=L,ATTR=READ,                                       X
               CLASS='$SVC231'
NOTFND   WTO   'RACHECK PROG(XXXXXXXX) CLASS($SVC231) - NOT FOUND',    X
               MF=L
RCHKLEN  EQU   *-RCHKPAT
         REGEQU
         PRINT NOGEN
         IHACDE
         CVT DSECT=YES
         IHAGDA
         IHARB
         END
