*          DATA SET P50257     AT LEVEL 001 AS OF 02/18/81
         PRINT  GEN
P50257   CSECT
*
*         PRINT USER ID LOGON INFORMATION
*            UTILIZES RACF INFORMATION AND UADS ENTRIES
*            TO SELECT AND PRINT INFORMATION ABOUT
*            RACF USER LOGONS.
*
*         //STEP010  EXEC RACFUSER,OPTION=OOOOO,DAYS=DDDDD
*
*                 OOOOO = AUDIT OR RUN
*                      AUDIT    WILL LIST ALL USERS
*                         WHO HAVE NOT LOGGED ON FOR THE NUMBER
*                         OF DAYS SPECIFIED.  USERS WHO HAVE BEEN
*                         ADDED AFTER THE DETERMINED CUTOFF DATE
*                         WILL BE BYPASSED.
*                      RUN      PERFORMS THE SAME AS THE AUDIT
*                         OPTION, EXCEPT THAT THE SELECTED USERS
*                         WILL HAVE THE WORD DELETE PRINTED NEXT
*                         TO THEM.
*                 DDDDD = THE NUMBER OF DAYS SINCE THE LAST LOGON.
*                         THE NUMBER CANNOT BE GREATER THAN 365
*                         HOWEVER 000365 IS VALID AS IS 10.  IF THE
*                         NUMBER OF DAYS IS ZERO, ALL USERS WILL
*                         BE LISTED.
*               THE DEFAULTS ARE OPTION=AUDIT,DAYS=00
*
*
         REGISTER
         USING *,R12
         SAVE  (14,12)             SAVE REGISTERS
         LR    R12,R15             GET BASE ADDR
         ST    R13,SAVE+4          SAVE POINTER TO CALLERS SAVE AREA
         LR    R10,R13
         LA    R13,SAVE            GET ADDR SAVE AREA
         ST    R13,8(R10)          SAVE IN CALLERS SAVE AREA
         B     BEGIN
SAVE     DS    9D                   REGISTER SAVE AREA
BEGIN    L     R1,0(R1)            GET ADDR PARM
         LH    R2,0(R1)            GET LENGTH OF PARM
         LTR   R2,R2               WAS PARM ENTERED
         BZ    NOPARM              NO - USE DEFAULTS
         LA    R1,2(R1)            POINT TO START OF PARM
         CLC   0(5,R1),=C'AUDIT'   IS THIS THE DEFAULT
         BE    CHKDAYS             CHECK FOR DAYS
         CLC   0(3,R1),=C'RUN'     IS THE OPTION = RUN
         BNE   CHKDAYS             NO - THEN USE AUDIT
         MVI   RUNWORK,C'R'        SAVE RUN OPTION TILL LATER
CHKDAYS  CLI   0(R1),C','          FIND THE COMMA
         BE    GOTIT               GOT IT
         LA    R1,1(R1)            INCREMENT POINTER
         BCT   R2,CHKDAYS          CONTINUE
         B     NOPARM              NO COMMA - USE ZERO DAYS
GOTIT    BCTR  R2,0                DECREMENT NUMBER OF CHARACTERS
         LR    R3,R2               SAVE LENGTH OF DAYS
         AR    R1,R2               POINT TO LAST DIGIT
         LA    R4,DAYWORK+7        POINT TO LAST DIGIT OF WORK AREA
CHKNUM   CLI   0(R1),C'0'          VERIFY THAT DAYS ARE NUMERIC
         BL    NOPARM
         CLI   0(R1),C'9'
         BH    NOPARM
         MVC   0(1,R4),0(R1)       MOVE TO WORK AREA
         BCTR  R4,0                POINT TO NEXT POSITION
         BCTR  R1,0                NEXT CHARACTER
         BCT   R3,CHKNUM           CONTINUE
         PACK  DOUBL,DAYWORK       PACK DAYS
         CVB   R3,DOUBL            GET BINARY DAYS
         TIME
         LR    R0,R1               SAVE DATE
         N     R0,=X'0000FFFF'     GET RID OF YEAR
         N     R1,=X'00FF0000'     GET RID OF DAYS
         SRL   R1,12               X'00000YY0'
         O     R1,=X'0000000F'     X'00000YYF'
         ST    R0,CURDAY           SAVE CURRENT DATE
         ST    R1,CURYEAR          SAVE CURRENT YEAR
         SP    CURDAY,DOUBL        SUBTRACT DAYS FROM TODAY
         BP    GOTDATE             OK IF THIS YEAR
         SP    CURYEAR,=PL1'1'     GET LAST YEAR
         ZAP   DOUBL,CURYEAR
         CVB   R1,DOUBL            CONVERT YEAR TO BINARY
         N     R1,=X'00000003'     WAS LAST YEAR LEAP YEAR
         BZ    LEAPYR
         AP    CURDAY,=PL2'365'    ADD NUMBER OF DAYS LAST YEAR
         BNP   NOPARM              CANT GO BACK MORE THAN ONE YEAR
         B     GOTDATE
LEAPYR   AP    CURDAY,=PL2'366'    ADD NUMBER OF DAYS LAST YEAR
         BNP   NOPARM              CANT GO BACK MORE THAN ONE YEAR
GOTDATE  L     R1,CURYEAR
         N     R1,=X'FFFFFFF0'     GET RID OF SIGN
         SLL   R1,12               SHIFT TO RIGHT POSITION
         O     R1,CURDAY           YYDDDF  COMBINE YEAR AND DAY
         ST    R1,CUTOFF           SAVE AS CUTOFF DATE
         STH   R3,DAYS             INDICATE THAT DAYS HAVE BEEN ENTERED
         MVC   RUNOPT,RUNWORK      SET RUN OPTION IF PARM IS OKAY
NOPARM   OPEN  (FILE,(OUTPUT),PDS,(INPUT))
         LA    R1,AUTH
         SVC   231
NEXT     ICHEINTY  MF=(E,XXX),ENTRY=ENTRY,WKAREA=WORK
         LTR   R15,R15
         BNZ   EXIT
         CLC   DAYS,=H'0'                HAS DAYS BEEN ENTERED
         BE    SKIPCK                    NO - SKIP THE CHECK
         CLC   LOGDATE,=X'FFFFFF'        HAS HE EVER LOGGED ON
         BE    USEAUTH                   NO - USE DATE HE WAS ADDED
         CP    LOGDATE,CUTOFF            HOW MANY DAYS SINCE LOGON
         BH    NEXT                      SKIP IF LOGGED ON SINCE CUTOFF
         B     SKIPCK
USEAUTH  CP    IAUTH,CUTOFF              HOW LONG SINCE USER DEFINED
         BH    NEXT                      SKIP IF DEFINED AFTER CUTOFF
SKIPCK   MVI   OUTREC,C' '
         MVC   OUTREC+1(79),OUTREC       CLEAR OUTPUT RECORD
         MVC   OUSER,=C'        '        MOVE IN USER ID
         SR    R1,R1                     CLEAR REGISTER
         IC    R1,ENTRY                  GET LENGTH OF USER ID
         BCTR  R1,0                      DECREMENT FOR MOVE
         EX    R1,MOVUSER                MOVE USER ID
         MVC   ONAME,INAME               MOVE IN NAME
         SR    R1,R1                     CLEAR REGISTER
         IC    R1,INCR                   GET PASSWORD CHANGE INCR
         CVD   R1,DOUBL                  CONVERT TO DECIMAL
         UNPK  OINCR,DOUBL               UNPACK INCREMENT
         OI    OINCR+2,X'F0'
*
*     COMPUTE THE DATE FOR PASSWORD EXPIRATION
*
         CP    PASSCHG,=PL1'0'           HAS PASSWORD EVER BEEN CHANGED
         BNE   LOOKCHG                   YES - CONTINUE
         ZAP   DOUBL,=PL1'0'             NO - USE ZERO
         B     OKMOV                     CONTINUE
LOOKCHG  AP    DOUBL,PASSCHG             ADD LAST CHANGED DATE AND INCR
         CP    DOUBL+6(2),=PL2'366'      CHECK FOR NEXT YEAR
         BL    OKMOV                     NO - CONTINUE
         MVC   DOUBL2,DOUBL              SAVE DATE
         SR    R1,R1                     CLEAR WORK REGISTER
         IC    R1,DOUBL+5                INSERT YEAR - DECIMAL
         SLL   R1,4                      MAKE ROOM FOR SIGN
         O     R1,=F'15'                 MAKE A VALID SIGN
         ST    R1,DOUBL2+4               SAVE RESULT - YEAR
         CVB   R1,DOUBL2                 CONVERT
         STH   R1,HALF                   SAVE BINARY RESULT
         TM    HALF+1,X'03'              CHECK FOR LEAP YEAR
         BZ    LEAP
         AP    DOUBL,=PL3'1000'          INCREMENT YEAR
         SP    DOUBL,=PL2'365'           DECREMENT DAYS
         B     OKMOV                     MOVE TO OUTPUT
LEAP     CP    DOUBL+6(2),=PL2'367'      CHECK FOR NEXT YEAR
         BL    OKMOV                     NO - CONTINUE
         AP    DOUBL,=PL3'1000'          INCREMENT YEAR
         SP    DOUBL,=PL2'366'           DECREMENT DAYS
OKMOV    UNPK  EXPDATE,DOUBL             UNPACK TO OUTPUT
         OI    EXPDATE+4,X'F0'
*     GET OTHER LOGON INFORMATION
         CLC   LOGON,=X'FFFFFFFF'        HAS HE EVER LOGGED ON
         BNE   LOGOK                     YES - CONTINUE
         MVC   LOGON,=F'0'               NO - INITIALIZE TO ZERO
         MVC   LOGDATE,=X'00000F'        INITIALIZE TO ZERO
LOGOK    MVC   UNDATA,LOGON              MOVE TO WORK AREA
         UNPK  OLOG(9),UNPAK            UNPACK LOGON TIME
         UNPK  OLDATE,LOGDATE            UNPACK LOGON DATE
         OI    OLDATE+4,X'F0'
         UNPK  OCHANG,PASSCHG            UNPACK PASSWORD LAST CHANGE
         OI    OCHANG+4,X'F0'
         MVI   ODELETE,C' '              RESET DELETE SWITCH
         CLI   RUNOPT,C'R'               CHECK FOR OPTION=RUN
         BNE   NODELETE                  NO - DONT DELETE USER
         MVI   ODELETE,C'D'              MARK FOR DELETE
NODELETE MVC   SAVUSER,OUSER             MOVE USER ID TO WORK AREA
         LA    R1,8                      SET COUNTER
         LA    R2,SAVUSER                ADDR OF SAVE AREA
         MVI   OUADS,C'N'                INITIALIZE UADS ANSWER
USRLOOP  CLI   0(R2),C' '                FIND END OF USER ID
         BE    INS0
         LA    R2,1(R2)                  INCREMENT POINTER
         BCT   R1,USRLOOP                CONTINUE SEARCH
         B     NOINS0                    SKIP IT
INS0     MVI   0(R2),C'0'                INSERT ZERO
NOINS0   FIND  PDS,SAVUSER,D             DOES UADS ENTRY EXIST
         LTR   R15,R15                   WAS MEMBER FOUND
         BNZ   WRITE                     NO - DONT INSERT A 'Y'
         MVI   OUADS,C'Y'                YES UADS ENTRY DOES EXIST
WRITE    PUT   FILE,OUTREC               WRITE RECORD
         B     NEXT
EXIT     CLOSE (FILE,,PDS)               CLOSE FILES
         SR    R1,R1
         SVC   231                       NOT AUTHORIZED
         L     R13,SAVE+4
         RETURN (14,12),RC=0
MOVUSER  MVC   OUSER(0),ENTRY+1          MOVE USER ID
CUTOFF   DS    F                         CUTOFF DATE FOR SEARCH
CURDAY   DS    F                         PACKED JULIAN DAY
CURYEAR  DS    F                         PACKED YEAR YY
DAYWORK  DC    CL8'00000000'
SAVUSER  DS    CL8                       SAVE USER ID
RUNOPT   DC    CL1'A'                    RUN OPTION A=AUDIT R=RUN
RUNWORK  DC    CL1'A'                    RUN OPTION A=AUDIT R=RUN
DAYS     DC    H'0'                      NUMBER OF DAYS
HALF     DS    H                         WORK AREAS
DOUBL    DC    D'0'
DOUBL2   DC    D'0'
NAME     ICHEACTN  MF=L,FIELD=PGMRNAME
INT      ICHEACTN  MF=L,FIELD=PASSINT
LTIME    ICHEACTN  MF=L,FIELD=LJTIME
LDATE    ICHEACTN  MF=L,FIELD=LJDATE
PDATE    ICHEACTN  MF=L,FIELD=PASSDATE
RACAUTH  ICHEACTN  MF=L,FIELD=AUTHDATE
XXX      ICHEINTY  NEXTC,TYPE='USR',MF=L,WKAREA=WORK,                  X
               ACTIONS=(NAME,INT,LTIME,LDATE,PDATE,RACAUTH)
ENTRY    DC    AL1(8)
         DC    CL8'        '
         DS    0F
UNPAK    DS    0CL5
UNDATA   DS    F
         DC    X'0C'
         DS    0F
AUTH     DC    CL4'AUTH'
WORK     DC    F'400'       LENGTH OF ENTIRE WORK AREA IN BYTES
         DS    5F           FILLER
BUFLEN   DS    F            LENGTH OF ENTIRE BUFFER
         DS    H            LENGTH OF NAME
INAME    DS    CL20         20 CHARACTER NAME
         DS    CL2
INCR     DS    CL1          NUMBER OF DAYS TILL PASSWORD CHANGE
         DS    CL2
LOGON    DS    CL4          LAST TIME LOGGED ON (PACKED - NO SIGN)
         DS    CL2
LOGDATE  DS    PL3          PACKED DATE LAST LOGGED ON
         DS    CL2
PASSCHG  DS    PL3          PACKED LAST CHANGED DATE
         DS    CL2
IAUTH    DS    PL3          PACKED DATE USER DEFINED TO RACF
         DS    350C
OUTREC   DS    0CL80       80 BYTE RECORD AREA
OUSER    DS    CL8         USER ID
ONAME    DS    CL20        NAME
OINCR    DS    CL3         NUMBER OF DAYS TILL PASSWORD CHANGE  NNN
OLOG     DS    CL8         TIME LAST LOGGED ON  HHMMSSTH
OLDATE   DS    CL5         LOGON DATE   YYDDD
OCHANG   DS    CL5         PASSWORD CHANGED DATE YYDDD
EXPDATE  DS    CL5         PASSWORD EXPIRE DATE  YYDDD
OUADS    DS    CL1         UADS ENTRY PRESENT  Y/N
ODELETE  DS    CL1         WAS DELETE GENERATED D=DELETE
         DS    CL24
         LTORG
FILE     DCB   DSORG=PS,DDNAME=PRINT,RECFM=FB,LRECL=80,MACRF=PM
PDS      DCB   DSORG=PO,DDNAME=UADS,MACRF=R,EODAD=WRITE
         END
