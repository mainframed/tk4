*          DATA SET P50249     AT LEVEL 001 AS OF 09/23/80
P50249   CSECT
   TITLE '*****RACHECK SUBROUTINE           &SYSDATE &SYSTIME **'
         PRIME
         REGISTER
         EJECT
***********************************************************************
*                                                                     *
*   SUBROUTINE - P50249                                               *
*   PURPOSE    - TO PERFORM A RACHECK FOR A GIVEN RESOURCE, CLASS,    *
*                AND USER. TO USE THIS SUBROUTINE YOU MUST BE AUTH-   *
*                ORIZED TO ISSUE SVC231                               *
*                                                                     *
*   ENTRY      - VIA STANDARD OS CALL                                 *
*                CALL P50249,(RESOURCE,ACCESS,CLASS,ID),VL            *
*                WHERE -                                              *
*                  RESOURCE IS AN N-BYTE CHARACTER FIELD DETERMINED   *
*                     BY THE CLASS DESCRIPTOR TABLE. IF THE RESOURCE  *
*                     NAME IS SHORTER THAN THE MAX IN THE CDT, IT MUST*
*                     BE PADDED WITH BLANKS.                          *
*                  ACCESS IS A 1 BYTE FIELD CONTAINING THE TYPE OF    *
*                     ACCESS REQUESTED-                               *
*                          1 - READ ACCESS                            *
*                          2 - UPDATE ACCESS                          *
*                          3 - CONTROL ACCESS                         *
*                          4 - ALTER ACCESS                           *
*                  CLASS IS AN 8 BYTE FIELD CONTAINING THE CLASS OF   *
*                     THE RESOURCE TO BE CHECKED. THIS FIELD IS       *
*                     OPTIONAL BUT MUST BE SUPPLIED IF THE ID FIELD   *
*                     IS SUPPLIED. IF NOT SUPPLIED, THE DEFAULT CLASS *
*                     IS $TECHSER.                                    *
*                  ID IS A 4 BYTE FIELD CONTAINING THE USER ID TO BE  *
*                     CHECKED. THIS FIELD IS OPTIONAL. IF NOT SUPPLIED*
*                     THE CURRENT USER IS USED FOR THE RACHECK.       *
*                                                                     *
*   RETURN     - REG 15 WILL CONTAIN THE RETURN CODE ON EXIT. A CODE  *
*                OF ZERO MEANS ACCESS IS PERMITTED. ANY OTHER CODE IS *
*                AN ERROR AND ARE INTERPRETED AS FOLLOWS:             *
*                RETURN CODES 04 - 0C -- ERROR IN RACHECK. SEE MVS    *
*                             SUPERVISOR SERVICES MACROS UNDER RACHECK*
*                             FOR MEANINGS                            *
*                RETURN CODES 10 - 40 -- ERROR IN RACINIT. SUBTRACT   *
*                             X'10' AND SEE RACINIT ERROR CODES IN    *
*                             SPL:SUPERVISOR                          *
*                RETURN CODE  44 -- BAD PARAMETER LIST                *
*                                                                     *
* *********************************************************************
         EJECT
         LR    R2,R1               COPY ADDR OF PARMS
         GETMAIN R,LV=WORKLEN      GET WORK AREA
         LR    R11,R1              COPY ADDR OF AREA
         USING WRKAR,R11           ESTABLISH ADDRESSABILITY
         LA    R1,=C'AUTH'         MAKE AUTHORIZED
         SVC   231
         MVC   RCINITE(RCINITPL),RCINITP MOVE IN PATTERN
         MVI   IDL,4               MOVE IN USER LENGTH
         MVI   CLASSL,8            MOVE IN CLASS LENGTH
         MVC   ACEEADDR,=F'0'      ZERO ACEEADDR
         TM    4(R2),X'80'         WAS CLASS SPECIFIED??
         BO    RACH0005            NO--THEN ID COULD'NT BE SPECIFIED
         TM    8(R2),X'80'         WAS--ID SPECIFIED??
         BO    RACH0005            NO--THEN TAKE DEFAULT
         L     R3,12(R2)           GET ADDR OF ID
         MVC   ID,0(R3)            MOVE ID OUT OF PARMS
         LA    R15,ACEEADDR
         LA    R14,IDL
         RACINIT USERID=(R14),ACEE=(R15),PASSCHK=NO,ENVIR=CREATE,      X
               MF=(E,RCINITE)
         LTR   R15,R15             CHECK RETURN
         BZ    RACH0005            GOOD--
         LA    R4,16(R15)          ADD X'10' TO RETURN AND
         B     RACHEXIT            GET OUT
RACH0005 MVC   CLASS,=C'$TECHSER'  DEFAULT CLASS
         TM    4(R2),X'80'         WAS CLASS SPECIFIED
         BO    RACH0006            NO--
         L     R3,8(R2)            GET ADDR OF CLASS
         MVC   CLASS,0(R3)         MOVE CLASS OUT OF PARMS
RACH0006 L     R4,0(R2)            GET ADDR OF RESOURCE
         L     R5,4(R2)            GET ADDR OF ACCESS REQUEST
         LA    R6,2                DEFAULT READ
         CLI   0(R5),C'1'          READ REQUESTED?
         BE    RACH0010            YES--
         LA    R6,4                DEFAULT UPDATE
         CLI   0(R5),C'2'          UPDATE REQUESTED?
         BE    RACH0010            YES--
         LA    R6,8                DEFAULT CONTROL
         CLI   0(R5),C'3'          CONTROL REQUESTED?
         BE    RACH0010            YES--
         LA    R6,128              DEFAULT ALTER
         CLI   0(R5),C'4'          ALTER REQUESTED?
         BE    RACH0010            YES--
         LA    R15,X'44'           OTHERWISE INVALID ACCESS REQUESTED
         B     RACHEXIT            GET OUT
RACH0010 L     R15,ACEEADDR
         LA    R14,CLASSL
         MVC   RCHKE(RCHKPL),RCHKP MOVE IN RACHECK PATTERN
         RACHECK ENTITY=((R4)),CLASS=(R14),ACEE=(R15),ATTR=(R6),       X
               MF=(E,RCHKE)
         LR    R4,R15              STORE RETURN
RACHEXIT EQU   *
         ICM   R14,15,ACEEADDR
         BZ    RACH0020
         MVC   RCINITE(RCINITPL),RCINITP MOVE IN PATTERN
         RACINIT ACEE=ACEEADDR,ENVIR=DELETE,MF=(E,RCINITE)
RACH0020 SR    R1,R1               MAKE UN-AUTHORIZED
         SVC   231
         FREEMAIN R,LV=WORKLEN,A=(R11)
         LR    R15,R4              SET UP RETURN CODE
         TERME                     RETURN
         EJECT
*      CONSTANT SECTION
RCINITP  RACINIT ENVIR=CREATE,MF=L
RCINITPL EQU   *-RCINITP           LENGHT OF RACINIT LIST
RCHKP    RACHECK MF=L
RCHKPL   EQU   *-RCHKP             LENGTH OF RACHECK LIST
WRKAR    DSECT
RCINITE  RACINIT ENVIR=CREATE,MF=L
RCHKE    RACHECK MF=L
IDL      DC    X'04'               ID LENGTH
ID       DC    CL4' '              ID
ACEEADDR DS    A                   ACEE ADDRESS
CLASSL   DC    X'08'               CLASS LENGTH
CLASS    DC    CL8' '              CLASS
WORKLEN  EQU   *-WRKAR
         END
