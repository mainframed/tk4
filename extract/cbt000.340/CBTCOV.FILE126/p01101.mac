*          DATA SET P01101     AT LEVEL 001 AS OF 01/15/81
P01101   START 0
*
*        THIS ROUTINE WILL DISPLAY A MESSAGE ON THE CONSOLE.
*
*        USAGE  --  CALL P01101 (MSG,LEN)
*                   WHERE
*                        MSG - THE MESSAGE TO BE DISPLAYED
*                        LEN - THE LENGTH OF THE MESSAGE - FULLWORD
*
*        MAXIMUM MESSAGE LENGTH IS 60 CHARACTERS
*
         EJECT
*
*        REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
*
*        ESTABLISH STANDARD LINKAGE
*
         SAVE  (14,12),,*          SAVE REGISTERS
         BALR  R2,R0               R2 - BASE REGISTER
         USING *,R2
         LA    R3,SAVEAREA         CHAIN SAVE AREAS
         ST    R13,4(R3)
         ST    R3,8(R13)
         LR    R13,R3
         LR    R4,R1
         SPACE 3
*
*        GET THE MESSAGE
*
         MVC   CMSG(60),=CL60' '   CLEAR THE MESSAGE
         L R5,0(R4)                R5 - ADDRESS OF THE MESSAGE
         L     R6,4(R4)
         L     R6,0(R6)            R6 - LENGTH OF MESSAGE
         C R6,=F'0'                LENGTH OF ZERO ?
         BNH   RETN
         C R6,=F'60'               RESET LENGTH IF GREATER THAN 60
         BNH   NORESET
         LA    R6,60
NORESET  S R6,=F'1'                SET MOVE LENGTH
         STC   R6,MOVE+1
MOVE     MVC   CMSG(0),0(R5)       MOVE IN THE MESSAGE
         SPACE 3
*        ISSUE THE CONSOLE MESSAGE
         MVC   OPWTO+8(60),CMSG    MOVE THE MESSAGE AGAIN
OPWTO    WTO   '                                                       X
                    '
         SPACE 3
*        RETURN
RETN     L     R13,SAVEAREA+4
         SR    R15,R15
         RETURN (14,12),,(15)
         SPACE 3
*        MISCELLANEOUS STUFF
         DS    0F
SAVEAREA DC    72F'0'
CMSG     DC    CL60' '
         LTORG
         END   P01101
