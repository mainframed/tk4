000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID.    P50281B.
000030 AUTHOR.        DICK GLOJEK.
000040 INSTALLATION.  MILLER BREWING COMPANY - MILWAUKEE.
000050 DATE-WRITTEN.  DEC.1981.
000060 DATE-COMPILED.
000070 REMARKS.       *** ENVIRON-1 SECURITY MORNING REPORT EDIT ***.
000080 ENVIRONMENT DIVISION.
000090 CONFIGURATION SECTION.
000100 SOURCE-COMPUTER.     IBM-370.
000110 OBJECT-COMPUTER.     IBM-370.
000120 INPUT-OUTPUT SECTION.
000130 FILE-CONTROL.
000140     SELECT E1-SECURITY-FILE ASSIGN UT-S-SECURITY.
000150     SELECT PRINTER-FILE ASSIGN UT-S-PRINTER.
000160     SELECT SORT-FILE ASSIGN UT-S-SORTWK01.
000170 DATA DIVISION.
000180 FILE SECTION.
000190 FD  E1-SECURITY-FILE
000200     BLOCK CONTAINS 0 RECORDS
000210     RECORDING MODE IS F
000220     LABEL RECORD STANDARD.
000230 01  E1-SECURITY-RECORD      PIC X(35).
000240 FD  PRINTER-FILE
000250     BLOCK CONTAINS 0 RECORDS
000260     RECORDING MODE F
000270     LABEL RECORD STANDARD.
000280 01  PRINT-LINE.
000290     05  PRT-CNTL            PIC X.
000300     05  FILLER              PIC X(132).
000310 SD  SORT-FILE
000320     RECORDING MODE IS F.
000330 01  SORT-RECORD.
000340     05  SORT-KEY            PIC X(3).
000350     05  FILLER              PIC X(15).
000360     EJECT
000370 WORKING-STORAGE SECTION.
000380 01  ODDS-AND-ENDS.
000390     05  PREV-SYSID              PIC XXX.
000400     05  ACC-TERM                PIC S9(5)   COMP-3.
000410     05  ACC-TRAN                PIC S9(5)   COMP-3.
000420     05  ACC-PASS                PIC S9(5)   COMP-3.
000430     05  ZP01616Z                PIC X(8)    VALUE 'P01616'.
000440     05  ZP00655Z                PIC X(8)    VALUE 'P00655'.
000450     05  SELECT-DATE.
000460         10  SELECT-MON          PIC XX.
000470         10  FILLER              PIC X.
000480         10  SELECT-DAY          PIC XX.
000490         10  FILLER              PIC X.
000500         10  SELECT-YR           PIC XX.
000510     05  SELECT-JULIAN           PIC S9(5)   COMP-3.
000520     05  PREV-DAYS-JULIAN        PIC S9(5)   COMP-3.
000530     05  BUILD-JULIAN            PIC 9(5).
000540     05  FILLER REDEFINES BUILD-JULIAN.
000550         10  BUILD-YR            PIC 99.
000560         10  BUILD-DAY           PIC 999.
000570     SKIP3
000580     05  SORT-RECORD-LAYOUT.
000590         10  SR-SYSID            PIC XXX.
000600         10  SR-UNAUTH-TERM      PIC S9(5)   COMP-3.
000610         10  SR-UNAUTH-TRAN      PIC S9(5)   COMP-3.
000620         10  SR-INVALID-PASS     PIC S9(5)   COMP-3.
000630     SKIP3
000640     05  E1-SECURITY-LAYOUT.
000650         10  SC-AUDTIM       PIC S9(6)       COMP-3.
000660         10  SC-AUDATE       PIC S9(6)       COMP-3.
000670         10  SC-AUDJULIAN    PIC S9(5)       COMP-3.
000680         10  SC-SYSID        PIC X(3).
000690         10  SC-UNAUTH-TERM  PIC S9(5)       COMP-3.
000700         10  SC-UNAUTH-TRAN  PIC S9(5)       COMP-3.
000710         10  SC-INVALID-PASS PIC S9(5)       COMP-3.
000720         10  FILLER          PIC X(12).
000730     SKIP3
000740     05  DET-LINE            PIC X(133).
000750     05  FILLER REDEFINES DET-LINE.
000760         10  DET-CNTL            PIC X.
000770         10  DET-SYSID           PIC XXX.
000780         10  DET-UNAUTH-TERM     PIC Z(5).
000790         10  DET-UNAUTH-TRAN     PIC Z(5).
000800         10  DET-INVALID-PASS    PIC Z(5).
000810         10  FILLER              PIC X(36).
000820         10  DET-REPORT-ID       PIC X(14).
000830     SKIP3
000840 01  CONV-DATE-DATA.
000850     COPY CONVAREA.
000860     SKIP3
000870 LINKAGE SECTION.
000880 01  EXECPARM.
000890     05  PARM-LENGTH             PIC S9(4)      COMP.
000900     05  PARM-DATE-OVERIDE       PIC X(8).
000910     EJECT
000920 PROCEDURE DIVISION USING EXECPARM.
000930     IF PARM-LENGTH NOT = 0
000940         MOVE PARM-DATE-OVERIDE TO SELECT-DATE
000950         ELSE
000960         MOVE CURRENT-DATE TO SELECT-DATE.
000970     PERFORM GET-JULIAN THRU GET-JULIAN-EX.
000980     MOVE BUILD-JULIAN TO SELECT-JULIAN PREV-DAYS-JULIAN.
000990     SUBTRACT +1 FROM PREV-DAYS-JULIAN.
001000     SORT SORT-FILE ASCENDING KEY SORT-KEY
001010         INPUT PROCEDURE SORT-SECURITY-RECORDS
001020             THRU SORT-EX
001030         OUTPUT PROCEDURE CREATE-SECURITY-EDIT
001040             THRU CREATE-EX.
001050     GOBACK.
001060     SKIP3
001070 SORT-SECURITY-RECORDS SECTION.
001080     OPEN INPUT E1-SECURITY-FILE.
001090 READ-SECURITY.
001100     READ E1-SECURITY-FILE INTO E1-SECURITY-LAYOUT
001110          AT END
001120          CLOSE E1-SECURITY-FILE GO TO SORT-EX.
001130     IF SC-AUDJULIAN = SELECT-JULIAN GO TO BUILD-SORT.
001140     IF SC-AUDJULIAN NOT = PREV-DAYS-JULIAN GO TO READ-SECURITY.
001150     IF SC-AUDTIM IS LESS THAN 060000
001160         GO TO READ-SECURITY.
001170 BUILD-SORT.
001180     MOVE SC-SYSID TO SR-SYSID.
001190     MOVE SC-UNAUTH-TERM TO SR-UNAUTH-TERM.
001200     MOVE SC-UNAUTH-TRAN TO SR-UNAUTH-TRAN.
001210     MOVE SC-INVALID-PASS TO SR-INVALID-PASS.
001220     RELEASE SORT-RECORD FROM SORT-RECORD-LAYOUT.
001230     GO TO READ-SECURITY.
001240 SORT-EX.
001250     EXIT.
001260     SKIP3
001270 CREATE-SECURITY-EDIT SECTION.
001280     OPEN OUTPUT PRINTER-FILE.
001290     PERFORM RETURN-SORTED.
001300     IF SORT-RECORD-LAYOUT = HIGH-VALUES
001310         CLOSE PRINTER-FILE
001320         GO TO CREATE-EX.
001330     MOVE SPACES TO DET-LINE.
001340     MOVE 0 TO ACC-TERM ACC-TRAN ACC-PASS.
001350     GO TO BUILD-EDIT.
001360 RETURN-SORTED.
001370     RETURN SORT-FILE INTO SORT-RECORD-LAYOUT
001380         AT END MOVE HIGH-VALUES TO SORT-RECORD-LAYOUT.
001390 BUILD-EDIT.
001400     MOVE SR-SYSID TO DET-SYSID PREV-SYSID.
001410 ACC-TOTALS.
001420     ADD SR-UNAUTH-TERM TO ACC-TERM.
001430     ADD SR-UNAUTH-TRAN TO ACC-TRAN.
001440     ADD SR-INVALID-PASS TO ACC-PASS.
001450 NEXT-SORT-REC.
001460     PERFORM RETURN-SORTED.
001470     IF SORT-RECORD-LAYOUT = HIGH-VALUES
001480         PERFORM WRITE-EDIT THRU WRITE-EX
001490         CLOSE PRINTER-FILE
001500         GO TO CREATE-EX.
001510     IF SR-SYSID NOT = PREV-SYSID
001520         PERFORM WRITE-EDIT THRU WRITE-EX
001530         GO TO BUILD-EDIT.
001540     GO TO ACC-TOTALS.
001550 WRITE-EDIT.
001560     MOVE ACC-TERM TO DET-UNAUTH-TERM.
001570     MOVE ACC-TRAN TO DET-UNAUTH-TRAN.
001580     MOVE ACC-PASS TO DET-INVALID-PASS.
001590     MOVE 'ENVIRON/1 DATA' TO DET-REPORT-ID.
001600     WRITE PRINT-LINE FROM DET-LINE AFTER POSITIONING PRT-CNTL.
001610     MOVE 0 TO ACC-TERM ACC-TRAN ACC-PASS.
001620     MOVE SPACES TO DET-LINE.
001630 WRITE-EX.
001640     EXIT.
001650 CREATE-EX.
001660     EXIT.
001670     SKIP3
001680 GET-JULIAN SECTION.
001690     MOVE SELECT-DATE TO CONV-DATE.
001700     MOVE 'D16' TO CONV-DATE-CODE.
001710     CALL ZP01616Z USING CONV-MODULE-NAME
001720                         CONV-MODULE-FUNC
001730                         CONV-DATE-CODE
001740                         CONV-DATE
001750                         CONV-BIN-DATE.
001760     IF CONV-DATE-CODE = SPACES CALL ZP00655Z.
001770     MOVE 'BX6' TO CONV-DATE-CODE.
001780     CALL ZP01616Z USING CONV-MODULE-NAME
001790                         CONV-MODULE-FUNC
001800                         CONV-DATE-CODE
001810                         CONV-BIN-DATE
001820                         CONV-DATE-DATA.
001830     IF CONV-DATE-CODE = SPACES CALL ZP00655Z.
001840     MOVE CONV-YEAR TO BUILD-YR.
001850     MOVE CONV-JULIAN TO BUILD-DAY.
001860 GET-JULIAN-EX.
001870     EXIT.
001880     SKIP3
001890 END-OF-CODING.
