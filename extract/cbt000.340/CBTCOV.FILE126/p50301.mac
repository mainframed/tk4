*          DATA SET P50301     AT LEVEL 001 AS OF 05/26/81
*
*
*
*
*           OBTAINS DATA SET PROFILES AND CREATES A USEABLE
*           SEQUENTIAL FILE OF PERT INFO.
*
*
*
         EJECT
RACFDSN  CSECT
         PRIME
 TITLE '**** RACF DATA SET PROFILE RETRIEVER &SYSDATE &SYSTIME '
         REGISTER
         OPEN  (RACFDATA,(OUTPUT))
         EJECT
         LA    R1,AUTH
         SVC   231
NXTUSR   EQU   *
         ICHEINTY MF=(E,REQUESTU)
         LTR   R15,R15
         BZ    NXTDS1
         CH    R15,=H'12'             NO ENTRY
         BE    GROUP
         B     NOUSER
GROUP    EQU   *
         TM    USERSW,X'80'        FIRST TIME
         BO    UADSRET             NO
         OI    USERSW,X'80'        TURN ON
         MVI   FIRSTU,X'08'        LENGTH
         MVC   FIRSTU+1(8),=CL8' '
         ICHEINTY MF=(E,REQUESTU),TYPE='GRP'
         LTR   R15,R15
         BZ    NXTDS1
         CH    R15,=H'12'             NO ENTRY
         BE    UADSRET
         B     NOUSER
NXTDS1   EQU   *
         MVI   FIRST,X'04'        LENGTH
         MVC   FIRST+1(4),FIRSTU+1
NXTDS    EQU   *
         ICHEINTY MF=(E,REQUEST),                                      X
               WKAREA=WORK,                                            X
               ACTIONS=(ACTLST1,ACTLST2,ACTLST3,ACTLST4,ACTLST5)
         LTR   R15,R15
         BZ    PUTDS
         CH    R15,=H'12'             NO ENTRY
         BE    NXTUSR
NOUSER   EQU   *
         SR    R1,R1
         SVC   231
         ABEND 1,DUMP
PUTDS    EQU   *
         SR    R2,R2
         IC    R2,FIRST            LENGTH
         BCTR  R2,0
         EX    R2,MVCDSN
         L     R2,WORK+24          LENGTH OF FIELDS
         LA    R2,4(R2)            ADD L'LENGTH
         BCTR  R2,0
         EX    R2,MVCDATA          MOVE OWNER
         AH    R2,=H'53'           44 DSN 1 BCTR 4 RDW 4 WORKL
         STH   R2,RACFLNG          STORE IN RECORD
         PUT   RACFDATA,RACFREC
         MVC   RACFDS,=CL44' '     BLANKS
         XC    RACFOWN(255),RACFOWN
         B     NXTDS
UADSRET  EQU   *
         SR    R1,R1
         SVC   231
         CLOSE RACFDATA
         TERME
         EJECT
*
*        CONSTANTS OR SAVE/WORK AREAS FOR PROGRAM
*
RACFDATA DCB   DSORG=PS,                                               X
               DDNAME=RACFDATA,                                        X
               MACRF=PM
MVCDSN   MVC   RACFDS(0),FIRST+1
MVCDATA  MVC   RACFOWN(0),WORK+24
         SPACE 3
USERSW   DC    X'00'
AUTH     DC    CL4'AUTH'
         DS    0F
FIRST    DC    AL1(4)
         DC    CL44'MB98'
FIRSTU   DC    AL1(8)
         DC    CL8' '
ACTLST1  ICHEACTN MF=L,FIELD=AUTHOR
ACTLST2  ICHEACTN MF=L,FIELD=UNIVACS
ACTLST3  ICHEACTN MF=L,FIELD=ACLCNT
ACTLST4  ICHEACTN MF=L,FIELD=USERID
ACTLST5  ICHEACTN MF=L,FIELD=USERACS
REQUEST  ICHEINTY NEXT,MF=L,TYPE='DS',WKAREA=WORK,ENTRY=FIRST,         X
               ACTIONS=(ACTLST1,                                       X
               ACTLST2,                                                X
               ACTLST3,                                                X
               ACTLST4,                                                X
               ACTLST5)
REQUESTU ICHEINTY NEXT,MF=L,TYPE='USR',WKAREA=WORK,ENTRY=FIRSTU
WORK     DC    F'300'              LENGTH OF WORK AREA
         DC    300X'00'
RACFREC  DS    0CL322              RACF RECORD
RACFLNG  DS    H                   RECORD LENGTH
         DC    H'0'                RESERVED
RACFDS   DC    CL44' '             DSNAME
RACFOWN  DS    CL276               RACF DATA
         END
