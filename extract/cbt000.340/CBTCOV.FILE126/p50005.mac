*          DATA SET P50005     AT LEVEL 002 AS OF 01/22/81
         TITLE 'IEFUTL - SMF TIME EXIT MILLER BREWING COMPANY IGC0023I'
* *********************************************************************
*    SMF PARAMETER AREA LAYOUT                                        *
*        DISPLACEMENT   FIELD  DATA    DESCRIPTION                    *
*        FROM POINTER   SIZE   FORMAT                                 *
*             0           8    EBCDIC  JOB NAME                       *
*             8           4    BINARY  TIME IN HUNDRETHS OF A SECOND  *
*                                      THAT THE READER RECOGNIZED THE *
*                                      JOB CARD                       *
*            12           4    PACKED  DATE JOB CARD WAS READ BY THE  *
*                                      READER IN THE FORM 00YYDDDF    *
*                                      WHERE F IS THE SIGN            *
*            16           4    EBCDIC  SYSTEM IDENTIFICATION FROM     *
*                                      'SMFPRM.. SID PARAMETER        *
*            20           8    EBCDIC  USER IDENTIFICATION FIELD. SMF *
*                                      PLACES THIS DATA IN ALL        *
*                                      SUBSEQUENT RECORDS FOR THIS JOB*
*                                      THIS FIELD IS INITIALIZED TO   *
*                                      EBCDIC BLANKS FOR EACH JOB.    *
*                                      THIS FIELD MAY BE MODIFIED BY  *
*                                      THE USER.                      *
*            28           1    BINARY  NUMBER OF THE STEP BEING       *
*                                      PROCESSED.                     *
*            29           1    BINARY  EIGHT INDICATORS OF SMF OPTIONS*
*                                      THAT ARE SELECTED BY THE USER. *
*                                      A BIT SETTING OF 1 INDICATES   *
*                                      THAT THE RELATED OPTION WAS    *
*                                      SELECTED.                      *
*                                      BIT                            *
*                                       0  SYSTEM & JOB ACCOUNTING    *
*                                       1  STEP ACCOUNTING            *
*                                       2  DYNAMIC EXITS              *
*                                       3  DATA SET ACCOUNTING        *
*                                       4  VOLUME ACCOUNTING          *
*                                       5  RESERVED                   *
*                                       6  TEMPORARY DATA SET SCRATCH *
*                                          RECORDS                    *
*                                       7  0 - BACKGROUND JOB         *
*                                          1 - FOREGROUND JOB TSO     *
*            30           1    BINARY  VS2 ONLY                       *
*                                      BIT                            *
*                                       0  STEP RESATRT               *
*                                       1  CHECKPOINT/RESTART         *
*                                       2  CONTINUE RESTART           *
*                                       3  RESERVED                   *
*                                       4  WARM START                 *
*                                       5  RESERVED                   *
*                                       6  RESERVED                   *
*                                       7  RESERVED                   *
*            31           1    BINARY  RESERVED                       *
*            32           4    BINARY  USER COMMUNICATION FIELD. THIS *
*                                      FIELD IS INTENDED TO BE A USER *
*                                      EXIT ROUTINE COMMUNICATION     *
*                                      FIELD. THIS FIELD IS NOT       *
*                                      INITIALIZED IN RELEASE 1.7 OF  *
*                                      VS2. IT IS INITIALIZED TO      *
*                                      ZEROS ONLY WHEN THE READER IS  *
*                                      STARTED IN VS1 AND WHEN JES2   *
*                                      IS STARTED IN VS2.             *
* *********************************************************************
*        EJECT
*        THIS SMF TIME EXIT IS ENTERED WHENEVER THE FOLLOWING
*        TIMES ARE EXCEEDED:
*              1. JOB CPU TIME USED LIMIT
*              2. STEP CPU TIME USED LIMIT
*              3. CONTINUOUS WAIT TIME LIMIT
*        FUNCTION:
*              TIME EXTENSION :
*                  1. A TIME EXTENSION OF 5 MINUTES IS ALLOWED FOR
*                     WAIT AND STEP TIMES, 50 % FOR TESTS.
*                  2. THE EXIT INDICATES THAT THE JOB IS TO CONTINUE
*                     PROCESSING WITH AN ADDITIONAL TIME ALLOCATION
*                     AND IT RETURNS TO THE CONTROL PROGRAM.
*                  4. AFTER 5 MINUTES A CONSOLE MESSAGE WILL BE
*                     ISSUED INDICATING THE 5 MINUTE EXTENSION.
*                  3. AN SMF RECORD IS WRITTEN TO THE SMF DATA SET
*                     DESCRIBING THE ACTION TAKEN.
*              TIME CANCEL :
*                  1. IF EITHER JOB OR STEP CPU TIME IS EXCEEDED:
*                     A. TWO TIME EXTENSION OF 5 MINUTES WILL BE
*                        ALLOWED FOR STEP AND WAIT TIME, 50% WILL
*                        BE EXTENDED FOR TEST JOB TIME, 5 MIN FOR
*                        PRODUCTION JOBS
*                     B. AN SMF RECORD IS WRITTEN TO THE SMF DATA
*                        SET DESCRIBING THE ACTION TAKEN.
*                     C. A CONSOLE MESSAGE WILL BE ISSUED INDICATING
*                        THE 5 MINUTE EXTENSION.
*                     D. AFTER TWO EXTENSIONS OF 5 MINUTES THE JOB
*                        IS CANCELLED.
*
*              NOTE: IF THE JOB CONTROL TABLE (JCT), IN WHICH THE
*                      ORIGINAL EXECUTION TIME VALUE IS KEPT, IS NOT
*                      ACCESSIBLE, THE TIME EXTENSION VALUE WILL BE
*                      DETERMINED USING THE TIME EXTENSION TABLE
*                      (TIMEXTAB).
*
*        INPUT AT ENTRY:
*              REGISTER 0 - CONTAINS ONE OF THE FOLLOWING VALUES
*                           INDICATING WHICH TIME LIMIT WAS EXCEEDED
*                           O - JOB CPU TIME
*                           4 - STEP CPU TIME
*                           8 - JOB WAIT TIME
*              REGISTER 1 - CONTAINS THE ADDRESS OF A FULL WORD WHICH
*                           CONTAINS THE ADDRESS OF THE SMF PARAMETER
*                           AREA.
*
*                           NOTE:  THIS REGISTER MUST NOT BE ALTERRED
*                           UNTIL AFTER SVC 229 (FIND JCT) HAS BEEN
*                           INVOKED, AS SVC 229 ASSUMES THAT REGISTER
*                           ONE CONTAINS THE SMF PARM AREA POINTER
*                           ADDRESS.
*        OUTPUT AT EXIT:
*              REGISTER 1 - CONTAINS THE AMOUNT OF THE EXTENSION
*                           IN EITHER TIMER UNITS OR SECONDS (SEE R15)
*                           IF THE TIME LIMIT WAS EXTENDED
*              REGISTER 15 -  CONTAINS ONE OF THE FOLLOWING
*                             RETURN CODES
*                             0 - CONTINUE PROCESSING (CANCEL THE JOB)
*                             4 - CONTINUE THE JOB WITH THE TIME
*                                 EXTENSION IN TIMER UNITS IN REG1
*                                 1 SECOND =  38,400 TIMER UNITS
*                             8 - CONTINUE THE JOB WITH THE TIME
*                                 EXTENSION IN SECONDS IN REG1
*                                 (VS2 OPTION ONLY)
*              SMF RECORD TYPE X'84':
*                           EACH TIME THE ANY TIME LIMIT IS
*                           EXCEEDED A 43 BYTE RECORD IS WRITTEN
*                           TO THE SMF DATA SET USING THE SMFWTM MACRO.
*                           THE FORMAT OF THIS RECORD IS DESCRIBED BY
*                           THE DSECT FOR THE SMF RECORD.
*              CONSOLE MESSAGES:
*                           EACH TIME THE JOB WAIT TIME LIMIT IS
*                           EXCEEDED THE COMPUTER OPERATOR RECEIVES
*                           A MESSAGE INDICATING THE JOB NAME AND
*                           WHICH TIME WAS EXCEEDED.
*                           AFTER THE THIRD MESSAGE THE JOB IS
*                           CANCELLED, EXCEPT JOB STEP TIME.
*                           THE 4 BYTE USER EXTENSION AREA IS USED
*                           AS FOLLOWS:
*                                  BYTE 0 - ENTRY COUNT
*                                  BYTE 1 - NUMBER OF JOB CPU ENTRIES
*                                  BYTE 2 - NUMBER OF STEP CPU ENTRIES
*                                  BYTE 3 - NUMBER OF WAIT TIME ENTRIES
         SPACE 2
         GBLB  &TGMD
&TGMD    SETB  1                SET FOR JCT DSECT COPY
         EJECT
IGC0023I CSECT
         USING IGC0023I,R6
         B     AROUND                   BRANCH AROUND MODULE NAME
         DC    CL8'IEFUTL  '
         DC    CL8'IGC0023I'
AROUND   STM   R14,R12,12(R13)
         L     R2,0(R1)                 GET PARAMETER AREA ADDRESS
         LR    R9,R0                    SAVE ENTRY VALUE IN R9
         SVC   229                      JES2 JCT SVC
         LR    R12,R1                   ADDR OF JCT FROM SVC 229
         USING JCTDSECT,R12
*
*        BUILD CONSOLE MESSAGE
         GETMAIN R,LV=110
         LR    R5,R1                    R5 IS BASE FOR WTO MSG
         LA    R3,MSGEND-WTOMSG(R5)     R3 IS BASE FOR SMF RECORD
         USING WTOMDS,R5
         MVC   WTOFILL(54),WTOMSG
         MVC   MSGJOB(8),0(R2)          CAPTURE JOB NAME FROM PARAMETER
         SPACE 3
*        BUILD SMF RECORD
*
*        USING SMFDSECT,R3              SMF RECORD
*        TIME  BIN                      SMF RECORD
*        ST    R0,TIME                  TIME FOR SMF RECORD
*        ST    R1,DATE                  DATE FOR SMF RECORD
*        MVC   SYSID(4),16(R2)          SMF RECORD
*        MVC   JOBLOG(16),0(R2)         SMF RECORD
*        MVC   USERID(8),20(R2)         SMF RECORD
*        MVC   HEADER(6),SMFRCD         SMF RECORD
         SPACE 3
*        CHECK WHICH CONDITION OCCURRED
*
         L     R10,TEDEFALT             DEFAULT TIME EXTENSION
         LA    R8,4                     PUT 4 IN REG8 FOR COMPARE
         CR    R9,R8                    COMPARE TO 8
         BH    JCWAIT                   MUST BE CONTINUOUS WAIT
         BE    JSWAIT                   MUST BE JOB STEP CPU ELAPSED
*  JOB CPU ELAPSED
*                    R9 = 0
JWAIT    EQU   *
         SR    R11,R11
         IC    R11,33(R2)
*        STC   R11,EXTCODE        SMF RECORD
         MVC   MSGTYPE(15),MSG2
         LA    R11,4(R11)
         STC   R11,33(R2)
         LTR   R12,R12                 IS A JCT AVAILABLE ?
         BNZ   JWAIT2                  YES. CALCULATE TIME EXTENSION.
         MVC   MSGTX,MSG5              MOVE 'NO JCT' MSG. TO MSG. AREA
         MVC   MSGTX5JN,SMFPJOBN(R2)   MOVE JOBNAME FROM SMF PARM AREA
*
* IF THE JCT IS NOT ACCESSIBLE, DETERMINE THE AMOUNT OF TIME TO BE
*   EXTENDED BASED ON THE JOB'S EXECUTION CLASS.
*
         DROP  R12                     DISCONTINUE FOR THE JCT DSECT
         USING TEDSECT,R12             USE FOR TIME EXT. TABLE DSECT
         SPACE
         LA    R12,TIMEXTAB            LD ADDR. OF TIME EXT. TABLE
CKTIMEXT CLC   TEJOBC,SMFPJOBC(R2)     DOES THE JOB CLASS MATCH ?
         BE    SETEXT                  YES. SET THE TIME EXTENSION
         LA    R12,L'TEENTRY(R12)      POINT TO NEXT TABLE ENTRY
         CLI   TEJOBC,X'FF'            IS IT THE END OF THE TABLE ?
         BNE   CKTIMEXT                NO. PROCESS NEXT TABLE ENTRY
SETEXT   L     R10,TESECS              LOAD TIME EXTENSION VALUE
         MVC   MSGTX5TE,TETEXT         MOVE TIME EXTENSION TEXT TO MSG.
         B     SETEXIT
         SPACE
         DROP  R12                     DISCONTINUE FOR TAB EXT. DSECT
         USING JCTDSECT,R12            USE FOR JCT DSECT
*
JWAIT2   CLI   JCTMBTST,C'T'            TEST JOB
         BNE   SETEXIT                  YES,BR
         L     R8,JCTMBMIN              MIN ON TIME PARM
         SRDA  R8,32                    SHIFT TO ODD REG
         LA    R1,60                    60 SEC IN A MIN
         MR    R8,R1                    CHANGE MIN TO SEC
         A     R9,JCTMBSEC              ADD ODD SECONDS
         SRA   R9,1                     DIVIDE BY 2
         LA    R10,2                    MINIMUM TIME
         CR    R9,R10                   CHECK CALCULATED TIME
         BNH   *+6                      BR AROUND
         LR    R10,R9                   LOAD DEFAULT TIME REG
         LA    R11,4(R11)                 ONLY ONE TIME EXTENSION
         STC   R11,33(R2)                SAVE IN USER AREA
         B     SETEXIT
*     CONTINUOUS WAIT
*                    R9 = 8
JCWAIT   EQU   *
         SR    R11,R11
         IC    R11,35(R2)
*        STC   R11,EXTCODE        SMF RECORD
         MVC   MSGTYPE(15),MSG1
         LA    R11,4(R11)
         STC   R11,35(R2)
         LA    R11,16             CANCEL ALL AT 30 MIN
         B     SETEXIT
*     JOB STEP ELASPED
*                    R9 = 4
JSWAIT   EQU   *
         SR    R11,R11
         IC    R11,34(R2)
*        STC   R11,EXTCODE        SMF RECORD
         MVC   MSGTYPE(15),MSG3
         LA    R11,4(R11)
         STC   R11,34(R2)
         LA    R11,0              DON NOT CANCEL STEP TIMESL 2580
         B     SETEXIT
         EJECT
SETEXIT   EQU   *
         LA R8,9                8  = 2ND TIME IN AFTER 1 EXTENSIONS
         SR    R9,R9                 USED TO BE 3 AFTER 2 EXTENSIONS
         CR    R11,R8
         BL   WRITEALL
         LA    R9,16                    SET R9 TO CANCEL INDICATOR
         SPACE 3
WRITEALL DS    0H
*        LA    R1,SMFOUT                SMF RECORD
*        SMFWTM   (1)                   WRITE SMF RECORD
         LA    R8,16
         CR    R8,R9                    SEE IF CANCEL FLAG SET
         BE    CANCEL                   MUST BE THIRD ENTRY INTO EXIT
         SPACE 3
*
*        HAVE NOT BEEN HERE THREE TIMES YET
*        RETURN WITH EXTENSION
*
NOCANCEL  EQU   *
        ST    R10,24(R13)         MOVE TIME EXTENSION INTO SAVE AREA
         LA    R8,8                     SET TIME IN SECONDS INDICATOR
         B     EXIT
         SPACE 3
CANCEL   EQU   *
         SR    R8,R8                    SET CANCEL INDICATOR
         MVC   WTOFILL(4),WTOMSG
         CVD   R10,MSGTX1+4(8)
         UNPK  MSGTX1(5),MSGTX1+9(3)
         OI    MSGTX1+4,X'F0'
         MVC   MSG4+45(5),MSGTX1
         MVC   MSGTX1(50),MSG4
         MVC   12(8,R5),0(R2)
         SPACE 3
EXIT     DS    0H
         WTO   MF=(E,(R5))
         LR    R1,R5
          FREEMAIN R,LV=110,A=(1)
         ST    R8,16(R13)               PUT RETURN CODE IN REG 15 AREA
         LM    R14,R12,12(R13)          RESTORE REGISTERS
         BR    R14
         EJECT
*        DSECTS AND WORK AREAS
*        FOR SMF RECORD, CONSOLE MESSAGE, JOB TABLES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
JCT      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
*  SMF COMMON PARAMETER AREA EQUATES
SMFPJOBN EQU   0                   JOB NAME DISPLACEMENT
SMFPJOBC EQU   31                  JOB EXECUTION CLASS DISPLACEMENT
         EJECT
TIMEXTAB DS    0F
*
*  THE TIME EXTENSION TABLE, USED ONLY WHEN THE JOB CONTROL TABLE (JCT)
*    IS NOT ACCESSIBLE, IS USED TO DETERMINE HOW MUCH TIME A JOB IS TO
*    BE EXTENDED, BASED ON THE JOB'S EXECUTION CLASS. THE TIME VALUE
*    SPECIFIED IN THIS TABLE IS APPROXIMATELY 50% OF THE MAXIMUM
*    EXECUTION TIME ALLOWED FOR THAT JOB CLASS.
*
         DC    F'03'               TIME EXTENSION IN SECONDS
         DC    C'X'                JOB CLASS IDENTIFIER
         DC    CL7' 3 SEC.'        TIME EXTENSION MSG. TEXT
         DC    F'08'
         DC    C'A'
         DC    CL7' 8 SEC.'
         DC    F'30'
         DC    C'B'
         DC    CL7'30 SEC.'
TEDEFALT DC    F'300'
         DC    X'FF'
         DC    CL7' 5 MIN.'
         SPACE 2
TEDSECT  DSECT                     TIME EXTENSION TABLE DSECT
TEENTRY  DS    0CL12               LENGTH OF A TABLE ENTRY
TESECS   DS    F                   TIME EXTENSION VALUE DISPLACEMENT
TEJOBC   DS    C                   JOB CLASS ID DISPLACEMENT
TETEXT   DS    CL7                 MSG. TEXT DISPLACEMENT
         SPACE
IGC0023I CSECT
         EJECT
WTOMSG   DS    0F
         DC    AL2(MSGEND-*)            MESSAGE LENGTH
         DC    AL2(0)                   SPACING
         DC    C'SMF XXXXXXXXXXXXXXX TIME EXCEEDED FOR JOB YYYYYYYY'
MSGEND   EQU   *
         SPACE 3
MSG1     DC    C'CONTINUOUS WAIT'
MSG2     DC    C'JOB ELAPSED CPU'
MSG3     DC    C'JOB STEP CPU   '
MSG4     DC    C'SMF JOB XXXXXXXX WILL BE CANCELLED,EXTENSION=00300'
MSG5     DC    C'IGC0023I - XXXXXXXX TIME EXTENSION XX XXXX- NO JCT'
         SPACE 3
SMFRCD   DC    X'002B00000084'
         LTORG
         EJECT
SMFDSECT DSECT
         DS    1H
SMFOUT   EQU   *
HEADER   DS    6C
TIME     DS    1F
DATE     DS    1F
SYSID    DS    1F
JOBLOG   DS    16C
USERID   DS    8C
EXTCODE  DS    1C
         SPACE 3
WTOMDS   DSECT
WTOFILL  DS    4C
MSGTX    DS    0CL50
MSGTX1   DS    4C
MSGTYPE  DS    0CL15
         DS    CL7
MSGTX5JN DS    CL8
MSGTX2   DS    0CL23
         DS    CL16
MSGTX5TE DS    CL7
MSGJOB   DS    8C
         SPACE 3
JOBSDS   DSECT
JOBS     DS    80C
IGC0023I CSECT
         EJECT
         $BUFFER
         EJECT
         $JCT
IGC0023I CSECT
         END   IGC0023I
