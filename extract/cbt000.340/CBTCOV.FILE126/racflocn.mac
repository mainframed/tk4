DRIVER   $PROLOG
         LA    R1,=C'AUTH'
         SVC   231
         OPEN  (X,(OUTPUT))
         LA    R10,25
LOOP     EQU   *
         CALL RACFLOCN,(DDSN,DDVOL)
         PUT   X,DDSN
         L     R5,=A(RFWK1)
         PUT   X,(R5)
         BCT   R10,LOOP
         CLOSE (X)
X        DCB   DDNAME=X,MACRF=PM,DSORG=PS,LRECL=200,BLKSIZE=200,       X
               RECFM=FB
DDSN     DC    AL1(4),CL44'MB88'
DDVOL    DC    CL6'MODEL'
RACFLOCN CSECT
***********************************************************************
*                                                                    *
*   PURPOSE: THIS ROUTINE LOCATES THE NEXT DATASET PROFILE IN THE    *
*            RACF DATASET.                                           *
*                                                                    *
*   USE:    CALL RACFLOCN,(DSN,VOLUME)                               *
*      WHERE - DSN IS A 44 BYTE FIELD INITIALIZED TO BLANKS TO LOCATE*
*              THE FIRST DATASET                                     *
*            - VOLUME IS A 6 CHARACTER FIELD INTO WHICH WILL BE      *
*              RETURNED THE VOLUME THAT THE DATASET IS ON            *
*                                                                    *
**********************************************************************
         STM   R14,R12,12(R13)     STORE REGISTERS
         USING RACFLOCN,R15
         ST    R13,RFSAV+4         CHAIN BACK
         LA    R11,RFSAV           GET ADDR OF NEW SAVE
         ST    R11,8(R13)          CHAIN FORWARD
         LR    R13,R11             SET NEW SAVE
         DROP  R15
         USING RACFLOCN,R12
         LR    R12,R15             SET UP R12 AS BASE
         L     R2,0(R1)            ADDR OF DSN
         L     R3,4(R1)            ADDR OF VOLUME
         ICHEINTY NEXTC,TYPE='DS',                                     X
               ENTRY=(R2),ACTIONS=(RFACT4,RFACT2,RFACT3,RFACT1),       X
               WKAREA=RFWK1
         LTR   R15,R15
         BZ    BYPASS
         ABEND 100,DUMP
BYPASS   EQU   *
         L     R13,RFSAV+4         RESTORE REGS
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14                 RETURN
         SPACE 5
         DS    0D
RFSAV    DS    18F
RFACT3   ICHEACTN MF=L,FIELD=DEVTYPX
RFACT2   ICHEACTN MF=L,FIELD=DEVTYP
RFACT1   ICHEACTN MF=L,FIELD=VOLSER
RFACT4   ICHEACTN MF=L,FIELD=DSTYPE
RFWK1    DS    0F
         DC    A(200)
         DS    CL200
RFLOW    DC    6X'00'
         END
