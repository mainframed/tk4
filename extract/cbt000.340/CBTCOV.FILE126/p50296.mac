*          DATA SET P50296     AT LEVEL 007 AS OF 05/20/81
*          DATA SET P50296     AT LEVEL 006 AS OF 05/18/81
*          DATA SET P50296     AT LEVEL 005 AS OF 05/18/81
*          DATA SET P50296     AT LEVEL 004 AS OF 05/15/81
*          DATA SET P50296     AT LEVEL 003 AS OF 05/15/81
*          DATA SET P50296     AT LEVEL 002 AS OF 05/15/81
 PUNCH ' INCLUDE ISP(ISPLINK) '
 PUNCH ' ENTRY P50296 '
P50296   CSECT
 TITLE '**** SPF/RACF LIST COMMAND                  &SYSDATE &SYSTIME'
*
*
*
*
*   P50296 AUTHORIZES USER AND ATTACHES RACF COMMANDS
*
*
*
*
         EJECT
         PRIME
         REGISTER
         SPACE 3
*
*        DEFINE PROGRAM VARIABLES TO DIALOG MANAGER
*
         CALL  ISPLINK,(VDEF,NAMES,MUSER,CHAR,LNG),VL
         LTR   R15,R15
         BZ    DEFC
         ABEND 10,DUMP
VDEF     DC    CL8'VDEFINE'
NAMES    DC    C'(USERID GROUP)'
CHAR     DC    CL8'CHAR'
LNG      DC    F'4'
         SPACE 3
*
*        DEFINE PROGRAM CLASS AND RESOURCE VARIABLES
*
DEFC     EQU   *
         CALL  ISPLINK,(VDEF,CNAME,MCLASS,CHAR,LNG8),VL
         LTR   R15,R15
         BZ    DEFDS
         ABEND 10,DUMP
CNAME    DC    C'(CLASS GENERL)'
LNG8     DC    F'8'
         SPACE 3
*
*        DEFINE PROGRAM DATA SET VARIABLE
*
DEFDS    EQU   *
         CALL  ISPLINK,(VDEF,DNAME,DSNM,CHAR,LNG44),VL
         LTR   R15,R15
         BZ    REDISPL
         ABEND 10,DUMP
DNAME    DC    C'(DATASET)'
LNG44    DC    F'44'
         SPACE 3
*
*        CALL SPF DIALOG MANAGER TO DISPLAY LIST MENU
*
REDISPL  EQU   *
         CALL  ISPLINK,(DISPLAY,LIST,,CURSOR),VL
         CH    R15,=H'8'           PF3 ? ? ? ?
         BE    RETURN
         LTR   R15,R15
         BZ    DPARMS
         ABEND 1,DUMP
CURSOR   DC    CL8'USERID'
DISPLAY  DC    CL8'DISPLAY'
LIST     DC    CL8'RACFLIST'       MENU NAME
         SPACE 3
*
*        LINK TO LIST USER
*
DPARMS   EQU   *
         CLC   MUSER,=CL4' '       USER ENTERED ? ? ? ? ?
         BE    TRYGRP              YES
         LINK  EP=P50297,PARAM=(MUSER),VL=1
         B     REDISPL
         SPACE 3
*
*        LINK TO LIST GROUP
*
TRYGRP   EQU   *
         CLC   MGROUP,=CL4' '      GROUP ENTERED ? ? ? ? ?
         BE    TRYGNL              NO
         LINK  EP=P50298,PARAM=(MGROUP),VL=1
         MVC   CURSOR,=CL8'GROUP'
         B     REDISPL
         SPACE 3
*
*        LINK TO GENERALIZED RESOURCE DISPLAY
*
TRYGNL   EQU   *
         CLC   MGENERL(8),=CL8' '  RESOURCE SPECIFIED
         BE    CHKDSN              CHECK FOR DATA SET NAME
         LINK  EP=P50299,PARAM=(MGENERL,MCLASS),VL=1
         MVC   CURSOR,=CL8'GENERL'
         B     REDISPL
         SPACE 3
*
*        LINK TO DATA SET DISPLAY
*
CHKDSN   EQU   *
         CLI   DSNM,C' '           DATA SET NAME SPECIFIED
         BE    REDISPL             NO
         LINK  EP=P50300,PARAM=(DSNM),VL=1
         MVC   CURSOR,=CL8'DATASET'
         B     REDISPL
RETURN   EQU   *
         TERME
         EJECT
MUSER    DC    CL4' '
MGROUP   DC    CL4' '
MCLASS   DC    CL8' '
MGENERL  DC    CL8' '
DSNM     DC    CL44' '
         EJECT
         END
