*
//*
//* RECEIVED AT MILLER BREWING 2-1-80
//*
//* TMS/RACF MODIFICATION:
//*
//*              BOISE CASCADE INFO. SERVICES
//*              ONE JEFFERSON SQUARE
//*              BOISE, IDAHO      83728
//*                                       ATTN:  RON HOPPINS
//*                                              (208) 384-6100
//*
//*
************************************************************************
************************************************************************
*   BOISE CASCADE'S IMPLEMENTATION OF RACF TAPE BY DSNAME SUPPORT
*   FROM A B.O.F. SESSION AT SHARE 55 IN ATLANTA GA, AUGUST 1980
*   PRESENTED BY RON HOPPINS (BCC).
************************************************************************
************************************************************************

        WE PROTECT ALL OF OUR DATA SETS. ALL USERS ARE DEFINED WITH ADSP
        THEIR PROFILES (THIS IS NOT AN OPTION).

        THIS WRITEUP PRESUPPOSES INSTALLATION OF UCC/ONE(TMS) TAPE MANAG

        AS PART OF OUR INSTALLATION OF RACF WE WANTED ALL DATA SET SECUR
        APPEAR THE SAME TO THE USER REGARDLESS OF THE MEDIUM ON WHICH IT
        PENED TO RESIDE. WE REQUIRE ALL OF OUR DATA SETS TO BE CATALOGED
        THAT WE CAN MOVE DATA SETS AROUND ON DISK. JCL MAY BE CHANGED TO
        DATA SETS TO TAPE INSTEAD OF DISK OR VICE VERSA, EITHER TEMPORAR
        PERMANENTLY. BACKLEVEL GENERATIONS MAY BE MIGRATED OFF TO TAPE W
        THE USER BEING AWARE OF IT'S MOVEMENT. WE HAVE A LARGE NUMBER OF
        SEQUENTIAL DATA SETS AND MAKE HEAVY USAGE OF GENERATION DATA SET

        WE REPLACED TMS'S PASSWORD SECURITY PROCESSING MODULE WITH OUR O
        ISSUES RACDEF'INES FOR NEW DATA SETS AND RACHECK'S FOR OLD DATA
        SEVERAL PLACES IN TMS WE FORCED ENTRY INTO THE SECURITY MODULE S
        REQUIRE ALL DATA SETS TO BE PROTECTED. THE INDICATION THAT A TAP
        SET IS PROTECTED IS IN TMS'S CATALOG AND DOES NOT ALLOW FOR PROT
        OUTSIDE OUR DATA CENTER. BLP WAS CRIPPLED IN TMS SO THERE IS NO
        LITY TO READ A LABELED TAPE WITHOUT GOING THROUGH THE LABEL. THE
        COUPLE OF WAYS OF REMOVING BLP, WE CHOSE TMS BECAUSE IT WAS HAND
        SIDE TAPES WHICH DUPLICATE OUR VOLUME SERIAL NUMBERS WILL BE FAI
        THIS HAS NOT BEEN A PROBLEM BECAUSE ALL OF OUR TAPE VOLSER NUMBE
        PREFIXED WITH 'TP'. THE RACF PROFILES ARE DELETED BY A CHANGE TO
        TMSCLEAN. THERE IS CODE TO ENSURE THAT A PROTECTED SCRATCH TAPE
        OPENED FOR OUTPUT BEFORE IT CAN BE READ.

        ONE MINOR CHANGE HAD TO BE MADE TO RACF TO ALLOW RACDEF TO IGNOR
        DEVICE TYPE IN THE UCB WHEN HE WAS CREATING A PROFILE. WE USE BA
        STEPS IN PRODUCTION JCL TO DO PERMITS WHEN NECESSARY SO THAT NOR
        USER DOES NOT HAVE TO ISSUE ANY PERMIT COMMANDS. THE RACF EXITS
        PROBLEM SINCE THEY CONCERN THEMSELVES WITH ACCESS LISTS, AUTHORI
        NAMING SCHEMES, ETC., AND NORMALLY DO NOT CARE ABOUT THE DEVICE
        THE DATA SET RESIDES.

        ONE PROBLEM WE HAVE IGNORED IS  THE ABILITY TO ALOCATE OR ACCESS
        SET ON A MULTI DATA SET TAPE AND THEN WITH EXCP ISSUE TAPE POSIT
        COMMANDS TO READ PROTECTED DATA. ONE SOLUTION MIGHT BE TO DISALL
        MULTI DATA SET TAPES. ANOTHER WOULD BE TO CHECK ACCESS TO ALL OT
        SETS ON A TAPE BEFORE ALLOWING ACCESS.

        THE UCC PEOPLE PROVIDE OUR CHANGES AS ONE OF THE USER MODS IN TH
        LIBRARY.








//* THE CONTENTS OF THIS TAPE IS  3 JOBS:
//*   RHC0BXXX - OUR TMS SECURITY MODULE USING RACF COMMAND TO DEFINE
//*              AND VERIFY TAPES BY DSNAME.
//*              THIS REPLACES TMS'S IGC0BXXX.
//*   RHTMSCHG - IEBUPDTES TO THE TMS IGC0XXXX MODULES TO FORCE
//*              XCTL TO OUR IGC0BXXX MODULE.  'TMSDSS' DD CARD IS
//*              NOT REQUIRED.
//*   RHTMSRPT   IEBUPDTES TO TMSCLEAN TO DELETE THE RACF PROFILES
//*              WHEN A TAPE IS SCRATCHED.
//*
//* THE ABOVE CHANGES WERE MADE TO TMS AT THE 4.5 LEVEL
//*
//* THE ONLY CHANGE TO STANDARD RACF REQUIRED WAS TO ALLOW A DEVICE
//*   TYPE OF 'TAPE' TO BE TREATED THE SAME AS 'DISK'.
//*      THE SUPERZAP TO DO THIS IS:
//*           NAME IGC00130 ICHRDF00        (RELEASE 3)
//*           VER 1206 91202012,47E0A1FF    TEST UCB FOR DA
//*           REP 1206 91202012,4700A1FF    NOP IT
//*
//*
//*  NO ATTEMPT IS MADE TO CHECK FOR THE USER GETTING TO OTHER
//*    DATA SETS ON THE SAME TAPE USING EXCP.  ONE POSSIBILITY
//*    WOULD BE TO DISALLOW MULTIPLE DATA SETS PER TAPE OR
//*    CHECK ACCESS AUTHORITY TO ALL OTHER DATA SETS ON THE TAPE.
//*  INCLUDED IS A CHANGE TO IGC00XXX WHICH DISALLOWS BLP
//*  PROCESSING UNLESS JSCBPASS IS ON IN THE JSCB.  YOU MIGHT
//*    CONSIDER OTHER METHODS OF CURTAILING USE OF BLP.
//*  SECURITY LOOPHOLES OPENED UP BY USE OF EXPDT=98000 HAS NOT
//*    BEEN FULLY RESEARCHED.
//*
//*  ONE OTHER CHANGE WE MADE (NOT INCLUDED)  WAS TO ALLOW US
//*    TO RENAME ALL OF THE TAPE DATASETS WITHOUT COPYING THE TAPES.
//*    ALL WE DID WAS NOP THE CHECKS FOR THE 17 CHAR DSNAME.
//*    IF THE UCCONE TMC AND THE CATALOG SAID THE TAPE HAD A
//*    CERTAIN DSNAME THEN WE FELT THAT WAS A SUFFICIENT CHECK.
//*
//*
//RHC0BXXX JOB  (188800,P18450,RTH),TMS-RACF-XIT.HOPPINS,CLASS=7     *
// EXEC ASMLNK,LIB='SYS1.LPALIB',NAME=IGC0B23F,
//   LKED='LIST,MAP,TERM,RENT'  BAL           JCL RH     17:06 02/24/78
//ASM.SYSLIB  DD  DISP=SHR,DSN=SYS1.MACLIB
//            DD  DISP=SHR,DSN=BCISSYS.TMS45.TMSGEN
//            DD  DISP=SHR,DSN=SYS2.AMODGEN
//            DD  DISP=SHR,DSN=SYS1.RACFSRC
//SYSIN  DD   *
RTH      TITLE  'TMS RACF EXIT - REPLACES IGC0BXXX'
*          DATA SET RHRTXB1    AT LEVEL 010 AS OF 03/29/78
         COPY  TMSSVCNO
         EJECT
IGC0B&XA CSECT  ,
         TMSVCEQU SVC=X
         EJECT
*
ENTRY    BALR  R3,0
         USING *,R3
         USING SVCXWORK,R6
         USING IEC,R4                                             40095
         USING TMVT,R10
         LA    R13,SAVEAREA       POINT TO SAVE AREA
         ST    R2,SAVER2          SAVE RETURN ADDR FOR DSNBS      40095
         MVI   SWITCH,0           CLEAR THE SWITCH
******************************************************************
*   CHECK TO SEE IF IT IS A TEMPORARY DS AND BYPASS IF SO
******************************************************************
         TM    TMFLAG2,TMTEMPDS   TEMPORARY DATASET?
         BO    EXIT                YES - EXIT
******************************************************************
*   CHECK TO SEE IF IT IS OUTPUT
******************************************************************
         TM    0(R7),OUTPUT       Q. FILE OPENED FOR OUTPUT
         BO    PROUTPUT           A. YES - GO PROCESS OUTPUT
******************************************************************
*
*   PROCESS INPUT TAPES
*
******************************************************************
******************************************************************
*   CHECK TO SEE IF JSCBPASS IS ON AND BYPASS THIS EXIT IF SO
*       INPUT ONLY
******************************************************************
         L     R15,CVTPTR         POINT TO CVT
         L     R15,CVTTCBP(R15)         TO QUAD WORD
         L     R15,CURRTCB(R15)         TO CURRENT TCB
         L     R15,TCBJSTCB(R15)        TO JOB STEP TCB
         L     R15,TCBJSCB(R15)         TO JSCB
         TM    JSCBSWT1(R15),JSCBPASS  IS JSCBPASS ON?
         BO    EXIT                     YES - EXIT
         B     IRACHECK
*   THE FOLLOWING CODE IS CURRENTLY BEING BYPASSED FOR CONVERSION*
*   PURPOSES.  IT'S PURPOSE WAS TO FAIL JOBS ALLOCATING TO A DATA
*   SET AFTER IT WAS SCRATCHED, PROFILE DELETED, THEN MANUALLY
*   TAKEN OUT OF SCRATCH STATUS.
******************************************************************
*   CHECK FOR READ=NO AND IF SO FAIL THE TAPE
******************************************************************
         TM    TMDSSIF,X'80'      PASSWORD PROTECTED?
         BNO   IRACHECK            NO
         TM    TMDSSIF,X'40'      READ=YES?
         BNO   ABEND6              NO - FAIL IT
******************************************************************
*   SET UP AND ISSUE RACHECK
******************************************************************
IRACHECK MVC   RCPARM(RCPATL),RCPAT   MOVE PATTERN RACHECK
         RACHECK ENTITY=JFCBDSNM,VOLSER=TMVOLSER,                      X
               ATTR=READ,MF=(E,RCPARM)
         B     *+4(R15)           TEST RETURN CODE
         B     EXIT                0 OK
         B     EXIT                4 NO PROFILE - OK
         B     ABEND6              8 FAIL IT
         B     ABEND6              12 NOT USED
*
******************************************************************
*
*   PROCESS OUTPUT TAPES
*
******************************************************************
******************************************************************
*   CHECK FOR ADSP
******************************************************************
PROUTPUT OI    SWITCH,SWNOPASS    INDICATE NO PASSWORD ON OUTPUT
         CLC   =C'SYS7',JFCBDSNM  TEMPORARY FILE?                 03278
         BE    EXIT                YES                            03278
         CLC   =C'SYS8',JFCBDSNM  TEMPORARY FILE?                 03278
         BE    EXIT                YES                            03278
         L     R15,CVTPTR         POINT TO CVT
         L     R15,CVTTCBP(R15)   POINT TO THE QUAD WORD
         L     R15,CURRASCB(R15)  POINT TO THE ASCB
         L     R15,ASCBASXB(R15)  POINT TO THE ASXB
         L     R15,ASXBSENV(R15)  POINT TO THE ACEE
         TM    ACEEFLG1-ACEE(R15),ACEEADSP
         BNO   ORACHECK           BR IF NO ADSP TO DO THE OUT RACHECK
******************************************************************
*   CHECK FOR 98000
******************************************************************
         CLC   JFCBXPDT,=X'620000'  98000?
         BE    ORACHECK           BR IF SO TO DO THE OUT RACHECK
         NI    SWITCH,255-SWNOPASS  INDICATE PASSWORD ON OUTPUT
******************************************************************
*   SET UP AND ISSUE RACDEF
******************************************************************
         MVC   RDPARM(RDPATL),RDPAT   MOVE RACDEF PATTERN
         RACDEF ENTITY=JFCBDSNM,VOLSER=TMVOLSER,TYPE=DEFINE,           X
               MF=(E,RDPARM)
         B     *+4(R15)               TEST RETURN CODE
         B     SETPSWD                 0 - OK
         B     ORACHECK                4 (PREV DEFINED) DO RACHECK
         B     ABEND6                  8 FAIL IT
         B     ABEND6                  12 FAIL IT (SHOULDN'T HAPPEN)
         B     ABEND6                  16 FAIL IT (SHOULDN'T HAPPEN)
******************************************************************
*   SET UP AND ISSUE RACHECK FOR OUTPUT
******************************************************************
ORACHECK MVC   RCPARM(RCPATL),RCPAT   MOVE RACHECK PATTERN
         RACHECK ENTITY=JFCBDSNM,VOLSER=TMVOLSER,ATTR=UPDATE,          X
               MF=(E,RCPARM)
         B     *+4(R15)               TEST RETURN CODE
         B     RCOK                    0 - OK
         B     EXIT                    4 (NO ENTRY) OK
         B     ABEND6                  8 FAIL
         B     ABEND6                  12 UNUSED
*
******************************************************************
*   RACHECK WAS OK TURN ON PASSWORD PROTECT BIT IN TMC
******************************************************************
RCOK     TM    SWITCH,SWNOPASS         NO PASSWORD SW ON?
         BO    EXIT                     YES - SKIP TMDSSIF SET
SETPSWD  LA    R1,PASSWORD             SET PASSWORD TO MASTER
         BAL   R2,TMPSWRD              CONVERT IT
         ST    R1,SVCXTEMP
         MVC   TMDSSPW,SVCXTEMP        POST THE PASSWORD
         MVI   TMDSSIF,X'C0'           INDICATE PASSWORD AND READ=YES
         B     EXIT                    DONE
*
         TITLE 'DEFINE SUBROUTINES'
*
*
TMPSWRD  TMSPSWRD
         BR    R2
*
*                                                                 40095
CALLSVCY DS    0H                                                 40095
         LA    R1,TMSPARM         POINT TO PARAMETER LIST         40095
         SVC   SVCYYY             SVC Y                           40095
         BR    R2                 AND RETURN                      40095
*                                                                 40095
         TITLE 'EXIT TO NEXT LOAD MODULE'
*
EXIT     DS    0H
         LA    R11,IGC0FXXX       POINT TO MODULE NAME            12199
         TM    XFLAG1,VOLSWTCH    Q. EOV PROCESSING?              12199
         BO    XCTL               A. YES                          12199
         LA    R11,IGC0EXXX       POINT TO MODULE NAME            40189
         CLC   JFCBFLSQ,=H'01'    Q. SECONDARY DATA SET           40095
         BNH   XCTL               A. LOW,EQUAL - NO               40095
         TM    JFCBLTYP,X'63'     Q. LABEL TYPE SL OR NL          04101
         BZ    XCTL                GO TO F IF ZERO                04101
         LA    R11,IGC02XXX       POINT TO MODULE NAME            04095
         L     R2,SAVER2          LOAD RETURN ADDR FOR DSNBS      04095
*
XCTL     DS    0H
         MVC   SVCXXCTL+11(2),0(R11)  COMPLETE LOAD NAME          40095
         LA    R1,SVCXXCTL        POINT TO PARAMETER LIST
         LR    R15,R1             POINT TO XCTL PARAMETER LIST
         XCTL  MF=(E,(1)),SF=(E,(15))
*
IGC02XXX DC    C'02'              DSNB PROCESSING                 40095
IGC0EXXX DC    C'0E'              FINAL PROCESSING                40189
IGC0FXXX DC    C'0F'              EOV PROCESSING                  12199
*
PASSWORD DC    CL8'(TMS)321'
*
***********************************************************************
*    PATTERN FOR RACHECK
***********************************************************************
RCPAT    RACHECK CLASS='DATASET',MF=L
RCPATL   EQU   *-RCPAT
*
***********************************************************************
*    PATTERN FOR RACDEF
***********************************************************************
RDPAT    RACDEF  CLASS='DATASET',TYPE=DEFINE,MF=L
RDPATL   EQU   *-RDPAT
*
***********************************************************************
*    EQUATES
***********************************************************************
CVTPTR   EQU   16
CVTTCBP  EQU   0
TCBJSTCB EQU   124                JOB STEP TCB POINTER
TCBJSCB  EQU   180                POINTER TO JSCB
JSCBSWT1 EQU   243                JSCB SWITCH 1
JSCBPASS EQU   X'80'
CURRTCB  EQU   0
CURRASCB EQU   12
ASCBASXB EQU   X'6C'
ASXBSENV EQU   X'C8'
         LTORG ,
         TITLE 'DEFINE DSECTS'
*
SVCXWORK DSECT ,
         SVCXWORK
*
**                 DEFINE PASSWORD WORK AREA   (REDEFINES IOAREA2)
*
         ORG   IOAREA2
*
DWORD    DS    0D
*                                                                 40095
SAVER2   DS    A                  RETURN ADDR FOR DSNBS           40095
*
SWITCH   DS    X                  SWITCH
SWNOPASS EQU   1                  INDICATE NO PASSWORD REQUIRED
*
         SPACE 2
RCPARM   RACHECK CLASS='DATASET',MF=L
         ORG   RCPARM
RDPARM   RACDEF  CLASS='DATASET',MF=L
         ORG   ,
         SPACE 2
WORKEND  EQU   *-IOAREA2          CANNOT EXCEED 210 BYTES (X'0D2')
         ORG   ,
         SPACE 3
IEC      DSECT ,                                                  40095
         TMSIEC ,                                                 40095
         SPACE 2                                                  40095
TMVT     DSECT ,
         TMVT  TYPE=DSECT,TMVTEXT=NO
         SPACE 2
         PRINT NOGEN
TIOT     DSECT ,
         IEFTIOT1
         SPACE 2
DCB      DCBD  DSORG=(QS),DEVD=(DA)
         SPACE 2
         IHAACEE ,
         END
//RHTMSCHG JOB (188800,P18450,RTH),TMS-CHGS.HOPPINS
// EXEC PGM=IEBUPDTE,REGION=48K
//*ORIGIN RHTMS45.RHTMSCHG      JCL,NOFORMAT  JCL MD     14:42 12/19/79
//SYSPRINT DD SYSOUT=A
//SYSUT1  DD    DISP=SHR,DSN=BCISSYS.TMS45.TMSGEN
//SYSUT2  DD    DISP=SHR,DSN=BCISSYS.TMS45.TMSGEN
//SYSIN   DD    *
./        CHANGE NAME=TMSCLEAN,SEQFLD=738                         12079
         TM     TMDSSIF,X'80'    PASSWORD PROTECTED?              12079
         BNO    BCSKIPA           NO SKIP RACDEF DELETE           12079
         NI     TMDSSIF,255-X'40' TURN OFF READ=YES               12079
         RACDEF ENTITY=TMDSN,VOLSER=TMVOLSER,TYPE=DELETE,         12079X
               MF=(E,RACDEF)     DELETE THE RACF ENTRY            12079
         LTR    R15,R15                                           12079
         BZ     BCSKIPA                                           12079
         STC    R15,HWORD                                         12079
         UNPK   FWORD,HWORD                                       12079
         TR     FWORD,HEXTABLE-240                                12079
         MVC    P5RACRC,FWORD+1       PRINT OUT THE RETURN CODE   12079
BCSKIPA  DS     0H                                                12079
         TM     TMDSSIF,X'80'     TAPE PASSWORD PROTECTED?        12079
         BNO    BCSKIP             NO SKIP RACDEF DELETE          12079
         RACDEF ENTITY=DSNBDSN,VOLSER=DSNBVSN,TYPE=DELETE,        12079X
               MF=(E,RACDEF)     DELETE THE RACF ENTRY            12079
         LTR    R15,R15                                           12079
         BZ     BCSKIP                                            12079
         STC    R15,HWORD                                         12079
         UNPK   FWORD,HWORD                                       12079
         TR     FWORD,HEXTABLE-240                                12079
         MVC    P5RACRC,FWORD+1     PRINT OUT THE RETURN CODE     12079
BCSKIP   DS     0H                                                12079
RACDEF   RACDEF TYPE=DELETE,CLASS='DATASET',MF=L                  12079
         DS    CL01                                               12079
P5RACRC  DS    CL02                                               12079
         DS    CL01                                               12079
./        CHANGE NAME=IGC0AXXX,SEQFLD=738                         12079
         BO   BCCPASSE           A. ON - YES                      12079
         B    FINALOUT           NOT EOV                          12079
BCCPASSE MVI  TMDSSIF,X'01'      POST 'ASSGN PASSWORD' BIT        12079
         B    EXIT               GO TO EXIT                       12079
         B    BCCPASS            FORCE RACF CHECKING              12079
BCCPASS  DS   0H                 FORCE RACF CHECKING              12079
         BO   BCCHKRV            A. ON - YES GO RACF PROTECT      12189
*                                                                 12189
BCCHKRV  CLI  TMDSSIF,X'00'      CHECK PASSWORD FLAGS             12189
         BE   XCTL                                                12189
./        CHANGE NAME=IGC02XXX,SEQFLD=738                         12079
         BAL  R2,CHECKDSS        FORCE RACF CHECKING              12079
         B    BCFORCE            FORCE RACF CHECKING              12079
BCFORCE  DS   0H                                                  12079
./        CHANGE NAME=IGC00XXX,SEQFLD=738                         12079
*         DISALLOW BLP UNLESS JSCBPASS IS ON                      12079
         TM    JFCBLTYP,X'10'   IS IT BLP?                        12079
         BZ    BCNL              YES- ALLOW IT                    12079
         L     R11,CVTPTR       POINT TO CVT                      12079
         L     R11,CVTTCBP(R11) POINT TO TCB/ASCB WORDS           12079
         L     R11,CURRTCB(R11) GET OUR TCB ADDRESS               12079
         L     R11,TCBJSCB(R11) POINT TO JSCB                     12079
         TM    JSCBSWT1(R11),JSCBPASS PASSWORD CHECKING?          12079
         BNO   ABEND6                                             12079
BCNL     DS    0H                                                 12079
CVTPTR   EQU   X'10'                                              12079
CVTTCBP  EQU   X'00'                                              12079
CURRTCB  EQU   X'04'                                              12079
TCBJSCB  EQU   180                                                12079
JSCBSWT1 EQU   243                                                12079
JSCBPASS EQU   X'80'                                              12079
//*Ô RHTMS45.RHC0BXXX      ++INCLUDED
/*ROUTE  PRINT REMOTE13
