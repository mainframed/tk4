000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID. P50100.
000030 REMARKS.
000040**************************************************************
000050*    SYSTEM NAME.    ENVIRON/1.                              *
000060*                                                            *
000070*    PROGRAM NAME.   E/1 PASSWORD FILE MAINTENANCE(RACF)     *
000080*                                                            *
000090*    EFFECTIVE DATE  01/01/75.                               *
000100*                                                            *
000110*    ANALYST.        W. R. GULLEY.                           *
000120*                                                            *
000130*    PROGRAMMED BY.  W. R. GULLEY.                           *
000140*                                                            *
000150*    DATE WRITTEN.   09/27/74                                *
000160*                                                            *
000170**************************************************************
000180*                    PROGRAM ABSTRACT                        *
000190*                                                            *
000200*    1.  PURPOSE.    TO PROVIDE UPDATE & INQUIRE ABILITY     *
000210*                    FOR THE E/1 PASSWORD FILE AND RACF      *
000220*                    DATASET.                                *
000230*                                                            *
000240*    2.  FILE DESCRIPTIONS.                                  *
000250*        I/O     NAME    MODE    TYPE    DDNAME              *
000260*        I/O     F02420  RAND    TOTAL   PASS                *
000270*                                                            *
000280*    3.  MINIMUM MACHINE REQUIREMENTS.                       *
000290*        N/A-E/1 PROGRAM                                     *
000300*                                                            *
000310*    4.  PROGRAM OPTIONS.                                    *
000320*        N/A                                                 *
000330*                                                            *
000340*    5.  OPERATION INSTRUCTIONS.                             *
000350*        N/A-E/1 SYSTEM.                                     *
000360*                                                            *
000370*    6.  CONSOLE MSG.                                        *
000380*        NONE.                                               *
000390*                                                            *
000400*    7.  LIBRARY SUBROUTINES.                                *
000410*        TOTAL,E/1                                           *
000420*                                                            *
000430*    8.  MAINTENANCE CONSIDERATIONS.                         *
000440*        KNOWLEDGE OF TOTAL,E/1,AND E/1 SECURITY SYSTEM.     *
000450*        AND KNOWLEDGE OF RACF AND SVC 132 PROCESSING        *
000460*                                                            *
000470*    9.  MAINTENANCE PERFORMED.                              *
000480*        DATE    NAME    WORK                                *
000490*        6/79    RAC     THIS PROGRAM IS A MODIFIED VERSION  *
000500*                        OF P02760 WHICH HANDLES PASSWORD    *
000510*                        FILE MAINTAINANCE UNDER RACF.       *
000520*                        TRANSACTON           PURPOSE        *
000530*                         ANP1          ADD TYPE 1 REC       *
000540*                         ANP2          ADD TYPE 2 REC       *
000550*                         ANP4          ADD TYPE 4 REC       *
000560*                         CNP1          CHANGE TYPE 1 REC    *
000570*                         CNP2          CHANGE TYPE 2 REC    *
000580*                         DNP1          DELETE TYPE 1 REC    *
000590*                         DNP2          DELETE TYPE 2 REC    *
000600*                         DNP4          DELETE TYPE 4 REC    *
000610*                         INP1          INQUIRE TYPE 1 REC   *
000620*                         INP2          INQUIRE TYPE 2 REC   *
000630*                         LNPW          LIST PASSWORDS       *
000640*                         DNPW          MASS DELETE          *
000650*                         NOTE:WHILE THE TYPE 1 AND 2 RECORDS*
000660*                              HAVE NOT CHANGED IN FORMAT,   *
000670*                              TRANSACTIONS ARE PROVIDED TO  *
000680*                              HANDLE THEM. THIS DUPLICATION *
000690*                              WILL ALLOW THIS PROGRAM TO    *
000700*                              TOTALLY REPLACE P02760 WHEN   *
000710*                              EVERYONE IS UNDER RACF        *
000720*                                                            *
000730*        7/79    RAC      THE PROGRAM WAS FURTHER MODIFIED   *
000740*                         TO REMOVE THE TYPE 3 TRANSACTIONS  *
000750*                         AND MODIFY THE TYPE 2 TRANSACTIONS *
000760*                         TO ALLOW CONTROL OF TRANSACTIONS   *
000770*                         THROUGH RACF. THIS MODIFICATION    *
000780*                         CHANGES THE FORMAT OF THE TYPE 2   *
000790*                         TRANSACTION BY REMOVING THE        *
000800*                         'SYSTEM' FROM THE KEY AND ADDING   *
000810*                         A TRANSACTION TYPE TO DETERMINE    *
000820*                         WHETHER THE ENTRY POINT IS FOR AN  *
000830*                         ASYNCHRONOUS TASK OR NOT.          *
000840*                                                            *
000850*        1/80    RAC      THE PROGRAM WAS MODIFIED TO CALL   *
000860*                         SUBROUTINES WHICH INVOKE THE RACF  *
000870*                         MANAGER DIRECTLY. THIS ALLOWS THE  *
000880*                         USER TO UPDATE THE RACF DATASET    *
000890*                         AT THE SAME TIME AS THE PASSWORD   *
000900*                         FILE IS BEING UPDATED. THIS CHANGE *
000910*                         ONLY AFFECTS THE TYPE 2 TRANS-     *
000920*                         ACTIONS. THE SUBROUTINES USED AND  *
000930*                         THEIR FUNCTIONS ARE:               *
000940*                                                            *
000950*                          RACFADD - ADD A TRANSACTION WITH  *
000960*                            UACC(NONE) AND USER XXXX WITH   *
000970*                            READ AUTHORIZATION              *
000980*                                                            *
000990*                          RACFALT - ALTER THE AUTHORIZATION *
001000*                            LIST OR THE USER DATA. BASED ON *
001010*                            ON A REQUEST CODE, THIS ROUTINE *
001020*                            WILL ADD, DELETE OR MODIFY THE  *
001030*                            ACCESS LIST. THIS ROUTINE WILL  *
001040*                            ONLY BE INVOKED IF THE USER IS  *
001050*                            AUTHORIZED(IE. HE IS RUNNING    *
001060*                            UNDER THE PRODUCTION SYSTEM).   *
001070*                                                            *
001080*                          RACFAUTH - RETRIEVE ACCESS LIST   *
001090*                            AND USER DATA FROM A RACF PROF- *
001100*                            ILE.                            *
001110*                                                            *
001120*                          RCFDELE - DELETE A RESOURCE FROM  *
001130*                            THE RACF DATASET.               *
001140*                                                            *
001150*                         IN ADDITION, TRANSACTION CNP4 WAS  *
001160*                         ADDED TO ALLOW CHANGING OF THE     *
001170*                         LOCATION FIELD IN THE TYPE 4 PASS- *
001180*                         WORD RECORD.                       *
001190*                                                            *
001200*    10  EXCEPTIONS TO STANDARDS.                            *
001210*        E/1                                                 *
001220*                                                            *
001230**************************************************************
001240 ENVIRONMENT DIVISION.
001250 DATA DIVISION.
001260 FILE SECTION.
001270 01  TPDSR                       PIC X.
001280 01  MIDSR                       PIC X.
001290 01  MODSR                       PIC X.
001300 01  C1DSR COPY C1DSR.
001310     05  C1-RACF-OVLY REDEFINES C1-TASK-PARM.
001320       10  C1-RACF-TERM-ID       PIC X(4).
001330       10  C1-RACF-SIGNON-PARMS.
001340         15  C1-RACF-SYSTEM-ID   PIC X(3).
001350         15  C1-RACF-TRAN-ID     PIC X(4).
001360         15  FILLER              PIC X(6).
001370         15  C1-RACF-USER-ID     PIC X(4).
001380         15  C1-RACF-SIGNON-FLAG PIC X(1).
001390*          88  RACF-SIGNON                VALUE X'99'.
001400         15  FILLER PIC X(1).
001410*
001420     05  C1-ASC-DELAY-RD REDEFINES C1-XAREA.
001430       10  C1-ASC-DELAY-INTERVAL PIC S9(5)               COMP.
001440     05  C1-NET-OVLY REDEFINES C1-XAREA.
001450       10  C1-NETWORK            PIC XXX.
001460 01  RSDSR                       PIC X.
001470 01  DBDSR.
001480     05  DB-PASS-REC.
001490     07  DB-KEY.
001500       10  DB-SYSID              PIC XXX.
001510       10  DB-TRANID             PIC X(4).
001520       10  DB-OPERID             PIC X(4).
001530       10  DB-BLANKS             PIC X(8).
001540         88  RACF-TYPE-REC   VALUE '        '.
001550     07  DB-DATA.
001560       10  DB-CODE               PIC X.
001570       10  DB-OC                 PIC X.
001580       10  DB-TRAN-TY            PIC X.
001590       10  DB-EP                 PIC X(8).
001600       10  DB-ASCTNO             PIC X(3).
001610       10  FILLER                PIC X(479).
001620     05  PASS-T4-REC REDEFINES DB-PASS-REC.
001630       10  FILLER                PIC X(7).
001640       10  PASS-T4-UID           PIC X(4).
001650       10  FILLER                PIC X(9).
001660       10  PASS-T4-LA-TIME       PIC X(6).
001670       10  PASS-T4-LA-DATE       PIC X(8).
001680       10  PASS-T4-LA-TERM       PIC X(4).
001690       10  PASS-T4-LC            PIC X(6).
001700 01  A1DSR COPY M5010001.
001710   02  PASS08-OV1 REDEFINES A1DMB-AREA.
001720     05  FILLER              PIC X(301).
001730     05  USER-ARRAY1 OCCURS 30 TIMES.
001740         10  XUSER1          PIC X.
001750         10  USER1           PIC XXXX.
001760         10  XAUTH1          PIC X.
001770         10  AUTH1           PIC X.
001780   02 PASS08-OV3 REDEFINES A1DMB-AREA.
001790     05  FILLER              PIC X(301).
001800     05  PASS08-SPACE1       PIC X(211).
001810 01  A2DSR COPY M5010002.
001820   02  PASS08-OV2 REDEFINES A2DMB-AREA.
001830     05  FILLER              PIC X(6).
001840     05  USER-ARRAY2 OCCURS 61 TIMES.
001850         10  XUSER2          PIC X.
001860         10  USER2           PIC XXXX.
001870         10  XAUTH2          PIC X.
001880         10  AUTH2           PIC X.
001890   02 PASS08-OV4 REDEFINES A2DMB-AREA.
001900     05  FILLER              PIC X(6).
001910     05  PASS08-SPACE2       PIC X(427).
001920 01  A3DSR COPY M5010003.
001930 01  A4DSR COPY M5010004.
001940 WORKING-STORAGE SECTION.
001950 01  W1DSR.
001960     05  W1-UPDT-CT              PIC 9(5) INDEX VALUE 0.
001970     05  W1-DELAY                PIC 9(5) INDEX VALUE 300.
001980     05  W1-INDX                 PIC 9(5) INDEX VALUE ZERO.
001990     05  W1-READ-CNT             PIC 99         VALUE ZERO.
002000     05  HOLD-KEY                PIC X(3).
002010     05  BUILD-KEY.
002020       10  BK-SYSID              PIC XXX.
002030       10  BK-TRNID              PIC X(4).
002040       10  BK-OPER               PIC X(4).
002050       10  FILLER                PIC X(8).
002060     05  LPSW-LINE.
002070       10  FILLER                PIC XX.
002080       10  LPSW-SYSID            PIC X(3).
002090       10  FILLER                PIC X(5).
002100       10  LPSW-TRANID           PIC X(4).
002110       10  FILLER                PIC X(4).
002120       10  LPSW-OPID             PIC X(4).
002130       10  FILLER                PIC X(6).
002140       10  LPSW-CODE             PIC X.
002150       10  FILLER                PIC X(4).
002160       10  LPSW-OC               PIC X.
002170       10  FILLER                PIC X(2).
002180       10  LPSW-EP               PIC X(8).
002190       10  FILLER                PIC X(5).
002200       10  LPSW-ASCTNO           PIC X(3).
002210       10  FILLER                PIC X(5).
002220       10  LPSW-ASCEP            PIC X(8).
002230       10  FILLER                PIC X(2).
002240       10  LPSW-LA-DATE          PIC X(8).
002250     05  W1-QUAL.
002260       10  W1-QUAL1              PIC X(4).
002270       10  W1-QUAL2              PIC X(4).
002280     05  LPSW-SW                 PIC 9.
002290     05  DELETE-SW               PIC 9.
002300         88  DELETE                             VALUE  1.
002310     05  SYSID-SW                PIC 9.
002320       88  SYSTST                               VALUE  1.
002330     05  TRNID-SW                PIC 9.
002340       88  TRNTST                               VALUE  1.
002350     05  PASS-SW                 PIC 9.
002360       88  PASTST                               VALUE  1.
002370     05  OPID-SW                 PIC 9.
002380       88  OPIDTST                              VALUE  1.
002390     05  OPIN-SW                 PIC 9.
002400       88  OPINTST                              VALUE  1.
002410     05  W1-LNCT                 PIC 99         VALUE ZERO.
002420     05  W1-AUTH-FLAG            PIC X.
002430       88  NOT-AUTHORIZED        VALUE 'N'.
002440       88  AUTHORIZED            VALUE 'A'.
002450     05  W1-RACF-ERROR.
002460       10  W1-RACF-F1            PIC X(11)
002470                                 VALUE 'RACF ERROR '.
002480       10  W1-RACF-ERR-CD        PIC 9999.
002490     05  W1-WORK                 PIC X(7).
002500     05  W1-WORK-OV  REDEFINES W1-WORK.
002510         10  W1-XUSER            PIC X.
002520         10  W1-USER             PIC XXXX.
002530         10  W1-XAUTH            PIC X.
002540         10  W1-AUTH             PIC X.
002550     05  W1-EXIST-TRAN           PIC X.
002560     05  W1-SCREEN-FLAG          PIC X.
002570     05  W1-K                    PIC S999 COMP VALUE 0.
002580 01  W2DSR.
002590     05  STORE  OCCURS 20 TIMES PIC X(19).
002600 01  W3DSR.
002610     02  W3DSR-O                PIC X(512).
002620     02  W3-ADD-OV REDEFINES W3DSR-O.
002630         05  W3-ADD-RET-CD   PIC S999 COMP.
002640         05  W3-ADD-TRAN     PIC X(4).
002650         05  W3-ADD-LEN      PIC X.
002660         05  W3-ADD-DESC     PIC X(30).
002670     02  W3-ALT-OV REDEFINES W3DSR-O.
002680         05  W3-ALT-RET-CD   PIC S999 COMP.
002690         05  W3-ALT-REQ-CD   PIC S999 COMP.
002700         05  W3-ALT-USER     PIC X(4).
002710         05  W3-ALT-AUTH     PIC X.
002720         05  W3-ALT-TRAN     PIC XXXX.
002730         05  W3-ALT-LEN      PIC X.
002740         05  W3-ALT-DESC     PIC X(30).
002750     02  W3-DEL-OV REDEFINES W3DSR-O.
002760         05  W3-DEL-RET-CD   PIC S999 COMP.
002770         05  W3-DEL-TRAN     PIC X(4).
002780 01  W4DSR.
002790     02  W4DSR-O                PIC X(512).
002800     02  W4-LOC-OV REDEFINES W4DSR-O.
002810         05  W4-LOC-RET-CD   PIC S999 COMP.
002820         05  W4-LOC-NO-RT    PIC S999 COMP.
002830         05  W4-LOC-NO-RE    PIC S999 COMP.
002840         05  FILLER          PIC XX.
002850         05  W4-LOC-WK1      PIC S99999 COMP.
002860         05  W4-LOC-WK2      PIC S99999 COMP.
002870         05  W4-TRAN         PIC XXXX.
002880         05  W4-DESC         PIC X(30).
002890         05  W4-USER-ARRAY   OCCURS 90 TIMES.
002900             10  W4-LOC-USER   PIC X(4).
002910             10  W4-LOC-AUTH   PIC X.
002920 PROCEDURE DIVISION.
002930 AUTH1KEY SECTION.
002940     FILL W1DSR.
002950     MOVE 'PASS01' TO C1-MAP-ID.
002960     PERFORM GETMAP.
002970     MOVE 'ENTER SYSTEM RECORD KEY' TO C1-TASK-NAME.
002980     MOVE C1-HDR TO PASS01-HDG.
002990     PERFORM F3270.
003000 PASS1KEY-READ.
003010     PERFORM R3270.
003020     IF SHUTDOWN-NOT-IN-PROGRESS
003030         NEXT SENTENCE ELSE
003040         GO TO TASK-ENDING.
003050     IF AID-CANCEL
003060         GO TO TRAN-LOOP.
003070     IF AID-END
003080         GO TO TASK-ENDING.
003090     IF C1-AID-ENTER IS NOT = C1-AID-RECEIVED
003100         PERFORM BADAID
003110         GO TO PASS1KEY-READ.
003120 TASK-CHECK.
003130     MOVE PASS01-SYSID TO HOLD-KEY.
003140     IF C1-TRAN-ID = 'ANP1'
003150         GO TO PROCESS-ADD1.
003160     IF C1-TRAN-ID = 'CNP1'
003170         GO TO PROCESS-CHG1.
003180     IF C1-TRAN-ID = 'DNP1'
003190         GO TO PROCESS-DEL1.
003200     GO TO PROCESS-INQ1.
003210 PROCESS-ADD1.
003220     MOVE HOLD-KEY TO C1-MMMMCTRL.
003230     MOVE SPACES TO DB-PASS-REC.
003240     MOVE HOLD-KEY TO DB-SYSID.
003250     MOVE '1' TO DB-CODE.
003260     MOVE 'C' TO DB-OC.
003270     PERFORM APASS.
003280     IF TOTAL-ERROR-FREE
003290         MOVE 'ADD COMPLETED' TO PASS01-ERROR
003300         MOVE C1-ACB-WRITE-ERRORS TO XPASS01-ERROR
003310         PERFORM W3270S
003320         PERFORM R3270
003330         GO TO TRAN-LOOP.
003340     IF TOTAL-DUPLICATE-MASTER-RECORD
003350         MOVE 'REJECTED-RECORD EXISTS' TO PASS01-ERROR
003360         MOVE C1-ACB-WRITE-ERRORS TO XPASS01-ERROR
003370         PERFORM W3270S
003380         PERFORM R3270
003390         GO TO TRAN-LOOP.
003400     PERFORM TOTERR.
003410 PROCESS-CHG1.
003420     MOVE 'PASS06' TO C1-MAP-ID.
003430     PERFORM GETMAP.
003440     MOVE 'SYSTEM PASSWORD CHANGE' TO C1-TASK-NAME.
003450     MOVE C1-HDR TO PASS06-HDG.
003460     MOVE HOLD-KEY TO C1-MMMMCTRL.
003470     MOVE SPACES TO DB-PASS-REC.
003480     PERFORM RPASSU.
003490     IF TOTAL-MASTER-RECORD-NOT-FOUND
003500         MOVE 'REQUESTED RECORD NOT FOUND' TO PASS06-ERROR
003510         PERFORM F3270
003520         PERFORM R3270
003530         GO TO TRAN-LOOP.
003540     IF TOTAL-ERROR-FREE
003550         NEXT SENTENCE ELSE
003560         PERFORM TOTERR.
003570     MOVE DB-SYSID TO PASS06-SYSID.
003580     MOVE DB-OC TO PASS06-OC.
003590     PERFORM F3270.
003600 REREAD-CHG1.
003610     PERFORM R3270.
003620     IF FORCE-SHUTDOWN-IN-PROGRESS
003630         GO TO TASK-ENDING.
003640     IF AID-CANCEL
003650         GO TO TRAN-LOOP.
003660     IF AID-END
003670         GO TO TASK-ENDING.
003680     IF XPASS06-OC = C1-ACB-INPUT AND
003690         PASS06-OC = 'O'
003700         MOVE 'REJECTED-YOU MAY NOT OPEN A SYSTEM RECORD' TO
003710         PASS06-ERROR
003720         MOVE C1-ACB-WRITE-SELECT TO XPASS06-ERROR
003730         PERFORM W3270S
003740         PERFORM R3270
003750         GO TO TRAN-LOOP.
003760     IF XPASS06-OC = C1-ACB-INPUT
003770         MOVE PASS06-OC TO DB-OC.
003780     PERFORM WPASS.
003790     IF TOTAL-ERROR-FREE
003800         MOVE 'CHANGE COMPLETE' TO PASS06-ERROR
003810         MOVE C1-ACB-WRITE-ERRORS TO XPASS06-ERROR
003820         PERFORM W3270S
003830         PERFORM R3270
003840         GO TO TRAN-LOOP.
003850     PERFORM TOTERR.
003860 PROCESS-DEL1.
003870     MOVE 'PASS09' TO C1-MAP-ID.
003880     PERFORM GETMAP.
003890     MOVE 'SYSTEM PASSWORD DELETE' TO C1-TASK-NAME.
003900     MOVE C1-HDR TO PASS09-HDG.
003910     MOVE HOLD-KEY TO C1-MMMMCTRL.
003920     MOVE SPACES TO DB-PASS-REC.
003930     PERFORM RPASSU.
003940     IF TOTAL-MASTER-RECORD-NOT-FOUND
003950         MOVE 'REQUESTED RECORD DOES NOT EXIST' TO PASS09-ERROR
003960         MOVE C1-ACB-WRITE-ERRORS TO XPASS09-ERROR
003970         PERFORM F3270
003980         PERFORM R3270
003990         GO TO TRAN-LOOP.
004000     IF TOTAL-ERROR-FREE
004010         NEXT SENTENCE ELSE
004020         PERFORM TOTERR.
004030     IF DB-CODE NOT = '1'
004040         MOVE 'REQUESTED RECORD DOES NOT EXIST' TO
004050         PASS09-ERROR
004060         MOVE C1-ACB-WRITE-ERRORS TO XPASS09-ERROR
004070         PERFORM F3270
004080         PERFORM R3270
004090         GO TO TRAN-LOOP.
004100     MOVE DB-SYSID TO PASS09-SYSID.
004110     PERFORM F3270.
004120 REREAD-DELETE.
004130     PERFORM R3270.
004140     IF FORCE-SHUTDOWN-IN-PROGRESS
004150         GO TO TASK-ENDING.
004160     IF AID-CANCEL
004170         GO TO TRAN-LOOP.
004180     IF AID-END
004190         NEXT SENTENCE ELSE
004200         PERFORM BADAID
004210         GO TO REREAD-DELETE.
004220     IF DB-SYSID = 'ENV'
004230         MOVE 'REJECTED-ATTEMPT TO DELETE E/1 MASTER RECORD'
004240         TO PASS09-ERROR
004250         MOVE C1-ACB-WRITE-ERRORS TO XPASS09-ERROR
004260         PERFORM W3270S
004270         PERFORM R3270
004280         GO TO TRAN-LOOP.
004290     PERFORM DPASS.
004300     IF TOTAL-ERROR-FREE
004310         MOVE 'DELETE COMPLETED' TO PASS09-ERROR
004320         MOVE C1-ACB-WRITE-ERRORS TO XPASS09-ERROR
004330         PERFORM W3270S
004340         PERFORM R3270
004350         GO TO TRAN-LOOP.
004360     PERFORM TOTERR.
004370 PROCESS-INQ1.
004380     MOVE 'PASS12' TO C1-MAP-ID.
004390     PERFORM GETMAP.
004400     MOVE 'SYSTEM PASSWORD INQUIRE' TO C1-TASK-NAME.
004410     MOVE C1-HDR TO PASS12-HDG.
004420     MOVE HOLD-KEY TO C1-MMMMCTRL.
004430     PERFORM RPASS.
004440     IF TOTAL-MASTER-RECORD-NOT-FOUND
004450         MOVE 'RECORD NOT FOUND' TO PASS12-ERROR
004460         PERFORM F3270
004470         PERFORM R3270
004480         GO TO TRAN-LOOP.
004490     IF TOTAL-ERROR-FREE
004500         NEXT SENTENCE ELSE
004510         PERFORM TOTERR.
004520     MOVE DB-SYSID TO PASS12-SYSID.
004530     MOVE DB-OC TO PASS12-OC.
004540     PERFORM F3270.
004550     PERFORM R3270.
004560     IF AID-END
004570         GO TO TASK-ENDING.
004580     GO TO TRAN-LOOP.
004590 AUTH2KEY SECTION.
004600     FILL W1DSR.
004610     CALL 'GETBLOCK' USING W3DSR.
004620     CALL 'YIELD'.
004630     CALL 'GETBLOCK' USING W4DSR.
004640     CALL 'YIELD'.
004650     PERFORM CHKNETWK.
004660     IF C1-NETWORK ='PRD' THEN MOVE 'A' TO W1-AUTH-FLAG
004670         ELSE MOVE 'N' TO W1-AUTH-FLAG.
004680     IF C1-RACF-USER-ID = 'TS14' MOVE 'A' TO W1-AUTH-FLAG.
004690 PASS2KEY-1.
004700     IF SHUTDOWN-NOT-IN-PROGRESS
004710         NEXT SENTENCE ELSE
004720         GO TO TASK-ENDING.
004730     MOVE 'PASS02' TO C1-MAP-ID.
004740     PERFORM GETMAP.
004750     MOVE 'ENTER TRANSACTION REC KEY' TO C1-TASK-NAME.
004760     MOVE C1-HDR TO PASS02-HDG.
004770     MOVE BK-TRNID TO PASS02-TRANID.
004780     PERFORM F3270.
004790 PASS2KEY-READ.
004800     PERFORM R3270.
004810     IF SHUTDOWN-NOT-IN-PROGRESS
004820         NEXT SENTENCE ELSE
004830         GO TO TASK-ENDING.
004840     IF AID-CANCEL
004850         GO TO TRAN-LOOP.
004860     IF AID-END
004870         GO TO TASK-ENDING.
004880     IF C1-AID-ENTER IS NOT = C1-AID-RECEIVED
004890         PERFORM BADAID
004900         GO TO PASS2KEY-READ.
004910 TASK2-CHECK.
004920     MOVE SPACES TO BUILD-KEY.
004930     MOVE PASS02-TRANID TO BK-TRNID.
004940     IF C1-TRAN-ID = 'ANP2'
004950         GO TO PROCESS-ADD2.
004960     IF C1-TRAN-ID = 'CNP2'
004970         GO TO PROCESS-CHG2.
004980     IF C1-TRAN-ID = 'DNP2'
004990         GO TO PROCESS-DEL2.
005000     GO TO PROCESS-INQ2.
005010 PROCESS-ADD2.
005020     MOVE 'PASS04' TO C1-MAP-ID.
005030     PERFORM GETMAP.
005040     MOVE 'N' TO W1-EXIST-TRAN.
005050     IF NOT-AUTHORIZED GO TO PROCESS-ADD2-0010.
005060         MOVE 0 TO W4-LOC-WK1.
005070         MOVE 0 TO W4-LOC-WK2.
005080         MOVE BK-TRNID TO W4-TRAN.
005090         PERFORM RACFAUTH.
005100         IF W4-LOC-RET-CD NOT = 12 AND W4-LOC-RET-CD NOT = 0
005110            MOVE W4-LOC-RET-CD TO W1-RACF-ERR-CD
005120            MOVE W1-RACF-ERROR TO PASS04-ERROR
005130            MOVE C1-ACB-WRITE-ERRORS TO XPASS04-ERROR
005140            PERFORM W3270S
005150            PERFORM R3270
005160            GO TO PASS2KEY-1.
005170         IF W4-LOC-RET-CD = 0
005180             MOVE W4-DESC TO PASS04-DESC
005190             MOVE 'A' TO W1-EXIST-TRAN.
005200 PROCESS-ADD2-0010.
005210     MOVE 'TRANSACTION PASSWORD ADD' TO C1-TASK-NAME.
005220     MOVE C1-HDR TO PASS04-HDG.
005230     MOVE BK-TRNID TO PASS04-TRANID.
005240     PERFORM F3270.
005250 REREAD-ADD2.
005260     PERFORM R3270. MOVE SPACES TO DB-PASS-REC.
005270     IF SHUTDOWN-NOT-IN-PROGRESS
005280         NEXT SENTENCE ELSE
005290         GO TO TASK-ENDING.
005300     IF AID-CANCEL
005310         GO TO TRAN-LOOP.
005320     IF AID-END
005330         GO TO TASK-ENDING.
005340     IF C1-AID-RECEIVED = C1-AID-ENTER
005350         NEXT SENTENCE ELSE
005360         PERFORM BADAID
005370         GO TO REREAD-ADD2.
005380     IF XPASS04-DESC = 'R' AND W1-EXIST-TRAN = 'N' THEN
005390         MOVE 'REQUIRED DESCRIPTION NOT ENTERED' TO PASS04-ERROR
005400         PERFORM F3270
005410         GO TO REREAD-ADD2.
005420     IF XPASS04-ENTRYPT = C1-ACB-INPUT
005430         MOVE PASS04-ENTRYPT TO DB-EP.
005440     IF XPASS04-ASCTNO = C1-ACB-INPUT
005450         MOVE PASS04-ASCTNO TO DB-ASCTNO.
005460     MOVE 'O' TO DB-OC.
005470     MOVE '2' TO DB-CODE.
005480     IF PASS04-TRANTY NOT EQUAL 'S' AND
005490        PASS04-TRANTY NOT EQUAL 'A'
005500        MOVE 'ILLEGAL TRAN TYPE -- ENTER S OR A' TO PASS04-ERROR
005510        PERFORM F3270
005520        GO TO REREAD-ADD2
005530     ELSE MOVE PASS04-TRANTY TO DB-TRAN-TY.
005540     MOVE BK-TRNID TO DB-TRANID.
005550     MOVE BUILD-KEY TO C1-MMMMCTRL.
005560     PERFORM APASS.
005570     IF NOT TOTAL-ERROR-FREE GO TO ADD2-TOT-ERR.
005580     MOVE BK-TRNID TO W3-ADD-TRAN.
005590     MOVE PASS04-DESC TO W3-ADD-DESC.
005600     PERFORM RACFADD.
005610     IF W3-ADD-RET-CD = 0 OR W3-ADD-RET-CD = 8
005620         GO TO ADD2-DUP-CHK.
005630         MOVE W3-ADD-RET-CD TO W1-RACF-ERR-CD
005640         MOVE W1-RACF-ERROR TO PASS04-ERROR
005650         MOVE C1-ACB-WRITE-ERRORS TO XPASS04-ERROR
005660         PERFORM DPASS.
005670         IF NOT TOTAL-ERROR-FREE PERFORM TOTERR.
005680         PERFORM W3270S
005690         PERFORM R3270
005700         GO TO PASS2KEY-1.
005710 ADD2-DUP-CHK.
005720     IF W3-ADD-RET-CD = 8 AND AUTHORIZED
005730         MOVE '** WARNING - RACF EXISTING RACF PROFILE **'
005740            TO PASS04-WERROR
005750         MOVE C1-ACB-WRITE-ERRORS TO XPASS04-WERROR
005760         GO TO ADD2-GOOD-ADD.
005770     IF W3-ADD-RET-CD = 0 GO TO ADD2-GOOD-ADD.
005780         MOVE 'RACF PROFILE ALREADY EXISTS' TO PASS04-ERROR.
005790         MOVE BUILD-KEY TO C1-MMMMCTRL.
005800         MOVE SPACES TO DB-PASS-REC.
005810         PERFORM RPASSU.
005820         IF NOT TOTAL-ERROR-FREE PERFORM TOTERR.
005830         PERFORM DPASS.
005840         IF NOT TOTAL-ERROR-FREE PERFORM TOTERR.
005850         MOVE C1-ACB-WRITE-ERRORS TO XPASS04-ERROR.
005860         PERFORM W3270S.
005870         PERFORM R3270
005880         GO TO PASS2KEY-1.
005890 ADD2-GOOD-ADD.
005900     MOVE 'ADD COMPLETE ** USE RACF TRANSACTION TO ACTIVATE **'
005910     TO PASS04-ERROR.
005920     ADD 1 TO W1-UPDT-CT.
005930     MOVE C1-ACB-WRITE-ERRORS TO XPASS04-ERROR
005940     PERFORM W3270S
005950     PERFORM R3270
005960     IF AUTHORIZED GO TO PROCESS-CHG2.
005970     GO TO PASS2KEY-1.
005980 ADD2-TOT-ERR.
005990     IF TOTAL-DUPLICATE-MASTER-RECORD
006000         MOVE 'ADD REJECTED-RECORD ALREADY EXISTS' TO
006010         PASS04-ERROR
006020         MOVE C1-ACB-WRITE-ERRORS TO XPASS04-ERROR
006030         PERFORM W3270S
006040         PERFORM R3270
006050         GO TO PASS2KEY-1.
006060     PERFORM TOTERR.
006070 PROCESS-CHG2.
006080     MOVE 'N' TO W1-SCREEN-FLAG.
006090     IF AUTHORIZED MOVE 'PASS08' TO C1-MAP-ID
006100     ELSE
006110         MOVE 'PASS07' TO C1-MAP-ID.
006120     PERFORM GETMAP.
006130     MOVE 'TRANSACTION PASSWORD CHG' TO C1-TASK-NAME.
006140     MOVE C1-HDR TO PASS07-HDG.
006150     MOVE BUILD-KEY TO C1-MMMMCTRL.
006160     MOVE SPACES TO DB-PASS-REC.
006170     PERFORM RPASSU.
006180     IF TOTAL-MASTER-RECORD-NOT-FOUND AND NOT-AUTHORIZED
006190         MOVE 'REQUESTED RECORD NOT FOUND' TO PASS07-ERROR
006200         MOVE C1-ACB-WRITE-ERRORS TO XPASS07-ERROR
006210         PERFORM F3270
006220         PERFORM R3270
006230         GO TO PASS2KEY-1.
006240     IF TOTAL-MASTER-RECORD-NOT-FOUND AND AUTHORIZED
006250         MOVE 'REQUESTED RECORD NOT FOUND' TO PASS08-ERROR
006260         MOVE C1-ACB-WRITE-ERRORS TO XPASS08-ERROR
006270         PERFORM F3270
006280         PERFORM R3270
006290         GO TO PASS2KEY-1.
006300     IF TOTAL-ERROR-FREE
006310         NEXT SENTENCE ELSE
006320         PERFORM TOTERR.
006330     MOVE DB-TRANID TO PASS07-TRANID.
006340     MOVE DB-OC TO PASS07-OC.
006350     MOVE DB-EP TO PASS07-ENTRYPT.
006360     MOVE DB-ASCTNO TO PASS07-ASCTNO.
006370     MOVE DB-TRAN-TY TO PASS07-TRANTY.
006380 CHG2-LOOP.
006390     IF NOT-AUTHORIZED MOVE 0 TO W4-LOC-WK1
006400         MOVE BK-TRNID TO W4-TRAN
006410         MOVE 0 TO W4-LOC-WK2
006420         PERFORM RACFAUTH
006430     ELSE PERFORM FILL-SCREEN.
006440         IF W4-LOC-RET-CD NOT = 0 AND AUTHORIZED
006450           MOVE W4-LOC-RET-CD TO W1-RACF-ERR-CD
006460           MOVE W1-RACF-ERROR TO PASS08-ERROR
006470           MOVE C1-ACB-WRITE-ERRORS TO XPASS08-ERROR
006480           PERFORM W3270S
006490           PERFORM R3270
006500           GO TO PASS2KEY-1.
006510         IF W4-LOC-RET-CD NOT = 0 AND NOT-AUTHORIZED
006520           MOVE W4-LOC-RET-CD TO W1-RACF-ERR-CD
006530           MOVE W1-RACF-ERROR TO PASS07-ERROR
006540           MOVE C1-ACB-WRITE-ERRORS TO XPASS07-ERROR
006550           PERFORM W3270S
006560           PERFORM R3270
006570           GO TO PASS2KEY-1.
006580 CHG2-LOOP-0010.
006590     MOVE W4-DESC TO PASS08-DESC.
006600     PERFORM F3270.
006610 REREAD-CHG2.
006620     PERFORM R3270.
006630     IF FORCE-SHUTDOWN-IN-PROGRESS
006640         GO TO TASK-ENDING.
006650     IF AID-CANCEL
006660         GO TO TRAN-LOOP.
006670     IF AID-END
006680         GO TO TASK-ENDING.
006690     IF C1-MAP-ID = 'PASS08  ' AND AID-PAGE GO TO CHG2-LOOP.
006700     IF XPASS07-OC = C1-ACB-INPUT
006710         MOVE PASS07-OC TO DB-OC.
006720     IF XPASS07-ENTRYPT = C1-ACB-INPUT
006730         MOVE PASS07-ENTRYPT TO DB-EP.
006740     IF XPASS07-ASCTNO = C1-ACB-INPUT
006750         MOVE PASS07-ASCTNO TO DB-ASCTNO.
006760     IF XPASS07-TRANTY = C1-ACB-INPUT
006770         MOVE PASS07-TRANTY TO DB-TRAN-TY.
006780     PERFORM WPASS.
006790     IF NOT TOTAL-ERROR-FREE PERFORM TOTERR.
006800     IF NOT-AUTHORIZED GO TO CHG2-COMPLETE.
006810         MOVE 1 TO W1-K.
006820         IF XPASS08-DESC NOT = C1-ACB-INPUT GO TO CHG2-AUTH-0010.
006830         MOVE 'M' TO W3-ALT-LEN.
006840         MOVE PASS08-DESC TO W3-ALT-DESC.
006850         MOVE BK-TRNID TO W3-ALT-TRAN.
006860         PERFORM RACFALT.
006870         IF W3-ALT-RET-CD = 0 GO TO CHG2-AUTH-0010.
006880             MOVE W3-ALT-RET-CD TO W1-RACF-ERR-CD.
006890             MOVE W1-RACF-ERROR TO PASS08-ERROR.
006900             MOVE C1-ACB-WRITE-ERRORS TO XPASS08-ERROR.
006910             PERFORM W3270S.
006920             PERFORM R3270.
006930             GO TO PASS2KEY-1.
006940 CHG2-AUTH-0010.
006950         IF W1-K IS GREATER THAN 30 GO TO CHG2-AUTH-0020.
006960         IF XUSER1 ( W1-K ) NOT = C1-ACB-INPUT AND
006970             XAUTH1 ( W1-K ) NOT = C1-ACB-INPUT
006980             GO TO CHG2-AUTH-0015.
006990             MOVE USER-ARRAY1 ( W1-K ) TO W1-WORK
007000             PERFORM RACF-UPDT
007010             IF W3-ALT-RET-CD NOT = 0
007020                 MOVE W3-ALT-RET-CD TO W1-RACF-ERR-CD
007030                 MOVE W1-RACF-ERROR TO PASS08-ERROR
007040                 MOVE C1-ACB-WRITE-ERRORS TO XPASS08-ERROR
007050                 MOVE '*' TO AUTH1 ( W1-K)
007060                 MOVE C1-ACB-WRITE-ERRORS TO XAUTH1 ( W1-K )
007070                 PERFORM W3270S
007080                 PERFORM R3270
007090                 GO TO PASS2KEY-1.
007100             MOVE W1-WORK TO USER-ARRAY1 ( W1-K ).
007110 CHG2-AUTH-0015.
007120         ADD 1 TO W1-K.
007130         GO TO CHG2-AUTH-0010.
007140 CHG2-AUTH-0020.
007150         MOVE 1 TO W1-K.
007160 CHG2-AUTH-0030.
007170         IF W1-K IS GREATER THAN 61 GO TO CHG2-AUTH-0040.
007180         IF XUSER2 ( W1-K ) NOT = C1-ACB-INPUT AND
007190             XAUTH2 ( W1-K ) NOT = C1-ACB-INPUT
007200             GO TO CHG2-AUTH-0035.
007210             MOVE USER-ARRAY2 ( W1-K ) TO W1-WORK
007220             PERFORM RACF-UPDT
007230             IF W3-ALT-RET-CD NOT = 0
007240                 MOVE W3-ALT-RET-CD TO W1-RACF-ERR-CD
007250                 MOVE W1-RACF-ERROR TO PASS08-ERROR
007260                 MOVE C1-ACB-WRITE-ERRORS TO XPASS08-ERROR
007270                 MOVE '*' TO AUTH2 ( W1-K)
007280                 MOVE C1-ACB-WRITE-ERRORS TO XAUTH2 ( W1-K )
007290                 PERFORM W3270S
007300                 PERFORM R3270
007310                 GO TO PASS2KEY-1.
007320             MOVE W1-WORK TO USER-ARRAY2 ( W1-K ).
007330 CHG2-AUTH-0035.
007340         ADD 1 TO W1-K.
007350         GO TO CHG2-AUTH-0030.
007360 CHG2-AUTH-0040.
007370     ADD 1 TO W1-UPDT-CT.
007380     MOVE 'CHANGE COMPLETE **USE RACF TRANSACTION TO ACTIVATE**'
007390         TO PASS08-ERROR.
007400     MOVE C1-ACB-WRITE-ERRORS TO XPASS08-ERROR.
007410     PERFORM W3270S.
007420     PERFORM R3270.
007430     GO TO PASS2KEY-1.
007440 CHG2-COMPLETE.
007450     MOVE 'CHANGE COMPLETE' TO PASS07-ERROR
007460     MOVE C1-ACB-WRITE-ERRORS TO XPASS07-ERROR
007470     PERFORM W3270S
007480     PERFORM R3270
007490     GO TO PASS2KEY-1.
007500 PROCESS-DEL2.
007510     MOVE 'PASS10' TO C1-MAP-ID.
007520     PERFORM GETMAP.
007530     MOVE 'TRANSACTION PASSWORD DEL' TO C1-TASK-NAME.
007540     MOVE C1-HDR TO PASS10-HDG.
007550     MOVE BUILD-KEY TO C1-MMMMCTRL.
007560     MOVE SPACES TO DB-PASS-REC.
007570     PERFORM RPASSU.
007580     IF TOTAL-MASTER-RECORD-NOT-FOUND
007590         MOVE 'REQUESTED RECORD DOES NOT EXIST' TO PASS10-ERROR
007600         MOVE C1-ACB-WRITE-ERRORS TO XPASS10-ERROR
007610         PERFORM F3270
007620         PERFORM R3270
007630         GO TO PASS2KEY-1.
007640     IF TOTAL-ERROR-FREE
007650         NEXT SENTENCE ELSE
007660         PERFORM TOTERR.
007670     IF DB-CODE NOT = '2'
007680         MOVE 'REQUESTED RECORD DOES NOT EXIST' TO
007690         PASS10-ERROR
007700         MOVE C1-ACB-WRITE-ERRORS TO XPASS10-ERROR
007710         PERFORM F3270
007720         PERFORM R3270
007730         GO TO PASS2KEY-1.
007740     MOVE DB-TRANID TO PASS10-TRANID.
007750     MOVE DB-OC TO PASS10-OC.
007760     MOVE DB-EP TO PASS10-EP.
007770     MOVE DB-ASCTNO TO PASS10-ASCTNO.
007780     MOVE DB-TRAN-TY TO PASS10-TRANTY.
007790     MOVE 0 TO W4-LOC-WK1.
007800     MOVE 0 TO W4-LOC-WK2.
007810     MOVE DB-TRANID TO W4-TRAN.
007820     PERFORM RACFAUTH.
007830     IF W4-LOC-RET-CD = 0 OR W4-LOC-RET-CD = 12 GO TO DEL2-0010.
007840         MOVE W4-LOC-RET-CD TO W1-RACF-ERR-CD.
007850         MOVE W1-RACF-ERROR TO PASS10-ERROR.
007860         GO TO DEL2-0030.
007870 DEL2-0010.
007880     IF W4-LOC-RET-CD = 0 GO TO DEL2-0020.
007890     MOVE '** WARNING RACF PROFILE NOT FOUND **' TO
007900         PASS10-WARN2.
007910     GO TO DEL2-0900.
007920 DEL2-0020.
007930     MOVE W4-DESC TO PASS10-DESC.
007940     IF AUTHORIZED GO TO DEL2-0100.
007950     IF W4-LOC-NO-RT > 1 MOVE
007960         'RACF ACCESS LIST NOT EMPTY' TO PASS10-ERROR
007970         GO TO DEL2-0030.
007980     IF W4-LOC-USER ( 1 ) NOT = 'XXXX' MOVE
007990         'INSUFFICIENT AUTHORITY TO DELETE LAST USER' TO
008000         PASS10-ERROR
008010         GO TO DEL2-0030.
008020     GO TO DEL2-0900.
008030 DEL2-0030.
008040     PERFORM F3270.
008050     PERFORM R3270.
008060     GO TO PASS2KEY-1.
008070 DEL2-0100.
008080     IF W4-LOC-NO-RT > 1 MOVE
008090         '** WARNING -- USERS REMAINING ON ACCESS LIST **'
008100         TO PASS10-ERROR.
008110 DEL2-0900.
008120     PERFORM F3270.
008130 REREAD-DELETE2.
008140     PERFORM R3270.
008150     IF FORCE-SHUTDOWN-IN-PROGRESS
008160         GO TO TASK-ENDING.
008170     IF AID-CANCEL
008180         GO TO TRAN-LOOP.
008190     IF AID-END
008200         NEXT SENTENCE ELSE
008210         PERFORM BADAID
008220         GO TO REREAD-DELETE2.
008230     PERFORM DPASS.
008240     IF NOT TOTAL-ERROR-FREE GO TO DEL2-0999.
008250     IF W4-LOC-RET-CD = 12 GO TO DEL2-0998.
008260     MOVE DB-TRANID TO W3-DEL-TRAN.
008270     PERFORM RCFDELE.
008280     IF W3-DEL-RET-CD NOT = 0
008290         MOVE W3-DEL-RET-CD TO W1-RACF-ERR-CD
008300         MOVE W1-RACF-ERROR TO PASS10-ERROR
008310         MOVE C1-ACB-WRITE-ERRORS TO XPASS10-ERROR
008320         PERFORM W3270S
008330         PERFORM R3270
008340         GO TO PASS2KEY-1.
008350 DEL2-0998.
008360         MOVE 'DELETE COMPLETED' TO PASS10-ERROR
008370         MOVE C1-ACB-WRITE-ERRORS TO XPASS10-ERROR
008380         PERFORM W3270S
008390         PERFORM R3270
008400         GO TO PASS2KEY-1.
008410 DEL2-0999.
008420     PERFORM TOTERR.
008430 PROCESS-INQ2.
008440     MOVE 'N' TO W1-SCREEN-FLAG.
008450     IF AUTHORIZED MOVE 'PASS14' TO C1-MAP-ID ELSE
008460     MOVE 'PASS13' TO C1-MAP-ID.
008470     PERFORM GETMAP.
008480     MOVE 'TRANSACTION PASSWORD INQ' TO C1-TASK-NAME.
008490     MOVE C1-HDR TO PASS13-HDG.
008500     MOVE BUILD-KEY TO C1-MMMMCTRL.
008510     MOVE SPACES TO DB-PASS-REC.
008520     PERFORM RPASS.
008530     IF TOTAL-MASTER-RECORD-NOT-FOUND AND NOT-AUTHORIZED
008540         MOVE 'RECORD NOT FOUND' TO PASS13-ERROR
008550         MOVE C1-ACB-WRITE-ERRORS TO XPASS13-ERROR
008560         PERFORM F3270
008570         PERFORM R3270
008580         GO TO PASS2KEY-1.
008590     IF TOTAL-MASTER-RECORD-NOT-FOUND AND AUTHORIZED
008600         MOVE 'RECORD NOT FOUND' TO PASS14-ERROR
008610         MOVE C1-ACB-WRITE-ERRORS TO XPASS14-ERROR
008620         PERFORM F3270
008630         PERFORM R3270
008640         GO TO PASS2KEY-1.
008650     IF TOTAL-ERROR-FREE
008660         NEXT SENTENCE ELSE
008670         PERFORM TOTERR.
008680     MOVE DB-TRANID  TO PASS13-TRANID.
008690     MOVE DB-OC      TO PASS13-OC.
008700     MOVE DB-EP      TO PASS13-EP.
008710     MOVE DB-ASCTNO  TO PASS13-ASCTNO.
008720     MOVE DB-TRAN-TY TO PASS13-TRANTY.
008730 INQ2-LOOP.
008740     IF NOT-AUTHORIZED MOVE 0 TO W4-LOC-WK1
008750         MOVE BK-TRNID TO W4-TRAN
008760         MOVE 0 TO W4-LOC-WK2
008770         PERFORM RACFAUTH
008780     ELSE PERFORM FILL-SCREEN.
008790         IF W4-LOC-RET-CD NOT = 0 AND AUTHORIZED
008800           MOVE W4-LOC-RET-CD TO W1-RACF-ERR-CD
008810           MOVE W1-RACF-ERROR TO PASS14-ERROR
008820           MOVE C1-ACB-WRITE-ERRORS TO XPASS14-ERROR
008830           PERFORM W3270S
008840           PERFORM R3270
008850           GO TO PASS2KEY-1.
008860         IF W4-LOC-RET-CD NOT = 0 AND NOT-AUTHORIZED
008870           MOVE W4-LOC-RET-CD TO W1-RACF-ERR-CD
008880           MOVE W1-RACF-ERROR TO PASS13-ERROR
008890           MOVE C1-ACB-WRITE-ERRORS TO XPASS13-ERROR
008900           PERFORM W3270S
008910           PERFORM R3270
008920           GO TO PASS2KEY-1.
008930     MOVE W4-DESC TO PASS08-DESC.
008940     PERFORM F3270.
008950 REREAD-INQ2.
008960     PERFORM R3270.
008970     IF FORCE-SHUTDOWN-IN-PROGRESS
008980         GO TO TASK-ENDING.
008990     IF AID-CANCEL
009000         GO TO TRAN-LOOP.
009010     IF AID-PAGE AND AUTHORIZED GO TO INQ2-LOOP.
009020     GO TO TASK-ENDING.
009030 AUTH4KEY SECTION.
009040     FILL W1DSR.
009050     MOVE 'PASS16' TO C1-MAP-ID.
009060     PERFORM GETMAP.
009070     MOVE 'ENTER USER ID' TO C1-TASK-NAME.
009080     MOVE C1-HDR TO PASS16-HDG.
009090     PERFORM F3270.
009100 AUTH4KEY-READ.
009110     PERFORM R3270.
009120     IF SHUTDOWN-NOT-IN-PROGRESS
009130         NEXT SENTENCE ELSE
009140         GO TO TASK-ENDING.
009150     IF AID-CANCEL
009160         GO TO TRAN-LOOP.
009170     IF AID-END
009180         GO TO TASK-ENDING.
009190     IF C1-AID-ENTER IS NOT = C1-AID-RECEIVED
009200         PERFORM BADAID
009210         GO TO AUTH4KEY-READ.
009220 USERID-CHECK.
009230     MOVE SPACES TO BUILD-KEY.
009240     MOVE PASS16-USERID TO BK-OPER.
009250     MOVE BUILD-KEY TO C1-MMMMCTRL.
009260     MOVE SPACES TO DB-PASS-REC.
009270     MOVE BUILD-KEY TO DB-KEY.
009280     MOVE '4' TO DB-CODE.
009290     IF C1-TRAN-ID = 'CNP4'
009300         GO TO PROCESS-CHG4.
009310     IF C1-TRAN-ID = 'DNP4'
009320         GO TO PROCESS-DEL4.
009330 PROCESS-ADD4.
009340     MOVE 'PASS17' TO C1-MAP-ID.
009350     PERFORM GETMAP.
009360     MOVE 'ENTER LOCATION CD' TO C1-TASK-NAME.
009370     MOVE C1-HDR TO PASS17-HDG.
009380     MOVE BK-OPER TO PASS17-USERID.
009390     PERFORM F3270.
009400 ADD4-READ.
009410     PERFORM R3270.
009420     IF SHUTDOWN-NOT-IN-PROGRESS
009430         NEXT SENTENCE ELSE
009440         GO TO TASK-ENDING.
009450     IF AID-CANCEL
009460         GO TO TRAN-LOOP.
009470     IF AID-END
009480         GO TO TASK-ENDING.
009490     IF C1-AID-ENTER IS NOT = C1-AID-RECEIVED
009500         PERFORM BADAID
009510         GO TO ADD4-READ.
009520     IF XPASS17-LOCCD = C1-ACB-INPUT
009530         MOVE PASS17-LOCCD TO PASS-T4-LC
009540     ELSE MOVE SPACES TO PASS-T4-LC.
009550     PERFORM APASS.
009560     IF TOTAL-ERROR-FREE
009570         MOVE 'ADD COMPLETED ' TO PASS17-ERROR
009580         MOVE C1-ACB-WRITE-ERRORS TO XPASS17-ERROR
009590         PERFORM W3270S
009600         PERFORM R3270
009610         GO TO TRAN-LOOP.
009620     IF TOTAL-DUPLICATE-MASTER-RECORD
009630         MOVE 'REJECTED- RECORD EXISTS' TO PASS17-ERROR
009640         MOVE C1-ACB-WRITE-ERRORS TO XPASS17-ERROR
009650         PERFORM W3270S
009660         PERFORM R3270
009670         GO TO TRAN-LOOP.
009680     PERFORM TOTERR.
009690 PROCESS-CHG4.
009700     MOVE 'PASS18' TO C1-MAP-ID.
009710     PERFORM GETMAP.
009720     MOVE 'ENTER LOCATION CD' TO C1-TASK-NAME'
009730     MOVE C1-HDR TO PASS18-HDG.
009740     MOVE BK-OPER TO PASS18-USERID.
009750     PERFORM RPASSU.
009760     IF TOTAL-MASTER-RECORD-NOT-FOUND
009770         MOVE 'REQUESTED RECORD DOES NOT EXIST' TO PASS18-ERROR
009780         PERFORM F3270
009790         PERFORM R3270
009800         GO TO TRAN-LOOP.
009810     IF TOTAL-ERROR-FREE
009820         NEXT SENTENCE ELSE
009830         PERFORM TOTERR.
009840     MOVE PASS-T4-LC TO PASS18-LOCCD.
009850     PERFORM F3270.
009860 CHG4-READ.
009870         PERFORM R3270.
009880         IF FORCE-SHUTDOWN-IN-PROGRESS GO TO TASK-ENDING.
009890         IF AID-END GO TO TASK-ENDING.
009900         IF AID-CANCEL GO TO TRAN-LOOP.
009910         IF C1-AID-RECEIVED IS NOT = C1-AID-ENTER
009920             PERFORM BADAID
009930             GO TO CHG4-READ.
009940         MOVE PASS18-LOCCD TO PASS-T4-LC.
009950         PERFORM WPASS.
009960         IF TOTAL-ERROR-FREE
009970             MOVE 'CHANGE COMPLETED' TO PASS18-ERROR
009980             MOVE C1-ACB-WRITE-ERRORS TO XPASS18-ERROR
009990             PERFORM W3270S
010000             PERFORM R3270
010010             GO TO TRAN-LOOP.
010020          PERFORM TOTERR.
010030 PROCESS-DEL4.
010040     PERFORM RPASSU.
010050     IF TOTAL-MASTER-RECORD-NOT-FOUND
010060         MOVE 'REQUESTED RECORD DOES NOT EXIST' TO PASS16-ERROR
010070         MOVE C1-ACB-WRITE-ERRORS TO XPASS16-ERROR
010080         PERFORM W3270S
010090         PERFORM R3270
010100         GO TO TRAN-LOOP.
010110     IF TOTAL-ERROR-FREE
010120         NEXT SENTENCE ELSE
010130         PERFORM TOTERR.
010140         MOVE 'CONFIRM DELETE    ENTER = PROCESS   PF4 = CANCEL'
010150          TO PASS16-ERROR.
010160         MOVE C1-ACB-WRITE-ERRORS TO XPASS16-ERROR.
010170         PERFORM W3270S.
010180 DEL4-CONFIRM.
010190         PERFORM R3270.
010200         IF FORCE-SHUTDOWN-IN-PROGRESS GO TO TASK-ENDING.
010210         IF AID-END GO TO TRAN-LOOP.
010220         IF C1-AID-RECEIVED IS NOT = C1-AID-ENTER
010230             PERFORM BADAID
010240             GO TO DEL4-CONFIRM.
010250         PERFORM DPASS.
010260         IF TOTAL-ERROR-FREE
010270             MOVE 'DELETE COMPLETED' TO PASS16-ERROR
010280             MOVE C1-ACB-WRITE-ERRORS TO XPASS16-ERROR
010290             PERFORM W3270S
010300             PERFORM R3270
010310             GO TO TRAN-LOOP.
010320          PERFORM TOTERR.
010330 TASK-ENDING SECTION.
010340     MOVE 'TASKEND' TO C1-MAP-ID.
010350     CALL 'LBRANCH' USING C1-MAP-ID C1-VADDR.
010360     CALL 'YIELD'.
010370 TRAN-LOOP SECTION.
010380     MOVE 'TRANLOOP' TO C1-PAGE-ID.
010390     CALL 'LBRANCH' USING C1-PAGE-ID C1-VADDR.
010400     CALL 'YIELD'.
010410     EJECT
010420 FILL-SCREEN SECTION.
010430     MOVE BK-TRNID TO W4-TRAN.
010440     MOVE SPACES TO PASS08-SPACE1.
010450     MOVE SPACES TO PASS08-SPACE2.
010460     IF W1-SCREEN-FLAG = 'N'
010470         MOVE 0 TO W4-LOC-WK1
010480         MOVE 0 TO W4-LOC-WK2
010490         PERFORM RACFAUTH.
010500     IF W4-LOC-NO-RE NOT = 0
010510         MOVE 'I' TO W1-SCREEN-FLAG.
010520     IF W4-LOC-RET-CD NOT = 0 OR W4-LOC-NO-RT = 0
010530         GO TO FILL-SCREEN-EXIT.
010540     MOVE 1 TO W1-K.
010550 FILL-0010.
010560     IF W1-K IS GREATER THAN 30 GO TO FILL-0020.
010570     MOVE W4-LOC-USER ( W4-LOC-NO-RT ) TO USER1 ( W1-K ).
010580     MOVE W4-LOC-AUTH ( W4-LOC-NO-RT ) TO AUTH1 ( W1-K ).
010590     SUBTRACT 1 FROM W4-LOC-NO-RT.
010600     IF W4-LOC-NO-RT = 0 AND W4-LOC-NO-RE IS GREATER THAN 0
010610         GO TO FILL-0040.
010620     IF W4-LOC-NO-RT = 0 GO TO FILL-SCREEN-EXIT.
010630     ADD 1 TO W1-K.
010640     GO TO FILL-0010.
010650 FILL-0020.
010660     MOVE 1 TO W1-K.
010670 FILL-0030.
010680     IF W1-K IS GREATER THAN 61 GO TO FILL-SCREEN-EXIT.
010690     MOVE W4-LOC-USER ( W4-LOC-NO-RT ) TO USER2 ( W1-K ).
010700     MOVE W4-LOC-AUTH ( W4-LOC-NO-RT ) TO AUTH2 ( W1-K ).
010710     SUBTRACT 1 FROM W4-LOC-NO-RT.
010720     IF W4-LOC-NO-RT = 0 AND W4-LOC-NO-RE IS GREATER THAN 0
010730         GO TO FILL-0050.
010740     IF W4-LOC-NO-RT = 0 GO TO FILL-SCREEN-EXIT.
010750     ADD 1 TO W1-K.
010760     GO TO FILL-0030.
010770 FILL-0040.
010780     PERFORM RACFAUTH.
010790     IF W4-LOC-RET-CD NOT = 0 GO TO FILL-SCREEN-EXIT.
010800     ADD 1 TO W1-K.
010810     GO TO FILL-0010.
010820 FILL-0050.
010830     PERFORM RACFAUTH.
010840     IF W4-LOC-RET-CD NOT = 0 GO TO FILL-SCREEN-EXIT.
010850     ADD 1 TO W1-K.
010860     GO TO FILL-0030.
010870 FILL-SCREEN-EXIT.
010880     IF W4-LOC-NO-RE = 0 AND W4-LOC-NO-RT = 0
010890     MOVE 'N' TO W1-SCREEN-FLAG.
010900     EJECT
010910 RACF-UPDT SECTION.
010920     MOVE 'N' TO W3-ALT-LEN.
010930     MOVE BK-TRNID TO W3-ALT-TRAN.
010940     MOVE W1-USER TO W3-ALT-USER.
010950     IF W1-AUTH = 'D' MOVE 3 TO W3-ALT-REQ-CD.
010960     IF W1-AUTH = 'A' MOVE 1 TO W3-ALT-REQ-CD
010970         MOVE '' TO W3-ALT-AUTH.
010980     IF W1-AUTH = 'P' MOVE 1 TO W3-ALT-REQ-CD
010990         MOVE '' TO W3-ALT-AUTH.
011000     IF W1-AUTH = 'R' MOVE 2 TO W3-ALT-REQ-CD
011010         MOVE '' TO W3-ALT-AUTH.
011020     IF W1-AUTH = 'N' MOVE 2 TO W3-ALT-REQ-CD
011030         MOVE '' TO W3-ALT-AUTH.
011040     PERFORM RACFALT.
011050 RACF-UPDT-EXIT.
011060*    NOTE         MAINTENANCE HISTORY
011070*    REVISION  MODIFICATION   PGR   PROJECT     DATE    TIME
011080*      001          000       269   910097    10/24/74  20/53
011090*      001          001       269   910097    10/31/74  14/05
011100*      001          002       269   910097    10/31/74  14/47
011110*      001          003       269   910097    11/19/74  13/42
011120*      001          004       269   910097    11/20/74   9/08
011130*      001          005       269   910097    11/21/74   8/19
011140*      001          006       269   910097    11/21/74  16/50
011150*               END OF MAINTENANCE HISTORY .
