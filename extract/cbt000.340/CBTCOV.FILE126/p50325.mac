000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID.    P50325.
000030 AUTHOR.        DICK GLOJEK.
000040 INSTALLATION.  MILLER BREWING COMPANY - MILWAUKEE.
000050 DATE-WRITTEN.  DEC., 1981.
000060 DATE-COMPILED.
000070 REMARKS.       *** RACF SECURITY STATUS REPORT ***.
000080 ENVIRONMENT DIVISION.
000090 CONFIGURATION SECTION.
000100 SOURCE-COMPUTER.     IBM-370.
000110 OBJECT-COMPUTER.     IBM-370.
000120 INPUT-OUTPUT SECTION.
000130 FILE-CONTROL.
000140     SELECT RACF-SMF-FILE ASSIGN UT-S-RACFSMF.
000150     SELECT PRINTER-FILE ASSIGN UT-S-PRINTER.
000160     SELECT SORT-FILE ASSIGN UT-S-SORTWK01.
000170 DATA DIVISION.
000180 FILE SECTION.
000190 FD  RACF-SMF-FILE
000200     BLOCK CONTAINS 0 RECORDS
000210     RECORDING MODE V
000220     RECORD CONTAINS 0 TO 456 CHARACTERS
000230     LABEL RECORD STANDARD.
000240 01  RACF-SMF-RECORD         PIC X(456).
000250 FD  PRINTER-FILE
000260     BLOCK CONTAINS 0 RECORDS
000270     RECORDING MODE F
000280     LABEL RECORD STANDARD.
000290 01  PRINT-LINE.
000300     05  PRT-CNTL            PIC X.
000310     05  FILLER              PIC X(132).
000320 SD  SORT-FILE
000330     RECORDING MODE IS F.
000340 01  SORT-RECORD.
000350     05  SORT-KEY            PIC X(28).
000360     05  FILLER              PIC X(54).
000370     EJECT
000380 WORKING-STORAGE SECTION.
000390 01  ODDS-AND-ENDS.
000400     05  LINE-CT             PIC 99         VALUE 0.
000410     05  PAGE-CT             PIC 99         VALUE 0.
000420     05  PREV-USER           PIC X(4)       VALUE SPACES.
000430     05  PREV-LUNME          PIC X(8)       VALUE SPACES.
000440     05  PREV-DATE           PIC S9(5)      COMP-3    VALUE +0.
000450     05  PREV-REPT           PIC 9          VALUE 0.
000460     05  PREV-TYPE           PIC 9          VALUE 0.
000470     05  PARM-REPT           PIC 9          VALUE 0.
000480     05  SUB                 PIC 99         VALUE 0   COMP.
000490     05  TIME-SIGN           PIC X          VALUE ''.
000500*                                             HEX '0C'
000510     05  ZP50212HZ           PIC X(8)       VALUE 'P50212H'.
000520     05  ZP50324Z            PIC X(8)       VALUE 'P50324'.
000530     05  ZP01616Z            PIC X(8)       VALUE 'P01616'.
000540     05  ZP00655Z            PIC X(8)       VALUE 'P00655'.
000550     05  CONVERT-JULIAN      PIC 9(6)       COMP-3.
000560     05  CONVERTED-GREGORIAN.
000570         10  CONV-MON        PIC 99.
000580         10  CONV-DAY        PIC 99.
000590         10  CONV-YR         PIC 99.
000600     05  EDITED-DATE.
000610         10  EDITED-MON      PIC 99.
000620         10  FILLER          PIC X          VALUE '/'.
000630         10  EDITED-DAY      PIC 99.
000640         10  FILLER          PIC X          VALUE '/'.
000650         10  EDITED-YR       PIC 99.
000660     05  DAY-OF-WEEK         PIC X(9).
000670     05  TIME-BREAKDOWN      PIC 9(7).
000680     05  FILLER REDEFINES TIME-BREAKDOWN.
000690         10  TIME-BRKDWN-HH  PIC 99.
000700         10  TIME-BRKDWN-MM  PIC 99.
000710         10  TIME-BRKDWN-SS  PIC 99.
000720     05  BUILT-COMMAND       PIC 99         COMP.
000730     05  FILLER REDEFINES BUILT-COMMAND.
000740         10  RCDEVENT-XX-1   PIC X.
000750         10  RCDEVENT-XX-2   PIC X.
000760     05  RACF-FUNC           PIC X.
000770     05  RACF-KEY            PIC X(8).
000780     05  RACF-INFO           PIC X(256).
000790     05  FILLER REDEFINES RACF-INFO.
000800         10  RACF-RESULTS    PIC X(14).
000810     05  FILLER REDEFINES RACF-INFO.
000820         10  RACF-OWNERID    PIC X(4).
000830         10  RACF-OWNERNME   PIC X(20).
000840     05  REPT1-SUBTITLE.
000850         10  FILLER                  PIC X(25)  VALUE
000860            'TSO LOGONS FOR OFF HOURS '.
000870         10  REPT1-START-SUBTITLE    PIC X(8)   VALUE
000880             '00:00 XX'.
000890         10  FILLER                  PIC XXXX   VALUE ' TO '.
000900         10  REPT1-END-SUBTITLE      PIC X(8)   VALUE
000910             '00:00 XX'.
000920     SKIP3
000930     05  RACF-SMF-RECORD-LAYOUT.
000940         10  RCDRELNO        PIC X.
000950         10  RCDSYSID        PIC X(4).
000960         10  RCDTYPE         PIC X.
000970             88  TYPE-80         VALUE '&'.
000980*                                  HEX '50'
000990             88  TYPE-81         VALUE '‚'.
001000*                                  HEX '51'
001010         10  RCDTIME         PIC S9(7)       COMP-3.
001020         10  RE-RCDTIME REDEFINES RCDTIME.
001030             15  FILLER          PIC XXX.
001040             15  RCDTIME-SIGN    PIC X.
001050         10  RCDDATE         PIC S9(5)       COMP-3.
001060         10  RCDEVENT        PIC X.
001070         10  RCDQUAL         PIC X.
001080             88  SUCCESSFUL-INIT          VALUE '
001090*                                           HEX '00'
001100             88  INVALID-PASSWORD         VALUE ''.
001110*                                           HEX '01'
001120             88  INSUFFICIENT-AUTHORITY   VALUE ''.
001130*                                           HEX '01'
001140         10  RCD80FLG        PIC X.
001150         10  RCDUSER         PIC X(8).
001160         10  RCDGROUP        PIC X(8).
001170         10  RCDLOGCL        PIC X.
001180             88  LOGON-JOBINIT            VALUE ''.
001190*                                           HEX '01'
001200             88  DATASET-ACCESS           VALUE ''.
001210*                                           HEX '02'
001220             88  RACF-COMMAND             VALUE ''.
001230*                                           HEX '03'
001240         10  RCDCLASS        PIC X(8).
001250         10  RCDNAME         PIC X(44).
001260         10  RCDJOBID        PIC X(8).
001270         10  RCDDATID        PIC S9(5)       COMP-3.
001280         10  R-RCDDATID REDEFINES RCDDATID   PIC XXX.
001290         10  RCDTIMID        PIC 9(7)        COMP-3.
001300         10  R-RCDTIMID REDEFINES RCDTIMID   PIC X(4).
001310         10  RCDUSRDA        PIC X(8).
001320         10  RCD80AUTH       PIC X.
001330             88  NORMAL-AUTHORITY         VALUE 'ç'.
001340*                                           HEX '80'
001350             88  SPECIAL-AUTHORITY        VALUE ' '.
001360*                                           HEX '40'
001370             88  OPERATIONS-AUTHORITY     VALUE '½'.
001380*                                           HEX '20'
001390             88  AUDITOR-AUTHORITY        VALUE ''.
001400*                                           HEX '10'
001410             88  EXIT-AUTHORITY           VALUE '©'.
001420*                                           HEX '08'
001430         10  RCD80REA        PIC X.
001440         10  RCD80ERR        PIC X.
001450         10  RCD80TRM        PIC X(8).
001460         10  RCD80TML        PIC X.
001470         10  RCDQUAL1        PIC X(8).
001480         10  RCDQUAL2        PIC X(8).
001490         10  RCDCNT          PIC 9(4)        COMP.
001500         10  RCDDLEV         PIC X.
001510         10  RCDDINT         PIC X.
001520         10  RCDDALWD        PIC X.
001530         10  RCDDVOL         PIC X(6).
001540         10  RCDDOLDV        PIC X(6).
001550         10  RCDSECTION.
001560             15  RCDDTYPE    PIC X.
001570             15  RCDDLGT     PIC X.
001580             15  RCCDATE     PIC X(301).
001590     SKIP3
001600     05  SORT-RECORD-LAYOUT.
001610         10  SR-KEY          PIC X(28).
001620         10  FILLER REDEFINES SR-KEY.
001630             15  SR-REPT-K       PIC 9.
001640             15  SR-TYPE-K       PIC 9.
001650             15  SR-USER-K       PIC X(4).
001660             15  SR-LUNME-K      PIC X(8).
001670             15  SR-DATE-K       PIC S9(5)      COMP-3.
001680             15  SR-TIME-K       PIC S9(7)      COMP-3.
001690         10  SR-JOBID            PIC X(8).
001700         10  SR-DATASET          PIC X(44).
001710         10  SR-COMMAND-CODE     PIC 99         COMP.
001720     SKIP3
001730     05  HEADINGA.
001740         10  FILLER              PIC X          VALUE '1'.
001750         10  FILLER              PIC X(7)       VALUE 'P50325-'.
001760         10  HDGA-REPORT-NO      PIC 9          VALUE 0.
001770         10  FILLER              PIC X(37)      VALUE SPACES.
001780         10  FILLER              PIC X(31)      VALUE
001790             'RACF SECURITY STATUS REPORT FOR'.
001800         10  FILLER              PIC X(24)      VALUE SPACES.
001810         10  HDGA-DATE           PIC X(8).
001820         10  FILLER              PIC X(3)       VALUE '   '.
001830         10  HDGA-TIME           PIC X(6).
001840         10  FILLER              PIC X(11)      VALUE
001850             '      PAGE '.
001860         10  HDGA-PAGE           PIC ZZZ9.
001870     05  HEADINGB.
001880         10  FILLER              PIC X(40)      VALUE SPACES.
001890         10  HDGB-SUBTITLE       PIC X(45)      VALUE SPACES.
001900     05  HEADING1A.
001910         10  FILLER              PIC X          VALUE '0'.
001920         10  FILLER              PIC X(48)      VALUE
001930             'USER                       TERMINAL             '.
001940     05  HEADING1B.
001950         10  FILLER              PIC X          VALUE ' '.
001960         10  FILLER              PIC X(48)      VALUE
001970             ' ID. ++++++++NAME++++++++     ID.   ++++++LOCATI'.
001980         10  FILLER              PIC X(48)      VALUE
001990             'ON++++++  ++DATE++ DAY  ++TIME++                '.
002000     05  HEADING2A.
002010         10  FILLER              PIC X          VALUE '0'.
002020         10  FILLER              PIC X(48)      VALUE
002030             'USER                       TERMINAL             '.
002040         10  FILLER              PIC X(48)      VALUE
002050             '                                    JOB         '.
002060     05  HEADING2B.
002070         10  FILLER              PIC X          VALUE ' '.
002080         10  FILLER              PIC X(48)      VALUE
002090             ' ID. ++++++++NAME++++++++     ID.   ++++++LOCATI'.
002100         10  FILLER              PIC X(48)      VALUE
002110             'ON++++++  ++DATE++ DAY  ++TIME++  ++NAME++      '.
002120     05  HEADING3A.
002130         10  FILLER              PIC X          VALUE '0'.
002140         10  FILLER              PIC X(48)      VALUE
002150             'USER                       TERMINAL             '.
002160         10  FILLER              PIC X(48)      VALUE
002170             '                                    JOB         '.
002180     05  HEADING3B.
002190         10  FILLER              PIC X          VALUE ' '.
002200         10  FILLER              PIC X(48)      VALUE
002210             ' ID. ++++++++NAME++++++++     ID.   ++++++LOCATI'.
002220         10  FILLER              PIC X(48)      VALUE
002230             'ON++++++  ++DATE++ DAY  ++TIME++  ++NAME++ +++++'.
002240         10  FILLER              PIC X(36)      VALUE
002250             '++++++++DATASET NAME++++++++++++++++'.
002260     05  HEADING4A.
002270         10  FILLER              PIC X          VALUE '0'.
002280         10  FILLER              PIC X(48)      VALUE
002290             'USER                       TERMINAL             '.
002300         10  FILLER              PIC X(48)      VALUE SPACES.
002310         10  FILLER              PIC X(4)       VALUE
002320             ' JOB'.
002330     05  HEADING4B.
002340         10  FILLER              PIC X          VALUE ' '.
002350         10  FILLER              PIC X(48)      VALUE
002360             ' ID. ++++++++NAME++++++++     ID.   ++++++LOCATI'.
002370         10  FILLER              PIC X(48)      VALUE
002380             'ON++++++  ++DATE++ DAY  ++TIME++  ++COMMAND++  +'.
002390         10  FILLER              PIC X(36)      VALUE
002400             '+NAME++                             '.
002410     05  DET-LINE                PIC X(133).
002420     05  FILLER REDEFINES DET-LINE.
002430         10  DET-CNTL            PIC X.
002440         10  DET-USER-1          PIC X(4).
002450         10  FILLER              PIC X.
002460         10  DET-USERNAME-1      PIC X(20).
002470         10  FILLER              PIC XX.
002480         10  DET-LUNME-1         PIC X(8).
002490         10  FILLER              PIC X.
002500         10  DET-LOC-1           PIC X(20).
002510         10  FILLER              PIC XX.
002520         10  DET-DATE-1          PIC X(8).
002530         10  FILLER              PIC X.
002540         10  DET-DAY-1           PIC XXX.
002550         10  FILLER              PIC XX.
002560         10  DET-HH-1            PIC 99.
002570         10  DET-COLON1-1        PIC X.
002580         10  DET-MM-1            PIC 99.
002590         10  DET-COLON2-1        PIC X.
002600         10  DET-SS-1            PIC 99.
002610     05  FILLER REDEFINES DET-LINE.
002620         10  FILLER              PIC X.
002630         10  DET-USER-2          PIC X(4).
002640         10  FILLER              PIC X.
002650         10  DET-USERNAME-2      PIC X(20).
002660         10  FILLER              PIC XX.
002670         10  DET-LUNME-2         PIC X(8).
002680         10  FILLER              PIC X.
002690         10  DET-LOC-2           PIC X(20).
002700         10  FILLER              PIC XX.
002710         10  DET-DATE-2          PIC X(8).
002720         10  FILLER              PIC X.
002730         10  DET-DAY-2           PIC XXX.
002740         10  FILLER              PIC XX.
002750         10  DET-HH-2            PIC 99.
002760         10  DET-COLON1-2        PIC X.
002770         10  DET-MM-2            PIC 99.
002780         10  DET-COLON2-2        PIC X.
002790         10  DET-SS-2            PIC 99.
002800         10  FILLER              PIC XX.
002810         10  DET-JOBNAME-2       PIC X(8).
002820     05  FILLER REDEFINES DET-LINE.
002830         10  FILLER              PIC X.
002840         10  DET-USER-3          PIC X(4).
002850         10  FILLER              PIC X.
002860         10  DET-USERNAME-3      PIC X(20).
002870         10  FILLER              PIC XX.
002880         10  DET-LUNME-3         PIC X(8).
002890         10  FILLER              PIC X.
002900         10  DET-LOC-3           PIC X(20).
002910         10  FILLER              PIC XX.
002920         10  DET-DATE-3          PIC X(8).
002930         10  FILLER              PIC X.
002940         10  DET-DAY-3           PIC XXX.
002950         10  FILLER              PIC XX.
002960         10  DET-HH-3            PIC 99.
002970         10  DET-COLON1-3        PIC X.
002980         10  DET-MM-3            PIC 99.
002990         10  DET-COLON2-3        PIC X.
003000         10  DET-SS-3            PIC 99.
003010         10  FILLER              PIC XX.
003020         10  DET-JOBNAME-3       PIC X(8).
003030         10  FILLER              PIC X.
003040         10  DET-DATASET-3       PIC X(44).
003050     05  FILLER REDEFINES DET-LINE.
003060         10  FILLER              PIC X.
003070         10  DET-USER-4          PIC X(4).
003080         10  FILLER              PIC X.
003090         10  DET-USERNAME-4      PIC X(20).
003100         10  FILLER              PIC XX.
003110         10  DET-LUNME-4         PIC X(8).
003120         10  FILLER              PIC X.
003130         10  DET-LOC-4           PIC X(20).
003140         10  FILLER              PIC XX.
003150         10  DET-DATE-4          PIC X(8).
003160         10  FILLER              PIC X.
003170         10  DET-DAY-4           PIC XXX.
003180         10  FILLER              PIC XX.
003190         10  DET-HH-4            PIC 99.
003200         10  DET-COLON1-4        PIC X.
003210         10  DET-MM-4            PIC 99.
003220         10  DET-COLON2-4        PIC X.
003230         10  DET-SS-4            PIC 99.
003240         10  FILLER              PIC XX.
003250         10  DET-COMMAND-4       PIC X(10).
003260         10  FILLER              PIC XXX.
003270         10  DET-JOBNAME-4       PIC X(8).
003280     SKIP3
003290     05  LAST-LINE.
003300         10  FILLER              PIC X        VALUE '-'.
003310         10  FILLER              PIC X(58)    VALUE ALL '+'.
003320         10  FILLER              PIC X(15)    VALUE
003330             ' END OF REPORT '.
003340         10  FILLER              PIC X(59)    VALUE ALL '+'.
003350     EJECT
003360     05  RACF-COMMAND-TABLE.
003370         10  FILLER              PIC 99       VALUE 08.
003380         10  FILLER              PIC X(10)    VALUE 'ADDSD     '.
003390         10  FILLER              PIC 99       VALUE 09.
003400         10  FILLER              PIC X(10)    VALUE 'ADDGROUP  '.
003410         10  FILLER              PIC 99       VALUE 10.
003420         10  FILLER              PIC X(10)    VALUE 'ADDUSER   '.
003430         10  FILLER              PIC 99       VALUE 11.
003440         10  FILLER              PIC X(10)    VALUE 'ALTDSD    '.
003450         10  FILLER              PIC 99       VALUE 12.
003460         10  FILLER              PIC X(10)    VALUE 'ALTGROUP  '.
003470         10  FILLER              PIC 99       VALUE 13.
003480         10  FILLER              PIC X(10)    VALUE 'ALTUSER   '.
003490         10  FILLER              PIC 99       VALUE 14.
003500         10  FILLER              PIC X(10)    VALUE 'CONNECT   '.
003510         10  FILLER              PIC 99       VALUE 15.
003520         10  FILLER              PIC X(10)    VALUE 'DELDSD    '.
003530         10  FILLER              PIC 99       VALUE 16.
003540         10  FILLER              PIC X(10)    VALUE 'DELGROUP  '.
003550         10  FILLER              PIC 99       VALUE 17.
003560         10  FILLER              PIC X(10)    VALUE 'DELUSER   '.
003570         10  FILLER              PIC 99       VALUE 18.
003580         10  FILLER              PIC X(10)    VALUE 'PASSWORD  '.
003590         10  FILLER              PIC 99       VALUE 19.
003600         10  FILLER              PIC X(10)    VALUE 'PERMIT    '.
003610         10  FILLER              PIC 99       VALUE 20.
003620         10  FILLER              PIC X(10)    VALUE 'RALTER    '.
003630         10  FILLER              PIC 99       VALUE 21.
003640         10  FILLER              PIC X(10)    VALUE 'RDEFINE   '.
003650         10  FILLER              PIC 99       VALUE 22.
003660         10  FILLER              PIC X(10)    VALUE 'RDELETE   '.
003670         10  FILLER              PIC 99       VALUE 23.
003680         10  FILLER              PIC X(10)    VALUE 'REMOVE    '.
003690         10  FILLER              PIC 99       VALUE 24.
003700         10  FILLER              PIC X(10)    VALUE 'SETROPTS  '.
003710         10  FILLER              PIC 99       VALUE 25.
003720         10  FILLER              PIC X(10)    VALUE 'RVARY     '.
003730         10  FILLER              PIC 99       VALUE 00.
003740         10  FILLER              PIC X(10)    VALUE '*UNKNOWN* '.
003750         10  FILLER              PIC 99       VALUE 00.
003760         10  FILLER              PIC X(10)    VALUE '*UNKNOWN* '.
003770     05  FILLER REDEFINES RACF-COMMAND-TABLE OCCURS 20 TIMES.
003780         10  RACF-COMMAND-CODE   PIC 99.
003790         10  RACF-COMMAND-DESC   PIC X(10).
003800     SKIP3
003810 01  CONV-DATE-DATA.
003820     COPY CONVAREA.
003830     SKIP3
003840 LINKAGE SECTION.
003850 01  EXECPARM.
003860     05  PARM-LENGTH             PIC S9(4)      COMP.
003870     05  PARM-START-TIME         PIC X(8).
003880     05  FILLER                  PIC X.
003890     05  PARM-END-TIME           PIC X(8).
003900     05  FILLER                  PIC X.
003910     05  PARM-REPORT-NO          PIC X.
003920     EJECT
003930 PROCEDURE DIVISION USING EXECPARM.
003940     IF PARM-LENGTH NOT = 0
003950         MOVE PARM-START-TIME TO REPT1-START-SUBTITLE
003960         MOVE PARM-END-TIME TO REPT1-END-SUBTITLE
003970         MOVE PARM-REPORT-NO TO PARM-REPT.
003980     IF PARM-REPT NOT NUMERIC MOVE 0 TO PARM-REPT.
003990     IF PARM-REPT NOT = 0 AND
004000                  NOT = 1 AND
004010                  NOT = 2 AND
004020                  NOT = 3 AND
004030                  NOT = 4
004040                  MOVE 0 TO PARM-REPT.
004050     SORT SORT-FILE ASCENDING SORT-KEY
004060         INPUT PROCEDURE SELECT-RACF-RECORDS THRU SELECT-EX
004070        OUTPUT PROCEDURE STATUS-REPORTS THRU STATUS-EX.
004080     PERFORM PRINT-LAST-LINE THRU LAST-LINE-EX.
004090     CLOSE PRINTER-FILE.
004100     GOBACK.
004110     SKIP3
004120 SELECT-RACF-RECORDS SECTION.
004130     OPEN INPUT RACF-SMF-FILE.
004140 READ-SMF.
004150     READ RACF-SMF-FILE INTO RACF-SMF-RECORD-LAYOUT
004160         AT END
004170         CLOSE RACF-SMF-FILE
004180         GO TO SELECT-EX.
004190     MOVE TIME-SIGN TO RCDTIME-SIGN.
004200     IF NOT TYPE-80 GO TO READ-SMF.
004210     MOVE SPACES TO SORT-RECORD-LAYOUT.
004220     IF LOGON-JOBINIT AND SUCCESSFUL-INIT MOVE 1 TO SR-REPT-K
004230         GO TO CHECK-TERMID.
004240     IF LOGON-JOBINIT AND INVALID-PASSWORD MOVE 2 TO SR-REPT-K
004250         GO TO BUILD-SORT.
004260     IF DATASET-ACCESS AND INSUFFICIENT-AUTHORITY
004270         MOVE 3 TO SR-REPT-K GO TO BUILD-SORT.
004280     IF RACF-COMMAND MOVE 4 TO SR-REPT-K
004290         GO TO CHECK-TYPE.
004300     GO TO READ-SMF.
004310 CHECK-TERMID.
004320     IF RCD80TRM = LOW-VALUES OR SPACES GO TO READ-SMF.
004330     GO TO BUILD-SORT.
004340 CHECK-TYPE.
004350     IF NORMAL-AUTHORITY GO TO READ-SMF.
004360     MOVE 5 TO SR-TYPE-K.
004370     MOVE 0 TO SR-COMMAND-CODE.
004380     IF SPECIAL-AUTHORITY MOVE 1 TO SR-TYPE-K.
004390     IF OPERATIONS-AUTHORITY MOVE 2 TO SR-TYPE-K.
004400     IF AUDITOR-AUTHORITY MOVE 3 TO SR-TYPE-K.
004410     IF EXIT-AUTHORITY MOVE 4 TO SR-TYPE-K.
004420     IF SR-TYPE-K = 5 GO TO BUILD-SORT.
004430     MOVE RCDEVENT TO RCDEVENT-XX-2.
004440     MOVE LOW-VALUES TO RCDEVENT-XX-1.
004450     MOVE BUILT-COMMAND TO SR-COMMAND-CODE.
004460 BUILD-SORT.
004470     MOVE RCDUSER TO SR-USER-K.
004480     MOVE RCDJOBID TO SR-JOBID.
004490     MOVE RCDDATE TO SR-DATE-K.
004500     MOVE RCDTIME TO SR-TIME-K.
004510     MOVE RCD80TRM TO SR-LUNME-K.
004520     MOVE RCDNAME TO SR-DATASET.
004530     IF PARM-REPT = 0
004540         GO TO SORT-SMF.
004550     IF PARM-REPT = 1 AND SR-REPT-K = 1
004560         GO TO SORT-SMF.
004570     IF PARM-REPT = 2 AND SR-REPT-K = 2
004580        GO TO SORT-SMF.
004590     IF PARM-REPT = 3 AND SR-REPT-K = 3
004600        GO TO SORT-SMF.
004610     IF PARM-REPT = 4 AND SR-REPT-K = 4
004620        GO TO SORT-SMF.
004630     GO TO READ-SMF.
004640 SORT-SMF.
004650     RELEASE SORT-RECORD FROM SORT-RECORD-LAYOUT.
004660     GO TO READ-SMF.
004670 SELECT-EX.
004680     EXIT.
004690     SKIP3
004700 STATUS-REPORTS SECTION.
004710     OPEN OUTPUT PRINTER-FILE.
004720     MOVE SPACES TO DET-LINE.
004730     MOVE CURRENT-DATE TO HDGA-DATE.
004740     MOVE TIME-OF-DAY TO HDGA-TIME.
004750     PERFORM RETURN-SORTED THRU RETURN-EX.
004760     IF SORT-RECORD-LAYOUT = HIGH-VALUES
004770         PERFORM HDG-ROUTINE THRU HDG-ROUTINE-EX
004780         GO TO STATUS-EX.
004790 CHECK-REPORT-TYPE.
004800     IF SR-REPT-K = 1 GO TO REPORT-1.
004810     IF SR-REPT-K = 2 GO TO REPORT-2.
004820     IF SR-REPT-K = 3 GO TO REPORT-3.
004830     IF SR-REPT-K = 4 GO TO REPORT-4.
004840     PERFORM HDG-ROUTINE THRU HDG-ROUTINE-EX.
004850     GO TO STATUS-EX.
004860 REPORT-1.
004870     MOVE 1 TO HDGA-REPORT-NO.
004880     MOVE REPT1-SUBTITLE TO HDGB-SUBTITLE.
004890     MOVE SR-REPT-K TO PREV-REPT.
004900     MOVE 0 TO PAGE-CT.
004910     PERFORM HDG-ROUTINE THRU HDG-ROUTINE-EX.
004920 REPT1-DETAIL.
004930     MOVE SR-USER-K TO DET-USER-1 PREV-USER RACF-KEY.
004940     MOVE '2' TO RACF-FUNC.
004950     PERFORM CALL-RACF.
004960     MOVE RACF-INFO TO DET-USERNAME-1.
004970 REPT1-LUNME-BREAK.
004980     MOVE SR-LUNME-K TO DET-LUNME-1 PREV-LUNME.
004990     MOVE SR-LUNME-K TO RACF-KEY.
005000     MOVE '5' TO RACF-FUNC.
005010     PERFORM CALL-RACF.
005020     MOVE RACF-INFO TO DET-LOC-1.
005030 REPT1-DATE-BREAK.
005040     MOVE SR-DATE-K TO CONVERT-JULIAN PREV-DATE.
005050     PERFORM EDIT-JULIAN THRU EDIT-JULIAN-EX.
005060     MOVE EDITED-DATE TO DET-DATE-1.
005070     MOVE DAY-OF-WEEK TO DET-DAY-1.
005080 REPT1-TIME-BREAK.
005090     MOVE SR-TIME-K TO TIME-BREAKDOWN.
005100     MOVE TIME-BRKDWN-HH TO DET-HH-1.
005110     MOVE TIME-BRKDWN-MM TO DET-MM-1.
005120     MOVE TIME-BRKDWN-SS TO DET-SS-1.
005130     MOVE ':' TO DET-COLON1-1 DET-COLON2-1.
005140     PERFORM PRINT-DETLINE THRU PRINT-DETLINE-EX.
005150     PERFORM RETURN-SORTED THRU RETURN-EX.
005160     IF SORT-RECORD-LAYOUT = HIGH-VALUES GO TO STATUS-EX.
005170     IF SR-REPT-K NOT = PREV-REPT
005180         PERFORM PRINT-LAST-LINE THRU LAST-LINE-EX
005190         GO TO CHECK-REPORT-TYPE.
005200     IF SR-USER-K NOT = PREV-USER GO TO REPT1-DETAIL.
005210     IF SR-LUNME-K NOT = PREV-LUNME GO TO REPT1-LUNME-BREAK.
005220     IF SR-DATE-K NOT = PREV-DATE GO TO REPT1-DATE-BREAK.
005230     GO TO REPT1-TIME-BREAK.
005240 REPORT-2.
005250     MOVE 2 TO HDGA-REPORT-NO.
005260     MOVE '        INVALID PASSWORD CONFLICTS' TO
005270         HDGB-SUBTITLE.
005280     MOVE SR-REPT-K TO PREV-REPT.
005290     MOVE 0 TO PAGE-CT.
005300     PERFORM HDG-ROUTINE THRU HDG-ROUTINE-EX.
005310 REPT2-DETAIL.
005320     MOVE SR-USER-K TO DET-USER-2 PREV-USER RACF-KEY.
005330     MOVE '2' TO RACF-FUNC.
005340     PERFORM CALL-RACF.
005350     MOVE RACF-INFO TO DET-USERNAME-2.
005360 REPT2-LUNME-BREAK.
005370     IF SR-LUNME-K = LOW-VALUES OR SPACES
005380         MOVE '  N/A' TO DET-LUNME-2
005390         ELSE
005400         MOVE SR-LUNME-K TO DET-LUNME-2
005410                            RACF-KEY
005420         MOVE '5' TO RACF-FUNC
005430         PERFORM CALL-RACF
005440         MOVE RACF-INFO TO DET-LOC-2.
005450     MOVE SR-LUNME-K TO PREV-LUNME.
005460 REPT2-DATE-BREAK.
005470     MOVE SR-DATE-K TO CONVERT-JULIAN PREV-DATE.
005480     PERFORM EDIT-JULIAN THRU EDIT-JULIAN-EX.
005490     MOVE EDITED-DATE TO DET-DATE-2.
005500     MOVE DAY-OF-WEEK TO DET-DAY-2.
005510 REPT2-TIME-BREAK.
005520     MOVE SR-TIME-K TO TIME-BREAKDOWN.
005530     MOVE TIME-BRKDWN-HH TO DET-HH-2.
005540     MOVE TIME-BRKDWN-MM TO DET-MM-2.
005550     MOVE TIME-BRKDWN-SS TO DET-SS-2.
005560     MOVE ':' TO DET-COLON1-2 DET-COLON2-2.
005570     IF SR-JOBID = LOW-VALUES OR = SPACES GO TO REPT2-PRINT.
005580     MOVE SR-JOBID TO DET-JOBNAME-2.
005590     IF SR-JOBID = 'G015CSAV' OR
005600                   'G015CSPV' OR
005610                   'G015WSAV' OR
005620                   'G015WSPV'
005630         MOVE 'PROD. E1' TO DET-JOBNAME-2.
005640     IF SR-JOBID = 'SP99E1DC' OR
005650                   'SP99E1RC'
005660         MOVE 'TEST E1' TO DET-JOBNAME-2.
005670 REPT2-PRINT.
005680     PERFORM PRINT-DETLINE THRU PRINT-DETLINE-EX.
005690     PERFORM RETURN-SORTED THRU RETURN-EX.
005700     IF SORT-RECORD-LAYOUT = HIGH-VALUES GO TO STATUS-EX.
005710     IF SR-REPT-K NOT = PREV-REPT
005720         PERFORM PRINT-LAST-LINE THRU LAST-LINE-EX
005730         GO TO CHECK-REPORT-TYPE.
005740     IF SR-USER-K NOT = PREV-USER GO TO REPT2-DETAIL.
005750     IF SR-LUNME-K NOT = PREV-LUNME GO TO REPT2-LUNME-BREAK.
005760     IF SR-DATE-K NOT = PREV-DATE GO TO REPT2-DATE-BREAK.
005770     GO TO REPT2-TIME-BREAK.
005780 REPORT-3.
005790     MOVE 3 TO HDGA-REPORT-NO.
005800     MOVE '         INVALID DATASET ACCESSES' TO
005810         HDGB-SUBTITLE.
005820     MOVE SR-REPT-K TO PREV-REPT.
005830     MOVE 0 TO PAGE-CT.
005840     PERFORM HDG-ROUTINE THRU HDG-ROUTINE-EX.
005850 REPT3-DETAIL.
005860     MOVE SR-USER-K TO DET-USER-3 PREV-USER RACF-KEY.
005870     MOVE '2' TO RACF-FUNC.
005880     PERFORM CALL-RACF.
005890     MOVE RACF-INFO TO DET-USERNAME-3.
005900 REPT3-LUNME-BREAK.
005910     IF SR-LUNME-K = LOW-VALUES OR SPACES
005920         MOVE '  N/A' TO DET-LUNME-3
005930         ELSE
005940         MOVE SR-LUNME-K TO DET-LUNME-3
005950                            RACF-KEY
005960         MOVE '5' TO RACF-FUNC
005970         PERFORM CALL-RACF
005980         MOVE RACF-INFO TO DET-LOC-3.
005990     MOVE SR-LUNME-K TO PREV-LUNME.
006000 REPT3-DATE-BREAK.
006010     MOVE SR-DATE-K TO CONVERT-JULIAN PREV-DATE.
006020     PERFORM EDIT-JULIAN THRU EDIT-JULIAN-EX.
006030     MOVE EDITED-DATE TO DET-DATE-3.
006040     MOVE DAY-OF-WEEK TO DET-DAY-3.
006050 REPT3-TIME-BREAK.
006060     MOVE SR-TIME-K TO TIME-BREAKDOWN.
006070     MOVE TIME-BRKDWN-HH TO DET-HH-3.
006080     MOVE TIME-BRKDWN-MM TO DET-MM-3.
006090     MOVE TIME-BRKDWN-SS TO DET-SS-3.
006100     MOVE ':' TO DET-COLON1-3 DET-COLON2-3.
006110     IF SR-LUNME-K = LOW-VALUES OR SPACES
006120         MOVE SR-JOBID TO DET-JOBNAME-3.
006130     MOVE SR-DATASET TO DET-DATASET-3.
006140     PERFORM PRINT-DETLINE THRU PRINT-DETLINE-EX.
006150     PERFORM RETURN-SORTED THRU RETURN-EX.
006160     IF SORT-RECORD-LAYOUT = HIGH-VALUES GO TO STATUS-EX.
006170     IF SR-REPT-K NOT = PREV-REPT
006180         PERFORM PRINT-LAST-LINE THRU LAST-LINE-EX
006190         GO TO CHECK-REPORT-TYPE.
006200     IF SR-USER-K NOT = PREV-USER GO TO REPT3-DETAIL.
006210     IF SR-LUNME-K NOT = PREV-LUNME GO TO REPT3-LUNME-BREAK.
006220     IF SR-DATE-K NOT = PREV-DATE GO TO REPT3-DATE-BREAK.
006230     GO TO REPT3-TIME-BREAK.
006240 REPORT-4.
006250     MOVE 4 TO HDGA-REPORT-NO.
006260     MOVE '        UNKNOWN ATTRIBUTE ACTIVITY'
006270         TO HDGB-SUBTITLE.
006280     IF SR-TYPE-K = 1
006290         MOVE '        SPECIAL ATTRIBUTE ACTIVITY' TO
006300         HDGB-SUBTITLE.
006310     IF SR-TYPE-K = 2
006320         MOVE '       OPERATIONS ATTRIBUTE ACTIVITY' TO
006330         HDGB-SUBTITLE.
006340     IF SR-TYPE-K = 3
006350         MOVE '        AUDITOR ATTRIBUTE ACTIVITY' TO
006360         HDGB-SUBTITLE.
006370     IF SR-TYPE-K = 4
006380         MOVE '         EXIT ATTRIBUTE ACTIVITY' TO
006390         HDGB-SUBTITLE.
006400     MOVE SR-REPT-K TO PREV-REPT.
006410     MOVE SR-TYPE-K TO PREV-TYPE.
006420     MOVE 0 TO PAGE-CT.
006430     PERFORM HDG-ROUTINE THRU HDG-ROUTINE-EX.
006440 REPT4-DETAIL.
006450     MOVE SR-USER-K TO DET-USER-4 PREV-USER RACF-KEY.
006460     MOVE '2' TO RACF-FUNC.
006470     PERFORM CALL-RACF.
006480     MOVE RACF-INFO TO DET-USERNAME-4.
006490 REPT4-LUNME-BREAK.
006500     IF SR-LUNME-K = LOW-VALUES OR SPACES
006510         MOVE '  N/A' TO DET-LUNME-4
006520         ELSE
006530         MOVE SR-LUNME-K TO DET-LUNME-4
006540                            RACF-KEY
006550         MOVE '5' TO RACF-FUNC
006560         PERFORM CALL-RACF
006570         MOVE RACF-INFO TO DET-LOC-4.
006580     MOVE SR-LUNME-K TO PREV-LUNME.
006590 REPT4-DATE-BREAK.
006600     MOVE SR-DATE-K TO CONVERT-JULIAN PREV-DATE.
006610     PERFORM EDIT-JULIAN THRU EDIT-JULIAN-EX.
006620     MOVE EDITED-DATE TO DET-DATE-4.
006630     MOVE DAY-OF-WEEK TO DET-DAY-4.
006640 REPT4-TIME-BREAK.
006650     MOVE SR-TIME-K TO TIME-BREAKDOWN.
006660     MOVE TIME-BRKDWN-HH TO DET-HH-4.
006670     MOVE TIME-BRKDWN-MM TO DET-MM-4.
006680     MOVE TIME-BRKDWN-SS TO DET-SS-4.
006690     MOVE ':' TO DET-COLON1-4 DET-COLON2-4.
006700     IF SR-LUNME-K = LOW-VALUES OR SPACES
006710         MOVE SR-JOBID TO DET-JOBNAME-4.
006720     MOVE 1 TO SUB.
006730 REPT4-TABLE-LOOP.
006740     IF SR-COMMAND-CODE = RACF-COMMAND-CODE (SUB)
006750         MOVE RACF-COMMAND-DESC (SUB) TO DET-COMMAND-4
006760         GO TO REPT4-PRINT.
006770     ADD 1 TO SUB.
006780     GO TO REPT4-TABLE-LOOP.
006790 REPT4-PRINT.
006800     PERFORM PRINT-DETLINE THRU PRINT-DETLINE-EX.
006810     PERFORM RETURN-SORTED THRU RETURN-EX.
006820     IF SORT-RECORD-LAYOUT = HIGH-VALUES GO TO STATUS-EX.
006830     IF SR-REPT-K NOT = PREV-REPT
006840         PERFORM PRINT-LAST-LINE THRU LAST-LINE-EX
006850         GO TO CHECK-REPORT-TYPE.
006860     IF SR-TYPE-K NOT = PREV-TYPE
006870         PERFORM PRINT-LAST-LINE THRU LAST-LINE-EX
006880         GO TO REPORT-4.
006890     IF SR-USER-K NOT = PREV-USER GO TO REPT4-DETAIL.
006900     IF SR-LUNME-K NOT = PREV-LUNME GO TO REPT4-LUNME-BREAK.
006910     IF SR-DATE-K NOT = PREV-DATE GO TO REPT4-DATE-BREAK.
006920     GO TO REPT4-TIME-BREAK.
006930 STATUS-EX.
006940     EXIT.
006950 RETURN-SORTED SECTION.
006960     RETURN SORT-FILE INTO SORT-RECORD-LAYOUT
006970         AT END MOVE HIGH-VALUES TO SORT-RECORD-LAYOUT.
006980 RETURN-EX.
006990     EXIT.
007000 HDG-ROUTINE.
007010     MOVE 0 TO LINE-CT.
007020     ADD 1 TO PAGE-CT.
007030     MOVE PAGE-CT TO HDGA-PAGE.
007040     WRITE PRINT-LINE FROM HEADINGA AFTER POSITIONING PRT-CNTL.
007050     WRITE PRINT-LINE FROM HEADINGB AFTER POSITIONING PRT-CNTL.
007060     IF PREV-REPT = 1
007070         WRITE PRINT-LINE FROM HEADING1A
007080             AFTER POSITIONING PRT-CNTL
007090         WRITE PRINT-LINE FROM HEADING1B
007100             AFTER POSITIONING PRT-CNTL.
007110     IF PREV-REPT = 2
007120         WRITE PRINT-LINE FROM HEADING2A
007130             AFTER POSITIONING PRT-CNTL
007140         WRITE PRINT-LINE FROM HEADING2B
007150             AFTER POSITIONING PRT-CNTL.
007160     IF PREV-REPT = 3
007170         WRITE PRINT-LINE FROM HEADING3A
007180             AFTER POSITIONING PRT-CNTL
007190         WRITE PRINT-LINE FROM HEADING3B
007200             AFTER POSITIONING PRT-CNTL.
007210     IF PREV-REPT = 4
007220         WRITE PRINT-LINE FROM HEADING4A
007230             AFTER POSITIONING PRT-CNTL
007240         WRITE PRINT-LINE FROM HEADING4B
007250             AFTER POSITIONING PRT-CNTL.
007260     MOVE 6 TO LINE-CT.
007270     MOVE '0' TO DET-CNTL.
007280 HDG-ROUTINE-EX.
007290     EXIT.
007300 PRINT-DETLINE SECTION.
007310     WRITE PRINT-LINE FROM DET-LINE AFTER POSITIONING PRT-CNTL.
007320     IF DET-CNTL = ' ' ADD 1 TO LINE-CT.
007330     IF DET-CNTL = '0' ADD 2 TO LINE-CT.
007340     IF DET-CNTL = '-' ADD 3 TO LINE-CT.
007350     MOVE SPACES TO DET-LINE.
007360     IF LINE-CT GREATER THAN 57
007370         PERFORM HDG-ROUTINE THRU HDG-ROUTINE-EX.
007380 PRINT-DETLINE-EX.
007390     EXIT.
007400 PRINT-LAST-LINE SECTION.
007410     WRITE PRINT-LINE FROM LAST-LINE AFTER POSITIONING PRT-CNTL.
007420 LAST-LINE-EX.
007430     EXIT.
007440 CALL-RACF SECTION.
007450     MOVE SPACES TO RACF-INFO.
007460     CALL ZP50212HZ USING RACF-FUNC RACF-KEY RACF-INFO.
007470     IF RACF-RESULTS = 'NAME NOT FOUND' OR
007480                       'DATA NOT FOUND' OR
007490                       'TERM NOT FOUND'
007500                        MOVE SPACES TO RACF-INFO.
007510     IF RACF-FUNC = '2' AND RACF-INFO = SPACES
007520         MOVE 'UNIDENTIFIED USER' TO RACF-INFO.
007530 CALL-RACF-EXIT.
007540     EXIT.
007550 EDIT-JULIAN SECTION.
007560     CALL ZP50324Z USING CONVERT-JULIAN CONVERTED-GREGORIAN.
007570     MOVE CONV-MON TO EDITED-MON.
007580     MOVE CONV-DAY TO EDITED-DAY.
007590     MOVE CONV-YR TO EDITED-YR.
007600     MOVE EDITED-DATE TO CONV-DATE.
007610     MOVE 'D16' TO CONV-DATE-CODE.
007620     CALL ZP01616Z USING CONV-MODULE-NAME
007630                         CONV-MODULE-FUNC
007640                         CONV-DATE-CODE
007650                         CONV-DATE
007660                         CONV-BIN-DATE.
007670     IF CONV-DATE-CODE = SPACES CALL ZP00655Z.
007680     MOVE 'BX6' TO CONV-DATE-CODE.
007690     CALL ZP01616Z USING CONV-MODULE-NAME
007700                         CONV-MODULE-FUNC
007710                         CONV-DATE-CODE
007720                         CONV-BIN-DATE
007730                         CONV-DATE-DATA.
007740     IF CONV-DATE-CODE = SPACES CALL ZP00655Z.
007750     MOVE CONV-DAY-NAME TO DAY-OF-WEEK.
007760 EDIT-JULIAN-EX.
007770     EXIT.
007780 END-OF-CODING.
