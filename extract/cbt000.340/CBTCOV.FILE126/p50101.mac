*          DATA SET P50101     AT LEVEL 002 AS OF 03/11/81
*          DATA SET P50101     AT LEVEL 001 AS OF 01/27/81
CSINIT    PAGE  CSESCSIE
          USING PSCR,R14
          USING U$ERAREA,R3
CSESCSIE  EQU   *
          L     R3,PSCRUSER
          L     R4,E1UPSW
          MVI   0(R4),X'FF'
          STM   R0,R15,CSIREGS
          LA    R13,CSISAVE
          PUSH  USING
          DROP  R15
          USING CSINIT1,R9
          LR    R9,R15               SET UP NEW BASE REGISTER
          LA    R7,E1TBL             GET ADDR OF MODULE TO BE LOADED
          LOAD  EPLOC=(R7)           LOAD IT
          LR    R5,R0
          L     R4,0(R5)             GET ADDR OF START TABLE
          ST    R4,STARTBLE          STORE IN USER VECTOR TABLE
          L     R4,4(R5)             GET ADDR OF RACF USER TABLE
          ST    R4,RACFUTBL          STORE IN USER VECTOR TABLE
          L     R4,8(R5)             GET ADDR OF TERM STAT TABLE
          ST    R4,TERMSTAT          STORE IN USER VECTOR TABLE
          L     R4,12(R5)            GET ADD OF SORTED TERM TABLE
          ST    R4,SORTTABL          STORE IN USER VECTOR TABLE
          L     R4,16(R5)            GET ADD OF AUDIT TABLE
          ST    R4,AUDTTABL          STORE IN USER VECTOR TABLE
          L     R12,P50176             LOAD ADR OF MBSTATS CSECT
          DROP  R3
          LTR   R12,R12                TEST FOR NO MBSTATS
          BZ    NOMBS                  BYPASS PROCESSING
          L     R12,0(R12)             LOAD ADR OF STATS TABLE
          BALR  R11,R12                BUILD TERMINAL STATISTICS TABLE
NOMBS     EQU   *
          LA    R1,=C'AUTH'
          SVC   231
          LM    R0,R15,CSIREGS
          POP   USING
          ASSIGN RDSR=0,DSR=19,STN=1
          YIELD EP=*
          TERMWRT RDSR=19,DSR=20
          YIELD EP=*
          L     R3,DSR20
          MVC   0(L'CSIETXT+2,R3),CSIEMSG
          RELEASER DSR=20
          YIELD EP=*
          RELEASER DSR=19
          YIELD EP=*
          GETBLOCK DSR=20
          YIELD EP=*
          L     R3,DSR20
          MVC   0(2,R3),=H'7'
          MVC   2(7,R3),=C'$CSINIT'
          MSGIN DSR=20,STN=4
          YIELD EP=*
          PERFEXIT
CSIEMSG   DC    AL2(L'CSIETXT)
CSIETXT   DC    C'E/1 COLD START INITIALIZATION EXIT TAKEN'
CSIREGS   DS    16F
CSISAVE   DS    18F
E1TBL     DC    CL8'E1STABLE'
          PAGEND
          U$ERAREA
          $ISSPSCR
          SRCEND
          END
