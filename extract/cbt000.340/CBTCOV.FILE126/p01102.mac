*          DATA SET P01102     AT LEVEL 001 AS OF 01/15/81
P01102   START 0
*
*        UTILITY MODULE TO ACCEPT A CONSOLE REPLY
*
*        USAGE  --  CALL P01102 (REPLY,LEN)
*                        WHERE
*                             REPLY - ADDRESS OF AREA TO WHICH REPLY
*                                       WILL BE RETURNED
*                             LEN   - LENGTH OF EXPECTED REPLY,
*                                       FULLWORD BINARY. THE LENGTH
*                                       MUST BE GREATER THAN 1 AND
*                                       LESS THAN 61. IF THE LENGTH
*                                       IS LESS THAN ONE, NO REPLY
*                                       BE RETURNED. IF LENGTH IS
*                                       GREATER THAN 60, LENGTH WILL
*                                       BE RESET TO 60.
*
*        REPLY MAY ORIGINATE EITHER FROM THE CONSOLE OR FROM
*              CARD IMAGE RECORDS IN FILE 'SYSREPLY'.  SEE
*              ROUTINE DOCUMENTATION FOR FURTHER DISCUSSION.
*
         EJECT
         STREG
         EJECT
*
*        ESTABLISH STANDARD LINKAGE
*
         SAVE  (14,12),,*
         BALR  R2,R0          R2 - BASE REGISTER
         USING *,R2
         LA    R3,SAVEAREA    CHAIN SAVE AREAS
         ST    R13,4(R3)
         ST    R3,8(R13)
         LR    R13,R3
         ST    R1,SAVE1
         EJECT
*
*        CHECK REPLY LENGTH
*
         L     R3,SAVE1
         L     R4,4(R3)            R4 - ADDR OF LENGTH
         L     R5,0(R4)            R5 - LENGTH OF REPLY
         C     R5,=F'1'            LENGTH < 1
         BL    RETN
         C     R5,=F'60'           LENGTH > 60
         BNH   LENOKAY
         L     R5,=F'60'           RESET LENGTH TO 60
LENOKAY  ST    R5,RLEN
*              IF FIRST TIME, INITIALIZE
         CLI   FIRST,C'1'
         BNE   NOTFIRST
         BAL   R10,PFIRST
*              PROCESS REPLY REQUEST
NOTFIRST MVI   REPLY,C' '          CLEAR REPLY AREA
         MVC   REPLY+1(119),REPLY
         CLI   COMMSW,C'1'         CONSOLE REPLY ?
         BE    CNSRPLY                  YES
         B     CRDRPLY                  NO
         EJECT
*
*        GET A REPLY FROM THE CONSOLE
*              USE WTOR AND WAIT MOCROS
*
CNSRPLY  L     R5,RLEN             R5 - REPLY LENGTH
         STC   R5,OPWTOR+4         STORE LENGTH IN WTOR
         L     R5,=F'0'            CLEAR EVENT CONTROL BLOCK
         ST    R5,WTORECB
OPWTOR   WTOR  ' ** REPLY AS INDICATED **',REPLY,01,WTORECB
         WAIT  1,ECB=WTORECB       WAIT FOR REPLY
         B     RETNRPLY
         EJECT
*
*        GET NEXT CARD FOR REPLY
*              IF COLUMNS 1 - 9 ARE '$$CONSOLE' USE CONSOLE
*                   FOR THIS REPLY
*
CRDRPLY  L     R6,CURRCARD
         C     R6,CTBLEND          END OF TABLE ?
         BL    COKAY
         MVC   MESAG(45),=CL45'TOO FEW SYSREPLY CARDS - U(2000) ABEND IX
               SSUED'
         L     R7,=F'45'
         ST    R7,MLEN
         BAL   R10,CONSOUT
         ABEND 2000,DUMP
COKAY    L     R7,RLEN
         S     R7,=F'1'            R7 - LENGTH FOR MVC
         STC   R7,COPMVC+1
COPMVC   MVC   REPLY(1),0(R6)
         LA    R6,80(R6)           INCREMENT TO NEXT CARD
         ST    R6,CURRCARD
         CLC   REPLY(9),=CL9'$$CONSOLE'
         BE    CNSRPLY
*              INDICATE SYSREPLY REPLY ON CONSOLE
         MVC   MESAG(23),=CL23' ** SYSREPLY FOLLOWS **'
         L     R7,=F'23'
         ST    R7,MLEN
         BAL   R10,CONSOUT
         MVC   MESAG,REPLY
         L     R7,RLEN
         ST    R7,MLEN
         BAL   R10,CONSOUT
*              RETURN
         B     RETNRPLY
         EJECT
*
*        RETURN THE REPLY TO THE CALLER
*
RETNRPLY L     R7,RLEN
         S     R7,=F'1'            R7 - LENGTH FOR MVC
         STC   R7,ROPMVC+1
         L     R3,SAVE1
         L     R4,0(R3)            R4 - ADDRESS OF CALLER REPLY AREA
ROPMVC   MVC   0(1,R4),REPLY
         B     RETN
         EJECT
*
*        RETURN TO CALLER
*
RETN     SR    R15,R15
         L     R13,SAVEAREA+4
         RETURN (14,12),,RC=(15)
         EJECT
*
*        DETERMINE IF SYSREPLY IS TO BE USED
*              IF SO, LOAD SYSREPLY CARDS
*              RETURN TO ADDRESS IN R10
*
PFIRST   MVI   FIRST,C'0'          CLEAR FIRST TIME SWITCH
*              USE RDJFCB TO CHECK FOR SYSREPLY DD CARD
         CALL  P01616,(MDRJFCB,MDSFUNC,DDNAME,JFCBAREA,RCODE)
         L     R7,RCODE
         LTR   R7,R7
         BNZ   USECONS
         OPEN  (SYSREPLY,(INPUT))
*              TEST FOR GOOD OPEN
         LA    R5,SYSREPLY
         USING IHADCB,R5
         TM    DCBOFLGS,X'10'      GOOD OPEN ?
         DROP  R5
         BNZ   USECARDS            YES
*              USE THE CONSOLE
USECONS  MVI   COMMSW,C'1'
         BR    R10
*              GET FIRST CARD, CARD COUNT CARD
USECARDS LA    R12,USECONS         END OF FILE ADDRESS
         BAL   R11,RDCARD
         PACK  PCNT(3),CARD(5)     CONVERT COUNT TO PACKED
         ZAP   PACK8(8),PCNT(3)    CONVERT TO BINARY
         CVB   R6,PACK8
         ST    R6,CARDCNT
         C     R6,=F'1'            IF NO CARDS, USE CONSOLE
         BL    CDONE
*              ISSUE GETMAIN FOR TABLE TO HOLD CARDS
         LR    R7,R6
         MH    R7,=H'80'
         LR    R0,R7
         GETMAIN R,LV=(0)
         ST    R1,CTBLADDR         SAVE TABLE START ADDRESS
*              LOAD THE CARDS
         LA    R6,0           R6 - COUNT OF CARDS LOADED
         L     R7,CTBLADDR
         LA    R12,CDONE
CNEXT    BAL   R11,RDCARD          READ NEXT CARD
         MVC   0(80,R7),CARD       STORE THIS CARD
         LA    R7,80(R7)           R7 - NEXT CARD ADDRESS
         LA    R6,1(R6)            R6 - CARD COUNT
         C     R6,CARDCNT          ALL CARDS READ ?
         BL    CNEXT               NO, CONTINUE
CDONE    ST    R6,CARDCNT
*              CLOSE THE FILE
         CLOSE SYSREPLY
*              CHECK IF ANY CARDS
         C     R6,=F'1'
         BL    USECONS
        MVI   COMMSW,C'0'
         L     R7,CTBLADDR
         ST    R7,CURRCARD         CURRENT CARD ADDRESS
         MH    R6,=H'80'
         AR    R6,R7               R6 - TABLE END ADDRESS
         ST    R6,CTBLEND
         BR    R10
         EJECT
*
*        READ A CARD FROM FILE SYSREPLY
*              RETURN TO ADDRESS IN R11 IF CARD READ
*              RETURN TO ADDRESS IN R12 AT END OF FILE
*
RDCARD   GET   SYSREPLY,CARD
         BR    R11
ENDCARD  BR    R12
         EJECT
*
*        DISPLAY A MESSAGE ON THE CONSOLE
*
         DS    0F
CONSOUT  CALL  P01616,(MDCONRTN,MDMFUNC,MESAG,MLEN)
         MVI   MESAG,C' '          CLEAR CONSOLE MESSAGE AREA
         MVC   MESAG+1(119),MESAG
         BR    R10
         EJECT
*
*        MISCELLANEOUS STUFF
*
         DS    0D
PACK8    DC    PL8'0'
SAVEAREA DC    18F'0'
SAVE1    DC    F'0'
WTORECB  DC    F'0'
RLEN     DC    F'0'
MLEN     DC    F'0'
CTBLADDR DC    F'0'
CURRCARD DC    F'0'
CTBLEND  DC    F'0'
CARDCNT  DC    F'0'
RCODE    DC    F'0'
REPLY    DC    CL120' '
FIRST    DC    CL1'1'
COMMSW   DC    CL1' '
MDCONRTN DC    CL8'P01101'
MDRJFCB  DC    CL8'P01110'
MDSFUNC  DC    CL1'S'
MDMFUNC  DC    CL1'M'
DDNAME   DC    CL8'SYSREPLY'
PCNT     DC    PL3'0'
CARD     DC    CL80' '
MESAG    DC    CL120' '
JFCBAREA DC    CL176' '
         LTORG
         EJECT
*
*        SYSREPLY FILE
*
SYSREPLY DCB   DDNAME=SYSREPLY,MACRF=GM,DSORG=PS,EODAD=ENDCARD
         EJECT
*
*        DCB DSECT FOR SYSREPLY
*
         DCBD  DSORG=QS,DEVD=DA
         END P01102
