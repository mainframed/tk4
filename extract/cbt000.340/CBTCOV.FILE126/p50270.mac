000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID. P50270.
000030*
000040 REMARKS.
000050*    THIS PROGRAM READS AND WRITES THE CARD IMAGES OF MEMBERS
000060*    OF A PDS:
000070*                  THIS PROGRAM REPLACES AND PERFORMS THE
000080*                  SAME FUNCTION AS P01831.
000090*
000100*
000110*            //CARDLIB  DD - SPECIFIES PDS (LIBRARY) WHERE
000120*                            MEMBERS ARE LOCATED.
000130*            //OUTFILE  DD - SPECIFIES WHERE CARD IMAGES ARE TO
000140*                            BE WRITTEN,
000150*                            (I.E. TO THE INTERNAL READER).
000160*            //JOBSFILE DD - ONE WAY OF SPECIFYING MEMBER NAMES,
000170*                            A DATA SET CONTAINING 80 BYTE RECORDS
000180*                            MEMBER NAME FROM 1 TO 8 BYTES
000190*                            LEFT JUSTIFIED ONE PER RECORD.
000200*                            IF NOT USED, 'DUMMY' MUST BE
000210*                            SPECIFIED.
000220*                PARM      - OPTIONAL - IF USED, SPECIFY 1 TO 8
000230*                            BYTE MEMBER NAMES SEPERATED BY
000240*                            COMMAS.
000250*
000260*    THE INTENDED USAGE OF THIS PROGRAM IS TO LOAD JCL FROM A
000270*    JCL LIBRARY TO THE INTERNAL READER FOR EXECUTION.
000280*
000290 ENVIRONMENT DIVISION.
000300 CONFIGURATION SECTION.
000310 SOURCE-COMPUTER. IBM-370-158.
000320 OBJECT-COMPUTER. IBM-370-158.
000330 INPUT-OUTPUT SECTION.
000340 FILE-CONTROL.
000350     SELECT MEMBFILE ASSIGN TO UT-S-JOBSFILE.
000360     SELECT OUTFILE ASSIGN TO UT-S-OUTFILE.
000370 DATA DIVISION.
000380 FILE SECTION.
000390 FD  MEMBFILE
000400     RECORDING MODE F
000410     LABEL RECORDS STANDARD
000420     BLOCK CONTAINS 0 RECORDS
000430     DATA RECORD IS JCL-CARD.
000440 01  JCL-CARD                            PIC X(80).
000450 FD  OUTFILE
000460     RECORDING MODE F
000470     LABEL RECORDS STANDARD
000480     BLOCK CONTAINS 0 RECORDS
000490     DATA RECORD IS OUT-CARD.
000500 01  OUT-CARD                            PIC X(80).
000510*
000520*
000530 WORKING-STORAGE SECTION.
000540*
000550*
000560 01  WORK-CARD.
000570     05  CARD-SLASH                      PIC XX.
000580     05  CARD-ALPHA                      PIC X.
000590     05  CARD-NUMERIC                    PIC X(3).
000600     05  FILLER                          PIC X(74).
000610*
000620 01  INTERFACE-AREA.
000630*
000640     05  MODULE-NAME                     PIC X(8).
000650     05  INTERFACE-USAGE                 PIC X.
000660*
000670 01  PDS-CNTL.
000680*
000690     05  IO-FUNC                         PIC XX.
000700     05  RET-CODE                        PIC XX.
000710       88  READ-OKAY                 VALUE SPACES.
000720       88  MEMBER-END                VALUE 'EM'.
000730     05  FILE-NAME                       PIC X(8).
000740 01  F-CTL.
000750*
000760     05  F-RET                           PIC XX.
000770       88  F-OK                      VALUE 'OK'.
000780       88  F-NP                      VALUE 'NP'.
000790*
000800 01  COMP-CDE                            PIC X.
000810       88  PROD-JOB                  VALUE 'P'.
000820       88  TWO-JOBS-FOUND            VALUE 'D'.
000830 01  MEMBER-NAME                         PIC X(8).
000840*
000850 01  BINARY-FIELDS.
000860*
000870     05  MESSAGE-LENGTH                  PIC S9(6)   COMP
000880             VALUE +60.
000890     05  RETURN-LENGTH1                  PIC S9(6)   COMP
000900             VALUE +1.
000910     05  RETURN-LENGTH2                  PIC S9(6)   COMP
000920             VALUE +8.
000930     05  I                               PIC S9(6)   COMP
000940             VALUE ZERO.
000950     05  J                               PIC S9(6)   COMP
000960             VALUE ZERO.
000970*
000980 01  CARDFILE-SWITCH                     PIC X.
000990     88  MODE-IS-CARDS               VALUE '*'.
001000*
001010 01  PARM-SWITCH                         PIC X.
001020     88  PARMS-USED                  VALUE '*'.
001030*
001040 01  MESSAGE-VALUES.
001050*
001060     05  PDS-ERRMSG                      PIC X(60)
001070             VALUE ' MBRDR027  ** CARDLIB ERROR, DDCARD OR LIBRARY
001080-                  ' NOT FOUND ** '.
001090*
001100     05  BADMEMB-ERRMSG.
001110         10 FILLER                       PIC X(14)
001120            VALUE ' MBRDR030  ** '.
001130         10 BAD-MEMB                     PIC X(8).
001140         10 FILLER                       PIC X(30)
001150            VALUE ' HAD ERROR DURING RETRIEVAL **'.
001160         10 FILLER                       PIC X(8)
001170            VALUE SPACES.
001180*
001190     05  PARM-ERRMSG                     PIC X(60)
001200             VALUE ' MBRDR031  ** PARM MEMBER NAME IS LONGER THAN
001210-                  '8 POSITIONS **'.
001220*
001230     05  PROD-JOB-MSG                    PIC X(60)  VALUE
001240               ' MBRDR028  ** PRODUCTION JOBS MUST RESIDE ON PROD
001250-              'LIBRARIES'.
001260*
001270     05  TWOJOBS-ERRMSG                  PIC X(60)  VALUE
001280               ' MBRDR029  ** ONLY ONE JOB PER PDS MEMBER ALLOWED
001290-              ' ** '.
001300*
001310     05  NOWORK-ERRMSG                   PIC X(60)  VALUE
001320               ' MBRDR033  ** NO JOB NAMES WERE SUPPLIED FOR Z999L
001330-              ' OAD **'.
001340*
001350 01  SAVE-CARD.
001360*
001370     05  SAVE-FIRST-FIVE                 PIC X(5).
001380       88  EOF-CARD                  VALUE '/*EOF'.
001390     05  FIRST-FIVE-AGAIN REDEFINES SAVE-FIRST-FIVE.
001400         10 FIRST-THREE                  PIC X(3).
001410            88 COMMENT-CARD          VALUE '//*'.
001420         10 FILLER                       PIC XX.
001430     05  SEARCH-TABLE OCCURS 12 TIMES INDEXED BY S
001440                     PIC X.
001450     05  FILLER                          PIC X(63).
001460*
001470 01  JOB-AREA.
001480*
001490     05  JOBNAME-LENGTH                  PIC S9(6)       COMP.
001500     05  JOBNAME-LOADER                  PIC X(8).
001510     05  STEPNAME-LENGTH                 PIC S9(6)       COMP.
001520     05  STEPNAME-LOADER                 PIC X(8).
001530     05  PROCSTEP-NAME-LEN               PIC S9(6)       COMP.
001540     05  PROCSTEP-NAME                   PIC X(8).
001550*
001560 01  TABLE-AREA.
001570*
001580     05  JOB-TABLE.
001590         10  PARM-BYTE OCCURS 100 TIMES  PIC X.
001600     05  PARM-MEMBER.
001610         10  MEMB-BYTE OCCURS 8   TIMES  PIC X.
001620*
001630 LINKAGE SECTION.
001640*
001650 01  JOBS-PARM.
001660     05  PARM-LENGTH                     PIC S9(4)   COMP.
001670     05  PARM-JOBS                       PIC X(100).
001680 PROCEDURE DIVISION USING JOBS-PARM.
001690     OPEN INPUT MEMBFILE.
001700     OPEN OUTPUT OUTFILE.
001710     MOVE SPACES TO CARDFILE-SWITCH
001720                    COMP-CDE
001730                    PARM-SWITCH.
001740     MOVE 'CARDLIB ' TO FILE-NAME.
001750     MOVE 'OI' TO IO-FUNC.
001760     MOVE 'P01569' TO MODULE-NAME.
001770     MOVE 'M' TO INTERFACE-USAGE.
001780     CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE PDS-CNTL.
001790     IF RET-CODE NOT EQUAL SPACES
001800         MOVE 'P01101' TO MODULE-NAME
001810         CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
001820             PDS-ERRMSG MESSAGE-LENGTH
001830         CALL 'P00655'.
001840     READ MEMBFILE INTO MEMBER-NAME
001850         AT END GO TO PARM-ROUTINE.
001860     MOVE '*' TO CARDFILE-SWITCH.
001870     PERFORM PROCESS-MEMBER THRU PROCESS-MEMBER-EXIT.
001880 GET-NEXT-CARD-MEMBER.
001890     READ MEMBFILE INTO MEMBER-NAME
001900         AT END GO TO PARM-ROUTINE.
001910     PERFORM PROCESS-MEMBER THRU PROCESS-MEMBER-EXIT.
001920     GO TO GET-NEXT-CARD-MEMBER.
001930 PARM-ROUTINE.
001940     IF PARM-LENGTH EQUAL ZERO
001950         GO TO EOJ.
001960     MOVE '*' TO PARM-SWITCH.
001970     MOVE SPACES TO PARM-MEMBER.
001980     MOVE PARM-JOBS TO JOB-TABLE.
001990 PROCESS-PARM.
002000     ADD 1 TO I.
002010     IF I GREATER THAN PARM-LENGTH
002020         GO TO PROCESS-PARM-MEMB.
002030     IF PARM-BYTE (I) EQUAL ','
002040         GO TO PROCESS-PARM-MEMB.
002050     IF PARM-BYTE (I) EQUAL SPACE
002060         GO TO PROCESS-PARM.
002070     ADD 1 TO J.
002080     IF J GREATER THAN 8
002090         MOVE 'P01101' TO MODULE-NAME
002100         CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002110             PARM-ERRMSG MESSAGE-LENGTH
002120         CALL 'P00655'.
002130     MOVE PARM-BYTE (I) TO MEMB-BYTE (J).
002140     GO TO PROCESS-PARM.
002150 PROCESS-PARM-MEMB.
002160     IF J EQUAL ZERO
002170         GO TO PROCESS-PARM-END.
002180     MOVE PARM-MEMBER TO MEMBER-NAME.
002190     PERFORM PROCESS-MEMBER THRU PROCESS-MEMBER-EXIT.
002200     MOVE SPACES TO PARM-MEMBER.
002210     MOVE ZERO TO J.
002220 PROCESS-PARM-END.
002230     IF I GREATER THAN PARM-LENGTH
002240         GO TO EOJ.
002250     GO TO PROCESS-PARM.
002260 PROCESS-MEMBER.
002270     MOVE 'P50248' TO MODULE-NAME.
002280     MOVE SPACES TO F-RET.
002290     CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002300         F-CTL MEMBER-NAME.
002310     IF F-OK
002320       GO TO PROCESS-MEMBER-EXIT.
002330     IF NOT F-NP
002340       CALL 'P00655'.
002350     MOVE 'P01569' TO MODULE-NAME.
002360     MOVE 'RR' TO IO-FUNC.
002370     CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE PDS-CNTL
002380         MEMBER-NAME WORK-CARD.
002390     IF NOT READ-OKAY
002400             CALL 'P00655'.
002410     IF CARD-ALPHA IS ALPHABETIC AND
002420        CARD-NUMERIC IS NUMERIC
002430            MOVE 'P' TO COMP-CDE
002440     ELSE MOVE SPACE TO COMP-CDE.
002450     IF PROD-JOB
002460        MOVE 'P01101' TO MODULE-NAME
002470        CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002480              PROD-JOB-MSG MESSAGE-LENGTH
002490        CALL 'P00655'.
002500 WRITE-CARD.
002510     WRITE OUT-CARD FROM WORK-CARD.
002520     CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE PDS-CNTL
002530         MEMBER-NAME WORK-CARD.
002540     MOVE WORK-CARD TO SAVE-CARD.
002550     IF NOT READ-OKAY
002560         GO TO FIND-PROBLEM.
002570     PERFORM LOOK-FOR-JOB THRU LOOK-END.
002580     IF TWO-JOBS-FOUND
002590         MOVE 'P01101' TO MODULE-NAME
002600         CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002610             TWOJOBS-ERRMSG MESSAGE-LENGTH
002620         CALL 'P00655'.
002630     GO TO WRITE-CARD.
002640 FIND-PROBLEM.
002650     IF NOT MEMBER-END
002660         MOVE MEMBER-NAME TO BAD-MEMB
002670         MOVE 'P01101' TO MODULE-NAME
002680         CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002690             BADMEMB-ERRMSG MESSAGE-LENGTH
002700         CALL 'P00655'.
002710     IF NOT EOF-CARD
002720         MOVE '/*EOF' TO WORK-CARD
002730         WRITE OUT-CARD FROM WORK-CARD.
002740 PROCESS-MEMBER-EXIT.  EXIT.
002750 LOOK-FOR-JOB.
002760     IF COMMENT-CARD GO TO LOOK-END.
002770     SET S TO +1.
002780 LOOK-FOR-LOOP.
002790     IF SEARCH-TABLE (S) NOT EQUAL 'J'
002800        GO TO LOOK-INC-LOOP.
002810     IF SEARCH-TABLE (S - 1) EQUAL ' ' AND
002820        SEARCH-TABLE (S + 1) EQUAL 'O' AND
002830        SEARCH-TABLE (S + 2) EQUAL 'B' AND
002840        SEARCH-TABLE (S + 3) EQUAL ' '
002850           MOVE 'D' TO COMP-CDE
002860           GO TO LOOK-END.
002870 LOOK-INC-LOOP.
002880     SET S UP BY +1.
002890     IF S EQUAL +12
002900        GO TO LOOK-END.
002910     GO TO LOOK-FOR-LOOP.
002920 LOOK-END.
002930     EXIT.
002940 EOJ.
002950     CLOSE MEMBFILE.
002960     CLOSE OUTFILE
002970     IF CARDFILE-SWITCH NOT EQUAL '*' AND
002980        PARM-SWITCH NOT EQUAL '*'
002990         MOVE 'P01101' TO MODULE-NAME
003000         CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
003010             NOWORK-ERRMSG MESSAGE-LENGTH
003020         CALL 'P00655'.
003030     STOP RUN.
