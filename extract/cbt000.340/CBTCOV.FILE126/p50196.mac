*          DATA SET P50196     AT LEVEL 001 AS OF 12/07/79
P50196   TITLE 'CREATE RACF-IDENTIFIED USERID AUTHORIZATION FILE'
         PRINT NOGEN
P50196   PRIME REG=4,ID=_M.B.C._&SYSDATE_&SYSTIME_
         REGISTER
*
* THIS PROGRAM CREATES A FILE OF USERS WITH SPECIFICLY AUTHORIZED
*  ACCESS TO RACF-PROTECTED DATASETS.  THE FILE CONTAINS THE
*  USERID, AUTH, DSN, OWNER, VOL, UACC, AND AUDIT.
*
* NOTE THAT THIS PROGRAM TRANSFERS ONLY THE FIRST 20 ENTRIES FROM
*  A DATA SET'S ACCESS LIST, AND THE FIRST VOL OF A MULTI-VOL FILE.
*
* REGISTER USAGE
*        4     BASE
*        5     IOREG
*        6     POINTER TO DSN IN RACF RECORD
*        7     COUNTS PSEUDO-RECORDS TO BE SKIPPED
*        8     POINTER TO VARIABLE PART OF RACF RECORD
*        9     UTILITY
*       12     INTERNAL LINKAGE
*
         OPEN  (IN,(INPUT),OUT,(OUTPUT))
         USING INAREA,R1
LOOP     GET   IN
         CLI   RECID,X'82'         IS THIS AN ENTITY RECORD  Q
         BNE   LOOP                NO, SKIP INDEX AND BAM RECS
         SR    R7,R7               SET R7 TO RECSIZE/256, IT WILL
         IC    R7,1(R1)              HOLD NUM OF PSEUDO-RECS IN RECS
         SR    R6,R6
         IC    R6,LRECNAM          LOAD LEN OF RECORD NAME
         LR    R5,R1               LOAD IOREG
         DROP  R1
         USING INAREA,R5
         LR    R8,R1
         LA    R8,6(R8)            POINT PAST HEADER TO DSN REC
         AR    R8,R6               ADD LENGTH OF DSN
         USING USRREC,R8
         CLI   ENTYPE,X'04'        IS THIS REALLY A DSN REC  Q
         BNE   LOOP                NO
         C     R7,ONE
         BNH   EXTRACT
         MVC   HOLDA1(256),0(R5)
         LA    R9,HOLDA1+256
         BCTR  R7,0
GETMORE  GET   IN                  READ REST OF SPANNED REC
         MVC   0(256,R9),0(R1)
         LA    R9,256(R9)
         BCT   R7,GETMORE
         LA    R5,HOLDA1
         LR    R8,R5
         LA    R8,6(R8)
         AR    R8,R6
         SPACE
EXTRACT  EQU   *                   PREPARE + WRITE OUTPUT REC
         BCTR  R6,0
         MVC   OUTDSN,BLANKS
         EX    R6,MVCDSN
         MVC   OUTVOL,VOL1
         MVC   OUTOWN,AUTHOR
         LA    R9,UNIVACS
         BAL   R12,CONVACSS        TRANS ACCESS BITS TO CODE
         STH   R9,OUTUACC
         LA    R9,AUDIT
         BAL   R12,CONVAUDT        CONVERT AUDIT BITS TO CODE
         STH   R9,OUTAUDIT
         LA    R6,VOLCNT+2
         LH    R9,VOLCNT
         MH    R9,HSIX
         AR    R6,R9
         DROP  R8
         LH    R8,0(R6)            R8 HAS COUNT OF ENT IN ACCESS LIST
         LA    R6,2(R6)            R6 POINTS TO FIRST ENT
         USING RACFVAR,R6
         LTR   R8,R8               ANY ENTRIES IN ACCESS LIST  Q
         BNP   LOOP                NO
         C     R8,TWENTY           MORE THAN TWENTY ENT
         BNH   GETUSER
         L     R8,TWENTY           YES, SET LIMIT TO TWENTY
GETUSER  MVC   OUTUSER,USERID
         LA    R9,USERACS
         BAL   R12,CONVACSS
         STH   R9,OUTAUTH
         PUT   OUT,OUTREC
         LA    R6,11(R6)
         BCT   R8,GETUSER
         B     LOOP
         SPACE 2
         USING INAREA,R5
MVCDSN   MVC   OUTDSN(0),RECNAM
         SPACE 3
*
*  THIS SUB TRANSLATES ACCESS BITS TO A NUMBER FROM 1 TO 5
*
*   ON ENTRY, R9 POINTS TO THE ACCESS BYTE
*   ON EXIT,  R9 CONTAINS THE NUMBER CODE
*
CONVACSS CLI   0(R9),X'40'
         BH    SETALTER
         BE    SETCTL
         CLI   0(R9),X'10'
         BH    SETUPD
         BL    SETNONE
         LH    R9,ACCREAD
         BR    R12
SETALTER LH    R9,ACCALTR
         BR    R12
SETUPD   LH    R9,ACCUPD
         BR    R12
SETCTL   LH    R9,ACCCTL
         BR    R12
SETNONE  LH    R9,ACCNONE
         BR    R12
         SPACE 2
*
* THIS SUBROUTINE TRANSLATES AUDIT BITS TO NUMBER 1 TO 4
*
*  ON ENTRY, R9 POINTS TO AUDIT BYTE
*  ON EXIT,  R9 CONTAINS NUMBER CODE
*
CONVAUDT EQU   *
         CLI   0(R9),X'20'
         BL    SETANONE
         BE    SETAFAIL
         CLI   0(R9),X'40'
         BH    SETAALL
         LH    R9,AUDSUCC
         BR    R12
SETAALL  LH    R9,AUDALL
         BR    R12
SETANONE LH    R9,AUDNONE
         BR    R12
SETAFAIL LH    R9,AUDFAIL
         BR    R12
         SPACE 2
EOF      CLOSE (IN,,OUT)
         L     13,4(13)
         RETURN (14,12),RC=0
         SPACE 2
         LTORG
IN       DCB   DDNAME=IN,EODAD=EOF,LRECL=256,RECFM=FB,BLKSIZE=1024,    *
               DSORG=PS,MACRF=GL
OUT      DCB   DDNAME=OUT,DSORG=PS,MACRF=PM
         SPACE
OUTREC   DS    0CL72
OUTUSER  DS    CL8                 ACCESS LIST USERID
OUTAUTH  DS    H                   USER'S AUTHORITY
OUTDSN   DS    CL44                DSNAME
OUTVOL   DS    CL6                 VOLUME (ONLY FIRST VOL CAPTURED)
OUTOWN   DS    CL8                 OWNER
OUTUACC  DS    H                   UACC CODE
*                                               * 1  NONE
*                                               * 2  READ
*                                               * 3  UPDATE
*                                               * 4  CONTROL
*                                               * 5  ALTER
OUTAUDIT DS    H                   AUDIT CODE
*                                               * 1  NONE
*                                               * 2  FAILURES
*                                               * 3  SUCCESS
*                                               * 4  ALL
         SPACE
ACCNONE  DC    H'1'
ACCREAD  DC    H'2'
ACCUPD   DC    H'3'
ACCCTL   DC    H'4'
ACCALTR  DC    H'5'
         SPACE
AUDNONE  DC    H'1'
AUDFAIL  DC    H'2'
AUDSUCC  DC    H'3'
AUDALL   DC    H'4'
         SPACE
BLANKS   DC    CL256' '
ONE      DC    F'1'
HSIX     DC    H'6'
TWENTY   DC    F'20'
         SPACE
HOLDA1   DS    CL1024
         SPACE
         DSECT
INAREA   DS    0CL20
RECID    DS    CL1                 X'82' FOR AN ENTITY RECORD
         DS    CL2
         DS    CL2                 LRECL
LRECNAM  DS    CL1                 LENGTH OF NEXT FIELD
RECNAM   DS    CL44                LENGTH VARIES
         SPACE
         DSECT
USRREC   DS    0CL80
ENTYPE   DS    CL1
VERSION  DS    CL1
CREADATE DS    CL3
AUTHOR   DS    CL8
LREFDAT  DS    PL3
LCHGDAT  DS    PL3
ACSALTR  DS    CL2
ACSCNTL  DS    CL2
ACSUPDT  DS    CL2
ACSREAD  DS    CL2
UNIVACS  DS    CL1
FLAG1    DS    CL1
AUDIT    DS    CL1
GROUPNM  DS    CL8
DSTYPE   DS    CL1
LEVEL    DS    CL1
DEVTYP   DS    CL4
DEVTYPX  DS    CL8
GAUDIT   DS    CL1
RESRV    DS    CL15
VOLCNT   DS    CL2
VOL1     DS    CL6
         SPACE
         DSECT
RACFVAR  DS    0CL11
USERID   DS    CL8
USERACS  DS    CL1
ACSCNT   DS    CL2
         SPACE 2
         END
