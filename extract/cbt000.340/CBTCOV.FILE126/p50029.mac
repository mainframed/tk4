*          DATA SET P50029     AT LEVEL 001 AS OF 12/01/80
*          DATA SET P50029     AT LEVEL 008 AS OF 11/17/80
         MACRO
         CSECTNM
         DC    C'&SYSECT'    CSECT NAME
         MEND
         MACRO
         RACFWTO
         TS    *+1            FOR ONE TIME OPERATION (MLPA ONLY)
         BNZ   RACF&SYSNDX    SKIP WTO AFTER FIRST EXECUTION
         TIME
         ST    1,FULL&SYSNDX
         ED    EDIT&SYSNDX,FULL&SYSNDX+1
         MVC   W&SYSNDX+8(5),EDIT&SYSNDX+1
         WTO '******************************************************', X
               ROUTCDE=(9)
W&SYSNDX WTO '      &SYSECT ACTIVE - LAST COMPILE &SYSDATE &SYSTIME',  X
               ROUTCDE=(9)
         WTO '******************************************************', X
               ROUTCDE=(9)
         B     RACF&SYSNDX
FULL&SYSNDX DC F'0'
EDIT&SYSNDX DC X'402020202020'
         CNOP  0,4
RACF&SYSNDX EQU *
         MEND
********************************************************************
*                                                                  *
*   NAME:  RACHECK PRE-PROCESSING EXIT                             *
*                                                                  *
*   PURPOSE: THIS EXIT WILL ALLOW ACCESS TO ALL RACF PROTECTED     *
*            RESOURCES WHEN RACF IS INACTIVE. IT WILL LOG ALL      *
*            ACCESS TO THE SECURITY CONSOLE.                       *
*                                                                  *
*                                                                  *
*                                                                  *
*           THIS IS MODULE P50029 ON PANVALET.                     *
*                                                                  *
*                                                                  *
*                                                                  *
********************************************************************
         EJECT
ICHRCX01 CSECT
 TITLE 'RACHECK PREPROCESSING EXIT  &SYSTIME  &SYSDATE'
********************************************************************
*        SET UP SAVE AREA AND SAVE CALLER'S REGISTERS
********************************************************************
         PRIME LV=WORKSIZE
         LR    R2,R1          SAVE PARAMETER LIST
         USING SAVEAREA,R13
         REGISTER
         RACFWTO
         B     START          BRANCH AROUND ID
         CSECTNM
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
         EJECT
         CNOP  0,4
START    EQU   *
*
*        ADDRESSABILITY IS ESTABLISHED FOR THESE AREAS.
*
*              R4 = CVT
*              R5 = CURRENT ASCB
*              R6 = CURRENT ASXB
*              R7 = ACCESS CONTROL ENVIRONMENT ELEMENT
*
         L     R4,16          CVT ADDRESS
         USING CVT,R4
         L     R5,CVTTCBP     TCB POINTERS FROM CVT
         L     R5,12(R5)      CURRENT ASCB POINTER
         USING ASCB,R5
         L     R6,ASCBASXB    CURRENT ASXB POINTER
         USING ASXB,R6
         L     R7,ASXBSENV    ACEE POINTER
         USING ACEE,R7
         EJECT
*
*        RC FOR RACHECK
*
*              00 = NORMAL SVC PROCESSING IS TO CONTINUE.
*              04 = REQUEST IS TO BE FAILED.
*              08 = REQUEST IS ACCEPTED - NO MORE SVC PROCESSING.
*              0C = REQUEST IS GRANTED - NO OTHER AUTHORIZATION
*                   IS TO BE PERFORMED ON THE ACCESS LIST. BUT
*                   LOGGING IS TO CONTINUE.
*
         LA    R10,0            DEFAULT RETURN CODE
         MVC   WTO(RACFMSGE-WTOC),WTOC MOVE MODEL MESSAGE
         LA    R3,WTO           WORKAREA
         USING WTOC,R3
         L     R11,20(R2)       DATA SET NAME
         MVC   DSN,0(R11)       MOVE DSN TO WTO
         SPACE 3
*
*        CHECK TO SEE IF RACHECK MACRO SPECIFIED
*        USER ID. IF IT DID USE IT AND NOT CURRENT
*        ID.
*
         L     R1,68(R2)      ACEE ADDRESS POINTER
         L     R1,0(R1)       TO ACEE
         LTR   R1,R1          ZERO ? ? ? ?
         BZ    NOUSER         NO ACEE TO GET USER FROM
         MVC   RACFMSG+10(4),21(R1) MOVE USER ID FROM ACEE
         B     ISSUE
         SPACE 1
*
*        IF USER ID NOT SPECIFIED ON RACHECK MACRO
*        USE CURRENT ID IN ACEE FOR SECURITY.
*
NOUSER   EQU   *
         MVC   RACFMSG+10(4),ACEEUSRI
ISSUE    EQU   *
********************************************************************
*        CHECK FOR CLASS OF TAPEVOL AND TMS CALLER
********************************************************************
         L     R11,24(R2)          GET ADDRESS OF CLASS
         XR    R9,R9               CLEAR R9
         IC    R9,0(R11)           GET ONE BYTE LENGTH OF CLASS
         C     R9,=F'7'            CHECK FOR LENGTH(TAPEVOL)
         BNE   ISSUE1              NO, -- GET OUT
         CLC   1(7,R11),=C'TAPEVOL' YES -- CHECK FOR TAPEVOL
         BNE   ISSUE1              NO, -- GET OUT
         LA    R10,12              SET TO BYPASS AUTH CHECKING
         B     ISSUE2
ISSUE1   EQU   *
         MVC   WTO+4(60),RACFMSG
         WTO   MF=(E,WTO)
         CLC   RACFMSG+10(4),=C'G019' CHECK FOR DMS USER
         LA    R10,8
         BE    EXIT                YES, ACCEPT NO MATTER WHAT
         LA    R10,0
ISSUE2   EQU   *
         L     R3,CVTRAC        RACF CVT
         LTR   R3,R3            SEE IF RACF CVT EXISTS
         BZ    NORACF           RACF CVT DOES NOT EXIST
         USING RCVT,R3
         TM    RCVTFLGS,X'80'   SEE IF RACF INACTIVE
         BO    NORACF           RACF VARIED INACTIVE
         TM    RCVTSTAT,X'80'   RACF INACTIVE ? ? ? ?
         BZ    EXIT             NO LEAVE. . . . .
NORACF   EQU   *
         MVC   WTO+4(60),NORACFM NO RACF MESSAGE
         WTO   MF=(E,WTO)
         LA    R10,8          ACCEPT REQUEST
         EJECT
********************************************************************
*        RESTORE REGISTERS FREE STORAGE AND RETURN
********************************************************************
EXIT     DS    0H
         LR    R15,R10          SET UP EXIT RETURN CODE
         TERME LV=72+WORKSIZE
         EJECT
********************************************************************
*        CONSTANTS AND LITERALS
********************************************************************
         LTORG
WTOC     WTO   '1234567890123456789012345678901234567890123456789012345X
               67890',MF=L,ROUTCDE=(9)
RACFMSG  DC    0CL60' '
         DC    CL16'RACHECK U=    : '
DSN      DC    CL44' '
NORACFM  DC    CL60'*** WARNING *** RACF NOT ACTIVE --- ACCESS ALLOWED'
RACFMSGE EQU   *
********************************************************************
*        SAVE AREA AND WORK AREA
********************************************************************
SAVEAREA DSECT
SAVE     DS    18F'0'
WTO      DS    CL(RACFMSGE-WTOC)
WORKSIZE EQU   *-SAVEAREA
********************************************************************
         EJECT
********************************************************************
*   MAPPING MACROS
********************************************************************
         EJECT
         ICHPRCVT
         EJECT
         CVT       DSECT=YES
         EJECT
         IHAASCB
         EJECT
         IHAASXB
         EJECT
         IHAACEE
         END
