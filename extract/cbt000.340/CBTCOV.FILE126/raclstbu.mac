DRIVER   $PROLOG
         LA    R1,=C'AUTH'
         SVC   231
         CALL RACLSTBU,(CLASS,ANCHOR)
         DC    H'0'
CLASS    DC    CL8'USER'
ANCHOR   DS    F
         LTORG
RACLSTBU $PROLOG
         EJECT
**********************************************************************
*
*  THIS ROUTINE IS A SUBROUTINE WHICH PERFORMS A RACLIST AND BUILDS
*  A LIST OF POINTERS TO THE RACLIST PROFILES. THIS PERMITS SEQUENTIAL
*  PROCESSING OF THE RACLIST PROCESSING OF THE PROFILES. TO USE THIS
*  ROUTINE, THE CALLER MUST BE AUTHORIZED
*  THE CALL IS:
*        CALL RACLSTBU,(CLASS,ANCHOR)
*           WHERE-
*               CLASS IS AN 8 BYTE FIELD CONTAINING THE CLASS ON WHICH
*                     THE RACLIST WILL BE PERFORMED
*               ANCHOR IS A FULLWORD WHICH ON RETURN WILL HAVE THE
*                     ADDRESS OF THE LIST OF POINTERS IN THE 3 LOW
*                     ORDER BYTES AND THE LENGTH IN THE HIGH ORDER
*                     BYTE.
*
**********************************************************************
         EJECT
         LR    R2,R1               COPY ADDR OF PARM LIST
         L     R6,0(R1)            GET ADDR OF  CLASS
         L     R9,4(R1)            GET ADDR OF ANCHOR
         L     R4,544              LOAD PTR TO ASCB
         L     R4,108(R4)          LOAD PTR TO ASXB
         L     R4,200(R4)          R4 NOW PTS TO ACEE
         RACLIST CLASS=(R6),                                           X
               ACEE=(R3),SUBPOOL=(0,0),ENVIR=CREATE
         LTR   R15,R15             CHECK FOR VALID RETURN
         BZ    RACLST01
         ABEND 1,DUMP
*                                  IS NON ZERO---ABEND
RACLST01 EQU   *
         USING ACEE,R4
         L     R3,ACEECLCP         GET ADDR OF IN STG PROFILE ANCHOR
         DROP  R4
         USING RACRTE,R3
         L     R3,RTETREE          GET ADDR OF TOP NODE OF TREE
         USING RACRNE,R4
         XR    R5,R5
         LR    R4,R3               BEGIN CHASING LEFT DAUGHTER CHAIN
LEFTLOOP LTR   R4,R4               CHECK FOR END OF CHAIN
         BZ    GETSTG              YES, GET STORAGE FOR TABLE
         LA    R5,1(R5)            INCREMENT LEVEL COUNT
         L     R4,RNELEFT          GET ADDR OF NEXT LEFT
         B     LEFTLOOP
GETSTG   LA    R6,1                LOAD 1 IN R6
         SLL   R6,2(R5)            COMPUTE 2**N
         GETMAIN RU,LV=(R6)        GET STORAGE FOR TABLE
         ST    R1,0(R9)            STORE ADDR OF TABLE IN USER AREA
         MVI   0(R9),C' '          BLANK HIGH ORDER BYTE
         ST    R6,0(R1)            STORE LENGTH OF TABLE IN FIRST CELL
         LR    R9,R1               STORE BASE ADDR OF TABLE
         S     R5,=F'1'            SUBTRACT 1 FROM COUNT
         LA    R6,1                SET R6 AT 1
         SLL   R6,0(R5)            COMPUTE 2**(N-1) -INIT VALUE FOR I
         SLL   R6,2                MULTIPLY BY 4 TO GET ADDR OF MID
         ST    R3,0(R6,R9)         SET UP COUNT REGISTER
         LA    R11,1               SET UP M
         B     RSRCH020
RSRCH010 SLL   R11,1               MULTIPLY BY 2
RSRCH020 LR    R10,R11             SET UP COUNT REG
         LR    R7,R6               SET UP J
         SRL   R7,1                DIVIDE I/2 GIVING J
         LR    R8,R6               SET UP K
RSRCH030 L     R4,0(R8,R9)         LOAD ADDR OF T(K)
         C     R4,=F'0'            CHECK FOR ZEROES
         BNE   RSRCH035
         LA    R4,ZEROES
RSRCH035 LR    R5,R8
         SR    R5,R7               COMPUTE T(K-J)
         AR    R5,R9
         MVC   0(4,R5),RNELEFT     MOVE IN LEFT DAUGHTER
         LR    R5,R8
         AR    R5,R7               COMPUTE T(K+J)
         AR    R5,R9
         MVC   0(4,R5),RNERIGHT    MOVE IN RIGHT DAUGHTER
RSRCH040 LR    R5,R6
         SLL   R5,1                MULTIPLY I BY 2
         AR    R8,R5               COMPUTE K = K + 2I
         BCT   R10,RSRCH030
         SRL   R6,1                DIVIDE I BY 2
         C     R6,=F'4'            CHECK FOR 1
         BNE   RSRCH010
RACLST03 $EPILOG
E1SAV    DS    7F
ZEROES   DC    F'0'
         DC    F'0'
         DC    F'0'
         LTORG
         IHAACEE
         COPY ICHPISP
         END
