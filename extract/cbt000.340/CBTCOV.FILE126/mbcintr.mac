MBCINTR  CSECT
***********************************************************************
*                                                                     *
*  THIS ROUTINE FRONT ENDS IKJSCAN AND DEPENDING ON A SWITCH, BRANCES *
*  TO EITHER PCFSCAN OR MBCSCAN. THE SWITCH CAN BE SET BY ZAPPING THE *
*  BYTE AT LABEL MBCSW TO EITHER X'00' FOR PCFSCAN OR X'80' FOR       *
*  MBCSCAN                                                            *
*                                                                     *
***********************************************************************
         REGISTER
         USING MBCINTR,R15
         TM    MBCSW,MBCON         CHECK WHICH SCAN ROUTINE
         BO    MBCSCAN1            IF ON, THEN MBCSCAN
         L     R15,=V(PCFSCAN)     OTHERWISE GET ADDR OF PCFSCAN
         BR    R15                 AND GO THERE (NO RETURN)
MBCSCAN1 EQU   *
         STM   R14,R12,12(R13)     STORE REGS
         LR    R2,R1               COPY PARM REG
         LR    R12,R15             ESTABLISH R12 AS BASE
         DROP  R15
         USING MBCINTR,R12
         GETMAIN R,LV=WORKLEN      GET SAVE AREA
         ST    R13,4(R1)           CHAIN BACK
         ST    R1,8(R13)           CHAIN FORWARD
         LR    R13,R1              SET UP NEW SAVE AREA
         MVC   ESTAEOFF(ESTAELEN,R13),ESTAEPAT MOVE IN ESTAE
         L     R4,=A(DUMPIT)       GET ADDR OF ABEND ROUTINE
         LA    R3,ESTAEOFF(R13)
         ESTAE (R4),PARAM=(R13),MF=(E,(R3))
         L     R15,=V(MBCSCAN)     ADDR OF MBCSCAN
         LR    R1,R2               RESTORE PARM REG
         BALR  R14,R15             GO THERE (BUT RETURN)
         LR    R10,R15             COPY RETURN
         ESTAE 0,MF=(E,(R3))       CANCEL ESTAE
         L     R8,4(R13)           GET ADDR OF PREV SAVE
         FREEMAIN R,LV=WORKLEN,A=(R13)
         LR    R13,R8              RESTORE SAVE AREA POINTER
         LR    R15,R10             RESTORE RETURN CODE
         L     R14,12(R13)         RELOAD REGS
         LM    R0,R12,20(R13)
         BR    R14                 AND RETURN
MBCSW    DC    X'00'
MBCON    EQU   X'80'
         DS    0D
WORK     EQU   *
SAVEAREA DS    18F
ESTAEOFF EQU   *-WORK
ESTAEPAT ESTAE DUMPIT,XCTL=NO,TERM=YES,MF=L
ESTAELEN EQU   *-ESTAEPAT
WORKLEN  EQU   *-WORK
         LTORG
         EJECT
DUMPIT   SAVE  (14,12)
         DROP  R12
         LR    R3,R1
         LR    R12,R15
         USING DUMPIT,R12
         GETMAIN R,LV=DUMPLEN
         LR    R4,R1
         L     R10,=A(SDUMPL)
         MVC   0(DUMPLEN,R4),0(R10)
         LA    R1,=C'AUTH'
         SVC   231
         SDUMP MF=(E,(R4))
         FREEMAIN R,LV=DUMPLEN,A=(R4)
         SETRP DUMP=YES,RC=0,REGS=(14,12),WKAREA=(R3)
SDUMPL   SDUMP HDR='RACF/TSO SUB COMMANDS',                            X
               SDATA=(PSA,CSA,NUC,RGN,SQA,LPA,SUM),MF=L
DUMPLEN  EQU   *-SDUMPL
         LTORG
         EJECT
         PRINT NOGEN
         IHASDWA
         END
