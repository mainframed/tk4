000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID.    P50281A.
000030 AUTHOR.        DICK GLOJEK.
000040 INSTALLATION.  MILLER BREWING COMPANY - MILWAUKEE.
000050 DATE-WRITTEN.  AUGUST 1981.
000060 DATE-COMPILED.
000070 REMARKS.       *** ENVIRON-1 SECURITY AUDIT FILE CREATE ***
000080 ENVIRONMENT DIVISION.
000090 CONFIGURATION SECTION.
000100 SOURCE-COMPUTER.     IBM-370.
000110 OBJECT-COMPUTER.     IBM-370.
000120 INPUT-OUTPUT SECTION.
000130 FILE-CONTROL.
000140     SELECT E1-AUDIT-FILE ASSIGN UT-S-E1AUDIT.
000150     SELECT E1-SECURITY-FILE ASSIGN UT-S-SECURITY.
000160     SELECT PRINTER-FILE ASSIGN UT-S-PRINTER.
000170     SELECT SORT-FILE ASSIGN UT-S-SORTWK01.
000180 DATA DIVISION.
000190 FILE SECTION.
000200 FD  E1-AUDIT-FILE
000210     BLOCK CONTAINS 0 RECORDS
000220     RECORDING MODE F
000230     LABEL RECORD STANDARD.
000240 01  E1-AUDIT-RECORD         PIC X(576).
000250 FD  E1-SECURITY-FILE
000260     BLOCK CONTAINS 0 RECORDS
000270     RECORDING MODE IS F
000280     LABEL RECORD STANDARD.
000290 01  E1-SECURITY-RECORD      PIC X(35).
000300 FD  PRINTER-FILE
000310     BLOCK CONTAINS 0 RECORDS
000320     RECORDING MODE F
000330     LABEL RECORD STANDARD.
000340 01  PRINT-LINE.
000350     05  PRT-CNTL            PIC X.
000360     05  FILLER              PIC X(132).
000370 SD  SORT-FILE
000380     RECORDING MODE IS F.
000390 01  SORT-RECORD.
000400     05  SORT-KEY            PIC X(7).
000410     05  FILLER              PIC X(11).
000420     EJECT
000430 WORKING-STORAGE SECTION.
000440 01  ODDS-AND-ENDS.
000450     05  LINE-CT                 PIC S99     COMP   VALUE +0.
000460     05  PREV-AUDATE             PIC S9(6)   COMP-3.
000470     05  PREV-SYSID              PIC XXX.
000480     05  ZP01616Z            PIC X(8)       VALUE 'P01616'.
000490     05  ZP00655Z            PIC X(8)       VALUE 'P00655'.
000500     05  EDITED-DATE.
000510         10  EDITED-MON      PIC 99.
000520         10  FILLER          PIC X          VALUE '/'.
000530         10  EDITED-DAY      PIC 99.
000540         10  FILLER          PIC X          VALUE '/'.
000550         10  EDITED-YR       PIC 99.
000560     05  DATE-BREAKDOWN      PIC 9(6).
000570     05  FILLER REDEFINES DATE-BREAKDOWN.
000580         10  BRKDWN-YR       PIC XX.
000590         10  BRKDWN-MON      PIC XX.
000600         10  BRKDWN-DAY      PIC XX.
000610     05  JULIAN-DATE         PIC 9(5).
000620     05  FILLER REDEFINES JULIAN-DATE.
000630         10  JULIAN-YR       PIC 99.
000640         10  JULIAN-DAY      PIC 999.
000650     SKIP3
000660     05  AUDIT-RECORD-LAYOUT.
000670         10  FILLER              PIC X(60).
000680         10  AUDIT-RECORD-DATA.
000690             15  AR-ID           PIC X(6).
000700                 88  AUDIT-RECORD            VALUE '$AUDIT'.
000710             15  AR-AUDATE       PIC S9(6)                COMP-3.
000720             15  AR-AUDTIM       PIC S9(6)                COMP-3.
000730             15  AR-TERMID       PIC X(4).
000740             15  AR-LUNME        PIC X(8).
000750             15  AR-TRMLOC       PIC X(19).
000760             15  AR-WRMIND       PIC X.
000770             15  AR-TYPE         PIC 99.
000780                88  E1-SIGNON               VALUE 10.
000790                88  SYSTEM-SIGNON           VALUE 15.
000800                88  TRANSACTION-REQUEST     VALUE 20.
000810                88  TRANSACTION-ENDED       VALUE 25.
000820                88  E1-SIGNOFF              VALUE 30.
000830             15  AR-SECDE        PIC 99.
000840                 88  NO-VIOLATION                VALUE 00.
000850             15  AR-E1SON        PIC S9(6)                COMP-3.
000860             15  AR-SYSID        PIC XXX.
000870             15  AR-USERID.
000880                 20  FILLER          PIC X(3).
000890                 20  AR-RACF-ID      PIC X.
000900                     88  NOT-RACF-ID  VALUE ' '.
000910                 20  AR-USER-RACF    PIC X.
000920                     88  RACF-USER   VALUE 'r'.
000930*                    88  RACF-USER   VALUE X'99'.
000940                 20  FILLER          PIC X(3).
000950             15  AR-USERID-RACF REDEFINES AR-USERID.
000960                 20  AR-USER-GRP-ID  PIC XX.
000970                 20  FILLER          PIC XX.
000980             15  AR-SYSSON       PIC S9(6)                COMP-3.
000990             15  AR-LSTDTE       PIC S9(6)                COMP-3.
001000             15  AR-LSTTIM       PIC S9(4)                COMP-3.
001010             15  AR-TRANID       PIC X(4).
001020             15  AR-PGMENT       PIC X(8).
001030             15  AR-TRNENT       PIC S9(6)                COMP-3.
001040             15  AR-TRNEND       PIC S9(6)                COMP-3.
001050             15  AR-E1SOFF       PIC S9(6)                COMP-3.
001060             15  AR-DBCONT       PIC X.
001070             15  AR-LOCCDE       PIC XXX.
001080             15  AR-LOCCDE-1 REDEFINES AR-LOCCDE PIC X.
001090             15  FILLER          PIC X(24).
001100             15  AR-DBINFO OCCURS 16 TIMES.
001110                 20  AR-DBFILE   PIC X(4).
001120                 20  AR-DBPATH   PIC X(4).
001130                 20  AR-DBREAD   PIC S9(9)                COMP.
001140                 20  AR-DBUPDT   PIC S9(9)                COMP.
001150                 20  AR-DBADD    PIC S9(9)                COMP.
001160                 20  AR-DBDELT   PIC S9(9)                COMP.
001170         10  FILLER REDEFINES AUDIT-RECORD-DATA.
001180             15  FILLER          PIC X(69).
001190             15  AR-ERR15        PIC X(35).
001200         10   FILLER REDEFINES AUDIT-RECORD-DATA.
001210             15  FILLER          PIC X(80).
001220             15  AR-ERR20        PIC X(35).
001230         10  FILLER              PIC X(4).
001240     SKIP3
001250     05  SORT-RECORD-LAYOUT.
001260         10  SR-AUDATE           PIC S9(6)   COMP-3.
001270         10  SR-SYSID            PIC XXX.
001280         10  SR-AUDTIM           PIC S9(6)   COMP-3.
001290         10  SR-TYPE             PIC 99.
001300         10  SR-SECDE            PIC 99.
001310     SKIP3
001320     05  E1-SECURITY-LAYOUT.
001330         10  SC-AUDTIM       PIC S9(6)       COMP-3.
001340         10  SC-AUDATE       PIC S9(6)       COMP-3.
001350         10  SC-AUDJULIAN    PIC S9(5)       COMP-3.
001360         10  SC-SYSID        PIC X(3).
001370         10  SC-UNAUTH-TERM  PIC S9(5)       COMP-3.
001380         10  SC-UNAUTH-TRAN  PIC S9(5)       COMP-3.
001390         10  SC-INVALID-PASS PIC S9(5)       COMP-3.
001400         10  FILLER          PIC X(12).
001410     SKIP3
001420     05  DET-LINE            PIC X(133).
001430     05  FILLER REDEFINES DET-LINE.
001440         10  DET-CNTL            PIC X.
001450         10  DET-AUDTIM          PIC 9(6).
001460         10  FILLER              PIC XX.
001470         10  DET-AUDATE          PIC 9(6).
001480         10  FILLER              PIC XX.
001490         10  DET-AUDJULIAN       PIC 9(5).
001500         10  FILLER              PIC XX.
001510         10  DET-SYSID           PIC XXX.
001520         10  FILLER              PIC XX.
001530         10  DET-UNAUTH-TERM     PIC 9(5).
001540         10  FILLER              PIC XX.
001550         10  DET-UNAUTH-TRAN     PIC 9(5).
001560         10  FILLER              PIC XX.
001570         10  DET-INVALID-PASS    PIC 9(5).
001580     SKIP3
001590 01  CONV-DATE-DATA.
001600     COPY CONVAREA.
001610     EJECT
001620 PROCEDURE DIVISION.
001630     SORT SORT-FILE ASCENDING KEY SORT-KEY
001640         INPUT PROCEDURE SORT-SECURITY-RECORDS
001650             THRU SORT-EX
001660         OUTPUT PROCEDURE CREATE-SECURITY-AUDIT
001670             THRU CREATE-EX.
001680     GOBACK.
001690     SKIP3
001700 SORT-SECURITY-RECORDS SECTION.
001710     OPEN INPUT E1-AUDIT-FILE.
001720 READ-AUDIT.
001730     READ E1-AUDIT-FILE INTO AUDIT-RECORD-LAYOUT
001740          AT END
001750          CLOSE E1-AUDIT-FILE GO TO SORT-EX.
001760     IF NOT AUDIT-RECORD GO TO READ-AUDIT.
001770     IF NO-VIOLATION GO TO READ-AUDIT.
001780     MOVE AR-AUDATE TO SR-AUDATE.
001790     MOVE AR-AUDTIM TO SR-AUDTIM.
001800     MOVE AR-SYSID TO SR-SYSID.
001810     MOVE AR-TYPE TO SR-TYPE.
001820     MOVE AR-SECDE TO SR-SECDE.
001830     RELEASE SORT-RECORD FROM SORT-RECORD-LAYOUT.
001840     GO TO READ-AUDIT.
001850 SORT-EX.
001860     EXIT.
001870     SKIP3
001880 CREATE-SECURITY-AUDIT SECTION.
001890     OPEN OUTPUT E1-SECURITY-FILE
001900                 PRINTER-FILE.
001910     PERFORM RETURN-SORTED.
001920     IF SORT-RECORD-LAYOUT = HIGH-VALUES
001930         CLOSE E1-SECURITY-FILE PRINTER-FILE GO TO CREATE-EX.
001940     MOVE SPACES TO DET-LINE.
001950     MOVE '1' TO DET-CNTL.
001960     GO TO BUILD-SECURITY-REC.
001970 RETURN-SORTED.
001980     RETURN SORT-FILE INTO SORT-RECORD-LAYOUT
001990         AT END MOVE HIGH-VALUES TO SORT-RECORD-LAYOUT.
002000 BUILD-SECURITY-REC.
002010     MOVE SPACES TO E1-SECURITY-LAYOUT.
002020     MOVE +0 TO SC-UNAUTH-TERM
002030                SC-UNAUTH-TRAN
002040                SC-INVALID-PASS.
002050     MOVE SR-AUDATE TO SC-AUDATE
002060                       PREV-AUDATE.
002070     MOVE SR-AUDTIM TO SC-AUDTIM.
002080     MOVE SR-SYSID TO SC-SYSID
002090                      PREV-SYSID.
002100 CHECK-TYPE.
002110     IF SR-TYPE = 15 AND SR-SECDE = 99
002120         ADD 1 TO SC-INVALID-PASS.
002130     IF SR-TYPE = 15 AND SR-SECDE = 02
002140         ADD 1 TO SC-INVALID-PASS.
002150     IF SR-TYPE = 15 AND SR-SECDE = 12
002160         ADD 1 TO SC-UNAUTH-TERM.
002170     IF SR-TYPE = 15 AND SR-SECDE = 97
002180         ADD 1 TO SC-UNAUTH-TRAN.
002190     IF SR-TYPE = 20 AND SR-SECDE = 99
002200         ADD 1 TO SC-UNAUTH-TRAN.
002210     IF SR-TYPE = 20 AND SR-SECDE = 97
002220         ADD 1 TO SC-UNAUTH-TRAN.
002230 NEXT-SORT-REC.
002240     PERFORM RETURN-SORTED.
002250     IF SORT-RECORD-LAYOUT = HIGH-VALUES
002260         PERFORM WRITE-SECURITY THRU WRITE-EX
002270         CLOSE E1-SECURITY-FILE PRINTER-FILE
002280         GO TO CREATE-EX.
002290     IF SR-AUDATE NOT = PREV-AUDATE
002300         PERFORM WRITE-SECURITY THRU WRITE-EX
002310         GO TO BUILD-SECURITY-REC.
002320     IF SR-SYSID NOT = PREV-SYSID
002330         PERFORM WRITE-SECURITY THRU WRITE-EX
002340         GO TO BUILD-SECURITY-REC.
002350     GO TO CHECK-TYPE.
002360 WRITE-SECURITY.
002370     IF SC-UNAUTH-TERM = +0 AND
002380         SC-UNAUTH-TRAN = +0 AND
002390         SC-INVALID-PASS = +0 GO TO WRITE-EX.
002400     MOVE SC-AUDATE TO DATE-BREAKDOWN.
002410     PERFORM GET-JULIAN THRU GET-JULIAN-EX.
002420     MOVE JULIAN-DATE TO SC-AUDJULIAN.
002430     WRITE E1-SECURITY-RECORD FROM E1-SECURITY-LAYOUT.
002440     MOVE SPACES TO DET-LINE.
002450     MOVE SC-AUDATE TO DET-AUDATE.
002460     MOVE SC-AUDJULIAN TO DET-AUDJULIAN.
002470     MOVE SC-AUDTIM TO DET-AUDTIM.
002480     MOVE SC-SYSID TO DET-SYSID.
002490     MOVE SC-UNAUTH-TERM TO DET-UNAUTH-TERM.
002500     MOVE SC-UNAUTH-TRAN TO DET-UNAUTH-TRAN.
002510     MOVE SC-INVALID-PASS TO DET-INVALID-PASS.
002520     IF LINE-CT GREATER THAN 60
002530         MOVE '1' TO DET-CNTL
002540         MOVE 0 TO LINE-CT.
002550     WRITE PRINT-LINE FROM DET-LINE AFTER POSITIONING PRT-CNTL.
002560     ADD 1 TO LINE-CT.
002570 WRITE-EX.
002580     EXIT.
002590 CREATE-EX.
002600     EXIT.
002610 GET-JULIAN.
002620     MOVE BRKDWN-MON TO EDITED-MON.
002630     MOVE BRKDWN-DAY TO EDITED-DAY.
002640     MOVE BRKDWN-YR TO EDITED-YR.
002650     MOVE EDITED-DATE TO CONV-DATE.
002660     MOVE 'D16' TO CONV-DATE-CODE.
002670     CALL ZP01616Z USING CONV-MODULE-NAME
002680                         CONV-MODULE-FUNC
002690                         CONV-DATE-CODE
002700                         CONV-DATE
002710                         CONV-BIN-DATE.
002720     IF CONV-DATE-CODE = SPACES CALL ZP00655Z.
002730     MOVE 'BX6' TO CONV-DATE-CODE.
002740     CALL ZP01616Z USING CONV-MODULE-NAME
002750                         CONV-MODULE-FUNC
002760                         CONV-DATE-CODE
002770                         CONV-BIN-DATE
002780                         CONV-DATE-DATA.
002790     IF CONV-DATE-CODE = SPACES CALL ZP00655Z.
002800     MOVE CONV-YEAR TO JULIAN-YR.
002810     MOVE CONV-JULIAN TO JULIAN-DAY.
002820 GET-JULIAN-EX.
002830     EXIT.
002840     SKIP3
002850 END-OF-CODING.
