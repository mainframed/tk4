000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID. P01831.
000030*
000040 REMARKS.
000050*    THIS PROGRAM READS AND WRITES THE CARD IMAGES OF MEMBERS
000060*    OF A PDS:
000070*
000080*            //CARDLIB  DD - SPECIFIES PDS (LIBRARY) WHERE
000090*                            MEMBERS ARE LOCATED.
000100*            //OUTFILE  DD - SPECIFIES WHERE CARD IMAGES ARE TO
000110*                            BE WRITTEN,
000120*                            (I.E. TO THE INTERNAL READER).
000130*            //JOBSFILE DD - ONE WAY OF SPECIFYING MEMBER NAMES,
000140*                            A DATA SET CONTAINING 80 BYTE RECORDS
000150*                            MEMBER NAME FROM 1 TO 8 BYTES
000160*                            LEFT JUSTIFIED ONE PER RECORD.
000170*                            IF NOT USED, 'DUMMY' MUST BE
000180*                            SPECIFIED.
000190*                PARM      - OPTIONAL - IF USED, SPECIFY 1 TO 8
000200*                            BYTE MEMBER NAMES SEPERATED BY
000210*                            COMMAS.
000220*             CONSOLE      - IF NEITHER JOBSFILE OR PARM IS USED,
000230*                            CONSOLE OPERATOR WILL BE PROMPTED
000240*                            FOR MEMBER NAMES.
000250*
000260*    THE INTENDED USAGE OF THIS PROGRAM IS TO LOAD JCL FROM A
000270*    JCL LIBRARY TO THE INTERNAL READER FOR EXECUTION. IF USED
000280*    IN 'CONSOLE MODE' THE JOB USING THIS PROGRAM WILL RELOAD
000290*    ITSELF THRU THE INTERNAL READER.
000300*
000310 ENVIRONMENT DIVISION.
000320 CONFIGURATION SECTION.
000330 SOURCE-COMPUTER. IBM-370-158.
000340 OBJECT-COMPUTER. IBM-370-158.
000350 INPUT-OUTPUT SECTION.
000360 FILE-CONTROL.
000370     SELECT MEMBFILE ASSIGN TO UT-S-JOBSFILE.
000380     SELECT OUTFILE ASSIGN TO UT-S-OUTFILE.
000390 DATA DIVISION.
000400 FILE SECTION.
000410 FD  MEMBFILE
000420     RECORDING MODE F
000430     LABEL RECORDS STANDARD
000440     BLOCK CONTAINS 0 RECORDS
000450     DATA RECORD IS JCL-CARD.
000460 01  JCL-CARD                            PIC X(80).
000470 FD  OUTFILE
000480     RECORDING MODE F
000490     LABEL RECORDS STANDARD
000500     BLOCK CONTAINS 0 RECORDS
000510     DATA RECORD IS OUT-CARD.
000520 01  OUT-CARD                            PIC X(80).
000530*
000540*
000550 WORKING-STORAGE SECTION.
000560*
000570*
000580 01  WORK-CARD                           PIC X(80).
000590*
000600 01  INTERFACE-AREA.
000610*
000620     05  MODULE-NAME                     PIC X(8).
000630     05  INTERFACE-USAGE                 PIC X.
000640*
000650 01  PDS-CNTL.
000660*
000670     05  IO-FUNC                         PIC XX.
000680     05  RET-CODE                        PIC XX.
000690       88  READ-OKAY                 VALUE SPACES.
000700       88  MEMBER-END                VALUE 'EM'.
000710     05  FILE-NAME                       PIC X(8).
000720 01  F-CTL.
000730*
000740     05  F-RET                           PIC XX.
000750       88  F-OK                      VALUE 'OK'.
000760*
000770 01  MEMBER-NAME                         PIC X(8).
000780*
000790 01  BINARY-FIELDS.
000800*
000810     05  MESSAGE-LENGTH                  PIC S9(6)   COMP
000820             VALUE +60.
000830     05  RETURN-LENGTH1                  PIC S9(6)   COMP
000840             VALUE +1.
000850     05  RETURN-LENGTH2                  PIC S9(6)   COMP
000860             VALUE +8.
000870     05  I                               PIC S9(6)   COMP
000880             VALUE ZERO.
000890     05  J                               PIC S9(6)   COMP
000900             VALUE ZERO.
000910*
000920 01  CONSOLE-SWITCH                      PIC X.
000930     88  MODE-IS-CONSOLE             VALUE '*'.
000940*
000950 01  CARDFILE-SWITCH                     PIC X.
000960     88  MODE-IS-CARDS               VALUE '*'.
000970*
000980 01  PARM-SWITCH                         PIC X.
000990     88  PARMS-USED                  VALUE '*'.
001000*
001010 01  MESSAGE-VALUES.
001020*
001030     05  PDS-ERRMSG                      PIC X(60)
001040             VALUE ' ** CARDLIB ERROR, DDCARD OR LIBRARY NOT FOUND
001050-                  ' **           '.
001060     05  NEWMEMB-MSG                     PIC X(60)
001070             VALUE ' ** ENTER JOBNAME **
001080-                  '              '.
001090     05  NEXTMEMB-MSG                    PIC X(59)
001100             VALUE ' ** DO YOU WISH TO SUBMIT ANOTHER JOB ? REPLY
001110-                  '"Y" OR "N" **'.
001120     05  NOMEMB-ERRMSG.
001130         10  FILLER                      PIC X(4)
001140             VALUE ' ** '.
001150         10  ERR-MEMB                    PIC X(8).
001160         10  FILLER                      PIC X(13)
001170             VALUE ' NOT FOUND **'.
001180         10  FILLER                      PIC X(35)
001190             VALUE SPACES.
001200     05  BADMEMB-ERRMSG.
001210         10  FILLER                      PIC X(4)
001220             VALUE ' ** '.
001230         10  BAD-MEMB                    PIC X(8).
001240         10  FILLER                      PIC X(30)
001250             VALUE ' HAD ERROR DURING RETRIEVAL **'.
001260         10  FILLER                      PIC X(18)
001270             VALUE SPACES.
001280     05  PARM-ERRMSG                     PIC X(60)
001290             VALUE ' ** PARM ERROR, A MEMBER NAME IS LONGER THAN 8
001300-                  ' POSITIONS ** '.
001310*
001320 01  SAVE-CARD.
001330*
001340     05  SAVE-FIRST-FIVE                 PIC X(5).
001350       88  EOF-CARD                  VALUE '/*EOF'.
001360     05  FILLER                          PIC X(75).
001370*
001380 01  JOB-AREA.
001390*
001400     05  JOBNAME-LENGTH                  PIC S9(6)       COMP.
001410     05  JOBNAME-LOADER                  PIC X(8).
001420     05  STEPNAME-LENGTH                 PIC S9(6)       COMP.
001430     05  STEPNAME-LOADER                 PIC X(8).
001440     05  PROCSTEP-NAME-LEN               PIC S9(6)       COMP.
001450     05  PROCSTEP-NAME                   PIC X(8).
001460*
001470 01  TABLE-AREA.
001480*
001490     05  JOB-TABLE.
001500         10  PARM-BYTE OCCURS 100 TIMES  PIC X.
001510     05  PARM-MEMBER.
001520         10  MEMB-BYTE OCCURS 8   TIMES  PIC X.
001530*
001540 01  CONTINUE-MESG                       PIC X.
001550     88  REPLY-IS-YES                VALUE 'Y'.
001560*
001570 LINKAGE SECTION.
001580*
001590 01  JOBS-PARM.
001600     05  PARM-LENGTH                     PIC S9(4)   COMP.
001610     05  PARM-JOBS                       PIC X(100).
001620 PROCEDURE DIVISION USING JOBS-PARM.
001630     OPEN INPUT MEMBFILE.
001640     OPEN OUTPUT OUTFILE.
001650     MOVE SPACES TO CONSOLE-SWITCH
001660                    CARDFILE-SWITCH
001670                    PARM-SWITCH.
001680     MOVE 'CARDLIB ' TO FILE-NAME.
001690     MOVE 'OI' TO IO-FUNC.
001700     MOVE 'P01569' TO MODULE-NAME.
001710     MOVE 'M' TO INTERFACE-USAGE.
001720     CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE PDS-CNTL.
001730     IF RET-CODE NOT EQUAL SPACES
001740         MOVE 'P01101' TO MODULE-NAME
001750         CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
001760             PDS-ERRMSG MESSAGE-LENGTH
001770         CALL 'P00655'.
001780     READ MEMBFILE INTO MEMBER-NAME
001790         AT END GO TO PARM-ROUTINE.
001800     MOVE '*' TO CARDFILE-SWITCH.
001810     PERFORM PROCESS-MEMBER THRU PROCESS-MEMBER-EXIT.
001820 GET-NEXT-CARD-MEMBER.
001830     READ MEMBFILE INTO MEMBER-NAME
001840         AT END GO TO PARM-ROUTINE.
001850     PERFORM PROCESS-MEMBER THRU PROCESS-MEMBER-EXIT.
001860     GO TO GET-NEXT-CARD-MEMBER.
001870 PARM-ROUTINE.
001880     IF PARM-LENGTH EQUAL ZERO
001890         GO TO CONSOLE-CHECK.
001900     MOVE '*' TO PARM-SWITCH.
001910     MOVE SPACES TO PARM-MEMBER.
001920     MOVE PARM-JOBS TO JOB-TABLE.
001930 PROCESS-PARM.
001940     ADD 1 TO I.
001950     IF I GREATER THAN PARM-LENGTH
001960         GO TO PROCESS-PARM-MEMB.
001970     IF PARM-BYTE (I) EQUAL ','
001980         GO TO PROCESS-PARM-MEMB.
001990     IF PARM-BYTE (I) EQUAL SPACE
002000         GO TO PROCESS-PARM.
002010     ADD 1 TO J.
002020     IF J GREATER THAN 8
002030         MOVE 'P01101' TO MODULE-NAME
002040         CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002050             PARM-ERRMSG MESSAGE-LENGTH
002060         CALL 'P00655'.
002070     MOVE PARM-BYTE (I) TO MEMB-BYTE (J).
002080     GO TO PROCESS-PARM.
002090 PROCESS-PARM-MEMB.
002100     IF J EQUAL ZERO
002110         GO TO PROCESS-PARM-END.
002120     MOVE PARM-MEMBER TO MEMBER-NAME.
002130     PERFORM PROCESS-MEMBER THRU PROCESS-MEMBER-EXIT.
002140     MOVE SPACES TO PARM-MEMBER.
002150     MOVE ZERO TO J.
002160 PROCESS-PARM-END.
002170     IF I GREATER THAN PARM-LENGTH
002180         GO TO EOJ.
002190     GO TO PROCESS-PARM.
002200 CONSOLE-CHECK.
002210     IF PARMS-USED OR MODE-IS-CARDS
002220         GO TO EOJ.
002230     MOVE '*' TO CONSOLE-SWITCH.
002240 CONSOLE-ROUTINE.
002250     MOVE 'P01101' TO MODULE-NAME.
002260     CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002270         NEWMEMB-MSG MESSAGE-LENGTH.
002280     MOVE 'P01102' TO MODULE-NAME.
002290     CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002300         MEMBER-NAME RETURN-LENGTH2.
002310     PERFORM PROCESS-MEMBER THRU PROCESS-MEMBER-EXIT.
002320     MOVE 'P01101' TO MODULE-NAME.
002330     CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002340         NEXTMEMB-MSG MESSAGE-LENGTH.
002350     MOVE 'P01102' TO MODULE-NAME.
002360     CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002370         CONTINUE-MESG RETURN-LENGTH1.
002380     IF REPLY-IS-YES
002390         GO TO CONSOLE-ROUTINE.
002400     GO TO EOJ.
002410 PROCESS-MEMBER.
002420     MOVE 'P50248' TO MODULE-NAME.
002430     MOVE SPACES TO F-RET.
002440     CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002450         F-CTL MEMBER-NAME.
002460     IF F-OK
002470       GO TO PROCESS-MEMBER-EXIT.
002480     MOVE 'P01569' TO MODULE-NAME.
002490     MOVE 'RR' TO IO-FUNC.
002500     CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE PDS-CNTL
002510         MEMBER-NAME WORK-CARD.
002520     IF NOT READ-OKAY
002530         MOVE MEMBER-NAME TO ERR-MEMB
002540         MOVE 'P01101' TO MODULE-NAME
002550         CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002560             NOMEMB-ERRMSG MESSAGE-LENGTH
002570         IF MODE-IS-CARDS OR PARMS-USED
002580             CALL 'P00655'
002590         ELSE
002600             GO TO PROCESS-MEMBER-EXIT.
002610 WRITE-CARD.
002620     WRITE OUT-CARD FROM WORK-CARD.
002630     CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE PDS-CNTL
002640         MEMBER-NAME WORK-CARD.
002650     MOVE WORK-CARD TO SAVE-CARD.
002660     IF READ-OKAY
002670         GO TO WRITE-CARD.
002680     IF NOT MEMBER-END
002690         MOVE MEMBER-NAME TO BAD-MEMB
002700         MOVE 'P01101' TO MODULE-NAME
002710         CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE
002720             BADMEMB-ERRMSG MESSAGE-LENGTH
002730         CALL 'P00655'.
002740     IF NOT EOF-CARD
002750         MOVE '/*EOF' TO WORK-CARD
002760         WRITE OUT-CARD FROM WORK-CARD.
002770 PROCESS-MEMBER-EXIT.  EXIT.
002780 EOJ.
002790     IF MODE-IS-CONSOLE
002800         MOVE 'P01820' TO MODULE-NAME
002810         CALL 'P01616' USING MODULE-NAME INTERFACE-USAGE JOB-AREA
002820         MOVE JOBNAME-LOADER TO MEMBER-NAME
002830         PERFORM PROCESS-MEMBER THRU PROCESS-MEMBER-EXIT.
002840     CLOSE MEMBFILE.
002850     CLOSE OUTFILE
002860     STOP RUN.
