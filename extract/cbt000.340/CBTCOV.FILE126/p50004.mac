*          DATA SET P50004     AT LEVEL 005 AS OF 03/27/81
*          DATA SET P50004     AT LEVEL 004 AS OF 03/26/81
*          DATA SET P50004     AT LEVEL 003 AS OF 03/26/81
         MACRO
         SMFWTO
         TS    *+1            FOR ONE TIME OPERATION (MLPA ONLY)
         BNZ   SMF&SYSNDX    SKIP WTO AFTER FIRST EXECUTION
         LOAD  EP=P50252
         ST    R0,TABADDR     SAVE ADDR OF JCL TABLE
         TIME
         ST    1,FULL&SYSNDX
         ED    EDIT&SYSNDX,FULL&SYSNDX+1
         MVC   W&SYSNDX+8(5),EDIT&SYSNDX+1
         WTO '******************************************************', X
               ROUTCDE=(9)
W&SYSNDX WTO '      &SYSECT ACTIVE - LAST COMPILE &SYSDATE &SYSTIME',  X
               ROUTCDE=(9)
         WTO '******************************************************', X
               ROUTCDE=(9)
         B     SMF&SYSNDX
FULL&SYSNDX DC F'0'
EDIT&SYSNDX DC X'402020202020'
TABADDR  DC    F'0'      SAVE AREA
         CNOP  0,4
SMF&SYSNDX EQU *
         MEND
         TITLE 'SMF IEFUJI REENTRANT EXIT MILLER BREWING COMPANY'
*
*
*        THIS IS SMF ACCOUNTING ROUTINE IEFUJI
*        IT OPERATES AS SVC NUMBER 232 FROM MODULE
*        IEFSD060.
*
*        THE ROUTINES BASIC FUNCTION IS TO VERIFY THE
*        VALIDITY OF USER IDEAS THAT ARE IN THE BATCH
*        JOBS ACCOUNTING FIELD.  E.G.  JOB (TS01,,,,,)
*        THIS IS DONE WITH THE RACINIT SVC. THE USER ID
*        IS USED TO DETERMINE IF THERE IS A PROFILE FOR THIS
*        USER IF THERE IS NOT THE JOB IS CANCELLED.
*
*  MODIFICATIONS:
*
*   T. FREDERICK 3/81
*
*        IN ADDITION THIS ROUTINE PERFORMS A RACHECK ON ANY RESTRICTED
*        JCL PARAMETERS. RESTRICTED PARAMETERS ARE DETERMINED IN THE
*        IEFUJV EXIT AND THE RESOURCE NAME TO BE CHECKED CORRESPONDS TO
*        A BIT POSITION IN THE USER COMMUNICATION FIELD OF THE SMF
*        COMMON EXIT PARAMETER LIST.  SEE P50252 FOR FORMAT OF THE
*        TABLE ENTRIES USED IN JCL PARAMETER RACF CHECKING.
*
*        THE PROGRAM WILL CURRENTLY AUDIT AND NOT FAIL VIOLATORS
*        OF THE RESTRICTED JCL PARAMETERS CHECKING.  THE PROGRAM,
*        HOWEVER, WILL FAIL JOBS SPECIFICALLY DENIED ACCES TO
*        JCL PARAMETERS.   SEE THE COMMENTED CHANGES BELOW.
*
*
*
R15      EQU   15
R14      EQU   14
R13      EQU   13
R12      EQU   12
R11      EQU   11
R10      EQU   10
R9       EQU    9
R8       EQU    8
R7       EQU    7
R6       EQU    6
R5       EQU    5
R4       EQU    4
R3       EQU    3
R2       EQU    2
R1       EQU    1
R0       EQU    0
JCT      EQU    4
D0       EQU   0
D4       EQU   4
D8       EQU   8
D20      EQU   20
D39      EQU   39
L4       EQU   4
L8       EQU   8
L61      EQU   61
L16      EQU   16
L69      EQU   69
L96      EQU   96
IEFUJI   CSECT
IGC0023B EQU   *                  SVC 232
         STM   R14,R12,12(R13)
         BALR  R11,0
         USING *,R11
         LR    R3,R7               SAVE POINTER TO ASCB
         LM    R6,R9,0(R1)         LOAD PARAMETER LIST
*
*        R2 - POINTS TO GETMAINED WORK AREA
*        R6 - POINTS TO SMF EXIT PARAMETER AREA ADDRESS
*        R7 - POINTS TO PROGRAMMER NAME FIELD ADDRESS
*        R8 - POINTS TO JOB PRIORITY BYTE ADDRESS
*        R9 - POINTS TO JOB ACCOUNTING INFORMATION ADDRESS
*
         SMFWTO
         GETMAIN R,LV=WORKSIZE         GET THE WTO MESSAGE AREA
         LR    R2,R1                   R2 IS THE WTO MESSAGE AREA
         USING WORKAREA,R2
         GTRACE DATA=(6),LNG=36,ID=2
         MVC   WTO,BASEWTO             MOVE BASIC WTO LIST INTO GETMAIN
         TM    29(R6),X'01'     CHECK IS TSO FOREGROUND USER
         BZ    BATCH                   MUST BE A BATCH TASK
*
*        MUST BE A TSO USER
*        USE JOB NAME FOR USER ID
*
         MVI   USERL,X'04'             ASSUME TSO USER IS 4
         MVC   USERID,0(R6)            JOBNAME IS USER ID FOR TSO
         B     RACINIT                 CHECK USER WITH RACINIT SVC
BATCH    EQU   *
         CLI   0(R9),X'00'             IS THERE ANY ACCOUTNING INFO ?
         BE    NOFLDS                  NOPE GO CANCEL
         CLI   1(R9),X'00'            IS THIS A ZERO LENGTH FIELD?
         BE    NOFLDS                 IF SO ACCT FIELD IS ABSENT
         CLI   1(R9),X'04'             IS USER ID 4 BYTES LONG
         BNE   LENGERR                 NOPE NOT LONG ENOUGH
         MVC   USERL(5),1(R9)          MOVE LENGTH AND USER ID TO WORK
*
*        WE HAVE A VALID BATCH/TSO USER ID
*        I THINK. . . . LETS VERIFY WITH RACF
*
        SPACE 3
RACINIT  EQU   *
         USING ASCB,R3
         L     R15,ASCBASXB         GET POINTER TO ASXB
         DROP  R3
         USING ASXB,R15
         L     R15,ASXBSENV         GET POINTER TO ACEE
         DROP  R15
         USING ACEE,R15
         MVC   SAVUSER,ACEEUSRI     SAVE THE USER ID FOR LATER
         CLI   ACEEUSRI,C'*'        IS USER ID = '*   '
         DROP  R15
         BNE   NOINIT               NO - RACINIT HAS ALREADY BEEN DONE
         LA    R4,USERL             ADDRESS USER FIELD
         MVC   RACINX(RACINL),RACINP MOVE IN PATTERN FOR RACINIT
         LA    R15,ACEEADDR        PLACE TO PUT ADDR OF ACEE
         RACINIT MF=(E,RACINX),ACEE=(R15),ENVIR=CREATE,USERID=(4),     X
               PASSCHK=NO
         LR    R4,R15               SAVE RETURN CODE
         LTR   R4,R4                PROFILE NOT FOUND ? ? ? ?
         BNZ   NOTFOUND           IS THERE ONE ? ? ? ?
         MVC   RACINX(RACINL),RACINP MOVE IN PATTERN FOR RACINIT
         LA    R14,ACEEADDR
         RACINIT MF=(E,RACINX),ENVIR=DELETE,ACEE=(R14)
NOINIT   SVC   229                  GET POINTER TO JCT
         LTR   R1,R1                DID WE FIND THE JCT
         BZ    NOJCT                YES - CONTINUE  NO - USE CEPA
         LR    R4,R1                SAVE JCT ADDR
         USING JCTDSECT,R4
         LA    R1,JCTUCOM           POINT TO USER COMM
         CLC   JCTID,=CL4'JCT'      IS THIS REALLY THE JCT?
         BNE   NOJCT                NO - USE THE CEPA
         GTRACE DATA=(1),LNG=4,ID=1
         ICM   R4,15,JCTUCOM        GET USER COMMUNICATION WORD
         DROP  R4
         BZ    ENDTAB               NO BITS ON
         B     CHKBITS             TO CHECK PARAMETERS  (RACHECK)
NOJCT    ICM   R4,15,32(R6)        IS THERE A BIT MAP
         BZ    ENDTAB              NO - GO DO RACINIT DELETE
CHKBITS  SR    R3,R3               CLEAR REGISTER
         ST    R3,32(R6)           ZERO USER COMMUNICATION WORD
         L     R3,TABADDR          GET ADDR JCL TABLE
         LTR   R3,R3               IS TABLE LOADED
         BNZ   GOTTABL             YES - CONTINUE
         LOAD  EP=P50252
         ST    R0,TABADDR          SAVE ADDR OF JCL TABLE
         LR    R3,R0
GOTTABL  L     R3,12(R3)           GET CLASS SUBTABLE ADDR
CHKLOOP  LTR   R4,R4               IS THE HIGH ORDER BIT ON?
         BM    GOTBIT              YES - DO RACHECK
LOOPENT  SLL   R4,1                TRY NEXT BIT
         LA    R3,8(R3)            POINT TO NEXT TABLE ENTRY
         CLC   0(4,R3),=X'00000000'  IS IT EMPTY
         BE    ENDTAB              YES - END OF TABLE
         B     CHKLOOP             CONTINUE CHECKING BITS
GOTBIT   MVC   RACNAME,0(R3)       SAVE CLASS NAME
         MVI   RACWORK,X'08'       ADD LENGTH
         CLC   RACNAME,=C'$PASWORD'  IS IT CLASS OF PASSWORD=
         BNE   DOCHECK             NO - DO RACHECK
         CLI   SAVUSER,C'*'        DID USER PASS RACINIT?
*  HERE WE HAVE DETERMINED THAT THE USER HAS ENTERED THE
*  PASSWORD= PARAMETER ON HIS JOB CARD, BUT A NULL PASSWORD
*  WAS USED.  TO FAIL THIS UNIDENTIFIED USER CODE:
*        BE    IEFUJI95            NO - FAIL HIM
*  TO ALLOW HIM TO CONTINUE CODE:
         BE    DOCHECK             NO - RECORD THE VIOLATION
         B     LOOPENT             CONTINUE CHECKING TABLE
DOCHECK  MVC   RACHKX(RACHKL),RACHKP MOVE IN PATTERN FOR RACHECK
         RACHECK MF=(E,RACHKX),ENTITY=((R6)),CLASS=RACWORK
         B     RACHRET(R15)        BRANCH ON RETURN CODE
RACHRET  B     LOOPENT             OKAY TO USE
*    FOR RETURN CODE 4 - ENTITY NOT DEFINED TO RACF
*    WE PRINT A MESSAGE INDICATING A RACF VIOLATION
*    AND CONTINUE PROCESSING.  TO FAIL USERS FOR USING
*    JCL PARAMETERS THEY ARE NOT SPECIFICALLY AUTHORIZED TO
*    USE, CODE:
*        B     IEFUJI95            RESOURCE NOT DEFINED - FAIL JOB
         B     RACNF               RESOURCE NOT DEFINED
         B     IEFUJI95            NOT PERMITTED
         B     IEFUJI95            UNKNOWN ERROR
ENDTAB   LR    R1,R2
         FREEMAIN R,A=(1),LV=WORKSIZE    FREE WORK AREA
         LM    R14,R12,12(R13)         RESTORE THE REGISTERS
         XR    R15,R15                 ZERO R15 TO INDICATE OKAY
         SVC   3              EXIT
         EJECT
RACNF    EQU   *
         MVC   D4(L61,R2),M8           MOVE MESSAGE FOR NOT FOUND
         MVC   24(L8,R2),0(R6)         MOVE JOB NAME TO MESSAGE
         MVC   40(L8,R2),RACWORK+1     MOVE CLASS NAME TO MESSAGE
         WTO   MF=(E,(R2))             WRITE MESSAGE
         B     LOOPENT                 CONTINUE CHECKING
NOFLDS   EQU   *
         MVC   D4(L61,R2),M2           MOVE MESSAGE FOR NO FIELDS
         MVC   D39(L8,R2),0(R6)        MOVE JOB NAME INTO MESSAGE
         B     WTOCANCL                GO TO WRITE IT
LENGERR  EQU   *
         MVC   D4(L61,R2),M3           MOVE MESSAGE FOR INVALID LENGTH
         MVC   D39(L8,R2),0(R6)        MOVE JOB NAME INTO MESSAGE
         B     WTOCANCL                GO TO WRITE IT
NOTFOUND EQU   *
         MVC   D4(L61,R2),M1           MOVE MESSAGE FOR USER NOT FOUND
         MVC   D39(L8,R2),0(R6)        MOVE JOBNAME INTO MESSAGE
         MVC   D20(L4,R2),2(R9)        MOVE USER ID INTO MESSAGE
         B     WTOCANCL                GO TO WRITE IT
WTOCANCL EQU   *                       GENERAL WTO AND EXIT COND 4
         WTO   MF=(E,(R2))
IEFUJI95 LR    R1,R2
         FREEMAIN R,A=(1),LV=WORKSIZE  FREE UP CONSOLE GETMAIN
         LM    R14,R12,12(R13)
         SPACE 3
*
*        SET CANCEL RETURN CODE B/4 EXIT
*
*              FOR TESTING CAN BE SET TO 0
*                             ERROR MESSAGE WILL BE ISSUED
*                             BUT USER WILL NOT BE CANCELLED
*              FOR PRODUCTION SET TO 4
         LA    R15,4                SET RETURN CODE TO ZERO FOR TESTING
*
*
         SPACE 3
         SVC   3              EXIT
         EJECT
*
         CNOP  0,4         ALIGNMENT
BASEWTO  EQU   *
         DC    AL2(65)                 LENGTH OF WHOLE MESS
         DC    B'1000000000000000'     MCS FLAGS
         DC    CL61' '                 DUMMY MESSAGE AREA
         DC    B'0000000000000000'     DESCRIPTOR CODES
         DC    B'0000000000100000'     ROUTING CODES
RACINP   RACINIT MF=L,ENVIR=CREATE
RACINL   EQU   *-RACINP            LENGTH OR RACINIT
RACHKP   RACHECK ATTR=READ,MF=L
RACHKL   EQU   *-RACHKP            LENGTH OF RACHECK
BASEEND  EQU   *
*       +INCLUDE P50011
*          DATA SET P50011     AT LEVEL 001 AS OF 05/25/78
M1 DC C'MBCSMF1-USER ID XXXX NOT FOUND JOB YYYYYYYY WILL BE CANCELLED'
M2 DC C'MBCSMF2-NO ACCOUNTING FIELDS   JOB YYYYYYYY WILL BE CANCELLED'
M3 DC C'MBCSMF3-USER ID NOT 4 POS LONG JOB YYYYYYYY WILL BE CANCELLED'
M4 DC C'MBCSMF4-OPEN FOR UADS FAILED-NOTIFY TECHNICAL SERVICES       '
M5 DC C'MBCSMF5-USER ID VALIDATION BYPASSED FOR TSO USER             '
M6 DC C'MBCSMF6-USER ID MISSING/WRONG JOB YYYYYYYY WILL BE CANCELLED '
M7 DC C'MBCSMF7-USERID DOES NOT MATCH JOB NAME ID                    '
M8 DC C'MBCSMF8-RACHECK JOB(XXXXXXXX) CLASS(XXXXXXXX) - NOT FOUND    '
         LTORG
WORKAREA DSECT
WTO      DS    CL69                 BASE WTO
USERL    DS    X                    USER LENGTH
USERID   DS    CL4                  USER ID
RACINX   RACINIT MF=L,ENVIR=CREATE
RACHKX   RACHECK MF=L,ATTR=READ
ACEEADDR DS    F
RACWORK  DS    CL1   LENGTH         WORK AREA FOR CLASS NAME
RACNAME  DS    CL8   CLASS NAME
SAVUSER  DS    CL4                  USER ID SAVE AREA
         CNOP  0,4
WORKSIZE EQU   *-WORKAREA
IEFUJI   CSECT
 TITLE '**** ASCB  DSECT'
         IHAASCB
IEFUJI   CSECT
 TITLE '**** ASXB  DSECT'
         IHAASXB
IEFUJI   CSECT
 TITLE '**** ACEE  DSECT'
         IHAACEE
IEFUJI   CSECT
 TITLE '**** JCT  DSECT'
         $BUFFER
         $JCT
IEFUJI   CSECT
         END   IEFUJI
