IKJEFTB2 CSECT
         TITLE 'IKJEFTB2 RACF/TSO COMMAND VALIDATION'
**********************************************************************
*                                                                    *
*  THIS ROUTINE REPLACES EXIT IKJEFTB2 PROVIDED BY PCF AND MVS. IT IS*
*  ENTERED EACH TIME A SYNTACTICALLY VALID COMMAND NAME HAS BEEN     *
*  IDENTIFIED IN A COMMAND BUFFER. THIS ROUTINE ASSUMES THAT PCF IS  *
*  INSTALLED AND WILL A FORCE A BYPASS OF PCF COMMAND ACCOUNTING     *
*                                                                    *
*  ON ENTRY-                                                         *
*    REGISTER 1 POINTS TO A TWO WORD LIST OF TMP WORKAREA ADDRESSES  *
*        TMPWRKA1                                                    *
*        TMPWRKA2                                                    *
*                                                                    *
*  ON RETURN TO THE TERMINAL MONITOR PROGRAM (IKJEFT02) --           *
*    REGISTER 15 CONTAINS                                            *
*        0 - COMMAND KNOWN AND AUTHORIZED, GO ATTACH DIRECTLY        *
*        8 - COMMAND NOT KNOWN OR                                    *
*            USER NOT AUTHORIZED, ASSUME CLIST                       *
*                                                                    *
**********************************************************************
         EJECT
         REGISTER
         SAVE (14,12)
         LR    R12,R15             ESTABLISH NEW BASE
         USING IKJEFTB2,R12
         LR    R5,R1               COPY PARM REG
         GETMAIN R,LV=WORKLEN,SP=1 GET WORK STORAGE
         MVC   0(WORKLEN,R1),WORKAREA
         ST    R13,4(R1)           CHAIN BACK
         ST    R1,8(R13)           CHAIN FORWARD
         LR    R13,R1              NEW SAVE AREA
         B     BEGIN
         DC    CL8'IKJEFTB2'
         DC    C'-'
         DC    CL8'&SYSDATE'
         DC    C'-'
         DC    CL8'&SYSTIME'
BEGIN    EQU   *
         USING WORKAREA,R13
         ESTAE DUMPIT
         USING TMPWRKA1,R6
         USING TMPWRKA2,R7
         LM    R6,R7,0(R5)         ESTABLISH ADDRESSIBILITY
         LA    R9,4                SET DEFAULT RETURN CODE
         L     R3,TPLPSCB          ADDR OF PSCB
         USING PSCB,R3
         ICM   R3,15,PSCBU         PCF ACTIVE? ? ?
         BZ    EXIT                NO--
         USING PCFCVT,R3
         L     R11,PCFCVT2A        UNPROTECTED PCFCVT
         USING PCFCVT2,R11
         MODESET EXTKEY=ZERO,SAVEKEY=(2)
         ST    R6,PCFTMPW          STORE
         ST    R7,PCFTMPW2
         MODESET KEYADDR=(2)
         MVI   PCFSWS,0            INDICATE NO SMF ACCOUNTING
         DROP  R11
         DROP  R3
         L     R11,X'10'            ADDR OF CVT
         USING CVT,R11
         L     R3,CVTTCBP          TCB POINTER
         L     R8,CVTRAC           ADDR OF RCVT
         USING RCVT,R8             ESTABLISH ADDRESSABILITY
         DROP  R11
         TM    RCVTSTAT,RCVTRNA    RACF NOT ACTIVE? ? ?
         BO    INACT               YES--
         TM    RCVTFLGS,RCVTROFF   RACF VARIED OFFLINE? ? ?
         BO    INACT               YES--
         L     R3,12(R3)           ADDR OF ASCB
         USING ASCB,R3
         L     R3,ASCBASXB         GET ADDR OF ASXB
         USING ASXB,R3
         L     R4,ASXBSENV         ADDR OF ACEE
         USING ACEE,R4             ESTABLISH ADDRESSABILITY
         TM    ACEEFLG1,ACEESPEC   USER SPECIAL? ? ?
         BNO   CMDCK010            NO, GO CHECK AUTHORIZATION
         LA    R9,0                YES, LET HIM EXECUTE THE COMMAND
         B     EXIT                GET OUT
CMDCK010 EQU   *
         DROP  R3
         USING CSPL,R3
         L     R3,SRPLPTR
         L     R3,CSPLOA           COMMAND SCAN OUTPUT AREA
         DROP  R3
         USING CSOA,R3
         LA    R9,0                SET DEFAULT RETURN CODE
         ICM   R5,15,CSOACNM       GET ADDR OF COMMAND NAME
         BZ    EXIT                GET OUT IF NO NAME
         MVC   RCHKCMD,=8X'40'     BLANK OUT COMMAND WORK AREA
         LH    R2,CSOALNM          GET COMMAND LENGTH
         BCTR  R2,0                REDUCE FOR EXECUTE
         EX    R2,MOVCMD           MOVE IT INTO WORK AREA
         RACHECK ENTITY=RCHKCMD,MF=(E,RCHKPAT)
         LTR   R15,R15             CHECK RETURN
         BZ    EXIT                GOOD--
         LA    R9,8                BAD
EXIT     L     R3,4(13)            GET ADDR OF OLD SAVE
         ST    R9,WTOL+8
         WTO   MF=(E,WTOL)
         FREEMAIN R,LV=WORKLEN,A=(R13),SP=1
         LR    R13,R3              RESTORE OLD SAVE AREA ADDR
         LR    R15,R9              SET RETURN CODE
         RETURN (14,12),RC=(15)
INACT    WTO 'RACF NOT ACTIVE - NO ACCESS TO TSO',ROUTCDE=(9,11)
         LA    R9,16
         B     EXIT
DUMPADD  DC    X'11'
         DC    C'RACF/TSO COMMANDS'
         LTORG
         EJECT
         TITLE 'IKJEFTA1 CONSTANT SECTION'
WORKAREA DS    9D
RCHKPAT  RACHECK CLASS='$AAMISC',ATTR=READ,MF=L
RCHKLEN  EQU   *-RCHKPAT
RCHKCMD  DS    CL8
MOVCMD   MVC   RCHKCMD(0),0(R5)
SDUMPL   SDUMP HDR='RACF/TSO COMMANDS',                                X
               SDATA=(PSA,CSA,NUC,RGN,SQA,LPA,SUM),MF=L
DUMPLEN  EQU *-SDUMPL
WTOL     WTO   'RC =XXXX',MF=L
WORKLEN  EQU   *-WORKAREA
         EJECT
DUMPIT   SAVE  (14,12)
         DROP  R12
         LR    R3,R1
         LR    R12,R15
         USING DUMPIT,R12
         GETMAIN R,LV=DUMPLEN
         LR    R4,R1
         DROP  R13
         L     R10,=A(WORKAREA)
         USING WORKAREA,R10
         MVC   0(DUMPLEN,R4),SDUMPL
         MODESET EXTKEY=ZERO,SAVEKEY=(2)
         SDUMP MF=(E,(R4))
         MODESET KEYADDR=(2)
         FREEMAIN R,LV=DUMPLEN,A=(R4)
         SETRP DUMP=YES,RC=0,REGS=(14,12),WKAREA=(R3)
         EJECT
         PRINT NOGEN
         IHASDWA
         CVT   DSECT=YES
         PCFCVT
         ICHPRCVT
         IHAACEE
  COPY   ICHPISP
         IHAASCB
         IHAASXB
         IKJCSPL
         IKJCSOA
         IKJTMPWA
         IKJPSCB
         IKJECT
         END
