*          DATA SET P01110     AT LEVEL 001 AS OF 01/27/81
P01110   START 0
*
*        READ A JOB FILE CONTROL BLOCK FOR A SPECIFIED DD CARD.
*              A STANDARD RDJFCB MACRO WILL BE USED.  THE JFCB
*              AND A RETURN CODE WILL BE RETURNED TO THE CALLER.
*
*        USAGE -- CALL P01110 (DDNAME,JFCB,RCODE)
*                   WHERE
*                       DDNAME - 8 BYTE AREA CONTAINING THE DDNAME
*                                 OF THE DESIRED JFCB
*                        JFCB   - 176 BYTE AREA TO WHICH THE JFCB
*                                 WILL BE RETURNED
*                        RCODE  - 4 BYTE BINARY RETURN CODE
*                                       0 - JFCB FOUND
*                                       4 - JFCB NOT FOUND
*                                 IN THE EVENT OF JFCB NOT FOUND,
*                                 THE JFCB WILL BE BINARY ZEROS.
*
         EJECT
*
*        REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
*
*        ESTABLISH STANDARD LINKAGE
*
         SAVE  (14,12),,*
         BALR  R2,R0          R2 - BASE REGISTER
         USING *,R2
         LA    R3,SAVEAREA    CHAIN SAVE AREAS
         ST    R13,4(R3)
         ST    R3,8(R13)
         LR    R13,R3
         ST    R1,SAVE1
         EJECT
*
*        GET THE DDNAME
*
         L     R3,SAVE1
         L     R4,0(R3)       R4 - ADDR OF DDNAME
         MVC   DDNAME(8),0(R4)
         CLC   DDNAME(8),=CL8' '   IS IT BLANK ?
         BE    RETN4               YES
         LA    R5,DDDCB            MOVE DDNAME INTO DCB
         USING IHADCB,R5
         MVC   DCBDDNAM(8),DDNAME
         DROP  R5
         EJECT
*
*        ISSUE THE READ JFCB
*
         RDJFCB (DDDCB)
         LTR   R15,R15             TEST THE RETURN CODE
         BZ    RETNJFCB
RETN4    XC    JFCBAREA(176),JFCBAREA   SET THE JFCB TO BINARY ZEROS
         L     R15,=F'4'
RETNJFCB L     R3,SAVE1
         L     R4,4(R3)       R4 - ADDR OF CALLER JFCB AREA
         MVC   0(176,R4),JFCBAREA       RETURN THE JFCB
         L     R4,8(R3)       R4 - ADDR OF CALLER RETURN CODE
         ST    R15,0(R4)                RETURN THE RETURN CODE
         EJECT
*
*        RETURN TO THE CALLER
*
         SR    R15,R15
         L     R13,SAVEAREA+4
         RETURN (14,12),,RC=(15)
         EJECT
*
*        MISCELLANEOUS STUFF
*
         DS    0F
SAVEAREA DC    72F'0'
SAVE1    DC    F'0'
DDNAME   DC    CL8' '
         LTORG
         EJECT
*
*        DCB FOR READ JFCB
*
DDDCB    DCB  DSORG=PS,MACRF=GM,EXLST=DCBLST
         EJECT
*
*        DCB EXIT LIST FOR READ JFCB
*
         DS    0F
DCBLST   EQU   *
         DC    X'07'
         DC    AL3(JFCBAREA)
         DC    X'80'
         DC    AL3(0)
JFCBAREA DS    CL176
         EJECT
*
*        DCB   DSECT
*
         DCBD  DSORG=PS,DEVD=DA
         END   P01110
