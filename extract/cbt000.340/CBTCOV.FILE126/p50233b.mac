*          DATA SET P50233B    AT LEVEL 006 AS OF 06/02/81
*          DATA SET P50233B    AT LEVEL 004 AS OF 02/17/81
*          DATA SET P50233B    AT LEVEL 003 AS OF 02/17/81
*          DATA SET P50233B    AT LEVEL 002 AS OF 02/01/81
*          DATA SET P50233B    AT LEVEL 001 AS OF 01/26/81
P50233B  CSECT
 TITLE '***** WTO MESSAGES FOR THE MBRDR       &SYSDATE &SYSTIME ****'
         SPACE 10
*
*
*        THIS MODULE ISSUES ALL WTO MESSAGES FOR THE MBRDR
*
*             CALL P50233B,(MESSAGE,MEMBER,LIBRARY,USER,JOBNAME),VL
*
*                   MESSAGE = 1 BYTE MESSAGE NUMBER (HEX)
*                   MEMBER  = MEMBER NAME YOU WANT INSERTED
*                             IN MESSAGE. LENGTH OF 8.
*                   LIBRARY = LIBRARY NAME YOU WANT INSERTED
*                             IN MESSAGE.  LENGTH OF 11.
*                   USER    = USER ID YOU WANT INSERTED IN
*                             MESSAGE.
*                   JOBNAME = NAME OF JOB NAME SUBMITTING THE
*                             REQUEST TO MBRDR
*
*
*
         EJECT
         PRIME
         REGISTER
         LR    R2,R1
         USING PARMS,R2
         L     R3,MSGNO            OBTAIN MESSAGE NUMBER
         SLL   R3,2                FOR BRANCH TABLE
         L     R4,MSGTABLE(R3)     MESSAGE TO ISSUE
         USING MESSAGE,R4          ADDRESSIBILITY TO MESSAGE TABLE
         LA    R3,WTOL             ADDR OF MESSAGE WORK AREA
         USING WTOD,R3             WTO DSECT
         MVI   WTOMSG,C' '         CLEAR MESSAGE AREA
         MVC   WTOMSG+1(L'WTOMSG-1),WTOMSG
         MVC   WTODESC,DESCMSG     MOVE DESCRIPTOR
         MVC   WTOROUT,ROUTMSG     MOVE ROUTING CODE
         LA    R5,WTOMSG           POINT TO MESSAGE OUTPUT AREA
         MVC   0(5,R5),=C'MBRDR'   MBRDR
         LA    R5,5(R5)            INCREMENT POINTER
         MVC   0(3,R5),NUMBMSG     MBRDR000
         LA    R5,5(R5)            INCREMENT POINTER
         TM    MSGNO,X'80'         TEST MESSAGE NUMBER
         BO    MOVCAPT             MOVE CAPTION
         CLC   LIBRARY,=F'0'       ANYTHING THERE?
         BE    MOVMEMB             NO NEXT PARAMETER
         L     R6,LIBRARY          LOAD ADDR DSN
         MVC   0(11,R5),0(R6)      MBRDR000  MB98.F01640
         LA    R5,11(R5)           INCR
MOVMEMB  TM    LIBRARY,X'80'       TEST ADDR
         BO    MOVCAPT             MOVE CAPTION
         CLC   MEMBER,=F'0'        ANYTHING THERE?
         BE    MOVUSER             NEXT ARGUMENT
         L     R6,MEMBER           GET ADDR MEMBER
         MVI   0(R5),C'('
         LA    R5,1(R5)            INCREMENT
         LA    R7,8                SET MAX
NXMEMB   MVC   0(1,R5),0(R6)       MOVE TO MESSAGE
         CLI   0(R6),C' '          CHECK FOR END
         BNE   NOBLANK             CONTINUE
         MVI   0(R5),C')'
         LA    R5,1(R5)            INCREMENT
         B     MOVUSER
NOBLANK  LA    R5,1(R5)            INCREMENT
         LA    R6,1(R6)
         BCT   R7,NXMEMB
         MVI   0(R5),C')'         MBRDR000  MB98.F01640(JOBNAME)
         LA    R5,1(R5)
MOVUSER  TM    MEMBER,X'80'        WAS MEMBER PASSED?
         BO    MOVCAPT             NO - MOVE CAPTION
         CLC   USERID,=F'0'        IS IT THERE
         BE    MOVNAME             NO - NEXT ARGUMENT
         LA    R5,4(R5)            GIVE IT SOME SPACE
         L     R6,USERID           GET ADDR USER ID
         MVC   0(4,R5),0(R6)       MOVE USER ID TO MSG
         LA    R5,4(R5)            INCREMENT
MOVNAME  TM    USERID,X'80'        ANY MORE
         BO    MOVCAPT             NO - MOVE CAPTION
         CLC   JOBNAME,=F'0'       IS IT THERE?
         BE    MOVCAPT             NO - NEXT ARGUMENT
         L     R6,JOBNAME          GET ADDR JOBNAME
         LA    R5,2(R5)            GIVE IT SOME SPACE
         MVC   0(8,R5),0(R6)       MOVE JOB NAME TO MESSAGE
         LA    R5,8(R5)            INCREMENT
MOVCAPT  LA    R5,2(R5)            GIVE IT SOME SPACE
         LA    R6,WTODESC          GET END OF WTO BUFFER
         SR    R6,R5               GET LENGTH REMAINING IN BUFFER
         SH    R6,LENGMSG          WILL THE MESSAGE FIT?
         BM    CHGLEN              NO - FIX IT
         LH    R6,LENGMSG          GET LENGTH OF CAPTION
CONTCAP  BCTR  R6,R0               DECREMENT LENGTH FOR MOVE
         EX    R6,MVCWTO           MOVE CAPTION TO MESSAGE
         WTO   MF=(E,WTOL)         WRITE THE MESSAGE
         TERME                     RETURN TO CALLER
CHGLEN   LA    R6,WTODESC          CALCULATE REMAINING SPACE IN BUFFER
         SR    R6,R5               AND USE ONLY WHAT IS LEFT
         B     CONTCAP
         LTORG
 TITLE '***** CONSTANTS AND WORK AREAS         &SYSDATE &SYSTIME ****'
MVCWTO   MVC   0(0,R5),CAPTMSG      MOVE CAPTION TO WTO BUFFER
WTOL     WTO    '******************************************************X
               **********************************************',ROUTCDE=X
               1,MF=L
         SPACE 5
         DS    0F
MSGTABLE EQU   *
         DC    A(MSG1)             DUMMY FOR MH INSTRUCTION
         DC    A(MSG1)
         DC    A(MSG2)
         DC    A(MSG3)
         DC    A(MSG4)
         DC    A(MSG5)
         DC    A(MSG6)
         DC    A(MSG7)
         DC    A(MSG8)
         DC    A(MSG9)
         DC    A(MSG10)
         DC    A(MSG11)
         DC    A(MSG12)
         DC    A(MSG13)
         DC    A(MSG14)
         DC    A(MSG15)
 TITLE '***** MESSAGES                         &SYSDATE &SYSTIME ****'
MSG1     DS    0F
         DC    H'38'
         DC    X'4000'
         DC    X'8000'
         DC    C'014'
         DC    C'SECURITY READER MUST BE A STARTED TASK'
MSG2     DS    0F
         DC    H'35'
         DC    X'4000'
         DC    X'8000'
         DC    C'015'
         DC    C'RECORD LENGTH MUST BE 80 CHARACTERS'
MSG3     DS    0F
         DC    H'21'
         DC    X'0000'
         DC    X'4000'
         DC    C'016'
         DC    C'STOP COMMAND ACCEPTED'
MSG4     DS    0F
         DC    H'29'
         DC    X'4000'
         DC    X'8000'
         DC    C'017'
         DC    C'JOB NOT FOUND - NOT SUBMITTED'
MSG5     DS    0F
         DC    H'45'
         DC    X'4000'
         DC    X'8000'
         DC    C'018'
         DC    C'NO JOB NAME WAS REQUESTED - NOTHING SUBMITTED'
MSG6     DS    0F
         DC    H'22'
         DC    X'0000'
         DC    X'4000'
         DC    C'019'
         DC    C'JOB HAS BEEN SUBMITTED'
MSG7     DS    0F
         DC    H'25'
         DC    X'4000'
         DC    X'8000'
         DC    C'020'
         DC    C'DYNAMIC ALLOCATION FAILED'
MSG8     DS    0F
         DC    H'18'
         DC    X'4000'
         DC    X'8000'
         DC    C'021'
         DC    C'FREE DSNAME FAILED'
MSG9     DS    0F
         DC    H'32'
         DC    X'4000'
         DC    X'8000'
         DC    C'022'
         DC    C'INVALID JOB CARD - NOT SUBMITTED'
MSG10    DS    0F
         DC    H'33'
         DC    X'4000'
         DC    X'8000'
         DC    C'023'
         DC    C'USER NOT AUTHORIZED TO SUBMIT JOB'
MSG11    DS    0F
         DC    H'40'
         DC    X'4000'
         DC    X'8000'
         DC    C'024'
         DC    C'ABEND CONTINUING  - PLEASE RESTART MBRDR'
MSG12    DS    0F
         DC    H'34'
         DC    X'4000'
         DC    X'8000'
         DC    C'025'
         DC    C'ABEND '
         ENTRY M12EXTRN
M12EXTRN DC    C'XXX - MBRDR BEING REINSTATED'
MSG13    DS    0F
         DC    H'35'
         DC    X'4000'
         DC    X'8000'
         DC    C'034'
         DC    C'ONLY ONE JOB PER PDS MEMBER ALLOWED'
MSG14    DS    0F
         DC    H'35'
         DC    X'4000'
         DC    X'8000'
         DC    C'035'
         DC    C'TWO MBRDR TASKS ACTIVE - ENQ FAILED'
MSG15    DS    0F
         DC    H'47'
         DC    X'4000'
         DC    X'8000'
         DC    C'039'
         DC    C'PASSWORD= SPECIFIED AND JOB NOT DEFINED TO RACF'
 TITLE '***** DSECTS                           &SYSDATE &SYSTIME ****'
PARMS    DSECT       CALLING PARAMETERS
MSGNO    DS    F       MESSAGE NUMBER
MEMBER   DS    F       MEMBER NAME TO BE SUBMITTED
LIBRARY  DS    F       LIBRARY NAME WHERE MEMBER RESIDES
USERID   DS    F       USER ID OF SUBMITTER
JOBNAME  DS    F       WHICH JOB IS SUBMITTING
         SPACE 3
MESSAGE  DSECT       MESSAGE FORMATS IN TABLE
LENGMSG  DS    CL2     LENGTH OF CAPTION
DESCMSG  DS    CL2     DESCRIPTOR FOR WTO
ROUTMSG  DS    CL2     ROUTING CODES FOR MESSAGE
NUMBMSG  DS    CL3     MESSAGE NUMBER  MBRDRXXX
CAPTMSG  DS    0C      MESSAGE CAPTION
         SPACE 3
WTOD     DSECT       WTO MACRO EXPANSION
WTOLEN   DS    H       LENGTH OF DATA FOR WRITE
WTOFLAG  DS    H       FLAGS
WTOMSG   DS    CL100   TEXT TO BE WRITTEN
WTODESC  DS    H       DESCRIPTORS TO BE USED
WTOROUT  DS    H       ROUTING CODE FOR MESSAGE
         END
