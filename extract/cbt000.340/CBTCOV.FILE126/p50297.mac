*          DATA SET P50297     AT LEVEL 006 AS OF 06/01/81
*          DATA SET P50297     AT LEVEL 005 AS OF 06/01/81
*          DATA SET P50297     AT LEVEL 004 AS OF 05/29/81
*          DATA SET P50297     AT LEVEL 003 AS OF 05/28/81
*          DATA SET P50297     AT LEVEL 002 AS OF 05/19/81
*          DATA SET P50297     AT LEVEL 001 AS OF 05/18/81
 PUNCH ' INCLUDE ISP(ISPLINK) '
 PUNCH ' INCLUDE SYSLMOD(P50215) '
 PUNCH ' ENTRY P50297 '
P50297   CSECT
 TITLE '**** SPF/RACF LIST USER COMMAND             &SYSDATE &SYSTIME'
*
*
*
*
*   P50297 LIST PERTINENT RACF USER DATA
*
*
*
*
         EJECT
         PRIME
         REGISTER
         L     R1,0(R1)
         ST    R1,USERADDR
         MVC   USERID(4),0(R1)     SAVE USER ID
         EXTRACT TIOT,'S',FIELDS=TIOT
         L     R2,TIOT
         MVC   WHO,0(R2)           SAVE WHO YOU ARE
         GETMAIN R,LV=400
         ST    R1,WORK2
         LA    R2,=A(400)
         ST    R2,0(R1)            LENGTH OF WORK AREA
         SPACE 3
*
*        DEFINE PROGRAM VARIABLES TO DIALOG MANAGER
*
         CALL  ISPLINK,(VDEF,MSGE,MESSAGE,CHAR,LNG21),VL
         LTR   R15,R15
         BZ    DEFNAMEX
         ABEND 10,DUMP
MSGE     DC    C'(MESSAGE)'
LNG21    DC    F'21'
DEFNAMEX EQU   *
         CALL  ISPLINK,(VDEF,NAMES,MGROUP,CHAR,LNG),VL
         LTR   R15,R15
         BZ    DEFNAME
         ABEND 10,DUMP
VDEF     DC    CL8'VDEFINE'
NAMES    DC    C'(GROUPL OWNER GRP1 GRP2 GRP3 GRP4 GRP5 CON WHOX)'
CHAR     DC    CL8'CHAR'
LNG      DC    F'4'
DEFNAME  EQU   *
         CALL  ISPLINK,(VDEF,NAMEL,NAME,CHAR,LNG20),VL
         LTR   R15,R15
         BZ    DEFDATES
         ABEND 10,DUMP
NAMEL    DC    C'(NAME ONAME)'
LNG20    DC    F'20'
DEFDATES EQU   *
         CALL  ISPLINK,(VDEF,DEFINEL,DEFINED,CHAR,LNG12),VL
         LTR   R15,R15
         BZ    DEFATTR
         ABEND 10,DUMP
DEFINEL  DC    C'(DEFINED LDATE LCHNG)'
LNG12    DC    F'12'
DEFATTR  EQU   *
         CALL  ISPLINK,(VDEF,ATTRL,ATTR1,CHAR,LNG10),VL
         LTR   R15,R15
         BZ    DEFINT
         ABEND 10,DUMP
ATTRL    DC    C'(ATTR1 ATTR2 ATTR3 ATTR4 ATTR5 ATTR6)'
LNG10    DC    F'10'
         SPACE 3
DEFINT   EQU   *
         CALL  ISPLINK,(VDEF,INTRL,DAY,CHAR,LNG3),VL
         LTR   R15,R15
         BZ    RACFINQ
         ABEND 10,DUMP
INTRL    DC    C'(DAY)'
LNG3     DC    F'3'
*
*        OBTAIN RACF INFORMATION ABOUT THIS USER
*
RACFINQ  EQU   *
         LA    R1,AUTH
         SVC   231
         ICHEINTY LOCATE,MF=(E,LOCATE),ENTRY=USERL
         LTR   R15,R15
         BZ    DISPL
         CH    R15,=H'12'          NOT FOUND ? ? ? ?
         BE    DISPLNF
         ABEND 91,DUMP
DISPLNF  EQU   *
         CALL  ISPLINK,(DISPLAY,USER,R2971,CURSOR),VL
         CH    R15,=H'8'           PF3
         BE    RETURN
         LTR   R15,R15
         BZ    USERCHNG
         ABEND 51,DUMP
R2971    DC    CL8'RL971A'
         DS    0H
DISPL    EQU   *
         SR    R1,R1
         SVC   231
         BAL   R10,CANDO
         MVC   WHOX,WHO            FOR DISPLAY
         LA    R2,WORK+X'1C'       LENGTH OF FIRST FIELD (GROUP)
         LH    R3,0(R2)
         BCTR  R3,0
         EX    R3,MVCGRP
         LA    R2,3(R3,R2)         NEXT FIELD (OWNER)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCOWNER
         LA    R2,3(R3,R2)         NEXT FIELD(NAME)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCNAME
         LA    R2,3(R3,R2)         NEXT FIELD(DEFINED)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCDEF
         CALL  P50215,(DEFPACK,DEFINED),VL
         LA    R2,3(R3,R2)         NEXT FIELD(LAST USED)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCDEF
         CALL  P50215,(DEFPACK,LDATE),VL
         LA    R2,3(R3,R2)         NEXT FIELD(LCHNG)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCDEF
         CALL  P50215,(DEFPACK,LCHNG),VL
         LA    R2,3(R3,R2)         NEXT FIELD(PASSINT)
         SR    R15,R15
         IC    R15,2(R2)           PASSWORD INTERVAL
         CVD   R15,DBLWRD
         MVC   DAYE(4),=X'40202020'
         ED    DAYE,DBLWRD+6
         MVC   DAY,DAYE+1
         LA    R2,3(R2)            NEXT FIELD(ATTR1)
         LA    R5,ATTR1            FIRST ATTRIBUTE
         MVI   ATTR1,C' '          CLEAR
         MVC   ATTR1+1(59),ATTR1
         TM    2(R2),X'80'         ADSP ? ? ? ?
         BNO   NATTR2
         MVC   0(10,R5),=CL10'ADSP'
         LA    R5,10(R5)           NEXT ATTRIBUTE
NATTR2   EQU   *
         LA    R2,3(R2)            ATTR2
         TM    2(R2),X'80'         SPECIAL ? ? ? ?
         BNO   NATTR3
         MVC   0(10,R5),=CL10'SPECIAL'
         LA    R5,10(R5)
NATTR3   EQU   *
         LA    R2,3(R2)            ATTR 3
         TM    2(R2),X'80'         OPERATIONS
         BNO   NATTR4
         MVC   0(10,R5),=CL10'OPERATIONS'
         LA    R5,10(R5)
NATTR4   EQU   *
         LA    R2,3(R2)            ATTR4
         TM    2(R2),X'80'         REVOKE ? ? ? ?
         BNO   NATTR5
         MVC   0(10,R5),=CL10'REVOKED'
         LA    R5,10(R5)
NATTR5   EQU   *
         LA    R2,3(R2)            ATTR5
         TM    2(R2),X'80'         GRPACC ? ? ? ?
         BNO   NATTR6
         MVC   0(10,R5),=CL10'GRPACC'
         LA    R5,10(R5)
NATTR6   EQU   *
         LA    R2,3(R2)            ATTR 6
         TM    2(R2),X'80'         AUDITOR ? ? ? ?
         BNO   NUMCONS
         MVC   0(10,R5),=CL10'AUDITOR'
NUMCONS  EQU   *
         LA    R2,3(R2)            NEXT FIELD(NUMBER OF CONNECTS)
         LH    R6,2(R2)            NUMBER OF CONNECTS
         LTR   R6,R6               ANY CONNECTS ? ? ? ?
         BZ    OWNAME              NO
         CVD   R6,DBLWRD
         MVC   DAYE,=X'40202020'
         ED    DAYE,DBLWRD+6
         MVC   CON(3),DAYE+1
         LA    R2,4(R2)            FIRST CONNECT
         LA    R5,GRP1             FIRST DISPLAY
         CH    R6,=H'6'            LIMIT TO 5
         BL    OK5
         LA    R6,5
OK5      EQU   *
         LH    R3,0(R2)            CONNECT LENGTH
         MVC   0(4,R5),2(R2)
         LA    R5,4(R5)            NEXT DISPLAY
         LA    R2,8(R2)         NEXT CONNECT
         BCT   R6,OK5
OWNAME   EQU   *
         MVC   OWNERL+1(4),MOWNER
         LA    R1,AUTH
         SVC   231
         L     R2,WORK2
         ICHEINTY MF=(E,LOCATEO),ENTRY=OWNERL,WKAREA=(R2)
         LTR   R15,R15
         BZ    MOVEONM
         CH    R15,=H'12'          NOT FOUND ? ? ? ?
         BE    DISPLNFO
         ABEND 92,DUMP
DISPLNFO EQU   *
         CALL  ISPLINK,(DISPLAY,USER,R2972,CURSOR),VL
         CH    R15,=H'8'           PF3
         BE    RETURN
         LTR   R15,R15
         BZ    USERCHNG
         ABEND 52,DUMP
R2972    DC    CL8'RL971B'
MOVEONM  EQU   *
         SR    R1,R1
         SVC   231
         L     R2,WORK2
         LA    R2,X'1C'(R2)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         EX    R3,MVCONAME
DISP     EQU   *
         CALL  ISPLINK,(DISPLAY,USER,R2974,CURSOR),VL
         CH    R15,=H'8'           PF3 ? ? ? ?
         BE    RETURN
         LTR   R15,R15
         BZ    USERCHNG
         ABEND 53,DUMP
CURSOR   DC    CL8'USERID'
DISPLAY  DC    CL8'DISPLAY'
USER     DC    CL8'RACFUSER'       MENU NAME
R2974    DC    CL8'RL971D'
USERCHNG EQU   *
         MVI   MGROUP,C' '         CLEAR OUTPUT AREAS
         MVC   MGROUP+1(LDATA-1),MGROUP
         L     R15,USERADDR
         CLC   USERID(4),0(R15)    USERID THE SAME AS B/4 ? ? ? ?
         BE    GETNEXT             YES - ISSUE GET NEXT
         MVC   USERID(4),0(R15)
         B     RACFINQ
GETNEXT  EQU   *
         LA    R1,AUTH
         SVC   231
         ICHEINTY NEXT,MF=(E,LOCATE),ENTRY=USERL
         LTR   R15,R15
         BZ    CHGUSER
         CH    R15,=H'12'          NOT FOUND ? ? ? ?
         BE    DISPLNF
         ABEND 91,DUMP
CHGUSER  EQU   *
         L     R15,USERADDR
         MVC   0(4,R15),USERID     DISPLAY NEW USER ID
         B     DISPL
RETURN   EQU   *
         SR    R1,R1               INSURE NOT AUTHORIZED ON EXIT
         SVC   231
         CALL  ISPLINK,(VDELETE,VARIAB),VL
         LTR   R15,R15
         BZ    TERME
         ABEND 99,DUMP
VDELETE  DC    CL8'VDELETE'
VARIAB   DC    C'(GROUPL OWNER NAME ONAME DEFINED LDATE LCHNG ATTR1 ATTX
               R2 ATTR3 ATTR4 ATTR5 ATTR6 DAY GRP1 GRP2 GRP3 GRP4 GRP5 X
               CON WHOX MESSAGE)'
         DS    0H
TERME    EQU   *
         L     R1,WORK2
         FREEMAIN R,LV=400,A=(1)
         TERME
         SPACE 3
*
*        CHECK USERS AUTHORITY
*
CANDO    EQU   *
         MVI   MESSAGE,C' '
         MVC   MESSAGE+1(20),MESSAGE
         LA    R2,WORK+X'1C'       LENGTH OF FIRST FIELD (GROUP)
         LH    R3,0(R2)
         LA    R2,2(R3,R2)         NEXT FIELD (OWNER)
         LH    R3,0(R2)            LENGTH
         BCTR  R3,0
         MVC   MESSAGE(17),=C'OWNS THIS PROFILE'
         EX    R3,CLCOWNER         MUST BE OWNER
         BE    RET10
         MVI   MESSAGE,C' '
         MVC   MESSAGE+1(20),MESSAGE
         MVC   MESSAGE(6),=C'IS YOU'
         L     R2,USERADDR             OR
         MVC   USERID(4),0(R2)
         CLC   WHO,USERID          MUST BE YOURS
         BE    RET10
         LA    R1,AUTH                 OR
         SVC   231
         L     R2,WORK2              SPECIAL
         ICHEINTY MF=(E,LOCATEO),ENTRY=WHOL,WKAREA=(R2)
         LTR   R15,R15
         BZ    CHKAUTH
         ABEND 93,DUMP
CHKAUTH  EQU   *
         SR    R1,R1
         SVC   231
         MVI   MESSAGE,C' '
         MVC   MESSAGE+1(20),MESSAGE
         MVC   MESSAGE(21),=C'HAS THE SPECIAL ATTR'
         L     R2,WORK2
         LA    R2,X'1C'(R2)
         LH    R3,0(R2)            LENGTH OF PGRMNAME
         LA    R2,2(R3,R2)         NEXT FIELD(SPECIAL)
         TM    2(R2),X'80'         SPECIAL ? ? ? ?
         BO    RET10               OK
NOTAUTH  EQU   *
         CALL  ISPLINK,(DISPLAY,USER,R2973,CURSOR),VL
         CH    R15,=H'8'           PF3
         BE    RETURN
         LTR   R15,R15
         BZ    USERCHNG
         ABEND 52,DUMP
R2973    DC    CL8'RL971C'
RET10    EQU   *
         BR    R10
         EJECT
WORK2    DC    F'0'
TIOT     DC    F'0'
WHOL     DC    X'04'
WHO      DC    CL8' '
MVCCON   MVC   0(0,R5),2(R2)
MVCGRP   MVC   MGROUP(0),2(R2)
MVCOWNER MVC   MOWNER(0),2(R2)
CLCOWNER CLC   WHO(0),2(R2)
MVCNAME  MVC   NAME(0),2(R2)
MVCONAME MVC   ONAME(0),2(R2)
MVCDEF   MVC   DEFPACK+1(0),2(R2)
DBLWRD   DC    D'0'
DAYE     DC    X'40202020'
USERADDR DC    F'0'
AUTH     DC    C'AUTH'
OWNERL   DC    X'04'
         DC    CL4' '
USERL    DC    X'04'               USER LENGTH
USERID   DC    CL8' '
MGROUP   DC    CL4' '
MOWNER   DC    CL4' '
GRP1     DC    CL4' '
GRP2     DC    CL4' '
GRP3     DC    CL4' '
GRP4     DC    CL4' '
GRP5     DC    CL4' '
CON      DC    CL4' '
WHOX     DC    CL4' '
NAME     DC    CL20' '
ONAME    DC    CL20' '
DEFINED  DC    CL12' '             DATE DEFINED
LDATE    DC    CL12' '             DATE LAST USED
LCHNG    DC    CL12' '             DATE LAST CHANGED
ATTR1    DC    CL10' '             ATTRIBUTES
         DC    CL10' '
         DC    CL10' '
         DC    CL10' '
         DC    CL10' '
         DC    CL10' '
DAY      DC    CL3' '
MESSAGE  DC    CL21' '
LDATA    EQU   *-MGROUP
DEFPACK  DC    PL4'0'              DATE DEFINED PACKED
         EJECT
*
*        RACF MACROS
*
LOCATEO  ICHEINTY LOCATE,MF=L,TYPE='USR',WKAREA=WORK,                  X
               ACTIONS=(ACTLST3,ACTLST7)
LOCATE   ICHEINTY LOCATE,MF=L,TYPE='USR',WKAREA=WORK,                  X
               ACTIONS=(ACTLST1,                                       X
               ACTLST2,                                                X
               ACTLST3,                                                X
               ACTLST4,                                                X
               ACTLST5,                                                X
               ACTLST5A,                                               X
               ACTLST5B,                                               X
               ACTLST6,                                                X
               ACTLST7,                                                X
               ACTLST8,                                                X
               ACTLST9,                                                X
               ACTLSTA,                                                X
               ACTLSTB,                                                X
               ACTLSTC,                                                X
               ACTLSTD)
ACTLST1  ICHEACTN MF=L,FIELD=DFLTGRP
ACTLST2  ICHEACTN MF=L,FIELD=AUTHOR
ACTLST3  ICHEACTN MF=L,FIELD=PGMRNAME
ACTLST4  ICHEACTN MF=L,FIELD=AUTHDATE
ACTLST5  ICHEACTN MF=L,FIELD=LJDATE
ACTLST5A ICHEACTN MF=L,FIELD=PASSDATE
ACTLST5B ICHEACTN MF=L,FIELD=PASSINT
ACTLST6  ICHEACTN MF=L,FIELD=FLAG1
ACTLST7  ICHEACTN MF=L,FIELD=FLAG2
ACTLST8  ICHEACTN MF=L,FIELD=FLAG3
ACTLST9  ICHEACTN MF=L,FIELD=FLAG4
ACTLSTA  ICHEACTN MF=L,FIELD=FLAG5
ACTLSTB  ICHEACTN MF=L,FIELD=FLAG6
ACTLSTC  ICHEACTN MF=L,FIELD=CONGRPCT
ACTLSTD  ICHEACTN MF=L,FIELD=CONGRPNM
WORK     DC    F'300'
         DC    300X'00'
         END
