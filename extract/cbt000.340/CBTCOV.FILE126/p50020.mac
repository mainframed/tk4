*          DATA SET P50020     AT LEVEL 006 AS OF 08/07/80
//TS01ASM JOB (TS01,TS,1,1,0),'TOM CAIN',NOTIFY=TS01,
// TIME=(,15)
/*JOBPARM FORMS=LOC1,QUEUE=TSO
//ASMFCL EXEC ASMFCL,
//             PARM.LKED='LET,LIST,NCAL,RENT,XREF,REFR'
//ASM.SYSIN DD *
         MACRO
         CSECTNM
         DC    C'&SYSECT'    CSECT NAME
         MEND
         MACRO
         RACFWTO
         TS    *+1            ISSUE ONE TIME ONLY (MLPA ONLY)
         BNZ   RACF&SYSNDX    SKIP WTO
         TIME
         ST    1,FULL&SYSNDX
         ED    EDIT&SYSNDX,FULL&SYSNDX+1
         MVC   W&SYSNDX+8(5),EDIT&SYSNDX+1
         WTO '******************************************************', X
               ROUTCDE=(9)
W&SYSNDX WTO '      &SYSECT ACTIVE - LAST COMPILE &SYSDATE &SYSTIME',  X
               ROUTCDE=(9)
         WTO '******************************************************', X
               ROUTCDE=(9)
         B     RACF&SYSNDX
FULL&SYSNDX DC F'0'
EDIT&SYSNDX DC X'402020202020'
         CNOP  0,4
RACF&SYSNDX EQU *
         MEND
********************************************************************
*                                                                  *
*   NAME:  RACINIT PRE-PROCESSING EXIT                             *
*                                                                  *
*   PURPOSE: THIS EXIT WILL SUPPLY THE USER ID FIELD FROM          *
*            MILLER BREWING JOB CARD ACCOUNTING FIELD.             *
*            //JOBNAME JOB (USERID,......),........                *
*            THE PASSWORD WILL BE SUPPLIED BY THE PASSWORD         *
*            PARAMETER ON THE JOB CARD.                            *
*            // PASSWORD=XXXXXXXX                                  *
*                                                                  *
*                                                                  *
*                                                                  *
*           THIS IS MODULE P50020 ON PANVALET.                     *
*                                                                  *
*                                                                  *
*                                                                  *
********************************************************************
         EJECT
ICHRIX01 CSECT
 TITLE 'RACINIT PREPROCESSING EXIT  &SYSTIME  &SYSDATE'
********************************************************************
*        SET UP SAVE AREA AND SAVE CALLER'S REGISTERS
********************************************************************
         PRIME LV=WORKSIZE
         LR    R6,R1            SET UP PARAMETER REGISTER
         USING RACLIST,R6
         REGISTER
         RACFWTO
         B     START          BRANCH AROUND ID
         CSECTNM
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
         USING SAVEAREA,R13
         EJECT
         CNOP  0,4
START    EQU   *
********************************************************************
*        SET UP BASE REGISTERS
********************************************************************
         SR    R15,R15          SET RETURN CODE OF CONTINUE NORMAL
         ST    R15,FLAGS        INITIALIZE PROCESSING SWITCHES
         ST    R15,EXITRC       INITIALIZE EXIT RETURN CODE
         L     R1,X'10'         CVT ADDRESS
         USING CVT,R1
         L     R2,CVTRAC        RACF CVT
         LTR   R2,R2            SEE IF RACF CVT EXISTS
         BZ    NORCVT           RACF CVT DOES NOT EXIST
         USING RCVT,R2
         TM    RCVTFLGS,X'80'   SEE IF RACF INACTIVE
         BO    INACTIVE         RACF VARIED INACTIVE
********************************************************************
*   RACF IS ACTIVE
*   IF RACINIT IS TO VERIFY A BATCH JOB AND NO USER IS ON JOB CARD
*   SEE IF A DEFAULT SHOULD BE SUPPLIED.
********************************************************************
         L     R3,RACPFLG1      PICK UP ADDR OF FUNCTION BYTE
         USING RACCFLG1,R3
         CLI   RACCFLG1,X'00'   SEE IF ZERO, CREATE FUNCTION
         BNZ   EXIT             NOT CREATE, PERFORM NO PROCESSING
         L     R3,RACPJOBN      PICK UP ADDRESS OF BATCH JOB
         CLC   0(8,R3),BLANKS   SEE IF NO JOB NAME
         BE    EXIT             NOT BATCH JOB, PERFORM NO PROCESSING
         L     R3,RACPUSER      PICK UP ADDRESS OF USERID
         LTR   R3,R3            INSURE NOT ZERO
         BZ    EXIT             NO USER FIELD SUPPLIED
         USING RACCUSER,R3
         CLI   USERL,X'00'      SEE IF NO USERID
         BNE   EXIT             USERID SUPPLIED VIA USER=
         L     R4,RACPPASS      PICK UP ADDRESS OF PASSWORD
         LTR   R4,R4            INSURE NOT ZERO
         BZ    EXIT             NO PASSWORD PARAMETER SUPPLIED
         USING RACCPASS,R4
         CLI   PASSL,X'00'      SEE IF THERE IS NO PASS WORD
         BE    EXIT             EXIT IF THERE IS NO USER ID AND
*                               NO PASSWORD CONTINUE NORMALLY
         SPACE 3
*
*        IF THERE IS A PASSWORD BUT THERE IS NO
*        USER ID SPECIFIED WE WILL OBTAIN THE USER
*        ID FROM THE ACCOUNTING INFORMATION.
*
         L     R4,RACPACC     RACF ACCT INFO
         SR    R5,R5
         IC    R5,1(R4)       USER ID LENGTH
         CH    R5,=H'8'       MUST NOT BE GT 8
         BH    EXIT
         STC   R5,USERL       STORE ID LNGTH IN PARM
         BCTR  R5,0           REDUCE FOR MVC
         EX    R5,MVCUSER     MOVE USERID TO PARM
         B     EXIT
         EJECT
********************************************************************
*        RESTORE REGISTERS FREE STORAGE AND RETURN
********************************************************************
INACTIVE DS    0H               RACF INACTIVE
NORCVT   DS    0H               NO RACF CVT POINTED TO BY CVT (DO)
EXIT     DS    0H
         L     R15,EXITRC       SET UP EXIT RETURN CODE
         TERME LV=72+WORKSIZE
         EJECT
********************************************************************
*        CONSTANTS AND LITERALS
********************************************************************
MVCUSER  MVC   USER(0),2(R4)
PASSRC   DC    F'8'    RETURN CODE OF PASS
PASSCONT DC    F'12'   RETURN CODE OF PASS/CONTINUE
BLANKS   DC    CL8' '
         LTORG
********************************************************************
*        SAVE AREA AND WORK AREA
********************************************************************
SAVEAREA DSECT
SAVEPLI  DS    F
SAVEOLD  DS    F
SAVENEW  DS    F
SAVE14   DS    F
SAVE15   DS    F
SAVE0    DS    F
SAVE1    DS    F
SAVE2    DS    F
SAVE3    DS    F
SAVE4    DS    F
SAVE5    DS    F
SAVE6    DS    F
SAVE7    DS    F
SAVE8    DS    F
SAVE10   DS    F
SAVE11   DS    F
SAVE12   DS    F
SAVESIZE EQU   *-SAVEAREA
WORKAREA EQU   *
FLAGS    DS    F
EXITRC   DS    F       EXIT RETURN CODE
WORKSIZE EQU   *-WORKAREA
         EJECT
ICHRIX01 CSECT
RACLIST  DSECT                  RACINIT PARAMETER LIST
RACPNUM  DS    F     ADDR NUMBER OF PARMS
RACPFLG1 DS    F     ADDR FUNCTION BYTE
RACPUSER DS    F     ADDR USERID
RACPPASS DS    F     ADDR PASSWORD
RACPSPRO DS    F     ADDR STARTED PROC NAME
RACPIEP  DS    F     RESERVED FOR INSTALLATION USE
RACPGRPN DS    F     ADDR GROUP NAME
RACPNPAS DS    F     ADDR NEW PASSWORD
RACPACEE DS    F     ADDR ACEE
RACPPGR  DS    F     ADDR PROGRAMMER NAME
RACPACC  DS    F     ADDR ACCOUNTING INFORMATION
RACPOID  DS    F     ADDR OPERATOR ID CARD INFO
RACPTRM  DS    F     ADDR TERMINAL ID
RACPWORK DS    F     ADDR INSTALLATION WORK ARE
RACPABND DS    F     ADDR RACINIT ABEND CODE
RACPPRC  DS    F     ADDR RACINIT RETURN CODE
RACPEFLG DS    F     ADDR BYPASS OID OPTION BYTE
RACPTDAT DS    F     ADDR TERMINAL INSTALLATION DATA
RACPUDAT DS    F     ADDR USER INSTALLATION DATA
RACPTLEV DS    F     ADDR TERMINAL LEVEL FIELD
RACPJOBN DS    F     ADDR JOB NAME
RACCFLG1 DSECT
         DS    XL1   FUNCTION BYTE
CHANGE   EQU   X'40'
DELETE   EQU   X'80'
RACCUSER DSECT
USERL    DS    XL1
USER     DS    CL8
RACCPASS DSECT
PASSL    DS    XL1
PASS     DS    CL8
RACCNPAS DSECT
NPASL    DS    XL1
NPAS     DS    CL8
RACCJOB  DSECT
JOBL     DS    XL1
JOB      DS    CL8
RACCTERM DSECT
TERML    DS    XL1
TERM     DS    CL8
RACCPROC DSECT
PROCL    DS    XL1
PROC     DS    CL8
RACCGRP  DSECT
GRPL     DS    XL1
GRP      DS    CL8
RACCPGMR DSECT
PGMR     DS    CL20
RACCACCT DSECT
ACCTNUM  DS    XL1
ACCT     DS    CL144
RACCOID  DSECT
OIDL     DS    XL1
OID      DS    CL256
RACCUDTA DSECT
UDTAL    DS    XL1
UDTA     DS    CL256
RACCTDTA DSECT
TDTAL    DS    XL1
TDTA     DS    CL256
********************************************************************
         EJECT
********************************************************************
*   MAPPING MACROS
********************************************************************
TCBASCB  DSECT
TCBNEXT  DS    F
TCBPTR   DS    F
ASCBNEXT DS    F
ASCBPTR  DS    F
         EJECT
         ICHPRCVT
         EJECT
         CVT       DSECT=YES
         EJECT
         IHAASCB
         EJECT
         IHAASXB
         EJECT
         IHAACEE
         END
//LKED.SYSLMOD DD DSN=SYS1.TSLINK,DISP=SHR
//LKED.SYSIN DD *
 NAME ICHRIX0X(R)
