//* FLOATING STORAGE FENCE (NON MVSSE).
//* THIS STRATEGY DIFFERS FROM OTHER STORAGE FENCING IN THAT THE
//* NUMBER OF FENCED PAGE FRAMES VARIES WITH THE DEMAND (AS MEASURED
//* BY UIC) OF THE PROTECTED ADDRESS SPACE. AS ITS PRIVATE UIC DROPS
//* STEAL IS INHIBITED AND MORE FRAMES ARE PROTECTED. AS IT IDLES
//* UIC RISES AND STEAL RESUMES, RETURNING MORE OF ITS FRAMES TO THE
//* PAGE FRAME POOL FOR USE BY OTHER ADDRESS SPACES.
//* ANOTHER KEY DIFFERENCE IS THAT THIS MOD EXCLUDES THE PROTECTED
//* ADDRESS SPACE FROM SYSTEM-WIDE HIGH UIC CALCULATION, ENABLING
//* SRM TO CONTINUE TO USE UIC IN ADJUSTING SYSTEM MPL.
//*
//SMPPTFIN DD *
++USERMOD(SM00023) /* PAGE STEAL MODIFICATION AT 7906 LEVEL ---
THIS MOD TO PAGE STEAL MONITOR MODULE PROTECTS A FAVORED, NONSWAPPABLE
JOB FROM EXCESSIVE PAGE FAULTING BY INSULATING IT FROM PAGE STEALING
AS ORIGINALLY IMPLEMENTED. PART 1 REVERSES THE ORDER OF SELECTING
ADDRESS SPACES FOR PAGE STEALING FROM LEAST-RECENTLY-SWAPPED-IN TO
MOST-RECENTLY-SWAPPED-IN, THEREBY PROTECTING NON-SWAPPABLE, LONG-
RUNNING JOBS AT ANY GIVEN LEVEL OF UIC.  PART 2 BYPASSES STEALING FOR
THE 'FAVORED' JOB IF ITS SPECIFIC UIC IS NOT ABOVE A FENCE LEVEL.
HOWEVER, A SAFETY-VALVE IS PROVIDED BY RESUMING NORMAL STEAL IF
THIS JOB'S FRAME COUNT EXCEEDS A SPECIFIED DANGER LEVEL.
PART 3 EXCLUDES THE FENCED JOB'S UIC FROM THE SYSTEM-WIDE HIGH UIC
BECAUSE THE FENCED JOB WOULD DISTORT ONE OF THE IMPORTANT PAGING
CONTENTION INDICATORS USED BY THE SRM.
     */.
++VER (Z038) FMID(EBB1102) PRE(UZ21470) .
++ZAP (IRARMSTM).
NAME IRARMSTM
 EXPAND IRARMSTM(88)
*
*
* PART 1 - START STEALING FROM BOTTOM OF RMCTINQE AND CONTINUE
* STEALING FROM MOST RECENTLY SWAPPED IN TO LEAST RECENTLY
* SWAPPED IN.
*
VER 01C8 58404004                   L     OUCBPTR,OUCBFWD(,OUCBPTR)
VER 0216 58404004                         DITTO
REP 01C8 58404008                   L     OUCBPTR,OUCBBWD(,OUCBPTR)
REP 0216 58404008                         DITTO
*
* FOLLOWING NEEDED IF INSTALLING EITHER PART 2 OR 3.
*
VER 0A58 00000000,00000000,00000000,00000000  EXPANSION PATCH AREA
VER 0A68 00000000,00000000,00000000,00000000
VER 0A78 00000000,00000000,00000000,00000000
VER 0A88 00000000,00000000,00000000,00000000
VER 0A98 00000000,00000000,00000000,00000000
VER 0AA8 00000000,00000000
*
REP 0A58 C3C9C3E2F0F0F0F0  FENCEJOB DC    C'CICS0000' *INITIATED JOB
*                                                      NAME*
*
* PART 2 - IF PRODUCTION CICS JOB, BYPASS STEAL IF ADDRESS SPACE
* DOES NOT HAVE MORE THAN 300 FRAMES AND IF ITS HIGH UIC IS BELOW 16.
*
VER 01FA 45E0C926                   BAL   @14,STEAL
REP 01FA 47F0CA5E                   B     PATCH1
*
* CHECK INITIATED JOB NAME POINTED TO BY ASCB. IF JOB NOT FENCED, THEN
* STEAL FRAMES NORMALLY. NOTE - IF JOB IS A STARTED TASK, MOD MUST USE
* ASCBJBNS INSTEAD OF ASCBJBNI.
*
*                          PATCH1   EQU   *
REP 0A60 589050AC                   L     @09,ASCBJBNI(,ASCBPTR)
REP 0A64 D507CA569000               CLC   FENCEJOB,0(@9)
REP 0A6A 4770CA84                   BNE   STEALPGE
*
* CHECK FRAME COUNT AGAINST SAFETY LIMIT. IF FRAME COUNT TOO HIGH,
* INVOKE NORMAL STEAL. IF FRAME COUNT OK, CHECK UIC AGAINST PROTECTION
* LEVEL.  IF BELOW PROTECTION LEVEL, DONT INVOKE STEAL AND RETURN TO
* CHECK NEXT INCORE USER.  IF UIC HIGH ENOUGH, INVOKE NORMAL STEAL.
* CAUTION, BECAUSE OF WAY STEALING WORKS, BE SURE TO SET YOUR FENCE
* LEVEL ABOUT 50 PERCENT ABOVE THE ABSOLUTE MINIMUM UIC YOU CAN
* TOLERATE. AT OUR INSTALLATION, UIC FENCE OF 16 KEEPS ACTUAL UIC
* BETWEEN 10 AND 16 WHEN PAGING IS VERY HEAVY.
*
REP 0A6E 4190012C                   LA    @09,300  *SAFETY LIMIT*
REP 0A72 49905098                   CH    @09,ASCBFMCT(,ASCBPTR)
REP 0A76 4740CA84                   BL    STEALPGE  *TOO HIGH*
REP 0A7A 41900010                   LA    @09,16  *OK-TO-STEAL LEVEL*
REP 0A7E 49906086                   CH    @09,OUXBUIC(,OUXBPTR)
REP 0A82 4720C214                   BH    @RF00144  *STEP TO NEXT USER*
*                          STEALPGE EQU   *
REP 0A86 45E0C926                   BAL   @14,STEAL  *NORMAL STEAL*
REP 0A8A 47F0C1FC                   B     @RF00144-X'18'
*
* PART 3 - EXCLUDE FENCED JOB FROM SYSTEM-WIDE HIGH UIC CALCULATION.
* STRONGLY RECCOMMENDED IF YOU INSTALL PART 2.
*
VER 00EC 58605094                   L     OUXBPTR,ASCBOUXB(,OUXBPTR)
REP 00EC 47F0CA8E                   B     PATCH2
*
* CHECK INITIATED JOB NAME POINTED TO BY ASCB. IF STARTED JOB, THEN
* THIS MOD MUST USE ASCBJBNS.  IF FENCED, BYPASS CHECKING ITS HIGH UIC
* AGAINST SYSTEM-WIDE HIGH UIC AS CANDIDATE FOR NEW SYSTEM HIGH UIC.
*
*                          PATCH2   EQU   *
REP 0A90 58F050AC                   L     @15,ASCBJBNI(,ASCBPTR)
REP 0A94 D507CA56F000               CLC   FENCEJOB,0(@15)
REP 0A9A 4780C102                   BE    @RF00107 *BYPASS UIC CHECK*
REP 0A9E 58605094                   L     OUXBPTR,ASCBOUXB(,ASCBPTR)
REP 0AA2 47F0C0EE                   B     @RF00107-X'14'*NORMAL CHECK*
