00010029PROC 0 NOMSG
00020000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **
00030000/**                                                                   **
00040000/** Receive TSO/E IDTF XMIT Messages in Browse.                       **
00050000/** By Mike Theys, Dave Fay, and Andy Anderson  Rockwell International**
00060020/** (TSO/E R2 needs to trunc captured output to 80 characters)        **
00070020/** (TSO/E R3 Version Uses SYSNSUB to avoid problems with ampersands) **
00080020/**                                                                   **
00090000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **
00100010CONTROL NOFLUSH MSG NOPROMPT ASIS
00110000ISPEXEC CONTROL ERRORS RETURN
00120028%ROCPROF OPTION(NOMSGID)   /* Make sure PROFILE NOMSGID is set. */
00130028SET &RPRC = &LASTCC
00140000SET &SYSOUTTRAP = 999999
00150032  RECEIVE
00160002SET TRAPKNT = &SYSOUTLINE
00170000SET  &SYSOUTTRAP = 0
00180016IF &TRAPKNT > 1 THEN DO
00190016  SET K = 1
00200016  ISPEXEC TBCREATE &SYSUID.@ NAMES(LINE) NOWRITE REPLACE
00210016  DO WHILE (&K <= &TRAPKNT)
00220016    SET TEMP = &&SYSOUTLINE&K
00230020    IF &SYSTSOE ^=   THEN SET LINE = &STR(&SYSNSUB(2,&TEMP))
00240021    ELSE /* NOT TSO/E R3 */ DO
00250020      SET LINE = &STR(&TEMP)
00260020      IF (&LENGTH(&STR(&LINE)) > 80) THEN +
00270020        SET LINE = &STR(&SUBSTR(1:80,&STR(&LINE)))
00280020    END
00290016    ISPEXEC TBADD &SYSUID.@
00300016    SET K = &K + 1
00310016  END
00320016  ISPEXEC VGET (ZUSER)
00330016  ISPEXEC TBTOP &ZUSER.@
00340016  ISPEXEC FTOPEN TEMP
00350016  ISPEXEC FTINCL RIDTFRCV
00360016  ISPEXEC FTCLOSE
00370016  ISPEXEC TBEND &ZUSER.@
00380016  ISPEXEC VGET (ZTEMPF)
00390016  SET ECMDBRTI = &STR(RECEIVE Output)
00400016  ISPEXEC VPUT (ECMDBRTI)
00410016  ISPEXEC BROWSE DATASET('&ZTEMPF') PANEL(ECMDBROW)
00420016END
00430016ELSE DO
00440029  IF &NOMSG =  THEN DO
00450029    SET SHORT = &STR(No XMIT Mail)
00460029    SET LONG  = &STR(There were NO XMIT Messages to RECEIVE.)
00470029    ISPEXEC SETMSG MSG(SYS010)
00480029  END
00490016END
00500028IF &RPRC = 0 THEN ACFPROF MSGID  /* Reset PROFILE MSGID if turn OFF */
