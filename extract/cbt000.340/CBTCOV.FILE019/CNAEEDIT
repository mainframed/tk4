00010003 PROC 1   DSN VOLUME() PASSWORD() MACRO() PROFILE() DEBUG
00020003 IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST MSG
00030005 
00040005 ISPEXEC VGET (ZAPPLID)
00050005 IF &ZAPPLID ^= ISR THEN DO
00060006   ISPEXEC SELECT CMD(+
00070006     %CNAEEDIT &DSN VOLUME('&VOLUME') +
00080006               MACRO('&MACRO') PROFILE('&PROFILE') &DEBUG) +
00090005     NEWAPPL(ISR)
00100005   EXIT
00110005 END
00120005 
00130000 SET FUNC = EDIT
00140000/**********************************************************************
00150000/*       BOB ZIMMERMAN
00160000/*       CNA INSURANCE
00170000/*       (312) 822-5719
00180000/*       LAST MODIFIED 8503
00190004/*       LAST MODIFIED 8706 M.THEYS ROCKWELL FIX FOR VOLUME
00200000/**********************************************************************
00210000/*       THIS CLIST USED WITH THE ED COMMAND TABLE COMMAND WILL ALLOW
00220000/*       A USER TO EDIT A DATASET FROM ANY SCREEN.
00230000/**********************************************************************
00240000 
00250002 ISPEXEC SELECT PGM(EDREC)
00260002 IF &LASTCC = 20 THEN EXIT
00270000 
00280002 IF &STR(&DSN) = &STR(*) THEN DO
00290004   ISPEXEC VGET (BRDSN BRMEM BRVOL) PROFILE
00300004   IF &BRVOL ^=  THEN SET VOLUME = &BRVOL
00310002   IF &BRMEM =    +
00320002     THEN SET DSN = &STR('&BRDSN')
00330002     ELSE SET DSN = &STR('&BRDSN(&BRMEM)')
00340002 END
00350002 
00360000 LOOP: +
00370002 ISPEXEC CONTROL ERRORS RETURN
00380004 IF &VOLUME ^= THEN DO
00390004   SET ZVOL = &VOLUME
00400004   ISPEXEC VPUT (ZVOL) SHARED
00410004 END
00420004 
00430002 ISPEXEC EDIT   DATASET(&DSN) VOLUME(&VOLUME) +
00440002                PASSWORD(&PASSWORD) +
00450002                MACRO(&MACRO) +
00460002                PROFILE(&PROFILE)
00470002 SET LCC = &LASTCC
00480004 
00490004 IF &VOLUME ^= THEN DO
00500004   SET ZVOL =
00510004   ISPEXEC VPUT (ZVOL) SHARED
00520004 END
00530002 
00540002 IF &LCC > 4 THEN DO
00550002   ISPEXEC DISPLAY PANEL(EDDDSN) MSG(&ZERRMSG) CURSOR(DSN)
00560002   IF &LASTCC = 0 THEN GOTO LOOP
00570002 END
00580002 
00590002 EXIT CODE(&LCC)
