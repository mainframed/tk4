00010000PROC     0        DEBUG
00020000 
00030000IF       &DEBUG = DEBUG +
00040000THEN     CONTROL  ASIS   CONLIST NOFLUSH   LIST   MSG   PROMPT   SYMLIST
00050000ELSE     CONTROL  ASIS NOCONLIST NOFLUSH NOLIST   MSG   PROMPT NOSYMLIST
00060000 
00070000ISPEXEC  CONTROL  ERRORS   RETURN
00080000 
00090000ISPEXEC  DISPLAY  PANEL(EXCOPY)
00100000SET      &CC = &LASTCC
00110000DO       WHILE    &CC = 0
00120000         ALLOCATE FILE(SYSUT1) DATASET(&ASMDSN) SHR REUSE
00130000         ALLOCATE FILE(SYSLIB) DATASET(&ASMDSN &SYSLIB1 &SYSLIB2 &SYSLIB3 &SYSLIB4 &SYSLIB5 &SYSLIB6 &SYSLIB7) SHR REUSE
00140000         IF       &EXCOPYO = &STR() +
00150000         THEN     ALLOCATE FILE(SYSUT2) UNIT(SYSVIO) SPACE(1 1) CYLINDER DSORG(PS) RECFM(F B S) LRECL(80) BLKSIZE(6160) REUSE
00160000         ELSE     ALLOCATE FILE(SYSUT2) DSNAME(&EXCOPYO) OLD REUSE
00170000         ISPEXEC  SELECT   PGM(EXCOPY) PARM(&MEMBER)
00180000         SET      &EXCOPYCC = &LASTCC
00190000         IF       &EXCOPYCC = 4 +
00200000         THEN     DO
00210000                  SET      &SHORT = &STR(***** ERROR *****)
00220000                  SET      &LONG  = &STR(ERROR ENCOUNTERED: SEE MESSAGE AT LOCATION OF COPY STATEMENT)
00230000                  ISPEXEC  SETMSG   MSG(AOST010)
00240000                  END
00250000         IF       &EXCOPYCC < 16 +
00260000         THEN     DO
00270000                  IF       &EXCOPYO = &STR() +
00280000                  THEN     DO
00290000                           ISPEXEC  LMINIT   DATAID(SYSUT2) DDNAME(SYSUT2)
00300000                           ISPEXEC  BROWSE   DATAID(&SYSUT2)
00310000                           ISPEXEC  LMFREE   DATAID(&SYSUT2)
00320000                           END
00330000                  ELSE     DO
00340000                           IF       &SUBSTR(1,&EXCOPYO) ^= ' +
00350000                           THEN     ISPEXEC  SELECT   CMD(%GENPRINT DSN(&EXCOPYO))
00360000                           ELSE     ISPEXEC  SELECT   CMD(%GENPRINT DSN(''&EXCOPYO''))
00370000                           END
00380000                  END
00390000         ELSE     DO
00400000                  SET      &SHORT = OUTPUT ERROR
00410000                  SET      &LONG  = OUTPUT DATA SET IS UNUSABLE
00420000                  ISPEXEC  SETMSG   MSG(AOST010)
00430000                  END
00440000         ISPEXEC  DISPLAY  PANEL(EXCOPY)
00450000         SET      &CC = &LASTCC
00460000         END
00470000 
00480000CONTROL  NOMSG
00490000FREE     FILE(SYSLIB SYSUT1 SYSUT2)
00500000EXIT     CODE(0)
