00010000PROC 0 DEBUG ALL
00020000 
00030000/**********************************************************************/
00040000/* THIS CLIST IS DESIGNED TO SCAN THE TABLE OF ADVANCED FUNCTION      */
00050000/* PRINTING FONTS AND GENERATE A SUMMARY THAT CAN BE USED BY THE USER */
00060000/* IN SELECTING A FONT TO BE USED IN SCRIPTING A DOCUMENT.            */
00070000/*                                                                    */
00080000/* THE CLIST IS INVOKED BY ENTERING %FONTSUMM                         */
00090000/*                               OR %FONTSUMM ALL                     */
00100000/*                                                                    */
00110000/*      BY SPECIFYING THE OPTION 'ALL' A SUMMARY WILL BE              */
00120000/*      GENERATED OF ALL AVAILABLE FONTS.  THE DEFAULT IS TO          */
00130000/*      EXCLUDE THE FOREIGN (NON-U.S.) FONTS.                         */
00140000/*                                                                    */
00150000/*  THE OUTPUT PRODUCED BY THIS CLIST SHOULD THEN BE PROCESSED BY     */
00160000/*  SCRIPT TO GENERATE THE SUMMARY DOCUMENT                           */
00170000/**********************************************************************/
00180000 
00190000IF &DEBUG = DEBUG +
00200000  THEN CONTROL LIST CONLIST SYMLIST NOFLUSH PROMPT MSG
00210000  ELSE CONTROL NOLIST NOCONLIST NOSYMLIST FLUSH NOPROMPT NOMSG
00220000 
00240000 
00250000ISPEXEC LIBDEF FTABLE DATASET ID('AOST.ISPF.TABLES')
00260000 
00270000DEL FONTSUMM.TEXT
00280000 
00290001ALLOC F(CONTENTS) DS(FONTSUMM.TEXT) NEW +
00300000      SPACE(10,10) TRACK RECFM(F B) LRECL(80) BLKSIZE(23440)
00310001FREE  F(CONTENTS)
00320001ALLOC F(CONTENTS) DS(FONTSUMM.TEXT) MOD +
00330001      SPACE(10,10) TRACK RECFM(F B) LRECL(80) BLKSIZE(23440)
00340000 
00350000ISPEXEC TBOPEN AFPFONTS NOWRITE LIBRARY(FTABLE) SHARE
00360000ISPEXEC TBSORT AFPFONTS FIELDS(TYPEFACE,C,A POINTSIZ,N,A)
00370000ISPEXEC TBSKIP AFPFONTS
00380000 
00390000SET STEP = 0
00400000SET JOB  = NO
00410000SET PAGE = 0
00420000SET TOC = 0
00430000SET SAMPLE = 0
00440000SET XFONT =
00450000SET FONT = FONT
00460000SET CP = CP
00470006SET MAXTOC = 24  /* MAX FONTS PER PAGE */
00480000 
00490000OPENFILE CONTENTS OUTPUT
00500000 
00510000SET CONTENTS = &STR(.DF FONT TYPE('SONORAN SERIF' 8   ) CODEPAGE T1DCDCFS )
00520000PUTFILE CONTENTS
00530000SET CONTENTS = &STR(.BF FONT)
00540000PUTFILE CONTENTS
00550000SET CONTENTS = &STR(.PN PREF FONTS-)
00560000PUTFILE CONTENTS
00570000SET CONTENTS = &STR(.RH ON)
00580000PUTFILE CONTENTS
00590000SET CONTENTS = &STR(AFP FONT SUMMARY)
00600000PUTFILE CONTENTS
00610000SET CONTENTS = &STR(.RH OFF)
00620000PUTFILE CONTENTS
00630000 
00640000SET HEAD1 = &STR(:RDEF ID=FMTOC CWIDTHS='2.6I .5I 1.2I 1.2I .8I' HP='2' ALIGN='L R L L R'.)
00650000SET HEAD2 = &STR(:RDEF ID=ROW2  CWIDTHS='3.1I 3.2I' HP='1 1' ALIGN='R R'.)
00660000SET HEAD3 = &STR(:RDEF ID=HEAD  CWIDTHS='2.6I .5I 1.2I 1.2I .8I' HP='2' ALIGN='L R L L R'.)
00670000SET HEAD4 = &STR(:TABLE REFID=FMTOC.)
00680000SET LINE1 = &STR(:C.FONT NAME:C.SIZE:C.WIDTH:C.WEIGHT:C.CODEPAGE)
00690000 
00700000SET CONTENTS = &STR(&HEAD1)
00710000PUTFILE CONTENTS
00720000SET CONTENTS = &STR(&HEAD2)
00730000PUTFILE CONTENTS
00740000SET CONTENTS = &STR(&HEAD3)
00750000PUTFILE CONTENTS
00760000SET CONTENTS = &STR(&HEAD4)
00770000PUTFILE CONTENTS
00780000SET CONTENTS = &STR(:ROW REFID=HEAD.)
00790000PUTFILE CONTENTS
00800000SET CONTENTS = &STR(&LINE1)
00810000PUTFILE CONTENTS
00820000 
00830000FIXUP: +
00840000 IF &ALL ^= ALL THEN DO
00850000   IF &SYSINDEX(AUSTRAL,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00860000   IF &SYSINDEX(AUSTRIA,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00870000   IF &SYSINDEX(BELGIUM,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00880000   IF &SYSINDEX(BRAZIL,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00890000   IF &SYSINDEX(DENMARK,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00900000   IF &SYSINDEX(FINLAND,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00910000   IF &SYSINDEX(FRANCE,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00920000   IF &SYSINDEX(GERMANY,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00930000   IF &SYSINDEX(FRENCH,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00940000   IF &SYSINDEX(ICELAND,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00950000   IF &SYSINDEX(KATAKANA,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00960000   IF &SYSINDEX(ITALY,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00970000   IF &SYSINDEX(JAPAN,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00980000   IF &SYSINDEX(KINGDOM,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
00990000   IF &SYSINDEX(LUXEMBOURG,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
01000000   IF &SYSINDEX(LATIN,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
01010000   IF &SYSINDEX(U.K.&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
01020000   IF &SYSINDEX(H.K.,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
01030000   IF &SYSINDEX(NETHER,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
01040000   IF &SYSINDEX(NORWAY,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
01050000   IF &SYSINDEX(PORTU,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
01060000   IF &SYSINDEX(SPAIN,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
01070000   IF &SYSINDEX(SWEEDEN,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
01080000      END
01090000 
01100000   IF &TOC = &MAXTOC THEN DO
01110000                          SET TOC = 0
01120000                          SET CONTENTS = &STR(:ETABLE.)
01130000                          PUTFILE CONTENTS
01140000                          SET CONTENTS = &STR(.PA)
01150000                          PUTFILE CONTENTS
01160000                          SET CONTENTS = &STR(&HEAD4)
01170000                          PUTFILE CONTENTS
01180000                          SET CONTENTS = &STR(:ROW REFID=HEAD.)
01190000                          PUTFILE CONTENTS
01200000                          SET CONTENTS = &STR(&LINE1)
01210000                          PUTFILE CONTENTS
01220000                          END
01230000 
01240000   SET CONTENTS = &STR(:ROW REFID=FMTOC.)
01250000   PUTFILE CONTENTS
01260000 
01270000   SET OFF = &SYSINDEX(NORMAL,&STR(&WIDTH),1)
01280000       IF &OFF > 0 THEN SET &WIDTH =
01290000 
01300000   SET OFF = &SYSINDEX(MEDIUM,&STR(&WEIGHT),1)
01310000       IF &OFF > 0 THEN SET &WEIGHT =
01320000 
01330000   SET CONTENTS = &STR(:C.&TYPEFACE:C.&POINTSIZ:C.&WIDTH:C.&WEIGHT:C.&CODEPAGE)
01340000   PUTFILE CONTENTS
01350000 
01360000   SET CONTENTS = &STR(:ROW REFID=ROW2.)
01370000   PUTFILE CONTENTS
01380000   SET CONTENTS = &STR(:C.&STYLE:C.&NRSTR(&CODEPD))
01390000   PUTFILE CONTENTS
01400000 
01410000   IF &STR(&XFONT) ^= &STR(&TYPEFACE) THEN DO
01420000                                           IF &STR(&WIDTH) ^= THEN GOTO SETTOC
01430000                                           IF &STR(&WEIGHT) ^= THEN GOTO SETTOC
01440000                                           IF &POINTSIZ ^= 10 THEN GOTO SETTOC
01450000                                           IF &STR(&STYLE) ^= THEN GOTO SETTOC
01460000                                           SET SAMPLE = &SAMPLE + 1
01470000                                           SET XFONT = &STR(&TYPEFACE)
01480000                                           SET &&FONT&SAMPLE = &STR(&TYPEFACE)
01490000                                           SET &&CP&SAMPLE = &CODEPAGE
01500000                                           END
01510000SETTOC: +
01520000   SET TOC = &TOC + 1
01530000 
01540000SKIP: +
01550000   ISPEXEC TBSKIP AFPFONTS
01560000      IF &LASTCC = 0 THEN GOTO FIXUP
01570000 
01580000EXIT: +
01590000      SET CONTENTS = &STR(:ETABLE.)
01600000      PUTFILE CONTENTS
01610000 
01620000IF &SAMPLE = 0 THEN GOTO EXIT2
01630000 
01640000SET COUNT = 1
01650000 
01660001   SET CONTENTS = &STR(.DS;.FO OFF)
01670000   PUTFILE CONTENTS
01680001   CLOSFILE CONTENTS
01690000 
01700000SAMPLES: +
01710001      OPENFILE CONTENTS OUTPUT
01720000      SET CONTENTS = &STR(.PA)
01730000      PUTFILE CONTENTS
01740000 
01741003      CONTROL NOCAPS
01750000      SET FONT = &&FONT&COUNT
01760000      SET CP = &&CP&COUNT
01770000      SET CONTENTS = &STR(.DF F&COUNT TYPE('&FONT' 10) CODEPAGE &CP)
01780000      PUTFILE CONTENTS
01790004      SET CONTENTS = &STR(.BF FONT)
01791004      PUTFILE CONTENTS
01792004      SET CONTENTS = &STR(Font &FONT Size:10 )
01800000      PUTFILE CONTENTS
01810002      SET CONTENTS = &STR(.BF F&COUNT)
01820002      PUTFILE CONTENTS
01830001      CLOSFILE CONTENTS
01840000 
01850001      ISPEXEC SELECT PGM(FONTSAMP)
01860000 
01870000 
01880000      SET COUNT = &COUNT + 1
01890000      IF &COUNT <= &SAMPLE THEN GOTO SAMPLES
01900000 
01910000EXIT2: +
01920000      ISPEXEC TBEND AFPFONTS
01930000      ISPEXEC LIBDEF FTABLE
01940000      FREE FILE(CONTENTS)
01950005      ISPEXEC BROWSE DATASET(FONTSUMM.TEXT)
