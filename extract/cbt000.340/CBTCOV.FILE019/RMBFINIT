00010013PROC 0 TABLE() MENU() POOL() APPLID() CLEAR NOMSG SHOW DEBUG +
00020010       TBLDDN()  TBLDSN()
00021013/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00030013/* User version of Re-Build/Set Options from Table               */
00040013/* Can be run under ISPF or from "READY" mode.  Uses %RMBF#U4T   */
00050013/* MENU=PDF, YOUR, THE, USER, GROUP or AOST. Defines POOL        */
00060013/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00070013/** Rockwell Menu Build Facility: BUILD                         **/
00080013/**                                                             **/
00090013/** Michael E. Theys                                            **/
00100013/** Southwestern Computing Center                               **/
00110013/** Information Systems Center                                  **/
00120013/** Rockwell International                                      **/
00130013/** 1200 N. Alma Rd.  Bldg 401-109                              **/
00140013/** Richardson, Texas 75081-2205                                **/
00150013/** (214) 996-2860  COMNET 437-2860                             **/
00160013/** XMIT Address    SWCC.XTS0008                    Oct 1986    **/
00170013/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00171013IF &DEBUG = DEBUG THEN  CONTROL   MSG   LIST   CONLIST   SYMLIST
00172013ELSE                    CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST
00180000 
00190000/* Initialize  a users "R???" Menu/Hidden/Alias options   */
00200000/* Where ??? is a MBF Variable POOL: PRM,YOR,USR,GRP,MBF. */
00210000/* Requires an ISPF input table member named "RMBF&TABLE" */
00220000/* GROUP table should be named "RMBFGRP"                  */
00230000/* USER  table should be name  "RMBFUSER"                 */
00240000/* Any other table should be named "RMBF????"             */
00250000/* where "????" should be used in keywork  "TABLE()"      */
00260000/* Invoke  "Rebuild menu/alias options from table" clist. */
00270000 
00280000/* If no TABLE() was specified the the most recent */
00290000/*  saved table is used, if that is null then  RMBFSAVE is used    */
00300000/*  And if the RMBFSAVE table does not exist then an ERROR message */
00310000 
00320000IF      &STR(&MENU)  = PDF       THEN  SET POOL = PRM
00330000ELSE IF &STR(&MENU)  = AOST      THEN  SET POOL = PRM
00340000ELSE IF &STR(&MENU)  = YOUR      THEN  SET POOL = YOR
00350009ELSE IF &STR(&MENU)  = THE       THEN  SET POOL = THE
00360000ELSE IF &STR(&MENU)  = USER      THEN  SET POOL = USR
00370000ELSE IF &STR(&MENU)  = GROUP     THEN  SET POOL = GRP
00380001ELSE IF &STR(&MENU)  = SYS       THEN  SET POOL = SYS
00390002ELSE IF &STR(&MENU)  = OST       THEN  SET POOL = OST
00400002ELSE IF &STR(&MENU)  = ICF       THEN  SET POOL = ICF
00410000ELSE IF &STR(&POOL) ^=           THEN  SET POOL = PRM
00420000 
00430000IF      &STR(&MENU) = AOST      THEN  SET APPLID = ISP
00440000 
00450010IF      &STR(&TBLDDN) ^= THEN DO
00460010  WHATIF FILE(&TBLDDN) ALLOCATED
00470010  IF &LASTCC ^= 0 THEN DO
00480010    IF &SYSISPF = ACTIVE THEN DO
00490010      SET SHORT = &STR(MBF TABLE DD NOT ALLOC)
00500012      SET LONG  = &STR(%RMBFINIT WAS INVOKED WITH TBLDDN(&TBLDDN) THAT WASN'T ALLOCATED.)
00510010      ISPEXEC SETMSG MSG(SYS010)
00520010    END
00530010    ELSE WRITE *ERROR* %RMBFINIT TBLDDN(&TBLDDN).  FILE WAS NOT ALLOCATED.
00540010    EXIT
00550010  END
00560010  SET VDDN = &STR( TBLDDN(&TBLDDN) )
00570010END
00580010ELSE IF &STR(&TBLDSN) ^= THEN DO
00590011  WHATIF DATASET(&TBLDSN) CATALOGED
00600010  IF &LASTCC ^= 0 THEN DO
00610010    IF &SYSISPF = ACTIVE THEN DO
00620010      SET SHORT = &STR(NO MBF TABLE DSN)
00630012      SET LONG  = &STR(%RMBFINIT WAS INVOKED WITH TBLDSN(&TBLDSN) THAT DIDN'T EXIST.)
00640010      ISPEXEC SETMSG MSG(SYS010)
00650010    END
00660010    ELSE WRITE *ERROR* %RMBFINIT TBLDSN(&TBLDSN). TABLE DATASET DIDN'T EXIST.)
00670010    EXIT
00680010  END
00690010  SET VDSN = &STR( TBLDDN(RMBFINIT) )
00700010  ALLOC FI(RMBFINIT) REUSE DA(&TBLDSN) SHR
00710010END
00720010 
00730000IF &SYSISPF = ACTIVE THEN DO
00740007  CONTROL ASIS
00750008  ISPEXEC VGET (ZAPPLID)
00760005  IF &SHOW = SHOW THEN DO
00770005     ISPEXEC CONTROL DISPLAY LOCK
00780005     ISPEXEC VGET (RMBFLOAD)
00790005     SET RMBFLOD1 = &STR(Menu is being rebuilt - Please wait.)
00800005     ISPEXEC DISPLAY PANEL(RMBFLOAD)
00810005  END
00820000  IF &STR(&APPLID) ^= THEN SET NEWAPPL = &STR( NEWAPPL(&APPLID) )
00830008  IF &STR(&APPLID)  = &STR(&ZAPPLID) THEN SET NEWAPPL = &STR( )
00840008  IF &STR(&NEWAPPL) ^= THEN +
00850014       ISPEXEC SELECT CMD(%RMBF#U4T   +
00860015                 TABLE('&TABLE') POOL('&POOL') &CLEAR &NOMSG &VDDN &VDSN) &NEWAPPL
00870015  ELSE %RMBF#U4T TABLE('&TABLE') POOL('&POOL') &CLEAR &NOMSG &VDDN &VDSN
00880005  IF &SHOW = SHOW THEN DO
00890005     ISPEXEC CONTROL DISPLAY LOCK
00900007     SET RMBFLOD1 = &STR(Menu options are now available.)
00910005     ISPEXEC DISPLAY PANEL(RMBFLOAD)
00920005  END
00930000 
00940000END
00950000ELSE DO  /* ISPF is NOT active, so invoke ISPF.  */
00960000  /* Requires ISPF datasets to be allocated!!    */
00970000 
00980000  IF &STR(&APPLID) =  THEN SET APPLID = ISR
00990000  ISPF CMD(+
01000015  %RMBF#U4T TABLE('&TABLE') POOL('&POOL') &CLEAR &NOMSG &VDDN &VDSN +
01010003          ) NEWAPPL(&APPLID)
01020000 
01030000END
01040010 
01050010IF &STR(&VDSN) ^= THEN DO
01060010  CONTROL NOMSG
01070010  FREE FI(RMBFINIT)
01080010  CONTROL   MSG
01090010END
