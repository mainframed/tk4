00010000 PROC 0 TEST TBLDSN(TASKLIST)
00020000 /*TITLE TASKDR: DRIVER FOR ONLINE TASKS-LIST APPLICATION   (CSS)    */
00030000 /*    WAS TITLED SPTASKDR                                           */
00040000 /* AUTHOR:  JOE VEILLEUX                                              */
00050000 /* HISTORY: 27FEB85-JJV-1.00-ORIGINAL EXPERIMENTAL RELEASE            */
00060000 /*          28FEB85-JJV-1.01-ADD ACK FOR SORT, SAVE                   */
00070000 /*          08MAR85-JJV-1.02-ADD NOTEBOOK, CHANGE DEFAULT SORT ORDER  */
00080000 /*          18MAR85-JJV-1.03-CHANGE DEFAULT SORT ORDER AGAIN          */
00090000 CONTROL MAIN END(ENDO) NOMSG
00100000 IF &TEST EQ TEST THEN CONTROL LIST SYMLIST CONLIST MSG
00110000 ISPEXEC VGET (TASKSRTF) PROFILE
00120000 IF &LASTCC EQ 8 OR &TASKSRTF EQ THEN SET &TASKSRTF = &STR(**UNKNOWN**)
00130000 ISPEXEC VGET (TASKNDSN) PROFILE
00140000 IF &LASTCC EQ 8 OR &TASKNDSN EQ THEN SET &TASKNDSN = &STR(**NOT SET**)
00150000 ISPEXEC TBOPEN TASKLIST WRITE LIBRARY(ISPTABL) SHARE
00160000 IF &LASTCC EQ 8 THEN DO
00170000   ISPEXEC TBCREATE TASKLIST WRITE LIBRARY(ISPTABL) SHARE +
00180000     NAMES(CATEGORY TASK PRIORITY TARGDATE TARGSORT HOURS STATUS)
00190000   IF &LASTCC NE 0 THEN DO
00200000     ISPEXEC SETMSG MSG(TASK001S)
00210000     EXIT
00220000     ENDO
00230000   ISPEXEC SETMSG MSG(TASK001T)
00240000   ISPEXEC TBSORT TASKLIST +
00250000     FIELDS(STATUS,C,D PRIORITY,N,A HOURS,N,D TARGSORT,N,A)
00260000   SET &TASKSRTF = &STR(STATUS(D) PRIORITY HOURS(D) DATE)
00270000   ISPEXEC VPUT (TASKSRTF) PROFILE
00280000   ENDO
00290000 /* SET FLAG TO DELETE "PRINT" DATASET ON EXIT */
00300000 SET &PRTDEL =
00310000 SET &RC = 0
00320000 SET &ZTDTOP = 0
00330000 SET &UPDTFLAG =
00340000 /* DISPLAY THE TABLE FOR UPDATE UNTIL "END" OR "CANCEL" */
00350000 DO WHILE 1=1
00360000   IF &RC EQ 4 THEN ISPEXEC TBDISPL TASKLIST POSITION(CRP)
00370000   ELSE DO
00380000     /* REPOSITION THE TABLE AS THE USER LAST SAW IT */
00390000     ISPEXEC TBTOP   TASKLIST
00400000     ISPEXEC TBSKIP  TASKLIST NUMBER(&ZTDTOP)
00410000     /* HONOR A PENDING SCROLL REQUEST (IF ANY) */
00420000     ISPEXEC VGET (ZVERB ZSCROLLN)
00430000     IF &ZVERB EQ UP THEN ISPEXEC TBSKIP TASKLIST NUMBER(-&ZSCROLLN)
00440000     ELSE IF &ZVERB EQ DOWN THEN ISPEXEC TBSKIP TASKLIST +
00450000       NUMBER(&ZSCROLLN)
00460000     /* NOW REDISPLAY THE TABLE */
00470000     ISPEXEC TBDISPL TASKLIST PANEL(TASKTBD) POSITION(CRP)
00480000     ENDO
00490000   SET &RC = &LASTCC
00500000   IF &CMD EQ CANCEL OR &RC GT 4 THEN GOTO LOOPEXIT /* EXIT LOOP */
00510001   SET CMD = &SUBSTR(1:2,&CMD  )
00520001   IF &CMD EQ SO THEN DO   /* SORT */
00530000     IF &SF1 EQ THEN DO
00540000       SET &SF1 = STATUS
00550000       SET &SF2 = PRIORITY
00560000       SET &SF3 = HOURS
00570000       SET &SF4 = DATE
00580000       SET &SF5 =
00590000       ENDO
00600000     SET &SORTFLDS =
00610000     SET &TASKSRTF =
00620000     SET &I = 1
00630000     DO WHILE &I LT 6
00640000       SET &SF = SF&I
00650000       SET &S = &&&SF
00660000       IF &S EQ CATEGORY THEN DO
00670000         SET &SORTFLDS = &STR(&SORTFLDS CATEGORY,C,A)
00680000         SET &TASKSRTF = &STR(&TASKSRTF CATEGORY)
00690000         ENDO
00700000       ELSE IF &S EQ PRIORITY THEN DO
00710000         SET &SORTFLDS = &STR(&SORTFLDS PRIORITY,N,A)
00720000         SET &TASKSRTF = &STR(&TASKSRTF PRIORITY)
00730000         ENDO
00740000       ELSE IF &S EQ DATE THEN DO
00750000         SET &SORTFLDS = &STR(&SORTFLDS TARGSORT,N,A)
00760000         SET &TASKSRTF = &STR(&TASKSRTF DATE)
00770000         ENDO
00780000       ELSE IF &S EQ HOURS THEN DO
00790000         SET &SORTFLDS = &STR(&SORTFLDS HOURS,N,D)
00800000         SET &TASKSRTF = &STR(&TASKSRTF HOURS(D))
00810000         ENDO
00820000       ELSE IF &S EQ STATUS THEN DO
00830000         SET &SORTFLDS = &STR(&SORTFLDS STATUS,C,D)
00840000         SET &TASKSRTF = &STR(&TASKSRTF STATUS(D))
00850000         ENDO
00860000       SET &I = &I + 1
00870000       ENDO
00880000     ISPEXEC VPUT (TASKSRTF) PROFILE
00890000     ISPEXEC TBSORT TASKLIST FIELDS(&SORTFLDS)
00900000     ISPEXEC SETMSG MSG(TASK005R)
00910000     ENDO
00920001   ELSE IF &CMD EQ SA THEN DO   /* SAVE  */
00930000     ISPEXEC TBSAVE TASKLIST REPLCOPY PAD(15) LIBRARY(ISPTABL)
00940000     SET &UPDTFLAG =
00950000     ISPEXEC SETMSG MSG(TASK005S)
00960000     ENDO
00970001   ELSE IF &CMD EQ IN THEN DO     /* INPUT  */
00980000     ISPEXEC CONTROL DISPLAY SAVE
00990000     DO WHILE &INPN GE 1
01000000       ISPEXEC TBVCLEAR TASKLIST
01010000       ISPEXEC DISPLAY PANEL(TASKUPD)
01020000       IF &LASTCC EQ 0 THEN DO
01030000         ISPEXEC TBADD TASKLIST ORDER
01040000         SET &UPDTFLAG = YES
01050000         ENDO
01060000       SET &INPN = &INPN-1
01070000       ENDO
01080000     ISPEXEC CONTROL DISPLAY RESTORE
01090000     ENDO
01100001   ELSE IF &CMD EQ NO THEN DO     /* NOTEBOOK  */
01110000     ISPEXEC CONTROL DISPLAY SAVE
01120000     IF &NBDSN EQ THEN DO /* EDIT NOTEBOOK DATASET */
01130000       ISPEXEC VGET (TASKNDSN) PROFILE
01140000       IF &LASTCC NE 0 OR &TASKNDSN EQ THEN +
01150000         ISPEXEC DISPLAY PANEL(TASKNDSN)
01160000       IF &LASTCC EQ 0 THEN DO
01170000         ISPEXEC VPUT (TASKNDSN) PROFILE
01180000         ERROR DO
01190000           ERROR DO
01200000             ISPEXEC SETMSG MSG(TASK006E)
01210000             GOTO SKPNBALC
01220000             ENDO
01230000           ATTR NBATTR RECFM(V B) LRECL(80) BLKSIZE(6233)
01240000           ALLOC F(N) DSN(&TASKNDSN($TASK$)) NEW REUSE +
01250000             SP(10 10) TRACKS DIR(5) USING(NBATTR)
01260000           FREE ATTR(NBATTR)
01270000           ERROR OFF
01280000           ENDO
01290000         ALLOC F(N) DSN(&TASKNDSN) SHR REUSE
01300000         SKPNBALC: ERROR OFF
01310000         FREE F(N)
01320000         ERROR OFF
01330000         ISPEXEC CONTROL ERRORS RETURN
01340000         ISPEXEC EDIT DATASET(&TASKNDSN)
01350000         IF &LASTCC GT 4 THEN ISPEXEC SETMSG MSG(TASK006T)
01360000         ISPEXEC CONTROL ERRORS CANCEL
01370000         ENDO
01380000       ENDO
01390000     ELSE DO
01400000       SET &TASKNDSN = &NBDSN
01410000       ISPEXEC VPUT (TASKNDSN) PROFILE
01420000       ENDO
01430000     ISPEXEC CONTROL DISPLAY RESTORE
01440000     ENDO
01450001   ELSE IF &CMD EQ PR THEN DO     /*  PRINT  */
01460000     ERROR DO
01470000       ERROR OFF
01480000       FREE ATTR(VBA)
01490000       ATTR VBA RECFM(V B A) LRECL(137) BLKSIZE(6233) DSORG(PS)
01500000       ALLOC F(ISPFILE) DSN(TASKTEMP.LISTING) REUSE +
01510000         USING(VBA) SP(1 1) TRACKS
01520000       SET &PRTDEL = DELETE
01530000       ENDO
01540000     ALLOC F(ISPFILE) DSN(TASKTEMP.LISTING) OLD REUSE
01550000     ERROR OFF
01560000     ISPEXEC SELECT PGM(X1A100)
01570000     ISPEXEC FTOPEN
01580000     ISPEXEC FTINCL TASKPRT
01590000     ISPEXEC FTCLOSE
01600000     IF &PRTDEST EQ THEN +
01610001       %GENPRINT DSN(TASKTEMP.LISTING) NODEL NOBRO
01620001     ELSE %GENPRINT DSN(TASKTEMP.LISTING) NODEL NOBRO GPDEST(&PRTDEST)
01630000     ISPEXEC SETMSG MSG(TASK003P)
01640000     ENDO
01650000   ELSE IF &ZTDSELS GE 1 THEN DO
01660000     IF &ACT EQ D THEN -
01670000       DO WHILE &N GE 1
01680000         ISPEXEC TBDELETE TASKLIST
01690000         ISPEXEC TBSKIP TASKLIST
01700000         /* CHECK RETURN CODE TO GUARD AGAINST DELETING TOO MANY */
01710000         IF &LASTCC NE 0 THEN SET &N = 0
01720000         ELSE SET &N = &N-1
01730000         SET &UPDTFLAG = YES
01740000         ENDO
01750000     ELSE IF &ACT EQ R THEN -
01760000       DO WHILE &N GE 1
01770000         ISPEXEC TBADD TASKLIST ORDER
01780000         SET &N = &N-1
01790000         SET &UPDTFLAG = YES
01800000         ENDO
01810000     ELSE IF &ACT EQ U THEN DO
01820000       ISPEXEC CONTROL DISPLAY SAVE
01830000       ISPEXEC DISPLAY PANEL(TASKUPD)
01840000       IF &LASTCC EQ 0 THEN DO
01850000         ISPEXEC TBPUT TASKLIST ORDER /* RESAVE ROW IN TABLE */
01860000         SET &UPDTFLAG = YES
01870000         ENDO
01880000       ISPEXEC CONTROL DISPLAY RESTORE
01890000       ENDO
01900000     ELSE IF &ACT EQ N THEN DO
01910000       ISPEXEC CONTROL DISPLAY SAVE
01920000       ISPEXEC VGET (TASKNDSN) PROFILE
01930000       IF &LASTCC NE 0 OR &TASKNDSN EQ THEN -
01940000         ISPEXEC DISPLAY PANEL(TASKNDSN)
01950000       IF &LASTCC EQ 0 THEN DO
01960000         ISPEXEC VPUT (TASKNDSN) PROFILE
01970000         ERROR DO
01980000           ERROR DO
01990000             ISPEXEC SETMSG MSG(TASK006E)
02000000             GOTO SKPNALC
02010000             ENDO
02020000           ATTR NBATTR RECFM(V B) LRECL(80) BLKSIZE(6233)
02030000           ALLOC F(N) DSN(&TASKNDSN) NEW REUSE +
02040000             SP(10 10) TRACKS DIR(5) USING(NBATTR)
02050000           FREE ATTR(NBATTR)
02060000           ERROR OFF
02070000           ENDO
02080000         ALLOC F(N) DSN(&TASKNDSN) SHR
02090000         SKPNALC: ERROR OFF
02100000         FREE F(N)
02110000         ERROR OFF
02120000         ISPEXEC EDIT DATASET(&TASKNDSN(&CATEGORY))
02130000         IF &LASTCC EQ 0 THEN ISPEXEC SETMSG MSG(TASK006S)
02140000         ELSE ISPEXEC SETMSG MSG(TASK006N)
02150000         ENDO
02160000       ISPEXEC CONTROL DISPLAY RESTORE
02170000       ENDO
02180000     ENDO
02190000   ENDO
02200000 LOOPEXIT: -
02210000 FREE F(ISPFILE) ATTR(VBA) &PRTDEL
02220001 IF &CMD EQ CA THEN DO          /* CANCEL  */
02230000   ISPEXEC TBEND TASKLIST
02240000   ISPEXEC SETMSG MSG(TASK002C)
02250000   ENDO
02260000 ELSE IF &UPDTFLAG EQ THEN DO
02270000   ISPEXEC TBEND TASKLIST
02280000   ISPEXEC SETMSG MSG(TASK002N)
02290000   ENDO
02300000 ELSE DO
02310000   ISPEXEC TBCLOSE TASKLIST REPLCOPY PAD(15) LIBRARY(ISPTABL)
02320000   ISPEXEC SETMSG MSG(TASK002S)
02330000   ENDO
