00010004PROC 1 DSN VOLUME() UNIT(3380) CATALOGED DROPDUMP NODROPDUMP DEBUG NODEBUG
00020000/*********************************************************************/
00030000/*                                                                   */
00040000/*  CLIST NAME: DUMPINIT               REL/DATE  2.00 / 04.11.80     */
00050000/*                                                                   */
00060000/*  FUNCTION: TO SETUP THE PROPER DUMP DATA SET FOR IPCS EXECUTION   */
00070000/*                                                                   */
00080000/*  SYMBOLS EQUATED: NONE                                            */
00090000/*                                                                   */
00100000/*  SYMBOLS USED: DSN     - DUMP DATA SET NAME                       */
00110000/*                DROPDUMP- OPTIONAL TO ISSUE DROPDUMP CMD           */
00120000/*                VOLUME  - OPTIONAL DUMP DATA SET VOLUME SERIAL     */
00130000/*                DEBUG   - OPTIONAL PARM FOR DEBUGING               */
00140000/*                                                                   */
00150000/*  RETURN: THIS CLIST WILL RETURN TO CALLER.                        */
00160000/*                                                                   */
00170000/*  CLIST CALLED BY: USER                                            */
00180000/*                                                                   */
00190000/*  CLISTS CALLED: NONE                                              */
00200000/*                                                                   */
00210000/*  DEPENDENCIES: MUST BE CALLED AS A SUBCOMMAND OF IPCS             */
00220000/*                                                                   */
00230000/*  USE: ISSUES A SETDEF FOR THE SPECIFIED DSN AND WILL              */
00240000/*       OPTIONALLY ISSUE A DROPDUMP AND                             */
00250000/*       ALLOCATE THE DSN IF THE VOLUME IS SPECIFIED                 */
00260000/*                                                                   */
00270000/*********************************************************************/
00280000/*  SET DEBUG OPTION IF SPECIFIED                                    */
00290000/*********************************************************************/
00300000    IF &DEBUG NE DEBUG THEN CONTROL MSG NOSYM NOCON NOLIST NOPROMPT
00310000    ELSE                  CONTROL MSG   SYM   CON   LIST   PROMPT
00320000/*********************************************************************/
00330000/*  EXEC DROPDUMP IF REQUESTED                                       */
00340000/*********************************************************************/
00350000    IF &DROPDUMP EQ AND &NODROPDUMP EQ THEN                            +
00360000      DO                                               /* DO LOOP #1 */
00370000        WRITENR IS A DROPDUMP OF &DSN NECESSARY ? Y/(N)
00380000        READ &ANS
00390000        IF &ANS EQ Y THEN SET &DROPDUMP = DROPDUMP
00400000      END                                              /* DO END  #1 */
00410000    IF &DROPDUMP EQ DROPDUMP THEN                                      +
00420000      DROPDUMP  &DSN
00430005      IF &LASTCC = 12 THEN DO
00440005                           DROPDUMP DSN(&DSN)
00450006                           END
00460000/*********************************************************************/
00470000/*  SET DEFAULT DSNAME                                               */
00480000/*********************************************************************/
00490000    %SETDEF0 DSNAME(&DSN.)
00500000/*********************************************************************/
00510000/*  ALLOCATE DATA SET IF VOLUME SPECIFIED                            */
00520000/*********************************************************************/
00530000    IF &VOLUME EQ  AND &CATALOGED EQ THEN                              +
00540000      DO                                               /* DO LOOP #2 */
00550000        WRITENR ENTER VOLSER OR NULL FOR CATALOG ALLOCATION ===>
00560000        READ &ANS
00570000        IF &ANS NE  THEN SET &VOLUME = &ANS
00580000      END                                              /* DO END  #2 */
00590000    IF &VOLUME NE  THEN                                                +
00600000      DO                                               /* DO LOOP #3 */
00610003        ALLOC DA(&DSN) SHR UNIT(&UNIT) VOLUME(&VOLUME)
00620003        WRITE DATA SET &DSN  ON &VOLUME (&UNIT) ALLOCATED
00630000      END                                              /* DO END  #3 */
00640000    ELSE
00650000      EQ DSN 0. LEN(4) REMARK('  OUTPUT FROM DUMP '&DSN'')
00660000    WRITE DATA SET &DSN  READY FOR USE
00670000    PGMCALL IEFBR14
00680000    EXIT
