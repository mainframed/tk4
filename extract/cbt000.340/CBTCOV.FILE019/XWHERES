00010004PROC     2     LOCATION       /* DECIMAL ADDRESS                    */ +
00020004               SPQECHAIN      /* IPCS POINTER TO SPQE CHAIN         */ +
00030004               DEBUG          /* PRODUCE FULL OUTPUT LISTING        */
00040004IF       &DEBUG = DEBUG +
00050004THEN     CONTROL CONLIST LIST MSG PROMPT SYMLIST
00060001GLOBAL   SUBPOOL KEY  FREE
00070000SET      &DQFQEPTR = 0N    /* DQE  -> FQE      */
00080000SET      &DQEPTR = 4N      /* DQE  -> DQE      */
00090000SET      &DQEBLKAD = 8N    /* DQE     START    */
00100000SET      &DQELNTH = 12N    /* DQE     LENGTH   */
00110000SET      &FBQAREA = 12N    /* FBQE    START    */
00120000SET      &FQAREA = 8N      /* FQE     END      */
00130000SET      &FQELNTH = 4N     /* FQE     LENGTH   */
00140000SET      &FQEPTR = 0N      /* FQE  -> FQE      */
00150000SET      &FWDPTR = 0N      /* FBQE -> FBQE     */
00160003SET      &HEX = 0123456789ABCDEF
00170000SET      &PQEFFBQE = 0N    /* PQE  -> FBQE     */
00180000SET      &PQESIZE = 20N    /* PQE     SIZE     */
00190000SET      &PQEREGN = 24N    /* PQE     START    */
00200000SET      &SIZE = 8N        /* FBQE    SIZE     */
00210000SET      &SPQEAD = 0N      /* SPQE -> SPQE     */
00220000SET      &SPDQEPTR = 4N    /* SPQE -> DQE      */
00230000SET      &SPQEID = 10N     /* SPQE    SUBPOOL  */
00240000SET      &SPQEKEY = 11N    /* SPQE    KEY      */
00250000SET      &AREA = INVALID
00260000SET      &PTRSPQE = &STR(&SPQECHAIN)
00270000COMPARE  ADDRESS(&PTRSPQE) WITH(VALUE(X'00000000')) LENGTH(4)
00280000IF       &LASTCC = 8 +
00290000THEN     SET   &SPQE = VALID
00300000ELSE     SET   &SPQE = INVALID
00310000SET      &FOUND = NO
00320000DO       WHILE &SPQE = VALID && &FOUND = NO /* SPQE LOOP */
00330005         EQUATE XSPQE &PTRSPQE? STRUCTURE(SPQE) LENGTH(16)
00340000         SET   &PTRDQE = &STR(XSPQE+&SPDQEPTR)
00350000         COMPARE ADDRESS(&PTRDQE) WITH(VALUE(X'00000000')) LENGTH(4)
00360000         IF    &LASTCC = 8 +
00370000         THEN  SET   &DQE = VALID
00380000         ELSE  SET   &DQE = INVALID
00390000         DO    WHILE &DQE = VALID && &FOUND = NO /* DQE LOOP */
00400005               EQUATE XDQE &PTRDQE? STRUCTURE(DQE) LENGTH(16)
00410000               EVALUATE XDQE+&DQEBLKAD LENGTH(4)
00420000               SET   &START = &LASTCC
00430000               EVALUATE XDQE+&DQELNTH LENGTH(4)
00440000               SET   &END=&START+&LASTCC
00450000               IF    &START <= &LOCATION && &LOCATION < &END +
00460000               THEN  SET   &FOUND = YES
00470000               SET   &PTRDQE = &STR(XDQE+&DQEPTR)
00480000               COMPARE ADDRESS(&PTRDQE) WITH(VALUE(X'00000000')) +
00490000                       LENGTH(4)
00500000               IF    &LASTCC ^= 8 +
00510000               THEN  SET   &DQE = INVALID
00520000               END   /* DQE LOOP */
00530000         SET   &PTRSPQE = &STR(XSPQE+&SPQEAD)
00540000         COMPARE ADDRESS(&PTRSPQE) WITH(VALUE(X'00000000')) LENGTH(4)
00550000         IF    &LASTCC ^= 8 +
00560000         THEN  SET   &SPQE = INVALID
00570000         END   /* SPQE LOOP */
00580000IF       &FOUND = YES +
00590000THEN     DO    /* DEFINE LOCATION */
00600000         EVALUATE XSPQE+&SPQEID LENGTH(1)
00610000         SET   &SUBPOOL = &LASTCC
00620000         EVALUATE XSPQE+&SPQEKEY LENGTH(1)
00630000         SET   &DEC = &LASTCC
00640000         SET   &KEY = &SUBSTR(&DEC/16+1,&HEX)&SUBSTR(&DEC//16+1,&HEX)
00650000         SET   &PTRFQE = &STR(XDQE+&DQFQEPTR)
00660000         COMPARE ADDRESS(&PTRFQE) WITH(VALUE(X'00000000')) LENGTH(4)
00670000         IF    &LASTCC = 8 +
00680000         THEN  SET   &FQE = VALID
00690000         ELSE  SET   &FQE = INVALID
00700000         SET   &FREE = NO
00710000         DO    WHILE &FQE = VALID && &FREE = NO /* FQE LOOP */
00720005               EQUATE XFQE &PTRFQE? STRUCTURE(FQE) LENGTH(16)
00730000               EVALUATE XFQE+&FQAREA LENGTH(4)
00740000               SET   &END = &LASTCC
00750000               EVALUATE XFQE+&FQELNTH LENGTH(4)
00760000               SET   &START = &END-&LASTCC
00770000               IF    &START <= &LOCATION && &LOCATION < &END +
00780000               THEN  SET   &FREE = YES
00790000               EVALUATE XFQE+&FQEPTR+1 LENGTH(3)
00800000               SET   &PTRFQE = &STR(XFQE+&FQEPTR)
00810000               COMPARE ADDRESS(&PTRFQE+1) WITH(VALUE(X'000000')) +
00820000                       LENGTH(3)
00830000               IF    &LASTCC ^= 8 +
00840000               THEN  SET   &FQE = INVALID
00850000               END   /*FQE LOOP */
00860000         EXIT  CODE(0)
00870000         END   /* DEFINE LOCATION */
00880000ELSE     DO    /* LOCATION NOT FOUND */
00890000         SET   &SUBPOOL =
00900000         SET   &KEY =
00910000         SET   &FREE =
00920000         EXIT  CODE(4)
00930000         END   /* LOCATION NOT FOUND */
