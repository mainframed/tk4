00010000         ISREDIT MACRO (TYPE) NOPROCESS
00020000  IF &TYPE = TEST THEN +
00030000    DO
00040000  WRITE &STR(TESTING . . . .)
00050000      CONTROL LIST CONLIST MSG
00060000    END
00070000/*********************************************************************/
00080000/*   BOB ZIMMERMAN                                                   */
00090000/*   CNA INSURANCE                                                   */
00100000/*   (312) 822-5719                                                  */
00110000/*   LAST MODIFIED 10/10/85 BY DAVE FAY                              */
00120000/*********************************************************************/
00130000/*       SETUP COMMENT ENVIRONMENT -
00140000/*           * THIS INCLUDES SETTING BOUNDS IN BY 3 COLS ON EACH SIDE
00150000/*           * WRITING A LINE OF A BOX FOR THE APPROPRIATE TYPE
00160000/*           * ENTERING TF LINE MODE FOR THE USER
00170000/*           * WHEN 'OFF' IS INDICATED, BOUNDS ARE RESET AND BOTTOM
00180000/*           *   OF BOX IS DRAWN
00190000/*********************************************************************/
00200000 
00210000         ISPEXEC CONTROL ERRORS CANCEL
00220000 
00230000SETOFF:  IF &STR(&TYPE) = OFF THEN +
00240000             DO /* FINISH OFF THE COMMENT      */
00250000             ISREDIT PROCESS
00260000             ISREDIT (LEFT,RIGHT) = BOUNDS /* FIND THE CURRENT BOUNDS*/
00270000             SET &LEFT = &LEFT - 3         /* AND SET OUT 3          */
00280000             SET &RIGHT = &RIGHT +3
00290000 
00300000             IF &LEFT < 0 OR &RIGHT < 0 THEN +
00310000                 DO
00320000                 SET ZEDSMSG = &STR(CURRENT BOUNDS INVALID)
00330000                 SET ZEDLMSG = &STR(UNABLE TO RESET BOUNDS FROM +
00340000                 CURRENT VALUE)
00350000                 ISPEXEC SETMSG MSG(ISRZ001)
00360000                 ISREDIT MASKLINE = (SAVEMASK)
00370000                 EXIT CODE(12)
00380000                 END
00390000             ISREDIT BOUNDS = &LEFT &RIGHT   /* RESET BOUNDS  */
00400000             ISPEXEC VGET (LINE SAVEMASK)    /* PREVIOUSLY SAVED */
00410000             ISREDIT MASKLINE = (SAVEMASK)   /* RESET MASK    */
00420000             ISREDIT (IROW) = CURSOR         /* WHERE'S THE CURSOR? */
00430000             IF &LASTCC = 0 THEN +
00440000               ISREDIT LINE_AFTER &IROW = DATALINE '&LINE'
00450000             ISREDIT CURSOR = &EVAL(&IROW+1),0 /* INSERT BOX BOTTOM  */
00460000             EXIT CODE(0)                      /* AND POSITION CURSOR*/
00470000             END
00480000 
00490000         ELSE +
00500000             DO                                /* NOT COMMENT OFF    */
00510000             ISREDIT (LEFT,RIGHT) = BOUNDS     /* FIND THE BOUNDS    */
00520000             SET &LEFT = &LEFT + 3             /* AND RESET IN 3     */
00530000             SET &RIGHT = &RIGHT -3
00540000             END
00550000 
00560000         ISREDIT PROCESS DEST                  /* FIND THE 'A' OR 'B'*/
00570000         IF &LASTCC ^= 0 THEN +
00580000             DO                                /* NO 'A' OR 'B' GIVEN*/
00590000             SET ZEDSMSG = &STR(COMMENT COMMAND PENDING)
00600000             SET ZEDLMSG = &STR(ENTER AN 'A' OR 'B' LINE COMMAND WITH +
00610000             FOR 'COMMENT' PRIMARY COMMAND)
00620000             ISPEXEC SETMSG MSG(ISRZ001)
00630000             EXIT CODE(12)
00640000             END
00650000         ISREDIT (SAVEMASK) = MASKLINE         /* PRESERVE THE MASK  */
00660000 
00670000         IF &STR(&TYPE) = THEN +
00680000             DO                                /* NO TYPE SPECIFIED  */
00690000             ISREDIT (TYPE) = PROFILE          /* GET IT FROM DSNAME */
00700000             END
00710000 
00720000/**********************************************************************/
00730000/* CHECK FOR SUFFIX "ALIASES" AND SET TO THE SUPPORTED TYPE NAMES     */
00740000/*                                                                    */
00750000/**********************************************************************/
00760000         IF &STR(&TYPE) = PANELS THEN SET TYPE = CLIST
00770000         IF &STR(&TYPE) = PLIB THEN SET TYPE = CLIST
00780000         IF &STR(&TYPE) = ISPPLIB THEN SET TYPE = CLIST
00790000         IF &STR(&TYPE) = ASMB THEN SET TYPE = ASM
00800000         IF &STR(&TYPE) = BAL THEN SET TYPE = ASM
00810000         IF &STR(&TYPE) = CNTL THEN SET TYPE = JCL
00820000 
00830000/**********************************************************************/
00840000/* NOW CHECK FOR THE DIFFERENT DATASET TYPES AND GENERATE THE CORRECT */
00850000/* "BOX"LINE; SET THE MASK FOR COMMENTS.                              */
00860000/**********************************************************************/
00870000         IF &STR(&TYPE) = CLIST THEN +
00880000             DO
00890000             IF &RIGHT > 69 THEN SET RIGHT = 69
00900000             ISREDIT MASKLINE = <1 '/*' 71 '*/'>
00910000             SET &LINE =        &STR(/*****************************+
00920000*****************************************/        )
00930000             GOTO FLOWIT
00940000             END
00950000 
00960000         IF &STR(&TYPE) = ASM THEN +
00970000             DO
00980000             IF &RIGHT > 69 THEN SET RIGHT = 69
00990000             ISREDIT MASKLINE = <1 '*' 72 '*'>
01000000             SET &LINE =        &STR(******************************+
01010000******************************************        )
01020000             GOTO FLOWIT
01030000             END
01040000 
01050000         IF &STR(&TYPE) = COBOL THEN +
01060000             DO
01070000             IF &RIGHT > 69 THEN SET RIGHT = 69
01080000             ISREDIT MASKLINE = <1 '*' 72 '*'>
01090000             SET &LINE =        &STR(************************+
01100000************************************************  )
01110000             GOTO FLOWIT
01120000             END
01130000 
01140000         IF &STR(&TYPE) = JCL THEN +
01150000             DO
01160000             IF &RIGHT > 69 THEN SET RIGHT = 69
01170000             ISREDIT MASKLINE = <1 '//*' 71 '*'>
01180000             SET &LINE =        &STR(//* * * * * * * * * * * * * * +
01190000* * * * * * * * * * * * * * * * * * * * *         )
01200000             GOTO FLOWIT
01210000             END
01220000 
01230000         IF &STR(&TYPE) = PLI THEN +
01240000             DO
01250000             IF &RIGHT > 69 THEN SET RIGHT = 69
01260000             ISREDIT MASKLINE = <2 '/*' 71 '*/'>
01270000             SET &LINE =        &STR( /****************************+
01280000*****************************************/        )
01290000             GOTO FLOWIT
01300000             END
01310000         IF &STR(&TYPE) = FORT THEN +
01320000             DO
01330000             IF &RIGHT > 69 THEN SET RIGHT = 69
01340000             ISREDIT MASKLINE = <1 'C' 72 '*'>
01350000             SET &LINE =        &STR(C*****************************+
01360000******************************************        )
01370000             GOTO FLOWIT
01380000             END
01390000/*  IF WE GET HERE, THE TYPE WAS NOT A VALID ONE                     */
01400000         SET ZEDSMSG = &STR(INVALID COMMENT TYPE)
01410000         SET ZEDLMSG = &STR(VALID TYPES ARE CLIST, PANEL, +
01420000         COBOL, JCL, ASM && FORT)
01430000         ISPEXEC SETMSG MSG(ISRZ001)
01440000         EXIT CODE(12)
01450000 
01460000FLOWIT:  +
01470000         ISPEXEC VPUT (LINE SAVEMASK) SHARED  /* SAVE FOR LATER RESTORE
01480000         ISREDIT (DEST) = LINENUM .ZDEST      /* FIND WHERE IT GOES
01490000         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE' /* INSERT BOXTOP
01500000         ISREDIT BOUNDS &LEFT &RIGHT          /* RESET NEW BOUNDS
01510000         ISREDIT TENTER &EVAL(&DEST + 1)      /* PUT USER IN "TE"
01520000         ISREDIT LOCATE &DEST     /* SET TOP LINE DISPLAY ON SCREEN */
01530000         EXIT CODE(0)
