00010027PROC 0 PARM() DEBUG
00020000/** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** ** ** **/
00030000/** Used by ROCKWELL ISPF primary command: LISTA           **/
00040000/** BY: Michael E. Theys   Rockwell International/SWCC     **/
00050000/** Revised version of GUIDE/ISPF  LST0A PGM/Panels.       **/
00060000/** Table display of allocated files/datasets              **/
00070000/** w/ Browse/Edit/PDS/FREE/ENQ and LISTSP command use.    **/
00080000/** -- -- -- -- -- -- -- -- -*- -- -- -- -- -- -- -- -- -- **/
00090000/** Full-Screen LIST ALLOACTED Files Command:              **/
00100000/**   SELECT CMD(%FSLISTA PARM('&ZPARM')) NEWAPPL(ISR)     **/
00110000/** -- -- -- -- -- -- -- -- -*- -- -- -- -- -- -- -- -- -- **/
00120000/** "LISTA" command w/o operand shows all Allocated files  **/
00130000/** "LISTA ddname" only shows files allocated to ddname    **/
00140000/** "LISTA ddname1 ddname2" only files alloc to dd1 and dd2**/
00150000/** "LISTA FULL"   shows expanded list of allocated dsnames**/
00160000/** "LISTA ST SYS" TSO LISTA operands invokes TSO LISTA    **/
00170027/** "LISTA !"  Show LISTA Entry menu                       **/
00180027/** "LISTA ?"  Show tutorial                               **/
00190000/** Default lists only shows first dsname per alloc ddname **/
00200000/** This clist interface will show table display in last   **/
00210000/**   used format of FULL or COMPRESS.                     **/
00220000/** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** ** ** **/
00230000IF &DEBUG = DEBUG THEN  CONTROL   MSG   LIST   CONLIST   SYMLIST
00240000ELSE                    CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST
00250000 
00260024IF &STR(&PARM) = &STR(?) THEN DO
00270024  ISPEXEC SELECT PGM(ISPTUTOR) PARM($LISTALC)
00280024  EXIT
00290024END
00300026IF &STR(&PARM) = &STR(!) THEN GOTO SHOW
00310024 
00320008ISPEXEC VGET (LST0APRM) PROFILE
00330000IF &STR(&PARM) = FULL THEN DO
00340012  SET TYPE     = ISPF
00350000  SET LST0APRM = FULL
00360012  SET PARM     =
00370000  ISPEXEC VPUT (LST0APRM) PROFILE
00380000END
00390000 
00400000/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
00410000/* See if user wanted TSO LISTA with its operands (STATUS SYSTEM etc)*/
00420012/* ISPF Command operands can be: DDNAME | DDN1 DDN2 | DDN1* DDN2*    */
00430000/* So see if PARM is an allocated DDNAME or not.                     */
00440000/* PARM can be multiple DDNAMEs. Some may not be allocated           */
00450000/* WHATIF only checks first ddname in PARM                           */
00460000/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
00470024IF &SYSINDEX(&STR(*),&STR(&PARM)) > 0 THEN SET TYPE = ISPF
00480000IF &TYPE ^= ISPF THEN DO
00490009  CONTROL NOFLUSH
00500012  WHATIF FILE(&STR(&PARM)) ALLOCATED
00510023  SET FARC = &LASTCC
00520024  IF &FARC = 0  THEN    SET TYPE = ISPF
00530000END
00540012IF &STR(&PARM) =   THEN SET TYPE = ISPF
00550012 
00560012IF &TYPE ^= ISPF THEN DO
00570012  /* So far can't tell if operands are for TSO or ISPF command */
00580015  SET TYPE = TSO
00590012  %ECMD LISTA OPERAND('&STR(&PARM)') NOGO
00600022  SET LARC = &LASTCC
00610022  IF &LARC > 0 THEN SET TYPE = ISPF
00620020  ISPEXEC VGET (ECMDEXIT)
00630021  IF &ECMDEXIT = RETURN THEN EXIT
00640012END
00650012 
00660013SET FSLARAND = &STR(&PARM)
00670015SET FSLATYPE = &STR(&TYPE)
00680016ISPEXEC VPUT (FSLATYPE,FSLARAND) SHARED
00690015 
00700012IF &TYPE = ISPF THEN ISPEXEC CONTROL NONDISPL ENTER
00710028SHOW: ISPEXEC SELECT PANEL(FSLISTA)
