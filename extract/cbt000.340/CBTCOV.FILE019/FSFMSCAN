00010000PROC 0 FMLIB() FMPROC() FMDDNAME() FMMEMBER() FMMANY() FMFINDBP() +
00020000       FMUSRPRM() FMVERS() WRKSYMBL(YES) WIPSN() DEBUG
00030000 
00040000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00050000/**                                                                **/
00060000/**   FULL-SCREEN FIND MEMBER IN SYSTEM LIBRARY  VERSION 4.        **/
00070000/**                                                                **/
00080000/** MICHAEL E. THEYS                                               **/
00090000/** INFORMATION SYSTEMS CENTER - SWCC                              **/
00100000/** ROCKWELL INTERNATIONAL CORPORATION                             **/
00110000/** MAIL/STATION 401-109                                           **/
00120000/** 1200 N. ALMA RD                                                **/
00130000/** RICHARDSON, TEXAS  75081                                       **/
00140000/** (214) 996-2860           COMNET 437-2860                       **/
00150000/**                                                                **/
00160000/** == == == == == == == == == == == == == == == == == == == == == **/
00170000/** PANELS: FSFMMENU, FSFMMANY, FSFMDSP1/FSFMDSP2.                 **/
00180000/**         MODIFIED PDS 7.4 PANELS: PDSPN1,PDSPN5,PDSPN7.         **/
00190000/** MSGS:   FSFM01.                                                **/
00200000/** CLIST:  FSFMSCAN,FSFMLINK,FSFMLPA,FSFMPROC,FSFMDRDS,FSFMUCC7.  **/
00210000/**         FSFMTWRL (TO DISPLAY "WORKING" TWRILL SYMBOLS.)        **/
00220000/** USES: SHOWTSO TO FIND CENTER,CPU INFORMATION                   **/
00230000/**       XATST   TO FIND MVS OR XA (COULD USE ISPF &ZENVIR)       **/
00240002/** USES: KDSN: SYSOUTTRAP USABLE VERS OF KDSN FROM G. ANDERSON    **/
00250000/**   OR: CNA0KBR: GUIDE/AOST SCAN DDNAME FOR MEMBER DIALOG        **/
00260000/** USES: LINKLIST COMMAND TO FIND MVS/XA LINK LIST DATASETS       **/
00270000/**       (SEE %FSFMLINK)                                          **/
00280006/** USES: WAIT5,VSCAN                                              **/
00290000/** == == == == == == == == == == == == == == == == == == == == == **/
00300000/** TAILOR THE FOLLOWING FOR YOUR CENTER:                          **/
00310000/** THE FOLLOWING CLISTS ALLOCATE SYSTEM LIBRARIES FOR:            **/
00320000/**  LINKLIST:      %FSFMLINK                                      **/
00330000/**  LPALIBS:       %FSFMLPA                                       **/
00340000/**  JES3 PROCLIBS: %FSFMPROC                                      **/
00340106/**   (CURENTLY FSFMSCAN DEFINES THE "ALL" VALUES.)                **/
00350000/**  JES3 DISKRDR:  %FSFMDRDS                                      **/
00360000/**  UCC7 JOBS:     %FSFMUCC7                                      **/
00370000/**  USER LIBS:     %FSFMUSER                                      **/
00380000/** PARAMATERS PASSED TO THE CLISTS INCLUDE:                       **/
00390000/**  CENTER NAME, MVS TYPE, CPU NAME, OPTIONAL PARM,               **/
00400000/**  AND DDNAME TO ALLOCATE FOR %FSFMSCAN.                         **/
00410000/** == == == == == == == == == == == == == == == == == == == == == **/
00420000/**                                                                **/
00430006/** FUTURE ENHANCEMENTS: (VERSION 5)                               **/
00440006/** 1) MEMBER ATTRIBUTE SEARCH (IE FIND MEMBERS LKED RENT)         **/
00450006/** 2) MEMBER DATE SEARCH (LIST MEMBERS CHANGED BETWEEN DATES)     **/
00460006/** 3) FIND CHARACTER/HEX STRING IN MEMBER/MODULES.                **/
00470006/** ITEMS #1-3 IN PROGRESS FOR VERSION 5.                          **/
00480000/**                                                                **/
00490000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00500000 
00510000CONTROL NOFLUSH
00520000SET &SYSOUTTRAP = 0
00530000IF &DEBUG = DEBUG THEN  CONTROL   MSG PROMPT LIST CONLIST SYMLIST
00540000ELSE                    CONTROL NOMSG
00550000 
00560000/* SELECT WHICH VERSION TO USE FOR SCANS:          */
00570002/* KDSN:   USE SYSOUTTRAP COMPATIBLE VERS (KDSN)   */
00580000/* KBR:    USE AOST/GUIDE PGM(CNA0KBR) DIALOG (I.E. DDBR ISPF CMD) */
00590000IF      &FMVERS =      THEN SET FMVERS = KDSN
00600000ELSE IF &FMVERS = KDSN THEN SET FMVERS = KDSN
00610000ELSE IF &FMVERS = KBR  THEN SET FMVERS = KBR
00620000ELSE                        SET FMVERS = KDSN
00630000 
00640000XATST
00650000IF &LASTCC = 0 THEN SET MVS = XA
00660000ELSE                SET MVS = 370
00670000 
00680000/* MAKE SURE THAT %SYSISPF CPU/MVS/CENTER VARIABLES ARE SET */
00690000SHOWTSO CENTER SYSNAME
00700000SET Z$CTR   = &CENTER
00710000SET Z$CPU   = &SUBSTR(1:1,&SYSNAME.)
00720000SET Z$CPUOS = &STR(&Z$CPU./&MVS)
00730000 
00740000IF &FMLIB = DD AND &STR(&FMDDNAME) = &STR(?) THEN DO
00750000  %FSFMECMD LISTA OPERAND('ST SYS H') MAXLINES(300)
00760000  EXIT CODE(0)
00770000END
00780000 
00790000/* SEE IF GENERIC MEMBER OR SINGLE MEMBER */
00800000SET WLINE = &STR(&FMMEMBER)
00810000VSCAN WLINE '('
00820000IF &LASTCC > 0 THEN SET GENERIC = YES
00830000VSCAN WLINE ')'
00840000IF &LASTCC > 0 THEN SET GENERIC = YES
00850000VSCAN WLINE ','
00860000IF &LASTCC > 0 THEN SET GENERIC = YES
00870000VSCAN WLINE ':'
00880000IF &LASTCC > 0 THEN SET GENERIC = YES
00890000VSCAN WLINE '/'
00900000IF &LASTCC > 0 THEN SET GENERIC = YES
00910000/* MEMBER PREFIX NOTATION XX* CONVERTED TO XX:XX BY PANEL FSFMMENU */
00920000 
00930000IF &STR(&FMMEMBER) ^= &STR(?) THEN DO
00940000  IF &GENERIC = YES AND &FMVERS = KBR THEN DO
00950000      SET SHORT = &STR()
00960000      SET LONG  = &STR(WARNING: SCAN DISPLAY FORMAT "ONLY" IS FOR SIMPLE MEMBER NAME SEARCHES.)
00970000      ISPEXEC CONTROL DISPLAY LOCK
00980000      ISPEXEC DISPLAY MSG(FSFM019)
00990000      SET KBRMSG1  = YES
01000000      WAIT5
01010000  END
01020000  IF &FMFINDBP ^=   AND &FMVERS = KBR THEN DO
01030000      SET SHORT = &STR()
01040000      SET LONG  = &STR(WARNING: XEQ COMMAND OPTION IS FOR SCAN DISPLAY FORMAT "STATUS".)
01050000      ISPEXEC CONTROL DISPLAY LOCK
01060000      ISPEXEC DISPLAY MSG(FSFM019)
01070000      SET KBRMSG2  = YES
01080000      WAIT5
01090000  END
01100000  IF &KBRMSG1 = YES OR (&KBRMSG2 = YES AND &GENERIC = YES) THEN DO
01110000      SET SHORT = &STR()
01120000      SET LONG  = &STR(FIND MEMBER SCAN IS CONTINUING WITH THE FULL "STATUS" DISPLAY FORMAT.)
01130000      ISPEXEC CONTROL DISPLAY LOCK
01140000      ISPEXEC DISPLAY MSG(FSFM019)
01150000      WAIT5
01160000  END
01170000  IF (&KBRMSG2 = YES AND &GENERIC ^= YES) THEN DO
01180000      SET SHORT = &STR()
01190000      SET LONG  = &STR(CONTINUING WITH THE "ONLY" DISPLAY FORMAT. XEQ COMMAND IS BEING IGNORED.)
01200000      ISPEXEC CONTROL DISPLAY LOCK
01210000      ISPEXEC DISPLAY MSG(FSFM019)
01220000      WAIT5
01230000  END
01240000  IF (&GENERIC = YES AND &FMFINDBP = BROWSE) THEN DO
01250000      SET SHORT = &STR()
01260000      SET LONG  = &STR(CURRENTLY ONLY PDS COMMAND WITH MEMLIST IS SUPPORTED FOR MEMBER GROUP SEARCHES.)
01270000      ISPEXEC CONTROL DISPLAY LOCK
01280000      ISPEXEC DISPLAY MSG(FSFM019)
01290000      WAIT5
01300000      SET LONG  = &STR(TO BROWSE FROM MEMLIST ENTER LINECMD B BEFORE MEMBER OR PRIMARY CMD "BROWSE *")
01310000      ISPEXEC CONTROL DISPLAY LOCK
01320000      ISPEXEC DISPLAY MSG(FSFM019)
01330000      WAIT5
01340000  END
01350000END
01360001IF &GENERIC = YES THEN DO
01370001  SET &FMVERS = KDSN
01380001END
01390000 
01400000IF &FMMANY ^= THEN DO
01410000  IF       &FMMANY = CMD THEN DO
01420000    SET MANYMAX = 5
01430000    SET LOADLIBS = &STR(DD      DD      LPA     LINK    DD      - END - .)
01440000    SET LOADDDNS = &STR(ISPLLIB STEPLIB                 SYSPROC         .)
01450000  END
01460000  ELSE IF  &FMMANY = LOAD THEN DO
01470000    SET MANYMAX = 4
01480000    SET LOADLIBS = &STR(DD      DD      LPA     LINK    - END - .)
01490000    SET LOADDDNS = &STR(ISPLLIB STEPLIB                         .)
01500000  END
01510000  /***  ***  ***  ***  ***  ***  ***  ***  ***  ***  ***  ***  ***  ***/
01520000  /*** FUTURE: UPDATE %FSFMPROC  AND  %FSFMSCAN  TO SET ISPF VARS   ***/
01530000  /***   TO INITIALIZE THE PROC=ALL SEARCH OPTION.                  ***/
01540000  /***   THEN THERE SHOULD BE NO CENTER DEPENDENCIES IN THE         ***/
01550000  /***   PANELS/MSGS/%FMFSCAN CLIST.                                ***/
01560000  /***   CENTER DEPENDENCES WILL ONLY BE IN THE %FSFM&LIB CLISTS    ***/
01570000  /***   WHERE &LIB IS LINK, LPA, PROC, UCC7, AND DRDS.             ***/
01580000  /***  ***  ***  ***  ***  ***  ***  ***  ***  ***  ***  ***  ***  ***/
01590000  ELSE IF &FMMANY=PROC AND &CENTER=SWCC THEN DO
01600000    SET MANYMAX = 7
01610000    SET PROCLIBS = &STR(PROC    PROC    PROC    PROC    PROC    PROC    PROC    - END - .)
01620000    SET PROCXX   = &STR(MC      MX      DC      ST      TT      SY      TS              .)
01630000  END
01640000  ELSE IF &FMMANY=PROC AND &CENTER=SWC2 THEN DO
01650000    SET MANYMAX = 9
01660000    SET PROCLIBS = &STR(PROC    PROC    PROC    PROC    PROC    PROC    PROC    PROC    PROC    - END - .)
01670000    SET PROCXX   = &STR(AA      LL      QU      ST      MC      MX      SY      TU      TS              .)
01680000  END
01690000  SET FMLIMSGE =
01700000  SET MK = 0
01710000END
01720000 
01730000SET FINDMANY = 16
01740000SET DSPPNL = FSFMDSP1
01750000MANYCHK:+
01760000IF &FMMANY ^= THEN DO
01770000  IF &DDRC = 0   THEN SET FMLIS&MK =     DONE
01780000  ELSE DO
01790000    SET FMLIS&MK = &STR(NOT ALLOCATED)
01800000    SET MANYFMRC = 16
01810000  END
01820000  IF &STR(&FMMEMBER) = &STR(?) THEN SET QM = LISTED
01830000  ELSE                              SET QM = FOUND
01840000  IF &MANYFMRC=0 THEN DO
01850000    SET FINDMANY = 0
01860000    SET    FMLIM&MK =          &QM
01870000  END
01880000  ELSE SET FMLIM&MK = &STR(NOT &QM)
01890000  SET MK = &MK + 1
01900000  SET ME = &EVAL(8 * &MK)
01910000  SET MB = &EVAL(&ME - 7)
01920000  IF      &FMMANY = LOAD OR &FMMANY = CMD THEN DO
01930000    SET FMLIB    = &SUBSTR(&MB:&ME,&STR(&LOADLIBS))
01940000    SET FMDDNAME = &SUBSTR(&MB:&ME,&STR(&LOADDDNS))
01950000    IF      &STR(&FMLIB)  = DD THEN SET FMLI&MK = &FMDDNAME
01960000    ELSE IF &STR(&FMLIB) ^= DD THEN SET FMLI&MK = &STR(&FMLIB)
01970000  END
01980000  ELSE IF &FMMANY = PROC THEN DO
01990000    SET FMLIB   = &SUBSTR(&MB:&ME,&STR(&PROCLIBS))
02000000    SET FMPROC  = &SUBSTR(&MB:&ME,&STR(&PROCXX))
02010000    SET FMLI&MK = &STR(PROC=&FMPROC)
02020000  END
02030000  SET FMLIS&MK = WORKING
02040000  SET FMLIM&MK =
02050000  IF &MK >  &MANYMAX THEN DO
02060000    SET FMMANY =
02070000    SET FMLI&MK = &STR(- END -)
02080000    SET FMLIS&MK =
02090000    SET FMLIMSGE = &STR(PRESS  END  KEY TO TERMINATE.)
02100000    IF &FINDMANY = 0 THEN SET FMLIMSGS = &STR( FOUND!)
02110000    ELSE                  SET FMLIMSGS = &STR(NO FIND)
02120000    SET &SHORT = &STR(SEARCH DONE! &FMLIMSGS.)
02130000    IF &FINDMANY = 0 THEN SET &LONG = &STR(&FMMEMBER WAS FOUND IN AT LEAST ONE LIBRARY.)
02140000    ELSE                  SET &LONG = &STR(&FMMEMBER WAS  NOT  FOUND IN ANY SEARCH LIBRARY.)
02150000    ISPEXEC SETMSG MSG(FSFM010)
02160000  END
02170000  ELSE ISPEXEC CONTROL DISPLAY LOCK
02180000  ISPEXEC DISPLAY PANEL(FSFMMANY)
02190000END
02200000 
02210000IF &STR(&FMLIB) = &STR(- END -) THEN DO
02220000  EXIT
02230000END
02240000 
02250000IF &STR(&FMLIB) = DD THEN DO
02260000  SET LIBRARY = &FMDDNAME
02270000  WHATIF FILE(&LIBRARY) ALLOCATED
02280000  SET DDRC = &LASTCC
02290000  IF &DDRC ^= 0 AND &FMMANY   ^=    THEN GOTO MANYCHK
02300000  IF &DDRC ^= 0 AND &STR(&FMLIB) = DD THEN DO
02310000    SET SHORT = &STR(&FMDDNAME NOT ALLOCATED!)
02320000    SET LONG  = &STR(FILE &FMDDNAME WAS NOT PRE-ALLOCATED. USE TSO ALLOC FI(&FMDDNAME) COMMAND.)
02330000    ISPEXEC SETMSG MSG(FSFM010)
02340000    EXIT
02350000  END
02360000END
02370000ELSE DO /* INVOKE APPROPRIATE FSFM&FMLIB CLIST TO ALLOCATE SEARCH DSNS */
02380000  SET LIBRARY = FSFM&FMLIB
02390000  IF &FMMANY =  THEN DO
02400000    SET SHORT =
02410000    SET &FMLIBMSG = &FMLIB
02420000    IF      &FMLIB = PROC THEN SET &FMLIBMSG = &STR(PROC=&FMPROC.)
02430000    ELSE IF &FMLIB = USER THEN SET &FMLIBMG2 = &STR( WITH PARM=&FMUSRPRM.)
02440000    ELSE                       SET &FMLIBMG2 =
02450000    SET LONG  = &STR(NOW ALLOCATING &FMLIBMSG DATASETS&FMLIBMG2..)
02460000    ISPEXEC CONTROL DISPLAY LOCK
02470000    ISPEXEC DISPLAY MSG(FSFM019)
02480000  END
02490000  %FSFM&FMLIB &DEBUG +
02500000              ALLOCDD(&LIBRARY) +
02510000              CENTER(&Z$CTR) CPU(&Z$CPU) MVS(&MVS) +
02520000              PARM('&FMPROC.&FMUSRPRM')
02530000  SET DDRC = &LASTCC.
02540000  IF &STR(&FMUSRPRM) = &STR(?) THEN EXIT CODE(&DDRC.)
02550000  IF &FMMANY =  AND &DDRC > 0   THEN DO
02560000    SET SHORT =
02570000    SET &FMLIBMSG = &FMLIB
02580000    IF      &FMLIB = PROC THEN SET &FMLIBMSG = &STR(PROC=&FMPROC.)
02590000    ELSE IF &FMLIB = USER THEN SET &FMLIBMG2 = &STR( WITH PARM=&FMUSRPRM.)
02600000    ELSE                       SET &FMLIBMG2 =
02610000    SET LONG  = &STR(&FMLIBMSG DATASETS&FMLIBMG2. WERE  NOT  ALLOCATED!)
02620000    ISPEXEC CONTROL DISPLAY LOCK
02630000    ISPEXEC DISPLAY MSG(FSFM019)
02640000  END
02650000END
02660000 
02670000IF &GENERIC ^= YES            THEN SET SRCHPARM = &STR(&LIBRARY.(&FMMEMBER.))
02680000IF &STR(&FMMEMBER.) = &STR(?) THEN SET SRCHPARM = &STR(&LIBRARY.)
02690000 
02700000IF &FMVERS = KDSN THEN DO
02710000  CONTROL   NOLIST NOCONLIST NOSYMLIST
02720000  SET &SYSOUTTRAP = 35
02730000  /* USE VERSION OF AOST/GUIDE KDSN TSO COMMAND THAT ALLOWS SYSOUTTRAP TO WORK */
02740002  KDSN &LIBRARY
02750000  SET &SYSOUTTRAP =  0
02760000  IF &DEBUG = DEBUG THEN  CONTROL   MSG PROMPT LIST CONLIST SYMLIST
02770000  SET &TRAPKNT = &SYSOUTLINE - 1
02780000  IF &TRAPKNT <= 1 THEN DO
02790000    SET &TEMP    = &&SYSOUTLINE1
02800000    SET &KDSNMSG = &TEMP
02810002    SET SHORT = &STR(** FSFM KDSN ERROR **)
02820000    SET LONG  = &STR(&KDSNMSG OR NO PDS WAS ALLOCATED TO &LIBRARY..)
02830000    ISPEXEC SETMSG MSG(FSFM010)
02840000    IF &FMMANY  = THEN EXIT
02850000    IF &FMMANY ^= THEN DO
02860000      SET &DDRC = 16
02870000    END
02880000  END
02890000  ELSE SET &DDRC = 0
02900000  /***** INITIALIZE THE "WORKING" TWRILL SYMBOL *****/
02910000  IF &WRKSYMBL = YES THEN  %FSFMTWRL OPT(INIT) NUMBER('&WIPSN.')
02920000  /***** INITIALIZE THE "WORKING" TWRILL SYMBOL *****/
02930000  SET MANYFMRC = 16
02940002  /****** BECAUSE OF USING TWO SYSOUTTRAPS SAVE KDSN IN OTHER VARS */
02950000  SET &K = 2
02960000  DO WHILE ( &K <= &TRAPKNT )
02970000    SET  TEMPLINE   = &&SYSOUTLINE&K
02980000    SET  KDSNLINE&K = &TEMPLINE
02990000    SET &K = &K + 1
03000000  END
03010000  SET &K = 2
03020000  SET &DKMAX = 0
03030000  DO WHILE ( &K <= &TRAPKNT )
03040000    SET &DK = &EVAL(&K-1)
03050000    SET &DKMAX = &DK
03060000    IF &DK <= 16 THEN SET DSPPNL = FSFMDSP1
03070000    ELSE              SET DSPPNL = FSFMDSP2
03080000    SET &TEMP = &&KDSNLINE&K
03090000    SET &KLIB = &TEMP
03100000    SET FMDS&DK = '&KLIB'
03110000    IF &STR(&FMMEMBER) ^= &STR(?) THEN DO
03120000      SET FMDSS&DK = SEARCHING
03130000    END
03140000    ELSE SET FMDSS&DK = WORKING
03150000    /***** SET TWRILL "WORKING" SYMBOL *****/
03160000    IF &WRKSYMBL = YES THEN  %FSFMTWRL OPT(WORK) NUMBER('&WIPSN.')
03170000    /***** SET TWRILL "WORKING" SYMBOL *****/
03180000    ISPEXEC CONTROL DISPLAY LOCK
03190000    ISPEXEC DISPLAY PANEL(&DSPPNL)
03200000    SET SEARCH =
03210000    IF &STR(&FMMEMBER) ^= &STR(?) THEN DO
03220000      IF &GENERIC = YES THEN DO
03230000        /*ISPEXEC CONTROL DISPLAY NONDISPL END*/
03240000        SET FSFMGMEM = YES
03250000        ISPEXEC VPUT (FSFMGMEM) PROFILE
03260000        SET &PDSDBUG = &STR(PDS '&KLIB' MEMLIST &FMMEMBER.)
03270000        SET &SYSOUTTRAP = 5
03280000        ISPEXEC SELECT PGM(PDS) PARM(PDS '&KLIB' MEMLIST &STR(&FMMEMBER.))
03290000        SET &SYSOUTTRAP = 0
03300000        SET PDSTRAP = &SYSOUTLINE
03310000        SET FSFMGMEM =
03320000        ISPEXEC VPUT (FSFMGMEM) PROFILE
03330000        IF &PDSTRAP > 0 THEN DO
03340000          SET TEMP   = &&SYSOUTLINE1
03350000          SET PDSMSG = &STR(&TEMP)
03360000          SET L = &LENGTH(&STR(&PDSMSG))
03370000          IF &L >= 33 THEN DO
03380000            IF &SUBSTR(1:7,&STR(&PDSMSG)) = PDS401W | &SUBSTR(1:33,&STR(&PDSMSG)) = &STR(NO MEMBER NAMES ARE IN THIS RANGE) +
03390000             | &SUBSTR(1:7,&STR(&PDSMSG)) = PDS402W | &SUBSTR(1:33,&STR(&PDSMSG)) = &STR(NO MEMBER NAMES MATCH THIS PATTER) THEN DO
03400000                 SET &SEARCH = &STR(NOT FOUND)
03410000            END
03420000            ELSE SET &SEARCH = &STR(ERROR OR UNKOWN)
03430000          END
03440000        END
03450000        ELSE DO
03460000          /* ASSUME OK UNTIL I CAN SET ISPF VARIABLES IN MENU */
03470000          SET &SEARCH = OK
03480000        END
03490000      END
03500000      ELSE DO
03510000        /** NOW USE TSO/E &SYSDSN COMMAND TO SEE IF MEMBER EXISTS **/
03520000        SET SEARCH = &SYSDSN('&KLIB(&FMMEMBER.)')
03530000      END
03540000    END
03550000    IF &STR(&SEARCH) = OK THEN DO
03560000      SET FSFMRC = 0
03570000      SET MANYFMRC = 0
03580000      SET FMDSM&DK = &STR(FOUND)
03590000      SET FMDS&DK = '&KLIB(&STR(&FMMEMBER))'
03600000      IF &GENERIC = THEN DO
03610000        IF      &FMFINDBP = PDS    THEN PDS '&KLIB(&FMMEMBER)' MEMLIST
03620000        ELSE IF &FMFINDBP = BROWSE THEN ISPEXEC BROWSE DATASET('&KLIB(&FMMEMBER)')
03630000      END
03640000      ELSE DO  /* GENERIC SEARCH ONLY USE PDS CMD AT THIS TIME */
03650000        IF &FMFINDBP = PDS OR &FMFINDBP = BROWSE THEN DO
03660000          ISPEXEC CONTROL DISPLAY SAVE
03670000          ISPEXEC SELECT PGM(PDS) PARM(PDS '&KLIB' MEMLIST &STR(&FMMEMBER.))
03680000          ISPEXEC CONTROL DISPLAY RESTORE
03690000        END
03700000      END
03710000    END
03720000    ELSE DO
03730000      IF &STR(&FMMEMBER) = &STR(?) THEN DO
03740000        SET FMDSM&DK = LISTED
03750000        SET MANYFMRC = 0
03760000      END
03770000      ELSE DO
03780000       SET FMDSM&DK = &STR(NOT FOUND)
03790000       SET FSFMRC = 16
03800000      END
03810000    END
03820000    IF &DDRC = 0 THEN SET FMDSS&DK =      DONE
03830000    ELSE              SET FMDSS&DK = &STR(NOT ALLOCATED)
03840000    /***** SET TWRILL "WORKING" SYMBOL *****/
03850000    IF &WRKSYMBL = YES THEN  %FSFMTWRL OPT(WORK) NUMBER('&WIPSN.')
03860000    /***** SET TWRILL "WORKING" SYMBOL *****/
03870000    IF (&DK>= 14) AND ((&TRAPKNT-1) > 16) THEN SET &FMDSMORE = &STR(* > 16 DATASETS! *)
03880000    IF &DK <= 16 THEN SET DSPPNL = FSFMDSP1
03890000    ELSE              SET DSPPNL = FSFMDSP2
03900000    ISPEXEC CONTROL DISPLAY LOCK
03910000    ISPEXEC DISPLAY PANEL(&DSPPNL)
03920000    SET &K = &K + 1
03930000  END
03940000  /***** SET TWRILL "STOP" SYMBOL *****/
03950000  IF &WRKSYMBL = YES THEN  %FSFMTWRL OPT(STOP) NUMBER('&WIPSN.')
03960000  /***** SET TWRILL "STOP" SYMBOL *****/
03970000  IF &MANYFMRC = 0 THEN SET FMDSMSGS = &STR( FOUND!)
03980000  ELSE                  SET FMDSMSGS = &STR(NO FIND)
03990000  SET &SHORT = &STR(SEARCH DONE! &FMDSMSGS.)
04000000  IF &MANYFMRC = 0 THEN SET &LONG = &STR(&FMMEMBER WAS FOUND IN AT LEAST ONE LIBRARY.)
04010000  ELSE                  SET &LONG = &STR(&FMMEMBER WAS NOT FOUND IN ANY SEARCH LIBRARY.)
04020000  SHSCNPNL:+
04030000  IF &DKMAX > 16 THEN DO
04040000    IF &DSPPNL = FSFMDSP1 THEN SET &FMDSMSGX = &STR(OR PRESS  ENTER  FOR NEXT  PAGE.)
04050000    IF &DSPPNL = FSFMDSP2 THEN SET &FMDSMSGX = &STR(OR PRESS  ENTER  FOR FIRST PAGE.)
04060000  END
04070000  ELSE                         SET &FMDSMSGX =
04080000  SET FMDSMSGE = &STR(PRESS  END  KEY TO TERMINATE. &FMDSMSGX)
04090000  ISPEXEC DISPLAY PANEL(&DSPPNL) MSG(FSFM010)
04100000  IF  &LASTCC = 0 THEN DO
04110000    ISPEXEC VGET (FMDSNBRP,FMBRPAL1)
04120000    IF      &FMDSNBRP  = THEN DO
04130000      IF (&DKMAX > 16) THEN DO /* OVER ONE PANEL OF DSNS */
04140000        /* IF ENTER PRESSED AND NOT BROWSE/PDS COMMAND FLIP PANELS */
04150000        IF      &DSPPNL = FSFMDSP1 THEN SET DSPPNL = FSFMDSP2
04160000        ELSE IF &DSPPNL = FSFMDSP2 THEN SET DSPPNL = FSFMDSP1
04170000      END
04180000      GOTO SHSCNPNL
04190000    END
04200000    ELSE IF &FMDSNBRP ^= THEN DO
04210000      SET SHOWN1ST =
04220000      SET &DK = 1
04230000      DO WHILE(&DK <= &DKMAX)
04240000        SET FMDSMEMF = &&FMDSM&DK
04250000        SET FINDDSNM = &&FMDS&DK
04260000        IF &FMDSMEMF        = FOUND THEN DO
04270000          IF (&FMBRPAL1 = ALL) OR (&FMBRPAL1 = FIRST AND &SHOWN1ST = ) THEN DO
04280000            SET SHOWN1ST = YES
04290000            IF &GENERIC = YES THEN DO
04300000              SET WLINE = &STR(&FINDDSNM)
04310000              VSCAN WLINE '('
04320000              SET VSC = &LASTCC
04330000              SET PDSNAME = &SUBSTR(1:&VSC-1,&STR(&FINDDSNM))
04340000              SET PDSNAME = &STR(&PDSNAME.')
04350000              /* GENERIC SEARCH ONLY USE PDS CMD AT THIS TIME */
04360000              ISPEXEC CONTROL DISPLAY SAVE
04370000              ISPEXEC SELECT PGM(PDS) PARM(PDS &PDSNAME. MEMLIST &STR(&FMMEMBER.))
04380000              ISPEXEC CONTROL DISPLAY RESTORE
04390000            END
04400000            ELSE DO /* SINGLE MEMBER SEARCH USE PDS OR BROWSE */
04410000              IF      &FMDSNBRP = PDS    THEN DO
04420000                PDS  &FINDDSNM  MEMLIST
04430000              END
04440000              ELSE IF &FMDSNBRP = BROWSE THEN DO
04450000                ISPEXEC BROWSE DATASET(&FINDDSNM)
04460000              END
04470000            END
04480000          END
04490000        END
04500000        SET &DK = &DK + 1
04510000      END
04520000      GOTO SHSCNPNL
04530000    END
04540000  END
04550000  SET &FMDSMSGE =
04560000  SET &DK = 1
04570000  DO WHILE(&DK <= &DKMAX )
04580000    SET FMDS&DK =
04590000    SET FMDSS&DK =
04600000    SET FMDSM&DK =
04610000    SET &FMDSMORE =
04620000    SET &DK = &DK + 1
04630000  END
04640000END
04650000ELSE IF &FMVERS = KBR THEN DO /* FMVERS=KBR "ONLY" */
04660000  SET MANYFMRC = 16
04670000  IF &DDRC = 0 THEN DO
04680000    ISPEXEC SELECT PGM(CNA0KBR) PARM(&SRCHPARM.)
04690000    SET FSFMRC = &LASTCC.
04700000    IF &FSFMRC = 0 THEN SET MANYFMRC = 0
04710000  END
04720000  ELSE SET FSFMRC = 20
04730000END
04740000ELSE  DO
04750000 SET &SHORT = &STR(UNKOWN SEARCH:&FMVERS.)
04760000 SET &LONG =                                                            ANY SEARCH LIBRARY.)
04770000 ISPEXEC SETMSG MSG(FSFM010)
04780000END
04790000 
04800000IF &STR(&FMLIB) ^= DD THEN FREE FI(&LIBRARY)
04810000 
04820000IF &FMMANY ^= THEN GOTO MANYCHK
04830004 
04840004IF      &FMVERS = KDSN THEN  SET &FSFMSDF= STATUS
04850004ELSE IF &FMVERS = KBR  THEN  SET &FSFMSDF= ONLY
04860004ISPEXEC VPUT (FSFMSDF) PROFILE
04870000 
04880000EXIT
