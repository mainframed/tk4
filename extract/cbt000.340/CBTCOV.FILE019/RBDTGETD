00010000PROC 0 DEBUG
00020000   IF &DEBUG = DEBUG THEN +
00030000     CONTROL LIST CONLIST MSG
00040000   CONTROL NOFLUSH
00050000  SET Z =
00060000  SET TABLE = MBDTAB
00070000  SET PANEL = RBDTDIR
00080000  SET CRP = 1
00090000  SET ACTION = &Z
00100000  SET MSG = &Z
00110000  SET CURSOR = SEL             /* FILL IN YOUR CURSOR FIELD */
00120000  SET FIRST = YES
00130000  SET VALID = NO
00140000  ISPEXEC VGET (BDTFDSN Z)
00150000  SET IORE = &Z
00160000  ISPEXEC SELECT PGM(DIRBUILD) PARM(MBDTAB,BDTFDSN)
00170000  ISPEXEC TBCREATE MEMTABLE NAMES(DIRNAME NEWNAME) NOWRITE
00180000  SET DESC = &STR(ENTER "S" TO SELECT OR "E" TO EXCLUDE MEMBERS)
00190000  SET LOCATE = NO
00200000/******************************************************************/
00210000/* POSITION TABLE TO DISPLAY FROM 1ST ROW                         */
00220000/******************************************************************/
00230000ISPEXEC TBTOP &TABLE
00240000/******************************************************************/
00250000/* DISPLAY THE TABLE AND PROCESS USER ENTRIES UNTIL THE USER      */
00260000/* ENTERS END, RETURN OR CANCEL                                   */
00270000/******************************************************************/
00280000   DO WHILE (&ACTION ^= USEREND)
00290000     IF &FIRST = YES THEN +
00300000       DO
00310000         ISPEXEC TBDISPL &TABLE PANEL(&PANEL) MSG(&MSG) CSRROW(&CRP) +
00320000            CURSOR(&CURSOR)
00330000         SET RC = &LASTCC
00340000         IF &ZCMD = CANCEL THEN +
00350000           DO
00360000             SET RC = 16
00370000             GOTO ENDUP
00380000           END
00390000         SET FIRST = NO
00400000       END
00410000     ELSE +
00420000       DO
00430000   /******************************************************************/
00440000   /* ISPF WILL CORRECTLY POSITION THE TABLE IF THE PANEL NAME       */
00450000   /* IS NOT RESPECIFIED.                                            */
00460000   /******************************************************************/
00470000         IF &LOCATE = YES THEN +
00480000           ISPEXEC TBDISPL &TABLE PANEL(&PANEL) CSRROW(&CRP) +
00490000              CURSOR(&CURSOR)
00500000           ELSE +
00510000             ISPEXEC TBDISPL &TABLE MSG(&MSG) CSRROW(&CRP) +
00520000                CURSOR(&CURSOR)
00530000         SET RC = &LASTCC
00540000         SET CURSOR = SEL
00550000         SET LOCATE = NO
00560000       END
00570000   /******************************************************************/
00580000   /* RC = 0  - ONE LINE MODIFIED AND/OR COMMAND ENTERED             */
00590000   /* RC = 4  - TWO OR MORE LINES MODIFIED; USER MAY HAVE ENTERED    */
00600000   /*           A FUNCTION COMMAND                                   */
00610000   /* RC = 8  - USER ENTERED END OR RETURN                           */
00620000   /******************************************************************/
00630000     IF &ZCMD = CANCEL THEN +
00640000       DO
00650000         SET RC = 16
00660000         GOTO ENDUP
00670000       END
00680000     IF &RC = 0 THEN +
00690000       SET ACTION = ONEROW
00700000     ELSE +
00710000       IF &RC = 4 THEN +
00720000         SET ACTION = MULTIROW
00730000       ELSE +
00740000         SET ACTION = USEREND         /* WAS AN 8 */
00750000   /******************************************************************/
00760000   /* PROCESS EACH ROW WHICH THE USER HAS MODIFIED                   */
00770000   /******************************************************************/
00780000     SET ROWSDONE = NO
00790000     SET LASTROW = NO
00800000     SET COMMAND = NO
00810000     SET CRP = &Z
00820000     SET MSG = &Z
00830000     /*
00840000     DO WHILE (&ROWSDONE = NO)
00850000       IF &LASTROW = YES | &ACTION = ONEROW THEN +
00860000         SET ROWSDONE = YES
00870000       ISPEXEC TBQUERY &TABLE POSITION (CRP)
00880000       IF &ACTION ^= MULTIROW | &LASTROW = YES THEN +
00890000         DO
00900000   /******************************************************************/
00910000   /* CRP = 0  - NO MORE LINES MODIFIED; LOOK FOR COMMAND (ERROR)    */
00920000   /*            ENTERED                                             */
00930000   /******************************************************************/
00940000           IF &CRP = 0 THEN +
00950000             DO
00960000               IF &ACTION = USEREND THEN +
00970000                 GOTO ENDUP
00980000               ELSE +
00990000                 DO         /* THERE WAS A COMMAND     ?????? */
01000000                   SET COMMAND = YES
01010000                   SET ROWSDONE = YES
01020000                 END
01030000             END
01040000         END
01050000       IF &COMMAND = NO THEN +
01060000         DO
01070000   /******************************************************************/
01080000   /* PROCESS THE ROW                                                */
01090000   /*                                                                */
01100000   /******************************************************************/
01110001           IF &SEL ^= &Z && &SUBSTR(1:1,&STR(&DIRACT )) +
01120001                   ^= &STR(*) THEN +
01130000             DO
01140000               IF &VALID = NO THEN +
01150000                 DO                       /* FIRST TIME VALIDATION */
01160000                   SET IORE = &SEL
01170000                   IF &IORE ^= S && &IORE ^= E THEN +
01180000                     DO
01190000                       SET SHORT = NOT SELECT OR EXCLUDE
01200000                       SET LONG = &STR(FIELD MUST CONTAIN EITHER +
01210000                                  "S" OR "E")
01220000                       SET MSG = RBDT010
01230000                       SET SEL = &Z
01240000                       GOTO ROWERROR
01250000                     END
01260000                   ELSE +
01270000                     DO
01280000                       ISPEXEC VPUT (IORE) SHARED
01290000                       SET VALID = YES
01300000                     END
01310000                 END
01320001               IF &IORE = S && &DIRACT ^= ALIAS THEN +
01330000                 SET NEWNAME = &DIRACT
01340000               ELSE +
01350000                 SET NEWNAME = &Z
01360000               ISPEXEC TBADD MEMTABLE
01370000               IF &IORE = S THEN +
01380000                DO
01390000                 IF &NEWNAME = &Z THEN +
01400000                  SET DIRACT  = &STR(*SELECT)
01410000                 ELSE +
01420000                  SET DIRACT = &STR(*RENAMED)
01430000                END
01440000               ELSE +
01450000                  SET &DIRACT = &STR(*EXCLUDE)
01460000               ISPEXEC TBPUT MBDTAB
01470000               SET SEL = &Z
01480000             END
01490001           ELSE +
01500001             SET SEL = &Z
01510000   /******************************************************************/
01520000   /* ERRORS FOUND???                                                */
01530000   /******************************************************************/
01540000           IF &MSG ^= &Z THEN +
01550000             DO
01560000   /******************************************************************/
01570000   /* DON'T END IF THERE IS A MESSAGE                                */
01580000   /******************************************************************/
01590000               IF &ACTION = USEREND THEN +
01600000                 SET &ACTION = &Z
01610000               GOTO ROWERROR
01620000             END
01630000           IF &ROWSDONE = YES && &ZCMD ^= &Z THEN +
01640000             DO
01650000   /******************************************************************/
01660000   /* COMMAND WAS ENTERED                                            */
01670000   /******************************************************************/
01680000               SET COMMAND = YES
01690000             END
01700000           IF &ROWSDONE = NO THEN +
01710000             DO
01720000   /******************************************************************/
01730000   /* RETRIEVE THE NEXT MODIFIED ROW                                 */
01740000   /******************************************************************/
01750000               ISPEXEC TBDISPL &TABLE
01760000               SET RC = &LASTCC
01770000               IF &ACTION = MULTIROW && &RC = 0 THEN +
01780000                 SET &LASTROW = YES
01790000             END
01800000         END                        /* END "IF &COMMAND = NO"        */
01810000       IF &COMMAND = YES THEN +
01820000         DO
01830000   /******************************************************************/
01840000   /* PROCESS THE FUNCTION COMMAND   (MAY BE AN ERROR)               */
01850000   /******************************************************************/
01860000           SET DIRNAME = &ZCMD
01870001           IF &LENGTH(&DIRNAME) > 1 THEN +
01880000           IF &SUBSTR(1:2,&DIRNAME) = &STR(L ) THEN +
01890000             DO                                      /* LOCATE COMMAND */
01900000               SET DIRNAME = &SUBSTR(3:&LENGTH(&DIRNAME),&DIRNAME)&STR(*)
01910000               ISPEXEC TBTOP &TABLE
01920000               ISPEXEC TBSCAN &TABLE ARGLIST(DIRNAME)
01930000               ISPEXEC TBQUERY &TABLE POSITION(CRP)
01940000               IF &CRP > 1 THEN +
01950000                 ISPEXEC TBSKIP &TABLE NUMBER(-1)
01960000               ELSE +
01970000                 SET CRP = 1
01980000               SET LOCATE = YES
01990000             END
02000000           ELSE +
02010000             DO                                 /* GARBAGE COMMAND */
02020000               SET SHORT = INVALID COMMAND
02030000               SET LONG = &STR(THE ENTRY IN THE COMMAND AREA IS NOT +
02040000                  RECOGNIZED)
02050000               SET MSG = RBDT010
02060000               SET CURSOR = ZCMD
02070000             END
02080001           ELSE +
02090001             DO                                 /* GARBAGE COMMAND */
02100001               SET SHORT = INVALID COMMAND
02110001               SET LONG = &STR(THE ENTRY IN THE COMMAND AREA IS NOT +
02120001                  RECOGNIZED)
02130001               SET MSG = RBDT010
02140001               SET CURSOR = ZCMD
02150001             END
02160000         END
02170000     END
02180000ROWERROR: +
02190000   END
02200000ENDUP: +
02210000  ISPEXEC TBEND MBDTAB
02220000  IF &RC = 16 THEN +
02230000    DO
02240000      ISPEXEC TBEND MEMTABLE
02250000      EXIT CODE(8)
02260000    END
02270000END
