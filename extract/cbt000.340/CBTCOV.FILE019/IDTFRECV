00010000PROC 0 NOMSG
00020000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **
00030000/** Receive TSO/E IDTF XMIT Messages in Browse.                       **
00040000/** By Mike Theys, Dave Fay, and Andy Anderson  Rockwell International**
00050000/** (TSO/E R3 Version Uses SYSNSUB to avoid problems with ampersands) **
00060001/** Formerly %RIDTFRCV.                                TSO/E R3 ONLY  **
00070000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **
00080000CONTROL NOFLUSH MSG NOPROMPT ASIS
00090000ISPEXEC CONTROL ERRORS RETURN
00100002%ROCPROF OPTION(NOMSGID) /* Make sure PROFILE NOMSGID is set. */
00110000SET &RPRC = &LASTCC
00120000SET &SYSOUTTRAP = 999999
00130000  RECEIVE
00140000SET TRAPKNT = &SYSOUTLINE
00150000SET  &SYSOUTTRAP = 0
00160000IF &TRAPKNT > 1 THEN DO
00170000  SET K = 1
00180001  ISPEXEC TBCREATE IDTFRECV NAMES(LINE) NOWRITE REPLACE
00190000  DO WHILE (&K <= &TRAPKNT)
00200000    SET TEMP = &&SYSOUTLINE&K
00210001    SET LINE = &STR(&SYSNSUB(2,&TEMP))
00220001    ISPEXEC TBADD IDTFRECV
00230000    SET K = &K + 1
00240000  END
00250001  ISPEXEC TBTOP   IDTFRECV
00260000  ISPEXEC FTOPEN TEMP
00270001  ISPEXEC FTINCL  IDTFRECV
00280000  ISPEXEC FTCLOSE
00290001  ISPEXEC TBCLOSE IDTFRECV
00300000  ISPEXEC VGET (ZTEMPF)
00310000  SET ECMDBRTI = &STR(RECEIVE Output)
00320000  ISPEXEC VPUT (ECMDBRTI)
00330000  ISPEXEC BROWSE DATASET('&ZTEMPF') PANEL(ECMDBROW)
00340000END
00350000ELSE DO
00360000  IF &NOMSG =  THEN DO
00370000    SET SHORT = &STR(No XMIT Mail)
00380000    SET LONG  = &STR(There were NO XMIT Messages to RECEIVE.)
00390000    ISPEXEC SETMSG MSG(SYS010)
00400000  END
00410000END
00420002IF &RPRC = 0 THEN ACFPROF MSGID/* Reset PROFILE MSGID if turn OFF */
