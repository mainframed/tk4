00010009PROC 1 PANEL OPT() NEWAPPL() NEWPOOL  DEBUG USRPFK(OFF)
00020005IF &DEBUG = DEBUG THEN  CONTROL   MSG   LIST   CONLIST   SYMLIST
00030005ELSE                    CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST
00040000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00050000/**                                                                **/
00060000/** This clist is now used by many ISPF commands to invoke a       **/
00070000/** PANEL with an optionial OPTion under the specified NEWAPPL.    **/
00080000/** If the current ISPF ZAPPLID is alread the same as NEWAPPL,     **/
00090000/** then do NOT add the NEWAPPL to the ISPEXEC SELECT PANEL.       **/
00100000/** This prevents the SHARED pool from being wiped out!            **/
00110000/**                                                                **/
00120000/** By Mike Theys  01/24/87 <<DON'T DELETE used by "HOLD" etc>>    **/
00130000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00140000ISPEXEC CONTROL ERRORS RETURN
00150000ISPEXEC VGET    (ZAPPLID)
00160000SET VOPT  =
00170000IF &STR(&NEWAPPL)  =          THEN SET NEWAPPL = ISR
00180000IF &STR(&NEWAPPL) ^= &ZAPPLID THEN SET VAPPL = &STR( NEWAPPL(&NEWAPPL))
00190000IF &STR(&OPT)     ^=          THEN SET VOPT  = &STR( OPT(&OPT))
00200000 
00210004IF &STR(&VAPPL) =  THEN DO /* Save    Rockwell modifable PF Keys */
00220009  IF &USRPFK = ON THEN DO  /* Rockwell modifable PF Keys in use  */
00230009    ISPEXEC VGET (USRUP,USRDOWN,USRRIGHT,USRLEFT,USRRFIND,USRRCHG)
00240009    SET SAVUP    = &USRUP
00250009    SET SAVDOWN  = &USRDOWN
00260009    SET SAVRIGHT = &USRRIGHT
00270009    SET SAVLEFT  = &USRLEFT
00280009    SET SAVRFIND = &USRRFIND
00290009    SET SAVRCHG  = &USRRCHG
00300009    /* Clear   Rockwell modifable PF Keys for IBM PF Key use */
00310009    SET USRUP    =
00320009    SET USRDOWN  =
00330009    SET USRRIGHT =
00340009    SET USRLEFT  =
00350009    SET USRRFIND =
00360009    SET USRRCHG  =
00370009    ISPEXEC VPUT (USRUP,USRDOWN,USRRIGHT,USRLEFT,USRRFIND,USRRCHG)
00380009  END
00390004END
00400001 
00410008ISPEXEC SELECT PANEL(&PANEL) &VOPT  &VAPPL. &NEWPOOL
00420000 
00430007IF &LASTCC >  4 THEN DO
00440007  CONTROL ASIS
00450007  SET SHORT = &STR(ERROR with option &OPT)
00460007  SET LONG  = &STR(Either "&OPT" is an invalid option OR an error occured using panel &PANEL)
00470007  ISPEXEC SETMSG MSG(SYS010)
00480007END
00490007 
00500003IF &STR(&VAPPL) =  THEN DO /* Restore Rockwell modifable PF Keys */
00510009  IF &USRPFK = ON THEN DO  /* Rockwell modifable PF Keys in use  */
00520009    SET USRUP    = &SAVUP
00530009    SET USRDOWN  = &SAVDOWN
00540009    SET USRRIGHT = &SAVRIGHT
00550009    SET USRLEFT  = &SAVLEFT
00560009    SET USRRFIND = &SAVRFIND
00570009    SET USRRCHG  = &SAVRCHG
00580009    ISPEXEC VPUT (USRUP,USRDOWN,USRRIGHT,USRLEFT,USRRFIND,USRRCHG)
00590009  END
00600002END
