00010000PROC     0        DEBUG    HARDCOPY
00020000 
00030000/*********************************************************************/
00040000/*                   DUMP DATA SET MANAGEMENT                        */
00050000/* IPCS: INITIALIZE PRINT/BROWSE VARIABLES                           */
00060000/*********************************************************************/
00070000 
00080000IF       &DEBUG = DEBUG +
00090000THEN     CONTROL  ASIS   CONLIST NOFLUSH   LIST   MSG   PROMPT   SYMLIST
00100000ELSE     CONTROL  ASIS NOCONLIST NOFLUSH NOLIST   MSG   PROMPT NOSYMLIST
00110000 
00120000ISPEXEC  VGET     (DEFPRINT DEFBRO IPCBRWS IPCOS ZSCREEN IPCPRTP IPCPRTS IPCPRTL IPCBROP IPCBROS IPCBROL NOTEBRWS NOTEPRNT)
00130000 
00140000IF       (&DEFPRINT = PRINT | &HARDCOPY = HARDCOPY)  && &NOTEPRNT = &STR() +
00150000THEN     DO
00160000         SET      &PRTPRI = &IPCPRTP * &IPCPRTL / 6233
00170000         SET      &PRTSEC = &IPCPRTS * &IPCPRTL / 6233
00180000         SET      &PRTDCB = RECFM(V B A) LRECL(&IPCPRTL) BLKSIZE(6233) DSORG(PS)
00190000         IF       &SYSDSN(IPCPRNT&ZSCREEN..LIST) = OK +
00200000         THEN     ALLOCATE FILE(IPCPRNT&ZSCREEN) DATASET(IPCPRNT&ZSCREEN..LIST) OLD &PRTDCB REUSE
00210000         ELSE     ALLOCATE FILE(IPCPRNT&ZSCREEN) DATASET(IPCPRNT&ZSCREEN..LIST) NEW &PRTDCB REUSE SPACE(&PRTPRI &PRTSEC)
00220000         OPENFILE IPCPRNT&ZSCREEN OUTPUT
00230000         SET      &IPCPRNT&ZSCREEN = &STR( )
00240000         PUTFILE  IPCPRNT&ZSCREEN
00250000         CLOSFILE IPCPRNT&ZSCREEN
00260000         ALLOCATE FILE(IPCPRNT&ZSCREEN) DATASET(IPCPRNT&ZSCREEN..LIST) MOD &PRTDCB REUSE
00270000         SET      &NOTEPRNT = PRINT already allocated
00280000         ISPEXEC  VPUT     (NOTEPRNT)
00290000         END
00300000 
00310000IF       &DEFBRO = BROWSE && &NOTEBRWS = &STR() +
00320000THEN     DO
00330000         SET      &BROPRI = &IPCBROP * &IPCBROL / 6233
00340000         SET      &BROSEC = &IPCBROS * &IPCBROL / 6233
00350000         SET      &BRODCB = RECFM(V B A) LRECL(&IPCBROL) BLKSIZE(6233) DSORG(PS)
00360000         IF       &SYSDSN(IPCBRWS&ZSCREEN..LIST) = OK +
00370000         THEN     ALLOCATE FILE(IPCBRWS&ZSCREEN) DATASET(IPCBRWS&ZSCREEN..LIST) OLD &BRODCB REUSE
00380000         ELSE     ALLOCATE FILE(IPCBRWS&ZSCREEN) DATASET(IPCBRWS&ZSCREEN..LIST) NEW &BRODCB REUSE SPACE(&BROPRI &BROSEC)
00390000         ISPEXEC  LMINIT   DATAID(IPCBRWS) DDNAME(IPCBRWS&ZSCREEN)
00400000         SET      &NOTEBRWS = BROWSE already allocated
00410000         ISPEXEC  VPUT     (NOTEBRWS IPCBRWS)
00420000         END
00430000 
00440000IF       &IPCOS = XA +
00450000THEN     SET      &DATA = DATAID(&IPCBRWS)
00460000ELSE     SET      &DATA = DATASET(IPCBRWS&ZSCREEN..LIST)
00470000 
00480000IF       &DEFPRINT = NOPRINT && &DEFBRO = NOBROWSE +
00490000THEN     DO
00500000         SET      &PREFIX = &STR()
00510000         SET      &SUFFIX = &STR()
00520000         SET      &QUOTE = &STR()
00530000         END
00540000 
00550000IF       &DEFPRINT = PRINT && &DEFBRO = NOBROWSE +
00560000THEN     DO
00570000         SET      &PREFIX = %IPCPRINT &ZSCREEN CMD( '
00580000         SET      &SUFFIX = ' )
00590000         SET      &QUOTE = '
00600000         END
00610000 
00620000IF       &DEFPRINT = NOPRINT && &DEFBRO = BROWSE +
00630000THEN     DO
00640000         SET      &PREFIX = %IPCBRWS &DATA &ZSCREEN CMD( '
00650000         SET      &SUFFIX = ' )
00660000         SET      &QUOTE = '
00670000         END
00680000 
00690000IF       &DEFPRINT = PRINT && &DEFBRO = BROWSE +
00700000THEN     DO
00710000         SET      &PREFIX = %IPCBRPRT &DATA &ZSCREEN CMD( '
00720000         SET      &SUFFIX = ' )
00730000         SET      &QUOTE = '
00740000         END
00750000 
00760000ISPEXEC  VPUT     (PREFIX SUFFIX QUOTE) SHARED
00770000 
00780000EXIT     CODE(0)
