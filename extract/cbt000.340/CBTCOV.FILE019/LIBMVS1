00010000         PROC 0         DEBUG
00020000         CONTROL NOFLUSH
00030000         /*                                                         */
00040000         /*   WILL ALLOC DVVOUT DD AND THEN INVOKE LIB/INFO         */
00050000         /*    PROGRAM PRODUCT.                                     */
00060000         /*                                                         */
00070000         IF &DEBUG NE DEBUG THEN CONTROL NOLIST NOMSG
00080000         ELSE CONTROL LIST PROMPT CONLIST SYMLIST MSG
00090000         ISPEXEC CONTROL ERRORS RETURN
00100000         ISPEXEC VGET (LIBDSN,LIBDST,LIBCLS)
00110000         ISPEXEC DISPLAY PANEL(LIBMVS1)
00120000         IF &LASTCC NE 0 THEN EXIT
00130000         IF &LIBDSN NE THEN DO
00140000            IF &SYSDSN(&LIBDSN) NE OK THEN +
00150000                 SET &DISP = &STR(NEW CAT SPACE(1,10) TRACKS )
00160000             ELSE SET &DISP = SHR
00170000             ALLOC F(DVVOUT) DS(&LIBDSN) &DISP  REU
00180000             SET &ARC = &LASTCC
00190000             IF &ARC NE 0 THEN DO
00200000               CLRSCRN
00210000               WRITE  ******   ERROR IN ALLOCATION  ******
00220000               WRITE          RETURN CODE WAS &ARC
00230000               EXIT CODE(&ARC)
00240000               END
00250000          END
00260000          IF &LIBDSN = THEN DO
00270000            ALLOC F(DVVOUT) SYSOUT(&LIBCLS) DEST(&LIBDST) REU
00280001             SET &ARC = &LASTCC
00290000             IF &ARC NE 0 THEN DO
00300000               CLRSCRN
00310000               WRITE  ******   ERROR IN ALLOCATION  ******
00320000               WRITE          RETURN CODE WAS &ARC
00330000               EXIT CODE(&ARC)
00340000               END
00350000          END
00351003 
00352003               SCRSIZE   /* DETERMINE THE CURRENT TERMINAL CONFIGURATION
00353003               SET &TERM = &LASTCC
00354003               TERM SCRSIZE(24,80)   /*  SET AS MODEL 2
00354103               CLRSCRN
00355003 
00360000         ALLOC DSNAME('AINFO.DVV.TEXTMVSL') DDNAME(DVVTEXT) SHR  REU
00370000         ALLOC DSNAME('AINFO.DVV.TNDXMVSL') DDNAME(DVVTNDX) SHR  REU
00380000         ALLOC DSNAME('AINFO.DVV.DICTMVSL') DDNAME(DVVDICT) SHR  REU
00390000         ALLOC DSNAME('AINFO.DVV.INVTMVSL') DDNAME(DVVINVT) SHR  REU
00400000         ALLOC DSNAME('AINFO.DVV.PMRCMVSL') DDNAME(DVVPMRC) SHR  REU
00410004         ALLOC DSNAME('AINFO.DVV.PMRYMVSL') DDNAME(DVVPMRX) SHR  REU
00420000REDO:    CALL 'AINFO.DVV.LOADLIB(DVVENT)'
00430000         SET &CRC = &LASTCC
00440000         IF &CRC = 8 THEN DO
00450000            SET &TRY =
00460000            ISPEXEC DISPLAY PANEL(LIBMVS2)
00470000            IF &TRY = YES THEN GOTO REDO
00480000         END
00490000         ISPEXEC VPUT (LIBDSN,LIBDST,LIBCLS) PROFILE
00500000         FREE DDNAME(DVVTEXT,DVVTNDX,DVVDICT,DVVINVT,DVVPMRC,DVVPMRX)
00510000         FREE DDNAME(DVVOUT)
00511002 
00512002         IF &TERM ^= 2 THEN DO
00513002                            IF &TERM = 3 THEN TERM SCRSIZE(32,80)
00514002                            IF &TERM = 4 THEN TERM SCRSIZE(43,80)
00515002                            IF &TERM = 5 THEN TERM SCRSIZE(27,132)
00516002                            END
00517002 
00520001         ISPEXEC CONTROL DISPLAY REFRESH
00530000         END
