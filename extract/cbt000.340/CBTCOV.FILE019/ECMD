00010005 PROC 1 COMMAND OPERAND() DEBUG  SPFV  +
00020000        NOSHOW NOFREE NOTEMP EDIT      +
00030009        EDSN() EFILE() MAXLINES(5000)  +
00040000        STRIP() SKIP1ST() SKIPLST()    +
00050000        TITLE() TITLE2() BRTITLE()     +
00060000        NOGO NOGORC(0) NOGOKNT(0)      +
00070012        FB80  VB255 WRAP ERRONLY       +
00080000        MSG(MSG) MSGID()
00090019 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
00100019 /*                                                                   */
00110019 /* ECMD: Used to trap TSO command output for use with Browse/Edit    */
00120019 /* By:   Mike Theys  and  Andy Anderson      Rockwell International  */
00130019 /* Operand can be either specified via operand() keyword or          */
00140019 /*  or if SPFV is specified then via ISPF variable ECMDRAND          */
00150019 /*                                                                   */
00160019 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
00170019 IF &DEBUG = DEBUG THEN CONTROL LIST PROMPT CONLIST SYMLIST MSG
00180019 ELSE                   CONTROL NOLIST
00190019 CONTROL NOFLUSH ASIS
00200019 
00210018 IF &SYSISPF = ACTIVE THEN DO
00220018   ISPEXEC CONTROL ERRORS RETURN
00230018   ISPEXEC CONTROL DISPLAY SAVE
00240019 END
00250018 ELSE DO
00260019   SET &NOSHOW = NOSHOW
00270019   SET &SPFV =
00280019   SET &EDIT =
00290019 END
00300019 
00310000 IF      &MSGID =   MSGID THEN PROFILE   MSGID
00320000 ELSE IF &MSGID = NOMSGID THEN PROFILE NOMSGID
00330000 
00340000 IF      &MSG   =   MSG   THEN CONTROL   MSG
00350000 ELSE IF &MSG   = NOMSG   THEN CONTROL NOMSG
00360000 
00370019 IF &SYSISPF = ACTIVE THEN DO
00380019   IF &SPFV = SPFV THEN DO /* Command operands saved in ISPF Variable */
00390019     ISPEXEC VGET (ECMDRAND)
00400019     SET OPERAND = &STR(&ECMDRAND)
00410019   END
00420019 END
00430005 
00440000 IF &EDIT = EDIT THEN SET &FUNC = EDIT
00450000 ELSE DO
00460000   SET &FUNC = BROWSE
00470000   SET &BRPNL= PANEL(ECMDBROW)
00480000   IF &STR(&BRTITLE) ^=  THEN SET ECMDBRTI = &STR(&BRTITLE)
00490000   ELSE                       SET ECMDBRTI = &STR(&COMMAND &OPERAND Output)
00500019   IF &SYSISPF = ACTIVE THEN ISPEXEC VPUT (ECMDBRTI)
00510000 END
00520000 
00530000 IF &DEBUG = DEBUG THEN CONTROL NOLIST NOCONLIST NOSYMLIST
00540000 SET &SYSOUTTRAP = &MAXLINES
00550000   &COMMAND &OPERAND
00560000   SET &CMDRC = &LASTCC
00570000 SET &SYSOUTTRAP = 0
00580000 SET &TRAPKNT = &SYSOUTLINE
00590000 IF &DEBUG = DEBUG THEN CONTROL LIST PROMPT CONLIST SYMLIST MSG
00600000 
00610006 SET ECMDRC = &CMDRC
00620019 IF &SYSISPF = ACTIVE THEN ISPEXEC VPUT (ECMDRC)
00630011 IF &NOGO = NOGO AND &CMDRC   >  &NOGORC  THEN GOTO XIT
00640011 IF &NOGO = NOGO AND &TRAPKNT <= &NOGOKNT THEN DO
00650011   SET ECMDRC = 3
00660011   GOTO XIT
00670011 END
00680012 IF &ERRONLY = ERRONLY AND &ECMDRC = 0 THEN GOTO XIT
00690000 
00700019 IF &SYSISPF = ACTIVE THEN ISPEXEC VGET (ZSCREEN)
00710000 IF &DEBUG =  THEN CONTROL NOMSG
00720000 IF &STR(&EFILE) =  THEN SET EFILE = &STR(ECMDF&ZSCREEN)
00730000 IF &STR(&EDSN) ^=  THEN SET NOTEMP = NOTEMP
00740000 IF &VB255 = VB255 THEN +
00750000   SET DCB = &STR( RECFM(V B) LRECL(255) BLKSIZE(3120) )
00760000 ELSE    /* FB80 SET OR NULL */ +
00770000   SET DCB = &STR( RECFM(F B) LRECL(80)  BLKSIZE(3120) )
00780000 IF &NOTEMP = NOTEMP THEN DO
00790000   IF &STR(&EDSN) =  THEN SET &EDSN = &STR(ECMD&ZSCREEN..LIST)
00800000   IF &SYSDSN(&EDSN) ^= OK THEN DO
00810019     ALLOC F(&EFILE) REUSE DS(&EDSN) NEW CAT +
00820019            &DCB +
00830019            SPACE(1 5) TRACKS
00840000   END
00850000   ELSE ALLOC F(&EFILE) REUSE DS(&EDSN) SHR
00860000 END
00870000 ELSE DO
00880000   ALLOC F(&EFILE) REUSE NEW DELETE +
00890000          &DCB +
00900000          SPACE(1 5) TRACKS
00910000 END
00920000 IF &DEBUG =  THEN CONTROL MSG
00930000 
00940000 OPENFILE &EFILE OUTPUT
00950000 SET &RC = &LASTCC
00960000 
00970000 SET &CNT = 1
00980000 IF &DATATYPE(&SKIP1ST) = NUM AND &SKIP1ST >= 1 THEN +
00990000   SET &CNT = &EVAL(&SKIP1ST + 1)
01000000 SET &MAXLINES = &TRAPKNT
01010000 IF &DATATYPE(&SKIPLST) = NUM AND &SKIPLST < &TRAPKNT THEN +
01020000   SET &MAXLINES = &EVAL(&TRAPKNT - &SKIPLST)
01030000 
01040000 IF &STR(&TITLE) ^=  THEN DO
01050000   SET &&EFILE = &STR(&TITLE)
01060000   PUTFILE &EFILE
01070000   IF &STR(&TITLE2) ^= THEN DO
01080000     SET &&EFILE = &STR(&TITLE2)
01090000     PUTFILE &EFILE
01100000   END
01110000 END
01120000 
01130000 LOOP:+
01140000 DO WHILE (&CNT <= &MAXLINES)
01150000   SET &LINE = &&SYSOUTLINE&CNT
01160013   SET &L    = &LENGTH(&STR(&SYSNSUB(2,&LINE)))
01170000   IF &DATATYPE(&STRIP) = NUM  AND &CMDRC = 0 THEN DO
01180000     /* STRIP OFF THE FIRST &STRIP COLUMNS OF OUTPUT */
01190000     IF &STRIP < &L THEN +
01200013       SET &LINE = &SUBSTR(&STRIP+1:&L,&STR(&SYSNSUB(2,&LINE)))
01210000   END
01220001   IF &WRAP = WRAP AND &L > 80 THEN DO
01230016     SET &&EFILE = &SUBSTR(1:80,&STR(&SYSNSUB(2,&LINE)))
01240001     PUTFILE &EFILE
01250016     SET &&EFILE = &SUBSTR(81:&L,&STR(&SYSNSUB(2,&LINE)))
01260002     PUTFILE &EFILE
01270001   END
01280002   ELSE DO
01290017     SET &&EFILE = &STR(&SYSNSUB(2,&LINE))
01300002     PUTFILE &EFILE
01310002   END
01320000   SET &CNT = &CNT + 1
01330000 END
01340000 
01350000 CLOSFILE &EFILE
01360019 
01370000 IF &NOSHOW ^= NOSHOW THEN DO
01380019   IF &NOTEMP = NOTEMP THEN DO
01390019     ISPEXEC  CONTROL DISPLAY  REFRESH
01400019     ISPEXEC &FUNC DATASET(&EDSN) &BRPNL
01410019     SET FUNCRC = &LASTCC
01420019     ISPEXEC  CONTROL DISPLAY  REFRESH
01430019   END
01440019   ELSE DO
01450019     SET &DDID =
01460019     ISPEXEC LMINIT DATAID(DDID) DDNAME(&EFILE)
01470019     ISPEXEC CONTROL DISPLAY  REFRESH
01480019     ISPEXEC &FUNC DATAID(&DDID) &BRPNL
01490019     SET FUNCRC = &LASTCC
01500019     ISPEXEC LMFREE DATAID(&DDID)
01510019     ISPEXEC CONTROL DISPLAY  REFRESH
01520019   END
01530019   SET ECMDFRC = &FUNCRC
01540019   ISPEXEC VPUT (ECMDFRC)
01550019   IF &FUNC = BROWSE AND &FUNCRC = 12 THEN DO
01560019     SET SHORT = &STR(No output)
01570019     SET LONG = &STR("&COMMAND &OPERAND" didn't have any output to browse.)
01580019     ISPEXEC SETMSG MSG(SYS010)
01590019   END
01600000 END
01610000 
01620000 IF &DEBUG =  THEN CONTROL NOMSG
01630000 IF &NOFREE ^= NOFREE THEN FREE F(&EFILE)
01640011 
01650011XIT:+
01660019 IF &SYSISPF = ACTIVE THEN ISPEXEC CONTROL DISPLAY RESTORE
01670000 EXIT CODE(&CMDRC)
