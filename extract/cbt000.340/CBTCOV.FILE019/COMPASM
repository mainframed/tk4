00010002PROC     0        DEBUG
00020002IF       &DEBUG = DEBUG +
00030002THEN     CONTROL   CONLIST NOFLUSH   LIST   MSG   PROMPT   SYMLIST
00040002ELSE     CONTROL NOCONLIST NOFLUSH NOLIST NOMSG   PROMPT NOSYMLIST
00050002FREE     FILE(NEW,OLD,PLIDUMP,SYSIN,SYSPRINT,SYSPUNCH,SYSTERM)
00060002ISPEXEC  DISPLAY  PANEL(COMPASM)
00070002SET      &DISPLAY = &LASTCC
00080002DO       WHILE    &DISPLAY = 0
00090002         IF       &TERM = TERM +
00100002         THEN     ALLOCATE FILE(SYSTERM) DATASET(*)
00110002         SET      &SPACE = (2*&SIZE+2*&SIZE/&LINECNT)/44+2
00120002         ALLOCATE FILE(SYSPRINT) DATASET(COMPASM.OUTLIST) +
00130002                  RECFM(V B A) LRECL(125) BLKSIZE(6032) SPACE(&SPACE 10) ROUND
00140002         IF       &LASTCC ^= 0 +
00150002         THEN     DO
00160002                  DELETE   COMPASM.OUTLIST
00170002                  ALLOCATE FILE(SYSPRINT) DATASET(COMPASM.OUTLIST) +
00180002                           RECFM(V B A) LRECL(125) BLKSIZE(6032) SPACE(&SPACE 10) ROUND
00190002                  END
00200002         ALLOCATE FILE(OLD) DATASET(&OLD) SHR
00210002         ALLOCATE FILE(NEW) DATASET(&NEW) SHR
00220002         IF       &DECK = DECK +
00230002         THEN     ALLOCATE FILE(SYSPUNCH) DATASET(&SYSPUNCH) OLD
00240002         IF       &SYSIN ^= +
00250002         THEN     ALLOCATE FILE(SYSIN) DATASET(&SYSIN) SHR
00260002         IF       &DEBUG = DEBUG +
00270002         THEN     ALLOCATE FILE(PLIDUMP) SYSOUT(H)
00280002         SET      &PARM = &STR(SIZE=&SIZE)
00290002         SET      &PARM = &STR(&PARM,COLUMN=&COLUMN)
00300002         SET      &PARM = &STR(&PARM,&TRANSLAT)
00310002         SET      &PARM = &STR(&PARM,&TERM)
00320002         SET      &PARM = &STR(&PARM,&LIST)
00330002         SET      &PARM = &STR(&PARM,LINECOUNT=&LINECNT)
00340002         SET      &PARM = &STR(&PARM,&DECK)
00350002         SET      &PARM = &STR(&PARM,FORMAT=&FORMAT)
00360002         SET      &PARM = &STR(&PARM,INCR=&INCR)
00370002         SET      &PARM = &STR(&PARM,SEQFLD=&SEQFLD)
00380002         SET      &PARM = &STR(&PARM,MODID=&MODID)
00390002         SET      &ISASIZE = 164*&SIZE+4500
00400002         IF       &DEBUG = DEBUG +
00410002         THEN     SET      &PLIPARM = &STR(ISASIZE(&ISASIZE),REPORT,SPIE,STAE)
00420002         ELSE     SET      &PLIPARM = &STR(ISASIZE(&ISASIZE))
00430002         ISPEXEC  CONTROL  DISPLAY  LINE     START(1)
00440002         ISPEXEC  SELECT   PGM(COMPASM) PARM(&PLIPARM/&PARM)
00450002         %GENPRINT DSN(COMPASM.OUTLIST)
00460002         FREE     FILE(NEW,OLD,PLIDUMP,SYSIN,SYSPRINT,SYSPUNCH,SYSTERM)
00470002         ISPEXEC  DISPLAY  PANEL(COMPASM)
00480002         SET      &DISPLAY = &LASTCC
00490002         END
