00010000PROC     0        DEBUG    VERIFY
00020000 
00030000/*********************************************************************/
00040000/*                   DUMP DATA SET MANAGEMENT                        */
00050000/* IPCS: THIS CLIST OPENS A DUMP DATA SET. IT OBTAINS INFORMATION    */
00060000/* FOR USE BY LOWER LEVEL PANELS AND CLISTS. IT THEN CREATES AN      */
00070000/* ENTRY IN A PERMANENT ISPF TABLE. THE ENTRY CORRESPONDS TO AN      */
00080000/* ENTRY IN THE USER'S IPCS DIRECTORY.                               */
00090000/*********************************************************************/
00100000 
00110000IF       &DEBUG = DEBUG +
00120000THEN     CONTROL  ASIS   CONLIST NOFLUSH   LIST   MSG   PROMPT   SYMLIST
00130000ELSE     CONTROL  ASIS NOCONLIST NOFLUSH NOLIST   MSG   PROMPT NOSYMLIST
00140000 
00150000SET      &IPCRETCD = 0
00160000SET      &INIT     = YES
00170000 
00180000ISPEXEC  VGET     (DEFMACH DEFREM DEFREQ DEFSTOR DEFSYM DEFFLAG DEFLEN DEFTERM DEFVER DEFCONF +
00190000                   IPCOS ZSCREEN +
00200000                   CPU DATE DATETIME DEVTYPE DSNAME DUMPOS FILE IBMPMR ID JOB NODE OWNER PRDUMPT QUAL +
00210000                   SERIAL SOURCE SPR SRABD SRRC SRCID SRLM SRCSECT SRFRR SREGPSW SRPGM SRLVL SRSC SRRRL +
00220000                   TIME TITLE TITLE2 TYPE VOLUME)
00230000 
00240000ISPEXEC  TBTOP    DDIR&IPCOS
00250000ISPEXEC  TBSCAN   DDIR&IPCOS ARGLIST(SOURCE) NOREAD
00260000SET      &TBSCAN = &LASTCC
00270000 
00280000IF       &VERIFY  = VERIFY +
00290000THEN     DO
00300000         IF       &IPCOS = XA +
00310000         THEN     ISPEXEC  SELECT   PGM(BLSGSCMD) PARM(EVALDUMP = &SOURCE)
00320000         ELSE     ISPEXEC  SELECT   PGM(BLSGSCMD) PARM(EVALDUMP = &DSNAME)
00330000         IF       &LASTCC ^= 0 +
00340000         THEN     SET      &INIT = NO
00350000         ELSE     DO
00360000                  ISPEXEC  DISPLAY  PANEL(IPCVER)
00370000                  IF       &LASTCC = 0 +
00380000                  THEN     DO
00390000                           SET      &INIT = NO
00400000                           ISPEXEC  TBDELETE DDIR&IPCOS
00410000                           ISPEXEC  SELECT   PGM(BLSGSCMD) PARM(DROPDUMP &SOURCE)
00420000                           END
00430000                  END
00440000         END
00450000 
00460000IF       &FILE ^= &STR() +
00470000THEN     ALLOCATE FILE(&FILE) DSNAME(&DSNAME) VOLUME(&VOLUME) UNIT(&DEVTYPE) SHR REUSE
00480000 
00490000ISPEXEC  SELECT   PGM(BLSGSCMD) PARM(SETDEF &SOURCE &DEFCONF DISPLAY(&DEFMACH &DEFREM &DEFREQ &DEFSTOR &DEFSYM) +
00500000                                     FLAG(&DEFFLAG) LENGTH(&DEFLEN) NOLIST NOPRINT &DEFTERM &DEFVER)
00510000SET      &IPCRETCD = &LASTCC
00520000IF       &IPCRETCD = 0 +
00530000THEN     DO
00540000         ISPEXEC  SELECT   PGM(BLSGSCMD) PARM(OPEN &SOURCE DEFAULT)
00550000         SET      &IPCRETCD = &LASTCC
00560000         END
00570000 
00580000IF       &IPCRETCD = 0 && &INIT = NO +
00590000THEN     DO
00600000         IF       &SOURCE = ACTIVE +
00610000         THEN     ISPEXEC  SELECT   PGM(BLSGSCMD) PARM(EQUATE TITLE 20. HEADER CHARACTER LENGTH(100))
00620000         ISPEXEC  SELECT   PGM(BLSGSCMD) PARM(%IPCEVAL TITLE DATA(TITLE))
00630000         ISPEXEC  VGET     (TITLE)
00640000         IF       &LENGTH(&STR(&TITLE)) <= 50 +
00650000         THEN     SET      &TITLE2 = &STR()
00660000         ELSE     SET      &TITLE2 = &SUBSTR(51:&LENGTH(&STR(&TITLE)),&STR(&TITLE))
00670000         ISPEXEC  VPUT     (TITLE TITLE2)
00680000         IF       &TBSCAN = 0 +
00690000         THEN     ISPEXEC  TBPUT    DDIR&IPCOS ORDER
00700000         ELSE     ISPEXEC  TBADD    DDIR&IPCOS ORDER
00710000         %IPCQUAL &DEBUG
00720000         IF       &IPCOS = XA && &SOURCE ^= ACTIVE +
00730000         THEN     ISPEXEC  SELECT   PGM(BLSGSCMD) PARM(%IPCSTACK &DEBUG)
00740000         END
00750000 
00760000ISPEXEC  VPUT     (IPCRETCD) SHARED
00770000 
00780000EXIT     CODE(0)
