00010000         PROC 0  DEBUG GO ALL
00020001         /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
00030001         /* DIALOG TO SCAN ACF2 LOGONID DATABASE FOR NAME/ID/PHONE  */
00040001         /* WRITTEN BY ANDY ANDERSON.                               */
00050001         /* WITH MODS BY MIKE THEYS FOR:                            */
00060001         /*  ISPF COMMAND INTERFACING BY %SYSUID (USERID,USERNAME)  */
00070001         /*  SUPPORT OF SWCC,SWC2,WCC1,CGAD                         */
00080001         /*  SWC,CGAD HAVE TSO AND ALL ACF2 IDS DATABASES           */
00090001         /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
00100000         CONTROL NOFLUSH
00110000         IF &DEBUG NE DEBUG THEN CONTROL NOLIST NOMSG
00120000         ELSE CONTROL LIST PROMPT CONLIST SYMLIST MSG
00130000 
00140000         /*SET CONTROL TO RETURN TO CLIST FOR ALL ISPF SERVICE ERRORS
00150000         ISPEXEC CONTROL ERRORS RETURN
00160000         SET &TRYCNT = 1
00170000TRY2:    ALLOC F(UID) DS('ATSO.MEMO.TABLES') SHR REU
00180000         IF &LASTCC NE 0 THEN DO
00190000           IF &TRYCNT <= 1 THEN DO
00200000            FREE F(XXX)
00210000            ISPEXEC TBOPEN XXX NOWRITE LIBRARY(XXX)
00220000            SET &TRYCNT = &TRYCNT + 1
00230000            GOTO TRY2
00240000           END
00250000           ELSE DO
00260000            SET &SHORT = &STR(FILE ERR)
00270000            SET &LONG = &STR(CAN NO ALLOC UID FILE)
00280000            ISPEXEC SETMSG MSG(SYS010)
00290000            EXIT CODE(8)
00300000           END
00310000          END
00320000          SET &Z0 = &STR(000000000)
00330000          SET &SEL =
00340000          SET &MSG =
00350000          SET &HUSER =
00360000          SET &HNAME =
00370000          SET &HPHONE =
00380000          SET &HDIV =
00390000          SET &HDEPT =
00400000          SET &HGROUP =
00410000          SET &HMCODE =
00420000          SET &MEMCOMP = &STR(ROCKWELL)
00430000 
00440000          /* WHICH TABLE TO USE: WCC1/WCC2  TSO/ALL IDS */
00450000NEWTAB:   IF &GO = GO THEN DO
00460000               SHOWTSO CENTER
00470000               SET UIDCTR = &CENTER
00480000               IF &ALL = ALL THEN SET UIDTYPE = ALL
00490000               ELSE               SET UIDTYPE = TSO
00500000               ISPEXEC CONTROL NONDISPL
00510000          END
00520000          ISPEXEC DISPLAY PANEL(SYSUIDA)
00530000          IF &LASTCC NE 0 THEN DO
00540000             FREE FI(UID)
00550000             EXIT
00560000          END
00570000          IF &UIDCTR = SWCC THEN SET CTRN = 1
00580000          IF &UIDCTR = SWC2 THEN SET CTRN = 2
00590000          IF &UIDCTR = WCC1 THEN SET CTRN = 1
00600000          IF &UIDCTR = WCC2 THEN SET CTRN = 2
00610001          IF &UIDCTR = CGAD THEN SET CTRN =
00620000          SET &TABLE = &STR(UIDS&UIDTYPE.&CTRN)
00630000          IF &SUBSTR(1:3,&STR(&UIDCTR)) = WCC THEN SET WCC = YES
00640000          IF &WCC = YES THEN SET &TABLE = &STR(UIDTAB&CTRN)
00650000          /* TABLE=UIDSTSO1,UIDSALL1,UIDSTSO2,UIDSALL2 */
00660000          /* OPEN THE LOGON ID TABLE TO SEARCH */
00670000          ISPEXEC TBEND &TABLE
00680000          ISPEXEC TBOPEN &TABLE NOWRITE LIBRARY(UID)
00690000          IF &LASTCC ^= 0 THEN DO
00700000                ISPEXEC TBEND &TABLE
00710000                ISPEXEC TBOPEN &TABLE NOWRITE LIBRARY(UID)
00720000                IF &LASTCC ^= 0 THEN DO
00730000                      ISPEXEC SETMSG MSG(UIDL220)
00740000                      EXIT CODE(4)
00750000                END
00760000          END
00761002          ISPEXEC TBSTATS &TABLE UDATE(UIDDATE) LIBRARY(UID)
00770000REENT:    +
00780000             ISPEXEC  TBVCLEAR &TABLE
00790000             SET &USER  = &STR(&HUSER)
00800000             SET &NAME  = &STR(&HNAME)
00810000             SET &PHONE = &STR(&HPHONE)
00820000             SET &DIV   = &STR(&HDIV)
00830000             SET &DEPT  = &STR(&HDEPT)
00840000             SET &GROUP = &STR(&HGROUP
00850000             SET &MCODE = &STR(&HMCODE)
00860000BADSER:      +
00870000             ISPEXEC DISPLAY PANEL(SYSUIDE) MSG(&MSG)
00880000             SET &RC = &LASTCC
00890000             SET &MSG =
00900000             IF &RC ^= 0 THEN DO
00910000                 ISPEXEC TBCLOSE &TABLE LIBRARY(UID)
00920000                 ISPEXEC TBOPEN XXX NOWRITE LIBRARY(XXX)
00930000                 IF &GO = GO THEN EXIT
00940000                 GOTO NEWTAB
00950000             END
00960000             SET &MSG =
00970000            IF &STR(&DIV) NE THEN DO
00980000               IF &LENGTH(&STR(X&DIV)) < 5 THEN DO
00990000               IF &LENGTH(&STR(X&DIV)) = 2 THEN
01000000                   IF &STR(&DIV) = &STR(*) THEN GOTO NOSUB
01010000               SET &DIV = &STR(&SUBSTR(1:4 - &LENGTH(&STR(&DIV)),&STR(&Z0))&DIV)
01020000               END
01030000            END
01040000NOSUB:       ISPEXEC TBTOP &TABLE
01050000             ISPEXEC TBSARG  &TABLE
01060000             SET &RC = &LASTCC
01070000             SET &HUSER  = &STR(&USER)
01080000             SET &HNAME  = &STR(&NAME)
01090000             SET &HPHONE = &STR(&PHONE)
01100000             SET &HDIV   = &STR(&DIV)
01110000             SET &HDEPT  = &STR(&DEPT)
01120000             SET &HGROUP = &STR(&GROUP
01130000             SET &HMCODE = &STR(&MCODE)
01140000RESHW:       SET &SEL =
01150000             ISPEXEC TBDISPL &TABLE PANEL(SYSUIDF) AUTOSEL(NO)
01160000             SET &RC = &LASTCC
01170000             IF &RC ^= 0 THEN DO
01180000                    GOTO REENT
01190000             END
01200000             GOTO RESHW
