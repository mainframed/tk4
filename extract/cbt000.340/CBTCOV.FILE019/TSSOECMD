00010000         PROC 1 COMMAND OPERAND() DEBUG NOSHOW +
00020000                NOFREE EDIT NOTEMP MAXLINES(500) +
00030000                STRIP() MSG(MSG) MSGID() CMDNAME() RNDNAME()
00040000 
00050000         CONTROL NOFLUSH ASIS
00060000         ISPEXEC CONTROL ERRORS RETURN
00070000 
00080000         IF &DEBUG = DEBUG THEN CONTROL LIST PROMPT CONLIST SYMLIST MSG
00090000         ELSE                   CONTROL NOLIST
00100000 
00110000         IF      &MSGID =   MSGID THEN PROFILE   MSGID
00120000         ELSE IF &MSGID = NOMSGID THEN PROFILE NOMSGID
00130000 
00140000         IF      &MSG   =   MSG   THEN CONTROL   MSG
00150000         ELSE IF &MSG   = NOMSG   THEN CONTROL NOMSG
00160000 
00170000         IF &EDIT = EDIT THEN SET &FUNC = EDIT
00180000         ELSE                 SET &FUNC = BROWSE
00190000 
00200001         SET NOTEMP = /* NOTEMP */
00210000         IF &STR(&CMDNAME)  =  THEN SET CMDNAME = &STR(&COMMAND)
00220000         SET TSSOCMD = &STR(&CMDNAME)
00230000         SET TSSORND = &STR(&RNDNAME)
00240000 
00250000         ISPEXEC VGET (ZSCREEN)
00260000         IF &DEBUG =  THEN CONTROL NOMSG
00270000         IF &NOTEMP = NOTEMP THEN DO
00280000           IF &SYSDSN(TSSO&ZSCREEN..&TSSOCMD..LIST) ^= OK THEN DO
00290000                ALLOC F(TSSOF&ZSCREEN.) REUSE DS(TSSO&ZSCREEN..&TSSOCMD..LIST) NEW CAT +
00300000                       LRECL(80) RECFM(F B) BLKSIZE(3120) +
00310000                       SPACE(1 1) TRACKS
00320000           END
00330000           ELSE ALLOC F(TSSOF&ZSCREEN.) REUSE DS(TSSO&ZSCREEN..&TSSOCMD..LIST) SHR +
00340000                       LRECL(80) RECFM(F B) BLKSIZE(3120)
00350000         END
00360000         ELSE DO
00370000           ALLOC F(TSSOF&ZSCREEN.) REUSE NEW DELETE +
00380000                  LRECL(80) RECFM(F B) BLKSIZE(3120) +
00390000                  SPACE(1 1) TRACKS
00400000         END
00410000         IF &DEBUG =  THEN CONTROL MSG
00420000 
00430000         OPENFILE TSSOF&ZSCREEN. OUTPUT
00440000         SET &RC = &LASTCC
00450000         SET &SYSOUTTRAP = &MAXLINES
00460000           &COMMAND &OPERAND
00470000           SET &CMDRC = &LASTCC
00480000         SET &LEM = &SYSOUTLINE
00490000         SET &SYSOUTTRAP = 0
00500000 
00510000         SET &CNT = 1
00520000LOOP:    DO WHILE (&CNT <= &LEM)
00530000           SET &LINE = &&SYSOUTLINE&CNT
00540000           IF &DATATYPE(&STRIP) = NUM  AND &CMDRC = 0 AND &LEM > 1 THEN DO
00550000             /* STRIP OFF THE FIRST &STRIP COLUMNS OF OUTPUT */
00560000             SET &L = &LENGTH(&STR(&LINE))
00570000             IF &STRIP < &L THEN +
00580000               SET &LINE = &SUBSTR(&STRIP+1:&L,&STR(&LINE))
00590000           END
00600000           SET  TSSOF&ZSCREEN. = &STR(&LINE)
00610000           PUTFILE TSSOF&ZSCREEN.
00620000           SET &CNT = &CNT + 1
00630000         END
00640000 
00650000         CLOSFILE TSSOF&ZSCREEN.
00660000         IF &NOSHOW ^= NOSHOW THEN DO
00670000           SET L = &LENGTH(&STR(&TSSOCMD.))
00680000           IF &L < 8 THEN SET BLANKS = &SUBSTR(1:8-&L,&STR(        ))
00690000           SET SHORT = &STR(&BLANKS.&TSSOCMD. output!)
00700000           SET LONG  = &STR(Browseable output for TSSO command "&TSSOCMD &TSSORND".)
00710000           ISPEXEC SETMSG MSG(AOST010)
00720000             IF &NOTEMP = NOTEMP THEN DO
00730000               ISPEXEC &FUNC DATASET(TSSO&ZSCREEN..&TSSOCMD..LIST) PANEL(TSSOBROW)
00740000               SET FUNCRC = &LASTCC
00750000               ISPEXEC  CONTROL DISPLAY  REFRESH
00760000             END
00770000             ELSE DO
00780000               SET &DDID =
00790000               ISPEXEC LMINIT DATAID(DDID) DDNAME(TSSOF&ZSCREEN.)
00800000               ISPEXEC &FUNC DATAID(&DDID) PANEL(TSSOBROW)
00810000               SET FUNCRC = &LASTCC
00820000               ISPEXEC LMFREE DATAID(&DDID)
00830000               ISPEXEC CONTROL DISPLAY  REFRESH
00840000             END
00850000         END
00860000 
00870000         IF &FUNCRC = 12 THEN DO
00880000           SET SHORT = &STR(No &TSSOCMD output/ACF2err!)
00890000           SET LONG  = &STR("&TSSOCMD &TSSORND" didn't have any output OR ACF2 rules didn't let &TSSOCMD. execute.)
00900000           ISPEXEC SETMSG MSG(AOST010)
00910000         END
00920000         IF &DEBUG =  THEN CONTROL NOMSG
00930000         IF &NOFREE ^= NOFREE THEN FREE F(TSSOF&ZSCREEN.)
00940000 
00950000         EXIT CODE(0)
