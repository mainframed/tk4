00010000PROC 0 DEBUG INIT          +
00020000             RMBFRUSR   RMBFAUSR +
00030000             SCROLLIT OPT#()
00040000  /* GENERAL PURPUSE SCROLLABLE PRIMARY OPTION MENU w/ 36 Opts Max */
00050000  /* YOR@PRIM-Like Scrollable Primary Menu Version #3 with 36 Opts */
00060000  /* This version will handle the ISPF "Jump" cmds if PRIMARY Menu */
00070000  /* This clist sets the initial menu options on PANEL(????PRIM)   */
00080000  /* It is invoked by either ????PRIM panel on first startup       */
00090000  /* Or it is invoked by %MBFSSCRL clist when SCROLL PF Keys used. */
00100000  /* RMBFRUSR: Convert Rockwell InfoCenter "Design a User Menu"    */
00110000  /*           variables to the Menu BUILD Format. Use Scroll YOUR.*/
00120000  /* RMBFAUSR: Convert AOST User Dialog Primary Option Menu vars   */
00130000  /*           to the Menu BUILD Format. Use THE MBF Primary       */
00140000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00150000  /**                                                             **/
00160000  /** User/Group Modifable Primary Option Menu: BUILD             **/
00170000  /**                                                             **/
00180000  /** Michael E. Theys                                            **/
00190000  /** Southwestern Computing Center                               **/
00200000  /** Information Systems Center                                  **/
00210000  /** Rockwell International                                      **/
00220000  /** 1200 N. Alma Rd.  Bldg 401-109                              **/
00230000  /** Richardson, Texas 75081-2205                                **/
00240000  /** (214) 996-2860  COMNET 437-2860                             **/
00250000  /** XMIT Address    SWCC.XTS0008                    Dec 1986    **/
00260000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00270000 
00280000CONTROL ASIS
00290000ISPEXEC CONTROL ERRORS RETURN
00300000IF &DEBUG = DEBUG +
00310000  THEN CONTROL   LIST   CONLIST   SYMLIST NOFLUSH   PROMPT   MSG
00320000  ELSE CONTROL NOLIST NOCONLIST NOSYMLIST   FLUSH NOPROMPT NOMSG
00330000 
00340000/* Validate the BUILD Variable Pool and Max Displayable Menu Options */
00350000%RMBFPOOL
00360000ISPEXEC VGET (RMBFPOOL,RMBFMAXM,RMBFMAXD)
00370000SET POOL = &RMBFPOOL
00380000 
00390000/* Used when old Rockwell RUSRPRIM is replaced with Scroll YOUR Menu */
00400000IF &RMBFRUSR = RMBFRUSR THEN %RMBFRUSR POOL(&POOL)
00410000/* Used when old AOST     USERPRIM is replaced with Scroll YOUR Menu */
00420000IF &RMBFAUSR = RMBFAUSR THEN %RMBFAUSR POOL(&POOL)
00430000 
00440000IF &INIT = INIT THEN DO
00450000  ISPEXEC VGET (Z,R&POOL.DOIT,R&POOL.ZPRM)
00460000  SET CLR = &STR(&Z)
00470000  SET ZERRSM = &CLR
00480000  SET ZERRLM = &CLR
00490000  ISPEXEC VPUT (ZERRSM,ZERRLM)
00500000 
00510000  /* Perform Panel Initialization if requested */
00520000  SET DOIT =  &STR(&&R&POOL.DOIT)
00530000  IF &DOIT =  YES THEN &STR(%&&POOL.FIRST)
00540000END
00550000 
00560000/*  RMBFPOOL = ???                                           */
00570000/*  RMBFMAXM = 36     Maximum number of options available    */
00580000/*  RMBFMAXD = 18     Maximum that can be displayed on panel */
00590000ISPEXEC VGET (RMBFPOOL,RMBFMAXM,RMBFMAXD)
00600000 
00610000/* Get the Displayable Menu Options */
00620000/* Begin Option to Display (Varies with Scroll) */
00630000/* If this was invoked by %MBFSSCRL Scroll clist then use passed OPT# */
00640000IF &SCROLLIT =          THEN SET OPT# = 1
00650000SET FDOPT = 999 /* Position of the First Non-Blank Displayable Option */
00660000SET LDOPT =  -1 /* Position of the Last  Non-Blank Displayable Option */
00670000SET I = &OPT#
00680000SET Q = 1    /* Start Display position on panel */
00690000DO WHILE(&Q <= &RMBFMAXM)
00700000  IF &I <=  &RMBFMAXM THEN DO
00710000    ISPEXEC VGET (R&POOL.O&I,R&POOL.SD&I,R&POOL.LD&I,R&POOL.C&I) PROFILE
00720000    SET OPT      = &STR(&&R&POOL.O&I)
00730000    SET SD       = &STR(&&R&POOL.SD&I)
00740000    SET LD       = &STR(&&R&POOL.LD&I)
00750000    SET CMD      = &STR(&&R&POOL.C&I)
00760000  END
00770000  ELSE DO
00780000    SET OPT      = &STR( )
00790000    SET SD       = &STR( )
00800000    SET LD       = &STR( )
00810000    SET CMD      = &STR( )
00820000  END
00830000  IF &STR(&OPT) ^= THEN DO
00840000    IF &I < &FDOPT THEN SET FDOPT = &I
00850000    IF &I > &LDOPT THEN SET LDOPT = &I
00860000  END
00870000  SET RQQQO&Q  = &STR(&OPT)
00880000  SET RQQQSD&Q = &STR(&SD)
00890000  SET RQQQLD&Q = &STR(&LD)
00900000  SET RQQQC&Q  = &STR(&CMD)
00910000  ISPEXEC VPUT (RQQQO&Q,RQQQSD&Q,RQQQLD&Q,RQQQC&Q)
00920000  SET I = &I + 1
00930000  SET Q = &Q + 1
00940000END
00950000IF &FDOPT  = 999 THEN SET &FDOPT = 1
00960000IF &LDOPT  =  -1 THEN SET &LDOPT = &RMBFMAXM
00970000 
00980000SET R&POOL.OPT# = &OPT#
00990000ISPEXEC VPUT (R&POOL.OPT#,RMBFMAXM)
01000000 
01010000/* Menu options have been load to the displayable portion of the menu */
01020000/* or they have been scrolled either up or down the proper amount.    */
01030000/* Now back to the Scrollable YOUR Own Primary Menu */
01040000EXIT
