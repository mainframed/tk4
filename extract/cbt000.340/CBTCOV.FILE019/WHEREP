00010008PROC     1     LOCATION         /* DECIMAL ADDRESS                  */ +
00020008               ASID(0)          /* ADDRESS SPACE NUMBER             */ +
00030008               DEBUG            /* PRODUCE FULL OUTPUT LISTING      */
00040008IF       &DEBUG = DEBUG +
00050008THEN     CONTROL CONLIST LIST MSG PROMPT SYMLIST
00060008GLOBAL   SUBPOOL KEY FREE
00070008SET      &ASCBASXB = 108N       /* ASCB -> ASXB     */
00080008SET      &ASCBLDA = 48N         /* ASCB -> LDA      */
00090008SET      &ASVTFRST = 524N       /* ASVT -> ASCB     */
00100008SET      &ASXBFTCB = 4N         /* ASXB -> TCB      */
00110008SET      &TCBJPQ = 44N          /* TCB  -> CDE      */
00120008SET      &TCBTCB = 116N         /* TCB  -> TCB      */
00130010EVALUATE ASVT STRUCTURE(ASVT)
00140006IF       &ASID = 0 +
00150011THEN     DO       /* ASID DEFINITION */
00160006         EVALUATE 2. HEADER LENGTH(2)
00170011         SET      &ASID = &LASTCC
00180011         IF       &ASID = 0 +
00190011         THEN     DO      /* ASK FOR ASID */
00200011                  WRITENR ENTER ASID:
00210011                  READ    &ASID
00220011                  END     /* ASK FOR ASID */
00230006         END   /* ASID DEFINITION */
00240006SETDEF   ASID(&ASID)
00250008SET      &ASVTENTY = &STR(ASVT+&ASVTFRST+&EVAL(&ASID*4)N)
00260010EQUATE   XASCB &ASVTENTY% STRUCTURE(ASCB) ASID(&ASID)
00270010EQUATE   XASXB XASCB+&ASCBASXB% STRUCTURE(ASXB) ASID(&ASID)
00280008SET      &PTRTCB = &STR(XASXB+&ASXBFTCB)
00290006SET      &TCB = VALID
00300006SET      &FOUND = NO
00310011DO       WHILE   &TCB = VALID  && &FOUND = NO /* TCB LOOP */
00320011         EQUATE  XTCB  &PTRTCB% STRUCTURE(TCB) ASID(&ASID)
00330011         SET     &PTRCDE = &STR(XTCB+&TCBJPQ)
00340011         EXEC    'JES3.CLIST(WHEREM)' '&LOCATION &PTRCDE &DEBUG'
00350011         IF      &LASTCC = 0 +
00360011         THEN    SET   &FOUND = YES
00370011         SET     &PTRTCB = &STR(XTCB+&TCBTCB)
00380008         COMPARE ADDRESS(&PTRTCB) WITH(VALUE(X'00000000')) LENGTH(4)
00390011         IF      &LASTCC ^= 8 +
00400011         THEN    SET   &TCB = INVALID
00410011         END     /* TCB LOOP */
00420000IF       &FOUND = NO +
00430011THEN     DO      /* CLEANUP */
00440011         NOTE    'NOT FOUND'
00450006         DROPSYM (XASCB XASXB XTCB)
00460011         END     /* CLEANUP */
