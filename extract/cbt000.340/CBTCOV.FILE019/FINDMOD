00010005PROC     1     MODULENAME ASID(0) DEBUG NEXT
00020002IF       &DEBUG = DEBUG +
00030002THEN     CONTROL SYMLIST CONLIST LIST PROMPT
00040002SET      &NAME = &SUBSTR(1:8,&MODULENAME       )
00050000SET      &ASCBASXB = 108N
00060003SET      &ASVTFRST = 524N
00070000SET      &ASXBFTCB = 4N
00080003SET      &CDATTR = 28N
00090003SET      &CDCHAIN = 0N
00100003SET      &CDMIN = X'04'
00110003SET      &CDNAME = 8N
00120003SET      &CDXLMJP = 20N
00130000SET      &TCBJPQ = 44N
00140000SET      &TCBTCB = 116N
00150002SET      &XTLMSBAA = 12N
00160003SET      &XTLMSBLN = 9N
00170002IF       &ASID = 0 +
00180001THEN     DO    /* ASID DEFINITION */
00190000         EVALUATE 2. HEADER LENGTH(2)
00200000         SET   &ASID = &LASTCC
00210002         IF    &ASID = 0 +
00220000         THEN  SET   &ASID = 1
00230001         END   /* ASID DEFINITION */
00240002SETDEF   ASID(&ASID)
00250000EVALUATE ASVT STRUCTURE(ASVT)
00260004SET      &ASVTENTY= &STR(ASVT+&ASVTFRST+&EVAL(&ASID*4)N)
00270004EQUATE   XASCB &ASVTENTY% STRUCTURE(ASCB)
00280000EQUATE   XASXB XASCB+&ASCBASXB% STRUCTURE(ASXB)
00290007SET      &TCBPTR = &STR(XASXB+&ASXBFTCB)
00300003SET      &TCB = VALID
00310003SET      &FOUND = NO
00320003DO       WHILE &TCB = VALID  && &FOUND = NO /* TCB LOOP */
00330003         IF    &NEXT = NEXT +
00340003         THEN  DO    /* CURRENT TCB */
00350003               SET   &NEXT =
00360007               SET   &CDEPTR = &STR(XCDE+&CDCHAIN)
00370003               END   /* CURRENT TCB */
00380003         ELSE  DO    /* NEXT TCB */
00390007               EQUATE XTCB  &TCBPTR% STRUCTURE(TCB)
00400007               SET   &CDEPTR = &STR(XTCB+&TCBJPQ)
00410003               END   /* NEXT TCB */
00420007         COMPARE ADDRESS(&CDEPTR) WITH(VALUE(X'00000000')) +
00430002                 LENGTH(4)
00440003         IF    &LASTCC = 8 +
00450003         THEN  SET   &CDE = VALID
00460003         ELSE  SET   &CDE = INVALID
00470003         DO    WHILE &CDE = VALID && &FOUND = NO /* CDE LOOP */
00480007               EQUATE XCDE &CDEPTR% STRUCTURE(CDE)
00490002               COMPARE  ADDRESS(XCDE+&CDNAME) +
00500002                        WITH(VALUE(C'&NAME')) LENGTH(8)
00510002               IF    &LASTCC = 0 +
00520001               THEN  DO    /* MODULE DEFINITION */
00530001                     SET   &FOUND = YES
00540001                     EQUATE CDE&MODULENAME XCDE STRUCTURE(CDE)
00550003                     COMPARE ADDRESS(XCDE+&CDATTR) MASK(&CDMIN) +
00560003                             WITH(VALUE(&CDMIN)) LENGTH(1)
00570003                     IF    &LASTCC ^= 0 +
00580003                     THEN  SET   &MAJORCDE = XCDE
00590003                     ELSE  SET   &MAJORCDE = &STR(XCDE+&CDXLMJP%)
00600003                     EQUATE XL&MODULENAME &MAJORCDE+&CDXLMJP% +
00610001                            STRUCTURE(XTLST)
00620001                     EVALUATE XL&MODULENAME+&XTLMSBLN LENGTH(3)
00630002                     SET   &SIZE = &LASTCC
00640002                     EQUATE &MODULENAME XL&MODULENAME+&XTLMSBAA% +
00650002                            MODULE(&MODULENAME) LENGTH(&SIZE)
00660002                     LIST  &MODULENAME NODISPLAY
00670001                     END   /* MODULE DEFINITION */
00680001               ELSE  DO    /* CDE CHAIN */
00690007                     SET   &CDEPTR = &STR(XCDE+&CDCHAIN)
00700007                     COMPARE ADDRESS(&CDEPTR) +
00710002                             WITH(VALUE(X'00000000')) LENGTH(4)
00720003                     IF    &LASTCC ^= 8 +
00730003                     THEN  SET   &CDE = INVALID
00740001                     END   /* CDE CHAIN */
00750001               END   /* CDE LOOP */
00760007         SET   &TCBPTR = &STR(XTCB+&TCBTCB)
00770007         COMPARE ADDRESS(&TCBPTR) WITH(VALUE(X'00000000')) LENGTH(4)
00780003         IF    &LASTCC ^= 8 +
00790003         THEN  SET   &TCB = INVALID
00800001         END   /* TCB LOOP */
00810002IF       &FOUND = NO +
00820002THEN     DO    /* CLEANUP */
00830006         NOTE  'MODULE &MODULENAME NOT FOUND ON JOB PACK QUEUE'
00840001         DROPSYM (XASCB XASXB XTCB XCDE)
00850001         END   /* CLEANUP */
