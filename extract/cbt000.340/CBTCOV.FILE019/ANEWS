00010000PROC 0 DEBUG BUILD DEL
00020000IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST NOFLUSH PROMPT MSG
00030000                  ELSE CONTROL NOLIST NOCONLIST NOSYMLIST FLUSH NOPROMPT NOMSG
00040000 
00050000 SET &ANEWSDS = &STR(AOST.NEWS.DATA)    /* DATASET NAME OF NEWS DATASET */
00060000 SET &AOSTLIB = &STR(AOST.ISPF.TABLES) /* DATASET NAME OF AOST TABLE LIBRARY */
00070000 SET &WORK    = NO
00080000 
00090000IF &BUILD = BUILD THEN DO
00100000                       ISPEXEC TBOPEN ANEWS WRITE
00110000                       SET &LCC = &LASTCC
00120000                       IF &LCC = 0 THEN GOTO ADD
00130000                       IF &LCC = 8 THEN GOTO CREATE
00140000                       SET &SHORT = &STR(***ERROR***)
00150000                       SET &LONG = &STR(TABLE OPEN ERROR CODE=&LCC)
00160000                       ISPEXEC VPUT (SHORT LONG)
00170000                       ISPEXEC SETMSG MSG(AOST010)
00180000                       GOTO EXIT
00190000                       END
00200000                     ELSE GOTO OPEN
00210000 
00220000CREATE: +
00230000        ISPEXEC TBCREATE ANEWS KEYS(ANEWSID) NAMES(ANDESC) WRITE
00240000        SET &LCC = &LASTCC
00250000        IF &LCC = 0 THEN GOTO ADD
00260000             SET &SHORT = &STR(***ERROR***)
00270000             SET &LONG = &STR(TABLE CREATE ERROR CODE=&LCC)
00280000             ISPEXEC VPUT (SHORT LONG)
00290000             ISPEXEC SETMSG MSG(AOST010)
00300000             GOTO EXIT
00310000 
00320000 
00330000OPEN: +
00340000  ISPEXEC  TBOPEN ANEWS
00350000  IF &LASTCC ^= 0 THEN DO
00360000                       SET &SHORT = &STR(***ERROR***)
00370000                       SET &LONG = &STR(AOST NEWS TABLE ERROR - CONTACT AOST COORDINATOR)
00380000                       ISPEXEC VPUT (SHORT LONG)
00390000                       ISPEXEC SETMSG MSG(AOST010)
00400000                       GOTO EXIT
00410000                       END
00420000 
00430000          SET &LCC = 0
00440000 
00450000ISPEXEC VGET ZENVIR
00460000IF &STR(&ZENVIR) ^= THEN DO
00470000                   ISPEXEC TBSORT ANEWS FIELDS(ANEWSID C,D)
00480000                   END
00490000 
00500000DISP: +
00510000     SET &ANSEL =
00520000     IF &LCC = 0 THEN +
00530000     ISPEXEC TBDISPL ANEWS PANEL(ANEWSPS)   /* DISPLAY ANEWS SELECTION PANEL */
00540000             ELSE +
00550000                    ISPEXEC TBDISPL ANEWS
00560000     SET &LCC = &LASTCC
00570000 
00580000     IF &LCC > 4 THEN GOTO CLOSE
00590000 
00600000     IF &ANSEL = D THEN DO
00610000                        IF &DEL  = DEL THEN DO
00620000                                               SET &SHORT = &STR(***DELETED***)
00630000                                               SET &LONG  = &STR(NEWS ITEM &ANEWSID DELETED)
00640000                                               ISPEXEC VPUT (SHORT LONG)
00650000                                               ISPEXEC SETMSG MSG(AOST010)
00660000                                               DELETE '&ANEWSDS(&ANEWSID)'
00670000                                               ISPEXEC TBDELETE ANEWS
00680000                                               ALLOC F(AOSTABL) DS('&AOSTLIB') SHR
00690000                                               ISPEXEC TBSAVE ANEWS LIBRARY(AOSTABL)
00700000                                               FREE F(AOSTABL)
00710000                                               GOTO DISP
00720000                                               END
00730000                                          ELSE DO
00740000                                               SET &SHORT = &STR(***ERROR***)
00750000                                               SET &LONG = &STR(INVALID OPTION &ANSEL)
00760000                                               ISPEXEC SETMSG MSG(AOST010)
00770000                                               SET &LCC = 0
00780000                                               GOTO DISP
00790000                                               END
00800000                        END
00810000 
00820000     IF &ANSEL ^= P THEN ISPEXEC BROWSE DATASET('&ANEWSDS(&ANEWSID)')
00830000 
00840000     IF &ANSEL = P  THEN %GENPRINT  DSN('''&ANEWSDS(&ANEWSID)''') NOBRO  NODEL NOED
00850000 
00860000     GOTO DISP
00870000 
00880000CLOSE: +
00890000       ISPEXEC TBEND ANEWS
00900000       SET &SHORT = &STR(***COMPLETE***)
00910000       SET &LONG  = &STR(AOST NEWS PROCESSING COMPLETED)
00920000       ISPEXEC VPUT (SHORT LONG)
00930000       ISPEXEC SETMSG MSG(AOST010)
00940000       GOTO EXIT
00950000 
00960000ADD: +
00970000     ISPEXEC DISPLAY PANEL(ANEWSPA)    /* DISPLAY AOST NEWS ADD PANEL */
00980000     IF &LASTCC ^= 0 THEN GOTO SAVEIT
00990000        ELSE DO
01000000             SET &WORK = YES
01010000             ISPEXEC VPUT (ANEWSID ANDESC)
01020000             ISPEXEC EDIT DATASET('&ANEWSDS(&ANEWSID)')  MACRO(ANEWSIN)
01030000             SET &LCC = &LASTCC
01040000             IF &LCC = 0 THEN DO
01050000                              ISPEXEC TBADD ANEWS
01060000                              SET &SHORT = &STR(***ADDED***)
01070000                              SET &LONG  = &STR(AOST NEWS ITEM &ANEWSID HAS BEEN ADDED AND SAVED)
01080000                              ISPEXEC VPUT (SHORT LONG)
01090000                              ISPEXEC SETMSG MSG(AOST010)
01100000      BDT Q FROM LOC(WCC1) DS('&ANEWSDS') SHR DAP(PDS) PARM(R=Y S M=&ANEWSID) TO LOC(WCC2) DS('&ANEWSDS') SHR DAP(PDS)
01110000      BDT Q FROM LOC(WCC1) DS('&ANEWSDS') SHR DAP(PDS) PARM(R=Y S M=&ANEWSID) TO LOC(SWC2) DS('&ANEWSDS') SHR DAP(PDS)
01111000      BDT Q FROM LOC(WCC1) DS('&ANEWSDS') SHR DAP(PDS) PARM(R=Y S M=&ANEWSID) TO LOC(SWCC) DS('&ANEWSDS') SHR DAP(PDS)
01120000                              GOTO ADD
01130000                              END
01140000             IF &LCC > 4 THEN DO
01150000                              SET &SHORT = &STR(***COMPLETE***)
01160000                              SET &LONG  = &STR(AOST NEWS ITEM ADDS HAVE BEEN TERMINATED)
01170000                              ISPEXEC VPUT (SHORT LONG)
01180000                              ISPEXEC SETMSG MSG(AOST010)
01190000                              GOTO ADD
01200000                              END
01210000             END
01220000 
01230000SAVEIT: +
01240000      ALLOC F(AOSTABL) DS('&AOSTLIB') SHR
01250000      ISPEXEC TBCLOSE ANEWS LIBRARY(AOSTABL)
01260000      FREE F(AOSTABL)
01270000      IF &WORK = NO THEN GOTO EXIT   /* TEST FOR ANY ACTIVITY */
01280000 
01290000      BDT Q FROM LOC(WCC1) DS('&AOSTLIB') SHR DAP(PDS) PARM(R=Y S M=ANEWS) TO LOC(WCC2) DS('&AOSTLIB') SHR DAP(PDS)
01300000      BDT Q FROM LOC(WCC1) DS('&AOSTLIB') SHR DAP(PDS) PARM(R=Y S M=ANEWS) TO LOC(SWC2) DS('&AOSTLIB') SHR DAP(PDS)
01301000      BDT Q FROM LOC(WCC1) DS('&AOSTLIB') SHR DAP(PDS) PARM(R=Y S M=ANEWS) TO LOC(SWCC) DS('&AOSTLIB') SHR DAP(PDS)
01310000 
01320000 
01330000EXIT: END
