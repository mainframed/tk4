00010000PROC 0 DEBUG SMRYREPT SMRYVTOC
00020000/** ** ** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** ** ** ** **/
00030000/**                                                                 **/
00040000/** Extended Vtoc Dialog: Uses $RSVP to look like File-AID VTOC Util**/
00050000/** By Mike Theys         Rockwell International          June 1986 **/
00060000/**                                                                 **/
00070000/** ** ** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** ** ** ** **/
00080000/*===================================================================*/
00090000/*===================================================================*/
00100000/*======================== $RSVP VTOC DIALOG ========================*/
00110000/*===================================================================*/
00120000/*===================================================================*/
00130000 
00140000CONTROL NOFLUSH
00150000ISPEXEC CONTROL ERRORS RETURN
00160000SET SYSOUTTRAP = 0
00170000SET OUTLIM     = 0
00180000IF &DEBUG = DEBUG +
00190000  THEN CONTROL   MSG   LIST   CONLIST   SYMLIST
00200000ISPEXEC VGET +
00210000        (RSVPVOLS RSVPNVOL STGPOOL ALLOCU RSVPSORT RSVPTEST)
00220000 
00230000/*===================================================================*/
00240000/*================ STORAGE POOL VOLUME SELECTION ====================*/
00250000/*===================================================================*/
00260000SET &SEGMENT = &STR(STORAGE POOL VOLUME SELECTION.)
00270000SET &MISCINFO= &STR(STGPOOL=&STGPOOL              )
00280000SET &MISCINF2= &STR(ISPF.PROFILE(STGPOOL) &SYSDSN(ISPF.PROFILE(STGPOOL))
00290000IF &STR(&STGPOOL) = &STR(?) THEN +
00300000  DO
00310000    IF &SYSDSN( ISPF.PROFILE(STGPOOL) ) = OK +
00320000      THEN ISPEXEC BROWSE DATASET(ISPF.PROFILE(STGPOOL))
00330000      ELSE ISPEXEC BROWSE DATASET('ATSO.SYS.TABLES(STGPOOL)')
00340000    EXIT
00350000  END
00360000IF &STR(&STGPOOL) ^=   THEN +
00370000  DO
00380000    IF &SYSDSN( ISPF.PROFILE(STGPOOL) ) = OK +
00390000      THEN ALLOC FI(STGPOOLD) REUSE DA(ISPF.PROFILE(STGPOOL))      SHR
00400000      ELSE ALLOC FI(STGPOOLD) REUSE DA('ATSO.SYS.TABLES(STGPOOL)') SHR
00410000    SET RCODE = 0
00420000    /* ON ERROR UNIT FOR READING STGPOOL INPUT. TRAP EOF */
00430000    ERROR +
00440000      DO
00450000        SET RCODE = &LASTCC.  /* SAVE THE RETURN CODE */
00460000        IF &RCODE  = 400  /* EOF */ THEN +
00470000          GOTO STGEND
00480000        SET SHORT =&STR(%$RSVTOCX RC=&RCODE)
00490000        SET LONG  =&STR(ERROR IN READ STGPOOL SECTION)
00500000        ISPEXEC SETMSG MSG(RSVT000)
00510000        WRITE POOL=&POOL
00520000        WRITE RECORD: |&STGPOOLD.|
00530000        EXIT
00540000      END
00550000    SET FOUND = 0
00560000    OPENFILE STGPOOLD INPUT
00570000    GETFILE STGPOOLD
00580000    DO WHILE 1 = 1
00590000      SET POOL = &SUBSTR(1:8,&STR(&STGPOOLD))
00600000      IF &STR(&POOL) = &STR(&STGPOOL) THEN +
00610000        DO
00620000          SET FOUND = 1
00630000          SET VOLUMES  = &STR(&SUBSTR(10:72,&STR(&STGPOOLD)))
00640000          SET VOLUMES  = &VOLUMES.   /* TRIM TRAILING BLANKS */
00650000          SET RSVPVOLS = &STR(VOL(&VOLUMES.))
00660000          GOTO STGEND
00670000        END
00680000      GETFILE STGPOOLD
00690000    END
00700000    STGEND:+
00710000    ERROR OFF
00720000  CLOSFILE STGPOOLD
00730000  FREE FI(STGPOOLD)
00740000  IF &FOUND = 0 THEN +
00750000    DO
00760000      SET SHORT = &STR(&STGPOOL NOT FOUND)
00770000      SET LONG  = &STR(STORAGE POOL NOT IN  ISPF.PROFILE(STGPOOL)  OR 'ATSO.SYS.TABLES(STGPOOL)')
00780000      ISPEXEC SETMSG MSG(RSVT000)
00790000      EXIT
00800000    END
00810000  END
00820000/* ELSE  USING &VOLUMES AND &VOLUMES2 */ +
00830000SET SRCHVOLS = &STR(&RSVPVOLS. &RSVPNVOL.)
00840000 
00850000/*===================================================================*/
00860000/*=============== SET THE USERS PROFILE TO MSGID ON =================*/
00870000/*===================================================================*/
00880000SET &SEGMENT = &STR(SET USERS PROFILE TO MSGID ON.)
00890000SET &MISCINFO = &STR()
00900000SET &MISCINF2 = &STR()
00910000SET SYSOUTTRAP = 100
00920000ACFPROF
00930000SET &SYSOUTTRAP = 0
00940000SET &OUTLIM = &SYSOUTLINE
00950000SET I = 1
00960000SET RESETMSG = NO
00970000DO WHILE (&I <= &OUTLIM AND &RESETMSG = NO)
00980000  SET PROFLIST = &&SYSOUTLINE&I
00990000  SET LINE = &STR(&PROFLIST)
01000000  VSCAN LINE 'NOMSGID'
01010000  SET VRC = &LASTCC
01020000  IF &VRC > 0 THEN +
01030000    DO
01040000       ACFPROF MSGID
01050000       SET RESETMSG = YES
01060000    END
01070000  SET I = &I + 1
01080000END
01090000/* HERE IF PROFILE SET TO MSGID */
01100000 
01110000/*===================================================================*/
01120000/*================ INVOKE $RSVP FOR VOLUME DATA =====================*/
01130000/*===================================================================*/
01140000SET &SEGMENT = &STR(INVOKE $RSVP FOR VOLUME DATA.)
01150000SET &MISCINFO= &STR(VOLS=&RSVPVOLS)
01160000SET &MISCINF2= &STR(TEST=&RSVPTEST)
01170000CONTROL NOLIST NOCONLIST NOSYMLIST
01180000SET SYSOUTTRAP = 500  /* ASSUME MAX OF 500 VOLUMES */
01190000$RSVP &RSVPVOLS. &RSVPNVOL. +
01200000 &RSVPTEST. +
01210000 NOHEADING &RSVPSORT. &ALLOCU. CHARS(133) +
01220000 PRINT(NEW (VOLUME CUU UNIT VOLLSPC CYLPVOL TRKPCYL +
01230000 VTOCL DSTOT DSREC VTOCI VOLATTR DEVTYPE VTOCE ))
01240000SET RSVPRC = &LASTCC.
01250000SET SYSOUTTRAP = 0   /* SHUT IT OFF */
01260000IF &DEBUG = DEBUG THEN +
01270000  CONTROL   MSG LIST CONLIST SYMLIST
01280000SET OUTLIM = &SYSOUTLINE
01290000/* WRITE $RSVP VOLUME.  #LINES OUTPUT=&OUTLIM.
01300000 
01310000IF &RSVPRC > 0 THEN +
01320000  DO
01330000    SET I = 1
01340000    DO WHILE ( (&I <= &OUTLIM) AND (&I <= 7) )
01350000      SET DBUGLINE = &STR(&&SYSOUTLINE&I)
01360000      SET RSDBUG&I = &STR(&DBUGLINE)
01370000      SET I = &I + 1
01380000    END
01390000    SET RSORTSO = &STR($RSVP)
01400000    SET DEBUGRC = &RSVPRC
01410000    DEBUG01:+
01420000    ISPEXEC DISPLAY PANEL($RSDEBUG)
01430000    IF &LASTCC = 0 THEN GOTO DEBUG01
01440000    SET SHORT = &STR($RSVP RC=&RSVPRC)
01450000    SET LONG  = &STR($RSVP VTOC SELECTION ABORTED)
01460000    ISPEXEC SETMSG MSG(RSVT000)
01470000    GOTO VTOCEXIT
01480000  END
01490000 
01500000/*===================================================================*/
01510000/*================= CREATE THE VOLUME TABLE =========================*/
01520000/*===================================================================*/
01521001ISPEXEC VGET (ZSCREEN)
01530001SET TBLNAME = $RSVTOC&ZSCREEN
01540000SET RC = 0                  /* INIT THE RETURN CODE */
01550000SET MSG  =                  /* INIT MESSAGE PARM    */
01560000SET VOLSEL =
01570000 
01580000GETVTOC: +
01590000ISPEXEC TBEND  &TBLNAME
01600000TRYCRVOL: +
01610000ISPEXEC TBCREATE &TBLNAME NOWRITE SHARE +
01620000          KEYS(VOLUME) +
01630000          NAMES(VOLSEL        CUU UNIT VOLATTR VMA VOLUSED VOLPCT +
01640000                VTOCL DSTOT VTOCUSED VTOCPCT VTOCX +
01650000                DSREC NXT NCYL LCYL NTRK LTRK TTRK MTRK +
01660000                TRKPCYL  TOTTRKS +
01670000                VOLLSPC CYLPVOL DSTOT VTOCI DEVTYPE VTOCE +
01680000                NXTN NCYLN LCYLN VOLPCTN +
01690000                AVOLUSED AVTOCL AVTOCUSE ADSREC ANXT ANCLY ALCYL ATTRK AMTRK +
01700000                ) +
01710000          REPLACE
01720000SET &RC = &LASTCC
01730000IF &RC > 4 THEN +
01740000  DO
01750000    IF &RC > 8 THEN +
01760000      DO
01770000        SET SHORT = &STR(OPEN ERROR)
01780000        SET LONG  = &STR(COULD NOT CREATE TABLE &TBLNAME.)
01790000        ISPEXEC SETMSG MSG(RSVT000)
01800000      END
01810000    ISPEXEC TBERASE &TBLNAME
01820000    IF &LASTCC >  0 THEN GOTO VTOCEXIT
01830000    GOTO TRYCRVOL
01840000  END
01850000 
01860000SET RCODE = 0
01870000ERROR +
01880000  DO
01890000    SET RCODE = &LASTCC.  /* SAVE THE RETURN CODE */
01900000    IF &RCODE  = 400  /* EOF */ THEN +
01910000      GOTO DONE
01920000    IF &RCODE > 10 THEN +
01930000      DO
01940000        SET SHORT =&STR(%$RSVTOCX RC=&RCODE)
01950000        SET LONG  =&STR(ERROR IN READ $RSVP VOL - REC#=&I)
01960000        ISPEXEC SETMSG MSG(RSVT000)
01970000        WRITE ************* RSVPVTOC SYSOUT LINE #&I. *************
01980000        WRITE |&RSVPVTOC.|
01990000        WRITE RSVPVOLS = &RSVPVOLS
02000000        WRITE RSVPNVOL = &RSVPNVOL
02010000        WRITE STGPOOL  = &STGPOOL
02020000        WRITE ALLOCU   = &ALLOCU
02030000        WRITE RSVPSORT = &RSVPSORT
02040000        WRITE RSVPTEST = &RSVPTEST
02050000        WRITE  $RSVP &RSVPVOLS. &RSVPNVOL. +
02060000                &RSVPTEST. +
02070000                NOHEADING &RSVPSORT. &ALLOCU. CHARS(133) +
02080000                PRINT(NEW (VOLUME CUU UNIT VOLLSPC CYLPVOL TRKPCYL +
02090000                VTOCL DSTOT DSREC VTOCI VOLATTR DEVTYPE VTOCE ))
02100000        SET &I = 1
02110000        DO WHILE &I <= &OUTLIM
02120000          SET LINE = &&SYSOUTLINE.&I
02130000          WRITE ************* RSVPVTOC SYSOUT LINE #&I. *************
02140000          WRITE |&LINE|
02150000          SET &I = &I + 1
02160000        END
02170000        GOTO VTOCEXIT
02180000      END
02190000    RETURN
02200000  END
02210000 
02220000SET I = 0 /* SYSOUTLINE COUNT */
02230000SET N = 0 /* NUMBER OF VOLUMES FOUND */
02240000SET KUN = 0
02250000SET K50 = 0
02260000SET K80 = 0
02270000SET K30 = 0
02280000SET K31 = 0
02290000SET K3V = 0
02300000SET TVU = 0
02310000SET KST = 0
02320000SET KPR = 0
02330000SET KPU = 0
02340000SET KMG = 0   /* COUNT OF MSS GROUP FINDS     NOT USED */
02350000SET KINX= 0
02360000SET KNIX= 0
02370000SET TCU = 0
02380000SET KE50= 0
02390000SET KE99= 0
02400000SET VL85= 0
02410000SET VL00= 0
02420000SET VL99= 0
02430000SET VT85= 0
02440000SET KDFG= 0
02450000SET KFDS= 0
02460000SET VOLERRS = 0
02470000 
02480000VOLBEGIN:+
02490000IF &I > &OUTLIM THEN +
02500000  DO
02510000    WRITE ******* RSVPVTOC LOOP. ALL SYSOUTLINES ARE NULL *******
02520000    GOTO VTOCEXIT
02530000  END
02540000SET I = &I + 1
02550000SET RSVPVTOC = &&SYSOUTLINE&I
02560000IF &DEBUG = DEBUG THEN +
02570000  DO
02580000    WRITE ************* RSVPVTOC SYSOUT LINE #&I. *************
02590000    WRITE |&RSVPVTOC.|
02600000  END
02610000IF &STR(&RSVPVTOC) =     THEN GOTO VOLBEGIN
02620000SET RSVPMSGN = &SUBSTR(001:003,&STR(&RSVPVTOC))
02630000IF   &STR(&RSVPMSGN) = &STR($RS) THEN +
02640000  DO
02650000    SET VOLERRS = &VOLERRS + 1
02660000    SET RSERR&VOLERRS = &STR(&RSVPVTOC)
02670000    IF &VOLERRS >= 16 THEN GOTO SHOWVERR
02680000    GOTO SKIPIT1
02690000    SET SHORT = &STR(** $RSVP ERROR **)
02700000    SET L = &LENGTH(&STR(&RSVPVTOC))
02710000    SET LONG  = &STR(&SUBSTR(001:&L,&STR(&RSVPVTOC))
02720000    DO WHILE &I <= &OUTLIM
02730000      WRITE ************* RSVPVTOC SYSOUT LINE #&I. *************
02740000      WRITE |&RSVPVTOC.|
02750000      SET I = &I + 1
02760000    END
02770000    ISPEXEC SETMSG MSG(RSVT000)
02780000    GOTO VTOCEXIT
02790000    SKIPIT1:+
02800000  END
02810000 
02820000IF   &STR(&RSVPMSGN) = &STR(   ) THEN +
02830000  DO
02840000    WRITE $RSVP VOLUME. #SYSOUT LINES = &OUTLIM.
02850000    SET SHORT = &STR(NO VOLUMES FOUND)
02860000    SET LONG  = &STR(NO WERE VOLUMES THAT MET YOUR VTOC SEARCH CRITERIA)
02870000    ISPEXEC SETMSG MSG(RSVT000)
02880000    GOTO VTOCEXIT
02890000  END
02900000 
02910000DO WHILE &I <= &OUTLIM
02920000  IF &STR(&RSVPVTOC) = THEN GOTO SKIPVOL
02930000  SET RSVPMSGN = &SUBSTR(001:003,&STR(&RSVPVTOC))
02940000  IF   &STR(&RSVPMSGN) = &STR($RS) THEN +
02950000    DO
02960000      SET VOLERRS = &VOLERRS + 1
02970000      SET RSERR&VOLERRS = &STR(&RSVPVTOC)
02980000      IF &VOLERRS >= 16 THEN GOTO SHOWVERR
02990000      GOTO SKIPVOL
03000000    END
03010000  SET VOLUME   = &SUBSTR(001:006,&STR(&RSVPVTOC))
03020000  SET N = &N +1
03030000  SET CUU      = &SUBSTR(008:010,&STR(&RSVPVTOC))
03040000  SET UNIT     = &SUBSTR(012:017,&STR(&RSVPVTOC))
03050000  IF      &STR(&UNIT) = &STR(3380)   THEN SET K80 = &K80 +1
03060000  ELSE IF &STR(&UNIT) = &STR(3350)   THEN SET K50 = &K50 +1
03070000  ELSE IF &STR(&UNIT) = &STR(3330)   THEN SET K30 = &K30 +1
03080000  ELSE IF &STR(&UNIT) = &STR(3330-1) THEN SET K31 = &K31 +1
03090000  ELSE IF &STR(&UNIT) = &STR(3330V)  THEN SET K3V = &K3V +1
03100000  ELSE                                    SET KUN = &KUN +1
03110000  SET CYLPVOL  = &SUBSTR(054:057,&STR(&RSVPVTOC))
03120000  SET TRKPCYL  = &SUBSTR(062:064,&STR(&RSVPVTOC))
03130000  SET VOLLSPC  = &SUBSTR(021:050,&STR(&RSVPVTOC))
03140000  SET LSPACE   = &SUBSTR(027:030,&STR(&RSVPVTOC))
03150000  IF &DATATYPE(&LSPACE) = NUM  THEN +
03160000    DO /* LSPACE SVC INFO OK CALC %USED ETC */
03170000      SET NCYL     = &SUBSTR(027:030,&STR(&RSVPVTOC))
03180000      SET NTRK     = &SUBSTR(032:035,&STR(&RSVPVTOC))
03190000      SET NXT      = &SUBSTR(037:040,&STR(&RSVPVTOC))
03200000      SET LCYL     = &SUBSTR(042:045,&STR(&RSVPVTOC))
03210000      SET LTRK     = &SUBSTR(047:050,&STR(&RSVPVTOC))
03220000      /* REMOVE LEADING ZEROS FOR LSPACE OUTPUT */
03230000      SET NCYL     = &EVAL(&NCYL)
03240000      SET NCYLN    = &STR(000000&NCYL.)
03250000      SET L        = &LENGTH(&STR(&NCYLN))
03260000      SET NCYLN    = &SUBSTR(&L-3:&L,&STR(&NCYLN))
03270000      SET NTRK     = &EVAL(&NTRK)
03280000      SET NXT      = &EVAL(&NXT)
03290000      SET NXTN     = &STR(000000&NXT.)
03300000      SET L        = &LENGTH(&STR(&NXTN))
03310000      SET NXTN     = &SUBSTR(&L-3:&L,&STR(&NXTN))
03320000      IF  &NXT >  50 THEN SET KE50 = &KE50 + 1
03330000      IF  &NXT >  99 THEN SET KE99 = &KE99 + 1
03340000      IF  &NXT > 999 THEN SET NXT = &STR(***)
03350000      SET LCYL     = &EVAL(&LCYL)
03360000      SET LCYLN    = &STR(000000&LCYL.)
03370000      SET L        = &LENGTH(&STR(&LCYLN))
03380000      SET LCYLN    = &SUBSTR(&L-4:&L,&STR(&LCYLN))
03390000      SET LTRK     = &EVAL(&LTRK)
03400000      /* CALC TOTAL FREE SPACE IN TRACKS  NCYL + NTRK */
03410000      SET TTRK     = &EVAL(&NCYL * &TRKPCYL + &NTRK )
03420000      SET MTRK     = &EVAL(&LCYL * &TRKPCYL + &LTRK )
03430000      SET TOTTRKS  = &EVAL(&CYLPVOL * &TRKPCYL)
03440000      SET TOTUSED  = &EVAL(&TOTTRKS - (&NCYL * &TRKPCYL + &NTRK))
03450000      SET VOLUSED  = &EVAL(((&TOTUSED * 1000)/&TOTTRKS)+5)
03460000      SET LEN      = &LENGTH(&STR(&VOLUSED))
03470000      IF &LEN = 1 +
03480000        THEN SET VOLUSED = 0
03490000        ELSE SET VOLUSED  = &STR(&SUBSTR(1:&LEN-1,&STR(&VOLUSED.)))
03500000      IF &VOLUSED = 00 THEN SET VL00 = &VL00 + 1
03510000      IF &VOLUSED > 99 THEN SET VL99 = &VL99 + 1
03520000      IF &VOLUSED > 85 THEN SET VL85 = &VL85 + 1
03530000      IF &VOLUSED < 60 AND &NXT > 50 THEN SET KDFG = &KDFG + 1
03540000      SET TVU = &TVU + &VOLUSED
03550000      SET VOLPCT   = &VOLUSED  /* SORT PARM */
03560000      SET VOLPCTN  = &STR(000&VOLPCT)
03570000      SET L        = &LENGTH(&STR(&VOLPCTN))
03580000      SET VOLPCTN  = &SUBSTR(&L-2:&L,&STR(&VOLPCTN))
03590000      SET VOLUSED  = &VOLUSED.&STR(%)
03600000    END
03610000  ELSE /* BAD LSPACE SVC INFORMATION SET FIELDS TO ... */ +
03620000    DO
03630000      SET VOLPCT  = &STR(100) /* SORT PARM */
03640000      SET VOLUSED = &STR(.??%)
03650000      SET NXT     = &STR(.ERR.)
03660000      SET NCYL    = &STR(.ERR.)
03670000      SET LCYL    = &STR(.ERR.)
03680000      SET TTRK    = &STR(.ERR.)
03690000      SET MTRK    = &STR(.ERR.)
03700000    END
03710000  SET AL       = &LENGTH(&STR(&VOLUSED))
03720000  SET AVOLUSED = &SUBSTR(&AL:&AL+3,&STR(   &VOLUSED))
03730000  SET AL       = &LENGTH(&STR(&NXT))
03740000  SET ANXT     = &SUBSTR(&AL:&AL+3,&STR(   &NXT))
03750000  SET AL       = &LENGTH(&STR(&NCYL))
03760000  SET ANCYL    = &SUBSTR(&AL:&AL+3,&STR(   &NCYL))
03770000  SET AL       = &LENGTH(&STR(&LCYL))
03780000  SET ALCYL    = &SUBSTR(&AL:&AL+4,&STR(    &LCYL))
03790000  SET AL       = &LENGTH(&STR(&TTRK))
03800000  SET ATTRK    = &SUBSTR(&AL:&AL+5,&STR(     &TTRK))
03810000  SET AL       = &LENGTH(&STR(&MTRK))
03820000  SET AMTRK    = &SUBSTR(&AL:&AL+4,&STR(    &MTRK))
03830000  SET VTOCL    = &SUBSTR(071:073,&STR(&RSVPVTOC))
03840000  SET AVTOCL   = &STR( &VTOCL)
03850000  SET DSTOT    = &SUBSTR(076:080,&STR(&RSVPVTOC))
03860000  SET DSREC    = &SUBSTR(082:086,&STR(&RSVPVTOC))
03870000  SET ADSREC   = &STR(&DSREC)
03880000  IF &DSREC < 100 THEN SET KFDS = &KFDS + 1
03890000  SET VTOCI    = &SUBSTR(090:091,&STR(&RSVPVTOC))
03900000  SET VTOCX    = &SUBSTR(2:2,&STR(&VTOCI))
03910000  IF  &VTOCX = 1 | &VTOCX = 3 | &VTOCX = 5 | &VTOCX = 7  +
03920000    | &VTOCX = 9 | &VTOCX = B | &VTOCX = D | &VTOCX = F  THEN +
03930000    DO
03940000      SET VTOCX = Y
03950000      SET KINX = &KINX + 1
03960000    END
03970000  ELSE +
03980000    DO
03990000      SET VTOCX = N
04000000      SET KNIX = &KNIX + 1
04010000    END
04020000  SET VOLATTR  = &SUBSTR(094:096,&STR(&RSVPVTOC))
04030000  SET VMA = &STR( ? )
04040000  IF      &VOLATTR = PRI THEN DO
04050000                                SET KPR = &KPR + 1
04060000                                SET VMA = &STR(PRV)
04070000                              END
04080000  ELSE IF &VOLATTR = STO THEN DO
04090000                                SET KST = &KST + 1
04100000                                SET VMA = &STR(STG)
04110000                              END
04120000  ELSE IF &VOLATTR = PUB THEN DO
04130000                                SET KPU = &KPU + 1
04140000                                SET VMA = &STR(PUB)
04150000                              END
04160000  SET DEVTYPE  = &SUBSTR(103:110,&STR(&RSVPVTOC))
04170000  SET VTOCE    = &SUBSTR(112:130,&STR(&RSVPVTOC))
04180000  SET VTOCUSED = &EVAL((((&DSTOT - &DSREC) * 1000) / &DSTOT) +5)
04190000  SET LEN      = &LENGTH(&STR(&VTOCUSED))
04200000  IF &LEN = 1 +
04210000    THEN SET VTOCUSED = 0
04220000    ELSE SET VTOCUSED = &STR(&SUBSTR(1:&LEN-1,&STR(&VTOCUSED.)))
04230000  IF &VTOCUSED > 85 THEN SET VT85 = &VT85 + 1
04240000  SET TCU = &TCU + &VTOCUSED
04250000  SET VTOCPCT  = &STR(&VTOCUSED)
04260000  SET VTOCUSED = &STR(&VTOCUSED.%)
04270000  SET AL        = &LENGTH(&STR(&VTOCUSED))
04280000  SET AVTOCUSE  = &SUBSTR(&AL:&AL+3,&STR(   &VTOCUSED))
04290000  ISPEXEC TBADD &TBLNAME.
04300000 SKIPVOL:+
04310000  SET I = &I + 1
04320000  SET RSVPVTOC = &&SYSOUTLINE&I
04330000  IF &DEBUG = DEBUG THEN +
04340000    DO
04350000      WRITE ************* RSVPVTOC SYSOUT LINE #&I. *************
04360000      WRITE |&RSVPVTOC.|
04370000    END
04380000END
04390000 
04400000DONE: +
04410000ERROR OFF
04420000SHOWVERR:+
04430000SET VOLS = &N
04440000SET DSNN = 0
04450000IF &VOLERRS > 0 THEN +
04460000  DO
04470000    IF &VOLERRS >= 2 THEN +
04480000      IF &STR(&RSERR1) = &STR(&RSERR2) THEN SET RSERR1 =
04490000    ISPEXEC DISPLAY PANEL($RSERROR)
04500000  END
04510000IF &VOLS = 0 THEN +
04520000  DO
04530000    SET SHORT = &STR(NO VOLUMES FOUND)
04540000    SET LONG  = &STR(NO WERE VOLUMES THAT MET YOUR VTOC SEARCH CRITERIA)
04550000    ISPEXEC SETMSG MSG(RSVT000)
04560000    GOTO VTOCEXIT
04570000  END
04580000SET AVU = &EVAL(&TVU/&VOLS)
04590000SET ACU = &EVAL(&TCU/&VOLS)
04600000SET VOLSEL =
04610000 
04620000SET SEGMENT = &STR()
04630000SET MISCINFO= &STR()
04640000SET MISCINF2= &STR()
04650000/*===================================================================*/
04660000/*=================== SHOW THE VOLUME TABLE =========================*/
04670000/*===================================================================*/
04680001SET &VOLTBL = $RSVTOC&ZSCREEN
04690000SET &VOLPNL = $RSVTOCL
04700000IF &SMRYVTOC = SMRYVTOC THEN SET &VOLPNL = $RSVTOCX
04710000SET &VOLPROC = NO
04720000SET &VOLRC = 0       /* RETURN CODE FOR THIS CLIST                */
04730000SET &VOLUSRRC = 0    /* RETURN CODE FROM TBDISPL                  */
04740000SET &VOLROWP =       /* ROW IN WHICH CURSOR IS DISPLAYED          */
04750000SET &VOLCUR = VOLSEL /* FIELD IN WHICH CURSOR IS DISPLAYED        */
04760000SET &VOLERRB = NO    /* INDICATES WHETHER AN ERROR HAS BEEN FOUND */
04770000                     /* BEFORE ALL ROW PROCESSING IS FINISHED.    */
04780000SET &VOLERRA = NO    /* INDICATES WHETHER AN ERROR HAS BEEN FOUND */
04790000                     /* AFTER  ALL ROW PROCESSING IS FINISHED.    */
04800000SET &VOLMSG = RSVT009/* ID OF MESSAGE TO BE DISPLAYED             */
04810000SET &VOLREPOS = 1    /* NUMBER OF TOP ROW TO BE DISPLAYED         */
04820000IF &SMRYREPT = SMRYREPT THEN +
04830000  DO
04840000    ISPEXEC CONTROL DISPLAY SAVE
04850000    SHOWSMRY:+
04860000    ISPEXEC DISPLAY PANEL($RSVTOCS)
04870000    IF &LASTCC = 0 THEN GOTO SHOWSMRY
04880000    ISPEXEC CONTROL DISPLAY RESTORE
04890000  END
04900000DO WHILE ( &VOLUSRRC < 8 OR +
04910000          ((&VOLERRB = YES OR &VOLERRA = YES) AND  &VOLPROC = YES) )
04920000  ISPEXEC TBSKIP &VOLTBL ROW(&VOLREPOS) NOREAD
04930000  IF &VOLERRB = NO THEN +
04940000    DO
04950000      SET &VOLPNLD = &VOLPNL
04960000      SET &VOLASEL = NO
04970000      ISPEXEC VGET (ZVERB, ZSCROLLN) SHARED
04980000      IF &ZVERB = UP   THEN  ISPEXEC TBSKIP &VOLTBL NUMBER(-&ZSCROLLN)
04990000      IF &ZVERB = DOWN THEN  ISPEXEC TBSKIP &VOLTBL NUMBER(&ZSCROLLN)
05000000    END
05010000  ELSE +
05020000    DO
05030000      SET &VOLPNLD =
05040000      IF &VOLERRB = YES +
05050000        THEN SET &VOLASEL = YES
05060000        ELSE SET &VOLASEL = NO
05070000    END
05080000 
05090000VDISPL:+
05100000ISPEXEC TBDISPL &VOLTBL PANEL(&VOLPNLD) +
05110000   CURSOR(&VOLCUR) CSRROW(&VOLROWP) AUTOSEL(&VOLASEL) MSG(&VOLMSG)
05120000SET &VOLUSRRC = &LASTCC
05130000  IF &VOLUSRRC = 8 AND &VOLPROC = NO THEN +
05140000    GOTO VOLDONE
05150000 
05160000  ISPEXEC VGET (RSVCMD,RSVPARM,CMD)
05170000  IF &STR(&RSVCMD) = LOCVOL  THEN +
05180000    DO
05190000      SET &VOLUME = &STR(&RSVPARM.*)
05200000      ISPEXEC TBTOP  &VOLTBL
05210000      ISPEXEC TBSCAN &VOLTBL NOREAD ARGLIST(VOLUME) ROWID(VOLRLOC)
05220000      SET VOLROWP  = &VOLRLOC    /* ??? */
05230000      SET VOLREPOS = &VOLRLOC    /* ??? */
05240000      SET RSVCMD =
05250000      SET RSVPARM=
05260000      SET CMD    =
05270000      GOTO VDISPL
05280000    END
05290000  IF &STR(&RSVCMD) = DEBUG THEN +
05300000    DO
05310000      CONTROL LIST,CONLIST,SYMLIST,MSG
05320000      SET RSVCMD =
05330000      SET RSVPARM=
05340000      SET CMD    =
05350000    END
05360000  IF &STR(&RSVCMD) = NODEBUG THEN +
05370000    DO
05380000      CONTROL NOLIST,NOCONLIST,NOSYMLIST,NOMSG
05390000      SET RSVCMD =
05400000      SET RSVPARM=
05410000      SET CMD    =
05420000    END
05430000  IF &STR(&RSVCMD) = ONLY    | &STR(&RSVCMD) = SHOW +
05440000   | &STR(&RSVCMD) = SCAN                           THEN +
05450000    DO
05460000      ISPEXEC TBVCLEAR &VOLTBL
05470000      ISPEXEC TBTOP  &VOLTBL
05480000      SET &VOLUME =
05490000      SET &VOLSEL=
05500000      SET  CUU =
05510000      SET  UNIT =
05520000      SET  VOLATTR =
05530000      SET  VMA =
05540000      SET  VOLUSED =
05550000      SET  VOLPCT =
05560000      SET  VOLPCTN=
05570000      SET  VTOCL =
05580000      SET  DSTOT =
05590000      SET  VTOCUSED=
05600000      SET  VTOCPCT =
05610000      SET  VTOCX =
05620000      SET  DSREC =
05630000      SET  NXT =
05640000      SET  NCYL =
05650000      SET  LCYL =
05660000      SET  NXTN=
05670000      SET  NCYLN=
05680000      SET  LCYLN=
05690000      SET  NTRK =
05700000      SET  LTRK =
05710000      SET  TTRK =
05720000      SET  MTRK =
05730000      SET  TRKPCYL=
05740000      SET  TOTTRKS=
05750000      SET  VOLLSPC=
05760000      SET  CYLPVOL=
05770000      SET  DSTOT =
05780000      SET  VTOCI =
05790000      SET  DEVTYPE=
05800000      SET  VTOCE =
05810000      SET  NAMECOND =
05820000      IF &RSVCMD = ONLY THEN +
05830000        DO
05840000          SET &L = &LENGTH(&STR(&RSVPARM.))
05850000          IF &SUBSTR(&L:&L,&STR(&RSVPARM)) ^= &STR(*) THEN +
05860000            IF &L < 6 THEN SET RSVPARM = &STR(&RSVPARM.*)
05870000          SET &VOLUME = &STR(&RSVPARM.)
05880000        END
05890000      IF &RSVCMD = SHOW THEN +
05900000        DO
05910000          ISPEXEC CONTROL DISPLAY SAVE
05920000          ISPEXEC DISPLAY PANEL($RSVSHOW)
05930000          ISPEXEC VGET (NAMECOND)
05940000          ISPEXEC VGET (VOLUME,CUU,VOLPCTN,NCYLN,LCYLN,NXTN,VOLATTR)
05950000          IF &STR(&VOLUME) ^= THEN +
05960000            DO
05970000              SET &L = &LENGTH(&STR(&VOLUME.))
05980000              IF &SUBSTR(&L:&L,&STR(&VOLUME.)) ^= &STR(*) THEN +
05990000                IF &L < 6 THEN SET VOLUME = &STR(&VOLUME.*)
06000000            END
06010000          IF &STR(&CUU) ^= THEN +
06020000            DO
06030000              SET &L = &LENGTH(&STR(&CUU.))
06040000              IF &SUBSTR(&L:&L,&STR(&CUU.)) ^= &STR(*) THEN +
06050000                IF &L < 3 THEN SET CUU = &STR(&CUU.*)
06060000            END
06070000          IF &VOLPCTN ^= THEN +
06080000            DO
06090000              SET VOLPCTN = &STR(000&VOLPCTN)
06100000              SET L     = &LENGTH(&STR(&VOLPCTN)
06110000              SET VOLPCTN = &SUBSTR(&L-2:&L,&STR(&VOLPCTN))
06120000            END
06130000          IF &NCYLN ^= THEN +
06140000            DO
06150000              SET NCYLN = &STR(000000&NCYLN)
06160000              SET L     = &LENGTH(&STR(&NCYLN)
06170000              SET NCYLN = &SUBSTR(&L-3:&L,&STR(&NCYLN))
06180000            END
06190000          IF &LCYLN ^= THEN +
06200000            DO
06210000              SET LCYLN = &STR(000000&LCYLN)
06220000              SET L     = &LENGTH(&STR(&LCYLN)
06230000              SET LCYLN = &SUBSTR(&L-4:&L,&STR(&LCYLN))
06240000            END
06250000          IF &NXTN ^= THEN +
06260000            DO
06270000              SET NXTN  = &STR(000000&NXTN)
06280000              SET L     = &LENGTH(&STR(&NXTN)
06290000              SET NXTN  = &SUBSTR(&L-3:&L,&STR(&NXTN))
06300000            END
06310000          ISPEXEC CONTROL DISPLAY RESTORE
06320000        END
06330000      IF &RSVCMD = SCAN THEN +
06340000        DO
06350000          ISPEXEC VGET (SCANCOND)
06360000          ISPEXEC VGET (VOLUME,CUU,VOLPCTN,NCYLN,LCYLN,NXTN,VOLATTR)
06370000          IF &STR(&VOLUME) ^= THEN +
06380000            DO
06390000              SET &L = &LENGTH(&STR(&VOLUME.))
06400000              IF &SUBSTR(&L:&L,&STR(&VOLUME.)) ^= &STR(*) THEN +
06410000                IF &L < 6 THEN SET VOLUME = &STR(&VOLUME.*)
06420000            END
06430000          IF &STR(&CUU) ^= THEN +
06440000            DO
06450000              SET &L = &LENGTH(&STR(&CUU.))
06460000              IF &SUBSTR(&L:&L,&STR(&CUU.)) ^= &STR(*) THEN +
06470000                IF &L < 3 THEN SET CUU = &STR(&CUU.*)
06480000            END
06490000          IF &VOLPCTN ^= THEN +
06500000            DO
06510000              SET VOLPCTN = &STR(000&VOLPCTN)
06520000              SET L     = &LENGTH(&STR(&VOLPCTN)
06530000              SET VOLPCTN = &SUBSTR(&L-2:&L,&STR(&VOLPCTN))
06540000            END
06550000          IF &NCYLN ^= THEN +
06560000            DO
06570000              SET NCYLN = &STR(000000&NCYLN)
06580000              SET L     = &LENGTH(&STR(&NCYLN)
06590000              SET NCYLN = &SUBSTR(&L-3:&L,&STR(&NCYLN))
06600000            END
06610000          IF &LCYLN ^= THEN +
06620000            DO
06630000              SET LCYLN = &STR(000000&LCYLN)
06640000              SET L     = &LENGTH(&STR(&LCYLN)
06650000              SET LCYLN = &SUBSTR(&L-4:&L,&STR(&LCYLN))
06660000            END
06670000          IF &NXTN ^= THEN +
06680000            DO
06690000              SET NXTN  = &STR(000000&NXTN)
06700000              SET L     = &LENGTH(&STR(&NXTN)
06710000              SET NXTN  = &SUBSTR(&L-3:&L,&STR(&NXTN))
06720000            END
06730000          SET NAMECOND = &STR(&SCANCOND)
06740000        END
06750000      IF &STR(&NAMECOND)  = THEN +
06760000        DO
06770000          ISPEXEC TBSARG &VOLTBL
06780000        END
06790000      ELSE +
06800000        DO
06810000          ISPEXEC TBSARG &VOLTBL  NAMECOND(&NAMECOND.)
06820000        END
06830000      ISPEXEC TBDISPL &VOLTBL PANEL($RSVTOCZ) AUTOSEL(NO)
06840000      SET &VOLUSRRC = &LASTCC
06850000    END
06860000  IF &STR(&RSVCMD) = RESET   THEN +
06870000    DO
06880000      ISPEXEC TBTOP  &VOLTBL
06890000      SET VOLPNLD = &VOLPNL
06900000      SET RSVCMD =
06910000      SET RSVPARM=
06920000      SET CMD    =
06930000      GOTO VDISPL
06940000    END
06950000  IF &STR(&RSVCMD) = LOCADR  THEN +
06960000    DO
06970000      SET &CUU = &STR(&RSVPARM.*)
06980000      ISPEXEC TBTOP  &VOLTBL
06990000      ISPEXEC TBSCAN &VOLTBL NOREAD ARGLIST(CUU) ROWID(VOLRLOC)
07000000      SET VOLROWP  = &VOLRLOC    /* ??? */
07010000      SET VOLREPOS = &VOLRLOC    /* ??? */
07020000      SET RSVCMD =
07030000      SET RSVPARM=
07040000      SET CMD    =
07050000      GOTO VDISPL
07060000    END
07070000  IF &STR(&RSVCMD) = REPORT  THEN +
07080000    DO
07090002      ISPEXEC TBTOP &VOLTBL   /* 08/23/87 was $RSVTOCX */
07100000      ISPEXEC FTOPEN TEMP
07110000      ISPEXEC FTINCL $RSVTOCX
07120000      ISPEXEC FTCLOSE
07130000      ISPEXEC VGET (ZTEMPF)
07140000/*    %CNAEBROW '&ZTEMPF'  */
07141000      PRINTOFF  '&ZTEMPF' NOMSG NOHEADING RCL
07150000      SET LINESP = &LASTCC
07160000      SET RSVCMD =
07170000      SET CMD    =
07180000      SET SHORT = &STR(REPORT PRINTED)
07190000      SET LONG  = &STR(VTOC REPORT PRINTED WITH &LINESP LINES TO YOUR ACF2 DFT-DEST.)
07200000      ISPEXEC SETMSG MSG(SYS010)
07210000    END
07220000  IF &STR(&RSVCMD) = SUMMARY THEN +
07230000    DO
07240000      ISPEXEC CONTROL DISPLAY SAVE
07250000      SHWSMRY:+
07260000      ISPEXEC DISPLAY PANEL($RSVTOCS)
07270000      IF &LASTCC = 0 THEN GOTO SHWSMRY
07280000      ISPEXEC CONTROL DISPLAY RESTORE
07290000      SET RSVCMD =
07300000      SET RSVPARM=
07310000      SET CMD    =
07320000    END
07330000  IF &STR(&RSVCMD) = SORT   THEN +
07340000    DO
07350000      ISPEXEC VGET (SORTPARM)
07360000      ISPEXEC TBSORT &VOLTBL FIELDS(&SORTPARM)
07370000      SET SORTCC=&LASTCC
07380000      IF &SORTCC > 0 THEN +
07390000        DO
07400000          SET SHORT = &STR(SORT ERROR. RC=&SORTCC)
07410000          SET LONG  = &STR(AN ERROR OCCURED DURING THE SORT OF THE VOLUME TABLE)
07420000          ISPEXEC SETMSG MSG(RSVT000)
07430000        END
07440000      SET RSVCMD =
07450000      SET RSVPARM=
07460000      SET CMD    =
07470000      GOTO VDISPL
07480000    END
07490000  SET &VOLREPOS = &ZTDTOP
07500000  SET &VOLERRB = NO
07510000  SET &VOLERRA = NO
07520000  SET &VOLMSG =
07530000  SET &VOLCUR =
07540000  SET &VOLROWP =
07550000  DO WHILE ( &ZTDSELS > 0 )
07560000    IF &STR(&RSVCMD) = CANCEL THEN +
07570000      DO
07580000        SET &VOLRC = 20
07590000        GOTO VOLDONE
07600000      END
07610000    IF      &STR(&VOLSEL) = M THEN +
07620000      DO
07630000        SET VOLSEL = &STR( )
07640000        SET SHORT = &STR(VTOC OPTION M NOT AVAIL.)
07650000        SET LONG  = &STR(THE VOLUME MAP OPTION IS NOT READY.)
07660000        ISPEXEC SETMSG MSG(RSVT000)
07670000      END
07680000    IF      &STR(&VOLSEL) = X THEN +
07690000      DO
07700000/*===================================================================*/
07710000/*====== VOLUME OPTION X: VOLUME EXCLUDE FROM TABLE DISPLAY =========*/
07720000/*===================================================================*/
07730000      ERROR OFF
07740000      SET VOLSEL = &STR( )
07750000      ISPEXEC TBDELETE &VOLTBL
07760000      IF &LASTCC > 0 THEN +
07770000        DO
07780000          SET SHORT = &STR(ERROR IN EXCLUDE)
07790000          SET LONG  = &STR(THERE WAS AN ERROR TRYING TO EXCLUDE THE VOLUME FROM THE DISPLAY)
07800000          ISPEXEC SETMSG MSG(RSVT000)
07810000        END
07820000      END
07830000    IF      &STR(&VOLSEL) = V THEN +
07840000      DO
07850000/*===================================================================*/
07860000/*============= VOLUME OPTION V: VOLUME SUMMARY ====================*/
07870000/*===================================================================*/
07880000        ERROR OFF
07890000        CONTROL NOLIST NOCONLIST NOSYMLIST
07900000        SET VOLSEL = &STR( )
07910000        SET SYSOUTTRAP = 10
07920000        $RSVP VOL(&VOLUME.) NOHEADING CHARS(133) NOSORT &ALLOCU +
07930000          PRINT(NEW (DEVDB DEVTK VOLEXT VOLSPC VOL1XSP DEVDT))
07940000        SET RSVP1RC = &LASTCC.
07950000        SET SYSOUTTRAP = 0   /* SHUT IT OFF */
07960000        SET OUTLIMV= &SYSOUTLINE
07970000        IF &DEBUG = DEBUG THEN CONTROL   MSG LIST CONLIST SYMLIST
07980000        SET VI = 1
07990000        DO WHILE &VI <= &OUTLIMV
08000000          SET RSVPOPTV = &&SYSOUTLINE&VI
08010000          SET L = &LENGTH(&STR(&RSVPOPTV))
08020000          IF &L>3 THEN +
08030000            DO
08040000              SET RSVPMSGN = &SUBSTR(1:3,&STR(&RSVPOPTV))
08050000              IF &STR(&RSVPMSGN) = &STR($RS) THEN +
08060000                DO
08070000                  WRITE |&STR(&RSVPOPTV)|
08080000                  GOTO SKIPV1
08090000                END
08100000              SET DEVDB    = &SUBSTR(001:005,&STR(&RSVPOPTV))
08110000              SET DEVTK    = &SUBSTR(007:011,&STR(&RSVPOPTV))
08120000              SET VOLEXT   = &SUBSTR(013:018,&STR(&RSVPOPTV))
08130000              SET VOLSPC   = &SUBSTR(020:025,&STR(&RSVPOPTV))
08140000              SET VOL1XSP  = &SUBSTR(027:033,&STR(&RSVPOPTV))
08150000              SET DEVDT    = &SUBSTR(035:039,&STR(&RSVPOPTV))
08160000              GOTO SKIPV1
08170000            END
08180000         SKIPV1:+
08190000          SET VI = &VI + 1
08200000        END
08210000        CONTROL NOLIST NOCONLIST NOSYMLIST
08220000        SET SYSOUTTRAP = 10
08230000        $RSVP VOL(&VOLUME.) NOHEADING CHARS(133) TOTAL NOSORT +
08240000          NOPRINT(NEW (DSCOUNT TRKALLOC KBALLOC USED UNUSED)) TRK
08250000        SET RSVP2RC = &LASTCC.
08260000        SET SYSOUTTRAP = 0   /* SHUT IT OFF */
08270000        SET OUTLIMV= &SYSOUTLINE
08280000        IF &DEBUG = DEBUG +
08290000          THEN CONTROL   MSG LIST CONLIST SYMLIST
08300000        SET VI = 1
08310000        DO WHILE &VI <= &OUTLIMV
08320000          SET RSVPOPTV = &&SYSOUTLINE&VI
08330000          SET L = &LENGTH(&STR(&RSVPOPTV))
08340000          IF &L>3 THEN +
08350000            DO
08360000              SET RSVPMSGN = &SUBSTR(1:3,&STR(&RSVPOPTV))
08370000              IF &STR(&RSVPMSGN) = &STR($RS) THEN +
08380000                DO
08390000                  WRITE |&STR(&RSVPOPTV)|
08400000                  GOTO SKIPV2
08410000                END
08420000              SET DSCOUNT  = &SUBSTR(001:007,&STR(&RSVPOPTV))
08430000              SET TOTVTRKS = &SUBSTR(009:016,&STR(&RSVPOPTV))
08440000              SET TOTVKBA  = &SUBSTR(018:025,&STR(&RSVPOPTV))
08450000              SET TOTVTUSD = &SUBSTR(027:034,&STR(&RSVPOPTV))
08460000              SET TOTVTUNU = &SUBSTR(036:043,&STR(&RSVPOPTV))
08470000            END
08480000         SKIPV2:+
08490000          SET VI = &VI + 1
08500000        END
08510000        IF &RSVP1RC > 0 | &RSVP2RC > 0 THEN +
08520000          DO
08530000            SET &RSVPRC = &RSVP1RC
08540000            IF &RSVP2RC > &RSVP1RC THEN SET RSVPRC = &RSVP2RC
08550000            SET SHORT = &STR($RSVP RC=&RSVPRC)
08560000            SET LONG  = &STR($RSVP VTOC OPTION V ABORTED)
08570000            ISPEXEC SETMSG MSG(RSVT000)
08580000          END
08590000        /* DISPLAY RESULTS OF VTOC OPTION V */
08600000        ISPEXEC CONTROL DISPLAY SAVE
08610000        DISPVINF:+
08620000        ISPEXEC DISPLAY PANEL($RSVTOCV)
08630000        IF &LASTCC = 0 THEN GOTO DISPVINF
08640000        ISPEXEC CONTROL DISPLAY RESTORE
08650000      END
08660000    ELSE IF &STR(&VOLSEL) = S THEN +
08670000      DO
08680000        ERROR OFF
08690000        SET VOLSEL = &STR( )
08700000  /*===================================================================*/
08710000  /*======== CREATE THE DATA SET LIST TABLE FOR SELECTED VOLUME =======*/
08720000  /*===================================================================*/
08730000 
08740001        SET TBLSNAME = $RSDSNL&ZSCREEN
08750000        ISPEXEC TBEND  &TBLSNAME
08760000 
08770000       TRYCRDSN: +
08780000        ISPEXEC TBCREATE &TBLSNAME NOWRITE SHARE +
08790000            KEYS(DSNAME) +
08800000            NAMES(DSNSEL        DSORG TRKALLOC LSTUS DSNSTAT PCT +
08810000            EXT USEDTRK UNUSDTRK) +
08820000            REPLACE
08830000        SET &RC = &LASTCC
08840000 
08850000        IF &RC > 4 THEN +
08860000          DO
08870000            IF &RC > 8 THEN +
08880000              DO
08890000                SET SHORT = &STR(OPEN ERROR)
08900000                SET LONG  = &STR(COULD NOT CREATE TABLE &TBLSNAME.)
08910000                ISPEXEC SETMSG MSG(RSVT000)
08920000              END
08930000            ISPEXEC TBERASE &TBLSNAME
08940000            IF &LASTCC >  0 THEN GOTO VTOCEXIT
08950000            GOTO TRYCRDSN
08960000          END
08970000 
08980000  /*===================================================================*/
08990000  /*============= INVOKE $RSVP FOR DATA SET LIST ON VOLUME ============*/
09000000  /*===================================================================*/
09010000         CONTROL NOLIST NOCONLIST NOSYMLIST
09020000         SET SYSOUTTRAP = 3000
09030000         $RSVP VOL(&VOLUME) +
09040000          NOHEADING SORT(DSNAME) NOTOTAL CHARS(133) TRK +
09050000          PRINT(NEW (DSNAME TRKALLOC EXT DSORG  PCT LSTUS USED UNUSED))
09060000          SET RSVPRC  = &LASTCC
09070000          SET SYSOUTTRAP = 0
09080000          SET OUTLIM = &SYSOUTLINE
09090000 
09100000         IF &RSVPRC > 0 THEN +
09110000           DO
09120000             SET SHORT = &STR($RSVP RC=&RSVPRC)
09130000             SET RSVPDSNL = &&SYSOUTLINE00
09140000             SET L = &LENGTH(&STR(&RSVPDSNL))
09150000             IF &L > 3 THEN  SET RSVPMSGN = &SUBSTR(001:003,&STR(&RSVPDSNL))
09160000             IF   &STR(&RSVPMSGN) = &STR($RS) THEN +
09170000                SET LONG  = &STR(&SUBSTR(001:&L,&STR(&RSVPDSNL))
09180000             ELSE
09190000                SET LONG  = &STR($RSVP DSN SELECTION ABORTED FOR VOLUME &VOLUME)
09200000             ISPEXEC SETMSG MSG(RSVT000)
09210000             GOTO VTOCEXIT
09220000           END
09230000         ERROR +
09240000           DO
09250000             SET RCODE = &LASTCC
09260000             IF &RCODE > 8  THEN +
09270000               DO
09280000                SET SHORT =&STR(%$RSVTOCX RC=&RCODE)
09290000                SET LONG  =&STR(ERROR IN READ $RSVP DSN SECTION {ONLY RC > 20})
09300000                ISPEXEC SETMSG MSG(RSVT000)
09310000                WRITE VOLUME=&VOLUME
09320000                WRITE DSN#=&DSNN  SYSOUTLINE COUNTER=&I   TOTAL SYSOUTLINES=&OUTLIM
09330000                WRITE |&RSVPDSNL.|
09340000                GOTO VTOCEXIT
09350000               END
09360000             RETURN
09370000           END
09380000         SET DSNN = 0
09390000         SET DSNTRKS = 0
09400000         SET I = 1    /* SYSOUTLINE COUNTER */
09410000         SET RSVPDSNL = &&SYSOUTLINE&I
09420000         SET L = &LENGTH(&STR(&RSVPDSNL))
09430000         SET RSVPMSGN = &STR(***)
09440000         IF &L >= 3 THEN  SET RSVPMSGN = &SUBSTR(001:003,&STR(&RSVPDSNL))
09450000         IF   &STR(&RSVPMSGN) = &STR($RS) THEN +
09460000           DO
09470000            SET SHORT = &STR(** $RSVP ERROR **)
09480000            SET L = &LENGTH(&STR(&RSVPDSNL))
09490000            SET LONG  = &STR(&SUBSTR(001:&L,&STR(&RSVPDSNL))
09500000            ISPEXEC SETMSG MSG(RSVT000)
09510000            GOTO VTOCEXIT
09520000           END
09530000         IF   &STR(&RSVPMSGN) = &STR(   ) THEN +
09540000           DO
09550000            SET SHORT = &STR(NO DATA SETS FOUND)
09560000            SET LONG  = &STR(NO DATASETS FOUND ON &VOLUME  )
09570000            ISPEXEC SETMSG MSG(RSVT000)
09580000            GOTO VTOCEXIT
09590000           END
09600000 
09610000         /*===================================================================*/
09620000         /*============= BUILD THE DATASET TABLE FOR VOLUME ==================*/
09630000         /*===================================================================*/
09640000         DO WHILE &I <= &OUTLIM
09650000           IF &STR(&RSVPDSNL) =  THEN GOTO SKIPDSN
09660000           SET DSNAME   = &SUBSTR(001:045,&STR(&RSVPDSNL))
09670000           SET DSNN =&DSNN + 1
09680000           SET DSORG    = &SUBSTR(063:065,&STR(&RSVPDSNL))
09690000           IF &DSORG =  THEN SET DSORG    = &STR(??)
09700000           SET TRKALLOC = &SUBSTR(049:054,&STR(&RSVPDSNL))
09710000           SET EXT      = &SUBSTR(058:060,&STR(&RSVPDSNL))
09720000           SET PCT      = &SUBSTR(068:070,&STR(&RSVPDSNL))
09730000           IF &PCT   =  THEN SET PCT = &STR(0)
09740000           SET LSTUS    = &SUBSTR(072:079,&STR(&RSVPDSNL))
09750000           SET USEDTRK  = &SUBSTR(080:087,&STR(&RSVPDSNL))
09760000           SET UNUSDTRK = &SUBSTR(089:096,&STR(&RSVPDSNL))
09770000           SET DSNSEL =
09780000           SET DSNSTAT =
09790000           SET DSNTRKS = &EVAL(&DSNTRKS + &TRKALLOC)
09800000           ISPEXEC TBADD &TBLSNAME.
09810000          SKIPDSN:+
09820000           SET I = &I + 1
09830000           SET RSVPDSNL = &&SYSOUTLINE&I
09840000         END
09850000        DSNEND: +
09860000       ERROR OFF
09870000 
09880000 
09890000       /*===================================================================*/
09900000       /*============= DISPLAY THE DATA SET TABLE FOR VOLUME ===============*/
09910000       /*===================================================================*/
09920000       SET VTDSELS = &ZTDSELS
09930000       ISPEXEC CONTROL DISPLAY SAVE
09940002       SET &DSNTBL = &STR($RSDSNL&ZSCREEN)
09950000       SET &DSNPNL = &STR($RSDSNLX)
09960000       SET &DSNPROC = NO
09970000       IF &DEBUG = DEBUG THEN CONTROL SYMLIST LIST CONLIST
09980000       SET &ZTDSELS = 0
09990000       SET &DSNRC = 0       /* RETURN CODE FOR THIS CLIST                */
10000000       SET &DSNUSRRC = 0    /* RETURN CODE FROM TBDISPL                  */
10010000       SET &DSNROWP =       /* ROW IN WHICH CURSOR IS DISPLAYED          */
10020000       SET &DSNCUR = DSNSEL /* FIELD IN WHICH CURSOR IS DISPLAYED        */
10030000       SET &DSNERRB = NO    /* INDICATES WHETHER AN ERROR HAS BEEN FOUND */
10040000                            /* BEFORE ALL ROW PROCESSING IS FINISHED.    */
10050000       SET &DSNERRA = NO    /* INDICATES WHETHER AN ERROR HAS BEEN FOUND */
10060000                            /* AFTER  ALL ROW PROCESSING IS FINISHED.    */
10070000       SET &DSNMSG = RSVT013/* ID OF MESSAGE TO BE DISPLAYED             */
10080000       SET &DSNREPOS = 1    /* NUMBER OF TOP ROW TO BE DISPLAYED         */
10090000       DO WHILE ( &DSNUSRRC < 8 OR +
10100000                ( (&DSNERRB = YES OR &DSNERRA = YES) AND  &DSNPROC = YES) )
10110000         ISPEXEC TBSKIP &DSNTBL ROW(&DSNREPOS) NOREAD
10120000         IF &DSNERRB = NO THEN +
10130000           DO
10140000             SET &DSNPNLD = &DSNPNL
10150000             SET &DSNASEL = NO
10160000             ISPEXEC VGET (ZVERB, ZSCROLLN) SHARED
10170000             IF &ZVERB = UP THEN +
10180000               ISPEXEC TBSKIP &DSNTBL NUMBER(-&ZSCROLLN)
10190000             IF &ZVERB = DOWN THEN +
10200000               ISPEXEC TBSKIP &DSNTBL NUMBER(&ZSCROLLN)
10210000           END
10220000         ELSE +
10230000           DO
10240000             SET &DSNPNLD =
10250000             IF &DSNERRB = YES +
10260000               THEN SET &DSNASEL = YES
10270000               ELSE SET &DSNASEL = NO
10280000           END
10290000 
10300000       ISPEXEC TBDISPL &DSNTBL PANEL(&DSNPNLD) +
10310000         CURSOR(&DSNCUR) CSRROW(&DSNROWP) AUTOSEL(&DSNASEL) MSG(&DSNMSG)
10320000       SET &DSNUSRRC = &LASTCC
10330000       IF &DSNUSRRC = 8 AND &DSNPROC = NO THEN +
10340000         DO
10350000           GOTO DSNDONE
10360000         END
10370000       SET &DSNREPOS = &ZTDTOP
10380000 
10390000       SET &DSNERRB = NO
10400000       SET &DSNERRA = NO
10410000       SET &DSNMSG =
10420000       SET &DSNCUR =
10430000       SET &DSNROWP =
10440000 
10450000       IF &STR(&CMD) = CANCEL THEN +
10460000         DO
10470000           SET &DSNRC = 20
10480000           GOTO DSNDONE
10490000         END
10500000 
10510000 
10520000       DO WHILE ( &ZTDSELS > 0 )
10530000          /*=============== PROCESS THE TABLES ROWS =====================*/
10540000          /*=============== PROCESS THE TABLES ROWS =====================*/
10550000          /*=============== PROCESS THE TABLES ROWS =====================*/
10560000          IF &STR(&DSNSEL) = B THEN +
10570000            DO
10580000              SET DSNSEL = &STR( )
10590000              SET BRDSN = &DSNAME.
10600000              IF &DSORG = PS OR &DSORG = PO THEN +
10610000                DO
10620000                  ISPEXEC CONTROL DISPLAY SAVE
10630000                  ISPEXEC BROWSE DATASET('&BRDSN.') VOLUME(&VOLUME)
10640000                  SET DSNBRRC = &LASTCC
10650000                  ISPEXEC CONTROL DISPLAY RESTORE
10660000                END
10670000              ELSE SET DSNBRRC = 256
10680000              IF      &DSNBRRC =  0 THEN DO
10690000                                           SET DSNSTAT = &STR(*BROWSE*)
10700000                                           SET LSTUS = &STR(&SYSDATE)
10710000                                         END
10720000              ELSE IF &DSNBRRC = 12 THEN DO
10730000                                           SET LSTUS = &STR(&SYSDATE)
10740000                                           SET DSNSTAT = &STR(*EMPTY *)
10750000                                           SET SHORT=&STR(DATA SET WAS EMPTY      )
10760000                                           SET LONG =&STR('&BRDSN.' WAS EMPTY)
10770000                                           SET DSNERRB = YES
10780000                                         END
10790000              ELSE IF &DSNBRRC = 16 THEN DO
10800000                                           SET LSTUS = &STR(&SYSDATE)
10810000                                           SET DSNSTAT = &STR(*NO MBRS)
10820000                                           SET SHORT=&STR(NO MEMBERS IN PDS       )
10830000                                           SET LONG =&STR(PDS '&BRDSN.' HAS NO MEMBERS)
10840000                                           SET DSNERRB = YES
10850000                                         END
10860000              ELSE IF &DSNBRRC = 256 THEN DO
10870000                                           SET DSNSTAT = &STR(*BR ERR*)
10880000                                           SET SHORT=&STR(DSORG MUST BE PS OR PO.)
10890000                                           SET LONG =&STR(ERROR WHILE TRYING TO BROWSE DSORG=&DSORG DSN='&BRDSN')
10900000                                           SET DSNERRB = YES
10910000                                          END
10920000              ELSE                       DO
10930000                                           SET DSNSTAT = &STR(*BR ERR*)
10940000                                           SET SHORT=&STR(SEVERE ERROR IN BROWSE  )
10950000                                           SET LONG =&STR(SEVERE ERROR WHILE TRYING TO BROWSE '&BRDSN')
10960000                                           SET DSNERRB = YES
10970000                                         END
10980000            END
10990000 
11000000          IF &STR(&DSNSEL) = E THEN +
11010000            DO
11020000              SET DSNSEL = &STR( )
11030000              SET EDDSN = &DSNAME.
11040000              IF &DSORG = PS OR &DSORG = PO THEN +
11050000                DO
11060000                  ISPEXEC CONTROL DISPLAY SAVE
11070000                  ISPEXEC EDIT   DATASET('&EDDSN.') VOLUME(&VOLUME.)
11080000                  SET DSNEDRC = &LASTCC
11090000                  ISPEXEC CONTROL DISPLAY RESTORE
11100000                END
11110000              ELSE SET DSNEDRC = 256
11120000              IF      &DSNEDRC =  0 THEN DO
11130000                                           SET DSNSTAT = &STR(*EDIT  *)
11140000                                           SET LSTUS = &STR(&SYSDATE)
11150000                                         END
11160000              ELSE IF &DSNEDRC =  4 THEN DO
11170000                                           SET DSNSTAT = &STR(*NOSAVE*)
11180000                                           SET LSTUS = &STR(&SYSDATE)
11190000                                         END
11200000              ELSE IF &DSNEDRC = 16 THEN DO
11210000                                           SET DSNSTAT = &STR(*NO MBRS)
11220000                                           SET SHORT=&STR(NO MEMBERS IN PDS       )
11230000                                           SET LONG =&STR(PDS '&EDDSN.' HAS NO MEMBERS)
11240000                                           SET DSNERRB = YES
11250000                                           SET LSTUS = &STR(&SYSDATE)
11260000                                         END
11270000              ELSE IF &DSNEDRC =256 THEN DO
11280000                                           SET SHORT=&STR(SEVERE ERROR IN EDIT    )
11290000                                           SET DSNERRB = YES
11300000                                           SET SHORT=&STR(DSORG MUST BE PS OR PO.)
11310000                                           SET LONG =&STR(ERROR WHILE TRYING TO EDIT DSORG=&DSORG DSN='&EDDSN')
11320000                                         END
11330000              ELSE                       DO
11340000                                           SET DSNSTAT = &STR(*ED ERR*)
11350000                                           SET SHORT=&STR(SEVERE ERROR IN EDIT    )
11360000                                           SET LONG =&STR(SEVERE ERROR WHILE TRYING TO EDIT '&EDDSN')
11370000                                           SET DSNERRB = YES
11380000                                         END
11390000          END
11400000 
11410000          IF &STR(&DSNSEL) = P         THEN +
11420000            DO
11430000              SET DSNSEL = &STR( )
11440000              SET PDSDSN = &DSNAME.
11450000              IF &DSORG = PO THEN +
11460000                DO
11470000                  ISPEXEC CONTROL DISPLAY SAVE
11480000                  PDS '&PDSDSN'
11490000                  ISPEXEC CONTROL DISPLAY RESTORE
11500000                  SET DSNSTAT = &STR(*PDS CMD)
11510000                  SET LSTUS = &STR(&SYSDATE)
11520000                END
11530000              ELSE +
11540000                DO
11550000                  SET DSNSTAT = &STR(*NOT PDS)
11560000                  SET SHORT=&STR(DSN IS NOT A PDS        )
11570000                  SET LONG =&STR(DSORG MUST BE PO. PDS COMMAND NOT VALID FOR '&PDSDSN'.)
11580000                  SET DSNERRB = YES
11590000                END
11600000            END
11610000          IF &STR(&DSNSEL) = C | &STR(&DSNSEL) = U +
11620000           | &STR(&DSNSEL) = R | &STR(&DSNSEL) = D THEN +
11630000            DO
11640000              SET INFOSEL = &DSNSEL
11650000              SET DSNSEL = &STR( )
11660000              SET DSNSTAT = &STR(*&INFOSEL.-NOGO*)
11670000            END
11680000          IF &STR(&DSNSEL) = S | &STR(&DSNSEL) = I THEN +
11690000            DO
11700000              SET INFOSEL = &DSNSEL
11710000              SET DSNSEL = &STR( )
11720000              SET INFODSN = &STR(&DSNAME.)
11730000              SET INFODSN = &INFODSN
11740000              SET DSNSTAT = &STR(*INFO-&INFOSEL.*)
11750000              CONTROL NOLIST NOCONLIST NOSYMLIST
11760000              SET &SYSOUTTRAP = 5
11770000                $RSVP VOL(&VOLUME) LIKE(&INFODSN.) +
11780000                 CHARS(133) TRK NOTOTAL NOSORT NOHEADING +
11790000                 PRINT(NEW (ADDRESS CAT CB DSIND RECFM LRECL BLKSZ +
11800000                 SECT SECQ KEYL OPTCD PW RD RKP DSORG +
11810000                 CREDT EXPDT LMDATE LMJOB LMTIM KBALLOC   ))
11820000              SET &SYSOUTTRAP = 0
11830000              IF &DEBUG = DEBUG THEN CONTROL   LIST   CONLIST   SYMLIST
11840000              SET OUTLIM = &SYSOUTLINE
11850000              SET II = 1
11860000              SET INFOERR = 0
11870000              DO WHILE (&II <= &OUTLIM)
11880000                SET INFOLINE = &&SYSOUTLINE&II
11890000                IF &LENGTH(&STR(&INFOLINE)) < 3 THEN GOTO SKIPINF1
11900000                IF &SUBSTR(1:3,&STR(&INFOLINE)) = $RS THEN +
11910000                  DO
11920000                    SET INFOERR = &INFOERR + 1
11930000                    IF &INFOERR > 10 THEN GOTO SHOWIERR
11940000                    SET RSERR&INFOERR = &INFOLINE
11950000                    GOTO SKIPINF1
11960000                  END
11970000                SET ADDRESS = &SUBSTR(001:010,&STR(&INFOLINE))
11980000                SET CAT     = &SUBSTR(013:013,&STR(&INFOLINE))
11990000                SET CB      = &SUBSTR(016:016,&STR(&INFOLINE))
12000000                SET DSIND   = &SUBSTR(021:022,&STR(&INFOLINE))
12010000                SET RECFM   = &SUBSTR(025:029,&STR(&INFOLINE))
12020000                SET LRECL   = &SUBSTR(031:035,&STR(&INFOLINE))
12030000                SET BLKSZ   = &SUBSTR(037:041,&STR(&INFOLINE))
12040000                SET SECT    = &SUBSTR(045:045,&STR(&INFOLINE))
12050000                SET SECQ    = &SUBSTR(049:052,&STR(&INFOLINE))
12060000                SET KEYL    = &SUBSTR(054:057,&STR(&INFOLINE))
12070000                SET OPTCD   = &SUBSTR(061:062,&STR(&INFOLINE))
12080000                SET PW      = &SUBSTR(065:065,&STR(&INFOLINE))
12090000                SET RD      = &SUBSTR(068:068,&STR(&INFOLINE))
12100000                SET RKP     = &SUBSTR(071:074,&STR(&INFOLINE))
12110000                SET VSDSORG = &SUBSTR(076:080,&STR(&INFOLINE))
12120000                SET CREDT   = &SUBSTR(082:089,&STR(&INFOLINE))
12130000                SET EXPDT   = &SUBSTR(091:098,&STR(&INFOLINE))
12140000                SET LMDATE  = &SUBSTR(100:107,&STR(&INFOLINE))
12150000                SET LMJOB   = &SUBSTR(109:116,&STR(&INFOLINE))
12160000                SET LMTIM   = &SUBSTR(118:122,&STR(&INFOLINE))
12170000                SET KBALLOC = &SUBSTR(124:131,&STR(&INFOLINE))
12180000                SKIPINF1:+
12190000                SET II = &II + 1
12200000              END
12210000              /******** USE A VERSION OF %R$DSINFO  ********/
12220000              /**      PGM=ISRUDA   PANEL=RSVUDA2 MODIFED ISRUDA2 **/
12230000              /**      ALSO MODIFIED PANELS=ISRUAIP,ISRUAIPO      **/
12240000              /** SIMULATE ISPF 3.2 OPTS I/S AND USE ISPF VARIABLES**/
12250000              /** TO GETS ITS DATASET INFORMATION.        **/
12260000              SET ROCUAOPT = &INFOSEL /* I=PDSDIR INFO,S=SHORT INFO */
12270000              SET R$DSN = &STR('&INFODSN.')
12280000              SET R$VOL = &VOLUME
12290000              SET ROCUDA = YES
12300000              SET IFLAG =
12310000              SET BEFORE =
12320000              SET ROCUDAM =
12330000              ISPEXEC VPUT (ROCUAOPT,R$DSN,R$VOL,ROCUDA,ROCUDAM) SHARED
12340000              ISPEXEC VPUT (IFLAG,BEFORE) SHARED
12350000              ISPEXEC CONTROL DISPLAY SAVE
12360000              ISPEXEC CONTROL NONDISPL ENTER
12370000              ISPEXEC SELECT PGM(ISRUDA) PARM(RSVUDA2)
12380000              IF &LASTCC > 0 THEN +
12390000                DO
12400000                  SET DSNSTAT = &STR(INFO ERR)
12410000                  SET DSNERRB = YES
12420000                END
12430000              ISPEXEC CONTROL DISPLAY RESTORE
12440000              ISPEXEC VGET (ZAL1EX,ZAL2EX,ZALSPC)
12450000              ISPEXEC VGET (TOTA,EXTA,TOTU,EXTU)
12460000              IF &DSORG=PO AND &INFOSEL=I THEN +
12470000                DO
12480000                  ISPEXEC VGET (ZALDIR,DIRU,NRMEM,ROCUDAM)
12490000                END
12500000              IF &INFOERR = 0 THEN +
12510000                DO
12520000                  ISPEXEC CONTROL DISPLAY SAVE
12530000                  DISPINFO:+
12540000                  ISPEXEC DISPLAY PANEL($RSDSNLI)
12550000                  IF &LASTCC = 0 THEN GOTO DISPINFO
12560000                  /** ADD INFOSEL=I     CLUSTER/CAT=W/CVOL INFO **/
12570000                  ISPEXEC VGET (RSVTIEX,RSVTIVC) PROFILE
12580000                  /** ADD INFOSEL=I DATASET EXTENT INFOMRATION. **/
12590000                  IF &RSVTIEX=YES THEN +
12600000                    DO
12610000                       SET SORTEXT = EXTSEQ
12620000                       /* OR USE "ABSTR" OR "CCHH" FOR SORT FIELD */
12630000                       CONTROL NOLIST NOCONLIST NOSYMLIST
12640000                       SET &SYSOUTTRAP = 20
12650000                       $RSVP VOL(&VOLUME) LIKE(&DSNAME) SORT(&SORTEXT.) IF(EXTSEQ LE 15) +
12660000                        PRINT(NEW (ABSTR ABLEN CCHH EXTSEQ EXT)) NOHEADING NOTOTAL
12670000                       SET &SYSOUTTRAP = 0
12680000                       IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST
12690000                       SET &OUTLIM = &&SYSOUTLINE
12700000                       SET &I = 1
12710000                       SET &EXTKNT = 0
12720000                       SET &LINE = &&SYSOUTLINE&I
12730000                       SET &RSVP = &STR(&LINE)
12740000                       DO WHILE &I <= &OUTLIM
12750000                         SET L = &LENGTH(&STR(&RSVP))
12760000                         SET &RSVPMSG =
12770000                         IF &L >= 8 THEN IF &SUBSTR(1:3,&STR(&RSVP)) = &STR($RS) THEN +
12780000                           SET &RSVPMSG = &SUBSTR(1:8,&STR(&RSVP))
12790000                         IF &L = 0  OR  &RSVPMSG ^=   OR  &L < 42  THEN GOTO SKIPEXNT
12800000                         SET &EXTKNT = &EXTKNT + 1
12810000                         SET  ABSTR   = &SUBSTR(01:05,&STR(&RSVP))
12820000                         SET  ABLEN   = &SUBSTR(07:11,&STR(&RSVP))
12830000                         SET  CCHH    = &SUBSTR(13:29,&STR(&RSVP))
12840000                         SET  EXTSEQ  = &SUBSTR(31:35,&STR(&RSVP))
12850000                         SET  DSNEXTS = &SUBSTR(38:42,&STR(&RSVP))
12860000                         SET  EXTL&EXTKNT = &STR(        &ABSTR         &ABLEN       &CCHH &EXTSEQ      &DSNEXTS)
12870000                       SKIPEXNT:+
12880000                         SET &I = &I + 1
12890000                         SET &LINE = &&SYSOUTLINE&I
12900000                         SET &RSVP = &STR(&LINE)
12910000                       END
12920000                       /* DISPLAY EXTENT INFORMATION */
12930000                       DSPEXINF:+
12940000                       ISPEXEC DISPLAY PANEL($RSDSNLE)
12950000                       IF &LASTCC = 0 THEN GOTO DSPEXINF
12960000                       /* NOW CLEAR OUT VARIABLES */
12970000                       SET &I = 1
12980000                       DO WHILE &I <= &EXTKNT
12990000                         SET  EXTL&I =
13000000                         SET &I = &I + 1
13010000                       END
13020000                    END /* INFOSEL=I EXTENT INFO */
13030000                  /** END INFOSEL=I DATASET EXTENT INFOMRATION. **/
13040000                  IF &RSVTIVC=YES THEN +
13050000                    DO
13060000                      CONTROL NOCAPS
13070000                      SET VSWMSG0 =
13080000                      SET VSWMSG1 =
13090000                      SET VSWMSG2 =
13100000                      SET VSWMSG3 =
13110000                      SET VSWMSG4 =
13120000                      SET VSWMSG5 =
13130000                      SET VSWERR  = NO
13140000                      SET CLUSTER =
13150000                      SET CTLGNAME=
13160000                      SET UCATCVOL=
13170000                      SET CATVOL  =
13180000                      SET VSTYPE =
13190000                      SET VSWDSN = &DSNAME
13200000                      SET DSNAME = &VSWDSN
13210000                      SET L   = &LENGTH(&STR(&DSNAME))
13220000                      IF &L > 8 THEN IF &SUBSTR(1:8,&STR(&DSNAME)) = Z9999992 THEN +
13230000                        DO
13240000                          SET VS = &STR(VSAM DataSpace)
13250000                          SET VSWMSG0= &STR(This is a VSAM User Catalog DataSpace with one or more clusters.)
13260000                          SET VSWERR = NO
13270000                          GOTO ENDVSW
13280000                        END
13290000                      CONTROL NOLIST NOCONLIST NOSYMLIST
13300000                      SET &SYSOUTTRAP = 10
13310000                      $RSVP VOL(&VOLUME) LIKE(&DSNAME) IF(EXTSEQ EQ 0) +
13320000                        PRINT(NEW (CCHH DSNAME EXTSEQ EXT)) CHARS(133) NOHEADING NOTOTAL
13330000                      SET &SYSOUTTRAP = 0
13340000                      IF &DEBUG = DEBUG THEN CONTROL   LIST   CONLIST   SYMLIST
13350000                      SET &OUTLIM = &SYSOUTLINE
13360000                      IF &OUTLIM = 0 THEN +
13370000                        DO
13380000                          SET VSWERR = YES
13390000                          SET VSWMSG1= &STR(NOT on this volume anymore.)
13400000                          GOTO ENDVSW
13410000                        END
13420000                      SET &I = 1
13430000                      SET LINE = &&SYSOUTLINE&I
13440000                      SET RSVP = &STR(&LINE)
13450000                      DO WHILE &I <= &OUTLIM
13460000                        SET L = &LENGTH(&STR(&RSVP))
13470000                        IF &L > 3 THEN +
13480000                          DO
13490000                            SET RSVPMSG =
13500000                            IF &SUBSTR(1:3,&STR(&RSVP)) = &STR($RS) THEN +
13510000                              DO
13520000                                SET RSVPMSG = &SUBSTR(1:8,&STR(&RSVP))
13530000                                SET VSWMSG2= &STR(&RSVP)
13540000                              END
13550000                          END
13560000                        IF &L >= 62 THEN +
13570000                          DO
13580000                            SET CCHH = &SUBSTR(01:08,&STR(&RSVP))
13590000                            SET CCHHDSN = &SUBSTR(19:62,&STR(&RSVP))
13600000                            IF &STR(CCHHDSN) = &STR(&DSNAME) THEN +
13610000                              GOTO GOTCCHH
13620000                          END
13630000                        SET &I = &I + 1
13640000                        SET LINE = &&SYSOUTLINE&I
13650000                        SET RSVP = &STR(&LINE)
13660000                      END
13670000                      GOTCCHH:+
13680000                      IF &DSORG = VS THEN +
13690000                        DO
13700000                          CONTROL NOLIST NOCONLIST NOSYMLIST
13710000                          SET &SYSOUTTRAP = 100
13720000                          $RSVP VOL(&VOLUME) IF(CCHH EQ &CCHH) CLUSTER +
13730000                          PRINT(NEW (CCHH DSNAME VS CAT EXTSEQ EXT)) CHARS(133) NOHEADING NOTOTAL
13740000                          SET &SYSOUTTRAP = 0
13750000                          IF &DEBUG = DEBUG THEN CONTROL   LIST   CONLIST   SYMLIST
13760000                          SET &OUTLIM = &SYSOUTLINE
13770000                          SET &I = 1
13780000                          SET LINE = &&SYSOUTLINE&I
13790000                          SET RSVP = &STR(&LINE)
13800000                          DO WHILE &I <= &OUTLIM
13810000                            SET L = &LENGTH(&STR(&RSVP))
13820000                            SET RSVPERR =
13830000                            IF &L > 3 THEN +
13840000                              DO
13850000                                IF &SUBSTR(1:3,&STR(&RSVP)) = &STR($RS) +
13860000                                 | &SUBSTR(1:3,&STR(&RSVP)) = &STR($VS) THEN +
13870000                                  DO
13880000                                    SET RSVPMSG = &SUBSTR(1:8,&STR(&RSVP))
13890000                                    SET RSVPERR = &SUBSTR(1:3,&STR(&RSVP))
13900000                                  END
13910000                              END
13920000                            IF &L = 0 OR &STR(&RSVPERR) ^= THEN GOTO SKPCHCAT
13930000                            IF &SUBSTR(1:8,&STR(&RSVP)) = &STR(&CCHH) THEN +
13940000                              DO
13950000                                SET VSAMNAME= &SUBSTR(019:62,&STR(&RSVP))
13960000                                SET CLUSTER = &VSAMNAME
13970000                                SET VS      = &SUBSTR(065:65,&STR(&RSVP))
13980000                                SET CAT     = &SUBSTR(069:69,&STR(&RSVP))
13990000                                IF   IF &VS = U THEN SET VSTYPE = &STR(VSAM User Catalog)
14000000                                ELSE IF &VS = M THEN SET VSTYPE = &STR(VSAM Master Catalog)
14010000                                ELSE IF &VS = C THEN SET VSTYPE = &STR(Cluster)
14020000                                ELSE IF &VS = I THEN SET VSTYPE = &STR(Clusters Index)
14030000                                ELSE IF &VS = D THEN SET VSTYPE = &STR(Clusters Data)
14040000                                ELSE IF &VS = P THEN SET VSTYPE = &STR(Page Dataset)
14050000                                ELSE IF &VS = S THEN SET VSTYPE = &STR(Swap Dataset)
14060000                                ELSE IF &VS = X THEN SET VSTYPE = &STR(VVDS)
14070000                                ELSE IF &VS = G THEN SET VSTYPE = &STR(Alternate Index)
14080000                                ELSE                     VSTYPE = &STR(* Unkown *)
14090000                              END
14100000                      SKPCHCAT:+
14110000                            SET &I = &I + 1
14120000                            SET LINE = &&SYSOUTLINE&I
14130000                            SET RSVP = &STR(&LINE)
14140000                          END
14150000                       END
14160000                      IF &CAT  = W OR &CAT = C THEN +
14170000                        DO
14180000                          SET &SYSOUTTRAP = 100
14190000                          IF &DSORG = VS AND &STR(&CLUSTER) ^= +
14200000                            THEN  LISTC ENT('&CLUSTER') VOLUME
14210000                            ELSE  LISTC ENT('&DSNAME')  VOLUME
14220000                          SET &SYSOUTTRAP = 0
14230000                          IF &DEBUG = DEBUG THEN CONTROL   LIST   SYMLIST   CONLIST
14240000                          SET OUTLIM = &&SYSOUTLINE
14250000                          SET &I = 1
14260000                          SET OUTLINE = &&SYSOUTLINE&I
14270000                          SET LINE = &STR(&OUTLINE)
14280000                          SET CATVOL =
14290000                          SET UCATCVOL=
14300000                          DO WHILE &I <= &OUTLIM
14310000                            SET L = &LENGTH(&STR(&LINE))
14320000                            IF &L >= 17 THEN +
14330000                              DO
14340000                                SET CATWLINE = &SUBSTR(06:16,&STR(&LINE.))
14350000                                IF &STR(&CATWLINE) = &STR(IN-CAT --- ) THEN +
14360000                                  DO
14370000                                    SET CTLGNAME = &SUBSTR(17:&L,&STR(&LINE))
14380000                                  END
14390000                              END
14400000                            IF &L >= 31 THEN +
14410000                              DO
14420000                                SET CATWLINE = &SUBSTR(08:14,&STR(&LINE.))
14430000                                IF &STR(&CATWLINE) = &STR(VOLSER-) THEN +
14440000                                  DO
14450000                                    SET CATVOL = &SUBSTR(26:31,&STR(&LINE))
14460000                                    GOTO ENDWVOL
14470000                                  END
14480000                              END
14490000                            SET &I = &I + 1
14500000                            SET OUTLINE = &&SYSOUTLINE&I
14510000                            SET LINE = &STR(&OUTLINE)
14520000                          END
14530000                          ENDWVOL:+
14540000                          IF &STR(&CATVOL) ^=    THEN +
14550000                            DO
14560000                              SET L=&LENGTH(&STR(&CTLGNAME))
14570000                              SET UCATCVOL= &STR(VSAM Catalog)
14580000                              IF &L>=15 THEN IF &SUBSTR(1:7,&STR(&CTLGNAME)) = SYSCTLG THEN +
14590000                                SET UCATCVOL = CVOL
14600000                            END
14610000                            ELSE +
14620000                              DO
14630000                                SET VSWMSG3= &STR(Could not find where its cataloged.)
14640000                                SET VSWERR = YES
14650000                              END
14660000                          IF &CAT = W THEN +
14670000                            DO
14680000                              SET VSWMSG4= &STR(Dataset is cataloged elsewhere on &CATVOL)
14690000                              SET VSWERR = NO
14700000                            END
14710000                        END
14720000                      ENDVSW: SET DUMMY = &DUMMY
14730000                       DISPVSCW:+
14740000                       ISPEXEC DISPLAY PANEL($RSDSNLC)
14750000                       IF &LASTCC=0 THEN GOTO DISPVSCW
14760000                       IF &DEBUG = DEBUG THEN CONTROL NOLIST NOCONLIST NOSYMLIST
14770000                    END /* OF INFOSEL=I CLUSTER/CAT=W/CVOL INFO */
14780000                  /** END INFOSEL=I VSAM CLUSTER/CATALOG INFORMATION **/
14790000                  CONTROL CAPS
14800000                  ISPEXEC CONTROL DISPLAY RESTORE
14810000                  IF &DSORG=PO AND &INFOSEL=I THEN +
14820000                    SET LSTUS = &STR(&SYSDATE.)
14830000                END
14840000               SHOWIERR:+
14850000               IF &INFOERR > 0 THEN +
14860000                 DO
14870000                   SET DSNERRB = YES
14880000                   SET DSNSTAT = &STR(INFO ERR)
14890000                   ISPEXEC CONTROL DISPLAY SAVE
14900000                   ISPEXEC DISPLAY PANEL($RSERROR)
14910000                   ISPEXEC CONTROL DISPLAY RESTORE
14920000                 END
14930000            END
14940000          IF &DSNERRB = YES THEN +
14950000            DO
14960000              SET DSNCUR = DSNSEL
14970000              SET DSNMSG = RSVT000
14980000              ISPEXEC TBQUERY &DSNTBL POSITION(DSNROWP)
14990000              SET DSNREPOS = &DSNROWP
15000000              ISPEXEC TBPUT &DSNTBL /* UPDATE ROW ANYWAY */
15010000              GOTO DSNTBLER
15020000            END
15030000          ISPEXEC TBPUT &DSNTBL
15040000          IF &ZTDSELS > 1 +
15050000            THEN ISPEXEC TBDISPL &DSNTBL
15060000            ELSE SET &ZTDSELS = 0
15070000       END
15080000     SET &DSNMSG =
15090000     SET &DSNROWP =
15100000     SET &DSNCUR = DSNSEL
15110000     DSNTBLER: +
15120000   END
15130000 
15140000     DSNDONE: +
15150000      ISPEXEC CONTROL DISPLAY RESTORE
15160000      SET ZTDSELS = &VTDSELS
15170000    END /* OF VOLUME OPTION S */
15180000    ISPEXEC TBPUT &VOLTBL
15190000    IF &ZTDSELS > 1 +
15200000      THEN ISPEXEC TBDISPL &VOLTBL
15210000      ELSE SET &ZTDSELS = 0
15220000  END
15230000SET &VOLMSG =
15240000SET &VOLROWP =
15250000SET &VOLCUR = VOLSEL
15260000VOLTBLER: +
15270000END
15280000 
15290000VOLDONE: +
15300000SET VOLRC = &VOLRC. /* DUMMY STATEMENT */
15310000 
15320000/* TURN THE USERS PROF NOMSGID BACK ON IF IT WAS RESET */
15330000VTOCEXIT:+
15340000IF &RESETMSG = YES THEN PROFILE NOMSGID
15350000 
15360000EXIT
