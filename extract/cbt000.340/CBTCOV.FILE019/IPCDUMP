00010000PROC     0        DEBUG    OPTION()
00020000 
00030000/*********************************************************************/
00040000/*                   DUMP DATA SET MANAGEMENT                        */
00050000/* THIS IS AN IPCS/ISPF DUMP MANAGEMENT AND ANALYSIS APPLICATION.    */
00060000/* IT SETS UP AN ENVIRONMENT FOR THE LOWER LEVEL CLISTS AND PANELS.  */
00070000/* YOU MUST BE UNDER ISPF. THE HIGH LEVEL PANELS ALLOW YOU TO MANAGE */
00080000/* DUMP DATA SETS. YOU CAN SELECT A DUMP FROM ONE OF THE HIGH LEVEL  */
00090000/* PANELS FOR ANALYSIS VIA LOWER LEVEL PANELS. YOU MUST BE UNDER     */
00100000/* IPCS FOR ANALYSIS UNLESS YOU INTEND TO USE ONLY AMDPRDMP. SEE     */
00110000/* CLIST IPCSTART TO INVOKE IPCS.                                    */
00120000/*********************************************************************/
00130000 
00140000IF       &DEBUG = DEBUG +
00150000THEN     CONTROL  ASIS   CONLIST NOFLUSH   LIST   MSG   PROMPT   SYMLIST
00160000ELSE     CONTROL  ASIS NOCONLIST NOFLUSH NOLIST NOMSG   PROMPT NOSYMLIST
00170000 
00180000ISPEXEC  VGET     (ZENVIR IPCINIT)
00190000 
00200000IF       &SUBSTR(9:16,&STR(&ZENVIR)) = &STR(MVS/XA) +
00210000THEN     SET      &IPCENVIR = XA
00220000ELSE     SET      &IPCENVIR = 370
00230000 
00240000IF       &IPCINIT ^= YES +
00250000THEN     %IPCPARM &IPCENVIR &DEBUG
00260000 
00270000ISPEXEC  VGET     (IPCOS ZSCREEN DEFMACH DEFREM DEFREQ DEFSTOR DEFSYM DEFFLAG DEFLEN DEFVER)
00280000 
00281005SHOWTSO CENTER
00282005SET &NODENAME = &CENTER
00300000ISPEXEC  VPUT     (NODENAME) PROFILE
00310000 
00320001IF &IPCOS ^= XA THEN DO
00330001                     ISPEXEC  LIBDEF   ISPMLIB  DATASET  ID('ATSO.IPCS&IPCOS..MSGS')   UNCOND
00340001                     ISPEXEC  LIBDEF   ISPPLIB  DATASET  ID('ATSO.IPCS&IPCOS..PANELS') UNCOND
00350001                     END
00360001                ELSE DO
00380004                     IF &CENTER = WCC1 THEN DO
00390004                                  ISPEXEC  LIBDEF   ISPMLIB  DATASET  ID('SYS1.SBLSMSG0')   UNCOND
00400004                                  ISPEXEC  LIBDEF   ISPPLIB  DATASET  ID('SYS1.SBLSPNL0') UNCOND
00410004                                             END
00420004                                       ELSE DO
00430004                                  ISPEXEC  LIBDEF   ISPMLIB  DATASET  ID('ATSO.IPCSXA.MSGS')   UNCOND
00440004                                  ISPEXEC  LIBDEF   ISPPLIB  DATASET  ID('ATSO.IPCSXA.PANELS') UNCOND
00450004                                             END
00460001                     END
00470001 
00480003/* ISPEXEC  LIBDEF   ISPTABL  DATASET  ID('AOST.ISPF.TABLES')        UNCOND */
00490001 
00500000IF       &IPCOS = XA +
00510000THEN     SET      &RFIND = PASSTHRU
00520000ELSE     SET      &RFIND = &STR()
00530000ISPEXEC  SELECT   PGM(BLSG)
00540002IF       &LASTCC = 0 +
00550000THEN     SET      &SYSIPCS = ACTIVE
00560000ELSE     SET      &SYSIPCS = NOT ACTIVE
00570000IF       &SYSIPCS = NOT ACTIVE +
00580000THEN     DO
00590000         SET      &IPCOS = &IPCENVIR
00600000         ISPEXEC  VPUT     (IPCOS) PROFILE
00610000         ISPEXEC  SETMSG   MSG(IPC005)
00620000         END
00630000 
00640000SET      &NOTEPRNT = &STR()
00650000SET      &NOTEBRWS = &STR()
00660000 
00670000ISPEXEC  VPUT     (RFIND IPCENVIR SYSIPCS NOTEPRNT NOTEBRWS) SHARED
00680000 
00690000ISPEXEC  TBOPEN   DDIR&IPCOS WRITE SHARE LIBRARY(ISPTABL)
00700000IF       &LASTCC = 8 +
00710000THEN     DO
00720000         ISPEXEC  TBCREATE DDIR&IPCOS +
00730000                           NAMES (CPU DATE DATETIME DEVTYPE DSNAME DUMPOS FILE IBMPMR ID JOB NODE OWNER PRDUMPT QUAL +
00740000                                  SERIAL SOURCE SPR SRABD SRRC SRCID SRLM SRCSECT SRFRR SREGPSW SRPGM SRLVL SRSC SRRRL +
00750000                                  TIME TITLE TITLE2 TYPE VOLUME) +
00760000                           WRITE SHARE LIBRARY(ISPTABL)
00770000         ISPEXEC  TBSORT   DDIR&IPCOS FIELDS(DATETIME,C,D)
00780000         END
00790000 
00800000ISPEXEC  SELECT   PANEL(IPCDUMP) OPT(&OPTION)
00810000 
00820001ISPEXEC  LIBDEF   ISPMLIB
00830001ISPEXEC  LIBDEF   ISPPLIB
00840003/* ISPEXEC  LIBDEF   ISPTABL  */
00850000 
00860000ISPEXEC  TBCLOSE  DDIR&IPCOS REPLCOPY
00870000 
00880000ISPEXEC  VGET     (IPCPRTO IPCBROO IPCPRTC IPCBROC IPCPRTD IPCBROD NOTEPRNT NOTEBRWS IPCBRWS)
00890000SET      &RFIND = &STR()
00900000ISPEXEC  VPUT     (RFIND)
00910000 
00920000IF       &NOTEPRNT ^= &STR() +
00930000THEN     DO
00940000         IF       &IPCPRTO = ? +
00950000         THEN     DO
00960000                  ISPEXEC  CONTROL  NONDISPL END
00970000                  ISPEXEC  DISPLAY  PANEL(IPCDUMMY)
00980000                  ISPEXEC  CONTROL  NONDISPL END
00990000                  ISPEXEC  DISPLAY  PANEL(IPCDUMMY)
01000000                  %GENPRINT DSN(IPCPRNT&ZSCREEN..LIST) GPCLASS(&IPCPRTC) GPDEST(&IPCPRTD) NOBRO NOTELL &DEBUG
01010000                  END
01020000         IF       &IPCPRTO = PD +
01030000         THEN     PRINTOFF IPCPRNT&ZSCREEN..LIST CLASS(&IPCPRTC) DEST(&IPCPRTD)
01040000         IF       &IPCPRTO = PD | &IPCPRTO = D +
01050000         THEN     FREE     FILE(IPCPRNT&ZSCREEN) DELETE
01060000         ELSE     FREE     FILE(IPCPRNT&ZSCREEN) DATASET(IPCPRNT&ZSCREEN..LIST)
01070000         END
01080000 
01090000IF       &NOTEBRWS ^= &STR() +
01100000THEN     DO
01110000         IF       &IPCBROO = ? +
01120000         THEN     DO
01130000                  ISPEXEC  CONTROL  NONDISPL END
01140000                  ISPEXEC  DISPLAY  PANEL(IPCDUMMY)
01150000                  ISPEXEC  CONTROL  NONDISPL END
01160000                  ISPEXEC  DISPLAY  PANEL(IPCDUMMY)
01170000                  %GENPRINT DSN(IPCBRWS&ZSCREEN..LIST) GPCLASS(&IPCBROC) GPDEST(&IPCBROD) NOBRO NOTELL &DEBUG
01180000                  END
01190000         IF       &IPCBROO = PD +
01200000         THEN     PRINTOFF IPCBRWS&ZSCREEN..LIST CLASS(&IPCBROC) DEST(&IPCBROD)
01210000         ISPEXEC  LMFREE   DATAID(&IPCBRWS)
01220000         IF       &IPCBROO = PD | &IPCBROO = D +
01230000         THEN     FREE     FILE(IPCBRWS&ZSCREEN) DELETE
01240000         ELSE     FREE     FILE(IPCBRWS&ZSCREEN) DATASET(IPCBRWS&ZSCREEN..LIST)
01250000         END
01260000 
01270000EXIT     CODE(0)
