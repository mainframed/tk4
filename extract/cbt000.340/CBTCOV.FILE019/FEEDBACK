00010000PROC 0 DEBUG
00020007  CONTROL ASIS
00030000  IF &DEBUG = DEBUG +
00040001    THEN CONTROL   LIST   CONLIST   SYMLIST NOFLUSH   PROMPT   MSG
00050001    ELSE CONTROL NOLIST NOCONLIST NOSYMLIST   FLUSH NOPROMPT NOMSG
00060000 
00070005  ISPEXEC CONTROL ERRORS RETURN
00080005  ISPEXEC VGET ZTEMPF
00090005 
00100005  SET FEEDDATE = &STR(&SYSSDATE)
00110005  SET FEEDTIME = &STR(&SYSTIME)
00120005  SET FEEDSTMP = &STR(&FEEDDATE &FEEDTIME)
00130017  ISPEXEC VPUT (FEEDSTMP) SHARED
00140007 
00150015  SHOWTSO NAME FDS DEPT GROUP MAIL PHONE CENTER
00160015  SET FEEDFRM1 = &STR(&NAME -- &SYSUID at &CENTER)
00170016  SET FEEDFRM2 = &STR(Div: &FDS    Dept/Group: &DEPT/&GROUP)
00180016  SET FEEDFRM3 = &STR(M/S: &MAIL    Phone: &PHONE)
00190007 
00200011  SET FEEDDSN = &STR(FEEDBK&ZSCREEN..MSG)
00210011  IF &SYSDSN(&FEEDDSN) ^= OK THEN +
00220011    ALLOC DA(&FEEDDSN) RECFM(F B) LRECL(80) BLKSIZE(3120) +
00230011          SPACE(1 1) TRACK NEW CAT
00240011 
00250011  ISPEXEC EDIT DATASET(&FEEDDSN) MACRO(FEEDEMAC) PANEL(FEEDEDIT)
00260005  SET EDRC = &LASTCC
00270007 
00280011  LISTDSI &FEEDDSN
00290005  IF &SYSUSED = 0 THEN SET EDRC = 4 /* NOTHING SAVED. */
00300001 
00310005  IF      &EDRC   = 0 THEN DO
00320011    ISPEXEC EDIT DATASET(&FEEDDSN) MACRO(FEEDXMAC)
00330005    SET EDRC = &LASTCC
00340005    IF &EDRC > 0 THEN GOTO ERR
00350020 
00360020    IF &SYSDSN(LOG.MISC) = OK THEN SET LOG =
00370020                              ELSE SET LOG = NOLOG
00380020 
00390018    XMIT (SWCC.XTS0008,WCC1.$A1238,WCC1.YA0873) +
00400020         MSGDS(&FEEDDSN) NOPROLOG NOEPILOG &LOG  NONOTIFY
00410020 
00420005    SET SHORT = Sent
00430005    SET LONG  = &STR(&SYSNSUB(0,Feedback was sent to OS&T Support))
00440001  END
00450005  ELSE IF &EDRC   = 4 THEN DO
00460005    SET SHORT = Cancelled
00470005    SET LONG  = &STR(&SYSNSUB(0,Feedback was cancelled and was not sent to OS&T Support.))
00480005  END
00490004  ELSE DO
00500005   ERR:+
00510005    SET SHORT = &STR(Edit Error)
00520005    SET LONG  = &STR(&SYSNSUB(0,An unexpected error occurred during creation of your feedback.)
00530004  END
00540004 
00550001  ISPEXEC SETMSG MSG(AOST010)
00560011 
00570011  CONTROL NOMSG
00580011  DELETE (&FEEDDSN)
