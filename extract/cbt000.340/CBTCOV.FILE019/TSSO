00010022PROC 0 CMD() DEBUG
00020040 
00030009IF &DEBUG = DEBUG THEN  CONTROL   MSG   LIST   CONLIST   SYMLIST
00040009ELSE                    CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST
00050009 
00060001/** ** ** ** ** ** ** ** **  TSSO ** ** ** ** ** ** ** ** ** ** ** **/
00070001/**                                                                **/
00080001/**  USED WITH ISPF PRIMARY COMMAND: TSSO                          **/
00090001/**  TSSO  PRIMARY COMMAND DEFINED AS:                             **/
00100001/**      TSSO  0 SELECT CMD(%TSSO CMD('&ZPARM'))                   **/
00110001/**  OSCMD PRIMARY COMMAND DEFINED AS:                             **/
00120001/**      OSCMD 0 SELECT CMD(%TSSO CMD('OSCMD &ZPARM'))             **/
00130001/**  INVOKE AS:  TSSO UCB 6AX                                      **/
00140001/**              TSSO ACT D=XTS0008                                **/
00150001/**              TSSO OSCMD D R,L     or   OSCMD D R,L             **/
00160001/**      OR AS:  TSSO               TO GET PANEL ASSISTED VERSION. **/
00170001/**                                                                **/
00180004/**  Written by: Mike Theys                               08/08/86 **/
00190004/**              SouthWestern Computing Center                     **/
00200004/**              Information Systems Center                        **/
00210004/**              Rockwell International Corporation                **/
00220004/**              1200 N. Alma Rd.   Bldg 401-109                   **/
00230004/**              Richardson, Texas  75081                          **/
00240004/**              (214) 006-2860     Comnet 437-2860                **/
00250004/**  LastUpdated: 10/18/86 - CACHE/INT/TIOT/LOCATE                 **/
00260027/**  LastUpdated: 10/21/86 - MEMBER/any TSSO clist support/PDS     **/
00270001/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00280001CONTROL ASIS
00290001IF  &STR(&CMD) = &STR(?)  THEN DO
00300001  SET RERRSM  =
00310001  SET RERRLM  =  &STR(Syntax for all valid TSSO commands can be obtained by pressing the HELP pf key.)
00320001  SET ZERRALRM = YES
00330001  SET ZERRHM   = &STR(TSSO$ALL)
00340001  ISPEXEC VPUT (RERRSM,RERRLM,ZERRALRM,ZERRHM)
00350001  ISPEXEC SETMSG MSG(ROCZ002)
00360001  EXIT
00370001END
00380001 
00390001IF &STR(&CMD) =  THEN DO
00400001  SET RERRSM  =
00410001  SET RERRLM  =
00420001  SET ZERRALRM = YES
00430001  SET ZERRHM   =
00440001  ISPEXEC VPUT (RERRSM,RERRLM,ZERRALRM,ZERRHM)
00450001  ISPEXEC SELECT PANEL(TSSO)
00460001  EXIT
00470001END
00480001 
00490001XATST
00500001IF &LASTCC = 0 THEN   SET MVS = XA
00510001SHOWTSO CENTER
00520001SET CENTER = &SUBSTR(1:3,&CENTER)
00530001IF      &CENTER = SWC THEN SET TSSOPFX = SYS1
00540001ELSE IF &CENTER = WCC THEN SET TSSOPFX = AJES
00550001 
00560001/* STRIP OFF ANY LEADING # OR #% */
00570001SET CMDLEN = &LENGTH(&STR(&CMD))
00580001IF &CMDLEN > 1 THEN IF &SUBSTR(1:1,&STR(&CMD)) = &STR(#) THEN +
00590001  SET CMD = &SUBSTR(2:&CMDLEN,&STR(&CMD))
00600001SET CMDLEN = &LENGTH(&STR(&CMD))
00610006IF &CMDLEN > 1 THEN IF &SUBSTR(1:1,&STR(&CMD)) = &STR(%) THEN DO
00620015  SET CLIST = YES /* remember in case non-supported clist */
00630001  SET CMD = &SUBSTR(2:&CMDLEN,&STR(&CMD))
00640006END
00650001 
00660001SET CMDLEN = &LENGTH(&STR(&CMD))
00670001VSCAN CMD ' '
00680001SET BLANK = &LASTCC
00690001 
00700001SET COMMAND = &STR(&CMD)
00710001IF &CMDLEN > 0 AND &BLANK > 0 AND &BLANK < &CMDLEN THEN DO
00720001  SET COMMAND = &SUBSTR(1:&BLANK,&STR(&CMD))
00730001  SET OPERAND = &SUBSTR(&BLANK+1:&CMDLEN,&STR(&CMD))
00740014  /* strip off all leading blanks */
00750014  GETBLNK:+
00760014  SET L = &LENGTH(&STR(&OPERAND))
00770014  IF &L >= 2 THEN DO
00780016    IF &SUBSTR(1:1,&STR(&OPERAND)) = &STR( ) THEN DO
00790017      SET OPERAND = &SUBSTR(2:&L,&STR(&OPERAND))
00800016      GOTO GETBLNK
00810016    END
00820014  END
00830001END
00840014 
00850002IF &MVS ^= XA +
00860001  AND  (&STR(&COMMAND) = RQE       +
00870005   OR   &STR(&COMMAND) = INT       +
00880005   OR   &STR(&COMMAND) = LINKLIST) THEN DO
00890001  SET RERRSM = &STR(NOT valid for MVS/370)
00900001  SET RERRLM = &STR(TSSO command "&COMMAND" is ONLY available for MVS/XA systems.)
00910001  SET ZERRALRM = YES
00920001  SET ZERRHM   = &STR(TSSO$ALL)
00930001  ISPEXEC VPUT (RERRSM,RERRLM,ZERRALRM,ZERRHM)
00940001  ISPEXEC SETMSG MSG(ROCZ002)
00950001  EXIT
00960001END
00970001 
00980001IF       &STR(&COMMAND) = ACT       +
00990001      |  &STR(&COMMAND) = SYSIND    +
01000001      |  &STR(&COMMAND) = SYSDISP   +
01010001      |  &STR(&COMMAND) = FINDLPA   +
01020001      |  &STR(&COMMAND) = PAGE      +
01030001      |  &STR(&COMMAND) = RQE       +
01040001      |  &STR(&COMMAND) = RSV       +
01050001      |  &STR(&COMMAND) = SHOWSS    +
01060001      |  &STR(&COMMAND) = SPMON     +
01070001      |  &STR(&COMMAND) = LINKLIST  +
01080001      |  &STR(&COMMAND) = CORE      +
01090001      |  &STR(&COMMAND) = JOBS      +
01100001      |  &STR(&COMMAND) = ENQ       +
01110004      |  &STR(&COMMAND) = LOCATE    +
01120001      |  &STR(&COMMAND) = UCB       THEN DO
01130001  IF &COMMAND = UCB THEN SET &ADDED = &STR( STRIP(11) )
01140001  VSCAN OPERAND 'ALL'
01150001  SET HASALL = &LASTCC
01160001  IF &STR(&COMMAND) = SYSIND AND &HASALL > 0 THEN +
01170001    %TSSOECMD %#SYSIND CMDNAME(SYSIND) RNDNAME(ALL)
01180001  ELSE +
01190001    %TSSOECMD $C +
01200001           OPERAND(' LIB(''&TSSOPFX..TSSO&MVS..LOAD'') +
01210001           &COMMAND &OPERAND ')   &ADDED. +
01220001           CMDNAME(&COMMAND) RNDNAME('&OPERAND')
01230001END
01240001ELSE IF  &STR(&COMMAND) = SMAP      +
01250001      |  &STR(&COMMAND) = VOLSER    +
01260001      |  &STR(&COMMAND) = VALLOC    +
01270006      |  &STR(&COMMAND) = IO        THEN DO /* Used to xeq clists */
01280032  SET ECMDRND =
01290030  IF &STR(&OPERAND) ^=    THEN DO
01300030     IF &COMMAND = VOLSER | &COMMAND = VALLOC THEN +
01310030           SET ECMDRND = &STR( OPERAND('VOLUME(&OPERAND)') )
01320030     ELSE  SET ECMDRND = &STR( OPERAND(&OPERAND) )
01330031  END
01340001  %TSSOECMD %#&COMMAND &ECMDRND CMDNAME(&COMMAND) RNDNAME('&OPERAND')
01350001END
01360029ELSE IF  &STR(&COMMAND) = MEMBER    +
01370029      |  &STR(&COMMAND) = PDS       THEN DO /*Handle quotes on dsname*/
01380007  /* Must strip of "dsname(member)" and quotes because of clist %ECMD */
01390011  SET ECMDRND = &STR(&OPERAND    )
01400035  SET L = &LENGTH(&STR(&ECMDRND))
01410027  VSCAN ECMDRND ')'
01420033  SET ENDDSN = &LASTCC
01430033  IF &COMMAND = PDS AND &ENDDSN = 0 THEN DO
01440033    VSCAN ECMDRND ' '
01450033    SET ENDDSN = &LASTCC
01460033    IF &ENDDSN > 0 THEN SET ENDDSN = &ENDDSN - 1
01470033    IF &STR(&OPERAND) =  THEN SET ENDDSN = 0
01480033  END
01490039  IF &COMMAND = PDS AND &L >= 5 THEN DO
01500039    SET LPRN = &SUBSTR(2:2,&STR(X(X))
01510039    IF &SUBSTR(1:5,&STR(&ECMDRND)) = &STR(FILE&LPRN) THEN +
01520039      SET ENDDSN = 0
01530039  END
01540033  IF &ENDDSN ^= 0 THEN DO
01550033    /* MUST BE   DSNAME(MEMBER)  XXX XXX */
01560033    /* If PDS command and no member name, end of dsname at first blank */
01570007    /* Convert  dsname(member)  to 'dsname(member)'           */
01580033    SET DATASET = &SUBSTR(1:&ENDDSN,&STR(&ECMDRND))
01590028    SET L = &LENGTH(&STR(&ECMDRND))
01600028    SET REST  =
01610033    IF &ENDDSN < &L THEN SET REST = &SUBSTR(&ENDDSN+1:&L,&STR(&ECMDRND))
01620028    SET ECMDRND = &STR(''&DATASET'' &REST)
01630008  END
01640008  %TSSOECMD $C +
01650008            OPERAND(' LIB(''&TSSOPFX..TSSO&MVS..LOAD'') +
01660027            &COMMAND &ECMDRND ') +
01670008            CMDNAME(&COMMAND) RNDNAME('&OPERAND')
01680007END
01690001ELSE IF  &STR(&COMMAND) = OSCMD     THEN DO
01700001  IF &STR(&OPERAND) =    THEN DO
01710001    SET RERRSM = &STR(OSCMD requires operand!)
01720001    SET RERRLM = &STR(The OSCMD command of TSSO requires an MVS operator system command.)
01730001    SET ZERRALRM = YES
01740001    SET ZERRHM   = &STR(TSSO$OSC)
01750001    ISPEXEC VPUT (RERRSM,RERRLM,ZERRALRM,ZERRHM)
01760001    ISPEXEC SETMSG MSG(ROCZ002)
01770001    EXIT
01780001  END
01790001  ELSE SET ECMDRND = &STR( OPERAND('MVSCMD(''&OPERAND'')') )
01800001  %TSSOECMD %#&COMMAND &ECMDRND CMDNAME(&COMMAND) RNDNAME('&OPERAND')
01810001END
01820006ELSE IF  &STR(&COMMAND) = CACHE     THEN DO /* Cmd invokes browse */
01830001  IF &STR(&OPERAND) =    THEN DO
01840001    SET RERRSM = &STR(CACHE requires operand!)
01850001    SET RERRLM = &STR(The CACHE command of TSSO requires a UCBaddress or Volser.)
01860001    SET ZERRALRM = YES
01870001    SET ZERRHM   = &STR(TSSO$CAC)
01880001    ISPEXEC VPUT (RERRSM,RERRLM,ZERRALRM,ZERRHM)
01890001    ISPEXEC SETMSG MSG(ROCZ002)
01900001    EXIT
01910001  END
01920001  SET L = &LENGTH(&OPERAND)
01930001  VSCAN OPERAND ' '
01940001  SET BLANK = &LASTCC
01950001  IF (&L > 0) AND (&BLANK > 0) AND (&BLANK < &L) THEN DO
01960001    SET UCBORVOL = &SUBSTR(1:&BLANK,&STR(&OPERAND))
01970001    SET CACHRAND = &SUBSTR(&BLANK+1:&L,&STR(&OPERAND))
01980001    SET ECMDRND  = &STR( OPERAND('&UCBORVOL OPERAND(''&CACHRAND'')') )
01990001  END
02000001  ELSE DO
02010001    SET UCBORVOL = &STR(&OPERAND)
02020001    SET ECMDRND  = &STR( OPERAND(&UCBORVOL) )
02030001  END
02040001  /* Set to "NOSHOW" since %#CACHE invokes BRowse on IDCAMS SYSPRINT */
02050001  %TSSOECMD %#CACHE &ECMDRND CMDNAME(CACHE) RNDNAME('&OPERAND') NOSHOW
02060001END
02070005ELSE IF  &STR(&COMMAND) = TIOT      +
02080005      |  &STR(&COMMAND) = INT       THEN DO /* Authorized TSSO cmds */
02090004  IF &STR(&OPERAND) ^=    THEN SET ECMDRND = &STR( OPERAND(&OPERAND) )
02100004  %TSSOECMD @C +
02110004           OPERAND(' LIB(''&TSSOPFX..TSSO&MVS..LOAD'') +
02120004           &COMMAND &OPERAND ')   &ADDED. +
02130004           CMDNAME(&COMMAND) RNDNAME('&OPERAND')
02140004END
02150005ELSE DO /* Just execute anything else so sysprgmrs can test new cmds */
02160003  IF &STR(&OPERAND) ^=    THEN SET ECMDRND = &STR( OPERAND(&OPERAND) )
02170006  IF &CLIST = YES THEN DO
02180021    SET COMMAND = &COMMAND   /* STRIP OFF TRAILING BLANKS */
02190006    IF &SYSDSN('&TSSOPFX..TSSO.CLIST(&COMMAND)') = OK THEN DO
02200024      %TSSOECMD EX OPERAND(' ''&TSSOPFX..TSSO.CLIST(&COMMAND)'' ''&OPERAND'' ') +
02210023                CMDNAME(&COMMAND) RNDNAME('&OPERAND')
02220006    END
02230006    ELSE DO
02240023      /* Execute out of SYSPROC */
02250023      %TSSOECMD %&COMMAND OPERAND('&OPERAND')
02260006    END
02270025/*  IF &CLISTRC > 0 THEN DO
02280025/*    SET RERRSM = &STR(Invalid TSSO Clist!)
02290025/*    SET RERRLM = &STR(Clist didn't exist or had a Non-Zero Return Code. RC=&CLISTRC)
02300025/*    SET ZERRALRM = YES
02310025/*    SET ZERRHM   = &STR(TSSO$ALL)
02320025/*    ISPEXEC VPUT (RERRSM,RERRLM,ZERRALRM,ZERRHM)
02330025/*    ISPEXEC SETMSG MSG(ROCZ002)
02340025/*    EXIT
02350025/*  END
02360006  END
02370006  ELSE  %TSSOECMD $C +
02380006             OPERAND(' LIB(''&TSSOPFX..TSSO&MVS..LOAD'') +
02390006             &COMMAND &OPERAND ')   &ADDED. +
02400006             CMDNAME(&COMMAND) RNDNAME('&OPERAND')
02410003END
02420005/*ELSE DO /* Use this for last else/do to indicated invalid cmd */
02430003/*  SET RERRSM = &STR(Invalid TSSO command!)
02440003/*  SET RERRLM = &STR(Press HELP pf key for TSSO command syntax and tutorial.)
02450003/*  SET ZERRALRM = YES
02460003/*  SET ZERRHM   = &STR(TSSO$ALL)
02470003/*  ISPEXEC VPUT (RERRSM,RERRLM,ZERRALRM,ZERRHM)
02480003/*  ISPEXEC SETMSG MSG(ROCZ002)
02490003/*  EXIT
02500003/*END
