00010000         PROC 0   MSG()   /* M. THEYS ADDED FOR ISPF CMD: MCH */ +
00020000                  DEBUG MAINT +
00030000                  TBDSN(ATSO.SYS.TABLES) +
00040005                  MSGDSN(ATSO.MESSAGES.TEXT)
00050000 
00060000         CONTROL NOFLUSH ASIS
00070007         IF &DEBUG ^= DEBUG THEN CONTROL NOLIST NOMSG
00080000         ELSE CONTROL LIST PROMPT CONLIST SYMLIST MSG
00090000         ISPEXEC CONTROL ERRORS RETURN
00100000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00110000/*  DILAOG DRIVER FOR MESSAGE HELP FACILITY                         */
00120000/*                                                                  */
00130000/* ORIGINAL BY:  ANDERSON GW          CREATED: 11/19/86             */
00140000/* MODIFIED BY:  THEYS    ME          UPDATED: 01/10/87             */
00150000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00160000        ALLOC DS('&TBDSN') F(STAB) SHR REU
00170000        SET &RC = &LASTCC
00180000        SET &DDID =
00190000        SHOWTSO CENTER
00200000        IF &CENTER = WCC1 THEN SET MSGDSN = &STR(ATSO.MSGHLP.DATA)
00201008        IF &CENTER = WCC2 THEN SET MSGDSN = &STR(ATSO.MSGHLP.DATA)
00210000        ISPEXEC LMINIT DATAID(DDID) DATASET('&MSGDSN')
00220000        SET &RC = &LASTCC
00230000        ISPEXEC LMOPEN DATAID(&DDID)
00240000        SET &RC = &LASTCC
00250000        ISPEXEC TBEND MSGHLP
00260000        SET &RC = &LASTCC
00270000        SET &OPCNT = 0
00280000REOPNMSG: +
00290000        ISPEXEC TBOPEN MSGHLP LIBRARY(STAB) NOWRITE
00300000        SET &TBOPRC = &LASTCC
00310007        IF &TBOPRC ^= 0 THEN DO
00320000           IF &TBOPRC = 12 THEN DO
00330000             IF &OPCNT < 2 THEN DO
00340000              WAIT5
00350000              SET &OPCNT = &OPCNT + 1
00360000              GOTO REOPNMSG
00370000             END DO
00380000           END DO
00390000           WRITE ERROR IN TBOPEN OF MSGHLP TABLE
00400000           ISPEXEC TBOPEN XXXX LIBRARY(XXXXX)
00410000           ISPEXEC LMCLOSE DATAID(&DDID)
00420000           ISPEXEC LMFREE DATAID(&DDID)
00430000           FREE F(STAB,MSGFIL)
00440000           EXIT
00450000        END DO
00460000        SET &OPCNT = 0
00470000        IF &STR(&MSG) ^= THEN DO
00480000             SET MSGNUM = &STR(&MSG)                          /* MCH */
00490001             SET ABNMSG = &STR(&MSG)                          /* MCH */
00500001             SET MCTYP  = MESSAGE
00510000        END
00520000REDISP: +
00530000        ISPEXEC TBVCLEAR MSGHLP
00540007        IF &STR(&OMEM) ^= THEN SET &MSGMEM = &STR(&OMEM)
00550007        IF &STR(&ONUM) ^= THEN SET &MSGNUM = &STR(&ONUM)
00560007        IF &STR(&OTXT) ^= THEN SET &MSGTXT = &STR(&OTXT)
00570000        IF &STR(&MSG) ^= THEN DO
00580000             SET MSGNUM = &STR(&MSG)                          /* MCH */
00590000             GOTO NONDISPL
00600000        END
00610002        ISPEXEC DISPLAY PANEL(MCHELP2)
00620007        IF &LASTCC ^= 0 THEN DO
00630000            ISPEXEC TBEND MSGHLP
00640000            ISPEXEC TBOPEN XXXX LIBRARY(XXXXX)
00650000            ISPEXEC TBOPEN XXXX LIBRARY(XXXXX)
00660000            ISPEXEC LMCLOSE DATAID(&DDID)
00670000            ISPEXEC LMFREE DATAID(&DDID)
00680000            FREE F(STAB,MSGFIL)
00690000            EXIT
00700000        END
00710007        IF &STR(&ABNMSG) ^=  THEN SET MSGNUM = &STR(&ABNMSG)
00720000NONDISPL:+
00730000        SET &CAPS =
00740007        IF &STR(&MSGMEM) ^= THEN SET &OMEM = &STR(&MSGMEM)
00750000        ELSE SET &OMEM =
00760007        IF &STR(&MSGNUM) ^= THEN SET &ONUM = &STR(&MSGNUM)
00770000        ELSE SET &ONUM =
00780007        IF &STR(&MSGTXT) ^= THEN SET &OTXT = &STR(&MSGTXT)
00790000        ELSE SET &OTXT =
00800000        SET &SVAR =
00810007        IF &STR(&MSGMEM) ^= THEN SET &SVAR = &STR(&SVAR MSGMEM)
00820007        IF &STR(&MSGNUM) ^= THEN SET &SVAR = &STR(&SVAR MSGNUM)
00830007        IF &STR(&MSGTXT) ^= THEN SET &SVAR = &STR(&SVAR MSGTXT)
00840000        IF &STR(&SVAR) = THEN DO
00850000           SET MSG =    /* MCH */
00860000           GOTO REDISP
00870000        END
00880000        ISPEXEC TBCREATE TEMP NAMES(MSGNUM,MSGMEM,MSGTXT) NOWRITE REPLACE
00890000        SET &RC = &LASTCC
00900000        ISPEXEC TBTOP MSGHLP
00910000        SET &RC = &LASTCC
00920000GETLOOP: +
00930000        IF &GFLG ^= THEN DO
00940000             ISPEXEC TBVCLEAR MSGHLP
00950007             IF &STR(&OMEM) ^= THEN SET &MSGMEM = &STR(&OMEM)
00960000             ELSE SET &MSGMEM =
00970007             IF &STR(&ONUM) ^= THEN SET &MSGNUM = &STR(&ONUM)
00980000             ELSE SET &MSGNUM =
00990007             IF &STR(&OTXT) ^= THEN SET &MSGTXT = &STR(&OTXT)
01000000             ELSE SET &MSGTXT =
01010000        END DO
01020000DOSCAN: ISPEXEC TBSCAN MSGHLP ARGLIST(&SVAR)
01030007        IF &LASTCC ^= 0 THEN DO
01040000            IF &GFLG = THEN DO
01050000               IF &CAPS =  THEN DO
01060000                 SET &CAPS = YES
01070000                 SET &MSGNUM = &STR(&SYSCAPS(&MSGNUM))
01080000                 SET &ONUM = &STR(&SYSCAPS(&MSGNUM))
01090000                 GOTO DOSCAN
01100000               END DO
01110000               SET &SHORT = &STR(NO HITS)
01120000               SET &LONG = &STR(NO ENTRIES FOUND FOR ARGUMENTS ENTERED)
01130000               ISPEXEC SETMSG MSG(SYS010)
01140000               SET MSG =    /* MCH */
01150000               GOTO REDISP
01160000            END
01170000            GOTO DODSP
01180000        END
01190000        ISPEXEC TBADD TEMP
01200000        SET &OHMEM = &NRSTR(&MSGMEM)
01210000        IF &GFLG = THEN SET &GFLG = YES
01220000        ELSE IF &GFLG ^= THEN DO
01230000           IF &DATATYPE(&STR(&GFLG)) = NUM THEN SET &GFLG = &GFLG + 1
01240000           ELSE SET &GFLG = 1
01250000        END DO
01260000        GOTO GETLOOP
01270000DODSP: +
01280000        IF &GFLG = YES THEN DO
01290000             SET &MSGMEM = &STR(&OHMEM)
01300000             GOTO BRLOOP
01310000        END DO
01320000        ISPEXEC TBSORT TEMP FIELDS(MSGMEM,C,A)
01330000        SET &RC = &LASTCC
01340000        ISPEXEC TBTOP TEMP
01350000TBREDSP: +
01360000        SET &SEL =
01370002        ISPEXEC  TBDISPL TEMP PANEL(MCHELP4)
01380000        SET &DRC = &LASTCC
01390003        SET ABNMSG = &STR(&MSGTXT)
01400000        IF &DRC > 4 THEN DO
01410000BRLOPE: +
01420000             ISPEXEC TBEND TEMP
01430000             SET &GFLG =
01440000             SET &OHMEM =
01450000             IF &STR(&MSG) ^= THEN EXIT  /* MCH */
01460006             SET MSG =
01470000             GOTO REDISP
01480000        END DO
01490000BRLOOP: +
01500000        SET &NOT = &STR(&MSGMEM)
01510000        ISPEXEC LMMFIND DATAID(&DDID) MEMBER(&MSGMEM)
01520007        IF &LASTCC ^= 0 THEN DO
01530000           SET &SHORT = &STR(MEMBER ERROR)
01540000           SET &LONG = &STR(MESSAGE MEMBER FOR &MSGNUM NOT FOUND)
01550000           ISPEXEC SETMSG MSG(SYS010)
01560000           SET MSG =    /* MCH */
01570000           GOTO REDISP
01580000        END DO
01590000        ISPEXEC CONTROL DISPLAY SAVE
01600002        ISPEXEC BROWSE DATAID(&DDID) MEMBER(&MSGMEM)  PANEL(MCHELP3)
01610000        ISPEXEC CONTROL DISPLAY RESTORE
01620000        IF &GFLG = YES THEN GOTO BRLOPE
01630000        IF &DRC = 4 THEN DO
01640000            ISPEXEC  TBDISPL TEMP
01650000            SET &DRC = &LASTCC
01660000            GOTO BRLOOP
01670000            SET &SEL =
01680000        END
01690000        GOTO TBREDSP
