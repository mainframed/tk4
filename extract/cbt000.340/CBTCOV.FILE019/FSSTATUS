00010001PROC 0 JOBS()
00020000/** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** ** ** **/
00030000/** Used by ROCKWELL ISPF primary command: STATUS          **/
00040000/** BY: Michael E. Theys   Rockwell International/SWCC     **/
00050000/** -- -- -- -- -- -- -- -- -*- -- -- -- -- -- -- -- -- -- **/
00060000/** Full-Screen display of STATUS command:                 **/
00070000/**   SELECT CMD(%FSSTATUS JOBS('&ZPARM')                  **/
00080000/** %ECMD: traps a commands output for ISPF Browse display **/
00090000/** -- -- -- -- -- -- -- -- -*- -- -- -- -- -- -- -- -- -- **/
00100000/** "STATUS" command w/o jobs displays a panel for entry.  **/
00110000/** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** ** ** **/
00120000 
00130000IF &DEBUG = DEBUG THEN  CONTROL   MSG   LIST   CONLIST   SYMLIST
00140000ELSE                    CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST
00150000 
00160007/* If JOBS was "*" just invoke using previous saved jobslist */
00170009/* If JOBS was "?" show tutorial on STATUS ISPF Command      */
00180009/* If JOBS was "!" do NOT invoke.  Allow for Mode change.    */
00190014/* If JOBS was "^" erase saved joblist and invoke STATUS.    */
00200007IF      &STR(&JOBS)  = &STR(*) THEN +
00210007  ISPEXEC VGET (FSSTJOBS) PROFILE
00220008ELSE IF &STR(&JOBS)  = &STR(^) THEN DO
00230008  SET FSSTJOBS =
00240004  ISPEXEC VPUT (FSSTJOBS) PROFILE
00250000END
00260009ELSE IF &STR(&JOBS)  = &STR(?) THEN DO
00270009  ISPEXEC SELECT PGM(ISPTUTOR) PARM($STATUS)
00280009  EXIT
00290009END
00300009ELSE IF &STR(&JOBS)  = &STR(!) THEN DO
00310014  /* Do nothing now */
00320009END
00330008ELSE IF &STR(&JOBS) ^=         THEN DO
00340008  SET FSSTJOBS = &STR(&JOBS)
00350008  ISPEXEC VPUT (FSSTJOBS) PROFILE
00360008END
00370000ELSE DO
00380008  ISPEXEC VGET (FSSTJOBS) PROFILE
00390008  SET FSSTSAVE = &STR(&FSSTJOBS)
00400000  SET FSSTJOBS =
00410013  ISPEXEC VPUT (FSSTJOBS) PROFILE
00420000END
00430007 
00440006IF &STR(&JOBS) = &STR(*) AND &STR(&FSSTJOBS) =  THEN DO
00450006  SET SHORT = &STR(NO SAVED JOBS)
00460006  SET LONG  = &STR(Enter a jobname, list of jobs, on nothing for JES JOB STATUS)
00470006  ISPEXEC SETMSG MSG(SYS010)
00480006END
00490012ELSE IF &STR(&JOBS) = &STR(!) THEN /* NO AUTO ENTER */
00500010ELSE DO
00510010 ISPEXEC VGET (FSSTMODE) PROFILE
00520012 IF &STR(&FSSTMODE) = TSO THEN DO
00530010   %FSSTTSO ATLINE(3)
00540011   EXIT
00550010 END
00560012 ELSE ISPEXEC CONTROL NONDISPL ENTER
00570010END
00580006 
00590002ISPEXEC SELECT  PANEL(FSSTATUS)
00600008 
00610008IF &STR(&FSSTSAVE) ^=   THEN DO
00620008  SET FSSTJOBS = &STR(&FSSTSAVE)
00630008  ISPEXEC VPUT (FSSTJOBS) PROFILE
00640008END
