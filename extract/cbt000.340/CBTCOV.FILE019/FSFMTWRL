00010000PROC 0 OPT() NUMBER() DEBUG
00020000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00030000/**                                                                **/
00040000/**      DISPLAY "WORKING" SYMBOLS: TWRILL,BLINK,SNAIL,ETC..       **/
00050000/**                                                                **/
00060000/** MICHAEL E. THEYS                                               **/
00070000/** INFORMATION SYSTEMS CENTER                                     **/
00080000/** SOUTHWESTERN COMPUTING CENTER                                  **/
00090000/** ROCKWELL INTERNATIONAL CORPORATION                             **/
00100000/** RICHARDSON, TEXAS                                              **/
00110000/** CASNET 437-2860    M/S 401-109                                 **/
00120000/**                                                                **/
00130000/** == == == == == == == == == == == == == == == == == == == == == **/
00140000/** USED IN FULL-SCREEN FIND MEMBER IN SYSTEM LIBRARY DIALOG.      **/
00150000/** == == == == == == == == == == == == == == == == == == == == == **/
00160000/**                                                                **/
00170000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00180000CONTROL ASIS
00190000IF &DEBUG = DEBUG THEN  CONTROL   MSG PROMPT LIST CONLIST SYMLIST
00200000ELSE                    CONTROL NOMSG
00210000 
00220000 
00230000/** INITIALIZE THE "WORKING" SYMBOLS **/
00240000IF &OPT = INIT  THEN DO
00250000  IF &NUMBER =   THEN DO
00260000    ISPEXEC VGET (FSFMTWRL) PROFILE
00270000    IF &FSFMTWRL =  THEN SET FSFMTWRL = 0
00280000    SET &FSFMTWRL = &FSFMTWRL + 1
00290000    SET &TWRILLN = &EVAL((&FSFMTWRL//15)+1)
00300000    ISPEXEC VPUT (FSFMTWRL) PROFILE
00310000    ISPEXEC VPUT (TWRILLN)
00320000  END
00330000  ELSE SET &TWRILLN = &NUMBER
00340000  /* SET TWRILL "WORKING" SYMBOLS */
00350000  IF      &TWRILLN = 1 THEN SET &TWRILL = &STR( (\) )   /* TWRILL    */
00360000  ELSE IF &TWRILLN = 2 THEN SET &TWRILL = &STR(  .* )   /* TOGGLE    */
00370000  ELSE IF &TWRILLN = 3 THEN SET &TWRILL = &STR(~)       /* SNAIL     */
00380000  ELSE IF &TWRILLN = 4 THEN SET &TWRILL = &STR(  X  )   /* BLINK     */
00390000  ELSE IF &TWRILLN = 5 THEN SET &TWRILL = &STR(_____)   /* FALLDOWN  */
00400000  ELSE IF &TWRILLN = 6 THEN SET &TWRILL = &STR( )       /* WORKING   */
00410000  ELSE IF &TWRILLN = 7 THEN SET &TWRILL = &STR(*)       /* SNAKE     */
00420000  ELSE IF &TWRILLN = 8 THEN SET &TWRILL = &STR(  /  )   /*TETER/TOTER*/
00430000  ELSE IF &TWRILLN = 9 THEN SET &TWRILL = &STR(=-->)    /* ARROW     */
00440000  ELSE IF &TWRILLN =10 THEN SET &TWRILL = &STR(WORKING!)/* SMALLWORK */
00450000  ELSE IF &TWRILLN =11 THEN SET &TWRILL = &STR( < > )   /* BLINK 2   */
00460000  ELSE IF &TWRILLN =12 THEN SET &TWRILL = &STR( <_> )   /* WINK      */
00470000  ELSE IF &TWRILLN =13 THEN SET &TWRILL = &STR(<    )   /* BACK/FORTH*/
00480000  ELSE IF &TWRILLN =14 THEN SET &TWRILL = &STR(!O.O!)   /* FACE      */
00490000  ELSE IF &TWRILLN =15 THEN SET &TWRILL = &STR(  O  )   /*bigOLITTLEo*/
00500000  ISPEXEC VPUT (TWRILL)
00510000  EXIT
00520000END  /** OF "INITIALIZE SECTION" **/
00530000 
00540000/** GENERATE THE SETS OF "WORK IN PROGRESS" SYMBOLS **/
00550000IF &OPT = WORK THEN DO
00560000  ISPEXEC VGET (TWRILL)
00570000  IF &NUMBER =  THEN  ISPEXEC VGET (TWRILLN)
00580000  ELSE                SET TWRILLN = &NUMBER
00590000  IF      &TWRILLN = 1 THEN DO
00600000    IF      &STR(&TWRILL) = &STR( (!) ) THEN SET &TWRILL = &STR( (/) )
00610000    ELSE IF &STR(&TWRILL) = &STR( (/) ) THEN SET &TWRILL = &STR( (-) )
00620000    ELSE IF &STR(&TWRILL) = &STR( (-) ) THEN SET &TWRILL = &STR( (\) )
00630000    ELSE IF &STR(&TWRILL) = &STR( (\) ) THEN SET &TWRILL = &STR( (!) )
00640000  END
00650000  ELSE IF &TWRILLN = 2 THEN DO
00660000    IF      &STR(&TWRILL) = &STR(  .* ) THEN SET &TWRILL = &STR( *.  )
00670000    ELSE IF &STR(&TWRILL) = &STR( *.  ) THEN SET &TWRILL = &STR(  .* )
00680000  END
00690000  ELSE IF &TWRILLN = 3 THEN DO
00700000    IF &LENGTH(&STR(&TWRILL)) <  25 THEN SET &TWRILL = &STR( &TWRILL)
00710000    ELSE                                 SET &TWRILL = &STR(~)
00720000  END
00730000  ELSE IF &TWRILLN = 4 THEN DO
00740000    IF      &STR(&TWRILL) = &STR(  X  ) THEN SET &TWRILL = &STR(  +  )
00750000    ELSE IF &STR(&TWRILL) = &STR(  +  ) THEN SET &TWRILL = &STR(  X  )
00760000  END
00770000  ELSE IF &TWRILLN = 5 THEN DO
00780000    IF      &STR(&TWRILL) = &STR(|||||) THEN SET &TWRILL = &STR(_____)
00790000    ELSE IF &STR(&TWRILL) = &STR(_____) THEN SET &TWRILL = &STR(|||||)
00800000  END
00810000  ELSE IF &TWRILLN = 6 THEN DO
00820000    IF      &STR(&TWRILL) = &STR( )      THEN SET &TWRILL = &STR(W)
00830000    ELSE IF &STR(&TWRILL) = &STR(W)      THEN SET &TWRILL = &STR(WO)
00840000    ELSE IF &STR(&TWRILL) = &STR(WO)     THEN SET &TWRILL = &STR(WOR)
00850000    ELSE IF &STR(&TWRILL) = &STR(WOR)    THEN SET &TWRILL = &STR(WORK)
00860000    ELSE IF &STR(&TWRILL) = &STR(WORK)   THEN SET &TWRILL = &STR(WORKI)
00870000    ELSE IF &STR(&TWRILL) = &STR(WORKI)  THEN SET &TWRILL = &STR(WORKIN)
00880000    ELSE IF &STR(&TWRILL) = &STR(WORKIN) THEN SET &TWRILL = &STR(WORKING)
00890000    ELSE DO
00900000      IF &LENGTH(&STR(&TWRILL)) < 25  THEN SET &TWRILL = &STR(&TWRILL..)
00910000      ELSE                                 SET &TWRILL = &STR(W)
00920000    END
00930000  END
00940000  ELSE IF &TWRILLN = 7 THEN DO
00950000    IF &LENGTH(&STR(&TWRILL)) <  25 THEN SET &TWRILL = &STR(&TWRILL.&SUBSTR(1:1,&STR(&TWRILL)))
00960000    ELSE                                 SET &TWRILL = &STR(@)
00970000  END
00980000  ELSE IF &TWRILLN = 8 THEN DO
00990000    IF      &STR(&TWRILL) = &STR(  /  ) THEN SET &TWRILL = &STR(  \  )
01000000    ELSE IF &STR(&TWRILL) = &STR(  \  ) THEN SET &TWRILL = &STR(  /  )
01010000  END
01020000  ELSE IF &TWRILLN = 9 THEN DO
01030000    IF &LENGTH(&STR(&TWRILL)) <= 25 THEN SET &TWRILL = &STR( &TWRILL)
01040000    ELSE                                 SET &TWRILL = &STR(=-->)
01050000  END
01060000  ELSE IF &TWRILLN =10 THEN DO
01070000    IF      &STR(&TWRILL) = &STR(WORKING!) THEN SET &TWRILL = &STR(working.)
01080000    ELSE IF &STR(&TWRILL) = &STR(working.) THEN SET &TWRILL = &STR(WORKING!)
01090000  END
01100000  ELSE IF &TWRILLN =11 THEN DO
01110000    IF      &STR(&TWRILL) = &STR( < > ) THEN SET &TWRILL = &STR( <*> )
01120000    ELSE IF &STR(&TWRILL) = &STR( <*> ) THEN SET &TWRILL = &STR( < > )
01130000  END
01140000  ELSE IF &TWRILLN =12 THEN DO
01150000    IF      &STR(&TWRILL) = &STR( <_> ) THEN SET &TWRILL = &STR( <0> )
01160000    ELSE IF &STR(&TWRILL) = &STR( <0> ) THEN SET &TWRILL = &STR( <_> )
01170000  END
01180000  ELSE IF &TWRILLN =13 THEN DO
01190000    IF      &STR(&TWRILL) = &STR(<    ) THEN SET &TWRILL = &STR(>    )
01200000    ELSE IF &STR(&TWRILL) = &STR(>    ) THEN SET &TWRILL = &STR( >   )
01210000    ELSE IF &STR(&TWRILL) = &STR( >   ) THEN SET &TWRILL = &STR(  >  )
01220000    ELSE IF &STR(&TWRILL) = &STR(  >  ) THEN SET &TWRILL = &STR(   > )
01230000    ELSE IF &STR(&TWRILL) = &STR(   > ) THEN SET &TWRILL = &STR(    >)
01240000    ELSE IF &STR(&TWRILL) = &STR(    >) THEN SET &TWRILL = &STR(    <)
01250000    ELSE IF &STR(&TWRILL) = &STR(    <) THEN SET &TWRILL = &STR(   < )
01260000    ELSE IF &STR(&TWRILL) = &STR(   < ) THEN SET &TWRILL = &STR(  <  )
01270000    ELSE IF &STR(&TWRILL) = &STR(  <  ) THEN SET &TWRILL = &STR( <   )
01280000    ELSE IF &STR(&TWRILL) = &STR( <   ) THEN SET &TWRILL = &STR(<    )
01290000  END
01300000  ELSE IF &TWRILLN =14 THEN DO
01310000    IF      &STR(&TWRILL) = &STR(!O.O!) THEN SET &TWRILL = &STR(!o.o!)
01320000    ELSE IF &STR(&TWRILL) = &STR(!o.o!) THEN SET &TWRILL = &STR(!O.O!)
01330000  END
01340000  ELSE IF &TWRILLN =15 THEN DO
01350000    IF      &STR(&TWRILL) = &STR(  O  ) THEN SET &TWRILL = &STR(  o  )
01360000    ELSE IF &STR(&TWRILL) = &STR(  o  ) THEN SET &TWRILL = &STR(  O  )
01370000  END
01380000  ISPEXEC VPUT (TWRILL)
01390000  EXIT
01400000END /** OF "WORK SECTION" **/
01410000 
01420000/** SET "STOPPED" TWRILL SYMBOL **/
01430000IF &OPT = STOP THEN DO
01440000  ISPEXEC VGET (TWRILL)
01450000  IF &NUMBER =  THEN ISPEXEC VGET (TWRILLN)
01460000  ELSE               SET TWRILLN = &NUMBER
01470000  IF      &TWRILLN = 1 THEN SET &TWRILL = &STR( (*) )
01480000  ELSE IF &TWRILLN = 2 THEN SET &TWRILL = &STR( .*. )
01490000  ELSE IF &TWRILLN = 3 THEN SET &TWRILL = &STR( {~} )
01500000  ELSE IF &TWRILLN = 4 THEN SET &TWRILL = &STR( -X- )
01510000  ELSE IF &TWRILLN = 5 THEN SET &TWRILL = &STR(/_|_\)
01520000  ELSE IF &TWRILLN = 6 THEN SET &TWRILL = &STR(WORKING AND DONE!)
01530000  ELSE IF &TWRILLN = 7 THEN SET &TWRILL = &STR(&TWRILL.!)
01540000  ELSE IF &TWRILLN = 8 THEN DO
01550000    IF      &STR(&TWRILL) = &STR(  /  ) THEN SET &TWRILL = &STR(  \. )
01560000    ELSE IF &STR(&TWRILL) = &STR(  \  ) THEN SET &TWRILL = &STR( ./  )
01570000  END
01580000  ELSE IF &TWRILLN = 9 THEN SET &TWRILL = &STR( &TWRILL.X)
01590000  ELSE IF &TWRILLN =10 THEN SET &TWRILL = &STR(WoRkInG?)
01600000  ELSE IF &TWRILLN =11 THEN SET &TWRILL = &STR( END )
01610000  ELSE IF &TWRILLN =12 THEN SET &TWRILL = &STR( <@> )
01620000  ELSE IF &TWRILLN =13 THEN SET &TWRILL = &STR(>>X<<)
01630000  ELSE IF &TWRILLN =14 THEN DO
01640000    IF      &STR(&TWRILL) = &STR(!O.O!) THEN SET &TWRILL = &STR(!o.o!)
01650000    ELSE IF &STR(&TWRILL) = &STR(!o.o!) THEN SET &TWRILL = &STR(!O.O!)
01660000  END
01670000  ELSE IF &TWRILLN =15 THEN DO
01680000    IF      &STR(&TWRILL) = &STR(  O  ) THEN SET &TWRILL = &STR(-:o:-)
01690000    ELSE IF &STR(&TWRILL) = &STR(  o  ) THEN SET &TWRILL = &STR(-:O:-)
01700000  END
01710000  ISPEXEC VPUT (TWRILL)
01720000  EXIT
01730000END /** OF "STOP SECTION" **/
01740000 
01750000EXIT
