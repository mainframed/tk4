00010000 PROC 0 USERS()
00020000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00030016/**                    CBTALK                                   **/
00040000/**  THIS IS AN EXTENDED VERSION OF SEND.                       **/
00050000/**  IF YOU ARE CARRYING ON A LONG CONVERSATION WITH SOMEONE,   **/
00060000/**  OR IF YOU ARE COMMUNICATING WITH MULTIPLE USERS,           **/
00070000/**  THIS WILL REDUCE THE NUMBER OF KEYSTROKES (AND ERRORS).    **/
00080012/**  HANDLES AMPERSANDS AND APOSTHERPHES IN SEND MESSAGE TEXT.  **/
00090012/**  BY: MIKE THEYS          ROCKWELL/SWCC                      **/
00100000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00110000CLRSCRN
00120000IF &STR(&USERS) =    THEN DO
00130019ASK:+
00140000  WRITENR Enter User(s): ==>
00150000  READ
00160019  IF &STR(&SYSDVAL)  =   THEN GOTO ASK
00170021  SET    IDS = &STR(&SYSDVAL)
00180000END
00190019ELSE SET IDS = &STR(&USERS)
00200021SET U = &STR(USER(&IDS),LOGON)
00210016SET BLANKS = &STR(                                                                      )
00220016 
00230000/* SEND without LOGON so user can see if they are on TSO now. */
00240018SET DT = &STR(&&SYSDATE &&SYSTIME)
00250019CONTROL ASIS
00260022SE '&STR(CBTALK: to &IDS.. On: &DT.. From:)',USER(&IDS)
00270024WRITE Enter a Message to send or a null line for answer back.  Enter  EXIT  to quit.|
00280019 
00290000LOOP: +
00300007WRITENR *==MSG==>
00310000READ
00320020SET TALKMSG = &STR(&SYSNSUB(1,&SYSDVAL))
00330022IF &STR(&SYSCAPS(&SYSNSUB(1,&TALKMSG))) = EXIT +
00340022 | &STR(&SYSCAPS(&SYSNSUB(1,&TALKMSG))) = QUIT THEN DO
00350000  SET DT = &STR(&&SYSDATE &&SYSTIME)
00360022  SE '&STR(Over and out!     CBTALK Ended: &DT..     From TSO userid:)',&U
00370000  EXIT
00380000END
00390018IF &STR(&SYSNSUB(1,&TALKMSG)) =   THEN GOTO LOOP
00400000 
00410012/* MAKE SURE MESSAGE DOESN'T HAVE 'S. CONVERT TO TWO 'S */
00420018SET S  = &STR(&SYSNSUB(1,&TALKMSG))
00430018SET V  =
00440018SET #Q = 0
00450018SET Q  = 999
00460018SET K  = 1
00470018SET L  = &LENGTH(&STR(&SYSNSUB(1,&S)))
00480018DO WHILE (&K <= &L AND &Q ^= 0)
00490018  SET Q = &SYSINDEX(',&STR(&SYSNSUB(1,&S)),&K)
00500018  IF &Q > 0 THEN DO
00510018    SET   #Q = &#Q + 1
00520018    SET    V = &STR(&SYSNSUB(1,&V)&SUBSTR(&K:&Q,&STR(&SYSNSUB(1,&S)))')
00530018  END
00540018  ELSE SET V = &STR(&SYSNSUB(1,&V)&SUBSTR(&K:&L,&STR(&SYSNSUB(1,&S))))
00550018  SET K = &Q + 1
00560018END
00570019SET TALKMSG = &SUBSTR(1:69+&#Q,&STR(&SYSNSUB(1,&V&BLANKS&BLANKS)))
00580018SE '&STR(&SYSNSUB(1,&TALKMSG):)',&U
00590000 
00600000GOTO LOOP
