00010007PROC 0 DEBUG NOSUB
00020012 
00030012/**********************************************************************/
00040012/* THIS CLIST IS DESIGNED TO READ THE ADVANCED FUNCTION PRINTING FONT */
00050012/* TABLE AND PERFORM THE FOLLOWING FUNCTION:                          */
00060012/*                                                                    */
00070012/*  1)  CREATE A TABLE OF CONTENTS LIST OF ALL FONTS LESS THAN 37     */
00080012/*      POINTS IN SIZE.                                               */
00090012/*  2)  GENERATE AND SUBMIT THE NECESSARY JCL AND OGL STATEMENTS      */
00100012/*      TO PRODUCE A MAP OF EVERY FONT SELECTED IN (1)                */
00110012/*                                                                    */
00120012/*  OPTIONS:  DEBUG  -  TURN ON CLIST TRACING FOR DIAGNOSTIC PURPOSES */
00130012/*            NOSUB  -  CREATE TABLE OF CONTENTS BUT DO NOT SUBMIT    */
00140012/*                      THE JOBS TO MAP THE FONTS                     */
00150012/*                                                                    */
00160012/*  NOTE:  THE SUBMITTED JOBS ARE STRUCTURED TO CONTAIN 50 OGL        */
00170012/*         STEPS                                                      */
00180012/**********************************************************************/
00190012 
00200006IF &DEBUG = DEBUG +
00210006  THEN CONTROL LIST CONLIST SYMLIST NOFLUSH PROMPT MSG
00220006  ELSE CONTROL NOLIST NOCONLIST NOSYMLIST FLUSH NOPROMPT NOMSG
00230000 
00240000ISPEXEC LIBDEF FTABLE DATASET ID('AOST.ISPF.TABLES')
00250001 
00260001DEL FONTMAPS.CNTL
00270002DEL FONTMAP.CONTENTS
00280002 
00290006ALLOC F(CONTENTS) DS(FONTMAP.CONTENTS) NEW +
00300006      SPACE(10,10) TRACK RECFM(F B) LRECL(80) BLKSIZE(23440)
00310006 
00320006ALLOC F(ISPFILE) DS(FONTMAPS.CNTL) NEW +
00330006      SPACE(30) CYL RECFM(F B) LRECL(80) BLKSIZE(23440)
00340000 
00350006ISPEXEC TBOPEN AFPFONTS NOWRITE LIBRARY(FTABLE) SHARE
00360006ISPEXEC TBSORT AFPFONTS FIELDS(TYPEFACE,C,A POINTSIZ,N,A)
00370006ISPEXEC TBSKIP AFPFONTS
00380006 
00390006SET STEP = 0
00400006SET JOB  = NO
00410006SET PAGE = 0
00420007SET TOC = 0
00430011SET MAXTOC = 46  /* MAX FONTS PER PAGE */
00440006 
00450006OPENFILE CONTENTS OUTPUT
00460006 
00470009SET CONTENTS = &STR(.DF FONT TYPE(SERIF  8   ) CODEPAGE T1D0BASE )
00480009PUTFILE CONTENTS
00490009SET CONTENTS = &STR(.BF FONT)
00500009PUTFILE CONTENTS
00510010SET CONTENTS = &STR(.PN PREF AFP-TOC-)
00520009PUTFILE CONTENTS
00530009SET CONTENTS = &STR(.RH ON)
00540009PUTFILE CONTENTS
00550009SET CONTENTS = &STR(AFP FONT MAPS - TABLE OF CONTENTS)
00560009PUTFILE CONTENTS
00570009SET CONTENTS = &STR(.RH OFF)
00580009PUTFILE CONTENTS
00590009 
00600009SET HEAD1 = &STR(:RDEF ID=FMTOC CWIDTHS='2.6I .5I 1.2I 1.2I .7I' HP='2 0' ALIGN='L R'.)
00610007SET HEAD2 = &STR(:TABLE REFID=FMTOC.)
00620009SET LINE1 = &STR(:C.FONT NAME:C.SIZE:C.WIDTH:C.WEIGHT:C.PAGE #)
00630007 
00640007SET CONTENTS = &STR(&HEAD1)
00650007PUTFILE CONTENTS
00660007SET CONTENTS = &STR(&HEAD2)
00670009PUTFILE CONTENTS
00680009SET CONTENTS = &STR(:ROW.)
00690009PUTFILE CONTENTS
00700009SET CONTENTS = &STR(&LINE1)
00710009PUTFILE CONTENTS
00720007 
00730006FIXUP: +
00740006    IF &POINTSIZ < 37 THEN DO
00750006              SET STEP = &STEP + 1
00760006              IF &STEP = 1  THEN SET JOB = YES
00770006              SET TFONT = &SUBSTR(3:&LENGTH(&CODEFONT),&CODEFONT  )
00780006              SET PAGE = &PAGE + 1
00790007 
00800007   IF &TOC = &MAXTOC THEN DO
00810007                          SET TOC = 0
00820007                          SET CONTENTS = &STR(:ETABLE.)
00830007                          PUTFILE CONTENTS
00840007                          SET CONTENTS = &STR(.PA)
00850007                          PUTFILE CONTENTS
00860007                          SET CONTENTS = &STR(&HEAD2)
00870007                          PUTFILE CONTENTS
00880009                          SET CONTENTS = &STR(:ROW.)
00890009                          PUTFILE CONTENTS
00900009                          SET CONTENTS = &STR(&LINE1)
00910009                          PUTFILE CONTENTS
00920007                          END
00930007 
00940007   SET CONTENTS = &STR(:ROW.)
00950007   PUTFILE CONTENTS
00960009 
00970009   SET OFF = &SYSINDEX(NORMAL,&STR(&WIDTH),1)
00980009       IF &OFF > 0 THEN SET &WIDTH =
00990009 
01000009   SET OFF = &SYSINDEX(MEDIUM,&STR(&WEIGHT),1)
01010009       IF &OFF > 0 THEN SET &WEIGHT =
01020009 
01030007   SET CONTENTS = &STR(:C.&TYPEFACE:C.&POINTSIZ:C.&WIDTH:C.&WEIGHT:C.&PAGE)
01040009   PUTFILE CONTENTS
01050009 
01060007   SET TOC = &TOC + 1
01070007 
01080007              IF &NOSUB = THEN DO
01090006              ISPEXEC FTOPEN
01100006              ISPEXEC FTINCL OGL
01110006              IF &STEP = 50 THEN DO
01120006                                 ISPEXEC FTCLOSE
01130006                                 SUB FONTMAPS.CNTL
01140006                                 WRITE JOB SUBMITTED UP TO PAGE &PAGE
01150006                                 SET STEP = 0
01160006                                 END
01170006                           SET JOB = NO
01180006                           END
01190007                           END
01200000                    ISPEXEC TBSKIP AFPFONTS
01210000                    IF &LASTCC = 0 THEN GOTO FIXUP
01220000EXIT: +
01230007      SET CONTENTS = &STR(:ETABLE.)
01240007      PUTFILE CONTENTS
01250007      CLOSFILE CONTENTS
01260007 
01270000      ISPEXEC TBEND AFPFONTS
01280000      ISPEXEC LIBDEF FTABLE
01290002      FREE FILE(CONTENTS ISPFILE)
01300002      DEL FONTMAPS.CNTL
