00010000PROC     0        DEBUG
00020000 
00030000/*********************************************************************/
00040000/*                   DUMP DATA SET MANAGEMENT                        */
00050000/* IPCS: DISPLAY ALL DUMPED JOBS/ASIDS                               */
00060000/*********************************************************************/
00070000 
00080000IF       &DEBUG = DEBUG +
00090000THEN     CONTROL  ASIS   CONLIST NOFLUSH   LIST   MSG   PROMPT   SYMLIST
00100000ELSE     CONTROL  ASIS NOCONLIST NOFLUSH NOLIST   MSG   PROMPT NOSYMLIST
00110000 
00120000SET      &ASCBASXB = 108N           /* POINTER TO ASXB                          */
00130000SET      &ASCBJBNI = 172N           /* POINTER TO JOBNAME FOR INITIATED JOBS    */
00140000SET      &ASCBJBNS = 176N           /* POINTER TO JOBNAME FOR START/MOUNT/LOGON */
00150000SET      &ASCBMCC  = 168N           /* MEMORY ABEND CODE                        */
00160000SET      &ASVTAVAL = X'80'          /* ASID IS AVAILABLE                        */
00170000SET      &ASVTFRST = 524N           /* FIRST ASVT ENTRY                         */
00180000SET      &ASVTMAXU = 516N           /* MAXIMUM NUMBER OF ASIDS                  */
00190000SET      &ASXBFTCB = 4N             /* POINTER TO FIRST TCB                     */
00200000SET      &TCBCMPC  = 17N            /* TASK ABEND CODE                          */
00210000SET      &TCBTCB   = 116N           /* POINTER TO NEXT TCB                      */
00220000SET      &TCBRTWA  = 224N           /* POINTER TO RTM2WA                        */
00230000 
00240000ISPEXEC  VGET     (IPCOS QUAL)
00250000 
00260000IF       &SUBSTR(1:4,&QUAL    ) = ASID +
00270000THEN     SET      &DEFASID = &SUBSTR(6:&LENGTH(&QUAL)-1,&QUAL)
00280000ELSE     SET      &DEFASID = &STR()
00290000 
00300000ISPEXEC  TBCREATE ASID     KEYS(ASID) NAMES(JOBNAME DEFAULT ERROR) NOWRITE REPLACE
00310000ISPEXEC  TBSORT   ASID     FIELDS(JOBNAME,C,A)
00320000 
00330000EQUATE   ASVT ASVT STRUCTURE(ASVT)
00340000EVALUATE ASVT+&ASVTMAXU UNSIGNED LENGTH(4) CLIST(STORAGE(LASTASID))
00350000EVALDEF  CLIST(FLAG(FLAG))
00360000SETDEF   FLAG(TERMINATING)
00370000 
00380000SET      &ASID = 1
00390000DO       WHILE    &ASID <= &LASTASID /* ASID LOOP */
00400000         SET      &ASVTENTY = &STR(ASVT+&ASVTFRST+&EVAL(&ASID*4)N)
00410000         COMPARE  ADDRESS(&ASVTENTY) WITH(VALUE(&ASVTAVAL)) LENGTH(1) MASK(&ASVTAVAL)
00420000         SET      &COMPARE = &LASTCC
00430000         IF       &COMPARE = 4 +
00440000         THEN     DO
00450000                  EVALUATE &ASVTENTY%+&ASCBASXB% ASID(&ASID) STRUCTURE(ASXB) CHECK
00460000                  SET      &ASXBSTAT = &LASTCC
00470000                  END
00480000         IF       &COMPARE = 4 && &ASXBSTAT ^= 12 +
00490000         THEN     DO       /* ASID IS DUMPED */
00500000                  SET      &JOBNAME = &STR()
00510000                  SET      &DEFAULT = &STR()
00520000                  SET      &ERROR = &STR()
00530000                  IF       &ASID = &DEFASID +
00540000                  THEN     SET      &DEFAULT = DEFAULT
00550000                  COMPARE  ADDRESS(&ASVTENTY%+&ASCBJBNI) LENGTH(4) WITH(VALUE(X'00000000'))
00560000                  IF       &LASTCC = 8 +
00570000                  THEN     EVALUATE &ASVTENTY%+&ASCBJBNI% CHARACTER LENGTH(8) CLIST(STORAGE(JOBNAME))
00580000                  ELSE     DO
00590000                           COMPARE  ADDRESS(&ASVTENTY%+&ASCBJBNS) LENGTH(4) WITH(VALUE(X'00000000'))
00600000                           IF       &LASTCC = 8 +
00610000                           THEN     EVALUATE &ASVTENTY%+&ASCBJBNS% CHARACTER LENGTH(8) CLIST(STORAGE(JOBNAME))
00620000                           END
00630000                  COMPARE  ADDRESS(&ASVTENTY%+&ASCBMCC) LENGTH(4) WITH(VALUE(X'00000000'))
00640000                  IF       &LASTCC = 8 +
00650000                  THEN     SET      &ERROR = ERROR
00660000                  SET      &COMPARE = -1
00670000                  EQUATE   IPCTCB   &ASVTENTY%+&ASCBASXB%+&ASXBFTCB POINTER ASID(&ASID)
00680000                  DO       WHILE    &ERROR = &STR() && &COMPARE ^= 0 && &COMPARE ^= 12 /* TCB LOOP */
00690000                           COMPARE  ADDRESS(IPCTCB%+&TCBCMPC) LENGTH(3) WITH(VALUE(X'000000'))
00700000                           IF       &LASTCC = 8 +
00710000                           THEN     SET      &ERROR = ERROR
00720000                           COMPARE  ADDRESS(IPCTCB%+&TCBRTWA) LENGTH(4) WITH(VALUE(X'00000000'))
00730000                           IF       &LASTCC = 8 +
00740000                           THEN     SET      &ERROR = ERROR
00750000                           COMPARE  ADDRESS(IPCTCB%+&TCBTCB) LENGTH(4) WITH(VALUE(X'00000000'))
00760000                           SET      &COMPARE = &LASTCC
00770000                           EQUATE   IPCTCB   IPCTCB%+&TCBTCB POINTER ASID(&ASID)
00780000                           END      /* TCB LOOP */
00790000                  ISPEXEC  TBADD    ASID     ORDER
00800000                  END      /* ASID IS DUMPED */
00810000         SET      &ASID = &ASID+1
00820000         END      /* ASID LOOP */
00830000 
00840000DROPSYM  IPCTCB   NOSUMMARY
00850000 
00860000ISPEXEC  TBTOP    ASID
00870000SET      &ASID = &DEFASID
00880000ISPEXEC  TBSCAN   ASID     ARGLIST(ASID) NOREAD
00890000 
00900000ISPEXEC  TBDISPL  ASID     PANEL(IPCDASID)
00910000SET      &TBDISPL = &LASTCC
00920000DO       WHILE    &ZTDSELS = 0 && &TBDISPL = 0        /* DISPLAY DUMPED JOBS */
00930000         IF       &VERB = FIND +
00940000         THEN     DO       /* FIND */
00950000                  IF       &DATATYPE(&STR(&OPER)) = CHAR +
00960000                  THEN     DO
00970000                           SET      &JOBNAME = &STR(&OPER)
00980000                           SET      &ARG = JOBNAME
00990000                           END
01000000                  ELSE     DO
01010000                           SET      &ASID = &OPER
01020000                           SET      &ARG = ASID
01030000                           END
01040000                  END      /* FIND */
01050000         IF       &VERB = LOCATE +
01060000         THEN     DO       /* LOCATE */
01070000                  SET      &DEFAULT = DEFAULT
01080000                  SET      &ERROR   = ERROR
01090000                  SET      &ARG = &OPER
01100000                  END      /* LOCATE */
01110000         ISPEXEC  TBTOP    ASID
01120000         ISPEXEC  TBSKIP   ASID     NUMBER(&ZTDTOP) NOREAD
01130000         ISPEXEC  TBSCAN   ASID     ARGLIST(&ARG) NOREAD
01140000         IF       &LASTCC ^= 0 +
01150000         THEN     DO
01160000                  ISPEXEC  TBTOP    ASID
01170000                  ISPEXEC  TBSKIP   ASID     NUMBER(&ZTDTOP) NOREAD
01180000                  END
01190000         ISPEXEC  TBDISPL  ASID     PANEL(IPCDASID)
01200000         SET      &TBDISPL = &LASTCC
01210000         END      /* DISPLAY DUMPED JOBS */
01220000 
01230000IF       &TBDISPL = 0 && &ZTDSELS ^= 0 +
01240000THEN     DO
01250000         SETDEF   ASID(&ASID) FLAG(&FLAG)
01260000         %IPCQUAL &DEBUG
01270000         END
01280000 
01290000EXIT     CODE(0)
