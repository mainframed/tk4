00010002PROC 1 DSNAME /** FILE() FI() F() **/ DEBUG TRAP(1) +
00020002       VOL() VOLUME() VOLSET() SHR OLD XISPMODE  NUMBER()
00030000/** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** ** ** **/
00040000/** Used by ROCKWELL ISPF primary command: PDS             **/
00050002/** By Mike Theys   Rockwell/SWCC/ISC                      **/
00060000/** -- -- -- -- -- -- -- -- -*- -- -- -- -- -- -- -- -- -- **/
00070000/** PDS primary command is defined as:                     **/
00080000/**   SELECT CMD(%ROCPDS !&ZPARM)                          **/
00090000/** The "!" allows PDS to be invoked without any dsname.   **/
00100000/** -- -- -- -- -- -- -- -- -*- -- -- -- -- -- -- -- -- -- **/
00110000/** ISPF PRIMARY COMMAND "PDS" SYNTAX:                     **/
00120000/** 1) PDS                                                 **/
00130000/** 2) PDS  dataset {vol} {volset} {SHR | OLD} {XISPMODE}  **/
00140000/**     where dataset :=  dsname  |  dsname(member)        **/
00150000/**                      'dsname' | 'dsname(member)'       **/
00160000/**     where vol     :=  VOLUME(volser) | VOL(volser)     **/
00170000/**     where volset  :=  VOLSET(volset)                   **/
00180000/** 3) PDS  file numbr   {XISPMODE}                        **/
00190000/**     where file := FILE(ddname) | FI(ddname) | F(ddname)**/
00200000/**     where numbr:= NUMBER(n)    | NUM(n)     | N(n)     **/
00210000/**     where n    := concatenation number of dsn in FILE  **/
00220000/** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** ** ** **/
00230000IF &DEBUG = DEBUG THEN  CONTROL   MSG   LIST   CONLIST   SYMLIST
00240000 
00250006IF &STR(&DSNAME) = &STR(!) THEN DO /* No dsn/file. Show PDS menu */
00260000  ISPEXEC SELECT PANEL(PDSPN@P)
00270000  EXIT
00280000END
00290006IF &STR(&DSNAME) = &STR(!?) THEN DO /* PDS ISPF command tutorial */
00300006  ISPEXEC SELECT PGM(ISPTUTOR) PARM($PDS)
00310006  EXIT
00320006END
00330000 
00340000  /* "DSNAME" MAY BE EITHER  DSNAME, DSNAME(MEMBER) W/O QUOTES */
00350000  /* OR IT MAY BE FILE(DDNAME) OR FI(DDNAME)                   */
00360000  SET L = &LENGTH(&STR(&DSNAME))
00370002  SET &DSNAME = &SUBSTR(2:&L,&STR(&DSNAME)) /* Strip off "!" */
00380004  SET &FULLDSN= &STR(&DSNAME)  /* SAVE DSNAME(MEMBER) FOR XISPMODE */
00390002  SET RPC = )
00400002  SET LPC = &STR((
00410002  SET LP = &SYSINDEX(&LPC,&STR(&DSNAME))
00420002  SET RP = &SYSINDEX(&RPC,&STR(&DSNAME))
00430000 
00440000  SET ADDED = ISPMODE
00450000 
00460000  /* SEE IF "FILE(", "FI(" OR "F("  SPECIFIED INSTEAD OF DSNAME */
00470000  IF &L > 4 THEN IF &SUBSTR(1:4,&STR(&DSNAME)) = FILE THEN +
00480000    IF &LP  = 5 AND &RP >= 7 AND &LP < &RP AND (&RP - &LP) <= 9 THEN +
00490000      SET FILE = &SUBSTR(&LP+1:&RP-1,&STR(&DSNAME))
00500000  IF &STR(&FILE) ^=  THEN GOTO GETPARMS
00510000  IF &L > 2 THEN IF &SUBSTR(1:2,&STR(&DSNAME)) = FI   THEN +
00520000    IF &LP  = 3 AND &RP >= 5 AND &LP < &RP AND (&RP - &LP) <= 9 THEN +
00530000      SET FILE = &SUBSTR(&LP+1:&RP-1,&STR(&DSNAME))
00540000  IF &STR(&FILE)   ^=  THEN GOTO GETPARMS
00550000  IF &L > 1 THEN IF &SUBSTR(1:1,&STR(&DSNAME)) = F    THEN +
00560000    IF &LP  = 2 AND &RP >= 4 AND &LP < &RP AND (&RP - &LP) <= 9 THEN +
00570000      SET FILE = &SUBSTR(&LP+1:&RP-1,&STR(&DSNAME))
00580000  IF &STR(&FILE)    ^=  THEN GOTO GETPARMS
00590000 
00600000  /* SEE IF MEMBER SPECIFIED. IF SO INVOKE WITH "MEMLIST &MEMBER" */
00610000  IF (&LP > 0) AND (&RP > 0) AND (&LP+1 <= &RP-1) THEN DO
00620000    SET MEMBER = &SUBSTR(&LP+1:&RP-1,&STR(&DSNAME))
00630000    SET ADDED = &STR(MEMLIST &MEMBER)
00640000  END
00650000 
00660000  /* IF MEMBER WAS SPECIFIED JUST GET PDS NAME */
00670000  IF &STR(&MEMBER) ^=    THEN DO
00680002    IF &SYSINDEX(',&STR(&DSNAME)) > 0 THEN DO
00690002      SET    DSNAME = &SUBSTR(2:&LP-1,&STR(&DSNAME))
00700002      SET    DSNAME = &STR('&DSNAME')
00710000    END
00720002    ELSE SET DSNAME = &SUBSTR(1:&LP-1,&STR(&DSNAME))
00730000  END
00740000 
00750000  GETPARMS:+
00760000  IF      &STR(&VOL)     ^=          THEN SET VOLS  = &STR( VOL(&VOL) )
00770000  ELSE IF &STR(&VOLUME)  ^=          THEN SET VOLS  = &STR( VOLUME(&VOLUME) )
00780000  IF      &STR(&VOLSET)  ^=          THEN SET VOLST = &STR( VOLSET(&VOL) )
00790000  IF      &STR(&SHR)      =  SHR     THEN SET DISP  = &STR( SHR )
00800000  ELSE IF &STR(&OLD)      =  OLD     THEN SET DISP  = &STR( OLD )
00810000  IF      &STR(&FILE)    ^=          THEN SET FILEN = &STR( FILE(&FILE) )
00820002  IF &STR(&FILEN)        ^=          THEN +
00830000    IF      &STR(&NUMBER)  ^=        THEN SET FILEN = &STR(&FILEN NUMBER(&NUMBER) )
00840000 
00850000  IF &XISPMODE =   THEN DO
00860007    ISPEXEC VGET (PDSENDX) PROFILE
00870007    IF &STR(&PDSENDX) =  THEN DO
00880007      SET PDSENDX = NO
00890007      ISPEXEC VPUT (PDSENDX) PROFILE
00900007    END
00910000    IF &STR(&FILEN) ^=   +
00920000      THEN  ISPEXEC SELECT PGM(PDS) PARM(PDS &FILEN                     ISPMODE)
00930005      ELSE  ISPEXEC SELECT PGM(PDS) PARM(PDS &DSNAME &DISP &VOLS &VOLST &ADDED )
00940000    EXIT
00950000  END
00960000 
00970000  /* NON-ISPF MODE */
00980000  IF &STR(&FILEN) ^=   +
00990000    THEN  PDS &FILEN                      &XISPMODE
01000000    ELSE  PDS &FULLDSN &DISP &VOLS &VOLST &XISPMODE
