00010000PROC 0 TRAP(1000) SPFV DEBUG
00020000/** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** ** ** **/
00030000/** USED BY ROCKWELL ISPF PRIMARY COMMAND: LISTSP          **/
00040000/** ACTION MODE ALLOWS BROWSE/EDIT/PDS/FREE,DELETE, ETC.   **/
00050000/** BY: MIKE THEYS ROCKWELL  INTERNATIONAL/SWCC  03/29/87  **/
00060000/** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** ** ** **/
00070000IF &DEBUG = DEBUG THEN CONTROL   MSG LIST SYMLIST CONLIST
00080000ISPEXEC CONTROL ERRORS RETURN
00090000 
00100000ISPEXEC VGET (EZLSPVAR)
00110000SET EZLSPIDX = &STR(&EZLSPVAR)
00120000 
00130000ISPEXEC VGET (ZSCREEN)
00140000IF &DEBUG = THEN CONTROL NOMSG
00150000 
00160000IF &STR(&EZLSPIDX) ^=  THEN DO
00170000  IF &DEBUG = DEBUG THEN CONTROL NOLIST NOSYMLIST NOCONLIST
00180000  CONTROL NOFLUSH NOPROMPT NOMSG
00190000  LISTSP  &STR(&EZLSPIDX) PRINT(EZLISTSP.REPORT&ZSCREEN)
00200000  SET RC = &LASTCC
00210000  CONTROL   FLUSH MSG
00220000  IF &DEBUG = DEBUG THEN CONTROL   MSG LIST SYMLIST CONLIST
00230000  IF &RC > 0 THEN DO
00240000    SET SHORT = &STR(LISTSP ERROR)
00250000    SET LONG  = &STR(RC=&RC OCCURRED WITH "LISTSP &STR(&EZLSPIDX)")
00260000    ISPEXEC SETMSG MSG(SYS010)
00270000    EXIT
00280000  END
00290000  ALLOC FI(EZLISTSP) REUSE DA(EZLISTSP.REPORT&ZSCREEN) SHR
00300000END
00310000 
00320000ISPEXEC VGET (ZSCREEN)
00330000SET EZTBL = &STR(EZLSP&ZSCREEN)
00340000 
00350000ISPEXEC TBSTATS  &EZTBL STATUS2(EZTBLST2)
00360000IF &EZTBLST2 ^= 1 THEN ISPEXEC TBEND   &EZTBL
00370000ISPEXEC TBCREATE &EZTBL KEYS(EZLSPKEY) NOWRITE REPLACE SHARE +
00380000        NAMES(EZLSPDSN,EZLSPALT,EZLSPUST,EZLSPNXT,EZLSPDEV, +
00390000              EZLSPVOL,EZLSPSPT,EZLSP2XT,EZLSPRFM,EZLSPBLK, +
00400000              EZLSPLRC,EZLSPORG,EZLSPDLU,EZLSPDCR,EZLSPTAG)
00410000ISPEXEC TBSORT   &EZTBL FIELDS(EZLSPDSN,C,A)
00420000 
00430000ERROR DO
00440000  SET LCC = &LASTCC
00450000  IF &LCC = 400 THEN GOTO EOF
00460000  SET SHORT = &STR(EZLISTSP ERROR)
00470000  SET LONG  = &STR(ERROR &LCC OCCURED DURING EZLISTSP)
00480000  ISPEXEC SETMSG MSG(SYS010)
00490000  EXIT
00500000END
00510000 
00520000OPENFILE EZLISTSP INPUT
00530000SET DSNKNT = 0
00540000SET RECKNT = 0
00550000 
00560000DO WHILE( 1 = 1 )
00570000 
00580000  GETFILE EZLISTSP
00590000  SET RECKNT = &RECKNT + 1
00600000  IF &RECKNT <= 2                           THEN GOTO NEXT
00610000  IF &SUBSTR(2:2,&STR(&EZLISTSP)) = &STR( ) THEN GOTO NEXT
00620000 
00630000  SET EZLSPDSN = &SUBSTR(002:045,&STR(&EZLISTSP))
00640000  SET EZLSPALT = &SUBSTR(047:051,&STR(&EZLISTSP))
00650000    IF &DATATYPE(&EZLSPALT) ^= NUM THEN GOTO NEXT
00660000  SET EZLSPUST = &SUBSTR(053:057,&STR(&EZLISTSP))
00670000    IF &STR(&EZLSPUST) =  THEN SET EZLSPUST = 0
00680000    IF &DATATYPE(&EZLSPUST) ^= NUM THEN GOTO NEXT
00690000  SET EZLSPNXT = &SUBSTR(059:060,&STR(&EZLISTSP))
00700000    IF &DATATYPE(&EZLSPNXT) ^= NUM THEN GOTO NEXT
00710000  SET EZLSPDEV = &SUBSTR(063:068,&STR(&EZLISTSP))
00720000  IF &STR(&EZLSPDEV) = &STR(330-1) THEN +
00730000    SET EZLSPDEV = &SUBSTR(062:068,&STR(&EZLISTSP))
00740000  SET EZLSPVOL = &SUBSTR(070:075,&STR(&EZLISTSP))
00750000  SET EZLSPSPT = &SUBSTR(078:080,&STR(&EZLISTSP))
00760000  SET EZLSP2XT = &SUBSTR(082:084,&STR(&EZLISTSP))
00770000    IF &DATATYPE(&EZLSP2XT) ^= NUM THEN GOTO NEXT
00780000  SET EZLSPRFM = &SUBSTR(087:089,&STR(&EZLISTSP))
00790000  SET EZLSPBLK = &SUBSTR(091:095,&STR(&EZLISTSP))
00800000    IF &DATATYPE(&EZLSPBLK) ^= NUM THEN GOTO NEXT
00810000  SET EZLSPLRC = &SUBSTR(097:101,&STR(&EZLISTSP))
00820000    IF &DATATYPE(&EZLSPLRC) ^= NUM THEN GOTO NEXT
00830000  SET EZLSPORG = &SUBSTR(104:106,&STR(&EZLISTSP))
00840000  SET EZLSPDLU = &SUBSTR(108:115,&STR(&EZLISTSP))
00850000  SET EZLSPDCR = &SUBSTR(117:124,&STR(&EZLISTSP))
00860000  SET EZLSPKEY = &STR(&EZLSPDSN!&EZLSPVOL)
00870000  SET EZLSPTAG =
00880000 
00890000  ISPEXEC TBADD &EZTBL ORDER
00900000  SET DSNKNT = &DSNKNT + 1
00910000 
00920000  NEXT:+
00930000END
00940000 
00950000EOF:+
00960000ERROR OFF
00970000CLOSFILE EZLISTSP
00980000IF &DEBUG = THEN CONTROL NOMSG
00990000  FREE FI(EZLISTSP) DA(EZLISTSP.REPORT&ZSCREEN)
01000000IF &DEBUG = THEN CONTROL   MSG
01010000 
01020000IF &DSNKNT > 0 THEN DO
01030000  ISPEXEC TBTOP   &EZTBL
01040000  SET PANEL = &STR( PANEL(EZLISTSP) )
01050000  SET EZLSPSEL =
01060000 
01070000  SHOWTBL:+
01080000  ISPEXEC TBDISPL &EZTBL &PANEL
01090000  SET TBRC = &LASTCC
01100000  IF &TBRC >= 8 THEN GOTO QUIT
01110000 
01120000  SET EZLSPDSN = &EZLSPDSN  /* STRIP OFF TRAILING BLANKS */
01130000 
01140000  IF      &EZLSPSEL = B THEN DO
01150000    ISPEXEC CONTROL DISPLAY SAVE
01160000    ISPEXEC BROWSE DATASET('&EZLSPDSN')
01170000    SET BRRC = &LASTCC
01180000    ISPEXEC CONTROL DISPLAY RESTORE
01190000    IF      &BRRC =  0 THEN SET EZLSPTAG = &STR(*BROWSED*)
01200000    ELSE IF &BRRC = 12 THEN SET EZLSPTAG = &STR(*EMPTY DATASET*)
01210000    ELSE IF &BRRC = 16 THEN SET EZLSPTAG = &STR(*NO MEMBERS IN PDS*)
01220000    ELSE                    SET EZLSPTAG = &STR(*ERROR*)
01230000  END
01240000  ELSE IF &EZLSPSEL = E THEN DO
01250000    ISPEXEC CONTROL DISPLAY SAVE
01260000    ISPEXEC EDIT   DATASET('&EZLSPDSN')
01270000    SET EDRC = &LASTCC
01280000    ISPEXEC CONTROL DISPLAY RESTORE
01290000    IF      &EDRC =  0 THEN SET EZLSPTAG = &STR(*EDITED*)
01300000    ELSE IF &EDRC =  4 THEN SET EZLSPTAG = &STR(*EDIT NO SAVE*)
01310000    ELSE IF &EDRC = 16 THEN SET EZLSPTAG = &STR(*NO MEMBERS IN PDS*)
01320000    ELSE                    SET EZLSPTAG = &STR(*ERROR*)
01330000  END
01340000  ELSE IF &EZLSPSEL = P THEN DO
01350000    ISPEXEC CONTROL DISPLAY SAVE
01360000    PDS '&EZLSPDSN'
01370000    SET PDSRC = &LASTCC
01380000    ISPEXEC CONTROL DISPLAY RESTORE
01390000    IF     &PDSRC =  0 THEN SET EZLSPTAG = &STR(*PDS*)
01400000    ELSE                    SET EZLSPTAG = &STR(*PDS ERROR*)
01410000  END
01420000  ELSE IF &EZLSPSEL = M THEN DO
01430000    ISPEXEC CONTROL DISPLAY SAVE
01440000    %ECMD HMIGRATE OPERAND(' ''&EZLSPDSN'' NOWAIT') ERRONLY
01450000    SET MIGRC = &LASTCC
01460000    ISPEXEC CONTROL DISPLAY RESTORE
01470000    IF     &MIGRC =  0 THEN SET EZLSPTAG = &STR(*MIGRATED*)
01480000    ELSE                    SET EZLSPTAG = &STR(*HMIGRATE ERROR*)
01490000  END
01500000  ELSE IF &EZLSPSEL = V THEN DO
01510000    ISPEXEC CONTROL DISPLAY SAVE
01520000    IF &EZLSPORG = PO | &EZLSPORG = POU THEN SET VEROPT = &STR(:)
01530000    ELSE                                     SET VEROPT =
01540000    %ECMD PDS OPERAND(' ''&EZLSPDSN'' VOL(&EZLSPVOL) VERIFY &VEROPT')
01550000    SET VERRC = &LASTCC
01560000    ISPEXEC CONTROL DISPLAY RESTORE
01570000    IF     &VERRC =  0 THEN SET EZLSPTAG = &STR(*VERIFIED*)
01580000    ELSE                    SET EZLSPTAG = &STR(*VERIFY ERROR*)
01590000  END
01600000  ELSE IF &EZLSPSEL = R THEN DO
01610000    IF &SYSTSOE ^=  THEN DO
01620000      LISTDSI '&EZLSPDSN'
01630000      IF &EZLSPVOL = &SYSVOLUME THEN SET EZLSPCAT = YES
01640000      ELSE                           SET EZLSPCAT = NO
01650000    END
01660000    ELSE SET EZLSPCAT = YES /* ASSUME CATALOGED */
01670000    IF &EZLSPCAT = YES THEN DO
01680000      SET EZLSPNEW = &STR('&EZLSPDSN')
01690000      ISPEXEC CONTROL DISPLAY SAVE
01700000      ISPEXEC DISPLAY PANEL(EZLSPREN)
01710000      SET SHOWREN = &LASTCC
01720000      ISPEXEC CONTROL DISPLAY RESTORE
01730000      IF &SHOWREN= 0 THEN DO
01740000        SET ECMDRAND = &STR('&EZLSPDSN',&EZLSPNEW)
01750000        ISPEXEC VPUT (ECMDRAND)
01760000        %ECMD RENAME SPFV
01770000        SET RENRC = &LASTCC
01780000        IF     &RENRC =  0 THEN SET EZLSPTAG = &STR(*RENAMED*)
01790000        ELSE                    SET EZLSPTAG = &STR(*RENAME ERROR*)
01800000      END
01810000    END
01820000    ELSE SET EZLSPTAG = &STR(*MUST BE CATALOGED*)
01830000  END
01840000  ELSE IF &EZLSPSEL = X THEN DO
01850000    ISPEXEC TBDELETE &EZTBL
01860000    SET EZLSPTAG =
01870000    SET EZLSPSEL =
01880000  END
01890000  ELSE IF &EZLSPSEL = Z THEN DO
01900000    IF &EZLSPORG = PO | &EZLSPORG = POU THEN DO
01910000      ISPEXEC CONTROL DISPLAY SAVE
01920000      ISPEXEC DISPLAY PANEL(EZLSPCMP)
01930000      SET SHOWCMP= &LASTCC
01940000      ISPEXEC CONTROL DISPLAY RESTORE
01950000      IF &SHOWCMP= 0 THEN DO /* COMPRESS */
01960000        IF &EZSMBKP = NO AND &EZSMREL = NO THEN DO /* USE PDS CMD */
01970000          %ECMD PDS OPERAND(' ''&EZLSPDSN'' VOL(&EZLSPVOL) COMPRESS')
01980000          SET CMPRC = &LASTCC
01990000        END
02000000        ELSE DO /* USE THE $SM COMMAND TO COMPRESS/RELEASE SPACE */
02010000          CONTROL NOMSG
02020000          FREE FI(SYSPRINT) DA(EZLISTSP.SYSPRNT&ZSCREEN)
02030000          IF &SYSDSN(EZLISTSP.SYSPRNT&ZSCREEN) = OK +
02040000            THEN SET SMDISP = &STR(OLD)
02050000            ELSE SET SMDISP = &STR(NEW CAT)
02060000          ALLOC FI(SYSPRINT) DA(EZLISTSP.SYSPRNT&ZSCREEN) &SMDISP
02070000          IF &EZSMBKP  = YES    THEN +
02080000               SET EZSMBKUP = &STR(BACKUP BKPUNIT(&EZLSPDEV) BKPVOL(&EZLSPVOL) )
02090000          ELSE SET EZSMBKUP = NOBACKUP
02100000          IF &EZSMREL  = YES    THEN DO
02110000               SET EZSMRELS = &STR(RLSE)
02120000               IF      &EZSMPCT ^= THEN SET EZSMRELS = &STR(&EZSMRELS PCTSAVE(&EZSMPCT))
02130000               ELSE IF &EZSMSAV ^= THEN SET EZSMRELS = &STR(&EZSMRELS SAVE(&EZSMSAV))
02140000          END
02150000          ELSE SET EZSMRELS = NORLSE
02160000          $SM DSN('&EZLSPDSN') COMPRESS LIST +
02170000              VOL(&EZLSPVOL) UNIT(&EZLSPDEV) +
02180000              &EZSMBKUP &EZSMRELS
02190000          SET CMPRC = &LASTCC
02200000          IF &EZSMBKP = YES THEN SET EZSMBKUP = BACKUP
02210000          ELSE                   SET EZSMBKUP = NOBACKUP
02220000          SET ECMDBRTI = &STR($SM '&EZLSPDSN' COMPRESS &EZSMRELS &EZSMBKUP)
02230000          ISPEXEC VPUT (ECMDBRTI)
02240000          ISPEXEC CONTROL DISPLAY SAVE
02250000          ISPEXEC BROWSE DATASET(EZLISTSP.SYSPRNT&ZSCREEN) PANEL(ECMDBROW)
02260000          ISPEXEC CONTROL DISPLAY RESTORE
02270000          FREE FI(SYSPRINT) DA(EZLISTSP.SYSPRNT&ZSCREEN)
02280000          ALLOC FI(SYSPRINT) DA(*)
02290000        END
02300000        IF     &CMPRC =  0 THEN SET EZLSPTAG = &STR(*COMPRESSED*)
02310000        ELSE                    SET EZLSPTAG = &STR(*COMPRESS ERROR*)
02320000        CONTROL   MSG
02330000      END
02340000    END
02350000    ELSE SET EZLSPTAG = &STR(*NOT A PDS*)
02360000  END
02370000  ELSE IF &EZLSPSEL = L | &EZLSPSEL = I | &EZLSPSEL = S THEN DO
02380000    IF &EZLSPSEL = S THEN SET &LDSIDIR = NODIR
02390000    ELSE                  SET &LDSIDIR =
02400000    ISPEXEC CONTROL DISPLAY SAVE
02410000    %RLISTDSI '&EZLSPDSN' VOL(&EZLSPVOL) &LDSIDIR LOCK
02420000    SET LDSIRC = &LASTCC
02430000    ISPEXEC CONTROL DISPLAY RESTORE
02440000    IF    &LDSIRC =  0 THEN SET EZLSPTAG = &STR(*LISTDSI*)
02450000    ELSE                    SET EZLSPTAG = &STR(*LISTDSI ERROR*)
02460000  END
02470000  ELSE IF &EZLSPSEL = D THEN DO
02480000    ISPEXEC CONTROL DISPLAY SAVE
02490000    ISPEXEC DISPLAY PANEL(EZLSPDEL)
02500000    SET DELDSN = &LASTCC
02510000    ISPEXEC CONTROL DISPLAY RESTORE
02520000    IF &DELDSN = 0 THEN DO
02530000      %ECMD DELETE OPERAND(' ''&EZLSPDSN'' ') ERRONLY
02540000      SET DELRC = &LASTCC
02550000      IF    &DELRC =  0 THEN SET EZLSPTAG = &STR(*DELETED*)
02560000      ELSE                   SET EZLSPTAG = &STR(*DELETE ERROR*)
02570000    END
02580000  END
02590000  ELSE IF &EZLSPSEL = A THEN DO
02600000    ISPEXEC CONTROL DISPLAY SAVE
02610000    ISPEXEC DISPLAY PANEL(EZLSP$AR)
02620000    SET ARDO = &LASTCC
02630000    ISPEXEC CONTROL DISPLAY RESTORE
02640000    IF &ARDO = 0 THEN DO
02650000      SET ARPARMS =
02660000      IF &STR(&EZARTYP)  = YES THEN SET &ARPARMS = PERM
02670000      IF &STR(&EZARDAY) ^=     THEN SET &ARPARMS = &STR(&ARPARMS RETPD(&EZARDAY))
02680000      SET ARCMD = &STR(DSN('&EZLSPDSN') &ARPARMS VOL(&EZLSPVOL))
02690000      CONTROL NOMSG
02700000      FREE FI(SYSPRINT) DA(EZLISTSP.SYSPRNT&ZSCREEN)
02710000      IF &SYSDSN(EZLISTSP.SYSPRNT&ZSCREEN) = OK +
02720000        THEN SET SMDISP = &STR(OLD)
02730000        ELSE SET SMDISP = &STR(NEW CAT)
02740000      CONTROL MSG
02750000      ALLOC FI(SYSPRINT) DA(EZLISTSP.SYSPRNT&ZSCREEN) &SMDISP
02760000      $AR &ARCMD
02770000      SET ARRC = &LASTCC
02780000      SET ECMDBRTI = &STR($AR &ARCMD)                                   ))
02790000      ISPEXEC VPUT (ECMDBRTI)
02800000      ISPEXEC CONTROL DISPLAY SAVE
02810000      ISPEXEC BROWSE DATASET(EZLISTSP.SYSPRNT&ZSCREEN) PANEL(ECMDBROW)
02820000      ISPEXEC CONTROL DISPLAY RESTORE
02830000      CONTROL NOMSG
02840000      FREE FI(SYSPRINT) DA(EZLISTSP.SYSPRNT&ZSCREEN)
02850000      CONTROL MSG
02860000      ALLOC FI(SYSPRINT) DA(*)
02870000      IF    &ARRC =  0  THEN SET EZLSPTAG = &STR(*$ARCHIVE*)
02880000      ELSE                   SET EZLSPTAG = &STR(*$ARCHIVE ERROR*)
02890000    END
02900000  END
02910000  ELSE IF &EZLSPSEL = C THEN DO
02920000    SET ECMDRAND = &STR(NVSAM(NAME('&EZLSPDSN') DEVT(&EZLSPDEV) VOLUMES(&EZLSPVOL)) )
02930000    ISPEXEC VPUT (ECMDRAND)
02940000    %ECMD DEFINE SPFV ERRONLY
02950000    SET CATRC = &LASTCC
02960000    IF    &CATRC =  0 THEN SET EZLSPTAG = &STR(*CATALOGED*)
02970000    ELSE                   SET EZLSPTAG = &STR(*CATLG ERROR*)
02980000  END
02990000  ELSE IF &EZLSPSEL = ? THEN DO
03000000    SET &EZLSPDSN = &EZLSPDSN
03010000    SET &EZLSPVOL = &EZLSPVOL
03020000    SET &EZLSPUVL =
03030000    ISPEXEC DISPLAY PANEL(EZLSPUSR)
03040000    SET USRRC = &LASTCC
03050000    IF &USRRC = 0 THEN DO
03060000      ISPEXEC CONTROL DISPLAY SAVE
03070000      IF &EZLSPUVL = YES THEN SET USRVOL = &STR(VOL(&EZLSPVOL))
03080000      ELSE                    SET USRVOL =
03090000      SET ECMDRAND = &STR('&EZLSPDSN' &USRVOL &EZLSPUOP)
03100000      ISPEXEC VPUT (ECMDRAND)
03110000      %ECMD &EZLSPUCM SPFV
03120000      SET &USRCMDRC = &LASTCC
03130000      IF    &USRCMDRC=0 THEN SET EZLSPTAG = &STR(*USER &EZLSPUCM.*)
03140000      ELSE                   SET EZLSPTAG = &STR(*USER &EZLSPUCM ERROR*)
03150000      ISPEXEC CONTROL DISPLAY RESTORE
03160000    END
03170000  END
03180000  ELSE IF &EZLSPSEL = U THEN DO
03190000    %ECMD DELETE OPERAND(' ''&EZLSPDSN'' NSCR') ERRONLY
03200000    SET UNCRC = &LASTCC
03210000    IF    &UNCRC =  0 THEN SET EZLSPTAG = &STR(*UNCATALOGED*)
03220000    ELSE                   SET EZLSPTAG = &STR(*UNCATLG ERROR*)
03230000  END
03240000  ELSE IF &EZLSPSEL = T THEN DO
03250000    ISPEXEC CONTROL DISPLAY SAVE
03260000    ISPEXEC DISPLAY PANEL(EZLSPREL)
03270000    SET RELDSN = &LASTCC
03280000    ISPEXEC CONTROL DISPLAY RESTORE
03290000    IF &RELDSN = 0 THEN DO
03300000      IF      &EZRELTYP = ALL THEN DO
03310000        IF &EZRELTYP = SAVE THEN SET &EZTRIM = &STR( SAVE(&EZRELSAV) )
03320000        ELSE                     SET &EZTRIM =
03330000        SET ECMDRAND = &STR('&EZLSPDSN' VOL(&EZLSPVOL) &EZTRIM )
03340000        ISPEXEC VPUT (ECMDRAND)
03350000        %ECMD TRIM SPFV
03360000        SET TRMRC = &LASTCC
03370000        IF    &TRMRC =  0 THEN SET EZLSPTAG = &STR(*TRIMED*)
03380000        ELSE                   SET EZLSPTAG = &STR(*TRIM ERROR*)
03390000      END
03400000      ELSE IF &EZRELTYP = SAVE THEN DO /* USE $SM RELEASE/SAVE */
03410000        CONTROL NOMSG
03420000        FREE FI(SYSPRINT) DA(EZLISTSP.SYSPRNT&ZSCREEN)
03430000        IF &SYSDSN(EZLISTSP.SYSPRNT&ZSCREEN) = OK +
03440000          THEN SET SMDISP = &STR(OLD)
03450000          ELSE SET SMDISP = &STR(NEW CAT)
03460000        ALLOC FI(SYSPRINT) DA(EZLISTSP.SYSPRNT&ZSCREEN) &SMDISP
03470000        IF      &EZRELSTY = PCT THEN SET EZSMRELS = &STR( PCTSAVE(&EZRELPCT) )
03480000        ELSE IF &EZRELSTY = TRK THEN SET EZSMRELS = &STR( SAVE(&EZRELSAV) )
03490000        ELSE                         SET EZSMRELS =                     )
03500000        $SM DSN('&EZLSPDSN') RLSE &EZSMRELS LIST VOL(&EZLSPVOL) UNIT(&EZLSPDEV)
03510000        SET SMRRC = &LASTCC
03520000        ISPEXEC CONTROL DISPLAY SAVE
03530000        SET ECMDBRTI = &STR($SM '&EZLSPDSN' RLSE &EZSMRELS VOL(&EZLSPVOL))
03540000        ISPEXEC VPUT (ECMDBRTI)
03550000        ISPEXEC BROWSE DATASET(EZLISTSP.SYSPRNT&ZSCREEN) PANEL(ECMDBROW)
03560000        ISPEXEC CONTROL DISPLAY RESTORE
03570000        FREE FI(SYSPRINT) DA(EZLISTSP.SYSPRNT&ZSCREEN)
03580000        ALLOC FI(SYSPRINT) DA(*)
03590000        IF    &SMRRC =  0 THEN SET EZLSPTAG = &STR(*RELEASED SPACE*)
03600000        ELSE                   SET EZLSPTAG = &STR(*RELEASE ERROR*)
03610000      END
03620000      ELSE DO
03630000        SET ECMDRAND = &STR('&EZLSPDSN' VOL(&EZLSPVOL) EXTENTS )
03640000        ISPEXEC VPUT (ECMDRAND)
03650000        %ECMD RELEASE SPFV
03660000        SET RELRC = &LASTCC
03670000        IF    &RELRC =  0 THEN SET EZLSPTAG = &STR(*RELEASED EXTENTS*)
03680000        ELSE                   SET EZLSPTAG = &STR(*RELEASE ERROR*)
03690000      END
03700000    END
03710000  END
03720000  ELSE IF &EZLSPSEL = F THEN DO
03730000    %ECMD FREE OPERAND(' DA(''&EZLSPDSN'') ') ERRONLY
03740000    SET FRERC = &LASTCC
03750000    IF    &FRERC =  0 THEN SET EZLSPTAG = &STR(*FREE*)
03760000    ELSE                   SET EZLSPTAG = &STR(*FREE ERROR*)
03770000  END
03780000  ELSE IF &EZLSPSEL = Q THEN DO
03790000    %ECMD ENQ OPERAND(' SYSDSN/&EZLSPDSN ')
03800000    SET ENQRC = &LASTCC
03810000    IF    &ENQRC =  0 THEN SET EZLSPTAG = &STR(*ENQUE*)
03820000    ELSE                   SET EZLSPTAG = &STR(*ENQUE ERROR*)
03830000  END
03840000 
03850000  IF &EZLSPSEL ^= THEN ISPEXEC TBMOD &EZTBL ORDER
03860000  SET EZLSPSEL =
03870000 
03880000  IF &TBRC =  0 THEN SET PANEL = &STR( PANEL(EZLISTSP) )
03890000  IF &TBRC =  4 THEN SET PANEL =
03900000  IF &TBRC <  8 THEN GOTO SHOWTBL
03910000 
03920000END
03930000ELSE DO /* NO DATASET SELECTED */
03940000  SET SHORT = &STR(NONE SELECTED)
03950000  SET LONG  = &STR(NO DATASETS FOUND FOR "LISTSP &EZLSPIDX".)
03960000  ISPEXEC SETMSG MSG(SYS010)
03970000END
03980000 
03990000QUIT:+
04000000ISPEXEC TBCLOSE &EZTBL
04010000CONTROL NOMSG
04020000FREE FI(EZLSPTBL) DA(EZLISTSP.TABLES)
04030000EXIT
