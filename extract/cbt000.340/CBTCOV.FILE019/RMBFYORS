00010000PROC 0 DEBUG    UP DOWN LEFT RIGHT AMT()
00020002  /* RMBF@YOR      Scrollable Primary Menu Version #3 with 36 Opts */
00030000  /* This version will handle the ISPF "Jump" cmds if PRIMARY Menu */
00040002  /* This clist sets the initial menu options on PANEL(RMBF@YOR)   */
00050002  /* It is invoked by either RMBF@YOR panel of first startup.      */
00070000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00080000  /**                                                             **/
00090000  /** User/Group Modifable Primary Option Menu: BUILD             **/
00100000  /**                                                             **/
00110000  /** Michael E. Theys                                            **/
00120000  /** Southwestern Computing Center                               **/
00130000  /** Information Systems Center                                  **/
00140000  /** Rockwell International                                      **/
00150000  /** 1200 N. Alma Rd.  Bldg 401-109                              **/
00160000  /** Richardson, Texas 75081-2205                                **/
00170000  /** (214) 996-2860  COMNET 437-2860                             **/
00180000  /** XMIT Address    SWCC.XTS0008                    Dec 1986    **/
00190000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00200000 
00210000CONTROL ASIS
00220000ISPEXEC CONTROL ERRORS RETURN
00230000IF &DEBUG = DEBUG +
00240000  THEN CONTROL   LIST   CONLIST   SYMLIST NOFLUSH   PROMPT   MSG
00250000  ELSE CONTROL NOLIST NOCONLIST NOSYMLIST   FLUSH NOPROMPT NOMSG
00260000 
00270002/* PANEL(RMBF@YOR) has RMBFMAXD= Max Displayable options=18 */
00280002/* PANEL(RMBF@YOR) has RMBFMAXM= Max available   options=36 */
00290000ISPEXEC VGET (RMBFPOOL,RMBFMAXD,RMBFMAXM)
00300000SET POOL = &RMBFPOOL
00310000 
00320000/* Define the amount to scroll by, if needed. */
00330000IF &DATATYPE(&AMT) ^= NUM THEN DO
00340000  IF      &STR(&AMT) = HALF OR &STR(&AMT) = H THEN +
00350000       SET SCROLL = &RMBFMAXD / 2
00360000  ELSE IF &STR(&AMT) = PAGE OR &STR(&AMT) = P THEN +
00370000       SET SCROLL = &RMBFMAXD
00380000  ELSE SET SCROLL = 1
00390000END
00400000ELSE   SET SCROLL = &AMT
00410000 
00420000/* Get current TOP displayable menu position  OPT#  */
00430000ISPEXEC VGET (R&POOL.OPT#)
00440000SET OPT# = &&R&POOL.OPT#
00450000IF &DATATYPE(&OPT#) ^= NUM THEN SET OPT# = 1
00460000IF &OPT# < 1               THEN SET OPT# = 1
00470000IF &OPT# > &RMBFMAXM       THEN SET OPT# = &RMBFMAXM
00480000 
00490000/* See if scroll pf key, option selected, or if END */
00500000IF      &UP = UP                         THEN DO
00510000   SET OPT# = &OPT# - &SCROLL
00520000   IF &OPT# < 1  THEN SET OPT# = 1
00530002   %RMBFYORI SCROLLIT OPT#(&OPT#)
00540000END
00550000ELSE IF &LEFT = LEFT   OR &FIRST = FIRST THEN DO
00560000   /* Scroll "LEFT" is FIRST or TOP option. */
00570000   SET OPT# = 1
00580002   %RMBFYORI SCROLLIT OPT#(&OPT#)
00590000END
00600000ELSE IF &DOWN = DOWN                     THEN DO
00610000   SET OPT# = &OPT# + &SCROLL
00620000   IF &OPT# > &RMBFMAXM  THEN SET OPT# = &RMBFMAXM
00630002   %RMBFYORI SCROLLIT OPT#(&OPT#)
00640000END
00650000ELSE IF &RIGHT = RIGHT OR &LAST = LAST   THEN DO
00660000   /* Scroll "RIGHT" is LAST or BOTTOM option. */
00670000   SET OPT# = &RMBFMAXM - &RMBFMAXD + 1
00680002   %RMBFYORI SCROLLIT OPT#(&OPT#)
00690000END
00700000 
00710000EXIT
