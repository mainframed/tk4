00010000PROC     0        DEBUG
00020000/**********************************************************************/
00030000/* ISPF APPLICATION TO UPDATE A PERSONAL LIBRARY                      */
00040000/*                                                                    */
00050000/* ISPF TABLE "LIBRARY" CONTAINS A LIST OF MANUAL FORM NUMBERS AND    */
00060000/* TITLES.  THE TABLE IS SORTED BY FORM NUMBER.                       */
00070000/*                                                                    */
00080000/* THIS APPLICATION ALLOWS YOU TO ENTER NEW TITLES. YOU MAY ALSO      */
00090000/* SPECIFY A STATUS OF "NEW", "ORDER", OR "RECEIVED".                 */
00100000/**********************************************************************/
00110000IF       &DEBUG = DEBUG +
00120000THEN     CONTROL  ASIS   CONLIST NOFLUSH   LIST   MSG   PROMPT   SYMLIST
00130004ELSE     CONTROL  ASIS NOCONLIST NOFLUSH NOLIST NOMSG   PROMPT NOSYMLIST
00140000ISPEXEC  VGET     (LIBTABL ZMONTH ZDAY ZYEAR)
00150000/**********************************************************************/
00160000/* OPEN "LIBRARY" TABLE                                               */
00170000/**********************************************************************/
00180000IF       &SYSDSN(&LIBTABL) = OK +
00190000THEN     ALLOCATE FILE(LIBTABL) DATASET(&LIBTABL) REUSE OLD
00200000ELSE     ALLOCATE FILE(LIBTABL) DATASET(&LIBTABL) REUSE NEW DSORG(PO) RECFM(F B) LRECL(80) BLKSIZE(6160) +
00210005                  SPACE(1,1) TRACK DIR(1)
00220000ISPEXEC  TBOPEN   LIBRARY  WRITE    LIBRARY(LIBTABL)
00230000SET      &RETURN = &LASTCC
00240000IF       &RETURN = 12 +
00250000THEN     DO       /* TABLE IN USE */
00260000         ISPEXEC  SETMSG   MSG(LIB001)
00270000         EXIT     CODE(12)
00280000         END      /* TABLE IN USE */
00290000IF       &RETURN = 8 +
00300000THEN     DO       /* TABLE DOES NOT EXIST */
00310000         ISPEXEC  TBCREATE LIBRARY  KEYS(FORM) NAMES(STATUS OLDSTAT DATE SORT TITLE CONTINUE) WRITE LIBRARY(LIBTABL)
00320000         ISPEXEC  TBSORT   LIBRARY  FIELDS(SORT,C,A)
00330000         END      /* TABLE DOES NOT EXIST */
00340000/**********************************************************************/
00350000/* DISPLAY DATA ENTRY PANEL                                           */
00360000/**********************************************************************/
00370000SET      &OLDSTAT = N
00380000SET      &DATE = &STR(&ZMONTH&ZDAY&ZYEAR)
00390000ISPEXEC  DISPLAY  PANEL(LIBRARYM)
00400000SET      &DISPLAY = &LASTCC
00410000DO       WHILE    &DISPLAY = 0      /* DISPLAY ENTRY PANEL */
00420000         SET      &FORM = &STR(&SYSCAPS(&FORM))
00430003         SET      &SORT = &SUBSTR(2:12,&STR(&FORM   ))
00440000         ISPEXEC  TBADD    LIBRARY  ORDER
00450000         IF       &LASTCC = 0 +
00460000         THEN     ISPEXEC  SETMSG   MSG(LIB004)
00470000         ELSE     ISPEXEC  SETMSG   MSG(LIB003)
00480000         ISPEXEC  DISPLAY  PANEL(LIBRARYM)
00490000         SET      &DISPLAY = &LASTCC
00500000         END      /* DISPLAY ENTRY PANEL */
00510000/**********************************************************************/
00520000/* CLOSE "LIBRARY" TABLE                                              */
00530000/**********************************************************************/
00540000ISPEXEC  TBCLOSE  LIBRARY  PAD(25)  LIBRARY(LIBTABL)
00550000/**********************************************************************/
00560000/* INVOKE STATUS APPLICATION                                          */
00570000/**********************************************************************/
00580000ISPEXEC  SELECT   CMD(%LIBRARYS &DEBUG)
