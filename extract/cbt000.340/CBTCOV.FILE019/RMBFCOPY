00010000PROC 0  DEBUG FROM() TO()
00020000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00030000/**              ROCKWELL MENU BUILD FACILITY                      **/
00040000/** COPY MENU BUILD OPTIONS FROM ONE BUILD VARIABLE POOL TO ANOTHER**/
00050000/** MUST BE INVOKED UNDER THE CORRECT ISPF APPLID.                 **/
00060000/**                                                                **/
00070000/** M.THEYS                                               01/16/87 **/
00080000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00090000 
00100000IF &DEBUG = DEBUG THEN  CONTROL   MSG   LIST   CONLIST   SYMLIST
00110000ELSE                    CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST
00120000 
00130000/* SAVE THE CURRENT BUILD VARIABLE POOL AND MAX MENU OPTIONS */
00140000ISPEXEC VGET (RMBFPOOL,RMBFMAXM)
00150000SET SAVEPOOL = &STR(&RMBFPOOL)
00160000SET SAVEMAXM = &STR(&RMBFMAXM)
00170000 
00180000 
00190000/* VERIFY THE FROM VARIABLE POOL AND MAX MENU OPTIONS */
00200000SET RMBFPOOL = &FROM
00210000ISPEXEC VPUT (RMBFPOOL)
00220000%RMBFPOOL
00230000ISPEXEC VGET (RMBFPOOL,RMBFMAXM)
00240000SET FROM    = &RMBFPOOL
00250002SET FROMMAXM= &RMBFMAXM
00260000 
00270000/* VERIFY THE TO   VARIABLE POOL AND MAX MENU OPTIONS */
00280000SET RMBFPOOL = &TO
00290000ISPEXEC VPUT (RMBFPOOL)
00300000%RMBFPOOL
00310000ISPEXEC VGET (RMBFPOOL,RMBFMAXM)
00320000SET TO      = &RMBFPOOL
00330000SET TOMAXM  = &RMBFMAXM
00340000 
00350002SET MAXM = &TOMAXM
00360002IF &FROMMAXM < &MAXM THEN SET MAXM = &FROMMAXM
00370000CONTROL ASIS
00380000 
00390000 
00400000 
00410000/****  COPY DISPLAYABLE MENU OPTIONS ****/
00420000SET MOPT = 0
00430000SET K    = 1
00440000DO WHILE (&K <= &MAXM)
00450000  /* GET VARIABLES USED BY THE "FROM" MENU */
00460000  ISPEXEC VGET (R&FROM.O&K,R&FROM.SD&K,R&FROM.LD&K,R&FROM.C&K) PROFILE
00470000  SET OPT    = &STR(&&R&FROM.O&K)
00480000  SET NAME   = &STR(&&R&FROM.SD&K)
00490000  SET DESCR  = &STR(&&R&FROM.LD&K)
00500000  SET ICMD   = &STR(&&R&FROM.C&K)
00510000  /* COPY THE MENU OPTIONS TO THE "TO" MENU VARIABLE POOL */
00520000  IF &STR(&OPT) ^=  THEN DO
00530000    SET MOPT = &MOPT + 1
00540000    SET R&TO.O&K = &STR(&OPT)
00550000    SET R&TO.SD&K = &STR(&NAME)
00560000    SET R&TO.LD&K = &STR(&DESCR)
00570000    SET R&TO.C&K = &STR(&ICMD)
00580000    ISPEXEC VPUT (R&TO.O&K,R&TO.SD&K,R&TO.LD&K,R&TO.C&K) PROFILE
00590000  END
00600000  SET K = &K + 1
00610000END
00620000SET R&TO.MOPT =
00630000IF &MOPT > 0 THEN SET R&TO.MOPT = YES
00640000ISPEXEC VPUT (R&TO.MOPT) PROFILE
00650000 
00660000 
00670000/****  COPY HIDDEN MENU OPTIONS ****/
00680000SET HOPT = 0
00690000SET K    = 1
00700000DO WHILE (&K <= 5)
00710000  /* TRANSLATE 1-5 TO A-E */
00720001  SET J = &SUBSTR(&K:&K,&STR(ABCDE))
00730000  /* GET VARIABLES USED BY THE "FROM" MENU */
00740000  ISPEXEC VGET (R&FROM.O&J,R&FROM.LD&J,R&FROM.C&J) PROFILE
00750000  SET OPT    = &STR(&&R&FROM.O&J)
00760000  SET DESCR  = &STR(&&R&FROM.LD&J)
00770000  SET ICMD   = &STR(&&R&FROM.C&J)
00780000  /* COPY THE HIDDEN MENU OPTION TO THE "TO" MENU VARIABLE POOL */
00790000  IF &STR(&OPT) ^=  THEN DO
00800003    SET HOPT = &HOPT + 1
00810003    SET R&TO.O&J  = &STR(&OPT)
00820000    SET R&TO.LD&J = &STR(&DESCR)
00830003    SET R&TO.C&J  = &STR(&ICMD)
00840000    ISPEXEC VPUT (R&TO.O&J,R&TO.LD&J,R&TO.C&J) PROFILE
00850000  END
00860000  SET K = &K + 1
00870000END
00880000SET R&TO.HOPT =
00890000IF &HOPT > 0 THEN SET R&TO.HOPT = YES
00900000ISPEXEC VPUT (R&TO.HOPT) PROFILE
00910000 
00920000 
00930000/****  COPY FULL ALIAS  OPTIONS ****/
00940000SET AOPT = 0
00950003SET AOPTF= 0
00960000SET K    = 1
00970000DO WHILE (&K <= 30)
00980000  /* GET VARIABLES USED BY THE "FROM" MENU */
00990000  /* CONVERT TO A TWO DIGIT NUMBER 01-09,10-30 */
01000000  IF &K <= 9 THEN SET J = &STR(0&K)
01010000  ELSE            SET J = &STR(&K)
01020000  ISPEXEC VGET (R&FROM.FA&J,R&FROM.FB&J) PROFILE
01030000  SET FA     = &STR(&&R&FROM.FA&J)
01040000  SET TA     = &STR(&&R&FROM.FB&J)
01050000  /* COPY THE FULL ALIAS  OPTION TO THE "TO" MENU VARIABLE POOL */
01060000  IF &STR(&FA) ^=  THEN DO
01070003    SET AOPT      = &AOPT  + 1
01080003    SET AOPTF     = &J /* MAX FULL ALIAS NUMBER */
01090000    SET R&TO.FA&J = &STR(&FA)
01100000    SET R&TO.FB&J = &STR(&TA)
01110000    ISPEXEC VPUT (R&TO.FA&J,R&TO.FB&J) PROFILE
01120000  END
01130000  SET K = &K + 1
01140000END
01150003 
01160000/****  COPY PARTIAL ALIAS  OPTIONS ****/
01170000SET K    = 1
01180003SET AOPTP= 0
01190000DO WHILE (&K <= 30)
01200000  /* GET VARIABLES USED BY THE "FROM" MENU */
01210000  /* CONVERT TO A TWO DIGIT NUMBER 01-09,10-30 */
01220000  IF &K <= 9 THEN SET J = &STR(0&K)
01230000  ELSE            SET J = &STR(&K)
01240000  ISPEXEC VGET (R&FROM.FA&J,R&FROM.FB&J) PROFILE
01250000  SET PA     = &STR(&&R&FROM.PA&J)
01260000  SET PA     = &STR(&&R&FROM.PB&J)
01270000  /* COPY THE FULL ALIAS  OPTION TO THE "TO" MENU VARIABLE POOL */
01280000  IF &STR(&PA) ^=  THEN DO
01290000    SET AOPT      = &AOPT + 1
01300003    SET AOPTP     = &J   /* MAX PREFIX ALIAS NUMBER */
01310000    SET R&TO.PA&J = &STR(&PA)
01320000    SET R&TO.PB&J = &STR(&PA)
01330000    ISPEXEC VPUT (R&TO.PA&J,R&TO.PB&J) PROFILE
01340000  END
01350000  SET K = &K + 1
01360000END
01370000 
01380000SET R&TO.TRNS =
01390000IF &AOPT > 0 THEN SET R&TO.TRNS = YES
01400003SET R&TO.FANN = &AOPTF
01410003SET R&TO.PANN = &AOPTP
01420003ISPEXEC VPUT (R&TO.TRNS,R&TO.FANN,R&TO.PANN) PROFILE
01430000 
01440000 
01450000 
01460000 
01470000/* DONE.NOW RESET THE SAVED MENU BUILD POOL AND MAX MENU OPTS VARS */
01480000 
01490000SET RMBFPOOL = &STR(&SAVEPOOL)
01500000SET RMBFMAXM = &STR(&SAVEMAXM)
01510000ISPEXEC VPUT (RMBFPOOL,RMBFMAXM)
01520000 
01530000SET SHORT = &STR(OPTIONS COPIED.)
01540000IF &MOPT = 0 AND &HOPT = 0 AND &AOPT = 0 THEN SET SHORT =&STR(NO OPTIONS COPIED.)
01550000SET LONG  = &STR(OPTIONS FROM=&FROM COPIED TO TO=&TO.. #MENU=&MOPT #HIDDEN=&HOPT #ALIAS=&AOPT..)
01560000ISPEXEC SETMSG MSG(SYS010)
