00010000ISREDIT MACRO (PARM)
00020000 
00030000/**********************************************************************/
00040000/* NAME: SPRINT   (LIONEL DYCK)                                       */
00050000/* NAME: EDSPRINT (MIKE THEYS)                                        */
00060000/*                                                                    */
00070000/* FUNCTION: TO PRINT A COPY OF THE DATASET CURRENTLY BEING EDITED.   */
00080000/*                                                                    */
00090000/* PARAMETERS:  DESTINATION FOR THE PRINT (OPTIONAL) OR "?" FOR HELP  */
00100000/*                                                                    */
00110000/* LOGIC:  1.  SAVE THE CURRENT DATASET (ONLY IF JUST CHANGED)        */
00120000/*         2.  EXECUTE PRINTOFF TO PRINT THE DATASET                  */
00130000/*                                                                    */
00140000/* MESSAGES: ISRZ001 IS USED FOR ALL MESSAGES                         */
00150000/*                                                                    */
00160000/* MODIFIED BY DAVE FAY,  04/08/87 FOR "?" OPERAND                    */
00170000/* MODIFIED BY MIKE THEYS 08/19/87 TO NOT SAVE IF NO CHANGES          */
00180000/**********************************************************************/
00190000 
00200000CONTROL FLUSH MSG NOPROMPT
00210000ISPEXEC CONTROL ERRORS CANCEL
00220000 
00230000IF      &STR(&PARM)     ^=    THEN DO
00240000  IF      &STR(&PARM) = ? THEN DO
00250000    ISPEXEC SELECT PGM(ISPTUTOR) PARM(#EDSPRINT)
00260000    EXIT
00270000  END
00280000  ELSE IF &DATATYPE(&PARM) = CHAR THEN SET DEST = &STR(&PARM)
00290000  ELSE DO
00300000    SET ZEDSMSG = &STR(INVALID ARGUMENT)
00310000    SET ZEDLMSG = &STR(THE ONLY VALID ARGUMENTS FOR EDSPRINT ARE A "?" OR A PRINTER DEST)
00320000    ISPEXEC SETMSG MSG(ISRZ001)
00330000    EXIT
00340000  END
00350000END
00360000 
00370000ISREDIT (CHANGED) = DATA_CHANGED
00380000IF &STR(&CHANGED) = YES THEN ISREDIT SAVE
00390000 
00400000ISREDIT (DSNAME) = DATASET
00410000ISREDIT (DSNMEM) = MEMBER
00420000 
00430000IF &DSNMEM ^= THEN SET DSNSINFO = &STR('&DSNAME(&DSNMEM)')
00440000ELSE               SET DSNSINFO = &STR('&DSNAME')
00450000 
00460000IF &DEST ^= THEN PRINTOFF &DSNSINFO RCL NOMSG DEST(&DEST)
00470000ELSE             PRINTOFF &DSNSINFO RCL NOMSG
00480000SET LINES = &LASTCC
00490000 
00500000IF &STR(&CHANGED) = YES THEN +
00510000  SET ZEDSMSG = &STR(SAVED AND PRINTED)
00520000ELSE +
00530000  SET ZEDSMSG = &STR(PRINTED)
00540000IF &DEST ^= THEN  SET ZEDLMSG = &STR(&DSNSINFO WAS PRINTED W/ &LINES LINES ON DEST=&DEST)
00550000ELSE              SET ZEDLMSG = &STR(&DSNSINFO WAS PRINTED W/ &LINES LINES)
00560000 
00570000ISPEXEC SETMSG MSG(ISRZ001)
