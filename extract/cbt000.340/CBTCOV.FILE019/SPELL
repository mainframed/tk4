00010000ISREDIT  MACRO    (SAVE DEBUG)
00020000 
00030000/**********************************************************************/
00040000/* DOCUMENT SPELLING VERIFICATION.                                    */
00050000/*                                                                    */
00060000/* THE SPELL MACRO INVOKES SCRIPT/VS TO VERIFY SPELLING IN A          */
00070000/* DOCUMENT. THE DOCUMENT IS FIRST SAVED IF IT HAS BEEN CHANGED.      */
00080000/* ISPF SKELETON SPELLCHK IS USED AS A SCRIPT/VS PROFILE TO WRITE     */
00090000/* MIS-SPELLED WORDS INTO FILE DSMUTWTF.  SPELL THEN READS DSMUTWTF   */
00100000/* AND INSERTS THE MIS-SPELLED WORDS AS MSG LINES AT THE TOP OF THE   */
00110000/* DOCUMENT. THE USER CAN THEN EDIT THE DOCUMENT TO CORRECT ANY       */
00120000/* ERRORS.                                                            */
00130000/**********************************************************************/
00140000 
00150000IF       &DEBUG = DEBUG +
00160000THEN     CONTROL  ASIS   CONLIST NOFLUSH   LIST   MSG   PROMPT   SYMLIST
00170000ELSE     CONTROL  ASIS NOCONLIST NOFLUSH NOLIST   MSG   PROMPT NOSYMLIST
00180000 
00190000ISREDIT  (SPELLDSN) = DATASET
00200000ISREDIT  (MEMBER) = MEMBER
00210000IF       &MEMBER ^= &STR( ) +
00220000THEN     SET      &SPELLDSN = &SPELLDSN(&MEMBER)
00230000 
00240000ISREDIT  (CHANGED) = DATA_CHANGED
00250000IF       &CHANGED = YES +
00260000THEN     DO
00270000         SET      &SAVED = 0
00280000         SET      &DISPLAY = 0
00290000         IF       &SUBSTR(1,&SYSCAPS(&SAVE )) ^= S +
00300000         THEN     DO
00310000                  ISPEXEC  DISPLAY  PANEL(SPELLSAV)
00320000                  SET      &DISPLAY = &LASTCC
00330000                  END
00340000         IF       &DISPLAY = 0 +
00350000         THEN     DO
00360000                  ISREDIT  SAVE
00370000                  SET      &SAVED = &LASTCC
00380000                  IF       &SAVED < 12 +
00390000                  THEN     DO
00400000                           SET      &ZEDSMSG = &STR(Data saved)
00410000                           SET      &ZEDLMSG = &STR(Data set &SPELLDSN saved)
00420000                           ISPEXEC  SETMSG   MSG(ISRZ001)
00430000                           END
00440000                  END
00450000         IF       &DISPLAY > 0 | &SAVED >= 12 +
00460000         THEN     DO
00470000                  SET      &ZEDSMSG = &STR(No spelling check)
00480000                  SET      &ZEDLMSG = &STR(Data not be saved--spelling check not done)
00490000                  ISPEXEC  SETMSG   MSG(ISRZ001)
00500000                  EXIT     CODE(&SAVED)
00510000                  END
00520000         END
00530000 
00540000ALLOCATE FILE(DSMUTWTF) UNIT(SYSVIO) DSORG(PS) RECFM(F B) LRECL(80) BLKSIZE(6160) REUSE
00550000ISPEXEC  LMINIT   DATAID(DSMUTWTF) DDNAME(DSMUTWTF)
00560000 
00570000ISPEXEC  VGET     (ZTEMPF)
00580000 
00590000SCRIPT   '&SPELLDSN' CONTINUE DEVICE(TERM) FILE('&ZTEMPF') MESSAGE(ID) PROFILE('AOST.ISPF.SKELS(SPELLCHK)') QUIET SPELLCHK
00600000 
00610000ISREDIT  RESET SPECIAL
00620000ISREDIT  LOCATE 0
00630000 
00640000ISPEXEC  LMOPEN   DATAID(&DSMUTWTF)
00650000ISPEXEC  LMGET    DATAID(&DSMUTWTF) MODE(INVAR) DATALOC(WORD) DATALEN(LEN) MAXLEN(80)
00660000SET      &LMGET = &LASTCC
00670000IF       &LMGET = 0 +
00680000THEN     ISREDIT  LINE_BEFORE 1 = MSGLINE 'The following words were not found in the spelling dictionary:'
00690000ELSE     ISREDIT  LINE_BEFORE 1 = MSGLINE 'No spelling errors were found.'
00700000ISREDIT  LINE_BEFORE 1 = MSGLINE ' '
00710000 
00720000DO       WHILE    &LMGET = 0
00730000         IF       &NRSTR(&WORD) ^= MSGLINE +
00740000         THEN     ISREDIT  LINE_BEFORE 1 = MSGLINE (WORD)
00750000         ISPEXEC  LMGET    DATAID(&DSMUTWTF) MODE(INVAR) DATALOC(WORD) DATALEN(LEN) MAXLEN(80)
00760000         SET      &LMGET = &LASTCC
00770000         END
00780000 
00790000ISPEXEC  LMCLOSE  DATAID(&DSMUTWTF)
00800000ISPEXEC  LMFREE   DATAID(&DSMUTWTF)
00810000FREE     FILE(DSMUTWTF)
00820000EXIT     CODE(1)
