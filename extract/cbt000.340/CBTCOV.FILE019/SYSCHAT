00010023PROC 0 USERID()
00020028 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
00030034 /* USED BY ISPF COMMAND "CHAT"                    (NEEDS TSO/E R3) */
00040028 /* CHAT SENDS MULTIPLE-LINE MESSAGE TO TSO USER VIA TSO SEND.      */
00050034 /* BY MIKE THEYS.      BASED ON JMTHELP FROM CEDAR RAPIDS AVIONICS */
00060028 /* THIS VERSION ALLOWS UPPER/LOWER CASE WITH NO JUSTIFICATION.     */
00070028 /* MESSAGE TEXT IS REMEMBERED FOR A RE-TRY MESSAGE.                */
00080028 /* SENDER NAME/PHONE ADDED TO HEADER LINE. DATE/TIME MESSAGE SENT. */
00090028 /* AVOIDS PROBLEMS WITH APOSTERPHE IN TSO SEND. CONVERTS TO QUOTE. */
00100028 /* AMPERSANDS DO NOT CAUSE CLIST PROBLEMS.                         */
00110028 /* WILL TELL IF USER IS LOGGED ON TSO AND IF ITS THIS CPU.         */
00120028 /* IF USER NOT ON TSO THIS CPU, HE WILL SEE AT NEXT LOGON.         */
00130028 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
00140007 CONTROL ASIS
00150046 ISPEXEC VGET (ROCCHATU) PROFILE
00160045 IF &STR(&USERID) ^=   THEN SET ROCCHATU = &USERID
00170043 ISPEXEC VGET (ROCCHAT1,ROCCHAT2,ROCCHAT3,+
00180043               ROCCHAT4,ROCCHAT5,ROCCHAT6,+
00190046               ROCCHAT7,ROCCHAT8,ROCCHAT9) PROFILE
00200028 
00210007 SET &ASTERS = &STR(****************************************)
00220040 SET &ASTERS = &SUBSTR(1:69,&STR(&ASTERS&ASTERS))
00230007 SET &BLANKS = &STR(                                        )
00240007 SET &BLANKS = &STR(&BLANKS&BLANKS)
00250028 
00260028 SHOWTSO NAME PHONE CENTER SYSNAME
00270028 SET CPU  = &SUBSTR(1:1,&SYSNAME)
00280039 SET NAME = &SUBSTR(1:20,&STR(&NAME                     ))
00290028 
00300028 SET &ENDKEY = NO
00310007 ISPEXEC DISPLAY PANEL(SYSCHAT)
00320028 IF &LASTCC = 8 THEN SET ENDKEY = YES
00330028 
00340010 DO WHILE (&ENDKEY = NO)
00350035   /* MAKE SURE CHAT LINES 1-9 DON'T HAVE 'S. CONVERT TO "S */
00360040   SET M = 1
00370042   DO WHILE(&M <= 9)
00380043     SET S = &STR(&SYSNSUB(2,&&ROCCHAT&M)
00390047     SET #Q = 0
00400040     IF &STR(&SYSNSUB(1,&S)) ^=    THEN DO
00410035       SET V =
00420035       SET K = 1
00430040       SET L = &LENGTH(&STR(&SYSNSUB(1,&S)))
00440047       SET &Q = 999
00450040       DO WHILE (&K <= &L AND &Q ^= 0)
00460041         SET &Q = &SYSINDEX(',&STR(&SYSNSUB(1,&S)),&K)
00470041         IF &Q > 0 THEN DO
00480047           SET   #Q = &#Q + 1
00490041           SET    V = &STR(&SYSNSUB(1,&V)&SUBSTR(&K:&Q,&STR(&SYSNSUB(1,&S)))')
00500041         END
00510041         ELSE SET V = &STR(&SYSNSUB(1,&V)&SUBSTR(&K:&L,&STR(&SYSNSUB(1,&S))))
00520040         SET K  = &Q  + 1
00530035       END
00540041       SET S = &STR(&SYSNSUB(1,&V))
00550040       SET N = &M
00560035     END
00570047     SET &ROCSEND&M = &SUBSTR(1:65+&#Q,&STR(&SYSNSUB(1,&S&BLANKS)))
00580040     SET M = &M + 1
00590035   END
00600028 
00610040   SET &HEAD     = &SUBSTR(1:65,&STR(CHAT Message.   From: &SYSUID  &NAME  &PHONE&BLANKS))
00620040   SET &BLANK    = &SUBSTR(1:65,&STR(&BLANKS))
00630043   SEND &STR('&ASTERS')    USER(&ROCCHATU) LOGON
00640043   SEND &STR('* &HEAD *')  USER(&ROCCHATU) LOGON
00650043   SEND &STR('* &BLANK *') USER(&ROCCHATU) LOGON
00660040   SET  M = 1
00670040   DO WHILE(&N >= &M)
00680046     SEND &STR('* &SYSNSUB(2,&&ROCSEND&M) *') USER(&ROCCHATU) LOGON
00690040     SET M = &M + 1
00700037   END
00710040   SET &TRAIL    = &SUBSTR(1:65,&STR(End of CHAT message.                    Sent &SYSTIME on &SYSDATE&BLANKS))
00720043   SEND &STR('* &BLANK *')  USER(&ROCCHATU) LOGON
00730043   SEND &STR('* &TRAIL *')  USER(&ROCCHATU) LOGON
00740043   SEND &STR('&ASTERS')     USER(&ROCCHATU) LOGON
00750028 
00760043   %ISONCTR &ROCCHATU
00770017   SET CT = &LASTCC
00780017   SET CP = 3
00790017   IF &CT = 0 THEN DO
00800043     %ISONCPU &ROCCHATU
00810017     SET CP = &LASTCC
00820017   END
00830017   IF      &CT  = 0 AND &CP  = 0 THEN +
00840043     SET LONG = &STR(&ROCCHATU was logged onto this TSO at &CENTER..  Message sent.)
00850017   ELSE IF &CT  = 0 AND &CP ^= 0 THEN +
00860043     SET LONG = &STR(&ROCCHATU was logged onto TSO at &CENTER but NOT this cpu (&CPU). Will see next logon.)
00870017   ELSE +
00880043     SET LONG = &STR(&ROCCHATU was NOT logged onto TSO at this center. Will see message at logon.)
00890017   SET SHORT =
00900011   ISPEXEC SETMSG MSG(SYS010)
00910028 
00920007   ISPEXEC DISPLAY PANEL(SYSCHAT)
00930010   IF &LASTCC = 8 THEN SET ENDKEY = YES
00940007 END
00950028 
00960007 EXIT CODE(0)
