00010037 PROC 1 D  C() COPIES()  DEST()  ASA MACH NOCC  OPTIONS TUTORIAL +
00020029        SS DS  NOHEADING NOHEAD NOHDG NOH  CAPS ASIS DEBUG
00030025/**********************************************************************/
00040025/* NAME: BRPRINT    BY MIKE THEYS                                     */
00050025/*                                                                    */
00060025/* FUNCTION: TO PRINT A COPY OF THE DATASET CURRENTLY BEING BROWSED.  */
00070025/*                                                                    */
00080025/* PARAMETERS:  DESTINATION FOR THE PRINT (OPTIONAL)                  */
00090025/*                                                                    */
00100025/* DEPENDENCIES: ISRBROB/ISRBROBF MODIFIED TO SAVE BRDSN/BRMEM/BRVOL  */
00110025/*               FOR THAT &ZSCREEN.  PANELS ALSO HAVE CNA BR * MODS.  */
00120033/*               TSO/E R3  for SYSINDEX/LISTDSI                       */
00130025/*                                                                    */
00140025/* MESSAGES: ISRZ002 IS USED FOR THE MESSAGES                         */
00150025/*                                                                    */
00160025/* ALSO SEE THE EDIT MACRO FROM LIONEL DYCK:                          */
00170025/* SPRINT/EDSPRINT WILL SAVE AND PRINT CURRENT EDIT DATASET/MEMBER.   */
00180025/**********************************************************************/
00190025  CONTROL FLUSH MSG NOPROMPT ASIS
00200025  ISPEXEC CONTROL ERRORS CANCEL
00210025  IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST
00220025 
00230025  /* Strip off the "!" before "dest" for C( COPIES( ASA MACH NOCC    */
00240025  /* BRPRINT RMJ70 C(2)  ===>  D="!RMJ70"  C=2  => DEST=RMJ70 C=2    */
00250025  /* BRPRINT C(2)        ===>  D="!C(2)"        => DEST=      C=2    */
00260025  SET L = &LENGTH(&STR(&D))
00270025  IF &STR(&D) = &STR(!) THEN SET D =
00280025  ELSE DO
00290025    /* Is first field passed on BRPRINT: C( COPIES( ASA MACH NOCC */
00300032    SET RPC = )
00310032    SET LPC = &STR((
00320025    SET D = &SUBSTR(2:&L,&STR(&D))
00330025    SET L = &LENGTH(&STR(&D))
00340032    SET LP = &SYSINDEX(&STR(&LPC),&STR(&D),1)
00350032    SET RP = &SYSINDEX(&STR(&RPC),&STR(&D),1)
00360025    IF &LP < &RP THEN DO
00370025      IF &L >= 4 THEN IF &SUBSTR(1:2,&STR(&D)) = C(       THEN +
00380025         SET COPIES = &SUBSTR(&LP+1:&RP-1,&STR(&D))
00390025      IF &L >= 9 THEN IF &SUBSTR(1:7,&STR(&D)) = COPIES(  THEN +
00400025         SET COPIES = &SUBSTR(&LP+1:&RP-1,&STR(&D))
00410025      IF &L >= 7 THEN IF &SUBSTR(1:5,&STR(&D)) = DEST(     THEN +
00420025         SET DEST   = &SUBSTR(&LP+1:&RP-1,&STR(&D))
00430025    END
00440027    ELSE IF &STR(&D) = ASA        THEN SET ASA  = ASA
00450027    ELSE IF &STR(&D) = MACH       THEN SET MACH = MACH
00460027    ELSE IF &STR(&D) = NOCC       THEN SET NOCC = NOCC
00470027    ELSE IF &STR(&D) = SS         THEN SET SS   = SS
00480027    ELSE IF &STR(&D) = DS         THEN SET DS   = DS
00490029    ELSE IF &STR(&D) = CAPS       THEN SET CAPS = CAPS
00500029    ELSE IF &STR(&D) = ASIS       THEN SET ASIS = ASIS
00510029    ELSE IF &STR(&D) = NOHEADING  THEN SET NOHEADING = NOHEADING
00520027    ELSE IF &STR(&D) = NOHEAD     THEN SET NOHEADING = NOHEADING
00530027    ELSE IF &STR(&D) = NOHDG      THEN SET NOHEADING = NOHEADING
00540027    ELSE IF &STR(&D) = NOH        THEN SET NOHEADING = NOHEADING
00550036    ELSE IF &STR(&D) = OPTIONS    THEN SET OPTIONS = OPTIONS
00560037    ELSE IF &STR(&D) = &STR(!)    THEN SET OPTIONS = OPTIONS
00570037    ELSE IF &STR(&D) = &STR(?)    THEN SET TUTORIAL= TUTORIAL
00580027    ELSE IF &STR(&DEST) =         THEN SET DEST = &STR(&D)
00590025  END
00600025 
00610037  IF &TUTORIAL = TUTORIAL THEN DO
00620037    ISPEXEC SELECT PGM(ISPTUTOR) PARM($BRPRINT)
00630037    EXIT
00640037  END
00650037 
00660025  IF &STR(&COPIES) ^=   THEN DO
00670025    IF &DATATYPE(&COPIES) ^= NUM THEN DO
00680025      SET ZERRSM = &STR(COPIES not numeric)
00690025      SET ZERRLM = &STR(COPIES(&COPIES) is not valid. Value should be 1 thru 10)
00700025      SET ZERRALRM=YES
00710025      SET ZERRHM = &STR(*)
00720025      ISPEXEC SETMSG MSG(ISRZ002)
00730025      EXIT
00740025    END
00750025    IF &COPIES <= 0 | &COPIES > 10 THEN DO
00760025      SET ZERRSM = &STR(Only 1 to 10 COPIES)
00770025      SET ZERRLM = &STR(COPIES(&COPIES) is out of allowed range for BRPRINT)
00780025      SET ZERRALRM=YES
00790025      SET ZERRHM = &STR(*)
00800025      ISPEXEC SETMSG MSG(ISRZ002)
00810025      EXIT
00820025    END
00830025  END
00840025 
00850025  ISPEXEC VGET (ZSCREEN)
00860025  ISPEXEC VGET (BRDSN&ZSCREEN,BRMEM&ZSCREEN,BRVOL&ZSCREEN)
00870025  SET BRDSN = &&BRDSN&ZSCREEN
00880025  SET BRMEM = &&BRMEM&ZSCREEN
00890025  SET BRVOL = &&BRVOL&ZSCREEN
00900025 
00910025  IF &STR(&BRDSN)  = THEN  DO
00920025    SET ZERRSM = &STR(No browse dsn to print)
00930025    SET ZERRLM = &STR(No dataset has been browsed recently from this ISPF screen #&ZSCREEN..)
00940025    SET ZERRALRM=YES
00950025    SET ZERRHM = &STR(*)
00960025    ISPEXEC SETMSG MSG(ISRZ002)
00970025    EXIT
00980025  END
00990025 
01000025  IF &STR(&BRMEM) ^= THEN  SET BRPDSN = &STR('&BRDSN(&BRMEM)')
01010025  ELSE                     SET BRPDSN = &STR('&BRDSN')
01020025 
01030025  /* CHECK TO SEE IF CATALOGED DATASET STILL EXITS */
01040025  IF &STR(&BRVOL) =  THEN DO
01050025    IF &SYSDSN(&STR(&BRPDSN)) ^= OK THEN DO
01060025      WHATIF DATASET(&BRPDSN) NALLOCATED
01070025      /* If its still allocated then try to print. If not EXIT. */
01080025      IF &LASTCC = 0 THEN DO
01090025        SET ZERRSM = &STR(Can't find browse dsn)
01100025        SET ZERRLM = &STR(Can't print &BRPDSN.. It's gone or uncatlged.)
01110025        SET ZERRALRM=YES
01120025        SET ZERRHM = &STR(*)
01130025        ISPEXEC SETMSG MSG(ISRZ002)
01140025        EXIT
01150025      END
01160025    END
01170025  END
01180025 
01190025  /* CURRENTLY AT SWCC ONLY: GET MSG IKJ56241I */
01200025  /* GET NO UNIT AVAILABLE WHEN USING THE VOLUME() OPERAND */
01210030  IF &BRVOL ^= THEN  SET VOLUME = &STR(VOLUME(&BRVOL))
01220025  ELSE               SET VOLUME =
01230025 
01240025  ISPEXEC VGET (BRPPOSET) PROFILE
01250025  /* See if BRPRINT PRINTOFF Defaults have ever been set */
01260037  IF &STR(&BRPPOSET)=  | &STR(&OPTIONS)=OPTIONS THEN DO
01270025    SET DEST =
01280025    SHOWTSO DEST=BRPADEST
01290025    SET BRPVOL = &BRVOL
01300025    ISPEXEC VPUT (BRPDSN,BRPVOL,BRPADEST)
01310025    ISPEXEC DISPLAY PANEL(BRPRINT)
01320025    IF &LASTCC >= 8 THEN EXIT /* ABORTED */
01330025  END
01340025 
01350025  ISPEXEC VGET (BRPDEST,BRPCLASS,BRPCAPS,BRPPLN) PROFILE
01360025  IF      &DEST     ^=   THEN SET VDEST = &STR(DEST(&DEST))
01370025  ELSE IF &BRPDEST  ^=   THEN SET VDEST = &STR(DEST(&BRPDEST))
01380025  ELSE                        SET VDEST =
01390029  IF      &CAPS = CAPS   THEN SET BRPCAPS = CAPS
01400029  ELSE IF &ASIS = ASIS   THEN SET BRPCAPS = ASIS
01410025  IF &BRPCLASS ^=        THEN SET VCLAS = &STR(CLASS(&BRPCLASS))
01420025  ELSE                        SET VCLAS =
01430025  IF &BRPPLN   ^=        THEN SET VPLN  = &STR(PAGELENGTH(&BRPPLN))
01440025  ELSE                        SET VPLN  =
01450025  IF &BRPPLN    =  0     THEN SET VPLN  = &STR(NOPL)
01460025  IF      &COPIES ^=     THEN SET VCOP  = &STR(COPIES(&COPIES))
01470025  ELSE IF &C      ^=     THEN SET VCOP  = &STR(COPIES(&C))
01480025  ELSE                        SET VCOP  =
01490027  IF      &NOHEAD = NOHEAD  THEN SET NOHEADING = NOHEADING
01500027  ELSE IF &NOHDG  = NOHDG   THEN SET NOHEADING = NOHEADING
01510027  ELSE IF &NOH    = NOH     THEN SET NOHEADING = NOHEADING
01520027  SET BRPADDED = &STR(&VOLUME RCL NOMSG &VDEST &VCLAS &VPLN  &BRPCAPS +
01530027                      &ASA &MACH &NOCC &SS &DS &NOHEADING &VCOP)
01540025 
01550025  PRINTOFF &BRPDSN &BRPADDED
01560025  SET LINES = &LASTCC
01570025 
01580025  SET ZERRSM = &STR(BRdsn  Printed)
01590025  SET ZERRALRM=YES
01600025  SET ZERRHM = &STR(*)
01610025  IF &BRVOL^= THEN  SET VOLINFO= &STR( on &BRVOL)
01620025  ELSE              SET VOLINFO=
01630025  SET ZERRLM = &STR(&BRPDSN&VOLINFO printed w/ &LINES lines &VCOP &VDEST)
01640025 
01650025  ISPEXEC SETMSG MSG(ISRZ002)
