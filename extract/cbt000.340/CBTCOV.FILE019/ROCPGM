00010007PROC 1 PGM  PARM() NEWAPPL() NOCHECK NEWPOOL NOFUNC USRPFK(OFF)
00020006IF &DEBUG = DEBUG THEN  CONTROL   MSG   LIST   CONLIST   SYMLIST
00030006ELSE                    CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST
00040000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00050000/**                                                                **/
00060000/** This clist is now used by many ISPF commands to invoke a       **/
00070000/** PGM with an optionial PARM under the specified NEWAPPL.        **/
00080000/** If the current ISPF ZAPPLID is alread the same as NEWAPPL,     **/
00090000/** then do NOT add the NEWAPPL to the ISPEXEC SELECT PGM.         **/
00100000/** This prevents the SHARED pool from being wiped out.            **/
00110000/**                                                                **/
00120000/** By Mike Theys  01/26/87                                        **/
00130000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00140000ISPEXEC CONTROL ERRORS RETURN
00150000ISPEXEC VGET    (ZAPPLID)
00160000SET VOPT  =
00170000IF &STR(&NEWAPPL)  =          THEN SET NEWAPPL = ISR
00180000IF &STR(&NEWAPPL) ^= &ZAPPLID THEN SET VAPPL = &STR( NEWAPPL(&NEWAPPL))
00190000IF &STR(&PARM)    ^=          THEN SET VPARM = &STR( PARM(&PARM))
00200002 
00210004IF &STR(&VAPPL) =  THEN DO /* Save    Rockwell modifable PF Keys */
00211008  IF &USRPFK = ON THEN DO  /* Rockwell modifable PF Keys in use  */
00220007    ISPEXEC VGET (USRUP,USRDOWN,USRRIGHT,USRLEFT,USRRFIND,USRRCHG)
00230007    SET SAVUP    = &USRUP
00240007    SET SAVDOWN  = &USRDOWN
00250007    SET SAVRIGHT = &USRRIGHT
00260007    SET SAVLEFT  = &USRLEFT
00270007    SET SAVRFIND = &USRRFIND
00280007    SET SAVRCHG  = &USRRCHG
00290007    /* Clear   Rockwell modifable PF Keys for IBM PF Key use */
00300007    SET USRUP    =
00310007    SET USRDOWN  =
00320007    SET USRRIGHT =
00330007    SET USRLEFT  =
00340007    SET USRRFIND =
00350007    SET USRRCHG  =
00360007    ISPEXEC VPUT (USRUP,USRDOWN,USRRIGHT,USRLEFT,USRRFIND,USRRCHG)
00361007  END
00370004END
00380000 
00390001ISPEXEC SELECT PGM(&PGM) &VPARM &VAPPL &NOCHECK &NEWPOOL &NOFUNC
00400000 
00410005IF &LASTCC > 4 THEN DO
00420005  CONTROL ASIS
00430005  SET SHORT = &STR(ERROR with &PGM)
00440005  SET LONG  = &STR(An error occurred executing program &PGM using PARM(&PARM))
00450005  ISPEXEC SETMSG MSG(SYS010)
00460005END
00470002 
00480003IF &STR(&VAPPL) =  THEN DO /* Restore Rockwell modifable PF Keys */
00480108  IF &USRPFK = ON THEN DO  /* Rockwell modifable PF Keys in use  */
00490007    SET USRUP    = &SAVUP
00500007    SET USRDOWN  = &SAVDOWN
00510007    SET USRRIGHT = &SAVRIGHT
00520007    SET USRLEFT  = &SAVLEFT
00530007    SET USRRFIND = &SAVRFIND
00540007    SET USRRCHG  = &SAVRCHG
00550007    ISPEXEC VPUT (USRUP,USRDOWN,USRRIGHT,USRLEFT,USRRFIND,USRRCHG)
00551007  END
00560002END
