00010001          ISREDIT MACRO (DSN TSO PARMS) NOPROCESS
00020001 
00030001 /*********************************************************************/
00040001 /*   BOB ZIMMERMAN                                                   */
00050001 /*   CNA INSURANCE                                                   */
00060001 /*   (312) 822-5719                                                  */
00070001 /*   LAST MODIFIED 8505                                              */
00080001 /*********************************************************************/
00090001 
00100001          ISPEXEC CONTROL ERRORS CANCEL
00110001 
00120001          IF &STR(&PARMS) ^= THEN +
00130001              DO
00140001              SET ZEDSMSG = &STR(INVALID PARAMETERS)
00150001              SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO +
00160001              SYNTAX ONLY)
00170001              ISPEXEC SETMSG MSG(ISRZ001)
00180001              EXIT CODE(12)
00190001              END
00200001 
00210001          IF &STR(&TSO) = OR &STR(&TSO) = TSO THEN
00220001          ELSE +
00230001              DO
00240001              SET ZEDSMSG = &STR(INVALID PARAMETER)
00250001              SET ZEDLMSG = &STR(THE ONLY VALID PARAMETER FOR THIS +
00260001              COMMAND IS "TSO")
00270001              ISPEXEC SETMSG MSG(ISRZ001)
00280001              EXIT CODE(12)
00290001              END
00300001 
00310001          ISREDIT PROCESS DEST
00320001          IF &LASTCC ^= 0 THEN +
00330001              DO
00340001              SET ZEDSMSG = &STR(GENJCL COMMAND PENDING)
00350001              SET ZEDLMSG = &STR(ENTER AN 'A' OR 'B' LINE COMMAND WITH +
00360001              THE 'GENJCL' PRIMARY COMMAND)
00370001              ISPEXEC SETMSG MSG(ISRZ001)
00380001              EXIT CODE(12)
00390001              END
00400001 
00410001          IF &STR(&DSN) = THEN +
00420001              DO
00430001              SET ZEDSMSG = &STR(SPECIY A DATASET)
00440001              SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO +
00450001              SYNTAX)
00460001              ISPEXEC SETMSG MSG(ISRZ001)
00470001              EXIT CODE(12)
00480001              END
00490001 
00500001         SET DSNINFO = &STR(&DSN)
00510001         ISPEXEC CONTROL ERRORS RETURN
00520001         ISPEXEC LMINIT DATAID(ID) DATASET(&DSNINFO)
00530001         SET LCC = &LASTCC
00540001         ISPEXEC CONTROL ERRORS CANCEL
00550001         IF &LCC ^= 0 THEN DO
00560001             ISPEXEC SETMSG MSG(&ZERRMSG)
00570001             EXIT CODE(12)
00580001             END
00590001 
00600001         ISPEXEC VPUT (DSNINFO) SHARED
00610001         ISPEXEC CONTROL ERRORS RETURN
00620001         ISPEXEC SELECT PGM(DSNINFO) PARM(DSNINFO)
00630001         SET LCC = &LASTCC
00640001         ISPEXEC CONTROL ERRORS CANCEL
00650001         ISPEXEC LMFREE DATAID(&ID)
00660001         IF &LCC ^= 0 THEN DO
00670001              SET ZEDSMSG = &STR(DATASET NOT FOUND)
00680001              SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO +
00690001              SYNTAX)
00700001              ISPEXEC SETMSG MSG(ISRZ001)
00710001              EXIT CODE(12)
00720001              END
00730001         ISPEXEC VGET (DEVTYPE VOLSER LRECL BLKSIZE RECFM) SHARED
00740001         ISPEXEC VGET (DSORG DIRALLOC SCALO TRALC ATRECFM) SHARED
00750001 
00760001         IF &SUBSTR(1:1,&DSN) ^= &STR(') THEN DO
00770001             SET DSN = &STR(&SYSPREF..&DSN)
00780001             END
00790001         ELSE DO
00800001              SET &DSN = &SUBSTR(2:&LENGTH(&DSN)-1,&DSN)
00810001              END
00820001 
00830001          IF &STR(&TSO) = TSO THEN GOTO FLOWTSO
00840001 
00850001 FLOWIT:  +
00860001          IF &STR(&DIRALLOC) ^= THEN SET DIR = &STR(,&DIRALLOC)
00870001          ELSE SET DIR =
00880001          ISREDIT (DEST) = LINENUM .ZDEST
00890001          SET LINE = &STR(//GENJCL  DD  DSN=&DSN,)
00900001          ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
00910001          SET DEST = &DEST + 1
00920001          SET LINE = &STR(//            DISP=(NEW,CATLG),)
00930001          ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
00940001          SET DEST = &DEST + 1
00950001          SET LINE = &STR(//            UNIT=&DEVTYPE,)
00960001          ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
00970001          SET DEST = &DEST + 1
00980001          SET LINE = &STR(//            VOL=SER=&VOLSER,)
00990001          ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
01000001          SET DEST = &DEST + 1
01010001          SET LINE = &STR(//            DCB=(LRECL=&LRECL,+
01020001          BLKSIZE=&BLKSIZE,RECFM=&RECFM,DSORG=&DSORG),)
01030001          ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
01040001          SET DEST = &DEST + 1
01050001          SET LINE = &STR(//            SPACE=(TRK,(&TRALC,+
01060001          &SCALO.&DIR)))
01070001          ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
01080001 
01090001          EXIT CODE(0)
01100001 
01110001 FLOWTSO: +
01120001          IF &DIRALLOC = THEN SET DIR =
01130001          ELSE SET DIR = &STR(DIR(&DIRALLOC))
01140001          ISREDIT (DEST) = LINENUM .ZDEST
01150001          SET LINE = &STR(ALLOC DA('&DSN') NEW CATALOG +)
01160001          ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
01170001          SET DEST = &DEST + 1
01180001          SET LINE = &STR(    UNIT(&DEVTYPE) VOL(&VOLSER) +)
01190001          ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
01200001          SET DEST = &DEST + 1
01210001          SET LINE = &STR(    LRECL(&LRECL) BLKSIZE(&BLKSIZE) +)
01220001          ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
01230001          SET DEST = &DEST + 1
01240001          SET LINE = &STR(    RECFM(&ATRECFM) &DIR +
01250001          DSORG(&DSORG) +)
01260001          ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
01270001          SET DEST = &DEST + 1
01280001          SET LINE = &STR(    SPACE(&TRALC &SCALO) TRACK)
01290001          ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
01300001 
01310001          EXIT CODE(0)
