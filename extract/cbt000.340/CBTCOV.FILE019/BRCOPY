00010001 PROC 0 PARM()
00020000/**********************************************************************/
00030001/* NAME: BRcopy   By Michael E. Theys   Rockwell International  (V1C) */
00040000/* FUNCTION: TO MAKE  A COPY OF THE DATASET CURRENTLY BEING BROWSED.  */
00050001/* PARAMETERS:  Menu will prompt for Datasetname.                     */
00060001/*              ?     Show the BRCOPY tutorial                        */
00070000/* DEPENDENCIES: ISRBROB/ISRBROBF MODIFIED TO SAVE BRDSN/BRMEM/BRVOL  */
00080000/*               FOR THAT &ZSCREEN.  PANELS ALSO HAVE CNA BR * MODS.  */
00090001/*               TSO/E R3 SYSINDEX/LISTDSI                            */
00100000/* MESSAGES: ISRZ002 IS USED FOR THE MESSAGES                         */
00110000/* ALSO SEE BRPRINT CLIST TO PRINT CURRENT BROWSE DATASET/MEMBER.     */
00120001/* DEFINED AS AN ISPF COMMAND "SELECT CMD(%BRCOPY PARM('&ZPARM'))"    */
00130000/**********************************************************************/
00140001  CONTROL MSG NOPROMPT ASIS
00150000  ISPEXEC CONTROL ERRORS CANCEL
00160001 
00170001  IF &STR(&PARM) = &STR(?) THEN DO
00180001    ISPEXEC SELECT PGM(ISPTUTOR) PARM($BRCOPY)
00190001    EXIT
00200001  END
00210000 
00220004  ISPEXEC VGET (ZSCREEN,ZAPPLID)
00230001  ISPEXEC VGET (BRDSN&ZSCREEN,BRMEM&ZSCREEN,BRVOL&ZSCREEN)
00240001  SET BRDSN  = &&BRDSN&ZSCREEN
00250001  SET BRMEM  = &&BRMEM&ZSCREEN
00260001  SET BRVOL  = &&BRVOL&ZSCREEN
00270000 
00280000  IF &STR(&BRDSN)  = THEN  DO
00290001    SET ZERRSM = &STR(No browse dsn to copy)
00300000    SET ZERRLM = &STR(No dataset has been browsed recently from this ISPF screen #&ZSCREEN..)
00310000    SET ZERRALRM=YES
00320000    SET ZERRHM = &STR(*)
00330000    ISPEXEC SETMSG MSG(ISRZ002)
00340000    EXIT
00350000  END
00360000 
00370000  IF &STR(&BRMEM) ^= THEN  SET DSNCINFO = &STR('&BRDSN(&BRMEM)')
00380000  ELSE                     SET DSNCINFO = &STR('&BRDSN')
00390000 
00400001  /* Check to see if cataloged dataset still exists */
00410000  IF &STR(&BRVOL) =  THEN DO
00420000    IF &SYSDSN(&STR(&DSNCINFO)) ^= OK THEN DO
00430000      WHATIF DATASET(&DSNCINFO) NALLOCATED
00440000      /* If its still allocated then try to copy. If not EXIT. */
00450000      IF &LASTCC = 0 THEN DO
00460001        SET ZERRSM = &STR(Can't find browse dsn)
00470000        SET ZERRLM = &STR(Can't copy  &DSNCINFO.. It's gone or uncatlged.)
00480000        SET ZERRALRM=YES
00490000        SET ZERRHM = &STR(*)
00500000        ISPEXEC SETMSG MSG(ISRZ002)
00510000        EXIT
00520000      END
00530000    END
00540000  END
00550001 
00560000  /* Get the name of TO dataset */
00570001  SET &BRCMSG1  =
00580001  SET &BRCMSG2  =
00590001  TRYAGAIN:+
00600001  SET &BRCMSG3  =
00610001  SET &BRCMSG4  =
00620001  SET &BRCISOK  =
00630001  SET &BRVOLUME = &BRVOL
00640000  ISPEXEC DISPLAY PANEL(BRCOPY)
00650001  IF &LASTCC > 0 THEN DO
00660000    SET ZERRALRM=NO
00670000    SET ZERRHM = &STR(*)
00680000    SET ZERRSM = &STR(BRdsn Copy Abort.)
00690000    SET ZERRLM = &STR(You didn't want a COPY of &DSNCINFO&VOLINFO..)
00700000    ISPEXEC SETMSG MSG(ISRZ002)
00710000    EXIT
00720000  END
00730000 
00740003CKAGAIN:+
00750001  ISPEXEC VGET (BRCOPYTO)
00760002  SET SVCOPYTO = &STR(&BRCOPYTO)
00770001 
00780001  /* Must next use dsname only, without PDS member name if present */
00790001  SET RPS = )
00800001  SET LPS = &STR((
00810001  SET &DSNAME = &STR(&BRCOPYTO)
00820001  SET LP = &SYSINDEX(&LPS,&DSNAME)
00830001  SET RP = &SYSINDEX(&RPS,&DSNAME)
00840001  /* See if member specified. if so strip out */
00850001  SET MEMBER =
00860001  IF (&LP > 0) AND (&RP > 0) AND (&LP+1 <= &RP-1) THEN +
00870001    SET MEMBER = &SUBSTR(&LP+1:&RP-1,&STR(&DSNAME))
00880001  /* If member was specified just get PDS name */
00890001  IF &STR(&MEMBER) ^=    THEN DO
00900001    IF  &SYSINDEX(',&DSNAME) = 0 THEN +
00910001      SET DSNAME = &SUBSTR(1:&LP-1,&STR(&DSNAME))
00920001    ELSE +
00930001      SET DSNAME = &STR('&SUBSTR(2:&LP-1,&STR(&DSNAME))')
00940001  END
00950001 
00960001  SET TOSTATUS = &SYSDSN(&DSNAME)
00970001  IF &STR(&TOSTATUS) = OK THEN DO
00980000    /* Dataset already exists. Find Its current RECFM/LRECL/BLKSIZE */
00990000    /* Invoke TSO COPY command with DCB info so it won't modify dsn */
01000001    LISTDSI &DSNAME
01010001    SET DCBINFO = &STR( RECFM(&SYSRECFM) LRECL(&SYSLRECL) BLOCK(&SYSBLKSIZE) )
01020001    SET BRCMSG1 = &STR(COPY TO dataset currently exists)
01030001    SET BRCMSG2 = &STR(Will copy preserving output DCB of &DCBINFO)
01040001    SET BRCMSG3 = &STR(OK to update? (Y/N))
01050001    SET BRCMSG4 = &STR({Y/N})
01060001    ISPEXEC DISPLAY PANEL(BRCOPY)
01070001    ISPEXEC VGET (BRCISOK)
01080001    IF &BRCISOK ^= Y THEN DO
01090001      SET ZERRALRM=NO
01100001      SET ZERRHM = &STR(*)
01110001      SET ZERRSM = &STR(BRdsn Copy Abort.)
01120001      SET ZERRLM = &STR(You didn't want to overwrite &BRCOPYTO w/ &DSNCINFO)
01130001      ISPEXEC SETMSG MSG(ISRZ002)
01140001      EXIT
01150001    END
01160002    IF &STR(&SVCOPYTO) ^= &STR(&BRCOPYTO) THEN GOTO CKAGAIN
01170000  END
01180001  ELSE IF &STR(&TOSTATUS) ^= &STR(MEMBER NOT FOUND)  AND +
01190001          &STR(&TOSTATUS) ^= &STR(DATASET NOT FOUND) THEN DO
01200001      SET ZERRALRM=NO
01210001      SET ZERRHM = &STR(*)
01220001      SET ZERRSM = &STR(Error with TO dsn)
01230001      SET ZERRLM = &STR(&BRCOPYTO: &TOSTATUS)
01240001      SET BRCMSG1= &STR(TO dataset STATUS: &TOSTATUS)
01250001      ISPEXEC SETMSG MSG(ISRZ002)
01260001      GOTO TRYAGAIN
01270001  END
01280000 
01290001  /* Set up to trap bad copy messages, like Seq to PDS */
01300001  SET &BRCMSG1  =
01310001  SET &BRCMSG2  =
01320001  CONTROL NOMSG
01330001  FREE DA(&BRCOPYTO.)
01340001  CONTROL   MSG NOFLUSH
01350001  SET SYSOUTTRAP = 5
01360001    COPY    &DSNCINFO,&BRCOPYTO.  NONUM  &DCBINFO
01370001    SET COPYRC = &LASTCC
01380001  SET TRAPKNT = &SYSOUTLINE
01390001  SET SYSOUTTRAP = 0
01400001  CONTROL NOMSG FLUSH
01410001  FREE DA(&BRCOPYTO.)
01420001  CONTROL   MSG
01430001 
01440001  IF &TRAPKNT > 0 THEN DO
01450001    SET BRCMSG1 = &STR(&SYSOUTLINE1)
01460001    IF &TRAPKNT > 1 THEN SET BRCMSG2 = &STR(&SYSOUTLINE2)
01470001    GOTO TRYAGAIN
01480001  END
01490000 
01500000  IF &COPYRC > 0 THEN DO
01510001    SET ZERRSM = &STR(BRdsn Copy Err)
01520000    SET ZERRLM = &STR(RC=&COPYRC for COPY of &DSNCINFO&VOLINFO to &BRCOPYTO..)
01530000  END
01540000  ELSE DO
01550001    SET ZERRSM = &STR(BRdsn Copied OK)
01560000    IF &BRVOL^= THEN  SET VOLINFO= &STR( on &BRVOL)
01570000    ELSE              SET VOLINFO=
01580000    SET ZERRLM = &STR(Dataset &DSNCINFO&VOLINFO copied to &BRCOPYTO..)
01590004    /* Free FROM dataset if NOT open. Don't do if under File-AID */
01600004    CONTROL NOMSG
01610004    IF &SYSINDEX(&ZAPPLID,FAXE.) = 0 THEN FREE DA(&DSNCINFO)
01620000  END
01630000  SET ZERRALRM=YES
01640000  SET ZERRHM = &STR(*)
01650000  ISPEXEC SETMSG MSG(ISRZ002)
