00010021PROC 0 DEBUG INIT SCROLL UP DOWN LEFT RIGHT AMT()
00020025                    /* * * * * * * * * * * * * * * * * * * * * * * */
00030017                    /* SWCC 01/10/87 V2: Added YOUR Option         */
00040017  SET VERSION = V2  /* <=== CHANGE for Next Version of SYS Options */
00050011                    /*      Will auto reload NEW SYS/ISPF Menu Opts*/
00060025                    /*      MBF Saved Table "RMBFSYSP"             */
00070010  /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
00080001  /* SYS@PRIM Scrollable Primary Menu with 36 menu options         */
00090001  /* This clist sets the initial menu options on PANEL(SYS@PRIM)   */
00100001  /* It is invoked by either SYS@PRIM panel on first startup       */
00110025  /* And it is also invoke when SCROLL PF Keys are used.           */
00120000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00130000  /**                                                             **/
00140002  /** Rockwell Menu BUILD Facility for Scrollable SYS@PRIM.       **/
00150000  /**                                                             **/
00160000  /** Michael E. Theys                                            **/
00170000  /** Southwestern Computing Center                               **/
00180000  /** Information Systems Center                                  **/
00190000  /** Rockwell International                                      **/
00200000  /** 1200 N. Alma Rd.  Bldg 401-109                              **/
00210000  /** Richardson, Texas 75081-2205                                **/
00220000  /** (214) 996-2860  COMNET 437-2860                             **/
00230002  /** XMIT Address    SWCC.XTS0008                    Jan 1987    **/
00240000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00250000 
00260000CONTROL ASIS
00270000ISPEXEC CONTROL ERRORS RETURN
00280000IF &DEBUG = DEBUG +
00290000  THEN CONTROL   LIST   CONLIST   SYMLIST NOFLUSH   PROMPT   MSG
00300000  ELSE CONTROL NOLIST NOCONLIST NOSYMLIST   FLUSH NOPROMPT NOMSG
00310000 
00320000/* Validate the BUILD Variable Pool and Max Displayable Menu Options */
00330020/* PANEL(SYS@PRIM) has RMBFMAXD= Max Displayable options=18 */
00340020/* PANEL(SYS@PRIM) has RMBFMAXM= Max available   options=36 */
00350026/* PANEL(SYS@PRIM) has RMBFPOOL= Variable Pool   "SYS"      */
00360000%RMBFPOOL
00370000ISPEXEC VGET (RMBFPOOL,RMBFMAXM,RMBFMAXD)
00380000SET POOL = &RMBFPOOL
00390000 
00400020/**************** START of SCROLL SET UP ****************************/
00410020IF &SCROLL =  SCROLL THEN DO
00420020  /* Define the amount to scroll by, if needed. */
00430020  IF &DATATYPE(&AMT) ^= NUM THEN DO
00440020    IF      &STR(&AMT) = HALF OR &STR(&AMT) = H THEN +
00450020         SET SCROLL = &RMBFMAXD / 2
00460020    ELSE IF &STR(&AMT) = PAGE OR &STR(&AMT) = P THEN +
00470020         SET SCROLL = &RMBFMAXD
00480020    ELSE SET SCROLL = 1
00490020  END
00500020  ELSE   SET SCROLL = &AMT
00510020 
00520020  /* Get current TOP displayable menu position  OPT#  */
00530020  ISPEXEC VGET (R&POOL.OPT#)
00540020  SET OPT# = &&R&POOL.OPT#
00550020  IF &DATATYPE(&OPT#) ^= NUM THEN SET OPT# = 1
00560020  IF &OPT# < 1               THEN SET OPT# = 1
00570020  IF &OPT# > &RMBFMAXM       THEN SET OPT# = &RMBFMAXM
00580020 
00590026  /* See if scroll pf key  option selected */
00600020  IF      &UP = UP                         THEN DO
00610020     SET OPT# = &OPT# - &SCROLL
00620020     IF &OPT# < 1  THEN SET OPT# = 1
00630020  END
00640020  ELSE IF &LEFT = LEFT   OR &FIRST = FIRST THEN DO
00650020     /* Scroll "LEFT" is FIRST or TOP option. */
00660020     SET OPT# = 1
00670020  END
00680020  ELSE IF &DOWN = DOWN                     THEN DO
00690051     ISPEXEC VGET (RSYSLDO)
00700051     IF &OPT# >= &RSYSLDO  THEN EXIT
00710020     SET OPT# = &OPT# + &SCROLL
00720020     IF &OPT# > &RMBFMAXM  THEN SET OPT# = &RMBFMAXM
00730050     IF &OPT# > &RSYSLDO   THEN SET OPT# = &RSYSLDO
00740020  END
00750020  ELSE IF &RIGHT = RIGHT OR &LAST = LAST   THEN DO
00760020     /* Scroll "RIGHT" is LAST or BOTTOM option. */
00770020     SET OPT# = &RMBFMAXM - &RMBFMAXD + 1
00780020  END
00790020  ELSE  EXIT
00800020  /* Now scroll it! */
00810020  SET SCROLLIT = SCROLLIT
00820020  /* OPT# now set to the number of the first option to be displayed */
00830020END
00840020/****************** END of SCROLL SET UP ****************************/
00850000 
00860000IF &INIT = INIT THEN DO
00870026  ISPEXEC VGET (R&POOL.DOIT,SYS1STDO)
00880026  SET ZERRSM =
00890026  SET ZERRLM =
00900000  ISPEXEC VPUT (ZERRSM,ZERRLM)
00910026  SET RSYSFDO = 1  /* Position of the First Non-Blank Displayable Option */
00920026  SET RSYSLDO = 1  /* Position of the Last  Non-Blank Displayable Option */
00930035  SET RSYSMAXM = &RMBFMAXM
00940000 
00950001  /* Auto Load the  SYS@PRIM Menu options      */
00960001  ISPEXEC VGET (SYS@VERS) PROFILE
00970001  IF &STR(&SYS@VERS) ^= &VERSION THEN DO
00980001    %RMBFINIT MENU(SYS) TABLE(SYSP) CLEAR NOMSG
00990001    SET SYS@VERS = &VERSION
01000001    ISPEXEC VPUT (SYS@VERS) PROFILE
01010001  END
01020001 
01030001  /* Perform Panel Initialization if requested */
01040000  SET DOIT =  &STR(&&R&POOL.DOIT)
01050024  IF &DOIT =  YES AND &STR(&SYS1STDO) ^= DONE THEN DO
01060003    %SYSFIRST
01070024    SET SYS1STDO = DONE
01080024    ISPEXEC VPUT (SYS1STDO)
01090003  END
01100024END    /* OF INIT */
01110035 
01120000/* Get the Displayable Menu Options */
01130000/* Begin Option to Display (Varies with Scroll) */
01140035IF &INIT     =   THEN ISPEXEC VGET (RSYSMAXM)
01150035IF &SCROLLIT =   THEN SET OPT# = 1
01160000SET I = &OPT#
01170040SET S = 1    /* Start Display position on panel */
01180040DO WHILE(&S <= &RSYSMAXM)
01190038  IF &I <=  &RSYSMAXM THEN DO
01200000    ISPEXEC VGET (R&POOL.O&I,R&POOL.SD&I,R&POOL.LD&I,R&POOL.C&I) PROFILE
01210000    SET OPT      = &STR(&&R&POOL.O&I)
01220000    IF &DATATYPE(&OPT) = NUM THEN +
01230000      IF &OPT <= 9 THEN SET OPT = &STR( &OPT)
01240000      ELSE              SET OPT = &STR(&OPT)
01250000    ELSE                SET OPT = &STR( &OPT)  /* Alpha option */
01260000    SET SD       = &STR(&&R&POOL.SD&I)
01270000    SET LD       = &STR(&&R&POOL.LD&I)
01280000    SET CMD      = &STR(&&R&POOL.C&I)
01290000  END
01300000  ELSE DO
01310000    SET OPT      = &STR( )
01320000    SET SD       = &STR( )
01330000    SET LD       = &STR( )
01340000    SET CMD      = &STR( )
01350000  END
01360016  IF &STR(&OPT) ^= AND &INIT = INIT THEN DO
01370026    IF &I < &RSYSFDO THEN SET RSYSFDO = &I
01380026    IF &I > &RSYSLDO THEN SET RSYSLDO = &I
01390028    SET SD = &STR(&SUBSTR(1:12,&STR(&SD            ))-)
01400000  END
01410042 
01420040  IF &S <= &RMBFMAXD THEN DO
01430040    SET RSSSO&S  = &STR(&OPT)
01440040    SET RSSSSD&S = &STR(&SD)
01450040    SET RSSSLD&S = &STR(&LD)
01460040    SET RSSSC&S  = &STR(&CMD)
01470040    ISPEXEC VPUT (RSSSO&S,RSSSSD&S,RSSSLD&S,RSSSC&S)
01480032  END
01490000  SET I = &I + 1
01500040  SET S = &S + 1
01510000END
01520035IF &INIT = INIT THEN DO
01530035     SET RSYSMAXM = &RSYSLDO
01540035     ISPEXEC VPUT (RSYSFDO,RSYSLDO,RSYSMAXM)
01550035END
01560035ELSE ISPEXEC VGET (RSYSFDO,RSYSLDO)
01570000 
01580042/* Set ShortName to "* NO MORE *" after the last displayed option */
01590044SET EOPT = (&RSYSLDO - &OPT# + 1) + 1
01600052IF (&EOPT >= 1 AND &EOPT <= &RMBFMAXD) AND +
01601053   (&RSYSLDO > &RMBFMAXD OR  &OPT# > 1) THEN DO
01602054  SET ELD = &STR(* End of Options *)
01603054  SET RSSSLD&EOPT = &STR(&ELD)
01604054  ISPEXEC VPUT (RSSSLD&EOPT)
01640042END
01650042 
01660000SET R&POOL.OPT# = &OPT#
01670026ISPEXEC VPUT (R&POOL.OPT#)
01680016IF      (&OPT# + &RMBFMAXD) <= &RSYSLDO THEN +
01690043                               SET RSYSMSG = &STR(-> More options ... )
01700016ELSE IF (&OPT#            ) >  &RSYSFDO THEN +
01710043                               SET RSYSMSG = &STR(<- More options ... )
01720018ELSE                           SET RSYSMSG = &STR( )
01730004ISPEXEC VPUT (RSYSMSG)
01740003 
01750000/* Menu options have been load to the displayable portion of the menu */
01760000/* or they have been scrolled either up or down the proper amount.    */
01770002/* Now back to the Scrollable SYS@PRIM Primary Menu */
01780000EXIT
