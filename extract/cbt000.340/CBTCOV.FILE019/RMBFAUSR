00010001PROC 0  DEBUG MENU() POOL() MSG
00020000/* POOL SHOULD BE "YOR" FOR UPTO 36 MENU OPTIONS */
00030000/*             OR "MBF" FOR UPTO 18 MENU OPTIONS */
00040000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00050000/**                                                             **/
00060000/** CONVERT AOST "USER DIALOG PRIMARY OPTION MENU": INTO TO     **/
00070000/** THE MENU BUILD FACILITY FORMAT.  DEFAULT IS FOR YOUR PANEL. **/
00080000/**                                                             **/
00090000/** M. THEYS  12/04/86                                          **/
00100000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00110000 
00120000IF &DEBUG = DEBUG THEN  CONTROL   MSG   LIST   CONLIST   SYMLIST
00130000ELSE                    CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST
00140000 
00150000IF      &STR(&MENU)  = PDF       THEN  SET POOL = PRM
00160000ELSE IF &STR(&MENU)  = AOST      THEN  SET POOL = PRM
00170000ELSE IF &STR(&MENU)  = YOUR      THEN  SET POOL = YOR
00180000ELSE IF &STR(&MENU)  = GROUP     THEN  SET POOL = GRP
00190000ELSE IF &STR(&MENU)  = USER      THEN  SET POOL = USR
00200003ELSE IF &STR(&MENU)  = THE       THEN  SET POOL = THE
00220000ELSE IF &STR(&POOL)  =           THEN  SET POOL = YOR
00230000/* IF MENU=AOST THEN THIS SHOULD BE INVOKED UNDER APPLID=ISP ! */
00240000 
00250000/* SAVE THE CURRENT BUILD VARIABLE POOL AND MAX MENU OPTIONS */
00260000ISPEXEC VGET (RMBFPOOL,RMBFMAXM)
00270000SET SAVEPOOL = &STR(&RMBFPOOL)
00280000SET SAVEMAXM = &STR(&RMBFMAXM)
00290000 
00300000/* SET THE SELECTED BUILD VARIABLE POOL FOR INFOCENTER USER MENU */
00310000/* THEN VERIFY IF VALID AND SET CURRENT MAX MENU OPTIONS.        */
00320000SET RMBFPOOL = &POOL
00330000ISPEXEC VPUT (RMBFPOOL)
00340000%RMBFPOOL
00350000ISPEXEC VGET (RMBFPOOL,RMBFMAXM)
00360000SET POOL = &RMBFPOOL
00370000/* AOST USER MODIFABLE MENU AS 15 OPTIONS */
00380000SET MAXM = 15
00390000IF &RMBFMAXM < &MAXM THEN SET MAXM = &RMBFMAXM
00400000 
00410000SET MOPT = 0
00420000SET K    = 1
00430000CONTROL ASIS
00440000DO WHILE (&K <= &MAXM)
00450000 
00460000  /* GET VARIABLES USED BY AOST USER DIALOG MENU */
00470000  ISPEXEC VGET (ZC&K,ZUD&K,ZUC&K) PROFILE
00480000  SET OPT    = &STR(&&ZC&K)
00490000  SET NAME   = &STR(       )
00500000  SET DESCR  = &STR(&&ZUD&K)
00510000  SET ICMD   = &STR(&&ZUC&K)
00520000 
00530000  /* ASSIGN TO THE MENU BUILD VARIABLE POOL SPECIFIED */
00540000  IF &STR(&OPT) ^=  THEN DO
00550000    SET MOPT = &MOPT + 1
00560000    SET R&POOL.O&K = &STR(&OPT)
00570000    SET R&POOL.SD&K = &STR(&NAME)
00580000    SET R&POOL.LD&K = &STR(&DESCR)
00590000    SET R&POOL.C&K = &STR(&ICMD)
00600000    ISPEXEC VPUT (R&POOL.O&K,R&POOL.SD&K,R&POOL.LD&K,R&POOL.C&K) PROFILE
00610000  END
00620000 
00630000  SET K = &K + 1
00640000END
00650000 
00660000SET R&POOL.MOPT =
00670000IF &MOPT > 0 THEN SET R&POOL.MOPT = YES
00680000ISPEXEC VPUT (R&POOL.MOPT) PROFILE
00690000 
00700000SET RMBFPOOL = &STR(&SAVEPOOL)
00710000SET RMBFMAXM = &STR(&SAVEMAXM)
00720000ISPEXEC VPUT (RMBFPOOL,RMBFMAXM)
00730001 
00740001IF &MSG = MSG THEN DO
00750001  SET SHORT = &STR(NO OPTIONS COPIED)
00760001  IF &MOPT > 0 THEN SET SHORT = &STR(OPTIONS COPIED)
00770002  SET LONG  = &STR(AOST USERPRIM MENU CONVERTED. #OPTS=&MOPT  TO MBF VARPOOL=&POOL)
00780001  ISPEXEC SETMSG MSG(SYS010)
00790001END
