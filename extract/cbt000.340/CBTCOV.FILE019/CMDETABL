        PROC 0    DEBUG
 
        /*    RON MURA, TECHNICAL SERVICES, ISPF V1R1M0
        /*    UPDATED BY BOB ZIMMERMAN CNA INSURANCE
 
                  IF DEBUG = &DEBUG THEN CONTROL SYMLIST CONLIST LIST
                  CONTROL FLUSH MSG NOPROMPT
                  SET TBNM = &STR(CMDT&SUBSTR(5:8,&SYSTIME))
                  ISPEXEC CONTROL ERRORS CANCEL
 
                  ISPEXEC TBCREATE &TBNM NOWRITE +
                      KEYS(ZCTVERB)              +
                      NAMES(ZCTACT ZCTDESC TYPE ZCTTRUNC) +
                      REPLACE
                  ISPEXEC TBSORT &TBNM FIELDS(ZCTVERB,C,A)
 
                  SET TYPE = &STR( *)
                  SET COMMENT =
                  SET COMMEN2 =
                  ISPEXEC VGET (ZAPPLID)
                  ISPEXEC TBSTATS &ZAPPLID.CMDS STATUS2(S2)
                  IF &S2 = 1 THEN GOTO ISP
 
                  SET COMMENT = &STR($ *¢)
                  SET COMMEN2 = &STR($**¢)
 
                  ISPEXEC TBQUERY &ZAPPLID.CMDS POSITION(CRP)
                  ISPEXEC TBTOP &ZAPPLID.CMDS
                  SET LCC = 0
                  DO WHILE &LCC = 0
                      ISPEXEC TBSKIP &ZAPPLID.CMDS
                      SET LCC = &LASTCC
                      IF &LCC = 0 THEN ISPEXEC TBADD &TBNM ORDER
                      END
 
                  ISPEXEC TBSKIP &ZAPPLID.CMDS NUMBER(&CRP)
        ISP:      +
                  SET TYPE =
                  ISPEXEC TBQUERY ISPCMDS POSITION(CRP)
                  ISPEXEC TBTOP ISPCMDS
                  SET LCC = 0
                  DO WHILE &LCC = 0
                      ISPEXEC TBSKIP ISPCMDS
                      SET LCC = &LASTCC
                      IF &LCC = 0 THEN +
                          DO
                          ISPEXEC TBADD &TBNM ORDER
                          IF &LASTCC = 8 THEN +
                              DO
                              ISPEXEC TBGET &TBNM
                              SET TYPE = &STR(**)
                              ISPEXEC TBPUT &TBNM ORDER
                              SET TYPE =
                              END
                          END
                      END
 
                  ISPEXEC TBSKIP ISPCMDS NUMBER(&CRP)
 
                  ISPEXEC TBTOP &TBNM
                  SET MSG =
                  SET &PNL = CMDB
 
        DISPLAY:  +
                  ISPEXEC TBDISPL &TBNM PANEL(&PNL) MSG(&MSG)
                  SET &RC = &LASTCC
                  IF &RC < 8 && &ZCMD ^= +
                     THEN DO
                             SET &MSG = ISPZ001
                             SET &PNL =
                             GOTO DISPLAY
                          END
                  ISPEXEC TBEND &TBNM
                  EXIT
