00010003 PROC 0  DEBUG   +
00020003         GROUP() +
00030003         PANELS() MSGS() SKELS() TABLES() CLIST() HELP() LLIB() +
00040003         PANEL() OPT() +
00050000         PGM() PARM()  +
00060000         CMD()         +
00070000         NEWAPPL()
00080000 /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00090000 /**                                                       **/
00100003 /** CLIST TO LIBDEF A FUNCTION UNDER ISPF                 **/
00110003 /** BY: MIKE THEYS AND ANDY ANDERSON       ROCKWELL       **/
00120000 /**                                                       **/
00130000 /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00140000 
00150000 IF &DEBUG ^= DEBUG THEN CONTROL NOLIST                        NOMSG
00160000 ELSE                    CONTROL   LIST PROMPT CONLIST SYMLIST   MSG
00170000 
00180000 CONTROL NOFLUSH
00190000 
00200000 IF &SYSISPF ^= ACTIVE THEN DO
00210000    WRITE
00220000    WRITE *ERROR* MAY ONLY BE EXECUTED WHILE UNDER ISPF.
00230000    WRITE
00240000    EXIT
00250000 END
00260000 
00270000 ISPEXEC CONTROL ERRORS RETURN
00280003 IF &STR(&GROUP) ^=  THEN DO
00290003   DO WHILE &SUBSTR(1:1,&STR(&GROUP)) = &STR(')
00300003     SET &GLEN = &LENGTH(&STR(&GROUP))
00310003     IF &SUBSTR(&GLEN:&GLEN,&STR(&GROUP)) ^= &STR(') THEN DO
00320003       SET &SHORT =
00330003       SET &LONG = &STR( UNBALANCED " ' "S IN GROUP OPERAND)
00340003       ISPEXEC SETMSG MSG(SYS010)
00350003       EXIT CODE(12)
00360003     END
00370003     SET &GROUP = &SUBSTR(2:&GLEN - 1,&STR(&GROUP))
00380003   END
00390003   IF &STR(&PANELS) ^= NO THEN SET &PANELS = &STR('&GROUP..PANELS')
00400003   IF &STR(&MSGS)   ^= NO THEN SET &MSGS   = &STR('&GROUP..MSGS')
00410003   IF &STR(&SKELS)  ^= NO THEN SET &SKELS  = &STR('&GROUP..SKELS')
00420003   IF &STR(&LLIB)   ^= NO THEN SET &LLIB   = &STR('&GROUP..LOAD')
00430003   IF &STR(&TABLES) ^= NO THEN SET &TABLES = &STR('&GROUP..TABLES')
00440003   IF &STR(&HELP)   ^= NO THEN SET &HELP   = &STR('&GROUP..HELP')
00450003   IF &STR(&CLIST)  ^= NO THEN SET &CLIST  = &STR('&GROUP..CLIST')
00460003 END
00470000 
00480000 SET &MAXCC = 0
00490000 
00500002 IF &STR(&PANELS) ^= THEN IF &STR(&PANELS) ^= NO THEN +
00510000   ISPEXEC LIBDEF ISPPLIB DATASET ID(&PANELS)
00520000 SET &RC = &LASTCC
00530000 
00540003 IF &STR(&MSGS) ^=   THEN IF &STR(&MSGS)   ^= NO THEN +
00550000   ISPEXEC LIBDEF ISPMLIB DATASET ID(&MSGS)
00560000 SET &RC = &LASTCC
00570000 
00580003 IF &STR(&SKELS) ^=  THEN IF &STR(&SKELS)  ^= NO THEN +
00590000   ISPEXEC LIBDEF ISPSLIB DATASET ID(&SKELS)
00600000 SET &RC = &LASTCC
00610000 
00620002 IF &STR(&TABLES) ^= THEN IF &STR(&TABLES) ^= NO THEN +
00630000   ISPEXEC LIBDEF ISPTLIB DATASET ID(&TABLES)
00640000 SET &RC = &LASTCC
00650000 
00660003 IF &STR(&LLIB) ^=   THEN IF &STR(&LLIB)   ^= NO THEN +
00670000   ISPEXEC LIBDEF ISPLLIB DATASET ID(&LLIB)
00680000 SET &RC = &LASTCC
00690000 
00700003 IF &STR(&CLIST) ^=  THEN IF &STR(&CLIST)  ^= NO THEN +
00710000   %CONCAT &STR(&CLIST) ADD PRE
00720000 SET &RC = &LASTCC
00730000 
00740003 IF &STR(&HELP) ^=   THEN IF &STR(&HELP)   ^= NO THEN +
00750000   %CONCAT &STR(&HELP) FILE(SYSHELP) ADD AUX
00760000 SET &RC = &LASTCC
00770000 
00780000 IF &MAXCC = 0 THEN DO
00790000   SET &VEXEC =
00800003   IF       &STR(&PANEL) ^=  THEN DO
00810003     IF &STR(&OPT) ^= THEN +
00820003       SET VEXEC = &STR(&VEXEC PANEL(&PANEL) OPT(&OPT) )
00830003     ELSE SET &VEXEC = &STR(&VEXEC PANEL(&PANEL) )
00840003   END
00850000   ELSE IF  &STR(&PGM)     ^=     OR   &STR(&PARM)    ^=    THEN +
00860000     SET VEXEC = &STR(&VEXEC PGM(&PGM) PARM(&PARM) )
00870000   ELSE IF  &STR(&CMD)     ^=                               THEN +
00880000     SET VEXEC = &STR(&VEXEC CMD(&CMD) )
00890000   IF       &STR(&NEWAPPL) ^=                               THEN +
00900000     SET VEXEC = &STR(&VEXEC NEWAPPL(&NEWAPPL) )
00910000   IF &STR(&VEXEC) ^= THEN DO
00920003     ISPEXEC CONTROL ERRORS RETURN
00930003     ISPEXEC  SELECT &STR(&VEXEC) PASSLIB
00940003     IF &LASTCC > 4 THEN ISPEXEC SETMSG MSG(ISRZ002)
00950000   END
00960000 END
00970000 ELSE DO
00980003   SET &SHORT = &STR(LIBDEF ERR)
00990003   SET &LONG = &STR(UNABLE TO OBTAIN REQUIRED LIBRARIES)
01000003   ISPEXEC SETMSG MSG(SYS010)
01010000 END
01020000 
01030003 IF &STR(&PANELS) ^= THEN IF &STR(&PANELS)^= NO THEN ISPEXEC LIBDEF ISPPLIB
01040003 IF &STR(&MSGS)   ^= THEN IF &STR(&MSGS)  ^= NO THEN ISPEXEC LIBDEF ISPMLIB
01050003 IF &STR(&SKELS)  ^= THEN IF &STR(&SKELS) ^= NO THEN ISPEXEC LIBDEF ISPSLIB
01060003 IF &STR(&TABLES) ^= THEN IF &STR(&TABLES)^= NO THEN ISPEXEC LIBDEF ISPTLIB
01070003 IF &STR(&LLIB)   ^= THEN IF &STR(&LLIB)  ^= NO THEN ISPEXEC LIBDEF ISPLLIB
01080003 IF &STR(&CLIST)  ^= THEN IF &STR(&CLIST) ^= NO THEN %CONCAT &STR(&CLIST) DEL
01090003 IF &STR(&HELP)   ^= THEN IF &STR(&HELP)  ^= NO THEN %CONCAT &STR(&HELP)  DEL FILE(SYSHELP)
01100000 
01110000 EXIT
