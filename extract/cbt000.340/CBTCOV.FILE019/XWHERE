00010013PROC     1     ADDRESS        /* HEXADECIMAL STORAGE LOCATION       */ +
00020013               ASID(0)        /* ADDRESS SPACE NUMBER               */ +
00030013               DEBUG          /* REQUEST FULL OUTPUT LISTING        */
00040013/**********************************************************************/
00050013/*       DETERMINE LOCATION OF ADDRESS.  OUTPUT CONSISTS OF:          */
00060013/*       (1) AREA WITHIN ADDRESS SPACE                                */
00070013/*       (2) SUBPOOL AND STORAGE KEY                                  */
00080013/*       (3) WHETHER OR NOT STORAGE IS FREE                           */
00090013/*       (4) LOAD MODULE CONTAINING ADDRESS, IF ANY                   */
00100013/**********************************************************************/
00110013IF       &DEBUG = DEBUG +
00120013THEN     CONTROL CONLIST LIST MSG PROMPT SYMLIST
00130013GLOBAL   SUBPOOL KEY FREE
00140013SET      &CVTLPDIA = 360N  /* LPA */
00150013SET      &CVTMZ00 = 164N   /* HIGHEST ADDRESS */
00160013SET      &CVTNUCB = 128N   /* PRIVATE AREA */
00170013SET      &CVTSHRVM = 416N  /* CSA */
00180013SET      &HEX = 0123456789ABCDEF
00190013SET      &LOCATION = 0
00200013SET      &DIGIT = 1
00210013SET      &LAST = &LENGTH(&ADDRESS)
00220013IF       &SUBSTR(&LAST,&ADDRESS) = . +
00230013THEN     SET   &LAST = &LAST-1
00240013DO       WHILE &DIGIT <= &LAST /* CONVERT ADDRESS */
00250013         SET   &INDEX = 0
00260013         DO    /* CONVERT DIGIT */ +
00270013               WHILE &SUBSTR(&DIGIT,&ADDRESS) ^= &SUBSTR(&INDEX+1,&HEX)
00280013               SET   &INDEX = &INDEX+1
00290013               IF    &INDEX > 15 +
00300013               THEN  DO    /* ADDRESS ERROR */
00310013                     WRITE &ADDRESS IS NOT HEXADECIMAL
00320013                     EXIT  CODE(12)
00330013                     END   /* ADDRESS ERROR */
00340013               END   /* CONVERT DIGIT */
00350013         SET   &LOCATION = &LOCATION*16+&INDEX
00360013         SET   &DIGIT = &DIGIT+1
00370013         END   /* CONVERT ADDRESS */
00380013EVALUATE CVT STRUCTURE(CVT)
00390013EVALUATE CVT+&CVTNUCB LENGTH(4)
00400013SET      &PRIVATE = &LASTCC
00410013EVALUATE CVT+&CVTSHRVM LENGTH(4)
00420013SET      &COMMON = &LASTCC
00430013EVALUATE CVT+&CVTMZ00 LENGTH(4)
00440013SET      &MAXIMUM = &LASTCC
00450013SET      &AREA = INVALID
00460013IF       0 <= &LOCATION && &LOCATION < &PRIVATE +
00470013THEN     SET   &AREA = NUCLEUS
00480013IF       &PRIVATE <= &LOCATION && &LOCATION < &COMMON +
00490013THEN     SET   &AREA = PRIVATE
00500013IF       &COMMON <= &LOCATION && &LOCATION <= &MAXIMUM +
00510013THEN     SET   &AREA = COMMON
00520013NOTE     '&ADDRESS IS IN THE &AREA AREA'
00530013IF       &AREA = PRIVATE +
00540013THEN     %WHEREP    &LOCATION ASID(&ASID) &DEBUG
00550013IF       &AREA = COMMON +
00560013THEN     %WHEREC   &LOCATION &DEBUG
00570013IF       &AREA = INVALID +
00580013THEN     EXIT  CODE(12)
00590013ELSE     EXIT  CODE(0)
