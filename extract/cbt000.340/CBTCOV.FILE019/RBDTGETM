00010000PROC 0 DEBUG
00020000   IF &DEBUG = DEBUG THEN +
00030000     CONTROL LIST CONLIST MSG
00040000   CONTROL NOFLUSH
00050000   ISPEXEC VGET (BDTFDSN Z)
00060000   SET TABLE = DUMTAB
00070000   SET PANEL = RBDTMEM
00080000   SET CRP = &Z
00090000   SET ACTION = &Z
00100000   SET MSG = &Z
00110000   SET CURSOR = SEL              /* FILL IN YOUR CURSOR FIELD */
00120000   SET FIRST = YES
00130000   SET VALID = NO
00140000/******************************************************************/
00150000/*   GET THE TABLE FILLED WITH 20 EMPTY ROWS                      */
00160000/******************************************************************/
00170000   ISPEXEC TBOPEN DUMTAB NOWRITE
00180000/******************************************************************/
00190000/*   CREATE THE TABLE FOR ACTUAL ENTRIES                          */
00200000/******************************************************************/
00210000   ISPEXEC TBCREATE MEMTABLE NAMES(DIRNAME NEWNAME) NOWRITE
00220000   SET DESC = +
00230000    &STR(ENTER "S" TO SELECT OR "E" TO EXCLUDE, AND MEMBER NAMES)
00240000/******************************************************************/
00250000/* POSITION TABLE TO DISPLAY FROM 1ST ROW                         */
00260000/******************************************************************/
00270000   ISPEXEC TBTOP &TABLE
00280000   SET CRP = 1
00290000/******************************************************************/
00300000/* DISPLAY THE TABLE AND PROCESS USER ENTRIES UNTIL THE USER      */
00310000/* ENTERS END, RETURN OR CANCEL                                   */
00320000/******************************************************************/
00330000   DO WHILE (&ACTION ^= USEREND)
00340000     IF &FIRST = YES THEN +
00350000       DO
00360000         ISPEXEC TBDISPL &TABLE PANEL(&PANEL) MSG(&MSG) CSRROW(&CRP) +
00370000            CURSOR(&CURSOR)
00380000         SET RC = &LASTCC
00390000         IF &ZCMD = CANCEL THEN +
00400000           DO
00410000             SET RC = 16
00420000             GOTO ENDUP
00430000           END
00440000         SET FIRST = NO
00450000       END
00460000     ELSE +
00470000       DO
00480000   /******************************************************************/
00490000   /* ISPF WILL CORRECTLY POSITION THE TABLE IF THE PANEL NAME       */
00500000   /* IS NOT RESPECIFIED.                                            */
00510000   /******************************************************************/
00520000         IF &MSG = &Z THEN +
00530000           DO
00540000             SET CRP = &CRP + 1
00550000             ISPEXEC TBDISPL &TABLE CSRROW(&CRP) CURSOR(&CURSOR)
00560000           END
00570000         ELSE +
00580000           ISPEXEC TBDISPL &TABLE MSG(&MSG) CSRROW(&CRP) +
00590000                   CURSOR(&CURSOR)
00600000         SET RC = &LASTCC
00610000         SET CURSOR = SEL
00620000       END
00630000   /******************************************************************/
00640000   /* RC = 0  - ONE LINE MODIFIED AND/OR COMMAND ENTERED             */
00650000   /* RC = 4  - TWO OR MORE LINES MODIFIED; USER MAY HAVE ENTERED    */
00660000   /*           A FUNCTION COMMAND                                   */
00670000   /* RC = 8  - USER ENTERED END OR RETURN                           */
00680000   /******************************************************************/
00690000     IF &ZCMD = CANCEL THEN +
00700000       DO
00710000         SET RC = 16
00720000         GOTO ENDUP
00730000       END
00740000     IF &RC = 0 THEN +
00750000       SET ACTION = ONEROW
00760000     ELSE +
00770000       IF &RC = 4 THEN +
00780000         SET ACTION = MULTIROW
00790000       ELSE +
00800000         SET ACTION = USEREND         /* WAS AN 8 */
00810000   /******************************************************************/
00820000   /* PROCESS EACH ROW WHICH THE USER HAS MODIFIED                   */
00830000   /******************************************************************/
00840000     SET ROWSDONE = NO
00850000     SET LASTROW = NO
00860000     SET COMMAND = NO
00870000     SET CRP = &Z
00880000     SET MSG = &Z
00890000     /*
00900000     DO WHILE (&ROWSDONE = NO)
00910000       IF &LASTROW = YES | &ACTION = ONEROW THEN +
00920000         SET ROWSDONE = YES
00930000       ISPEXEC TBQUERY &TABLE POSITION (CRP)
00940000       IF &ACTION ^= MULTIROW | &LASTROW = YES THEN +
00950000         DO
00960000   /******************************************************************/
00970000   /* CRP = 0  - NO MORE LINES MODIFIED; LOOK FOR COMMAND (ERROR)    */
00980000   /*            ENTERED                                             */
00990000   /******************************************************************/
01000000           IF &CRP = 0 THEN +
01010000             DO
01020000               IF &ACTION = USEREND THEN +
01030000                 GOTO ENDUP
01040000               ELSE +
01050000                 DO         /* THERE WAS A COMMAND */
01060000                   SET COMMAND = YES
01070000                   SET ROWSDONE = YES
01080000                 END
01090000             END
01100000         END
01110000       IF &COMMAND = NO THEN +
01120000         DO
01130000   /******************************************************************/
01140000   /* PROCESS THE ROW                                                */
01150000   /******************************************************************/
01160000           IF &SEL ^= &Z && &SUBSTR(1:1,&STR(&DIRACT )) +
01170000                 ^= &STR(*) THEN +
01180000             DO
01190000               SET DIRNAME = &DIRNAME   /* GET CAPS */
01200000               IF &DIRNAME = &Z THEN +
01210000                 DO
01220000                   SET SHORT = &STR(ENTER MEMBER NAME)
01230000                   SET LONG = &STR("S" OR "E" WAS SPECIFIED, BUT NO +
01240000                              MEMBER NAME WAS SUPPLIED)
01250000                   SET MSG = RBDT010
01260000                   GOTO ROWERROR
01270000                 END
01280000               IF &VALID = NO THEN +
01290000                 DO             /* FIRST TIME VALIDATION             */
01300000                   SET IORE = &SEL
01310000                   IF &IORE ^= S && &IORE ^= E THEN +
01320000                     DO
01330000                       SET &SHORT = &STR(NOT SELECT OR EXCLUDE)
01340000                       SET &LONG = &STR(ENTER AN "S" OR "E" IN +
01350000                          THE SELECT COLUMN)
01360000                       SET &MSG = RBDT010
01370000                       SET SEL = &Z
01380000                       GOTO ROWERROR
01390000                     END
01400000                   ELSE +
01410000                    DO
01420000                      ISPEXEC VPUT (IORE) SHARED
01430000                      SET VALID = YES
01440000                    END
01450000                 END
01460000               IF &IORE = S THEN +
01470000                 SET NEWNAME = &STR(&DIRACT)
01480000               ISPEXEC TBADD MEMTABLE
01490000               IF &IORE = S THEN +
01500000                 DO
01510000                   IF &NEWNAME = &Z THEN +
01520000                    SET DIRACT = &STR(*SELECT)
01530000                   ELSE +
01540000                    SET DIRACT = &STR(*RENAMED)
01550000                 END
01560000               ELSE +
01570000                 SET DIRACT = &STR(*EXCLUDE)
01580000               ISPEXEC TBPUT &TABLE
01590000               SET SEL = &Z
01600000             END
01610001           ELSE +
01620001             SET SEL = &Z
01630000   /******************************************************************/
01640000   /* ERRORS FOUND???                                                */
01650000   /******************************************************************/
01660000           IF &MSG ^= &Z THEN +
01670000             DO
01680000   /******************************************************************/
01690000   /* DON'T END IF THERE IS AN ERROR                                 */
01700000   /******************************************************************/
01710000               IF &ACTION = USEREND THEN +
01720000                 SET &ACTION = &Z
01730000               GOTO ROWERROR
01740000             END
01750000           IF &ROWSDONE = YES && &ZCMD ^= &Z THEN +
01760000             SET COMMAND = YES        /* COMMAND WAS ENTERED */
01770000           IF &ROWSDONE = NO THEN +
01780000             DO
01790000   /******************************************************************/
01800000   /* RETRIEVE THE NEXT MODIFIED ROW                                 */
01810000   /******************************************************************/
01820000               ISPEXEC TBDISPL &TABLE
01830000               SET RC = &LASTCC
01840000               IF &ACTION = MULTIROW && &RC = 0 THEN +
01850000                 SET &LASTROW = YES
01860000             END
01870000         END                        /* END "IF &COMMAND = NO"        */
01880000       IF &COMMAND = YES THEN +
01890000         DO
01900000   /******************************************************************/
01910000   /* PROCESS THE FUNCTION COMMAND   (MAY BE AN ERROR)               */
01920000   /******************************************************************/
01930000           SET SHORT = &STR(INVALID COMMAND)
01940000           SET LONG = &STR(THE ENTRY IN THE COMMAND FIELD IS NOT +
01950000              RECOGNIZED)
01960000           SET MSG = RBDT010
01970000         END
01980000     END
01990000ROWERROR: +
02000000   END
02010000ENDUP: +
02020000   ISPEXEC TBEND DUMTAB
02030000   IF &RC = 16 THEN +
02040000     DO
02050000       ISPEXEC TBEND MEMTABLE
02060000       EXIT CODE(8)
02070000     END
02080000END
