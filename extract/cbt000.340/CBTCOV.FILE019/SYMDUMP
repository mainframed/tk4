00010000PROC     2                                                            +
00020000         ADDRESS              /* VIRTUAL ADDRESS OF DUMP DATA (HEX) */+
00030000         CONTROLBLOCK         /* CONTROL BLOCK MEMBER IN SYMPDS     */+
00040000         BROWSE               /* INVOKE IPCSBRWS                    */+
00050000         NOBROWSE             /* DO NOT INVOKE IPCSBRWS             */+
00060000         DISP()               /* DISP OF IPCSBRWS DATA SET          */+
00070000         PRINT                /* IPCS PARAMETER                     */+
00080000         NOPRINT              /* IPCS PARAMETER                     */+
00090000         TERMINAL             /* IPCS PARAMETER                     */+
00100000         NOTERMINAL           /* IPCS PARAMETER                     */+
00110000         TEST                 /* IPCS PARAMETER                     */+
00120000         NOTEST               /* IPCS PARAMETER                     */
00130000/********************************************************************/
00140000/* IPCS CLIST TO EXECUTE THE SYMDUMP GENERALIZED CONTROL BLOCK      */
00150000/* FORMATTING ROUTINE. PRIOR TO EXECUTING THIS CLIST, FILE SYMPDS   */
00160000/* MUST BE ALLOCATED TO A PARTITIONED DATA SET CONTAINING CONTROL   */
00170000/* BLOCK DEFINITION OBJECT DECKS ASSEMBLED WITH THE TEST OPTION.    */
00180000/********************************************************************/
00190000IF       &TEST ^= TEST THEN CONTROL MSG NOSYM NOCON NOLIST PROMPT
00200000ELSE                        CONTROL MSG   SYM   CON   LIST PROMPT
00210000IF       &BROWSE =  && &NOBROWSE =  +
00220000THEN     DO
00230000         WRITENR  DO YOU WANT TO BROWSE THE OUTPUT? (N)/Y ===>
00240000         READ     &ANS
00250000         IF       &ANS = Y +
00260000         THEN     SET &BROWSE = BROWSE
00270000         END
00280000IF       &BROWSE = BROWSE +
00290000THEN     DO       /* BROWSE REQUSTED */
00300000         IF       &DISP =  +
00310000         THEN     DO WHILE &DISP ^= OLD && &DISP ^= NEW && &DISP ^= MOD
00320000                  WRITENR ENTER BROWSE DATA SET DISP OLD/NEW/MOD ===>
00330000                  READ    &DISP
00340000                  END
00350000         IF       &DISP = NEW +
00360000         THEN     DELETE IPCSBRWS.LIST
00370000         ALLOCATE FILE(IPCSBRWS) DSNAME(IPCSBRWS.LIST) &DISP +
00380000                  TRACK SPACE(5,5)
00390000         IF       &LASTCC ^= 0 && &DISP = OLD +
00400000         THEN     DO
00410000                  DELETE   IPCSBRWS.LIST
00420000                  ALLOCATE FILE(IPCSBRWS) DSNAME(IPCSBRWS.LIST) NEW +
00430000                           TRACK SPACE(5,5)
00440000                  IF       &LASTCC ^= 0 +
00450000                  THEN     EXIT CODE(12)
00460000                  END
00470000         VERBEXIT IPCSBRWS 'SYMDUMP,&ADDRESS,&CONTROLBLOCK' +
00480000                  &PRINT &NOPRINT &TERMINAL &NOTERMINAL &TEST &NOTEST
00490000         WRITE    THE BROWSE DATA SET IS IPCSBRWS.LIST
00500000         WRITE    GO TO BROWSE TO SCAN THE OUTPUT
00510000         END      /* BROWSE REQUESTED */
00520000ELSE     VERBEXIT SYMDUMP  '&ADDRESS,&CONTROLBLOCK' +
00530000                  &PRINT &NOPRINT &TERMINAL &NOTERMINAL &TEST &NOTEST
00540000PGMCALL  IEFBR14
