00010000PROC 0 DEBUG    UP DOWN LEFT RIGHT AMT()
00020000  /* RMBF@GRP Scrollable Primary Menu with 36 menu options         */
00030000  /* This clist sets the initial menu options on PANEL(RMBF@GRP)   */
00040000  /* It is invoked by either RMBF@PRIM panel on first startup.     */
00050000  /* Or when scroll PF Keys are used to scroll menu options.       */
00060000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00070000  /**                                                             **/
00080000  /** User/Group Modifable Primary Option Menu: BUILD             **/
00090000  /**                                                             **/
00100000  /** Michael E. Theys                                            **/
00110000  /** Southwestern Computing Center                               **/
00120000  /** Information Systems Center                                  **/
00130000  /** Rockwell International                                      **/
00140000  /** 1200 N. Alma Rd.  Bldg 401-109                              **/
00150000  /** Richardson, Texas 75081-2205                                **/
00160000  /** (214) 996-2860  COMNET 437-2860                             **/
00170000  /** XMIT Address    SWCC.XTS0008                    Dec 1986    **/
00180000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00190000 
00200000CONTROL ASIS
00210000ISPEXEC CONTROL ERRORS RETURN
00220000IF &DEBUG = DEBUG +
00230000  THEN CONTROL   LIST   CONLIST   SYMLIST NOFLUSH   PROMPT   MSG
00240000  ELSE CONTROL NOLIST NOCONLIST NOSYMLIST   FLUSH NOPROMPT NOMSG
00250000 
00260001/* PANEL(RMBF@GRP) has RMBFMAXD= Max Displayable options=18 */
00270001/* PANEL(RMBF@GRP) has RMBFMAXM= Max available   options=36 */
00280000ISPEXEC VGET (RMBFPOOL,RMBFMAXD,RMBFMAXM)
00290000SET POOL = &RMBFPOOL
00300000 
00310000/* Define the amount to scroll by, if needed. */
00320000IF &DATATYPE(&AMT) ^= NUM THEN DO
00330000  IF      &STR(&AMT) = HALF OR &STR(&AMT) = H THEN +
00340000       SET SCROLL = &RMBFMAXD / 2
00350000  ELSE IF &STR(&AMT) = PAGE OR &STR(&AMT) = P THEN +
00360000       SET SCROLL = &RMBFMAXD
00370000  ELSE SET SCROLL = 1
00380000END
00390000ELSE   SET SCROLL = &AMT
00400000 
00410000/* Get current TOP displayable menu position  OPT#  */
00420000ISPEXEC VGET (R&POOL.OPT#)
00430000SET OPT# = &&R&POOL.OPT#
00440000IF &DATATYPE(&OPT#) ^= NUM THEN SET OPT# = 1
00450000IF &OPT# < 1               THEN SET OPT# = 1
00460000IF &OPT# > &RMBFMAXM       THEN SET OPT# = &RMBFMAXM
00470000 
00480000/* See if scroll pf key, option selected, or if END */
00490000IF      &UP = UP                         THEN DO
00500000   SET OPT# = &OPT# - &SCROLL
00510000   IF &OPT# < 1  THEN SET OPT# = 1
00520000   %RMBFGRPI SCROLLIT OPT#(&OPT#)
00530000END
00540000ELSE IF &LEFT = LEFT   OR &FIRST = FIRST THEN DO
00550000   /* Scroll "LEFT" is FIRST or TOP option. */
00560000   SET OPT# = 1
00570000   %RMBFGRPI SCROLLIT OPT#(&OPT#)
00580000END
00590000ELSE IF &DOWN = DOWN                     THEN DO
00600000   SET OPT# = &OPT# + &SCROLL
00610000   IF &OPT# > &RMBFMAXM  THEN SET OPT# = &RMBFMAXM
00620000   %RMBFGRPI SCROLLIT OPT#(&OPT#)
00630000END
00640000ELSE IF &RIGHT = RIGHT OR &LAST = LAST   THEN DO
00650000   /* Scroll "RIGHT" is LAST or BOTTOM option. */
00660000   SET OPT# = &RMBFMAXM - &RMBFMAXD + 1
00670000   %RMBFGRPI SCROLLIT OPT#(&OPT#)
00680000END
00690000 
00700000EXIT
