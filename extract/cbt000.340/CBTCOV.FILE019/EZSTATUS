00010000PROC 0 DEBUG          TRAP(500)
00020000IF &DEBUG = DEBUG THEN CONTROL MSG LIST CONLIST SYMLIST
00030000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00040000/**                                                                **/
00050000/** EZ STATUS/CANCEL WITH SINGLE LINE SORTED ISPF TABLE DISPLAY    **/
00060000/** VERSION 1.0  BY MIKE THEYS         ROCKWELL INTERNATIONAL       **/
00070000/**                                                                **/
00080000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00090000ISPEXEC CONTROL ERRORS RETURN
00100000%ROCPROF OPTION(NOMSGID)
00110000SET &RPRC = &LASTCC  /* LASTCC=4 THEN NOMSGID ALREADY SET */
00120000 
00130000ISPEXEC VGET (ZSCREEN)
00140000SET EZTBL = &STR(EZST&ZSCREEN)
00150000ISPEXEC TBSTATS &EZTBL STATUS2(EZTBLST2)
00160000IF &EZTBLST2 ^= 1 THEN ISPEXEC TBEND &EZTBL
00170000 
00180000SET RP = )
00190000SET LP = &STR((
00200000ISPEXEC VGET (FSSTJOBS) PROFILE
00210000 
00220000AGAIN:+
00230000ISPEXEC TBCREATE &EZTBL KEYS(JOBKEY) +
00240000                 NAMES(EZSTJOB,EZSTJESN,EZSTJSI,EZSTTAG) +
00250000                 NOWRITE REPLACE
00260000ISPEXEC TBSORT   &EZTBL FIELDS(JOBKEY,C,A)
00270000 
00280000CONTROL NOFLUSH NOPROMPT NOLIST NOCONLIST NOSYMLIST
00290000SET &SYSOUTTRAP = &TRAP
00300000  ST     &STR(&FSSTJOBS)
00310000SET STRC = &LASTCC
00320000SET TRAPKNT = &SYSOUTLINE
00330000SET  &SYSOUTTRAP = 0
00340000CONTROL   FLUSH
00350000IF &DEBUG = DEBUG THEN CONTROL MSG LIST CONLIST SYMLIST
00360000 
00370000IF (&STRC > 0)  THEN DO
00380000  SET SHORT = &STR(STATUS COMMAND ERROR)
00390000  SET LONG  = &STR(COMMAND "STATUS &FSSTJOBS" DID NOT COMPLETE NORMALLY. RC=&STRC)
00400000  ISPEXEC SETMSG MSG(SYS010)
00410000  ISPEXEC TBEND &EZTBL
00420000  EXIT
00430000END
00440000 
00450000SET K = 1
00460000SET JOBKNT = 0
00470000SET ERRKNT = 0
00480000 
00490000DO WHILE (&K <= &TRAPKNT)
00500000 
00510000  SET TEMP1 = &&SYSOUTLINE&K
00520000  SET LINE1 = &STR(&TEMP1)
00530000  IF &STR(&LINE1) = &STR(NO JOBS FOUND+) THEN GOTO NOJOBS
00540000  SET L1 = &LENGTH(&STR(&LINE1))
00550000  IF &SYSTSOE ^=   THEN SET RP1 = &SYSINDEX(&STR(&RP),&STR(&LINE1))
00560000  ELSE DO
00570000    VSCAN LINE1,')'
00580000    SET RP1 = &LASTCC
00590000  END
00600000  IF &RP1 = 0 THEN /* "JOB X NOT FOUND" OR SYNTAX ERROR */ GOTO SKIPIT
00610000  SET K = &K + 1
00620000  SET TEMP2 = &&SYSOUTLINE&K
00630000  SET LINE2 = &STR(&TEMP2)
00640000  SET L2 = &LENGTH(&STR(&LINE2))
00650000  IF &SYSTSOE ^= THEN SET RP2 = &SYSINDEX(&STR(&RP),&STR(&LINE2))
00660000  ELSE DO
00670000    VSCAN LINE2,')'
00680000    SET RP2 = &LASTCC
00690000  END
00700000 
00710000  IF   (&RP1+1 <= &L1-1) AND (&RP2 > 0) AND (&RP2+1 <= &L2) THEN DO
00720000    SET LX2 = &STR(&SUBSTR(&RP1+1:&L1-1,&STR(&LINE1))
00730000    SET LX1 = &STR(&SUBSTR(1:&RP2,&STR(&LINE2))
00740000    SET LX3 = &STR(&SUBSTR(&RP2+1:&L2,&STR(&LINE2))
00750000    SET LINE= &STR(&LX1 &SUBSTR(1:23,&STR(&LX2                        ))&LX3)
00760000    SET JOBKEY = &SUBSTR(1:14,&STR(&LINE              ))
00770000  END
00780000  ELSE /* SYNTAX ERROR */ GOTO SKIPIT
00790000 
00800000  SET L = &LENGTH(&STR(&LINE))
00810000  IF &L < 17 THEN GOTO SKIPIT
00820000 
00830000  SET EZSTJOB  = &SUBSTR(01:08,&STR(&LINE))
00840000  SET EZSTJOB  = &EZSTJOB /* STRIF OFF TRAILING BLANKS */
00850000  SET EZSTJESN = &SUBSTR(10:13,&STR(&LINE))
00860000  IF &DATATYPE(&EZSTJESN) ^= NUM THEN GOTO SKIPIT
00870000  SET EZSTJSI  = &SUBSTR(17:&L,&STR(&LINE))
00880000  SET EZSTTAG  =
00890000  ISPEXEC TBADD &EZTBL ORDER
00900000  SET JOBKNT = &JOBKNT + 1
00910000 
00920000  SKIPIT:+
00930000  SET K = &K + 1
00940000 
00950000END
00960000 
00970000IF &JOBKNT > 0 THEN DO
00980000  ISPEXEC TBTOP &EZTBL
00990000  SET PANEL   = &STR( PANEL(EZSTATUS) )
01000000  SET EZSTSEL =
01010000 
01020000  SHOWTBL: +
01030000  ISPEXEC TBDISPL &EZTBL    &PANEL
01040000  SET TBRC = &LASTCC
01050000  IF &TBRC > 8 THEN GOTO QUIT
01060000 
01070000  IF      &EZSTSEL = C THEN DO
01080000    /* CANCEL JOB/SAVE OUTPUT */
01090000    CONTROL NOFLUSH NOPROMPT
01100000    CANCEL (&EZSTJOB.(J&EZSTJESN.))
01110000    SET CANRC = &LASTCC
01120000    CONTROL   FLUSH
01130000    IF &CANRC = 0 THEN ISPEXEC TBDELETE &EZTBL
01140000  END
01150000  ELSE IF &EZSTSEL = Z | &EZSTSEL = CP THEN DO
01160000    /* CANCEL JOB/PURGE OUTPUT */
01170000    CONTROL NOFLUSH NOPROMPT
01180000    CANCEL (&EZSTJOB.(J&EZSTJESN.)) PURGE
01190000    SET CANRC = &LASTCC
01200000    CONTROL   FLUSH
01210000    IF &CANRC = 0 THEN ISPEXEC TBDELETE &EZTBL
01220000  END
01230000  ELSE IF &EZSTSEL = O | &EZSTSEL = B THEN DO
01240000    /* BROWSE OUTPUT */
01250000    CONTROL NOFLUSH NOPROMPT
01260000    OUTPUT (&EZSTJOB.(J&EZSTJESN.)) HOLD KEEP +
01270000            PRINT('&SYSUID..EZSTJOB&ZSCREEN..OUTLIST')
01280000    SET OUTRC = &LASTCC
01290000    CONTROL   FLUSH
01300000    IF &OUTRC = 0 THEN DO
01310000      SET ECMDBRTI = &STR(EZST JOB: &EZSTJOB.(J&EZSTJESN.) )
01320000      ISPEXEC VPUT (ECMDBRTI)
01330000      ISPEXEC CONTROL DISPLAY SAVE
01340000      ISPEXEC BROWSE  DATASET('&SYSUID..EZSTJOB&ZSCREEN..OUTLIST') +
01350000              PANEL(ECMDBROW)
01360000      SET BRRC = &LASTCC
01370000      ISPEXEC CONTROL DISPLAY RESTORE
01380000    END
01390000    ELSE DO
01400000      SET SHORT = &STR(OUTPUT ERROR)
01410000      SET LONG  = &STR(OUTPUT CAPTURE OF JOB &EZSTJOB.(J&EZSTJESN.) FAILED WITH RC=&OUTRC)
01420000      ISPEXEC SETMSG MSG(SYS010)
01430000    END
01440000  END
01450000 
01460000  IF &TBRC  = 0 THEN SET PANEL = &STR( PANEL(EZSTATUS) )
01470000  IF &TBRC  = 4 THEN SET PANEL =
01480000 
01490000  IF &EZSTSEL =  && &TBRC=0 THEN DO
01500000    /* REFRESH STATUS DISPLAY */
01510000    ISPEXEC TBEND   &EZTBL
01520000    GOTO AGAIN
01530000  END
01540000 
01550000  SET EZSTSEL =
01560000  IF &TBRC <  8 THEN GOTO SHOWTBL
01570000  IF &TBRC >= 8 THEN GOTO QUIT
01580000 
01590000  QUIT:+
01600000  ISPEXEC TBEND &EZTBL
01610000  IF &RPRC ^= 4 THEN ACFPROF MSGID
01620000  EXIT
01630000 
01640000END
01650000 
01660000NOJOBS:+
01670001IF &STR(&FSSTJOBS) =   +
01680001  THEN  SET SHORT = &STR(NO JOBS FOUND)
01690001  ELSE  SET SHORT = &STR(JOBS NOT FOUND)
01700000SET LONG  = &STR(STATUS COMMAND COULD NOT FIND REQUESTED JOBS: &FSSTJOBS)
01710000ISPEXEC SETMSG MSG(SYS010)
01720000IF &RPRC ^= 4 THEN ACFPROF MSGID
