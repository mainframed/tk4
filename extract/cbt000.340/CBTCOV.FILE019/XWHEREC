00010000PROC     1     LOCATION    DEBUG
00020000IF       &DEBUG = DEBUG +
00030000THEN     CONTROL LIST SYMLIST CONLIST PROMPT
00040003GLOBAL   SUBPOOL KEY FREE
00050000SET      &CSAPQEP = 8N     /* GDA  -> CSA PQE  */
00060000SET      &CSASPQEP = 52N   /* GDA  -> CSA SPQE */
00070000SET      &CVTGDA = 560N    /* CVT  -> GDA      */
00080008SET      &CVTQLPAQ = 188N  /* CVT  -> LPAQ CDE */
00090000SET      &PQEFFBQE = 0N    /* PQE  -> FBQE     */
00100000SET      &PQESIZE = 20N    /* PQE     SIZE     */
00110000SET      &PQEREGN = 24N    /* PQE     START    */
00120000SET      &SQASPQEP = 24N   /* GDA  -> SQA SPQE */
00130000SET      &AREA = INVALID
00140010SET      &PTRCDE = &STR(CVT+&CVTQLPAQ?)
00150009WRITE    NOW SEARCHING LINK PACK AREA QUEUE
00160008%WHEREM  &LOCATION &PTRCDE &DEBUG
00170009SET      &WHEREM = &LASTCC
00180009IF       &WHEREM ^= 0 +
00190009THEN     DO
00200009         WRITE    NOW SEARCHING LINK PACK AREA DIRECTORY
00210009         %WHEREL  &LOCATION &DEBUG
00220009         SET      &WHEREL = &LASTCC
00230009         END
00240009IF       &WHEREM ^= 0 && &WHEREL ^=0 +
00250009THEN     DO
00260009         WRITENR  DO YOU WANT TO FIND THE SUBPOOL ((N)/Y)? ===>
00270009         READ     &ANSWER
00280009         END
00290009IF       &ANSWER = Y +
00300008THEN     DO       /* FIND SUBPOOL */
00310009         WRITE    NOW SEARCHING FOR SUBPOOL
00320010         EQUATE   GDA   CVT+&CVTGDA? STRUCTURE(GDA) LENGTH(64)
00330010         EVALUATE GDA+&CSAPQEP?+&PQEREGN LENGTH(4)
00340008         SET      &START = &LASTCC
00350010         EVALUATE GDA+&CSAPQEP?+&PQESIZE LENGTH(4)
00360008         SET      &END = &LASTCC+&START
00370008         IF       &START <= &LOCATION && &LOCATION < &END +
00380008         THEN     SET   &PTRSPQE = &STR(GDA+&CSASPQEP)
00390008         ELSE     SET   &PTRSPQE = &STR(GDA+&SQASPQEP)
00400008         %WHERES  &LOCATION &PTRSPQE &DEBUG
00410008         IF       &LASTCC = 0 -
00420008         THEN     DO    /* ANSWER */
00430008                  NOTE  'IN SUBPOOL &SUBPOOL STORAGE KEY &KEY'
00440008                  IF    &FREE = YES -
00450008                  THEN  NOTE  'IN FREE STORAGE'
00460008                  END   /* ANSWER */
00470008         ELSE     NOTE  'NOT FOUND'
00480008         END      /* FIND SUBPOOL */
