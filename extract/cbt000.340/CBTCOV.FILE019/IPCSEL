00010000PROC     0        VERIFY DEBUG
00020000 
00030000/*********************************************************************/
00040000/*                   DUMP DATA SET MANAGEMENT                        */
00050000/* THIS CLIST IS INVOKED WHEN THE USER SELECTS A DUMP. OPEN THE      */
00060000/* DUMP, SELECT THE DUMP ANALYSIS PANEL, THEN CLOSE THE DUMP.        */
00070000/*********************************************************************/
00080000 
00090000IF       &DEBUG = DEBUG +
00100000THEN     CONTROL  ASIS   CONLIST NOFLUSH   LIST   MSG   PROMPT   SYMLIST
00110000ELSE     CONTROL  ASIS NOCONLIST NOFLUSH NOLIST   MSG   PROMPT NOSYMLIST
00120000 
00130000SET      &IPCRETCD = 0
00140000 
00150000ISPEXEC  VGET     (SYSIPCS NODE NODENAME IPCOS TYPE DSNAME DUMPOS)
00160000 
00170000%IPCEXEC &DEBUG
00180000 
00190000IF       &NODE = &STR() +
00200000THEN     DO
00210000         SET      &NODE = &NODENAME
00220000         ISPEXEC  VPUT     (NODE)
00230000         END
00240000 
00250000IF       &SYSIPCS = ACTIVE && &NODE = &NODENAME && &IPCOS = &DUMPOS && &TYPE = DUMP +
00260000THEN     DO
00270000         ISPEXEC  SELECT   CMD(%IPCOPEN &VERIFY &DEBUG)
00280000         ISPEXEC  VGET     (IPCRETCD) SHARED
00290000         END
00300000ELSE     DO
00310000         IF       &SYSIPCS = NOT ACTIVE +
00320000         THEN     ISPEXEC  SETMSG   MSG(IPC005)
00330000         IF       &IPCOS   ^= &DUMPOS +
00340000         THEN     ISPEXEC  SETMSG   MSG(IPC007)
00350000         IF       &NODE    ^= &NODENAME +
00360000         THEN     ISPEXEC  SETMSG   MSG(IPC009)
00370000         IF       &TYPE    ^= DUMP +
00380000         THEN     ISPEXEC  SETMSG   MSG(IPC010)
00390000         END
00400000 
00410000IF       &TYPE = GTF && &NODE = &NODENAME && &SYSDSN(&DSNAME) ^= OK +
00420000THEN     SET      &IPCRETCD = 16
00430000 
00440000IF       &IPCRETCD = 0 +
00450000THEN     ISPEXEC  SELECT   PANEL(IPCOPT)
00460000ELSE     ISPEXEC  SETMSG   MSG(IPC003)
00470000 
00480000IF       &SYSIPCS = ACTIVE && &NODE = &NODENAME && &IPCOS = &DUMPOS && &TYPE = DUMP +
00490000THEN     ISPEXEC  SELECT   PGM(BLSGSCMD) PARM(CLOSE ALL)
00500000 
00510000EXIT     CODE(&IPCRETCD)
