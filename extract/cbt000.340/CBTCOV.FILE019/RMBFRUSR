00010003PROC 0  DEBUG MENU() POOL() MSG
00020000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00030000/**                                                             **/
00040000/** CONVERT INFO CENTER "DESIGN A USER MENU" OPTIONS TO         **/
00050000/** THE MENU BUILD FACILITY FORMAT.  DEFAULT IS FOR YOUR PANEL. **/
00060000/**                                                             **/
00070000/** M. THEYS  12/04/86                                          **/
00080000/** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00090000 
00100000IF &DEBUG = DEBUG THEN  CONTROL   MSG   LIST   CONLIST   SYMLIST
00110000ELSE                    CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST
00120000 
00130000IF      &STR(&MENU)  = PDF       THEN  SET POOL = PRM
00140000ELSE IF &STR(&MENU)  = AOST      THEN  SET POOL = PRM
00150000ELSE IF &STR(&MENU)  = YOUR      THEN  SET POOL = YOR
00160000ELSE IF &STR(&MENU)  = GROUP     THEN  SET POOL = GRP
00170000ELSE IF &STR(&MENU)  = USER      THEN  SET POOL = USR
00180005ELSE IF &STR(&MENU)  = THE       THEN  SET POOL = THE
00200000ELSE IF &STR(&POOL)  =           THEN  SET POOL = YOR
00210000/* IF MENU=AOST THEN THIS SHOULD BE INVOKED UNDER APPLID=ISP ! */
00220000 
00230000/* SAVE THE CURRENT BUILD VARIABLE POOL AND MAX MENU OPTIONS */
00240000ISPEXEC VGET (RMBFPOOL,RMBFMAXM)
00250000SET SAVEPOOL = &STR(&RMBFPOOL)
00260000SET SAVEMAXM = &STR(&RMBFMAXM)
00270000 
00280000/* SET THE SELECTED BUILD VARIABLE POOL FOR INFOCENTER USER MENU */
00290000/* THEN VERIFY IF VALID AND SET CURRENT MAX MENU OPTIONS.        */
00300000SET RMBFPOOL = &POOL
00310000ISPEXEC VPUT (RMBFPOOL)
00320000%RMBFPOOL
00330000ISPEXEC VGET (RMBFPOOL,RMBFMAXM,RMBFMAXD)
00340000SET POOL = &RMBFPOOL
00350000/* INFOCENTER USER MODIFABLE MENU AS 20 OPTIONS */
00360000SET MAXM = 20
00370000IF &RMBFMAXM < &MAXM THEN SET MAXM = &RMBFMAXM
00380000 
00390000SET MOPT = 0
00400000SET K    = 1
00410000CONTROL ASIS
00420000DO WHILE (&K <= &MAXM)
00430000 
00440000  /* GET VARIABLES USED BY INFOCENTER DESIGN A USER MENU */
00450000  ISPEXEC VGET (R@O&K,R@U&K,R@UD&K,R@UC&K) PROFILE
00460000  SET OPT    = &STR(&&R@O&K)
00470000  SET NAME   = &STR(&&R@U&K)
00480000  SET DESCR  = &STR(&&R@UD&K)
00490000  SET ICMD   = &STR(&&R@UC&K)
00500000 
00510000  /* ASSIGN TO THE MENU BUILD VARIABLE POOL SPECIFIED */
00520000  IF &STR(&OPT) ^=  THEN DO
00530000    SET MOPT = &MOPT + 1
00540000    SET R&POOL.O&K = &STR(&OPT)
00550000    SET R&POOL.SD&K = &STR(&NAME)
00560000    SET R&POOL.LD&K = &STR(&DESCR)
00570000    SET R&POOL.C&K = &STR(&ICMD)
00580000    ISPEXEC VPUT (R&POOL.O&K,R&POOL.SD&K,R&POOL.LD&K,R&POOL.C&K) PROFILE
00590000  END
00600000 
00610000  SET K = &K + 1
00620000END
00630000 
00640000SET R&POOL.MOPT =
00650000IF &MOPT > 0 THEN SET R&POOL.MOPT = YES
00660000ISPEXEC VPUT (R&POOL.MOPT) PROFILE
00670000 
00680000SET RMBFPOOL = &STR(&SAVEPOOL)
00690000SET RMBFMAXM = &STR(&SAVEMAXM)
00700000ISPEXEC VPUT (RMBFPOOL,RMBFMAXM)
00710001 
00720002ISPEXEC VGET (R&POOL.ICF) PROFILE
00730002SET ICFDO = &STR(&&R&POOL.ICF)
00740002IF &ICFDO = YES THEN DO
00750002  SET R&POOL.ICF = DONE
00760002  ISPEXEC VPUT (R&POOL.ICF) PROFILE
00770002END
00780003 
00790003IF &MSG = MSG THEN DO
00800003  SET SHORT = &STR(NO OPTIONS COPIED)
00810003  IF &MOPT > 0 THEN SET SHORT = &STR(OPTIONS COPIED)
00820004  SET LONG  = &STR(INFOCENTER OPT M MENU CONVERTED. #OPTS=&MOPT  TO MBF VARPOOL=&POOL)
00830003  ISPEXEC SETMSG MSG(SYS010)
00840003END
