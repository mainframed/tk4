00010022PROC 1 DSNAME DEBUG +
00020009       REAL NOREAL MIG NOMIG DETAIL NODETAIL SUMMARY +
00030023       V() VOL() VOLUME() PRINT() P PR PRI PRIN FILE
00040009/** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** ** ** **/
00050009/** Used by ROCKWELL ISPF primary command: LISTSP          **/
00060009/** BY: Michael E. Theys   Rockwell International/SWCC     **/
00070009/** -- -- -- -- -- -- -- -- -*- -- -- -- -- -- -- -- -- -- **/
00080009/** Full-Screen display of LISTSP command:                 **/
00090009/**   SELECT CMD(%FSLISTSP !&ZPARM)                        **/
00100009/** The "!" allows LISTSP to be invoked without any dsname.**/
00110009/** %ECMD: traps a commands output for ISPF Browse display **/
00120009/** VSCAN: determine if substring exists and find position **/
00130009/** -- -- -- -- -- -- -- -- -*- -- -- -- -- -- -- -- -- -- **/
00140009/** "LISTSP" command w/o operand displays panel for info.  **/
00150009/** ** ** ** ** ** ** ** ** *** ** ** ** ** ** ** ** ** ** **/
00160009 
00170009/* LISTSP can have either PRINT or PRINT(dsname). Can't do in clist! */
00180009/* So take either P, PR, PRI, PRIN or new keyword FILE for "PRINT"   */
00190009/* "PRINT" outputs to terminal unless FILE(LISTSPRT) is allocated.   */
00200009IF &DEBUG = DEBUG THEN  CONTROL   MSG   LIST   CONLIST   SYMLIST
00210009ELSE                    CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST
00220009 
00230025IF &STR(&DSNAME) = &STR(!?) THEN DO /* Show Tutorial if "LISTSP ?" */
00240025  ISPEXEC SELECT PGM(ISPTUTOR) PARM($LISTSP)
00250025  EXIT
00260025END
00270025 
00280009/* Handle the case where LISTSP is used with no operands */
00290009/* ROCKWELL user version will prompt with panel entry.   */
00300026IF &STR(&DSNAME) = &STR(!)   +
00301026 | &STR(&DSNAME) = &STR(!!)  THEN GOTO SHOWPNL
00310009 
00320009SET LEN = &LENGTH(&STR(&DSNAME))
00330009IF &LEN > 1 THEN  SET &DSNAME = &SUBSTR(2:&LEN,&STR(&DSNAME))
00340009 
00350009SET ADDED = &STR(&REAL &NOREAL &MIG &NOMIG &DETAIL &NODETAIL &SUMMARY)
00360009 
00370009IF      &STR(&VOL)    ^= THEN SET AV = &STR(VOL(&VOL))
00380023ELSE IF &STR(&V)      ^= THEN SET AV = &STR(VOL(&V))
00390023ELSE IF &STR(&VOLUME) ^= THEN SET AV = &STR(VOLUME(&VOLUME))
00400009 
00410009IF  &STR(&PRINT) ^= THEN SET AP = &STR(PRINT(&PRINT))
00420009IF &P ^= | &PR ^= | &PRI ^= | &PRIN ^= | &FILE ^= THEN SET AP = PRINT
00430009 
00440009SET ADDED = &STR(&AV &AP &ADDED)
00450009 
00460010/* Give the user a chance to re-enter saved LISTSP Command */
00470024SET FSLSPDSN = &STR(&DSNAME)
00480024SET FSLSPOTH = &STR(&ADDED)
00490024ISPEXEC VPUT (FSLSPDSN,FSLSPOTH) PROFILE
00500023ISPEXEC CONTROL NONDISPL ENTER
00510023SHOWPNL:+
00520010ISPEXEC SELECT  PANEL(FSLISTSP)
