00010007PROC 0 DEBUG INIT SCROLL UP DOWN LEFT RIGHT AMT()
00020007  /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
00030007  /* RMBF@YOR Scrollable/Modifiable YOUR Menu with 36 Options.     */
00040007  /* This clist sets the initial menu options on PANEL(RMBF@YOR)   */
00050007  /* It is invoked by either YOUR panel on first startup           */
00060007  /* And it is also invoke when SCROLL PF Keys are used.           */
00070007  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00080007  /**                                                             **/
00090007  /** Rockwell Menu BUILD Facility for Scrollable RMBF@YOR.       **/
00100007  /**                                                             **/
00110007  /** Michael E. Theys                                            **/
00120007  /** Southwestern Computing Center                               **/
00130007  /** Information Systems Center                                  **/
00140007  /** Rockwell International                                      **/
00150007  /** 1200 N. Alma Rd.  Bldg 401-109                              **/
00160007  /** Richardson, Texas 75081-2205                                **/
00170007  /** (214) 996-2860  COMNET 437-2860                             **/
00180007  /** XMIT Address    SWCC.XTS0008                    Jan 1987    **/
00190007  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00200007 
00210007CONTROL ASIS
00220007ISPEXEC CONTROL ERRORS RETURN
00230007IF &DEBUG = DEBUG +
00240007  THEN CONTROL   LIST   CONLIST   SYMLIST NOFLUSH   PROMPT   MSG
00250007  ELSE CONTROL NOLIST NOCONLIST NOSYMLIST   FLUSH NOPROMPT NOMSG
00260007 
00270007/* Validate the BUILD Variable Pool and Max Displayable Menu Options */
00280007/* PANEL(RMBF@YOR) has RMBFMAXD= Max Displayable options=18 */
00290007/* PANEL(RMBF@YOR) has RMBFMAXM= Max available   options=36 */
00300007/* PANEL(RMBF@YOR) has RMBFPOOL= Variable Pool   "YOR"      */
00310007%RMBFPOOL
00320007ISPEXEC VGET (RMBFPOOL,RMBFMAXM,RMBFMAXD)
00330007SET POOL = &RMBFPOOL
00340007 
00350007/**************** START of SCROLL SET UP ****************************/
00360007IF &SCROLL =  SCROLL THEN DO
00370007  /* Define the amount to scroll by, if needed. */
00380007  IF &DATATYPE(&AMT) ^= NUM THEN DO
00390007    IF      &STR(&AMT) = HALF OR &STR(&AMT) = H THEN +
00400007         SET SCROLL = &RMBFMAXD / 2
00410007    ELSE IF &STR(&AMT) = PAGE OR &STR(&AMT) = P THEN +
00420007         SET SCROLL = &RMBFMAXD
00430007    ELSE SET SCROLL = 1
00440007  END
00450007  ELSE   SET SCROLL = &AMT
00460007 
00470007  /* Get current TOP displayable menu position  OPT#  */
00480007  ISPEXEC VGET (R&POOL.OPT#)
00490007  SET OPT# = &&R&POOL.OPT#
00500007  IF &DATATYPE(&OPT#) ^= NUM THEN SET OPT# = 1
00510007  IF &OPT# < 1               THEN SET OPT# = 1
00520007  IF &OPT# > &RMBFMAXM       THEN SET OPT# = &RMBFMAXM
00530007 
00540007  /* See if scroll pf key  option selected */
00550007  IF      &UP = UP                         THEN DO
00560007     SET OPT# = &OPT# - &SCROLL
00570007     IF &OPT# < 1  THEN SET OPT# = 1
00580007  END
00590007  ELSE IF &LEFT = LEFT   OR &FIRST = FIRST THEN DO
00600007     /* Scroll "LEFT" is FIRST or TOP option. */
00610007     SET OPT# = 1
00620007  END
00630007  ELSE IF &DOWN = DOWN                     THEN DO
00640012     ISPEXEC VGET (RYORLDO)
00650012     IF &OPT# >= &RYORLDO  THEN EXIT
00660007     SET OPT# = &OPT# + &SCROLL
00670007     IF &OPT# > &RMBFMAXM  THEN SET OPT# = &RMBFMAXM
00680011     IF &OPT# > &RYORLDO   THEN SET OPT# = &RYORLDO
00690007  END
00700007  ELSE IF &RIGHT = RIGHT OR &LAST = LAST   THEN DO
00710007     /* Scroll "RIGHT" is LAST or BOTTOM option. */
00720007     SET OPT# = &RMBFMAXM - &RMBFMAXD + 1
00730007  END
00740007  ELSE  EXIT
00750007  /* Now scroll it! */
00760007  SET SCROLLIT = SCROLLIT
00770007  /* OPT# now set to the number of the first option to be displayed */
00780007END
00790007/****************** END of SCROLL SET UP ****************************/
00800007 
00810007IF &INIT = INIT THEN DO
00820007  ISPEXEC VGET (R&POOL.DOIT,YOR1STDO)
00830007  SET ZERRSM =
00840007  SET ZERRLM =
00850007  ISPEXEC VPUT (ZERRSM,ZERRLM)
00860007  SET RYORFDO = 1  /* Position of the First Non-Blank Displayable Option */
00870007  SET RYORLDO = 1  /* Position of the Last  Non-Blank Displayable Option */
00880007  SET RYORMAXM = &RMBFMAXM
00890007 
00900007  /* Perform Panel Initialization if requested */
00910007  SET DOIT =  &STR(&&R&POOL.DOIT)
00920007  IF &DOIT =  YES AND &STR(&YOR1STDO) ^= DONE THEN DO
00930007    %RMBF@YOR
00940007    SET YOR1STDO = DONE
00950007    ISPEXEC VPUT (YOR1STDO)
00960007  END
00970007END    /* OF INIT */
00980007 
00990007/* Get the Displayable Menu Options */
01000007/* Begin Option to Display (Varies with Scroll) */
01010007IF &INIT     =   THEN ISPEXEC VGET (RYORMAXM)
01020007IF &SCROLLIT =   THEN SET OPT# = 1
01030007SET I = &OPT#
01040007SET Y = 1    /* Start Display position on panel */
01050007DO WHILE(&Y <= &RYORMAXM)
01060007  IF &I <=  &RYORMAXM THEN DO
01070007    ISPEXEC VGET (R&POOL.O&I,R&POOL.SD&I,R&POOL.LD&I,R&POOL.C&I) PROFILE
01080007    SET OPT      = &STR(&&R&POOL.O&I)
01090019    IF &DATATYPE(&STR(&OPT)) = NUM THEN +
01100007      IF &OPT <= 9 THEN SET OPT = &STR( &OPT)
01110007      ELSE              SET OPT = &STR(&OPT)
01120007    ELSE                SET OPT = &STR( &OPT)  /* Alpha option */
01130007    SET SD       = &STR(&&R&POOL.SD&I)
01140007    SET LD       = &STR(&&R&POOL.LD&I)
01150007    SET CMD      = &STR(&&R&POOL.C&I)
01160007  END
01170007  ELSE DO
01180007    SET OPT      = &STR( )
01190007    SET SD       = &STR( )
01200007    SET LD       = &STR( )
01210007    SET CMD      = &STR( )
01220007  END
01230020  IF &STR(&OPT) ^= THEN DO
01240020    SET SD = &STR(&SUBSTR(1:12,&STR(&SD            ))-)
01250020    IF  &INIT = INIT THEN DO
01260020      IF &I < &RYORFDO THEN SET RYORFDO = &I
01270020      IF &I > &RYORLDO THEN SET RYORLDO = &I
01280020    END
01290020  END
01300007  IF &Y <= &RMBFMAXD THEN DO
01310007    SET RYYYO&Y  = &STR(&OPT)
01320007    SET RYYYSD&Y = &STR(&SD)
01330007    SET RYYYLD&Y = &STR(&LD)
01340007    SET RYYYC&Y  = &STR(&CMD)
01350007    ISPEXEC VPUT (RYYYO&Y,RYYYSD&Y,RYYYLD&Y,RYYYC&Y)
01360007  END
01370007  SET I = &I + 1
01380007  SET Y = &Y + 1
01390007END
01400007IF &INIT = INIT THEN DO
01410007     SET RYORMAXM = &RYORLDO
01420007     ISPEXEC VPUT (RYORFDO,RYORLDO,RYORMAXM)
01430007END
01440007ELSE ISPEXEC VGET (RYORFDO,RYORLDO)
01450009 
01460009/* Set ShortName to "* NO MORE *" after the last displayed option */
01470009SET EOPT = (&RYORLDO - &OPT# + 1) + 1
01480013IF (&EOPT >= 1 AND &EOPT <= &RMBFMAXD) AND +
01490015   (&RYORLDO > &RMBFMAXD OR  &OPT# > 1 ) THEN DO
01500016  SET ELD = &STR(* End of Options *)
01510016  SET RYYYLD&EOPT = &STR(&ELD)
01520018  ISPEXEC VPUT (RYYYLD&EOPT)
01530009END
01540007 
01550007SET R&POOL.OPT# = &OPT#
01560007ISPEXEC VPUT (R&POOL.OPT#)
01570007IF      (&OPT# + &RMBFMAXD) <= &RYORLDO THEN +
01580008                               SET RYORMORE= &STR(   More options ->  )
01590007ELSE IF (&OPT#            ) >  &RYORFDO THEN +
01600008                               SET RYORMORE= &STR(<- More options     )
01610007ELSE                           SET RYORMORE= &STR( )
01620007ISPEXEC VPUT (RYORMORE)
01630007 
01640007/* Menu options have been load to the displayable portion of the menu */
01650007/* or they have been scrolled either up or down the proper amount.    */
01660007/* Now back to the Scrollable/Modifiable YOUR Primary Option Menu     */
01670007EXIT
