        /**********************************************************************/
        /* EDIT MACRO THAT IS RUN WITH A DATA SET MEMBER THAT CONTAINS        */
        /* THE OUTPUT FROM THE SAVEMEMS MACRO.  THIS MACRO WILL COPY THE      */
        /* MEMBERS BACK TO THE ORIGINAL MEMBERS.                              */
        /**********************************************************************/
        ISREDIT MACRO
 
        ISREDIT (CURMEMB) = MEMBER
        IF &STR(&CURMEMB) = &STR() THEN +
          DO
            SET &ZEDSMSG = &STR(DATA SET NOT A PDS)
            SET &ZEDLMSG = &STR(THE CURRENT DATA SET IS NOT A PDS - +
                                MOVE THE DATA TO A MEMBER OF A PDS)
            ISPEXEC SETMSG MSG(ISRZ001)
            EXIT
          END
 
        SET COUNT = 0
        SET CURLINE = 1
        ISREDIT (LINECNT) = LINENUM .ZLAST
 
        DO WHILE &CURLINE < &LINECNT
          ISREDIT (LINE) = LINE &CURLINE
          SET CHECKSTR = &SUBSTR(1:16,&LINE)
          SET MEMBER = &SUBSTR(27:34,&LINE)
          SET MEMLINES = &SUBSTR(44:49,&LINE)
          IF &STR(&CHECKSTR) ^= &STR(*** LOADMEMS ***) THEN +
            DO
              SET &ZEDSMSG = &STR(INVALID FORMAT)
              SET &ZEDLMSG = &STR(DATA IS NOT THE PROPER FORMAT FOR +
                                  THIS MACRO)
              ISPEXEC SETMSG MSG(ISRZ001)
              EXIT
            END
          SET CURLINE = &CURLINE + 1
 
          SET &ENDLINE = &CURLINE + &MEMLINES - 1
          IF &MEMLINES > 0 THEN +
            DO
              ISREDIT REPLACE &MEMBER &CURLINE &ENDLINE
              SET RC = &LASTCC
              WRITE MEMBER &MEMBER CREATED  RC = &RC
              SET COUNT = &COUNT + 1
            END
          ELSE +
            DO
              WRITE MEMBER &MEMBER NOT CREATED - NO LINES IN MEMBER
            END
 
          SET CURLINE = &ENDLINE + 1
        END
        SET &ZEDSMSG = &STR(&COUNT MEMBERS CREATED)
        SET &ZEDLMSG = &STR(&COUNT MEMBERS WERE CREATED IN THE +
                            CURRENT DATA SET)
        ISPEXEC SETMSG MSG(ISRZ000)
