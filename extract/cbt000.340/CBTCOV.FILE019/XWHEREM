00010010PROC     2 LOCATION CDECHAIN DEBUG
00020007/*********************************************************************/
00030007/*       SCAN CDE CHAIN FOR LOAD MODULE CONTAINING &LOCATION         */
00040007/*********************************************************************/
00050001IF       &DEBUG = DEBUG +
00060001THEN     CONTROL CONLIST LIST MSG PROMPT SYMLIST
00070007SET      &CDATTR = 28N        /* CDE ATTRIBUTE FLAGS                 */
00080007SET      &CDCHAIN = 0N        /* CDE -> NEXT CDE                     */
00090007SET      &CDMIN = X'04'       /* CDE(CDATTR) MINOR CDE FLAG          */
00100007SET      &CDNAME = 8N         /* CDE NAME OF MODULE                  */
00110007SET      &CDXLMJP = 20N       /* CDE -> EXTENT LIST OR MAJOR CDE     */
00120007SET      &XTLMSBAA = 12N      /* XTLST -> MODULE                     */
00130007SET      &XTLMSBLN = 9N       /* XTLST LENGTH OF MODULE              */
00140009EQUATE   NEXTCDE &CDECHAIN
00150009COMPARE  ADDRESS(NEXTCDE) WITH(VALUE(X'00000000')) LENGTH(4)
00160007IF       &LASTCC ^= 0 /* NOT EQUAL? */ +
00170007THEN     SET &CDE = VALID
00180007ELSE     SET &CDE = INVALID
00190003SET      &FOUND = NO
00200007DO       WHILE    &CDE = VALID && &FOUND = NO /* CDE LOOP */
00210012         SET      &XCDE = &STR(NEXTCDE?)
00220011         IF       &DEBUG = DEBUG +
00230011         THEN     LIST &XCDE STRUCTURE(CDE)
00240007         COMPARE  ADDRESS(&XCDE+&CDATTR) MASK(&CDMIN) +
00250007                  WITH(VALUE(&CDMIN)) LENGTH(1)
00260007         IF       &LASTCC ^= 0 /* NOT EQUAL? */ +
00270007         THEN     SET &MAJORCDE = &STR(&XCDE)
00280012         ELSE     SET &MAJORCDE = &STR(&XCDE+&CDXLMJP?)
00290012         SET      &XXTLST = &STR(&MAJORCDE+&CDXLMJP?)
00300007         EVALUATE &XXTLST+&XTLMSBAA LENGTH(4)
00310007         SET      &START = &LASTCC
00320007         EVALUATE &XXTLST+&XTLMSBLN LENGTH(3)
00330007         SET      &SIZE = &LASTCC
00340007         IF       &START <= &LOCATION && &LOCATION <= &START+&SIZE +
00350007         THEN     DO      /* LOCATION DEFINITION */
00360007                  SET     &FOUND = YES
00370007                  LIST    &MAJORCDE+&CDNAME CHARACTER LENGTH(8) +
00380007                          REMARK('MODULE NAME') +
00390007                          DISPLAY(NOMACHINE REMARK NOREQUEST STORAGE)
00400010                  LIST    &XXTLST+&XTLMSBAA+1 POINTER LENGTH(3) +
00410007                          REMARK('MODULE ADDRESS') +
00420007                          DISPLAY(NOMACHINE REMARK NOREQUEST STORAGE)
00430007                  LIST    &XXTLST+&XTLMSBLN HEXADECIMAL LENGTH(3) +
00440007                          REMARK('MODULE LENGTH') +
00450007                          DISPLAY(NOMACHINE REMARK NOREQUEST STORAGE)
00460007                  END     /* LOCATION DEFINITION */
00470007         ELSE     DO      /* CDE CHAIN */
00480009                  EQUATE  NEXTCDE &XCDE+&CDCHAIN
00490009                  COMPARE ADDRESS(NEXTCDE) +
00500007                          WITH(VALUE(X'00000000')) LENGTH(4)
00510007                  IF      &LASTCC = 0 /* EQUAL? */ +
00520007                  THEN    SET &CDE = INVALID
00530007                  END     /* CDE CHAIN */
00540007         END      /* CDE LOOP */
00550010DROPSYM  NEXTCDE
00560002IF       &FOUND = YES +
00570007THEN     EXIT     CODE(0)
00580007ELSE     EXIT     CODE(4)
