00010000PROC     0        DEBUG
00020000/**********************************************************************/
00030000/* ISPF APPLICATION TO UPDATE A PERSONAL LIBRARY                      */
00040000/*                                                                    */
00050000/* ISPF TABLE "LIBRARY" CONTAINS A LIST OF MANUAL FORM NUMBERS AND    */
00060000/* TITLES EXTRACTED FROM INFORMATION/LIBRARY. THE TABLE IS SORTED BY  */
00070000/* FORM NUMBER.                                                       */
00080000/*                                                                    */
00090000/* THIS APPLICATION INVOKES INFORMATION/LIBRARY. YOU MAY THEN OBTAIN  */
00100000/* A LIST OF TITLES VIA THE "SEARCH" COMMAND. THE "PRINT" COMMAND     */
00110000/* WILL TRANSFER THE TITLE LIST TO A DATA SET. THE "QUIT" COMMAND     */
00120000/* WILL RETURN CONTROL TO THIS APPLICATION. THE APPLICATION WILL THEN */
00130000/* TRANSFER THE TITLES FROM THE INFORMATION/LIBRARY OUTPUT TO THE     */
00140000/* "LIBRARY" TABLE. THE ENTRIES WILL HAVE A STATUS OF "NEW". ENTRIES  */
00150000/* WITH A PREVIOUS STATUS OF "NEW" WILL BE CHANGED TO BLANK.          */
00160000/* DUPLICATE ENTRIES ARE IGNORED.                                     */
00170000/**********************************************************************/
00180000IF       &DEBUG = DEBUG +
00190002THEN     CONTROL  ASIS   CONLIST NOFLUSH   LIST   MSG   PROMPT   SYMLIST
00200002ELSE     CONTROL  ASIS NOCONLIST NOFLUSH NOLIST NOMSG   PROMPT NOSYMLIST
00210000ISPEXEC  VGET     (LIBTABL LIBDSN ZMONTH ZDAY ZYEAR)
00220000/**********************************************************************/
00230000/* INVOKE INFORMATION/LIBRARY                                         */
00240000/**********************************************************************/
00250000IF       &SYSDSN(&LIBDSN) = OK +
00260000THEN     DO       /* WRITE EOF IN CASE NO OUTPUT */
00270000         ALLOCATE FILE(TITLES) DATASET(&LIBDSN) REUSE OLD
00280000         OPENFILE TITLES   OUTPUT
00290000         CLOSFILE TITLES
00300000         END      /* WRITE EOF IN CASE NO OUTPUT */
00310000ELSE     ALLOCATE FILE(TITLES) DATASET(&LIBDSN) REUSE NEW SPACE(1,10) TRACKS
00320000ISPEXEC  SELECT   CMD(%LIBMVS1)
00330000ISPEXEC  CONTROL  DISPLAY  LINE     START(1)
00340000/**********************************************************************/
00350000/* OPEN "LIBRARY" TABLE                                               */
00360000/**********************************************************************/
00370000IF       &SYSDSN(&LIBTABL) = OK +
00380000THEN     ALLOCATE FILE(LIBTABL) DSNAME(&LIBTABL) REUSE OLD
00390000ELSE     ALLOCATE FILE(LIBTABL) DSNAME(&LIBTABL) REUSE NEW DSORG(PO) RECFM(F B) LRECL(80) BLKSIZE(6160) +
00400000                  SPACE(1,1) TRACK DIR(10)
00410000ISPEXEC  TBOPEN   LIBRARY  WRITE    LIBRARY(LIBTABL)
00420000SET      &RETURN = &LASTCC
00430000IF       &RETURN = 12 +
00440000THEN     DO       /* TABLE IN USE */
00450000         ISPEXEC  SETMSG   MSG(LIB001)
00460000         EXIT     CODE(12)
00470000         END      /* TABLE IN USE */
00480000IF       &RETURN = 8 +
00490000THEN     DO       /* TABLE DOES NOT EXIST */
00500000         ISPEXEC  TBCREATE LIBRARY  KEYS(FORM) NAMES(STATUS OLDSTAT DATE SORT TITLE CONTINUE) WRITE LIBRARY(LIBTABL)
00510000         ISPEXEC  TBSORT   LIBRARY  FIELDS(SORT,C,A)
00520000         END      /* TABLE DOES NOT EXIST */
00530000/**********************************************************************/
00540000/* RESET ALL "NEW" ENTRIES TO BLANK                                   */
00550000/**********************************************************************/
00560000ISPEXEC  TBSKIP   LIBRARY
00570000SET      &TBSKIP = &LASTCC
00580000DO       WHILE    &TBSKIP = 0
00590000         IF       &STATUS = N +
00600000         THEN     DO
00610000                  SET      &STATUS = &STR()
00620000                  SET      &OLDSTAT = &STR()
00630000                  ISPEXEC  TBMOD    LIBRARY  ORDER
00640000                  END
00650000         ISPEXEC  TBSKIP   LIBRARY
00660000         SET      &TBSKIP = &LASTCC
00670000         END
00680000/**********************************************************************/
00690000/* OPEN INFO/LIB FILE, PREPARE FOR EOF                                */
00700000/**********************************************************************/
00710000OPENFILE TITLES   INPUT
00720000SET      &EOF = NO
00730000ERROR    DO       /* DETECT END OF FILE */
00740000         SET      &RETURN = &LASTCC
00750000         IF       &RETURN = 400 +
00760000         THEN     SET      &EOF = YES
00770000         RETURN
00780000         END      /* DETECT END OF FILE */
00790000/**********************************************************************/
00800000/* COPY INFORMATION/LIBRARY OUTPUT TO "LIBRARY" TABLE                 */
00810000/**********************************************************************/
00820000SET      &STATUS = N
00830000SET      &OLDSTAT = N
00840000SET      &DATE = &STR(&ZMONTH&ZDAY&ZYEAR)
00850000SET      &SEARCH  = YES
00860000GETFILE  TITLES
00870000DO       WHILE    &EOF = NO         /* READ TITLE DATA SET */
00880000         SET      &ID = &SUBSTR(3:8,&NRSTR(&TITLES))
00890000         IF       &NRSTR(&ID) = TITLE: +
00900000         THEN     DO       /* PROCESS TITLE */
00910000                  SET      &SEARCH = NO
00920000                  SET      &FORM = &SUBSTR(12:23,&NRSTR(&TITLES))
00930000                  SET      &FORM = &STR(&SYSCAPS(&FORM))
00940001                  SET      &SORT = &SUBSTR(2:12,&STR(&FORM   ))
00950000                  SET      &TITLE = &SUBSTR(25:&LENGTH(&NRSTR(&TITLES)),&NRSTR(&TITLES))
00960000                  SET      &CONTINUE = &STR()
00970000                  ISPEXEC  TBADD    LIBRARY  ORDER
00980000                  END      /* PROCESS TITLE */
00990000         ELSE     IF       &NRSTR(&ID) = &STR() && &SEARCH = NO +
01000000                  THEN     DO       /* PROCESS CONTINUATION */
01010000                           SET      &CONTINUE = &SUBSTR(25:&LENGTH(&NRSTR(&TITLES)),&NRSTR(&TITLES))
01020000                           ISPEXEC  TBMOD    LIBRARY  ORDER
01030000                           END      /* PROCESS CONTINUATION */
01040000                  ELSE     SET      &SEARCH = YES
01050000         GETFILE  TITLES
01060000         END      /* READ TITLES DATA SET */
01070000/**********************************************************************/
01080000/* CLOSE/FREE INPUT FILE AND "LIBRARY" TABLE                          */
01090000/**********************************************************************/
01100000ERROR    OFF
01110000CLOSFILE TITLES
01120000FREE     FILE(TITLES)
01130000ISPEXEC  TBCLOSE  LIBRARY  PAD(25)  LIBRARY(LIBTABL)
01140000/**********************************************************************/
01150000/* INVOKE STATUS APPLICATION                                          */
01160000/**********************************************************************/
01170000ISPEXEC  SELECT   CMD(%LIBRARYS &DEBUG)
