00010000PROC 1 OPT
00020000/* REVISED 3/12/85
00030000CONTROL    NOMSG /* LIST CONLIST
00040000ISPEXEC VGET (CENTER,Z)
00050000/* ================================================================= */
00060000/* PERFORM BUILD OF A TRANSACTION                                    */
00070000/* ================================================================= */
00080000   ISPEXEC VGET BDTFROM
00090000   IF &BDTFROM ^= &Z THEN +
00100000     SET DSN1 = &BDTFROM
00110000   ELSE +
00120000     SET DSN1 = &Z
00130000   SET DSN2 = &Z
00140000DOIT: +
00150000   SET PDS1 = &Z /* NO FOR DSORG=PS, YES FOR DSORG=PO               */
00160000   SET MEM1 = &Z /* SET FOR ANY DSORG=PO TO DIR, ALL OR ONE        */
00170000   SET MBR1 = &Z
00180000   SET PDS2 = &Z /* NO FOR DSORG=PS, YES FOR DSORG=PO               */
00190000   SET MEM2 = &Z /* SET FOR A MEMBER SPECIFIED TRANSACTION TO "ONE"*/
00200000   SET MBR2 = &Z
00210000   SET DSN = &STR(&DSN1)
00220000   ISPEXEC DISPLAY PANEL(RBDTDSN1)
00230000   SET LASTRC = &LASTCC
00240000GETDSNFM: +
00250000   IF &LASTRC > 4 THEN +
00260000     DO
00270000       SET DSN =
00280000       SET DSN1 =
00290001       SET MEMS =
00300001       ISPEXEC VPUT (DSN DSN1 MEMS)
00310000       EXIT
00320000     END
00330000   IF &ZCMD ^= &Z THEN +
00340000     DO
00350000       SET &SHORT = INVALID COMMAND
00360000       SET &LONG = &STR("&ZCMD" NOT RECOGNIZED AS A VALID COMMAND)
00370000       ISPEXEC DISPLAY CURSOR(ZCMD) MSG(RBDT010)
00380000       SET LASTRC = &LASTCC
00390000       GOTO GETDSNFM
00400000     END
00410000   SET CTR1 = 0
00420000   IF &VOL1 ^= &Z THEN +
00430000     SET CTR1 = &CTR1 + 1
00440000   IF &VOLREF1 ^= &Z THEN +
00450000     SET CTR1 = &CTR1 + 1
00460000   IF &CTR1 > 1 THEN +
00470000     DO
00480000       SET &SHORT = MUTUALLY EXCLUSIVE FIELD
00490000       SET &LONG = &STR(ONLY THE VOLUME OR THE VOLREF FIELD +
00500000                        MAY BE SPECIFIED)
00510000       ISPEXEC DISPLAY MSG(RBDT010) CURSOR(VOL1)
00520000       SET LASTRC = &LASTCC
00530000       GOTO GETDSNFM
00540000     END
00550000/* ================================================================= */
00560000/* SET BDTFDSN = PANEL CONTENTS
00570000/* ================================================================= */
00580000   ISPEXEC SELECT PGM(RSPFPARS)
00590000   SET LASTRC = &LASTCC
00600000   IF &LASTRC > 0 THEN +
00610000     DO
00620000        IF &LASTRC = 1 THEN +
00630000          DO
00640000            SET SHORT = &STR(INVALID DSNAME - QUOTES)
00650000            SET LONG = &STR(UNBALANCED QUOTES IN THE OTHER DSN FIELD)
00660000          END
00670000        IF &LASTRC = 2 THEN +
00680000          DO
00690000            SET SHORT = &STR(INVALID DSNAME)
00700000            SET LONG = &STR(THE OTHER DSNAME FIELD IS AN INVALID NAME)
00710000          END
00720000        IF &LASTRC > 2 THEN +
00730000          DO
00740000            SET SHORT = ERROR IN PARSING DSNAME
00750000            SET LONG = &STR(ERROR PARSING DSNAME &BDTFDSN - THE +
00760000                RETURN CODE WAS &LASTRC)
00770000          END
00780000        ISPEXEC DISPLAY MSG(RBDT010)
00790000        SET LASTRC = &LASTCC
00800000        GOTO GETDSNFM
00810000     END
00820000   ISPEXEC VGET (ZDSQ ZDSQMEM ZMEM) SHARED
00830000   SET BDTFDSN = &ZDSQ
00840000   ISPEXEC VPUT (BDTFDSN)
00850000   IF &STR(&ZMEM) ^= &Z THEN +
00860000      DO
00870000         IF &STR(&ZMEM) = &STR(*) THEN +
00880000           DO
00890000             SET MEM1 = ALL
00900000             SET ZMEM = &Z
00910000           END
00920000         ELSE +
00930000           DO
00940000             SET MBR1 = &ZMEM
00950000             SET MEM1 = ONE
00960000           END
00970000         SET PDS1 = YES
00980000      END
00990000  IF &VOLREF1 ^= &Z THEN +
01000000    DO
01010000/* ================================================================= */
01020000/* SET UP THE VOLREF NAME FULLY QUALIFIED WITHOUT QUOTES  */
01030000/* ================================================================= */
01040000      ISPEXEC VPUT (VOLREF1) SHARED
01050000      ISPEXEC SELECT PGM(RSPFPARS) PARM(/VOLREF1)
01060000      SET LASTRC = &LASTCC
01070000      IF &LASTRC > 0 THEN +
01080000        DO
01090000           SET SHORT = ERROR IN PARSING DSNAME
01100000           SET LONG = &STR(ERROR PARSING DSNAME &VOLREF1 - THE +
01110000               RETURN CODE WAS &LASTRC)
01120000           ISPEXEC DISPLAY MSG(RBDT010)
01130000           SET LASTRC = &LASTCC
01140000           GOTO GETDSNFM
01150000        END
01160000      ISPEXEC VGET (VOLREF1) SHARED
01170000    END
01180000/* ================================================================= */
01190000/* DETERMINE IF WE ARE AT THE SENDING NODE                           */
01200000/* ================================================================= */
01210000SET DSCAT = NO
01220000IF &CENTER = &BDTFNODE THEN +
01230000  DO                            /* FROM NODE HERE  */
01240000    IF &CTR1 = 0 THEN +
01250000      DO                        /* NO VOL OR VOLREF, M/B CATALOGED */
01260004        IF &SYSDSN('&ZDSQMEM') ^= OK THEN +
01270002          DO        /* DATA SET WAS NOT THERE                       */
01280005            SET SHORT = &STR(DATASET OR MBR MISSING)
01290004            SET LONG = &STR(DATASET &ZDSQMEM NOT FOUND)
01300006            ISPEXEC VGET (RDSN)
01310006            IF &RDSN ^= &Z THEN +
01320000               SET CURS = DSN
01330000            ELSE +
01340000               SET CURS = MEM
01350000            ISPEXEC DISPLAY MSG(RBDT010) CURSOR(&CURS)
01360000            SET LASTRC = &LASTCC
01370000            GOTO GETDSNFM
01380000          END
01390000        ELSE +
01400000          DO                                /* DATA SET IS CATALOGED */
01410000/**********************************************************************/
01420000/* INVOKE R$DSINFO TO GET INFORMATION ABOUT THE DATASET               */
01430000/**********************************************************************/
01440002   SET SHOW = S                  /* SET FOR SHORT DISPLAY */
01450009   ISPEXEC SELECT CMD(%R$DSINFO '&BDTFDSN' OPT(&SHOW))
01460000   ISPEXEC VGET (ZALVOL DEVT DSORG ZALRF ZALLREC ZALBLK ZALSPC ZALDIR +
01470000                 ZAL1EX ZAL2EX)
01480000/**********************************************************************/
01490010/* THE FOLLOWING VARIABLES ARE NOW AVAILABLE:                         */
01500000/*                                                                    */
01510000/* &ZALVOL   - Volume serial             &DEVT     - Device Type      */
01520000/* &DSORG    - Data Set Organization     &ZALRF    - RECFM            */
01530000/* &ZALLREC  - LRECL (Record Size)       &ZALBLK   - Blocksize        */
01540000/* &ZALSPC  #- Allocation Type           &ZALDIR   - No. of Dir. Blks.*/
01550000/* &ZAL1EX  *- Primary Allocation        &ZAL2EX  *- Secondary Alloc. */
01560000/*                                                                    */
01570000/* # = TRACKS, CYLINDERS or BLOCKS                                    */
01580000/* * = In Allocation Type                                             */
01590000/**********************************************************************/
01600000   /* LOCAL VARIABLES TO GET RID OF LATER */
01610000            SET SCALO = &ZAL2EX
01620000            SET DIRALLOC = &ZALDIR
01630000            SET RECFM = &ZALRF
01640000            SET LRECL = &ZALLREC
01650000            SET BLKSIZE = ZALBLK
01660000   /* END LOCAL VARIABLES TO GET RID OF LATER */
01670000            IF &ZALSPC = CYLINDER THEN +
01680000              SET UNIT = CYL
01690000            ELSE +
01700000              IF &ZALSPC = TRACK THEN +
01710000                SET UNIT = TRK
01720000              ELSE +
01730000                SET UNIT = BLK
01740000            SET DSCAT = YES
01750000            IF &PDS1 = &Z THEN +
01760000              IF &DSORG = PO THEN +
01770000                DO                         /* PARTITIONED            */
01780000                  SET PDS1 = YES
01790000                  SET MEM1 = DIR
01800000                END
01810000              ELSE +
01820000                DO                         /* SEQUENTIAL DSORG       */
01830000                  SET PDS1 = NO
01840000                  IF &MBR1 ^= &Z THEN +
01850000                    DO                     /* MEMBER WAS IMPLIED     */
01860000                      SET SHORT = &STR(DATASET NOT PARTITIONED)
01870000                      SET LONG = &STR(A MEMBER WAS GIVEN OR MEMBERS +
01880000                          WERE IMPLIED BY '*'; DS IS NOT PARTITIONED)
01890000                      ISPEXEC SETMSG MSG(RBDT010)
01900000                      GOTO DOIT
01910000                    END
01920000                END
01930000          END                              /* END DATA SET CATALOGED */
01940000      END                                  /* END NO VOL SPECIFIED   */
01950000  END                                        /* END SENDING CENTER */
01960000/* ================================================================= */
01970000/*      GET THE TO DATA SET INFORMATION                              */
01980000/* ================================================================= */
01990000   IF &STR(&DSN) ^= &Z THEN +
02000000     DO
02010000       IF &MEM1 = ALL THEN +
02020000         DO
02030000           ISPEXEC VGET (RDSN)  SHARED
02040000           SET DSN = &RDSN              /* GET RID OF THE (*)  */
02050000         END
02060000     END
02070000   ELSE +
02080000     SET DSN = &DSN2
02090000GETDSN2: +
02100000   IF &OPT = T THEN +
02110000       SET WHICH = &STR(TRANSACTION NAME)
02120000   ELSE +
02130000       SET WHICH = &STR(GMJD MEMBER NAME)
02140000   ISPEXEC DISPLAY PANEL(RBDTDSN2)
02150000   SET LASTRC = &LASTCC
02160000GETDSNTO: +
02170000   IF &LASTRC > 0 THEN +
02180000     GOTO DOIT
02190000   IF &ZCMD ^= &Z THEN +
02200000     DO
02210000       SET &SHORT = INVALID COMMAND
02220000       SET &LONG = &STR("&ZCMD" NOT RECOGNIZED AS A VALID COMMAND)
02230000       ISPEXEC DISPLAY CURSOR(ZCMD) MSG(RBDT010)
02240000       SET LASTRC = &LASTCC
02250000       GOTO GETDSNTO
02260000     END
02270000   SET &SHORT = MUTUALLY EXCLUSIVE FIELD
02280000   SET &LONG = &STR(ONLY 1 OF THE FIELDS VOLUME, VOLREF OR UNIT +
02290000                    MAY BE SPECIFIED)
02300000   SET CTR2 = 0
02310000   IF &VOL2 ^= &Z THEN +
02320000     SET CTR2 = &CTR2 + 1
02330000   IF &VOLREF2 ^= &Z THEN +
02340000     SET CTR2 = &CTR2 + 1
02350000   IF &TOUNIT ^= &Z THEN +
02360000     SET CTR2 = &CTR2 + 1
02370000   IF &CTR2 > 1 THEN +
02380000     DO
02390000       ISPEXEC DISPLAY MSG(RBDT010) CURSOR(VOL2)
02400000       SET LASTRC = &LASTCC
02410000       GOTO GETDSNTO
02420000     END
02430000/*********************************************************************/
02440000/*  DETERMINE WE ARE NOT DOING A THIRD PARTY TRANSACTION             */
02450000/*********************************************************************/
02460000   IF &CENTER ^= &BDTFNODE && &CENTER ^= &BDTTNODE THEN +
02470000      DO
02480000        SET SHORT = &STR(THIRD PARTY TRANSACTION)
02490000        SET LONG = &STR(YOUR PRESENT COMPUTING CENTER MUST BE EITHER +
02500000                        SENDER OR RECEIVER)
02510000        ISPEXEC DISPLAY MSG(RBDT010) CURSOR(BDTTNODE)
02520000        SET LASTRC = &LASTCC
02530000        GOTO GETDSNTO
02540000      END
02550000/* ================================================================= */
02560000/*   CONTINUE WITH "TO" DATASET PROCESSING                           */
02570000/* ================================================================= */
02580000   ISPEXEC SELECT PGM(RSPFPARS)
02590000   SET LASTRC = &LASTCC
02600000   IF &LASTRC > 0 THEN +
02610000     DO
02620000        SET SHORT = ERROR IN PARSING DSNAME
02630000        SET LONG = &STR(ERROR PARSING DSNAME &BDTTDSN - THE +
02640000            RETURN CODE WAS &LASTRC)
02650000        ISPEXEC DISPLAY MSG(RBDT010)
02660000        SET LASTRC = &LASTCC
02670000        GOTO GETDSNTO
02680000     END
02690017   ISPEXEC VGET (ZDSQ, ZMEM, ZDSQMEM) SHARED
02700000   SET BDTTDSN = &ZDSQ
02710000   SET DSN =
02720000   ISPEXEC VPUT (BDTTDSN,DSN)
02730000   IF &ZMEM ^= &Z THEN +
02740000      DO
02750000         SET MBR2 = &ZMEM
02760000         SET PDS2 = YES
02770000         SET MEM2 = ONE
02780000      END
02790000/*********************************************************************/
02800000/*  DETERMINE WE DON'T HAVE A DATASET AND CENTER CONFLICT            */
02810000/*********************************************************************/
02820000   IF &BDTFNODE = &BDTTNODE THEN +
02830000     IF &BDTFDSN = &BDTTDSN THEN +
02840020       IF &INTRDR = &Z THEN +
02850019         DO
02860019           SET SHORT = TRANSMIT TO SAME DS
02870019           SET LONG  = &STR(YOUR TRANSACTION HAS SPECIFIED THE SAME +
02880019                            DATASET AT THE SAME CENTER.)
02890019           ISPEXEC SETMSG MSG(RBDT010)
02900019           GOTO GETDSN2
02910019         END
02920000/* ================================================================= */
02930000/* PROCESS NON-INTRDR TRANSACTION                                    */
02940000/* ================================================================= */
02950000  IF &INTRDR = &Z THEN +
02960000    DO
02970000/* ================================================================= */
02980000/* CHECK THE VOLREF FIELD                                            */
02990000/* ================================================================= */
03000000      IF &VOLREF2 ^= &Z THEN +
03010000        DO
03020000/* ================================================================= */
03030000/* SET UP THE VOLREF NAME FULLY QUALIFIED WITHOUT QUOTES             */
03040000/* ================================================================= */
03050000          ISPEXEC VPUT (VOLREF2) SHARED
03060000          ISPEXEC SELECT PGM(RSPFPARS) PARM(/VOLREF2)
03070000          SET LASTRC = &LASTCC
03080000          IF &LASTRC > 0 THEN +
03090000            DO
03100000               SET SHORT = ERROR IN PARSING DSNAME
03110000               SET LONG = &STR(ERROR PARSING DSNAME &VOLREF2 - THE +
03120000                   RETURN CODE WAS &LASTRC)
03130000               ISPEXEC DISPLAY MSG(RBDT010)
03140000               SET LASTRC = &LASTCC
03150000               GOTO GETDSNTO
03160000            END
03170000          ISPEXEC VGET (VOLREF2) SHARED
03180013          SET VOLREF2 = '&VOLREF2'
03190000        END
03200000      IF &BDTTDISP = NEW THEN +
03210000        DO
03220003/**********************************************************************/
03230003/* IF THE FROM DATASET IS A PDS, WE HAVE TO GET THE DIRECTORY         */
03240011/* INFORMATION  FOR THE ALLOCATE SCREEN, WHEN POSSIBLE                */
03250003/**********************************************************************/
03260007          IF &CENTER = &BDTFNODE THEN +
03270007            IF &PDS1 = YES THEN +
03280007              DO
03290009                ISPEXEC SELECT CMD(%R$DSINFO '&BDTFDSN' OPT(I))
03300007                ISPEXEC VGET (ZALDIR)
03310007              END
03320003          ISPEXEC DISPLAY PANEL(RBDTNUDS)
03330000          IF &LASTCC > 0 THEN +
03340000            EXIT
03350000          IF &BDTDCBDS ^= &Z THEN +
03360000            DO
03370000/* ================================================================= */
03380000/* SET UP THE DCBDS  NAME FULLY QUALIFIED WITHOUT QUOTES  */
03390000/* ================================================================= */
03400000              SET DCBDS = &BDTDCBDS
03410000              ISPEXEC VPUT (DCBDS) SHARED
03420000              ISPEXEC SELECT PGM(RSPFPARS) PARM(/DCBDS)
03430000              SET LASTRC = &LASTCC
03440000              IF &LASTRC > 0 THEN +
03450000                DO
03460000                   SET SHORT = ERROR IN PARSING DSNAME
03470000                   SET LONG = &STR(ERROR PARSING DSNAME &DCBDS - THE +
03480000                       RETURN CODE WAS &LASTRC)
03490000                   ISPEXEC DISPLAY MSG(RBDT010)
03500000                   SET LASTRC = &LASTCC
03510000                   GOTO GETDSNTO
03520000                END
03530000              ISPEXEC VGET (DCBDS) SHARED
03540000            END
03550000          IF &DSORG = PO THEN +
03560000            SET PDS2 = YES
03570000          ELSE +
03580000            SET PDS2 = NO
03590000        END
03600000/* ================================================================= */
03610000/* SEE IF WE ARE ON THE RECEIVING CENTER                             */
03620000/* ================================================================= */
03630000      ELSE +
03640000        IF &CENTER = &BDTTNODE THEN +
03650000          DO               /* CENTER IS RECEIVER                     */
03660000            IF &CTR2 = 0 THEN +
03670000              DO                        /*  MUST BE CATALOGED        */
03680019                IF &SYSDSN('&ZDSQ') ^= OK THEN +
03690000                  DO               /* DS IS NOT THERE                */
03700007                    SET SHORT = &STR(DATASET OR MBR MISSING)
03710007                    SET LONG = &STR(OLD DATASET &ZDSQMEM NOT +
03720000                      FOUND IN CATALOG)
03730000                    ISPEXEC SETMSG MSG(RBDT010)
03740000                    GOTO GETDSN2
03750000                  END
03760000                ELSE +
03770000                 IF &PDS2 = &Z THEN +
03780000                  DO                               /* MUST SET DSORG */
03790000/* ================================================================= */
03800000/* CHECK THE DSORG OF RECEIVING  DATASET                             */
03810000/* ================================================================= */
03820000/**********************************************************************/
03830000/* INVOKE R$DSINFO TO GET INFORMATION ABOUT THE DATASET               */
03840000/**********************************************************************/
03850009                    ISPEXEC SELECT CMD(%R$DSINFO '&BDTTDSN' OPT(S))
03860000                    ISPEXEC VGET (DSORG)
03870000                    IF &DSORG = PO THEN +
03880000                      SET PDS2 = YES
03890000                    ELSE +
03900000                      SET PDS2 = NO
03910000                  END                 /* DATA SET WAS THERE     */
03920000              END                     /* MUST BE CATALOGED       */
03930000          END                         /* CENTER IS RECEIVER      */
03940000      IF &BDTFNODE = &CENTER THEN +
03950000         DO
03960000           IF &PDS1 ^= &Z THEN +
03970000             IF &PDS2 = &Z THEN +
03980000              IF &STR(&MEM1) = ONE THEN +
03990000               SET PDS2 = NO
04000000              ELSE +
04010000               SET PDS2 = &PDS1      /* DSN1 DSORG IS KNOWN         */
04020000           IF &PDS1 = &Z THEN +
04030000             IF &PDS2 ^= &Z THEN +
04040000               IF &MEM2 = ONE THEN +
04050000                 SET PDS1 = NO
04060000               ELSE +
04070000                 SET PDS1 = &PDS2
04080000           IF &PDS1 = &Z && &PDS2 = &Z THEN +
04090000                 DO
04100000                   ISPEXEC DISPLAY PANEL(RBDTDORG)   /* GET DSORG     */
04110000                   IF  &DSORG ^= PO THEN +
04120000                     SET PDS1 = NO                 /* WAS NOT A PDS  */
04130000                   ELSE +
04140000                     DO
04150000                       SET PDS1 = YES
04160000                       SET MEM1 = DIR
04170000                     END
04180000                   SET PDS2 = &PDS1
04190000                 END
04200000         END
04210000       IF &BDTTNODE = &CENTER THEN +
04220000         DO
04230000           IF &PDS2 ^= &Z THEN +
04240000             IF &PDS1 = &Z THEN +
04250000              IF &MEM2 = ONE THEN +
04260000               SET PDS1 = NO
04270000              ELSE +
04280000                DO
04290000                  SET PDS1 = &PDS2   /* DSN2 DSORG IS KNOWN         */
04300000                  IF &PDS1 = YES THEN +
04310000                    SET MEM1 = DIR   /* MUST BE A DIRECTORY TYPE    */
04320000                END
04330000           IF &PDS2 = &Z THEN +
04340000             IF &PDS1 ^= &Z THEN +
04350000               IF &STR(&MEM1) = ONE THEN +
04360000                 SET PDS2 = NO
04370000               ELSE +
04380000                   SET PDS2 = &PDS1
04390000           IF &PDS1 = &Z && &PDS2 = &Z THEN +
04400000                 DO
04410000                   ISPEXEC DISPLAY PANEL(RBDTDORG)   /* GET DSORG     */
04420000                   IF  &DSORG ^= PO THEN +
04430000                     SET PDS2 = NO                 /* WAS NOT A PDS  */
04440000                   ELSE +
04450000                     DO
04460000                       SET PDS2 = YES
04470000                       SET MEM1 = DIR
04480000                     END
04490000                   SET PDS1 = &PDS2
04500000                 END
04510000         END
04520000      END
04530000    ELSE +
04540000      SET PDS2 = NO                     /* INTRDR WAS SPECIFIED      */
04550000/* ================================================================= */
04560000/* NOW CHECK THE COMBINATIONS FOR VALIDITY (?)                       */
04570000/* ================================================================= */
04580000   SET SHORT = &STR(BAD DATASET RELATIONSHIP)
04590000   IF &PDS1 = YES && &PDS2 = YES THEN +
04600000     DO
04610000       IF &MEM1 ^= ONE && &MEM2 = ONE THEN +
04620000         DO
04630000           SET LONG = &STR(FROM DS SPECIFIED MULTIPLE MEMBERS; TO DS +
04640000             SPECIFIED ONE MEMBER ONLY.)
04650000           ISPEXEC SETMSG MSG(RBDT010)
04660000           GOTO DOIT
04670000         END                 /* TO MEMBER SPECIFIED                  */
04680000       IF &MEM2 ^= ONE && &MEM1 = ONE THEN +
04690000         DO
04700000           SET LONG = &STR(FROM DS SPECIFIED SINGLE MEMBER; TO DS +
04710000             SPECIFIED MULTIPLE MEMBERS.)
04720000           ISPEXEC SETMSG MSG(RBDT010)
04730000           GOTO DOIT
04740000         END                 /* TO MEMBER SPECIFIED OR SEQUENTIAL    */
04750000     END
04760000   IF &PDS1 = YES && &PDS2 = NO && &INTRDR = &Z THEN +
04770000     IF &MEM1 ^= ONE THEN +
04780000      DO
04790000         SET LONG = +
04800000          &STR(FROM DS SPECIFIED AS PARTITIONED; TO DS IS SEQUENTIAL)
04810000         ISPEXEC SETMSG MSG(RBDT010)
04820000         GOTO DOIT
04830000      END
04840000   IF &PDS2 = YES && &PDS1 = NO THEN +
04850000     IF &MEM2 ^= ONE THEN +
04860000      DO
04870000         SET LONG = +
04880000          &STR(TO DS SPECIFIED AS PARTITIONED; FROM DS IS SEQUENTIAL)
04890000         ISPEXEC SETMSG MSG(RBDT010)
04900000         GOTO DOIT
04910000      END
04920000/* ================================================================= */
04930000/* CHECK IF A DTC NETWORK WAS INDICATED                              */
04940000/* ================================================================= */
04950000   IF &BDTDTC = Y THEN +
04960000     DO
04970000       ISPEXEC DISPLAY PANEL(RBDTDTC)
04980000       IF &LASTCC > 0 THEN +
04990000         GOTO DOIT
05000000     END
05010000  ELSE +
05020000     DO
05030000       SET BDTNID = &Z
05040000       SET BDTNREL = &Z
05050000       SET BDTNCOND = &Z
05060000       SET BDTNHOLD = &Z
05070000     END
05080000/* ================================================================= */
05090000/* CHECK FOR A PDS AND DO NECESSARY MEMBER PROCESSING                */
05100000/* ================================================================= */
05110000  IF &MEM1 = DIR THEN +
05120000     DO                                  /* ASK FOR MEMBER NAMES     */
05130000/*================================================================== */
05140000/*   BUILD A SELECTION TABLE OF ALL MEMBER NAMES IN THE PDS        */
05150000/*================================================================== */
05160000       ISPEXEC VPUT (PDS1 MEM1 BDTFDSN)
05170000       IF &DSCAT = YES THEN +
05180000         ISPEXEC SELECT CMD(%RBDTGETD)
05190000       ELSE +
05200000         ISPEXEC SELECT CMD(%RBDTGETM)
05210000       IF &LASTCC = 8 THEN +
05220000         GOTO DOIT
05230000       SET &DIRNAME =
05240000       ISPEXEC TBTOP MEMTABLE /* PUT FIRST ONE IN FIRSTMEM */
05250000       ISPEXEC TBSKIP MEMTABLE
05260000       IF &LASTCC = 8 THEN +
05270000         DO                       /* NO MEMBERS SELECTED AT ALL */
05280000           SET SHORT = &STR(NO MEMBERS SELECTED)
05290000           SET LONG = +
05300000             &STR(DIRECTORY REQUESTED, BUT NO MEMBERS WERE +
05310000               SELECTED)
05320000           ISPEXEC SETMSG MSG(RBDT010)
05330000           ISPEXEC TBEND MEMTABLE
05340000           GOTO DOIT
05350000         END
05360000       SET &FIRSTMEM = &DIRNAME
05370000       SET &FIRSTNEW = &NEWNAME
05380000       ISPEXEC TBDELETE MEMTABLE
05390000       ISPEXEC TBSKIP MEMTABLE
05400000       IF &LASTCC = 8 THEN +
05410000         SET MEMS = ONE
05420000       ELSE +
05430000         SET MEMS = MORE
05440000     END             /* PO AND MEMBER LIST                      */
05450000/* ================================================================= */
05460000/* SET UP DAP WITH CORRECT VALUE BASED UPON DSORG DETERMINED ABOVE   */
05470000/* ================================================================= */
05480000  IF &PDS1 ^= &PDS2 THEN +
05490000    SET DAP = SEQ
05500000  ELSE +
05510000   IF &PDS1 = YES THEN +
05520000     SET DAP = PDS
05530000   ELSE +
05540000     SET DAP = SEQ
05550000/* ================================================================= */
05560000/* SET THE NECESSARY VARIABLES                                       */
05570000/* ================================================================= */
05580000   SET DOPT = INIT
05590000   ISPEXEC VPUT (BDTJNAME,BDTFDSN,BDTFDISP,DAP,BDTFNODE,DOPT,+
05600000     VOL1,VOLREF1,BDTMSG,FIRSTMEM,BDTTREP,MEMS,BDTTDSN,INTRDR,+
05610000     BDTTNODE,BDTDCBDS,VOL2,VOLREF2,BDTTDISP,TOUNIT,RLSE,+
05620000     PDS1,PDS2,MEM1,MEM2,MBR1,MBR2,DSN1,FIRSTNEW)
05630000   IF &BDTTDISP = NEW THEN +
05640000     DO
05650000       ISPEXEC VPUT (BDTTDSP2,RECFM,LRECL,BLKSIZE,PRI,UNIT)
05660000       ISPEXEC VPUT (SCALO,ALLOC,DIRALLOC)
05670000     END
05680000   IF &OPT = T THEN +
05690000     SET WHAT = TRANSACTION
05700000   ELSE +
05710000     SET WHAT = GMJD MEMBER
05720000   ISPEXEC VPUT (WHAT)
05730000   ISPEXEC SELECT PANEL(RBDTDOIT)
05740000/* ================================================================= */
05750000/* IF WE HAD A TABLE OF MEMBERS THEN CLOSE IT                        */
05760000/* ================================================================= */
05770000   IF &MEM1 = DIR THEN +
05780000      ISPEXEC TBEND MEMTABLE
05790000   GOTO DOIT
05800000 EXIT
