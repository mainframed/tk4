00010000PROC 0 DEBUG ALL TEST
00020000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00030000  /**                                                                **/
00040000  /** ROCKWELL TSO NEWS (RTN): SHOW ONLY NEW/UNREAD AT ISPF STARTUP  **/
00050000  /** BY MIKE THEYS    ROCKWELL INTERNATIONAL   ISC/SWCC   02/14/87  **/
00060000  /** VERSION 3.0      ALSO USES NEW PANEL(ROC1NEWS) OPTS 0 AND 2    **/
00070000  /**                                                                **/
00080000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00090000  IF &DEBUG = DEBUG THEN CONTROL PROMPT LIST CONLIST SYMLIST MSG
00100000  CONTROL NOMSG NOFLUSH ASIS NOPROMPT
00110000  ISPEXEC CONTROL ERRORS RETURN
00120000  /* * * * * * *  GET USERS TSONEWS STATUS FROM LAST TIME * * * * * * */
00130000  /* RTNLSTAT : R   N   V   X   W   E      (NO BLANKS IN REAL STRING) */
00140000  /* RTNLNEWS : J313J352J351Y414X007J371                              */
00150000  /* RTNLDATE : LAST TSO NEWS DISPLAY DATE (DATE LSTAT/LNEWS CREATED) */
00160000  /* RTNXDATE : DATE OF LAST EXECUTE (EVEN IF NO NEWS SHOWN)          */
00170000  /* STATUS:    R=READ  N=NOTREAD/UNREAD  P=PRINTED                   */
00180000  /* STATUS:    W=NEW   E=ERROR/MISSING   X=IGNORED/EXCLUDED          */
00190000  /* STATUS:    V=REVISED/UPDATED    B=BROWSE/READ(TODAY)             */
00200000  /* STATUS:    H=HARDCOPY/PRINT(TODAY) I=IGNORED(TODAY)              */
00210000  /* USE THIS INFO TO BUILD RTNCSTAT/RTNCNEWS  (CURRENT)              */
00220000  /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * **/
00230000  /* GET THE "RTN" VARIABLES ON THE "LAST" USE OF TSO NEWS */
00240000  TRYAGAIN:+
00250000  SET TODAY = &STR(&SYSSDATE)
00260000  SET RTN1D = &STR(87/04/06) /* FIRST TIME USERS SHOW NEWS SINCE DATE */
00270000  ISPEXEC VGET (RTNLSTAT,RTNLNEWS,RTNLDATE,RTNXDATE) PROFILE
00280000  /* SEE IF FIRST TIME USERS   */
00290000  IF &STR(&RTNXDATE) =  |  &STR(&RTNLDATE) =  THEN SET RTN1ST = YES
00300000  IF &STR(&RTNXDATE) =   THEN SET RTNXDATE = &STR(&RTNLDATE)
00310000  IF &STR(&SYSDSN(ISPF.PROFILE(RTNPROF))) ^= OK THEN DO
00320000    /* SET "FLAG" USED BY $ROCKON TO NOT LIST TSO NEWS AT LOGON */
00330000    ISPEXEC SELECT PGM(IEFBR14) NEWAPPL(RTN)
00340000    ISPEXEC TBSAVE RTNPROF LIBRARY(ISPPROF)
00350000  END
00360000  SET RTNCSTAT =  /* EMPTY STRING TO BUILD STATUS OF CURRENT ITEMS */
00370000  SET RTNCNEWS =  /* EMPTY STRING TO BUILD NAMES  OF CURRENT ITEMS */
00380000  SET RTNALL   = &ALL
00390000 
00400000  /* CHECK STATUS OF NEWS DATASET AND ALLOCATE */
00410000  SHOWTSO CENTER
00420000  IF &CENTER = CGAD +
00430000    THEN SET RTN#DSN = &STR(ATSO.ANEWS.THENEWS)
00440000    ELSE SET RTN#DSN = &STR(ANEWS.THENEWS)
00450000  IF &TEST = TEST THEN SET RTN#DSN = &STR(&SYSUID..ANEWS.THENEWS)
00460000  SET MAXCC = 0
00470000  SET ERR# = 1
00480000  SET DSNSTAT = &SYSDSN('&RTN#DSN(CURRENT)')
00490000  IF &STR(&DSNSTAT) ^= OK THEN GOTO ERR
00500000  ALLOC FI(RTNDD) REUSE DA('&RTN#DSN(CURRENT)') SHR
00510000  OPENFILE RTNDD INPUT
00520000  IF &MAXCC > 0 THEN GOTO ERR
00530000 
00540000  /* SKIP OVER HEADER INFO IN "CURRENT" MEMBER */
00550000  SET &RTNSKIP  = 6
00560000  DO WHILE(&RTNSKIP > 0)
00570000    GETFILE RTNDD
00580000    SET &RTNSKIP = &RTNSKIP - 1
00590000  END
00600000 
00610000  /* BUILD ISPF TABLE SHOWING: "NOT READ","REVISED" OR "NEW" NEWS */
00620000  ISPEXEC TBCREATE RTNTABLE KEYS(RTNITEM) NOWRITE +
00630000     NAMES(RTNDATE,RTNTITLE,RTNTAG) REPLACE
00640000  SET TBRC = &LASTCC
00650000  SET ERR# = 2
00660000  IF &TBRC   > 4 THEN GOTO ERR
00670000  ISPEXEC TBSORT   RTNTABLE FIELDS(RTNDATE,C,D,RTNITEM,C,D)
00680000 
00690000  SET RCODE = 0
00700000  ERROR DO
00710000    SET RCODE = &LASTCC.
00720000    IF &RCODE = 400 /* EOF */ THEN GOTO EOF
00730000    SET ERR# = 3
00740000    GOTO ERR
00750000  END
00760000 
00770000  SET #NEWS = 0                  /* COUNT ITEMS ADDED TO NEWS TABLE */
00780000  SET #LNEWS = &LENGTH(&STR(&RTNLNEWS))
00790000  SET #LSTAT = &LENGTH(&STR(&RTNLSTAT))
00800000  SET ERR# = 4
00810000  IF (&#LNEWS ^= 4 * &#LSTAT) THEN GOTO ERR
00820000 
00830000  DO WHILE ( 1 = 1 )    /* DO UNTIL ERROR CODE = EOF */
00840000    GETFILE RTNDD
00850000    SET RTNITEM  = &SUBSTR(10:13,&STR(&RTNDD))  /* NEWS MEMBERNAME */
00860000    SET RTNDATE  = &SUBSTR(01:08,&STR(&RTNDD))  /* YY/MM/DD        */
00870000    IF &STR(&RTNDATE) > &STR(&TODAY) THEN GOTO SKIPITEM
00880000    SET RTNTITLE = &SUBSTR(16:72,&STR(&RTNDD))  /* NEWS TITLE      */
00890000    SET RTNTAG   =
00900000    /* SEE IF THIS IS A PREVIOUSLY SEEN NEWS ITEM */
00910000    SET ITEMSTAT = W /* ASSUME "NEW" ITEM */
00920000    /* FIRST TIME USER ONLY SHOW TODAY NEWS. OLD NEWS MARK AS READ */
00930000    IF &RTN1ST=YES && &STR(&RTNDATE) < &STR(&RTN1D) THEN SET ITEMSTAT=R
00940000    IF &STR(&RTNLSTAT) ^=   THEN DO
00950000      /* SEE IF "RTNITEM" IS SAVED IN "LAST/PREVIOUS" NEWSITEMS LIST */
00960000      SET #LN = &#LNEWS
00970000      SET #LS = &#LSTAT
00980000      DO WHILE(&#LN > 0 AND &ITEMSTAT = W)
00990000        IF &RTNITEM = &SUBSTR(&#LN-3:&#LN,&STR(&RTNLNEWS)) THEN +
01000000          SET ITEMSTAT = &SUBSTR(&#LS:&#LS,&STR(&RTNLSTAT))
01010000        SET #LN = &#LN - 4
01020000        SET #LS = &#LS - 1
01030000      END /* OF SCAN PREVIOUS STATUS */
01040000    END /* OF PREVIOUS CHECK */
01050000    /* HANDLE REVISIONS AND NEW/REVISED LATER THAT DAY */
01060000    /* SEE IF A "REVISION"  (OTHERWISE ITS "NEW") */
01070000    IF &RTN1ST ^= YES THEN DO
01080000      IF (&ITEMSTAT = R | &ITEMSTAT = X | &ITEMSTAT = P) THEN +
01090000        IF (&STR(&RTNDATE) >= &STR(&RTNXDATE) THEN +
01100000          SET ITEMSTAT = V  /* "READ" ITEM HAS BEEN REVISED. SHOW AGAIN */
01110000      /* REVISIONS OF JUST LAST READ/PRINTED/IGNORED */
01120000      IF (&ITEMSTAT = B | &ITEMSTAT = H | &ITEMSTAT = I) THEN +
01130000        IF (&STR(&RTNDATE) >  &STR(&RTNXDATE) THEN +
01140000          SET ITEMSTAT = V  /* "READ" ITEM HAS BEEN REVISED. SHOW AGAIN */
01150000      /* WHAT IF REVISED LATER IN DAY AFTER NEWS ALREADY SEEN TODAY? */
01160000      /* CHANGE ITEMS READ/IGNORED/PRINTED LAST DAY (B/I/H) TO (R/X/P) */
01170000      IF &STR(&RTNXDATE) < &STR(&TODAY) THEN DO
01180000        IF &ITEMSTAT = B THEN SET ITEMSTAT = R
01190000        IF &ITEMSTAT = I THEN SET ITEMSTAT = X
01200000        IF &ITEMSTAT = H THEN SET ITEMSTAT = P
01210000      END /* OF CONVERT STATUS FROM "B" TO "R" */
01220000    END /* OF REVISIONS CHECKING */
01230000    /* NOW BUILD THE "CURRENT" NEWS ITEMS/STATUS STRING          */
01240000    SET RTNCSTAT = &STR(&RTNCSTAT.&ITEMSTAT)
01250000    SET RTNCNEWS = &STR(&RTNCNEWS.&RTNITEM)
01260000    /* SHOULD THIS NEWS ITEM BE SHOWN? */
01270000    IF &ITEMSTAT=W | &ITEMSTAT=N | &ITEMSTAT=V | &ALL=ALL THEN DO
01280000      IF      &ITEMSTAT = W THEN SET RTNTAG = &STR(NEW)
01290000      ELSE IF &ITEMSTAT = N THEN SET RTNTAG = &STR(UNREAD)
01300000      ELSE IF &ITEMSTAT = V THEN SET RTNTAG = &STR(REVISED)
01310000      ELSE IF &ITEMSTAT = R THEN SET RTNTAG = &STR(READ)
01320000      ELSE IF &ITEMSTAT = B THEN SET RTNTAG = &STR(READ)
01330000      ELSE IF &ITEMSTAT = P THEN SET RTNTAG = &STR(PRINTED)
01340000      ELSE IF &ITEMSTAT = H THEN SET RTNTAG = &STR(PRINTED)
01350000      ELSE IF &ITEMSTAT = X THEN SET RTNTAG = &STR(IGNORED)
01360000      ELSE IF &ITEMSTAT = I THEN SET RTNTAG = &STR(IGNORED)
01370000      ELSE IF &ITEMSTAT = E THEN SET RTNTAG = &STR(ERROR)
01380000      ELSE                       SET RTNTAG = &STR(UNKNOWN)
01390000      ISPEXEC TBADD RTNTABLE ORDER
01400000      SET #NEWS = &#NEWS + 1
01410000    END
01420000    SKIPITEM:+
01430000  END /* OF READ DATA FROM "CURRENT" AND END OF TABLE BUILD */
01440000 
01450000  EOF:+
01460000  ERROR OFF
01470000  CLOSFILE RTNDD
01480000  FREE FI(RTNDD) DA('&RTN#DSN')
01490000 
01500000 
01510000  SET #SEEN = 0                  /* COUNT ITEMS READ/PRINTED/IGNORED*/
01520000  IF (&ALL =  ) THEN DO
01530000    ISPEXEC VGET (RTNEND)  PROFILE  /* AUTOEND? YES/NO */
01540000    ISPEXEC VGET (RTNASEL) PROFILE  /* AUTO ENTRY OF R/X/P EACH ITEM */
01550000  END
01560000  SET ASEL =
01570000  /* NOW SHOW THE "NEW"/"UNREAD"/"REVISED" ROCKWELL TSO NEWS ITEMS  */
01580000  IF &#NEWS > 0 THEN DO
01590000 
01600000    SET RTNSEL =
01610000    SET RTNUPD =
01620000    SET #CNEWS = &LENGTH(&STR(&RTNCNEWS))
01630000    SET #CSTAT = &LENGTH(&STR(&RTNCSTAT))
01640000    SET PANEL = &STR( PANEL(RMBFRTN) )
01650000 
01660000    ISPEXEC VGET (RTNDEST) PROFILE
01670000    IF &STR(&RTNDEST) = THEN DO
01680000      SHOWTSO DEST
01690000      SET RTNDEST = &DEST
01700000      ISPEXEC VPUT (RTNDEST) PROFILE
01710000    END
01720000 
01730000    ISPEXEC TBTOP RTNTABLE
01740000 
01750000    IF &RTNASEL ^= THEN DO
01760000      SET SHORT = &STR("&RTNASEL" ALL NEWS)
01770000      SET LONG  = &STR(PRESS ENTER FOR INVOKING OPTION "&RTNASEL" FOR EACH NEWS ITEM.)
01780000      ISPEXEC SETMSG MSG(SYS010)
01790000    END
01800000 
01810000    SHOWTBL:+
01820000    IF &RTNZCMD ^=  THEN ISPEXEC TBTOP RTNTABLE
01830000    SET RTNZCMD =
01840000    ISPEXEC TBDISPL RTNTABLE &PANEL
01850000    SET TBRC = &LASTCC
01860000    IF &TBRC >= 8 THEN GOTO TBLEND
01870000 
01880000    ISPEXEC VGET (RTNZCMD)
01890000 
01900000    IF (&RTNZCMD = DEFAULT | &RTNZCMD = DEFAULTS) THEN DO
01910000      SET RTNZCMD =
01920000      ISPEXEC SELECT PANEL(RMBFRTND)
01930000      IF &ALL = THEN ISPEXEC VGET (RTNASEL,RTNEND) PROFILE
01940000      ISPEXEC TBTOP RTNTABLE
01950000      IF &RTNASEL ^= THEN DO
01960000        SET SHORT = &STR("&RTNASEL" ALL NEWS)
01970000        SET LONG  = &STR(PRESS ENTER FOR INVOKING OPTION "&RTNASEL" FOR EACH NEWS ITEM.)
01980000        ISPEXEC SETMSG MSG(SYS010)
01990000      END
02000000    END
02010000 
02020000    IF &RTNSEL ^= THEN SET RTNASEL =
02030000    ASELDO:+
02040000    IF (&RTNASEL ^=  AND  &RTNZCMD =  AND &ASEL ^= DONE) THEN DO
02050000      /* GET ITEM FROM TABLE */
02060000      ISPEXEC TBSKIP RTNTABLE
02070000      IF &LASTCC ^= 0 THEN SET ASEL = DONE
02080000      SET RTNSEL = &RTNASEL
02090000      SET ASELCMD = &RTNASEL
02100000      IF &ASEL = DONE THEN GOTO ASELDONE
02110000    END
02120000 
02130000    /* FOR "REPORT" OR "P"-PRINT */
02140000    IF &RTNSEL = P | &RTNZCMD = REPORT | &RTNZCMD = INDEX THEN DO
02150000      ISPEXEC VGET (RTNDEST,RTNCLASS,RTNCAPS,RTNPLN) PROFILE
02160000      IF &RTNDEST  ^=    THEN SET DEST  = &STR( DEST(&RTNDEST) )
02170000      ELSE                    SET DEST  =
02180000      IF &RTNCLASS ^=    THEN SET CLASS = &STR( CLASS(&RTNCLASS) )
02190000      ELSE                    SET CLASS =
02200000      IF &RTNPLN   ^=    THEN SET PLN   = &STR( PAGELENGTH(&RTNPLN) )
02210000      ELSE                    SET PLN   =
02220000      IF &RTNPLN    =  0 THEN SET PLN   = &STR( NOPL )
02230000    END
02240000 
02250000    IF &RTNZCMD = REPORT | &RTNZCMD = INDEX THEN DO
02260000      ISPEXEC TBTOP RTNTABLE
02270000      ISPEXEC FTOPEN TEMP
02280000      ISPEXEC FTINCL RMBFRTN
02290000      SET FTRC = &LASTCC
02300000      ISPEXEC FTCLOSE
02310000      ISPEXEC VGET (ZTEMPF)
02320000      PRINTOFF '&ZTEMPF' +
02330000               NOMSG NOHEADING RCL +
02340000               &DEST &CLASS &RTNCAPS &PLN
02350000      SET &LINES = &LASTCC
02360000      SET SHORT = &STR(INDEX PRINTED)
02370000      SET LONG  = &STR(CURRENT &CENTER TSO NEWS SUMMARY WAS PRINTED WITH &LINES LINES TO &DEST)
02380000      ISPEXEC SETMSG MSG(SYS010)
02390000    END
02400000 
02410000    IF &RTNZCMD = PRINTER THEN ISPEXEC SELECT PANEL(RMBFRTNP)
02420000 
02430000    IF &RTNSEL = S OR &RTNSEL = R THEN DO /* "READ"/BROWSE NEWS ITEM */
02440000       IF &STR(&RTNTAG) ^= THEN IF &SUBSTR(1:1,&STR(&RTNTAG)) ^= &STR(*) +
02450000         THEN SET #SEEN = &#SEEN + 1
02460000       IF &SYSDSN('&RTN#DSN(&RTNITEM)') = OK THEN DO
02470000         ISPEXEC CONTROL DISPLAY SAVE
02480000         ISPEXEC BROWSE DATASET('&RTN#DSN(&RTNITEM)') PANEL(RMBFRTNB)
02490000         ISPEXEC CONTROL DISPLAY RESTORE
02500000         SET RTNUPD = B
02510000         SET RTNTAG= &STR(*READ)
02520000       END
02530000       ELSE DO /* MISSING OR ERROR */
02540000         SET RTNTAG= &STR(*ERROR)
02550000         SET RTNUPD = E
02560000       END
02570000    END
02580000 
02590000    IF &RTNSEL = X THEN DO /* "IGNORE"/EXCLUDE FROM FUTURE DISPLAY */
02600000      SET RTNUPD = I
02610000      IF &SUBSTR(1:1,&STR(&RTNTAG)) ^= &STR(*) THEN SET #SEEN=&#SEEN + 1
02620000      SET RTNTAG= &STR(*SKIP)
02630000    END
02640000 
02650000    IF &RTNSEL = P THEN DO
02660000      IF &SUBSTR(1:1,&STR(&RTNTAG)) ^= &STR(*) THEN SET #SEEN=&#SEEN + 1
02670000      IF &SYSDSN('&RTN#DSN(&RTNITEM)') = OK THEN DO /* "PRINT" ITEM */
02680000        PRINTOFF '&RTN#DSN(&RTNITEM)' +
02690000                 NOMSG NOHEADING RCL  +
02700000                 &DEST &CLASS &RTNCAPS &PLN
02710000        SET RTNTAG= &STR(*PRINT)
02720000        SET RTNUPD = H
02730000      END
02740000       ELSE DO /* MISSING OR ERROR */
02750000         SET RTNTAG= &STR(*ERROR)
02760000         SET RTNUPD = E
02770000       END
02780000    END
02790000 
02800000    /* NOW UPDATE THE STATUS STRING FOR IGNORE/READ/ERRORS */
02810000    IF &RTNUPD ^= THEN DO
02820000      SET #CN = &#CNEWS
02830000      SET #CS = &#CSTAT
02840000      SET ERR# = 5
02850000      IF (&#CN ^= 4 * &#CS) THEN GOTO ERR
02860000      SET S# = 0
02870000      SET ERR# = 6
02880000      /* FIND POSITION OF ITEM IN NEWS/STATUS STRING */
02890000      DO WHILE(&#CN > 0 AND &S# = 0)
02900000        IF &RTNITEM = &SUBSTR(&#CN-3:&#CN,&STR(&RTNCNEWS)) THEN SET S# = &#CS
02910000        SET #CN = &#CN - 4
02920000        SET #CS = &#CS - 1
02930000      END /* OF SCAN FOR NEWS ITEM TO UPDATE */
02940000      IF &S# = 0 THEN GOTO ERR
02950000      IF &S# >  1 AND &S# <= &#CSTAT +
02960000        THEN SET &RTNB4 = &SUBSTR(1:&S#-1,&STR(&RTNCSTAT))
02970000        ELSE SET &RTNB4 =
02980000      IF &S# >= 1 AND &S# <  &#CSTAT +
02990000        THEN SET &RTNAF = &SUBSTR(&S#+1:&#CSTAT,&STR(&RTNCSTAT))
03000000        ELSE SET &RTNAF =
03010000      SET OLDSTAT  = &SUBSTR(&S#:&S#,&STR(&RTNCSTAT))
03020000      /* IF JUST "REVISED" AND "BROWSED" AGAIN MARK AS "REREAD"/"B" */
03030000      /* IF PREVIOUSLY "READ" STILL CONSIDER READ IF JUST PRINTED   */
03040000      IF &RTNUPD = H  AND (&OLDSTAT = R | &OLDSTAT = B) THEN SET RTNUPD = &OLDSTAT
03050000      SET RTNCSTAT = &STR(&RTNB4.&RTNUPD.&RTNAF)
03060000      SET RTNUPD =
03070000    END
03080000    ISPEXEC TBMOD RTNTABLE ORDER
03090000 
03100000    ASELDONE:+
03110000    IF &TBRC  = 0 THEN SET PANEL = &STR( PANEL(RMBFRTN) )
03120000    IF &TBRC  = 4 THEN SET PANEL =
03130000    IF &RTNZCMD =   AND &RTNSEL =   AND &RTNASEL = THEN ISPEXEC TBTOP RTNTABLE
03140000    SET RTNSEL =
03150000    SET RTNTAG =
03160000    IF (&#SEEN  = &#NEWS) THEN DO
03170000      IF &RTNEND ^= NO    THEN DO
03180000        IF &RTNASEL ^= THEN SET ASEL = DONE
03190000        GOTO TBLEND
03200000      END
03210000      ELSE                     ISPEXEC TBTOP RTNTABLE
03220000      SET RTNASEL =
03230000    END
03240000    IF (&RTNASEL ^= AND &ASEL =     ) THEN GOTO ASELDO
03250000    IF (&TBRC   < 8) | (&ASEL = DONE)  THEN GOTO SHOWTBL
03260000 
03270000    TBLEND:+
03280000    ISPEXEC TBEND RTNTABLE
03290000 
03300000    /* CONVERT ANY "NEW" UNREAD ITEMS SHOWN TO "NOT READ" STATUS */
03310000    SET RTNLSTAT =
03320000    SET RTNLNEWS = &STR(&RTNCNEWS)
03330000    SET #CSTAT = &LENGTH(&STR(&RTNCSTAT))
03340000    SET #CS    = &#CSTAT
03350000    DO WHILE(&#CS > 0)
03360000      SET ITEMSTAT = &SUBSTR(&#CS:&#CS,&STR(&RTNCSTAT))
03370000      /* "NEW/UPDATED"  ITEMS  CONVERT TO "NOT READ" STATUS */
03380000      IF &ITEMSTAT = W OR &ITEMSTAT = V THEN SET ITEMSTAT = N
03390000      SET RTNLSTAT = &STR(&ITEMSTAT&RTNLSTAT)
03400000      SET #CS = &#CS - 1
03410000    END /* OF SCAN FINAL STATUS FOR CENTER "CTR" */
03420000 
03430000    SET RTNLDATE     = &STR(&TODAY)
03440000    ISPEXEC VPUT (RTNLDATE) PROFILE
03450000 
03460000  END /* OF ACTUAL TABLE DISPLAY */
03470000 
03480000  /* SAVE THE CURRENT "RTN" VARIABLES */
03490000  IF &#NEWS = 0 THEN DO
03500000    SET RTNLSTAT = &STR(&RTNCSTAT)
03510000    SET RTNLNEWS = &STR(&RTNCNEWS)
03520000  END
03530000  SET RTNXDATE = &STR(&TODAY)
03540000  /* RTNLSTAT IS UPDATED EVEN IF NO NEWS SEEN. */
03550000  ISPEXEC VPUT (RTNLSTAT,RTNLNEWS,RTNLDATE,RTNXDATE) PROFILE
03560000  /* NOW FORCE THE SAVING OF THE CURRENT APPLID PROFILE VARIABLES */
03570000  ISPEXEC VGET (ZAPPLID)
03580000  ISPEXEC TBSAVE &ZAPPLID.PROF LIBRARY(ISPPROF)
03590000  IF (&ASEL = DONE AND &ASELCMD ^= AND &RTNEND ^= NO) THEN DO
03600000    SET LONG = &STR(ALL NEWS ITEMS PROCESSED WITH AUTO COMMAND ENTRY OF "&ASELCMD")
03610000    IF      &ASELCMD = R THEN SET SHORT = &STR(ALL NEWS READ)
03620000    ELSE IF &ASELCMD = X THEN SET SHORT = &STR(ALL NEWS IGNORED)
03630000    ELSE IF &ASELCMD = P THEN SET SHORT = &STR(ALL NEWS PRINTED)
03640000    ISPEXEC SETMSG MSG(SYS010)
03650000  END
03660000  EXIT /* EXIT HERE IF ALL WAS OK */
03670000 
03680000  ERR:+
03690000  ERROR OFF
03700000  /* DISPLAY THE  ERROR  MESSAGES */
03710000  SET SHORT = &STR(TSONEWS ERROR)
03720000  IF      &ERR# =  1 THEN SET LONG = &STR(ERROR WITH NEWS DATASET: '&RTN#DSN(CURRENT)'=&DSNSTAT)
03730000  ELSE IF &ERR# =  2 THEN SET LONG = &STR(ERROR WHEN CREATING NEWS DISPLAY TABLE. RC=&TBRC)
03740000  ELSE IF &ERR# =  3 THEN SET LONG = &STR(UNEXPECTED ERROR IN ROCKWELL TSO NEWS DISPLAY. ERROR RC=&RCODE)
03750000  ELSE IF &ERR# =  4 THEN SET LONG = &STR(PREVIOUS NEWS STATUS STRING/NEWS ITEMS MISMATCHED)
03760000  ELSE IF &ERR# =  5 THEN SET LONG = &STR(CURRENT  NEWS STATUS STRING/NEWS ITEMS MISMATCHED)
03770000  ELSE IF &ERR# =  6 THEN SET LONG = &STR(ERROR UPDATING NEWS STATUS ON ITEM &RTNITEM)
03780000  IF &ERR# >= 4 AND &ERR# <= 6 THEN DO /* RESET BAD "RTN" VARIABLES */
03790000    SET RTNLSTAT =
03800000    SET RTNLNEWS =
03810000    SET RTNLDATE =
03820000    SET RTNXDATE =
03830000    ISPEXEC VPUT (RTNLSTAT,RTNLNEWS,RTNLDATE,RTNXDATE) PROFILE
03840000    IF &AGAIN =  THEN DO
03850000      SET AGAIN = NO
03860000      SET SHORT =
03870000      SET LONG  =
03880000      IF &ERR# = 4 THEN CLOSFILE RTNDD
03890000      GOTO TRYAGAIN
03900000    END
03910000  END
03920000  ISPEXEC SETMSG MSG(SYS010)
03930000  EXIT /* EXIT HERE IF THERE WAS AN ERROR */
