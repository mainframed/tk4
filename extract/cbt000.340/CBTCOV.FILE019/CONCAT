00010000 PROC 1 DSN ADD DELETE PRE AUX FILE(SYSPROC) +
00020000        SETMSG NMSG NOMSG DEBUG
00030000 /*******************************************************/
00040000 /*  CONCAT:                                            */
00050000 /*  BY:      ANDY ANDERSON   ROCKWELL/WCC1             */
00060000 /*  REVISED: MIKE THEYS      ROCKWELL/SWCC   08/24/87  */
00070000 /*  CLIST CONCATENATES/DECONCATENTATES DATASETS AND    */
00080000 /*  ALLOCATES THEM TO THE FILE SPECIFIED.              */
00090000 /*  USER MUST SUPPLY DATASET NAME AND ACTION (ADD/DEL) */
00100001 /*  SWCC/SWC2: NOMSG    WCC1/WCC2: NMSG                */
00110000 /*  REQUIRES TSO/E R3 (SYSINDEX/LISTDSI/ALLOC BLKSIZE) */
00120000 /*******************************************************/
00130000 IF &DEBUG = DEBUG THEN CONTROL   LIST   MSG PROMPT CONLIST SYMLIST
00140000 IF &SUBSTR(1:1,&STR(&DSN)) ^= &STR(') THEN DO
00150000   SET &RU = NO
00160000   SET &DSNCNT = 0
00170000   IF &SYSPREF =  THEN SET DSN = &STR(&SYSUID..&DSN)
00180000   ELSE                SET DSN = &STR(&SYSPREF..&DSN)
00190000   SET DSN = &STR('&DSN')
00200000 END
00210000 
00220000 IF &SYSDSN(&STR(&DSN)) ^= OK THEN DO
00230000   SET ERRMSG  = &STR(DATASET &DSN IS NOT CATALOGED)
00240000   SET ERRCODE = 12
00250000   GOTO ERROR
00260000 END
00270000 
00280002 LISTDSI &STR(&DSN) NODIR
00290000 IF &LASTCC < 5 THEN SET &ALCSIZE = &SYSBLKSIZE
00300000 ELSE                SET &ALCSIZE = 3120
00310000 
00320000 WHATIF FILE(&FILE) ALLOCATED
00330000 IF &LASTCC > 0 THEN DO
00340000   SET ERRMSG  = &STR(FILE &FILE IS NOT CURRENTLY ALLOCATED)
00350000   SET ERRCODE = 12
00360000   GOTO ERROR
00370000 END
00380000 
00390001 IF &ADD = ADD  AND  &DELETE = DELETE THEN DO
00400000   SET ERRMSG =  &STR(CONFLICTING ADD AND DELETE ATTRIBUTES)
00410000   SET ERRCODE = 4
00420000   GOTO ERROR
00430000 END
00440000 IF &ADD =      AND  &DELETE =          THEN DO
00450000   SET ERRMSG = &STR(NO ADD OR DELETE ATTRIBUTE)
00460000   SET ERRCODE = 4
00470000   GOTO ERROR
00480000 END
00490000 
00500000 SET &SYSOUTTRAP = 999
00510000   KDSN   &FILE
00520000 SET &TRAPKNT = &SYSOUTLINE
00530000 SET &SYSOUTTRAP = 0
00540000 IF &DEBUG = DEBUG THEN SET CNT = 3
00550000 ELSE                   SET CNT = 2
00560000 DO WHILE (&CNT <= &TRAPKNT)
00570000   SET &DSNCNT = &DSNCNT + 1
00580000   SET &WDSN = &&SYSOUTLINE&CNT
00590000   SET &WDSN = &WDSN
00600000   IF &WDSN = THEN GOTO NOKP
00610000   IF &RU ^= YES THEN DO
00620000     SET &WKDSN = &STR('&WDSN')
00630002     LISTDSI &STR(&WKDSN) NODIR
00640000     IF &LASTCC = 0 THEN DO
00650000       IF &DSNCNT = 1 THEN +
00660000         IF &SYSRECFM = U THEN SET &RU = YES
00670000         ELSE                  SET &DSNCNT = &DSNCNT + 1
00680000       IF &ALCSIZE < &SYSBLKSIZE THEN SET &ALCSIZE = &SYSBLKSIZE
00690000     END
00700000   END
00710000   IF &WKLINE =  THEN SET WKLINE  = &STR('&WDSN')
00720000   ELSE               SET &WKLINE = &STR(&WKLINE '&WDSN')
00730000NOKP: +
00740000   SET &CNT = &CNT + 1
00750000 END
00760000 
00770000 IF &RU = YES THEN SET &VBLKSZ =
00780000 ELSE              SET &VBLKSZ = &STR( BLKSIZE(&ALCSIZE))
00790000 
00800000 SET &DLEN = &LENGTH(&DSN)
00810000 SET &WLEN = &LENGTH(&WKLINE)
00820000 SET &PRECAT =
00830000 
00840000 SET &FRC = &SYSINDEX('ATSO.FIXED.LARGE',&STR(&WKLINE),1)
00850000 
00860000 IF &FRC ^= 0 THEN DO
00870000   SET &PRECAT = &STR('ATSO.FIXED.LARGE')
00880000   SET &FLEN   = &LENGTH('ATSO.FIXED.LARGE')
00890000   SET &WKLINE = &SUBSTR(&FRC + &FLEN + 1:&WLEN,&STR(&WKLINE))
00900000   SET &WLEN = &LENGTH(&WKLINE)
00910000 END
00920000 ELSE DO
00930000   SET &FRC = &SYSINDEX('ATSO.VARIABLE.LARGE',&STR(&WKLINE),1)
00940000   IF &FRC ^= 0 THEN DO
00950000     SET &PRECAT = &STR('ATSO.VARIABLE.LARGE')
00960000     SET &FLEN   = &LENGTH('ATSO.VARIABLE.LARGE')
00970000     SET &WKLINE = &SUBSTR((&FRC + &FLEN + 1):&WLEN,&STR(&WKLINE))
00980000     SET &WLEN = &LENGTH(&WKLINE)
00990000   END
01000000 END
01010000 IF &ADD = ADD THEN DO
01020000   IF &AUX =  THEN DO
01030000     ALLOC F(&FILE) DS(&PRECAT &DSN &STR(&WKLINE)) SHR REU &VBLKSZ
01040000     IF &LASTCC = 0 THEN DO
01050000       SET ERRMSG  = &STR(&DSN HAS BEEN PRE-CONCATENATED TO &FILE)
01060000       SET ERRCODE = 0
01070000     END
01080000     ELSE DO
01090000       SET ERRMSG  = &STR(NEW &FILE CONCATENATION FAILED)
01100000       SET ERRCODE = 16
01110000     END
01120000   END
01130000   ELSE DO
01140000     ALLOC F(&FILE) DS(&PRECAT &STR(&WKLINE) &DSN) SHR REU &VBLKSZ
01150000     IF &LASTCC = 0 THEN DO
01160000       SET ERRMSG  = &STR(&DSN HAS BEEN AUX-CONCATENATED TO &FILE)
01170000       SET ERRCODE = 0
01180000     END
01190000     ELSE DO
01200000       SET ERRMSG  = &STR(NEW &FILE CONCATENATION FAILED)
01210000       SET ERRCODE = 16
01220000     END
01230000   END
01240000 END
01250000 ELSE DO        /* DELETE */
01260000   IF &WLEN ^> &DLEN THEN DO
01270000     SET ERRMSG =  &STR(ERROR: DELETE OF &DSN +
01280000                        WILL LEAVE NO FILES ALLOCATED - USE FREE)
01290000     SET ERRCODE = 8
01300000     GOTO ERROR
01310000   END
01320000   SET &SRC = &SYSINDEX(&DSN,&STR(&WKLINE),1)
01330000   IF &SRC ^= 0 THEN DO
01340000     IF &SRC < &DLEN THEN   /* DATASET IS FIRST ONE */ +
01350000       SET NULINE = &STR(&SUBSTR(&SRC+&DLEN:&WLEN,&STR(&WKLINE  ))
01360000     ELSE +
01370000       IF &WLEN-&DLEN < &SRC THEN   /* DATASET IS LAST */ +
01380000         SET NULINE = &STR(&SUBSTR(1:&SRC-1,&STR(&WKLINE)))
01390000       ELSE DO    /* DATASET IS IN THE MIDDLE */
01400000         SET NULINE = &STR(&SUBSTR(1:&SRC-1,&STR(&WKLINE)))
01410000         SET NULINE = &STR(&NULINE+
01420000                    &SUBSTR(&SRC+&DLEN+1:&WLEN,&STR(&WKLINE)))
01430000       END
01440000     ALLOC F(&FILE) DS(&PRECAT &STR(&NULINE)) SHR REU &VBLKSZ
01450000     IF &LASTCC = 0 THEN DO
01460000       SET ERRMSG  = &STR(&DSN HAS BEEN DE-CONCATENATED FROM &FILE)
01470000       SET ERRCODE = 0
01480000     END
01490000     ELSE DO
01500000       SET ERRMSG  = &STR(NEW &FILE DE-CONCATENATION FAILED)
01510000       SET ERRCODE = 16
01520000     END
01530000   END
01540000   ELSE DO       /* DATASET NOT IN CONCATENATION */
01550000     SET ERRMSG  =  &STR(ERROR: &DSN NOT PRESENT TO DELETE)
01560000     SET ERRCODE = 20
01570000   END
01580000 END
01590000ERROR: +
01600000 IF &SYSISPF = ACTIVE THEN DO
01610000   SET ROCLMSG = &STR(&ERRMSG)
01620000   IF &ERRCODE = 0 THEN DO
01630000     SET ROCSMSG = &STR(CONCAT SUCCESSFUL)
01640000     ISPEXEC VPUT (ROCSMSG ROCLMSG)
01650000     IF &SETMSG = SETMSG THEN ISPEXEC SETMSG MSG(ROCZ000)
01660000   END
01670000   ELSE DO
01680000     SET ROCSMSG = &STR(CONCAT FAILED)
01690000     ISPEXEC VPUT (ROCSMSG ROCLMSG)
01700000     IF &SETMSG = SETMSG THEN ISPEXEC SETMSG MSG(ROCZ001)
01710000   END
01720000   EXIT CODE(&ERRCODE)
01730000 END
01740000 ELSE DO
01750000   IF &NOMSG = NOMSG THEN SET NMSG = NMSG
01760000   IF &NMSG =  THEN DO
01770000     WRITE &STR(&ERRMSG)
01780000     EXIT CODE(&ERRCODE)
01790000   END
01800000 END
