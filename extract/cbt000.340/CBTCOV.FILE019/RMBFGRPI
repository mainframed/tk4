00010005PROC 0 DEBUG INIT SCROLL UP DOWN LEFT RIGHT AMT()
00020005  /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
00030005  /* RMBF@GRP Scrollable/Modifiable GROUP Menu with 36 Options.    */
00040005  /* This clist sets the initial menu options on PANEL(RMBF@GRP)   */
00050005  /* It is invoked by either GROUP panel on first startup          */
00060005  /* And it is also invoke when SCROLL PF Keys are used.           */
00070005  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00080005  /**                                                             **/
00090005  /** Rockwell Menu BUILD Facility for Scrollable RMBF@GRP.       **/
00100005  /**                                                             **/
00110005  /** Michael E. Theys                                            **/
00120005  /** Southwestern Computing Center                               **/
00130005  /** Information Systems Center                                  **/
00140005  /** Rockwell International                                      **/
00150005  /** 1200 N. Alma Rd.  Bldg 401-109                              **/
00160005  /** Richardson, Texas 75081-2205                                **/
00170005  /** (214) 996-2860  COMNET 437-2860                             **/
00180005  /** XMIT Address    SWCC.XTS0008                    Jan 1987    **/
00190005  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00200005 
00210005CONTROL ASIS
00220005ISPEXEC CONTROL ERRORS RETURN
00230005IF &DEBUG = DEBUG +
00240005  THEN CONTROL   LIST   CONLIST   SYMLIST NOFLUSH   PROMPT   MSG
00250005  ELSE CONTROL NOLIST NOCONLIST NOSYMLIST   FLUSH NOPROMPT NOMSG
00260005 
00270005/* Validate the BUILD Variable Pool and Max Displayable Menu Options */
00280005/* PANEL(RMBF@GRP) has RMBFMAXD= Max Displayable options=18 */
00290005/* PANEL(RMBF@GRP) has RMBFMAXM= Max available   options=36 */
00300005/* PANEL(RMBF@GRP) has RMBFPOOL= Variable Pool   "GRP"      */
00310005%RMBFPOOL
00320005ISPEXEC VGET (RMBFPOOL,RMBFMAXM,RMBFMAXD)
00330005SET POOL = &RMBFPOOL
00340005 
00350005/**************** START of SCROLL SET UP ****************************/
00360005IF &SCROLL =  SCROLL THEN DO
00370005  /* Define the amount to scroll by, if needed. */
00380005  IF &DATATYPE(&AMT) ^= NUM THEN DO
00390018    IF      &STR(&AMT) = HALF | &STR(&AMT) = H | &STR(&AMT) = CSR THEN +
00400005         SET SCROLL = &RMBFMAXD / 2
00410018    ELSE IF &STR(&AMT) = PAGE | &STR(&AMT) = P THEN +
00420005         SET SCROLL = &RMBFMAXD
00430005    ELSE SET SCROLL = 1
00440005  END
00450005  ELSE   SET SCROLL = &AMT
00460005 
00470005  /* Get current TOP displayable menu position  OPT#  */
00480005  ISPEXEC VGET (R&POOL.OPT#)
00490005  SET OPT# = &&R&POOL.OPT#
00500005  IF &DATATYPE(&OPT#) ^= NUM THEN SET OPT# = 1
00510005  IF &OPT# < 1               THEN SET OPT# = 1
00520005  IF &OPT# > &RMBFMAXM       THEN SET OPT# = &RMBFMAXM
00530005 
00540005  /* See if scroll pf key  option selected */
00550005  IF      &UP = UP                         THEN DO
00560005     SET OPT# = &OPT# - &SCROLL
00570005     IF &OPT# < 1  THEN SET OPT# = 1
00580005  END
00590005  ELSE IF &LEFT = LEFT   OR &FIRST = FIRST THEN DO
00600005     /* Scroll "LEFT" is FIRST or TOP option. */
00610005     SET OPT# = 1
00620005  END
00630005  ELSE IF &DOWN = DOWN                     THEN DO
00640012     ISPEXEC VGET (RGRPLDO)
00650012     IF &OPT# >= &RGRPLDO  THEN EXIT
00660005     SET OPT# = &OPT# + &SCROLL
00670005     IF &OPT# > &RMBFMAXM  THEN SET OPT# = &RMBFMAXM
00680010     IF &OPT# > &RGRPLDO   THEN SET OPT# = &RGRPLDO
00690005  END
00700005  ELSE IF &RIGHT = RIGHT OR &LAST = LAST   THEN DO
00710005     /* Scroll "RIGHT" is LAST or BOTTOM option. */
00720005     SET OPT# = &RMBFMAXM - &RMBFMAXD + 1
00730005  END
00740005  ELSE  EXIT
00750005  /* Now scroll it! */
00760005  SET SCROLLIT = SCROLLIT
00770005  /* OPT# now set to the number of the first option to be displayed */
00780005END
00790005/****************** END of SCROLL SET UP ****************************/
00800005 
00810005IF &INIT = INIT THEN DO
00820005  ISPEXEC VGET (R&POOL.DOIT,GRP1STDO)
00830005  SET ZERRSM =
00840005  SET ZERRLM =
00850005  ISPEXEC VPUT (ZERRSM,ZERRLM)
00860005  SET RGRPFDO = 1  /* Position of the First Non-Blank Displayable Option */
00870005  SET RGRPLDO = 1  /* Position of the Last  Non-Blank Displayable Option */
00880005  SET RGRPMAXM = &RMBFMAXM
00890005 
00900005  /* Perform Panel Initialization if requested */
00910005  SET DOIT =  &STR(&&R&POOL.DOIT)
00920005  IF &DOIT =  YES AND &STR(&GRP1STDO) ^= DONE THEN DO
00930005    %RMBF@GRP
00940005    SET GRP1STDO = DONE
00950005    ISPEXEC VPUT (GRP1STDO)
00960005  END
00970005END    /* OF INIT */
00980005 
00990005/* Get the Displayable Menu Options */
01000005/* Begin Option to Display (Varies with Scroll) */
01010005IF &INIT     =   THEN ISPEXEC VGET (RGRPMAXM)
01020005IF &SCROLLIT =   THEN SET OPT# = 1
01030005SET I = &OPT#
01040005SET G = 1    /* Start Display position on panel */
01050005DO WHILE(&G <= &RGRPMAXM)
01060005  IF &I <=  &RGRPMAXM THEN DO
01070005    ISPEXEC VGET (R&POOL.O&I,R&POOL.SD&I,R&POOL.LD&I,R&POOL.C&I) PROFILE
01080005    SET OPT      = &STR(&&R&POOL.O&I)
01090016    IF &DATATYPE(&STR(&OPT)) = NUM THEN +
01100005      IF &OPT <= 9 THEN SET OPT = &STR( &OPT)
01110005      ELSE              SET OPT = &STR(&OPT)
01120005    ELSE                SET OPT = &STR( &OPT)  /* Alpha option */
01130005    SET SD       = &STR(&&R&POOL.SD&I)
01140005    SET LD       = &STR(&&R&POOL.LD&I)
01150005    SET CMD      = &STR(&&R&POOL.C&I)
01160005  END
01170005  ELSE DO
01180005    SET OPT      = &STR( )
01190005    SET SD       = &STR( )
01200005    SET LD       = &STR( )
01210005    SET CMD      = &STR( )
01220005  END
01230017  IF &STR(&OPT) ^= THEN DO
01240017    SET SD = &STR(&SUBSTR(1:12,&STR(&SD            ))-)
01250017    IF  &INIT = INIT THEN DO
01260017      IF &I < &RGRPFDO THEN SET RGRPFDO = &I
01270017      IF &I > &RGRPLDO THEN SET RGRPLDO = &I
01280017    END
01290005  END
01300005  IF &G <= &RMBFMAXD THEN DO
01310005    SET RGGGO&G  = &STR(&OPT)
01320005    SET RGGGSD&G = &STR(&SD)
01330005    SET RGGGLD&G = &STR(&LD)
01340005    SET RGGGC&G  = &STR(&CMD)
01350005    ISPEXEC VPUT (RGGGO&G,RGGGSD&G,RGGGLD&G,RGGGC&G)
01360005  END
01370005  SET I = &I + 1
01380005  SET G = &G + 1
01390005END
01400005IF &INIT = INIT THEN DO
01410005     SET RGRPMAXM = &RGRPLDO
01420005     ISPEXEC VPUT (RGRPFDO,RGRPLDO,RGRPMAXM)
01430005END
01440005ELSE ISPEXEC VGET (RGRPFDO,RGRPLDO)
01450007 
01460007/* Set ShortName to "* NO MORE *" after the last displayed option */
01470007SET EOPT = (&RGRPLDO - &OPT# + 1) + 1
01480013IF (&EOPT >= 1 AND &EOPT <= &RMBFMAXD) AND +
01490014   (&RGRPLDO > &RMBFMAXD OR  &OPT# > 1) THEN DO
01500015  SET ELD = &STR(* End of Options *)
01510015  SET RGGGLD&EOPT = &STR(&ELD)
01520015  ISPEXEC VPUT (RGGGLD&EOPT)
01530007END
01540005 
01550005SET R&POOL.OPT# = &OPT#
01560005ISPEXEC VPUT (R&POOL.OPT#)
01570005IF      (&OPT# + &RMBFMAXD) <= &RGRPLDO THEN +
01580006                               SET RGRPMORE= &STR(   More options ->  )
01590005ELSE IF (&OPT#            ) >  &RGRPFDO THEN +
01600006                               SET RGRPMORE= &STR(<- More options     )
01610005ELSE                           SET RGRPMORE= &STR( )
01620005ISPEXEC VPUT (RGRPMORE)
01630005 
01640005/* Menu options have been load to the displayable portion of the menu */
01650005/* or they have been scrolled either up or down the proper amount.    */
01660005/* Now back to the Scrollable/Modifiable GROUP Primary Option Menu    */
01670005EXIT
