00010000PROC 0 DEBUG NEWITEM(RSNNOTE#) +
00020000       DSN() INDEX(@INDEX)  SHOW(20) SYS AOST ALL ISC +
00030000       SKIP(4) MCOL('01:08') ICOL('05:08') DCOL('11:18') TCOL('21:75') +
00040000       OLDNOTE() TYPE(GROUP) PREFIX() PCOL('01:04') SORT(ITEM)
00050000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00060000  /**                                                                **/
00070000  /** ROCKWELL STARTUP NOTICES (RSN): GROUP; SYS/ISPF; AOST          **/
00080000  /** BY MIKE THEYS  ROCKWELL INTERNATIONAL  ISC/SWCC  02/10/87      **/
00090000  /** VERSION 2.1                                                    **/
00100000  /**                                                                **/
00110000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00120000  /** 'DSN(INDEX)' MEMBER DEFAULTS TO THIS FORMAT AFTER #SKIP LINES: **/
00130000  /**  MEMBERNAME     COLUMNS 01 TO 08                               **/
00140000  /**  ITEM #         COLUMNS 05 TO 08 OF MBRNAME (NUMERIC)          **/
00150000  /**  PREFIX         COLUMNS 01 TO 04 OF MBRNAME MUST MATCH (OPTL.) **/
00160000  /**  DATE           COLUMNS 11 TO 18                               **/
00170000  /**  TITLE          COLUMNS 21 TO 75                               **/
00180000  /** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **/
00190000  IF &DEBUG = DEBUG THEN CONTROL PROMPT LIST CONLIST SYMLIST MSG
00200000  CONTROL NOMSG NOFLUSH ASIS NOPROMPT
00210000  ISPEXEC CONTROL ERRORS RETURN
00220000 
00230000  /* SET ISC DEFAULTS FOR SYS/ISPF, AOST (IF DIFFERENT THAN ABOVE) */
00240000  ISPEXEC VGET (RMBF@VER,ZAPPLID)
00250000  IF &STR(&DSN) = AND &RMBF@VER = SYS  THEN SET SYS  = SYS
00260000  IF &STR(&DSN) = AND &RMBF@VER = AOST THEN SET AOST = AOST
00270000  IF &STR(&DSN) = AND &RMBF@VER = ISC  THEN SET ISC  = ISC
00280000  IF      &SYS  = SYS   THEN DO
00290000    IF &ZAPPLID ^= ISR THEN EXIT
00300000    SET DSN     = &STR('ATSO.SYS.NOTICES')
00310000    SET TYPE    = &STR(SYS/ISPF)
00320000    SET OLDNOTE = &STR(SYS#NOTE)
00330000    SET PREFIX  = &STR(SYS0)
00340000    SET NEWITEM = RSNNOTE#
00350000    SET NOTETBL = RSNSYS#
00360000  END
00370000  ELSE IF &AOST = AOST  THEN DO
00380000    IF &ZAPPLID ^= ISP THEN EXIT
00390000    SET DSN     = &STR('AOST.NEWS.DATA')
00400000    SET TYPE    = &STR(  AOST  )
00410000    SET PREFIX  = &STR(AOST)
00420000    SET NEWITEM = RSNNOTE#
00430000    SET NOTETBL = RSNAOST#
00440000  END
00450000  ELSE IF &ISC  = ISC   THEN DO
00460000    IF &ZAPPLID ^= ISR THEN EXIT
00470000    SET DSN     = &STR('AISC.ROCK.NOTICES')
00480000    SET TYPE    = &STR(  ISC   )
00490000    SET PREFIX  = &STR(ISC)
00500000    SET NEWITEM = RSNNOTE#
00510000    SET NOTETBL = RSNISC#
00520000  END
00530000 
00540000  SET &RSNSKIP  = &SKIP
00550000  SET &RSNSHOW# = &SHOW
00560000  SET &RSNSHOW  =
00570000  IF &ALL = ALL THEN SET RSNSHOW = ALL
00580000  SET &RSNTYPE  = &STR(&TYPE)
00590000  /* WHAT TYPE OF "NEWITEM/RSNNEW#": GIVEN#, READ INDEX, TABLE GET */
00600000  IF  &DATATYPE(&NEWITEM) = NUM THEN /* NEWEST NOTICE # GIVEN */ DO
00610000    SET &RSNNEW#  = &NEWITEM
00620000  END
00630000  ELSE IF &STR(&NEWITEM) = READINDX THEN /* READ NOTICE# IN INDEX */ DO
00640000    /* NOT IMPLEMENTED! */
00650000  END
00660000  ELSE IF &STR(&NEWITEM) = RSNNOTE# THEN /* GET NOTICE# FOR TABLE */ DO
00670000    IF &NOTETBL = THEN SET NOTETBL = RSNNOTE#
00680000    ISPEXEC TBOPEN &NOTETBL NOWRITE
00690000    SET TBRC = &LASTCC
00700000    SET ERR# = 11
00710000    IF &TBRC > 0 THEN GOTO ERR
00720000    SET RSNKEY = NEW
00730000    ISPEXEC TBGET  &NOTETBL
00740000    /* GETS  &RSNNEW# &RSNDATE &RSNUPDID */
00750000    ISPEXEC TBEND  &NOTETBL
00760000  END
00770000 
00780000  SET ERR# = 1
00790000  IF &DATATYPE(&RSNNEW#)  ^= NUM OR &RSNNEW# < 0  THEN GOTO ERR
00800000  SET ERR# = 2
00810000  IF &DATATYPE(&RSNSKIP)  ^= NUM OR &RSNSKIP < 0  THEN GOTO ERR
00820000  SET ERR# = 3
00830000  IF &DATATYPE(&RSNSHOW#) ^= NUM | &RSNSHOW# > 50 THEN GOTO ERR
00840000 
00850000  /* * * * * * *  GET USERS NOTICE STATUS AND LAST ITEM  * * * * * * **/
00860000  /* &RSNUSRS =RNXEW (STATUS R:READ N:NOTREAD X:IGNORED E:ERROR W:NEW */
00870000  /* &RSNUSRS =RNXEW (STATUS P:PRINTED Z:UNKOWN                       */
00880000  /* &RSNUSR# =30    (NOTICE NUMBER OF LAST ITEM IN STATUS STRING     */
00890000  /* (NOTE THAT FIRST CHAR IN STATUS STRING IS FOR NOTICE #26)        */
00900000  /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * **/
00910000  ISPEXEC VGET (RSNUSRS,RSNUSR#) PROFILE
00920000 
00930000  /* THE CURRENT MAX NUMBER OF NOTICES TO KEEP STATUS INFO ON IS 50 */
00940000  SET &NEWSTR  = &STR(WWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW)
00950000  SET &READSTR = &STR(RRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRR)
00960000  SET &NEW     = &SUBSTR(1:&RSNSHOW#,&NEWSTR)
00970000 
00980000  /* SEE IF FIRST TIME USER OF NEW STARTUP NOTICES */
00990000  IF &RSNUSR# =  OR &RSNUSRS = OR &DATATYPE(&RSNUSR#) ^= NUM THEN DO
01000000    SET &RSNUSR# = 0
01010000    SET &RSNUSRS =
01020000    /* CONVERT OLD STARTUP NOTICES VARIABLES TO NEW FORMAT (SYS/AV) */
01030000    IF (&OLDNOTE ^= ) THEN DO
01040000        ISPEXEC VGET (&OLDNOTE) PROFILE
01050000        SET OLDNOTE# = &STR(&&&OLDNOTE)
01060000        SET OLDNOTE# = &STR(&OLDNOTE#)
01070000        IF &STR(&OLDNOTE#) ^= THEN DO
01080000          SET &RSNUSR# = &EVAL(&SUBSTR(5:8,&STR(&OLDNOTE#)))
01090000          SET &RSNUSRS = &SUBSTR(1:&RSNUSR#,&STR(&READSTR))
01100000        END
01110000    END
01120000  END
01130000 
01140000  SET #NOTES = &LENGTH(&STR(&RSNUSRS))
01150000  IF ( (&RSNUSR# > &RSNNEW#)  OR (&#NOTES > &RSNNEW#) ) THEN DO
01160000    /* USER'S NOTICES DATA NOT CORRECT FOR THE "NEWTIME#": RESET! */
01170000    SET ERR# = 4
01180000    SET #OVER   = &RSNUSR# - &RSNNEW#
01190000    IF (&#NOTES > &#OVER) THEN DO
01200000      SET NEWKEEP = (&#NOTES - &#OVER)
01210000      SET RSNUSR# = &RSNNEW#
01220000      SET RSNUSRS = &SUBSTR(1:&NEWKEEP,&STR(&RSNUSRS))
01230000      SET RSNUSRB =
01240000    END
01250000    ELSE DO
01260000      SET RSNUSR# =
01270000      SET RSNUSRS =
01280000      SET RSNUSRB =
01290000    END
01300000    ISPEXEC VPUT (RSNUSR#,RSNUSRS,RSNUSRB) PROFILE
01310000  END
01320000 
01330000  /* ONLY CARE ABOUT STATUS OF LAST "RSNSHOW#" NOTICES */
01340000  IF ( (&RSNUSR# + &RSNSHOW#) <= &RSNNEW#) THEN DO
01350000    /* USER HAS MISSED MORE THAN LAST "RSNSHOW#" NOTICES */
01360000    SET &RSNUSRS = &STR(&NEW)
01370000    SET &RSNUSR# = &RSNNEW#
01380000  END
01390000  ELSE DO /* ADD ANY THAT WERE MISSED TO THE STATUS STRING */
01400000    SET &RSNUSRM = &RSNNEW# - &RSNUSR#  /* NUMBER MISSED */
01410000    IF &RSNUSRM > 0 THEN SET &RSNUSRA = &SUBSTR(1:&RSNUSRM,&STR(&NEW))
01420000    SET &RSNUSRS = &STR(&RSNUSRS&RSNUSRA)
01430000    SET &RSNUSR# = &RSNNEW#
01440000  END
01450000 
01460000  /* SET WORK VARIABLES */
01470000  SET &#NOTES  = &LENGTH(&STR(&RSNUSRS)) /* # NOTICES IN STATUS STRING*/
01480000  SET &NOTE#   = &RSNUSR#
01490000  SET L#  = &NOTE#               /* LAST  NOTICE# IN THE STRING */
01500000  SET F#  = &L# - &#NOTES + 1    /* FIRST NOTICE# IN THE STRING */
01510000  SET ERR# = 5
01520000  IF (&L# <= 0  OR  &#NOTES <= 0  OR  &#NOTES > &L#) THEN GOTO ERR
01530000 
01540000  SET #NS = &#NOTES    /* NUMBER OF NOTICES IN THE STATUS STRING */
01550000  SET &SHOWN   = 0     /* THE NUMBER TO SHOW THE USER */
01560000  DO WHILE(&#NS > 0 )
01570000    SET STAT = &SUBSTR(&#NS:&#NS,&STR(&RSNUSRS))
01580000    IF &STAT = W | &STAT = N THEN SET &SHOWN = &SHOWN + 1
01590000    SET &#NS = &#NS - 1
01600000  END
01610000 
01620000 
01630000  /* NOW CREATE TABLE DISPLAY OF NOTES TO SEE */
01640000  IF &SHOWN > 0 | &ALL = ALL THEN DO
01650000 
01660000    /* CHECK NOTICES DATASET */
01670000    SET &RSN#DSN  = &STR(&DSN)
01680000    VSCAN RSN#DSN ''''
01690000    SET FQ = &LASTCC
01700000    SET L = &LENGTH(&STR(&RSN#DSN))
01710000    IF (&FQ > 0  AND  &FQ < &L) THEN DO
01720000      SET RSN#DSN = &SUBSTR(&FQ+1:&L,&STR(&RSN#DSN))
01730000      VSCAN RSN#DSN ''''
01740000      SET LQ = &LASTCC
01750000      SET RSN#DSN = &SUBSTR(1:&LQ-1,&STR(&RSN#DSN))
01760000    END
01770000    ELSE SET RSN#DSN = &STR(&SYSUID..&RSN#DSN)
01780000    SET DSNSTAT = &SYSDSN('&RSN#DSN')
01790000    SET ERR# = 6
01800000    IF &DSNSTAT  ^= OK  THEN GOTO ERR
01810000    SET IDXSTAT = &SYSDSN('&RSN#DSN(&INDEX)')
01820000    SET ERR# = 7
01830000    IF &IDXSTAT    ^= OK  THEN GOTO ERR
01840000    ALLOC FI(RSNDD) REUSE DA('&RSN#DSN(&INDEX)') SHR
01850000 
01860000    ISPEXEC TBCREATE RSNTABLE KEYS(RSNITEM) NOWRITE +
01870000              NAMES(RSNMBR,RSNDATE,RSNTITLE,RSNSTAT) REPLACE
01880000    SET TBRC = &LASTCC
01890000    SET ERR# = 8
01900000    IF &TBRC   > 4 THEN GOTO ERR
01910000    IF &STR(&SORT) = DATE +
01920000      THEN ISPEXEC TBSORT   RSNTABLE FIELDS(RSNDATE,C,D)
01930000      ELSE ISPEXEC TBSORT   RSNTABLE FIELDS(RSNITEM,C,D)
01940000 
01950000    OPENFILE RSNDD INPUT
01960000    SET RCODE = 0
01970000    ERROR DO
01980000      SET RCODE = &LASTCC.   /* SAVE THE RETURN CODE */
01990000      IF &RCODE = 400 /* EOF */ THEN GOTO EOF
02000000      IF &RCODE <= 8 THEN RETURN
02010000      SET ERR# = 9
02020000      GOTO ERR
02030000    END
02040000 
02050000    /* SKIP FIRST "SKIP(##)" RECORDS IN  NOTICES INDEX MEMBER */
02060000    DO WHILE (&SKIP > 0)
02070000      GETFILE  RSNDD
02080000      SET SKIP = &SKIP - 1
02090000    END
02100000 
02110000    /* BUILD THE ISPF TABLE WITH ITEMS NOT READ OR NEW ITEMS */
02120000    SET #TABLE = 0                 /* COUNT ITEMS ACTUALLY IN TABLE */
02130000 
02140000    SET #READ = &RSNSHOW#
02150000    /* ONLY READ THE "RSNSHOW#" NEWEST NOTICES */
02160000    DO WHILE ( (&#READ > 0) OR &ALL = ALL )
02170000      GETFILE RSNDD
02180000      SET RSNMBR   = &SUBSTR(&MCOL,&STR(&RSNDD))
02190000      SET RSNPFX   = &SUBSTR(&PCOL,&STR(&RSNMBR))
02200000      /* IF PREFIX IS SPECIFIED, ONLY USE MEMBERNAMES WITH SAME PREFIX*/
02210000      IF &STR(&PREFIX) =  OR  &STR(&PREFIX) = &STR(&RSNPFX) THEN DO
02220000        SET RSNITEM  = &SUBSTR(&ICOL,&STR(&RSNMBR))
02230000        IF &DATATYPE(&RSNITEM) = NUM AND +
02240000           (( &RSNITEM >= &F# AND &RSNITEM <= &L# ) | &ALL=ALL) THEN DO
02250000          /* CONVERT NOTICE ITEM# TO POSITION IN STATUS STRING */
02260000          SET RSN# = (&RSNITEM - &F#) + 1
02270000          IF &RSN# > 0  +
02280000            THEN SET STAT = &SUBSTR(&RSN#:&RSN#,&STR(&RSNUSRS))
02290000            ELSE SET STAT = Z
02300000          /* NOW  ONLY GET THOSE "NOT READ" YET OR THAT ARE "NEW" */
02310000          /* DO NOT SHOW THOSE ALREADY READ NOR THOSE MARKED AS IGNORED */
02320000          /* UNLESSS "ALL" WAS SPECIFIED */
02330000          IF &STAT = W OR &STAT = N  OR &ALL = ALL THEN DO
02340000            IF      &STAT = W THEN SET RSNSTAT = &STR(NEW)
02350000            ELSE IF &STAT = N THEN SET RSNSTAT = &STR(UNREAD)
02360000            ELSE IF &STAT = X THEN SET RSNSTAT = &STR(IGNORE)
02370000            ELSE IF &STAT = R THEN SET RSNSTAT = &STR(READ)
02380000            ELSE IF &STAT = P THEN SET RSNSTAT = &STR(PRINT)
02390000            ELSE IF &STAT = E THEN SET RSNSTAT = &STR(ERROR)
02400000            ELSE IF &STAT = Z THEN SET RSNSTAT = &STR(?)
02410000            /* NOW GET REST OF THE INFORMATION AND ADD TO TABLE */
02420000            SET RSNDATE  = &SUBSTR(&DCOL,&STR(&RSNDD))
02430000            SET RSNTITLE = &SUBSTR(&TCOL,&STR(&RSNDD))
02440000            ISPEXEC TBADD RSNTABLE ORDER
02450000            SET TBRC = &RCODE  /* MUST USE BECAUSE OF "ERROR DO" */
02460000            SET ERR# = 10
02470000            IF &TBRC > 0 THEN GOTO ERR
02480000            SET #TABLE = &#TABLE + 1
02490000          END  /* TABLE ADD OF "NEW" OR "UNREAD" (OR ALL) */
02500000        END  /* ITEM# IS IN CURRENT STATUS */
02510000      END  /* PREFIX MATCHES  OR WAS NOT CHECKED FOR */
02520000      SET #READ = &#READ - 1
02530000    END  /* OF LOOP TO READ INDEX MEMBER TO CREATE TABLE DISPLAY */
02540000 
02550000    EOF: +
02560000    ERROR OFF
02570000    CLOSFILE RSNDD
02580000    FREE FI(RSNDD)
02590000 
02600000    SET #SEEN = 0  /* COUNT ITEMS READ/PRINTED/IGNORED FOR AUTO-END */
02610000    SET ASEL  =    /* AUTO SELECT COMMAND ENTRY */
02620000    /* GET VARS FOR AUTOEND? YES/NO, AUTO ENTRY OF R/X/P EACH ITEM */
02630000    IF (&ALL = ) THEN ISPEXEC VGET (RSNEND,RSNASEL) PROFILE
02640000 
02650000    /* NOW SHOW THE ITEMS IN THE TABLE */
02660000    IF &#TABLE > 0 THEN DO
02670000 
02680000      SET RSNSEL =
02690000      SET PANEL = &STR( PANEL(RMBFRSN) )
02700000 
02710000      ISPEXEC VGET (RSNDEST) PROFILE
02720000      IF &STR(&RSNDEST) = THEN DO
02730000        SHOWTSO DEST
02740000        SET RSNDEST = &DEST
02750000        ISPEXEC VPUT (RSNDEST) PROFILE
02760000      END
02770000 
02780000      ISPEXEC TBTOP   RSNTABLE
02790000      IF &RSNASEL ^=  THEN DO
02800000        SET SHORT = &STR("&RSNASEL" ALL ITEMS)
02810000        SET LONG  = &STR(PRESS ENTER FOR INVOKING OPTION "&RSNASEL" FOR EACH NEWS ITEM.)
02820000        ISPEXEC SETMSG MSG(SYS010)
02830000      END
02840000 
02850000      SHOWTBL:+
02860000      IF &RSNZCMD ^=    THEN ISPEXEC TBTOP RSNTABLE
02870000      SET RSNZCMD =
02880000      ISPEXEC TBDISPL RSNTABLE &PANEL
02890000      SET TBRC = &LASTCC
02900000      IF &TBRC >= 8 THEN GOTO TBLEND
02910000 
02920000      ISPEXEC VGET (RSNZCMD)
02930000 
02940000      IF (&RSNZCMD = DEFAULT | &RSNZCMD = DEFAULTS  +
02950000       |  &RSNZCMD = OPTION  | &RSNZCMD = OPTIONS ) THEN DO
02960000        SET RSNZCMD =
02970000        ISPEXEC SELECT PANEL(RMBFRSND)
02980003        ISPEXEC VGET (RSNDEST) PROFILE
02990003        IF &ALL =  THEN ISPEXEC VGET (RSNASEL,RSNEND) PROFILE
03000000        ISPEXEC TBTOP RSNTABLE
03010000        IF &RSNASEL ^=  THEN DO
03020000          SET SHORT = &STR("&RSNASEL" ALL ITEMS)
03030000          SET LONG  = &STR(PRESS ENTER FOR INVOKING OPTION "&RSNASEL" FOR EACH NEWS ITEM.)
03040000          ISPEXEC SETMSG MSG(SYS010)
03050000        END
03060000      END
03070000 
03080000      IF &RSNSEL ^= THEN SET RSNASEL =
03090000      ASELDO:+
03100000      IF (&RSNASEL ^=  AND &RSNZCMD =  AND &ASEL ^= DONE) THEN DO
03110000        /* GET NOTICE FROM TABLE */
03120000        ISPEXEC TBSKIP RSNTABLE
03130000        IF &LASTCC ^= 0 THEN SET ASEL = DONE
03140000        SET RSNSEL  = &RSNASEL
03150000        SET ASELCMD = &RSNASEL
03160000        IF &ASEL = DONE THEN GOTO ASELDONE
03170000      END
03180000 
03190000      /* FOR "REPORT" OR "P"-PRINT */
03200000      IF &RSNSEL = P | &RSNZCMD = REPORT | &RSNZCMD = INDEX THEN DO
03210000        ISPEXEC VGET (RSNDEST,RSNCLASS,RSNCAPS,RSNPLN) PROFILE
03220000        IF &RSNDEST  ^=    THEN SET DEST  = &STR( DEST(&RSNDEST) )
03230000        ELSE                    SET DEST  =
03240000        IF &RSNCLASS ^=    THEN SET CLASS = &STR( CLASS(&RSNCLASS) )
03250000        ELSE                    SET CLASS =
03260000        IF &RSNPLN   ^=    THEN SET PLN   = &STR( PAGELENGTH(&RSNPLN) )
03270000        ELSE                    SET PLN   =
03280000        IF &RSNPLN    =  0 THEN SET PLN   = &STR( NOPL )
03290000      END
03300000 
03310000      IF &RSNZCMD = REPORT | &RSNZCMD = INDEX THEN DO
03320000        ISPEXEC TBTOP  RSNTABLE
03330000        ISPEXEC FTOPEN TEMP
03340000        ISPEXEC FTINCL RMBFRSN
03350000        SET FTRC = &LASTCC
03360000        ISPEXEC FTCLOSE
03370000        ISPEXEC VGET (ZTEMPF)
03380000        PRINTOFF '&ZTEMPF' +
03390000                 NOMSG NOHEADING RCL +
03400000                 &DEST &CLASS &RSNCAPS &PLN
03410000        SET &LINES = &LASTCC
03420000        SET SHORT = &STR(INDEX PRINTED)
03430000        SET LONG  = &STR(&CENTER &RSNTYPE STARTUP NOTICES SUMMARY WAS PRINTED WITH &LINES LINES TO &DEST)
03440000        ISPEXEC SETMSG MSG(SYS010)
03450000      END
03460000 
03470000      IF &RSNZCMD = PRINTER THEN DO
03480000        ISPEXEC SELECT PANEL(RMBFRSNP)
03490000      END
03500000 
03510000      IF &RSNSEL = S OR &RSNSEL = R THEN DO /* READ/BROWSE NOTICE */
03520000        IF &STR(&RSNSTAT) ^=  THEN +
03530000          IF &SUBSTR(1:1,&STR(&RSNSTAT)) ^= &STR(*) THEN SET #SEEN = &#SEEN + 1
03540000        IF &SYSDSN('&RSN#DSN(&RSNMBR)') = OK THEN DO
03550000          ISPEXEC CONTROL DISPLAY SAVE
03560000          ISPEXEC BROWSE DATASET('&RSN#DSN(&RSNMBR)') PANEL(RMBFRSNB)
03570000          ISPEXEC CONTROL DISPLAY RESTORE
03580000          SET RSNUPD = R
03590000          SET RSNSTAT= &STR(*READ)
03600000        END
03610000        ELSE DO
03620000          SET RSNSTAT= &STR(*ERROR)
03630000          SET RSNUPD = E
03640000        END
03650000      END
03660000 
03670000      IF &RSNSEL = X THEN DO /* IGNORE/EXCLUDE FROM FUTURE DISPLAY */
03680000        IF &STR(&RSNSTAT) ^=  THEN +
03690000          IF &SUBSTR(1:1,&STR(&RSNSTAT)) ^= &STR(*) THEN SET #SEEN = &#SEEN + 1
03700000        SET RSNUPD = X
03710000        SET RSNSTAT= &STR(*SKIP)
03720000      END
03730000 
03740000      IF &RSNSEL = P THEN DO /* PRINT SELECTED NOTICE TO RJE DEST */
03750000        IF &STR(&RSNSTAT) ^=  THEN +
03760000          IF &SUBSTR(1:1,&STR(&RSNSTAT)) ^= &STR(*) THEN SET #SEEN = &#SEEN + 1
03770000        IF &SYSDSN('&RSN#DSN(&RSNMBR)') = OK THEN DO
03780000          PRINTOFF '&RSN#DSN(&RSNMBR)' +
03790000                   NOMSG NOHEADING RCL +
03800000                   &DEST &CLASS &RSNCAPS &PLN
03810000          SET LINES = &LASTCC
03820000          SET RSNSTAT= &STR(*PRINT)
03830000          SET RSNUPD = P
03840000        END
03850000         ELSE DO
03860000           SET RSNSTAT= &STR(*ERROR)
03870000           SET RSNUPD = E
03880000         END
03890000      END
03900000 
03910000      /* NOW UPDATE THE STATUS STRING */
03920000      IF &RSNUPD ^= THEN DO
03930000        /* CONVERT THE NOTICE ITEM# TO ITS POSITION IN STATUS STRING */
03940000        SET RSN# = (&RSNITEM - &F#) + 1
03950000        IF &RSN# > 0 THEN DO
03960000          IF &RSN# > 1       THEN  SET &B4 = &SUBSTR(1:&RSN#-1,&STR(&RSNUSRS))
03970000          ELSE                     SET &B4 =
03980000          IF &RSN# < &#NOTES THEN  SET &AF = &SUBSTR(&RSN#+1:&#NOTES,&STR(&RSNUSRS))
03990000          ELSE                     SET &AF =
04000000          SET OLDSTAT = &SUBSTR(&RSN#:&RSN#,&STR(&RSNUSRS))
04010000          /* IF PREVIOUSLY "READ" STILL CONSIDER READ IF JUST PRINTED */
04020000          IF &RSNUPD = P AND &OLDSTAT = R THEN SET RSNUPD = R
04030000          SET &RSNUSRS = &STR(&B4.&RSNUPD.&AF)
04040000        END
04050000        SET RSNUPD =
04060000      END
04070000      ISPEXEC TBMOD RSNTABLE ORDER
04080000 
04090000      ASELDONE:+
04100000      IF &TBRC  = 0 THEN SET PANEL = &STR( PANEL(RMBFRSN) )
04110000      IF &TBRC  = 4 THEN SET PANEL =
04120000      IF &RSNZCMD =   AND &RSNSEL =  AND &RSNASEL =   THEN ISPEXEC TBTOP RSNTABLE
04130000      SET RSNSTAT =
04140000      SET RSNSEL  =
04150000      IF (&#SEEN = &#TABLE) THEN DO
04160000        IF &RSNEND ^= NO THEN DO
04170000          IF &RSNASEL ^=  THEN SET ASEL = DONE
04180000          GOTO TBLEND
04190000        END
04200000        ELSE  ISPEXEC TBTOP RSNTABLE
04210000      END
04220000      IF (&RSNASEL ^=  AND &ASEL =  ) THEN GOTO ASELDO
04230000      IF (&TBRC < 8) | (&ASEL = DONE)  THEN GOTO SHOWTBL
04240000 
04250000    END /* OF ACTUAL TABLE DISPLAY */
04260000 
04270000    TBLEND:+
04280000    ISPEXEC TBEND RSNTABLE
04290000 
04300000    /* CONVERT THE "NEW" ITEMS WHOSE TITLE HAVE BEEN SEEN TO "NOT READ */
04310000    QUIT:+
04320000    SET &#NOTES = &LENGTH(&STR(&RSNUSRS))
04330000    SET &NOTE#  = &RSNUSR#             /* LAST  NOTICE# IN THE STRING */
04340000    SET L#      = &NOTE#               /* LAST  NOTICE# IN THE STRING */
04350000    SET F#      = &L# - &#NOTES + 1    /* FIRST NOTICE# IN THE STRING */
04360000    ISPEXEC VGET (RSNUSRB) PROFILE
04370000    /* LASTEST NOTICE# THAT USER HAS BROWSED */
04380000    SET LBR = &RSNUSRB
04390000    IF &RSNUSRB =  OR  &RSNUSRB > &RSNNEW# THEN SET LBR = 0
04400000    SET #NS = &#NOTES
04410000    SET N#  = &NOTE#
04420000    DO WHILE(&#NS > 0)
04430000      /* CONVERT THE NOTICE ITEM# TO ITS POSITION IN STATUS STRING */
04440000      SET RSN# = (&N# - &F#) + 1
04450000      SET STAT = &SUBSTR(&RSN#:&RSN#,&STR(&RSNUSRS))
04460000      IF &STAT = W THEN DO /* NEW ITEM CONVERT TO "NOT READ" STATUS */
04470000        SET &STAT = N      /* "W" = NEW   "N" = UNREAD */
04480000        IF &RSN# > 1       THEN SET &B4 = &SUBSTR(1:&RSN#-1,&STR(&RSNUSRS) )
04490000        ELSE                    SET &B4 =
04500000        IF &RSN# < &#NOTES THEN SET &AF = &SUBSTR(&RSN#+1:&#NOTES,&STR(&RSNUSRS) )
04510000        ELSE                    SET &AF =
04520000        SET &RSNUSRS = &STR(&B4.&STAT.&AF)
04530000      END /* OF CONVERT TO "UNREAD" STATUS */
04540000      IF (&STAT = R AND &N# > &LBR) THEN SET LBR = &N#
04550000      SET #NS = &#NS - 1
04560000      SET N#  = &N#  - 1
04570000    END /* OF SCAN FINAL STATUS */
04580000    SET RSNUSRB = &LBR
04590000  END /* OF TABLE CREATION AND DISPLAY */
04600000 
04610000  ISPEXEC VPUT (RSNUSRS,RSNUSR#,RSNUSRB) PROFILE
04620000  /* NOW FORCE THE SAVING OF THE CURRENT APPLID PROFILE VARIABLES */
04630000  ISPEXEC VGET (ZAPPLID)
04640000  ISPEXEC TBSAVE &ZAPPLID.PROF LIBRARY(ISPPROF)
04650000  IF (&ASEL = DONE AND &ASELCMD ^=  AND &RSNEND ^= NO) THEN DO
04660000    SET LONG = &STR(ALL NOTICES PROCESSED WITH AUTO COMMAND ENTRY OF "&ASELCMD")
04670000    IF      &ASELCMD = R THEN SET SHORT = &STR(ALL WERE READ)
04680000    ELSE IF &ASELCMD = P THEN SET SHORT = &STR(ALL WERE PRINTED)
04690000    ELSE IF &ASELCMD = X THEN SET SHORT = &STR(ALL WERE IGNORED)
04700000    ISPEXEC SETMSG MSG(SYS010)
04710000  END
04720000  EXIT  /* EXIT HERE IF ALL OK */
04730000 
04740000  ERR:+
04750000  ERROR OFF
04760000  SET SHORT = &STR(NOTICES ERROR)
04770000  IF      &ERR# =  1 THEN SET LONG = &STR(LATEST GROUP NOTICE# IS NON-NUMERIC, NEGATIVE OR NOT SPECIFIED.  NEWITEM="&RSNNEW#".)
04780000  ELSE IF &ERR# =  2 THEN SET LONG = &STR(NUMBER OF LINES TO SKIP IN NOTICES INDEX MEMBER IS NOT VALID. SKIP="&RSNSKIP".)
04790000  ELSE IF &ERR# =  3 THEN SET LONG = &STR(NUMBER OF NOTICES TO SAVE STATUS ON IS OVER 50 OR NOT NUMERIC. SHOW="&RSNSHOW#".)
04800000  ELSE IF &ERR# =  4 THEN SET LONG = &STR(LAST USER NOTICE # &RSNUSR# IS GREATER THAN NEW GROUP #&RSNNEW#..)
04810000  ELSE IF &ERR# =  5 THEN SET LONG = &STR(BAD NOTICE# AND/OR BAD USER NOTICE STATUS STRING. FIRST#=&F# LAST#=&L#)
04820000  ELSE IF &ERR# =  6 THEN SET LONG = &STR(NOTICES DATASET '&RSN#DSN': &DSNSTAT)
04830000  ELSE IF &ERR# =  7 THEN SET LONG = &STR(INDEX MEMBER "&INDEX" DOESN'T EXIST IN '&RSN#DSN')
04840000  ELSE IF &ERR# =  8 THEN SET LONG = &STR(CREATION OF NOTICES DISPLAY TABLE FAILED. RC = &TBRC)
04850000  ELSE IF &ERR# =  9 THEN SET LONG = &STR(UNEXPECTED ERROR CREATING NOTICES LIST. ERROR RC=&RCODE)
04860000  ELSE IF &ERR# = 10 THEN SET LONG = &STR(DUPLICATE &RSNMBR DURING TABLE BUILD. CONTACT &RSNTYPE NOTICES SUPPORT)
04870000  ELSE IF &ERR# = 11 THEN SET LONG = &STR(&NOTETBL TABLE OPEN FAILURE DURING READ OF LATEST NOTICE#. RC=&TBRC)
04880000  ELSE                    SET LONG = &STR(UNEXPECTED ERROR IN ROCKWELL &RSNTYPE STARTUP NOTICES)
04890000  ISPEXEC SETMSG MSG(SYS010)
04900000  EXIT
