*  INCLUDE A SYSLIB DD CARD FOR THE LIBRARY CONTAINING PANEL AND $SBA
TUN0029  TITLE 'SYSTEM COMMITTMENT LEVEL DISPLAY'
         SPACE 5
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  THIS IS A PROGRAM TO MONITOR VARIOUS INDICATORS OF SYSTEM          *
*  PERFORMANCE.  THEY ARE DISPLAYED GRAPHICALLY ON A 3270 TYPE        *
*  TERMINAL SUCH THAT A TARGET LEVEL FOR EACH INDICATOR WILL          *
*  CORRESPOND MID-SCALE.  THIS INFORMATION CAN BE USED BY THE         *
*  SYSTEM OPERATOR TO HELP HER KEEP FROM OVER COMMITTING THE          *
*  RESOURCES OF THE SYSTEM.                                           *
*                                                                     *
*  THIS PROGRAM RUNS AS A TSO COMMAND.  IT SHOULD BE RUN IN A         *
*  SPECIAL PERFORMANCE GROUP SO THAT ITS RESPONSE WILL REMAIN         *
*  GOOD AND SO THAT ITS LONG TRANSACTION TIMES WILL NOT SKEW          *
*  RESPONSE TIME REPORTING.                                           *
*                                                                     *
*                                                                     *
*  ATTRIBUTES:                                                        *
*     NOT RENT OR REFR                                                *
*     PROBLEM PROGRAM STATE AND KEY                                   *
*                                                                     *
*  DEPENDENCIES:                                                      *
*     MVS SP R1 FORMAT OF:                                            *
*        CVT                                                          *
*        RMCT                                                         *
*        RCT                                                          *
*        DMDT                                                         *
*     CLEMSON PANEL MACRO                                             *
*                                                                     *
*  AUTHOR:                                                            *
*     DON FRASER                                                      *
*     CLEMSON UNIVERSITY COMPUTER CENTER                              *
*     50 NEW CHERRY STREET                                            *
*     CLEMSON, SC  29631-2793                                         *
*     (803) 656-3466                                            1981  *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                MODIFICATION AND USER'S GUIDE:                       *
*                                                                     *
*  THIS PROGRAM RUNS UNDER TSO ON AN MVS/SP JES2 RELEASE 1            *
*  SYSTEM AT CLEMSON UNIVERSITY.                                      *
*  MVS/SP1 AND MVS SE 2 KEEP A TIME WEIGHTED TSO RESPONSE TIME        *
*  FIELD IN THE DOMAIN DESCRIPTOR TABLE ENTRY FOR FIRST PERIOD        *
*  TSO DOMAINS IF RTO IS NOT SET TO ZERO.                             *
*  THIS FIELD IS THE SOURCE OF THE TSO RESPONSE TIME INDICATOR        *
*  AND IS NOT AVAILABLE IN PREVIOUS SYSTEMS.                          *
*  TO THE BEST OF MY KNOWLEDGE, THE REST OF THE INFORMATION           *
*  THIS DISPLAY USES IS AVAILABLE IN OTHER MVS SYSTEMS, HOWEVER       *
*  THE OFFSETS WITHIN THE VARIOUS CONTROL BLOCKS OF FIELDS WHICH      *
*  THIS PROGRAM USES WILL SURELY VARY ACCROSS DIFFERENT RELEASES.     *
*  CHECK THE DEBUGGING HANDBOOK FOR THE CORRECT DISPLACEMENTS         *
*  TO USE WITH YOUR SYSTEM.  NOTE ALSO THAT IN PRE SE SYSTEMS THE     *
*  SRM UPDATES THE CONTROL BLOCKS THIS PROGRAM USES EVERY 30          *
*  SECONDS, SO YOU SHOULD CHANGE THE SLEEP VALUE THAT THE STIMER      *
*  USES ACCORDINGLY.                                                  *
*                                                                     *
*  THE VALUES OF THE INDICATORS WE HAVE CHOSEN TO USE AS TARGETS      *
*  FOR OUR OPERATORS MAY WELL NOT BE VALID IN YOUR ENVIRONMENT.       *
*  I SUGGEST THAT YOU STUDY YOUR RMF DATA TO GET A FEEL AS TO WHAT    *
*  VALUES MIGHT BE BEST FOR YOUR INSTALLATION.                        *
*                                                                     *
*  MANY MANUFACTURERS OF 3270 COMPATIBLE TERMINALS HAVE LONG HAD      *
*  THE CAPABILITY TO DISPLAY BLINKING AND/OR REVERSE VIDEO FIELDS.    *
*  UNFORTUNATELY, THERE ARE MANY DIFFERENT IMPLEMENTATIONS OF THIS.   *
*  AT CLEMSON, WE USE THIS PROGRAM ON A BRAEGEN B-3 DISPLAY.          *
*  IF THE SYSTEM IS DETERMINED TO BE OVERCOMMITTED, WE CHANGE         *
*  AN ATTRIBUTE BYTE IN THE SCREEN IMAGE TO MAKE THE WORDS "OVER      *
*  COMMITTED" BLINK.  YOU WILL PROBABLY HAVE TO CHANGE THE EQUATE     *
*  FOR SYMBOL HBLINK TO GET THIS TO WORK RIGHT AT YOUR INSTALLATION.  *
*  ALSO, OUR TERMINALS DISPLAY A NICE BRIGHT SQUARE SYMBOL FOR A      *
*  CHARACTER OF X'73', SO WE USE THAT TO MAKE THE PLOT LINES WITH.    *
*  OTHER TERMINALS WON'T DISPLAY A X'73' AT ALL SO YOU MAY NEED       *
*  TO CHANGE THIS AS WELL - CHANGE " SYMBOL EQU X'73' " TO SOMETHING  *
*  ELSE LIKE " SYMBOL EQU C'*' ".                                     *
*                                                                     *
*  YOU CAN STOP THIS PROGRAM USING THE PA1 KEY, HOWEVER BE AWARE      *
*  THAT THE STIMER INTERVAL MUST EXPIRE BEFORE THE ATTENTION          *
*  REQUEST IS ACKNOWLEDGED.                                           *
*                                                                     *
*  THE KEY TO EFFECTIVE USE OF THIS PROGRAM BY OPERATORS IS THAT      *
*  THEY KNOW WHAT THE INFORMATION IT DISPLAYS IS TELLING THEM.        *
*  WE HAVE FOUND THAT ONCE GIVEN THIS DISPLAY AND AN UNDERSTANDING    *
*  OF WHAT IT MEANS, OUR OPERATORS NO LONGER RUN THE SYSTEM OVER      *
*  INTITIATED MOST OF THE TIME AS THEY ONCE DID.                      *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TITLE 'PERFORM HOUSEKEEPING'
TUN0029  CSECT
         USING TUN0029,R15
         B     AROUND                   BRANCH AROUND SAVE AREA, ETC
         DC    17F'0'                   SAVE AREA
         DC    AL1(L'EYECATCH)
EYECATCH DC    C'TUN0029 &SYSDATE &SYSTIME'
AROUND   STM   R14,R12,12(R13)          SAVE CALLER'S REGISTERS
         ST    R15,8(,R13)              FWD LINK SA
         ST    R13,4(,R15)              BCK LINK SA
         LR    R13,R15                  POINT TO MY SAVE AREA
         DROP  R15                      KILL TEMP BASE
         USING TUN0029,R13              ESTABLISH PERM BASE
         SPACE 2
R0       EQU   0                        PARAMETERS, WORK
R1       EQU   1                        PARAMETERS, WORK
R2       EQU   2                        WORK
R3       EQU   3                        DMDT BASE
R4       EQU   4                        RMCT BASE
R5       EQU   5                        RCT BASE
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13                       BASE, SA
R14      EQU   14                       RETURN, WORK
R15      EQU   15                       PARAMETERS, WORK
         TITLE 'INITIAL PROCESSING'
*        CLEAR THE SCREEN
         SPACE 1
         TPUT  ERASE,LERASE,FULLSCR
         SPACE 2
*        SET ADDRESSABILITY TO CONTROL BLOCKS
         SPACE 1
MAINLOOP L     R3,CVTPTR                A(CVT)
         USING CVT,R3
         L     R4,CVTOPCTP              A(RMCT)
         USING RMCT,R4
         DROP  R3
         L     R3,RMCTDMDT              A(DMDT)
         USING DMDT,R3
         L     R5,RMCTRCT               A(RCT)
         USING RCT,R5
         TITLE 'GET INDICATOR VALUES'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  THIS SECTION GETS THE INDICATOR VALUES FROM SRM CONTROL BLOCKS     *
*  (DMDT AND RCT) AND PUTS THE VALUES TO BE DISPLAYED INTO WORKING    *
*  STORAGE.                                                           *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
*        FOR EACH DOMAIN, ADD UP THE NUMBER OF USERS WHO ARE OUT
*        AND READY.  FOR THE FIRST PERIOD TSO DOMAIN (10 AT CLEMSON),
*        PICK UP THE TIME WEIGHTED ELAPSED TIME (RESPONSE TIME)
*        AND THE TIME WEIGHTED SERVICE RATE.
         SPACE 2
         LH    R2,RMCTDMNC              NUMBER OF DOMAINS
         SPACE 1
         SR    R1,R1                    CLEAR OUT READY ACCUMULATOR
DMDTLOOP CLI   DMDTNO,10                FIRST PERIOD TSO DMN?
         BNE   NOTTSODM                    NO, PROCEED
         MVC   TWET,DMDTTWET            COPY TIME WEIGHTED ELAPSD TIME
         MVC   TWSR,DMDTTWSR            COPY TIME WEIGHTED SERV RATE
NOTTSODM AH    R1,DMDTOUTU              ADD OUT READY USERS TO COUNT
         LA    R3,LDMDT(,R3)            INDEX DOWN TO NEXT DMDT
         BCT   R2,DMDTLOOP              PROCESS ALL DOMAINS
         ST    R1,OUTR                  SAVE COUNT OF OUT READY AS
         SPACE 3
*        IF THE TIME WEIGHTED SERVICE RATE FOR FIRST PERIOD
*        TSO IS SO LOW THAT THE RESPONSE TIME INDICATED
*        WOULD BE MEANINGLESS, BUT THE INDICATOR SHOWS A REPONSE
*        TIME OF OVER COMMITTED, FORCE THE VALUE TO MID SCALE.
         SPACE 1
         CLC   TWET,=F'1000'            RESP < 1000 MS?
         BL    RESPOK                      YES, LEAVE IT ALONE
         CLC   TWSR,=F'20'              SERV RATE > MINIMUM VALID?
         BH    RESPOK                      YES, ENOUGH TO BE VALID
         MVC   TWET,=F'1000'               NO, FORCE RESP = 1 SEC
RESPOK   DS    0H
         EJECT
*        FOR THE REMAINING INDICATORS (PAGE FAULT RATE, HIGH
*        UNREFERENCED INTERVAL COUNT, CPU BUSY, AND LOGICAL
*        CHANNEL UTILIZATION) COMPUTE A TIME WEIGHTED AVERAGE
*        VALUE:
*              VALUE=(OLD+CURRENT)/2
         SPACE 1
         L     R1,PTR                   GET PREVIOUS VALUE
         AH    R1,RCVPTR                ADD CURRENT AVE PAGE FAULT RATE
         SRA   R1,1                     DIVIDE BY 2 FOR TIME WEIGHTING
         ST    R1,PTR                   SAVE IT
         SPACE 1
         L     R1,UIC                   GET PREVIOUS VALUE
         AH    R1,RCVUICA               ADD CURRENT AVERAGE UIC
         SRA   R1,1                     DIVIDE BY 2 FOR TIME WEIGHTING
         ST    R1,UIC                   SAVE IT
         SPACE 1
         L     R0,CPU                   GET PREVIOUS CPU VALUE
         LH    R1,RCVCPUA               GET CURRENT AVE CPU BUSY * 16
         MH    R1,=H'10'                * 10
         SRA   R1,4                         / 16 = TENTHS
         AR    R1,R0                    ADD CURRENT AND PREVIOUS VALUE
         SRA   R1,1                     DIVIDE BY 2 FOR TIME WEIGHTING
         ST    R1,CPU                   SAVE IT
         SPACE 1
         L     R1,LCU                   GET LAST VALUE
         AH    R1,RCVTOTDF              ADD ACTIVE LCH UTILIZATION
         SRA   R1,1                     DIVIDE BY 2 FOR TIME WEIGHTING
         ST    R1,LCU                   SAVE IT
         EJECT
*        FROM THE UIC VALUE, CALCULATE THE STORAGE CONTENTION
*        INDICATOR (UIC1) AS 1/(UIC-1) AND SCALE IT FOR THE
*        DISPLAY ROUTINE.
         SPACE 1
         L     R1,UIC                   GET CURRENT UIC VALUE
         BCTR  R1,0                     UIC-1
         BCTR  R1,0                     UIC-2
         LTR   R1,R1                    UIC-2<=0?
         BP    UICOK                       NOPE, WE WON'T DIVIDE BY 0
         MVC   UIC1,BADNUM              MAKE UIC1 BE VERY HIGH
         B     UICDONE
UICOK    LA    R15,1000                 1000
         SR    R14,R14                  CLEAR EVEN REG FOR DIVIDE
         DR    R14,R1                   1000/(UIC-2)
         ST    R15,UIC1                 SAVE IT
UICDONE  DS    0H                       FINISHED WITH UIC
         SPACE 3
*        FROM THE CPU VALUE, CALCULATE THE CPU CONTENTION
*        INDICATOR (CPU1) AND SCALE IT FOR THE DISPLAY ROUTINE.
*        BECAUSE THERE IS NO EASY INDICATION OF HOW MUCH
*        IN AND READY WORK THERE IS, WE CHOSE TO USE
*        1/(CPUWAIT+1.3) AS THE INDICATOR TO HELP CONTROL
*        FALSE ALARMS WHEN RUNNING A NUMBER CRUNCHER.
*        NOTE THAT 100% CPU WITH MORE READY WORK IS REPRESENTED
*        AS 101% BY SRM.
         SPACE 1
         L     R1,CPU                   GET CPU PERCENT * 10
         L     R0,=F'1010'              MAX POSSIBLE CPU (101% * 10)
         SR    R0,R1                    GET CPU WAIT PERCENT * 10
         A     R0,=F'13'                ADD 13 TENTHS FOR CPUWAIT+1.3
         LTR   R0,R0                    ZERO?
         BP    CPUOK                       NO, WE CAN DIVIDE
         MVC   CPU1,BADNUM              MAKE 1/CPUWAIT BIG
         B     CPUDONE                     AND EXIT
CPUOK    LR    R1,R0                    PUT CPUWAIT*10 INTO R1
         L     R15,=F'10000'            1000*10
         SR    R14,R14                  CLEAR EVEN REG FOR DIVIDE
         DR    R14,R1                   GIVES 1/CPUWAIT * 1000
         ST    R15,CPU1                 SAVE IT
CPUDONE  DS    0H
         TITLE 'BUILD DISPLAY SCREEN IMAGE'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  NOW THAT WE HAVE THE NUMBERS, PUT THEM INTO THE DISPLAY SCREEN     *
*  IMAGE.                                                             *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
         MVC   SCPTR(L'EDPTR),EDPTR     MOVE IN EDIT MASK
         L     R1,PTR                   GET VALUE FOR PAGE FAULT RATE
         CVD   R1,DUBL                  MAKE IT PACKED
         ED    SCPTR(L'EDPTR),DUBL+5    EDIT IT IN
         SPACE 1
         MVC   SCUIC1(L'EDUIC1),EDUIC1  MOVE IN EDIT MASK
         L     R1,UIC1                  GET VALUE FOR STG CONTENTION
         CVD   R1,DUBL                  MAKE IT PACKED
         ED    SCUIC1(L'EDUIC1),DUBL+5  EDIT IT IN
         SPACE 1
         MVC   SCCPU1(L'EDCPU1),EDCPU1  MOVE IN EDIT MASK
         L     R1,CPU1                  GET VALUE FOR CPU CONTENTION
         CVD   R1,DUBL                  MAKE IT PACKED
         ED    SCCPU1(L'EDCPU1),DUBL+5  EDIT IT IN
         SPACE 1
         MVC   SCTWET(L'EDTWET),EDTWET  MOVE IN EDIT MASK
         L     R1,TWET                  GET VALUE FOR 1ST PER TSO RESP
         CVD   R1,DUBL                  MAKE IT PACKED
         ED    SCTWET(L'EDTWET),DUBL+5  EDIT IT IN
         SPACE 1
         MVC   SCOUTR(L'EDOUTR),EDOUTR  MOVE IN EDIT MASK
         L     R1,OUTR                  GET VALUE FOR OUT READY USERS
         CVD   R1,DUBL                  MAKE IT PACKED
         ED    SCOUTR(L'EDOUTR),DUBL+5  EDIT IT IN
         SPACE 1
         MVC   SCLCU(L'EDLCU),EDLCU     MOVE IN EDIT MASK
         L     R1,LCU                   GET VALUE FOR LCH UTIL
         CVD   R1,DUBL                  MAKE IT PACKED
         ED    SCLCU(L'EDLCU),DUBL+5    EDIT IT IN
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  THIS SECTION MAKES THE PLOT LINES IN THE DISPLAY SCREEN IMAGE      *
*  AND COMPARES THE VALUE OF EACH INDICATOR WITH THE LEVEL ABOVE      *
*  WHICH THE SYSTEM IS SAID TO BE OVER COMMITTED SETTING A FLAG       *
*  IF THAT VALUE IS EXCEEDED.                                         *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
*        BUILD PLOT LINE IN SCREEN IMAGE FOR OUT READY USERS
         SPACE 1
         XC    PLOUTR(50),PLOUTR        CLEAR OUT PLOT LINE
         L     R15,OUTR                 PICK UP VALUE
         S     R15,MINOUTR              LESS MINIMUM SCALE VALUE
         M     R14,SOUTR                MULTIPLY BY SCALE FACTOR
         C     R15,=F'1'                ANY POINTS?
         BL    OUTRDONE                    NOPE
         C     R15,=F'50'               OFF SCALE?
         BNH   OUTROK                      NOPE
         LA    R15,50                      YES, FULL SCALE
OUTROK   DS    0H
         LA    R2,PLOUTR                POINT TO PLOT LINE
OUTRLOOP MVI   0(R2),SYMBOL             PUT A SYMBOL AT THIS SPOT
         LA    R2,1(,R2)                POINT TO NEXT SPOT IN LINE
         BCT   R15,OUTRLOOP             PUT IN RIGHT NUMBER OF SYMBOLS
OUTRDONE DS    0H                       EXIT
         SPACE 2
*        BUILD PLOT LINE IN SCREEN IMAGE FOR 1ST PERIOD TSO RESPONSE
         SPACE 1
         XC    PLTWET(50),PLTWET        CLEAR OUT PLOT LINE
         L     R15,TWET                 PICK UP VALUE
         S     R15,MINTWET              LESS MINIMUM SCALE VALUE
         SR    R14,R14                  CLEAR EVEN REG FOR DIVIDE
         D     R14,STWET                DIVIDE BY SCALE FACTOR
         C     R15,=F'1'                ANY POINTS?
         BL    TWETDONE                    NOPE
         C     R15,=F'50'               OFF SCALE?
         BNH   TWETOK                      NOPE
         LA    R15,50                      YES, FULL SCALE
TWETOK   DS    0H
         LA    R2,PLTWET                POINT TO PLOT LINE
TWETLOOP MVI   0(R2),SYMBOL             PUT A SYMBOL AT THIS SPOT
         LA    R2,1(,R2)                POINT TO NEXT SPOT IN LINE
         BCT   R15,TWETLOOP             PUT IN RIGHT NUMBER OF SYMBOLS
TWETDONE DS    0H                       EXIT
         EJECT
*        BUILD PLOT LINE IN SCREEN IMAGE FOR CPU CONTENTION
         SPACE 1
         XC    PLCPU1(50),PLCPU1        CLEAR OUT PLOT LINE
         L     R15,CPU1                 PICK UP VALUE
         S     R15,MINCPU1              LESS MINIMUM SCALE VALUE
         SR    R14,R14                  CLEAR EVEN REG FOR DIVIDE
         D     R14,SCPU1                DIVIDE BY SCALE FACTOR
         C     R15,=F'1'                ANY POINTS?
         BL    CPU1DONE                    NOPE
         C     R15,=F'50'               OFF SCALE?
         BNH   CPU1OK                      NOPE
         LA    R15,50                      YES, FULL SCALE
CPU1OK   DS    0H
         LA    R2,PLCPU1                POINT TO PLOT LINE
CPU1LOOP MVI   0(R2),SYMBOL             PUT A SYMBOL AT THIS SPOT
         LA    R2,1(,R2)                POINT TO NEXT SPOT IN LINE
         BCT   R15,CPU1LOOP             PUT IN RIGHT NUMBER OF SYMBOLS
CPU1DONE DS    0H                       EXIT
         SPACE 2
*        BUILD PLOT LINE IN SCREEN IMAGE FOR I/O CONTENTION
         SPACE 1
         XC    PLLCU(50),PLLCU          CLEAR OUT PLOT LINE
         L     R15,LCU                  PICK UP VALUE
         S     R15,MINLCU               LESS MINIMUM SCALE VALUE
         M     R14,SLCU                 MULTIPLY BY SCALE FACTOR
         C     R15,=F'1'                ANY POINTS?
         BL    LCUDONE                     NOPE
         C     R15,=F'50'               OFF SCALE?
         BNH   LCUOK                       NOPE
         LA    R15,50                      YES, FULL SCALE
LCUOK    DS    0H
         LA    R2,PLLCU                 POINT TO PLOT LINE
LCULOOP  MVI   0(R2),SYMBOL             PUT A SYMBOL AT THIS SPOT
         LA    R2,1(,R2)                POINT TO NEXT SPOT IN LINE
         BCT   R15,LCULOOP              PUT IN RIGHT NUMBER OF SYMBOLS
LCUDONE  DS    0H                       EXIT
         EJECT
*        BUILD PLOT LINE IN SCREEN IMAGE FOR STORAGE CONTENTION
         SPACE 1
         XC    PLUIC1(50),PLUIC1        CLEAR OUT PLOT LINE
         L     R15,UIC1                 PICK UP VALUE
         S     R15,MINUIC1              LESS MINIMUM SCALE VALUE
         SR    R14,R14                  CLEAR EVEN REG FOR DIVIDE
         D     R14,SUIC1                DIVIDE BY SCALE FACTOR
         C     R15,=F'1'                ANY POINTS?
         BL    UIC1DONE                    NOPE
         C     R15,=F'50'               OFF SCALE?
         BNH   UIC1OK                      NOPE
         LA    R15,50                      YES, FULL SCALE
UIC1OK   DS    0H
         LA    R2,PLUIC1                POINT TO PLOT LINE
UIC1LOOP MVI   0(R2),SYMBOL             PUT A SYMBOL AT THIS SPOT
         LA    R2,1(,R2)                POINT TO NEXT SPOT IN LINE
         BCT   R15,UIC1LOOP             PUT IN RIGHT NUMBER OF SYMBOLS
UIC1DONE DS    0H                       EXIT
         SPACE 2
*        BUILD PLOT LINE IN SCREEN IMAGE FOR PAGE FAULT RATE
         SPACE 1
         XC    PLPTR(50),PLPTR          CLEAR OUT PLOT LINE
         L     R15,PTR                  PICK UP VALUE
         S     R15,MINPTR               LESS MINIMUM SCALE VALUE
         M     R14,SPTR                 MULTIPLY BY SCALE FACTOR
         C     R15,=F'1'                ANY POINTS?
         BL    PTRDONE                     NOPE
         C     R15,=F'50'               OFF SCALE?
         BNH   PTROK                       NOPE
         LA    R15,50                      YES, FULL SCALE
PTROK    DS    0H
         LA    R2,PLPTR                 POINT TO PLOT LINE
PTRLOOP  MVI   0(R2),SYMBOL             PUT A SYMBOL AT THIS SPOT
         LA    R2,1(,R2)                POINT TO NEXT SPOT IN LINE
         BCT   R15,PTRLOOP              PUT IN RIGHT NUMBER OF SYMBOLS
PTRDONE  DS    0H                       EXIT
         EJECT
*        COMPARE INDICATORS WITH OVER COMMITTED LEVELS
         SPACE 1
         MVI   OVER,C'N'                ASSUME NOT OVER COMMITTED
         SPACE 2
         CLC   OUTR,OOUTR               TOO MANY OUT READY USERS?
         BH    OVERCOMM                    YES
         CLC   TWET,OTWET               1ST PERIOD TSO TOO SLOW?
         BH    OVERCOMM                    YES
         CLC   CPU1,OCPU1               CPU CONTENTION TOO HIGH?
         BH    OVERCOMM                    YES
         CLC   LCU,OLCU                 LOGICAL CHANNELS TOO BUSY?
         BH    OVERCOMM                    YES
         CLC   UIC1,OUIC1               STORAGE CONTENTION TOO HIGH?
         BH    OVERCOMM                    YES
         CLC   PTR,OPTR                 PAGE FAULT RATE TOO HIGH?
         BH    OVERCOMM                    YES
         B     OVERX                    PROCEED TO NEXT PART
         SPACE 1
OVERCOMM MVI   OVER,C'Y'                INDICATE WE'RE OVER COMMITTED
OVERX    DS    0H                       EXIT
         SPACE 3
*        SET UP THE SCREEN TO INDICATE IF THE SYSTEM IS OVER COMMITTED
         SPACE 1
         CLI   OVER,C'Y'                OVER COMMITTED?
         BNE   SCROK                       NOPE
         MVI   OCFLAG+1,HBLINK          MAKE IT FLASH
         MVI   WCC,BEEP                 MAKE IT BEEP
         B     SCRX
SCROK    MVI   OCFLAG+1,HIGHPROT        NOT FLASHING
         MVI   WCC,QUIET                DON'T BEEP
SCRX     DS    0H
         TITLE 'DISPLAY THE SCREEN AND TAKE A NAP'
*        PUT A TIME STAMP ON THE SCREEN
         SPACE 1
SCRDISP  TIME  DEC
         SPACE 1
         ST    R0,DUBL                  STORE HHMMSSTH
         MVC   SCTIME(L'EDTIME),EDTIME  MOVE IN EDIT MASK
         ED    SCTIME(L'EDTIME),DUBL    PUT TIME THERE
         SPACE 3
*        THE SCREEN IMAGE IS BUILT, NOW WRITE IT TO TERMINAL
         SPACE 1
         TPUT  SCREEN,LSCRN,FULLSCR
         SPACE 5
*        NOW WAIT UNTIL SRM UPDATES HIS DATA AREAS AGAIN
*           SYSTEMS WITH SE2 AND BEYOND UPDATE EVERY 20 SECONDS
*           IN EARLIER SYSTEMS, SRM UPDATES EVERY 30 SECONDS
         SPACE 1
         STIMER WAIT,BINTVL=SLEEP
         SPACE 3
*        GO DO IT ALL AGAIN
         SPACE 1
         B     MAINLOOP                 DO IT AGAIN
         TITLE 'STATIC DATA'
         LTORG
         SPACE 1
SLEEP    DC    F'2000'                  20 SECONDS (USE 30 FOR NON-SP)
BADNUM   DC    F'9999'                  INFINITY
         SPACE 2
*        OVER COMMITTED LEVELS
         SPACE 1
OCPU1    DC    F'768'   CPU CONTENTION VALUE 1/(101-CPU+1.3) * 1000
OUIC1    DC    F'500'   STORAGE CONTENTION VALUE 1/(UIC-2) * 1000
OPTR     DC    F'50'    PAGE FAULT RATE
OLCU     DC    F'40'    LOGICAL CHANNEL UTILIZATION
OTWET    DC    F'1000'  1ST PERIOD TSO RESPONSE IN MILISECONDS
OOUTR    DC    F'5'     NUMBER OF OUT READY USERS
         SPACE 2
*        PLOT SCALING VALUES:
*           MINXXXX IS THE BOTTOM END OF THE SCALE FOR VALUE XXXX
*           SXXXX   IS THE SCALE FACTOR FOR XXXX AND IS EITHER
*           50 / ( MAX_SCALE_VALUE - MIN_SCALE_VALUE ) WHICH IS
*           MULTIPLIED BY THE VALUE OR ITS INVERSE WHICH IS DIVIDED
*           INTO THE VALUE IN ORDER TO DETERMINE HOW MANY SYMBOLS
*           TO PLACE IN THE PLOT LINE.
         SPACE 1
MINOUTR  DC    F'0'
SOUTR    DC    F'5'
MINTWET  DC    F'0'
STWET    DC    F'40'
MINCPU1  DC    F'0'
SCPU1    DC    F'20'
MINLCU   DC    F'15'
SLCU     DC    F'1'
MINUIC1  DC    F'0'
SUIC1    DC    F'20'
MINPTR   DC    F'25'
SPTR     DC    F'1'
         EJECT
*        EDIT MASKS FOR NUMBERS PUT IN SCREEN IMAGE
         SPACE 1
EDTWET   DC    XL7'4021204B202020'
EDPTR    DC    XL7'4020206B202120'
EDUIC1   DC    XL7'4021204B202020'
EDCPU1   DC    XL7'4021204B202020'
EDOUTR   DC    XL7'4020206B202120'
EDLCU    DC    XL7'4020206B202120'
EDTIME   DC    XL9'4021207A20207A2020'
         SPACE 2
*        EQUATES FOR SYMBOLS, ATTRIBUTES, WRITE CONTROL CHARACTERS
         SPACE 1
SYMBOL   EQU   X'73'                    SYMBOL TO USE IN PLOT LINE
HIGHPROT EQU   X'68'                    ATTRIBUTE FOR HIGH,PROT FIELD
HBLINK   EQU   X'8A'            BRAEGEN ATTRIBUTE FOR BLINKING FIELD
BEEP     EQU   X'C7'                    WCC FOR ALARM
QUIET    EQU   X'C3'                    WCC FOR NO ALARM
         TITLE 'DYNAMIC WORKING STORAGE'
*        VALUES FOR INDICATORS.  THESE VALUES ARE SET INITIALLY TO
*        MID SCALE FOR TIME WEIGHTED AVERAGES.
*        NOTE THAT DUE TO THE TIME WEIGHTING, WHEN THE PROGRAM IS
*        FIRST STARTED, IT MAY TAKE SEVERAL UPDATES FOR THE INDICATORS
*        TO SETTLE DOWN.
         SPACE 1
TWET     DC    F'0'     TIME WEIGHTED ELAPSED TIME IN MILLISECONDS
PTR      DC    F'80'    PAGE FAULT RATE
UIC      DC    F'4'     UIC
CPU      DC    F'990'   CPU% * 10
OUTR     DC    F'0'     OUT READY USERS
LCU      DC    F'40'    LOGICAL CHANNEL UTILIZATION
UIC1     DC    F'0'     1/(UIC-2) * 1000
CPU1     DC    F'0'     1/(101-CPU%+1.3) * 1000
TWSR     DC    F'0'     TIME WEIGHTED SERVICE RATE
         SPACE 2
DUBL     DC    D'0'                     WORK AREA
OVER     DC    C'X'                     OVER COMMITTED FLAG
         EJECT
SCREEN   PANEL TAG(WCC),WRITE,RESTORE,RESET,                           +
               SBA,1,1,SF,LOW,UNPROT,C'    ',SF,LOW,PROT,C'INDICATOR   +
                     VALUE',SBA,1,45,SF,HIGH,PROT,                     +
               C'OK   <-- × -->',TAG(OCFLAG),SF,HIGH,PROT,             +
               C'  OVER COMMITTED',                                    +
               SBA,02,80,SF,HIGH,PROT,                                 +
               SBA,02,1,SF,HIGH,PROT,C'OUT READY USER COUNT',          +
               SF,HIGH,PROT,TAG(SCOUTR),CL7' ',SF,HIGH,PROT,           +
               TAG(PLOUTR),CL50' ',                                    +
               SBA,03,29,SF,LOW,PROT,                                  +
               C'×----+----+----+----+----×----+----+----+----+----×', +
               SBA,04,29,SF,LOW,PROT,                                  +
               C'0    1    2    3    4    5    6    7    8    9   10', +
               SBA,05,54,SF,LOW,PROT,C'×',                             +
               SBA,06,80,SF,HIGH,PROT,                                 +
               SBA,06,1,SF,HIGH,PROT,C'TSO RESPONSE TIME   ',          +
               SF,HIGH,PROT,TAG(SCTWET),CL7' ',SF,HIGH,PROT,           +
               TAG(PLTWET),CL50' ',                                    +
               SBA,07,29,SF,LOW,PROT,                                  +
               C'×----+----+----+----+----×----+----+----+----+----×', +
               SBA,08,29,SF,LOW,PROT,                                  +
               C'0   0.2  0.4  0.6  0.8   1   1.2  1.4  1.6  1.8   2', +
               SBA,09,54,SF,LOW,PROT,C'×',                             +
               SBA,10,80,SF,HIGH,PROT,                                 +
               SBA,10,1,SF,HIGH,PROT,C'CPU CONTENTION      ',          +
               SF,HIGH,PROT,TAG(SCCPU1),CL7' ',SF,HIGH,PROT,           +
               TAG(PLCPU1),CL50' ',                                    +
               SBA,11,29,SF,LOW,PROT,                                  +
               C'×----+----+----+----+----×----+----+----+----+----×', +
               SBA,12,29,SF,LOW,PROT,                                  +
               C'0   .1   .2   .3   .4   .5   .6   .7   .8   .9    1', +
               SBA,13,54,SF,LOW,PROT,C'×',                             +
               SBA,14,80,SF,HIGH,PROT,                                 +
               SBA,14,1,SF,HIGH,PROT,C'I/O CONTENTION      ',          +
               SF,HIGH,PROT,TAG(SCLCU),CL7' ',SF,HIGH,PROT,            +
               TAG(PLLCU),CL50' ',                                     +
               SBA,15,29,SF,LOW,PROT,                                  +
               C'×----+----+----+----+----×----+----+----+----+----×', +
               SBA,16,29,SF,LOW,PROT,                                  +
               C'    20        30        40        50        60     ', +
               SBA,17,54,SF,LOW,PROT,C'×',                             +
               SBA,18,80,SF,HIGH,PROT,                                 +
               SBA,18,1,SF,HIGH,PROT,C'STORAGE CONTENTION  ',          +
               SF,HIGH,PROT,TAG(SCUIC1),CL7' ',SF,HIGH,PROT,           +
               TAG(PLUIC1),CL50' ',                                    +
               SBA,19,29,SF,LOW,PROT,                                  +
               C'×----+----+----+----+----×----+----+----+----+----×', +
               SBA,20,29,SF,LOW,PROT,                                  +
               C'0   .1   .2   .3   .4   .5   .6   .7   .8   .9    1', +
               SBA,21,54,SF,LOW,PROT,C'×',                             +
               SBA,22,80,SF,HIGH,PROT,                                 +
               SBA,22,1,SF,HIGH,PROT,C'PAGE FAULT RATE     ',          +
               SF,HIGH,PROT,TAG(SCPTR),CL7' ',SF,HIGH,PROT,            +
               TAG(PLPTR),CL50' ',                                     +
               SBA,23,29,SF,LOW,PROT,                                  +
               C'×----+----+----+----+----×----+----+----+----+----×', +
               SBA,24,29,SF,LOW,PROT,                                  +
               C'    30        40        50        60        70     ', +
               SBA,24,1,SF,LOW,PROT,TAG(SCTIME),CL9' ',ICA,1,2
LSCRN    EQU   *-SCREEN
         EJECT
ERASE    PANEL WRITE,RESET,CLEAR,IC
LERASE   EQU   *-ERASE
         TITLE 'CONTROL BLOCK MAPPING'
*        NOTE: THIS MAPPING IS FOR MVS SP1
         SPACE 1
CVT      DSECT
CVTPTR   EQU   X'10'
CVTOPCTP EQU   CVT+X'25C',4
         SPACE 1
RMCT     DSECT
RMCTDMDT EQU   RMCT+X'B4',4
RMCTDMNC EQU   RMCT+X'BC',2
RMCTRCT  EQU   RMCT+X'E4',4
         SPACE 1
RCT      DSECT
RCVUICA  EQU   RCT+X'3E',2
RCVCPUA  EQU   RCT+X'40',2
RCVAFQA  EQU   RCT+X'44',2
RCVASMQA EQU   RCT+X'46',2
RCVPTR   EQU   RCT+X'48',2
RCVMSPP  EQU   RCT+X'4E',2
RCVTOTDF EQU   RCT+X'74',2
         SPACE 1
DMDT     DSECT
DMDTNO   EQU   DMDT+0,1
DMDTLO   EQU   DMDT+1,1
DMDTHI   EQU   DMDT+2,1
DMDTMPLT EQU   DMDT+4,2
DMDTRUA  EQU   DMDT+6,2
DMDTCMPL EQU   DMDT+10,2
DMDTOUTU EQU   DMDT+12,2
DMDTTWSR EQU   DMDT+24,4
DMDTFLGS EQU   DMDT+38,1
DMDTRTO  EQU   X'80'
DMDTTWET EQU   DMDT+48,4
LDMDT    EQU   56
         END
