  /*                                                                  */
  /*  THIS PROGRAM PRODUCES THE MANAGEMENT REPORTS USED TO MONITOR    */
  /*  SYSTEM UTILIZATION AND EVALUATE OPERATOR PERFORMANCE IN         */
  /*  IN COMPENSATING FOR SYSTEM LOAD BY INITIATOR RECONFIGURATION.   */
  /*  IT PRODUCES PLOTS FOR INDICATORS DISPLAYED BY TUN0029.          */
  /*                                                                  */
  /*    * OUT READY USER COUNT                                        */
  /*    * TSO RESPONSE TIME                                           */
  /*    * CPU CONTENTION                                              */
  /*    * PAGE FAULT RATE                                             */
  /*                                                                  */
  /*  THE INFORMATION REPORTED HERE IS ACQUIRED FROM SMF RECORDS      */
  /*  WRITTEN BY RMF (SMF RECORD TYPES 70, 71, AND 72.                */
  /*                                                                  */
  /*  EACH PLOT HAS A VERTICAL REFERENCE BAR WHICH REPRESENTS THE     */
  /*  DIVIDING LINE BETWEEN ACCEPTABLE AND UNACCEPTABLE SYSTEM        */
  /*  COMMITMENT.                                                     */
  /*                                                                  */
  /*                                                                  */
OPTIONS NODATE PAGESIZE=65;
*;
DATA T70DATA(KEEP=TIME OUTREADY CPU_CNTN)
     T71DATA(KEEP=TIME PFR)
     T72DATA(KEEP=TIME TSO_RESP);
*;
FORMAT TIME DATETIME13.;
FORMAT OUTREADY TSO_RESP CPU_CNTN PFR 8.3;
*;
INFILE SMF;
INPUT @2 TYPE PIB1. @;
  /*                                                                  */
  /*                                                                  */
  /*  PROCESS TYPE 70 RECORDS:                                        */
  /*    * CALCULATE CPU CONTENTION BASED ON CPU WAIT TIME AND THE     */
  /*      NUMBER OF IN AND READY ADDRESS SPACES                       */
  /*    * GET THE NUMBER OF OUT READY ADDRESS SPACES                  */
  /*                                                                  */
   IF TYPE=70 THEN DO;
      INPUT @17  TIME     RMFSTAMP8.
            @25  SMF70INT       PD4.
            @33  SMF70SAM      PIB4.
            @51  SMF70CPU      PIB2.
            @53  SMF70SCD      PIB2. @;
*;
*     THIS CODE ASSUMES ONLY ONE CPU AND WON'T WORK IF THERE ARE MORE;
      DO I = 1 TO SMF70CPU;
        CPUDS=73+((I-1)*SMF70SCD);
        INPUT @CPUDS    SMF70WAT  PIB8.
              @CPUDS+13 SMF70SER   PK3. @;
      END;
      ASIDDAS=CPUDS+SMF70SCD;
      INPUT @ASIDDAS+4   SMF70RTT  PIB4.
            @ASIDDAS+132 SMF70OTT  PIB4.;
*;
*     GET LENGTH OF INTERVAL IN SECONDS, DISCARD SHORT ONES;
      M=FLOOR(SMF70INT/100000);
      T=SMF70INT - M*100000;
      INTERVAL=M*60 + T/1000;
      IF INTERVAL < 600 THEN DELETE;
*;
      CPUWAIT=(SMF70WAT/4096E4)/INTERVAL;
      INREADY=SMF70RTT/SMF70SAM;
      OUTREADY=SMF70OTT/SMF70SAM;
      IF CPUWAIT < 2 THEN
        DO;
          IF INREADY > 5 THEN
            DO;
              IF CPUWAIT < 1 THEN CPU_CNTN = 1;
              ELSE CPU_CNTN = 1/CPUWAIT;
            END;
          ELSE CPU_CNTN = .5;
        END;
      ELSE CPU_CNTN = 1/CPUWAIT;
      OUTPUT T70DATA;
   END;
  /*                                                                  */
  /*                                                                  */
  /*  PROCESS TYPE 71 RECORDS:                                        */
  /*    * GET THE PAGE FAULT RATE FROM THE SUM OF NON-SWAP RECLAIMS   */
  /*      AND PAGEINS.                                                */
  /*                                                                  */
   IF TYPE=71 THEN DO;
      INPUT @17 TIME     RMFSTAMP8.
            @25 SMF71INT       PD4.
            @53 SMF71PIN      PIB4.
            @61 SMF71PRC      PIB4.;
      M=FLOOR(SMF71INT/100000);
      T=SMF71INT - M*100000;
      INTERVAL=M*60 + T/1000;
      IF INTERVAL < 600 THEN DELETE;
      PFR=(SMF71PIN+SMF71PRC)/INTERVAL;
      OUTPUT T71DATA;
   END;
  /*                                                                  */
  /*                                                                  */
  /*  PROCESS TYPE 72 RECORDS:                                        */
  /*    * GET FIRST PERIOD TSO RESPONSE TIME FROM THE SUM OF ELAPSED  */
  /*      TIMES DIVIDED BY THE NUMBER OF TRANSACTIONS.  HANDLE THE    */
  /*      CASE OF VERY FEW TRANSACTIONS (STATISTICALLY INVALID        */
  /*      RESPONSE TIMES) SIMILARLY TO THE WAY THE ONLINE DISPLAY     */
  /*      DOES.                                                       */
  /*                                                                  */
   IF TYPE=72 THEN DO;
      INPUT @31  SMF72SUB      PIB2. @;
         IF SMF72SUB=100 THEN DO;
            INPUT @15  SMF72SIZ         PIB2.
                  @17  TIME        RMFSTAMP8.
                  @25  SMF72INT          PD4.
                  @15+SMF72SIZ SMF72SWC PIB2.
                               SMF72PGP PIB2.
                               SMF72SPD PIB2. @;
            M=FLOOR(SMF72INT/100000);
            T=SMF72INT - M*100000;
            INTERVAL=M*60 + T/1000;
            IF INTERVAL < 600 THEN DELETE;
            LOC=15+SMF72SIZ+SMF72SWC;
            INPUT @LOC    SMF72TTX PIB4.
                  @LOC+8  SMF72SER PIB4.
                  @LOC+12 SMF72TTM PIB4. @;
            SERVRATE=SMF72SER/INTERVAL;
            IF SMF72TTX < 10 THEN TSO_RESP=0;
            ELSE TSO_RESP=SMF72TTM*0.001024/SMF72TTX;
            IF TSO_RESP > 1 AND SERVRATE < 20 THEN TSO_RESP=1;
            OUTPUT T72DATA;
         END;
   END;
* SORT THE TEMPORARY FILES BY TIME SO THAT THEY CAN BE MERGED TOGETHER ;
PROC SORT DATA=T70DATA; BY TIME;
PROC SORT DATA=T71DATA; BY TIME;
PROC SORT DATA=T72DATA; BY TIME;
*;
DATA CNTN(KEEP=TIME OUTREADY TSO_RESP CPU_CNTN PFR);
MERGE T70DATA T71DATA T72DATA; BY TIME;
OUTPUT CNTN;
*;
PROC CHART DATA=CNTN;
  TITLE1 O U T   R E A D Y   U S E R   C O U N T;
  LABEL OUTREADY=OUT READY USER COUNT;
  HBAR TIME / TYPE=MEAN DISCRETE SUMVAR=OUTREADY REF=5 AXIS=10;
*;
PROC CHART DATA=CNTN;
  TITLE1 T S O   R E S P O N S E   T I M E;
  LABEL TSO_RESP=FIRST PERIOD TSO RESPONSE TIME (SECONDS);
  HBAR TIME / TYPE=MEAN DISCRETE SUMVAR=TSO_RESP REF=1 AXIS=2;
*;
PROC CHART DATA=CNTN;
  TITLE1 C P U   C O N T E N T I O N;
  LABEL CPU_CNTN=CPU CONTENTION LEVEL;
  HBAR TIME / TYPE=MEAN DISCRETE SUMVAR=CPU_CNTN REF=0.5 AXIS=1;
*;
PROC CHART DATA=CNTN;
  TITLE1 P A G E   F A U L T   R A T E;
  LABEL PFR=PAGE FAULTS PER SECOND;
  HBAR TIME / TYPE=MEAN DISCRETE SUMVAR=PFR REF=50 AXIS=100;
