***********************************************************************
*                                                                     *
*              .LINE LENGTH (.LL) --- CONTROL                         *
*                                                                     *
***********************************************************************
         SPACE
LL      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETNUM              GET NUMBER
         BC    BDNO,LL03           ERROR IF INVALID
         BC    BOMIT,LL01          BRANCH IF OMITTED
         CLI   NUMSIGN,C' '        WAS OPERAND SIGNED .Q
         BE    LL02                BRANCH IF NOT
         A     COUNT,LLZ           MAKE RELATIVE CHANGE
         B     LL02                GO CHECK AND ENTER
LL01     DS    0H
         L     COUNT,DEFLL+TAB3DEF  GET DEFAULT LINE LENGTH VALUE
LL02     DS    0H
         CH    COUNT,PICNT         TEST GREATER THAN PARAGRAPH INDENT
         BNH   LL05                ERROR IF NOT
         C     COUNT,DEFLL+TAB3MIN LESS THAN MINIMUM .Q
         BL    LL05                ERROR IF YES
         C     COUNT,DEFLL+TAB3MAX IS IT BEYOND MAX LENGTH .Q
         BH    LL04                YES, ERROR
         ST    COUNT,LLZ           SET LINE LENGTH
         OC    CLLZSAVE,CLLZSAVE   HAS COLUMN LENGTH BEEN SET .Q
         BNZ   LL06                BRANCH IF YES
         ST    COUNT,CLLZ          SET LINE LENGTH AGAIN FOR WORK
         B     LL06                GO TO TERMINATE
         SPACE
LL03     DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     LL06                GO TO TERMINATE
LL04     DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO BIG
         B     LL06                GO TO TERMINATE
LL05     DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         SPACE
LL06     DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
