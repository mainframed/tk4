***********************************************************************
*                                                                     *
*              .HS (HEADING SPACING) --- CONTROL                      *
*                                                                     *
***********************************************************************
         SPACE
HS      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETNUM              GET NUMBER
         BC    BDNO,HS03           ERROR IF INVALID
         BC    BOMIT,HS01          ENTER DEFAULT IF OMITTED
         CLI   NUMSIGN,C' '        WAS ARGUMENT SIGNED .Q
         BE    HS02                BRANCH IF NOT
         AH    COUNT,HSCNT         ALTER EXISTING HEADING SPACING
         B     HS02                GO CHECK AND ENTER
HS01     DS    0H
         LA    COUNT,1             DEFAULT HEADING SPACING VALUE
HS02     DS    0H
         LTR   COUNT,COUNT         IS HEADING SPACING NEGATIVE .Q
         BM    HS05                ERROR IF YES
         LH    TEMP,TOPMRG         GET TOP MARGIN
         SH    TEMP,HDMRG          LESS HEADING MARGIN
         CR    COUNT,TEMP          COMPARE VALUES
         BH    HS06                ERROR IF BIGGER
         C     COUNT,DEFHSFS+TAB3DEF    BIGGER THAN TT/BT OVERLAP .Q
         BH    HS04                ERROR IF YES
         STH   COUNT,HSCNT         SET HEADING SPACING
         B     HS07                BRANCH TO RETURN
         SPACE
HS03     DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     HS07                BRANCH TO RETURN
HS04     DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO BIG
         B     HS07                BRANCH TO RETURN
HS05     DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     HS07                BRANCH TO RETURN
HS06     DS    0H
         MVI   EFLAG041,ERRSET     ARG TOO BIG
         B     HS07                BRANCH TO RETURN
         SPACE
HS07     DS    0H
        $EXIT  RC=0                RETURN TO MAIN CONTROLLER
