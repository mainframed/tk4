***********************************************************************
*                                                                     *
*              .BS (BACKSPACE) --- CONTROL                            *
*                                                                     *
***********************************************************************
         SPACE
BS      $ENTER ,
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION PLEASE
        $CALL  GETARG              GET AN ARGUMENT
         BC    BOMIT,BS05          RESET DEFAULT IF OMITTED
        $CALL  TRHEX               SCAN RESULT FOR CHARACTER
         BC    BDNO,BS01           BRANCH IF INVALID
         STC   R1,BSIND            SET USER BACKSPACE CHAR
         B     BS02                AND CONTINUE
         SPACE
BS01     DS    0H
         ST    COUNT,ARGSTART      RESCAN FOR OPERAND
BS02     DS    0H
        $CALL  GETYESNO            SCAN SECOND OPERAND
         BC    BOMIT,BS06          BRANCH IF DONE
         CLI   YNRESULT,YNHJ       IS IT 'HJOIN' .Q
         BE    BS03                BRANCH IF YES
         CLI   YNRESULT,YNNOHJ     IS IT 'NOHJOIN' .Q
         BE    BS04                BRANCH IF YES
         B     BSE005              ELSE INVALID OPERAND
BS03     DS    0H
         MVI   BSHJ,YNHJ           HEX JOIN OPTION IN EFFECT
         B     BS06                GO TO RETURN
BS04     DS    0H
         MVI   BSHJ,YNNOHJ         NO HEX JOIN OPTION IN EFFECT
         B     BS06                GO TO RETURN
         SPACE
BS05     DS    0H
         MVC   BSIND(1),BSIND+1    RESET TO INITIAL VALUE
         MVC   BSHJ(1),BSHJ+1      RESET HEX JOIN ATTRIBUTE
         SPACE
BS06     DS    0H
        $EXIT  RC=0                AND CONTINUE
         SPACE
BSE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     BS06                GO TO RETURN
