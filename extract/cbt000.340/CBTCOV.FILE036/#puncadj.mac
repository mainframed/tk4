***********************************************************************
*                                                                     *
*              PUNCADJ  --  ADJUST THE LINE AFTER PUNCTUATION         *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --   (A)  NUMBER OF BLANKS TO BE ADDED
*                       (W)  INTERWORD SPACE COUNT
*
*        ON EXIT  --    (A)  NUMBER OF BLANKS LEFT TO BE ADDED
*                       CC = BPOS  IF MORE TO DO
*                       CC = BNEG  IF NOTHING LEFT TO DO
         SPACE
PUNCADJ $ENTER ,
         USING FOPMLIST,R1         DATA DESCRIPTOR
         LA    R15,3*4             NUMBER OF ITERATIONS
         SPACE
PADJ00   DS    0H
         LR    C,W                 NUMBER OF INTERWORD SPACES
         L     P,FOPLAST           POINTER TO LAST LINK BLOCK
         USING LINKELEM,P          AND TELL THE ASSEMBLER
PADJ01   DS    0H
         LTR   P,P                 BACK CHARACTER EXIST .Q
         BZ    PADJ10              BRANCH OUT IF NOT
         CLI   LINKCHAR,C' '       TRAILING BLANKS .Q
         BNE   PADJ03              BRANCH IF NOT
         L     P,LINKBACK          BACKUP ONE CHARACTER
         B     PADJ01              AND TRY AGAIN
         SPACE
PADJ02   DS    0H
         L     P,LINKBACK          FIND NEXT BLANK
PADJ03   DS    0H
         LTR   P,P                 AT THE BEGINNING .Q
         BZ    PADJ09              YES, THAT'S IT
         CLI   LINKCHAR,C' '       IS THIS A BLANK .Q
         BNE   PADJ02              BRANCH IF NOT
         L     POLD,LINKBACK       CHARACTER BEFORE
         LTR   POLD,POLD           DOES IT EXIST .Q
         BZ    PADJ02              BRANCH IF NOT
         USING LOLDELEM,POLD       LOOKS LIKE THIS
         CLI   LOLDCHAR,C' '       MULTIPLE BLANKS .Q
         BE    PADJ02              YES, TRY AGAIN
         B     *(R15)              LEVEL OF CHECK
         B     PADJ04              PAUSES
         B     PADJ05              HALF STOPS
         B     PADJ06              FULL STOPS
         SPACE
PADJ04   DS    0H
         CLI   LOLDCHAR,C','       'COMMA' .Q
         BE    PADJ07              BRANCH IF YES
         CLI   LOLDCHAR,C')'       RIGHT PAREN .Q
         BE    PADJ07              BRANCH IF YES
         B     PADJ08              ELSE CONTINUE
         SPACE
PADJ05   DS    0H
         CLI   LOLDCHAR,C':'       'COLON' .Q
         BE    PADJ07              BRANCH IF YES
         CLI   LOLDCHAR,C';'       'SEMI-COLON' .Q
         BE    PADJ07              BRANCH IF YES
         B     PADJ08              ELSE CONTINUE
         SPACE
PADJ06   DS    0H
         CLI   LOLDCHAR,C'.'       'PERIOD' .Q
         BE    PADJ07              BRANCH IF YES
         CLI   LOLDCHAR,C'!'       'EXCLAMATION' .Q
         BE    PADJ07              BRANCH IF YES
         CLI   LOLDCHAR,C'?'       'QUESTION' .Q
         BE    PADJ07              BRANCH IF YES
         B     PADJ08              ELSE CONTINUE
         SPACE
PADJ07   DS    0H
         IC    R0,LINKMULT         BLANK MULTIPLIER
         A     R0,F1               PLUS ONE
         STC   R0,LINKMULT         UPDATE BLANK MULTIPLIER
         S     A,F1                DECREMENT BLANKS TO BE ADDED
         BZ    PADJ10              BRANCH IF ALL DONE
         SPACE
PADJ08   DS    0H
         S     C,F1                DECREMENT INTERWORD SPACE
         BP    PADJ02              BRANCH IF MORE TO DO
         SPACE
PADJ09   DS    0H
         S     R15,F4              DECREMENT TYPE BEING SOUGHT
         BP    PADJ00              BRANCH IF MORE TO DO
         SPACE
PADJ10   DS    0H
         LTR   A,A                 MORE BLANKS STILL TO FILL .Q
         BP    PADJ11              BRANCH IF YES
        $CC    OMIT                SET DONE CODE
         B     PADJ12              GO TO RETURN
PADJ11   DS    0H
        $CC    POS                 SET NOT DONE CODE
         SPACE
PADJ12   DS    0H
        $EXIT  RC=0,KEEP=(A)       RETURN TO CALLER
         SPACE
         DROP  P,POLD,R1
