***********************************************************************
*                                                                     *
*              .PI (PARAGRAPH INDENT) --- CONTROL                     *
*                                                                     *
***********************************************************************
         SPACE
PI      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BNEG+BOMIT,PI02     BRANCH IF NEGATIVE OR OMITTED
         BC    BPOS,PI01           BRANCH IF AFFIRMATIVE
         ST    COUNT,ARGSTART      RESCAN FOR NUMERIC
        $CALL  GETNUM              GET OPERAND
         BC    BDNO,PIE005         ERROR IF INVALID
         BC    BOMIT,PI02          BRANCH IF OMITTED TO DEFAULT
         CLI   NUMSIGN,C' '        WAS OPERAND UNSIGNED .Q
         BE    PI03                BRANCH IF YES
         AH    COUNT,PICNT         MAKE RELATIVE CHANGE
         B     PI03                GO CHECK AND ENTER
         SPACE
PI01     DS    0H
         LA    COUNT,3             SAME AS .PP
         B     PI03                AND GO ENTER
PI02     DS    0H
         SR    COUNT,COUNT         DEFAULT PARAGRAPH INDENT
PI03     DS    0H
         LTR   COUNT,COUNT         TEST NOT TOO SMALL
         BM    PIE014              ERROR IF TOO SMALL
         C     COUNT,LLZ           TEST NOT TOO BIG
         BNL   PIE013              ERROR IF TOO BIG
         STH   COUNT,PICNT         SET PARAGRAPH INDENT
         B     PI04                GO TO RETURN
         SPACE
PI04     DS    0H
        $EXIT  RC=0                RETURN TO CONTROLLER
         SPACE
PIE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     PI04                GO TO RETURN
PIE013   DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO BIG
         B     PI04                GO TO RETURN
PIE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     PI04                GO TO RETURN
