***********************************************************************
*                                                                     *
*              .PR (PRINT ON CONSOLE) --- CONTROL                     *
*              .TY (TYPE ON TERMINAL) --- CONTROL                     *
*                                                                     *
***********************************************************************
         SPACE
PR      $ENTER ,
         CLC   PASSNO,DEFPASOF+TAB3DEF  IS THIS THE LAST PASS .Q
         BNE   PR01                BRANCH TO PRINT IF NOT
        $IFON  TTYPE,TFILE,PR01    BRANCH IF NO TERMINAL CONFLICT
        $IFOFF SWITCHS,HITSWS,PR01 BRANCH IF 'LOAD;HIT' NOT DISPLAYED
        $IFON  TTYPE,TONLINE,PR02  IF ONLINE, IGNORE REQUEST
         SPACE
PR01     DS    0H
         L     TEMP,ARGSTART       ADDRESS BLANK AFTER .PR
         L     COUNT,ARGSTOP       ADDRESS END OF RECORD
         SR    COUNT,TEMP          COMPUTE LENGTH OF LINE TO PRINT
         BNP   PR02                EXIT IF NOTHING TO PRINT
         SPACE
         L     X1,BUFF3AD          POINT TO WORK BUFFER
         XC    0(4,X1),0(X1)       CLEAR RDW AREA
         EX    COUNT,PRMVC         MOVE CC/RECORD TO WORK BUFFER
         LA    COUNT,5(,COUNT)     LENGTH INCLUDING RDW
         STH   COUNT,0(,X1)        SET RECORD LENGTH
         LR    R0,X1               POINT TO RECORD
        $CALL  PRTERM              DISPLAY RESULT ON TERMINAL
         SPACE
PR02     DS    0H
        $EXIT  RC=0                BACK TO MAIN CONTROL
         SPACE
PRMVC    MVC   4(*-*,X1),0(TEMP)   EXECUTED MOVE PRINT ARGUMENT
