***********************************************************************
*                                                                     *
*              FOHYPH  ---  AUTO HYPHENATE A WORD                     *
*                                                                     *
***********************************************************************
         SPACE
*        ON EXIT  --  (R15) SET TO 'HYUSER' AND BREAK POINT SET
*                     OR
*                     (R15) SET TO 'HYNONE'
         SPACE
FOHYPH  $ENTER ,
         USING LINKELEM,P          CALLER PROVIDED
        $IFON  SWITCHS,HYSUPSW,FOHY14   BRANCH IF HYPHENATION SUPPRESS
         LH    R0,HYSUPWRK         NUMBERED HYPHENATION SUPPRESS
         S     R0,F1               LESS ONE
         STH   R0,HYSUPWRK         UPDATE WITH REMAINDER
         BM    FOHY14              BRANCH IF MUST SUPPRESS
         CLI   ICORFLG,ICORNONE    FORMATTING INTO CORE .Q
         BNE   FOHY01              BRANCH IF YES
         LH    R0,PLCT             LINES LEFT ON PAGE
         S     R0,F1               LESS ONE
         LH    R1,BMMRG            BOTTOM MARGIN
         AH    R1,FNSEP            PLUS FOOTNOTE MARGIN
         CR    R0,R1               COMPARE WITH BOTTOM MARGIN
         BNH   FOHY14              SUPPRESS LAST LINE OF PAGE
         SPACE
FOHY01   DS    0H
         SR    B,B                 ZERO FILL COUNT
FOHY02   DS    0H
         CLI   LINKCHAR,C' '       CHARACTER A BLANK .Q
         BE    FOHY03              YES, GO GET WORD END
         IC    M,LINKMULT          LENGTH OF CHAR
         AR    B,M                 BUMP BACK AMOUNT
         L     P,LINKBACK          BACKUP TO PREV CHARACTER
         B     FOHY02              CONTINUE START SEARCH
FOHY03   DS    0H
         L     P,LINKFOR           FIRST CHAR OF WORD
         CLI   LINKCHAR,C'('       WORD START WITH LEFT PAREN .Q
         BE    FOHY04              BRANCH IF YES
         CLI   LINKCHAR,C'"'       WORD START WITH DOUBLE QUOTE .Q
         BNE   FOHY05              BRANCH IF NOT
FOHY04   DS    0H
         L     P,LINKFOR           SKIP SPECIAL FIRST CHAR
         BCTR  B,0                 DECREMENT SPACE AVAILABLE ON LINE
FOHY05   DS    0H
         CH    B,HYTHRESH          ENOUGH SPACES FOR THRESHOLD .Q
         BL    FOHY14              BRANCH IF NOT
         LA    POLD,0(,P)          REMEMBER WORD START
         MVC   HYTESTAR,BLANKS     BLANK WORD AREA
         LA    PNEW,HYTESTAR+1     START HERE
         LA    R0,HYTESTAR+L'HYTESTAR   LAST POSSIBLE ENTRY
         SR    C,C                 ZERO LENGTH OF WORD
FOHY06   DS    0H
         LTR   P,P                 AT THE END .Q
         BZ    FOHY08              BRANCH OUT IF YES
         CLI   LINKCHAR,C'-'       USER HYPHEN .Q
         BE    FOHY07              YES, LET THAT THROUGH
         CLI   LINKCHAR,C'A'-C' '  LESS THAN LOWER 'A' .Q
         BL    FOHY08              IF YES, END OF WORD
         CLI   LINKCHAR,C'Z'       MORE THAN UPPER 'Z' .Q
         BH    FOHY08              IF YES, END OF WORD
FOHY07   DS    0H
         CLI   LINKMULT,X'01'      ONLY ONE CHAR .Q
         BNE   FOHY14              NO, CANNOT HYPHENATE WORD
         LA    C,1(,C)             BUMP LENGTH OF WORD
         MVC   0(1,PNEW),LINKCHAR  ENTER WORD
         LA    PNEW,1(,PNEW)       NEXT CHAR
         CR    PNEW,R0             STILL OK .Q
         BNL   FOHY14              NO, CANNOT HYPHENATE WORD
         L     P,LINKFOR           NEXT CHARACTER PTR
         B     FOHY06              CONTINUE END SEARCH
FOHY08   DS    0H
         LR    R0,C                LENGTH OF WORD
         SH    R0,HYENDPT          LESS HYPHEN END POINT
         S     B,F2                LESS BLANK BEFORE AND HYPHEN AFTER
         CR    B,R0                COMPARE THE TWO
         BNH   FOHY09              BRANCH IF SPACE LIMITS
         LR    B,R0                ENDPT LIMITS
FOHY09   DS    0H
         LTR   B,B                 TEST THE RESULT
         BNP   FOHY14              BRANCH IF NOTHING CAN BE DONE
         STC   B,HYTESTAR          LAST TO FIT POINT
         C     C,F5                WORD LENGTH FIVE OR MORE .Q
         BL    FOHY14              IF NOT, DON'T EVEN TRY
         LH    R1,HYMINPT          START MINIMUM
         AH    R1,HYENDPT          PLUS END MINIMUM
         CR    C,R1                WORD LONGER THAN MINIMUMS .Q
         BL    FOHY14              BRANCH OUT IF NOT
         LA    R1,HYTESTAR         POINT TO INPUT WORK AREA
        $CALL  HYPHEN              DO HYPHENATION
         LTR   R15,R15             SUCCESSFUL REQUEST .Q
         BNP   FOHY14              BRANCH IF ERROR
         LA    P,0(,POLD)          FIRST LETTER OF WORD PTR
         LH    R0,HYMINPT          MINIMUM CHARS ON THIS LINE
         B     FOHY11              SKIP OVER MINPT CHARACTERS
FOHY10   DS    0H
         L     P,LINKFOR           FIRST CHAR MAY NOT BE ORPHANED
         LA    R15,1(,R15)         SECOND STATUS INDICATOR BYTE
         S     B,F1                LESS FIRST CHAR LENGTH
         BNP   FOHY14              BRANCH IF NONE LEFT
FOHY11   DS    0H
         S     R0,F1               ONE LESS CHAR LEFT
         BP    FOHY10              CONTINUE IF WE MUST
         SH    C,HYENDPT           LESS END MINIMUM
         BNP   FOHY14              BRANCH IF NOTHING LEFT
         CR    B,C                 DON'T ORPHAN LAST 'ENDPT' CHARS
         BNH   FOHY12              BRANCH IF OK
         LR    B,C                 ELSE TAKE THE MIN
FOHY12   DS    0H
         TM    0(R15),X'30'        DOES HYPHEN FOLLOW LETTER .Q
         BO    FOHY13              BRANCH IF YES
         LA    R15,1(,R15)         NEXT STATUS RETURN BYTE
         L     P,LINKFOR           NEXT CHARACTER
         BCT   B,FOHY12            AND TRY AGAIN
         B     FOHY14              FAILURE
FOHY13   DS    0H
         ST    P,WRKBRK            SET BREAK POINTER FOR WORD
         LA    R15,HYUSER          LET USER HYPHENATION TAKE OVER
         B     FOHY15              GO TO RETURN
FOHY14   DS    0H
         MVC   HYSUPWRK,HYSUPCNT   RESET NUMBERED HYPHENATION SUPPRESS
         LA    R15,HYNONE          LET NO HYPHENATION TAKE OVER
FOHY15   DS    0H
        $EXIT  RC=(R15)            AND RETURN TO CALLER
         SPACE
         DROP  P
