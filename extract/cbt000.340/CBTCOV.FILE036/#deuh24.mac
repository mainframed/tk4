*                       ********
* *      SOURCE BOOK *** DEUH24 *** PROBABILITY ALGORITHM (HALGO)
*                       ********
         SPACE 3
HALGO   $ENTER ,
*        WAS   DEUHYH01
         SPACE
*
*
*
*                            HALGO
*
*
*             ANY TIME THE -HYPHEN-MODULE FAILS TO FIND ALL THE
*        DIVISION POINTS OF A WORD EITHER BY SUFFIX ANALYSIS OR
*        EXCEPTION WORDS, THE - HALGO - PHASE IS EXECUTED.
*
*              THE PURPOSE OF THIS PHASE IS TO GENERATE THE
*        PROBABILITY FOR HYPHEN POINTS AT ALL REMAINING POINTS IN THE
*        WORD, AND TO SELECT THE HIGHEST VALUES BETWEEN NON-
*        CONTIGUOUS VOWELS AS HYPHEN POINTS. IN THE CASE OF CONTIGUOUS
*        VOWELS, THE POINT IS SELECTED IF THE VALUE IS GREATER THAN
*        A MINIMUM.
*
*              ALL PROBABILITIES ARE FOUND BY UTILIZING SIX TABLES
*        LOCATED IN CORE MEMORY. THEIR FUNCTIONS ARE SUMMARIZED BELOW-
*
*        TABLE      SIZE           GIVES PROBABILITY OF
*        *****     *****           ********************
*
*        BXHTB      1X26           A HYPHEN POINT AFTER THE BEGINNING
*                                  CHARACTER IN A WORD.
*        EHXTB      1X26           A HYPHEN POINT BEFORE THE TERMINAL
*                                  CHARACTER IN A WORD.
*        BXXHTB    26X26           A HYPHEN POINT OCCURRING AFTER THE
*                                  FIRST TWO CHARACTERS IN A WORD.
*        HXXTB     26X26           A HYPHEN POINT OCCURRING BEFORE ANY
*                                  TWO CHARACTERS INTERNALLY IN A WORD.
*        XHXTB     26X26           A HYPHEN POINT OCCURRING BETWEEN ANY
*                                  TWO CHARACTERS INTERNALLY IN A WORD.
*        XXHTB     26X26           A HYPHEN POINT OCCURRING AFTER ANY
*                                  TWO CHARACTERS INTERNALLY IN A WORD.
*
*              NOTE THAT ALL OF THESE TABLES ARE ARRANGED ACCORDING TO
*        THE HYPHENATION FREQUENCY CODING, AND ARE ADDRESSED IN A
*        MANNER SUCH THAT THE FIRST LETTER OF THE TWO-CHARACTER SET
*        CORRESPONDS TO THE Y-AXIS, AND THE SECOND TO THE X-AXIS. THIS
*        SCHEME PERMITS THE USE OF SIMPLE RELATIVE ADDRESSING
*        TECHNIQUES IN ACCESSING THE TABLES.
*
*              THE TABLES CONSIST OF ONE-BYTE BINARY NUMBERS.
*
*              - HALGO- BEGINS BY GENERATING THE RELATIVE TABLE
*        ADDRESSES FOR ALL TWO-CHARACTER COMBINATIONS IN THE WORD.
*        SINCE THE PROBABILITY TABLES ARE ARRANGED IN FREQUENCY-CODING
*        ORDER, THIS CAN BE ACCOMPLISHED BY MULTIPLYING THE LEFTMOST
*        CHARACTER BY 26 AND ADDING THE RIGHTMOST CHARACTER.
*
*              ANALYSIS THEN PROCEEDS BY MOVING ACROSS THE WORD FROM
*        LEFT TO RIGHT, GENERATING THE ACTUAL PROBABILITY VALUE
*        ADDRESSES BY ADDING THE RELATIVE ADDRESS TO THE APPROPRIATE
*        SET OF BASE TABLE ADDRESSES AND COMPUTING THE RESULTING
*        PROBABILITIES.
*              AFTER ALL THE PROBABILITIES ARE GENERATED, THE - SELHYP-
*       ROUTINE SELECTS AND MARKS A POINT IN EACH HYPHEN FIELD WHICH
*       HAS THE HIGHEST PROBABILITY VALUE WITHIN THE HYPHEN FILED. AN
*       EXIT IS THEN MADE TO -HYPHEN-.
*
*
*
*
*
*  REGAB USUALLY CONTAINS A VALUE OF 26*C1+C2, WHERE C1 AND C2 ARE A
*        PAIR OF CONSECUTIVE LETTERS IN A WORD.  IT IS USED TO POINT
*        TO THE PROBABILITY FOR A HYPHEN AFTER A PAIR OF CHARACTERS
*        (XX-).  IT IS INITIALLY SET TO POINT TO A SPECIAL TABLE FOR
*        HYPHENS AFTER THE FIRST LETTER IN A WORD (WHEN C1 = 0).
*
*  REGBC WILL CONTAIN THE VALUE OF 26*C2+C3 WHERE C2 AND C3 ARE THE
*        PAIR OF LETTERS FOLLOWING C1.  IT IS USED TO POINT TO THE
*        PROBABILITY FOR A HYPHEN BETWEEN A PAIR OF CHARACTERS (X-X).
*
*  REGCD WILL USUALLY CONTAIN THE VALUE OF 26*C3+C4 WHERE C3 AND C4 ARE
*        THE PAIR OF LETTERS FOLLOWING C2.  IT IS USED TO POINT TO THE
*        PROBABILITY FOR A HYPHEN PRECEEDING A PAIR OF LETTERS (-XX).
*        IT IS ULTIMATELY SET TO POINT TO A SPECIAL TABLE FOR HYPHENS
*        PRECEEDING THE LAST LETTER IN A WORD (C4=0).
*
*  REGCHR CONTAINS THE NEXT LETTER IN THE WORD (C5).
*
*   AFTER COMPUTING A HYPHEN POINT VALUE, THE FOLLOWING STEPS ARE
*        EXECUTED FOR THE FOUR REGISTERS ABOVE
*              1.  LOAD REGBC INTO REGAB
*              2.  LOAD REGCD INTO REGBC
*              3.  LOAD REGCHR INTO REGCD
*              4.  LOAD NEXT CHARACTER INTO REGCHR
*              5.  LOAD REGCD*26+REGCHR INTO REGCD
*
*
*  RCCHTB USUALLY CONTAINS THE ADDRESS OF THE TABLE OF PROBABILITIES
*        FOR HYPHENS AFTER LETTER PAIRS(XX-).  INITIALLY CONTAINS THE
*        ADDRESS OF A SPECIAL TABLE OF PROBABILITIES FOR HYPHENS AFTER
*        THE FIRST LETTER OF THE WORD.  IT IS USED IN CONJUNCTION WITH
*        REGAB.
*
*  RCHCTB CONTAINS THE ADDRESS OF THE TABLE OF PROBABILITIES FORHYPHENS
*        BETWEEN LETTER-PAIRS(X-X).  IT IS USED IN CONJUNCTION WITH
*        REGBC.
*
*  RHCCTB  USUALLY CONTAINS THE ADDRESS OF THE TABLE OF PROBABILITIES
*        FOR HYPHENS BEFORE LETTER-PAIRS (-XX).  FINALLY CONTAINS THE
*        ADDRESS OF A SPECIAL TABLE OF PROBABILITIES OF HYPHENS BEFORE
*        THE LAST LETTER IN THE ROOT WORD.  IT IS USED IN CONJUNCTION
*        WITH REGCD.
*
*  REGPRO  IS USED TO DEVELOP THE PRODUCT OF THE THREE PROBABILITIES.
*
*  REGIX1  IS THE INDEX TO THE NEXT LETTER IN THE WORD TO BE PROCESSED.
*        IT ALSO CONTROLS THE ADDRESSES LOADED INTO RCCHTB AND RHCCTB.
*        THE HYPHEN POINT BEING PROCESSED IS AT REGIX1-2.
*
*
*
*
*
REGPRO   EQU   1
RCCHTB   EQU   2
REGBST   EQU   2
RCHCTB   EQU   3
REGIX2   EQU   3
REGBC    EQU   4
REGCD    EQU   5
REGSYL   EQU   7
RHCCTB   EQU   7
REGAB    EQU   8
REGCHR   EQU   9
YES      EQU   X'30'
YESHLGO  EQU   X'F0'
*
*
         EJECT
*
*
         LA    RCCHTB,BXHTB        LOAD TABLE ADDRESSES
         LA    RCHCTB,XHXTB
         LA    RHCCTB,HXXTB
*
         SR    REGBC,REGBC         CLEAR THE REGISTER
         IC    REGBC,HYFCOM        GET THE FIRST CHARACTER
         LR    REGCD,REGBC
         MH    REGCD,BIN26+2       MLTPLY BY 26 FOR Y AXIS
         SR    REGCHR,REGCHR       CLEAR CHARACTER REGISTER
         IC    REGCHR,HYFCOM+1     GET 2ND CHARACTER
         AR    REGCD,REGCHR        ADD ITS VALUE FOR X AXIS
         LA    REGIX1,2            INITIALIZE INDEX REGISTER
         SR    REGWK0,REGWK0       CLEAR THE WORK REGISTER
         ST    REGWK0,PROBS
         MVC   PROBS+4(L'PROBS*28),PROBS    CLEAR PROB. SAVE AREA
NXTPRB   LR    REGAB,REGBC         TO GET AT NEXT DIGRAPH
         LR    REGBC,REGCD         SLIDE THE REGISTERS
         LR    REGCD,REGCHR
         EX    REGIX1,CMPLMT
         BL    CKSTAT                   BYPASS BUILDING OF LAST DIGRAPH
         IC    REGCHR,HYFCOM(REGIX1)
         MH    REGCD,BIN26+2       TO GET Y AXIS
         AR    REGCD,REGCHR        TO GET X AXIS
CKSTAT   LA    REGPRO,ASTATUS-2(REGIX1) GET ADDRESS OF POINT IN STATUS
         TM    0(REGPRO),YES            IS IT POSSIBLE
         BM    GTPRBS                   BRANCH IF IT IS.
         LNR   REGPRO,REGPRO            IF NOT - SET PROB. VALUE TO NEG
         B     STPRBS                   AND GO STORE IT.
*
*
* THIS ROUTINE CALCULATES THE PRODUCT OF THE PROBABILITIES OF THE THREE
* DIGRAPHS ABOUT A HYPHEN POINT.
*
GTPRBS   SR    REGPRO,REGPRO       CLEAR PROBABILITY REGISTER
         IC    REGWK0,0(REGCD,RHCCTB) GET PROBABILITY OF -XX
         STH   REGWK0,SAVEODD+2    SAVE FOR MULTIPLICATION
         IC    REGWK0,0(REGBC,RCHCTB) GET PROBABILITY OF X-X
         STH   REGWK0,SAVEODD      SAVE FOR MULTIPLICATION
         IC    REGPRO,0(REGAB,RCCHTB) GET PROBABILITY OF XX-
         MH    REGPRO,SAVEODD      TO GET PRODUCT OF  A X B
         MH    REGPRO,SAVEODD+2    TO GET PRODUCT OF  (AXB) X C
STPRBS   SLL   REGIX1,2 BECAUSE PROBABILITY SAVE AREAS ARE FULL WORDS
         ST    REGPRO,PROBS-8(REGIX1) SAVE THE PROBABILITY
         SRL   REGIX1,2            RESTORE TO ORIGINAL INDEX VALUE
         EX    REGIX1,CMPLMT       IF ALL PROBS ARE STORED,
         BL    SELHYP         GO TO ROUTINE TO SELECT THE BEST POINTS
         LA    REGIX1,1(0,REGIX1)  INCREMENT THE INDEX REGISTER
         C     REGIX1,BIN4 DID WE PASS THE BEGINNING OF THE WORD
         BL    BXHDUN              NOT QUITE - ONE MORE TO GO
         BE    BXXHDN              NOW WE DID
         EX    REGIX1,CMPLMT       DID WE REACH THE END OF THE WORD
         BNL   NXTPRB              NO, MORE TO GO
         LA    RHCCTB,EHXTB
         B     NXTPRB
BXHDUN   LA    RCCHTB,BXXHTB
         B     NXTPRB
BXXHDN   LA    RCCHTB,XXHTB
         B     NXTPRB
*
CMPLMT   CLI   NUMCHR+1,X'00'
*
*
*  THE FOLLOWING SUBROUTINE SELECTS THE BEST HYPHEN POINT BETWEEN
*  VOWELS.  CONTIGUOUS VOWELS ARE TREATED UNIQUELY.
*
SELHYP   CLC   FIRSTVOW,NUMCHR     ANY VOWELS BEFORE LAST LETTER
         BNL   HEXIT               IF NO,EXIT HALGO
         LA    REGWK2,255
         STH   REGWK2,BSTPNT       INITIALIZE BSTPNT & LSTBST
         LA    REGNCR,1
         LH    REGIX1,FIRSTVOW     SET POINTERS TO FIRST VOWEL
         LH    REGLMT,LASTVOW      PROCESS ONLY AS FAR AS LAST VOWEL
         LR    REGIX2,REGIX1
         SLL   REGIX2,2            MLTPLY BY 4,PROBS ARE FULL WORDS
CPRPROB1 L     REGBST,PROBS(REGIX2)
         LTR   REGBST,REGBST       IS PROBABILITY NEGATIVE
         BM    SCNPRB              BR IF YES
         STC   REGIX1,BSTPNT       SAVE PROBABILITY AS BEST SO FAR
SCNPRB   LA    REG1,ASTATUS(REGIX1) GET NEXT CHARACTER
         TM    1(REG1),X'01'       IS IT A VOWEL
         BZ    CPRPROB             BRANCH IF NOT
         TM    0(REG1),X'01'       IS PREVIOUS CHAR A VOWEL
         BZ    SETHYP              BRANCH IF NO
         L     REG1,MINPRB
         C     REG1,PROBS(REGIX2)  IS PROBABILITY ABOVE MINIMUM
         BH    SETHY1              BR IF NO
SETHYP   IC    REGWK2,BSTPNT       GET ADDRESS OF BEST POINT
         LA    REG1,ASTATUS(REGWK2)
         CLC   BSTPNT,LSTBST       SAME AS PREVIOUS BEST POINT
         BE    SETHY1              BRANCH IF YES
         TM    0(REG1),YES         IS POINT A -NO- IN STATUS
         BZ    SETHY1              BRANCH IF IT'S A NO  IN STATUS
         OI    0(REG1),YESHLGO     IF NOT,SET IT TO YES STATUS
         STC   REGWK2,LSTBST       STORE POINT AS PREV. BEST POINT
SETHY1   LNR   REGBST,REGBST       SET BEST SO FAR TO NEGATIVE
CPRPROB  BXH   REGIX1,REGNCR,HEXIT BRANCH IF ALL CHARACTERS SCANNED
         LA    REGIX2,4(0,REGIX2)  GET NEXT PROBABILITY
         C     REGBST,PROBS(REGIX2)     IS IT HIGHER THAN BEST SO FAR
         BNL   SCNPRB
         B     CPRPROB1
*
* HALGO EXITS AFTER RECORDING --ALL-- HYPHEN POINTS IN ROOT WORD.
*
HEXIT    DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE
PROBS    DS    29F
MINPRB   DC    F'125000'
*
*
         DS    0H
BSTPNT   DS    CL1                 MUST BE HALF-WORD BOUNDARY
LSTBST   DS    CL1
         LTORG
