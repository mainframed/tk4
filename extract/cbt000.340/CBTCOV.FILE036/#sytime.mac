***********************************************************************
*                                                                     *
*              'SYTIME' --- SYSTEM TIME OF DAY                        *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  $CALL  SYTIME
*
*        ON EXIT   --  (R0)  HAS TIME IN 'HHMMSS00'
*                      (R1)  HAS DATE IN '00YYDDDF'
         SPACE
SYTIME  $ENTER ,
         L     R15,=V(SYSINT)      SYSTEM DEPENDENT ROUTINE
         BAL   R14,76(,R15)        LET HIM DO IT
         SPACE
         LR    R2,R0               TIME IN TIMER UNITS
         SRDL  R2,32               PREPARE FOR A DIVIDE
         D     R2,=F'38400'        TIME IN SECONDS
         LR    R2,R3               SECONDS
         SRDL  R2,32               PREPARE FOR A DIVIDE
         D     R2,=F'60'           SECONDS IN R2, MINUTES IN R3
         LR    R0,R2               SAVE SECONDS
         LR    R2,R3               MINUTES
         SRDL  R2,32               PREPARE FOR DIVIDE
         D     R2,=F'60'           MINUTES IN R2, HOURS IN R3
         MH    R2,=H'100'          SCALE MINUTES BY 100
         AR    R0,R2               MINUTES,SECONDS IN BINARY
         MH    R3,=H'100'          SCALE HOURS BY 100
         MH    R3,=H'100'          AND AGAIN BY 10000
         AR    R0,R3               HOURS,MINUTES,SECONDS IN BINARY
         CVD   R0,DECM             CONVERT TO DECIMAL
         L     R0,DECM+4           GET '0HHMMSSF'
         SLL   R0,4                GET 'HHMMSSF0'
        $EXIT  RC=0,KEEP=(R0,R1)   RETURN TO CALLER
         SPACE
         LTORG ,
