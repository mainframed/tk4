***********************************************************************
*                                                                     *
*              .PE (PERFORM) --- CONTROL                              *
*                                                                     *
***********************************************************************
         SPACE
PE      $ENTER ,
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BOMIT,PE01          BRANCH IF OMITTED
         BC    BPOS,PE02           BRANCH IF AFFIRMATIVE
         BC    BNEG,PE03           BRANCH IF NEGATIVE
         CLI   YNRESULT,YNDEL      IS IT DELETE .Q
         BE    PE04                BRANCH IF YES
         ST    COUNT,ARGSTART      RESET START OF SCAN
        $CALL  GETNUM              ARGUMENT BETTER BE NUMERIC
         BC    BDNO,PEE005         ERROR IF INVALID
         BC    BNEG,PEE014         ERROR IF NEGATIVE
         B     PE05                ENTER THE VALUE
         SPACE
PE01     DS    0H
         LA    COUNT,1             ONE TIME BY DEFAULT
         B     PE05                ENTER THE VALUE
PE02     DS    0H
         LH    COUNT,H32767        LARGE POSITIVE VALUE
         B     PE05                ENTER THE VALUE
PE03     DS    0H
         SR    COUNT,COUNT         ZERO TO PURGE THIS LINE
         B     PE05                ENTER THE VALUE
PE04     DS    0H
         LA    R1,PEANCH           POINT TO PERFORM STACK ANCHOR
        $CALL  OVCLR               DELETE ANY AND ALL ENTRIES
         B     PE06                TERMINATE THIS LINE
         SPACE
PE05     DS    0H
         SR    R0,R0               ZERO WORK REGISTER
         LTR   COUNT,COUNT         ZERO PERFORM ARG .Q
         BE    PE06                YES, TERMINATE THIS LINE
         SPACE
        $CALL  READ                GET NEXT INPUT LINE FOR OPERAND
         LR    X1,R1               POINT TO INPUT LINE
         LH    R1,0(,X1)           LENGTH OF LINE
         LA    R1,OOQLEN(,R1)      PLUS OVERHEAD
        $CALL  GETCELL             GET SOME SPACE
         BC    BNEG,PEE024         ERROR IF NO SPACE
         USING OODSECT,TEMP        AREA LOOKS LIKE THIS
         XC    OODSECT(OOQLEN),OODSECT  CLEAR THE PREFIX
         ST    COUNT,OOQCNT        SET CALL COUNT
         MVC   OOQFCHN,PEANCH      CHAIN TO NEXT ELEMENT
         ST    TEMP,PEANCH         ANCHOR POINTS TO NEW FIRST
         LH    R1,0(,X1)           LENGTH OF DATA
         STH   R1,OOQRDW           SET LENGTH OF DATA
         BCTR  R1,0                LENGTH-1
         EX    R1,PEMVC            ENTER DATA
         B     PE06                AND CONTINUE
         SPACE
PEMVC    MVC   OOQDATA(*-*),4(X1)  ENTER PERFORM DATA LINE
         DROP  TEMP
         SPACE
PE06     DS    0H
         L     R1,HIDBANCH         REST OF CURRENT BUFFER
         LTR   R1,R1               ANY PRESENT .Q
         BZ    PE07                BRANCH IF NONE
         USING HIDB,R1             TELL THE ASSEMBLER
         STH   R0,HIDBLEN          CLEAR DATA LEFT IN LINE
         DROP  R1
PE07     DS    0H
        $EXIT  RC=0                CONTINUE WITH NEXT LINE
         SPACE
PEE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     PE07                GO TO RETURN
PEE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     PE07                GO TO RETURN
PEE024   DS    0H
         MVI   EFLAG024,ERRSET     NO STORAGE FOR PERFORM LINE
         B     PE07                GO TO RETURN
