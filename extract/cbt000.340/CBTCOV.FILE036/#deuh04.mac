*                             ********
* *            SOURCE BOOK *** DEUH04 *** ALPHA CHARACTER PROCESSOR
*                             ********                             1-1
         SPACE 3
*****  NON ALPHA SCAN CHARACTER PROCESSING ROUTINES
ALPH     TM    HYFSW,X'01'         TEST FOR PRIOR NUMBERS
         BO    REJWDSG             REJECT MIXED ALPHA-NUMERIC
         LA    REG1,GSTATUS(REGWK2) DEVELOP ACTUAL STATUS BYTE ADDRESS
         TM    HYFSW,X'06'         TEST FOR FIRST SIGNIF.ALPHA CHAR.
         BO    BYFST                & BRANCH IF THIS IS NOT 1ST
         ST    REG1,BGWDSG         SAVE ADDR. OF BEGINNING OF WORD
BYFST    BAL   REGRET,TESTSCND
         TM    SCNDSW,X'02'        TEST FOR ALPHA SECONDARY CHARACTER
         BZ    BYPOS                IF NOT-BYPASS SETTING POSS.INDIC.
         OI    0(REG1),X'10'       ELSE SET STATUS TO REFLECT POS.HYFN
BYPOS    OI    HYFSW,X'06'         INDICATE SIGNIF.ALPHA CHARACTER
         ST    REG1,LSIG           SAVE ADDR. OF LAST SIGNIF CHAR.
         LA    REGWK1,ASTATUS(REGWK3)
         MVC   0(1,REGWK1),0(REG1)      MOVE GSTATUS BYTE TO ASTATUS
         LA    REGWK1,HYFCOM(REGWK3)
         MVC   0(1,REGWK1),0(REG3)      MOVE CHARACTER TO HYFCOM
         LA    REGWK3,1(REGWK3)    INCREMENT HYFCOM/ASTATUS POINTER
         BCT   REGWK0,NULL         IF WORD SEGMENT UNDER LIMIT, GO GET
         B     HREJ                 NEXT CHAR. ELSE, REJECT THE WORD
* END OF ROUTINE ALPH FIR NON-ALPHA PROCESSING OF CHARS.
         SPACE 6
