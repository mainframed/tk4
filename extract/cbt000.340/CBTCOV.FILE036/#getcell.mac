***********************************************************************
*                                                                     *
*              INTERNAL ROUTINE 'GETCELL' --- GET QUEUE BUFFER        *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY - R1      LENGTH TO BE GOT                          *
*                 - RETURN  RETURN ADDRESS                            *
*        ON EXIT  - TEMP    POINTER TO GOT AREA                       *
*                 - CC=BPOS IF SUCCESSFUL                             *
*                 - CC=BNEG IF FAILED                                 *
         SPACE
GETCELL $ENTER ,
         L     X1,RGLISTAD         REGION MANAGER PARM ADDRESS
         USING RGLIST,X1           TELL THE ASSEMBLER
         LR    COUNT,R1            REMEMBER LENGTH REQUIRED
         CLI   PRGFLAG,PRGINIT     INITIAL CALL DONE .Q
         BNE   GETC02              BRANCH IF YES
         LA    R1,RGLIST           POINT TO PARM LIST
        $CALL  RGNMGR              INITIALIZE REGIONS
         BC    BPOS,GETC02         BRANCH IF SUCCESS
GETC01   DS    0H
         SR    TEMP,TEMP           ZERO RETURN ADDRESS
        $CC    NEG                 SET CONDITION CODE
         B     GETC04              AND BRANCH TO RETURN
         SPACE
GETC02   DS    0H
         ST    COUNT,PCELLLEN      SET GET LENGTH
         XC    PCELLPTR,PCELLPTR   THIS IS A GET
         LA    R1,RGLIST           PARM LIST
        $CALL  RGNMGR              TRY FOR AN ALLOTMENT
         BC    15-BPOS,GETC01      BRANCH IF FAILED
         L     TEMP,PCELLPTR       SET AREA POINTER
         LA    TEMP,0(,TEMP)       CLEAR REGION EXTENT NUMBER
         L     COUNT,PCELLLEN      GET USER'S LENGTH
         S     COUNT,F2            LENGTH-1 OF REMAINDER FOR MVC
         MVI   0(TEMP),X'7D'       DIRTY REGION GOTTEN
         EX    COUNT,GETCMVC       ALL OF IT
         SPACE
         L     R1,PRGUSED          FORMER SPACE FROM REGION
         A     R1,PCELLLEN         PLUS AREA JUST GOT
         ST    R1,PRGUSED          SET CURRENT SPACE USED
         DROP  X1
         C     R1,PRGHIGH          NEW HIGH WATER MARK .Q
         BNH   GETC03              BRANCH IF NOT
         ST    R1,PRGHIGH          SET NEW HIGH WATER MARK
GETC03   DS    0H
        $CC    POS                 SET CONDITION CODE
         SPACE
GETC04   DS    0H
        $RESTOR KEEP=(TEMP)        RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
         SPACE
GETCMVC  MVC   1(*-*,TEMP),0(TEMP) DIRTY ALL OF GOTTEN REGION
