***********************************************************************
*                                                                     *
*        INTERNAL FUNCTION 'OVREMOV' --- REMOVES ONE OVERLAY BUFFER   *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER:   $CALL  OVREMOV
*                    (R1) -> OVERLAY ANCHOR ELEMENT
*
*        ON EXIT:   CC=BPOS   OPERATION SUCCESSFUL
*                   CC=BOMIT  OPERATION SUCCESSFUL, LIST EMPTY
*                   CC=BNEG   NO BUFFER TO BE FREED
         SPACE
OVREMOV $ENTER ,
         LR    X1,R1               OLD POINTER
         LR    TEMP,R1             WORKING POINTER
         USING OODSECT,TEMP        NEXT POINTER
         L     TEMP,OOQFCHN        NEXT ON LIST
         LTR   R0,TEMP             AT THE END .Q
         BZ    OVR02               BRANCH IF YES
         LA    R1,OOQLEN           PREFIX LENGTH
         AH    R1,OOQRDW           PLUS DATA LENGTH
         L     TEMP,OOQFCHN        NEXT FORWARD POINTER
         DROP  TEMP
         ST    TEMP,OOQFCHN-OODSECT(,X1)     UNCHAIN FIRST ELEMENT
        $CALL  FREECELL            FREE FIRST OVERLAY ELEMENT
         LTR   TEMP,TEMP           ANY MORE .Q
         BP    OVR01               BRANCH IF YES
         SPACE
        $CC    OMIT                SUCCESS, LIST IS EMPTY
         B     OVR03               GO TO RETURN
OVR01    DS    0H
        $CC    POS                 SUCCESS
         B     OVR03               GO TO RETURN
OVR02    DS    0H
        $CC    NEG                 NO BUFFER TO FREE
         SPACE
OVR03    DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
         SPACE 2
OVCLR   $ENTER ,
         SPACE
*        TO DELETE ALL OVERLAY ELEMENTS
*        ON ENTRY --  (R1) -> OVERLAY ANCHOR ELEMENT
*        ON EXIT  --  CC=BOMIT   OPERATION SUCCESSFUL
*                     CC=BNEG    NO BUFFER TO BE FREED
         SPACE
OVC01    DS    0H
        $CALL  OVREMOV             DELETE FIRST LEVEL
         BC    BNEG,OVC02          BRANCH IF NONE
         BC    BOMIT,OVC02         BRANCH IF DONE
         B     OVC01               ELSE DO NEXT ONE
         SPACE
OVC02    DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
