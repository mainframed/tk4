***********************************************************************
*                                                                     *
*        INTERNAL FUNCTION 'ARGINIT' --- INITIALIZE OPERAND SCAN      *
*                                                                     *
***********************************************************************
         SPACE
*        ON EXIT --- (COUNT=ADDRESS)                                  *
*                    (TEMP=LENGTH-1)                                  *
         SPACE
ARGINIT $ENTER ,                   SKIP PAST CONTROL WORD
         LH    R1,BUFF2LG-1        GET LENGTH OF CONTROL LINE
         LA    R1,BUFF2-1(R1)      ADDRESS OF LAST CHARACTER
         LA    R0,1                INCREMENT FOR BXLE
         LA    COUNT,BUFF2         ADDRESS OF FIRST CHARACTER
         LR    TEMP,COUNT          WORKING POINTER FOR SCAN
         SPACE
INIT01   DS    0H
         CLI   0(TEMP),C' '        LOCATE BLANK AFTER CONTROL WORD
         BE    INIT02              IF FOUND, SKIP AHEAD
         BXLE  TEMP,R0,INIT01      NOT YET, KEEP TRYING
         SPACE
INIT02   DS    0H
        $OFF   SWITCHS,FNOCASE     OPERANDS TO UPPERCASE BY DEFAULT
         ST    R1,ARGSTOP          STORE SCAN STOP ADDRESS
         ST    TEMP,ARGSTART       STORE ARGUMENT START ADDRESS
         SR    R0,R0               ZERO WORK REGISTER
         ST    R0,OPENPARN         CLEAR OPEN PARENTHESIS POINTER
         ST    R0,CLOSPARN         CLEAR CLOSE PARENTHESIS POINTER
         ST    R0,ERRSTART         ZERO ERROR ADDRESS
         ST    R0,ERRTEXT          CLEAR EXTRA ERROR MESSAGE TEXT PTR
         SR    TEMP,COUNT          LENGTH OF RESULT
         BCTR  TEMP,0              LENGTH-1 OF RESULT
        $RESTOR KEEP=(COUNT,TEMP)  RESTORE CALLER'S REGISTERS
         BR    RETURN              RETURN TO CALLER
