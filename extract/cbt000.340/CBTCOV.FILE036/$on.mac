         MACRO
&LABEL  $ON        &BYTE,&FLAG
.*       BUILT TO HANDLE TEN SWITCHES, BUT RESTRICTED TO SIX
         LCLA      &I,&J,&K,&C(10)
         LCLC      &TC,&A(150)
         AIF       ('&LABEL' EQ '').SKIP01
&LABEL   DS        0H
.SKIP01  AIF       ('&BYTE' EQ '').MNOTE01
&I       SETA      N'&FLAG
         AIF       (&I LT 1 OR &I GT 6).MNOTE02
.SKIP02  ANOP
&TC      SETC      T'&FLAG(&I)
&J       SETA      0
         AIF       ('&TC' LT '0').SKIP03
&J       SETA      &TC
.SKIP03  ANOP
&K       SETA  &J*15+&C(&J+1)+1
         AIF   (&C(&J+1) EQ 0).SKIP04
&A(&K)   SETC  '+'
&C(&J+1) SETA  &C(&J+1)+1
&K       SETA  &K+1
.SKIP04  ANOP
&A(&K)   SETC  '&FLAG(&I)'
&C(&J+1) SETA  &C(&J+1)+1
&I       SETA      &I-1
         AIF       (&I GT 0).SKIP02
&I       SETA      9
.SKIP05  ANOP
&K       SETA  &I*15+1
         AIF   ('&A(&K)' EQ '').SKIP06
         OI    &BYTE.+&I,&A(&K).&A(&K+1).&A(&K+2).&A(&K+3).&A(&K+4).&A(*
               &K+5).&A(&K+6).&A(&K+7).&A(&K+8).&A(&K+9).&A(&K+10)
.SKIP06  ANOP
&I       SETA      &I-1
         AIF       (&I GE 0).SKIP05
         MEXIT
.MNOTE01 MNOTE     4,'$ON  --  MISSING FIRST PARAMETER'
         MEXIT
.MNOTE02 MNOTE     4,'$ON  --  INVALID SECOND PARAMETER'
         MEND
