***********************************************************************
*                                                                     *
*              .LS (LINE SPACING) --- CONTROL                         *
*                                                                     *
***********************************************************************
         SPACE
LS      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BOMIT+BNEG,LS02     OMITTED OR 'NO' IS SINGLE
         BC    BPOS,LS03           'YES' IS DOUBLE
         ST    COUNT,ARGSTART      RESET START OF SCAN
        $CALL  GETNUM              IT IS PROBABLY NUMERIC
         BC    BDNO,LSE005         BRANCH IF INVALID
         CLI   NUMSIGN,C' '        WAS OPERAND UNSIGNED .Q
         BE    LS01                BRANCH IF YES
         AH    COUNT,DSCNT         MAKE RELATIVE CHANGE
LS01     DS    0H
         LTR   COUNT,COUNT         IS OPERAND TOO SMALL .Q
         BM    LSE014              ERROR IF YES
         CH    COUNT,PLZ           IS OPERAND TOO BIG .Q
         BH    LSE013              ERROR IF YES
         STH   COUNT,DSCNT         STORE LINE SPACING COUNT
         B     LS04                GO TO TERMINATE
         SPACE
LS02     DS    0H
         XC    DSCNT,DSCNT         LINE SPACING FACTOR IS ZERO
         B     LS04                GO TO TERMINATE
LS03     DS    0H
         MVC   DSCNT,H1            LINE SPACING FACTOR IS ONE
         B     LS04                GO TO TERMINATE
         SPACE
LS04     DS    0H
        $EXIT  RC=0                AND RETURN
         SPACE
LSE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     LS04                GO TO TERMINATE
LSE013   DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO BIG
         B     LS04                GO TO TERMINATE
LSE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     LS04                GO TO TERMINATE
