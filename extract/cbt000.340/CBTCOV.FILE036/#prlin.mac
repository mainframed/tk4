***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'PRLIN' --- ENTER LINE/LEGAL         *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R0) POINTS TO OUTPUT RECORD
*                      (R1) OFFSET IN DATA FOR RESULT
         SPACE
PRLIN   $ENTER ,
        $IFON  SWITCHS,FOVLY,PRLIN03    BRANCH IF OVERLAY LINE
        $CALL  PRTARG              GET TARGET FOR DATA
         CLC   0(L'PRLINED,R1),BLANKS   A CLEAN TARGET AREA .Q
         BNE   PRLIN03             NO, FORGET LINE NUMBER
         LH    X1,PLZ              GET PAGE LENGTH
         SH    X1,PLCT             LESS REMAINDER OF PAGE
         SPACE
        $IFOFF SWITCHS,OUSWS,PRLIN02    BRANCH IF NOT LEGAL NUMBERING
         CH    X1,TOPMRG           WITHIN TOP MARGIN .Q
         BNH   PRLIN03             YES, DO NOT NUMBER
         CLC   PLCT,BMMRG          WITHIN BOTTOM MARGIN .Q
         BL    PRLIN03             YES, DO NOT NUMBER
         LR    X1,R0               POINT TO RECORD RDW
         LH    X2,0(,X1)           GET LENGTH, INCL RDW
         LA    X1,4(,X1)           POINT PAST RDW
         S     X2,F5               LENGTH-1 LESS RDW
        $IFON  TTYPE,TONLINE,PRLIN01    BRANCH IF ONLINE
         LA    X1,1(,X1)           POINT PAST CARRIAGE CONTROL
         BCTR  X2,0                DECREMENT LENGTH-1
PRLIN01  DS    0H
         EX    X2,PRLINCLC         DATA ALL BLANK .Q
         BE    PRLIN03             YES, DO NOT NUMBER
         L     X1,OUTSEQ           GET LAST LEGAL LINE SEQUENCE
         LA    X1,1(,X1)           BUMP BY ONE
         ST    X1,OUTSEQ           SAVE CURRENT VALUE
         SPACE
PRLIN02  DS    0H
         CVD   X1,DECM             CONVERT NUMBER TO DECIMAL
         MVC   0(L'PRLINED,R1),PRLINED  MOVE IN EDIT PATTERN
         ED    0(L'PRLINED,R1),DECM+6   EDIT IN LINE NUMBER
         LA    R1,L'PRLINED(,R1)   END OF FIELD
         LR    X2,R0               THIS IS RECORD START
         SR    R1,X2               LENGTH WHERE FIELD ENDED
         SR    X1,X1               ZERO WORK REGISTER
         IC    X1,1(,X2)           CURRENT RECORD LENGTH
         CR    R1,X1               IS THIS NOW LONGER .Q
         BNH   PRLIN03             BRANCH IF NOT
         STC   R1,1(,X2)           SET LONGER LENGTH
         SPACE
PRLIN03  DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE
PRLINCLC CLC   0(*-*,X1),BLANKS    COMPARE FOR BLANK DATA
PRLINED  DC    X'40202120'         LINE NUMBER EDIT PATTERN
