***********************************************************************
*                                                                     *
*              .HM (HEADING MARGIN) --- CONTROL                       *
*                                                                     *
***********************************************************************
         SPACE
HM      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETNUM              GET NUMBER
         BC    BDNO,HM03           ERROR IF INVALID
         BC    BOMIT,HM01          ENTER DEFAULT IF OMITTED
         CLI   NUMSIGN,C' '        WAS ARGUMENT SIGNED .Q
         BE    HM02                BRANCH IF NOT
         AH    COUNT,HDMRG         ALTER EXISTING HEADING MARGIN
         B     HM02                GO CHECK AND ENTER
HM01     DS    0H
         L     COUNT,DEFHDMG+TAB3DEF  DEFAULT HEADING MARGIN VALUE
HM02     DS    0H
         LTR   COUNT,COUNT         IS HEADING MARGIN NEGATIVE
         BM    HM05                ERROR IF YES
         LH    TEMP,TOPMRG         GET TOP MARGIN
         SH    TEMP,HSCNT          LESS HEADING SPACING
         CR    COUNT,TEMP          COMPARE VALUES
         BH    HM04                ERROR IF BIGGER
         STH   COUNT,HDMRG         SET HEADING MARGIN
         B     HM06                BRANCH TO COMMON RETURN
         SPACE
HM03     DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     HM06                BRANCH TO COMMON RETURN
HM04     DS    0H
         MVI   EFLAG041,ERRSET     OPERAND TOO BIG
         B     HM06                BRANCH TO COMMON RETURN
HM05     DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         SPACE
HM06     DS    0H
        $EXIT  RC=0                RETURN TO MAIN CONTROLLER
