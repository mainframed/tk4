***********************************************************************
*                                                                     *
*              .HW (HYPHENATE WORD) --- CONTROL                       *
*                                                                     *
***********************************************************************
         SPACE
HW      $ENTER ,
         LA    X1,HYUSER           USER HYPHENATION CODE
         C     X1,HYLEVEL          COMPARE WITH CURRENT
         BE    HW02                BRANCH IF NOW 'USER'
         L     R1,BUFF3AD          POINT TO STRING
         LA    R1,4(,R1)           SKIP RDW AREA
         MVC   0(B2LG,R1),BLANKS   CLEAR WORK AREA
         MVC   0(L'HWOFF,R1),HWOFF RESTORE HYPHENATION TO OFF
         LA    TEMP,L'HWOFF-1      LENGTH-1 OF STRING
         BH    HW01                BRANCH IF 'OFF'
         MVC   0(L'HWON,R1),HWON   RESTORE HYPHENATION TO ON
         LA    TEMP,L'HWON-1       LENGTH-1 OF STRING
HW01     DS    0H
         MVC   0(L'PERIND,R1),PERIND    CONTROL WORD INDICATOR
        $CALL  HIDBADD             ENTER STRING ON STACK
         BC    BNEG,HWE024         BRANCH IF ERROR
         ST    X1,HYLEVEL          USER HYPHENATION FOR A WHILE
         SPACE
HW02     DS    0H
         L     X1,ARGSTOP          END OF THE LINE
         SR    COUNT,COUNT         ZERO LINE SEGMENT LENGTH
HW03     DS    0H
         BCTR  X1,0                BACKUP POINTER BY ONE
         C     X1,ARGSTART         AT THE BEGINNING .Q
         BL    HW07                BRANCH IF YES
         CLI   1(X1),C'-'          IS CHAR HYPHEN .Q
         BNE   HW06                BRANCH IF NOT
         LTR   COUNT,COUNT         SEGMENT LENGTH ZERO .Q
         BZ    HW06                BRANCH IF YES
         CLI   2(X1),C'Z'          CHAR AFTER ALPHA .Q
         BH    HW06                BRANCH IF NOT
         CLI   2(X1),C'A'-C' '     CHAR AFTER ALPHA .Q
         BL    HW06                BRANCH IF NOT
         CLI   0(X1),C'Z'          CHAR BEFORE ALPHA .Q
         BH    HW05                BRANCH IF NOT
         CLI   0(X1),C'A'-C' '     CHAR BEFORE ALPHA .Q
         BL    HW05                BRANCH IF NOT
         SPACE
        $IFON  SWITCHS,NFSWS,HW04  BRANCH IF NO CONCATENATE
         OC    CECNT,CECNT         ANY .CE/.LA/.RA OUTSTANDING .Q
         BNZ   HW04                BRANCH IF YES
         SPACE
         LA    R1,2(,X1)           POINT TO SEGMENT OF LINE
         LR    TEMP,COUNT          LENGTH OF STRING
         BCTR  TEMP,0              LENGTH-1 OF STRING
        $CALL  HIDBADD             STACK SEGMENT
         BC    BNEG,HWE024         BRANCH IF ERROR
         SR    COUNT,COUNT         ZERO LENGTH
         B     HW06                AND CONTINUE SCANNING
         SPACE
HW04     DS    0H
         EX    COUNT,HWMVC         REMOVE SINGLE HYPHEN
         BCTR  COUNT,0             DECREMENT LENGTH
         B     HW06                AND CONTINUE
         SPACE
HW05     DS    0H
         CLI   0(X1),C'-'          DOUBLE HYPHEN .Q
         BNE   HW06                BRANCH IF NOT
         EX    COUNT,HWMVC         REMOTE DOUBLE HYPHEN
         BCTR  X1,0                BACKUP POINTER ONE
HW06     DS    0H
         LA    COUNT,1(,COUNT)     INCREMENT SEGMENT LENGTH
         B     HW03                AND CONTINUE
         SPACE
HW07     DS    0H
         LTR   COUNT,COUNT         NO OPERAND AT ALL .Q
         BZ    HW08                BRANCH IF YES
         LA    R1,2(,X1)           POINT TO STRING
         LR    TEMP,COUNT          LENGTH OF STRING
         BCTR  TEMP,0              LENGTH-1
        $CALL  HIDBADD             STACK IN INPUT
         BC    BNEG,HWE024         BRANCH IF ERROR
HW08     DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE
HWE024   DS    0H
         MVI   EFLAG024,ERRSET     NO SPACE FOR WORDS
         B     HW08                GO TO RETURN
         SPACE
HWMVC    MVC   1(*-*,X1),2(X1)     REMOVE DOUBLE HYPHEN
         SPACE
HWOFF    DC    C'.HY OFF '
HWON     DC    C'.HY ON  '
