***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'PAGETOP' ---                        *
*                                      CHECK FOR STOP AT PAGE BOTTOM  *
*                                      FORMAT HEADINGS ON NEXT PAGE   *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER:   $CALL  PAGETOP
*
*        ON EXIT:   NORMAL RETURN IF TO CONTINUE
*                   '$CALL QUITS'  IF TO TERMINATE
         SPACE
PAGETOP $ENTER ,
PTOP01   DS    0H
        $OFF   SWITCHS,FEMPTY      PAGE IS NO LONGER EMPTY
         CLI   ICOLFLG,ICOLMC      IS MULTI-COLUMN MODE ON .Q
         BNE   PTOP02              BRANCH IF NOT
         CLC   CDCURR,F1           IN FIRST COLUMN .Q
         BNE   PAOK04              BRANCH AHEAD IF NOT
         MVI   ICOLFLG,ICOLTC      TITLE COLUMN IN PROGRESS
         MVC   CLLZ,LLZ            RESTORE FULL LINE LENGTH
PTOP02   DS    0H
        $IFON  SWITCHS,NPSWS,NOSTOP1    NOPRINT OVERRIDES STOP
         CLC   PASSNO,DEFPASOF+TAB3DEF  IS THIS THE LAST PASS .Q
         BNE   NOSTOP1             NO, THIS OVERRIDES STOP
        $IFON  TTYPE,TFILE,NOSTOP1      'FILE' OUTPUT OVERRIDES
        $IFON  SWITCHS,NOSWS,NOSTOP1    NOWAIT OVERRIDES
        $IFOFF TTYPE,TONLINE,NOSTOP1    BRANCH IF OUTPUT NOT ONLINE
        $IFON  SWITCHS,FFIRST,PTOP03    BRANCH IF FIRST PAGE
        $IFOFF SWITCHS,HITSWS,PTOP03    BRANCH IF NO LOAD MESSAGE YET
        $IFOFF SWITCHS,STSWS,NOSTOP1    TEST FOR 'STOP'
         B     PTOP04              PROMPT FOR CARRIAGE RETURN
         SPACE
PTOP03   DS    0H
         LA    R0,LDMES            MESSAGE TO DISPLAY
        $CALL  PUTCONS             DISPLAY THE RESULT
         B     PTOP04              PROMPT FOR CARRIAGE RETURN
LDMES    DC    AL2(LDMESEND-*,0)
         DC    C'Load paper; hit return:'    'WAIT' MESSAGE
LDMESEND EQU   *
         SPACE
PTOP04   DS    0H
         MVC   CONSMAX,F1          READ ONE LINE FROM CONSOLE
         MVC   CONSGOTO,BLANKS     CLEAR GOTO TARGET
         XC    CONSFLAG,CONSFLAG   CLEAR CONSOLE FLAGS
        $IFOFF SWITCHS,HITSWS,PTOP05    BRANCH IF NOT FIRST TIME
        $ON    CONSFLAG,IO$EJCT    MOVE PAPER UP FOR EASY REMOVAL
PTOP05   DS    0H
        $ON    SWITCHS,HITSWS      NO LONGER FIRST TRY
        $ON    CONSFLAG,IO$IM      IMBED THIS DUMMY TERMINAL READ
        $ON    CONSFLAG,IO$UEOF    RETURN AFTER ANY EOF
         LA    R1,CONSBLOK         TO 'IMBED'  THE CONSOLE FILE
        $CALL  RDFILE              CALL INPUT COORDINATOR
         SPACE
        $CALL  TERMREAD            READ ONE LINE
         CLI   1(R1),X'00'         NULL READ .Q
         BE    NOSTOP1             YES, CONTINUE
         L     X1,ARGSTART         RETAIN ANY SCANNING START
         L     X2,ARGSTOP          RETAIN ANY SCANNING STOP
         LA    R0,4(,R1)           START OF INPUT
         ST    R0,ARGSTART         SET IT FOR SCAN
         AH    R0,0(,R1)           PLUS RECORD LENGTH
         ST    R0,ARGSTOP          FOR END OF SCAN
        $CALL  GETYESNO            WHAT DID USER ENTER .Q
         ST    X1,ARGSTART         RESTORE ANY SCANNING START
         ST    X2,ARGSTOP          RESTORE ANY SCANNING STOP
         BC    15-BNEG,NOSTOP0     CONTINUE ON IF NOT NEGATIVE
         LA    R1,WRETQQ           TERMINATE IMMEDIATELY
        $CALL  QUITS               CALL TERMINATION ROUTINE
         SPACE
NOSTOP0  DS    0H
        $CALL  TERMREAD            FIND OUR OWN EOF
         CLI   1(R1),X'00'         IS RECORD NULL .Q
         BNE   NOSTOP0             NO, TRY AGAIN
         SPACE
NOSTOP1  DS    0H
         LA    R1,SYSTOPRM         TOP OF PAGE REMOTE NAME
        $CALL  RMCOND              TRY IT
         SPACE
         L     X1,ADEVHEAD         ASSUME EVEN PAGE
        $CALL  ODDEVN              EVEN OR ODD PAGE .Q
         BC    BPOS,NOSTOP2        BRANCH IF EVEN
         L     X1,ADODHEAD         POINT TO ODD HEAD
NOSTOP2  DS    0H
         CLC   HSCNT,H1            ONE HEADING ENTRY .Q
         BL    NOSTOP3             BRANCH IF ZERO
         BNE   NOSTOP4             BRANCH IF NOT ONE
         OC    0(L'EVNHEAD,X1),0(X1)    ANY HEADING INFO .Q
         BNZ   NOSTOP4             BRANCH IF NOT NULL
NOSTOP3  DS    0H
         CLC   DEFPAGS+TAB3DEF,F1  ANY PAGE START OPTIONS .Q
         BH    NOSTOP4             IF YES, DO IT IN STEPS
         LH    COUNT,TOPMRG        GET TOP MARGIN
         B     PAOK03              AND BRANCH TO SPACE ALL AT ONCE
         SPACE
NOSTOP4  DS    0H
         LH    COUNT,TOPMRG        SKIP MOST OF TOP MARGIN
         SH    COUNT,HDMRG         EXLUDING HEADING MARGIN
         SH    COUNT,HSCNT         AND HEADING SPACING
         SPACE
        $ON    SWITCHS,OOSWS       DISABLE OUTPUT OVERLAY
        $CALL  SPACER              ..
         SPACE
        $IFOFF SWITCHS,NPSWS,PAOK  BRANCH IF NOT NOPRINT MODE
         L     COUNT,PACNTI        GET INTEGER PAGE NUMBER
         C     COUNT,DEFPAGS+TAB3DEF  THIS THE PAGE TO BE PRINTED .Q
         BNE   PAOK                IF NOT, SKIP
        $OFF   SWITCHS,NPSWS       YES, START PRINTING
        $ON    SWITCHS,FFIRST      FOR HEADING CONTROL
         MVC   PLCT,PLZ            WE HAVEN'T PRINTED ANYTHING, REALLY
         MVC   CDPLCT,PLCT         SET COLUMN COUNT FOR REST OF PAGE
         B     PTOP02              BEGIN WITH STOP AND SPACE
         SPACE
PAOK     DS    0H
         LH    COUNT,HSCNT         GET HEADING SPACE
PAOK01   DS    0H
         S     COUNT,F1            LESS ONE
         BM    PAOK02              BRANCH IF DONE
        $CALL  HEADPRT             PRINT ONE HEADING LINE
         LA    X1,L'EVNHEAD(,X1)   PLUS LENGTH ONE HEADING ENTRY
         B     PAOK01              DO REMAINDER
         SPACE
PAOK02   DS    0H
         LH    COUNT,HDMRG         SPACE HEADING MARGIN
PAOK03   DS    0H
        $ON    SWITCHS,OOSWS       DISABLE OUTPUT OVERLAY
        $CALL  SPACER              SPACE TO TOP OF TEXT AREA
         SPACE
         LA    R1,SYSTMRM          TOP MARGIN REMOTE NAME
        $CALL  RMCOND              TRY IT
         SPACE
         LA    X1,HNEAREA          EVEN HEADNOTE AREA
        $CALL  ODDEVN              CHECK EVEN OR ODD PAGE
         BC    BPOS,PAOK03A        BRANCH IF EVEN
         LA    X1,HNOAREA          ODD HEADNOTE AREA
PAOK03A  DS    0H
         USING CPFKFN,X1           DESCRIPTOR AREA
         OC    CPFKFNC,CPFKFNC     ANY EVEN OR ODD DATA .Q
         BNZ   PAOK03B             BRANCH IF YES
         LA    X1,HNAREA           HEADNOTE AREA
PAOK03B  DS    0H
         LA    R1,CPFKFN           POINT TO DESCRIPTOR AREA
        $CALL  HNPRT               PRINT HEADNOTES, IF ANY
         DROP  X1
         SPACE
         XC    OUTSEQ,OUTSEQ       CLEAR LEGAL LINE SEQUENCE
         MVC   CDPLCT,PLCT         SET COLUMN COUNT FOR REST OF PAGE
         SPACE
         CLI   ICOLFLG,ICOLTC      TITLE COLUMN IN PROGRESS .Q
         BNE   PAOK04              BRANCH IF NOT
         MVI   ICOLFLG,ICOLMC      MULTIPLE COLUMNS AGAIN
         L     COUNT,CLLZSAVE      GET COLUMN LENGTH
         LTR   COUNT,COUNT         HAS IT BEEN DEFINED .Q
         BZ    PAOK04              BRANCH IF NOT
         ST    COUNT,CLLZ          RESTORE COLUMN LENGTH
         SPACE
PAOK04   DS    0H
         MVC   PLCT,CDPLCT         SET COLUMNS LEFT ON THIS PAGE
         L     R1,FKQCNT           FLOATING KEEP COUNT
         LTR   R1,R1               ANYTHING THERE .Q
         BNP   PAOK06              BRANCH IF NOT
         CLC   PLCT,RMTRIG         REMOTE SET FOR TOP OF PAGE .Q
         BNE   PAOK05              BRANCH IF NOT
         LH    R1,RMTRIG           REMOTE NUMBER TO IMBED
        $CALL  RMIMBED             AND TRIGGER SAME FIRST
         SR    COUNT,COUNT         NO EXTRA CHECK
         BAL   RETURN,PAFULL       CHECK IF PAGE IS FULL
         BC    BNEG,PTOP01         BRANCH IF IT IS
PAOK05   DS    0H
         LH    X1,FNSEP            FOOTNOTE MARGIN
         XC    FNSEP,FNSEP         CLEAR FOOTNOTE MARGIN FOR CALL
         LA    R1,FKAREA           .FK CONTROL DESCRIPTOR
        $CALL  CPFKPRT             DUMP SAVED DATA
         BAL   RETURN,PAFNSEP      RESTORE FOOTNOTE MARGIN
         SPACE
         L     COUNT,CPQCNT        CONDITIONAL PAGE COUNT
         BAL   RETURN,PAFULL       CHECK IF PAGE IS FULL
         BC    BNEG,PTOP01         BRANCH IF IT IS
         SPACE
PAOK06   DS    0H
         L     R1,CPQCNT           CONDITIONAL KEEP COUNT
         LTR   R1,R1               ANYTHING THERE .Q
         BNP   PAOK08              BRANCH IF NOT
         CLC   PLCT,RMTRIG         REMOTE SET FOR TOP OF PAGE .Q
         BNE   PAOK07              BRANCH IF NOT
         LH    R1,RMTRIG           REMOTE NUMBER TO IMBED
        $CALL  RMIMBED             AND TRIGGER SAME FIRST
         SR    COUNT,COUNT         NO EXTRA CHECK
         BAL   RETURN,PAFULL       CHECK IF PAGE IS FULL
         BC    BNEG,PTOP01         BRANCH IF IT IS
PAOK07   DS    0H
         LH    X1,FNSEP            RETAIN FOOTNOTE MARGIN
         XC    FNSEP,FNSEP         CLEAR FOOTNOTE MARGIN FOR CALL
         LA    R1,CPAREA           .CP CONTROL DESCRIPTOR
        $CALL  CPFKPRT             DUMP SAVED DATA
         BAL   RETURN,PAFNSEP      RESTORE FOOTNOTE MARGIN
         SR    COUNT,COUNT         NO EXTRA CHECK
         BAL   RETURN,PAFULL       CHECK IF PAGE IS FULL
         BC    BNEG,PTOP01         BRANCH IF IT IS
         SPACE
PAOK08   DS    0H
         L     R1,CCQCNT           CONDITIONAL COLUMN KEEP COUNT
         LTR   R1,R1               ANYTHING THERE .Q
         BNP   PAOK10              BRANCH IF NOT
         CLC   PLCT,RMTRIG         REMOTE SET FOR TOP OF COLUMN .Q
         BNE   PAOK09              BRANCH IF NOT
         LH    R1,RMTRIG           REMOTE NUMBER TO IMBED
        $CALL  RMIMBED             AND TRIGGER SAME FIRST
         SR    COUNT,COUNT         NO EXTRA CHECK
         BAL   RETURN,PAFULL       CHECK IF PAGE IS FULL
         BC    BNEG,PTOP01         BRANCH IF IT IS
PAOK09   DS    0H
         LH    X1,FNSEP            RETAIN FOOTNOTE MARGIN
         XC    FNSEP,FNSEP         CLEAR FOOTNOTE MARGIN FOR CALL
         LA    R1,CCAREA           .CC CONTROL DESCRIPTOR
        $CALL  CPFKPRT             DUMP SAVED DATA
         BAL   RETURN,PAFNSEP      RESTORE FOOTNOTE MARGIN
         SR    COUNT,COUNT         NO EXTRA CHECK
         BAL   RETURN,PAFULL       CHECK IF PAGE IS FULL
         BC    BNEG,PTOP01         BRANCH IF IT IS
         SPACE
PAOK10   DS    0H
        $CALL  RMNEXT              AND SET UP FOR THE NEXT, IF ANY
        $RESTOR ,                  RESTORE CALLER'S REGS
         BR    RETURN              RETURN
         SPACE
SYSTOPRM DC    CL8'SYSTOP'         TOP OF PAGE REMOTE
SYSTMRM  DC    CL8'SYSTM'          TOP MARGIN REMOTE
         SPACE 2
PAFULL   DS    0H
        $SAVE  ,                   SAVE CALLER'S REGISTERS
        $IFON  SWITCHS,FEMPTY,PAFULL02  BRANCH IF PAGE NOW EMPTY
         LH    R1,PLCT             LINES LEFT ON THIS PAGE
         BCTR  R1,0                LESS ONE
         LH    X1,BMMRG            BOTTOM MARGIN
         AH    X1,FNSEP            PLUS FOOTNOTE MARGIN
         CR    R1,X1               COMPARE WITH BOTTOM MARGIN
         BL    PAFULL01            BRANCH IT NO ROOM LEFT
         SR    R1,COUNT            LESS CALLER'S QUALIFIER
         CR    R1,X1               COMPARE AGAIN
         BNL   PAFULL03            BRANCH IF ENOUGH SPACE
PAFULL01 DS    0H
        $CALL  PAGE                TERMINATE THIS PAGE
PAFULL02 DS    0H
        $CC    NEG                 SET TO START NEXT PAGE
         B     PAFULL04            GO TO RETURN
PAFULL03 DS    0H
        $CC    POS                 NO NEED TO START NEXT PAGE
PAFULL04 DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
         SPACE 2
PAFNSEP  DS    0H
        $SAVE  ,
         LTR   X1,X1               ANY FOOTNOTES OUTSTANDING .Q
         BZ    PAFNSE02            BRANCH IF NONE
         LH    R1,PLCT             LINES LEFT ON PAGE
         SH    R1,BMMRG            LESS BOTTOM MARGIN
         CH    R1,FTCNT            NEED FOOTNOTE SEPARATOR +1
         BNH   PAFNSE02            IF NOT AVAILABLE, IGNORE
         CR    X1,R1               NEED MORE THAN WE'VE GOT .Q
         BNH   PAFNSE01            BRANCH IF NOT
         LR    X1,R1               TAKE WHAT IS AVAILABLE
PAFNSE01 DS    0H
         STH   X1,FNSEP            SET FOOTNOTE MARGIN
PAFNSE02 DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
