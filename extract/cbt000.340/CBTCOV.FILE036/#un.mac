***********************************************************************
*                                                                     *
*              .UN (UNDENT) --- CONTROL                               *
*                                                                     *
***********************************************************************
         SPACE
UN      $ENTER ,
        $CALL  PRINTNB             BREAK
        $CALL  GETNUM              GET AMOUNT
         BC    BDNO,UNE005         ERROR IF INVALID
         BC    BOMIT,UN01          TAKE DEFAULT IF OMITTED
         CLI   NUMSIGN,C' '        WAS OPERAND UNSIGNED .Q
         BE    UN02                BRANCH IF YES
         AH    COUNT,UNDL          MAKE RELATIVE CHANGE
         B     UN02                GO CHECK AND ENTER
UN01     DS    0H
         SR    COUNT,COUNT         ZERO IS DEFAULT
UN02     DS    0H
         L     R1,LLZ              GET LINE LENGTH
         AH    R1,INDENTR          PLUS RIGHT INDENT
         LCR   R1,R1               MAKE IT NEGATIVE
         CR    COUNT,R1            IS VALUE TOO SMALL .Q
         BNH   UNE014              TOO SMALL IF NOT
         CH    COUNT,INDL          COMPARE WITH INDENT
         BH    UNE013              ERROR IF LARGER
         STH   COUNT,UNDL          SET UNDENTATION
         STH   COUNT,OFUNCNT       SET MARGIN VALUE FOR FORMAT
         SH    COUNT,INDL          LESS CURRENT INDENT
         LCR   COUNT,COUNT         POSITIVE VALUE
         STH   COUNT,RMARGIN       SET RIGHT MARGIN
         SPACE
UN03     DS    0H
        $EXIT  RC=0                AND CONTINUE
         SPACE
UNE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     UN03                AND GO TO RETURN
UNE013   DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO BIG
         B     UN03                AND GO TO RETURN
UNE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     UN03                AND GO TO RETURN
