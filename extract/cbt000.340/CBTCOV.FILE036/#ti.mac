***********************************************************************
*                                                                     *
*              TRANSLATE ON INPUT (.TI) --- CONTROL                   *
*                                                                     *
***********************************************************************
         SPACE
TI      $ENTER ,
         MVC   TRANAD,TRANADI      ADDR OF INPUT TRANSLATE TABLE
        $CALL  GETYESNO            LOOK AT FIRST ARGUMENT
         BC    15-BOMIT,TI02       BRANCH IF SOME ARGS
         L     TEMP,TRANADI        ADDR OF INPUT TRANSLATE TABLE
         L     R1,TRINITAD         IDENTITY TRANS TABLE ADDR
         MVC   0(256,TEMP),0(R1)   ENTER THE IDENTITY TRANS TABLE
TI01     DS    0H
         XC    ESTABAD(4),ESTABAD  ZERO ESCAPE TABLE POINTER
         B     TI06                AND GO TO RETURN
         SPACE
TI02     DS    0H
         CLI   YNRESULT,YNSET      IS ARGUMENT SET .Q
         BE    TI04                BRANCH IF YES
         ST    COUNT,ARGSTART      RESCAN FROM START
        $CALL  TRSCN               PROCESS REST AS .TR
         B     TI06                AND GO TO RETURN
         SPACE
TI04     DS    0H
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION PLEASE
        $CALL  GETARG              GET 'SET' ARGUMENT
         BC    BOMIT,TI01          ZERO POINTER, NOT TABLE IF MISSING
         LTR   TEMP,TEMP           IS LENGTH-1 OF OPERAND ZERO .Q
         BNZ   TI05                ERROR IF NOT
         L     TEMP,ESTABAD+4      GET REAL TABLE
         ST    TEMP,ESTABAD        SET THE WORKING TABLE POINTER
         XC    0(256,TEMP),0(TEMP) ZERO THE TABLE
         SR    R1,R1               ZERO WORKING REGISTER
         IC    R1,0(,COUNT)        GET FIRST CHAR OF ARGUMENT
         STC   R1,0(R1,TEMP)       ENTER IN THE TABLE
         STC   R1,ESTABAD          SAVE CHAR IN TABLE ADDRESS
         B     TI06                AND GO TO RETURN
         SPACE
TI05     DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         SPACE
TI06     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
