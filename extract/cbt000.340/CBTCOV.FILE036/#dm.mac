***********************************************************************
*                                                                     *
*              .DM (DEFINE MACRO) --- CONTROL                         *
*                                                                     *
***********************************************************************
         SPACE
DM      $ENTER ,
        $CALL  GETNUM              TEST FOR NUMERIC FIRST OPERAND
         BC    BOMIT,DME003        ERROR IF OMITTED
         BC    BDNO,DM01           BRANCH IF ERROR, TRY AGAIN
         LTR   COUNT,COUNT         TEST RESULT
         BNP   DME014              BRANCH IF TOO SMALL
         C     COUNT,F32767        TEST AGAIN
         BH    DME013              ERROR IF TOO BIG
         B     DM05                VALID NUMERIC OPERAND
         SPACE
DM01     DS    0H
         ST     COUNT,ARGSTART     RESCAN FOR STRING OPERAND
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION PLEASE
        $CALL  GETARG              GET ARGUMENT NAME
         LTR   TEMP,TEMP           LENGTH-1 A ZERO .Q
         BNZ   DM02                BRANCH IF NOT
         CLI   0(COUNT),C'*'       DEFAULT FIRST OPERAND .Q
         BE    DM04                BRANCH IF YES
DM02     DS    0H
         LA    R0,L'APIMNAME-1     MAX LENGTH-1 ALLOWED
         CR    TEMP,R0             CHECK WITH MAX
         BNH   DM03                BRANCH IF OK
         LR    TEMP,R0             MAX WILL DO
DM03     DS    0H
         MVC   APIMNAME,BLANKS     BLANK MACRO TARGET NAME
         EX    TEMP,DMNAMVC        MOVE IN MACRO NAME
         OC    APIMNAME,BLANKS     CONVERT TO UPPER CASE
         B     DM06                AND CONTINUE
DMNAMVC  MVC   APIMNAME(*-*),0(COUNT)   ENTER MACRO NAME
         SPACE
DM04     DS    0H
         LH    COUNT,TOPMRG        DEFAULT FIRST ARG, FIRST TEXT LINE
         LA    COUNT,1(,COUNT)     BUMP TOP MARGIN BY ONE
DM05     DS    0H
         XC    APIMNAME,APIMNAME   ZERO NAME TARGET
         ST    COUNT,APIMNAME+4    ADD LINE NUMBER
         SPACE
DM06     DS    0H
         XC    RMFLAG,RMFLAG       ZERO SCANNING FLAG
        $ON    RMFLAG,(RMQDM,RMQKEEP)   THIS IS A 'NOSAVE MACRO'
         MVI   RMDELIM,C' '        DELIMITING CHAR A BLANK
         MVC   RMCWIND,BUFF2       MACRO CONTROL WORD IND
         MVC   RMCNT,F32767        HIGHEST CALL COUNT
        $CALL  GETYESNO            GET AND TEST SECOND ARG
         BC    BPOS,DM08           BRANCH IF POSITIVE TO ACCUMULATE
         BC    BNEG,DM07           BRANCH IF NEGATIVE TO DELETE
         BC    BOMIT,DME003        BRANCH IF NOTHING
         CLI   YNRESULT,YNDEL      'DELETE' OPERAND .Q
         BE    DM07                BRANCH IF YES
         ST    COUNT,ARGSTART      RESET OPERAND START
         MVC   RMDELIM,0(COUNT)    SET DELIMITING CHAR
         B     DM08                AND CONTINUE
         SPACE
DM07     DS    0H
        $CALL  RMDEL               DELETE ONE ENTRY
         BC    BNEG,DME017         ERROR IF NOT FOUND
         B     DM31                GO TO RETURN
         SPACE
DM08     DS    0H
        $CALL  RMFIND              FIND QUEUE ENTRY POINT
         LA    R1,RMQLEN           QUEUE ELEMENT LEN
        $CALL  GETCELL             GET AN AREA
         BC    BNEG,DME018         BRANCH IF FAILED
         LR    X3,TEMP             USE THIS AREA
         USING RMQD,X3             AREA GOT
         XC    RMQD(RMQLEN),RMQD   CLEAR THE AREA
         MVC   RMQCHN,RMQCHN-RMQD(X1)   NEXT PTR
         ST    X3,RMQCHN-RMQD(,X1) LAST POINTER
         MVC   RMQFLAG,RMFLAG      FLAG ATTRIBUTES
         MVC   RMQCNT,RMCNT        AND CALL COUNT
         MVC   RMQNAME,APIMNAME    REMOTE IDENT
         SPACE
         CLI   RMQDELIM,C' '       BLANK STRING DELIMITER .Q
         BE    DM30                BRANCH IF YES
         LA    R1,DMEND            POINT TO MACRO TERMINATOR
         LA    TEMP,L'DMEND-1      LENGTH-1
        $CALL  HIDBADD             QUEUE IT 'LIFO'
         L     X2,ARGSTOP          END OF ARG
DM20     DS    0H
         LR    X1,X2               SCAN PTR FROM HERE
         C     X1,ARGSTART         WHERE IT ALL STARTED
         BL    DM30                BRANCH IF ALL DONE
DM21     DS    0H
         CLC   0(1,X1),RMDELIM     DELIMITER CHAR .Q
         BE    DM22                BRANCH IF YES
         BCT   X1,DM21             BACKUP ONE AND BRANCH
DM22     DS    0H
         LR    TEMP,X2             END PTR
         SR    TEMP,X1             LENGTH INCL DELIMITERS
         S     TEMP,F1             LENGTH-1 BETWEEN
         BM    DM23                BRANCH IF NOTHING
         LA    R1,1(,X1)           START OF STRING
        $CALL  HIDBADD             QUEUE IT 'LIFO'
DM23     DS    0H
         LR    X2,X1               LAST DELIMITER
         BCT   X2,DM20             BACKUP ONE AND BRANCH
         SPACE
DM30     DS    0H
         LA    R0,RMQD             POINT TO ANCHOR
        $CALL  RMREAD              GET MACRO LINES
         ST    R1,RMQFSIZ          ENTER COUNT OF RECORDS
         DROP  X3
         SPACE
DM31     DS    0H
        $CALL  RMNEXT              SET UP FOR NEXT MACRO
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
DMEND    DC    C'.DM END'          END OF MACRO DEFINITION
         SPACE
DME003   DS    0H
         MVI   EFLAG003,ERRSET     OMITTED OPERAND
         B     DM31                GO TO RETURN
DME005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     DM31                GO TO RETURN
DME013   DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO BIG
         B     DM31                GO TO RETURN
DME014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     DM31                GO TO RETURN
DME017   DS    0H
         MVI   EFLAG017,ERRSET     MACRO NOT FOUND
         B     DM31                GO TO RETURN
DME018   DS    0H
         MVI   EFLAG018,ERRSET     INSUFFICIENT STORAGE FOR MACRO
         B     DM31                GO TO RETURN
