***********************************************************************
*                                                                     *
*              TABIT  ---  EXPAND USER'S TABS                         *
*                                                                     *
*                                                                     *
*        REGISTER USAGE:                                              *
*                                                                     *
*        R1    POINTER TO FORMAT PARAMETER LIST                       *
*        PNEW  POINTER TO LINKED CHARACTER BLOCK                      *
*        TEMP  NEXT TABSTOP                                           *
*        P     POINTER TO LINKED CHARACTER BLOCK                      *
*        C     CURRENT COLUMN POINTER                                 *
*        TABS  POINTER TO LINKED TAB BLOCKS                           *
*                                                                     *
*        ON EXIT  --  CC = BPOS   IF TAB FOUND                        *
*                     CC = BNEG   IF NO TAB FOUND                     *
*                                                                     *
***********************************************************************
         SPACE
TABIT   $ENTER ,
         USING FOPMLIST,R1
         USING LINKELEM,P          CALLER PROVIDED CHAR POINTER
        $OFF   SWITCHS,THSWS       CLEAR HYPHEN SWITCH
         L     TABS,FOPTAB         GET ADDRESS OF POINTER
         L     TABS,0(,TABS)       GET POINTER TO TAB TABLE
         MVI   LINKCHAR,C' '       SET CHARACTER TO BLANK
         LTR   TABS,TABS           USER TABS DEFINED .Q
         BNZ   TAB20               BRANCH IF YES
TAB10    DS    0H
         LR    TEMP,C              COLUMN WE'RE AT
         LA    COUNT,6(,TEMP)      BUMP BY FIVE PLUS ONE FOR TAB
         SRDL  COUNT,32            PREPARE FOR DIVIDE
         D     COUNT,F5            MOD FIVE
         MH    TEMP,H5             THIS IS THE DEFAULT
         B     TAB30               ENTER ROUTINE ALREADY IN PROGRESS
         SPACE
TAB20    DS    0H
         USING TBAREA,TABS         USER TAB AREA
         CH    C,TBVALUE           IS THIS APPROPRIATE TAB STOP .Q
         BNL   TAB120              BRANCH IF NOT
         LH    TEMP,TBVALUE        YES, GET LOC OF NEXT TABSTOP
TAB30    DS    0H
         SR    PNEW,PNEW           ASSUME NO NEXT CHAR
        $IFON  FOSWTCH,NFSWS,TAB50 BRANCH IF IN NO FORMAT MODE
         L     R0,FOLL             CURRENT LINE LENGTH
        $IFON  SWITCHS,TCSWS,TAB40 USE REAL LINE LENGTH IF TC MODE
         AH    R0,FOINDR           PLUS RIGHT INDENT VALUE
TAB40    DS    0H
         CR    TEMP,R0             TAB LESS THAN LINE LENGTH .Q
         BL    TAB50               BRANCH IF YES
         LR    TEMP,R0             ONLY TO LINE END THEN
TAB50    DS    0H
         LTR   TABS,TABS           USER TABS DEFINED .Q
         BZ    TAB52               BRANCH IF NOT
         L     PNEW,LINKFOR        LOOK AT NEXT CHAR
TAB52    DS    0H
         SR    TEMP,C              CALC NUMBER OF FILL CHAR TO ADD
         BP    TAB60               BRANCH IF RESULT VALID
         B     TAB110
TAB60    DS    0H
        $CALL  TABPCC              COUNT CHARS IN NEXT PHRASE
         LTR   X2,X2               WAS PHRASE COUNT ZERO .Q
         BZ    TAB70               BR IF YES
         LTR   TABS,TABS           USER TABS DEFINED .Q
         BZ    TAB70               BR IF NO
         CLI   TBTYPE,TBLEFT       WAS LEFT MODE SPECIFIED .Q
         BNE   TAB80               BR IF NO
TAB70    DS    0H
        $CALL  TABITL              CALL LEFT TAB ROUTINE
         BC    BNEG,TAB120         UNABLE TO LEFT TAB - TRY NEXT TAB
         B     TAB170
TAB80    DS    0H
         CLI   TBTYPE,TBRIGHT      WAS RIGHT MODE SPECIFIED .Q
         BNE   TAB90               BR IF NO
        $CALL  TABITR              CALL RIGHT TAB ROUTINE
         BC    BNEG,TAB120         UNABLE TO TAB RIGHT - TRY NEXT TAB
         B     TAB170
TAB90    DS    0H
         CLI   TBTYPE,TBCENTRE     WAS CENTRE MODE SPECIFIED .Q
         BNE   TAB100              BR IF NO
        $CALL  TABITC              CALL CENTRE TAB ROUTINE
         BC    BNEG,TAB120         UNABLE TO CENTRE TEXT - TRY NEXT TAB
         B     TAB170              RETURN
TAB100   DS    0H
         CLI   TBTYPE,TBOK         WAS ALIGN MODE SPECIFIED .Q
         BNE   TAB110              BR IF NO
        $CALL  TABITA              CALL ALIGN TAB ROUTINE
         BC    BNEG,TAB120         UNABLE TO ALIGN - TRY NEXT TAB
         B     TAB170
TAB110   DS    0H
         MVI   LINKMULT,X'01'      SET MULTIPLIER FIELD TO 1
         B     TAB170              AND RETURN
TAB120   DS    0H
        $IFOFF SWITCHS,TCSWS,TAB150  BR IF NOT IN TC MODE
         LTR   TABS,TABS           USER TABS DEFINED .Q
         BZ    TAB122              BRANCH IF NOT
         OC    TBCHN,TBCHN         MORE TABS DEFINED .Q
         BNZ   TAB150              BRANCH IF YES
TAB122   DS    0H
         MVI   LINKCHAR,X'05'      RESET TAB CHAR
         L     TEMP,LINKBACK       PTR TO BACKWARD LINK
         USING LOLDELEM,TEMP       TELL ASSEMBLER
TAB130   DS    0H
         CLI   LOLDCHAR,C' '       DID WE FIND A BLANK .Q
         BE    TAB140              EXIT IF YES
         L     TEMP,LOLDBACK       BACKUP AGAIN
         B     TAB130              LOOP
TAB140   DS    0H
         L     TABS,LOLDFOR        LINK FORWARD
        $ON    SWITCHS,THSWS       TURN ON TAB HYPHEN SW FOR MERGE
         B     TAB160              RETURN TO MERGE
TAB150   DS    0H
         LTR   TABS,TABS           USER TABS DEFINED .Q
         BZ    TAB10               BR IF NO
         L     TABS,TBCHN          NEXT USER TAB
         LTR   TABS,TABS           DOES IT EXIST .Q
         BNZ   TAB20               BRANCH IF YES
TAB160   DS    0H
        $CC    NEG                 INDICATE JOB NOT DONE
         B     TAB180              NO TAB FOUND
         SPACE
TAB170   DS    0H
        $CALL  TABFILL             SET FILL CHARACTER(S)
         L     TEMP,NEWCOUNT       OBTAIN CURRENT CHAR COUNT
         AR    TEMP,COUNT          ADD LENGTH OF FILL
         ST    TEMP,NEWCOUNT       SAVE NEW LENGTH BACK
        $CC    POS                 INDICATE THE JOB WAS DONE
TAB180   DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         DROP  R1,P,TABS
