***********************************************************************
*                                                                     *
*        INTERNAL FUNCTION 'RMNEXT' --- SET PAGE COUNT WHICH          *
*        CORRESPONDS TO NEXT REMOTE TRIGGER                           *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER:   $CALL  RMNEXT
*
*        ON EXIT:   RMTRIG = 0  --  IF NO REMOTE WITHIN PAGE LENGTH
*                   RMTRIG > 0  --  IF REMOTE FOUND
*            AND:   RMNAMLEN    --  FLAGS SET FOR LENGTH OF REMOTE NAME
         SPACE
RMNEXT  $ENTER ,
         XC    RMNAMLEN,RMNAMLEN   CLEAR ALL REMOTE NAME LENGTH FLAGS
         LA    X3,RMQANCH-RMQCHN+RMQD   START REMOTE DESCRIPTOR QUEUE
         USING RMQD,X1             AND TELL THE ASSEMBLER
NRM00    DS    0H
         L     X1,RMQCHN-RMQD(,X3) NEXT REMOTE DESCRIPTOR
         LTR   X1,X1               AT THE END .Q
         BZ    NRM06               BRANCH OUT IF YES
        $IFON  RMQFLAG,RMQDEL,NRM03     BRANCH IF REMOTE DELETED
         CLC   RMQNAME(4),F0       A NUMBERED REMOTE .Q
         BE    NRM02               BRANCH IF YES
         LA    R1,RMQNAME+L'RMQNAME     END OF REMOTE NAME +1
NRM01    DS    0H
         BCTR  R1,0                BACKUP CHAR POINTER
         CLI   0(R1),C' '          TRAILING BLANK .Q
         BE    NRM01               YES, KEEP SCANNING
         LA    R0,RMQNAME          FIRST CHAR OF NAME
         SR    R1,R0               LENGTH-1 OF NAME
         LA    R1,RMNAMLEN(R1)     POINT TO LENGTH FLAGS
         MVI   0(R1),RMNAMDEF      AT LEAST ONE DEFINED
NRM02    DS    0H
         LR    X3,X1               LAST REMOTE DESCRIPTOR
         B     NRM00               DO THEM ALL
         SPACE
NRM03    DS    0H
         L     X2,RMQAD            FIRST REMOTE TEXT LINE
         L     R1,RMQEXIT          REMOTE READ EXIT ADDRESS
         LTR   R1,R1               ANY ADDRESS PRESENT .Q
         BZ    NRM04               BRANCH IF NOT
         SR    X2,X2               IF PRESENT, THEN NO REMOTE LINES
NRM04    DS    0H
         LA    R0,RMQD             REMOTE QUEUE DESCRIPTOR
         LA    R1,RMQLEN           AND ITS LENGTH
         MVC   RMQCHN-RMQD(L'RMQCHN,X3),RMQCHN    UNCHAIN THIS REMOTE
         L     X1,RMQCHN           NEXT REMOTE DESCRIPTOR
         DROP  X1
        $CALL  FREECELL            RELEASE REMOTE DESCRIPTOR
         SPACE
NRM05    DS    0H
         LTR   X2,X2               END OF REMOTE TEXT LINES .Q
         BZ    NRM00               BRANCH BACK IF YES
         USING RMQR,X2             THIS IS WHERE THE DATA IS
         LA    R0,RMQR             TEXT LINE INFO
         LH    R1,RMQRLEN          LENGTH OF DATA
         LA    R1,RMQRLN(,R1)      PLUS OVERHEAD
         L     X2,RMQRCHN          NEXT TEXT LINE
         DROP  X2
        $CALL  FREECELL            RELEASE THE DATA
         B     NRM05               DO ALL DATA LINES
         SPACE
NRM06    DS    0H
         LH    R1,PLZ              CURRENT PAGE LENGTH
         SH    R1,PLCT             LESS LINES LEFT ON PAGE
         LA    R1,1(,R1)           LINE ABOUT TO PRINT
         XC    APIMNAME,APIMNAME   CLEAR WORK NAME
         ST    R1,APIMNAME+4       ENTER LINE TO PRINT
         SPACE
NRM07    DS    0H
         SR    R0,R0               ZERO WORK REGISTER
         STH   R0,RMTRIG           ASSUME AUTO TRIGGER FAILURE
         CH    R0,RMNMNEST         IN AUTOMATIC REMOTE .Q
         BNE   NRM11               NO AUTOMATIC REMOTES IF YES
         L     X1,RMQANCH          START OF REMOTE QUEUE
         USING RMQD,X1             TELL THE ASSEMBLER
NRM08    DS    0H
         LTR   X1,X1               AT THE END .Q
         BZ    NRM11               BRANCH OUT IF YES
        $IFON  RMQFLAG,RMQDEL,NRM09     BRANCH IF DELETED
        $IFON  RMQFLAG,RMQDM,NRM09      BRANCH IF A MACRO DEFINITION
         L     R1,RMQCNT           GET REMOTE CALL COUNT
         S     R1,RMQUSE           LESS CURRENT USE COUNT
         BNP   NRM09               BRANCH IF REMOTE IS THROUGH
         CLC   RMQPAGE,PAGECNT     ALREADY AUTO TRIGGERED THIS PAGE .Q
         BNL   NRM09               YES, DON'T DO IT AGAIN
         SPACE
         CLC   RMQNAME,APIMNAME    CHECK AGAINST CURRENT LINE
         BNL   NRM10               IF CURRENT OR BIGGER, QUIT
NRM09    DS    0H
         L     X1,RMQCHN           CHAIN TO THE NEXT
         B     NRM08               AND AROUND AGAIN
         SPACE
NRM10    DS    0H
         C     R0,RMQNAME          IDENTIFIER OR NUMBER .Q
         BNE   NRM11               IDENTIFIER, SO RETURN
         L     R1,RMQNAME+4        GET LINE NUMBER OF NEXT REMOTE
         SH    R1,PLZ              LESS PAGE LENGTH
         BP    NRM11               NOT FOUND IF PAGE LENGTH GREATER
         LPR   R1,R1               MAKE RESULT POSITIVE
         LA    R1,1(,R1)           PLUS ONE
         STH   R1,RMTRIG           SET VALUE OF PLCT FOR REMOTE
         DROP  X1
         SPACE
NRM11    DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
