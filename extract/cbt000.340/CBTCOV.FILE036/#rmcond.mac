***********************************************************************
*                                                                     *
*        INTERNAL FUNCTION 'RMCOND' --- CONDITIONALLY IMBED A REMOTE  *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER:   LA  R1,=CL8'NAME'
*                   $CALL RMCOND
*
*        ON EXIT:    CC = BOMIT  IF REMOTE NOT FOUND
*                       = BPOS   IF REMOTE FOUND AND IMBEDDED
         SPACE
RMCOND  $ENTER ,
         LR    X3,R1               RETAIN REMOTE NAME POINTER
         L     X1,RMQANCH          START OF REMOTE QUEUE
         USING RMQD,X1             TELL THE ASSEMBLER
RMC01    DS    0H
         LTR   X1,X1               AT THE END .Q
         BZ    RMC04               BRANCH OUT IF YES
        $IFON  RMQFLAG,RMQDEL,RMC02     BRANCH IF REMOTE DELETED
         L     R1,RMQUSE           GET CURRENT REMOTE USE COUNT
         LTR   R1,R1               IS IT ACTIVE .Q
         BNZ   RMC02               IF YES, AVOID RECURSION
         CLC   RMQPAGE,PAGECNT     ALREADY AUTO TRIGGERED THIS PAGE .Q
         BNL   RMC02               YES, DON'T DO IT AGAIN
         CLC   RMQNAME,0(X3)       EQUAL IDENTIFIER
         BH    RMC04               NOT FOUND IF TOO BIG
         BE    RMC03               FOUND IF EQUAL
RMC02    DS    0H
         L     X1,RMQCHN           CHAIN TO NEXT REMOTE HEADER
         B     RMC01               AROUND AGAIN
         DROP  X1
         SPACE
RMC03    DS    0H
         MVC   APIMNAME,0(X3)      ENTER REMOTE NAME
         MVC   APIMMEMB,BLANKS     CLEAR MEMBER NAME
         XC    APIMFLAG,APIMFLAG   CLEAR FLAGS
        $ON    APIMFLAG,(IO$RM,IO$UEOF) I WANT IT LIKE THIS
        $ON    APIMFLAG,(IO$AURM,IO$SYRM)    AND LIKE THIS
         L     R1,FM8              NEGATIVE EIGHT MEANS NAME WORK DONE
        $CALL  RMIMBED             DO THE REMOTE IMBED
        $CALL  RMNEXT              SET UP FOR NEXT AUTO REMOTE, IF ANY
        $CC    POS                 'TIS DONE
         B     RMC05               GO TO RETURN
         SPACE
RMC04    DS    0H
        $CC    OMIT                REMOTE NOT FOUND
         SPACE
RMC05    DS    0H
        $EXIT  RC=0                RETURN TO CALLER
