DCOMPOZ $ENTER ,
         SPACE
*        ON ENTRY --  NUMCHR      IS LENGTH OF INPUT WORD
*                     HYWKWORD    IS INPUT STRING
*
*        ON EXIT  --  HYDCWORD    FIRST TO 13TH DECOMPOSED WORD
*                     HYWKWORD    2ND TO 13TH DECOMPOSED/PACKED WORD
*                     (R1)        FIRST CHARACTER OF WORD
         SPACE
         MVI   HYDCWORD,L'HYALPHA  INTERNAL BLANK TO TARGET
         MVC   HYDCWORD+1(L'HYDCWORD-1),HYDCWORD  ALL OF IT
         LH    COUNT,NUMCHR        GET LENGTH-1 OF INPUT STRING
         EX    COUNT,HYDCMVC       MOVE WORD TO DECOMP AREA
         LA    X2,1                SET INCREMENT
         LA    X1,HYDCWORD         SET INDEX TO START OF DECOMP AREA
HYDC01   DS    0H
         C     COUNT,F12           TEST LENGTH OF WORD
         BNH   HYDC04              EXIT IF UNDER 12 CHARS
         ST    COUNT,HYLENWRD      SAVE PRESENT LENGTH
         LA    X3,0(COUNT,X1)      ADDR OF LAST CHARACTER
         LR    COUNT,X1            ADDR OF LAST DECOMP CHAR STORED
         AR    X1,X2               BUMP TO NEXT TEST CHAR
         IC    R1,HYDCWORD         FIRST CHAR OF WORD
HYDC02   DS    0H
         EX    R1,HYDCCLI          COMPARE CHAR+1 TO CHAR
         IC    R1,0(,X1)           GET NEXT CHAR
         BNH   HYDC03              BRANCH IF CHAR+1 NOT HIGHER FREQ
         AR    COUNT,X2            BUMP ADDR OF LAST DECOMP CHAR
         STC   R1,0(,COUNT)        KEEP CHAR+1 IN DECOMP WORD
HYDC03   DS    0H
         BXLE  X1,X2,HYDC02        BUMP TO NEXT CHAR
         LA    X1,HYDCWORD         START OF DECOMP AREA
         SR    COUNT,X1            GET NUM CHAR IN DECOMP WORD
         C     COUNT,HYLENWRD      ANY CHARACTERS DELETED .Q
         BE    HYDC04              GO TRUNCATE WORD IF NOT
         B     HYDC01              GO TRY AGAIN
         SPACE
HYDC04   DS    0H
         LA    COUNT,L'HYWORD*8/5  MAX CHAR STORED
         LA    X1,HYDCWORD+1       RESULTING WORD
         SR    R0,R0               INITIALIZE TARGET
         LR    R1,R0               BOTH HALVES
         SR    TEMP,TEMP           CLEAR CHARACTER WORK REGISTER
HYDC05   DS    0H
         IC    TEMP,0(,X1)         CHARACTER
         SLDL  R0,5                MAKE ROOM
         OR    R1,TEMP             ADD NEXT CHARACTER
         LA    X1,1(,X1)           NEXT CHARACTER
         BCT   COUNT,HYDC05        DO ALL CHARACTERS
         STM   R0,R1,DECM          ALIGN RESULT
         MVC   HYWKWORD(8),DECM    RETURN RESULT TO CALLER
         SPACE
         SR    R1,R1               ZERO RETURN REGISTER
         IC    R1,HYDCWORD         INTERNAL FIRST CHARACTER
        $EXIT  KEEP=(R1),RC=0      RESTORE AND RETURN TO CALLER
         SPACE
HYDCCLI  CLI   0(X1),*-*           COMPARE CHAR TO CHAR+1
HYDCMVC  MVC   HYDCWORD(*-*),HYWKWORD   EXECUTED MOVE INPUT
