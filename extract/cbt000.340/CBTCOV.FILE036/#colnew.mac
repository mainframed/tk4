***********************************************************************
*                                                                     *
*              COLNEW --- NEW OUTPUT COLUMN ROUTINE                   *
*                                                                     *
***********************************************************************
         SPACE
COLNEW  $ENTER ,
COLN01   DS    0H
         OC    FNQCNT,FNQCNT       ANY FOOTNOTES QUEUED .Q
         BZ    COLN03              BRANCH IF NONE
         LH    R1,BMMRG            BOTTOM MARGIN
         AH    R1,FNSEP            PLUS FOOTNOTE MARGIN
         CH    R1,RMTRIG           REMOTE BEFORE BOTTOM MARGIN .Q
         BNL   COLN02              IF NOT, SKIP
         LH    COUNT,PLCT          YES, SPACE DOWN TO IT
         SH    COUNT,RMTRIG        AMOUNT TO SPACE
        $CALL  SPACER              DO IT NOW
         SPACE
         L     COUNT,PAGECNT       SAVE REAL PAGE EJECT COUNT
         LH    R1,RMTRIG           REMOTE TO IMBED
         LH    R1,RMTRIG           REMOTE TO IMBED
        $CALL  RMIMBED             IMBED A REMOTE SEQUENCE
         C     COUNT,PAGECNT       DID A PAGE EJECT OCCUR .Q
         BE    COLN01              IF NOT, TRY AGAIN
         B     COLN05              OTHERWISE QUIT
         SPACE
COLN02   DS    0H
         LH    COUNT,PLCT          LINES LEFT ON BOTTOM OF COLUMN
         SH    COUNT,BMMRG         LESS BOTTOM MARGIN
         SH    COUNT,FNSEP         LESS FOOTNOTE MARGIN
        $CALL  SPACER              GENERATE LINES UP TO FOOTNOTES
        $CALL  FNPRT               FLUSH FOOTNOTES
         L     R1,CDCURR           CURRENT COLUMN BEING PROCESSED
         BCTR  R1,0                DECREMENT COLUMN BY ONE
         MH    R1,MCALEN           LENGTH OF ONE COLUMN DESCRIPTOR
         LA    R1,MCAREA(R1)       POINT TO COLUMN ANCHOR
         USING CPFKFN,R1           AREA LOOKS LIKE THIS
        $ON    CPFKFNG,COLLOCK     MARK COLUMN LOCKED
         DROP  R1
         SPACE
COLN03   DS    0H
         L     COUNT,CDCURR        CURRENT COLUMN BEING PROCESSED
         C     COUNT,CDCOUNT       PROCESSING LAST COLUMN .Q
         BE    COLN04              BRANCH IF YES
         A     COUNT,F1            BUMP CURRENT COLUMN BY ONE
         B     COLN06              GO TO COMMON EXIT
         SPACE
COLN04   DS    0H
         LH    X1,FNSEP            RETAIN FOOTNOTE MARGIN
         XC    FNSEP,FNSEP         CLEAR FOOTNOTE MARGIN
        $CALL  COLPRT              FLUSH WHAT WE HAVE SO FAR
         STH   X1,FNSEP            RESTORE FOOTNOTE MARGIN
         SPACE
        $IFON  SWITCHS,FEMPTY,COLN05    BRANCH IF NEXT PAGE EMPTY
         MVI   ICOLFLG,ICOLSC      DISABLE MULTIPLE COLUMNS
         MVC   CLLZ,LLZ            RESTORE FULL LINE LENGTH
        $CALL  PAGE                NOW TERMINATE THIS PAGE
         MVI   ICOLFLG,ICOLMC      MULTIPLE COLUMN AGAIN
         MVC   CDPLCT,PLCT         SAVE LINES REMAINING IN COLUMN
         L     COUNT,CLLZSAVE      GET COLUMN LENGTH
         LTR   COUNT,COUNT         HAS IT BEEN DEFINED .Q
         BZ    COLN05              BRANCH IF NOT
         ST    COUNT,CLLZ          RESTORE COLUMN LENGTH
COLN05   DS    0H
         LA    COUNT,1             START WITH COLUMN ONE AGAIN
COLN06   DS    0H
         ST    COUNT,CDCURR        SET CURRENT OUTPUT COLUMN
         MVC   PLCT,CDPLCT         RESET LINES REMAINING
        $EXIT  RC=0                RETURN TO CALLER
