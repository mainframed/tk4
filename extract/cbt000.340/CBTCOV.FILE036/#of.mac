***********************************************************************
*                                                                     *
*              .OFFSET (.OF) --- CONTROL                              *
*                                                                     *
***********************************************************************
         SPACE
OF      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
         CLC   OFFLI,H0            HAS OFFSET BEEN TRIGGERED .Q
         BNZ   OFE012              NO, ERROR
        $CALL  GETNUM              GET AMOUNT OF OFFSET
         BC    BDNO,OFE005         BRANCH IF INVALID
         BC    BOMIT,OF02          BRANCH IF OMITTED ARGUMENT
         CLI   NUMSIGN,C' '        IS SIGN BLANK .Q
         BE    OF03                BRANCH IF YES TO SET
         LH    TEMP,OFFL           GET CURRENT OFFSET VALUE
         AR    TEMP,COUNT          PLUS THE ADJUSTMENT
         LH    R1,INDL             CURRENT LEFT INDENT
         SH    R1,OFFL             ADJUSTED BY CURRENT OFFSET
         LNR   R1,R1               MINUS LEFT INDENT
         CR    TEMP,R1             COMPARE NEW WITH MAX
         BL    OFE014              ERROR IF TOO SMALL
         L     R1,LLZ              LINE LENGTH
         AH    R1,INDENTR          PLUS RIGHT INDENT
         CR    TEMP,R1             TOO BIG .Q
         BH    OFE013              ERROR IF YES
         STH   TEMP,OFFL           SET NEW OFFSET VALUE
         STH   COUNT,OFFLI         IS NEGATIVE FOR ADJUSTMENT TO INDL
         LTR   TEMP,TEMP           TEST FOR NEGATIVE
         BNM   OF01                BRANCH IF NOT
         SR    TEMP,TEMP           SET TO ZERO FOR FORMAT
OF01     DS    0H
         STH   TEMP,OFUNCNT        SET MARGIN VALUE FOR FORMAT
         B     OF05                GO TO COMMON RETURN
         SPACE
OF02     DS    0H
         SR    COUNT,COUNT         THIS IS OFFSET ZERO
OF03     DS    0H
         LH    R1,INDL             CURRENT LEFT INDENT
         SH    R1,OFFL             ADJUSTED BY CURRENT OFFSET
         LNR   R1,R1               MINUS LEFT INDENT
         CR    COUNT,R1            COMPARE WITH NEW MAX
         BL    OFE014              ERROR IT TOO SMALL
         L     R1,LLZ              LINE LENGTH
         AH    R1,INDENTR          PLUS RIGHT INDENT
         CR    COUNT,R1            TOO BIG .Q
         BH    OFE013              ERROR IF YES
         LH    TEMP,INDL           CLEAR OFFSET
         SH    TEMP,OFFL           ..
         STH   TEMP,INDL           ADJUST INDENTATION
         SH    TEMP,UNDL           ..
         BM    OFE013              ..
         STH   TEMP,RMARGIN        ..
         STH   COUNT,OFFL          SET CURRENT VALUE
         STH   COUNT,OFFLI         SET NEXT OFFSET VALUE
         LTR   COUNT,COUNT         TEST OFFSET FOR NEGATIVE
         BNM   OF04                BRANCH IF NOT
         SR    COUNT,COUNT         ELSE SET TO ZERO
OF04     DS    0H
         STH   COUNT,OFUNCNT       SET MARGIN VALUE FOR FORMAT
         SPACE
OF05     DS    0H
        $EXIT  RC=0                THAT'S ALL
         SPACE
OFE005   DS    0H
         MVI   EFLAG005,ERRSET     OPERAND INVALID
         B     OF05                AND GO TO TERMINATE
OFE012   DS    0H
         MVI   EFLAG012,ERRSET     INVALID OFFSET
         B     OF05                AND GO TO TERMINATE
OFE013   DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO LARGE
         B     OF05                AND GO TO TERMINATE
OFE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     OF05                AND GO TO TERMINATE
