***********************************************************************
*                                                                     *
*              .RV (READ VARIABLE) --- CONTROL                        *
*                                                                     *
***********************************************************************
         SPACE
RV      $ENTER ,
         L     X1,ARGSTART         REMEMBER OUT BEGINNINGS
        $CALL  GETSYM              GET SYMBOL FROM INPUT STRING
         BC    BOMIT,RV03          IF NOTHING, EXIT
         BC    15-BDNO,RV01        BRANCH IF VALID
        $IFON  SRNMFLG,SRNMTSYM,RVE008  BRANCH IF BAD SYMBOL
        $IFON  SRNMFLG,SRNMTSUB,RVE038  BRANCH IF BAD SUBSCRIPT
        $IFANY SRNMFLG,(SRNMTFR,SRNMTTO),RVE039  BR IF BAD SUBSTRING
         CLC   SRNMFR,SRNMFR+L'SRNMFR   'FROM' SUBSTR SPECIFIED .Q
         BNE   RVE039              ERROR IF YES
         CLC   SRNMTO,SRNMTO+L'SRNMTO   'TO' SUBSTR SPECIFIED .Q
         BNE   RVE039              ERROR IF YES
         SPACE
RV01     DS    0H
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION PLEASE
        $CALL  GETARG              ANY MORE DATA .Q
         BC    15-BOMIT,RVE005     YES, ERROR
         SPACE
         LA    COUNT,1             WE NEED ONE LINE
         ST    COUNT,CONSMAX       STORE COUNT IN PARM BLOCK
         XC    CONSFLAG,CONSFLAG   CLEAR CONSOLE FLAGS
        $ON    CONSFLAG,IO$JIGG    SPECIFY 'JIGGLE' TYPEBALL
        $ON    CONSFLAG,IO$UEOF    IGNORE ALL EOFS
        $ON    CONSFLAG,IO$IM      AND IMBED THIS FILE
         MVC   CONSGOTO,BLANKS     CLEAR GOTO TARGET
         SPACE
         LA    R1,CONSBLOK         'IMBED' THE CONSOLE FILE
        $CALL  RDFILE              CALL INPUT SUPERVISOR
         SPACE
        $CALL  TERMREAD            READ ONE LINE
         LH    COUNT,0(,R1)        LENGTH OF RESULT
         LTR   COUNT,COUNT         ANYTHING THERE .Q
         BZ    RV03                BRANCH IF NOTHING
         L     TEMP,ARGSTART       ENTER OPERAND HERE
         EX    COUNT,RVMVC         MOVE OPERAND
         MVI   0(TEMP),C'='        ADD EQUAL SIGN
         LA    TEMP,2(TEMP,COUNT)  POINT TO LAST CHAR
         ST    TEMP,ARGSTOP        END HERE
         ST    X1,ARGSTART         START HERE
         LA    COUNT,BUFF1-1       START OF NEW STRING
         SR    TEMP,COUNT          RESULTING LENGTH
         STH   TEMP,BUFF2LGZ       SET LENGTH FOR ANY ERRORS
        $CALL  SR                  MAKE ASSIGNMENT
         SPACE
RV02     DS    0H
        $CALL  TERMREAD            FIND OUR OWN EOF
         LH    COUNT,0(,R1)        GET LENGTH
         LTR   COUNT,COUNT         BETTER BE NULL
         BNZ   RV02                NO, TRY AGAIN
RV03     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
RVE005   DS    0H
         MVI   EFLAG005,ERRSET     TOO MANY OPERANDS
         B     RV03                GO TO RETURN
RVE008   DS    0H
         MVI   EFLAG008,ERRSET     BAD REFERENCE SYMBOL
         B     RV03                GO TO RETURN
RVE038   DS    0H
         MVI   EFLAG038,ERRSET     INVALID SUBSCRIPT
         B     RV03                GO TO RETURN
RVE039   DS    0H
         MVI   EFLAG039,ERRSET     INVALID SUBSTRING
         B     RV03                GO TO RETURN
         SPACE
RVMVC    MVC   1(*-*,TEMP),3(R1)   RV OPERAND STRING
