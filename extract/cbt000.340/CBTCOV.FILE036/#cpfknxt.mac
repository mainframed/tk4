***********************************************************************
*                                                                     *
*              INTERNAL ROUTINE 'CPFKNXT' --- LENGTH OF NEXT BLOCK    *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R1) -> CPFKFN BLOCK
*        ON EXIT   --  CC = BOMIT IF CHAIN EMPTY
*                      CC = BPOS  IF CHAIN NOT EMPTY
*                      CPFKFNF    LENGTH OF NEXT BLOCK
*                      (COUNT)    ADJUSTED LENGTH OF NEXT BLOCK
*                      (TEMP)     PTR TO LAST RECORD IN BLOCK
         SPACE
CPFKNXT $ENTER ,
         LR    X1,R1               INPUT PARM POINTER
         USING CPFKFN,X1           AND TELL THE ASSEMBLER
         XC    CPFKFNF,CPFKFNF     CLEAR RETURN LENGTH
         SR    COUNT,COUNT         INITIALIZE USER ADJUSTED LENGTH
         L     TEMP,CPFKFNA        ADDRESS OF FIRST QUEUED LINE
         LTR   TEMP,TEMP           ANY PRESENT .Q
         BNZ   CFNXT01             YES, CONTINUE
        $CC    OMIT                SET COND CODE
         B     CFNXT04             AND GO TO RETURN
         SPACE
CFNXT01  DS    0H
         USING FNQELEM,TEMP        QUEUED RECORD POINTER
        $IFON  FNQFLAG,FNQOVLY,CFNXT02  BRANCH IF OVERLAY LINE
        $IFON  FNQFLAG,FNQTC,CFNXT02    BRANCH IF TC LINE
         A     COUNT,F1            BUMP BLOCK LINE COUNT
CFNXT02  DS    0H
        $IFON  FNQFLAG,FNQEOB,CFNXT03   BRANCH OUT IF END OF BLOCK
         OC    FNQFCHN,FNQFCHN     ANY NEXT IN LINE .Q
         BZ    CFNXT03             BRANCH IF NOT
         L     TEMP,FNQFCHN        FORWARD LINE LINK
         B     CFNXT01             GO CHECK AGAIN
CFNXT03  DS    0H
         ST    COUNT,CPFKFNF       SAVE FIRST BLOCK LENGTH
         AH    COUNT,FNQNUMOP      ADD USER QUALIFICATION
        $CC    POS                 SET COND CODE
         SPACE
CFNXT04  DS    0H
        $RESTOR KEEP=(COUNT,TEMP)  RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
         SPACE
         DROP  TEMP,X1
