         MACRO
         REGEQU
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
***********************************************************************
         MEND
         EJECT
***********************************************************************
         MACRO
&LABEL   PARM
.*       THIS MACRO IS DESIGNED TO MOVE THE PARM INTO A DC DEFINED
.*       WITHIN THE USER PROGRAM BY THE NAME OF PARM
.*       IT WILL MOVE THE EXACT LENGTH OF THE PARM VIA AN EX INSTR.
&LABEL   L     1,0(1)
         LH    2,0(1)
         EX    2,#&SYSNDX
         B     @&SYSNDX
#&SYSNDX MVC   PARM(0),2(1)
@&SYSNDX EQU   *
         MEND
***********************************************************************
         EJECT
         MACRO
&NAME    TERME &PASS,&RC=O
         GBLC  &LAB
         GBLB  &PRIM
&NAME    DS    0H
         AIF   (&PRIM).TSTRC
         MNOTE 8,'PRIME MUST OCCUR BEFORE TERME'
         MEXIT
.TSTRC   AIF   ('&RC' EQ '(15)').RET
         AIF   ('&RC' EQ 'O').RET
         AIF   ('&RC'(1,1) EQ '(').ISAREG
         LA    15,&RC
         AGO   .RET
.ISAREG  ANOP
         LR    15,&RC
.RET     AIF   ('&PASS' NE 'PASS').NOPASS
         L     14,4(13)
         STM   0,1,20(14)
.NOPASS  ANOP
         B     &LAB.X
         MEND
***********************************************************************
         EJECT
         MACRO
&NAME    PRIME &REG,&SAVE,&CONS,&NTER,&EQU
.* THIS IS THE PRIME MACRO
.* ITS FUNCTION IS
.* 1.  ESTABLISH ADDRESSABILITY SAVE REGISTERS ACT AS ENTRY
.* 2.  GENERATE REG SAVE AREA OR DO GETMAIN
.* 3.  PROVIDE EXIT USED BY TERME MACRO
.* 4.  PROVIDE ADDIT SAVE AREAS FOR SUBROUTINES SO ONLY 1 BAL REG
         GBLA  &PERFD,&PERFL
         GBLC  &PERFR,&LAB,&SNTRY
         GBLB  &EQFLG,&PRIM
         LCLB  &REENT
         LCLA  &CNT,&LOOP,&INC
         LCLC  &BASE(3),&SCSECT,&PNAME
         AIF   (&PRIM).DUMB
&PRIM    SETB  1
&LAB     SETC  'ICC'.'&SYSNDX'
&SNTRY   SETC  '&LAB'.'A'
&SCSECT  SETC  '&SYSECT'
         AIF   (T'&NAME EQ 'O').CSCT0
         AIF   ('&SCSECT' NE '').CSCT1
&NAME    CSECT
&SCSECT  SETC  '&NAME'
         AGO   .CSCT0
.DUMB    MNOTE 8,'ONLY ONE PRIME MAY BE DEFINED'
         MEXIT
.CSCT1   MNOTE 'A CSECT IS ALREADY DEFINED.'
&PNAME   SETC  '&NAME'
.CSCT0   ANOP
          USING *,15
         AIF   ('&EQU' EQ '').A1
         REGEQU
.A1      AIF   ('&CONS' EQ '').A2
&PNAME   SAVE  (14,12),,&CONS
         AGO   .A3
.A2      ANOP
&PNAME   SAVE  (14,12),,&SCSECT
.A3       ANOP
         LA    14,&LAB
         ST    14,&SNTRY
         B     &SNTRY+4
         AIF   ('&SAVE' NE '').A4
&REENT   SETB  1
.A4      AIF   ('&REG' EQ '').A6
         AIF   (N'&REG GT 3).BAD3
         AIF   (N'&REG LT 2).B1
&CNT     SETA  N'&REG
&LOOP    SETA  1
&INC     SETA  &LOOP+1
.A5      AIF   (T'&REG(&LOOP) NE 'N').BAD1
&BASE(&LOOP) SETC '&REG'(&INC,K'&REG(&LOOP))
&INC     SETA  &INC+K'&REG(&LOOP)+1
&LOOP    SETA  &LOOP+1
         AIF   (&LOOP LE &CNT).A5
         AGO   .A7
.B1      ANOP
&BASE(1) SETC  '&REG'
         AGO   .B2
.A6      ANOP
&BASE(1) SETC  '12'
.B2      ANOP
&CNT     SETA  1
.A7      ANOP
&SNTRY   DS    F
          DROP 15
         AIF   (&CNT NE 3).A8
         LA    &BASE(3),2048                     ESTABLISH ADDRESSAB
         LA    &BASE(2),2048(&BASE(3),15)        WITH THREE
         LA    &BASE(3),2048(&BASE(3),&BASE(2))  BASE REGS
         LR    &BASE(1),15
         USING &SCSECT,&BASE(1),&BASE(2),&BASE(3)
         AGO   .A10
.A8      AIF   (&CNT NE 2).A9
         LA    &BASE(2),2048                     ESTABLISH ADRESSAB
         LA    &BASE(2),2048(&BASE(2),15)        WITH TWO
         LR    &BASE(1),15                       BASE REGS
         USING &SCSECT,&BASE(1),&BASE(2)
         AGO   .A10
.A9      AIF   (&CNT NE 1).BAD4
         LR    &BASE(1),15                       ESTABLISH ADRESSAB
         USING &SCSECT,&BASE(1)
.A10     AIF   (&REENT).A11
         LA    15,&SAVE
         AGO   .A14
.A11     AIF   ('&NTER' NE '').A12
&PERFL   SETA  0
&CNT     SETA  72
         AGO   .A13
.A12     AIF   (T'&NTER NE 'N').BAD5
&PERFL   SETA  &NTER
&PERFR   SETC  '14'
&PERFD   SETA  0
&CNT     SETA  72+&NTER*4
* GENERATED SAVE AREA EXTENDED FOR USE BY NTER AND XIT MACROS
.A13     ANOP
         GETMAIN R,LV=&CNT
         LR    15,1
.A14     ANOP
         LM    0,1,20(13)
         ST    13,4(15)                          CHAIN THE
         ST    15,8(13)                          SAVE AREAS
         LR    13,15
         L     15,&SNTRY
          BR   15
         AIF   (&REENT).A20
&LAB.X   L     13,4(13)
         AGO   .A21
.A20     ANOP
&LAB.X   LR    1,13
         L     13,4(13)
         FREEMAIN R,LV=&CNT,A=(1)
.A21     ANOP
         RETURN (14,12),T,RC=(15)
         AIF   (&REENT).A24
         AIF   ('&NTER' NE '').A22
&CNT     SETA  18
         AGO   .A23
.A22     AIF   (T'&NTER NE 'N').BAD5
&PERFL   SETA  &NTER
&PERFR   SETC  '14'
&PERFD   SETA  0
&CNT     SETA  &NTER+18
* GENERATED SAVE AREA EXTENDED FOR USE BY NTER AND XIT MACROS
.A23     ANOP
&SAVE    DC    &CNT.F'0'
.A24     ANOP
         LTORG
&LAB     DS    0H
         MEXIT
.BAD1    MNOTE 8,'FIRST PARAMETER/S MUST BE SELF DEFINING'
         MEXIT
.BAD3    ANOP
.BAD4    MNOTE 8,'THIS MACRO WILL HANDLE 1-3 BASE REGS'
         MEXIT
.BAD5    MNOTE 8,'THIRD PARAMETER MUST BE SELF DEFINING'
         MEND
         PRINT NOGEN
         TITLE 'DUMPCOPY PROGRAM'
*          DATA SET DUMPCOPY   AT LEVEL 001 AS OF 08/29/79
**********************************************************************
*                                                                    *
*    DUMPCOPY                                                        *
*              THIS PROGRAM IS DESIGNED TO COPY A                    *
*              SYS1.DUMP DATA SET FROM FILE DUMPIN                   *
*              TO A DYNAMICALLY CREATED FILE ON THE                  *
*              DISK PACK SPECIFIED IN THE PARM.                      *
*                                                                    *
*              A SYSPRINT FILE IS CREATED WHICH CONTAINS             *
*              THE DATASET NAME OF THE OUTPUT FILE AND               *
*              A COUNT OF THE # OF RECORDS IN THE O/P FILE.          *
*                                                                    *
*              THE DUMPLOG FILE IS A MOD DATA SET THAT CONTAINS      *
*              THE DSN, CPUID, DATE, TIME, AND TITLE OF THE DUMP     *
*              THAT IS MIGRATED.                                     *
*                                                                    *
*              THE JCL TO EXECUTE DUMPCOPY ARE:                      *
*                                                                    *
*                   //A EXEC PGM=DUMPCOPY,PARM='VOLSER,COMM'         *
*                   //SYSPRINT DD SYSOUT=A                           *
*                   //DUMPIN   DD DSN=SYS1.DUMPXX,DISP=SHR           *
*                   //DUMPLOG  DD DSN=#A1000.DUMPLOG,DISP=MOD        *
*                                                                    *
*                VOLSER = VOLSER OF OUTPUT SERIAL NUMBER             *
*                COMM   = NAME OF GROUP DUMP IS TO GO TO OR BLANK    *
*                         (UP TO 5 CHARACTERS)                       *
*                                                                    *
*        MACROS : PRIME,PARM,TIME,RDJFCB,CVT,TERME,                  *
*                 DYNALLOC,ABEND,OPEN,CLOSE,GET,PUT,DATEROU,         *
*                 CALL,DCB,REGEQU,IEFZB4D0,IEFZB4D2,IEFJFCBN,        *
*                 AMDDATA                                            *
*                                                                    *
**********************************************************************
DUMPCOPY PRIME (11,12),,,,Y
         PARM
         EJECT
*        PROCESS THE PARM
*
         MVC   PARMX(6),PARM
         OC    PARM+7(5),=5X'40'  INSURE ALL UPPER CASE
         MVC   COMM(5),PARM+7    MOVE COMM FIELD
         MVC   FL3COMM(5),PARM+7    MOVE COMM FIELD
         RDJFCB (DUMPIN)
         LA    R5,JFCB
         USING INFMJFCB,R5
         MVC   DSNNO,JFCBDSNM+9    MOVE INPUT DSN NUMBER
         MVC   IDSN(24),JFCBDSNM   MOVE INPUT DSN TO MSG
         MVC   IVOLSER(6),JFCBVOLS MOVE VOLSER OF INPUT D/S
         CLI   JFCBVOLS+5,C'2'     CPU M
         BE    M                   YES
         CLI   JFCBVOLS+5,C'3'     CPU J
         BE    J                   YES
         CLI   JFCBVOLS+5,C'4'     CPU K
         BE    K                   YES
         CLI   JFCBVOLS+5,C'5'     CPU L
         BE    L                   YES
         CLI   JFCBVOLS+5,C'G'     CPU G
         BE    G                   YES
         MVI   DSNSID,C'$'         CPU UNKNOWN
         B     IDMOVED
J        MVI   DSNSID,C'J'
         B     IDMOVED
K        MVI   DSNSID,C'K'
         B     IDMOVED
L        MVI   DSNSID,C'L'
         B     IDMOVED
G        MVI   DSNSID,C'G'
         B     IDMOVED
M        MVI   DSNSID,C'M'
IDMOVED  EQU   *
         SPACE
         LA    R9,10
         OPEN  DUMPIN
READHDR  GET   DUMPIN           READ AN INPUT RECORD
         LR    R10,R1
         USING PRDINPUT,R10
         CLI   PRDRECID,PRDHDREC HEADER RECORD ?
         BE    HEADER           YES - GO PROCESS IT
         BCT   R9,READHDR       NO LOOP THRU
         ABEND 2,DUMP           NO HEADER RECORD FOUND
ALLOC    L     R3,CVTPTR          -> CVT
         USING CVTMAP,R3
         MVC   VOLSER(6),PARM   MOVE VOLSER INTO ALLOC PARAMETER
         MVC   DSNAME(DSNL),DSNM MOVE DSN INTO PARAMETER
         LA    R1,RB            ADDRESS OF REQUEST BLOCK
         USING S99RB,R1        AND ESTAB. ADDRESSABILITY
         LA    R2,S99RBEND-S99RB LOAD LENGTH OF RB
         STC   R2,S99RBLN        AND STORE IT FOR USE
         OI    S99VERB,S99VRBAL  SPECIFY ALLOCATION
         OI    S99FLG11,S99NOCNV DO NOT USE AN EXISTING ALLOCATION
         LA    R2,TXTPTRS        ADDRESS OF TEXT POINTERS
         ST    R2,S99TXTPP       STORE INTO RB
         DROP  R1
         LA    R1,RBPTR          -> TO RB
         DYNALLOC
         LTR  R15,R15           TEST RETURN
         BZ   OK                ALLOCATION WAS OK
         ABEND 1,DUMP           DYNAMIC ALLOCATION ERROR
OK       EQU   *
         DROP  R5
         CLOSE (DUMPIN,REREAD)
         OPEN  (DUMPIN,,SYSPRINT,OUTPUT,DUMPOUT,OUTPUT,DUMPLOG,OUTPUT)
         MVI   OPENSW,C'Y'
         SPACE
READ     GET   DUMPIN           READ AN INPUT RECORD
         LR    R10,R1
PUTIT    PUT   DUMPOUT,(10)     DON'T STOP - PUT IT OUT
         AP    CTR,=P'01'       ADD TO RECORD COUNTER
         B     READ             CONTINUE THE READ UNTIL
         EJECT
HEADER   EQU   *                DUMP HEADER ROUTINE
         USING PRDINPUT,R10
         MVC   TOD,PRDTODVL SAVE DATE
         LINK  EP=BLSUMTOD,PARAM=(TOD,TDATE),VL=1
         MVC   DSNT(2),DTIME
         MVC   DSNT+2(2),DTIME+3
         MVC   TITLE,PRDTITLE   MOVE TITLE TO OUTPUT LINE
         DROP  R10
         MVC   DSND(2),TDATE
         MVC   DSND+2(2),TDATE+3
         MVC   DSND+4(2),TDATE+6
         B     ALLOC            BACK TO READ NEXT RECORD
         EJECT
EOJ      EQU   *
         CLI   OPENSW,C'Y'
         BNE   EXIT
         UNPK  COUNT(6),CTR     UNPACK RECORD COUNTER
         OI    COUNT+5,X'F0'    SET FULL DISPLAY
         PUT   SYSPRINT,MSGA    MESSAGE OF DSNAME OF INPUT
         PUT   SYSPRINT,MSGB    MESSAGE OF DSNAME OF OUTPUT
         PUT   SYSPRINT,TITLINE AND PRINT WHAT WE HAVE
         MVC   DUMPDSN(DSNL),DSNM
         MVC   DUMPTLE,TITLE
         PUT   DUMPLOG,DUMPDATA
         MVC   FL4TITLE,TITLE      MOVE TITLE
         MVC   FL5DSN(DSNL),DSNM
         PUT   SYSPRINT,FL1
         PUT   SYSPRINT,FL2
         PUT   SYSPRINT,FL3
         PUT   SYSPRINT,FL4
         PUT   SYSPRINT,FL5
         PUT   SYSPRINT,FL6
         PUT   SYSPRINT,FL7
         PUT   SYSPRINT,FL8
         PUT   SYSPRINT,FL9
         PUT   SYSPRINT,FL10
         PUT   SYSPRINT,FL11
         PUT   SYSPRINT,FL12
         PUT   SYSPRINT,FL13
         PUT   SYSPRINT,FL14
         PUT   SYSPRINT,FL15
         PUT   SYSPRINT,FL16
         PUT   SYSPRINT,FL17
CLOSE    CLOSE (SYSPRINT,,DUMPIN,FREE,DUMPOUT,FREE,DUMPLOG)
EXIT     TERME
         EJECT
         LTORG
         SPACE
RBPTR    DC    X'80',AL3(RB)
RB       DC    20X'00'          DYNAMIC ALLOCATION REQUEST BLOCK
TXTPTRS  DC    A(DSN,STAT,DISP,CDISP,SPACEC,PRIM,SEC)
         DC    A(RLSE,VOL,DEV)
         DC    X'80',AL3(DDN)   END OF PARAMETERS
WORK     DC    F'0'             WORK AREA FOR DATE/TIME
CTR      DC    PL4'0'           COUNT FOR RECORDS WRITTEN
TOD DC         D'00'            WORK AREA FOR DUMP TIME/DATE
EDMASK   DC    X'21204B20204B2020'  TIME EDIT MASK
OPENSW   DC    C'N'             TO ELIMINATE 0C1 ABEND WHEN NO DUMP
         SPACE
         PRINT NOGEN
DUMPIN   DCB   DSORG=PS,MACRF=GL,DDNAME=DUMPIN,EODAD=EOJ,EXLST=DMPEXT
         SPACE
DUMPOUT  DCB   DSORG=PS,MACRF=PM,DDNAME=DUMPOUT,RECFM=F,BLKSIZE=4104,  X
               LRECL=4104,OPTCD=W
         SPACE
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,BLKSIZE=121,RECFM=FA
DUMPLOG DCB    DSORG=PS,MACRF=PM,DDNAME=DUMPLOG,                       X
               RECFM=FB,LRECL=155
         PRINT GEN
         EJECT
PARM     DC    CL20' '
         DS    0F
DMPEXT   DC    X'87',AL3(JFCB)
JFCB     DC    CL176' '
         SPACE 2
MSGA     DC    CL5'1DSN='
DSNM     DC    C'#A1000.DUMP'
DSNNO    DC    CL2'  ',C'.'
DSNSID   DC    C' ',C'.D'
DSND     DC    CL6' ',C'.T' DSN DATE
DSNT     DC    CL4' '       DSN TIME
DSNL     EQU   *-DSNM       LENGTH OF MESSAGE
         DC    C'  VOL=SER='
PARMX    DC    CL6'AVPPUA'  VOLSER OF OUTPUT
         DC    C'   RECORDS WRITTEN = '
COUNT    DC    CL6' ',CL80' '
         SPACE
MSGB     DC    C'0INPUT DSN='   INPUT DSNAME
IDSN     DC    CL24' '
         DC    C'VOL=SER='      INPUT VOLSER
IVOLSER  DC    CL6' '
         DC    CL80' '    FILLER
DUMPDATA DS    0CL155
COMM     DC    CL6' '
         DC    CL2' '
DUMPDSN  DC    CL31' '
         DC    CL18' '
DUMPTLE  DC    CL100' '
         EJECT
TITLINE  DC    CL6' '
TDATE    DC    CL9' '
DTIME    DC    CL8' '
         DC    CL4' '
TITLE    DC    CL100' '
         EJECT
DSN      DC    AL2(2)     DSNAME KEY
         DC    AL2(1),AL2(DSNL) COUNT AND LENGTH
DSNAME   DC    CL44' '    DSNAME FOR ALLOCATION
         SPACE
STAT     DC    AL2(4),AL2(1),AL2(1)  STATUS KEY,COUNT,LENGTH
         DC    X'04'      NEW
         SPACE
DISP     DC    AL2(5),AL2(1),AL2(1)  DISP KEY,COUNT,LENGTH
         DC    X'02'      CATLG
         SPACE
CDISP    DC    AL2(6),AL2(1),AL2(1)  C-DISP KEY,COUNT,LENGTH
         DC    X'04'      DELETE
         SPACE
SPACEC   DC    AL2(8),AL2(0)         TRACK ALLOCATION KEY
         SPACE
PRIM     DC    X'000A',AL2(1),AL2(3)  PRIM ALLOC QTY KEY,COUNT,LENGTH
PRIML    DC    AL3(10)     PRIMARY SPACE DEFAULT=300 TRACKS
         SPACE
SEC      DC    X'000B',AL2(1),AL2(3)  SECONDARY ALLOCATION KEY,COUNT,L
SECL     DC    AL3(05)     SEC ALLOC LENGTH
         SPACE
RLSE     DC    X'000D',AL2(0)          RELEASE UNUSED SPACE KEY
         SPACE
VOL      DC    X'0010'                 VOL SER KEY
         DC    AL2(1)                  COUNT
         DC    AL2(6)                  LENGTH OF VOLSER
VOLSER   DC    CL6'AVPPUA'             VOLSER DEFAULT = AVPPUA
         SPACE
DEV      DC    X'0015'                 DEVICE TYPE KEY
         DC    AL2(1)
         DC    AL2(8)                  LENGTH OF DEV NAME
         DC    CL8'SYSALLDA'           ASSUME SYSALLDA
         SPACE
DDN      DC    X'0001'                 DDNAME KEY
         DC    AL2(1)                  COUNT OF DDN'S
         DC    AL2(7)                  LENGTH OF DDNAME
         DC    CL7'DUMPOUT'            DDNAME
         EJECT
******************************************************************
*        THIS AREA CONTAINS THE DUMP FORM USED TO ROUTE          *
*        INFORMATION ON SYSTEM DUMPS TO THE RESPONSIBLE          *
*        GROUP                                                   *
******************************************************************
FL1      DC    CL121'1            SYSTEM DUMP NOTIFICATION FORM'
FL2      DC    C'0',CL5' ',CL115'FROM : CONTROL PROGRAM SERVICES '
FL3      DC    CL6' ',C'TO   : '
FL3COMM  DC    CL120' '            TO GROUP
FL4      DC    CL6'0'
         DC    C'DUMP TITLE :'
         DC    CL7' '
FL4TITLE DC    CL100' '            DUMP TITLE + FILLER
FL5      DC    CL6'0'
         DC    C'DATA-SET NAME :'
         DC    CL4' '
FL5DSN   DC    CL110' '
FL6      DC    CL6' '
         DC    C'FROM DUMP D/S :'
         DC    CL4' ',9C'_',C'×',CL4' ',C'×',CL5' ',C'×',CL5' ',C'×'
         DC    CL100' '            FILLER
FL7      DC    CL6' '
         DC    C'TAKEN ON CPU :'
         DC    CL5' ',14C'_',C'×',CL5' ',C'×',CL5' ',C'×'
         DC    CL80' '             FILLER
FL8      DC    CL6' '
         DC    C'DATE (MMDDYY) :'
         DC    CL4' ',20C'_',C'×',CL5' ',C'×'
         DC    CL80' '             FILLER
FL9      DC    CL6' '
         DC    C'TIME (HHMMSS) :'
         DC    CL4' ',26C'_',C'×'
         DC    CL80' '             FILLER
FL10     DC    CL6'0'
         DC    CL115'PLEASE SUPPLY ME WITH THE SPR # IF THIS DUMP IS '
FL11     DC    CL6' '
         DC    CL115'SPR''D OR INFORM ME WHEN THE DUMP IS NO LONGER'
FL12     DC    CL6' '
         DC    CL115'NEEDED AND IT WILL BE DELETED.....'
FL13     DC    CL6'0'
         DC    CL115'THIS DATA-SET WILL BE DELETED IN 7 DAYS FROM '
FL14     DC    CL6' '
         DC    CL115'CREATION UNLESS I AM INFORMED OTHERWISE!'
FL15     DC    CL6'0'
         DC    CL115'PLEASE CHECK AND RETURN :'
FL16     DC    CL6'0'
         DC    C'THE SPR NUMBER IS _____ - HOLD THIS DUMP UNTIL'
         DC    CL100' THE SPR IS CLOSED'
FL17     DC    CL6'0'
         DC    CL115'THIS DUMP MAY BE DELETED _______'
******************************************************************
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         EJECT
         DSECT
         IEFJFCBN
         EJECT
         AMDDATA
         EJECT
         CVT   DSECT=YES
         END
