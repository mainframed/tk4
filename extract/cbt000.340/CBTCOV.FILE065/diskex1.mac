*          DATA SET DISKEX1    AT LEVEL 001 AS OF 01/30/80
         SAVEALL (R12),CSECT=DISKEX1
         L     R2,0(R1)                GET PARM ADDR
         SR    R1,R1
         CH    R1,0(R2)                IS THERE A PARM ?
         BE    SETDFLT                 NO - SET DEFAAULTS
         LH    R1,0(R2)
         CH    R1,=H'10'
         BNE   INVPARM
         CLC   2(7,R2),=C'PASSES='     IS IT A VALID PARM ?
         BNE   INVPARM
         CLC   9(3,R2),=C'001'
         BL    INVPARM
         CLC   9(3,R2),=C'999'
         BH    INVPARM
         PACK  WORK(8),9(3,R2)
         CVB   R10,WORK
         B     GETIOA
INVPARM  EQU   *
         WTO   'INVALID PARM FIELD'
         B     RETURN
SETDFLT  EQU   *
         LA    R10,1
GETIOA   EQU   *
         OPEN  (PRINT,OUTPUT)
         OPEN  (TEST1,OUTPUT)
         LH    R3,TEST1+62
         GETMAIN R,LV=(R3)
         LR    R2,R1
         STM   R2,R3,MVCL
         LM    R2,R5,MVCL
         MVCL  R2,R4
         L     R9,MVCL
         WTO   '***** FORMATTING DATA SET ******'
INLOOP   EQU   *
         WRITE INA,SF,TEST1,(R9),'S'
         C     R15,=F'8'
         BNE   CK1
         MVI   ENLOOP+1,X'00'
CK1      CHECK INA
ENLOOP   B     INLOOP
         CLOSE TEST1
         EJECT
         OPEN  (TESTA,UPDAT)
         SR    R5,R5
         ICM   R5,7,TESTA+17           NUM BLKS
         ST    R5,NUMBLKS
         OPEN  (TESTB,UPDAT)
         SR    R8,R8                   ERR COUNTER
         SR    R7,R7                   READ COUNTER
         SR    R6,R6                   WRITE COUNTER
         B     *+16
NEWPASS  EQU   *
         BAL   R5,PRTERR
         BCT   R10,*+8
         B     EOJ
         L     R5,NUMBLKS
         SRL   R5,1
         LA    R4,1(R4)
         CVD   R4,WORK
         UNPK  PASSNUM(3),WORK+6(2)
         OI    PASSNUM+2,X'F0'
         LA    R1,WTO
         SVC   35
         MVI   WM+1,X'F0'
         MVI   RM+1,X'F0'
         LA    R2,0
         L     R3,NUMBLKS
         BCTR  R3,0
WRITE    STCM  R2,7,BLKADDRA
         STCM  R3,7,BLKADDRB
WM       B     LPA
         WRITE WA,DI,,(R9),,,MF=E
         CHECK WB
         WRITE WB,DI,,(R9),,,MF=E
         CHECK WA
         B     WINCR
LPA      WRITE WA,DI,,(R9),,,MF=E
         WRITE WB,DI,,(R9),,,MF=E
         CHECK WA
         MVI   WM+1,X'00'
WINCR    EQU   *
         LA    R6,2(R6)
         LA    R2,1(R2)
         BCTR  R3,0
         BCT   R5,WRITE
         L     R5,NUMBLKS
         SRL   R5,1
         L     R3,NUMBLKS
         BCTR  R3,0
         LA    R2,0
READ     STCM  R2,7,BLKADDRA
         STCM  R3,7,BLKADDRB
RM       B     LPB
         READ  RA,DI,,(R9),,,MF=E
         CHECK RB
         READ  RB,DI,,(R9),,,MF=E
         CHECK RA
         B     RINCR
LPB      READ  RA,DI,,(R9),,,MF=E
         READ  RB,DI,,(R9),,,MF=E
         CHECK RA
         MVI   RM+1,X'00'
RINCR    EQU   *
         LA    R2,1(R2)
         BCTR  R3,0
         LA    R7,2(R7)
         BCT   R5,READ
         B     NEWPASS
         EJECT
PRTERR   STM   R0,R15,ERRSAVE
         LA    R2,ERRTBLXX
         CVD   R4,WORK
         UNPK  PNUM(3),WORK+6(2)
         OI    PNUM+2,X'F0'
         CVD   R8,WORK
         MVC   ENUM(12),MASK
         ED    ENUM(12),WORK+3
         CVD   R6,WORK
         MVC   WNUM(12),MASK
         ED    WNUM(12),WORK+3
         CVD   R7,WORK
         MVC   RNUM(12),MASK
         ED    RNUM(12),WORK+3
         PUT   PRINT,PRTLINE
         LTR   R8,R8
         BZ    ENDPRT
         L     R1,TERR
         AR    R1,R8
         ST    R1,TERR
         LA    R8,100
         S     R8,TBLCOUNT
LOOP     EQU   *
         USING ERRTBL,R2
         UNPK  RCODE(3),RC(2)
         MVI   RCODE+2,C' '
         TR    RCODE(2),TRANSTBL
         UNPK  OPCDE(3),OP(2)
         MVI   OPCDE+2,C' '
         TR    OPCDE(2),TRANSTBL
         UNPK  CSWCDE(5),CSW(3)
         MVI   CSWCDE+4,C' '
         TR    CSWCDE(4),TRANSTBL
         UNPK  DEBCDE(5),DECBERR(3)
         MVI   DEBCDE+4,C' '
         TR    DEBCDE(4),TRANSTBL
         UNPK  IOBCDE(5),IOBSENSE(3)
         MVI   IOBCDE+4,C' '
         TR    IOBCDE(4),TRANSTBL
         UNPK  CCHCDE(11),CCHHR(6)
         MVI   CCHCDE+10,C' '
         TR    CCHCDE(10),TRANSTBL
         PUT   PRINT,LIST
         LA    R2,13(R2)
         BCT   R8,LOOP
         LA    R8,ERRTBLXX
         ST    R8,TBLENTRY
ENDPRT   EQU   *
         L     R1,TWRT
         AR    R1,R6
         ST    R1,TWRT
         L     R1,TRDS
         AR    R1,R7
         ST    R1,TRDS
         LM    R0,R15,ERRSAVE
         LA    R6,100
         ST    R6,TBLCOUNT
         SR    R6,R6
         SR    R7,R7
         SR    R8,R8
         BR    R5
EOJ      EQU   *
         L     R8,TERR
         MVC   PRTLINE+1(22),=CL22'  TOTALS   '
         CVD   R8,WORK
         MVC   ENUM(12),MASK
         ED    ENUM(12),WORK+3
         L     R8,TWRT
         CVD   R8,WORK
         MVC   WNUM(12),MASK
         ED    WNUM(12),WORK+3
         L     R8,TRDS
         CVD   R8,WORK
         MVC   RNUM(12),MASK
         ED    RNUM(12),WORK+3
         PUT   PRINT,PRTLINE
         CLOSE (TESTA,,TESTB,,PRINT)
RETURN   RESTORE R,RC=0
         EJECT
MVCL     DC    3F'0'
         DC    X'0F',AL3(0)
BLKADDRA DC    AL3(0)
BLKADDRB DC    AL3(0)
WTO      DS    0F
         DC    AL2(MSGLNGTH+4)
         DC    AL2(0)
BMSG     DC    C'BEGIN PASS '
PASSNUM  DC C'   '
MSGLNGTH EQU   *-BMSG
WORK     DC    D'0'
NUMBLKS  DC    F'0'
TERR     DC    F'0'
TWRT     DC    F'0'
TRDS     DC    F'0'
TRANSTBL EQU   *-C'0'
         DC    C'0123456789ABCDEF'
PRTLINE  DS    0CL133
         DC    C'0   PASS NUMBER =  '
PNUM     DC    C'   '
         DC    C'    ERROR COUNT =  '
ENUM     DS    CL12
         DC    C'    NO. WRITES  =  '
WNUM     DS    CL12
         DC    C'    NO. READS   =  '
RNUM     DS    CL12
         DC    CL20' '
MASK     DC    X'402020206B2020206B202120'
LIST     DS    0CL132
         DC    CL2' '
         DC    C'REASON '
RCODE    DC    CL2' '
         DC    CL2' '
         DC    C'OP CODE'
         DC    CL2' '
OPCDE    DC    CL5' '
         DC    CL2' '
         DC    C'CSW  STATUS'
         DC    CL2' '
CSWCDE   DC    CL5' '
         DC    CL2' '
         DC    C'DECB CODE'
         DC    CL2' '
DEBCDE   DC    CL5' '
         DC    CL2' '
         DC    C'IOB SENSE'
         DC    CL2' '
IOBCDE   DC    CL5' '
         DC    CL2' '
         DC    C'CCHHR    '
         DC    CL2' '
CCHCDE   DC    CL10' '
         DC    CL34' '
         READ  RA,DI,TESTA,,'S',0,BLKADDRA,MF=L
         READ  RB,DI,TESTB,,'S',0,BLKADDRB,MF=L
         WRITE WA,DI,TESTA,,'S',0,BLKADDRA,MF=L
         WRITE WB,DI,TESTB,,'S',0,BLKADDRB,MF=L
TESTA    DCB   DDNAME=TESTA1,DSORG=DA,MACRF=(RIC,WIC),SYNAD=ERREXIT,   X
               OPTCD=R,RECFM=F
TESTB    DCB   DDNAME=TESTA1,DSORG=DA,MACRF=(RIC,WIC),SYNAD=ERREXIT,   X
               OPTCD=R,RECFM=F
         EJECT
TEST1    DCB   DDNAME=TEST1,DSORG=PS,MACRF=(WL),RECFM=F
         EJECT
PRINT    DCB   DDNAME=PRINT1,DSORG=PS,MACRF=PM,RECFM=FBA,              X
               LRECL=133,BLKSIZE=1330
         LTORG
         EJECT
         DS    0H
ERREXIT  EQU   *
         USING *,R15
         LA    R8,1(R8)
         STM   R0,R15,ERRSAVE
         L     R12,TBLCOUNT
         LTR   R12,R12
         BZ    EXIT
         BCTR  R12,0
         ST    R12,TBLCOUNT
         L     R11,TBLENTRY
         USING ERRTBL,R11
         STCM  R1,8,RC
         LR    R1,R0
         TM    2(R1),X'08'             WRITE R0
         BO    EXIT                    YES DO NOTHING
         MVC   DECBERR(2),1(R1)
         L     R2,16(R1)               IOB ADDR
         MVC   CSW(2),12(R2)
         MVC   CCHHR(5),35(R2)
         ICM   R3,7,9(R2)
         S     R3,=F'8'
         MVC   OP(1),0(R3)
         MVC   IOBSENSE(2),2(R2)
         LA    R11,13(R11)
         ST    R11,TBLENTRY
EXIT     EQU   *
         LM    R0,R15,ERRSAVE
         BR    R14
ERRSAVE  DS    16F
TBLENTRY DC    A(ERRTBLXX)
ERRTBLXX DC    100XL13'00'
TBLCOUNT DC    A(100)
         LTORG
ERRTBL   DSECT
RC       DS    CL1
OP       DS    CL1
CSW      DS    CL2
DECBERR  DS    CL2
IOBSENSE DS    CL2
CCHHR    DS    CL5
TBLSIZE  EQU   *-ERRTBL
         END
