//SILKDMS1 JOB (BPXDVS401,XX,0607),'DMS 6.0 - IGC0021G',
//         CLASS=A,MSGCLASS=X,NOTIFY=SILK
//*
//DMSSVC   EXEC  ASMFCL,PARM.ASM=(OBJECT,NODECK,RENT),
//          PARM.LKED=(LIST,LET,XREF,RENT),
//          A='X'                             <===OUTPUT CLASS
//ASM.SYSLIB  DD  DISP=SHR,DSN=SYS1.MACLIB,DCB=BLKSIZE=32760
//            DD  DISP=SHR,DSN=SYS1.AMODGEN
//ASM.SYSIN  DD *
IGC0021G TITLE 'DMS SVC-217 FOR MVS   -   WITH PGM EXEMPTION'
* NAME   ADSMVS60 (TRUE NAME IS IGC0021G)
*
* FUNCTION   DMS SVC FOR MVS OPERATING SYSTEMS
*
* DESCRIPTION
*        ALL IBM LOGIC REQUIRED TO MAINTAIN THE S.U. 60 FIELDS HAS
*        BEEN CONSOLIDATED INTO THIS DMS SVC.  THE SVC IS INVOKED FROM
*        JUST ONE OPEN/CLOSE/END-OF-VOLUME MODULE (MODULE IFG0194A,
*        CSECT IFG0194C, LABEL DSCUPD) AT THE POINT WHERE CONTROL IS
*        NORMALLY GIVEN TO THE 'S.U.60-UPDATE-CODE'.
*
*        THE DSCB UPDATE CAN BE BYPASSED UNDER ANY OF THE FOLLOWING
*        CIRCUMSTANCES.
*            1. DSNAME  OR PREFIX   IS IN THE DSN EXEMPTION TABLE.
*            2. VOLUME  OR PREFIX   IS IN THE VOL EXEMPTION TABLE.
*            3. JOBNAME OR PREFIX   IS IN THE JOB EXEMPTION TABLE.
*            4. PROGRAM OR PREFIX   IS IN THE PGM EXEMPTION TABLE.
*
*        THE IDENTICAL MEANS USED BY S.U. 60 IS ALSO USED BY THIS SVC
*        TO CAUSE AN UPDATED DSCB TO BE WRITTEN BACK.
*
*        THE SVC UPDATES S.U. 60 FIELDS PLUS DMS DEFINED FIELDS.
*          S.U. 60 FIELDS MAINTAINED:
*            1. THE LAST USE (REFERENCE) DATE -------- AT OFFSET 75.
*            2. THE CHANGE BIT DEFINED WITHIN THE BYTE AT OFFSET 93.
*
*          ADDITIONAL DMS FIELDS MAINTAINED:
*            1. THE LAST MODIFY DATE ----------------- AT OFFSET 48
*            2. LAST USING JOB NAME ------------------ AT OFFSET 62
*            3. FIRST USING JOB NAME --------- 5-BYTES AT OFFSET 70
*                                    --------- 3-BYTES AT OFFSET 45
*            4. CUMULATIVE OPEN COUNT ---------------- AT OFFSET 78
*            5. SVC MODE (TYPE 1, 2, OR 3) ----------- AT OFFSET 103
*
* REGISTER CONTENTS UPON ENTRY TO AN SVC
*        REG  0 - CONTENTS AS WHEN SVC WAS EXECUTED.
*        REG  1 - CONTENTS AS WHEN SVC WAS EXECUTED.
*        REG  2 - UNPREDICTABLE
*        REG  3 - ADDRESS OF THE CVT
*        REG  4 - ADDRESS OF THE TCB
*        REG  5 - ADDRESS OF THE SVRB (IF TYPE 2, 3, OR 4 SVC)
*               - ADDRESS OF THE RB (IF TYPE 1 SVC)
*                 (NOTE - DMS SVC MUST BE TYPE 3 OR 4)
*        REG  6 - SVC ENTRY POINT ADDRESS
*        REG  7 - ADDRESS OF THE ASCB
*        REGS 8,9,10,11,12 - UNPREDICTABLE
*        REG 13 - CONTENTS AS WHEN SVC WAS EXECUTED.
*        REG 14 - RETURN ADDRESS
*        REG 15 - CONTENTS AS WHEN SVC WAS EXECUTED.
         EJECT
* LOGIC FLOW
*        - ESTABLISH ADDRESSABILITY TO THE O/C/E WORK AREA.
*        - INIT INDICATOR AREA (DXWORK2) TO MEAN NO UPDATE DONE.
*        - GET ADDRESS OF OPEN'S PARM LIST  (R7  FROM SVRB SAVE AREA)
*        - GET ADDRESS OF DSCB BEING OPENED (R11 FROM SVRB SAVE AREA)
*        - IF DSCB BEING OPENED IS A FORMAT 1,
*             IF DSCB BEING OPENED IS NOT VSAM DSORG (DATA SPACE),
*                DETERMINE IF THIS OPEN IS EXEMPT FROM UPDATING DSCB.
*                - IS ANY PROGRAM NAME IN THE RB CHAIN EXEMPT?
*                - IS THE VOLUME CONTAINING THIS DATA SET EXEMPT?
*                - IS THIS DATA SET EXEMPT?
*                - IS THIS JOB EXEMPT?
*                IF NOT EXEMPT,
*                   CONVERT TODAYS DATE TO BINARY YDD FORM.
*                   IF LAST USE DATE NOT EQUAL TO TODAYS DATE,
*                      SET LAST USE DATE TO TODAYS DATE.
*                      SET FLAG TO CAUSE DSCB WRITE BACK.
*                   IF BEING OPENED FOR OUTPUT,
*                      IF CHANGE BIT IS OFF,
*                         SET CHANGE BIT ON.
*                         SET FLAG TO CAUSE DSCB WRITE BACK.
*                   IF DMS DATA FIELDS BEING MAINTAINED,
*                      SET FLAG TO CAUSE DSCB WRITE BACK.
*                      IF BEING OPENED FOR OUTPUT,
*                         SET LAST MOD DATE TO TODAY'S DATE
*                      SET LAST USING JOB TO THIS JOBNAME.
*                      IF OPEN COUNT IS ZERO,
*                         SET THE FIRST USING JOB TO THIS JOBNAME.
*                      INCREMENT THE OPEN COUNT BY 1.
*        - RETURN
*
* SPECIAL NOTES
*     1. CHANGE ALL REFERENCES TO 'ADSMVS60' TO THE TRUE SVC NAME
*        BEFORE ASSEMBLING.
*
*     2. BACK OUT ANY OPEN ZAPS USED FOR EARLIER VERSIONS OF THE SVC
*        BEFORE APPLYING THE ZAP TO ACTIVATE THIS ONE.  NOTE THAT THE
*        ACTIVATING ZAP FOR REL 3.0 & 3.1 WAS TO IGC0001I IFG0196W.
*        THE ACTIVATING ZAP FOR RELEASE 4.0 AND SUBSEQUENT RELEASES
*        IS TO IFG0194A IFG0194C.
*
*     3. THE SVC HAS ONLY A SINGLE BASE REGISTER.  AN EXCESSIVE NUMBER
*        OF ADDITIONAL ENTRIES ADDED TO THE EXEMPTION TABLES MAY LEAD
*        TO ADDRESSABILITY ERRORS WHEN ASSEMBLING THE SVC.
*
*     4. IF THE ADDITIONAL DMS DATA FIELDS ARE NOT DESIRED, CHANGE THE
*        VALUE OF SYMBOL 'SVCMODE' FROM C'1' TO C'2' BEFORE ASSEMBLING.
*        IF IT IS DESIRED THAT THE DMS FIELDS BE MAINTAINED FOR EVERY
*        OPEN, CHANGE THE VALUE OF 'SVCMODE' TO A C'3'.
*
*        SPECIFY SYSTEMPARM 'DSCBLMOD000' IN THE DMS SYSTEMPARM TABLE.
*        SPECIFY SYSTEMPARM 'DSCBCRJD000' IN THE DMS SYSTEMPARM TABLE.
*        SPECIFY SYSTEMPARM 'DSCBUSJD000' IN THE DMS SYSTEMPARM TABLE.
*        SPECIFY SYSTEMPARM 'DSCBOPCD000' IN THE DMS SYSTEMPARM TABLE.
*
* MAPPING MACROS USED
*        IECDSECS - GENERAL MAPPING MACRO FOR SEVERAL CONTROL BLOCKS.
*                   USED HERE FOR 'MAIN' - THE O/C/E WORK AREA MAPPING,
*                             AND 'RB'   - THE REQUEST BLOCK MAPPING.
*
*        THESE MACROS ARE FOUND IN SYS1.AMODGEN WHICH MUST BE AVAIL-
*        ABLE FOR THE ASSEMBLY OF THIS SVC VIA THE SYSLIB DD STMT.
*
* UPDATES
*   3/23/81 KK  EXCLUSION TABLE FOR PROGRAM NAMES ADDED.
*   3/23/81 KK  JOBNAME STARTING WITH DOLLAR SIGN MOVED TO JOBTABLE.
*   3/23/81 KK  DMS SVC, DATE, TIME, AND A.D.S. ADDED AS DOCUMENTATION.
*   3/23/81 KK  EXCLUSION TABLES GIVEN ZAP EXPANSION AREAS.
*   3/23/81 KK  STORE CLOCK INSTRUCTION AND RECORDING OF LAST MOD TIME
*               HAVE BEEN ELIMINATED SINCE DMS NO LONGER USES IT.
*   3/23/81 KK  IBM MAPPING MACRO USED TO MAP O/C/E WORK AREA AND
*               REFERENCE WORK AREA FIELDS (FMT1 DSCB FIELDS).  THE RB
*               MAPPING MACRO IS ALSO USED TO STEP THROUGH THE RB CHAIN
*               LOOKING FOR PROGRAM NAMES.
*   3/23/81 KK  UPDATE CONCATENATED DATA SETS CORRECTLY BY USING THE
*               CORRECT BASE REG FOR THE DSCB FIELDS.  (1ST DSCB IS AT
*               START OF O/C/E AREA, BUT OTHERS ARE CHAINED FROM PTR AT
*               END OF EACH DSCB AREA.)
*   4/21/81 KK  PROVIDE ONLY SU60 FUNCTION VIA SIMPLE ZAP
*   1/06/82 KK  PMR 2793, 0C4 IN DSN EXEMPT COMPARE FOR MULTIVOL ISAM
*   3/31/83 TJP ALLOW 3 DIFFERENT SVC MODES TO BE SELECTED.
*   3/31/83 TJP PMR 3631-DO NOT UPDATE MOD DATE AND LAST 3 BYTES OF JOB
*               NAME IN MULTI-VOLUME DSN, UNLESS PROCESSING 1ST VOLUME.
         EJECT
         PRINT NOGEN
IGC0021G CSECT                         DMS SVC 217
         USING *,RBAS                  ESTABLISH ADDRESSABILITY
         SPACE 1
RLEN     EQU   1
RCDE     EQU   2                       CDE                  ADDRESS
RCVT     EQU   3                       CVT                  ADDRESS
ROCE     EQU   4      (TCB @ AT ENTRY) O/C/E WORK AREA      ADDRESS
RSVB     EQU   5                       SVRB                 ADDRESS
RBAS     EQU   6                       BASE (SVC ENTRY PT)  ADDRESS
RPRM     EQU   7                       PARM LIST FOR OPEN   ADDRESS
RBCT     EQU   8                       BRANCH ON COUNT LOOPS.
RJOB     EQU   9                       JOBNAME (IN TIOT)    ADDRESS
RRBL     EQU   10                      RB                   ADDRESS
RDS1     EQU   11                      DSCB (BEING OPENED)  ADDRESS
RWRK     EQU   12                      WORK REG.
RPTR     EQU   15                      EXEMPT TABLE WORK REG.
********************************************************************
* - REGISTER RDS1 MUST BE USED TO REFERENCE THE FOLLOWING FIELDS - *
* - OFFSETS TO DMS MAINTAINED FIELDS HAS 44 SUBTRACTED BECAUSE   - *
* - ONLY THE DATA PORTION OF THE DSCB IS POINTED TO, NOT THE KEY.- *
DMSMODDT EQU 48-44                     LAST MOD DATE.              *
DMSUSEDT EQU 75-44                     LAST USE (REF) DATE.        *
DMSUSJOB EQU 62-44                     LAST USING JOB NAME.        *
DMSCRJO5 EQU 70-44                     1ST  USING JOB NAME(5 BYTES)*
DMSCRJO3 EQU 45-44                     1ST  USING JOB NAME(3 BYTES)*
DMSOPCNT EQU 78-44                     OPEN COUNT.                 *
DMSDSIND EQU 93-44                     CHANGE BIT.                 *
DMSSVCMO EQU 103-44                    DMS/OS SVC MODE  3/31/83 TP *
********************************************************************
         B     BEGIN
         DC    CL8'DMS SVC '
         DC    C'&SYSTIME'
         DC    C' '
         DC    C'&SYSDATE'
         DC    C' '
         DC    C'LABS'                                       3/31/83 TP
BEGIN    DS   0H
         L     RJOB,12(,ROCE)          TCB + 12 IS TIOT ADDRESS, IT
*                                      HAS JOBNAME AS FIRST THING.
         L     ROCE,48(,RSVB)          GET R4  FROM SVRB SAVE AREA, IT
         LA    ROCE,0(,ROCE)           HAS ADDRESS OF O/C/E AREA.
         L     RPRM,60(,RSVB)          GET R7  FROM SVRB SAVE AREA, IT
*                                      HAS ADDRESS OF OPENS PARM LIST.
         L     RDS1,76(,RSVB)          GET R11 FROM SVRB SAVE AREA, IT
         LA    RDS1,0(,RDS1)           HAS ADDRESS OF FMT1 DSCB.
         USING FORCORE,ROCE            ADDRESSABILITY FOR O/C/E AREA.
         CLI   DSCFMTID-DSCFMTID(RDS1),C'1'  A FORMAT 1 DSCB?
         BNE   RETURN                  NO
         XC    DXWORK2(2),DXWORK2   FLAG INDICATES DSCB NOT UPDATED.
* EXCLUDE VSAM SPACES FROM LAST USE RECORDING
         TM    DSCFILTY-DSCFMTID+1(RDS1),X'08'  DSORG = AM? (VSAM)
         BO    RETURN                  YES,SKIP IT,VSAM SPACE NOT AGED.
         EJECT
* EXCLUDE PROGRAMS FROM LAST USE RECORDING
         LH    RBCT,PGMCOUNT           GET NO. OF PGMS IN EXCLUDE TBL.
         LTR   RBCT,RBCT               ARE THERE ANY ENTRIES.
         BZ    VOLCHECK                NO - GO TO NEXT EXCLUSION TEST.
         LR    RRBL,RSVB               CURRENT SVRB FOR THIS SVC.
         USING RBBASIC,RRBL
         L     RRBL,RBLINK             POINT TO SVRB FOR OPEN.
         L     RRBL,RBLINK             NORMALLY THIS SHOULD BE PRB
         SPACE 2
RBLOOP   DS    0H
         CLI   RBSTAB,RBFTPRB          IS THIS A PGM REQUEST BLK.
         BNE   NEXTRB                  NO - GO SET UP NEXT RB LOOP
         L     RCDE,RBCDE              CDE ADDRESS.
         SR    RLEN,RLEN               INIT FOR PGMLOOP.
         LA    RPTR,PGMTABLE           INIT FOR PGMLOOP.
         LH    RBCT,PGMCOUNT           INIT FOR PGMLOOP.
         SPACE 2
PGMLOOP  DS    0H                * * * THIS IS A LOOP WITHIN RBLOOP.
         IC    RLEN,0(,RPTR)           LENGTH OF PGM NAME.
         LTR   RLEN,RLEN               IS THIS AN ACTIVE ENTRY.
         BZ    VOLCHECK                NO - GO TO NEXT EXCLUDE TST
         BCTR  RLEN,0
         CLC   008(*-*,RCDE),1(RPTR)   CDENTRY
         EX    RLEN,*-6                IS PGMNAME EXCLUDED.
         BE    RETURN                  YES.
         LA    RPTR,2(RLEN,RPTR)       BUMP.
         BCT   RBCT,PGMLOOP            LOOP THRU PGMNAMES
*        ENDLOOP
         SPACE 2
NEXTRB   DS    0H
         TM    RBSTAB2,RBTCBNXT        IS NEXT RB A TCB.
         BO    VOLCHECK                YES, PGM NOT EXEMPT.
         L     RRBL,RBLINK             GET NEXT RB ADDR.
         B     RBLOOP                  LOOP THRU RB'S.
*        ENDLOOP
         DROP  RRBL
         EJECT
* EXCLUDE VOLUMES FROM LAST USE RECORDING
VOLCHECK DS    0H
         LH    RBCT,VOLCOUNT           GET NO. OF VOLS IN EXCLUDE TBL
         LTR   RBCT,RBCT               ARE THERE ANY ENTRIES.
         BZ    DSNCHECK                NO - GO TO NEXT EXCLUSION TEST.
         SR    RLEN,RLEN
         LA    RPTR,VOLTABLE
         SPACE 1
VOLLOOP  DS    0H
         IC    RLEN,0(,RPTR)           LENGTH OF VOL SERIAL.
         LTR   RLEN,RLEN               IS THIS AN ACTIVE ENTRY.
         BZ    DSNCHECK                NO - THEN NEXT EXCLUDE TEST.
         BCTR  RLEN,0
         CLC   506(*-*,ROCE),1(RPTR)   DXVOLMT2
         EX    RLEN,*-6                IS VOLUME EXCLUDED?
         BE    RETURN                  YES
         LA    RPTR,2(RLEN,RPTR)
         BCT   RBCT,VOLLOOP
         SPACE 3
* EXCLUDE DATA SETS FROM LAST USE RECORDING
DSNCHECK DS    0H
         LH    RBCT,DSNCOUNT           GET NO. OF DSN'S IN EXCLUDE TBL
         LTR   RBCT,RBCT               ARE THERE ANY ENTRIES.
         BZ    JOBCHECK                NO - GO TO NEXT EXCLUSION TEST.
         SR    RLEN,RLEN
         LA    RPTR,DSNTABLE
         LA    RWRK,100(ROCE)          PT TO DSN IN JFCB AREA
         CR    RDS1,ROCE               IS THIS FIRST DSCB, OR CONCAT?
         BE    DSNLOOP                 1ST, SO USE DSN IN JFCB.
         TM    DSCFILTY-DSCFMTID(RDS1),X'80'    DSORG = ISAM ?  1/6/82
         BO    DSNLOOP                 YES, USE JFCB DSN.       1/6/82
         LA    RWRK,0(RDS1)            PT TO DSCB WITHOUT KEY
         SH    RWRK,=H'44'             PT TO DSN(KEY) IN FRONT OF DSCB
         SPACE 1
DSNLOOP  DS    0H
         IC    RLEN,0(,RPTR)           LENGTH OF DSNAME.
         LTR   RLEN,RLEN               IS THIS AN ACTIVE ENTRY.
         BZ    JOBCHECK                NO - THEN NEXT EXCLUDE TEST
         BCTR  RLEN,0
         CLC   000(*-*,RWRK),1(RPTR)   JFCBDSNM OR DSCB DSN.
         EX    RLEN,*-6                IS DSN IN EXCLUDE TABLE?
         BE    RETURN                  YES - BYPASS UPDATE
         LA    RPTR,2(RLEN,RPTR)
         BCT   RBCT,DSNLOOP
         EJECT
* EXCLUDE JOB NAMES FROM LAST USE RECORDING
JOBCHECK DS    0H
         LH    RBCT,JOBCOUNT           GET NO. OF JOBNAMES IN TABLE.
         LTR   RBCT,RBCT               ARE THERE ANY ENTRIES.
         BZ    MODIFY                  NO - GO MODIFY DSCB.
         SR    RLEN,RLEN
         LA    RPTR,JOBTABLE
         SPACE 2
JOBLOOP  DS    0H
         IC    RLEN,0(,RPTR)           LENGTH OF JOBNAME.
         LTR   RLEN,RLEN               ARE THERE ANY ENTRIES.
         BZ    MODIFY                  NO - GO MODIFY DSCB.
         BCTR  RLEN,0
         CLC   000(*-*,RJOB),1(RPTR)   TIOCNJOB
         EX    RLEN,*-6                IS THIS JOBNAME EXEMPT?
         BE    RETURN                  YES
         LA    RPTR,2(RLEN,RPTR)
         BCT   RBCT,JOBLOOP
         EJECT
* UPDATE FMT 1 DSCB FIELDS (S.U. 60 AND DMS FIELDS)
MODIFY   EQU   *
         MVC   DMSSVCMO(1,RDS1),SVCMODE   SVC MODE IN DSCB   3/31/83 TP
         ZAP   96(8,RSVB),58(2,RCVT)       DDDF
         CVB   RLEN,96(,RSVB)
         STH   RLEN,106(,RSVB)             SAVE DD OF YDD
         MVO   96(8,RSVB),57(1,RCVT)       YY
         CVB   RLEN,96(,RSVB)
         STC   RLEN,105(,RSVB)             SAVE  Y  OF YDD
         CLC   DMSUSEDT(3,RDS1),105(RSVB)  IS LAST USE DATE THE SAME?
         BE    CHEKCBIT
         MVC   DMSUSEDT(3,RDS1),105(RSVB)  SET NEW LAST USE DATE
         MVC   DXWORK2(2),UPDATE           INDICATOR, DSCB UPDATED.
CHEKCBIT EQU   *
         TM    0(RPRM),X'0F'               OPEN FOR OUTPUT?
         BZ    DMSTEST                     NO, DON'T SET CHANGE BIT.
         SPACE 2
         TM    DMSDSIND(RDS1),X'02'        CHANGE BIT ALREADY ON?
         BO    DMSTEST                     YES
         OI    DMSDSIND(RDS1),X'02'        TURN ON CHANGE BIT.
         MVC   DXWORK2(2),UPDATE           DSCB UPDT REQ'D - DXWORK2
         EJECT
DMSTEST  EQU *
***********************************************************************
* THE SVCMODE FIELD DESIGNATES WHICH TYPE OF UPDATING IS DESIRED BY   *
* THE INSTALLATION.  THE SUPPORTED TYPES ARE:                         *
*   1 - UPDATE LAST USED AND CHANGE BIT ONLY WHEN A CHANGE            *
*       IS DETECTED.  KEEP DMS STATISTICS.                            *
*   2 - UPDATE LAST USED AND CHANGE BIT ONLY WHEN A CHANGE            *
*       IS DETECTED.  DO NOT UPDATE DMS STATISTICS. (SU 60 MODE)      *
*   3 - UPDATE LAST USED AND CHANGE BIT EVERY TIME THE DATA SET       *
*       IS OPENED.  UPDATES ALL DMS STATISTICS.                       *
***********************************************************************
         CLI   SVCMODE,C'3'       ARE STATS ALWAYS UPDATED?  3/31/83 TP
         BE    DMSDATA            YES - UPDATE DMS STATS     3/31/83 TP
         CLI   SVCMODE,C'2'       SU 60 MODE SPECIFIED?      3/31/83 TP
         BE    RETURN             YES - NO MORE UPDATES      3/31/83 TP
*---- MUST BE TYPE 1 MODE - THE DEFAULT VALUE                3/31/83 TP
         TM    DXWORK2+1,X'01'  USE DATE OR CHANGE BIT UPDATED? 3/31/83
         BZ    RETURN           NO - DO NOT UPDATE DMS STATS 3/31/83 TP
         SPACE
DMSDATA  DS    0H
* THE FOLLOWING CODE UPDATES THE ADDITIONAL DMS DEFINED FIELDS
         MVC   DXWORK2(2),UPDATE          INDICATOR, DSCB UPDATED.
         SPACE 2
MODDATE  DS    0H
         TM    0(RPRM),X'0F'              OPEN FOR OUTPUT?
         BZ    USINGJOB                   NO, DON'T SET MOD DATE.
*---- DO NOT UPDATE MODDATE IF NOT FIRST VOLUME OF MULTI-VOLUME 3/31/83
*---- DATA SET. IT CAUSES PROBLEMS WITH CERTAIN RELEASES OF MVS 3/31/83
         CLC   DSCVOLSR-DXDSCB(2,RDS1),=H'1'    FIRST VOLUME?   3/31/83
         BH    USINGJOB                   NO - DON'T UPDATE     3/31/83
         MVC   DMSMODDT(3,RDS1),DMSUSEDT(RDS1)  MOD DATE=USE DATE.
USINGJOB DS    0H
         MVC   DMSUSJOB(8,RDS1),0(RJOB)   LAST USING JOB NAME
         CLC   DMSOPCNT(4,RDS1),=XL4'00'  OPEN COUNT=ZERO
         BNE   INCROPEN
         MVC   DMSCRJO5(5,RDS1),0(RJOB) 1ST USING JOB - 5 BYTES
*---- DO NOT UPDATE JOBNAME IF NOT FIRST VOLUME OF MULTI-VOLUME 3/31/83
*---- DATA SET. IT CAUSES PROBLEMS WITH CERTAIN RELEASES OF MVS 3/31/83
         CLC   DSCVOLSR-DXDSCB(2,RDS1),=H'1'    FIRST VOLUME?   3/31/83
         BH    INCROPEN                   NO - DON'T UPDATE     3/31/83
         MVC   DMSCRJO3(3,RDS1),5(RJOB) 1ST USING JOB - 3 BYTES
         SPACE 2
INCROPEN DS   0H                          INCREMENT THE OPEN COUNT
         ICM   RLEN,B'1111',DMSOPCNT(RDS1)
         AL    RLEN,=F'1'
         LTR   RLEN,RLEN
         BNP   SETTOONE                   SET TO ONE IF NOT POSITIVE.
         STCM  RLEN,B'1111',DMSOPCNT(RDS1)
         B     RETURN
SETTOONE EQU   *
         MVC   DMSOPCNT(4,RDS1),=F'1'
         SPACE 2
RETURN   EQU   *
         BR    14
         EJECT
UPDATE   DC    H'1'               INDICATOR TO FORCE DSCB WRITE BACK.
SVCMODE  DC    C'1'  <=== DMS/OS SVC MODE (REFER TO INSTALLATION GUIDE)
         SPACE 1
********************************************************************
*     ----- DSCB UPDATE EXCLUSION/EXEMPTION TABLES -----
* BINARY ZERO PATCH AREAS ARE SUPPLIED FOR EACH TABLE.  ADDITIOANL
* ENTRIES MAY BE ADDED AND THE PATCH AREA REMOVED OR ADJUSTED, AS LONG
* AS THE 4K ADDRESSABILITY OF THE BASE REGISTER IS NOT EXCEEDED.
*********************************************************************
*              PGM EXCLUSION/EXEMPTION TABLE
*********************************************************************
PGMCOUNT DC    H'1'               NUMBER OF TABLE ENTRIES
PGMTABLE DS   0H
         DC    AL1(5),C'ADSMI'    ADS MODULE INTERFACE PROGRAMS.
*
         DC    45X'00'            ROOM FOR FIVE 9-BYTE ENTRIES.
         SPACE 1
*********************************************************************
*              DSN EXCLUSION/EXEMPTION TABLE
*********************************************************************
DSNCOUNT DC    H'3'               NUMBER OF TABLE ENTRIES
DSNTABLE DS    0H
         DC    AL1(4),C'SYS8'     1980'S SYSTEM TEMPORARY DATASETS.
         DC    AL1(4),C'SYS9'     1990'S SYSTEM TEMPORARY DATASETS.
         DC    AL1(7),C'**SYSUT'  IEHMOVE TEMPORARY DATASETS.
*
         DC 270X'00'              ROOM FOR SIX 45-BYTE ENTRIES.
         SPACE 1
*********************************************************************
*              VOL EXCLUSION/EXEMPTION TABLE
*********************************************************************
VOLCOUNT DC    H'0'               NUMBER OF TABLE ENTRIES
VOLTABLE DS    0H
         DC    AL1(6),C'VOLSER'   SAMPLE ENTRY ONLY, NOTE ZERO COUNT.
*
         DC    70X'00'            ROOM FOR TEN 7-BYTE ENTRIES.
         SPACE 1
*********************************************************************
*              JOB EXCLUSION/EXEMPTION TABLE
*********************************************************************
JOBCOUNT DC    H'1'              NUMBER OF TABLE ENTRIES
JOBTABLE DS    0H
         DC    AL1(1),C'$'       SPECIAL JOBS - 1ST CHAR. = DOLLAR SIGN
*
         DC    45X'00'           ROOM FOR FIVE 9-BYTE ENTRIES.
         SPACE 1
         LTORG
         SPACE 1
         ORG
         DC    C'IGC0021G'       PUT CSECT NAME AT END OF MODULE.
         PRINT GEN
         IECDSECS (MAIN,(IOB,NO)),(RB),EXPAND=YES
         END
//LKED.SYSLMOD DD DISP=SHR,DSN=SYS1.LPALIB
//LKED.SYSIN   DD *
  NAME  IGC0021G(R)
//*
