COMMAND  TITLE 'C O M M A N D  --  ISSUE SYSTEM OPERATOR COMMANDS V2.0'
***********************************************************************
*                                                                     *
*                           C O M M A N D                             *
*                                                                     *
*                   ISSUE SYSTEM OPERATOR COMMANDS                    *
*                                                                     *
*                                                                     *
*     THIS PROGRAM ACCEPTS SYSTEM OR JES OPERATOR COMMANDS AS         *
*     INPUT AND EXECUTES THEM USING SVC 34. IT MUST BE APF            *
*     AUTHORIZED.                                                     *
*                                                                     *
*     COMMAND CAN OPERATE IN ONE OF THREE MODES:                      *
*                                                                     *
*     1) BATCH OR CALLED PROGRAM                                      *
*        IN THIS MODE COMMAND ACCEPTS INPUT AS EITHER 80-BYTE         *
*        CARD IMAGES FROM SYSIN OR A SINGLE COMMAND FROM THE PARM     *
*        FIELD. THE RESPONSE TO THE OPERATOR COMMAND WILL             *
*        APPEAR ON ALL OPERATOR CONSOLES. FOR EXAMPLE:                *
*                                                                     *
*           //STEP1    EXEC PGM=COMMAND,PARM='VARY 017,ONLINE'        *
*                                                                     *
*           - OR -                                                    *
*                                                                     *
*           //STEP1    EXEC PGM=COMMAND                               *
*           //SYSIN    DD *                                           *
*           VARY 017,ONLINE                                           *
*           /*                                                        *
*                                                                     *
*        IF COMMANDS ARE FETCHED FROM SYSIN, A DELAY OF 2 SECONDS     *
*        WILL BE ADDED BETWEEN COMMANDS.                              *
*                                                                     *
*     2) TSO COMMAND PROCESSOR                                        *
*        IN THIS MODE COMMAND EXPECTS TO FIND THE OPERATOR COMMAND    *
*        DIRECTLY FOLLOWING ON THE TSO INPUT LINE. THE RESPONSE TO    *
*        OS COMMANDS WILL THEN BE DISPLAYED ON THE TSO TERMINAL.      *
*        RESPONSE TO JES COMMANDS WILL APPEAR ON YOUR TERMINAL        *
*        PROVIDED A LOCAL MODIFICATION IS MADE TO JES2 TO ALLOW       *
*        IT TO INTERCEPT THE COMMAND AND SET THE RESPONSE TO TSO.     *
*        OTHERWISE, JES2 COMMANDS WILL APPEAR ON ALL CONSOLES.        *
*        FOR EXAMPLE:                                                 *
*                                                                     *
*           COMMAND D T         <--- (RESPONSE WILL APPEAR ON CRT)    *
*                                                                     *
*     3) CALLABLE SUBROUTINE                                          *
*        IN THIS MODE COMMAND ACCEPTS INPUT ACCORDING TO STANDARD     *
*        OS LINKAGE CONVENTIONS. IN ADDITION TO THE COMMAND ITSELF,   *
*        A SECOND PARAMETER MAY BE PASSED WHICH CONTAINS THE          *
*        CONSOLE NUMBER WHERE YOU WISH THE RESPONSE TO APPEAR.        *
*        (THE SVC 34 WILL ALSO ASSUME THE AUTHORITY OF THAT           *
*        CONSOLE.) FOR EXAMPLE:                                       *
*                                                                     *
*           LA       R1,CMDADDR                                       *
*           LA       R15,=V(COMMAND)                                  *
*           BALR     R14,R15                                          *
*                                                                     *
*           CMDADDR  DC    A(OPERCMD)                                 *
*           CNSADDR  DC    XL1'80',AL3(CONSOLE)                       *
*           OPERCMD  DC    H(3),CL3'D T'                              *
*           CONSOLE  DC    F'5'                                       *
*                                                                     *
*        THIS WOULD MAKE IT APPEAR AS IF 'D T' WERE ISSUED FROM       *
*        CONSOLE 5. (THIS METHOD IS USED BY PROGRAM 'SPY')            *
*                                                                     *
*        COMMAND MAY BE USED TO ISSUE REPLIES TO WTORS.               *
*                                                                     *
*     AUTHOR:                                                         *
*       THE ORIGINAL VERSION OF COMMAND WAS WRITTEN BY MARK SORKIN.   *
*       THE CURRENT VERSION HAS BEEN HEAVILY MODIFIED AT SCE.         *
*       QUESTIONS AND COMMENTS MAY BE SENT TO                         *
*                                                                     *
*          STEVE LANGLEY                                              *
*          SOUTHERN CALIFORNIA EDISON                                 *
*          P.O. BOX 800                                               *
*          ROSEMEAD, CALIF. 91770                                     *
*          1-213-572-3435                                             *
*                                                                     *
*     IMPLEMENTOR:                                          *HMD 07/81*
*       THIS PROGRAM HAS BEEN REVISED AND IMPLEMENTED AT    *HMD 07/81*
*       GTE DATA SERVICES, MARINA DEL REY, CA BY:           *HMD 07/81*
*                                                           *HMD 07/81*
*          HOWARD M. DEAN                                   *HMD 07/81*
*          GTE DATA SERVICES                                *HMD 07/81*
*          4750 LINCOLN BLVD.                               *HMD 07/81*
*          MARINA DEL REY, CALIF. 90291                     *HMD 07/81*
*          PHONE - (213) - 821-0511                         *HMD 07/81*
*                                                           *HMD 07/81*
*       CHANGES MADE BY THE ABOVE ARE MARKED WITH           *HMD 07/81*
*       '*HMD XX/XX*' IN COL 61-71. XX/XX IS THE            *HMD 07/81*
*       CHANGE DATE (MONTH AND YEAR).                       *HMD 07/81*
***********************************************************************
         EJECT
COMMAND  CSECT
         SPACE 3
R0       EQU   0                  REGISTER 0
R1       EQU   1                  REGISTER 1
R2       EQU   2                  WORK REGISTER
R3       EQU   3                  WORK REGISTER
R4       EQU   4                  WORK REGISTER
R5       EQU   5                  WORK REGISTER
R6       EQU   6                  WORK REGISTER             *HMD 07/81*
R7       EQU   7                  WORK REGISTER             *HMD 07/81*
R8       EQU   8                  WORK REGISTER             *HMD 07/81*
R9       EQU   9                  WORK REGISTER             *HMD 07/81*
R10      EQU   10                 PARAMETER LIST ADDRESS REGISTER
R11      EQU   11                 SUBROUTINE LINKAGE REGISTER
R12      EQU   12                 BASE REGISTER
R13      EQU   13                 SAVE AREA ADDRESS REGISTER
R14      EQU   14                 REGISTER 14
R15      EQU   15                 REGISTER 15
         EJECT
         SAVE  (14,12)            SAVE CALLER'S REGISTERS
         LR    R12,R15            ESTABLISH
         USING COMMAND,R12        ADDRESSABILITY
         LA    R3,SAVEAREA        GET OUR SAVEAREA ADDRESS
         ST    R13,SAVEAREA+4     ESTABLISH BACK
         ST    R3,8(R13)          AND FORWARD SAVEAREA POINTERS
         LR    R13,R3             ESTABLISH OUR SAVEAREA ADDRESS
         LR    R10,R1             SAVE PARAMETER REGISTER
         SPACE 2
         L     R2,CVTPTR          R2 = ADDR OF CVT
         USING CVT,R2
         L     R3,CVTTCBP         R3 = ADDR OF MAGIC QUADWORD
         L     R4,12(R3)          R4 = ADDR OF OUR ASCB
         USING ASCB,R4
         MVC   ASID,ASCBASID      SAVE OUR ASID
         DROP  R4
         L     R4,CVTCUCB         ADDR OF 'CUCB' (UCM BASE) *HMD 07/81*
         DROP  R2                 NO NEED FOR CVT BASE NOW  *HMD 07/81*
         USING UCM,R4             ADDRESS THE UCM           *HMD 07/81*
         LR    R2,R4              R2 = R4                   *HMD 07/81*
         S     R2,=F'4'           R2 = ADDR OF ADDR OF UCMP *HMD 07/81*
         L     R2,0(R2)           R2 = ADDR OF UCM PREFIX   *HMD 07/81*
         USING UCMPRFX,R2         ADDRESS THE PREFIX        *HMD 07/81*
         L     R5,UCMVEA          R5 = ADDR OF 1ST UCM ENTRY*HMD 07/81*
         L     R6,UCMVEZ          R6 = LNGTH OF EACH ENTRY  *HMD 07/81*
         L     R7,UCMVEL          R7 = ADDR OF LAST UCM ENT.*HMD 07/81*
         SLR   R8,R8              R8 = CONSOLE COUNTER      *HMD 07/81*
UCMLOOP  LA    R8,1(R8)           R8 = CONSOLE NUMBER       *HMD 07/81*
         C     R5,UCMMCENT        IS THIS THE MASTER CON    *HMD 07/81*
         BNE   UCMNEXT            NO, CONTINUE LOOKING      *HMD 07/81*
         ST    R8,R0MASK          SAVE MSTR CONSOLE NUMBER  *HMD 07/81*
         B     INITDONE           DONE WITH THIS NOW        *HMD 07/81*
UCMNEXT  AR    R5,R6              BUMP TO NEXT UCM ENTRY    *HMD 07/81*
         CR    R5,R7              END OF UCM'S?             *HMD 07/81*
         BL    UCMLOOP            NO, CONTINUE              *HMD 07/81*
         DROP  R2,R4              GET RID OF BASES          *HMD 07/81*
         SPACE 3
INITDONE DS    0H                 CHECK OUT INPUT PARM
         MVI   CARD,C' '          CLEAR OUT COMMAND AREA    *HMD 07/81*
         MVC   CARD+1(L'CARD-1),CARD                        *HMD 07/81*
         TM    0(R10),X'80'       IS FIRST THE ONLY PARM?
         BO    ONEPARM            YES; ONLY PARM
         TM    4(R10),X'80'       IS SECOND THE LAST PARM?
         BO    TWOPARMS           YES; TWO PARMS (CALLED WITH CNXX)
***********************************************************************
*                                                                     *
*     A X'80' WAS NOT FOUND IN THE HIGH ORDER BYTE OF THE FIRST       *
*     TWO PARMS, SO WE CAN ASSUME COMMAND IS BEING CALLED AS A CP.    *
*     PLACE OUR ASID IN THE R0 MASK AND CONTINUE.                     *
*                                                                     *
***********************************************************************
         XR    R2,R2              CLEAR R2
         ICM   R2,B'0011',ASID    R2 = ASID OF CALLER
         O     R2,=XL4'00008000'  TELL SVC34 ITS AN ASID
         ST    R2,R0MASK          AND OVERRIDE THE R0 MASK
         L     R3,0(R10)          R3 = ADDR OF INPUT LINE
         LA    R4,4(R3)           R4 = ADDR OF FIRST CHAR OF INPUT
         AH    R4,2(R3)           R4 = ADDR OF FIRST CHAR PAST COMMAND
         LH    R2,0(R3)           R2 = TOTAL LENGTH OF STRING
         SH    R2,=H'4'           R2 = LENGTH MINUS HEADER  *HMD 07/81*
         SH    R2,2(R3)           R2 = LENGTH OF OPER CMD   *HMD 07/81*
         BNP   EOJ                IS NO COMMAND GOTO EOJ    *HMD 07/81*
         CH    R2,=H'100'         IS LENGTH TOO LONG?       *HMD 07/81*
         BNH   LENOK              LENGTH IS OK              *HMD 07/81*
         LA    R2,100             ONLY 1ST 100 BYTES        *HMD 07/81*
LENOK    BCTR  R2,0               R2 = R2 - 1 FOR EX MVC
         EX    R2,MOVE1           MOVE CMD INTO CARD
*************************************************************HMD 07/82*
** THE FOLLOWING CODE IS NECESSARY THE ENABLE THIS COMMAND  *HMD 07/82*
** TO WORK UNDER MVS/SP RELEASE 3. THIS IS DUE TO A CHANGE  *HMD 07/82*
** IN THE COMMAND SCHEDULER CSCB CREATION MODULE IEE0803D.  *HMD 07/82*
**                                                          *HMD 07/82*
** IN MVS/SP 3, IF AN ASID IS PRESENT IN THE XSA, THEN THE  *HMD 07/82*
** COMMAND SCHEDULER MOVES YOUR TSO USERID INTO THE NEWLY   *HMD 07/82*
** CREATED CSCB. WHY THIS IS DONE, WHO KNOWS. IT CAUSES,    *HMD 07/82*
** HOWEVER, THE STC INITIALIZATION ROUTINE IEEPRWI2 TO      *HMD 07/82*
** THINK THAT A 'SYSTEM' ADDRESS SPACE IS BEING CREATED     *HMD 07/82*
** DUE TO THE CHKEY FIELD OF THE CSCB CONTAINING YOUR       *HMD 07/82*
** USERID INSTEAD OF ZERO. BECAUSE THIS ADDRESS SPACE       *HMD 07/82*
** INITIALIZATION ROUTINE NAME FOR SYSTEM ADDRESS SPACES    *HMD 07/82*
** IS CONTAINED IN THE CHKEY FIELD, IEEPRWI2 ATTEMPT TO     *HMD 07/82*
** LINK TO YOUR TSO USERID TO INITIALIZE THE STARTED        *HMD 07/82*
** ADDRESS SPACE. THUS AN ABEND806-4 AND A RESULTING SVC    *HMD 07/82*
** DUMP.                                                    *HMD 07/82*
**                                                          *HMD 07/82*
** SINCE RESPONSE AT THE TERMINAL IS NOT REQUIRED FOR       *HMD 07/82*
** START AND MOUNT COMMANDS (AND THAT IS THE ONLY PLACE     *HMD 07/82*
** THIS CRAZY THING HAPPENS) WE WILL ISSUE THOSE COMMANDS   *HMD 07/82*
** WITH REGISTER ZERO EQUAL TO ZERO.                        *HMD 07/82*
*************************************************************HMD 07/82*
         CLC   =C'LOGON ',CARD    LOGON COMMAND?            *HMD 07/82*
         BE    EOJ                DON'T BE SILLY            *HMD 07/82*
         CLC   =C'logon ',CARD    LOGON COMMAND?            *HMD 07/82*
         BE    EOJ                DON'T BE SILLY            *HMD 07/82*
         CLC   =C'START ',CARD    IS IT A START COMMAND?    *HMD 07/82*
         BE    STARTM             YES..CLEAR R0MASK         *HMD 07/82*
         CLC   =C'S ',CARD        START COMMAND?            *HMD 07/82*
         BE    STARTM             YES, CLEAR R0MASK         *HMD 07/82*
         CLC   =C'MOUNT ',CARD    MOUNT COMMAND?            *HMD 07/82*
         BE    STARTM             YES, CLEAR R0MASK         *HMD 07/82*
         CLC   =C'M ',CARD        MOUNT COMMAND?            *HMD 07/82*
         BE    STARTM             NO COMTINUE WITH COMMANDS *HMD 07/82*
         CLC   =C'start ',CARD    L         C               *HMD 07/82*
         BE    STARTM               O          A            *HMD 07/82*
         CLC   =C's ',CARD            W            S        *HMD 07/82*
         BE    STARTM                   E             E     *HMD 07/82*
         CLC   =C'mount ',CARD            R                 *HMD 07/82*
         BE    STARTM                                       *HMD 07/82*
         CLC   =C'm ',CARD                                  *HMD 07/82*
         BNE   DOCMD              NO COMTINUE WITH COMMANDS *HMD 07/82*
STARTM   XC    R0MASK,R0MASK      CLEAR REGISTER 0 MASK     *HMD 07/82*
DOCMD    B     DOSVC34            GO DO THE COMMAND
***********************************************************************
*                                                                     *
*     X'80' WAS FOUND IN THE HIGH ORDER BYTE OF THE SECOND PARM,      *
*     SO WE HAVE BEEN CALLED AS A SUBROUTINE (MODE 3). THE SECOND     *
*     PARM CONTAINS THE CONSOLE ID WHERE THE COMMAND IS TO 'COME      *
*     FROM', SO USE THAT FOR THE R0 MASK.                             *
*                                                                     *
***********************************************************************
TWOPARMS L     R3,4(R10)          R3 = ADDR OF R0 MASK FOR SVC34
         MVC   R0MASK(4),0(R3)    SAVE R0 MASK FOR SVC34
***********************************************************************
*                                                                     *
*     ONLY ONE PARM WAS PASSED, SO WE ARE BEING CALLED IN MODE 1.     *
*     JUST ISSUE THE COMMAND FROM THE PARM STRING.                    *
*                                                                     *
***********************************************************************
ONEPARM  L     R10,0(R10)         GET PARAMETER ADDRESS
         SLR   R3,R3              CLEAR FOR ICM             *HMD 07/81*
         ICM   R3,B'0011',0(R10)  GET PARAMETER TEXT LENGTH
         BZ    USESYSIN           NO - GO GET COMMANDS FROM SYSIN
         CH    R3,=H'100'         COMMAND TOO LONG?         *HMD 07/81*
         BNH   CMDOK              NO, IS OK                 *HMD 07/81*
         LA    R3,100             ONLY DO 1ST 100 BYTES     *HMD 07/81*
CMDOK    BCTR  R3,R0              DECREMENT R3 FOR USE IN EXECUTE
         EX    R3,MOVE            EXECUTE MVC
DOSVC34  LA    R11,EOJ            GET ADDRESS FOR RETURN FROM SVC34
         B     SVC34              GO ISSUE COMMAND
         SPACE 3
***********************************************************************
*                                                                     *
*     NO PARM FIELD FOUND, SO LOOK AT SYSIN FOR INPUT                 *
*                                                                     *
***********************************************************************
USESYSIN OPEN  (SYSIN,INPUT)      OPEN SYSIN
         TM    SYSIN+DCBOFLGS-IHADCB,DCBOFOPN               *HMD 07/81*
         BZ    EOJ                IF BAD OPEN; END OF PGM   *HMD 07/81*
GETSYSIN GET   SYSIN,CARD         GET A SYSIN RECORD INTO SVC34 LIST
         MVC   CARD+72(8),CARD+80 BLANK OUT SEQUENCE FIELD IN 73-80
         CLI   CARD,C'*'          IS THIS A COMMENT?
         BE    GETSYSIN           YES;GO GET ANOTHER CARD
         LA    R11,GETSYSIN       GET ADDRESS FOR RETURN FROM SVC34
         B     SVC34              GO ISSUE COMMAND
         SPACE 3
EODSYSIN CLOSE (SYSIN)            CLOSE SYSIN
         SPACE 3
EOJ      L     R13,SAVEAREA+4     GET ADDRESS OF CALLER'S SAVEAREA
         RETURN (14,12),RC=0      RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*     THIS ROUTINE ACTUALLY ISSUES THE SVC 34. IT EITHER RETURNS      *
*     TO THE SYSIN-READ LOOP OR EOJ DEPENDING ON THE VALUE OF R11     *
*                                                                     *
***********************************************************************
SVC34    DS    0H
         LA    R3,CARD+L'CARD-1   POINT TO END OF TEXT      *HMD 07/81*
         LA    R4,CARD-1          POINT TO START OF TEXT    *HMD 07/81*
FINDTEXT DS    0H                 FIND LAST TEXT CHARACTER
         CR    R3,R4              ALL BLANK?
         BNH   SVC34END           YES - IGNORE IT
         CLI   0(R3),C' '         BLANK CHARACTER?
         BNE   GOTTEXT            NO - FOUND END OF TEXT
         BCT   R3,FINDTEXT        KEEP ON LOOKING
         SPACE
GOTTEXT  DS    0H                 BUILD PARAMETER LIST LENGTHS
         SR    R3,R4              COMPUTE LENGTH OF COMMAND *HMD 07/81*
         BNP   SVC34END           INVALID COMMAND           *HMD 07/81*
         LA    R3,5(R3)           TEXT AND LIST PREFIX
         STH   R3,CMDLIST         SET SVC 34 LIST LENGTH
         TESTAUTH FCTN=1          SEE IF WE ARE AUTHORIZED  *HMD 07/82*
         LTR   R15,R15            WELL, ARE WE?             *HMD 07/82*
         BZ    MODEOK             YES..JUST DO MODESET      *HMD 07/82*
         MVI   AUTHFLG,AUTHUS     INDICATE WE TURNED ON     *HMD 07/82*
* INSERT YOUR USER SVC HERE TO TURN ON APF AUTHORIZATION. A SAMPLE
* OF SUCH AN SVC IS ON THE LA MVS USER GROUP TAPE. THIS WILL ALLOW
* 'COMMAND' TO BE USED UNDER SPF OPTION 6 OR VIA THE 'CALL' COMMAND.
* ALSO, YOU WILL AVOID THE MESSY JOB OF UPDATING TABLES IN IKJEFT02.
         LTR   R15,R15            SYSTEM PROGRAMMER?        *HMD 07/82*
         BNZ   SVC34END           NOPE..DO NOT WASTE TIME   *HMD 07/82*
MODEOK   DS    0H                                           *HMD 07/82*
         MODESET KEY=ZERO         INDICATE KEY=ZERO
         SYSEVENT DONTSWAP        DONT GET SWAPPED          *HMD 07/81*
         L     R0,R0MASK          LOAD R0 FOR SVC34         *HMD 07/81*
         MGCR  CMDLIST            ISSUE OPERATOR COMMAND    *HMD 07/81*
         TM    SYSIN+DCBOFLGS-IHADCB,DCBOFOPN               *HMD 07/81*
*                                 ARE WE USING SYSIN?       *HMD 07/81*
         BZ    NOSTIMR            NO, DO NOT DELAY          *HMD 07/81*
         STIMER  WAIT,BINTVL=HOLD WAIT FOR A COMPLETION     *HMD 07/81*
NOSTIMR  SYSEVENT OKSWAP          OK TO GET SWAPPED NOW     *HMD 07/81*
         MODESET KEY=NZERO        GO BACK TO USER KEY
         CLI   AUTHFLG,AUTHUS     DID WE TURN ON AUTH?      *HMD 07/82*
         BNE   SVC34END           NO..JUST RETURN           *HMD 07/82*
* INSERT YOUR USER SVC HERE TO TURN OFF APF AUTHORIZATION, AS WE ARE
* DONE ISSUING COMMANDS.
         SPACE
SVC34END DS    0H                 END OF COMMAND ROUTINE
         BR    R11                RETURN TO MAINLINE
         EJECT
SAVEAREA DC    18F'0'             OUR SAVEAREA
HOLD     DC    F'200'             WAIT FOR 2 SECONDS        *HMD 07/81*
R0MASK   DC    A(0)               R0 MASK FOR SVC34
AUTHFLG  DC    X'00'              AUTHORIZATION FLAG        *HMD 07/82*
AUTHUS   EQU   X'FF'              WE AUTHORIZED FOR SVC34   *HMD 07/82*
MOREFLG  DC    X'00'              EXTRA FILLER              *HMD 07/82*
ASID     DC    H'00'              OUR ASID
         SPACE
         CNOP  4,8                ALIGN TO MIDDLE OF DOUBLE WORD
CMDLIST  DC    AL2(*-*,0)         SVC 34 LIST
CARD     DC    CL101' '           COMMAND TEXT
         SPACE 3
MOVE     MVC   CARD(*-*),2(R10)   MOVE PARM TEXT INTO SVC 34 LIST
MOVE1    MVC   CARD(*-*),0(R4)    MOVE PARM TEXT INTO SVC 34 LIST
         EJECT
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,LRECL=80,MACRF=(GM),RECFM=FB,     *
               EODAD=EODSYSIN
         EJECT
         LTORG                    BEGIN LITERAL POOL
         EJECT
***********************************************************************
*                                                                     *
*                SYSTEM CONTROL BLOCK MAPPING DSECTS                  *
*                                                                     *
*      ALL THESE MACROS CAN BE FOUND IN THE 'SYS1.AMODGEN' MACLIB     *
*                                                                     *
***********************************************************************
         CVT   DSECT=YES,LIST=YES COMMUNICATIONS VECTOR TABLE
         EJECT
         IHAASCB                  ADDRESS SPACE CONTROL BLOCK
         EJECT
         IEECUCM  FORMAT=NEW      UNIT CONTROL MODULE       *HMD 07/81*
         EJECT                                              *HMD 07/81*
         DCBD  DSORG=PS,DEVD=DA   DCB SYMBOLIC DEFN         *HMD 07/81*
         END
