WTORTN   CSECT 0
         USING WTORTN,15
         STM   14,12,12(13)
         LA    12,SAVAR
         ST    13,4(,12)
         ST    12,8(,13)
         LR    13,12
         LR    12,15
         DROP  15
         USING WTORTN,12
         L     1,0(,1)        GET ADDR OF PARM
         LH    2,0(,1)        GET PARM LENGTH
         LTR   2,2   FIND OUT IF LENGTH IS ZERO
         BZ    END
         CL    2,=F'62'  DOES IT EXCEED MAX LENGTH ALLOWED?
         BNH   *+8    NO,LEAVE LENGTH ALONE
         LA    2,62     FORCE MAXIMUM LENGTH
         LA    5,DESCCODE-2  END OF MSG POINTER--BACK IT OFF BY LENGTH
         SLR   5,2           FIND WHERE TO PUT MESSAGE
         LA    12,0(,12)   PURIFY BASE REGISTER
         SLR   5,12     GET DISPLACEMENT
         BM    END
         O     5,=F'49152'
         STH   5,MVC+2
         BCTR  5,0
         STH   5,MVI+2
         EX    0,MVI
         BCTR  2,0
         EX    2,MVC
         B     NEWL
         LA    3,MSG+1(2)
         MVI   0(3),C''''
         LA    2,WTOLN(,2)
NEWL     DS    0H
         LA    2,DESCCODE-WTO
         STH   2,WTO
         LA    1,16
         L     1,0(,1)        GET ADDR OF CVT
         L     1,0(,1)        GET ADDR OF NEXT/CURRENT TCB ADDRS
         L     1,4(,1)        GET ADDR OF MY TCB
         L     1,164(,1)      GET ADDR OF MY TCT
         L     1,28(,1)       GET ADDR OF MY JMR
         MVC   JOBNAME(8),0(1)
         LA    1,WTO
         WTO   MF=(E,(1))
         LA    1,WTO    SET UP THE WTL...
         WTL   MF=(E,(1))
         L     13,4(,13)
         LM    14,12,12(13)
         SR    15,15
         BR    14
END      DS    0H
         L     13,4(,13)   GET S.A
         LM    14,12,12(13)   GET ORIGINAL REGISTERS
         LA    15,4   SET CONDITION CODE OF 4 FOR ERRORS
         BR    14
MVC      MVC   MSG(0),2(1)
MVI      MVI   0(*-*),C''''
SAVAR    DC    18F'0'
WTO      DC    AL2(0)
         DC    AL1(X'80',X'00')   MCS FLAGS
JOBNAME  DC    C'JOBNAMEX--'
MSG      DC    CL62' ',C''''
DESCCODE DC    B'0100000000000000'  DESC CODE=2; MUST BE SPECIFICALLY
*                                    DELETED BY THE OPERATOR
ROUTCODE DC    B'0100110000000000'  ROUTCODE=2,5,6
WTOLN    EQU   MSG-WTO+1+1
         END   WTORTN
