 INVSRT:  PROC OPTIONS(MAIN); /* INVOKE FORGROUND DFSORT  MH */
  /* ************************************************************** */
  /*                                                                */
  /*                                                                */
  /*    INVOKE DFSORT FOR USE WITH TSO.                             */
  /*                                                                */
  /*    (WRITTEN IN A HIGH LEVEL LANGUAGE FOLKS: (WELL MOSTLY)      */
  /*                                                                */
  /*    THE ASSEMBLER SUBROUTINES ARE ALL INCLUDED WITH THIS CODE.  */
  /*                                                                */
  /*    THEY ARE 1) A DYNAMIC ALLOCATION INTERFACE - VERY EASY TO   */
  /*                                                 USE SEE CODE   */
  /*             2) CATALOG MANAGEMENT FOR DATASET LOCATE.          */
  /*             3) TSO COMMAND HANDLER SO WE CAN ISSUE TSO COMMANDS*/
  /*                SUCH AS FREE AND PRINTOFF WITHOUT HAVING TO     */
  /*                GO THROUGH ALL THAT TSO COMMAND PROCESSOR       */
  /*                INITIALISATION GARBAGE : PPL CPPL ECT ETC ETC.  */
  /*                                                                */
  /*             4) PDS MEMBER FINDER : RETURNS INFORMATION AS      */
  /*                TO WHETHER A MEMBER IS IN A PDS OR NOT.         */
  /*                                                                */
  /*  WISH LIST : THE USER DATASETS DO NOT HAVE TO BE CATALOGUED.   */
  /*              I CAN ADD VOLUME INFORMATION LATER. MAYBE         */
  /*              RELEASE 2 OF THIS (IF I EVER GET ROUND TO IT)     */
  /*              WILL ADD THIS FUNCTION.                           */
  /*                                                                */
  /*                                                                */
  /*  COMMENTS SUGGESTIONS IMPROVEMENTS AND ERROR INFORMATION       */
  /*  GREATLY APPRECIATED. ADDRESS BELOW.                           */
  /*                                                                */
  /*                                                                */
  /*   THIS PROGRAM IS ALMOST A CLONE OF INVPLI WHICH               */
  /*   INTERACTIVELY PERFORMS PLI PROCESSING. THE SAME              */
  /*   METHODOLOGY IS USED AND THE SAMF ASSEMBLER LANGUAGE          */
  /*   SUBPROGRAMS ARE CALLED.                                      */
  /*                                                                */
  /*                                                                */
  /*                                                                */
  /*    MIKE HAWTHORNE  TTI CITICORP                                */
  /*                    3100 OCEAN PK BLVD                          */
  /*                    SANTA MONICA CA 90405                       */
  /*                                                                */
  /*    (213) 450-9111 X2353                                        */
  /*                                                                */
  /*                                                                */
  /*                                                                */
  /* THIS PROGRAM DISPLAYS A PANEL TO OBTAIN USER SOURCE AND TARGET */
  /* DATASETS AND THE USER-ID. DATASETS REQUIRED BY THE DFSORT      */
  /* PRODUCT  ARE DYNAMICALLY ALLOCATED WITH THE DYNAM INTERFACE    */
  /* (EXISTING DATASETS ARE DELETED USING THE CATMAN CATALOG        */
  /* INTERFACE) AND THE DFSORT PROGRAM PRODUCT IS DYNAMICALLY       */
  /* INVOKED.                                                       */
  /*                                                                */
  /* ISPF BROWSE OF THE LIST DATASET IS THEN PERFORMED.             */
  /*                                                                */
  /* IF THE USER HAS SELECTED THE PRINT OPTION THE LIST DATASET     */
  /* IS PRINTED BY ISSUING THE PRINTOFF TSO COMMAND.                */
  /*                                                                */
  /* NOTE : USING PANELS AND ISPF SERVICES IS MUCH EASIER THAN      */
  /*        WRITING A TSO COMMAND PROCESSOR TO DO THIS JOB.         */
  /*        NOBODY HAS TSO THESE DAYS WITHOUT ALSO HAVING ISPF.     */
  /*        PARSING COMMANDS IS MUCH SIMPLER IN PL/I AND MOST       */
  /*        OF THE CODE CAN BE WRITTEN IN A HIGH LEVEL LANGUAGE.    */
  /*                                                                */
  /*                                                                */
  /* ************************************************************** */
  /*                                                                */
  /* DEFINE EXTERNAL REFERENCES : DYNAMIC ALLOCATION INTERFACE      */
  /*                            : CATALOG MANAGEMENT                */
  /*                            : PDS MEMBER FINDER                 */
  /*                            : TSO COMMAND PROCESSOR.            */
  /*                            : ISPF SERVICES                     */
  /*                            : LINKAGE EDITOR                    */
  /*                            : SORT/MERGE (VIA PLISRTA)          */
  /*                                                                */
  /* ************************************************************** */
 DCL ISPLINK          ENTRY EXTERNAL OPTIONS(ASM,INTER,RETCODE);
 DCL (CATMAN, DYNAM)  ENTRY EXTERNAL OPTIONS(ASM,INTER,RETCODE);
 DCL INVTSO           ENTRY EXTERNAL OPTIONS(ASM,INTER,RETCODE);
 DCL INVFIND          ENTRY EXTERNAL OPTIONS(ASM,INTER,RETCODE);
  /*                                                                */
  /* ************************************************************** */
  /*                                                                */
  /* DEFINE PLI BUILTIN FUNCTIONS/SERVICES.                         */
  /*                                                                */
  /* ************************************************************** */
 DCL (PLIRETV, LENGTH, SUBSTR, INDEX, ADDR, PLISRTA) BUILTIN;
  /* ************************************************************** */
  /*                                                                */
  /* DEFINE NON ISPF VARIABLES .                                    */
  /*                                                                */
  /* ************************************************************** */
 DCL CONS             CHAR(80) INIT(' '),
     FORSRT           CHAR(80) INIT(' '),
     BLANK            CHAR(8) INIT(' '),
     Q                PTR,
     I                FIXED BIN(15) INIT(0),
     J                FIXED BIN(15) INIT(0),
     K                FIXED BIN(15) INIT(0),
     MEMBER           CHAR(8) INIT(' '),
     MEML             FIXED BIN(15) INIT(0),
     LEN_USERID       FIXED BIN(15) INIT(0),
     LEN_ERR          FIXED BIN(15) INIT(0),
     SAVERC           FIXED BIN(15) INIT(0),
     L_SOURCE         FIXED BIN(15) INIT(0),
     L_COPY1          FIXED BIN(15) INIT(0),
     L_COPY2          FIXED BIN(15) INIT(0),
     WORK             FIXED BIN(31) INIT(0),
     RETCDE           FIXED BIN(31) INIT(0),
     TESTDSN          CHAR(54) INIT(' '),
     BROWSE_DSN       CHAR(44) INIT(' '),
     MSGFILE          FILE STREAM OUTPUT ENV(FB),
     FLAG             CHAR(1) INIT(' '),
     ERR_FLAG         CHAR(1) INIT('N'),
     PDSFLAG          CHAR(1) INIT('N'),
     MSGID            CHAR(8) INIT(' '),
     QUOTE            CHAR(1) INIT(''''), /* THIS IS A SINGLE QUOTE !*/
  /* ************************************************************** */
  /*                                                                */
  /* SET UP PARAMETER LIST FOR CATALOG MANAGEMENT INTERFACE         */
  /*                                                                */
  /* ************************************************************** */
  PARMS1              CHAR(169) BASED(Q),
  1  WORK1            UNALIGNED,
    3 FUNC            CHAR(1) INIT('P'),
    3 DSNAME          CHAR(44) INIT(' '),
    3 VOLCOUNT        FIXED BIN(31) INIT(0),
    3 VOLLIST(20)     CHAR(6),
  /* ************************************************************** */
  /*                                                                */
  /* SET UP PARAMETER LIST FOR MEMBER FINDER MODULE                 */
  /*                                                                */
  /* ************************************************************** */
  1 FORMEM            UNALIGNED,
    3  MPARML         FIXED BIN(15) INIT(8),
    3  LIBNAME        CHAR(9) INIT(' ');
 DCL 1 DDLIST,
    3  SNAME          CHAR(9) INIT(' '),
    3  CNAME1         CHAR(9) INIT(' '),
    3  CNAME2         CHAR(9) INIT(' '),
  /* ************************************************************** */
  /*                                                                */
  /* SET UP PARAMETER LIST FOR TSO COMMAND PROCESSING.              */
  /*                                                                */
  /* ************************************************************** */
  TSOLEN              FIXED BIN(31) INIT(0),
  TSOCMD              CHAR(255) INIT(' '),
  /* ************************************************************** */
  /*                                                                */
  /* DEFINE ISPF VARIABLES WE ARE GOING TO USE IN THE PROGRAM.      */
  /*                                                                */
  /* ************************************************************** */
     SOURCE           CHAR(54)  INIT(' '),
     COPY1            CHAR(44) INIT(' '),
     COPY2            CHAR(44) INIT(' '),
     ZUSER            CHAR(7)  INIT('ZUSER'),
     USERID           CHAR(8)  INIT(' ');
 DCL LSOURCE          FIXED BIN(31) INIT(LENGTH(SOURCE)),
     LCOPY1           FIXED BIN(31) INIT(LENGTH(COPY1)),
     LCOPY2           FIXED BIN(31) INIT(LENGTH(COPY2)),
     LZUSER           FIXED BIN(31) INIT(LENGTH(ZUSER));
  /* ************************************************************** */
  /*                                                                */
  /* SET UP THE INITIAL PROGRAM ENVIRONMENT :                       */
  /*                                                                */
  /*        A) SET UP ADDRESSABILITY OF BASED VARIABLES             */
  /*        B) INITIALIZE THE DYNAMIC ALLOCATION INTERFACE          */
  /*                                                                */
  /* ************************************************************** */
 Q = ADDR(WORK1);
 FETCH DYNAM;
 FETCH CATMAN;
 FETCH INVTSO;
 CALL DYNAM(WORK, 'INIT ');
 CALL DYNAM(WORK,'ALLOC ','DD=MSGFILE;','TE;');
 OPEN FILE(MSGFILE) LINESIZE(80);
  /* ************************************************************** */
  /*                                                                */
  /* SET UP THE ISPF ENVIRONMENT:                                   */
  /*                                                                */
  /*        A) DEFINE OUR PROGRAMS VARIABLES TO ISPF SO WE          */
  /*           CAN GET DATA FROM A PANEL.                           */
  /*        B) GET THE TSO USER-ID FROM ISPF. THIS IS THE SYSTEM    */
  /*           VARIABLE ZUSER FROM ISPF OBTAINED VIA VCOPY FUNCTION */
  /*        C) SHOW THE USER THE PANEL                              */
  /*        D) GET DATA BACK FROM THE PANEL                         */
  /*                                                                */
  /*       (IF PLIRETV = 8 THEN PFK3 OR END KEY HAS BEEN PRESSED)   */
  /*        IF THIS HAS HAPPENED WE EXIT WITHOUT MORE ADO.          */
  /*                                                                */
  /* ************************************************************** */
 CALL ISPLINK('VDEFINE', '(SOURCE)', SOURCE, 'CHAR', LSOURCE);
 CALL ISPLINK('VDEFINE', '(COPY1)', COPY1, 'CHAR', LCOPY1);
 CALL ISPLINK('VDEFINE', '(COPY2)', COPY2, 'CHAR', LCOPY2);
 CALL ISPLINK('VCOPY', ZUSER, LZUSER, USERID, 'MOVE');
 CALL ISPLINK('DISPLAY', 'DFSORT  ', 'INV016  ');
 LEN_USERID = INDEX(USERID,' ');
 IF LEN_USERID ^= 0
 THEN LEN_USERID = LEN_USERID - 1;
 REDOO: IF PLIRETV = 8 /* COME BACK HERE ON ANY ERROR     */
 THEN GO TO EXIT1;        /* EXIT WITHOUT DOING ANY PROCESSING*/
 TSOLEN = 1;
  /* ************************************************************** */
  /*                                                                */
  /*   PARSE THE DATA FROM THE USER.                                */
  /*                                                                */
  /*        A) GET SOURCE DATASET AND CHECK FOR VALIDITY.           */
  /*           IF THE DATASET IS UNQUOTED APPEND THE TSO USER-ID.   */
  /*           IF MISSING QUOTE THEN ISSUE ERROR MESSAGE AND EXIT.  */
  /*           IF UNCATALOGUED THEN ISSUE A MESSAGE.                */
  /*                                                                */
  /*        B) CHECK FOR VALIDITY OF USER DATASETS.                 */
  /*                                                                */
  /*        C) ALLOCATE DATASETS NEEDED FOR DFSORT EXECUTION.       */
  /*                                                                */
  /*                                                                */
  /* ************************************************************** */
  DSNAME = ' ';
 IF SUBSTR(SOURCE,1,1) = ' '          /* MISSING SOURCE DATASET     */
 THEN DO;
         CALL ISPLINK('DISPLAY', BLANK, 'INV019  ', 'SOURCE ');
         GO TO REDOO;
      END;
 TESTDSN = SOURCE;
 CALL PARSE;
 IF ERR_FLAG = 'Y'
 THEN DO;
         ERR_FLAG = 'N';
         CALL ISPLINK('DISPLAY', BLANK,  MSGID, 'SOURCE ');
         GO TO REDOO;
      END;
 SOURCE = DSNAME;                    /* GET CORRECT DSN FOR ALLOCATE*/
 IF PDSFLAG = 'Y'
 THEN DO;
         PDSFLAG = 'N';
         K = INDEX(LIBNAME,' ');
         SNAME = SUBSTR(LIBNAME,1,K-1);
      END;
 IF SUBSTR(COPY1,1,1) ^= ' '
 THEN DO;
          TESTDSN = COPY1;
          CALL PARSE;
          IF ERR_FLAG = 'Y'    /* PARSE ERROR            */
          THEN DO;
                  ERR_FLAG = 'N';
                  CALL ISPLINK('DISPLAY', BLANK,
                               MSGID, 'COPY1  ');
                  GO TO REDOO;
               END;
         IF PDSFLAG = 'Y'
         THEN DO;
                 PDSFLAG = 'N';
                 K = INDEX(LIBNAME,' ');
                 CNAME1 = SUBSTR(LIBNAME,1,K-1);
              END;
          COPY1 = DSNAME;
          L_COPY1 = INDEX(COPY1,' ') - 1;
      END;
 ELSE DO;
         CALL ISPLINK('DISPLAY', BLANK, 'INV017  ', 'COPY1  ');
         GO TO REDOO;
      END;
 IF SUBSTR(COPY2,1,1) ^= ' '
 THEN DO;
          TESTDSN = COPY2;
          CALL PARSE;
          IF ERR_FLAG = 'Y'    /* PARSE ERROR            */
          THEN DO;
                  ERR_FLAG = 'N';
                  CALL ISPLINK('DISPLAY', BLANK,
                               MSGID, 'COPY2  ');
                  GO TO REDOO;
               END;
          COPY2 = DSNAME;
          L_COPY2 = INDEX(COPY2,' ') - 1;
         IF PDSFLAG = 'Y'
         THEN DO;
                 PDSFLAG = 'N';
                 K = INDEX(LIBNAME,' ');
                 CNAME2 = SUBSTR(LIBNAME,1,K-1);
              END;
      END;
 ELSE DO;
         CALL ISPLINK('DISPLAY', BLANK, 'INV018  ', 'COPY2  ');
         GO TO REDOO;
      END;
 CONS= 'DFSORT PROMPTER        V1.0  MIKE HAWTHORNE AUG 1985';
 CALL WRITEIT;
  /* ************************************************************** */
  /*                                                                */
  /*   DELETE DATASET USERID.SRT.LIST                               */
  /*                                                                */
  /* ************************************************************** */
 FUNC = 'P';                    /* SET FOR PURGE OF DATASETS */
 DSNAME=SUBSTR(USERID,1,LEN_USERID) ×× '.SRT.LIST';
 CALL CATMAN(PARMS1);           /* IGNORE RET CODE IF NOT 0  */
  /* ************************************************************** */
  /*                                                                */
  /*   UNALLOCATE SYSOUT AND SYSIN                                  */
  /*   ALLOCATE DATASETS NEEDED BY THE DFSORT PROGRAM PRODUCT       */
  /*   AND CALL IT .                                                */
  /*   AT END OF EXECUTION ISSUE A MESSAGE INDICATING RETURN CODE   */
  /*   AND GO AND BROWSE THE LIST DATASET.                          */
  /*                                                                */
  /* ************************************************************** */
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SYSOUT;');
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SYSIN;');
 IF SNAME ^= ' '               /*  ARE WE USING A PDS     */
 THEN DO;                      /* YES WE ARE              */
         K = INDEX(SOURCE,'(');
         K = K - 1;
         I = INDEX(SNAME,' ');
         I = I - 1;
         CALL DYNAM(WORK, 'ALLOC ', 'DSN='
                   ×× SUBSTR(SOURCE,1,K) ×× ' SHR;',
                   'ME=' ×× SUBSTR(SNAME,1,I) ×× ';',
                   'DD=SYSIN;');
      END;
 ELSE DO;
         K = INDEX(SOURCE,' ');
         K = K - 1;
          CALL DYNAM(WORK, 'ALLOC ', 'DSN='
                    ×× SUBSTR(SOURCE,1,K) ×× ' SHR;',
                    'DD=SYSIN;');
      END;
 CALL DYNAM(WORK, 'ALLOC ',
 'DSN=' ×× SUBSTR(USERID,1,LEN_USERID) ×× '.SRT.LIST NEW CATLG;',
          'LRECL=133;','BLKSIZE=1330;','FB;',
          'UNIT=SYSDA;','DD=SYSOUT;',
          'TRK PRIMARY=1 SECONDARY=1;');
 IF CNAME1 ^= ' '
 THEN DO;
         K = INDEX(COPY1,'(');
         K = K - 1;
         I = INDEX(CNAME1,' ');
         I = I - 1;
         CALL DYNAM(WORK, 'ALLOC ', 'DSN='
                   ×× SUBSTR(COPY1,1,K) ×× ' SHR;',
                   'ME=' ×× SUBSTR(CNAME1,1,I) ×× ';',
                   'DD=SORTIN;');
      END;
 ELSE DO;
         K = INDEX(COPY1,' ');
         K = K - 1;
         CALL DYNAM(WORK, 'ALLOC ', 'DSN='
                    ×× SUBSTR(COPY1,1,L_COPY1) ×× ' SHR;',
                    'DD=SORTIN;');
      END;
 CONS = 'INPUT DATA SET TO BE SORTED : ' ×× COPY1;
 CALL WRITEIT;
 IF CNAME2 ^= ' '
 THEN DO;
         K = INDEX(COPY2,'(');
         K = K - 1;
         I = INDEX(CNAME2,' ');
         I = I - 1;
         CALL DYNAM(WORK, 'ALLOC ', 'DSN='
                   ×× SUBSTR(COPY2,1,K) ×× ' SHR;',
                   'ME=' ×× SUBSTR(CNAME2,1,I) ×× ';',
                   'DD=SORTOUT;');
      END;
 ELSE DO;
         K = INDEX(COPY2,' ');
         K = K - 1;
         CALL DYNAM(WORK, 'ALLOC ', 'DSN='
                    ×× SUBSTR(COPY2,1,L_COPY2) ×× ' SHR;',
                   'DD=SORTOUT;');
      END;
 CONS = 'TARGET (OUTPUT) DATA SET IS : ' ×× COPY2;
 CALL WRITEIT;
  /* ************************************************************** */
  /*                                                                */
  /*   ALLOCATE SORT WORK DATASETS.                                 */
  /*   PLEASE CHANGE UNIT NAME AND/OR VOLUME NAMES TO ALLOCATE      */
  /*   DATA SETS ON DEPENDING ON WHAT YOU HAVE IN YOUR SHOP.        */
  /*                                                                */
  /*   YOU MAY LIKE TO ALLOCATE MORE/LESS SPACE ALSO.               */
  /*                                                                */
  /*   IF YOU GET AN ABEND (SORT ABENDS/X37 ABENDS) ISSUE A         */
  /*   TSO FREEALL COMMAND TO UNALLOCATE DATASETS. YOU MAY NEED     */
  /*   TO MANUALLY DELETE THE SORT WORK DATASETS ALSO.              */
  /*                                                                */
  /*   SORT SHOULD NOT NORMALLY ABEND !!                            */
  /*                                                                */
  /* ************************************************************** */
 CALL DYNAM(WORK, 'ALLOC ',
     'DSN=' ×× SUBSTR(USERID,1,LEN_USERID) ×× '.SRT.WK1 NEW CATLG;',
          'UNIT=SYSDA;','DD=SORTWK01;','VOL=PUB702;',
          'TRK PRIMARY=75 SECONDARY=5;');
 CALL DYNAM(WORK, 'ALLOC ',
     'DSN=' ×× SUBSTR(USERID,1,LEN_USERID) ×× '.SRT.WK2 NEW CATLG;',
          'UNIT=SYSDA;','DD=SORTWK02;','VOL=PUB703;',
          'TRK PRIMARY=75 SECONDARY=5;');
 CALL DYNAM(WORK, 'ALLOC ',
     'DSN=' ×× SUBSTR(USERID,1,LEN_USERID) ×× '.SRT.WK3 NEW CATLG;',
          'UNIT=SYSDA;','DD=SORTWK03;','VOL=PUB704;',
          'TRK PRIMARY=75 SECONDARY=5;');
 CONS = 'ENTERING DFSORT: PLEASE STAND BY FOR COMPLETION';
 CALL WRITEIT;
  /* ************************************************************** */
  /*                                                                */
  /*   TO KEEP THE THING SMALL FOR TSO (COMPLEX SORTS CAN BETTER    */
  /*   USE BATCH JOBS I INSIST ON 2 AND ONLY 2 CONTROL STATEMENTS   */
  /*   SORT FIELDS = ETC.                                           */
  /*   RECORD = RECORD STATEMENT.                                   */
  /*                                                                */
  /*   BOTH MUST BE PRESENT OR IT WON'T WORK.                       */
  /*   I ALLOW NO CONTINUATION BUT 1 SORT FIELD CARD SHOULD ALLOW   */
  /*   MORE THAN ENOUGH SORT FIELDS FOR A SIMPLE SORT.              */
  /*                                                                */
  /* ************************************************************** */
 GET EDIT(FORSRT)(A(80));      /* SORT FIELDS=(ETC) STATEMENT */
 CONS = FORSRT;                /* RECORD STATEMENT            */
 GET EDIT(CONS)(A(80));
 K = INDEX (FORSRT,')');
 IF K = 0
 THEN K = 1;                   /* USER HAS GARBAGE CONTROL STMT      */
 K = K + 1;
 I = INDEX (CONS,')');
 IF I = 0
 THEN I = 1;                   /* USER HAS GARBAGE CONTROL STMT      */
 I = I + 1;
 CALL PLISRTA( SUBSTR(FORSRT,1,K),
               SUBSTR(CONS,1,I),
              512000, RETCDE);
 CONS = 'DFSORT HAS FINISHED : RC=' ×× RETCDE;
 CALL WRITEIT;
  /* ************************************************************** */
  /*                                                                */
  /*   ENTER ISPF WITH BROWSE FACILITY TO BROWSE THE DATASET        */
  /*   JUST CREATED. WHEN THE USER HAS FINISHED WITH BROWSE THEN    */
  /*   EXIT BACK TO TSO WITH MESSAGE PROMPTER PROCESSING            */
  /*   FINISHED.                                                    */
  /*                                                                */
  /*   NOTE : WHILE YOU ARE IN BROWSE YOU HAVE FULL ACCESS TO       */
  /*          SPLIT SCREENS AND OTHER TSO/ISPF SERVICES SO YOU      */
  /*          CAN EDIT, ISSUE TSO COMMANDS ETC. ETC.                */
  /*                                                                */
  /* ************************************************************** */
 CONS = 'BROWSING SORT MESSAGE DATASET: YOU MAY USE SPLIT'
        ×× ' SCREENS ETC WHILE IN BROWSE';
 CALL WRITEIT;
 CONS = 'PRESS ENTER TO CONTINUE : TO EXIT BROWSE PRESS PF3'
        ×× ' TO RESUME PROCESSING';
 CALL WRITEIT;
 BROWSE_DSN = 'SRT.LIST';
 CALL ISPLINK ('BROWSE', BROWSE_DSN);
  /* ************************************************************** */
  /*                                                                */
  /*   DE-ALLOCATE DDNAMES USED IN DFSORT PROCESSING                */
  /*                                                                */
  /* ************************************************************** */
 FUNC = 'P';                    /* SET FOR PURGE OF DATASETS */
 DSNAME=SUBSTR(USERID,1,LEN_USERID) ×× '.SRT.WK1';
 CALL CATMAN(PARMS1);           /* IGNORE RET CODE IF NOT 0  */
 FUNC = 'P';                    /* SET FOR PURGE OF DATASETS */
 DSNAME=SUBSTR(USERID,1,LEN_USERID) ×× '.SRT.WK2';
 CALL CATMAN(PARMS1);           /* IGNORE RET CODE IF NOT 0  */
 FUNC = 'P';                    /* SET FOR PURGE OF DATASETS */
 DSNAME=SUBSTR(USERID,1,LEN_USERID) ×× '.SRT.WK3';
 CALL CATMAN(PARMS1);           /* IGNORE RET CODE IF NOT 0  */
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SORTIN;');
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SORTOUT;');
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SYSIN;');
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SYSOUT;');
 CALL DYNAM(WORK, 'UNALLOC', 'DD=SORTWK01;');
 CALL DYNAM(WORK, 'UNALLOC', 'DD=SORTWK02;');
 CALL DYNAM(WORK, 'UNALLOC', 'DD=SORTWK03;');
 GO TO EXIT;       /* REDUNDANT STMT BUT MAKES CODE EASIER TO READ  */
  /* ************************************************************** */
  /*                                                                */
  /*  THIS SUBROUTINE DOES THE FOLLOWING :                          */
  /*                                                                */
  /*                  A) TOO MANY QUOTES ON DATASET NAME.           */
  /*                  B) IF WE HAVE ( OR ) IN THE DATASET           */
  /*                     IS IT VALID. I.E '(' MUST COME BEFORE ')'. */
  /*                  C) IS THE MEMBER NAME 8 CHARS OR LESS.        */
  /*                  D) CHECKS FOR QUOTED DATASET. IF QUOTED       */
  /*                     CHECKS FOR MATCHING END OF QUOTE.          */
  /*                  E) IF NOT QUOTED APPENDS THE USERS TSO ID     */
  /*                     IN FRONT OF THE DSN.                       */
  /*                  F) CHECKS WHETHER THE DATASET CATALOGUED.     */
  /*                  G) IS THE MEMBER IN THE PDS IF ITS A PDS.     */
  /*                                                                */
  /*                                                                */
  /* ERRORS WILL CAUSE A RE-DISPLAY OF THE PANEL WITH A MESSAGE.    */
  /* PF2 WILL PRESENT THE ALTERNATE LONG MESSAGE.                   */
  /*                                                                */
  /*                                                                */
  /* ************************************************************** */
 PARSE: PROC;
 MEML = 0;
 MEMBER=' ';
 DSNAME = ' ';
 K = INDEX (TESTDSN,QUOTE ×× QUOTE); /* CHECK FOR DOUBLE QUOTES */
 IF K ^= 0
 THEN DO;
         ERR_FLAG = 'Y';
         MSGID = 'INV009  '; /* TOO MANY QUOTES          */
         GO TO ENDPARS;
      END;
 IF SUBSTR(TESTDSN,1,1) = QUOTE   /* EXTRACT QUOTES IF ANY   */
  THEN DO;                        /* IF THERE APPEND USER ID */
          K = INDEX (TESTDSN,' ');     /* GET LENGTH OF SOURCE DSN  */
          K = K - 1;                   /* GET THE REAL LENGTH       */
          IF SUBSTR(TESTDSN,K,1) ^= QUOTE
          THEN DO;
                  ERR_FLAG = 'Y';
                  MSGID = 'INV004  ';
                  GO TO ENDPARS;
               END;
          ELSE DO;
                  DSNAME=SUBSTR(TESTDSN,2,K-2); /* EXTRACT QUOTES */
               END;
       END;
  ELSE DO;
          K = INDEX (TESTDSN,QUOTE);   /* CHECK FOR A SINGLE QUOTE */
          IF K = 0                             /* NOT FOUND   OKAY */
          THEN DO;
                  K = INDEX (TESTDSN,' ');
                  K = K - 1;
                  DSNAME=SUBSTR(USERID,1,LEN_USERID) ×× '.'
                  ×× SUBSTR(TESTDSN,1,K);
                END;
          ELSE DO;
                  ERR_FLAG = 'Y';
                  MSGID = 'INV004  ';
                  GO TO ENDPARS;
               END;
       END;
 K = INDEX (DSNAME,'(');  /* NOTE WE HAVE NOW EXTRACTED QUOTE */
 IF K = 0                 /* AND IF NECESSARY APPENDED TSO USER ID */
 THEN DO;
         K = INDEX (DSNAME,')');
         IF K ^=0
         THEN DO;
                 ERR_FLAG = 'Y';
                 MSGID = 'INV004  '; /* SET INVALID DATASET NAME */
                 GO TO ENDPARS;
             END;
      END;        /* THAT TAKES CARE OF UNBALANCED PARENTHESES */
 ELSE DO;         /* WE HAVE ALREADY FOUND A  '(' SO VALIDATE DS */
         I = INDEX (DSNAME,')');
         IF I = 0
         THEN DO;
                 ERR_FLAG = 'Y';    /* UNBALANCED PARENTHESES */
                 MSGID = 'INV004  ';
                 GO TO ENDPARS;
             END;
         IF SUBSTR(DSNAME,I+1,1) = QUOTE  /* QUOTED DSN FOLKS   */
         THEN DO;
                 IF SUBSTR(DSNAME,I+2,1) ^= ' ' /* GARBAGE DSN  */
                 THEN DO;
                         ERR_FLAG = 'Y';
                         MSGID = 'INV004  ';
                         GO TO ENDPARS;
                      END;
              END;
         IF SUBSTR(DSNAME,I+1,1) ^= ' ' /* GARBAGE DSN  */
         THEN DO;
                 ERR_FLAG = 'Y';
                 MSGID = 'INV004  ';
                 GO TO ENDPARS;
              END;
         ELSE DO;
                 IF I < K          /* PARENTHESES ASS ABOUT TIT  */
                 THEN DO;          /* GOT ) BEFORE (  TURKEY !   */
                         ERR_FLAG = 'Y';
                         MSGID = 'INV004  ';
                         GO TO ENDPARS;
                      END;
                 MEML = I - K;
                 IF MEML > 9      /* INVALID MEMBER NAME          */
                 THEN DO;
                         ERR_FLAG = 'Y';
                         MSGID = 'INV007  ';
                         GO TO ENDPARS;
                      END;
                 ELSE DO;
                         MEMBER = SUBSTR(DSNAME,K+1,I-1);
                      END;
             END;
       END;
  IF ERR_FLAG = 'N'          /* ON ERROR SKIP REST OF PROCESSING    */
  THEN DO;
          FUNC = 'I';        /* CHECK IF CATALOGED                  */
          CALL CATMAN(PARMS1);
          IF PLIRETV ^=0
          THEN DO;
                  MSGID = 'INV002  ';
                  ERR_FLAG = 'Y';
                  GO TO ENDPARS;
               END;
       END;
  ENDPARS: IF ERR_FLAG = 'N'
  THEN DO;
          IF MEML > 0       /* WE'VE GOT A PDS HERE                */
          THEN DO;          /* NOW CHECK FOR MEMBER OF A PDS       */
                  CALL CHEKMEM;
               END;
       END;
 END PARSE;
 CHEKMEM: PROC;
  /* ************************************************************** */
  /*                                                                */
  /* WE WILL CHECK FOR MEMBER OF A PDS. CATALOG MANAGEMENT WILL     */
  /* TELL US IF THE WHOLE PDS IS OUT THERE BUT WILL NOT TELL US     */
  /* IF AN ACTUAL MEMBER IS IN THE PDS.                             */
  /* WE CAN GET THAT FROM DYNAMIC ALLOCATION ROUTINES AND OUR       */
  /* PDS MEMBER FINDER WHICH NEEDS A DDNAME OF WXYZ.                */
  /*                                                                */
  /* ************************************************************** */
  IF ERR_FLAG = 'N'
  THEN DO;
         K = INDEX(DSNAME,'(');
         K = K - 1;
         CALL DYNAM(WORK, 'ALLOC ', 'DSN='
         ×× SUBSTR(DSNAME,1,K)  ×× ' SHR;', 'DD=WXYZ;');
         IF PLIRETV ^= 0
         THEN DO;
                 CONS= 'DYNALLOC FAILED DSN='
                 ×× SUBSTR(DSNAME,1,K) ×× 'RC='
                 ×× PLIRETV;
                 CALL WRITEIT;
                 GO TO EXIT;
              END;
         LIBNAME = ' ';
         MPARML = 8;
         LIBNAME = SUBSTR(MEMBER,1,MEML-1);
         FETCH INVFIND;
         CALL INVFIND(FORMEM);
         SAVERC = PLIRETV;
         RELEASE INVFIND;
         CALL DYNAM(WORK, 'UNALLOC ', 'DSN='
                    ×× SUBSTR(DSNAME,1,K) ×× ' KEEP;', 'DD=WXYZ;');
         IF SAVERC ^= 0
         THEN DO;
                 ERR_FLAG = 'Y';
                 MSGID = 'INV008';
              END;
         PDSFLAG='Y';           /* SET PDS INDICATOR ON TURKEY!! */
       END;
  END CHEKMEM;
- /* ************************************************************** */
  /*                                                                */
  /*  THIS SUBROUTINE ISSUES THE APPROPRIATE ERROR MESSAGES         */
  /*  AND EXITS THE PROGRAM                                         */
  /*                                                                */
  /* ************************************************************** */
 WRITEIT: PROC;
          PUT FILE(MSGFILE) EDIT (CONS)(A(80));
 END WRITEIT;
- /* ************************************************************** */
  /*                                                                */
  /*            CLEAN UP AND TERMINATION SECTION.                   */
  /*                                                                */
  /* THE ASSEMBLER KNOWS ITS UNDER TSO SO WE WILL ALSO HAVE         */
  /* TO FREE DDNAME=TERMFILE AND OUR OWN TERMINAL DATASET.          */
  /*                                                                */
  /* IF THE USER HAS INDICATED HE WANTS A PRINT THEN ISSUE THE      */
  /* PRINTOFF COMMAND.                                              */
  /*                                                                */
  /*                                                                */
  /* ************************************************************** */
 EXIT: CONS = 'DFSORT PROMPTER HAS FINISHED';
 CALL WRITEIT;
 EXIT1: RELEASE CATMAN;
 CLOSE FILE(MSGFILE);
 CALL DYNAM(WORK,'UNALLOC ','DD=MSGFILE;');
 RELEASE DYNAM;
 RELEASE INVTSO;
 CALL ISPLINK('VDELETE', '(SOURCE)');
 CALL ISPLINK('VDELETE', '(COPY1)');
 CALL ISPLINK('VDELETE', '(COPY2)');
 END INVSRT;
