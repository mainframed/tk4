         TITLE '$SMFBRWS - PROGRAM TO REPORT ON SMF RECORDS'
**********************************************************************
*                                                                    *
*        $SMFBRWS                                                    *
*                                                                    *
*   THIS PROGRAM WAS MODIFIED FROM AN SMF DATA REPORTING PROGRAM     *
*   ORIGINALLY DEVELOPED BY AND RECEIVED FROM CBT - CONNECTICUT      *
*   BANK AND TRUST COMPANY.  LOCAL MODS INCLUDE REGULARIZING         *
*   THE MESSAGE FORMAT (ESPECIALLY LOCATION OF JOB NAME) TO ALLOW    *
*   SORTING OF OUTPUT MESSAGES, ADDING SYSTEM ID AND DATE, PLUS      *
*   DATE SELECTION IN ADDITION TO TIME SELECTION, AND ALLOWING       *
*   THE PROGRAM TO CONTINUE RATHER THAN QUIT WHEN THE FIRST HIGH     *
*   TIME IS HIT.  THE CONTROL CARDS ARE ALSO LISTED TO THE OUTPUT    *
*   FILE OR DEVICE, AND ADDITIONAL SUPPORT FOR DEFAULT VALUES HAS    *
*   BEEN ADDED.                                                      *
*                                                                    *
*   KAISER'S TYPE 255 (IEFUJV) SMF RECORD IS ALSO SUPPORTED.         *
*                                                                    *
*   MISCELLANEOUS CHANGES HAVE ALSO BEEN DONE TO ADD OTHER INFO.     *
*   TO MESSAGES WHERE IT SEEMED APPLICABLE (AND TRIVIAL).            *
*                                                                    *
*   CHANGES MADE BY KAISER HAVE THE TEXT 'KHPRDC' IN COLUMS 65-70.   *
*                                                                    *
*   D. THEWLIS, JANUARY 15, 1979:                                    *
*                                                                    *
*   KAISER FOUNDATION HEALTH PLAN                                    *
*   NORTHERN CALIFORNIA REGIONAL DATA CENTER                         *
*   945 CAMELIA STREET                                               *
*   BERKELEY, CALIFORNIA 94710                                       *
*                                                                    *
**********************************************************************
         EJECT
*********************************************************************
*                                                                   *
*                         D I S C L A I M E R                       *
*                                                                   *
*   THIS COMPUTER PROGRAMMING MATERIAL  HAS  BEEN  DEVELOPED  BY,   *
*   OR   HAS  BEEN  MODIFIED  OR  EXTENDED  FROM  OTHER SIMILARLY   *
*   PROVIDED MATERIAL BY,   THE NORTHERN CALIFORNIA REGIONAL DATA   *
*   CENTER OF THE KAISER FOUNDATION HEALTH PLAN.   PLEASE ADDRESS   *
*   ANY INQUIRIES TO:                                               *
*                                                                   *
*         KAISER FOUNDATION HEALTH PLAN                             *
*         NORTHERN CALIFORNIA REGIONAL DATA CENTER                  *
*         945 CAMELIA STREET                                        *
*         BERKELEY, CALIFORNIA 94710                                *
*         (415) 645-6350                                            *
*                                                                   *
*         ATTN:  DAVE THEWLIS                                       *
*         (OR:   SYSTEMS PROGRAMMING)                               *
*                                                                   *
*   THIS MATERIAL  HAS  BEEN  USED  SUCCESSFULLY  BY  THE  KAISER   *
*   FOUNDATION HEALTH PLAN, AND TO THE BEST OF OUR KNOWLEDGE THIS   *
*   MATERIAL AND ANY SYSTEM(S)  OF  WHICH IT IS A PART ARE OPERA-   *
*   TIONAL AS OF THE SERVICE LEVEL OR DATE STATED IN  THE BODY OF   *
*   THIS MATERIAL   (IF SO STATED).     HOWEVER,  NO  WARRANTY IS   *
*   MADE OR IMPLIED AS  TO  THE  ACCURACY OF THIS MATERIAL OR ANY   *
*   RELATED MATERIAL OR SYSTEMS, AND NO RESPONSIBILITY IS ASSUMED   *
*   FOR ANY EFFECT OR MODIFICATION DIRECTLY OR INDIRECTLY  CAUSED   *
*   BY THE USE OF THIS MATERIAL.  IT IS THE RESPONSIBILITY OF ANY   *
*   USER OF THIS  MATERIAL TO  EVALUATE  IT'S  USEFULNESS  TO THE   *
*   USER'S ENVIRONMENT.                                             *
*                                                                   *
*   THE KAISER FOUNDATION HEALTH PLAN DOES NOT GUARANTEE  TO KEEP   *
*   THIS OR ANY RELATED MATERIAL CURRENT,   NOR DOES IT GUARANTEE   *
*   TO  PROVIDE  ANY  CORRECTIONS  OR EXTENSIONS DESCRIBED BY ANY   *
*   USERS OF THIS MATERIAL, OR ANY CORRECTIONS OR EXTENSIONS MADE   *
*   IN THE FUTURE BY THE KAISER FOUNDATION HEALTH PLAN ITSELF.      *
*                                                                   *
*********************************************************************
         EJECT
*********************************************************************
*                                                                    *
*                      D I S C L A I M E R                           *
*                                                                    *
*                                                                    *
*   THE PROGRAMS HAVE BEEN USED SUCCESSFULLY BY CBT AND TO THE BEST  *
*   OF OUR KNOWLEDGE THE SYSTEM IS OPERATIONAL, HOWEVER NO WARRANTY  *
*   IS MADE TO THE ACCURACY OF THE PROGRAMS OR RELATED MATERIAL AND  *
*   NO RESPONSIBILITY IS ASSUMED  FOR ANY MODIFICATION  DIRECTLY OR  *
*   INDIRECTLY  CAUSED BY THE USE OF THE PROGRAMS.  IT IS THE USERS  *
*   RESPONSIBILITY  TO  EVALUATE THE  USEFULLNESS  OF THE  MATERIAL  *
*   SUPPLIED.                                                        *
*                                                                    *
*   CBT DOES NOT GUARANTEE TO KEEP ANY MATERIAL PROVIDED UP TO DATE  *
*   NOR DOES IT GUARANTEE TO PROVIDE ANY CORRECTIONS OR  EXTENSIONS  *
*   DESCRIBED BY THE USER OR  CORRECTIONS OR EXTENSIONS MADE IN THE  *
*   FUTURE BY CBT ITSELF.                                            *
*                                                                    *
*                                                                    *
*                                                                    *
*********************************************************************
         EJECT
*/*******************************************************************/
*/*            G T E     D A T A    S E R V I C E S                 */
*/*                     D I S C L A I M E R                         */
*/*                                                                 */
*/* SOME OF THESE PROGRAMS HAVE NOT BEEN INSTALLED OR TESTED AT     */
*/* ANY GTE DATA SERVICES INSTALLATIONS. MOST HAVE BEEN OBTAINED    */
*/* FROM OUTSIDE USERS, AT NO CHARGE, AND HAVE BEEN MODIFIED FOR    */
*/* USE AT GTE DATA SERVICES, MARINA DEL REY, CALIFORNIA. NOTHING   */
*/* IN THESE PROGRAMS REFLECTS ANY CODE WHICH SHOULD BE CONSIDERED  */
*/* PROPRIATARY INFORMATION AND PERMISSION IS HEREBY GRANTED BY     */
*/* GTE DATA SERVICES TO REPRODUCE OR RUN ANY OF THESE PROGRAMS ON  */
*/* ANY COMPUTER FOR WHICH THEY ARE DESIGNED.                       */
*/*                                                                 */
*/* THE CODE CONTAINED IN THESE PROGRAMS IS CONSIDERED BY GTE DATA  */
*/* SERVICES TO BE IN THE PUBLIC DOMAIN, AND MAY NOT BE LEASED OR   */
*/* SOLD FOR ANY MONETARY CONSIDERATION.  THE AUTHORS, IF EMPLOYED  */
*/* AT GTE DATA SERVICES, MARINA DEL REY, CALIFORNIA, HAVE GIVEN    */
*/* GTE DATA SERVICES PERMISSION TO REPRODUCE AND DISTRIBUTE ALL    */
*/* OF THIS MATERIAL AT NO CHARGE TO ANYONE WHO SO DESIRES IT.      */
*/* THE MANAGEMENT OF GTE DATA SERVICES HAS CONCURRED WITH THIS     */
*/* POLICY. ALL COPYRIGHT CLAIMS WILL HAVE BEEN WAIVED BY THE       */
*/* INSTALLATION AND USE OF THESE PROGRAMS.                         */
*/*                                                                 */
*/* SOME OF THESE PROGRAMS HAVE BEEN PROVEN TO BE OPERATIONAL AT    */
*/* SOME GTE DATA SERVICES INSTALLATIONS. HOWEVER NO WARRANTY       */
*/* IS MADE TO THE ACCURACY OF THE PROGRAMS OR RELATED MATERIAL AND */
*/* NO RESPONSIBILITY IS ASSUMED  FOR ANY MODIFICATION  DIRECTLY OR */
*/* INDIRECTLY  CAUSED BY THE USE OF THE PROGRAMS.  IT IS THE USERS */
*/* RESPONSIBILITY  TO  EVALUATE THE  USEFULLNESS  OF THE  MATERIAL */
*/* SUPPLIED.                                                       */
*/*                                                                 */
*/* GTE DATA SERVICES DOES NOT GUARANTEE TO KEEP ANY MATERIAL       */
*/* PROVIDED UP TO DATE, NOR DOES IT GUARANTEE TO PROVIDE ANY       */
*/* CORRECTIONS OR EXTENTIONS DESCRIBED BY THE USER OR CORRECTIONS  */
*/* OR EXTENTIONS MADE IN THE FUTURE BY GTE DATA SERVICES ITSELF.   */
*/*                                                                 */
*/*******************************************************************/
         EJECT
**********************************************************************
*                                                                    *
*   THE CONTROL OPTIONS ARE NOW AS FOLLOWS:                          *
*                                                                    *
*        IF YOU RUN IT IN BATCH AND SET SYSIN TO DUMMY, ALL          *
*        OPTIONS HAVE DEFAULTS.  ALSO YOU DON'T NEED TO SUPPLY       *
*        ALL OPTION CARDS IF YOU ONLY WANT (FOR INSTANCE) THE        *
*        TIME SELECTION OPTION.                                      *
*                                                                    *
*        IF YOU RUN IT IN TSO, YOU CAN GIVE IT A NULL LINE WITH      *
*        THE SAME EFFECT AS ONE BLANK AND CAUSES IT TO USE DEFAULTS. *
*                                                                    *
*        IF YOU SPECIFY 'PARM=NOMSG' NEITHER THE PROMPTING           *
*        MESSAGES OR THE CTL CARD IMAGES ARE LISTED TO SYSPRINT.     *
*        THIS IS INTENDED WHEN YOU ARE DRIVING THE PROGRAM WITH      *
*        A TSO CLIST, OR JUST WANT A CLEANER OUTPUT LISTING.         *
*                                                                    *
*        THE FIRST INPUT IS TIME AND DATE SELECTION.  DEFAULTS ARE   *
*        0000 TO 2400 HOURS; 01001 THROUGH 99366.  TO SELECT TIME    *
*        ENTER 8 DIGITS IN COLS 1-8 HHMMHHMM LOW AND HIGH TIME.      *
*        RECORDS OUTSIDE OF THOSE TIMES WILL BE IGNORED (NOTE        *
*        THAT PROGRAM WILL CONTINUE TO SCAN SMF DATA -- TO           *
*        PICK, FOR INSTANCE, PRIME SHIFT ACROSS MULTIPLE DAYS.       *
*        IF YOU LEAVE DATE BLANK, ALL DAYS IN SMF INPUT FILE ARE     *
*        SELECTED.  IF YOU SPECIFY DATE, SPECIFY TIME IN COLS 1-8    *
*        (YOU CAN SPECIFY 00002400 FOR EVERYTHING), LEAVE COL 9      *
*        BLANK, AND SPECIFY LOW AND HIGH DATE IN COLS 10-19  AS      *
*        YYDDDYYDDD (JULIAN).                                        *
*        PROGRAM CONTINUES TO SCAN AS WITH TIME.                     *
*                                                                    *
*        THE SECOND INPUT CARD IS RECORD SUPPRESSION; THIS IS A      *
*        LIST OF 2-DIGIT SMF RECORD TYPES NOT TO REPORT SEPARATED    *
*        BY SINGLE BLANKS (0-9 MUST HAVE HIGH-ORDER ZERO, IE 05)     *
*        (EXCEPT FOR TYPE 255, WHICH IS OF COURSE 3 DIGITS).         *
*                                                                    *
*        THE THIRD INPUT CARD IS JOB(TSO) NAME(S) SELECTION.         *
*        DEFAULT (OR BLANK) IS ALL JOBS.  IF A NAME IS SPECIFIED     *
*        IT MUST START IN COLUMN 1; ONLY HITS ON THAT NAME WILL      *
*        BE REPORTED.  IF A PARTIAL NAME TERMINATED WITH PERIODS     *
*        IS SPECIFIED, (IE NAME....) ANYTHING MAY REPLACE THE ....   *
*        IE ANY JOB BEGINNING 'NAME' WOULD BE SELECTED.  A NAME      *
*        OF ALL PERIODS CAUSES NO NAME HITS AND THUS GIVES ONLY      *
*        NON-JOB RECORDS WHOSE TYPES ARE SELECTED.                   *
*                                                                    *
**********************************************************************
*
*
**********************************************************************
         EJECT
$SMFBRWS SAVEALL REG=(11,3)
         EJECT                                                  KHPRDC
         L     R2,0(,R1)            POINT TO PARAMETER LIST     KHPRDC
         CLC   0(2,R2),=H'5'        MESSAGES SUPPRESSED?        KHPRDC
         BNE   OKAYMSGS             CANNOT BE; LENGTH WRONG     KHPRDC
         CLC   2(5,R2),=C'NOMSG'    MESSAGES SUPPRESSED?        KHPRDC
         BNE   OKAYMSGS             NO, WRONG TEXT              KHPRDC
         MVI   NOMSGSWX,X'FF'       **NO COMMENTARY MSGS**      KHPRDC
         SPACE 1                                                KHPRDC
OKAYMSGS DS    0H                   CONTINUE                    KHPRDC
         EJECT                                                  KHPRDC
*
* THIS PROGRAM NEEDS 'SYS1.AMODGEN' AS PART OF SYSLIB
*
*        ESTAB SECND=11            SAVE REGS AND SET UP
*                                  ADDRESSABILITY.
         USING SMFRCD0,R5          COMMON RECORD SEGMENT.
         OPEN  SMFTP               OPEN THE TAPE FILE.
         OPEN  (OUT,OUTPUT)
         OPEN  INPUT
         CLI   NOMSGSWX,X'FF'      DOING MESSAGES?              KHPRDC
         BE    SKPMSG1             NO, SKIP MSG                 KHPRDC
         PUT   OUT,MSG1
SKPMSG1  DS    0H                                               KHPRDC
         GET   INPUT,INCARD                                     KHPRDC
         CLI   NOMSGSWX,X'FF'      DOING MESSAGES?              KHPRDC
         BE    SKPMSG1C            NO, SKIP CTL CARD            KHPRDC
         PUT   OUT,IMAGCARD                                     KHPRDC
SKPMSG1C DS    0H                                               KHPRDC
         LA    R5,INCARD                                        KHPRDC
         CLI   0(R5),C' '     ANY TIMES?                        KHPRDC
         BE    PUTTWO          NO, LEAVE DEFAULTS               KHPRDC
         PACK  WORK,0(2,R5)        START HH                     KHPRDC
         PACK  WORK2,2(2,R5)       START MM                     KHPRDC
         SR    R0,R0               SETUP FOR MULTIPLY           KHPRDC
         CVB   R1,WORK             HH TO BINARY                 KHPRDC
         M     R0,=A(3600*100)     HOURS IN 100THS SECONDS      KHPRDC
         SR    R14,R14             SETUP FOR MULTIPLY           KHPRDC
         CVB   R15,WORK2           MM TO BINARY                 KHPRDC
         M     R14,=A(60*100)      MINUTES IN 100THS SECONDS    KHPRDC
         AR    R1,R15              HOURS+MINUTES                KHPRDC
         ST    R1,STIME            STORE START TIME BIN         KHPRDC
         PACK  WORK,4(2,R5)        STOP HH                      KHPRDC
         PACK  WORK2,6(2,R5)       STOP MM                      KHPRDC
         SR    R0,R0               SETUP FOR MULTIPLY           KHPRDC
         CVB   R1,WORK             HH TO BINARY                 KHPRDC
         M     R0,=A(3600*100)     HOURS IN 100THS SECONDS      KHPRDC
         SR    R14,R14             SETUP FOR MULTIPLY           KHPRDC
         CVB   R15,WORK2           MM TO BINARY                 KHPRDC
         M     R14,=A(60*100)      MINUTES IN 100THS SECONDS    KHPRDC
         AR    R1,R15              HOURS+MINUTES                KHPRDC
         ST    R1,QTIME            STORE STOP  TIME BIN         KHPRDC
         CLI   9(R5),C' '   ANY DATES?                          KHPRDC
         BE    PUTTWO        NO, LEAVE DEFAULTS                 KHPRDC
         PACK  SDATE+1(3),9(5,R5) START DATE                    KHPRDC
         PACK  QDATE+1(3),14(5,R5) STOP DATE                    KHPRDC
         OI    SDATE+3,X'0F'                                    KHPRDC
         OI    QDATE+3,X'0F'                                    KHPRDC
PUTTWO   CLI   NOMSGSWX,X'FF'      DOING MESSAGES?              KHPRDC
         BE    SKPMSG2             NO                           KHPRDC
         PUT   OUT,MSG2                                         KHPRDC
         PUT   OUT,MSG3
SKPMSG2  DS    0H                                               KHPRDC
         GET   INPUT,INCARD                                     KHPRDC
         CLI   NOMSGSWX,X'FF'     DOING MESSAGES?               KHPRDC
         BE    SKPMSG2C           NO                            KHPRDC
         PUT   OUT,IMAGCARD                                     KHPRDC
SKPMSG2C DS    0H                                               KHPRDC
         LA    R5,INCARD                                        KHPRDC
         LA    R9,BRANCHES+2
NEXLOOP  EQU   *
         CLC   0(3,R5),=C'255'  CHECK FOR TYPE 255 (>100)       KHPRDC
         BNE   NEXLOVL          NO, TRY REGULAR CHECK           KHPRDC
         MVI   NO255,X'80'      FLAG NOT DO T=255 RCDS          KHPRDC
         LA    R5,4(R5)         JUMP PAST '255 '                KHPRDC
         B     NEXLOOP          AND TRY FOR ANOTHER             KHPRDC
NEXLOVL  CLI   0(R5),C'0'                                AXC    KHPRDC
         BL    NEXEND                                    AXC
         PACK  WORK,0(2,R5)
         CVB   R10,WORK
         LTR   R10,R10
         BZ    NEXEND
         SLA   R10,2
         LA    R10,0(R10,R9)
         MVC   0(2,R10),NEXAD
         LA    R5,3(R5)
         B     NEXLOOP
NEXEND   EQU   *
         SPACE 2
         CLI   NOMSGSWX,X'FF'       DOING MESSAGES?             KHPRDC
         BE    SKPMSG4                     NO                   KHPRDC
         PUT   OUT,MSG4
SKPMSG4   DS    0H                                              KHPRDC
         GET   INPUT,INCARD                                     KHPRDC
         CLI   NOMSGSWX,X'FF'       DOING MESSAGS?              KHPRDC
         BE    SKPMSG4C             NO                          KHPRDC
         PUT   OUT,IMAGCARD                                     KHPRDC
SKPMSG4C DS    0H                                               KHPRDC
         LA    R5,INCARD                                        KHPRDC
         CLI   0(R5),C' '
         BE    NAMEND
         MVC   JOBNAME,0(R5)
         LA    R1,L'JOBNAME  LENGTH OF JOB NAME FIELD           KHPRDC
         LA    R5,JOBNAME+7  POINT TO LAST BYTE OF JOB NAME     KHPRDC
JOBNMLOP CLI   0(R5),C'.'    ANY CHAR INDICATED?                KHPRDC
         BNE   JOBNMOUT     NO, SAVE CURRENT LENGTH             KHPRDC
         BCTR  R5,0        DROP CURSOR BY ONE                   KHPRDC
         BCT   R1,JOBNMLOP DROP LENGTH AND TRY NEXT CHAR        KHPRDC
         B     JOBNMFLG    FALL THROUGH = ALL ........ - NONE   KHPRDC
JOBNMOUT BCTR  R1,0        DROP BY ONE FOR EXECUTE              KHPRDC
         ST    R1,JOBNAMEL  SAVE LENGTH TO EX-CHECK             KHPRDC
JOBNMFLG MVI   FLAG,X'80'  SHOW DOING SPECIFIC JOBNAME(RANGE)   KHPRDC
NAMEND   EQU   *
DONEIN   CLOSE INPUT                                            KHPRDC
         CLI   NOMSGSWX,X'FF'      DOING MESSAGES?              KHPRDC
         BE    SKPMSG5             NO                           KHPRDC
         PUT   OUT,MSG5                                         KHPRDC
SKPMSG5  DS    0H                                               KHPRDC
         EJECT                                                  KHPRDC
NEXREC   EQU   *
         GET   SMFTP               READ THE TAPE.
         LR    R5,R1               SAVE THE BUFFER ADDRESS.
         SPACE
         CLI   SMF0RTY,X'02'       CHECK FOR HEADER REC.
         BE    NEXREC              IF SO, SKIP IT.
         CLI   SMF0RTY,X'03'       CHECK FOR TRAILER REC.
         BE    NEXREC              IF SO, SKIP IT.
         TM    SMF0SEG,X'03'       CHECK FOR COMPLETE REC.  *HMD 08/81*
         BNZ   NEXREC              NO, SEGMENT OF RECORD    *HMD 08/81*
         MVC   TIME,SMF0TME
         MVC   DATE,SMF0DTE                                     KHPRDC
         CLC   DATE,SDATE                                       KHPRDC
         BL    NEXREC                                           KHPRDC
         CLC   DATE,QDATE                                       KHPRDC
         BH    NEXREC                                           KHPRDC
         CLC   TIME,STIME
         BL    NEXREC
         CLC   TIME,QTIME
*        BH    EOF                                              KHPRDC
         BH    NEXREC                                           KHPRDC
         SPACE 3
         SLR   R10,R10             CLEAR REG 10.
         IC    R10,SMF0RTY         LOAD RECTYP.
         C     R10,=F'100'         CHECK FOR TYPE GREATER THAN 100.
         BH    TRYKFHP             YES, CHECK FOR KHPRDC 255.   KHPRDC
         C     R10,=F'0'           CHECK FOR TYPE LESS THAN ZERO.
         BL    NEXREC              READ AGAIN IF SO.
         SLA   R10,2               MULTIPLY TYPE BY FOUR.
         LA    R9,BRANCHES         LOAD ADDRESS OF THE BRANCH TABLE.
         B     0(R10,R9)           BRANCH INTO TABLE.
         EJECT                                                  KHPRDC
TRYKFHP  C     R10,=F'255'         IS THIS KFHP IEFUJV RECORD?  KHPRDC
         BNE   NEXREC              NO, ONLY ONE > 100           KHPRDC
         TM    NO255,X'80'      ARE WE NOT DOING 255?           KHPRDC
         BO    NEXREC           CORRECT; SKIP IT                KHPRDC
         B     TYPE255           GO DO 255 RCD                  KHPRDC
         SPACE 3
EOF      EQU   *
         CLOSE SMFTP               CLOSE THE TAPE FILE.
         CLOSE OUT
*        EXIT  RC=0                ....AND EXIT.
         B     END
         EJECT
BRANCHES DS    0H
         B     IPLREC              0 IPL.
         B     NEXREC              1 DOES NOT EXIST.
         B     NEXREC              2 DUMP HEADER.
         B     NEXREC              3 DUMP TRAILER.
         B     STPREC              4 STEP TERMINATION.
         B     JOBREC              5 JOB TERMINATION.
         B     SYSOUT              6 OUTPUT WRITER.
         B     LOSTD               7 SMF DATA LOST.
         B     NEXREC              8 I/O CONFIGURATION.
         B     VONLINE             9 VARY ONLINE.
         B     ALLREC              10 ALLOCATION RECOVERY.
         B     VOFFLINE            11 VARY OFFLINE.
         B     NEXREC              12 DOES NOT EXIST.
         B     NEXREC              13 DOES NOT EXIST.
         B     DASIN               14 INPUT DATA SET ACTIVITY.
         B     DASOUT              15 OUTPUT DATA SET ACTIVITY.
         B     NEXREC              16 DOES NOT EXIST.
         B     DASCR               17 SCRATCH DATA SET STATUS.
         B     RENAME              18 RENAME DATA SET STATUS.
         B     NEXREC              19 DIRECT-ACCESS VOLUME.
         B     NUJOB               20 JOB COMMENCEMENT.
         B     NEXREC              21 ERROR STATISTICS BY VOLUME.
         B     NEXREC              22 CONFIGURATION.
         B     NEXREC              23 DOES NOT EXIST.
         B     NEXREC              24 DOES NOT EXIST.
         B     NEXREC              25 DOES NOT EXIST.
         B     PURGE               26 JES2 PURGE .
         B     NEXREC              27 DOES NOT EXIST.
         B     NEXREC              28 DOES NOT EXIST.
         B     NEXREC              29 DOES NOT EXIST.
         B     NEXREC              30 DOES NOT EXIST.
         B     TIOCIN              31 TIOC INITIALIZATION.
         B     NEXREC              32 DOES NOT EXIST.
         B     NEXREC              33 DOES NOT EXIST.
         B     TSOCOM              34 TS STEP TERMINATION.
         B     LOGOFF              35 TS JOB TERMINATION (LOGOFF).
         B     NEXREC              36 DOES NOT EXIST.
         B     NEXREC              37 DOES NOT EXIST.
         B     NEXREC              38 DOES NOT EXIST.
         B     NEXREC              39 DOES NOT EXIST.
         B     NEXREC              40 DYNAMIC DD.
         B     NEXREC              41 DOES NOT EXIST.
         B     NEXREC              42 DOES NOT EXIST.
         B     JES2ST              43 JES2 START.
         B     NEXREC              44 DOES NOT EXIST.           KHPRDC
         B     JESTOP              45 JES2 WITHDRAWAL.
         B     NEXREC              46 DOES NOT EXIST.
         B     RJEON               47 RJE SIGNON.
         B     RJEOFF              48 RJE SIGNOFF.
         B     NEXREC              49 JES2 INTEGRITY.
         B     NEXREC              50 DOES NOT EXIST.
         B     NEXREC              51 DOES NOT EXIST.
         B     NEXREC              52 DOES NOT EXIST.
         B     NEXREC              53 DOES NOT EXIST.
         B     NEXREC              54 DOES NOT EXIST.
         B     NEXREC              55 DOES NOT EXIST.
         B     NEXREC              56 DOES NOT EXIST.
         B     NEXREC              57 DOES NOT EXIST.
         B     NEXREC              58 DOES NOT EXIST.
         B     NEXREC              59 DOES NOT EXIST.
         B     NEXREC              60 DOES NOT EXIST.
         B     NEXREC              61 DOES NOT EXIST.
         B     VSAMOPEN            62 VSAM CLUSTER OR COMP OPENED.
         B     VSAMDEFN            63 VSAM CLUSTER OR COMP CATLGD.
         B     NEXREC              64 VSAM COMPONENT STATUS.
         B     NEXREC              65 DOES NOT EXIST.
         B     NEXREC              66 DOES NOT EXIST.
         B     VSAMDEL             67 VSAM ENTRY DELETED.
         B     VSAMRNAM            68 VSAM ENTRY RENAMED.
         B     NEXREC              69 VSAM DATA SPC DEFD OR DELD.
         B     NEXREC              70 CPU ACTIVITY.
         B     NEXREC              71 PAGING ACTIVITY.
         B     NEXREC              72 WORKLOAD ACTIVITY.
         B     NEXREC              73 CHANNEL ACTIVITY.
         B     NEXREC              74 DEVICE ACTIVITY.
         B     NEXREC              75 PAG/SWAP DASET ACTIVITY.
         B     NEXREC              76 TRACE ACTIVITY.
         B     NEXREC              77 ENQUEUE ACTIVITY.
         B     NEXREC              78 DOES NOT EXIST.
         B     NEXREC              79 MONITOR II ACTIVITY.
         B     NEXREC              80 RACF PROCESSING.          KHPRDC
         B     NEXREC              81 RACF INITIALIZATION.      KHPRDC
         B     NEXREC              82 CRYPTOGRAPHIC ACTIVITY
         B     NEXREC              83 CRYPTOGRAPHIC ACTIVITY
         B     NEXREC              84 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              85 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              86 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              87 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              88 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              89 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              90 SYSTEM STATUS RECORD  *HMD 08/81*
         B     NEXREC              91 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              92 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              93 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              94 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              95 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              96 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              97 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              98 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC              99 DOES NOT EXIST        *HMD 08/81*
         B     NEXREC             100 DOES NOT EXIST        *HMD 08/81*
         EJECT
IPLREC   EQU   *
*        B     NEXREC                      AXC/REINSTATED       KHPRDC
         USING SMFRCD0,R5                                       KHPRDC
         TM    FLAG,X'80'   NEVER DO NON-JOB-RELATED            KHPRDC
         BO    NEXREC       IF RUNNING FOR JOB NAME(S)          KHPRDC
         BAL   R14,TIMED
         MVC   MESG(31),=C'SYSTEM WAS IPLED - MAIN STORAGE'     KHPRDC
         ICM   R0,15,SMF0RST  GET REAL STORAGE SIZE             KHPRDC
         CVD   R0,DBL         CONVERT TO DEC                    KHPRDC
         MVC   MESG+31(6),=X'402021202020'  PUT IN MESSAGE      KHPRDC
         ED    MESG+31(6),DBL+5                                 KHPRDC
         MVI   MESG+37,C'K'     MARK AS THOUSANDS               KHPRDC
         PUT   OUT,LINE
         B     NEXREC
         EJECT
STPREC   EQU   *
         USING SMFRCD4,R5
         TM    FLAG,X'80'
         BZ    STPA
         LA    R15,SMF4JBN                                      KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
STPA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(30),=C'XXXXXXXX STEP XXX XXXXXXXX PGM'      KHPRDC
         MVC   MESG(8),SMF4JBN                                  KHPRDC
         SR    R10,R10
         IC    R10,SMF4STN
         CVD   R10,WORK
         MVC   MESG+13(4),PAT1                                  KHPRDC
         ED    MESG+13(4),WORK+6                                KHPRDC
         MVC   MESG+18(8),SMF4STMN                              KHPRDC
         MVC   MESG+31(8),SMF4PGMN                              KHPRDC
         TM    SMF4STI,X'01'   WAS STEP FLUSHED?                KHPRDC
         BNO   STPB           NO, CONTINUE                      KHPRDC
         MVC   MESG+40(7),=C'FLUSHED'                           KHPRDC
         B     STPC           GO AROUND RC/CC                   KHPRDC
STPB     LA    R1,SMF4STI     POINT TO RC/CC INDICATOR          KHPRDC
         SR    R0,R0          CLEAR REGISTER                    KHPRDC
         ICM   R0,3,SMF4SCC   GET RC/CC                         KHPRDC
         LA    R15,MESG+40    POINT TO 'ENDED/ABENDED' AREA     KHPRDC
         BAL   R14,CCRCCOMP   COMPUTE RC/CC                     KHPRDC
STPC     PUT   OUT,LINE                                         KHPRDC
         B     NEXREC
         EJECT
JOBREC   EQU   *
         USING SMFRCD5,R5
         TM    FLAG,X'80'
         BZ    JOBA
         LA    R15,SMF5JBN                                      KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
JOBA     EQU   *
         BAL   R14,TIMED
*        MVC   MESG(22),=C'XXXXXXXX HAS ENDED'                  KHPRDC
         MVC   MESG(8),SMF5JBN                                  KHPRDC
         LA    R1,SMF5JBTI    POINT TO RC/CC INDICATOR          KHPRDC
         SR    R0,R0          CLEAR REGISTER                    KHPRDC
         ICM   R0,3,SMF5JCC   GET RC/CC                         KHPRDC
         LA    R15,MESG+9     POINT TO 'ENDED/ABENDED' AREA     KHPRDC
         BAL   R14,CCRCCOMP   COMPUTE RC/CC                     KHPRDC
         PUT   OUT,LINE
         B     NEXREC
         EJECT
NUJOB    EQU   *
         USING SMFRCD20,R5     ADDRESS THE SMF RECORD       *HMD 08/81*
         TM    FLAG,X'80'
         BZ    NUJA
         LA    R15,SMF20JBN                                     KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
NUJA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(16),=C'XXXXXXXX STARTED'                    KHPRDC
         MVC   MESG(8),SMF20JBN                                 KHPRDC
         PUT   OUT,LINE
         B     NEXREC
         EJECT
SYSOUT   EQU   *
         USING SMF6LEN,R5
         TM    FLAG,X'80'
         BZ    SYOA
         LA    R15,SMF6JBN                                      KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
SYOA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(24),=C'XXXXXXXX SYSOUT CLASS X;'            KHPRDC
         MVC   MESG+22(1),SMF6OWC                               KHPRDC
         MVC   MESG(8),SMF6JBN                                  KHPRDC
         MVC   MESG+24(15),=C' XXXXXXXX LINES'                  KHPRDC
         ICM   R1,15,SMF6NLR       GET # LINES DONE BY PPU      KHPRDC
         CVD   R1,DBL         CONVERT TO DEC                    KHPRDC
         MVC   MESG+24(9),=X'402020202020202120'                KHPRDC
         ED    MESG+24(9),DBL+3    EDIT TO PRINT                KHPRDC
         PUT   OUT,LINE
         B     NEXREC
         EJECT
LOSTD    EQU   *
         USING SMF7LEN,R5
         TM    FLAG,X'80'   NEVER DO NON-JOB-RELATED            KHPRDC
         BO    NEXREC       IF RUNNING FOR JOB NAME(S)          KHPRDC
         BAL   R14,TIMED
         MVC   MESG(13),=C'SMF DATA LOST'
         PUT   OUT,LINE
         B     NEXREC
         EJECT
VONLINE  EQU   *
         USING SMF9LEN,R5
         TM    FLAG,X'80'   NEVER DO NON-JOB-RELATED            KHPRDC
         BO    NEXREC       IF RUNNING FOR JOB NAME(S)          KHPRDC
         BAL   R14,TIMED
         MVC   MESG(26),=C'UNIT XXX WAS VARIED ONLINE'
         MVC   WORK+2(2),SMF9DVAD+2
         BAL   R14,CONV
         MVC   MESG+5(3),WORK+1
         PUT   OUT,LINE
         B     NEXREC
         EJECT
ALLREC   EQU   *
         USING SMF10LEN,R5
         TM    FLAG,X'80'
         BZ    ALLA
         LA    R15,SMF10JBN                                     KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
ALLA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(31),=C'XXXXXXXX IN ALLOC RCVRY ON UNIT'     KHPRDC
         MVC   MESG(8),SMF10JBN                                 KHPRDC
         MVC   WORK+2(2),SMF10DEV+2
         BAL   R14,CONV
         MVC   MESG+32(3),WORK+1                                KHPRDC
         PUT   OUT,LINE
         B     NEXREC
         EJECT
VOFFLINE EQU   *
         USING SMF11LEN,R5
         TM    FLAG,X'80'   NEVER DO NON-JOB-RELATED            KHPRDC
         BO    NEXREC       IF RUNNING FOR JOB NAME(S)          KHPRDC
         BAL   R14,TIMED
         MVC   MESG(23),=C'UNIT XXX VARIED OFFLINE'
         MVC   WORK+2(2),SMF11DEV+2
         BAL   R14,CONV
         MVC   MESG+5(3),WORK+1
         PUT   OUT,LINE
         B     NEXREC
         EJECT
DASIN    EQU   *
         USING SMF14LEN,R5
         TM    FLAG,X'80'
         BZ    DASA
         LA    R15,SMF14JBN                                     KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
DASA     EQU   *
         TM    SMF14RIN,X'10'
         BO    NEXREC
         BAL   R14,TIMED
         MVC   MESG(28),=C'XXXXXXXX HAS READ FROM NAME='        KHPRDC
         MVC   MESG(8),SMF14JBN                                 KHPRDC
         MVC   MESG+28(44),SMFJFCB1                             KHPRDC
         PUT   OUT,LINE
         B     NEXREC
         EJECT
DASOUT   EQU   *
         TM    SMF14RIN,X'10'
         BO    NEXREC
         TM    FLAG,X'80'
         BZ    DAOA
         LA    R15,SMF14JBN                                     KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
DAOA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(29),=C'XXXXXXXX HAS WRITTEN TO NAME='       KHPRDC
         MVC   MESG(8),SMF14JBN                                 KHPRDC
         MVC   MESG+29(44),SMFJFCB1                             KHPRDC
         PUT   OUT,LINE
         B     NEXREC
         EJECT
DASCR    EQU   *
         USING SMF17LEN,R5
         TM    FLAG,X'80'
         BZ    DACA
         LA    R15,SMF17JBN                                     KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
DACA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(23),=C'XXXXXXXX HAS SCRATCHED '             KHPRDC
         MVC   MESG(8),SMF17JBN                                 KHPRDC
         MVC   MESG+23(44),SMF17DSN                             KHPRDC
         PUT   OUT,LINE
         B     NEXREC
         EJECT
RENAME   EQU   *
         USING SMF18LEN,R5
         TM    FLAG,X'80'
         BZ    RENA
         LA    R15,SMF18JBN                                     KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
RENA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(17),=C'XXXXXXXX DATASET '                   KHPRDC
         MVC   MESG(8),SMF18JBN                                 KHPRDC
         MVC   MESG+17(44),SMF18ODS                             KHPRDC
         LA    R10,MESG+17                                      KHPRDC
RENLOOP  EQU   *
         CLI   0(R10),C' '
         BE    RENGOT
         LA    R10,1(R10)
         B     RENLOOP
RENGOT   EQU   *
         LA    R10,1(R10)
         MVC   0(11,R10),=C'WAS RENAMED'
         MVC   12(44,R10),SMF18NDS
         PUT   OUT,LINE
         B     NEXREC
         EJECT
PURGE    EQU   *
         USING SMF26LEN,R5
         TM    FLAG,X'80'
         BZ    PURA
         LA    R15,SMF26JBN                                     KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
PURA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(8),SMF26JBN                                 KHPRDC
         MVC   MESG+9(8),SMF26JID                               KHPRDC
         MVC   MESG+18(24),=C'FROM XXXXXXXX HAS PURGED'         KHPRDC
         MVC   MESG+23(8),SMF26DEV      INPUT DEVICE            KHPRDC
         CLC   SMF26UIF+2(6),=C'IEFUJV'  FLUSHED BY UJV?        KHPRDC
         BNE   PURB                                             KHPRDC
         MVC   MESG+44(20),=C'- IEFUJV FLUSH CODE '             KHPRDC
         MVC   MESG+64(2),SMF26UIF     IEFUJV FLUSH CODE        KHPRDC
PURB     PUT   OUT,LINE                                         KHPRDC
         B     NEXREC
         EJECT
TIOCIN   EQU   *
         TM    FLAG,X'80'   NEVER DO NON-JOB-RELATED            KHPRDC
         BO    NEXREC       IF RUNNING FOR JOB NAME(S)          KHPRDC
         BAL   R14,TIMED
         MVC   MESG(15),=C'TSO WAS STARTED'
         PUT   OUT,LINE
         B     NEXREC
         EJECT
TSOCOM   EQU   *
         USING TIVRLEN,R5
         TM    FLAG,X'80'
         BZ    TSOA
         LA    R15,TIVUIF
         BNE   NEXREC
TSOA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(29),=C'XXXXXXXX TSOCOM XXXXXXXX PGM '       KHPRDC
*        MVC   MESG(6),=C'TSOCOM'
         MVC   MESG(8),TIVUIF                                   KHPRDC
         MVC   MESG+29(8),TIVPRGNM                              KHPRDC
         MVC   MESG+16(8),TIVINVNM                              KHPRDC
         PUT   OUT,LINE
         B     NEXREC
         EJECT
LOGOFF   EQU   *
         USING TLGRLEN,R5
         TM    FLAG,X'80'
         BZ    LOGA
         LA    R15,TLGUIF
         BNE   NEXREC
LOGA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(22),=C'XXXXXXXX TSO LOGOFF - '              KHPRDC
         MVC   MESG(8),TLGUIF                                   KHPRDC
         MVC   MESG+22(14),=C'XXXXXXXX TRANS'                   KHPRDC
         ICM   R1,15,TLGNTSN     # TRANSACTIONS                 KHPRDC
         CVD   R1,DBL            TO DECIMAL                     KHPRDC
         MVC   MESG+21(9),=X'402020202020202120'                KHPRDC
         ED    MESG+21(9),DBL+3   EDIT # TRANSACTIONS           KHPRDC
         PUT   OUT,LINE
         B     NEXREC
         EJECT
JES2ST   EQU   *
         USING SMF43LEN,R5
         TM    FLAG,X'80'   NEVER DO NON-JOB-RELATED            KHPRDC
         BO    NEXREC       IF RUNNING FOR JOB NAME(S)          KHPRDC
         BAL   R14,TIMED
         MVC   MESG(12),=C'JES2 STARTED'
         PUT   OUT,LINE
         B     NEXREC
         EJECT
JESTOP   EQU   *
         USING SMF45LEN,R5
         TM    FLAG,X'80'   NEVER DO NON-JOB-RELATED            KHPRDC
         BO    NEXREC       IF RUNNING FOR JOB NAME(S)          KHPRDC
         BAL   R14,TIMED
         MVC   MESG(14),=C'JES2 WITHDRAWN'
         PUT   OUT,LINE
         B     NEXREC
         EJECT
RJEON    DS    0H
         USING SMF47LEN,R5
         TM    FLAG,X'80'   NEVER DO NON-JOB-RELATED            KHPRDC
         BO    NEXREC       IF RUNNING FOR JOB NAME(S)          KHPRDC
         BAL   R14,TIMED
         TM    SMF47EVT+1,SMF47STL   START LINE?                KHPRDC
         BZ    RJEONRMT       REMOTE SIGNON                     KHPRDC
         MVC   MESG(10),=C'LINE START'                          KHPRDC
         B     RJEONMOV                                         KHPRDC
RJEONRMT MVC   MESG(10),=C'RJE SIGNON'                          KHPRDC
RJEONMOV DS    0H             MOVE INFO TO OUTPUT LINE      *HMD 08/81*
         OC    SMF47RMT,SMF47RMT   IS FIELD ZEROS?          *HMD 08/81*
         BZ    RJEONMVL       ONLY MOVE LINE NUMBER         *HMD 08/81*
         MVC   MESG+11(8),SMF47RMT
RJEONMVL MVC   MESG+20(8),SMF47LIN                          *HMD 08/81*
         PUT   OUT,LINE                                     *HMD 08/81*
         B     NEXREC                                       *HMD 08/81*
         EJECT
RJEOFF   EQU   *
         USING SMF48LEN,R5
         TM    FLAG,X'80'   NEVER DO NON-JOB-RELATED            KHPRDC
         BO    NEXREC       IF RUNNING FOR JOB NAME(S)          KHPRDC
         BAL   R14,TIMED
         TM    SMF48EVT+1,SMF48CAN      LINE STOP?              KHPRDC
         BZ    RJEOFFRM       NO, REMOTE SIGNOFF                KHPRDC
         MVC   MESG(12),=C'LINE STOPPED'                        KHPRDC
         B     RJEOFFMV                                     *HMD 08/81*
RJEOFFRM MVC   MESG(12),=C'RJE SIGNOFF '                        KHPRDC
RJEOFFMV DS    0H             MOVE LINE INFO TO OUTPUT      *HMD 08/81*
         OC    SMF48RMT,SMF48RMT   IS FIELD ZEROS?          *HMD 08/81*
         BZ    RJEOFFLN       YES, ONLY LINE STOP           *HMD 08/81*
         MVC   MESG+13(8),SMF48RMT                          *HMD 08/81*
RJEOFFLN MVC   MESG+22(8),SMF48LIN                          *HMD 08/81*
         PUT   OUT,LINE                                     *HMD 08/81*
         B     NEXREC                                       *HMD 08/81*
         EJECT
VSAMOPEN EQU   *
         USING SMF62LEN,R5
         TM    FLAG,X'80'
         BZ    VSOA
         LA    R15,SMF62JBN                                     KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
VSOA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(18),=C'XXXXXXXX VSAMOPEN '                  KHPRDC
         MVC   MESG(8),SMF62JBN                                 KHPRDC
         MVC   MESG+18(44),SMF62DNM                             KHPRDC
         PUT   OUT,LINE
         B     NEXREC
         EJECT
VSAMDEFN EQU   *
         USING SMF63LEN,R5
         TM    FLAG,X'80'
         BZ    VSDA
         LA    R15,SMF63JBN                                     KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
VSDA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(20),=C'XXXXXXXX VSAMDEFINE '                KHPRDC
         MVC   MESG(8),SMF63JBN                                 KHPRDC
         MVC   MESG+20(44),SMF63ENM
         LA    R10,MESG+20
DEFNLOOP EQU   *
         CLI   0(R10),C' '
         BE    DEFNGOT
         LA    R10,1(R10)
         B     DEFNLOOP
DEFNGOT  EQU   *
         LA    R10,1(R10)
         MVC   0(11,R10),=C'IN CATALOG '
         MVC   12(44,R10),SMF63CNM
         PUT   OUT,LINE
         B     NEXREC
         EJECT
VSAMDEL  EQU   *
         USING SMF67LEN,R5
         TM    FLAG,X'80'
         BZ    VSLA
         LA    R15,SMF67JBN                                     KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
VSLA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(20),=C'XXXXXXXX VSAMDELETE '                KHPRDC
         MVC   MESG(8),SMF67JBN                                 KHPRDC
         MVC   MESG+20(44),SMF67DEN
         LA    R10,MESG+20
DELTLOOP EQU   *
         CLI   0(R10),C' '
         BE    DELTGOT
         LA    R10,1(R10)
         B     DELTLOOP
DELTGOT  EQU   *
         LA    R10,1(R10)
         MVC   0(11,R10),=C'IN CATALOG '                        KHPRDC
         MVC   12(44,R10),SMF67CNM
         PUT   OUT,LINE
         B     NEXREC
         EJECT
VSAMRNAM EQU   *
         USING SMF68LEN,R5
         TM    FLAG,X'80'
         BZ    VSRA
         LA    R15,SMF68JBN                                     KHPRDC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC
VSRA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(20),=C'XXXXXXXX VSAMRENAME '                KHPRDC
         MVC   MESG(8),SMF68JBN                                 KHPRDC
         MVC   MESG+20(44),SMF68ONM
         LA    R10,MESG+20
RNAMLOOP EQU   *
         CLI   0(R10),C' '
         BE    RNAMGOT
         LA    R10,1(R10)
         B     RNAMLOOP
RNAMGOT  EQU   *
         LA    R10,1(R10)
         MVC   0(11,R10),=C'WAS RENAMED'
         MVC   13(44,R10),SMF68NNM
         PUT   OUT,LINE
         B     NEXREC
         EJECT                                                  KHPRDC
TYPE255  EQU   *                                                KHPRDC
         USING SMFRCDFF,R5    ADDRESS IEFUJV RECORD             KHPRDC
         TM    FLAG,X'80'                                       KHPRDC
         BZ    T255A                                            KHPRDC
         LA    R15,SMFFJBN                                      KHPDRC
         L     R1,JOBNAMEL     GET EX-LENGTH                    KHPRDC
         EX    R1,JOBNAMEX     EXECUTE-COMPARE                  KHPRDC
         BNE   NEXREC                                           KHPRDC
T255A    EQU   *                                                KHPRDC
         BAL   R14,TIMED   TIME DONE                            KHPRDC
         MVC   MESG(8),SMFFJBN      JOB/TSO NAME                KHPRDC
         MVC   MESG+9(3),=C'JOB'    SETUP FOR JOB               KHPRDC
         TM    SMFFTYPE,SMFFTTSO     IS THIS A TSO USER?        KHPRDC
         BNO   T255B            NO                              KHPRDC
         MVC   MESG+9(3),=C'TSO'                                KHPRDC
T255B    TM    SMFFTYPE,SMFFTUFL   FAILED BY UJV JOB CARD SCAN? KHPRDC
         BZ    T255C          NO, PICK UP VALUES                KHPRDC
         MVC   MESG+13(4),=C'BPAS'   SET BAD PASSWORD IN CASE   KHPRDC
         MVC   MESG+18(8),SMFFBPAS   COPY POSSIBLE BAD PASSWORD KHPRDC
         MVC   MESG+27(80),SMFFJCRD  COPY POSSIBLE BAD JOBCARD  KHPRDC
         B     T255OUT         EXIT TO PRINT                    KHPRDC
T255C    MVC   MESG+13(16),=C'BY *** OF ****. '                 KHPRDC
         MVC   MESG+16(3),SMFFUSER    USER ID                   KHPRDC
         MVC   MESG+23(4),SMFFGNAM    GROUP NAME                KHPRDC
         MVC   MESG+29(L'SMFFRFOR),SMFFRFOR  RUN-FOR ACCT       KHPRDC
         LA    R15,MESG+29+L'SMFFRFOR       AFTER DPSR          KHPRDC
         CLI   SMFFDPSR,C' '         A DPSR PROVIDED?           KHPRDC
         BE    T255D                  NO                        KHPRDC
         MVI   0(R15),C'-'           YES, INSERT HYPHEN         KHPRDC
         MVC   1(L'SMFFDPSR,R15),SMFFDPSR  INSERT DPSR          KHPRDC
         LA    R15,L'SMFFDPSR+1(R15) POINT PAST DPSR            KHPRDC
T255D    LA    R15,1(,R15)   ADD A BLANK                        KHPRDC
         MVC   0(L'SMFFBTO,R15),SMFFBTO  INSERT BILL-TO ACCT    KHPRDC
         CLI   SMFFRCOD,C' '    A RERUN CODE PRESENT?           KHPRDC
         BE    T255OUT     NO, GO WRITE                         KHPRDC
         CLI   SMFFRCOD,X'00'   A RERUN CODE PRESENT?           KHPRDC
         BE    T255OUT     NO, GO WRITE                         KHPRDC
         MVI   L'SMFFBTO(R15),C'-'  INSERT A HYPHEN             KHPRDC
         MVC   L'SMFFBTO+1(L'SMFFRCOD,R15),SMFFRCOD ADD RERUN COKHPRDC
T255OUT  PUT   OUT,LINE   WRITE THE LINE                        KHPRDC
         B     NEXREC                                           KHPRDC
         EJECT                                                  KHPRDC
TIMED    EQU   *
         USING SMFRCD0,R5                                       KHPRDC
         L     R10,TIME
         CVD   R10,WORK
         DP    WORK,=P'100'
         DP    WORK(6),=P'3600'
         ZAP   WORK2,WORK(3)
         MP    WORK2,=P'10000'
         ZAP   WORK3,WORK+3(3)
         DP    WORK3,=P'60'
         ZAP   WORK,WORK3(6)
         MP    WORK,=P'100'
         AP    WORK2,WORK
         AP    WORK2,WORK3+6(2)
         ZAP   WORK,WORK2
         MVC   LTIME,PAT2
         ED    LTIME,WORK+4
         UNPK  LDATE(5),DATE+1(3)                               KHPRDC
         MVC   LSYSID,SMF0SID                                   KHPRDC
         MVC   MESG(120),BLANKS
         BR    R14
         SPACE 3
CONV     EQU   *
         LA    R6,CHARS
         L     R5,WORK
         SLL   R5,20
         SR    R4,R4
         SLDA  R4,4
         IC    R10,0(R4,R6)
         STC   R10,WORK+1
         SR    R4,R4
         SLDA  R4,4
         IC    R10,0(R4,R6)
         STC   R10,WORK+2
         SR    R4,R4
         SLDA  R4,4
         IC    R10,0(R4,R6)
         STC   R10,WORK+3
         BR    R14
         EJECT
CCRCCOMP TM    0(R1),X'02'    DID IT ABEND?                     KHPRDC
         BO    CCRCABND       YES, DO ABEND                     KHPRDC
         N     R0,=A(X'FFF')  ZERO UNUSED PORTION               KHPRDC
         CVD   R0,DBL         CONVERT TO DEC                    KHPRDC
         MVC   CCRC(6),=X'402020212020'                         KHPRDC
         ED    CCRC(6),DBL+5  RET CODE TO CHAR                  KHPRDC
         MVC   0(9,R15),=C'ENDED RC '                           KHPRDC
         MVC   9(4,R15),CCRC+2   RET CODE TO MSG                KHPRDC
         BR    R14            RETURN                            KHPRDC
CCRCABND MVC   0(8,R15),=C'ABENDED '                            KHPRDC
         CLM   R0,2,=X'80'    USER ABEND CODE?                  KHPRDC
         BL    CCRCSYSA    NO, DO SYSTEM ABEND                  KHPRDC
         N     R0,=A(X'FFF')   TURN OFF X'80' BIT               KHPRDC
         CVD   R0,DBL         CONVERT TO DECIMAL                KHPRDC
         MVC   CCRC(6),=X'402120202020'                         KHPRDC
         ED    CCRC(6),DBL+5  CONVERT TO CHAR                   KHPRDC
         MVC   8(3,R15),=C'UC '                                 KHPRDC
         MVC   11(4,R15),CCRC+2                                 KHPRDC
         BR    R14                                              KHPRDC
CCRCSYSA STH   R0,DBL         STORE SYS ABEND CODE              KHPRDC
         UNPK  DBL+3(5),DBL(3)   ADD ZONES TO CC FIELD          KHPRDC
         TR    DBL+4(3),CCRCTRTB-240                            KHPRDC
         MVC   8(3,R15),=C'SC '  ABEND TO MSG                   KHPRDC
         MVC   11(3,R15),DBL+4                                  KHPRDC
         BR    R14            BACK TO CALLER                    KHPRDC
         SPACE 3                                                KHPRDC
         DS    0D                                               KHPRDC
DBL      DC    D'0'                                             KHPRDC
CCRC     DC    CL6' '                                           KHPRDC
CCRCTRTB DC    C'0123456789ABCDEF'                              KHPRDC
         EJECT
LINE     DS    0CL133
         DC    C' '
LDATE    DC    CL4' '      AND OVERLAY EXTRA BLANK FOLLOWING    KHPRDC
LTIME    DC    CL10' '                                          KHPRDC
         DC    CL1' '                                           KHPRDC
LSYSID   DC    CL4' '                                           KHPRDC
         DC    CL2':  '                                         KHPRDC
MESG     DS    CL120
         SPACE
IMAGCARD DC    C' CONTROL CARD READ:  '                         KHPRDC
INCARD   DC    CL120' '                                         KHPRDC
         EJECT                                                  KHPRDC
WORK     DS    D
WORK2    DS    D
WORK3    DS    D
TIME     DS    F
DATE     DS    F                                                KHPRDC
STIME    DC    A(0)         DEFAULT START TIME 00000001 HRS     KHPRDC
QTIME    DC    A(2500*3600) DEFAULT STOP TIME > 2400 HRS        KHPRDC
SDATE    DC    XL4'0001001F'   DEFAULT START DATE 1901; JAN 1   KHPRDC
QDATE    DC    XL4'0099366F'   DEFAULT STOP DATE 1999; DEC 31   KHPRDC
JOBNAMEX CLC   JOBNAME(0),0(R15)  EXECUTED JOB NAME COMPARE     KHPRDC
JOBNAME  DC    CL8' '
JOBNAMEL DC    A(L'JOBNAME-1)  LENGTH OF JOB NAME FOR EX-COMP   KHPRDC
NEXAD    DC    S(NEXREC)
PAT1     DC    X'40202021'
PAT2     DC    X'402021207A20207A2020'
FLAG     DC    X'00'
NO255    DC    X'00'                                            KHPRDC
NOMSGSWX DC    X'00'                                            KHPRDC
CHARS    DC    C'0123456789ABCDEF'
MSG1     DC    C'1ENTER TIMES, BBBBEEEE  (0000-2400 RANGE); '   KHPRDC
         DC    C'ONE BLANK AND OPTIONAL DATES, YYDDDYYDDD; '    KHPRDC
         DC    CL50' OR NULL'                                   KHPRDC
MSG2     DS    0CL133
         DC    C' RECS PROC''D ARE:0,4-7,9-11,14,15,'
         DC    C'17,18,20,26,31,34,35,43,45,47,48,'
         DC    CL80'62,63,67,68,255'                            KHPRDC
MSG5     DC    C'1' THIS FOLLOWED BY 132 BLANKS SKIPS PAGE      KHPRDC
BLANKS   DC    CL120' '
BLANK    DC    CL13' '                                             AXC
MSG3     DC    CL133' TO SUPPRESS RECS ENTER NUMBERS SEP''D BY BLANKS; X
               OR ENTER NULL'
MSG4     DC    C' ENTER A SPECIFIC JOBNAME; A PARTIAL JOBNAME ' KHPRDC
         DC    C'WITH TRAILING .... FOR NAME RANGE; OR NULL'    KHPRDC
         DC    CL80' '                                          KHPRDC
         EJECT
         LTORG
         EJECT
INPUT    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,EODAD=DONEIN      KHPRDC
         EJECT
OUT      DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FA,   *
               BLKSIZE=133
         EJECT
SMFTP    DCB   DDNAME=SMFTPE,DSORG=PS,MACRF=GL,EODAD=EOF,              X
               BFTEK=A,BUFNO=10
         EJECT
END      ENDALL
         EJECT
RECDEF   DSECT
RECS     IFASMFR (0,4,5,6,7,9,10,11,14,17,18,20,26,31,34,35)
         IFASMFR (43,45,47,48,62,63,64,67,68,69)
         EJECT                                                  KHPRDC
***********************************************************************
*                                                                     *
*   KAISER HEALTH PLAN REGIONAL DATA CENTER SMF TYPE 255 RECORD.      *
*                                                                     *
*   SMF USER RECORD TYPE: 255   -   IEFUJV ACCOUNTING RECORD          *
*                                                                     *
*   THIS RECORD IS WRITTEN AT JOB INTERPRETATION TIME BY THE KHPRDC   *
*   IEFUJV ROUTINE.  THE PURPOSE OF THIS RECORD IS TO PROVIDE JOB-    *
*   RELATED INFORMATION IN SMF WHICH WAS ASSIGNED OR SET INTERNALLY   *
*   BY IEFUJV RATHER THAN (NECESSARILY) BY THE JOB ITSELF.            *
*                                                                     *
***********************************************************************
         SPACE 2
*--------------SMF STANDARD RECORD HEADER-----------------------------*
         DS    0F        ALIGN TO FULLWORD BOUNDARY
SMFRCDFF EQU   *         HEADER SEGMENT
SMFFLEN  DC    AL2(SMFFLGH)   RECORD LENGTH
SMFFSEG  DC    BL2'0'    SEGMENT DESCRIPTOR
SMFFFLG  DC    BL1'0'    HEADER FLAG BYTE:
SMFFMVS  EQU   X'02'     SMFFLG VALUE:  MVS
SMFFRTY  DC    BL1'0'    RECORD TYPE:
SMFF255  EQU   255                 SMFFRTY VALUE:  TYPE 255
SMFFTME  DC    BL4'0'    TOD USING FORMAT FROM TIME MACRO W/BIN. INTVL.
SMFFDTE  DC    PL4'0000' DATE IN PACKED DECIMAL FORM: 00YYDDDF
SMFFSID  DC    CL4' '    SYSTEM IDENTIFICATION (FROM JMR)
*--------------SMF JOB ID INFORMATION (FROM JMR)----------------------*
SMFFJBN  DC    CL8' '    JOB NAME
SMFFRST  DC    BL4'0'    READER START TIME (IN 100THS SECONDS)
SMFFRSD  DC    PL4'0000' READER START DATE
*--------------KAISER JOB ACCOUNTING INFORMATION----------------------*
*
* THE JOB CARD ACCOUNT NUMBER FIELD HAS TWO POSSIBLE FORMATS:
*
* 1. (G,RRR-RRRR-DPSRNR,BBB-BBBB-XX)
*                                  G        = PROG/USER GROUP ID
*                                             (1 ALPHA CHARACTER)
*                                  RRR-RRRR = RUN-FOR ENTITY/LOC/DEPT.
*                                             (NUMERICS: NNN-NNNN)
*                                  DPSRNR   = OPTIONAL DPSR NUMBER
*                                             (NUMERICS: NNN-NNNN)
*                                  BBB-BBBB = BILL-TO ENTITY/LOC/DEPT.
*                                             (NUMERICS: NNN-NNNN)
*                                  XX       = OPTIONAL RUN CODE
*                                             (NUMERICS: NN)
*
* 2. DPSRNR                        6-NUMERIC-DIGIT "KNOWN" DPSRNR
*
* (NOTE:  "DPSRNR" = DATA PROCESSING SERVICE REQUEST NUMBER.
*
* IEFUJV (EXIT CODE 1 = JOB CARD) DETERMINES THE FORMAT TYPE, VALIDATES
* THE INFORMATION SUPPLIED, AND FILLS IN ALL INFORMATION IN THE SMF
* RECORD, EITHER EXPLICITLY FROM THE JOB CARD INFORMATION OR IMPLICITLY
* WHERE A "KNOWN" DPSR# HAS A RUN-FOR AND BILL-TO ACCOUNT NUMBER, AND A
* GROUP CODE, ASSIGNED TO IT.
*
SMFFGRP  DC    CL1' '    PROGRAMMING/USER GROUP CODE
SMFFUSER DC    CL3' '    3-CHARACTER PROGRAMMER/USER ID
*                        (CURRENTLY FIRST 3 CHAR. OF PGMR NAME FIELD))
SMFFGNAM DC    CL4' '    4-CHARACTER USER GROUP NAME
SMFFGRPX DC    X'00'     (GRPSTATS) - GROUP STATUS INFO FROM GRP TABLE
SMFFTYPE DC    X'00'     IEFUJV FLAG BITS                      DCT-V1M1
SMFFTTSO EQU   X'80'          TYPE 255 RCD FOR TSO LOGON       DCT-V1M1
SMFFTJOB EQU   X'40'          TYPE 255 RCD FOR JOB SUBMISSION  DCT-V1M1
SMFFTPRD EQU   X'20'          PRODUCTION JOB/USER              DCT-V1M1
SMFFTUFL EQU   X'10'          IEFUJV FLUSHED IN JOBCARD SCAN   DCT-V1M1
*        EQU   B'00001111'    RESERVED BITS                    DCT-V1M1
SMFFRFOR DS    0CL8      RUN-FOR ACCOUNT NUMBER:
SMFFRFEN DC    CL1' '         1-DIGIT ENTITY
SMFFRFLO DC    CL2' '         2-DIGIT LOCATION
SMFFRFHY DC    CL1'-'         HYPHEN
SMFFRFDP DC    CL4' '         4-DIGIT DEPARTMENT
*
SMFFBTO  DS    0CL8      BILL-TO ACCOUNT NUMBER
SMFFBTEN DC    CL1' '         1-DIGIT ENTITY
SMFFBTLO DC    CL2' '         2-DIGIT LOCATION
SMFFBTHY DC    CL1'-'         HYPHEN
SMFFBTDP DC    CL4' '         4-DIGIT DEPARTMENT
SMFFRCOD DC    CL2' '    2-DIGIT OPTIONAL RUN CODE ON BILL-TO OR BLKS
*
SMFFDPSR DC    CL6' '    6-DIGIT NUMERIC DPSR NUMBER OR BLANKS
*
SMFFBPAS DC    CL8' '    BAD PASSWORD SUPPLIED ON JOB CARD
* NOTE:  SMFFBPAS IS FILLED IN ONLY IF THE USER GROUP REQUIRES A
*        PASSWORD AND THE PASSWORD SUPPLIED WAS INVALID.
* NOTE:  CURRENTLY ONLY THE FIRST 6 DIGITS OF THE JOB CARD PASSWORD
*        FIELD ARE PASSED DOWN TO IEFUJV AND CHECKED.  THIS THIS FIELD
*        WILL CONTAIN A 1-6 CHARACTER VALUE PADDED WITH TWO BLANKS.
*
* THE FOLLOWING FIELD CONTAINS THE JOB CARD IMAGE.  IF THE JOB CARD
* HAD CONTINUATIONS, ONLY THE JOB CARD ITSELF IS PRESENT.  IF IEFUJV
* FAILS THE JOB IN JOB CARD SCAN (REASON CODE 1X) THIS RECORD MAY BE
* USED IN CONJUNCTION WITH THE JMRUSEID FIELD FROM THE TYPE 26 RECORD
* TO ANALYZE THE ERROR.   IEFUJV WILL *NOT* FILL IN TYPE 255 RECORD
* FIELDS WITH INVALID INFORMATION.
*
SMFFJCRD DC    CL80' '   COPY OF JOB CARD IMAGE
* --------------------------------------------------------------------*
* NOTE THAT THE JOB MANAGEMENT RECORD (JMR) USER ID FIELD (JMRUSEID)
* WILL ALSO BE SET BY IEFUJV.  IF IEFUJV IS SUCCESSFUL, THIS FIELD WILL
* BE USED TO PASS INFORMATION TO IEFUJI BY BEING SET TO 'NNNGNNNN' :
* NNN-NNNN WAS THE BILL-TO ACCOUNT NUMBER, AND 'G' WAS THE GROUP ID
* PLACED IN PLACE OF THE HYPHEN.
*
* IF IEFUJV FAILS THE JOB, JMRUSEID IS SET TO 'NNIEFUJV' WHERE 'NN'
* IS THE IEFUJV FAILURE REASON.  THIS SETTING OF JMRUSEID SUBSEQUENTLY
* APPEARS IN THE JOB PURGE (TYPE 26) RECORD AS A RECORD OF WHY THE
* JOB WAS FAILED BY IEFUJV.  (ONLY TYPE 6 AND 26 RECORDS ARE CREATED
* FOR JOBS WHICH ARE FAILED BY IEFUJV.)
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
SMFFEND  EQU   *                      END OF TYPE 255 RECORD
SMFFLGH  EQU  SMFFEND-SMFRCDFF        LENGTH OF TYPE 255 RECORD
         END
