RECV     TITLE 'CALL SVC 208 TARGET CODE UNDER SRB'         *HMD 10/80*
***********************************************************************
*                                                                     *
*                                                                     *
*                              RECEVSRB                               *
*                                                                     *
*                                                                     *
* ABSTRACT: CALL SVC 208 TARGET CODE UNDER AN SRB ENVIRONMENT.        *
*                                                                     *
*                                                                     *
* FUNCTION: RECEVSRB RECEIVES CONTROL DUE TO IGC0020H (SVC 208)       *
*           SCHEDULING A SERVICE REQUEST BLOCK (SRB) WITH RECEVSRB AS *
*           THE ENTRY POINT. IGC0020H PASSES RECEVSRB A COMMUNICATION *
*           AREA IN CSA (R1 -> AT IT UPON ENTRY), AND IN THIS COM-    *
*           MUNICATION AREA IS THE TRUE TARGET CODE'S ENTRY POINT AND *
*           THE TARGET CODE'S PARAMETERS.                             *
*                                                                     *
*           BEFORE CALLING THE TARGET CODE, RECEVSRB ESTABLISHES A    *
*           FUNCTIONAL RECOVERY ROUTINE (FRR) VIA THE SETFRR MACRO.   *
*                                                                     *
*           IF THE TARGET CODE ABENDS, RECEVSRB GETS CONTROL AND      *
*           REQUESTS A RETRY IN ORDER THAT NORMAL RETURN CAN BE MADE. *
*                                                                     *
*           IN EITHER CASE, CONTROL IS RETURNED TO IGC0020H BY        *
*           TRANSFERING CONTROL VIA A BRANCH ENTRY INTO POST, AFTER   *
*           DELETING THE RECOVERY ENVIRONMENT VIA THE SETFRR MACRO.   *
*                                                                     *
*                                                                     *
* INPUT: R1 -> WORK AREA (SEE HACSAW EXPANSION IN IGC0020H)           *
*                                                                     *
*                                                                     *
* RETURN CODES: R10 = 256 => TARGET CODE ABEND                        *
*                     XXX => TARGET CODE RETURN CODE                  *
*                                                                     *
*                                                                     *
* DATA AREAS REFERENCED: CVT                                          *
*                        SDWA                                         *
*                        PSA                                          *
*                                                                     *
*                                                                     *
* MACROS USED: SETFRR                                                 *
*              SETRP                                                  *
*              REGS (USER)                                            *
*              CVT                                                    *
*              HACSAW (USER)                                          *
*              IHASDWA                                                *
*              IHAPSA                                                 *
*              IHAFRRS                                                *
*                                                                     *
*                                                                     *
* ATTRIBUTES: REENTRANT, SUPERVISOR, KEY 0, ENABLED, RESIDENT         *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
RECEVSRB CSECT                CALL SRB TARGET CODE
         USING RECEVSRB,R15   TEMPORARY CSECT ADDRESSABILITY
         B     STARTUP        BRANCH AROUND THE HEADER
         DROP  R15            PURELY TEMPORARY
         DC    AL1(L'HEADER)  LENGTH OF THE HEADER
HEADER   DC    C'RECEVSRB_&SYSDATE_&SYSTIME' HEADER
         USING CSAWORK,R1     TEMPORARY ADDRESSABILITY
STARTUP  STM   R14,R3,CSAREGSV SAVE THE CALLER'S REGS
         DROP  R1             TEMPORARY
         LR    RBASE,R15      A(RECEVSRB)
         USING RECEVSRB,RBASE ADDRESSABILITY
         LR    RCSA,R1        A(CSA WORK AREA FROM ATACHSRB)
         USING CSAWORK,RCSA   ADDRESSABILITY
         SETFRR A,FRRAD=RECEVFAD,WRKREGS=(R1,R15),PARMAD=CSAFRRPL
         L     R1,CSAFRRPL    A(COMMUNICATION PARM LIST)
         ST    RCSA,0(R1)     PASS A(CSA WORK AREA) TO THE RECOVERY RTN
         LA    R1,CSARECPL    A(TARGET CODE'S PARAMETER LIST)
         LA    R13,CSASAVE    A(A SAVE AREA FOR THE CODE)
         L     R15,CSAEP      A(TARGET CODE)
         BALR  R14,R15        CALL THE SRB TARGET CODE
         LR    R10,R15        SAVE THE TARGET CODE'S RETURN CODE
         B     RETURN         RETURN TO OUR CALLER
         EJECT
         USING SDWA,R1        SDWA ADDRESSABILITY
RECEVFRR LR    RBASE,R15      A(RECEVFRR)
         USING RECEVFRR,RBASE ADDRESSABILITY
         L     R15,SDWAPARM   A(FRR PARM LIST)
         L     RCSA,0(R15)    A(CSA WORK AREA)
         ST    RCSA,SDWASR03  SET THE RETRY ROUTINE'S R3
         SETRP RC=4,RETADDR=RECEVRTY,RECORD=YES,RECPARM=ABNDPARM,      *
               RETREGS=YES    REQUEST A RETRY
         BR    R14            RETURN TO OUR CALLER
RECEVRTY LA    R10,256        INDICATE ABEND
         EJECT
RETURN   BALR  RBASE,R0       A(*+2)
         USING *,RBASE        ADDRESSABILITY
         SETFRR D,WRKREGS=(R1,R15) CANCEL OUR FRR
         LA    R1,X'80'       TURN ON THE HIGH
         SLL   R1,24          ORDER BIT
         LA    R11,CSAECB     AND PASS POST
         ALR   R11,R1         THE COMMUNICATION ECB
         SR    R12,R12        ERRET <- 0
         L     R13,CSASCB     A(ASCB TO BE POSTED)
         LM    R14,R3,CSAREGSV RESTORE THE CALLER'S REGS
         L     R15,16         A(CVT)
         USING CVT,R15        ADDRESSABILITY
         L     R15,CVT0PT01   A(BRANCH ENTRY TO POST)
         DROP  R15            TEMPORARY
         BR    R15            POST AND RETURN TO SRB ATTACHER
         SPACE 2
RECEVFAD DC    A(RECEVFRR)    A(RECOVERY ROUTINE)
ABNDPARM DC    CL8'IGC0020H',CL8'RECEVSRB',CL8'HACXMFRR'
         EJECT
         REGS  ,
RBASE    EQU   R2             A(RECEVSRB)
RCSA     EQU   R3             A(CSA WORK AREA)
         EJECT
         CVT   DSECT=YES,LIST=YES
         HACSAW
         EJECT                                              *HMD 12/79*
         IHASDWA
         EJECT                                              *HMD 12/79*
         IHAPSA
         EJECT                                              *HMD 12/79*
         IHAFRRS
         END
