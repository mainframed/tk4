         PRINT NOGEN
*----------------------------------------------------------------------
*
*        FULL SCREEN BROWSE DIALOG MANAGER
*
*----------------------------------------------------------------------
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                 BASE REG #1
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
*----------------------------------------------------------------------
*        INITIALIZATION
*----------------------------------------------------------------------
SPFBRO   CSECT
         USING *,R15
         SAVE  (14,12),,SPFBRO_&SYSDATE._&SYSTIME.
         DROP  R15
         BALR  R12,0
         USING *,R12              INFORM ASSEMBLER OF BASE REGS
         SPACE
         LR    R10,R1             SAVE CPPL ADDRESS
         USING CPPL,R10           CPPL ADDRESSABILITY
         GETMAIN RU,LV=@DATAL     GET STORAGE FOR WORKAREA
         LR    R2,R1              ADDRESS OF 'TO' FIELD
         LA    R3,@DATAL          LENGTH OF 'TO' FIELD
         XR    R5,R5              ZERO PAD CHAR AND 'FROM' LENGTH
         MVCL  R2,R4              CLEAR WORK AREA
         ST    R13,4(R1)          SAVE ADDR OF CALLERS SAVEAREA IN OURS
         ST    R1,8(R13)          SAVE ADDR OF OUR SAVEAREA IN CALLERS
         LR    R13,R1             SET WORKAREA POINTER
         USING @DATA,R13          ADDRESSABILITY TO WORKAREA
         SPACE 2
*----------------------------------------------------------------------
*        SET UP TSO ENVIRONMENT
*----------------------------------------------------------------------
ENTRY010 DS    0H
         ST    R10,@CPPLPTR       STORE THE CPPL PTR
         MVC   @ECT,CPPLECT    MOVE ENVIRONMENT CONTROL TABLE PTR
         SPACE
         LA    R1,@PPL            LOAD ADDRESS OF OUR PARSE PARM LIST
         USING PPL,R1
         MVC   PPLUPT,CPPLUPT     MOVE USER PROFILE TABLE PTR
         MVC   PPLECT,CPPLECT     MOVE ENVIRONMENT CONTROL TABLE PTR
         LA    R0,@ECB            ADDRESS OF ECB
         ST    R0,PPLECB          STORE IT IN THE PARSE PARM LIST
         L     R0,PCLPTR          ADDRESS OF IKJPARM CSECT
         ST    R0,PPLPCL          STORE IT IN THE PARSE PARM LIST
         LA    R0,@PPLANS         ADDRESS OF THE PARSE ANSWER AREA
         ST    R0,PPLANS          STORE IT IN THE PARSE PARM LIST
         MVC   PPLCBUF,CPPLCBUF   MOVE COMMAND BUF POINTER
         ST    R13,PPLUWA         STORE THE ADDRESS OF THE WORKAREA
         DROP  R1
EXIT010  EQU   *
         EJECT
*----------------------------------------------------------------------
*        CHECK FOR OPTIONS
*----------------------------------------------------------------------
ENTRY020 DS    0H
         CALLTSSR EP=IKJPARS,MF=(E,@PPL)
         LTR   R15,R15
         BZ    AAA020
         TPUT  ERRPARS,ERRPARSL
         B     EOJ
AAA020   EQU   *
         L     R10,@PPLANS        GET POINTER TO PARSE ANSWER
         USING IKJPARMD,R10       GAIN ADDRESSABILITY TO ANSWER
         SPACE
         LA    R15,IDDSN          ADDR OF PDE
         XR    R2,R2              CLEAR REG
         ICM   R2,3,4(R15)        INSERT LENGTH OF DSNAME
         BZ    ZZZ020             NO DSNAME
         L     R3,0(R15)          ADDR OF PARSE DSNAME
         LA    R4,@DSNAME
         MVI   0(R4),X'7D'
         LA    R4,1(R4)
         MVI   0(R4),C' '
         MVC   1(54,R4),0(R4)
         BCTR  R2,0               DECREMENT FOR EXECUTE
         EX    R2,MOVOPT          MOVE IN SPECIFIED OPTION
         LA    R4,1(R2,R4)
         SPACE
         LA    R15,8(R15)         ADDR OF PDE+8
         XR    R2,R2              CLEAR REG
         ICM   R2,3,4(R15)        INSERT LENGTH OF MEMBER
         BZ    BBB020             NO MEMBER
         L     R3,0(R15)          ADDR OF PARSE MEMBER
         MVI   0(R4),C'('
         LA    R4,1(R4)
         BCTR  R2,0               DECREMENT FOR EXECUTE
         EX    R2,MOVOPT          MOVE IN SPECIFIED OPTION
         LA    R4,1(R2,R4)
         MVI   0(R4),C')'
         LA    R4,1(R4)
BBB020   EQU   *
         MVI   0(R4),X'7D'
         SPACE
         LA    R15,SFIDVOL        ADDR OF PDE
         XR    R2,R2              CLEAR REG
         ICM   R2,3,4(R15)        INSERT LENGTH OF VOLSER
         BZ    ZZZ020             NO VOLSER
         L     R3,0(R15)          ADDR OF PARSE VOLSER
         LA    R4,@VOLSER
         MVI   0(R4),C' '
         MVC   1(5,R4),0(R4)
         BCTR  R2,0               DECREMENT FOR EXECUTE
         EX    R2,MOVOPT          MOVE IN SPECIFIED OPTION
         LA    R4,1(R2,R4)
ZZZ020   EQU   *
         IKJRLSA @PPLANS
         DROP  R10                DROP ADDRESSABILITY
EXIT020  EQU   *
         B     ENTRY030
         SPACE
MOVOPT   MVC   0(0,R4),0(R3)
         EJECT
*----------------------------------------------------------------------
*        SET FOR RETURN OF ERRORS
*----------------------------------------------------------------------
ENTRY030 DS    0H
         LA    R15,CONTROL         ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,ERRORS          ADDRESS OF ERRORS
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         LA    R15,RETURN          ADDRESS OF RETURN
         ST    R15,@LKPARM3        UPDATE DYNAMIC STORAGE
         OI    @LKPARM3,X'80'      SET THE VL BIT
         BAL   R14,ISPCALL
EXIT030  EQU   *
         EJECT
*----------------------------------------------------------------------
*        LINK TO THE BROWSE SERVICE
*----------------------------------------------------------------------
ENTRY100 DS    0H
         CLI   @DSNAME,X'00'
         BE    ENTRY200
         LA    R15,BROWSE          ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,@DSNAME         ADDRESS OF DSNAME
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         CLI   @VOLSER,X'00'       WAS A VOLSER SPECIFIED ?
         BE    AAA040              NO - GO SET THE VL BIT
         LA    R15,@VOLSER         ADDRESS OF THE VOLSER
         ST    R15,@LKPARM3        UPDATE DYNAMIC STORAGE
         OI    @LKPARM3,X'80'      SET THE VL BIT
         B     BBB040
AAA040   EQU   *
         OI    @LKPARM2,X'80'      SET THE VL BIT
BBB040   EQU   *
         BAL   R14,ISPCALL         EFFECT BROWSE
         C     R15,F12
         BNE   CCC100
         TPUT  ERRMSG1,ERRMSG1L
CCC100   EQU   *
         C     R15,F14
         BNE   DDD100
         TPUT  ERRMSG2,ERRMSG2L
DDD100   EQU   *
         C     R15,F16
         BNE   EEE100
         TPUT  ERRMSG3,ERRMSG3L
EEE100   EQU   *
         C     R15,F20
         BNE   EXIT100
         TPUT  ERRMSG4,ERRMSG4L
EXIT100  EQU   *
         B     EOJ
         EJECT
*----------------------------------------------------------------------
*        DISPLAY THE BROWSE SCREEN
*----------------------------------------------------------------------
ENTRY200 DS    0H
         LA    R15,SELECT          ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,BUFLEN          LENGTH OF CMD BUFFER
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         LA    R15,CMDBUF          ADDRESS OF CMD BUFFER
         ST    R15,@LKPARM3        UPDATE DYNAMIC STORAGE
         OI    @LKPARM3,X'80'      SET THE VL BIT
         BAL   R14,ISPCALL         EFFECT ISRBRO
EXIT200  EQU   *
         SPACE 2
*----------------------------------------------------------------------
*        RETURN TO THE CALLER
*----------------------------------------------------------------------
EOJ      DS    0H
         ICM   R15,15,@ISPEPA
         BZ    EOJ1
         DELETE EP=ISPLINK
EOJ1     EQU   *
         LR    R1,R13                   GET ADDRESS TO FREE
         L     R13,4(,R13)              GET PREVIOUS SAVE AREA
         FREEMAIN RU,A=(1),LV=@DATAL    FREE THE GOTTEN AREA
         RETURN (14,12),,RC=0           RETURN TO CALLER
         EJECT
*----------------------------------------------------------------------
*        LOAD ISPLINK ROUTINE
*----------------------------------------------------------------------
ISPCALL  DS    0H
         ST    R14,@SAV14         SAVE RETURN ADDR
         ICM   R15,15,@ISPEPA     DYNAMIC ALLOCATION ROUTINE LOADED ?
         BNZ   ISP010             YES - USE IT
         SPACE
         LOAD  EP=ISPLINK         LOAD ISPLINK ROUTINE
         ST    R0,@ISPEPA         SAVE THE ENTRY POINT
         LR    R15,R0
         LA    R1,@LKPARM1
         ST    R1,@ISPADDR
ISP010   EQU   *
         LA    R1,@LKPARM1
         BALR  R14,R15
         L     R14,@SAV14         LOAD RETURN ADDR
         BR    R14
         EJECT
*----------------------------------------------------------------------
*        CONSTANTS
*----------------------------------------------------------------------
F12      DC    F'12'
F14      DC    F'14'
F16      DC    F'16'
F20      DC    F'20'
         SPACE
CONTROL  DC    CL8'CONTROL'
ERRORS   DC    CL8'ERRORS'
RETURN   DC    CL8'RETURN'
BROWSE   DC    CL8'BROWSE'
SELECT   DC    CL8'SELECT'
CMDBUF   DC    C'PGM(ISRBRO) PARM(ISRBRO01)'
CMDBUFL  EQU   *-CMDBUF
BUFLEN   DC    A(CMDBUFL)
         SPACE
ERRPARS  DC    C'*** PARSE FAILED.'
ERRPARSL EQU   *-ERRPARS
ERRMSG1  DC    C'*** EMPTY DATASET OR MEMBER.'
ERRMSG1L EQU   *-ERRMSG1
ERRMSG2  DC    C'*** SPECIFIED MEMBER NOT FOUND.'
ERRMSG2L EQU   *-ERRMSG2
ERRMSG3  DC    C'*** NO MEMBERS IN LIBRARY.'
ERRMSG3L EQU   *-ERRMSG3
ERRMSG4  DC    C'*** DATASET NOT FOUND.'
ERRMSG4L EQU   *-ERRMSG4
         LTORG
         EJECT
PCLPTR   DC    V(PCL)
PCL      IKJPARM
IDDSN    IKJPOSIT DSNAME,USID
KWVOL    IKJKEYWD
         IKJNAME 'VOLSER',SUBFLD=SFVOL,ALIAS='VOLUME'
*
SFVOL    IKJSUBF
SFIDVOL  IKJPOSIT DSNAME,VOLSER,PROMPT='VOLUME SERIAL NUMBER'
         IKJENDP
         EJECT
@DATA    DSECT
         DS    18F
@SAV14   DS    F
@ISPEPA  DS    F
@ISPADDR DS    F
@CPPLPTR DS    F
@ECT     DS    F
@ECB     DS    F
@PPL     DS    7F
@PPLANS  DS    F
@LKPARM1 DS    F
@LKPARM2 DS    F
@LKPARM3 DS    F
@DSNAME  DS    CL56
@VOLSER  DS    CL6
@TRTAB   DS    XL256
@DATAL   EQU   *-@DATA
         SPACE
         CVT   DSECT=YES
         SPACE
         IKJCPPL
         SPACE
         IKJECT
         SPACE
         IKJPPL
         END
