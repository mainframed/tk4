         MACRO
         REGEQU
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
***********************************************************************
         MEND
         EJECT
***********************************************************************
         MACRO
&LABEL   PARM
.*       THIS MACRO IS DESIGNED TO MOVE THE PARM INTO A DC DEFINED
.*       WITHIN THE USER PROGRAM BY THE NAME OF PARM
.*       IT WILL MOVE THE EXACT LENGTH OF THE PARM VIA AN EX INSTR.
&LABEL   L     1,0(1)
         LH    2,0(1)
         EX    2,#&SYSNDX
         B     @&SYSNDX
#&SYSNDX MVC   PARM(0),2(1)
@&SYSNDX EQU   *
         MEND
***********************************************************************
         EJECT
         MACRO
&NAME    TERME &PASS,&RC=O
         GBLC  &LAB
         GBLB  &PRIM
&NAME    DS    0H
         AIF   (&PRIM).TSTRC
         MNOTE 8,'PRIME MUST OCCUR BEFORE TERME'
         MEXIT
.TSTRC   AIF   ('&RC' EQ '(15)').RET
         AIF   ('&RC' EQ 'O').RET
         AIF   ('&RC'(1,1) EQ '(').ISAREG
         LA    15,&RC
         AGO   .RET
.ISAREG  ANOP
         LR    15,&RC
.RET     AIF   ('&PASS' NE 'PASS').NOPASS
         L     14,4(13)
         STM   0,1,20(14)
.NOPASS  ANOP
         B     &LAB.X
         MEND
***********************************************************************
         EJECT
         MACRO
&NAME    PRIME &REG,&SAVE,&CONS,&NTER,&EQU
.* THIS IS THE PRIME MACRO
.* ITS FUNCTION IS
.* 1.  ESTABLISH ADDRESSABILITY SAVE REGISTERS ACT AS ENTRY
.* 2.  GENERATE REG SAVE AREA OR DO GETMAIN
.* 3.  PROVIDE EXIT USED BY TERME MACRO
.* 4.  PROVIDE ADDIT SAVE AREAS FOR SUBROUTINES SO ONLY 1 BAL REG
         GBLA  &PERFD,&PERFL
         GBLC  &PERFR,&LAB,&SNTRY
         GBLB  &EQFLG,&PRIM
         LCLB  &REENT
         LCLA  &CNT,&LOOP,&INC
         LCLC  &BASE(3),&SCSECT,&PNAME
         AIF   (&PRIM).DUMB
&PRIM    SETB  1
&LAB     SETC  'ICC'.'&SYSNDX'
&SNTRY   SETC  '&LAB'.'A'
&SCSECT  SETC  '&SYSECT'
         AIF   (T'&NAME EQ 'O').CSCT0
         AIF   ('&SCSECT' NE '').CSCT1
&NAME    CSECT
&SCSECT  SETC  '&NAME'
         AGO   .CSCT0
.DUMB    MNOTE 8,'ONLY ONE PRIME MAY BE DEFINED'
         MEXIT
.CSCT1   MNOTE 'A CSECT IS ALREADY DEFINED.'
&PNAME   SETC  '&NAME'
.CSCT0   ANOP
          USING *,15
         AIF   ('&EQU' EQ '').A1
         REGEQU
.A1      AIF   ('&CONS' EQ '').A2
&PNAME   SAVE  (14,12),,&CONS
         AGO   .A3
.A2      ANOP
&PNAME   SAVE  (14,12),,&SCSECT
.A3       ANOP
         LA    14,&LAB
         ST    14,&SNTRY
         B     &SNTRY+4
         AIF   ('&SAVE' NE '').A4
&REENT   SETB  1
.A4      AIF   ('&REG' EQ '').A6
         AIF   (N'&REG GT 3).BAD3
         AIF   (N'&REG LT 2).B1
&CNT     SETA  N'&REG
&LOOP    SETA  1
&INC     SETA  &LOOP+1
.A5      AIF   (T'&REG(&LOOP) NE 'N').BAD1
&BASE(&LOOP) SETC '&REG'(&INC,K'&REG(&LOOP))
&INC     SETA  &INC+K'&REG(&LOOP)+1
&LOOP    SETA  &LOOP+1
         AIF   (&LOOP LE &CNT).A5
         AGO   .A7
.B1      ANOP
&BASE(1) SETC  '&REG'
         AGO   .B2
.A6      ANOP
&BASE(1) SETC  '12'
.B2      ANOP
&CNT     SETA  1
.A7      ANOP
&SNTRY   DS    F
          DROP 15
         AIF   (&CNT NE 3).A8
         LA    &BASE(3),2048                     ESTABLISH ADDRESSAB
         LA    &BASE(2),2048(&BASE(3),15)        WITH THREE
         LA    &BASE(3),2048(&BASE(3),&BASE(2))  BASE REGS
         LR    &BASE(1),15
         USING &SCSECT,&BASE(1),&BASE(2),&BASE(3)
         AGO   .A10
.A8      AIF   (&CNT NE 2).A9
         LA    &BASE(2),2048                     ESTABLISH ADRESSAB
         LA    &BASE(2),2048(&BASE(2),15)        WITH TWO
         LR    &BASE(1),15                       BASE REGS
         USING &SCSECT,&BASE(1),&BASE(2)
         AGO   .A10
.A9      AIF   (&CNT NE 1).BAD4
         LR    &BASE(1),15                       ESTABLISH ADRESSAB
         USING &SCSECT,&BASE(1)
.A10     AIF   (&REENT).A11
         LA    15,&SAVE
         AGO   .A14
.A11     AIF   ('&NTER' NE '').A12
&PERFL   SETA  0
&CNT     SETA  72
         AGO   .A13
.A12     AIF   (T'&NTER NE 'N').BAD5
&PERFL   SETA  &NTER
&PERFR   SETC  '14'
&PERFD   SETA  0
&CNT     SETA  72+&NTER*4
* GENERATED SAVE AREA EXTENDED FOR USE BY NTER AND XIT MACROS
.A13     ANOP
         GETMAIN R,LV=&CNT
         LR    15,1
.A14     ANOP
         LM    0,1,20(13)
         ST    13,4(15)                          CHAIN THE
         ST    15,8(13)                          SAVE AREAS
         LR    13,15
         L     15,&SNTRY
          BR   15
         AIF   (&REENT).A20
&LAB.X   L     13,4(13)
         AGO   .A21
.A20     ANOP
&LAB.X   LR    1,13
         L     13,4(13)
         FREEMAIN R,LV=&CNT,A=(1)
.A21     ANOP
         RETURN (14,12),T,RC=(15)
         AIF   (&REENT).A24
         AIF   ('&NTER' NE '').A22
&CNT     SETA  18
         AGO   .A23
.A22     AIF   (T'&NTER NE 'N').BAD5
&PERFL   SETA  &NTER
&PERFR   SETC  '14'
&PERFD   SETA  0
&CNT     SETA  &NTER+18
* GENERATED SAVE AREA EXTENDED FOR USE BY NTER AND XIT MACROS
.A23     ANOP
&SAVE    DC    &CNT.F'0'
.A24     ANOP
         LTORG
&LAB     DS    0H
         MEXIT
.BAD1    MNOTE 8,'FIRST PARAMETER/S MUST BE SELF DEFINING'
         MEXIT
.BAD3    ANOP
.BAD4    MNOTE 8,'THIS MACRO WILL HANDLE 1-3 BASE REGS'
         MEXIT
.BAD5    MNOTE 8,'THIRD PARAMETER MUST BE SELF DEFINING'
         MEND
         PRINT NOGEN
         TITLE 'SUB SYSTEM-INVOKATION DRIVER  11-07-80 '
************************************************************
* ACMD - THIS CODE IS THE DRIVER FOR THE ACMD SUBSYSTEM    *
*          PROC.  THIS CODE WILL READ THE FILE SPECIFIED   *
*          BY DD CARD SYSPARMS AND INVOKE THE ROUTINES     *
*          AFTER PLACING THE SYSTEM IN THE PROPER MODE.    *
*                                                          *
* INPUT CARD FORMAT IS :                                   *
* COL 1      P = PROGRAM INDICATOR               (DEFAULT) *
*            C = OPERATOR COMMAND INDICATOR                *
*            A = ALLOC A DDNAME                            *
*            F = FREE A DDNAME                             *
*            D = ALLOC A SPECIFIC DEVICE                   *
*            W = WTOR FOR INPUT COMMANDS                   *
*     2-9    MODULE TO BE INVOKED                          *
*     2-9    DDNAME FOR DYN PROCESSING (A F D)             *
*     10-12  UNIT FOR DEVICE ALLOCATION  (D)               *
*     10-55  DSNAME FOR DYN PROCESSING  (A)                *
*     10-80  PARM FOR THE ABOVE MODULE                     *
*            (NOTE: 2 BLANKS IN PARM AND THE END IS        *
*                   ASSUMED FOR THE LENGTH CALC OF YOUR    *
*                   PARM)                                  *
*                                                          *
*          CODED BY LIONEL DYCK                            *
*                   ROCKWELL INTERNATIONAL                 *
*                   PO BOX 2515                            *
*                   SEAL BEACH, CALIF 90740                *
*                   (213) 594-2520                         *
*                                                          *
*  MACROS USED ARE: PRIME,LINK,MODESET,TERME               *
*                   OPEN,CLOSE,LOAD,DELETE,DCB             *
*                   WTO,ALLOC,FREE,WTOR,WAIT               *
*                                                          *
************************************************************
         EJECT
ACMD     PRIME 12,,,,Y
         LR    R11,R1                    SAVE PARM POINTER
         USING ACMDCARD,R9               ESTABLISH ADDR TO INPUT
         OPEN  (SYSPARM)                 OPEN INPUT FILE
         LA    R6,1                      LOAD INCREMENT
         EJECT
READ     EQU   *
         TM    SW,WTORSW                 WTOR INPUT ?
         BZ    GETIT                     NO - BYPASS WTOR
         XC    ECB,ECB
         WTOR  'ACMD - ENTER INPUT RECORD OR END',WTORCARD,80,ECB,     *
               ROUTCDE=1
         WAIT  ECB=ECB
         CLC   WTORCARD(3),=C'END'       END OF WTOR ?
         BE    GETCARD                   YES
         LA    R1,WTORCARD               -> WTOR RECORD
         B     PROCESS                   BYPASS GET AND PROCESS INPUT
         EJECT
GETCARD  NI    SW,WTORSWF                TURN OFF WTOR
GETIT    GET   SYSPARM
PROCESS  LR    R9,R1
         SR    R1,R1                     CLEAR REG 1
         CLI   RMODE,C'C'                OPERATOR COMMAND
         BE    COMMAND                   GO ISSUE IT
         CLI   RMODE,C'P'                PROGRAM
         BE    LOADIT                    YES
         CLI   RMODE,C'A'                ALLOC FILE
         BE    ALLOCIT                   YES
         CLI   RMODE,C'F'                FREE FILE
         BE    FREEIT                    YES
         CLI   RMODE,C'D'                ALLOC DEVICE
         BE    ALLOCDEV                  YES
         CLI   RMODE,C'W'                WTOR INPUT
         BE    WTORINP                   YES
         CLI   RMODE,C' '                PROGRAM (DEFAULT)
         BE    LOADIT                    YES
         B     ERRORCC                   ERROR MESSAGE
         EJECT
WTORINP  OI    SW,WTORSW                 TURN ON WTOR SWITCH
         B     READ                      CONTINUE PROCESSING
         EJECT
COMMAND  MVC   CMD(79),RCMD              MOVE COMMAND TO SVC 34
         MVC   CMDWTO+8(50),RCMD
CMDWTO   WTO   '                                                    ', *
               ROUTCDE=(1,11)
         MODESET MODE=SUP,KEY=ZERO       GET IN RIGHT KEY
         SR    R0,R0                     CLEAR R0
         LA    R1,CMDL                   -> COMMAND
         SVC   34                        ISSUE THE COMMAND
         MODESET MODE=PROB,KEY=NZERO     LEAVE PRIV STATE
         B     READ
         EJECT
LOADIT   EQU   *
         LOAD  EPLOC=RPROG,ERRET=S806
         LR    R8,R0                     -> RPROG EPA
         MVC   PARM(70),RPARM            MOVE PARM
         LA    R5,PARM                   -> START OF PARM
         LA    R7,PARM+68                -> END OF PARM
FINDL    CLC   0(2,R5),=C'  '            LOOK FOR END OF PARM
         BE    PARMSET                   FOUND
         BXLE  R5,R6,FINDL               CONTINUE
PARMSET  LA    R4,PARM
         SR    R5,R4                     GET LENGTH
         STH   R5,PARML
         LA    R5,PARML                  -> PARM
         ST    R5,PARMA                  SAVE IT
         MVC   EXWTO+8(8),RPROG          MOVE MODULE NAME TO WTO
*        TELL THE WORLD WHERE WE ARE
EXWTO    WTO   '         - BEING INVOKED ***',ROUTCDE=(1,11)
         LA    R1,PARMA                  LOAD ADDR OF PARM ADDR
         LR    R15,R8                    -> MODULE EPA
         BALR  R14,R15                   GO TO MODULE
*        NOW DELETE THE INVOKED MODULE
         DELETE EPLOC=RPROG
         B     READ
         EJECT
S806     EQU   *
         MVC   S806WTO+8(8),RPROG        MOVE PROG NAME TO WTO
S806WTO  WTO   '         - MODULE NOT FOUND ***',ROUTCDE=(1,11)
         B     READ                      CONTINUE THE READ
ERRORCC  WTO   'ERROR IN ACMD COMMAND RECORD ***',ROUTCDE=(1,11)
         B     READ                      CONTINUE THE READ
         EJECT
ALLOCIT  EQU   *                         DYNAMICALLY ALLOC A FILE
         MVC   DDN(8),ADDNAME            MOVE DDNAME
         LA    R2,DDN+7                  -> END OF DDNAME
DDNLENL  CLI   0(R2),C' '                BLANK ?
         BNE   SDDNL                     NO - STORE LENGTH
         BCT   R2,DDNLENL                CONTINUE THE SCAN
SDDNL    LA    R1,DDN-1                  -> END OF DDNAME
         SR    R2,R1                     AND SUBTRACT FOR LENGTH
         STH   R2,DDNL                   AND SAVE IT
         CLC   ADSNAME(6),=CL6'DUMMY'
         BNE   ALLDSN                    GO ALLOC A DSNAME
         ALLOC DUMMY,DDN=DDNAME
         LTR   R15,R15                   ALLOCATED OK ?
         BNZ   S99FAIL                   NO
         B     READ                      CONTINUE PROCESSING
ALLDSN   EQU   *
         MVC   DSN(44),ADSNAME
         LA    R2,DSN+43                 -> END OF DSN
DSNLP    CLI   0(R2),C' '                BLANK ?
         BNE   SDSNL                     NO - STORE LENGTH
         BCT   R2,DSNLP                  CONTINUE THE SCAN
SDSNL    LA    R1,DSN-1                  -> END OF DDNAME
         SR    R2,R1                     AND SUBTRACT FOR LENGTH
         STH   R2,DSNL                   AND SAVE IT
         ALLOC DDN=DDNAME,DSN=DSNAME,DISP=SHR
         LTR   R15,R15                   ALLOCATED OK ?
         BNZ   S99FAIL                   NO
         B     READ                      CONTINUE PROCESSING
         EJECT
FREEIT   EQU   *                         DYNAMICALLY FREE A FILE
         MVC   DDN(8),FDDNAME            MOVE DDNAME
         LA    R2,DDN+7                  -> END OF DDNAME
XDNLENL  CLI   0(R2),C' '                BLANK ?
         BNE   XDDNL                     NO - STORE LENGTH
         BCT   R2,XDNLENL                CONTINUE THE SCAN
XDDNL    LA    R1,DDN-1                  -> END OF DDNAME
         SR    R2,R1                     AND SUBTRACT FOR LENGTH
         STH   R2,DDNL                   AND SAVE IT
         FREE  DDN=DDNAME
         LTR   R15,R15                   FREED OK ?
         BNZ   S99FAIL                   NO
         B     READ                      CONTINUE PROCESSING
         EJECT
ALLOCDEV EQU   *                         DYNAMICALLY FREE A FILE
         MVC   DDN(8),PDDNAME            MOVE DDNAME
         LA    R2,DDN+7                  -> END OF DDNAME
ADNLENL  CLI   0(R2),C' '                BLANK ?
         BNE   ADDNL                     NO - STORE LENGTH
         BCT   R2,ADNLENL                CONTINUE THE SCAN
ADDNL    LA    R1,DDN-1                  -> END OF DDNAME
         SR    R2,R1                     AND SUBTRACT FOR LENGTH
         STH   R2,DDNL                   AND SAVE IT
         MVC   UNIT(3),PUNIT             MOVE UNIT ADDRESS
         ALLOC DDN=DDNAME,UNIT=UNITADDR
         LTR   R15,R15                   ALLOCATED OK ?
         BNZ   S99FAIL                   NO
         B     READ                      CONTINUE PROCESSING
         EJECT
S99FAIL  EQU   *
         S99FAIL
         B     READ                      CONTINUE PROCESSING
         EJECT
ENDIP    EQU   *
         CLOSE (SYSPARM)
         TERME
         SPACE
         LTORG
         SPACE
ECB      DC    F'0'
         SPACE
SW       DC    X'00'                     SWITCH
WTORSW   EQU   1
WTORSWF  EQU   X'FE'
         SPACE
PARMA    DS    F
PARML    DS    H
PARM     DS    CL70
         SPACE
WTORCARD DC    CL80' '
         SPACE
         DS    0F
CMDL     DC    AL2(83),X'0000'
CMD      DC    CL79' '
         SPACE
UNITADDR DC    A(UNIT)
UNITL    DC    Y(3)
UNIT     DC    CL3' '                    UNIT FOR DYN PROCESSING
         SPACE
DDNAME   DC    A(DDN)
DDNL     DC    Y(8)
DDN      DC    CL8' '                    DDNAME FOR DYN PROCESSING
         SPACE
DSNAME   DC    A(DSN)
DSNL     DC    Y(44)
DSN      DC    CL44' '                   DSNAME FOR DYN PROCESSING
         EJECT
SYSPARM  DCB   DSORG=PS,MACRF=GL,DDNAME=SYSPARM,EODAD=ENDIP
         EJECT
         DYNSPACE
         EJECT
ACMDCARD DSECT
RMODE    DS    CL1                       REQUESTED MODE
*                                        P OR BLANK = PROGRAM
*                                        C =  OPER COMMAND
*                                        A = ALLOCATE
*                                        F = DEALLOCATE
*                                        P = PRINTER ALLOCATION
*                                        W = WTOR FOR INPUT
RCMD     DS    CL79                      OPERATOR COMMAND
         ORG   RCMD
RPROG    DS    CL8                       REQUESTED PROGRAM
RPARM    DS    CL70                      PARM FOR REQUESTED PROGRAM
         ORG   RCMD
FDDNAME  DS    CL8                       DDNAME FOR DE-ALLOCATION
         ORG   RCMD
ADDNAME  DS    CL8                       DDNAME FOR ALLOCATION
ADSNAME  DS    CL44                      DSNAME FOR ALLOCATION
         ORG   RCMD
PDDNAME  DS    CL8                       DDNAME FOR PRINTER
PUNIT    DS    CL3                       UNIT FOR PRINTER
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         END
