PAKBYT   TITLE 'SUBROUTINE WHICH PACKS WORDS INTO BYTES'
*
*      THIS PROGRAM PACKS A NUMBER OF WORDS WHICH ARE EITHER RIGHT-
* JUSTIFIED WITH LEADING BINARY ZEROES OR LEFT-JUSTIFIED WITH TRAILING
* BLANKS INTO THE SAME NUMBER OF BYTES WITH THE LEADING BINARY ZEROES
* OR TRAILING BLANKS STRIPPED OUT. UPON COMPLETION, REGISTER 15
* CONTAINS 0 IF THE PACK OPERATION SUCCEEDED OR 12 IF THE PACK WAS
* UNSUCCESSFUL. THE CALLING SEQUENCE FOR "PAKBYT" IS AS FOLLOWS:
*           1. THE SOURCE FIELD (TO BE PACKED FROM)
*           2. THE TARGET FIELD (TO BE PACKED INTO)
*           3. THE NUMBER OF WORDS TO BE PACKED (4-BYTE BINARY)
*           4. THE MODE OF JUSTIFICATION (1-BYTE CHARACTER) WHERE
*                  "L" INDICATES LEFT-JUSTIFIED AND
*                  "R" INDICATES RIGHT-JUSTIFIED
*
*      THE "PAKBYT" PROGRAM IS REENTRANT AND REUSEABLE.
*
PAKBYT   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(72,0)
         REGISTER                  REGISTER EQUATES
         LM    R4,R7,0(R2)         LOAD PARAMETER POINTERS
         L     R6,0(,R6)           LOAD NUMBER OF WORDS TO BE PACKED
         LTR   R6,R6               IF NUMBER OF WORDS IS NOT POSITIVE
         BNP   ERROR                  THEN QUIT
         CLI   0(R7),C'R'          IF RIGHT JUSTIFICATION IS INDICATED
         BE    RIGHTJST               THEN PREPARE FOR IT
         CLI   0(R7),C'L'          IF NOT LEFT JUSTIFICATION EITHER
         BNE   ERROR                  THEN FLAG FOR ERROR
         B     PARMOK                 ELSE GO PACK THE DATA
RIGHTJST DS    0H
         LA    R4,3(,R4)           SHIFT POINTER TO LAST BYTE OF WORD
PARMOK   DS    0H
         MVC   0(1,R5),0(R4)       MOVE A BYTE
         LA    R4,4(,R4)           INCREMENT SOURCE POINTER
         LA    R5,1(,R5)           INCREMENT TARGET POINTER
         BCT   R6,PARMOK           CONTINUE UNTIL ALL WORDS ARE PACKED
         LA    R15,0               INDICATE SUCCESSFUL
         B     EXIT                   AND LEAVE
ERROR    DS    0H
         LA    R15,12              INDICATE UNSUCCESSFUL
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(72,0)
         END
