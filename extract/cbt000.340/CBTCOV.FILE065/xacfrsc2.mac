RSC2     TITLE 'ACF2 RESOURCE POST-VALIDATION EXIT'
         GBLC  &CSECT
&CSECT   SETC  'XACFRSC2'
********************************************************************
*        THIS IS THE ACF2 RESOURCE POST-VALIDATION EXIT.
*
*        ITS PURPOSE IS:
*
*        1) IF RESOURCE TYPE IS "IAG" OR "TAG" (BMP VALIDATION) THEN
*           JOB CLASS MUST BE "K" FOR TYPE "IAG" AND "L" FOR "TAG".
*
*        CODED BY:      R.L. WELKER
*        LAST MODIFIED: 07/26/82
*
********************************************************************
*
*     DSECTS
*
         ACGXITP ,                RESOURCE VALIDATION EXIT PARMLIST
         IHAPSA ,                 PREFIXED SAVE AREA
         IKJTCB ,                 TASK CONTROL BLOCK
         IEZJSCB ,                JOB STEP CONTROL BLOCK
         IEFZB502 ,               SWA PREFIX
         IEFAJCTB ,               JOB CONTROL TABLE
*
*     HOUSEKEEPING
*
&CSECT   CSECT
         ENTER BASEREG=(R12),     SET UP LINKAGE                       X
               REGEQUS=YES,                                            X
               REGZERO=R10,                                            X
               PARMREG=R11,                                            X
               NOSAVEA=YES
         USING ACGXITP,R11        SET EXIT PARMLIST ADDRESSABILITY
         USING PSA,R0             SET PSA ADDRESSABILITY
*
*     LOCATE JOB CLASS
*
         ACF$TCB REG=R1,          GET ADDRESS OF TCB                   X
               TCB=GET,                                                X
               MODE=PSA
         USING TCB,R1             SET TCB ADDRESSABILITY
         L     R1,TCBJSCB         GET ADDRESS OF JSCB
         USING IEZJSCB,R1         SET JSCB ADDRESSABILITY
         L     R1,JSCBJCT         GET ADDRESS OF JCT
         USING ZB502,R1           SET JCT PREFIX ADDRESSABILITY
*
*     IF BMP VALIDATION THEN CHECK JOB CLASS
*
         SLR   R15,R15            ZERO R15
         C     R10,=F'16'         Q. WAS THIS A VIOLATION?
         BE    RETURN             A. YES, PROCESS NORMALLY
         L     R2,ACGXKEY         GET KEY ADDR
         CLC   0(4,R2),=C'RIAG'   Q. PRODUCTION BMP VALIDATION?
         BNE   TESTCLS            A. NO, CHECK FOR TEST BMP
         CLI   JCTJCSMF,C'J'      Q. MP REGION PRODUCTION JOB CLASS?
         BE    RETURN             A. YES, CONTINUE REQUEST
         CLI   JCTJCSMF,C'K'      Q. BMP PRODUCTION JOB CLASS?
         BE    RETURN             A. YES, CONTINUE REQUEST
         WTO   'FLOUR.&CSECT..001 INVALID JOB CLASS FOR PRODUCTION BMP'X
               ,ROUTCDE=11,DESC=7
         B     REJECT             A. NO, REJECT
TESTCLS  EQU   *
         CLC   0(4,R2),=C'RTAG'   Q. TEST BMP VALIDATION?
         BNE   RETURN             A. NO, THIS IS NOT A BMP
         CLI   JCTJCSMF,C'H'      Q. MP REGION TEST JOB CLASS?
         BE    RETURN             A. YES, CONTINUE REQUEST
         CLI   JCTJCSMF,C'L'      Q. BMP TEST JOB CLASS?
         BE    RETURN             A. YES, CONTINUE REQUEST
         WTO   'FLOUR.&CSECT..002 INVALID JOB CLASS FOR TEST BMP',     X
               ROUTCDE=11,DESC=7
         B     REJECT             A. NO, REJECT
*
*     RETURN
*
REJECT   EQU   *
         LA    R15,20             SET REJECT CODE
RETURN   EQU   *
         LEAVE NOSAVEA=YES,       RETURN TO CALLER                     X
               RETCODE=(15)
         END   &CSECT
