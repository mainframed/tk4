UADSLIST TITLE '-----  LIST UADS  -----'
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  UADSLIST                                           *
*                                                                     *
*   AUTHOR:        LEONARD D. WOREN                                   *
*                                                                     *
*   DATE:          JULY 16, 1981                                      *
*                                                                     *
*   INSTALLATION:  TITLE INSURANCE AND TRUST                          *
*                                                                     *
*   PURPOSE:       LIST UADS IN A MORE READABLE FORMAT THAN THE       *
*                  TRASHY "ACCOUNT" COMMAND                           *
*                                                                     *
*   UPDATES:                                                          *
*      06/22/83 LDW2  CHANGE MAXMEM TO 4095 (EVEN MORE PUTRID)        *
*      11/01/82 LDW1  FIX BUG IN PROCESSING UIDS WITH MULTIPLE MBRS   *
*      10/26/82 LDW   CHANGE MAXMEM FROM 1000 TO 2500 (MORE PUTRID)   *
*      07/23/81 SDM1  MAKE PROGRAM WORK IF CALLED                     *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT ,
UADSLIST OSENTER  EXIT=LEAVE,RC=0,GETMAIN=(WORKLEN,WORKD),             $
               PARMREG=R2,ENV=(CP,SIM),LENREG=R14,ADDRREG=R15      SDM1
LINK     EQU   R11                      PRIMARY BAL REGISTER
MAXMEM   EQU   4095                     MAX # OF MEMBERS IN SYS1.UADS
         SPACE 3
         MVI   FLAGS,0                  INIT FLAGS
         CH    R14,=H'8'                8 CHARACTER PARM FIELD?
         BNE   GO$OPEN                  NO - CAN'T BE 'PASSWORD'
         CLC   =C'PASSWORD',0(R15)      YES - DOES HE WANT PASSWORDS?
         BNE   GO$OPEN                  NO - DON'T SET FLAG
         OI    FLAGS,FPSWD              YES - REMEMBER THIS FACT
         SPACE 3
GO$OPEN  OPEN  (SYSUADS,,SYSPRINT,(OUTPUT))   OPEN FILES
         SPACE 2
         TM    SYSUADS+48,X'10'         DID IT OPEN?
         BO    OPENOK                   YES - CONTINUE
         SPACE 2
ABEND99  ABEND 99
         EJECT ,
         SPACE 2
OPENOK   TM    SYSPRINT+48,X'10'        DID PRINT FILE OPEN?
         BNO   ABEND99                  NO - DIE
         MVC   LINE,BLANKS
         SPACE 1
         PUT   SYSPRINT,HEADER1A
         SPACE 1
         PUT   SYSPRINT,HEADER1B
         SPACE 1
         PUT   SYSPRINT,HEADER2A
         SPACE 1
         PUT   SYSPRINT,HEADER2B
         SPACE 3
         LH    R0,SYSUADS+62            GET BLOCKSIZE
         MH    R0,=H'10'                ONE USERID CAN HAVE 10 MEMBERS
         ICM   R0,B'1000',=X'01'        PUT IT IN SUBPOOL 1
         ST    R0,BUFFSIZE              SAVE FOR FREEMAIN
         GETMAIN  R,LV=(0)              BUY SOME STORAGE
         LR    R9,R1                    SAVE ADDRESS
         SPACE 1
         LA    R7,MEMBERS               -> MEMBER/TTR LIST
         LA    R4,MAXMEM                NUMBER OF ENTRIES IN TABLE
         SPACE 1
         POINT SYSUADS,=FS8'1'          START AT TTR=1
         EJECT ,
DIRLOOP1 LR    R2,R9                    GET BUFFER ADDRESS FOR "READ"
         SPACE 1
         READ  DECBDIR,SF,SYSUADS,(R2),256  READ THE DIRECTORY BLOCK
         SPACE 1
         CHECK DECBDIR
         SPACE 1
         LH    R3,0(,R2)                GET NUMBER OF BYTES IN USE
         SH    R3,=H'2'                 ACCOUNT FOR LENGTH HALFWORD
         LA    R2,2(,R2)                -> FIRST ENTRY
         SPACE 1
DIRLOOP2 MVC   0(8+3,R7),0(R2)          SAVE MEMBERNAME/TTR
         CLC   =8X'FF',0(R2)            END OF DIRECTORY?
         BE    DIREND                   YES
         BCT   R4,DIROK1                COUNT 1 SLOT USED
         MVC   LINE(53),=C'-*** TOO MANY UADS MEMBERS -- RE-ASSEMBLE PR$
               OGRAM ***'
         BAL   LINK,PUTPRINT
         CLOSE (SYSPRINT,,SYSUADS)
         ABEND 100,DUMP
         EJECT ,
DIROK1   LA    R7,8+3(,R7)              -> NEXT AVAILABLE SLOT
         IC    R1,8+3(,R2)              GET FLAG BYTE
         N     R1,=X'0000001F'          GET # OF USER DATA HALFWORDS
         AR    R1,R1                    GET # OF USER DATA BYTES
         LA    R2,12(R1,R2)             -> NEXT DIRECTORY ENTRY
         SR    R3,R1                    DECR BYTES LEFT IN THIS BLOCK
         SH    R3,=H'12'                FINISH ADJUSTING
         BP    DIRLOOP2                 SAVE NEXT ENTRY IN THIS BLOCK
         B     DIRLOOP1                 GO READ NEXT BLOCK
         SPACE 3
DIREND   LA    R1,MEMBERS-(8+3)         -> MEMBER LIST
         ST    R1,MEMLPTR               SAVE NEW POINTER
         MVI   TTR+3,0                  INIT "N" OF "TTRN"
         EJECT ,
NEXTUSER LR    R2,R9                    INIT BUFFER ADDRESS
         SPACE 2
FIND     L     R1,MEMLPTR               -> LAST MEMBER NAME
         LA    R1,8+3(,R1)              -> NEXT MEMBER NAME
         ST    R1,MEMLPTR               SAVE NEW POINTER
         MVC   MEMBER(8+3),0(R1)        SAVE MEMBER NAME AND TTR
         CLI   0(R1),X'FF'              DONE?
         BE    CLOSE                    YES
         SPACE 3
         FIND  SYSUADS,TTR,C            LOCATE THE MEMBER
         SPACE 1
         LTR   R15,R15                  DID IT WORK?
         BZ    FINDOK                   YES - CONTINUE
         MVC   LINE(20),=C'-*** FIND FAILED ***'
         BAL   LINK,PUTPRINT            PRINT ERROR MSG
         CLOSE SYSPRINT
         ABEND 101
         SPACE 3
FINDOK   READ  DECB,SF,SYSUADS,(R2),'S' READ THIS MEMBER
         SPACE 1
         CHECK DECB                     BE SURE IT WORKED
         SPACE 1
         LA    R14,MEMBER+7             -> END OF MEMBER NAME
         CLI   0(R14),C' '              FIND END OF USERID?
         BNE   UID$END                  YES
         BCT   R14,*-8                  KEEP LOOKING
         EX    0,*                      IT'LL NEVER GET HERE...
         SPACE 2
UID$END  IC    R15,0(,R14)              GET DIGIT AT END
         LA    R15,1(,R15)              INCR
         STC   R15,0(,R14)              STORE BACK
         AH    R2,0(,R2)                PUSH BUFFER PTR
         CLI   0(R14),C'9'              GOT MAX?
         BH    PROCESS                  YES - NO MORE MEMBERS FOR USER
         L     R1,MEMLPTR               -> LAST MEMBER NAME
         CLC   MEMBER,8+3(R1)           IS NEXT MEMBER SAME USER?
         BE    FIND                     YES - READ NXT MBR OF UID  LDW1
         EJECT ,
PROCESS  LR    R2,R9                    LOAD BUFFER POINTER
         USNGX DHED,R2                  HEADER
         MVI   LINE,C'-'                TRIPLE SPACE FIRST USERID LINE
*---  USERID
         MVC   LINE+1(8),UADSUSER       USERID TO PRINT LINE
         LA    R1,LINE+1+8-1            -> END OF MEMBER NAME
         CLI   0(R1),C' '               FIND END?
         BNE   *+8                      YES - IT'S THE DIGIT AT THE END
         BCT   R1,*-8                   NO - KEEP LOOKING
         MVI   0(R1),C' '               CHANGE DIGIT AT END TO BLANK
*---  IBM ATTRIBUTES
         TM    UADSATTR,USATR00         OPER?
         BNO   *+8                      NO - SKIP
         MVI   LINE+10,C'O'             YES - SHOW IT
         TM    UADSATTR,USATR01         ACCT?
         BNO   *+8                      NO - SKIP
         MVI   LINE+11,C'A'             YES - SHOW IT
         TM    UADSATTR,USATR02         JCL?
         BNO   *+8                      NO - SKIP
         MVI   LINE+12,C'J'             YES - SHOW IT
         TM    UADSATTR,USATR03         MOUNT?
         BNO   *+8                      NO - SKIP
         MVI   LINE+13,C'M'             YES - SHOW IT
*---  INSTALLATION ATTRIBUTES
         HEX   LINE+15,UADSINST,LEN=2   USER ATTRIBUTES
*---  LOGON MAX REGION SIZE
         LH    R0,UADSMAXC              GET MAX LOGON SIZE
         MVC   LINE+21(5),=X'2020202120'
         CVD   R0,DWD
         ED    LINE+20(6),DWD+5
         LTR   R0,R0                    ANY LIMIT?
         BP    *+10                     YES - SKIP
         MVC   LINE+21(5),=C'NOLIM'     NO - CHANGE
*---  UPT IMAGE
         L     R1,UADSUPTP              UPT IMAGE OFFSET
         AR    R1,R9                    -> UPT IMAGE
         USNGX UPT,R1
         SPACE 1
         HEX   LINE+28,(0,R1),LEN=2     RESERVED
         HEX   LINE+33,UPTUSER,LEN=5    INSTALLATION USE
         HEX   LINE+43,UPTUSER+5,LEN=5  SECOND PART OF ABOVE
         HEX   LINE+54,UPTSWS,LEN=1     SWITCHES
         HEX   LINE+57,UPTCDEL,LEN=1    CHARACTER DELETE
         HEX   LINE+60,UPTLDEL,LEN=1    LINE DELETE
         HEX   LINE+63,(15,R1),LEN=1    RESERVED
         MVC   LINE+67(7),UPTPREFX      DSNAME PREFIX
         CLI   UPTPREFL,0               ANY PREFIX?
         BNE   *+10                     YES - SKIP
         MVC   LINE+67(7),BLANKS        NO - KILL IT
         CLC   LINE+67(7),LINE+1        PREFIX = USERID?
         BE    *+8                      YES - SKIP
         MVI   LINE+66,C'*'             NO - FLAG IT
*---  DESTINATION
         MVC   LINE+76(8),UADSDEST
*---  CLASSES:  HOLD, JOB, MESSAGE, SYSOUT
         MVC   LINE+86(4),UADSSUBH      ALSO:  SUBC, SUBM, SOUT
*---  DECODE SOME INTERESTING STUFF FROM THE UPT
*---  PROMPT
         MVC   LINE+92(3),=C'NO '       ASSUME NOPROMPT
         TM    UPTSWS,UPTNPRM           NOPROMPT?
         BO    *+10                     CORRECT
         MVC   LINE+92(3),=C'YES'
*---  MSGID
         MVC   LINE+97(3),=C'YES'       ASSUME MSGID
         TM    UPTSWS,UPTMID            MSGID?
         BO    *+10                     CORRECT
         MVC   LINE+97(3),=C'NO '
*---  INTERCOM
         MVC   LINE+102(3),=C'NO '      ASSUME NOINTERCOM
         TM    UPTSWS,UPTNCOM           NOINTERCOM?
         BO    *+10                     CORRECT
         MVC   LINE+102(3),=C'YES'
*---  PAUSE
         MVC   LINE+107(3),=C'YES'      ASSUME PAUSE
         TM    UPTSWS,UPTPAUS           PAUSE?
         BO    *+10                     CORRECT
         MVC   LINE+107(3),=C'NO '
*---  MODE
         MVC   LINE+112(3),=C'YES'      ASSUME MODE
         TM    UPTSWS,UPTMODE           MODE?
         BO    *+10                     CORRECT
         MVC   LINE+112(3),=C'NO '
*---  WTPMSG
         MVC   LINE+117(3),=C'YES'      ASSUME WTPMSG
         TM    UPTSWS,UPTWTP            WTPMSG?
         BO    *+10                     CORRECT
         MVC   LINE+117(3),=C'NO '
         SPACE 1
         DROPX R1
         SPACE 1
         BAL   LINK,PUTPRINT
         SPACE 2
         MVI   LINE,C'0'                DOUBLE SPACE FIRST PSWD LINE
*---  RUN DOWN PASSWORD TREE
         ICM   R3,B'1111',UADSPWD1      OFFSET OF FIRST PASSWORD BLOCK
         BNZ   NEXTPSWD
         EX    0,*
*---  PROCESS NEXT PASSWORD
NEXTPSWD AR    R3,R9                    -> PASSWORD OFFSET BLOCK
         USNGX DPOB,R3
         ICM   R1,B'1111',UADSPDAT      OFFSET OF PASSWORD DATA BLOCK
         BZ    NO$PSWD                  SKIP IF NONE
         AR    R1,R9                    -> PASSWORD DATA BLOCK
         USNGX DPOBD,R1
         MVC   LINE+10(8),=C'********'  SHOW WE SUPPRESSED THE PASSWORD
         TM    FLAGS,FPSWD              BUT DO WE?
         BNO   *+10                     YES - DON'T PRINT IT
         MVC   LINE+10(8),UADSPPWD      MOVE PASSWORD TO PRINT LINE
         DROPX R1
*---  RUN DOWN ACCOUNT TREE
NO$PSWD  ICM   R4,B'1111',UADSPSUB      OFFSET OF FIRST ACCOUNT BLOCK
         BNZ   NEXTACCT
         EX    0,*
*---  PROCESS NEXT ACCOUNT
NEXTACCT AR    R4,R9                    -> ACCOUNT OFFSET BLOCK
         USNGX DNOB,R4
         ICM   R1,B'1111',UADSADAT      OFFSET OF ACCOUNT DATA BLOCK
         BZ    NO$ACCT                  SKIP IF NO ACCOUNT
         AR    R1,R9                    -> ACCOUNT DATA BLOCK
         USNGX DNOBD,R1
         IC    R15,UADSALEN             GET LENGTH OF ACCOUNT DATA
         LTR   R15,R15                  ANY?
         BNZ   ACCT$OK                  YES - CONTINUE
         EX    0,*                      LEN=0 ==> BLOCK SHOULDN'T EXIST
MOVEACCT MVC   LINE+20(*-*),UADSANUM    << EXECUTED >>
ACCT$OK  BCTR  R15,0                    -1 FOR EX
         EX    R15,MOVEACCT             MOVE ACCOUNT DATA TO PRINT LINE
         DROPX R1
*---  RUN DOWN PROCEDURE TREE
NO$ACCT  ICM   R5,B'1111',UADSASUB      OFFSET OF FIRST PROC BLOCK
         BNZ   NEXTPROC
         EX    0,*
*---  PROCESS NEXT PROCEDURE
NEXTPROC AR    R5,R9                    -> PROCEDURE OFFSET BLOCK
         USNGX DROB,R5
         ICM   R1,B'1111',UADSRDAT      OFFSET OF PROCEDURE DATA BLOCK
         BNZ   *+8                      CONTINUE IF OK
         EX    0,*                      NO PROCEDURE DATA???
         AR    R1,R9                    -> PROCEDURE DATA BLOCK
         USNGX DROBD,R1
         MVC   LINE+62(8),UADSRNAM      LOGON PROC NAME TO PRINT LINE
         LH    R0,UADSRSIZ              DEFAULT LOGON REGION SIZE
         CVD   R0,DWD
         MVC   LINE+72-1(5),=X'2020202020'
         ED    LINE+72-2(6),DWD+5
         MVC   LINE+78(8),UADSUNAM      ESOTERIC GROUP UNIT NAME
         DROPX R1
         BAL   LINK,PUTPRINT            PRINT DETAIL LINE
*---  CHAIN BACK UP TO NEXT PROCEDURE
         TM    UADSRFLG,FLGR01          LAST PROCNAME FOR THIS ACCT?
         BO    UP$ACCT                  YES - GO UP TREE TO NEXT ACCT
         ICM   R5,B'0111',UADSRNEX      OFFSET OF NEXT PROC OFFSET BLK
         B     NEXTPROC                 PROCESS NEXT PROCEDURE
         DROPX R5
*---  CHAIN BACK UP TO NEXT ACCOUNT
UP$ACCT  TM    UADSAFLG,AFLG01          LAST ACCT FOR THIS PASSWORD?
         BO    UP$PSWD                  YES - GO UP TREE TO NEXT PSWD
         ICM   R4,B'0111',UADSANEX      OFFSET OF NEXT ACCT OFFSET BLK
         B     NEXTACCT                 PROCESS NEXT ACCOUNT
         DROPX R4
*---  CHAIN BACK UP TO NEXT PASSWORD
UP$PSWD  TM    UADSPFLG,PFLG01          LAST PSWD FOR THIS PASSWORD?
         BO    NEXTUSER                 YES - GO ON TO NEXT USERID
         ICM   R3,B'0111',UADSPNEX      OFFSET OF NEXT PSWD OFFSET BLK
         B     NEXTPSWD                 PROCESS NEXT PASSWORD
         DROPX R3
         SPACE 1
         DROPX R2
         SPACE 3
CLOSE    CLOSE (SYSUADS,,SYSPRINT)
         SPACE 1
         L     R0,BUFFSIZE              GET SP/LEN OF BUFFER
         FREEMAIN  R,LV=(0),A=(R9)      FREE THE UADS BUFFER
         SPACE 1
         B     LEAVE                    ALL DONE
         SPACE 3
PUTPRINT PUT   SYSPRINT,LINE
         SPACE 1
         MVC   LINE,BLANKS
         BR    LINK
         SPACE 3
         INUSE ,
         EJECT ,
BLANKS   DC    CL133' '
HEADER1A DC    CL133' USERID   OAJM XXXX  MAXSZ  UPTR UPTUSER          $
                   SW CD LD RV  PREFIX   DEST      HJMS  PMPT MID  ICOM$
                PAUS MODE WTP             '
HEADER1B DC    CL133' -------  ---- ----  -----  ---- -----------------$
               --- -- -- -- --  -------  --------  ----  ---  ---  --- $
                ---  ---  ---             '
HEADER2A DC    CL133'          PASSWORD  ACCOUNT                       $
                           PROCNAME  SIZE  UNIT    '
HEADER2B DC    CL133'          --------  ------------------------------$
               ----------  --------  ----  --------'
*              CL133'12345678901234567890123456789012345678901234567890$
               12345678901234567890123456789012345678901234567890123456$
               789012345678901234567890123'
HEXTAB   HEXTAB  ,
         SPACE 2
         LTORG ,
         SPACE 3
WORKD    DSECT                          RESUME
DWD      DS    D                        DOUBLE WORD WORK AREA
BUFFSIZE DS    F                        SP/LEN FOR UADS BUFFER
MEMLPTR  DS    A                        -> CURRENT ENTRY IN "MEMBERS"
MEMBER   DS    CL8                      MEMBER NAME
TTR      DS    XL3                      TTR OF MEMBER
         DS    X                        N
         SPACE 1
FLAGS    DS    X
FPSWD    EQU   X'80'                    PRINT PASSWORDS
         SPACE 1
LINE     DS    CL133
WORD     DS    F
MEMBERS  DS    (MAXMEM)CL(8+3)          8 CHAR MEMBER, 3 CHAR TTR
         SPACE 2
WORKLEN  EQU   *-WORKD                  LENGTH TO GETMAIN
         EJECT ,
         IKJUPT  ,
         SPACE 3
UADSLIST CSECT                          RESUME
         EJECT ,
         SPACE 3
SYSUADS  DCB   DDNAME=SYSUADS,DSORG=PO,MACRF=R,RECFM=U
         SPACE 3
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,RECFM=FBA,LRECL=133,           $
               EXLST=PRTEXLST,MACRF=PM
         SPACE 2
PRTEXLST DC    0F'0',X'85',AL3(PRTEXIT)
         SPACE 2
PRTEXIT  DCBEXIT  BLKSIZE=6118
         SPACE 8
         IKJEFUAD  DSPL,ALL
         SPACE 2
         END
