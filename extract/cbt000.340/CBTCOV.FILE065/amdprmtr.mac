MTR      TITLE 'MASTER TRACE DUMP FORMAT EXIT'              *HMD 07/81*
* THIS PROGRAM WILL RUN AS AN EXIT TO PRINTDUMP. ITS'S FUNCTION IS TO
* FORMAT THE MASTER TRACE TABLE FOR AN SVC OR STANDALONE DUMP. IT WILL
* 1ST CHECK TO MAKE SURE THAT THE SYSTEM HAS SU64 INSTALLED AND THE
* MASTER TRACE IS ACTIVE. THE OUTPUT WILL BE IN 'REVERSE' ORDER DUE
* TO THE FACT THAT THE MASTER TRACE IS A BACKWARD WRAP-AROUND TABLE.
* TO IMPLEMENT THIS EXIT, IT MUST FIRST BE LINKED INTO EITHER
* LINKLIB OR A USER LIBRARY (USE STEPLIB ON PRINTDUMP IF SO), AND THE
* PRINTDUMP EXIT CONTROL TABLE ZAPPED TO INCLUDE THE EXIT NAME.
*
*        E.G.    NAME AMDPRECT
*                VER 00C8 4040404040404040,00000000,4040404040404040
*                REP 00C8 D4E3D9C1C3C54040,20000000,D4E3D9C1C3C54040
*
*                THE ABOVE ZAP WILL CAUSE THE EXIT TO BE ENTERED
*                WHENEVER THE FORMAT OPTION IS CHOSEN IN PRINTDUMP,
*                OR THE KEYWORD  'MTRACE' IS USED.
*
*                IF IT IS PREFERRED TO USE THE EXIT VIA KEYWORD ONLY,
*                CHANGE THE REP TO :
*
*                REP 00C8 D4E3D9C1C3C54040,00000000,D4E3D9C1C3C54040
*
*                AND USE THE KEYWORD 'MTRACE' WHEN USING PRINTDUMP.
*                WITH THE FIRST ZAP THE FORMAT OPTION OR THE KEYWORD
*                MAY BE USED. WITH THE 2ND ONLY THE KEYWORD WILL CAUSE
*                THE EXIT TO BE CALLED.
*
*                THIS VERSION WAS LAST UPDATED ON 79/11/27
*
* PLEASE REPORT ANY PROBLEMS WITH THIS EXIT TO  :
*      TOM MCCAFFERTY  8/249/9565
*
* THIS IS NOT A SUPPORTED FIELD SERVICE AID, BUT PROBLEMS WILL BE
* ADDRESSED INFORMALLY AS TIME PERMITS.
*
         EJECT
MTRACE   CSECT
GETDSECT DSECT
REGSAVE  DC     19F'0'
ADDRSAVE DC     F'0'
SAVEPICA DC     F'0'
SWITCH   DC     F'0'
BUFF1    DS     CL128
         EJECT                                              *HMD 07/81*
MTRACE   CSECT
R0       EQU    0
R1       EQU    1
R2       EQU    2
R3       EQU    3
R4       EQU    4
R5       EQU    5
R6       EQU    6
R7       EQU    7
R8       EQU    8
R9       EQU    9
R10      EQU    10
R11      EQU    11
R12      EQU    12
R13      EQU    13
R14      EQU    14
R15      EQU    15
RPARM    EQU    1
RWORK    EQU    2
RADDR    EQU    3
RSTOP    EQU    4
RCURR    EQU    5
RMTT     EQU    6
RSIZE    EQU    7
REND     EQU    8
RWRK2    EQU    9
RLINK    EQU    10
RBUFF    EQU    11
         EJECT
         STM    R14,R12,12(R13)       SAVE CALLERS REGS
         LR     R12,R15               SET UP BASE REG
         USING  MTRACE,R12            ESTABLISH BASE
         SPACE  1
* GETMAIN THE AREA FOR REGISTER SAVE AREA AND BUFFER WORK AREA
         LA     R0,512
         GETMAIN R,LV=(0)             GET NEW SAVE AREA
         ST     R1,8(,R13)
         ST     R13,4(,R1)            CHAIN NEW SAVE AREA
         L      R13,8(,R13)           SET NEW SAVE AREA
         USING  GETDSECT,R13
         SPIE   SPIEXIT,((1,15))      CATCH ALL PGM CHECKS
         ST     R1,SAVEPICA           SAVE PICA FOR SPIE DELETE
         L      R1,4(,R13)            PICK UP ORIG SAVE AREA
         L      RPARM,24(,R1)         RESTORE INPUT PARM
         L      RBUFF,8(,RPARM)       PICK UP PRINTDUMPS OUTPUT BUFFER
         MVC    0(L'TITLE,RBUFF),TITLE        PUT OUT HEADER AND
         BAL    RLINK,PRINTRTN            TWO BLANK LINES
         BAL    RLINK,PRINTRTN
         SPACE  3
*  WE MUST NOW MAKE SURE THAT THE CVT IS AVAILABLE IN THE DUMP, AND
*  THAT SU64 IS INSTALLED.
         SPACE 3
         L      RWORK,16(,RPARM)      PICK UP CVT POINTER
         LTR    RWORK,RWORK           IS IT INITIALIZED
         BZ     EXIT1                 IF NOT LEAVE
         LA     RWORK,1044(,RWORK)    POINT TO CVTIHASU FIELD
         BAL    RLINK,ACCESS          FETCH POINTER
         L      RWORK,0(RADDR)        PICK UP THE POINTER
         LA     RWORK,8(,RWORK)       POINT TO 8TH BYTE OF SU STRING
         BAL    RLINK,ACCESS          FETCH IT
         TM     0(RADDR),X'80'        SU64 INSTALLED ?
         BNO    EXIT3                 IF NOT EXIT
         SPACE  3
* NOW PICK UP THE POINTER TO THE MASTER TRACE TABLE, IF THE TRACE
* IS NOT ACTIVE, PUT OUT A MESSAGE AND TERMINATE THE EXIT.
         SPACE  3
         L      RWORK,16(,RPARM)      RELOAD CVT POINTER
         LA     RWORK,148(,RWORK)     POINT TO MSRDA VECTOR CVT+148
         BAL    RLINK,ACCESS          FETCH IT
         L      RWORK,0(RADDR)
         LA     RWORK,140(RWORK)      MSRDA+140 --> MTT HEADER
         BAL    RLINK,ACCESS
         MVC    4(2,RPARM),CON1       SET ALL ACCESSES TO ASID 1
         L      RWORK,0(,RADDR)
         LA     RWORK,0(,RWORK)
         LTR    RWORK,RWORK           IS THERE A MTRACE POINTER
         BZ     EXIT4                 NO --> MTRACE NOT ACTIVE
         BAL    RLINK,ACCESS          FETCH 4K OF MTT
         LR     RMTT,RADDR            SAVE ADDR OF START OF MTT
         MVC    28(4,RPARM),4(RMTT)   PICK UP CURRENT POINTER
         MVC    32(4,RPARM),12(RMTT)  PICK UP LAST POINTER          RI1
         MVC    36(4,RPARM),32(RMTT)  PICK UP FIRST POINTER         RI1
         L      RSTOP,4(RMTT)
         LR     RCURR,RSTOP
         SPACE  1
* DUE TO THE FACT THAT THE MASTER TRACE IS A BACKWARD WRAPAROUND TABLE
* AND THE ENTRIES ARE VARIABLE LENGTH, WE WILL SET UP AN ARTIFICIAL
* STOP ADDRESS 256 BYTES IN FRONT OF CURRENT ENTRY. THIS WILL LOSE
* AT MOST THE OLDEST 1 OR 2 MESSAGES.
         SPACE  1
         SH     RSTOP,CON256
         C      RSTOP,36(RPARM)       MAKE SURE WE HAVEN'T BACKED   RI1
         BNL    SKIPCHK               TO BEFORE 1ST ENTRY IN MTT
         L      RSTOP,36(RPARM)       IF WE HAVE, USE THE FIRST AS  RI1
         B      SKIPCHK               STOPPER
         SPACE  2
NXTENTRY EQU    *
* THE SWITCH TO BE TESTED IS SIMPLY A MEANS OF HANDLING THE COMPARE
* FOR THE STOP SINCE WE ONLY WANT TO STOP AFTER WE HAVE WRAPPED ONCE.
         CLI    SWITCH,X'FF'
         BNE    SKIPCHK
         CR     RSTOP,RCURR           HAVE WE FINISHED ALL ENTRIES
         BC     12,ENDEXIT            IF SO --> TERMINATE
SKIPCHK  EQU    *
         LR     RWORK,RCURR
         N      RWORK,ANDTO4K         ROUND DOWN TO 4K BOUNDARY
         ST     RWORK,ADDRSAVE        SAVE START OF PAGE ADDR
         SR     RCURR,RWORK           FIND OFFSET INTO 4K PAGE
         BAL    RLINK,ACCESS          FETCH 4K CONTAINING CURRENT
         C      RCURR,CON10           IS IT WITHIN 10 BYTES OF END
         BNL    SPANPAG1              IF SO MUST BUILD RECORD
         LH     RSIZE,8(RCURR,RADDR)  PICK UP SIZE OF ENTRY
         LTR    RSIZE,RSIZE
         BNP    ENDEXIT              ZERO LENGTH - END OF TRACE
         LA     REND,10(RSIZE,RCURR) POINT TO END OF ENTRY
         CH     REND,CON4K            IS IT TOTTALLY WITHIN 4K
         BH     SPANPAG1              IF NOT BUILD
         CH     RSIZE,CON121          MAKE SURE ITS NOT TOO LARGE
         BL     SIZEOK
         LH     RSIZE,CON121          IF TOO LARGE TRUNCATE END
SIZEOK   EQU    *
         BCTR   RSIZE,0
         AR     RCURR,RADDR
         EX     RSIZE,MOVEWQE         MOVE TO PRINT BUFFER
         BAL    RLINK,PRINTRTN
         A      REND,ADDRSAVE        INCREMENT TO NEXT ENTRY
         LR     RCURR,REND
         LA     RWORK,10(REND)
         C      RWORK,32(,RPARM)       ARE WE AT END OF MTT         RI1
         BL     NXTENTRY             IF NOT LOOP
         L      RCURR,36(,RPARM)     PICK UP 1ST ENTRY AT START     RI1
         MVI    SWITCH,X'FF'
         B      NXTENTRY             LOOP
         SPACE  4
* AT THIS POINT THE CURRENT RECORD IS NOT TOTALLY WITHIN
* THE PAGE. WE MUST MOVE THE PARTIAL RECORD INTO THE OUTPUT AREA,
* FETCH THE NEXT PAGE AND COMPLETE THE RECORD.
         SPACE  1
SPANPAG1 L      RBUFF,8(,RPARM)      PICK UP POINTER TO OUTPUT AREA
         LH     RWRK2,CON4K
         SR     RWRK2,RCURR          GET LENGTH TO END
         BCTR   RWRK2,0
         LA     R15,0(RADDR,RCURR)    POINT TO DATA FOR MOVE
         EX     RWRK2,MOVEPRT1       MOVE 1ST PART TO SAVE AREA
         L      RWORK,ADDRSAVE
         AH     RWORK,CON4K          POINT TO NEXT BLOCK
         BAL    RLINK,ACCESS         FETCH IT
         LA     RWRK2,BUFF1+1(RWRK2)
         MVC    0(160,RWRK2),0(RADDR)     2ND PART TO SAVE AREA
         LH     RWRK2,BUFF1+8  NOW MOVE ENTIRE RECORD TO OUT BUFFER
         CH     RWRK2,CON121   MAKE SURE ITS NOT TOO LARGE
         BL     GOAHEAD
         LH     RWRK2,CON121   IF TOO LARGE TRUNCATE THE END
GOAHEAD  EQU    *
         BCTR   RWRK2,0
         EX     RWRK2,MOVEBUFF
         BAL    RLINK,PRINTRTN
         LH     RWRK2,BUFF1+8        RESTORE ORIG SIZE
         LA     RCURR,10(RWRK2,RCURR)
         A      RCURR,ADDRSAVE
         B      NXTENTRY             LOOP TILL END OF TRACE
         EJECT
* THIS SUBROUTINE INTERFACES WITH PRINTDUMPS ACCESS ROUTINE TO FETCH
* THE ADDRESS CONTAINED IN REG RWORK. IT WILL RETURN THE DATA POINTER
* IN REGISTER RADDR. IF THE ADDRESS CAN NOT BE ACCESSED, IT WILL
* PUT OUT A MESSAGE AND TERMINATE. THIS CAN HAPPEN IF FOR EXAMPLE ONE
* OF THE PAGES IN THE MASTER TRACE HAD NOT BEEN USED FOR A WHILE AND
* HAD BEEN PAGED OUT. IT DOES NOT NECESSARILY IMPLY AN ERROR CONDITION
         SPACE  1
ACCESS   LR     R0,RWORK
         L      R15,20(,RPARM)     PICK UP ADDRESS OF PRINTDUMPS ACCESS
         BALR   R14,R15            ROUTINE AND BRANCH TO IT.
         LR     RADDR,R0
         LTR    R15,R15            IF NON-ZERO RC --> EXIT
         BZR    RLINK
         B      EXIT2
         SPACE  2
* CVT COULD NOT BE ACCESSED. EXIT MUST TERMINATE.
EXIT1    MVC    0(L'NOCVT,RBUFF),NOCVT
         BAL    RLINK,PRINTRTN
         B      TERM
         SPACE  2
* ADDRESS NOT IN DUMP. EXIT MUST TERMINATE.
EXIT2    MVC    0(L'BADADDR,RBUFF),BADADDR
         BAL    RLINK,PRINTRTN
         B      TERM
         SPACE  2
* SU64 NOT INSTALLED. NO MTT TO FORMAT.
EXIT3    MVC    0(L'NOSU64,RBUFF),NOSU64
         BAL    RLINK,PRINTRTN
         B      TERM
         SPACE  2
* MASTER TRACE WAS NOT ACTIVE. TERMINATE NORMALLY.
EXIT4    MVC    0(L'NOMTTMSG,RBUFF),NOMTTMSG
         BAL    RLINK,PRINTRTN
         B      TERM
         SPACE  2
* EVERYTHING WENT OK. TERMINATE NORMALLY.
ENDEXIT  EQU    *
         BAL    RLINK,PRINTRTN
         MVC    0(L'NORMAL,RBUFF),NORMAL
         BAL    RLINK,PRINTRTN
TERM     EQU    *
         L      R1,SAVEPICA             PICK UP OLD PICA IF ANY
         SPIE   MF=(E,(R1))             DELETE OUR SPIE
         L      R13,4(R13)              RETURN
         LM     R14,12,12(R13)                TO
         BR     R14                             PRINTDUMP
         EJECT
* IF WE GET HERE THE SPIE EXIT INTERCEPTED A PROGRAM CHECK. TO AVOID
* TERMINATING PRINTDUMP, PUT OUT A MESSAGE AND TERMINATE THE EXIT.
PGMCHK   MVC    0(L'ABENDMSG,RBUFF),ABENDMSG
         BAL    RLINK,PRINTRTN
         B      TERM
         SPACE  2
* THIS SUBROUTINE BRANCHES TO THE PRINTDUMP BUFFER OUTPUT ROUTINE
* TO PRINT ANY DESIRED LINE.
PRINTRTN EQU    *
         L      R15,12(RPARM)
         BALR   R14,R15
         L      RBUFF,8(,RPARM)
         BR     RLINK
         EJECT
MOVEWQE  MVC    0(0,RBUFF),10(RCURR)  EX MOVE TO OUTPUT BUFFER
MOVEPRT1 MVC    BUFF1(0),0(R15)       EX MOVE TO WORK AREA
MOVEBUFF MVC    0(0,RBUFF),BUFF1+10   EX MOVE FROM WORK AREA TO OUTBUFF
CON4K    DC     H'4096'
CON256   DC     H'256'
CON121   DC     H'121'
CON1     DC     H'1'
         DS     F'0'
ANDTO4K  DC     X'FFFFF000' USE TO ROUND DOWN TO 4K BOUNDARY
CON10    DC     X'00000FF6' USE TO SEE IF SIZE FIELD IS IN BLOCK
ROUND    DC     X'00000FFF' USE TO DEVELOP OFFSET WITHIN BLOCK
NOCVT DC C'CVT COULD NOT BE ACCESSED - - MASTER TRACE FORMATTING ENDED'
BADADDR  DC     C'ADDRESS NOT IN DUMP -- MASTER TRACE FORMATTING ENDED'
NORMAL   DC     C'MASTER TRACE FORMATTING COMPLETE'
NOSU64 DC C'SU64 NOT ON SYSTEM - - MASTER TRACE FORMATTING IGNORED'
NOMTTMSG DC     C'MASTER TRACE WAS NOT ACTIVE AT TIME OF DUMP'
TITLE DC C'MASTER TRACE TABLE FOLLOWS - - MOST RECENT MESSAGE IS FIRST'
         DS     0F
         EJECT
* THE SPIE EXIT WILL SIMPLY SET UP THE RESUME ADDRESS TO POINT TO
* ROUTINE PGMCHK AND RETURN. THE ROUTINE WILL PUT OUT THE MESSAGE AND
* TERMINATE ONCE IT RECEIVES CONTROL.
SPIEXIT  EQU    *
         USING  *,R15
         L      R15,ABEND
         ST     R15,8(R1)      UPDATE PIE PSW TO EXIT
         BR     R14
ABEND    DC     A(PGMCHK)
ABENDMSG DC     C'MASTER TRACE FORMATTING ABENDED - - EXIT SKIPPED'
         DC     C'79/11/27'
         END
