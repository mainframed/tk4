CSA      TITLE 'ROCKWELL INTERNATIONAL CSA MONITOR COMMAND' *HMD 04/82*
         MACRO
&LABEL   @TPUT &LEN
         XC    TSOECB,TSOECB                                *HMD 04/82*
&LABEL   PUTLINE PARM=PTPB,OUTPUT=(&LEN,TERM,SINGLE,DATA),MF=(E,IOPL)
         TM    REPORT+X'30',X'10'  DCB OPEN ?
         BZ    $&SYSNDX           NO
         PUT   REPORT,&LEN
$&SYSNDX EQU *
         SPACE
         MEND
         EJECT
         MACRO
&L       FILL  &A,&B,&C
         LCLA  &LEN
         LCLC  &R
         AIF   ('&C'(1,1) NE '(').A
&LEN     SETA  K'&C-2
&R       SETC  '&C'(2,&LEN)
&L       LR    R1,&R
         AGO   .B
.A       ANOP
&L       L     R1,&C
.B       CVD   R1,DECIMAL
         MVC   FORMAT,EDITD
         ED    FORMAT,DECIMAL+4
         AIF   ('&A'(1,1) NE '(').C
&LEN     SETA  K'&A-2
&R       SETC  '&A'(2,&LEN)
         MVC   0(&B,&R),FORMAT+8-&B
         AGO   .D
.C       MVC   &A.(&B),FORMAT+8-&B
.D       MEND
         EJECT
         MACRO
&L       KFILL &A,&B,&C
         LCLA  &LEN
         LCLC  &R
         AIF   ('&C'(1,1) NE '(').A
&LEN     SETA  K'&C-2
&R       SETC  '&C'(2,&LEN)
&L       LR    R1,&R
         AGO   .B
.A       ANOP
&L       L     R1,&C
.B       SRL   R1,10
         CVD   R1,DECIMAL
         MVC   FORMAT,EDITK
         ED    FORMAT+1(6),DECIMAL+5
         AIF   ('&A'(1,1) NE '(').C
&LEN     SETA  K'&A-2
&R       SETC  '&A'(2,&LEN)
         MVC   0(&B,&R),FORMAT+8-&B
         AGO   .D
.C       MVC   &A.(&B),FORMAT+8-&B
.D       MEND
         EJECT
         MACRO
&L       HFILL &A,&B
         LCLA  &LEN
         LCLC  &R
         AIF   ('&B'(1,1) NE '(').A
&LEN     SETA  K'&B-2
&R       SETC  '&B'(2,&LEN)
&L       LR    R1,&R
         AGO   .B
.A       ANOP
&L       L     R1,&B
.B       STCM  R1,7,DECIMAL
         UNPK  FORMAT(7),DECIMAL(4)
         TR    FORMAT(6),HEXTBL-240
         AIF   ('&A'(1,1) NE '(').C
&LEN     SETA  K'&A-2
&R       SETC  '&A'(2,&LEN)
         MVC   0(6,&R),FORMAT
         AGO   .D
.C       MVC   &A.(6),FORMAT
.D       MEND
         EJECT
         MACRO
&L       PFILL &A,&B,&C,&D
         LCLA  &LEN
         LCLC  &R
         AIF   ('&C'(1,1) NE '(').A
&LEN     SETA  K'&C-2
&R       SETC  '&C'(2,&LEN)
&L       LR    R1,&R
         AGO   .B
.A       ANOP
&L       L     R1,&C
.B       MH    R1,=H'10000'
         SR    R0,R0
         AIF   ('&D'(1,1) NE '(').C
&LEN     SETA  K'&D-2
&R       SETC  '&D'(2,&LEN)
         DR    R0,&R
         AGO   .D
.C       D     R0,&D
.D       CVD   R1,DECIMAL
         MVC   FORMAT,EDITP
         ED    FORMAT+1(7),DECIMAL+5
         AIF   ('&A'(1,1) NE '(').E
&LEN     SETA  K'&A-2
&R       SETC  '&A'(2,&LEN)
         MVC   0(&B,&R),FORMAT+8-&B
         AGO   .F
.E       MVC   &A.(&B),FORMAT+8-&B
.F       MEND
         EJECT
*  FUNCTION:  CSAMON EXAMINES THE CSA FBQE CHAIN AND THE SPQE CHAIN TO
*       DETERMINE THE CSA USAGE ON A TIMER SAMPLED BASIS.
         SPACE 1
*  ENTRY POINT:  ENTER AT CSAMAP WITH NORMAL MVS LINKAGE CONVENTIONS.
         SPACE 1
         SPACE 1
*  OUTPUT:  A REPORT OF SYSTEM STATISTICS, THE USAGE OF CSA, AND THE
*       USAGE OF EACH OF THE SUBPOOLS IN CSA.
         SPACE 1
*  JCL:  A REPORT DD CARD MAY BE PROVIDED.
         SPACE 1
*  EXIT:  THE PROGRAM ISSUES THE FOLLOWING RETURN CODES:
*         0  SUCCESSFUL COMPLETION
*         4  ERROR IN PARAMETER STRING - EXECUTION TERMINATED
*         8  NO SYSPRINT - EXECUTION TERMINATED
         SPACE 1
*  VERSION:  THIS IS VERSION 1 WRITTEN APRIL 1977 BY STEVEN ANDERSON
*       OSS GROUP OF ISPL  TRW SYSTEMS GROUP
*
*  VERSION 2 : MODIFIED BY LIONEL DYCK - JUNE 1979 TO FUNCTION
*              WITH ROCKWELL TSO MONITOR ROUTER AND TO DISPLAY
*              ONLY CSA USAGE
*
*  VERSION 2A: MODIFIED BY HOWARD DEAN-GTE DATA SERVICES-TO FUNCTION
*              ONLY ON TSO AND TO USE PUTLINE/GETLINE SERIVES.
*
CSAMON   TSOENTER REGS=YES,PL=(CPPL,IO),BASE=12             *HMD 04/82*
         TM    FIRSTSW,X'01'             FIRST PASS ?
         BO    CLEAR                     NO - BYPASS TIME
         OI    FIRSTSW,X'01'             SET FIRST PASS SWITCH
         TIME  DEC
         STM   R0,R1,STARTIME     SAVE START TIME
         ED    EDIT11,STARTIME     FORMAT START TIME
         ED    EDIT12,STARTIME+5   FORMAT START DATE
CLEAR    CALL  CLR3270                   CLEAR SCREEN       *HMD 04/82*
************************************************************
         EJECT
*  INITIALIZATION SECTION
         SPACE 2
SETUP    L     WK1,CVTPTR          -> CVT
         USING CVT,WK1
         L     WK2,CVTGDA          -> GLOBAL DATA AREA
         USING GDA,WK2
         USING PQESECT,WK3
         L     WK3,CSAPQEP         -> CSA PQE
         L     WK4,PQESIZE         SIZE OF CSA AREA
         ST    WK4,CSASIZE
         L     WK4,PQEREGN         START OF CSA AREA
         ST    WK4,CSASTART
         DROP  WK3
         DROP  WK2
         DROP  WK1
         EJECT
*  SCAN UNALLOCATED CSA SPACE
         SPACE 2
CSASCAN  L     WK1,CVTPTR          -> CVT
         USING CVT,WK1
         L     WK1,CVTGDA          -> GLOBAL DATA AREA
         USING GDA,WK1
         L     WK2,CSAPQEP         -> CSA PQE
         USING PQESECT,WK2
         SR    WK4,WK4             CLEAR SUM
         ICM   WK3,F,PQEFFBQE      -> FIRST FBQE
         BZ    SETCSA              STOP IS ZERO
         USING FBQESECT,WK3
CSALOOP  A     WK4,SIZE            ADD FBQE SIZE TO TOTAL
         L     WK3,FWDPTR          -> NEXT FBQE
         CR    WK2,WK3             END OF CHAIN
         BNE   CSALOOP             NO, CONTINUE DOWN CHAIN
         DROP  WK3
         DROP  WK2
         DROP  WK1
SETCSA   L     WK1,CSASIZE         SIZE OF CSA
         SR    WK1,WK4             SUBTRACT UNALLOCATED SPACE
         C     WK1,CSAHI           IS THIS THE HIGHEST CSA VALUE
         BNH   *+8                 NO, SKIP
         ST    WK1,CSAHI           SET NEW HIGH
         C     WK1,CSALO           IS THIS THE LOWEST CSA VALUE
         BNL   *+8                 NO, SKIP
         ST    WK1,CSALO           SET NEW LOW
         A     WK1,CSATOTAL        ADD COUNT TO TOTAL
         ST    WK1,CSATOTAL        SET NEW TOTAL
         EJECT
         SPACE 2
*  EXAMINE THE ALLOCATED SPACE FOR EACH CSA SUBPOOL
         SPACE 2
SUBSCAN  L     WK1,CVTPTR          -> CVT
         USING CVT,WK1
         L     WK1,CVTGDA          -> GLOBAL DATA AREA
         USING GDA,WK1
         ICM   WK1,F,CSASPQEP      -> CSA SPQE CHAIN
         BZ    WRITEIT             EXIT IF ZERO
         USING SPQESECT,WK1
SUBSTART LA    WK2,SUBTABLE        -> SUBPOOL TABLE
         USING TABLE,WK2
         ICM   WK3,F,POOLS         NUMBER OF SUBPOOLS FOUND
         BZ    NEWPOOL             BRANCH IF NO SUBPOOLS
SUBLOOP  CLC   SUBID,SPQEID        MATCHING SUBPOOL
         BE    SUBSUM              YES, GO SUM SPACE
         LA    WK2,16(,WK2)        NEXT TABLE ENTRY
         BCT   WK3,SUBLOOP         CHECK ALL SUBPOOLS
NEWPOOL  L     WK3,POOLS           NUMBER OF SUBPOOLS FOUND
         C     WK3,SUBLIMIT        TABLE LIMIT REACHED
         BNL   NEXTSPQE            YES, SKIP SPQE
         MVC   SUBID,SPQEID        SET SUBPOOL ID
         MVC   SUBMIN,MEG16        SET A HIGH VALUE AS MINIMUM
         LA    WK3,1(,WK3)         INCREASE SUBPOOL COUNT BY 1
         ST    WK3,POOLS           SAVE VALUE
SUBSUM   SR    WK4,WK4             CLEAR SUM
         ICM   WK3,F,SPDQEPTR      -> FIRST DQE
         USING DQESECT,WK3
         BZ    SETSUB              STOP IF ZERO PTR
DQELOOP  A     WK4,DQELNTH         SIZE OF BLOCK
         ICM   WK3,F,DQEPTR        -> NEXT DQE
         BNZ   DQELOOP             ADD ALL DQES
SETSUB   C     WK4,SUBMAX          IS THIS THE LARGEST SUBPOOL SIZE
         BNH   *+8                 NO, SKIP
         ST    WK4,SUBMAX          SET NEW MAXIMUM
         C     WK4,SUBMIN          IS THIS THE SMALLEST SUBPOOL SIZE
         BNL   *+8                 NO, SKIP
         ST    WK4,SUBMIN          SET NEW MINIMUM
         SRL   WK4,10              CONVERT ADDRESS TO K
         A     WK4,SUBTOTAL        ADD SPACE TO TOTAL
         ST    WK4,SUBTOTAL        SET NEW TOTAL
NEXTSPQE ICM   WK1,F,SPQEAD        -> NEXT SPQE
         BNZ   SUBSTART            FOLLOW THE SPQE CHAIN
         DROP  WK3
         DROP  WK2
         DROP  WK1
*  WRITE SYSTEM STATISTICS REPORT
         EJECT
WRITEIT  TIME  DEC
         STM   R0,R1,STOPTIME      SAVE STOP TIME
         MVC   EDIT13(9),EDITTME
         MVC   EDIT14(7),EDITDATE
         ED    EDIT13,STOPTIME     FORMAT STOP TIME
         ED    EDIT14,STOPTIME+5   FORMAT STOP DATE
         TM    REPORT+X'30',X'10' DCB OPEN
         BZ    NOSKIP
         PUT   REPORT,SKIP
NOSKIP   @TPUT HEADER1
         @TPUT HEADER2
         MVC   BUFFER+10(20),=C'    CSA SIZE =      '
         KFILL BUFFER+30,6,CSASIZE
         MVC   BUFFER+40(20),=C' CSA ADDRESS =      '
         HFILL BUFFER+60,CSASTART
         BAL   LINK,LINE           PRINT THE LINE
         EJECT
         SPACE 1
*  WRITE CSA USAGE REPORT
         SPACE 2
         L     WK3,COUNT
         A     WK3,=F'01'         INCR COUNTER
         ST    WK3,COUNT          AND SAVE IT
         SR    WK1,WK1             DIVIDE TOTAL
         L     WK2,CSATOTAL         SPACE FOUND
         DR    WK1,WK3               BY # OF INTERVALS
         ST    WK2,CSAAVG             TO GET AVERAGE
         MVC   BUFFER+10(20),=C'    CSA   AVERAGE = '
         KFILL BUFFER+30,6,CSAAVG   PRINT THE AVERAGE CSA SPACE
         BAL   LINK,LINE           PRINT THE LINE
         MVC   BUFFER+10(20),=C'    CSA   HIGHEST = '
         KFILL BUFFER+30,6,CSAHI   PRINT THE HIGHEST CSA SPACE
         BAL   LINK,LINE           PRINT LINE
         MVC   BUFFER+10(20),=C'    CSA    LOWEST = '
         KFILL BUFFER+30,6,CSALO   PRINT THE LOWEST CSA SPACE
         BAL   LINK,LINE           PRINT LINE
         EJECT
         SPACE 1
*  WRITE CSA SUBPOOL USAGE REPORT
         SPACE 2
         ICM   WK1,F,POOLS         GET NUMBER OF SUBPOOLS
         BZ    WRITEIT             EXIT IF ZERO
         MVC   BUFFER+10(21),=C'TABLE OF CSA SUBPOOLS'
         BAL   LINK,LINE           PRINT LINE
         MVC   BUFFER+10(35),=C'SUBPOOL KEY   MIN      AVG      MAX'
         BAL   LINK,LINE           PRINT LINE
         BAL   LINK,LINE           PRINT LINE
         LA    WK2,SUBTABLE        -> SUBPOOL TABLE
         USING TABLE,WK2
         L     WK5,COUNT           NUMBER OF INTERVALS
RPTLOOP  SR    WK3,WK3
         IC    WK3,SUBID           GET SUBPOOL ID
         FILL  BUFFER+12,3,(WK3)   PRINT THE SUBPOOL ID
         IC    WK3,SUBID+1         GET USER KEY
         SRL   WK3,4               SHIFT KEY TO REMOVE ZERO
         FILL  BUFFER+18,2,(WK3)   PRINT KEY
         KFILL BUFFER+22,6,SUBMIN  PRINT SUBPOOL MINIMUM SIZE
         SR    WK3,WK3             DIVIDE TOTAL
         L     WK4,SUBTOTAL         SPACE FOUND
         DR    WK3,WK5               BY # OF INTERVALS
         SLL   WK4,10              CONVERT FROM K TO BYTES
         KFILL BUFFER+31,6,(WK4)   PRINT AVERAGE SUBPOOL SIZE
         KFILL BUFFER+40,6,SUBMAX  PRINT SUBPOOL MAXIMUM SIZE
         BAL   LINK,LINE           PRINT THE LINE
         LA    WK2,16(,WK2)        GET NEXT ENTRY
         BCT   WK1,RPTLOOP         PRINT OUT ALL SUBPOOLS
         XC    TSOECB,TSOECB       CLEAR ECB                *HMD 04/82*
         MVI   REPLY,C' '          CLEAR REPLY AREA         *HMD 04/82*
         GETLINE PARM=GTPB,TERMGET=(EDIT,WAIT),             *HMD 04/82*X
               MF=(E,IOPL)                                  *HMD 04/82*
         LTR   R15,R15            CHECK FOR ZERO CODE       *HMD 04/82*
         BNE   EXIT               ERROR, RETURN TO CALLER   *HMD 04/82*
         L     R1,GTPB+4          GET ADDRESS OF LINE       *HMD 04/82*
         LH    R10,0(R1)          GET LENGTH OF LINE        *HMD 04/82*
         CH    R10,=H'4'          ZERO LENGTH LINE?         *HMD 04/82*
         BE    GOFREE             YES, GO FREE BUFFER       *HMD 04/82*
         MVC   REPLY(1),4(R1)     RETURN CHAR               *HMD 04/82*
GOFREE   FREEMAIN R,LV=(10),A=(1),SP=1  FREE STORAGE        *HMD 04/82*
         TCLEARQ INPUT                                      *HMD 04/82*
         OI    REPLY,C' '         MAKE UPPER CASE           *HMD 04/82*
         CLI   REPLY,C'O'         OPENR
         BE    OPENR
         CLI   REPLY,C'C'         CLOSER
         BE    CLOSER
         CLI   REPLY,C'Z'         EXIT
         BE    EXIT
         CLI   REPLY,C'M'         MENU
         BE    EXIT               YES
         CLI   REPLY,C'0'         UPPER 0
         BL    CLEAR
         CLI   REPLY,C'9'         UPPER 9
         BH    CLEAR
EXIT     TM    REPORT+X'30',X'10' REPORT OPEN?              *HMD 04/82*
         BZ    EXIT1                                        *HMD 04/82*
         CLOSE (REPORT)
EXIT1    L     R1,4(R13)          GET PREVIOUS SAVE AREA    *HMD 04/82*
         MVC   56(1,R1),REPLY     MOVE REPLY CHARACTER      *HMD 04/82*
         TSOLEAVE ISN=LTORG                                 *HMD 04/82*
         EJECT                                              *HMD 04/82*
OPENR    OPEN  (REPORT,OUTPUT)
         B     CLEAR
CLOSER   CLOSE (REPORT)
         B     CLEAR
         EJECT
         SPACE 1
*  PRINT LINE BUFFER
         SPACE 2
LINE     @TPUT DATA
         MVI   BUFFER,C' '         SINGLE SPACE
         MVC   BUFFER+1(100),BUFFER CLEAR BUFFER
         BR    LINK
         EJECT                                              *HMD 04/82*
WK1      EQU   2
WK2      EQU   3
WK3      EQU   4
WK4      EQU   5
WK5      EQU   6
WK6      EQU   7
WK7      EQU   8
WK8      EQU   9
LINK     EQU   10
A        EQU   10
B        EQU   11
C        EQU   12
D        EQU   13
E        EQU   14
F        EQU   15
         EJECT
FIRSTSW  DC    X'00'                     FIRST PASS SWITCH
SAVEAREA DC    18F'0'
PTPB     PUTLINE MF=L                                       *HMD 04/82*
GTPB     GETLINE MF=L                                       *HMD 04/82*
COUNT    DC    F'0'               COUNT OF PASSES
NUCBOUND DC    F'0'                END OF NUCLEUS
CSASIZE  DC    F'0'                LENGTH OF CSA
CSASTART DC    F'0'                START OF CSA
SUBTABLE DC    256F'0'             -> SUBPOOL TABLE
SUBLIMIT DC    F'64'               * ROOM FOR 64 CSA SUBPOOLS *
STARTIME DC    2F'0'               STARTING TIME STAMP
STOPTIME DC    2F'0'               ENDING TIME STAMP
POOLS    DC    F'0'                NUMBER OF CSA SUBPOOLS FOUND
CSAHI    DC    F'0'                HIGHEST NUMBER OF CSA FRAMES
CSALO    DC    X'00FFFFFF'         LOWEST NUMBER OF CSA FRAMES
CSATOTAL DC    F'0'                TOTAL OF CSA FRAMES
CSAAVG   DC    F'0'                AVG   OF CSA FRAMES
MEG16    DC    X'00FFFFFF'         16 MEG - 1
DECIMAL  DC    D'0'                CONVERSION WORK AREA
FORMAT   DC    CL8' '              FORMATING WORK AREA
REPLY    DC    C' '
HEXTBL   DC    CL16'0123456789ABCDEF'
EDITD    DC    X'4020202020202120'
EDITK    DC    X'40402020202120D2'
EDITP    DC    X'40402021204B2020'
         DS    0F
SKIP     DC    AL2(SKIPE-SKIP),AL2(0)
         DC    CL20'1 '
SKIPE    EQU   *
         DS    0F
HEADER1  DC    AL2(HEADER1E-HEADER1),AL2(0)
         DC    C' CSA USAGE MONITOR '
HEADER1E EQU   *
         DS    0F
HEADER2  DC    AL2(HEADER2E-HEADER2),AL2(0)
         DC    C'  START TIME = '
EDIT11   DC    X'4021204B20204B2020' HH.MM.SS
EDIT12   DC    X'40212061202020'   YY/DDD
         DC    C'  STOP TIME = '
EDIT13   DC    X'4021204B20204B2020' HH.MM.SS
EDIT14   DC    X'40212061202020'   YY/DDD
HEADER2E EQU   *
         DS    0F
DATA     DC    AL2(DATAE-DATA),AL2(0)
BUFFER   DC    CL70' '
DATAE    EQU   *
         DC    CL50' '            FILLER
EDITTME  DC    X'4021204B20204B2020' HH.MM.SS
EDITDATE DC    X'40212061202020'   YY/DDD
         EJECT                                              *HMD 04/82*
REPORT   DCB   DSORG=PS,MACRF=PM,DDNAME=REPORT,RECFM=VBA,LRECL=100,    X
               BLKSIZE=1004
         LTORG
         EJECT                                              *HMD 04/82*
         CVT   DSECT=YES,LIST=YES
         EJECT                                              *HMD 04/82*
         DCBD  DSORG=PS,DEVD=DA
         EJECT                                              *HMD 04/82*
         IHACDE
         EJECT                                              *HMD 04/82*
         IHADQE
         EJECT                                              *HMD 04/82*
         IHAFBQE
         EJECT                                              *HMD 04/82*
         IHAGDA
         EJECT                                              *HMD 04/82*
         IHAPQE
         EJECT                                              *HMD 04/82*
         IHAPVT
         EJECT                                              *HMD 04/82*
         IHASPQE
         EJECT
TABLE    DSECT
SUBTOTAL DS    F
SUBMAX   DS    F
SUBMIN   DS    F
SUBID    DS    H
PAD      DS    H
         SPACE 2
         END   CSAMON
