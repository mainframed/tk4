ECDATEX  TITLE 'GREGORIAN DATE SUBROUTINE (MMM DD, 19YY)'
*
*      THIS PROGRAM CONVERTS A EUROPEAN DATE ("DDMMYY") INTO A
* SPECIALLY FORMATTED GREGORIAN DATE ("MMM DD, 19YY"). IF IT
* IS CONVERTED SUCCESSFULLY, 0 IS RETURNED IN REGISTER 15. IF THE DATE
* IS NOT CONVERTED SUCCESSFULLY, 12 IS RETURNED IN REGISTER 15.
* THE CALLING SEQUENCE FOR "ECDATEX" IS AS FOLLOWS
*           1. THE INPUT EUROPEAN DATE IN THE FORMAT "DDMMYY"
*           2. THE OUTPUT GREGORIAN DATE IN THE FORMAT "MMM DD, 19YY"
*
*      THE "ECDATEX" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
ECDATEX  ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING WORKAREA,R13        ADDRESS WORKAREA DSECT
         MVC   CALLIST,CCALLST     MOVE PARAMETER LIST TO WORK AREA
         LM    R10,R11,0(R2)       LOAD PARAMETER ADDRESSES
         MVC   GDATE+0(2),2(R10)   MOVE IN "MM"
         MVC   GDATE+2(2),0(R10)   MOVE IN "DD"
         MVC   GDATE+4(2),4(R10)   MOVE IN "YY"
         LA    R4,GDATE            POINT TO GREG DATE FIELD
         PACK  DBLWORD,0(2,R4)     CONVERT MONTH TO PACKED DECIMAL
         CVB   R6,DBLWORD          CONVERT MONTH TO BINARY
         ST    R6,BMONTH           SAVE MONTH IN BINARY FORMAT
         LA    R4,BMONTH           POINT TO MONTH NUMBER
         LA    R5,MONTH            POINT TO MONTH NAME
         LOAD  EP=GETMONX          LOAD MONTH CONVERSION ROUTINE
         LR    R15,R0              SAVE "GETMONX" ENTRY POINT ADDRESS
         CALL  (15),((R4),(R5)),  CALL "GETMONX"                       X
               MF=(E,CALLIST)
         LTR   R15,R15             IF THE SUBROUTINE FAILED
         BNZ   ERROR                  THEN FLAG FOR ERROR
         LR    R2,R11              POINT TO PARAMETER FROM CALLER
         MVC   0(12,R2),=C'MMM DD, 19YY'
         MVC   0(3,R2),MONTH       MOVE IN "MMM"
         MVC   4(2,R2),GDATE+2     MOVE IN "DD"
         MVC   10(2,R2),GDATE+4    MOVE IN "YY"
         B     EXIT                   AND LEAVE
ERROR    DS    0H
         LA    R15,12              INDICATE ERROR IN INPUT
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
         EJECT
CCALLST  CALL  ,(,),MF=L            GENERATE PARAMETER LIST FOR CALL
CALLEN   EQU   *-CCALLST           COMPUTE PARAMETER LIST LENGTH
         SPACE
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
WORKSTRT EQU   *
         DS    18A                 REGISTER SAVE AREA
DBLWORD  DS    D
BMONTH   DS    F                   MONTH IN BINARY NUMBER FORMAT
CALLIST  DS    CL(CALLEN)          PARAMETER LIST FOR CALL MACRO
GDATE    DS    CL6                 GREG DATE
MONTH    DS    CL3                 MONTH NAME ABBREVIATION
WORKEND  EQU   *
WORKSIZE EQU   WORKEND-WORKSTRT    COMPUTE SIZE OF WORKAREA
         END
