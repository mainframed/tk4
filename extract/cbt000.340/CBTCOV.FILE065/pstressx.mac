PSTRESSX CSECT
***********************************************************************
*                                                                     *
*        STANDARD REGISTER EQUATES                                    *
*                                                                     *
***********************************************************************
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
R00      EQU   00
R01      EQU   01
R02      EQU   02
R03      EQU   03
R04      EQU   04
R05      EQU   05
R06      EQU   06
R07      EQU   07
R08      EQU   08
R09      EQU   09
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING PSTRESSX,R12
         LA    R15,SAVEAREA
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         LM    R8,R9,0(R1)         PICK UP COMM ECB ADDRESSES
         EJECT
         OPEN  (JOURNAL,(OUTPUT))
         EXTRACT ASID,'S',FIELDS=(ASID)
         LA    R6,MAINLIST
GETMLOOP DS    0H
         GETMAIN RC,LV=32768
         LTR   15,15               DID WE GET THE STORAGE?
         BNZ   GETMEND             ...NO, THEN START THE MESS
         ST    R1,0(,R6)           ...YES, SAVE THE ADDRESS
         LA    R6,4(,R6)           UPDATE TABLE POINTER ADDRESS
         B     GETMLOOP            GOTO GET NEXT CHUNK OF STORAGE
GETMEND  DS    0H
         POST  (8)                 POST MOTHER TO START TIMER
         WAIT  ECB=(9)             WAIT ON ECB FIRST
         XC    0(4,9),0(9)         CLEAR THE ECB
MAINLOOP DS    0H
         TM    0(R9),X'FF'         IS THE SUBTASK FUNCTION COMPLETE?
         BO    TERMLOOP            ...YES, THEN TERMINATE LOOPS
         LA    R7,256              ...NO, THEN SET UP TO DO MAIN LOOP
         LA    R6,MAINLIST         GET ADDRESS OF LIST OF ADDRESSES
MOVELOOP DS    0H
         TIME  STCK,START
         L     R2,0(,R6)           PICK UP AREA ADDRESS
         BCTR  R7,R0
         EX    R7,MOVEIT           STORE PROPAGATION VALUE
         LA    R3,1                SET FIELD LENGTH
         ICM   R3,8,0(R2)          PICK UP PADDING CHARACTER
         LA    R4,1(,R2)           SET OPERAND 2 ADDRESS
         L     R5,=F'32767'        8 4096K PAGES
         MVCL  R4,R2               CLEAR THE FIELD
         TIME  STCK,STOP
         PUT   JOURNAL,RECORD
         LA    R6,4(,R6)           POINT TO NEXT STORAGE AREA
         CLC   0(4,R6),=F'0'       IS THERE ANOTHER AREA?
         BE    MAINLOOP            ...NO, THEN GOTO RESTART LOOP
         B     MOVELOOP            ...YES, THEN GOTO CLEAR IT
MOVEIT   MVI   0(R2),0
TERMLOOP DS    0H
         CLOSE (JOURNAL)
         L     R13,4(,R13)
         LM    R14,R12,12(R13)
         XR    R15,R15
         BR    R14
         LTORG
SAVEAREA DC    18F'0'
         DS    0D
RECORD   DS    0CL20
ASID     DS    XL4
START    DS    XL8
STOP     DS    XL8
MAINLIST DC    256F'0'
JOURNAL  DCB   DSORG=PS,DDNAME=JOURNAL,BLKSIZE=4080,LRECL=20,          *
               RECFM=FB,MACRF=(PM),BUFNO=10
         END   PSTRESSX
