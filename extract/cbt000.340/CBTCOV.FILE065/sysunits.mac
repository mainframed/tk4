SYSUNITS CSECT
TOP      SAVE  (14,12),T,'SYSUNITS &SYSDATE &SYSTIME'
         LR    R10,15
         USING TOP,R10
         LR    R2,R1
         LA    R1,WAREA
         ST    13,4(R1)                NEW CORE IS SAVE AREA
         ST    R1,8(13)
         LR    13,R1
         USING WAREA,13
***************************************************************
*** FUNC:  TO READ A LIST OF UNITNAMES AND RETURN           ***
***        THE RELATED UNITS AND VOLUMES TO SYSOUT          ***
***        A DATASET                                        ***
***                                                         ***
*** DATE:  06/14/90                                         ***
***                                                         ***
*** INPUT  UNITIN  DDNAME  COLUME 2 IS UNIT NAME            ***
***        OR * IN COLUME ONE FOR COMMENT                   ***
***        IN COLUME 10 OPTIONAL:  ALL                      ***
***                 THIS OPTION HAS NO MEANING FOR NON-DASD ***
***                 IF BLANK ONLY ONLINE DASD WILL BE PUT   ***
***                 OUT                                     ***
***                                                         ***
*** OUTPUT UNITOUT DDNAME  (RECFM=FB,LRECL=80) SYSOUT OR DISK *
***        INPUT RECORD  FOLLOWED BY RECORDS OF DEVICES:    ***
***        COLUME 10-CUU, 15-VOLUME 25-DEVICE TYPE          ***
***                DEVICE TYPES ARE DASD, TAPE, UREC        ***
***                  UREC IS USED FOR ALL OTHER TYPES       ***
***                                                         ***
***                                                         ***
***          THIS WORKS FOR ALL XA AND ESA SYSTEMS          ***
***                                                         ***
***   SEND INQUIRES TO:                                     ***       *
***            STEVEN D. SMITH   G12-99                     ***       *
***            SECURITY PACIFIC AUTOMATION COMPANY          ***       *
***            611 NORTH BRAND                              ***       *
***            GLENDALE, CA, 91203                          ***       *
***                                                         ***       *
***   OR PHONE: (818) 507-3014                              ***       *
***                                                         ***       *
*************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
OFLG     EQU   X'10'
         OPEN  (SYSUT1,INPUT)
         TM   DCBOFLGS-IHADCB+SYSUT1,X'10'
         BNO   ABEND1
         OPEN  (SYSUT2,OUTPUT)
         TM   DCBOFLGS-IHADCB+SYSUT2,X'10'
         BNO   ABEND1
         MVI   OUTPUT,C' '              *                      SS AUG89
         MVC   OUTPUT+1(79),OUTPUT      *                      SS AUG89
         SPACE 3
GETNXT   DS    0H                       *                      SS AUG89
         GET   SYSUT1,INPUT             *                      SS AUG89
         PUT   SYSUT2,INPUT             *                      SS AUG89
         CLI   INPUT,C'*'               *                      SS AUG89
         BE    GETNXT                   *                      SS AUG89
         MVC   CNTLTBL(8),INPUT+1       *                      SS AUG89
         OC    CNTLTBL(8),OUTPUT+30     FORCE UPPER CASE       SS AUG89
         NI    FLAGS,255-DISPOFLN       *                      SS AUG89
         CLI   INPUT+10,C' '            REQUEST ON             SS AUG89
         BE    *+8                      NO                     SS AUG89
         OI    FLAGS,DISPOFLN      DISPLAY OFFLINE             SS AUG89
* GET DASD UCB FOR GENERIC DEVICE TYPE USING IEFEB4UV          SS AUG89
         LA    R1,CNTLTBL          UNIT TABLE  ADDRESS         SS AUG86
         ST    R1,PARMLIST         SET POINTER                 SS AUG86
         LA    R1,REQUTYPE         REQUEST FLAG                SS AUG86
         ST    R1,PARMLIST+4       SET POINTER                 SS AUG86
         LA    R1,PARMLIST         POINT TO PARMLIST           SS AUG86
         LINK  EP=IEFEB4UV         XA SERVICE ROUTINE          SS AUG86
         LTR   R15,R15                  *                      SS AUG86
         BNZ   NOUCBS              NO ENTRIES                  SS AUG86
         L     R5,LISTPTR          GET POINTER TO LIST         SS AUG86
         L     R4,4(R5)            GET COUNT                   SS AUG86
         LA    R5,8(R5)            FIRST ENTRY                 SS AUG89
         B     CHKUCB                   *                      SS AUG89
SCANLOOP DS    0H                  LOOP                        SS NOV85
*  EXTRACT ENTRY FROM UCB LIST                                 SS NOV85
         LA    R5,4(,R5)         NEXT ADDRESS                  SS AUG86
         BCT   R4,CHKUCB         MORE TO DO                    SS AUG86
*  AT END OF LIST FREEMAIN                                     SS NOV85
         L     R1,LISTPTR        GET LIST ADDRESS              SS AUG86
         L     R0,0(,R1)         SUBPOOL AND LENGTH            SS AUG86
         FREEMAIN R,LV=(0),A=(1) FREE LIST                     SS AUG86
         B     SCANEND                  *                      SS AUG86
CHKUCB   DS    0H                       *                      SS AUG89
         L     R1,0(R5)                                        SS NOV85
         USING UCBOB,R1                                        SS NOV85
         CLI   UCBTBYT3,X'20'      REALLY A DASD DEVICE        SS AUG86
         BNE   PUTOTHER            NO, SKIP IT                 SS AUG86
         TM    UCBSTAT,UCBONLI     IS IT ONLINE                SS NOV85
         BZ    OFFLINE             NO, SKIP IT                 SS NOV85
         CLI   UCBVOLI,C' '        ANY VOLUME                  SS NOV85
         BNH   OFFLINE             NO VOLUME, SKIP IT          SS NOV85
         TM    UCBSTAT,UCBCHGS+UCBUNLD GOING OFFLINE           SS NOV85
         BNZ   OFFLINE             YES, SKIP IT                SS NOV85
* PROCESS UCB                           *                      SS AUG89
         MVC   OUTPUT+9(3),UCBNAME      *                      SS AUG89
         MVC   OUTPUT+14(6),UCBVOLI     *                      SS AUG89
         MVC   OUTPUT+24(4),DASD        *                      SS AUG89
         PUT   SYSUT2,OUTPUT            *                      SS AUG89
         B     SCANLOOP                 *                      SS AUG89
OFFLINE  DS    0H                       *                      SS AUG89
         TM    FLAGS,DISPOFLN      DISPLAY OFFLINE             SS AUG89
         BZ    SCANLOOP                 *                      SS AUG89
         MVC   OUTPUT+9(3),UCBNAME      *                      SS AUG89
         MVC   OUTPUT+14(6),OFFLNMG     *                      SS AUG89
         MVC   OUTPUT+24(4),DASD        *                      SS AUG89
         PUT   SYSUT2,OUTPUT            *                      SS AUG89
         B     SCANLOOP                 *                      SS AUG89
OFFLNMG  DC    C'*OFFLN'                *                      SS AUG89
PUTOTHER DS    0H                       *                      SS AUG89
         CLI   UCBTBYT3,X'80'      COULD BE TAPE               SS AUG86
         BE    PUTTAPE             NO, SKIP IT                 SS AUG86
         MVC   OUTPUT+9(3),UCBNAME      *                      SS AUG89
         MVC   OUTPUT+14(6),OUTPUT+30   BLANK                  SS AUG89
         MVC   OUTPUT+24(4),UREC        *                      SS AUG89
         PUT   SYSUT2,OUTPUT            *                      SS AUG89
         B     SCANLOOP                 *                      SS AUG89
PUTTAPE  DS    0H                       *                      SS AUG89
         MVC   OUTPUT+9(3),UCBNAME      *                      SS AUG89
         MVC   OUTPUT+14(6),OUTPUT+30   BLANK                  SS AUG89
         MVC   OUTPUT+24(4),TAPE        *                      SS AUG89
         PUT   SYSUT2,OUTPUT            *                      SS AUG89
         B     SCANLOOP                 *                      SS AUG89
DASD     DC    CL4'DASD'                *                      SS AUG89
UREC     DC    CL4'UREC'                *                      SS AUG89
TAPE     DC    CL4'TAPE'                *                      SS AUG89
* DONE                                  *                      SS AUG89
SCANEND  DS    0H                       *                      SS AUG89
         DROP  R1                       *                      SS AUG89
         B     GETNXT                   *                      SS AUG89
NOUCBS   DS    0H                       *                      SS AUG89
         PUT   SYSUT2,MSG1              *                      SS AUG89
         B     GETNXT                   *                      SS AUG89
EODAD    CLOSE (SYSUT1)
OUT      CLOSE (SYSUT2)
EXIT     L     13,4(13)                 LOAD ADDRESS OF PREV SAVE AREA
         RETURN (14,12),RC=0
         SPACE 1
ABEND1   ABEND 1
         SPACE 3
         DROP  10                       *                      SS AUG89
         USING *,15
OUTEXIT  DS    0H
         CLC   DCBBLKSI-IHADCB(L'DCBBLKSI,1),=H'0'
         BNE   *+10
         MVC   DCBBLKSI-IHADCB(L'DCBBLKSI,1),BLKSIZE
         BR    14
         DROP  15
         SPACE 1
WAREA    DS    18F
BLKSIZE  DC    H'3200'
REQUTYPE DC    X'1000'             GEN UCB LIST REQUEST        SS AUG89
FLAGS    DC    F'0'                ZERO FLAGS                  SS AUG89
DISPOFLN EQU   X'80'               DISPLAY OFFLINE             SS AUG89
PARMLIST DS    2F                       *                      SS AUG89
CNTLTBL  DS    0F                  SCAN ROUTINE ADDR           SS AUG89
         DS    2F                  UNIT NAME GOES HERE         SS AUG89
LISTPTR  DS    F                   RETURNED                    SS AUG89
INPUT    DS    CL80                     *                      SS AUG89
OUTPUT   DS    CL80                     *                      SS AUG89
MSG1     DC    CL80'NO VOLUMES FOR THIS UNIT NAME'             SS AUG89
SYSUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=UNITIN,EODAD=EODAD
SYSUT2   DCB   DSORG=PS,MACRF=PM,DDNAME=UNITOUT,EROPT=ACC,EXLST=YYYY,  X
               RECFM=FB,LRECL=80
         SPACE 1
         DS    0F
YYYY     DC    X'85',AL3(OUTEXIT)
         SPACE 1
         DCBD  DSORG=PS
         IEFUCBOB
         END
