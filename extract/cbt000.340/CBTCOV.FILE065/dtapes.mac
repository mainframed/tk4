DT       TITLE 'DTAPES TSO COMMAND FOR MVS REL 3.8'
*
*  THIS IS THE DTAPES COMMAND FOR TSO MVS REL 3.8.
*
*  COMMAND DISPLAYS THE STATUS AND VOLSERS OF ALL ONLINE TAPE UNITS
*  THE COMMAND SYNTAX IS:
*
*         DTAPES
*
*  ADAPTED FROM MICHIGAN MODS 'USPACE' COMMAND FROM UNITED AIR LINES
*  TSO MODS 3-75.  KEN TRUE FAIRCHILD MAY76
*
*  ALSO USED OBTAIN CODE FROM U.C.L.A  URLISTVO COMMAND FROM THE SAME
*  MICHMODS TAPE.  KT JUNE76.
*
*  SORT UCB ADDRESS CODE FROM MICHMODS DDASD COMMAND.  KT 13JULY76
*
*  INSTALLED SEARCH OF DEVNAMET FOR UNIT DESCRIPTION.  KT 26APR77
*
*  MODIFIED TO RUN AS TSO COMMAND OR OPERATOR STARTED TASK.  KT 27MAY77
*
*  MODIFIED TO DISPLAY ONLY ONLINE TAPE UNITS AND DISPLAY ADDITIONAL
*  STATUS INFORMATION.   STEVE ANDERSON TRW JUNE80
*
*  MODIFIED TO DISPLAY ADDITIONAL STATUS INFORMATION.       *HMD 08/81*
*  HOWARD M. DEAN  GTE DATA SERVICES                        *HMD 08/81*
*                                                           *HMD 08/81*
*  MODIFIED TO REMOVE WTO OPERATOR INTERFACE. THIS COMMAND  *HMD 08/81*
*  MUST NOW BE ISSUED FROM TSO ONLY.                        *HMD 08/81*
*                                                           *HMD 08/81*
*
         EJECT
DTAPES   TSOENTER REGS=NO,PL=(CPPL,IO),WORK=CORENEED        *HMD 08/81*
         LOAD  EP=DEVNAMET         LOAD DEVICE NAME TABLE
         ST    R0,DEVADDR          ...AND SAVE ITS ADDRESS
         SPACE
         MVI   BUFFER,C' '                                  *HMD 08/81*
         MVC   BUFFER+1(L'BUFFER-1),BUFFER                  *HMD 08/81*
         MVC   BUFFER,FMTMSG1                               *HMD 08/81*
         BAL   LINKREG,PUTMSG
         SPACE
         L     R7,CVTPTR           CVT ADDR.
         USING CVT,R7              CVT ADDRESSABILITY
         L     R7,CVTILK2          UCB LOOKUP TABLE ADDR.
         DROP  R7                  KILL CVT ADDRESSABILITY
         USING UCBSECT,R8          SET ADDRESSAB TO UCB.
         XR    R8,R8               CLEAR R8                 *HMD 08/81*
         XC    LCT,LCT             CLEAR COUNT
         MVC   LMAX(2),=H'1000'    SAY ROOM FOR 500 ENTRIES
CYCLE    ICM   R8,B'0011',0(R7)    UCB ADDR.                *HMD 08/81*
         BZ    INDEX               DUMMY PLACEHOLDER        *HMD 08/81*
         C     R8,=F'65535'        END OF UCB TABLE?        *HMD 08/81*
         BE    RLSE                YES, RETURN              *HMD 08/81*
         CLI   UCBID,UCBSTND       IS UCB STANDARD?
         BNE   INDEX               NO, ERROR
         CLI   UCBTBYT3,UCB3TAPE   IS IT A TAPE?
         BNE   INDEX               NO
         SR    R4,R4
COMPLIST CH    R4,LCT              SEE IF WEVE SEEN THIS ONE ?
         BNL   ADDLIST             NOPE-> GO ADDIT
         LA    R5,LIST(R4)         LOAD LIST ADDRESS        *HMD 08/81*
         CLM   R8,B'0011',0(R5)    COMPARE TO LIST          *HMD 08/81*
         BE    INDEX
         LA    R4,2(R4)
         B     COMPLIST
ADDLIST  TM    UCBSTAT,UCBONLI     IS DRIVE ONLINE?          ** SEA
         BNO   INDEX               ->NO..SKIP MESSAGE        ** SEA
         LH    R4,LCT
         CH    R4,LMAX             TABLE LIMIT?
         BNL   DISPLAY             ->YES..SKIP STORE
         STH   R8,LIST(R4)         SAVE
         LA    R4,2(R4)             FOR LATER
         STH   R4,LCT                REFERENCE
DISPLAY  MVI   BUFFER,C' '         CLEAR THE BUFFER         *HMD 08/81*
         MVC   BUFFER+1(L'BUFFER-1),BUFFER                  *HMD 08/81*
         MVC   MSGUNIT,UCBNAME     COPY UNIT ADDRESS
         CLI   UCBVOLI,C'A'        ANY VOLSER IN UCBVOLI ?
         BL    NOVOLSER            NO, NON-PRINTABLE        *HMD 08/81*
         CLI   UCBVOLI,C'Z'                                 *HMD 08/81*
         BL    VOLOK                                        *HMD 08/81*
         CLI   UCBVOLI,C'0'        NUMERIC VOLSER           *HMD 08/81*
         BL    NOVOLSER            NO, NON-PRINTABLE        *HMD 08/81*
         CLI   UCBVOLI,C'9'                                 *HMD 08/81*
         BH    NOVOLSER            NO-> SKIP MOVE OF VOLSER
VOLOK    MVC   MSGVOL,UCBVOLI      COPY VOLUME SERIAL NUMBER
*
NOVOLSER DS    0H                                           *HMD 08/81*
         LA    R5,MSGSTAT           ADDR OF STATUS AREA     *HMD 08/81*
         MVC   0(2,R5),=C'ON'       DEFAULT                 *HMD 08/81*
         TM    UCBSTAT,UCBONLI      IS DEVICE ONLINE ?      *HMD 08/81*
         BO    EEE100               YES - OK                *HMD 08/81*
         MVC   0(3,R5),=C'OFF'                              *HMD 08/81*
         LA    R5,1(R5)                                     *HMD 08/81*
EEE100   DS    0H                                           *HMD 08/81*
         LA    R5,3(R5)             BUMP POINTER            *HMD 08/81*
         MVC   0(1,R5),=C'A'                                *HMD 08/81*
         TM    UCBSTAT,UCBALOC      IS DEVICE ALLOCATED ?   *HMD 08/81*
         BO    FFF100               YES - OK                *HMD 08/81*
         MVC   0(2,R5),=C'NA'                               *HMD 08/81*
         LA    R5,1(R5)             BUMP POINTER            *HMD 08/81*
FFF100   DS    0H                                           *HMD 08/81*
         LA    R5,2(R5)             BUMP POINTER            *HMD 08/81*
         TM    UCBFLA,UCBBSY        IS DEVICE BUSY BIT ON ? *HMD 08/81*
         BZ    GGG100               NO - OK                 *HMD 08/81*
         MVC   0(4,R5),=C'BUSY'     YES - DISPLAY IT        *HMD 08/81*
         LA    R5,5(R5)             BUMP POINTER            *HMD 08/81*
GGG100   DS    0H                                           *HMD 08/81*
         TM    UCBFLA,UCBNRY        IS NOT-READY BIT ON ?   *HMD 08/81*
         BZ    HHH100               NO - OK                 *HMD 08/81*
         MVC   0(3,R5),=C'N/R'      YES - DISPLAY IT        *HMD 08/81*
         LA    R5,4(R5)             BUMP POINTER            *HMD 08/81*
HHH100   DS    0H                                           *HMD 08/81*
         TM    UCBFLC,UCBITF        IS THE INTERCEPT BIT ON ?HMD 08/81*
         BZ    III100               NO - OK                 *HMD 08/81*
         MVC   0(2,R5),=C'IB'       YES - DISPLAY IT        *HMD 08/81*
         LA    R5,3(R5)             BUMP POINTER            *HMD 08/81*
III100   DS    0H                                           *HMD 08/81*
         TM    UCBFLC,UCBIVRS       WAS INT REQ MSG ISSUED ?*HMD 08/81*
         BZ    JJJ100               NO - OK                 *HMD 08/81*
         MVC   0(3,R5),=C'I/R'      YES - DISPLAY IT        *HMD 08/81*
         LA    R5,4(R5)             BUMP POINTER            *HMD 08/81*
JJJ100   DS    0H                                           *HMD 08/81*
         TM    UCBFLC,UCBTICBT      CE/DE PENDING ?         *HMD 08/81*
         BZ    KKK100               NO - OK                 *HMD 08/81*
         MVC   0(5,R5),=C'CE/DE'    YES - DISPLAY IT        *HMD 08/81*
KKK100   DS    0H                                           *HMD 08/81*
*
CHKDEN   TM    UCBTBYT2,UCBDUDN1   IS IT 800/1600?
         BNO   CHKDEN1             ->NO ..KEEP LOOKING
         MVC   MSGDENS,DDEN1
         B     CHKMODL
CHKDEN1  TM    UCBTBYT2,UCBDUDN2   IS IT 1600/6250?
         BNO   CHKDEN2             -> NO
         MVC   MSGDENS,DDEN2
         B     CHKMODL
CHKDEN2  TM    UCBTBYT1,UCBD1600   HOW ABOUT JUST 1600?
         BNO   CHKDEN3             -> NO..ONCE MORE WITH FEELING
         MVC   MSGDENS,DEN1600
         B     CHKMODL
CHKDEN3  TM    UCBTBYT1,UCBD6250   LAST CHANCE..IS IT JUST 6250?
         BNO   CHKMODL             -> NO..DONT KNOW DENSITY..
         MVC   MSGDENS,DEN6250
         EJECT
***
***   USE SYSTEM DEVICE NAME TABLE
***
***
CHKMODL  L     R1,DEVADDR          ADDRESS OF DEVICE NAME TABLE
         L     R10,0(R1)           NUMBER OF ENTRIES
         LA    R1,4(R1)            ADDRESS OF FIRST ENTRY
DEVLP    CLC   UCBTYP(4),8(R1)     IS THIS THE DEVICE TYPE
         BE    DEVFND              YES... BR TO SAVE DEVICE TYPE
         LA    R1,12(R1)           NO... INCR TO NEXT TABLE ENTRY
         BCT   R10,DEVLP           LOOP UNTIL END OF TABLE
         MVC   MSGMODL,=C'????'    SAY WE DONT KNOW DEVICE TYPE
         B     GETWHO
*
DEVFND   MVC   MSGMODL,0(R1)       MOVE UNIT TYPE TO PRINT LINE
*
GETWHO   XR    R10,R10
         L     R10,UCBEXTPT        GET EXTENTION TO UCB
         LTR   R10,R10             ANYTHING THERE?
         BZ    PUTIT               NO->SPLIT
         LA    R10,14(R10)         GET UCBASID ADDRESS
         LH    R10,0(R10)          AND GET THE ASID
         N     R10,=X'0000FFFF'     PLUS MASK OFF RESIDUE..
         LTR   R10,R10             ANY BODY THERE?
         BZ    PUTIT               NO->SIGH,ALL THAT FOR NAUGHT..
*
GOTASID  L     R4,CVTPTR           GET ADDR OF CVT
         USING CVT,R4               AND ADDRESS YOURSELF TO IT..
         L     R4,CVTASVT          GET ADDR OF ASVT..
         DROP  R4
         USING ASVT,R4             AND ESTAB ADDRESSABILITY
         SLL   R10,2               ADJUST ASID..TO USE AS INDEX
         L     R10,ASVTFRST(R10)   R10=ADDR(ASCB) FOR ASID
         DROP  R4
         USING ASCB,R10            GOTCHA
         L     R4,ASCBJBNI         IS IT A JOB
         LTR   R4,R4
         BZ    ITSTSO
         MVC   MSGUSER,0(R4)       MOVIN JOBNAME
         B     PUTIT
ITSTSO   L     R4,ASCBJBNS         ITS START/MOUNT/LOGON
         LTR   R4,R4
         BZ    PUTIT
         MVC   MSGUSER,0(R4)       MOVEIN USERID
PUTIT    DS    0H
         BAL   LINKREG,PUTMSG      ISSUE MESSAGE
         SPACE
INDEX    LA    R7,2(R7)            NEXT ENTRY ADDR.
         B     CYCLE               CONTINUE
         SPACE 2
RLSE     DELETE EP=DEVNAMET         GET RID OF DEVICE TABLE *HMD 08/81*
         TSOLEAVE ISN=LTORG                                 *HMD 08/81*
         EJECT
*.....................................................................*
*        LOCAL SUBROUTINE FOR PUTLINE I/O                             *
*.....................................................................*
         SPACE
*
*
*        BAL  LINKREG,PUTMSG
*
*  USES R14 FOR WORK
*
*
PUTMSG   DS    0H
         MVC   MSGLEN,=H'84'       GET LENGTH OF WTO MESSAGE
         MVC   MSGOFF,=H'00'       OFFSET IS ZEROS          *HMD 08/81*
         PUTLINE PARM=PTPB,OUTPUT=(MSGLEN,TERM,SINGLE,DATA),MF=(E,IOPL)
         BR    LINKREG
         EJECT
*.....................................................................*
*        LIST FORMS OF MACROS                                         *
*.....................................................................*
         SPACE 2
EXTRACT  EXTRACT *-*,'S',FIELDS=(TSO),MF=L
EXTRACTL EQU   *-EXTRACT
PTPB     PUTLINE MF=L                                       *HMD 08/81*
         EJECT
*.....................................................................*
*        PROGRAM CONSTANTS/LITERALS                                   *
*.....................................................................*
         SPACE 2
FMTMSG1  DC    CL80'UNIT  MODEL    DENSITY  VOLUME  USER   STATUS'
         SPACE 2
VARY     DC    CL4'VARY'
DDEN1    DC    CL9'800/1600'
DDEN2    DC    CL9'1600/6250'
DEN800   DC    CL9'800'
DEN1600  DC    CL9'1600'
DEN6250  DC    CL9'6250'
         LTORG                                              *HMD 08/81*
*
         EJECT
*
* PROGRAM  E Q U A T E S
*
R0       EQU   0                   MACROS-WORK
R1       EQU   1                   MACROS-WORK
R2       EQU   2
R3       EQU   3
R4       EQU   4                   WORK/ASVT
R5       EQU   5
R6       EQU   6                   LINKREG FOR PUTGET
LINKREG  EQU   6                   LINK REGISTER
R7       EQU   7                   UCB LOOKUP TABLE
R8       EQU   8                   UCB ADDRESSABILITY
R9       EQU   9
R10      EQU   10                  WORK/ASCB
R11      EQU   11                  WORK AREA ADDRESSABILITY
R12      EQU   12                  PROGRAM BASE REGISTER
R13      EQU   13                  SAVE AREA POINTER
R14      EQU   14                  MACROS - WORK  RETURN ADDRESS
R15      EQU   15                  MACROS - WORK/ RETURN CODE
*
         EJECT
*
* DYNAMIC WORK AREA FOR PROGRAM
*
TSODSECT DSECT             RESUME PREVIOUS DSECT            *HMD 08/81*
CORE     DS    0F
         DS    18F
DEVADDR  DS    F
MSGLEN   DS    H           THIS MUST APPEAR RIGHT BEFORE    *HMD 08/81*
MSGOFF   DS    H           BUFFER LABEL...DO NOT REPOSITION *HMD 08/81*
BUFFER   DS    CL80            ** SEQUENCE DEPENDANT **     *HMD 08/81*
         ORG   BUFFER                                       *HMD 08/81*
         DS    CL1
MSGUNIT  DS    CL3
         DS    CL1
MSGMODL  DS    CL8
         DS    CL1
MSGDENS  DS    CL9
         DS    CL1
MSGVOL   DS    CL6
         DS    CL1
MSGUSER  DS    CL8
         DS    CL1
MSGSTAT  DS    C
*
         ORG   ,                                            *HMD 08/81*
MSGSTATL EQU   *-MSGSTAT                                    *HMD 08/81*
*
LCT      DS    H                   TO REMEMBER COUNT
LMAX     DS    H                   TO REMEMBER UCB MAX ADDR
LIST     DS    500H                TO REMEMBER EACH UCB ADDR
CORENEED EQU   *-CORE
         EJECT
CVT      DSECT
         CVT   LIST=YES,DSECT=YES
         EJECT
UCBSECT  DSECT
         IEFUCBOB LIST=YES
         EJECT
         IHAASCB
         EJECT
         IHAASVT
         END
