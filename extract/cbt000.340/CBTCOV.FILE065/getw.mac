GETW     TITLE 'SUBROUTINE WHICH MOVES WORDS'
*
*      THIS PROGRAM MOVES A WORD STRING FROM A SOURCE FIELD TO A
* TARGET FIELD. THE CALLER HAS THE OPTION OF PASSING THE ADDRESS OF
* THE SOURCE FIELD OR PASSING THE SOURCE FIELD ITSELF. THE NUMBER OF
* WORDS TO BE MOVED MUST BE SPECIFIED AS POSITIVE OR 12 IS RETURNED
* IN REGISTER 15. REGISTER 15 CONTAINS 0 IF THE MOVE WAS SUCCESSFUL.
* THE CALLING SEQUENCE FOR "GETW" IS AS FOLLOWS:
*           1. THE SOURCE FIELD OR SOURCE FIELD ADDRESS
*           2. THE OPTION CHOSEN TO SPECIFY THE SOURCE FIELD
*                  (4-BYTE BINARY)
*                  IF EQUAL TO 1, THE SOURCE FIELD IS SPECIFIED
*                  OTHERWISE, THE SOURCE FIELD ADDRESS IS SPECIFIED
*           3. THE TARGET FIELD
*           4. NUMBER OF WORDS TO BE MOVED (4-BYTE BINARY)
*
*     THE "GETW" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
GETW     ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(72,0)
         REGISTER                  REGISTER EQUATES
         LM    R4,R7,0(R2)         LOAD PARAMETER POINTERS
         L     R5,0(,R5)           LOAD OPTION CODE
         C     R5,=F'1'            IF OPTION CODE IS 1
         BE    CHKLEN                 THEN GO TEST LENGTH OF MOVE
         L     R4,0(,R4)              ELSE POINT TO SOURCE FIELD
CHKLEN   DS     0H
         L     R7,0(,R7)           LOAD NUMBER OF WORDS TO BE MOVED
         LTR   R7,R7               IF LENGTH OF MOVE IS NOT POSITIVE
         BNP   ERROR                  THEN FLAG AS ERROR
LOOP     DS    0H
         MVC   0(4,R6),0(R4)       MOVE A WORD
         LA    R4,4(,R4)           INCREMENT TO NEXT WORD
         LA    R6,0(,R6)           INCREMENT TO NEXT WORD
         BCT   R7,LOOP             CONTINUE MOVING UNTIL DONE
         LA    R15,0               SET SUCCESSFUL RETURN CODE
         B     EXIT                   AND LEAVE
ERROR    DS    0H
         LA    R15,12              SET RETURN CODE FOR ERROR CONDITION
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(72,0)
         END
