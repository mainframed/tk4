         MACRO
&NAME    GENOLD &LINE,&NEXT=0
         LCLA  &A,&B
&A       SETA  N'&LINE
         AIF   (&A GE 1).OK1
         MNOTE 8,'LINE(S) MUST BE SPECIFIED, MACRO EXIT'
         MEXIT
.OK1     ANOP
&NAME    DC    A(&NEXT) .               NEXT OLD
         DC    F'&A' .                  COUNT OF LINES
&B       SETA  1
.L1      DC    A(&LINE(&B)) .           SEGMENT ADDRESS
&B       SETA  &B+1
         AIF   (&B LE &A).L1
         SPACE 1
         MEND
DERR     TITLE 'DAIRERR - DAIR ERROR MESSAGE DISPLAY ROUTINE'
***********************************************************************
*                                                                     *
*        INPUT - R1 -> DAPL                                           *
*                DAIR RETURN CODE (R15) IN FIRST BYTE OF DAPB         *
*                R13-> OS SAVE AREA                                   *
*        OUTPUT- PUTLINE INDICATION OF ERROR(S)                       *
*                RETURN CODE IS ALWAYS ZERO                           *
*                                                                     *
*                                                                     *
*        WRITTEN      OCT. 1973                                       *
*        BY           BRUCE M. DANE - SPNB                            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        SYSTEM FACILITIES USED:                                      *
*              GETMAIN/FREEMAIN                                       *
*              PUTLINE (IKJPUTL)                                      *
*                                                                     *
*        MACROS USED:                                                 *
*              SAVE/RETURN                                            *
*              GETMAIN/FREEMAIN                                       *
*              PUTLINE                                                *
*              IOPL                                                   *
*              IKJDAPL                                                *
*              REGS                                                   *
*              LUSE/LDROP/INUSE                                       *
*              IKJDAP08/IKJDAP0C/IKJDAP10/IKJDAP18                    *
*              IKJDAP1C/IKJDAP24/IKJDAP30                             *
*                                                                     *
*        ATTRIBUTES -                                                 *
*        RE-ENTRANT, REUSABLE, REFRESHABLE                            *
*        PROBLEM STATE, PROBLEM KEY, ENABLED                          *
*                                                                     *
*                                                                     *
*        THIS MODULE IS COMPATIBLE WITH TSO OS/MVT 21.7 OR LESS       *
*                                                                     *
***********************************************************************
         EJECT
DAIRERR  CSECT
         REGS
         EJECT
         LUSE  DAIRERR,R12              TELL ASSEMBLER
         SAVE  (14,12),T,*              LEAVE NAME IN
         LR    R12,R15                  GET EP REG
         LR    R2,R1                    SAVE PARM POINTER
         LUSE  DAPL,R2                  TELL ASSEMBLER
         GETMAIN R,LV=WORKLEN           GET WORK CORE
         ST    R1,8(,R13)               CHAIN
         ST    R13,4(,R1)               SAVE
         LR    R13,R1                   AREAS
         LUSE  WORKAREA,R13             TELL ASSEMBLER
         XC    OLDLIST(28),OLDLIST      CLEAR OLDLIST
         MVC   IOPLUPT(12),DAPL         COPY IN NECESSARY THINGS
         L     R11,DAPLDAPB             GET PARM BLOCK
         LDROP R2                       DROP IT
         MVC   IOPB(PUTL),PUTLMFL       COPY IN MF=L FORM OF PUTLINE
         LA    R0,IOPB                  FINISH LIST
         ST    R0,IOPLIOPB              SAVE IT
         XR    R15,R15                  CLEAR REGISTER
         LA    R6,1                     A CONSTANT USED OFTEN
         IC    R15,1(,R11)              GET DAIR CODE
         CLI   1(R11),X'30'             CHECK HIGH RANGE
         MVI   FLAG,0                   CLEAR OPTION FLAGS
         CLEAR MEMBER
         CLEAR DDN
         BH    CKR15                    NO GOOD
         B     *+4(R15)                 INTO TABLE
         SPACE 1
         B     CKR15                    CODE=00 (00)
         B     CKR15                    CODE=04 (04)
         B     D08                      CODE=08 (08)
         B     D0C                      CODE=0C (12)
         B     D10                      CODE=10 (16)
         B     CKR15                    CODE=14 (20)
         B     D18                      CODE=18 (24)
         B     D1C                      CODE=1C (28)
         B     CKR15                    CODE=20 (32)
         B     D24                      CODE=24 (36)
         B     CKR15                    CODE=28 (40)
         B     CKR15                    CODE=3C (44)
         B     D30                      CODE=30 (48)
         EJECT
         INUSE
         SPACE 2
CKR15    CLI   0(R11),48                CHECK CODE
         MVC   OLDLIST(OLD1L),OLD1      COPY IN OLD
         BH    CKR15E                   ERROR
         CLI   0(R11),0                 PHONEY?
         BE    RETURN                   FORGET IT
         LA    R15,MSGTAB-4(R15)        GET TABLE ENTRY
         MVC   OLDLIST(4),0(R15)        PUT IT IN LIST
         B     PUTLINE                  CALL ROUTINE
         TITLE 'DAIRERR - ROUTINES TO SET UP MESSAGES'
         LUSE  DAPB08,R11
         INUSE
         SPACE 2
D08      LH    R8,DA08DARC              GET DYN. ALLOC. RC
         L     R9,DA08PDSN              GET DSNAME
         MVC   DDN(8),DA08DDN
         MVC   MEMBER(8),DA08MNM        COPY THEM ACROSS
         MVC   FLAG(1),DA08CTL          SAVE FLAGS
         LA    R10,#NLOC                LOAD MESSAGE ID
         B     DOCOM                    GO TO ROUTINE
         SPACE 3
         LDROP R11
         LUSE  DAPB0C,R11
         INUSE
         SPACE 1
D0C      LH    R8,DA0CDARC              GET CODE
         XR    R9,R9                    NO DSNAME
         MVC   DDN(8),DA0CDDN           COPY IN DDNAME1
         LA    R10,#NCAT                MESSAGE ADDRESS
         B     DOCOM                    GO TO ROUTINE
         EJECT
         LDROP R11
         LUSE  DAPB10,R11
         INUSE
         SPACE 2
D10      LH    R8,DA10DARC              GET CODE
         XR    R9,R9                    CLEAR DSNAME
         MVC   DDN(8),DA10DDN           GET DDNAME
         LA    R10,#NDCAT               GET MESSAGE
         B     DOCOM
         SPACE 3
         LDROP R11
         LUSE  DAPB18,R11
         INUSE
         SPACE 2
D18      LH    R8,DA18DARC              GET CODE
         L     R9,DA18PDSN              GET DSNAME ADDR
         MVC   DDN(8),DA18DDN           AND DDNAME
         MVC   MEMBER(8),DA18MNM        AND MEMBER NAME
         LA    R10,#NUNLOC              AND MESSAGE
         B     DOCOM                    CALL ROUTINE
         EJECT
         LDROP R11
         LUSE  DAPB1C,R11
         INUSE
         SPACE 2
D1C      LH    R8,DA1CDARC              GET CODE
         XR    R9,R9                    NO DSNAME
         LA    R10,#TERM                MESSAGE
         MVC   DDN(8),DA1CDDN           DDNAME
         B     DOCOM
         SPACE 3
         LDROP R11
         LUSE  DAPB24,R11
         INUSE
         SPACE 2
D24      LH    R8,DA24DARC              GET RETURN CODE
         L     R9,DA24PDSN              GET DSNAME
         MVC   DDN(8),DA24DDN           GET DDNAME
         MVC   MEMBER(8),DA24MNM
         MVC   FLAG(1),DA24CTL          COPY FLAG
         TM    DA24FLG,X'08'            IS DS=DUMMY?
         LA    R10,#NLOC                MESSAGE
         BZ    DOCOM                    NOPE
         OI    FLAG,X'04'               FAKE IT
         B     DOCOM
         EJECT
         LDROP R11
         LUSE  DAPB30,R11
         INUSE
         SPACE 2
D30      LH    R8,DA30DARC              GET CODE
         L     R9,DA30PDSN              GET DSNAME
         MVC   DDN(8),DA30DDN           DDNAME
         MVC   FLAG(1),DA30CTL          AND CONTROL
         LA    R10,#NLOC                MESSAGE
         B     DOCOM                    CALL IT
         TITLE 'DAIRERR - COMMON ERROR MESSAGE ROUTINE'
DOCOM    LTR   R9,R9                    TEST DSNLOC
         MVI   OLDLIST+7,3
         BNZ   DOCOM1                   OK
DOCOMN   TM    FLAG,X'04'               DUMMY
         BNO   DOCOM3                   NOPE
         LA    R9,NULLFILE              OK
         MVI   MEMBER,C' '              NO MEMBER ON NULLFILE
         B     DOCOM2                   AROUND GARBAGE
DOCOM1   CLI   1(R9),0                  NULL LENGTH?
         BE    DOCOMN                   CONTINUE
         B     DOCOM2                   GO TO ROUTINE
DOCOM3   CLI   MEMBER,C' '              ANY SPECIFIED?
         BNH   GENFILE                  FORGET IT
         XR    R14,R14                  ZERO LENGTH NAME
         B     DOCOMM                   GO TO ROUTINE
         SPACE 2
DOCOM2   LH    R14,0(,R9)               GET LENGTH
         MVC   LINE1+4(0),2(R9)         <<EXECUTED>>
         EX    R14,*-6                  DO IT
         SPACE 1
DOCOMM   LA    R0,#DSN                  GET MESSAGE
         ST    R0,OLDLIST+8
         LA    R0,LINE1                 GET PLACE
         ST    R0,OLDLIST+12            AND STORE IT
         ST    R10,OLDLIST+16           MOVE IT IN
         XC    LINE1(4),LINE1           CLEAR HEADER
         LA    R2,LINE1+4               PLACE
         AR    R2,R14                   POINT AT END
         CLI   MEMBER,C' '              CHECK FOR ANY
         BNH   DS2                      NOPE
         MVI   0(R2),C'('               LEFT PAREN
         AR    R14,R6                   +1
         AR    R2,R6                    SAME
         LA    R3,8                     MAX COUNT
         LA    R1,MEMBER                AND PLACE
         SPACE 1
LX2      MVC   0(1,R2),0(R1)            COPY IT
         AR    R2,R6
         AR    R1,R6                    INCREMENT BY ONE
         AR    R14,R6                   INCREMENT COUNT
         CLI   0(R1),C' '               CHECK IT
         BH    LX2                      OK
         MVI   0(R2),C')'               CLOSE PAREN
         AR    R14,R6                   INCREMENT
DS2      LA    R14,4(,R14)              HEADER LENGTH
         STH   R14,LINE1                LENGTH
         MVI   LINE1+3,10               OFFSET
         B     GETMSG
         EJECT
         INUSE
         SPACE 2
GENFILE  LA    R0,#DDN                  GET MESSAGE
         CLI   DDN,C' '                 CHECK IT
         BH    GENFILE1                 OK
         CLI   1(R11),X'30'             SYSOUT?
         BE    GENSOUT                  YES
         MVC   DDN(8),=CL8'* UNKN *'
         B     GENFILE1
GENSOUT  LA    R9,FORSOUT
         B     DOCOM2
GENFILE1 ST    R0,OLDLIST+8             AND PLACE
         XC    LINE1(4),LINE1           CLEAR IT
         LA    R0,LINE1                 PLACE
         ST    R0,OLDLIST+12            PLACE
         MVC   LINE2(50),0(R10)         COPY IN LINE
         MVI   LINE2+3,6                OFFSET
         LA    R0,LINE2                 PLACE
         ST    R0,OLDLIST+16            OFFSET
         MVI   LINE1+3,6
         MVI   LINE1+1,12               LEN=8,OFFSET=6
         MVC   LINE1+4(8),DDN           COPY IN DDNAME
         EJECT
         INUSE
         SPACE 1
GETMSG   STH   R8,CODE                  SAVE IT
         OC    CODE(2),CODE             CHECK IT
         BNZ   LOADMSG                  OK
         XR    R15,R15                  CLEAR IT
         CLI   0(R11),48                CZECH IT
         BH    CKR15E
         CLI   0(R11),0                 NULL
         BE    RETURN
         IC    R15,0(R11)               GET IT
         LA    R15,MSGTAB-4(R15)
         MVC   OLDLIST(4),0(R15)        SAVE IT
         B     PUTLINE
         SPACE 1
CKR15E   LA    R15,#RCE                 ERROR CODE
         ST    R15,OLDLIST              SAVE IT
         B     PUTLINE                  ROUTINE
         SPACE 3
LOADMSG  LOAD  EPLOC=DAIRMSGS
         LR    R3,R0                    SAVE EPA
         SPACE 1
FIND     LH    R4,0(,R3)                GET OFFSET
         CLC   CODE(2),2(R3)            SAME?
         BE    HAVE                     YES
         LTR   R4,R4                    TEST FOR MORE
         BNP   NOFIND                   DONE
         AR    R3,R4                    INCREMENT TO NEXT
         B     FIND                     TRY AGAIN
         SPACE 2
HAVE     LA    R0,4(,R3)                LOAD POINTER
         ST    R0,OLDLIST               PUT IT IN
         DELETE EPLOC=DAIRMSGS          DELETE IT
         B     PUTLINE
         SPACE 1
NOFIND   DELETE EPLOC=DAIRMSGS          UNLOAD IT
         B     CKR15E                   DO ERROR THING
         TITLE 'DAIRERR - PUTLINE AND TERMINATE'
PUTLINE  L     R3,OLDLIST               GET CHAIN
         L     R15,0(,R3)               CHECK FOR THIRD MESSAGE
         LTR   R15,R15                  IS THERE ONE
         BNZ   PUTLCTL                  YES
         MVI   OLDLIST+7,4              FOUR SEGMENTS
         L     R15,OLDLIST+16           OFFSET OF 3RD SEG
         MVC   LINE3(4),0(R15)          HEADER
         MVI   LINE3+1,5                LENGTH
         MVI   LINE3+4,C'+'             INDICATOR
         LA    R3,OLDLIST               MAKE IT POINT RIGHT
         LA    R0,LINE3                 POSITION
         ST    R0,OLDLIST+20            IN PLACE
         B     PUTLINE2
         SPACE 1
PUTLCTL  XC    OLDLIST(4),OLDLIST       CLEAR IT
         PUTLINE PARM=IOPB,MF=(E,IOPL),                                +
               OUTPUT=(OLDLIST,TERM,MULTLVL,INFOR),                    +
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK)
PUTLINE2 PUTLINE PARM=IOPB,MF=(E,IOPL),                                +
               OUTPUT=((R3),TERM,MULTLVL,INFOR),                       +
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK)
         SPACE 2
RETURN   LR    R1,R13                   GET IT
         L     R13,4(,R13)              PREV. SA
         FREEMAIN R,LV=WORKLEN,A=(1)    FREE IT
         RETURN (14,12),,RC=0
         TITLE 'DAIRERR - CONSTANTS AND DSECTS'
         IKJDAPL
         EJECT
         IKJDAP08
         EJECT
         IKJDAP0C
DA0CDDN  DC    CL8' '
         EJECT
         IKJDAP10
         EJECT
         IKJDAP18
         EJECT
         IKJDAP1C
         EJECT
         IKJDAP24
         EJECT
         IKJDAP30
         EJECT
WORKAREA DSECT
SAVE1    DC    18XL4'00'
OLDLIST  DC    7XL4'00'
         IOPL
IOPB     PUTLINE MF=L
LINE1    DC    2H'0',CL80' '
LINE2    DC    2H'0',CL80' '
LINE3    DC    2H'0',CL8' '
DDN      DC    CL8' '
MEMBER   DC    CL8' '
FLAG     DC    2X'00'
CODE     DC    2X'00'
WORKLEN  EQU   *-WORKAREA
         EJECT
DAIRERR  CSECT ,                        RE-EST.
OLD1     GENOLD #LINE1
OLD1L    EQU   *-OLD1
#LINE1   TSOMSG ' DYNAMIC ALLOCATION FAILURE'
         SPACE 2
MSGTAB   DC    A(#RC4,#RC8,#RC12,#RC16,#RC20,#RC24,#RC28)
         DC    A(#RC32,#RC36,#RC40,#RC44,#RC48)
         SPACE 1
#RC4     GENOLD #RC4A
#RC4A    TSOMSG ' INVALID PARAMETER LIST OR DATA SET NAME'
#RC8     GENOLD #RC8A
#RC8A   TSOMSG ' AN ERROR HAS OCCURRED IN A CATALOG MANAGEMENT ROUTINE'
#RC12    GENOLD #RC12A
#RC12A   TSOMSG ' AN ERROR HAS OCCURRED IN DYNAMIC ALLOCATION'
#RC16    GENOLD #RC16A,NEXT=#RC16B
#RC16B   GENOLD #RC16C
#RC16A   TSOMSG ' THERE ARE NO UNALLOCATED DD CARDS+'
#RC16C   TSOMSG ' USE ''FREE'' TO FREE UNNEEDED FILES'
DAIRMSGS DC    CL8'DAIRMSGS'
NULLFILE DC    H'10',CL10'''NULLFILE'''
#RC20    GENOLD #RC20A,NEXT=#RC16B
#RC20A   TSOMSG ' THE REQUESTED FILE IS ALREADY IN USE+'
#RC24    GENOLD #RC24A,NEXT=#RC16B
#RC24A   TSOMSG ' THE DATA SET REQUESTED IS CONCATENATED+'
#RC28    GENOLD #RC28A
#RC28A   TSOMSG ' THE FILE OR DATA SET REQUESTED IS NOT ALLOCATED'
#RC32    GENOLD #RC32A,NEXT=#RC16B
#RC32A   TSOMSG ' THE DATA SET IS ALREADY ALLOCATED+'
#RC36    GENOLD #RC36A
#RC36A   TSOMSG ' AN ERROR HAS OCCURRED IN A CATALOG INFO. ROUTINE'
#RC40    GENOLD #RC40A
#RC40A   TSOMSG ' QUALIFIER SPACE EXHAUSTED, TRY A DIFFERENT NAME'
#RC44    GENOLD #RC44A
#RC44A  TSOMSG ' DATA SET TO BE ACCESSED HAS DISP=DELETE, VOLID NEEDED'
#RC48    GENOLD #RC48A,NEXT=CALLOLD
#RC48A   TSOMSG ' PROGRAM CHECK OR ABEND IN DYNAMIC ALLOCATION+'
CALLOLD  GENOLD CALLTSD
CALLTSD  TSOMSG ' CALL TECHNICAL SUPPORT FOR ASSISTANCE'
         EJECT
PUTLMFL  PUTLINE MF=L
PUTL     EQU   *-PUTLMFL
#RCE     GENOLD #RCE1,NEXT=CALLOLD
#RCE1    TSOMSG ' UNKNOWN OR INVALID DAIR RETURN CODE(S)+'
         SPACE 2
#DSN     TSOMSG ' DATA SET          '
#DDN     TSOMSG ' FILE              '
#NUNLOC  TSOMSG ' NOT UNALLOCATED',OFFSET=10
#NLOC    TSOMSG ' NOT ALLOCATED',OFFSET=10
#NCAT    TSOMSG ' NOT CONCATENATED',OFFSET=10
#NDCAT   TSOMSG ' NOT DECONCATENATED',OFFSET=10
#TERM    TSOMSG ' NOT ALLOCATED TO TERMINAL',OFFSET=10
FORSOUT  DC    H'10',C'FOR SYSOUT'
         SPACE 2
LTORG    LTORG
         END
