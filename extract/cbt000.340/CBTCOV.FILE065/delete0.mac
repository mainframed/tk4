DELETE0  CSECT
         CNSRV ,,12
         L     1,0(1)             LOAD PARM ADDRESS
         LH    2,0(1)             LOAD LENGTH OF THE PARM
         LTR   2,2                CHECK IF ITS THERE
         BZ    NOPARM             NONE THERE SO SKIP IT
         STH   2,PLENGTH          SAVE THE LENGTH
         LA    1,2(1)             LOAD PAST THE LENGTH
         ST    1,PARMADD          SAVE IT
         OPEN  (SYSPRINT,(OUTPUT))
         GETMAIN R,LV=16000
         SR    2,2                CLEAR
         BCTR  2,0                SET TO -1
         ST    2,0(1)             STORE IN GETMAIN AREA
         ST    2,4(1)             SET AS THE LAST NAME =X'FFFFFF'
         ST    1,GETMAIN          SAVE POINTER TO GETMAIN AREA
         LA    1,8(1)             BUMP TO NEXT SLOT
         ST    1,LIST             SAVE FOR NEXT TIME
NEXTREAD CALL  SEQDIR,(ADDR,LEN,NAME),VL
         L     1,ADDR             POINTER TO NAME
         LTR   1,1                ANY NAME THERE
         BZ    ENDNAME            NO,,LAST ONE IS OUT
         L     2,LIST             PICK UP AVAIL SLOT
         CLC   0(4,1),=X'FFFFFFFF'     LAST NAME OF DIR
         BE    ENDNAME            YES,,GET OUT
         MVC   0(8,2),0(1)        MOVE MEMBER ANEM IN
         LA    2,8(2)             BUMP TO NEXT SLOT
         ST    2,LIST             SAVE
         L     2,COUNT            GET LAST COUNT
         LA    2,1(2)             BUMP BY ONE
         ST    2,COUNT            SAVE AGAIN
         C     2,TOTMEM           COMPARE TO MAX
         BE    ENDNAME            CANNOT GO ON THIS WAY
         B     NEXTREAD           BACK UP IF ITS OK WITH YOU
ENDNAME  L     2,COUNT            GET COUNT AGAIN
         CVD   2,WORK             INTO DECIMAL
         OI    WORK+7,X'0F'       MAKE PRINTABLE
         UNPK  UNPK(8),WORK(8)    UNPACK CHARACTERS
         MVC   MEMMSG(4),UNPK+4   MEMBER COUNT TO MESSAGE
         MVC   OUTLINE(L'MEMMSG),MEMMSG
         PUT   SYSPRINT,OUTPUT    PUT OUT MEMBER COUNT
         MVI   OUTPUT,X'40'       RESET THE C.C.
         MVC   OUTLINE(121),OUTPUT     CLEAR LINE
         L     1,PARMADD          RELOAD THE PARM ADDRESS
         CLC   0(4,1),=C'@ALL'    IS IT THE ALL CODE
         BNE   NOTALL             NO
         CLC   PLENGTH(2),=H'4'   YES,,BUT MAKE SURE ITS ONLY 4 CHTRS
         BNE   NOTALL             NOPE,,USE AS STRING
         OI    ALLNOP+1,X'F0'     SET COMPARE NOP,,ALL MEMBERS TO BE SC
NOTALL   OPEN  (PDS,(OUTPUT))     OPEN THE DATASET CONCERNED
         LH    3,PLENGTH          LOAD THE LENGTH OF THE PARM
         BCTR  3,0                DROP FOR EXECUTE
         L     4,PARMADD          LOAD WHERE CHARACTERS RESIDE
DLOOP    L     2,LIST             LOAD THE NEXT AVAIL SLOT ENTRY
         SH    2,=H'8'            BACK OFF TO LAST NAME
         ST    2,LIST             STORE ADDRESS BACK FOR NEXT TIME
         MVC   MEMBER(8),0(2)     MOVE MEMBER NAME IN
         CLC   MEMBER(4),=X'FFFFFFFF'  IS IT THE LAST ONE
         BE    LAST               YES,,GET OUT
ALLNOP   NOP   STOW               OVER COMPARE IF '@ALL'
         EX    3,MEMCLC           COMPARE STRING LENGTH
         BNE   DLOOP              NOT GOOD ENOUGH,,GET NEXT ONE
STOW     STOW  PDS,MEMBER,D       DELETE MEMBER NAME
         LTR   15,15              TEST RETURN CODE
         BZ    MSGD               NONE,,GIVE DELETE MESSAGE
         MVC   IOMSG+28(8),MEMBER SET MEMBER NAME INTO MESSAGE
         MVC   OUTLINE(L'IOMSG),IOMSG  MOVE MSG TO OUT AREA
         PUT   SYSPRINT,OUTPUT    GIVE I/O ERROR MESSAGE
         ABEND 1,DUMP             NOW ABEND FOR DUMP
MSGD     MVC   OUTLINE(L'MEMBER),MEMBER
         PUT   SYSPRINT,OUTPUT    GIVE MEMBER DELETED MSG
         B     DLOOP              NOW BACK FOR MORE
LAST     L     5,GETMAIN          ADDRESS OF GOTTEN CORE
         FREEMAIN R,LV=16000,A=(5)
         CLOSE (SYSPRINT)
         CLOSE (PDS)
         LINK  EP=IEBCOPY,PARAM=(ZERO,DDLIST),VL=1
         B     END
NOPARM   WTO   'DELETE PROGRAM REQUIRES PARM LIST',ROUTCDE=11
END      GOBCK
WORK     DS    D
UNPK     DS    D
PARMADD  DC    F'0'
LIST     DC    F'0'
ADDR     DC    2F'0'
LEN      DC    F'0'
NAME     DC    256C' '
COUNT    DC    F'0'
TOTMEM   DC    F'1999'
PLENGTH  DC    H'0'
MEMMSG   DC    C'XXXX MEMBERS IN DATASET'
OUTPUT   DC    C'1'
OUTLINE  DC    CL121' '
MEMBER   DC    C'XXXXXXXX SCRATCHED'
IOMSG    DC    C'I/O ERROR SCRATCHING MEMBER XXXXXXXX'
ZERO     DC    H'0'
DDLIST   DC    H'88'
         DC    BL56'0'
         DC    CL8'PDS'
         DC    CL8'PDS'
         DC    BL16'0'
GETMAIN  DC    F'0'
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,LRECL=121,BLKSIZE=121,         X
               MACRF=(PM),RECFM=FA
PDS      DCB   DDNAME=PDS,DSORG=PO,MACRF=(R,W)
MEMCLC   CLC   MEMBER(0),0(4)     ******EXECUTED********
         END
