ICFLIST2  CSECT
*/*****************************************************************/
*/*                                                               */
*/*  THIS PROGRAM LISTS DATASET INFO OUT OF ICF CATALOGS          */
*/*  IT USES THE DATASET PRODUCED BY ICFLIST1                     */
*/*  NOTE : THIS DOES NOT PROVIDE SPACE/STATISTICS INFORMATION.   */
*/*  PROGRAM ICFSTATS WILL DO THAT TASK.                          */
*/*  PROGRAM DODATE SIMPLY RETURNS THE CURRENT DATE AND TIME      */
*/*  IN THE FORMAT MMM DD HH:MM:SS                                */
*/*                                                               */
*/*  (PROGRAM DODATE NOW INCLUDED AT END OF THIS CODE)            */
*/*                                                               */
*/*  NOTE : A VOLUME SERIAL NUMBER OF ****** MEANS THAT           */
*/*         SUPERLOCATE WILL TAKE THIS TO BE THE IPL VOLUME.      */
*/*         SEE COMMENTS IN PROGRAM ICFLIST1                      */
*/*                                                               */
*/*                                                               */
*/*                                                               */
*/*****************************************************************/
         PRINT NOGEN
         STM   14,12,12(13) SAVE REGS
         BALR  2,0
         USING BASE,2,3
BASE     EQU   *
         L     3,BASE2
         LA    11,SAVE             OUR SAVE AREA                    JMH
         ST    13,SAVE+4
         ST    11,8(13)            STANDARD IBM LINK CONVENTIONS
         LA    13,SAVE
         B     OVERSAVE
         DS     0D
SAVE     DS    18F                 SAVE AREA
BASE2    DC    A(BASE+4096)        SET UP ADDRESSABILITY 2ND BASE REG
         LTORG
OVERSAVE EQU   *
         OPEN  (DCBIN,)            OUR RAW INPUT DATASET
         OPEN  (DCBOUT,(OUTPUT))
         ZAP   PAGENR,=P'0'
         ZAP   LINENR,=P'0'
         LA    1,PARMS
         L     15,=V(DODATE)
         BALR  14,15
         GET   DCBIN            WANT OUR IST REC
         LR    7,1
         MVC   OLDKAT(20),96(7)    OLD KAT NAME---MORE THAN 1 KAT
         MVC   NEWKAT(20),OLDKAT
         MVC   TEXT1A(20),96(7)
LUPE1    CLI   6(7),C'U'         CHECK FOR TYPE OF RECORD
         BE    UCAT              USERCATALOG
         CLI   6(7),C'N'
         BE    NONVSAM           NON VSAM DATASET (INC CVOL)
         CLI   6(7),C'C'
         BE    VSAM              VSAM DATASET (DATA,INDEX,CLUSTER)
         CLI   6(7),C'A'
         BE    ALIAS             ALIAS ENTRY
         CLI   6(7),C'G'         THIS IS A GDG ENTRY
         BE    GDG               ALIAS ENTRY
         B     GETNEXT           GET NEXT RECORD
UCAT     EQU   *
         CLI   USW,X'FF'      THIS IS NEEDED FOR NEXT ICFCAT AFTER
         BE    PUTUCAT        USERCATALOG DETAIL LINES
         BAL   11,UCATHEAD    UCAT HEADING LINES
         MVI   USW,X'FF'
         B     PUTUCAT
NONVSAM  CLI   NSW,X'FF'      AS ABOVE
         BE    PUTNVSAM
         BAL   11,NVSMHEAD    NON VSAM HEADING LINES
         MVI   NSW,X'FF'
         B     PUTNVSAM       NON VSAM DETAIL LINES
VSAM     CLI   VSW,X'FF'
         BE    PUTVSAM        VSAM DETAIL LINES
         BAL   11,VSAMHEAD    VSAM HEADING LINES
         MVI   VSW,X'FF'
         B     PUTVSAM        VSAM DETAIL LINES
ALIAS    EQU   *
         CLI   ASW,X'FF'
         BE    PUTALIAS       ALIAS DETAIL LINES
         BAL   11,ALIASHDS
         MVI   ASW,X'FF'      ALIAS HEADING LINES
         B     PUTALIAS
GDG      EQU   *
         CLI   GSW,X'FF'
         BE    PUTGDG         GDG DETAIL LINES
         BAL   11,GDGHEAD
         MVI   GSW,X'FF'      ALIAS HEADING LINES
         B     PUTGDG
UCATHEAD MVI   ASACHAR,C'1'              USERCATALOG HEADING LINES
         MVC   LINE+30(23),TEXT1
         MVC   LINE+55(20),TEXT1A
         MVC   LINE+91(20),TEXT2
         MVC   LINE+113(5),TEXT3
         AP    PAGENR,=P'1'
         MVC   TEMP(6),=X'402020202120'
         ED    TEMP(6),PAGENR
         MVC   LINE+119(2),TEMP+4
         BAL   9,PUTIT
         MVI   ASACHAR,C'0'
         MVC   LINE+30(38),TEXT4
         BAL   9,PUTIT
         MVC   LINE(19),TEXT5
         BAL   9,PUTIT
         MVI   ASACHAR,C' '
         MVC   LINE(19),TEXT6
         BAL   9,PUTIT
         MVI   ASACHAR,C'0'
         ZAP   LINENR,=P'7'
         BR    11
NVSMHEAD MVI   ASACHAR,C'1'          NON VSAM HEADING LINES
         MVC   LINE+30(23),TEXT1
         MVC   LINE+55(20),TEXT1A
         MVC   LINE+91(20),TEXT2
         MVC   LINE+113(5),TEXT3
         AP    PAGENR,=P'1'
         MVC   TEMP(6),=X'402020202120'
         ED    TEMP(6),PAGENR
         MVC   LINE+119(2),TEMP+4
         BAL   9,PUTIT
         MVI   ASACHAR,C'0'
         MVC   LINE+30(38),TEXT7
         BAL   9,PUTIT
         MVC   LINE(19),TEXT8
         BAL   9,PUTIT
         MVI   ASACHAR,C' '
         MVC   LINE(19),TEXT6
         BAL   9,PUTIT
         MVI   ASACHAR,C'0'
         ZAP   LINENR,=P'7'
         BR    11
GDGHEAD  MVI   ASACHAR,C'1'          GDG HEADING LINES
         MVC   LINE+30(23),TEXT1
         MVC   LINE+55(20),TEXT1A
         MVC   LINE+91(20),TEXT2
         MVC   LINE+113(5),TEXT3
         AP    PAGENR,=P'1'
         MVC   TEMP(6),=X'402020202120'
         ED    TEMP(6),PAGENR
         MVC   LINE+119(2),TEMP+4
         BAL   9,PUTIT
         MVI   ASACHAR,C'0'
         MVC   LINE+30(38),TEXT22
         BAL   9,PUTIT
         MVC   LINE(19),TEXT8
         MVI   ASACHAR,C' '
         MVC   LINE+55(31),TEXT23
         BAL   9,PUTIT
         MVI   ASACHAR,C'0'
         ZAP   LINENR,=P'7'
         BR    11
VSAMHEAD MVI   ASACHAR,C'1'           VSAM HEADING LINES
         MVC   LINE+30(23),TEXT1
         MVC   LINE+55(20),TEXT1A
         MVC   LINE+91(20),TEXT2
         MVC   LINE+113(5),TEXT3
         AP    PAGENR,=P'1'
         MVC   TEMP(6),=X'402020202120'
         ED    TEMP(6),PAGENR
         MVC   LINE+119(2),TEMP+4
         BAL   9,PUTIT
         MVI   ASACHAR,C'0'
         MVC   LINE+30(38),TEXT9
         BAL   9,PUTIT
         MVC   LINE(33),TEXT10
         MVC   LINE+55(25),TEXT11
         BAL   9,PUTIT
         ZAP   LINENR,=P'6'
         BR    11
ALIASHDS MVI   ASACHAR,C'1'           ALIAS HEADING LINES
         MVC   LINE+30(23),TEXT1
         MVC   LINE+55(20),TEXT1A
         MVC   LINE+91(20),TEXT2
         MVC   LINE+113(5),TEXT3
         AP    PAGENR,=P'1'
         MVC   TEMP(6),=X'402020202120'
         ED    TEMP(6),PAGENR
         MVC   LINE+119(2),TEMP+4
         BAL   9,PUTIT
         MVI   ASACHAR,C'0'
         MVC   LINE+30(38),TEXT12
         BAL   9,PUTIT
         MVC   LINE(13),TEXT13
         MVC   LINE+45(31),TEXT14
         ZAP   LINENR,=P'6'
         BAL   9,PUTIT
         BR    11
SUM1HEAD MVI   ASACHAR,C'1'           SUMMARY HEADING FOR LISTING
         MVC   LINE+30(23),TEXT20     OF ALL DATASETS FOUND IN ALL
         MVC   LINE+55(20),TEXT21     CATALOGS IN DATASET SEQUENCE
         MVC   LINE+91(20),TEXT2
         MVC   LINE+113(5),TEXT3
         AP    PAGENR,=P'1'
         MVC   TEMP(6),=X'402020202120'
         ED    TEMP(6),PAGENR
         MVC   LINE+119(2),TEMP+4
         BAL   9,PUTIT
         MVI   ASACHAR,C'0'
         MVC   LINE+30(38),TEXT15
         BAL   9,PUTIT
         MVC   LINE(33),TEXT16
         MVC   LINE+55(19),TEXT17
         MVC   LINE+78(18),TEXT17A
         ZAP   LINENR,=P'6'
         BAL   9,PUTIT
         BR    11
SUM2HEAD MVI   ASACHAR,C'1'           SUMMARY HEADING FOR LISTING
         MVC   LINE+30(23),TEXT20     OF ALL DATASETS FOUND IN ALL
         MVC   LINE+55(20),TEXT21     CATALOGS IN VOLUME SEQUENCE
         MVC   LINE+91(20),TEXT2
         MVC   LINE+113(5),TEXT3
         AP    PAGENR,=P'1'
         MVC   TEMP(6),=X'402020202120'
         ED    TEMP(6),PAGENR
         MVC   LINE+119(2),TEMP+4
         BAL   9,PUTIT
         MVI   ASACHAR,C'0'
         MVC   LINE+30(38),TEXT18
         BAL   9,PUTIT
         MVC   LINE(33),TEXT16
         MVC   LINE+55(19),TEXT17
         MVC   LINE+78(18),TEXT17A
         ZAP   LINENR,=P'6'
         BAL   9,PUTIT
         BR    11
PUTUCAT  CP    LINENR,=P'55'
         BL    SKIP1              DONT NEED NEW PAGE
         BAL   11,UCATHEAD        NEW PAGE WANTED
SKIP1    MVC   LINE(6),0(7)       VOLUME ID VOLUME SEQUENCE
         MVC   LINE+7(44),7(7)    DATASET NAME
         BAL   9,PUTIT            PUT DETAIL LINE (UCAT)
         MVI   ASACHAR,C' '
         AP    LINENR,=P'1'
         B     GETNEXT            NEXT RECORD
PUTNVSAM CP    LINENR,=P'55'
         BL    SKIP2              DONT NEED NEW PAGE
         BAL   11,NVSMHEAD        NEW PAGE WANTED
SKIP2    MVC   LINE(6),0(7)       VOLUME ID VOLUME SEQUENCE
         MVC   LINE+7(44),7(7)    DATASET NAME
         BAL   9,PUTIT            PUT DETAIL LINE (NON VSAM)
         AP    LINENR,=P'1'
         MVI   ASACHAR,C' '
         B     GETNEXT            NEXT RECORD
PUTGDG   CP    LINENR,=P'55'
         BL    SKIP2A             DONT NEED NEW PAGE
         BAL   11,GDGHEAD         NEW PAGE WANTED
SKIP2A   MVC   LINE(6),0(7)       VOLUME ID VOLUME SEQUENCE IF THERE
         MVC   LINE+7(44),7(7)    DATASET NAME
         CLI   51(7),C'G'         DATASET
         BE    SKIP2B
         MVC   LINE+60(3),52(7)   MAX GDGS DEFINED
         MVC   LINE+66(3),55(7)   MAX GDGS USED
SKIP2B   BAL   9,PUTIT            PUT DETAIL LINE
         AP    LINENR,=P'1'
         MVI   ASACHAR,C' '
         B     GETNEXT            NEXT RECORD
PUTVSAM  CP    LINENR,=P'55'
         BL    SKIP3              DONT NEED NEW PAGE
         BAL   11,NVSMHEAD        NEW PAGE WANTED
SKIP3    MVC   LINE(6),0(7)       VOLUME ID VOLUME SEQUENCE
         MVC   LINE+7(44),7(7)    DATASET NAME
         MVC   LINE+52(44),52(7)
         CLI   51(7),C'I'
         BNE   NOTINDEX           NOT THE INDEX COMPONENT
         MVC   LINE+100(13),=C'   INDEX     '
         B     SKIP4
NOTINDEX CLI   51(7),C'D'
         BNE   NOTDATA
         MVC   LINE+100(13),=C'    DATA     '
         B     SKIP4
NOTDATA  MVC   LINE+100(13),=C' UNKNOWN TYPE'
SKIP4    BAL   9,PUTIT            PUT DETAIL LINE (VSAM)
         AP    LINENR,=P'1'
         MVI   ASACHAR,C' '
         B     GETNEXT            NEXT RECORD
PUTALIAS CP    LINENR,=P'55'
         BL    SKIP5              DONT NEED NEW PAGE
         BAL   11,ALIASHDS        NEW PAGE WANTED
SKIP5    EQU   *
         MVC   LINE+7(44),7(7)    DATASET NAME
         MVC   LINE+52(44),52(7)  CATALOG ALIAS IS DEFINED IN
         BAL   9,PUTIT            PUT DETAIL LINE (NON VSAM)
         AP    LINENR,=P'1'
         MVI   ASACHAR,C' '
         CLI   DCB3,X'FF'         ARE WE DOING FINAL SUMMARY
         BE    LUPE4
         B     GETNEXT            NEXT RECORD
GETNEXT  GET   DCBIN        GET OUR NEXT INPUT RECORD
         LR    7,1
         CLC   OLDKAT(20),96(7)
         BE    LUPE1
         MVC   NEWKAT(20),96(7)
         MVC   OLDKAT(20),NEWKAT
         MVI   USW,X'00'
         MVI   NSW,X'00'
         MVI   ASW,X'00'
         MVI   GSW,X'00'
         MVI   VSW,X'00'
         MVC   TEXT1A(20),96(7)     NEW CATALOG HEADING
         B     LUPE1
         LTORG
EOJ1     CLOSE (DCBIN,)           NOW WE ARE GOING TO PROCESS IN
         OPEN  (DCBIN1,)          DATA SET SEQUENCE
GETIT    GET   DCBIN1
         LR    7,1
         CLI   6(7),C'A'     DONT WANT ALIASES THIS TIME
         BE    GETIT
         CLI   51(7),C'B'    DONT WANT GDG BASE EITHER
         BE    GETIT
         BAL   11,SUM1HEAD
LUPE2    CP    LINENR,=P'55'
         BL    SKIP6
         CLI   DCB2,X'FF'
         BE    GOON      CHOOSES VOLUME/DATASET SEQUENCE FOR HEADINGS
         BAL   11,SUM1HEAD
         B     SKIP6
GOON     BAL   11,SUM2HEAD
SKIP6    MVC   LINE(6),0(7)  PUT VOLUME SERIAL NR IN
         CLI   6(7),C'C'     VSAM CLUSTER RECORD
         BNE   NOTKLUS       NO
         MVC   LINE+7(44),52(7) MOVE THE RELATED NAME IN
         CLI   51(7),C'I'   GOT AN INDEX REC
         BNE   NOTIX        NO
         MVC   LINE+52(20),=C'VSAM INDEX COMPONENT'
         B     KONTINUE
NOTIX    MVC   LINE+52(20),=C'VSAM DATA COMPONENT '
         B     KONTINUE
NOTKLUS  MVC   LINE+7(44),7(7)
         CLI   6(7),C'G'         WE HAVE A GDG DATA SET
         BNE   TRYNV
         MVC   LINE+52(20),=C'GDG NON VSAM DATASET'
         B     KONTINUE
TRYNV    CLI   6(7),C'N'         NON VSAM DATASET
         BNE   UKAT              NO
         CLC   7(9,7),=C'SYSCTLG.V'  OS CVOLS ALWAYS START THUS
         BNE   NOTVSAM
         MVC   LINE+52(20),=C'OS CVOL DATASET     '
         B     KONTINUE
NOTVSAM  MVC   LINE+52(20),=C'NON VSAM DATASET    '
         B     KONTINUE
UKAT     MVC   LINE+52(20),=C'VSAM / ICF USERCAT  '
KONTINUE MVC   LINE+78(20),96(7)
         BAL   9,PUTIT
         AP    LINENR,=P'1'
         MVI   ASACHAR,C' '
         CLI   DCB2,X'FF'
         BE    GETNXT2
GETNXT1  GET   DCBIN1
         LR    7,1
         CLI   6(7),C'A'     DONT WANT ALIASES THIS TIME
         BE    GETNXT1       GET ANOTHER RECORD
         CLI   51(7),C'B'    DONT WANT GDG BASE EITHER
         BE    GETNXT1       GET ANOTHER RECORD
         B     LUPE2
PUTIT    PUT   DCBOUT,ASACHAR
         MVI   LINE,C' '
         MVC   LINE+1(131),LINE
         BR    9
EOJ2     CLOSE (DCBIN1,)
         OPEN  (DCBIN2,)
         MVC   TEXT4,TEXT18
GETAGAIN GET   DCBIN2
         LR    7,1
         CLI   6(7),C'A'     DONT WANT ALIASES THIS TIME
         BE    GETAGAIN      GET ANOTHER RECORD
         CLI   51(7),C'B'    DONT WANT GDG BASE EITHER
         BE    GETAGAIN      GET ANOTHER RECORD
         MVI   DCB2,X'FF'
         BAL   11,SUM2HEAD
         B     LUPE2
GETNXT2  GET   DCBIN2
         LR    7,1
         CLI   6(7),C'A'     DONT WANT ALIASES THIS TIME
         BE    GETNXT2       GET ANOTHER RECORD
         CLI   51(7),C'B'    DONT WANT GDG BASE EITHER
         BE    GETNXT2
         B     LUPE2
EOJ3     CLOSE (DCBIN1,,DCBIN2)
         OPEN  (DCBIN3,)
         MVC   TEXT12,TEXT19
         MVC   TEXT1,TEXT20
         MVC   TEXT1A,TEXT21
         MVI   DCB3,X'FF'
         MVI   ASW,X'00'    RESET ALIAS PROCESSING CODE
LUPE4    GET   DCBIN3
         LR    7,1
         CLI   6(7),C'A'
         BNE   LUPE4
         B     ALIAS
EOJ4     CLOSE (DCBOUT,,DCBIN3,)   FINISH PROCESSING
         L     13,SAVE+4
         LM    14,12,12(13)
         XR    15,15
         BR    14            GO BACK TO OS
         DS    0D
ASW      DC    X'00'
GSW      DC    X'00'
NSW      DC    X'00'
VSW      DC    X'00'
USW      DC    X'00'
DCB2     DC    X'00'
DCB3     DC    X'00'
OLDKAT   DC    CL20' '
NEWKAT   DC    CL20' '
ASACHAR  DC    CL1'1'
LINE     DC    CL132' '
TEXT1    DC    CL23'ICF CATALOG LISTING OF '
TEXT1A   DC    CL20'                   '
TEXT2    DC    CL20'                   '
TEXT3    DC    CL05'PAGE '
TEXT4    DC    CL38'U S E R  C A T A L O G  E N T R I E S '
TEXT5    DC    CL19'VOLUME CATALOG NAME'
TEXT6    DC    CL19'====== ======= ===='
TEXT7    DC    CL38'------ NON VSAM DATASET ENTRIES ------'
TEXT8    DC    CL19'VOLUME DATASET NAME'
TEXT9    DC    CL38'V S A M   D A T A S E T  E N T R I E S'
TEXT10   DC    CL33'VOLUME        CLUSTER NAME       '
TEXT11   DC    CL25' DATA / INDEX COMPONENT  '
TEXT12   DC    CL38'       A L I A S  E N T R I E S       '
TEXT13   DC    CL19'        ALIAS  NAME'
TEXT14   DC    CL31'RELATED / CONNECTED TO CATALOG '
TEXT15   DC    CL38'COMPLETE LISTING BY DATASET NAME      '
TEXT16   DC    CL33'VOLUME DATASET NAME              '
TEXT17   DC    CL19'DATASET TYPE       '
TEXT17A  DC    CL18'DEFINED IN CATALOG'
TEXT18   DC    CL38'COMPLETE LISTING IN VOLUME SEQUENCE   '
TEXT19   DC    CL38'   ALIAS ENTRIES IN CATALOG SEQUENCE  '
TEXT20   DC    CL23'ICF CATALOG LISTING :  '
TEXT21   DC    CL20'SUMMARIES          '
TEXT22   DC    CL38'     GENERATION DATA GROUP ENTRIES    '
TEXT23   DC    CL31'MAX GDGS  GDGS USED            '
OLDVOL   DC    CL6'      '
LINENR   DC    PL3'0'
PAGENR   DC    PL3'0'
TEMP     DC    CL6' '
         DC    D'0'
PARMS    DC    A(TEXT2)
DCBIN    DCB   DDNAME=DCBIN,LRECL=140,RECFM=FB,MACRF=(GL),             X
               DSORG=PS,EODAD=EOJ1
DCBIN1   DCB   DDNAME=DCBIN1,LRECL=140,RECFM=FB,MACRF=(GL),            X
               DSORG=PS,EODAD=EOJ2
DCBIN2   DCB   DDNAME=DCBIN2,LRECL=140,RECFM=FB,MACRF=(GL),            X
               DSORG=PS,EODAD=EOJ3
DCBIN3   DCB   DDNAME=DCBIN3,LRECL=140,RECFM=FB,MACRF=(GL),            X
               DSORG=PS,EODAD=EOJ4
DCBOUT   DCB   DDNAME=SYSPRINT,LRECL=133,RECFM=FBA,BLKSIZE=1330,       X
               DSORG=PS,MACRF=PM
 END
DODATE   CSECT
         STM   14,12,12(13) SAVE REGS
         BALR  10,0
         USING *,10
         LA    11,SAVE             OUR SAVE AREA                    JMH
         ST    13,SAVE+4
         ST    11,8(13)
         LA    13,SAVE
         B     OVERSAVE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R6       EQU   6
R12      EQU   12
R14      EQU   14
R15      EQU   15
         DS     0D
SAVE     DS    18F                 SAVE AREA
OVERSAVE EQU   *
         L     3,0(1)             USERS PARM
         TIME  BIN
         ST    R1,SAVEPARM         SAVE DATE
         LR    12,0                SAVE TIME
         MVC   WORK(7),=X'4021204B202020' MOVE IN EDIT MASK
         ED    WORK(7),SAVEPARM+1  FORMAT DATE
         MVC   ANSWER+9(2),WORK+1
         MVC   ANSWER(6),=CL6'      '                         MJH
         SPACE 1                                              MJH
         PACK  WORK2(2),WORK+4(3)                             MJH
         NI    WORK2+1,X'FC'                                  MJH
* SCAN DATE TABLE FOR MONTH  (NOTE U NEED TO FIX FOR LEAP YEAR)JH
         SPACE 1                                              MJH
NOLEAP   LA    R1,DTABLE                                      MJH
         XR    R0,R0                                          MJH
TABLOOP  CP    WORK2(2),3(2,R1)                               MJH
         BNH   GOTIT         IF DAY LOW OR EQUAL, WE GOT IT   MJH
         LA    R1,5(R1)       BUMP INDEX                      MJH
         LA    R0,1(R0)
         B     TABLOOP
GOTIT    MVC   ANSWER(3),0(R1)   MOVE IN MONTH
         CL    R0,=F'0'
         BNE   DUMMYF
         MVC   ANSWER(6),=CL6'??? ??'
         B     DOTIME
DUMMYF   S     R1,=F'5'       BACK UP ONE ENTRY
         SP    WORK2(2),3(2,R1) CALC DAY OF MONTH
         UNPK  WORK(3),WORK2(2)    CONVERT TO CHAR
         OI    WORK+2,X'F0'   FIX ZONE
MOVEDATE DS    0H
         MVC   ANSWER+4(2),WORK+1
         MVI   ANSWER+3,X'40'
         B     DOTIME
DOTIME   ST    R12,SAVEPARM        SAVE TIME
         LR    R1,R12              GET BINARY TOD
         SR    R0,R0               ZERO R0
         D     R0,=F'6000'         GET MINUTES
         CVD   R0,WORK             HUNDRENTHS OF SECONDS
         L     R15,WORK+4
         SRL   R15,4               SHIFT OFF SIGN
         STH   R15,WORK2+6
         SR    R0,R0               ZERO R0
         D     R0,=F'60'           GET HOURS
         CVD   R0,WORK             MINUTES
         L     R15,WORK+4
         SRL   R15,4               SHIFT OFF SIGN
         STC   R15,WORK2+5
         CVD   R1,WORK             HOURS
         L     R15,WORK+4
         SRL   R15,4               SHIFT OFF SIGN
         STC   R15,WORK2+4
         MVC   EWORK(12),=X'4021207A20207A20204B2020'
         ED    EWORK(12),WORK2+4   FORMAT TIME
         MVC   ANSWER+12(8),EWORK+1  MOVE IN ANSWER
         MVC   0(20,3),ANSWER    RETURN IT TO THE USER
EOJ      L     13,SAVE+4
         LM    14,12,12(13)
         XR    15,15
         BR    14            GO BACK TO OS
WORK     DC    D'0'
WORK2    DC    D'0'
EWORK    DC    XL16'00'
SAVEPARM DC    F'0'
ANSWER   DC    CL20'MMM DD 19YY HH:MM:SS'
* DATE TABLE
         SPACE 1
DTABLE   DC    CL3'???',PL2'000'
         DC    CL3'JAN',PL2'31'
         DC    CL3'FEB',PL2'59'   WILL FIX FOR LEAP YEAR
         DC    CL3'MAR',PL2'90'
         DC    CL3'APR',PL2'120'
         DC    CL3'MAY',PL2'151'
         DC    CL3'JUN',PL2'181'
         DC    CL3'JUL',PL2'212'
         DC    CL3'AUG',PL2'243'
         DC    CL3'SEP',PL2'273'
         DC    CL3'OCT',PL2'304'
         DC    CL3'NOV',PL2'334'
         DC    CL3'DEC',PL2'365'
         DC    CL3'???',PL2'999'
 END
