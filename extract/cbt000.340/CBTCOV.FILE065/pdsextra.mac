PDSEXTRA CSECT
         B     14(,R15)       BRANCH AROUND NAME
         DC    X'08'
         DC    CL8'PDSEXTRA'
         DC    X'00'
         PRINT NOGEN
         EJECT
*----------------D  O  C  U  M  E  N  T  A  T  I  O  N----------------*
*                                                                     *
*  TITLE      =  PDS EXTRACT                                          *
*                                                                     *
*  FUNCTION   =  READS AND EXTRACTS DATA FROM SPECIFIED PDS DIRECTORY *
*                AND LOAD MODULE RECORDS.                             *
*                                                                     *
*  OPERATION  =  BSAM IS USED TO READ THE DIRECTORY OF THE DATA SET.  *
*                THE DIRECTORY CONTAINS THE TRACK AND RECORD RELATIVE *
*                (TTR) TO THE BEGINNING OF THE DATA SET. THIS TTR IS  *
*                USED TO ESTABLISH THE INITIAL READ ADDRESS OF THE    *
*                LOAD MODULE.                                         *
*                                                                     *
*  INPUT      =  THE USER SPECIFIES A PDS TO BE EXTRACTED BY GIVING   *
*                A DDNAME WITH THE FIRST THREE CHARACTERS OF 'DSN'.   *
*                (ACCEPTABLE DEVICES ARE 3330,3330-1 & 3350) .        *
*                                                                     *
*  OUTPUT     =  VARIABLE LENTH RECORD CONTAINING DATA EXTRACTED      *
*                FROM THE SPECIFIED PDS'S DIRECTORY AND LOAD MODULE   *
*                RECORDS.                                             *
*                                                                     *
*  EXIT/NORMAL=  RETURN TO CALLER                                     *
*                                                                     *
*  EXIT/ERROR =  U100 - I/O ERROR OCCURRED                            *
*                U200 - ERROR OPENING SYSPRINT DD STATEMENT           *
*                                                                     *
*  ATTRIBUTES =  ***** AUTHORIZED *****                               *
*                                                                     *
*  AUTHOR     =  REVISED BY REX BIRCH                                 *
*                TRW DSSG, REDONDO BEACH, CALIFORNIA                  *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*     REGISTER  EQUATES                                               *
*---------------------------------------------------------------------*
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4    USED FOR ADDRESSABILITY OF I/O AREAS
*    I/O AREAS INCLUDE DIRECTORY, OUTPUT, & LOAD MODULE RECORDS
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
RBASE    EQU   12   USED FOR ADDRESSABILITY OF THE PROGRAM CODE
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
*---------------------------------------------------------------------*
*        H O U S E K E E P I N G                                      *
*---------------------------------------------------------------------*
         SPACE 1
         SAVE  (14,12)                 SAVE CALLER'S REGS
         BALR  RBASE,R0                     ESTABLISH
         USING *,RBASE                    ADDRESSABILITY
         LA    R2,SAVEAREA             ADDRESS OF MY SAVEAREA
         ST    R13,4(R2)               CHAIN THE SAVE AREAS
         ST    R2,8(R13)                      AND
         LR    R13,R2                  ESTABLISH THE POINTER TO MINE
         SPACE 2
*---------------------------------------------------------------------*
*        OPEN OUTPUT FILE THAT WILL CONTAIN                           *
*          STATISTICS AND ERROR MESSAGES                              *
*---------------------------------------------------------------------*
         SPACE 1
         OPEN  (SYSPRINT,(OUTPUT))     OPEN OUTPUT FILE
         LA    R1,SYSPRINT             LOAD ADDR IN CASE OF ERROR
         TM    SYSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO, EXIT INDICATING ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        OPEN OUTPUT FILE THAT WILL CONTAIN DATA EXTRACTED            *
*             FROM THE DIRECTORY AND LOAD MODULE RECORDS.             *
*---------------------------------------------------------------------*
         SPACE 1
         OPEN  (OUTDCB,(OUTPUT))     OPEN OUTPUT FILE
         LA    R1,SYSPRINT             LOAD ADDR IN CASE OF ERROR
         TM    SYSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO, EXIT INDICATING ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        OPEN OUTPUT FILE THAT IS USED TO WRITE THE SYNAD             *
*             MESSAGE REGARDING THE READ LOAD MODULE PROBLEM.         *
*---------------------------------------------------------------------*
         SPACE 1
         OPEN  (SYNADDCB,(OUTPUT))     OPEN SYNAD MESSAGE FILE
         LA    R1,SYSPRINT             LOAD ADDR IN CASE OF ERROR
         TM    SYSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO, EXIT INDICATING ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        GET TIOT ADDRESS AND CHECK FOR A VALID DD NAME (FIRST THREE  *
*        CHARACTERS OF THE DDNAME IS EQUAL TO DSN) .                  *
*---------------------------------------------------------------------*
         SPACE 1
         L     R2,CVTPTR               LOAD ADDRESS OF CVT
         L     R2,0(R2)                LOAD ADDRESS OF TCB DOUBLEWORD
         L     R2,4(R2)                LOAD ADDRESS OF MY TCB
         L     R2,12(R2)               LOAD ADDR OF MY TIOT
         LA    R2,24(R2)               POINT TO THE FIRST ENTRY
         SR    R3,R3                   R3 - LENGTH OF TIOT ENTRY
CHECKDDN ICM   R3,1,0(R2)              ?/END OF TIOT
         BZ    RETURN                  YES, ALL THROUGH -- EXIT
         CLC   4(3,R2),DDNID           ?/ACCEPTABLE DD NAME
         BE    VALIDDSN                YES, CONTINUE CHECKING
         AR    R2,R3                   NO, POINT TO NEXT ENTRY
         B     CHECKDDN                TRY AGAIN
         SPACE 2
*---------------------------------------------------------------------*
*        PREPARE FOR AND READ JFCB                                    *
*---------------------------------------------------------------------*
         SPACE 1
VALIDDSN CLC   SEQDDN(8),4(R2)         ?/DUPLICATE DD NAMES          V1
         BNE   VERDSN                  NO, CONTINUE WITH VERIFY      V1
         LA    R8,ERR9                 POINT TO ERROR MESSAGE        V1
         B     PROCERR                 PROCESS THE ERROR             V1
VERDSN   MVC   SEQDDN(8),4(R2)         SAVE THE DDNAME IN BOTH       V1
         STM   R2,R3,TIOTREGS          SAVE REGS USED FOR TIOT SCAN
         RDJFCB PDSSEQ                 READ JFCB
         LTR   R15,R15                 ?/ERROR OCCURRED
         BZ    VERIFYPO                NO, CONTINUE TO VERIFY
         LA    R8,ERR1                 LOAD ADDR OF ERROR MESSAGE
         B     PROCERR                 GO PROCESS THE ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        VERIFY THAT THE DATA SET IS PARTITION ORGANIZATION           *
*---------------------------------------------------------------------*
         SPACE 1
VERIFYPO MVC   DSCBNAME,JFCBAREA       GET THE DATA SET NAME
         MVC   VOLID,JFCBVOLS          GET THE VOL SER.
         OBTAIN DSCBREAD               GET THE FORMAT 1 DSCB
         LTR   R15,R15                 ?/OBTAINED WORKED
         BZ    TESTPO                  YES, CONTINUE TO VERIFY
         LA    R8,ERR2                 LOAD ADDR OF ERROR MESSAGE
         B     PROCERR                 GO PROCESS THE ERROR
TESTPO   TM    DSCBORG,X'02'           ?/IS IT PO
         BO    JFCBINFO                YES, GET JFCB INFORMATION
         LA    R8,ERR3                 LOAD ADDR OF ERROR MESSAGE
         B     PROCERR                 PROCESS THE ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        SAVE VARIOUS JFCB AND DSCB INFORMATION FOR LATER USE         *
*---------------------------------------------------------------------*
         SPACE 1
JFCBINFO MVC   DSNAME,JFCBAREA         SAVE THE DATA SET NAME
         MVC   MBBCCHHR+3(4),DSCBXTNT  SAVE BEGINNING EXTENT FOR DSN
         MVC   F1CCHHR,DSCBCCHR        SAVE CCHHR OF ASSOCIATED DSCB
         MVC   LRECLKEY,DSCBRECK       SAVE LRECL AND KEYLEN
         SPACE 2
*---------------------------------------------------------------------*
*        PREPARE TO READ THE DIRECTORY                                *
*---------------------------------------------------------------------*
         SPACE 1
         L     R5,CONGM        DIRECTORY, OUTPUT RECORD
         BAL   R8,GETAREA              GO TO GETAREA ROUTINE
         USING PDSDSECT,R4             ESTABLISH ADDRESSABILITY
         LR    R4,R1                     TO I/O AREA
         OPEN  (PDSSEQ)                OPEN PDS DS AS SEQUENTIAL
         OPEN  (LMDCB)                 OPEN LOAD MODULE DCB
         LA    R1,PDSSEQ               LOAD ADDR IN CASE OF ERROR
         TM    SEQOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 ERROR OPENING THE DATA SET
         SR    R5,R5                   CLEAR REG & USE AS A COUNTER
         SPACE 2
* GET AREA FOR LOAD MODULE RECORDS
         BAL   R14,GETMLM          GET MAIN FOR LOAD MODULE
         USING LMDSECT,R9
         OPEN  (SNAPP,OUTPUT)      DEBUG AID  * * * * * * * * * * *
*---------------------------------------------------------------------*
*        READ THE DIRECTORY (THE WHOLE DIRECTORY WILL BE READ).       *
*---------------------------------------------------------------------*
         SPACE 1
READDIR  READ  SEQDECB,SF,PDSSEQ,(R4)          READ DIRECTORY BLOCK
         CHECK SEQDECB                 WAIT FOR I/O
         LR    R7,R4       POINT R7 TO DIRECTORY RECORD
*        CLC   0(2,R7),=C'YM'  *********** DEBUG AID ***
*        BNE   READDIR    ****************** DEBUG AID *************
*        LA    R10,PDSKEY              * * * DEBUG AID * * * *
*        LA    R2,276(R10)            * * * DEBUG AID * * * *
*        SNAP  DCB=SNAPP,STORAGE=((R10),(R2)),PDATA=REGS * * * *
*        CLC   PDSKEY,HIKEY            ?/LAST USED DIRECTORY BLOCK
*        BNE   PROCDIR                 NO CONTINUE PROCESSING DIRECTORY
*---------------------------------------------------------------------*
*   START PROCESSING THE DIRECTORY RECORD                             *
*---------------------------------------------------------------------*
PROCDIR  EQU   *
*  R3 IS USED AS THE DIR ENTRY VARIBLE LENGTH COUNTER (BYTES PRESENT)
*  R4 AND ARE USED AS I/0 COVER REGISTERS
*  R7 POINTS TO THE START OF THE DIRECTORY ENTRY
*  R8 CONTAINS THE END OF THE DIRECTORY ADDRESS
         MVC   EOBMEMB,0(R7)   END OF BLOCK MEMBER * * * * T.L PROBLEM
         LH    R8,8(R7)              R8 NOW CONTAINS DIRECTORY LENGTH
         A     R7,=F'10'             R7 POINTS TO FIRST MEMBER
         AR    R8,R7                 END OF DIRECTORY
NEWMEMB  CLC   0(8,R7),HIGHCON       END OF DIRECTORY TEST
         BNE   ZEROTEST              END OF DIRECTORY
         CLC   EOBMEMB(8),0(R7) ? = EOBMEMBER * * * * * T.L PROBLEM
         BE    EOF        * * * * * * * *  * * * * T.L PROBLEM
         B     READDIR    * * * * * * * * * * * * * T.L PROBLEM
ZEROTEST CLC   0(8,R7),ZEROS         NOT USED ENTRIES TEST
         BE    EOF                   YES, NOT USED
         BAL   R14,REFRESH            INITIALIZE OUTPUT RECORD
         MVC   MODULE(8),0(R7)       MODULE NAME TO OUTREC
         MVC   LIBRARY(25),DSCBNAME  DSN
         MVC   VOLSER(6),VOLID       VOLUME
         MVC   TTR(3),8(R7)          TRACK & RECORD 0F LOAD MODULE
         TM    11(R7),X'80'          ALIAS TEST
         BNO   MEMBER                NO, MEMBER
         MVI   ALIAS,X'C1'           TURN ON ALIAS FLAG 'C1' = A
         MVC   EXMODULE,36(R7)       MODULE EXECUTED
MEMBER   IC    R3,11(R7)             MEMBER LENGTH PLUS FLAGS
         N     R3,=F'31'             CLEAR ALL BUT SIZE BITS
         SLL   R3,1                  TIMES 2 = NUMBER OF BYTES
         CH    R3,=H'10'             CONTAINS DATA ?
         BL    READLM                NO
         MVC   ATTR,20(R7)           ATTRIBUTES TO OUTPUT RECORD
         CH    R3,=H'13'             CONTAINS DATA ?
         BL    READLM                NO
         MVC   STORAGE(3),22(R7)     STORAGE TO OUTPUY RECORD
         CH    R3,=H'18'             CONTAINS DATA ?
         BL    READLM                NO
         MVC   ENTRYPT(3),27(R7)     ENTRY POINT TO OUTPUT RECORD
         CH    R3,=H'26'             IS ENTRY LARGE ENOUGH FOR SSI?
         BL    READLM                NO TOO SMALL
         TM    30(R7),X'01'        ? SSI BIT ON
         BNO   READLM                NO
         TM    20(R7),X'04'          TEST FOR SCATTER RECORD
         BNO   NOTSCTR               NOT SCATTER RECORD
         CH    R3,=H'34'            ? SCATTER ENTRY BIG ENOUGH FOR SSI
         BL    READLM
         MVC   SSI(4),44(R7)  SSI TO OUTPUT RECORD FROM SCATTER RECORD
         B     READLM
NOTSCTR  MVC   SSI(4),34(R7)  SSI TO OUTPUT RECORD FROM NON SCATTER
* PREPARE TO READ THE LOAD MODULE RECORDS
* R9 IS USED TO POINT TO THE CURRENT LOAD MODULE RECORD
READLM   EQU   *
         MVC   BLKADR(3),TTR       ESTABLISH INITIAL READ ADDRESS
         POINT LMDCB,BLKADR
LMREAD   L     R9,LMADDR       POINT R9 TO LOAD MODULE GET MAIN AREA
         READ  LMDECB,SF,LMDCB,(R9),'S'    READ LOADMOD RECORD
         CHECK LMDECB
*        NOTE  LMDCB
*        WTO   ' LMREAD',ROUTCDE=11
         TM    ERRFLAG,X'80'      ? SYNAD ROUTINE ENTERED
         BO    IDERROR              YES STOP READING THIS LOAD MODULE
         B     TYPETEST             YES    * * * * * * * * * * * * *
IDERROR  EQU   *
         MVI   ERRFLAG,X'00'        TURN OFF ERROR FLAG
*  IDENTIFY THE OUTPUT RECORD AS HAVING I/O PROBLEMS
         MVI   LMERROR,X'C5'        TURN ON I/O ERROR FLAG
         B     ENDLM
TYPETEST CLI   0(R9),X'20'         ? CESD RECORD
         BE    CESDREC              YES
         CLI   0(R9),X'80'         ? IDR RECORD
         BE    IDRREC               YES
         B     LMREAD               READ NEXT LOAD MODULE RECORD
ENDLM    EQU   *
         LH    R2,CSECTLEN
         SLL   R2,3            ENTRY COUNT X 8 = BYTE COUNT
         STH   R2,CSECTLEN     WRITE OUT BYTE COUNT
         AH    R2,IDRLEN       + IDR LENGTH IN BYTES
         AH    R2,LRECL        FIXED LENGTH PORTION OF OUTPUT RECORD
         STH   R2,LRECL
* MOVE IDR BUFFER TO THE END OF THE OUTPUT RECORD
         L     R2,CESDOADR         CESD NEXT AVAILABLE RECEIVE SLOT
         LA    R9,IDROUT           IDR START ADDRESS
         S     R9,=F'1'            ACCOMADATE EXECUTE THE MOVER
         LH    R1,IDRLEN          BYTES TO BE MOVED
         SR    R10,R10             ZERO R10
NEXTMOVE SR    R6,R6               ZERO R6
         IC    R6,1(R9)   IDR LENGTH THIS ENTRY (R9 ADJUSTED BY 1)
         LTR   R6,R6              TEST FOR ZERO
         BZ    WROUT               YES, ZERO
         EX    R6,MOVER            MOVE IDR DATA THIS ENTRY
         CR    R10,R1              COMPARE TO TOTAL BYTES TO BE MOVED
         BH    WROUT               ALL HAVE BEEN MOVED
         BE    WROUT               ALL HAVE BEEN MOVED
         AR    R9,R6               POINT TO NEXT INPUT IDR
         AR    R2,R6               POINT TO NEXT OUTPUT SLOT
         AR    R10,R6              SUM OF BYTES MOVED
         B     NEXTMOVE            MOVE THE NEXT IDR ENTRY
WROUT    EQU   *
*        CLC   MODULE(2),=C'YM'
*        BNE   PUTOUT
*        STM   R7,R8,R10R11
*        LA    R7,OUTREC         LOAD MODULE READ ADDRESS
*        LA    R8,1250(R7)      LENGTH
*        SNAP  DCB=SNAPP,STORAGE=((R7),(R8)),PDATA=REGS * * DEBUG *
*        LM    R7,R8,R10R11
PUTOUT   PUT   OUTDCB,OUTREC          WRITE THE FORMATED OUTPUT RECORD
*  TEST WAS LAST PROCESSED MEMBER THE LAST OF THIS DIRECTORY RECORD
         CLC   PDSKEY(8),0(R7)  COMPARE LAST PROCESSED TO BLOCK KEY
         BE    READDIR               END OF THIS DIRECTORY MEMBER
* ADVANCE R7 TO NEXT MEMBER
         A     R7,=F'12'    MINIMUM DIRECTORY LENGTH
         AR    R7,R3        PLUS VARIABLE DIRECTORY ENTRY LENGTH
         CR    R7,R8        TEST FOR END OF DIRECTORY BLOCK
         BL    NEWMEMB      NOT END OF DIRECTORY
         B     READDIR                GET NEXT DIRECTORY RECORD
         SPACE 1
*---------------------------------------------------------------------*
* THIS ROUTINE PROCESS LOAD MODULE TYPE 20 CESD RECORD
* R5 POINTS TO THE CESD NAME FROM THE TYPE 20 RECORD
* R9 POINTS TO THE INPUT LOAD MODULE TYPE 20 RECORD
* R2 POINTS TO THE OUTPUT RECORD CESD RECEIVING AREA
* R6 USED TO ACCUMULATE THE CESD COUNT IN THE OUTPUT RECORD
* R11 USED TO DECREMENT THE CESD COUNT IN THE INPUT RECORD
CESDREC  EQU   *
         L     R2,CESDOADR            NEXT RECEIVE ADDRESS
         LH    R6,CSECTLEN            CURRENT CESD COUNT
         LH    R10,6(R9)        BYTE COUNT OF CESD DATA
         LTR   R10,R10          SET CONDITION CODE
         BZ    LMREAD           NO CESD DATA
         BM    LMREAD           NEGATIVE COUNT
         C     R10,=F'240'      MAX CESD BYTE COUNT
         BH    LMREAD           FALSE COUNT
         SRDA  R10,32(0)        DIVIDEND TO R11 - PERPETUATE THE SIGN
         D     R10,=F'16'       QUOTIENT TO R11
         LA    R5,8(R9)         POINT R5 TO CESD NAME
NEXTCESD CLC   ZEROS(8),0(R1)   NULL TEST
         BE    NEXTINC          YES, NULL
* TEST FOR FIRST CHARACTER NUMERIC OF CSECT NAME
         SR    R0,R0            CLEAR R0
         IC    R0,0(R5)         FIRST CHARACTER OF CSECT NAME
         CLI   R0,X'C0'         HEX < A
         BNH   LMREAD           < A
         CLI   R0,X'CA'         HEX > I
         BL    ISTCNUM          IS A - I
         CLI   R0,X'D0'         HEX < J
         BNH   LMREAD           < J
         CLI   R0,X'DA'         HEX > R
         BL    ISTCNUM          IS J - R
         CLI   R0,X'E1'         HEX < S
         BNH   LMREAD           < S
         CLI   R0,X'EA'         HEX > Z
         BNL   LMREAD           > Z
ISTCNUM  LA    R6,1(R6)         INCREMENT CESD COUNT
         C     R6,=F'2048'      ? COUNT > 2048
         BH    CESDOVFL         YES, GO TO PROCESS ERROR
         MVC   0(8,R2),0(R5)    CESD NAME TO OUTPUT RECORD
         LA    R2,8(R2)         POINT R2 TO NEXT AVAILABLE SLOT
NEXTINC  LA    R5,16(R5)        POINT R5 TO NEXT AVAILABLE CESD DATA
         BCT   R11,NEXTCESD     NOT LAST GET NEXT
         STH   R6,CSECTLEN      YES LAST, STORE COUNT
         ST    R2,CESDOADR      NEXT AVAILABLE CESD OUTPUT ADDRESS
         B     LMREAD           GET NEXT LOAD MODULE RECORD
CESDOVFL EQU   *
         LA    R8,ERR10         ADDRESS OF ERROR MESSAGE
         B     PROCERR          PROCESS THE ERROR
         SPACE 1
*---------------------------------------------------------------------*
* PROCESS LOAD MODULE TYPE 80 IDR RECORD
* R2 POINTS TO  THE OUTPUT RECORD IDR RECEIVE AREA
* R6  PICKS UP INPUT IDR LENGTH
* R9 POINTS TO THE INPUT LOAD MODULE TYPE 80 RECORD
IDRREC   EQU   *
*        CLC   MODULE(2),=C'YM'
*        BNE   CLEARR6
*        LA    R6,260(R9)           * * * * * * * DEBUG AID * * * * *
*        SNAP  DCB=SNAPP,STORAGE=((R9),(R6)),PDATA=REGS * * DEBUG *
CLEARR6  SR    R6,R6         ZERO R6
         IC    R6,1(R9)      LENGTH OF IDR RECORD
         LH    R1,IDRLEN     PREVIOUS BYTE COUNT
         AR    R1,R6         PREVIOUS + CURRENT BYTE COUNTS
         C     R1,=F'10240'  ? BYTE COUNT > 10240
         BH    IDROVFL       YES, GO TO PROCESS ERROR
         STH   R1,IDRLEN     STORE UPDATED IDR BYTE COUNT
         L     R2,IDROADR    OUTPUT RECORD RECEIVE ADDRESS
         EX    R6,MOVER      EXECUTE THE MOVE INSTRUCTION
         AR    R2,R6         UPDATE OUTPUT RECEIVE ADDRESS
         ST    R2,IDROADR    STORE NEXT RECEIVE ADDRESS
*        CLC   MODULE(2),=C'II'
*        BNE   LASTTEST
*        STM   R7,R8,R10R11
*        LA    R7,IDROUT         LOAD MODULE READ ADDRESS
*        LA    R8,999(R7)      LENGTH
*        SNAP  DCB=SNAPP,STORAGE=((R7),(R8)),PDATA=REGS * * DEBUG *
*        LM    R7,R8,R10R11
LASTTEST TM    2(R9),X'84'   LAST OF LOAD MODULE TEST
         BO    ENDLM          YES
         B     LMREAD         READ NEXT LOAD MODULE RECORD
MOVER    MVC   0(0,R2),1(R9)    EXECUTED INSTRUCTION
IDROVFL  LA    R8,ERR11       ADDRESS OF OVERFLOW MESSAGE
         B     PROCERR        PROCESS THE ERROR
         SPACE 1
*---------------------------------------------------------------------*
*        THIS ROUTINE REFRESHES THE OUTPUT RECORD, LRECL IS SET TO 73 *
*        ALL NUMERIC FIELDS ARE ZEROD, ALPHABETIC FIELDS ARE BLANKED. *
*        CSECTOUT & AND IDROUT ADDRESS ARE INITIATED.                 *
*        CONTROL IS PASSED HERE VIA BAL R14                           *
*---------------------------------------------------------------------*
REFRESH  LH    R3,=H'73'      FIXED LENGTH PORTION OF OUTPUT RECORD
         STH   R3,LRECL
         MVI   LMERROR,X'40'      TURN OFF LOAD MODULE ERROR FLAG
         L     R3,=F'0'
         STH   R3,LRECL+2         RIGHT HALF RDW
         STH   R3,IDRLEN          IDR LENGTH
         STH   R3,CSECTLEN        CSECT LENGTH
         LA    R2,IDROUT          IDROUT ADDRESS
         ST    R2,IDROADR
         LA    R2,CESDOUT         CESD OUT ADDRESS
         ST    R2,CESDOADR
         MVC   SSI(4),BLANKS      SSI
         MVC   ENTRYPT(8),ZEROS      ZERO ENTRY POINT,
         MVC   IDRTYPE(1),BLANKS     IDR TYPE
         MVC   ALIAS(1),BLANKS       ALIAS
         MVC   TTR(3),ZEROS          TRACK AND RECORD
         MVC   EXMODULE(8),BLANKS8   MODULE EXECUTED FROM ALIAS
         BR    R14            RETURN
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  THIS ROUTINE GETS MAIN FOR THE LOAD MODULE RECORDS  * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
GETMLM   LA    R10,LMDCB          PROVIDE ADDRESSABILTY
         USING IHADCB,R10
         LH    R9,DCBBLKSI        GET LOAD MODULE BLKSIZE
         DROP  R10                DROP ADRESSABILTY
         GETMAIN R,LV=(R9)
         LTR   R15,R15           ? AREA ACQUIRED
         BNZ   GETERR
         LA    R1,0(R1)            CLEAR HIGH ORDER BIT
         ST    R1,LMADDR           ADDRESS OF LOAD MODULE AREA
         ST    R9,LMLENGTH          LENGTH OF LOAD MODULE
         BR    R14                 RETURN
*        POST PROCESSING AFTER THE DIRECTORY HAS BEEN READ
         SPACE 1
EOF      L     R5,SEQDEB               GET DEB ADDRESS IN ORDER
         MVC   UCBADDR+1(3),33(R5)       TO SAVE THE UCB ADDR
         DROP  R4                      DROP ADDRESSABILITY TO PDSDSECT
         BAL   R8,FREEAREA             GO FREE THE AREA
         L     R5,LMADDR               LOAD MODULE ADDRESS
         ST    R5,GETADDR
         L     R5,LMLENGTH             LOAD MODULE LENGTH
         ST    R5,GETLNGTH
         BAL   R8,FREEAREA             FREE LOAD MODULE AREA
         CLOSE PDSSEQ                  CLOSE THE DATA SET
         SPACE 2
*---------------------------------------------------------------------*
*        PREPARE TO PROCESS THE NEXT DD STATEMENT AND START ALL OVER. *
*---------------------------------------------------------------------*
         SPACE 1
NEXTDD   XC    MBBCCHHR,MBBCCHHR       CLEAR THE REUSABLE FIELD
         LM    R2,R3,TIOTREGS          RESTORE THE REGS USED FOR TIOT
         AR    R2,R3                   POINT TO THE NEXT TIOT ENTRY
         B     CHECKDDN                START ALL OVER
         SPACE 2
*---------------------------------------------------------------------*
*        THIS IS THE END, CONTROL WILL BE GIVEN BACK TO THE CALLER    *
*---------------------------------------------------------------------*
         SPACE 1
RETURN   CLOSE (SYSPRINT)              CLOSE STATS & MSG FILE
         CLOSE (OUTDCB)              CLOSE OUTPUT RECORD FILE
         CLOSE (LMDCB)               CLOSE LOAD MODULE DCB
         CLOSE (SNAPP)      * * * * * * DEBUG AID * * * * * * * * *
         CLOSE (SYNADDCB)   * * * * * * DEBUG AID * * * * * * * * *
         L     R13,4(R13)              RESTORE PTR TO CALLER'S SAVEAREA
         LM    R14,R12,12(R13)         RESTORE HIS REGISTERS
         SR    R15,R15                 SET RC TO 0
         BR    R14
         SPACE 2
* SYNAD ROUTINE FOR LOAD MODULE READ I/O ERROR
SR       EQU   *
         STM   R0,R15,SYNADREG   SAVE CALLERS REGISTERS
*        LA    R10,SYNADREG       THIS PROGRAMS SAVE AREA
*        ST    R10,8(,R13)        TO CALLERS AREA
*        ST    R13,4(,R10)        4 OFF SYNADREG
*        LR    R13,R10
         SYNADAF ACSMETH=BSAM
         ST    R13,R13SAVE
         PRINT GEN
         PUT   SYNADDCB,(R1)
         PRINT NOGEN
         L     R13,R13SAVE
         SYNADRLS
         OI    ERRFLAG,X'80'   TURN ON ERROR FLAG
         WTO   ' SR CODE',ROUTCDE=11
*        LA    R10,SYNADREG       THIS PROGRAMS SAVE AREA
*        L     R13,4(,R10)     CONTENTS AT ENTRY TO SR
         LM    R0,R15,SYNADREG  RESTORE REGISTERS TO ENTRY CONTENT
*        MVI   12(R13),X'FF'
         BR    R14     RETURN TO THE CHECK FOLLOWING THE LOAD MOD READ
*---------------------------------------------------------------------*
*        THIS ROUTINE IS RESPONSIBLE FOR ACQUIRING ALL AREA. CONTROL  *
*        IS PASSED TO IT VIA BAL R8.                                  *
*---------------------------------------------------------------------*
         SPACE 1
GETAREA  GETMAIN R,LV=(R5)             R5 = LENGTH OF AREA
         LTR   R15,R15                 ?/AREA ACQUIRED
         BNZ   GETERR                  NO, GO INDICATE AN ERROR
         LA    R1,0(R1)                CLEAR HI-ORDERED BYTE
         ST    R1,GETADDR              SAVE ADDR OF AREA
         ST    R5,GETLNGTH             SAVE LENGTH OF AREA
         BR    R8                      RETURN TO THE CALLER
GETERR   LA    R8,ERR4                 LOAD ADDR OF ERR MSG
         B     PROCERR                 GO PROCESS THE ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        THIS ROUTINE FREES ALL ACQUIRE AREA. CONTROL IS PASSED TO IT *
*        VIA BAL R8.                                                  *
*---------------------------------------------------------------------*
         SPACE 1
FREEAREA L     R5,GETLNGTH             GET AMOUNT TO FREE
         L     R6,GETADDR              GET BEGINNING ADDRESS
         FREEMAIN R,LV=(R5),A=(R6)     FREE THE AREA
         LTR   R15,R15                 ?/AREA FREED
         BCR   8,R8                    YES, RETURN TO THE CALLER
         LA    R8,ERR5                 LOAD ADDR OF ERROR MESSAGE
         B     PROCERR                 GO PROCESS THE ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        ANY FAILURE TO OPEN A DCB CAUSES OPENERR TO RECEIVE CONTROL. *
*        IF SYSPRINT IS THE DCB THAT FAILED WE DESTRUCT  WITH A USER  *
*        200. OTHERWISE AN ERROR MESSAGE WILL BE WRITTEN AND WE WILL  *
*        TERMINATE NORMALLY.                                          *
*---------------------------------------------------------------------*
         SPACE 1
OPENERR  CLC   40(8,R1),=C'SYSPRINT'   ?/OPEN SYSPRINT FAILED
         BNE   SAVEDDN                 NO, SAVE DDNAME IN MSG
         ABEND 200,DUMP
SAVEDDN  LA    R8,ERR6                 LOAD ADDR OF ERROR MSG
         MVC   22(8,R8),40(R1)         INDICATE FILE IN ERROR
PROCERR  SR    R2,R2                   CLEAR R2
         IC    R2,0(R8)                GET ERROR MESSAGE LENGTH
         MVI   OUTLINE,X'40'           CLEAR OUTPUT BUFFER           V1
         MVC   OUTLINE+1(120),OUTLINE         ××                     V1
         EX    R2,ERRORMSG             MOVE MESSAGE TO OUTPUT LINE
         MVI   OUTLINE,X'60'           INDICATE TRIPLE SPACING
         PUT   SYSPRINT,OUTLINE        WRITE THE MESSAGE
         B     RETURN                  PREPARE FOR EXIT
ERRORMSG MVC   OUTLINE+1(0),1(R8)      MOVE MESSAGE TO BUFFER
         EJECT
*---------------------------------------------------------------------*
*        ERROR MESSAGES. THE FOLLOWING IS THE FORMAT:                 *
*                                                                     *
*     --------------------------------------------------------        *
*     × LENGTH ×              THE                            ×        *
*     × OF MSG ×            MESSAGE                          ×        *
*     --------------------------------------------------------        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
ERR1     DC    X'11',C'ERROR DOING RDJFCB'                           V1
ERR2     DC    X'11',C'ERROR DOING OBTAIN'                           V1
ERR3     DC    X'0E',C'DSORG IS NOT PO'                              V1
ERR4     DC    X'0C',C'GETMAIN ERROR'                                V1
ERR5     DC    X'0D',C'FREEMAIN ERROR'                               V1
ERR6     DC    X'1B',C'ERROR OPENING FILE',CL10' '                   V1
ERR7     DC    X'1D',C'DATA SET ON UNSUPPORTED DEVICE'               V1
ERR8     DC    X'23',C'I/O ERROR---DATA SET COULD BE RUINED'         V1
ERR9     DC    X'10',C'DUPLICATE DDNAMES'                            V1
ERR10    DC    X'1A',C'MORE THAN 2048 CESD ENTRIES'                  V1
ERR11    DC    X'26',C'MORE THAN 10240 CHARACTERS OF IDR DATA'
         SPACE 2
*---------------------------------------------------------------------*
*        HEADERS FOR THE PRINT FILE                                   *
*---------------------------------------------------------------------*
         SPACE 1
OUTLINE  DC    CL121' '
HDR      DC    CL121'-* PDSEXTRACT STATISTICS FOR DATASET: '
LINE1    DC    CL121'0   NUMBER OF DIRECTORY BLOCKS PROCESSED : '
LINE2    DC    CL121'    NUMBER OF DIRECTORY BLOCKS AVAILABLE : '
         SPACE 2
*---------------------------------------------------------------------*
*        DATA CONTROL BLOCKS                                          *
*---------------------------------------------------------------------*
         SPACE 1
PDSSEQ   DCB   DSORG=PS,MACRF=R,DDNAME=DATASET1,KEYLEN=8,EODAD=EOF,RECFX
               M=F,LRECL=256,BLKSIZE=256,EXLST=JFCBPTR
LMDCB    DCB   DSORG=PS,MACRF=(RP),DDNAME=LOADMOD,EODAD=ENDLM,SYNAD=SR
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,BLKSIZE=121,RECFM=FA
SYNADDCB DCB   DSORG=PS,MACRF=PM,DDNAME=SYNADDD,RECFM=VA
SNAPP    DCB   DSORG=PS,RECFM=VBA,MACRF=(W),BLKSIZE=1632,LRECL=125,    X
               DDNAME=SNAPPED
OUTDCB   DCB   DSORG=PS,MACRF=PM,RECFM=VB,DDNAME=OUTFILE,LRECL=26697,  X
               BLKSIZE=32760
         SPACE 2
*---------------------------------------------------------------------*
*        PARAMETER LISTS                                              *
*---------------------------------------------------------------------*
         SPACE 1
JFCBPTR  DS    0F
         DC    X'87'
         DC    AL3(JFCBAREA)
JFCBAREA DS    176C
DSCBREAD CAMLST SEARCH,DSCBNAME,VOLID,DSCBAREA
DSCBNAME DC    CL44' '
VOLID    DC    CL6' '
DSCBAREA DS    0D
         DS    148C
         SPACE 2
*--------------------------------------------------------------------*
*        CONSTANTS/WORK AREAS/FLAGS/ETC.                             *
*--------------------------------------------------------------------*
         SPACE 1
         DS    0D
         LTORG
         DS    0D
WORK     DS    3F
EOBMEMB  DC    CL8'88888888'
PDSECB   DC    F'0'
DIRUCNT  DC    F'0'
DIRTCNT  DC    F'0'
MBBCCHHR DC    D'0'
TIOTREGS DC    2F'0'
GETADDR  DC    F'0'
UCBADDR  DC    F'0'
GETLNGTH DC    F'0'
EXTRADIR DC    F'0'
CPREGS   DC    2F'0'
TTRREGS  DC    2F'0'
MAJOR    DC    CL8'SYSVTOC'
SAVEAREA DC    18F'0'
DDNID    DC    C'DSN'
CCWFLAG  DC    XL1'00'
LRECLKEY DC    XL3'00'
F1CCHHR  DC    XL5'00'
DSNAME   DC    CL44' '
NULLKEY  DC    8X'00'
HIKEY    DC    8X'FF'
DATA     DC    XL2'000E',8X'FF'
RCNTCCW  DC    XL4'10000008'
         DS    0D
R10R11   DS    2F'0'
SYNADREG DC    18F'0'             SYNAD SAVE REGS
R13SAVE  DC    F'0'
ERRFLAG  DC    X'00'
BLANKS   DC    CL4'    '             CONSTANT OF 4 BLANKS
ZEROS    DC    2F'0'                 CONSTANT OF ZEROS
BLANKS8  DC    CL8'        '         CONSTANT OF 8 BLANKS
HIGHCON  DC    XL8'FFFFFFFFFFFFFFFF'
CONGM    DC    F'5000'              CONTSTANT USED FOR GETMAIN
BLKADR   DC    F'0'                  TTR OF CURRENT LOAD MODULE
LMADDR   DC    F'0'                  LOAD MODULE ADDRESS
LMLENGTH DC    F'0'                  LOAD MODULE LENGTH
CESDOADR DC    F'0'                  CESD OUTPUT RECORD AREA ADDRESS
IDROADR  DC    F'0'                  IDR  OUTPUT RECORD AREA ADDRESS
IDROUT   DS    CL10240        VARIABLE LENGTH IDR  RECORDS
*  IDR RECORDS ACCUMULATED HERE THEN MOVED TO NEXT AVAILABLE CESD
*  SLOT BEFORE WRITING THE OUTPUT RECORD
         SPACE 1
*---------------------------------------------------------------------*
*        EQUATES                                                      *
*---------------------------------------------------------------------*
         SPACE 1
MINOR    EQU   VOLID+0
CVTPTR   EQU   16
SYSOFLAG EQU   SYSPRINT+48
SEQDDN   EQU   PDSSEQ+40
JFCBVOLS EQU   JFCBAREA+118
DSCBORG  EQU   DSCBAREA+38
DSCBXTNT EQU   DSCBAREA+63
DSCBCCHR EQU   DSCBAREA+96
DSCBRECK EQU   DSCBAREA+44
SEQOFLAG EQU   PDSSEQ+48
SEQDEB   EQU   PDSSEQ+44
DSCBDIR  EQU   DSCBAREA+31
         SPACE 2
*---------------------------------------------------------------------*
*        DSECTS                                                       *
*---------------------------------------------------------------------*
*  R10 PROVIDES ADDRESSABILITY FOR THIS DCB WHICH GENERATES A DSECT
DUMDCB   DCBD  DSORG=BS,DEVD=DA
         SPACE 1
* R4 PROVIDES ADDRESABILTY OF THE FOLLOWING DSECT
PDSDSECT DSECT
* FOLLOWING IS THE INPUT DIRECTORY RECORD
PDSKEY   DS    CL8
PDSBLOCK DS    CL256
PDSEND   DC    C'END'
         SPACE 1
* FOLLOWING IS THE OUTPUT RECORD
         SPACE 2
         DS    0D
OUTREC   DS    0CL73          73 + 16384 + 10240 = 26697 MAX LENGTH
LRECL    DS    XL2            COMPUTED LENGTH OF OUTPUT RECORD22
         DS    XL2            RESERVED FOR IBM                    4
LIBRARY  DS    CL25           JFCB BYTE 0                        29
VOLSER   DS    CL6            UCB BYTE 28                        35
MODULE   DS    CL8            DIRECTORY BYTE 2                   43
SSI      DS    CL4            DIRECTORY BYTE 34 OR 42            47
ENTRYPT  DS    XL3            DIRECTORY BYTE 27                  50
ATTR     DS    XL2            DIRECTORY BYTE 20                  52
STORAGE  DS    XL3            DIRECTORY BYTE 22                  55
IDRTYPE  DS    CL1            LOAD MODULE TYPE 80 BYTE 2 (L,R,Z) 56
ALIAS    DS    CL1            ALIAS = A, MEMBER = BLANK          57
TTR      DS    XL3            TRACK & RECORD OF LOAD MODULE      60
EXMODULE DS    CL8            EXECUTED MODULE FROM ALIAS         68
CSECTLEN DS    XL2            COMPUTED                           70
IDRLEN   DS    XL2            COMPUTED                           72
LMERROR  DS    CL1            BLANK NO ERROR, E = ERROR          73
CESDOUT  DS    CL26624        LOAD MODULE TYPE 20-MAX OF 2048 26624
* CONTAINS 0 TO 2048 CSECT NAMES 8 CHARACTERS LONG MAX (8X2048=16384)
* AN ADDITIONAL 10240 BYTES ARE PROVIDED FOR IDR DATA
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* LOAD MODULE INPUT READ AREA FOLLOWS  * * * * * * * * * * * * * * *
* R9 PROVIDES ADDRESSABILTY TO THE LOAD MODULE DSECT
LMDSECT  DSECT
         DS    0D
LOADMOD  DS    0CL19096
LMID     DS    CL1             RECORD ID
         DS    CL19095
         SPACE 1
         END   PDSEXTRA
