WHO      TITLE 'WHO - LIST ACTIVE TASKS IN THE SYSTEM'
WHO      CSECT
         REGS
         EJECT
WHO      CSECT
START    STM   14,12,12(R13)
         BALR  R12,0
         USING *,R12
         LA    R11,SAVE
         ST    R11,8(,R13)
         ST    R13,4(,R11)
         LR    R13,R11
         L     R0,=F'21672'         LENGTH OF OUTPUT AREA   *HMD 01/82*
         GETMAIN R,LV=(0)
         LR    R11,R1               ADDRESS OF GRABBED CORE
         USING DATD,R7
         ST    R1,WORKA             SAVE ADDRESS
         B     AROUND
TITLE    DC    C'**** GTEDS ACTIVE TASKS ****   HHMMSSTH    '
HEADER   DC    C'USERID   '
ASID     DC    C'ASID '
PG       DC    C' PG'
SWPOUT   DC    C' SWAPOUT '
WSET     DC    C'WKG/SET '
SLOTS    DC    C'SLOTS '
STATUS   DC    C'STATUS'
HEADERL EQU   *-HEADER
LINE     DC    CL72' '
USERTOT  DC    C'- THERE ARE'
USERCT   DC    C'NNNN ADDRESS SPACES IN USE'
USERTOTL EQU   *-USERTOT
WORKA    DC    A(0)    PTR TO OUTPUT AREA
WORKB    DC    2F'0'
ZEROS    DC    F'0'
         DS    0D
CVD      DC    D'0'
SAVE     DC    18F'0'
         SPACE 1
AROUND   TIME  DEC                  R0: HHMMSSTH
         ST    R0,WORKB
         LA    R8,TITLE
         UNPK  31(7,R8),WORKB(4)
         MVC   37(2,R8),35(R8)      XXSS
         MVC   35(1,R8),34(R8)      MXSS
         MVC   34(1,R8),33(R8)      HHXM MXSS
         MVI   33(R8),C'.'
         MVI   36(R8),C'.'          HH.MM.SS
         TPUT  TITLE,L'TITLE        PRINT TITLE AND TIME
         TPUT  HEADER,HEADERL
         XR    R8,R8
         XR    R9,R9
*        XR    R7,R7
*        LA    R7,OUTBUF-72         BEGINNING OF OUTPUT BUFFER
         L     R11,WORKA            START OF OUTPUT AREA
         L     R3,CVTPTR            CVT ADDR
         USING CVT,R3               ADDRESS THE CVT         *HMD 09/82*
         L     R3,CVTASVT           ASVT PREFIX ADDR        *HMD 09/82*
         USING ASVT,R3              ADDRESS THE ASVT        *HMD 09/82*
         LA    R3,ASVTFRST          ASVT START              *HMD 09/82*
         DROP  R3                   BASE NO LONGER NEEDED   *HMD 09/82*
LOOP1    LA    R3,4(R3,0)           FIRST ASSIGNED ENTRY
         CLC   0(4,R3),=X'80000000' END OF TABLE?
         BE    ENDTAB               YES
         TM    0(R3),X'80'          IS THIS ASCB AVAILABLE?
         BO    LOOP1                YES, THEN CHECK NEXT ENTRY
         L     R5,0(R3,0)           THIS ASCB IS ASSIGNED.
         USING ASCB,R5              ADDRESS THE ASCB        *HMD 09/82*
         USING OUCB,R6              ADDRESS THE OUCB        *HMD 09/82*
         L     R6,ASCBOUCB          OUCB ADDRESS            *HMD 09/82*
         CLC   ASCBJBNI,ZEROS       IS JOBNAME FIELD ZEROS? *HMD 09/82*
         BE    TSO                  YES, TSO OR STARTED JOB
         L     R4,ASCBJBNI          ADDR OF USERID
         B     MOVES
TSO      CLC   ASCBJBNS,ZEROS       IS JOBNAME FIELD ZEROS? *HMD 09/82*
         BNE   TSOK                 NO, JOBNAME IS GOOD
         MVC   LINE(8),=CL8'STARTING'  INDICATE ADDRESS SPACE STARTUP
         B     SKIP                 FINISH REST OF PROCESSING
TSOK     L     R4,ASCBJBNS          ADDRESS OF USERID
*                                                           *HMD 09/82*
* CHECK FOR ANY ASCB DELETIONS HERE                         *HMD 09/82*
* BRANCH TO LOOP1 IF YOU DO NOT WANT TO DISPLAY ASCB        *HMD 09/82*
*                                                           *HMD 09/82*
MOVES    MVC   LINE(8),0(R4)        USERID TO O/P LINE
SKIP     SR    R2,R2
         LH    R2,ASCBASID          ASID                    *HMD 09/82*
         CVD   R2,CVD               CONVERT IT
         MVC   LINE+ASID-HEADER(4),=X'40202120'  EDIT MASK
         ED    LINE+ASID-HEADER(4),CVD+6    DO IT
         TM    ASCBRCTF,ASCBOUT     SWAPPED OUT?            *HMD 06/79*
         BZ    IN                   NO                      *HMD 06/79*
         MVI   LINE+9,C'*'          INDICATE SO             *HMD 06/79*
IN       SR    R2,R2                CLEAR IT                *HMD 06/79*
         LH    R2,OUCBPSO           GET PAGES AT LAST SWAP OUT
*                                                           *HMD 09/82*
         CVD   R2,CVD               CONVERT
         MVC   LINE+SWPOUT+1-HEADER(4),=X'40202120' EDIT MASK
         ED    LINE+SWPOUT+1-HEADER(4),CVD+6    DO IT
         SR    R2,R2
         LH    R2,OUCBWSS           WORKING SET SIZE IN PAGES
*                                                           *HMD 09/82*
         CVD   R2,CVD               CONVERT
         MVC   LINE+WSET-HEADER(4),=X'40202120'
         ED    LINE+WSET-HEADER(4),CVD+6
         SR    R2,R2
         LH    R2,OUCBNPG           PERFORMANCE GROUP       *HMD 09/82*
         CVD   R2,CVD
         MVC   LINE+PG-HEADER-1(4),=X'40202120'
         ED    LINE+PG-HEADER-1(4),CVD+6
         SR   R2,R2
         LH    R2,ASCBNVSC          GET AUXILIARY SLOT COUNT*HMD 09/82*
         CVD   R2,CVD
         MVC   LINE+SLOTS-HEADER(4),=X'40202120'
         ED    LINE+SLOTS-HEADER(4),CVD+6
         LA    R8,LINE+STATUS-HEADER    POSITION PTR
         CLI   OUCBEFL,X'00'        READY?                  *HMD 09/82*
         BNE   N01                  NO                      *HMD 09/82*
         MVC   0(6,R8),=CL6'READY,'                         *HMD 09/82*
         LA    R8,6(0,R8)           BUMP                    *HMD 09/82*
N01      TM    OUCBEFL,OUCBLWT      LWAIT?                  *HMD 09/82*
         BNO   N02                  NO                      *HMD 09/82*
         MVC   0(6,R8),=CL6'LWAIT,'                         *HMD 09/82*
*        LA    R8,6(0,R8)           BUMP                    *HMD 09/82*
N02      TM    OUCBEFL,OUCBTRM      IWAIT?                  *HMD 09/82*
         BNO   N03                  NO                      *HMD 09/82*
         MVC   0(6,R8),=CL6'IWAIT,'                         *HMD 09/82*
         LA    R8,6(0,R8)                                   *HMD 09/82*
N03      TM    OUCBEFL,OUCBOWT      OWAIT?                  *HMD 09/82*
         BNO   N04                  NO                      *HMD 09/82*
         SR    R10,R10
         LA    R10,6(0,R10)
         SR    R8,R10
         MVC   0(6,R8),=CL6'OWAIT,'
         LA    R8,6(0,R8)
N04      TM    OUCBEFL,OUCBNQF      ENQHOLD?                *HMD 09/82*
         BNO   N05        NO                                *HMD 09/82*
         MVC   0(5,R8),=CL5'ENQH,'                          *HMD 09/82*
         LA    R8,5(0,R8)           BUMP                    *HMD 09/82*
N05      TM    OUCBEFL,OUCBQSS      QUIESCE STARTED?        *HMD 09/82*
         BNO   N06     NO                                   *HMD 09/82*
         MVC   0(6,R8),=CL6'QSCST,'                         *HMD 09/82*
         LA    R8,6(0,R8)                                   *HMD 09/82*
N06      TM    OUCBEFL,OUCBQSC      QUIESCE PROCESSED?      *HMD 09/82*
         BNO   N07
         SR    R10,R10
         LA    R10,6(0,R10)
         SR    R8,R10
         MVC   0(6,R8),=CL6'QSCMP,'
         LA    R8,6(0,R8)
N07      TM    OUCBCFL,OUCBRDY      USERRDY SYSEVENT REC'D? *HMD 09/82*
         BNO   N08
         MVC   0(8,R8),=CL8'USERRDY,'
         LA    R8,8(0,R8)
N08      BCTR  R8,0                 BACK UP ONE
         MVI   0(R8),C' '           CLEAR COMMA
         LA    R9,1(0,R9)           UP USER COUNT
*        TPUT  LINE,L'LINE
         LA    R11,72(0,R11)        INCREMENT TO NEXT FREE LINE
         MVC   0(72,R11),LINE       MOVE FILLED LINE TO OUTBUF
         XC    CVD,CVD              CLEAR IT.
         MVI   LINE,C' '
         MVC   LINE+1(71),LINE      BLANK OUT THE WHOLE LINE
         B     LOOP1
         DROP  R5                   NO LONGER NEEDED        *HMD 09/82*
         DROP  R6                   NO LONGER NEEDED        *HMD 09/82*
ENDTAB   L     R3,WORKA             SET UP PTR
LOOP2    LA    R0,72(0,0)           LENGTH OF LINE
         LA    R3,72(0,R3)          PTR TO AN OUTPUT LINE
         LA    R1,0(0,R3)           SET REGISTER FOR TPUT
         CR    R3,R11               END OF OUTPUT?
         BH    TOT                  YES
         TPUT  (1),(0)
         LTR   R15,R15              O.K.?
         BNZ   ENDIT                NO, EXIT PROCESSOR
         B     LOOP2
TOT      CVD   R9,CVD
         C     R9,=F'1'             1 ADDRESS SPACE IN USE? *HMD 06/79*
         BNE   TOT01                INSURE CORRECT GRAMMER  *HMD 06/79*
         MVC   USERTOT+8(3),=C'IS '                         *HMD 06/79*
         MVI   USERCT+18,C' '                               *HMD 06/79*
TOT01    MVC   USERCT(4),=X'40202120'
         ED    USERCT(4),CVD+6
         TPUT  USERTOT,USERTOTL
ENDIT    L     R0,=F'21672'         LENGTH                  *HMD 01/82*
         L     R1,WORKA             ADDRESS
         FREEMAIN R,LV=(0),A=(1)
         L     R13,SAVE+4           RESTORE REG 13.
         RETURN (14,12),,RC=0                               *HMD 04/82*
         LTORG
DATD     DSECT
         DS    0F
OUTBUF   DS    21672C                                       *HMD 01/82*
         EJECT                                              *HMD 08/82*
         CVT   DSECT=YES,LIST=YES                           *HMD 08/82*
         EJECT                                              *HMD 08/82*
         IHAASVT ,                                          *HMD 08/82*
         EJECT                                              *HMD 08/82*
         IHAASCB ,                                          *HMD 08/82*
         EJECT                                              *HMD 08/82*
         IRAOUCB ,                                          *HMD 08/82*
         END   WHO
