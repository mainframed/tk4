         TITLE 'LOAD MODULE CHECK SUBROUTINE'
***********************************************************************
* PURPOSE: LOADMBR IS BALR TO FROM CHECKPDS IF THE RECFM=U FOR THE PDS*
*          LOADMBR DOES ALL THE LOAD MODULE PROCESSING.  SEE COMMENTS *
*          IN CHECKPDS FOR THE DESCRIPTION OF ITS OUTPUT.             *
*                                                                     *
*                                                                     *
* INPUT:   R1  FUNCTION CODE 0- PROCESS A MEMBER                      *
*                            1- CLOSE THE PDS (WHICH IT OPENS WITH    *
*                               BPAM).                                *
*          R3  THE ADDRESS OF THE DIRECTORY MEMBER TO BE PROCESSED.   *
*          R6  THE ADDRESS OF THE DCB FOR SYSPRINT.                   *
*                                                                     *
*                                                                     *
* WRITTEN BY ALAN FEINSTEIN, SECURITY PACIFIC NATIONAL BANK,          *
*            611 N. BRAND BLVD., GLENDALE, CALIFORNIA 91203.          *
*            TELEPHONE NO. (213)507-3329                              *
*         APRIL, 1982                                                 *
*                                                                     *
***********************************************************************
         EJECT
          MACRO
&N        BEGIN      &NAME,&BASE=R12,&TREG=R11,&SAVE=SAVEAREA,&SAW=18
           LCLA     &L
           LCLA     &M
           LCLA     &I
          AIF       ('&SYSLIST(0)' EQ '').ERROR
&N        CSECT
&I         SETA     &SAW*4+4
           AIF      ('&NAME'  EQ  '').LABEL
&L         SETA     ((K'&NAME)/2)*2+4
&M         SETA     K'&NAME
          B         &L.(0,15)
          DC        AL1(&M)
          DC        C&NAME
           AGO      .OK
.LABEL     ANOP
&M         SETA      K'&N
&L         SETA     ((K'&N+2)/2)*2+4
          B         &L.(0,15)
          DC        AL1(&M)
          DC        C'&N'
.OK         ANOP
R0        EQU    0
R1        EQU    1
R2        EQU    2
R3        EQU    3
R4        EQU    4
R5        EQU    5
R6        EQU    6
R7        EQU    7
R8        EQU    8
R9        EQU    9
R10       EQU    10
R11       EQU    11
R12       EQU    12
R13       EQU    13
R14       EQU    14
R15       EQU    15
*  SET UP ADDRESSABILITY, SAVE AREA, AND SAVE AREA CHAINING.
          STM    R14,R12,12(R13)
          LR        &BASE,R15
          USING     &N,&BASE
          LA        &TREG,&SAVE
          ST        R13,4(&TREG)
          ST        &TREG,8(R13)
          CNOP       2,4
          LR        R13,&TREG
          B          *+&I
&SAVE     DC         &SAW.F'0'
          MEXIT
.ERROR    MNOTE     12,'LABEL IS REQUIRED WITH THIS MACRO.'
          MEND
          EJECT
LOADMBR  BEGIN 'LOAD MEMBER V1.1 - &SYSDATE AT &SYSTIME'
         LTR   R1,R1              DO WE WANT TO CLOSE DATASET?
         BNZ   CLOSEPDS           NOT 0 MEANS CLOSE DATASET
         ST    R3,RSAVE           SAVE R3
         ST    R6,RSAVE+4         SAVE R6
         ST    R13,RSAVE+8        SAVE R13
         TM    PDS+48,X'10'       DID WE OPEN PDS ALREADY?
         BO    OPENNOW            YES
         OPEN  (PDS)              LET'S OPEN THE PDS
         TM    PDS+48,X'10'       DID IT OPEN O.K?
         BO    OPENNOW            YES
         PUT   (R6),=CL133' LIB DD FAILED TO OPEN IN LOADMBR '
         ABEND 777,DUMP
OPENNOW  TM    30(R3),X'80'       LINK EDITED WITH VS LKED?
         BO    CHKEXCUT           YES
         MVC   VSMSG+6(8),0(R3)   SETUP MSG
         PUT   (R6),VSMSG
CHKEXCUT TM    20(R3),X'02'       IS MODULE EXECUTABLE?
         BO    LOADIT             YES, TO SEE IF LOADABLE
         MVC   NOTEX+2(8),0(R3)   SETUP NOT EXECUTABLE MSG
         PUT   (R6),NOTEX         PRINT MSG
         LA    R15,1              INDICATE AN ERROR
         B     CHKALIAS           SEE IF IT IS AN ALIAS
*
*  ISSUE LOAD FOR LOAD-MODULE
*
LOADIT   ESTAE BADERROR        SETUP ESTAE INCASE OF SEVERE ERROR
         LOAD  EPLOC=(R3),DCB=PDS,ERRET=LOADERR
         ESTAE 0                  CANCEL ESTAE
*
*  IF WE ARE HERE, LOAD WAS O.K.  DELETE MODULE
*
         DELETE EPLOC=(R3)
         SR    R15,R15            INDICATE NO ERROR
CHKALIAS TM    11(R3),X'80'       IS THIS AN ALIAS?
         BO    ALIAS              YES, DO ALIAS PROCESSING
*
*  PREPARE TO EXIT
*
EXIT     L     R13,SAVEAREA+4     RESTORE R13
         L     R14,12(R13)        RESTORE RETURN ADDRESS
         LM    R0,R12,20(R13)       RESTORE REST OF REGS
*  RETURN
         BR    R14
         SPACE 3
*
*  THIS SECTION DOES CHECKING IF MODULE IS AN ALIAS
*
ALIAS    MVC   BLDLIST(8),36(R3)  PUT NAME OF TRUE NAME IN BLDL LIST
         TM    20(R3),X'04'       IS THIS MODULE SCATTER LOADED?
         BNO   CHKDBL             NO
         MVC   BLDLIST(8),44(R3)  YES, TRUE NAME IS 44 BYTES IN ENTRY
*  SEE IF WE HAVE WEIRD CASE WHERE MODULE IS ALIAS OF ITSELF
CHKDBL   CLC   0(8,R3),BLDLIST    ARE THE NAMES THE SAME?
         BNE   DOBLDL             NO
         MVC   DBLMSG+6(8),0(R3)  MOVE NAME INTO MSG
         PUT   (R6),DBLMSG        PRINT MSG
         SPACE
*  DO THE BLDL FOR THE TRUE NAME
         SPACE
DOBLDL   BLDL  PDS,BLDLTAB
         LTR   R15,R15            TEST R.C. FROM BLDL
         B     *+4(R15)
         B     FOUNDIT            R.C.=0
         B     NOTFND             R.C.=4
         B     RC8                R.C.=8
*
*  IF TRUE NAME IS FOUND CHECK TTRS
*
FOUNDIT  SR    R15,R15            SETUP FOR NO ERROR
         CLC   8(3,R3),BLDLIST+8  COMPARE TTRS
         BE    EXIT               EQ MEANS EVERYTING O.K
         MVC   MISSTTR+2(8),0(R3) TELL TTRS NOT THE SAME
         MVC   MISSTTR+34(8),BLDLIST  MOVE NAMES IN MSG
         PUT   (R6),MISSTTR       PRINT MSG
         LA    R15,1              INDICATE AN ERROR
         B     EXIT               RETURN
*
*  TRUE NAME WAS NOT FOUND
*
NOTFND   MVC   NOALIAS+2(8),0(R3) PUT MEMBER NAME IN MSG
         MVC   NOALIAS+40(8),BLDLIST PUT TRUE NAME IN MSG
         MVC   NOALIAS+54(8),BLDLIST
         PUT   (R6),NOALIAS       PRINT MSG
         LA    R15,1              INDICATE AN ERROR
         B     EXIT               RETURN
*
*  IF HERE, WE GOT A RETURN CODE 8: EITHER AN I/O ERROR OF INSUFFICIENT
*    STORAGE.
*
RC8      MVC   RC8NME1(8),0(R3)   MOVE MEMBER NAME INTO MSG
         MVC   RC8NME2(8),BLDLIST MOVE TRUE NAME INTO MSG
         PUT   (R6),RC8MSG        PRINT MSG
         LA    R15,1              INDICATE AN ERROR
         B     EXIT               RETURN
         SPACE
*
*  CLOSE THE PDS IF WE HAVE OPENNED IT
*
CLOSEPDS TM    PDS+48,X'10'       DID WE OPEN IT?
         BNO   EXIT               NO, RETURN
         CLOSE PDS                CLOSE THE DATASET
         B     EXIT               RETURN
         SPACE 3
*
*  THIS SECTION HANDLES ERROR IF LOAD IS NOT SUCCESSFULL
*
         SPACE
LOADERR  STH   R1,ERCODE          SAVE ABEND CODE IN R1
         STC   R15,REACODE        SAVE THE REASON CODE IN R15
LOADERR1 UNPK  ABCODE(5),ERCODE(3) UNPACK THE ABEND CODE
         NC    ABCODE(4),=4X'0F'
         TR    ABCODE(4),=C'0123456789ABCDEF'  MAKE PRINTABLE
         MVI   ABCODE+4,C'-'      SETUP FOR REASON CODE
         UNPK  ABCODE+5(3),REACODE(2) UNPACK THE ABEND CODE
         NC    ABCODE+5(2),=2X'0F'
         TR    ABCODE+5(2),=C'0123456789ABCDEF' MAKE PRINTABLE
         MVI   ABCODE+7,C' '
         MVI   ABCODE,C'S'
         MVC   NAME(8),0(R3)      MOVE IN MEMBER NAME
         PUT   (R6),ERRMSG        PRINT THE MSG
         LA    R15,1              INDICATE AN ERROR
         B     EXIT               RETURN
         SPACE 3
*
*  THIS ESTAE CODE WAS PUT IN TO CATCH AN ERRORS WHILE 'LOAD' WAS
*   INCONTROL (YES, IT ACTUALLY HAPPENED)
*
BADERROR DS    0H
         USING BADERROR,R15
         L     R12,=A(LOADMBR)    RESTORE BASE REG OF LOADMBR
         DROP  R15
         C     R0,=F'12'          DID WE GET SDWA?
         BE    NOSDWA             NO
         MVC   ERCODE(2),SDWACMPC-SDWA(R1) LOAD ERROR CODE(ABEND)
         MVC   REACODE(1),SDWAICD1-SDWA(R1) PUT INTERRUPT CODE IN
         B     RETRY             RETURN
NOSDWA   STH   R1,ERCODE         SAVE ERROR CODE
         MVI   REACODE,X'00'     0 OUT REASON CODE
         LA    R15,4             INDICATE RETRY
         LA    R0,LOADERR1       RETRY ADDRESS
         B     RETRY1            SKIP SETRP
RETRY    SETRP RC=4,RETADDR=LOADERR1,RETREGS=NO,FRESDWA=YES
RETRY1   L     R3,RSAVE          MAKE SURE R3 IS O.K.
         L     R6,RSAVE+4        MAKE SURE R4 IS O.K.
         L     R13,RSAVE+8       MAKE SURE R13 IS O.K.
         NC    ERCODE(2),=X'0FFF'   CLEAR HIGH ORDER BIT
         SR    R2,R2             ZERO OUT R2
         ICM   R2,B'0011',ERCODE PICKUP ABEND CODE
         SRL   R2,4              SHIFT IT OVER A NIBBLE
         STH   R2,ERCODE           AND SAVE
RETURN   BR    R14               RETURN TO MAINLINE
         EJECT
*
*  CONSTANTS, MSGS, DCBS, ETC.
*
RSAVE    DS    3F
ERCODE   DC    H'0',X'0D'
REACODE  DC    X'000D'
ERRMSG   DS    0CL133
         DC    C'  ERROR LOADING '
NAME     DC    CL8' '
         DC    C', '
ABCODE   DC    CL9' '
         DC    (133-(*-ERRMSG))C' '
BLDLTAB  DS    0H
NO       DC    H'1'
SIZE     DC    H'12'
BLDLIST  DC    CL8' '
         DC    XL4'00'
MISSTTR  DC    CL133'  XXXXXXXX IS NOT A TRUE ALIAS OF XXXXXXXX '
NOALIAS  DC    CL133'  XXXXXXXX IS SUPPOSE TO BE AN ALIAS OF XXXXXXXX, X
               BUT XXXXXXXX DOES NOT EXIST'
NOTEX    DC    CL133'  XXXXXXXX IS NOT AN EXECUTABLE LOAD MODULE'
RC8MSG   DS    0CL133
         DC    C' EITHER AN I/O ERROR OCCURRED, OR THERE WAS INSUFFICIEX
               NT STORAGE WHILE SEARCHING FOR ALIAS OF '
RC8NME1  DC    CL8' '
         DC    C'('
RC8NME2  DC    CL8' '
         DC    C')'
         DC    (133-(*-RC8MSG))C' '
VSMSG    DC    CL133'  -->          WAS NOT LINK-EDITED WITH VS LINKAGEX
                EDITOR <--'
DBLMSG   DC    CL133'  -->          IS AN ALIAS OF ITSELF <--'
PDS      DCB   DDNAME=LIB,DSORG=PS,MACRF=R
         IHASDWA
         END
