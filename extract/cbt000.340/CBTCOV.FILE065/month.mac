MONTH    CSECT
R0       EQU   0
R1       EQU   1
R14      EQU   14
R15      EQU   15
         SAVE  (14,12),,*
         LR    11,15
         USING MONTH,11
         LA    2,SAVER
         ST    2,8(13)
         ST    13,SAVER+4
         LR    13,2
         TIME
         ST    R0,ZWORK              STORE TIME
         UNPK  ZWORK(7),ZWORK(4)     CHANGE TO CHARACTER
         MVC   ZTIME(2),ZWORK        PLACE IN TIME
         MVC   ZTIME+3(2),ZWORK+2
         MVC   ZTIME+6(2),ZWORK+4
         SR    R0,R0                 MAKE YR & DAY DBL WORD
         STM   R0,R1,ZWORK
         UNPK  ZDATE+7(3),ZWORK+5(2) PLACE YR IN OUTPUT
         CVB   R1,ZWORK              YR & DAY DBL BINARY WORD
         D     R0,=F'1000'           YR IN R1, DAY IN R0
         BCTR  R1,0                  YR-1
         LR    R15,R1                YR-1
         SRL   R15,2                 (YR-1)/4
         N     R1,F3                 (YR-1) MOD 4
         MH    R15,H5                (YR-1)/4*5
         AR    R15,R1                (YR-1)/4*5 + ((YR-1) MOD 4)
         AR    R15,R0                (YR-1)/4*5 + ((YR-1) MOD 4)+DAYS
         SR    R14,R14               DBL WORD LONG
         D     R14,F7                DAY OF WEEK IN R14
         MH    R14,H9                INDEX DAY NAME TABLE
         LA    R14,ZDAYTAB(R14)      GET THE DAY
         MVC   ZDAY,0(R14)
         IC    R1,ZFEBTAB(R1)        28 OR 29 DAYS IN FEB
         STC   R1,ZFEB
         LA    R1,ZTAB-4             POINT AT MONTH TABLE
         SR    R15,R15
ZLOOP    LA    R1,4(0,R1)            POINT AT NEXT MONTH
         IC    R15,0(,R1)            PICK UP NO OF DAYS IN MONTH
         SR    R0,R15                SUBTRACT NO OF DAYS IN MONTH
         BH    ZLOOP                 DONE IF 0 OR NEGATIVE
         AR    R0,R15                ADD BACK LAST MONTH
         CVD   R0,ZWORK              CHANGE DAY IN MONTH TO BE PACKED
         MVC   ZDATE-2(4),ZEDIT      EDIT DAY INTO OUTPUT
         ED    ZDATE-2(4),ZWORK+6
         MVC   ZDATE+3(3),1(R1)      MOVE NAME OF MONTH TO OUTPUT
         MVC   ZDAYLIST(9),ZDAY
         MVC   ZDAYLIST+10(9),ZDATE
         MVC   ZDAYLIST+21(9),ZTIME
         LA    15,ZDAYLIST
         L     13,SAVER+4
         RETURN (14,12),RC=(15)
         EJECT
ZTAB     DC    AL1(31),C'JAN'
ZFEB     DC    AL1(28),C'FEB'
         DC    AL1(31),C'MAR'
         DC    AL1(30),C'APR'
         DC    AL1(31),C'MAY'
         DC    AL1(30),C'JUN'
         DC    AL1(31),C'JUL'
         DC    AL1(31),C'AUG'
         DC    AL1(30),C'SEP'
         DC    AL1(31),C'OCT'
         DC    AL1(30),C'NOV'
         DC    AL1(31),C'DEC'
ZFEBTAB  DC    AL1(28,28,28,29)
ZDAYTAB  DC    C'MONDAY   '
         DC    C'TUESDAY  '
         DC    C'WEDNESDAY'
         DC    C'THURSDAY '
         DC    C'FRIDAY   '
         DC    C'SATURDAY '
         DC    C'SUNDAY   '
ZWORK    DC    D'0'
         DC    2X'40'
ZDATE    DC    C'XX XXX XX'          DO NOT REMOVE THIS CARD
         DC    X'40'
ZTIME    DC    C'XX:XX:XX'
ZDAY     DC    CL9' '
ZEDIT    DC    C' ',X'202120'
ZDAYLIST DS    CL29
F3       DC    F'3'
H5       DC    H'5'
F7       DC    F'7'
H9       DC    H'9'
SAVER    DS    18F
         END
