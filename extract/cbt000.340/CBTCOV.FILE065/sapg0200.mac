SAPG0200 START 0
         DS    0H
         STM   14,12,12(13)
         BALR  3,0
         USING *,3,4
START    L     4,BASEADDR
         B     EXEC
BASEADDR DC    A(START+4096)
EXEC     ST    13,SAVE+4
         LA    13,SAVE
         REGISTER     EQATES AND USAGE
*
*     OPEN THE FILES AND CHECK THEM
*
         OPEN  (INFILE,,OUTFILE,OUTPUT,REPORT,OUTPUT)
         OPEN  BLOCK
         LTR   R15,R15
         BNZ   ERROR
READIN   DS    0H
         GET   INFILE,INREC                   GET THE RECORD
         CLC   INREC+1(2),=2X'00'             IS RECORD BLANK
         BE    READIN                         YES GO GET ANOTHER
CHECKER  DS    0H
         CLC   RECID,RECHOLD                  COMPARE AGAINST LAST
         BNE   GOFORIT                        NOT EQUAL GO AROUND
         MVC   ERRMSG,RECID                   MOVE RECID TO ERROR
         PUT   REPORT,ERRLINE                 PRINT THE LINE
         MVC   RECHOLD,RECID                  MOVE INPUT TO HOLD AREA
         B     READIN                         GO READ A RECORD
GOFORIT  DS    0H
         MVC   OUTREC,RESTOF+27               MOVE FOR MICRO OUTPUT
         PUT   OUTFILE,OUTREC                 PRINT THE LINE
         MVC   IN,INREC                       MOVE FOR INDEX FILE
         PUT   RPL=POSITION                   WRITE IT TO FILE
         LTR   R15,R15                        TEST RETURN CODE
         BNZ   ERROR                          IF NOT ZERO ERROR RUTINE
         B     READIN                         ELSE GO GET ANOTHER REC
EOFRTN   DS    0H
         CLOSE (INFILE,,OUTFILE,,REPORT)      CLOSE OUTPUT FILE
         CLOSE BLOCK                          CLOSE VSAM
         SR    R15,R15
         IC    R15,RETCD
         L     R13,SAVE+4
         RETURN (14,12),RC=(15)
         EJECT
ABEND    ABEND 1369,DUMP
ERROR    SHOWCB RPL=POSITION,AREA=FDBKAREA,LENGTH=4,FIELDS=(FDBK)
         LTR   R15,R15
         BNZ   ABEND
         MVC   HEXFIELD,FDBKCODE              MOVE
         L     R4,BCON2                       NUMBER OF CHARACTERS
         LA    R5,HEXFIELD                    HEX FIELD TO BE CONV
         LA    R6,RESULT                      ANSWER IN HERE
LOOP2    MVC   BYTE1,0(R5)                    MOVE FIRST BYTE
         UNPK  CL3,PL2                        UNPACK IT
         TR    CL3,TRTAB                      TRANSLATE IT
         MVC   0(2,R6),CL3                    MOVE TO OUT AREA
         LA    R5,1(,R5)                      LOAD NEXT BYTE
         LA    R6,2(,R6)                      INCREMENT
         BCT   R4,LOOP2                       GO DO IT AGAIN (2 TIMES)
RETURN   MVC   BAD,RESULT                     MOVE RESULT TO BAD
         MVC   ERRMSG,RECID
         PUT   REPORT,ERRLINE
         MVI   RETCD,16                       MOVE IN RETURN CODE
         B     EOFRTN                         GO CLOSE ALL OF THIS
*
         LTORG
INFILE   DCB   DDNAME=INFILE,DSORG=PS,MACRF=GM,EODAD=EOFRTN
REPORT   DCB   DDNAME=SYSOUT,DSORG=PS,MACRF=PM,LRECL=132
OUTFILE  DCB   DDNAME=OUTFILE,DSORG=PS,MACRF=PM,LRECL=133
BLOCK    ACB   DDNAME=FILEVSAM,AM=VSAM,MACRF=(KEY,SEQ,OUT)
POSITION RPL   ACB=BLOCK,AM=VSAM,AREA=IN,AREALEN=160,RECLEN=160,       X
               OPTCD=(KEY,SEQ,SYN,NUP,MVE)
*
BCON2    DC    F'2'
RESULT   DC    CL80' '
PL2      DS    0PL2
BYTE1    DC    XL1'00'
BYTE2    DC    XL1'0F'
CL3      DS    CL3
TRTAB    DC    XL256'00'
         ORG   TRTAB+X'F0'
         DC    CL16'0123456789ABCDEF'
         ORG   TRTAB+256
*
RETCD    DC    X'0'                 RETURN CODE
         DC    X'0'
*
IN       DS    CL160                INPUT AREA
*
RECHOLD  DS    CL27
*
HEXFIELD DS    XL1
*
FDBKAREA DS    0F                   ERROR CODES
         DS    XL1
R15CODE  DS    XL1
FTNCODE  DS    XL1
FDBKCODE DS    XL1
         DS    0F
*
INREC    DS    0CL160               INPUT AREA
RECID    DS    CL27
RESTOF   DS    CL133
*
OUTREC   DS    CL133                MICRO-FICHE OUTPUT
*
ERRLINE  DS    0CL132               SYSOUT ERROR MESSAGE AREA
         DS    CL2
BAD      DS    CL2
         DC    CL6'  --  '
ERRMSG   DS    CL27
ERRTRANS DS    CL97
*
SAVE     DS    18F                   SAVE AREA
*
         END   SAPG0200
