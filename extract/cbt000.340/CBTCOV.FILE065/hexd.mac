HEXD     TITLE 'SUBROUTINE FOR HEXADECIMAL DISPLAY'
*
*      THIS PROGRAM CONVERTS HEXADECIMAL DATA INTO A CHARACTER STRING
* TWICE AS LONG AS THE INPUT DATA; E.G., X'01' WOULD BE CONVERTED TO
* C'01'. THE CALLING SEQUENCE FOR "HEXD" IS AS FOLLOWS:
*           1. THE INPUT HEXADECIMAL DATA
*           2. THE OUTPUT HEXADECIMAL DISPLAY DATA
*           3. THE LENGTH OF THE INPUT DATA IN BYTES
*                  (FULL WORD BINARY)
* IF THE PROGRAM COMPLETES SUCCESSFULLY, REGISTER 15 IS SET TO ZERO,
* OTHERWISE, IT IS SET TO 12.
*      "HEXD" IS REENTRANT AND REUSEABLE.
*
HEXD     ENTER PARMREG=2,         SAVE REGISTERS, INITIALIZE           X
               GETMAIN=(WORKSIZE,0)
         REGISTER                 REGISTER EQUATES
         USING WORKAREA,R13       ADDRESS WORK AREA DSECT
         LM    R4,6,0(R2)         LOAD PARAMETER POINTERS
         L     R6,0(,R6)          LOAD LENGTH OF DATA STRING
         LTR   R6,R6              IF LENGTH IS NOT POSITIVE
         BNP   ERROR                 THEN FLAG AS AN ERROR
LOOP     DS    0H
         MVC   WORK1(1),0(R4)     MOVE ONE INPUT BYTE
         UNPK  WORK2,WORK1        UNPACK IT INTO WORK AREA
         NC    WORK2(2),=X'0F0F'  TURN OFF HIGH ORDER HALF-BYTES
         TR    WORK2(2),TRTABLE   TRANSLATE INTO HEX DISPLAY
         MVC   0(2,R5),WORK2      MOVE TO OUTPUT AREA
         LA    R5,2(,R5)          INCREMENT OUTPUT ADDRESS
         LA    R4,1(,R4)          INCREMENT INPUT ADDRESS
         BCT   R6,LOOP            DO THE NEXT INPUT BYTE
         LA    R15,0              CLEAR THE RETURN CODE
         B     EXIT                  AND EXIT
ERROR    DS    0H
         LA    R15,12             SET RETURN CODE TO 12
EXIT     DS    0H
         LEAVE RETCODE=(15),      RESTORE REGISTERS, RETURN TO CALLER  X
               GETMAIN=(WORKSIZE,0)
         EJECT
TRTABLE  DC    C'0123456789ABCDEF'
         SPACE 3
WORKAREA DSECT ,                  MAP OF GETMAINED WORK AREA
         DS    18A                REGISTER SAVE AREA
WORK1    DS    CL2
WORK2    DS    CL3
WORKSIZE EQU   *-WORKAREA
         END
