SPY      TITLE 'S P Y --  MVS CONSOLE SPY PROGRAM  --  VERSION 4.0'
***********************************************************************
*                                 S P Y                               *
*                                                                     *
*                               07/07/82                              *
*                                                                     *
*                       OPERATOR CONSOLE MONITOR                      *
*                                                           *HMD 07/82*
*                          (MVS/SP 1.3 SUPPORT)             *HMD 07/82*
*                                                                     *
*     THIS PROGRAM DISPLAYS THE CONTENTS OF ALL ACTIVE GRAPHIC        *
*     OPERATOR'S CONSOLES ON A TSO CRT. THE OPERATOR'S SCREEN CAN BE  *
*     EITHER A 327X OR A 370-168 INTEGRATED CONSOLE WITH 35 LINES.    *
*     THE TSO USER CAN USE ANY 327X TERMINAL.                         *
*                                                                     *
*     SPY MUST, ALAS, BE APF AUTHORIZED. THE OPERATOR CONSOLE BUFFERS *
*     AND CONTROL BLOCKS ARE NOW IN FETCH PROTECTED STORAGE. BE       *
*     SURE TO PLACE THE 'SPY' COMMAND IN IKJEFTE2 AND/OR IKJEFTE8     *
*     SO THAT IT WILL RETAIN APF AUTHORIZATION FROM TSO.              *
*                                                                     *
*     SPY GIVES A TSO USER THE CAPABILITY TO ENTER ANY                *
*     CONSOLE OPERATOR COMMAND. THIS FACILITY IN PROTECTED BY A       *
*     SIMPLE 3 CHARACTER PASSWORD (IN VIPWORD) TO PREVENT YOU         *
*     FROM ENTERING AN OPERATOR COMMAND BY ACCIDENT. THE SYNTAX IS    *
*                                                                     *
*     JES COMMAND:  $...  ANY JES COMMAND ...                         *
*     OS COMMAND:   /...  ANY OS COMMAND ...  (THE / IS STRIPPED OFF) *
*                                                                     *
*     TYPING THE 3-LETTER VIP PASSWORD 'TOGGLES' THE VIP FLAG.        *
*     THIS FACILITY WORKS BY PASSING THE COMMAND TO THE COMMAND       *
*     PROGRAM AS A PARM, WHO IN TURN EXECUTES IT USING SVC 34.        *
*     SPY AND COMMAND MUST BOTH BE APF AUTHORIZED IF THEY ARE         *
*     TO WORK PROPERLY IN CONCERT. NOTE THAT THE RESPONSE TO THE      *
*     COMMAND APPEARS ONLY ON THE SCREEN YOU ARE WATCHING AT THE      *
*     TIME. ALSO, YOUR COMMANDS HAVE THE SAME CONSOLE AUTHORITY       *
*     AS THE SCREEN YOU ARE WATCHING, INCLUDING THE MASTER CONSOLE.   *
*     THE MORAL BEING DON'T WATCH THE MASTER CONSOLE AND ACCIDENTALLY *
*     ENTER '/QUIESCE' OR SOMETHING LIKE THAT... <===                 *
*                                                                     *
*     THIS PROGRAM WILL PROBABLY REQUIRE BOTH THE SYS1.AMODGEN AND    *
*     SSU.MACLIB MACRO LIBRARIES TO ASSEMBLE PROPERLY.                *
*                                                                     *
*     SPY WAS DEVELOPED ON A 370/168 MVS RELEASE 3.7; IT WILL WORK    *
*     ON MVS 3.8 WITH OR WITHOUT SE/2.                                *
*                                                                     *
*                        STEVE LANGLEY                                *
*                        SOUTHERN CALIF. EDISON                       *
*                        ROSEMEAD, CALIF   91770                      *
*                        213-572-3435                                 *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT                                              *HMD 07/82*
***********************************************************************
*                                                           *HMD 07/82*
*                                                           *HMD 07/82*
*     THIS VERSION OF SPY SUPPORTS MVS/SE II,               *HMD 07/82*
*     MVS/SP1.1, MVS/SP 1.3. THE RESULTANT LOAD MODULE      *HMD 07/82*
*     CAN RUN ON EITHER SYSTEM. IF THE MACROS FOR           *HMD 07/82*
*     MVS/SP 1.3 ARE AVAILABLE, THEY CAN BE USED FOR        *HMD 07/82*
*     ASSEMBLY. IF NOT, THE FOLLOWING SYMBOLIC              *HMD 07/82*
*     VARIABLES MUST BE USED FOR CONDITIONAL ASSEMBLY:      *HMD 07/82*
*                                                           *HMD 07/82*
*     &SP3 SETB 0 = SP 1.3 MACROS ARE NOT AVAILABLE         *HMD 07/82*
*     &SP3 SETB 1 = SP 1.3 MACROS ARE AVAILABLE             *HMD 07/82*
*                                                           *HMD 07/82*
*                                                           *HMD 07/82*
*     THE MVS/SP 1.3 VERSION OF SPY WILL PROBABLY WORK      *HMD 07/82*
*     ON MVS/SP 1.2, HOWEVER THIS ASSUMPTION HAS NEVER      *HMD 07/82*
*     BEEN TESTED.                                          *HMD 07/82*
*                                                           *HMD 07/82*
*     NOTE THAT THIS PROGRAM REQUIRES THE FOLLOWING         *HMD 07/82*
*     LICENSED MACROS:                                      *HMD 07/82*
*                                                           *HMD 07/82*
*     IEERDCM        FROM MVS SP OPTIONAL MATERIAL          *HMD 07/82*
*     IEETDCM        FROM MVS SP OPTIONAL MATERIAL          *HMD 07/82*
*                                                           *HMD 07/82*
*     THESE MACROS ARE NOT SUPPLIED WITH THIS SOURCE.       *HMD 07/82*
*                                                           *HMD 07/82*
*     PLEASE DIRECT ALL QUESTIONS AND COMMENTS TO:          *HMD 07/82*
*                                                           *HMD 07/82*
*     HOWARD M. DEAN                                        *HMD 07/82*
*     SENIOR SYSTEMS PROGRAMMER                             *HMD 07/82*
*     GTE DATA SERVICES                                     *HMD 07/82*
*     4750 LINCOLN BLVD.                                    *HMD 07/82*
*     MARINA DEL REY, CA 90291                              *HMD 07/82*
*     (213) 821-0511 X330                                   *HMD 07/82*
***********************************************************************
      EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*  COMMAND     DESCRIPTION                                            *
*                                                                     *
*     B        END SPY                                                *
*     C        SWITCH MONITOR TO CONSOLE 1                            *
*     CXX      SWITCH MONITOR TO CONSOLE XX                           *
*     DXX      SET DELAY TO XX TENTHS SECONDS                         *
*     E        END SPY                                                *
*     F        FREEZE DISPLAY ON CURRENT PAGE                         *
*     L        LINK TO SWAP PROGRAM (IF AVAILABLE)                    *
*     R        RELEASE DISPLAY                                        *
*     S        STATUS OF ALL CONSOLES                                 *
*     W        START TIMER MODE FOR 30 SECONDS                        *
*     WXX      START TIMER MODE FOR XX SECONDS                        *
*     W0       START TIMER MODE UNTIL INTERRUPT                       *
*     ?        DISPLAY THIS PAGE                                      *
*     1        DISPLAY MODE 1  (TOP 22, BOTTOM 12)                    *
*     2        DISPLAY MODE 2  (TOP 22, BOTTOM 22)                    *
*                                                                     *
*  HITTING INTERRUPT WILL STOP THE WAIT TIMER                         *
*                                                                     *
*     DISPLAY MODES 1 AND 2 ARE ONLY USEFUL WHEN WATCHING A LARGE     *
*     35-LINE 3066 370-168 INTEGRATED CONSOLE FROM A 24-LINE 327X-2   *
*     TSO TERMINAL. THESE MODES ARE:                                  *
*                                                                     *
*     DISPLAY MODE 1 - PAGE 1 = TOP 22 LINES                          *
*                      PAGE 2 = BOTTOM 12 LINES                       *
*     DISPLAY MODE 2 - PAGE 1 = TOP 22 LINES                          *
*                      PAGE 2 = BOTTOM 22 LINES                       *
*                                                                     *
***********************************************************************
         EJECT
SPY      CSECT                                              *HMD 07/81*
         LCLB  &SP3              MVS/SP 1.3 SUPPORT         *HMD 07/82*
         LCLC  &DATE             ASSEMBLY DATE FOR HELP     *HMD 07/81*
&DATE    SETC  '&SYSDATE'(1,3).'&SYSDATE'(7,2).')'          *HMD 07/81*
&SP3     SETB  1   <==== CHANGE THIS TO 0 IF SP 1.3 MACROS  *HMD 07/82*
*                        ARE NOT AVAILABLE.                 *HMD 07/82*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
SPY      CSECT
         B     112(R15)           BRANCH AROUND SAVE AREAS
         DC    CL9'SPY'           IDENTIFIER
         DC    CL9'&SYSDATE'
         DC    CL7'&SYSTIME'
         DC    CL11'VERSION 4.0'                            *HMD 07/82*
SAVE     DC    18F'0'             SAVE AREA
         STM   R14,R12,12(R13)    SAVE REGISTERS
         LR    R12,R15            R12 = ADDR OF ENTRY POINT
         USING SPY,R12,R11,R10    ADDRESABILITY TO CSECT
         LA    R11,SAVE           R11 = ADDR OF OUR SAVE AREA
         ST    R13,SAVE+4         SAVE POINTER TO CALLERS SAVE AREA
         ST    R11,8(R13)         SAVE PTR TO OUR SAVE AREA IN CALLER'S
         LR    R13,R11            R13 = ADDR OF OUR SAVE AREA
         LA    R11,4095(R12)      R11 WILL BE
         LA    R11,1(R11)         SECOND BASE REGISTER
         LA    R10,4095(R11)      R10 WILL BE
         LA    R10,1(R10)         THIRD  BASE REGISTER      *HMD 07/81*
         EJECT
***********************************************************************
*    CHECK FOR AUTHORIZED USER -                                      *
*        AUTHORIZATION IS DETERMINED BY OPERATOR AUTHORITY            *
*                                                                     *
***********************************************************************
         L     R3,CVTPTR          LOAD CVT POINTER          *HMD 07/81*
         USING TCBLIST,R3         ADDRESS MAGIC DOUBLEWORD  *HMD 07/81*
         L     R3,0(R3)           LOAD PSATOLD ADDRESS      *HMD 07/81*
         L     R3,TCBCUR          GET MY TCB ADDRESS        *HMD 07/81*
         USING TCB,R3             ADDRESS THE TCB           *HMD 07/81*
         ICM   R3,7,TCBJSCBB      GET JSCB ADDRESS          *HMD 07/81*
         USING IEZJSCB,R3         ADDRESS THE JSCB          *HMD 07/81*
         ICM   R3,15,JSCBPSCB     INSERT PSCB ADDRESS       *HMD 07/81*
         BZ    ALLDONE1           NO PSCB: NOT TSO          *HMD 07/81*
         USING PSCB,R3            ADDRESS THE PSCB          *HMD 07/81*
         MVC   USERID(7),PSCBUSER SAVE USERID               *HMD 08/81*
         ST    R3,ADDRPSCB        SAVE PSCB ADDRESS         *HMD 08/81*
         TM    PSCBATR1,PSCBCTRL  OPERATOR AUTHORITY        *HMD 07/81*
         BZ    NOTAUTH            NO: ALL DONE HERE         *HMD 07/81*
         DROP  R3                                           *HMD 07/81*
         SPACE 1                                            *HMD 07/82*
***********************************************************************
*                                                                     *
*        CHECK FOR PCAUTH ADDRESS SPACE IN ASVT                       *
*        IF SO,  SET FLAG TO X'FF' - WE ARE RUNNING MVS/SP 1.3        *
*        IF NOT, SET FLAG TO X'00' - WE ARE NOT RUNNING MVS/SP 1.3    *
*                                                                     *
***********************************************************************
PCAUTH   MVI   SP3FLAG,X'00'      CLEAR MVS/SP 1.3 FLAG     *HMD 07/82*
         L     R3,CVTPTR          ADDRESS OF CVT            *HMD 07/82*
         USING CVT,R3             ADDRESSABILITY            *HMD 07/82*
         L     R3,CVTASVT         ADDRESS OF ASVT           *HMD 07/82*
         USING ASVT,R3            ADDRESSABILITY            *HMD 07/82*
         L     R3,ASVTENTY+4      POINT TO 2ND ASCB         *HMD 07/82*
         USING ASCB,R3            ADDRESSABILITY            *HMD 07/82*
         L     R3,ASCBJBNS        LOAD START/MOUNT JOBNAME  *HMD 07/82*
         CLC   0(8,R3),PCAUTHN    IS THIS 'PCAUTH'?         *HMD 07/82*
         BNE   EXITSCAN           NOPE, WE ARE NOT ON SP/3  *HMD 07/82*
         MVI   SP3FLAG,X'FF'      YES, SET FLAG FOR SP3     *HMD 07/82*
EXITSCAN DS    0H                                           *HMD 07/82*
         DROP  R3                                           *HMD 07/82*
         SPACE 1                                            *HMD 07/82*
***********************************************************************
*                                                           *HMD 07/82*
*  TEST FOR AND OPTIONALLY SET AUTHORIZATION (APF)          *HMD 07/82*
*                                                           *HMD 07/82*
***********************************************************************
         TESTAUTH FCTN=1          ARE WE APF AUTHORIZED?    *HMD 01/82*
         LTR   R15,R15            ARE WE?                   *HMD 01/82*
         BZ    DOSTAX             YES, NO MAGIC SVC NEEDED  *HMD 01/82*
* INSERT YOUR MAGIC SVC (SAMPLE IS IGC0025D ON THIS TAPE) TO TURN ON
* APF AUTHORIZATION. THIS WILL ALLOW SPY TO BE USED UNDER SPF OPTION
* NUMBER 6 AND WILL SAVE YOU THE TROUBLE OF UPDATING THE IKJEFT02
* TABLES.
* IF MAGIC SVC FAILS, WE WILL BLOWUP WHEN MODESET
* IS ISSUED, SO NOT TO WORRY.
         MVI   AUTHFLG,X'FF'      INDICATE WE TURNED ON AUTH*HMD 01/82*
         EJECT                                              *HMD 07/81*
***********************************************************************
*                                                                     *
*                       PROGRAM INITIALIZATION                        *
*                                                                     *
***********************************************************************
DOSTAX   LA    R3,STAXLIST  R3 = ADDRESS OF STAX LIST MACRO *HMD 01/82*
         STAX  ATTNEXIT,MF=(E,(3)) ATTN EXIT TRAP
         SPACE 2
         GTSIZE ,                 GET SCREEN SIZE           *HMD 07/82*
         ST    R1,SAVESIZE        SAVE CURRENT LINESIZE     *HMD 07/81*
         LTR   R0,R0              R0 = NUMBER OF LINES PER SCREEN
         BZ    HARDCOPY           IF NONZERO ASSUME A CRT IS IN USE
         EJECT
***********************************************************************
*                                                                     *
*                     THIS TERMINAL IS A CRT                          *
*                                                                     *
***********************************************************************
CRT      STH   R0,LPSCREEN        R0 = LINES PER SCREEN
         STH   R1,CPLINE          R1 = CHARACTERS PER LINE
         CH    R0,=H'24'          IS USER ON A 3277?
         BE    FLSCREEN           YES, JUST CONTINUE
         MVI   MOD4FLG,X'FF'      SET 3278-4 FLAG ON
         MVC   CMDCTRL(3),R41C1   ROW 41, COL 1
         MVC   PHEADING(3),R42C1  ROW 42, COL 1
         MVC   R24C1(3),R43C1     ROW 43, COL 1
         MVC   TRALR(3),R43C1     MOVE TO HELP TRAILER      *HMD 07/81*
         MVC   TRALX(3),R43C23    ROW 43, COL 23            *HMD 07/81*
         MVC   CLEARX(3),R43C80   ROW 43, COL 80            *HMD 07/81*
FLSCREEN STFSMODE ON,INITIAL=YES  TURN ON VTAM FULL SCREEN MODE
         B     BLDUCMS
         SPACE
HARDCOPY STSIZE SIZE=80           OTHERWISE, HARDCOPY; SET LSIZE=80
         MVI   CRTFLAG,X'00'      WE ARE USING A HARDCOPY
         MVC   CMDCTRL(5),BLANKS  ZAP OUT 3277 CNTRL CHARS
         MVC   PHEADING(5),BLANKS ZAP OUT 3277 CNTRL CHARS
         EJECT
***********************************************************************
*                                                                     *
*          BUILD A TABLE OF UCM ADDRESSES (ONE PER CONSOLE)           *
*                                                                     *
***********************************************************************
BLDUCMS  DS    0H                                           *HMD 01/80*
         L     R4,CVTPTR          R4 = ADDR OF CVT
         USING CVT,R4
         L     R4,CVTCUCB         R4 = ADDR OF 'CUCB' (UCM BASE)
         USING UCM,R4
         LR    R2,R4              R2 = R4                   *HMD 07/81*
         S     R2,=F'4'           R2 = ADDR OF ADDR OF UCMPRFX
         L     R2,0(R2)           R2 = ADDR OF UCMPRFX      *HMD 07/81*
         USING UCMPRFX,R2         TELL ASSEMBLER            *HMD 07/81*
         AIF   (NOT &SP3).L001                              *HMD 07/82*
         LH    R5,UCMCTID         CONSOLE TASK ASID         *HMD 07/82*
         STH   R5,CONSASID        SAVE FOR SSAR             *HMD 07/82*
.L001    ANOP                                               *HMD 07/82*
         L     R5,UCMVEA          R5 = ADDR OF FIRST UCM ENTRY
         L     R6,UCMVEZ          R6 = LENGTH OF EACH UCM ENTRY
         L     R7,UCMVEL          R7 = ADDR OF LAST UCM ENTRY
         LA    R8,UCMTAB+UCMTABL  R8 = ADDR OF UCMTAB       *HMD 07/81*
         L     R9,UCMTABE         R9 = ADDR OF TABLE END    *HMD 07/81*
         XR    R3,R3              R3 = 0 (NUMBER OF VALID UCMS)
UCMLOOP  ST    R5,0(R8)           SAVE UCM ADDRESS IN UCMTAB
         LA    R3,1(R3)           R3 = R3 + 1  (ONE MORE UCM)
         C     R5,UCMMCENT        IS THIS UCM THE MASTCONS? *HMD 07/81*
         BNE   UCMNEXT            NO, GO LOOK AT NEXT ENTRY *HMD 07/81*
         ST    R3,CONSOLE         START WITH THIS CONSOLE   *HMD 07/81*
         ST    R3,MCONS           SAVE MASTER CONSOLE ADDR  *HMD 07/81*
UCMNEXT  LA    R8,UCMTABL(R8)     R8 = ADDR OF NEXT UCMTAB ENTRY
         CR    R8,R9              DOES R8 POINT PAST END OF UCMTAB?
         BNL   UCMFULL            YES; LEAVE LOOP
         AR    R5,R6              R5 = ADDR OF NEXT UCM ENTRY
         CR    R5,R7              DOES R5 POINT PAST UCM ENTRIES?
         BL    UCMLOOP            NOPE; KEEP GOING
         B     UCMDONE            YES, TABLE COMPLETE       *HMD 07/81*
UCMFULL  MVC   ERROR(26),ERRMSG5  UCM TABLE TOO SMALL       *HMD 07/81*
UCMDONE  STH   R3,NUMUCMS         SAVE NUMBER OF UCMS FOUND
         CLC   CONSOLE,OLDCONS    IS CONSOLE SPECIFIED?     *HMD 07/81*
         BNE   UCMFINI            NOPE WE FOUND MASTER      *HMD 07/81*
         MVC   CONSOLE,=F'1'      DEFAULT TO CONSOLE 1      *HMD 07/81*
UCMFINI  DS    0H                                           *HMD 07/81*
         MVC   OLDCONS,CONSOLE    MOVE DEFAULT CONSOLE NO.  *HMD 07/81*
         DROP  R2,R4
         AIF   (NOT &SP3).L002                              *HMD 07/82*
         EJECT                                              *HMD 07/82*
***********************************************************************
*                                                                     *
*            SET UP CROSS MEMORY ADDRESS SPACE COMMUNICATION          *
*          PREPARE FOR DATA TRANSFER FROM CONSOLE ADDRESS SPACE       *
*                                                                     *
***********************************************************************
AXSETR   CLI   SP3FLAG,X'00'      ARE WE RUNNING SP 1.3?    *HMD 07/82*
         BE    AXSETX             NO, GET OUT QUICK         *HMD 07/82*
         MODESET  MODE=SUP,RELATED=('GET SUPV STATE')       *HMD 07/82*
         ESAR  R5                 GET SECONDARY ASID        *HMD 07/82*
         ST    R5,SASID           SAVE SECONDARY ASID       *HMD 07/82*
         AXSET AX=AXIX            SET AUTHORIZATION INDEX   *HMD 07/82*
         GETMAIN R,LV=DCMEND-DCMSTRT                        *HMD 07/82*
         ST    R1,LOCDCM          GET STORAGE FOR TDCM      *HMD 07/82*
         MODESET MODE=PROB,RELATED=('RESET TO PROB STATE')  *HMD 07/82*
AXSETX   DS    0H                 COME HERE IF NOT SP 1.3   *HMD 07/82*
.L002    ANOP                                               *HMD 07/82*
         EJECT
***********************************************************************
*                                                                     *
*                            TOP OF LOOP                              *
*              LOCATE SCREEN BUFFER AND PREPARE TO TPUT               *
*                                                                     *
***********************************************************************
NEXTPAGE CLI   ATTNFLG,X'00'      WAS ATTN HIT?
         BE    NOATTN             NO
*                                 ATTENTION KEY HIT; PROCESS IT
         MVI   ATTNFLG,X'00'      YES, RESET FLAG
         MVC   TIME(3),BLANKS     BLANK OUT TIMER FIELD
         MVI   WAITFLG,X'00'      TURN OFF WAIT FLAG
         XC    TIMER,TIMER        SET TIMER TO 0
         CLI   CRTFLAG,X'FF'      IS THIS A 3270 OR HRDCPY? *HMD 12/79*
         BE    NOATTN             IS A 3270                 *HMD 12/79*
         B     DOLASTH            HARDCOPY GETS SPECIAL END *HMD 12/79*
NOATTN   DS    0H
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB       *HMD 07/81*
         L     R4,CONSOLE         R4 = CONSOLE TO BE DISPLAYED
         CH    R4,NUMUCMS         IS NUMBER TOO HIGH?
         BNH   GETUCM             NO, CONTINUE
         MVC   ERROR(26),ERRMSG1  CONSOLE NUMBER INVALID
RESETCN  L     R4,OLDCONS         RESET TO OLD CONSOLE
         ST    R4,CONSOLE         AND SAVE IT
GETUCM   SLL   R4,4               MULTIPLY BY 16            *HMD 07/81*
         LA    R5,0(R5,R4)        R5 = ADDR OF ADDR OF UCM
         LR    R1,R5              R1 = ADDR OF UCMTAB ENTRY *HMD 07/81*
         L     R5,0(R5)           R5 = ADDR OF UCM
         USING UCMLIST,R5
         L     R6,UCMXB           R6 = ADDR OF RDCM
         USING DCMTSRT,R6
         LTR   R6,R6              IS THIS A GRAPHICS CONSOLE?
         BP    GRAPHICS           YES
***********************************************************************
*                                                                     *
*        THIS IS NOT AN OPERATOR GRAPHICS CONSOLE - ERROR             *
*                                                                     *
***********************************************************************
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB       *HMD 07/81*
         MVC   ERROR(26),ERRMSG4  NON-GRAPHIC CONSOLE
         B     RESETCN            RESET THE CONSOLE NUMBER
         SPACE 2
***********************************************************************
*                                                                     *
*   THIS IS A GRAPHICS CONSOLE - BUILD TRAILER LINE WITH CONSDATA     *
*                                                                     *
***********************************************************************
GRAPHICS DS    0H                                           *HMD 07/81*
         BAL   R14,CONSDATA       CALL CONSOLE DATA RTN     *HMD 07/81*
         AIF   (NOT &SP3).L006                              *HMD 07/82*
         EJECT                                              *HMD 07/82*
***********************************************************************
*                                                                     *
*       SETUP TO COPY PAGEABLE DCM (TDCM) INTO OUR ADDRESS SPACE      *
*                                                                     *
***********************************************************************
MOVETDCM CLI   SP3FLAG,X'00'      ARE WE RUNNING SP 1.3?    *HMD 07/82*
         BE    MOVETDCX           NO, BRANCH AROUND XMEMORY *HMD 07/82*
         MODESET KEY=ZERO,MODE=SUP,RELATED=('GET KEY ZERO/SUPV STATE')
         L     R5,DCMADTRN        R5 = ADDR OF TDCM 'FROM'  *HMD 07/82*
         LH    R7,CONSASID        R7 = ADDR OF CONSOLE ASID *HMD 07/82*
         SSAR  R7                 SET CONSOLE AS SECONDARY  *HMD 07/82*
         L     R7,LOCDCM          R7 = ADDR OF AREA 'TO'    *HMD 07/82*
         LA    R3,DCMEND-DCMSTRT  R3 = LENGHT TO MOVE       *HMD 07/82*
         SLR   R15,R15            KEY OF MOVE               *HMD 07/82*
MOVEDCM  MVCP  0(R3,R7),0(R5),R15 MOVE TDCM TO OUR AREA     *HMD 07/82*
         BZ    DCMMOVED                                     *HMD 07/82*
         LA    R5,256(,R5)        BUMP 'FROM' ADDRESS       *HMD 07/82*
         LA    R7,256(,R7)        BUMP 'TO'   ADDRESS       *HMD 07/82*
         SH    R3,=H'256'         DECREMENT TRUE LENGTH     *HMD 07/82*
         B     MOVEDCM            CONTINUE WITH MOVE        *HMD 07/82*
         SPACE 1                                            *HMD 07/82*
DCMMOVED L     R5,SASID           RESTORE PREVIOUS SASID    *HMD 07/82*
         SSAR  R5                                           *HMD 07/82*
         L     R7,LOCDCM          ADDRESS OUR TDCM COPY     *HMD 07/82*
         USING DCMSTRT,R7                                   *HMD 07/82*
         MODESET KEY=NZERO,MODE=PROB,RELATED=('RESET KEY/PROB STATE')
         B     EXITDCM            SP3 USERS BRANCH AROUND   *HMD 07/82*
MOVETDCX L     R7,DCMADTRN        R7 = ADDR OF TDCM         *HMD 07/82*
EXITDCM  DS    0H                 FOR NON SP/3 USERS        *HMD 07/82*
         AGO   .L007              BRANCH AROUND NON SP/3    *HMD 07/82*
.L006    ANOP                                               *HMD 07/82*
         L     R7,DCMADTRN        R7 = ADDR OF TDCM
         USING DCMSTRT,R7
.L007    ANOP                                               *HMD 07/82*
         EJECT
***********************************************************************
*                                                                     *
*                 FILLIN TRAILER AT BOTTOM OF SCREEN                  *
*                                                                     *
***********************************************************************
FILLTRLR CLI   SP3FLAG,X'FF'      WE COPIED DCM, REMEMBER   *HMD 07/82*
         BE    NOKEYZ             SO NO NEED FOR KEY ZERO   *HMD 07/82*
         MODESET KEY=ZERO         REMEMBER FETCH PROT       *HMD 07/81*
NOKEYZ   DS    0H                                           *HMD 07/82*
         MVC   LASTLINE(79),DCMINPUT   MOVE IN INPUT BUFFER LINE
         MVI   MODE+1,C' '        BLANK OUT VIP INDICATOR
         CLI   VIPFLG,X'FF'       IS THE VIP FLAG ON?
         BNE   CHKINTEG           NOPE
         MVI   MODE+1,C'*'        TURN ON VIP INDICATOR
         EJECT
***********************************************************************
*                                                                     *
*                    CHECK FOR INTEGRATED CONSOLE                     *
*                                                                     *
*     IF THIS IS A INTEGRATED CONSOLE, CHECK IF THE USER HAS A        *
*     327X-2 OR THE LARGE-SCREEN 327X-4.                              *
*                                                                     *
***********************************************************************
CHKINTEG MVI   INTEGFLG,X'FF'     TURN ON INTEGRATED CONSOLE FLAG
         CLC   DCMMSGAL(2),=H'30' CHECK FOR 30-LINE 168 INTEG CONS  DCT
         BNL   CHKMODEL     YES                                     DCT
         CLC   DCMMSGAL(2),=H'20' IS THIS 327X-2 TYPE       *HMD 07/81*
         BE    GOT327X            YES, WE SUPPORT THESE     *HMD 07/81*
         CLC   DCMMSGAL(2),=H'19' IS THIS 327X-2 TYPE       *HMD 07/82*
         BE    GOT327X            YES, WE SUPPORT THESE     *HMD 07/82*
         MVC   ERROR(26),ERRMSG6  INDICATE NON-SUPPORTED CRT*HMD 07/82*
         B     RESETCN            RESET CONS AND CONTINUE   *HMD 07/81*
GOT327X  MVI   INTEGFLG,X'00'     NO; TURN OFF INTEGRATED CONSOLE FLAG
         MVI   FREEZE,C'F'        FREEZE DISPLAY
         MVI   PAGE,C'1'          ON PAGE 1
         MVC   TPUTLEN(4),MOD2TPUT SET TPUT LENGTH FOR SHORT SCREEN
         B     MOD2               AND TREAT AS MOD2 FOR NOW
         EJECT
***********************************************************************
*     THIS IS A 35-LINE 3066 OPERATORS CONSOLE. IF THE USER HAS       *
*     A 43-LINE 3278-4 THERE IS NO PROBLEM. THE WHOLE THING CAN       *
*     BE DISPLAYED AT ONCE.  HOWEVER, IF THE USER HAS A 24-LINE       *
*     327X-2, WE WILL HAVE TO BREAK THE DISPLAY INTO 2 PAGES.         *
***********************************************************************
CHKMODEL CLI   MOD4FLG,X'FF'      IS THIS A 3278-4?
         BNE   MOD2               NO - MUST BE A MOD2
***********************************************************************
*                                                                     *
*                USER HAS A 43-LINE 3278-4 CRT                        *
*     WE CAN JUST FREEZE HIS DISPLAY ON 'PAGE 1' AND CONTINUE         *
*                                                                     *
***********************************************************************
         MVI   FREEZE,C'F'        FREEZE DISPLAY
         MVI   PAGE,C'1'          ON PAGE 1
         MVC   TPUTLEN(4),MOD4TPUT SET LENGTH FOR FULL 3278-4 SCREEN
         L     R8,DCMASCRN        R8 = ADDR OF SCREEN BUFFER + 6
         LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M4BUFLEN        R5 = 3278-4 BUFFER LEN (35 LINES)
         LA    R9,M4BUFLEN        R9 = 3278-4 BUFFER LEN (35 LINES)
         B     MOVEBUFF
         SPACE 2
MOD2     CLI   PAGE,C'1'          ARE WE ON PAGE 1?
         BNE   ONTWO              NO, SO WE MUST BE ON 2
         CLI   FREEZE,C'F'        ARE WE FROZEN ON PAGE 1?
         BNE   PAGE2              NO, SO DISPLAY PAGE 2
         B     PAGE1              YES, SO DISPLAY PAGE 1
ONTWO    CLI   FREEZE,C'F'        ARE WE FROZEN ON PAGE 2?
         BE    PAGE2              YES, SO DISPLAY PAGE 2
PAGE1    MVI   PAGE,C'1'          PAGE = 1
         L     R8,DCMASCRN        R8 = ADDR OF SCREEN BUFFER + 6
         LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M2BUFLEN        R5 = LENGTH OF OUTPUT BUF (21 LINES)
         LA    R9,M2BUFLEN        R9 = CONSOLE BUFFER LEN   (21 LINES)
         B     MVETRAIL           GO MOVE THE BUFFER
PAGE2    MVI   PAGE,C'2'          PAGE = 2
         L     R8,DCMASCRN        R8 = ADDR OF SCREEN BUFFER + 6
         CLI   MODE,C'2'          ARE WE IN DISPLAY MODE 2?
         BE    DMODE2             YES, BRANCH TO DMODE2
DMODE1   LA    R8,LEN22(R8)       MOVE POINTER DOWN 23 LINES
         LA    R9,LEN13           R9 = LENGTH OF LAST 12 LINES
         B     CONTINUE           JUMP AROUND MODE 2 DISPLAY
DMODE2   LA    R8,LEN9(R8)        MOVE POINTER DOWN 9 LINES
         LA    R9,LEN21           R9 = LENGTH OF SOURCE BUFFER
CONTINUE LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M2BUFLEN        R5 = LENGTH OF OUTPUT BUFFER
MVETRAIL MVC   ENDMOD2(TRAILEN),CMDCTRL MOVE IN TRAILER
         AIF   (NOT &SP3).L014                              *HMD 07/82*
MOVEBUFF DS    0H                                           *HMD 07/82*
         CLI   SP3FLAG,X'00'      ARE WE RUNNING MVS/SP 1.3?*HMD 07/82*
         BE    MOVEBFR1           NO, BRANCH AROUND XMEMORY *HMD 07/82*
         STM   R4,R9,MOVEBSAV     SAVE REGISTERS            *HMD 07/82*
         MODESET KEY=ZERO,MODE=SUP,RELATED=('GET KEY ZERO/SUPV STATE')
*        SETUP TO COPY CONSOLE BUFFER TO OUR ADDRESS SPACE  *HMD 07/82*
*        FIRST FILL AREA WITH SPACES                        *HMD 07/82*
         SLR   R9,R9              CLEAR REGISTER            *HMD 07/82*
         ICM   R9,B'1000',PAD     PAD WITH SPACES           *HMD 07/82*
         LA    R8,0               NOT IMPORTANT REGISTER    *HMD 07/82*
         MVCL  R4,R8              PAD WITH SPACES           *HMD 07/82*
         LH    R4,CONSASID        GET CONSOLE ASID          *HMD 07/82*
         SSAR  R4                 SET IT AS SECONDARY       *HMD 07/82*
         LM    R4,R9,MOVEBSAV     RESTORE ADDRESSES/LENGTHS *HMD 07/82*
         CLI   INTEGFLG,X'FF'     IS THIS AN INTEGRATED CONS*HMD 07/82*
         BE    NOSUBTR1           DO NOT SUBTRACT           *HMD 07/82*
         S     R8,=F'6'           R8 = ADDR OF SCREEN BFR   *HMD 07/82*
NOSUBTR1 DS    0H                 COME HERE FOR INTEG CONS  *HMD 07/82*
         SR    R15,R15            CLEAR KEY                 *HMD 07/82*
MOVEBFR  MVCP  0(R9,R4),0(R8),R15 MOVE CONSOLE BUFFFER      *HMD 07/82*
         BZ    BFRMOVED           ALL MOVED                 *HMD 07/82*
         LA    R8,256(,R8)        BUMP 'FROM' ADDRESS       *HMD 07/82*
         LA    R4,256(,R4)        BUMP 'TO  ' ADDRESS       *HMD 07/82*
         SH    R9,=H'256'         DECREMENT TRUE LENGTH     *HMD 07/82*
         B     MOVEBFR            CONTINUE WITH MOVE        *HMD 07/82*
BFRMOVED L     R4,SASID           GET PREVIOUS SASID        *HMD 07/82*
         SSAR  R4                 RESET IT                  *HMD 07/82*
         MODESET KEY=NZERO,MODE=PROB,RELATED=('RESET KEY/PROB STATE')
         B     MOVEBFRX           EXIT THIS MESS QUICKLY    *HMD 07/82*
MOVEBFR1 ICM   R9,8,PAD           MAKE BLANK THE PAD CHARACTER
         CLI   INTEGFLG,X'FF'     IS THIS AN INTEGRATED CONS*HMD 07/82*
         BE    NOSUBTR            DO NOT SUBTRACT           *HMD 07/82*
         S     R8,=F'6'           R8 = ADDR OF SCREEN BFR   *HMD 07/82*
NOSUBTR  DS    0H                 COME HERE FOR INTEG CONS  *HMD 07/82*
         MVCL  R4,R8              MOVE CONSOLE BUFFER TO OUTPUT BUFFER
         DROP  R5,R6,R7
         MODESET KEY=NZERO        NO NEED FOR THIS NOW      *HMD 01/80*
MOVEBFRX DS    0H                 POINT TO BRANCH AROUND    *HMD 07/82*
         AGO   .L022                                        *HMD 07/82*
.L014    ANOP                                               *HMD 07/82*
MOVEBUFF ICM   R9,8,PAD           MAKE BLANK THE PAD CHARACTER
         CLI   INTEGFLG,X'FF'     IS THIS AN INTEGRATED CONS*HMD 07/81*
         BE    NOSUBTR            DO NOT SUBTRACT           *HMD 07/81*
         S     R8,=F'6'           R8 = ADDR OF SCREEN BFR   *HMD 07/81*
NOSUBTR  DS    0H                 COME HERE FOR INTEG CONS  *HMD 07/81*
         MVCL  R4,R8              MOVE CONSOLE BUFFER TO OUTPUT BUFFER
         DROP  R5,R6,R7
         MODESET KEY=NZERO        NO NEED FOR THIS NOW      *HMD 01/80*
.L022    ANOP                                               *HMD 07/82*
         CLI   CRTFLAG,X'FF'      IS THIS A CRT?
         BE    TPUTCRT            YES
         EJECT
***********************************************************************
*                                                                     *
*      DISPLAY THE OPERATOR'S SCREEN ON A TSO HARDCOPY TERMINAL       *
*                                                                     *
***********************************************************************
         XR    R8,R8              R8 = COUNTER = 0
         LA    R1,BUF             SET POINTER TO FIRST LINE
         ICM   R1,8,EDITFLG       EDIT MODE
         L     R0,=F'78'          R0 LENGTH OF OUTPUT LINE
NEXTL    LR    R3,R1              SAVE R1 SINCE TPUT ZAPS IT
         CLI   INTEGFLG,X'FF'     IS THIS AN INTEGRATED CONSOLE?
         BE    INTEG01            YES, NO CNTL CHARS        *HMD 08/81*
         LA    R1,5(,R1)          BUMP PAST CNTL CHARS      *HMD 08/81*
INTEG01  DS    0H                                           *HMD 08/81*
         CLC   5(74,R1),NULLS     IS LINE ALL NULLS?        *HMD 09/81*
         BE    NEXTL0             YES, DO NOT PRINT         *HMD 07/81*
         CLC   5(74,R1),BLANKS    IS LINE ALL BLANKS?       *HMD 09/81*
         BE    NEXTL0             YES, DO NOT PRINT         *HMD 09/81*
         TR    0(78,R1),TABLE     MAKE PRINTABLE            *HMD 08/81*
         TPUT  (1),(0),R          PRINT ONE LINE ON HARDCOPY
         CLI   ATTNFLG,X'FF'      WAS ATTENTION HIT?        *HMD 07/81*
         BE    NEXTPAGE           YES, ASK FOR INPUT        *HMD 07/81*
NEXTL0   LA    R8,1(R8)           ADD 1 TO COUNTER
NEXTL1   C     R8,=F'20'          HAVE WE PRINTED LAST LINE?*HMD 07/81*
         BE    DOLAST2            YES, CONTINUE
         LA    R1,80(R3)          NOPE, POINT TO NEXT LINE  *HMD 07/81*
         CLI   INTEGFLG,X'FF'     IS THIS AN INTEGRATED CONSOLE?
         BE    NOT3270            YES
         MVC   0(5,R1),BLANKS     BLANK OUT 3270 CTRL CHARS
         LA    R1,4(R1)           ADD 4 EXTRA BYTES TO SKIP CTRL CHARS
NOT3270  L     R0,=F'78'          LOAD LENGTH OF LINE
         ICM   R1,8,EDITFLG       EDIT MODE
         B     NEXTL              PRINT NEXT LINE
DOLAST2  CLC   LASTLINE,NULLS     IS LAST LINE BLANK?       *HMD 09/81*
         BE    DOLAST3            YES, SKIP TPUT            *HMD 09/81*
         TPUT  LASTLINE,79,EDIT   DISPLAY OPER CMD LINE
         CLI   ATTNFLG,X'FF'      WAS ATTENTION HIT?        *HMD 07/81*
         BE    NEXTPAGE           YES, ASK FOR INPUT        *HMD 07/81*
DOLAST3  TPUT  HEADING,79,EDIT    DISPLAY CONSOLE STATUS LINE
         CLI   ATTNFLG,X'FF'      WAS ATTENTION HIT?        *HMD 07/81*
         BE    NEXTPAGE           YES, ASK FOR INPUT        *HMD 07/81*
         TPUT  USERLINE,79,EDIT   DISPLAY USER CMD LINE
         CLI   ATTNFLG,X'00'      IS ATTN FLAG ON?          *HMD 12/79*
         BE    INPUT              NO, READ NEXT COMMAND     *HMD 07/81*
         B     NEXTPAGE           ELSE ASK FOR INPUT        *HMD 12/81*
         EJECT
***********************************************************************
*                                                                     *
*               DISPLAY THE OPERATORS SCREEN ON A 327X                *
*                                                                     *
***********************************************************************
TPUTCRT  LA    R1,CLEAR           R1 = ADDR OF OUTPUT STREAM
         L     R0,TPUTLEN         R0 = LENGTH OF TPUT
         ICM   R1,8,FULLSCR       SET ASIS TYPE FOR TPUT
         TPUT  (1),(0),R          DISPLAY ENTIRE SCREEN
***********************************************************************
*        IF WE ARE IN WAIT MODE; DO A STIMER FOR THE NECESSARY        *
*        LENGTH OF TIME, THEN CALCULATE AND DISPLAY THE NEW           *
*        TIMER COUNT. IF COUNT HAS HIT ZERO, TURN OFF WAIT FLAG       *
***********************************************************************
CHKWAIT  DS    0H                 ALTERNATE LABEL           *HMD 07/81*
INPUT    MVC   ERROR(66),BLANKS   BLANK OUT ERROR FIELD
         CLI   WAITFLG,X'FF'      IS THE WAIT FLAG ON?
         BNE   READCHAR           NO, SO GO GET A COMMAND
         STIMER WAIT,BINTVL=DELAY WAIT FOR DELAY*.01 SECONDS
         L     R2,TIMER           R2 = CURRENT VALUE OF TIMER
         BCTR  R2,0               TIMER = TIMER - 1
         ST    R2,TIMER           STORE NEW VALUE OF TIMER
         CVD   R2,WORK            CONVERT TO DECIMAL.
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   TIME(3),SCRATCH+1  MOVE TIME LEFT INTO PLACE
         LTR   R2,R2              HAS TIMER HIT ZERO?
         BNZ   NEXTPAGE           NO, CONTINUE TO COUNT
         MVC   TIME(3),BLANKS     CLEAR COUNTER FIELD
         XI    WAITFLG,X'FF'      TOGGLE WAIT FLAG OFF
         B     NEXTPAGE           AND GO ON AS IF NOTHING HAPPENED..
         EJECT
***********************************************************************
*                                                                     *
*                       READ COMMAND FROM USER                        *
*                                                                     *
***********************************************************************
READCHAR MVC   REPLY(79),BLANKS   CLEAR REPLY FIELD         *HMD 07/81*
         TGET  REPLY,79           GET 79 CHARACTERS FROM TERMINAL
         CLI   REPLY,C' '         JUST A BLANK?             *HMD 07/81*
         BE    NEXTPAGE           YES, GO REFRESH           *HMD 07/81*
         CLI   REPLY,X'6E'        IS THIS A RESHOW? (VTAM ONLY)
         BE    NEXTPAGE           YES, GO REFRESH           *HMD 07/81*
         EJECT                                              *HMD 07/81*
***********************************************************************
*                                                                     *
*                      SYSTEM OPERATOR COMMANDS                       *
*                                                                     *
***********************************************************************
INPUT1   CLI   VIPFLG,X'FF'       ARE WE IN VIP MODE?
         BNE   PARSECMD           NO, SO CONTINUE
OPER     CLI   REPLY,C'/'         IS THIS AN OPERATOR COMMAND?
         BNE   JES                NO, CHECK FOR JES CMD
         MVC   OPERCMD(79),REPLY+1   MOVE COMMAND TO REPLY AREA
         MVI   OPERCMD+79,C' '    BLANK OUT COLUMN 80       *HMD 07/81*
         B     DOCMD              BRANCH TO 'COMMAND' RTN   *HMD 07/81*
JES      CLI   REPLY,C'$'         IS THIS A JES COMMAND?
         BNE   PARSECMD           NO, CONTINUE
         MVC   OPERCMD(80),REPLY  MOVE IN COMMAND           *HMD 07/81*
         MVC   REPLY(80),BLANKS   BLANK OUT REPLY FIELD
DOCMD    DS    0H                 DO THE COMMAND NOW        *HMD 07/81*
         LA    R3,OPERCMD+79      R3 = ADDR OF LAST COLUMN  *HMD 07/81*
         LA    R1,OPERCMD         R1 = ADDR OF OPERCMD      *HMD 07/81*
NXTCHAR  CLI   0(R3),C' '         IS THIS BLANK             *HMD 07/81*
         BNE   LASTCHAR           MUST BE END OF COMMAND    *HMD 07/81*
         BCTR  R3,0               R3 = R3 - 1               *HMD 07/81*
         CR    R3,R1              IS WHOLE THINK BLANK?     *HMD 07/81*
         BL    NEXTPAGE           YES, SOMETHING WRONG      *HMD 07/81*
         B     NXTCHAR            CONTINUE                  *HMD 07/81*
LASTCHAR SR    R3,R1              R3 = R3 - R1              *HMD 07/81*
         BM    NEXTPAGE           ERROR..CONTINUE ANYWAY    *HMD 07/81*
         LA    R3,1(R3)           R3 = R3 + 1               *HMD 07/81*
         STH   R3,OPERCMDL        SAVE COMMAND LENGTH       *HMD 07/81*
         MVC   OPERCN(4),CONSOLE  SAVE CONSOLE ID           *HMD 07/81*
         L     R3,SAVEUPT         GET UPT ADDRESS           *HMD 07/82*
         USING UPT,R3             ADDRESS THE UPT           *HMD 07/82*
         MVC   SAVEWTP(1),UPTSWS  SAVE UPT SWITCHES         *HMD 07/82*
         NI    UPTSWS,X'FF'-UPTWTP     TURN OFF WTP IF ON   *HMD 07/82*
         CALL  COMMAND,(OPERCMDL,OPERCN),VL                 *HMD 07/81*
         L     R3,SAVEUPT         REGET UPT ADDRESS         *HMD 07/82*
         MVC   UPTSWS(1),SAVEWTP  MOVE SAVED SWITCHES       *HMD 07/82*
         B     NEXTPAGE                                     *HMD 07/81*
         DROP  R3                                           *HMD 07/82*
         EJECT                                              *HMD 07/81*
***********************************************************************
*         CONVERT COMMAND TO UPPERCASE AND CALL IKJSCAN               *
*                                                                     *
* THIS CALL TO IKJSCAN IS COMPLETELY UNNECESSARY TO THE OPERATION     *
* OF SPY.  IT IS INCLUDED  SOLELY TO ALLOW THE 'X' FEATURE OF PCF2    *
* TO OPERATE.  IF YOU DO NOT HAVE PCF2,  THIS COULD BE REMOVED.       *
*                                                                     *
***********************************************************************
PARSECMD DS    0H                                           *HMD 07/81*
         BAL   R14,INITSCAN            INITIALIZE SCAN      *HMD 07/81*
         OC    REPLY(79),BLANKS        CONVERT CHARS TO UPPER CASE
         XC    CBUF+2(2),CBUF+2        CLEAR OFFSET
         MVC   CSPLBLOK(24),CSPLSAVE   COPY IN CSPL BLOK
         XC    CSOABLOK(8),CSOABLOK
         MODESET KEY=ZERO              GET INTO KEY ZERO    *HMD 07/81*
         L     R2,SAVEJSCB        R2 = ADDR OF OUR JSCB?
         USING IEZJSCB,R2         SET UP ADDRESSIBILITY
         NI    JSCBOPTS,255-JSCBAUTH
*                                 TURN OFF APF BIT TO FOOL PCFSCAN
         LA    R1,CSPLBLOK        R1 = ADDR OF CMD SCAN PARM LIST
         L     R15,ADDRSCAN       R15 = ADDR OF IKJSCAN
         XC    SYNCHPRM(SYNCHL),SYNCHPRM                    *HMD 07/82*
         SYNCH (15),RESTORE=YES,MF=(E,SYNCHPRM)             *HMD 07/81*
         L     R2,SAVEJSCB        GET SUPPOSED ADDR OF JSCB *HMD 08/81*
         CLC   ADDRPSCB+1(3),JSCBPSCB+1  VALIDITY CHECK     *HMD 08/81*
         BNE   ABENDXX            ABEND IF NOT VALID        *HMD 08/81*
         L     R15,JSCBCSCB       GET CSCB ADDRESS          *HMD 08/81*
         USING CSCB,R15           ADDRESS THE CSCB          *HMD 08/81*
         CLC   USERID,CHKEY       SEE IF JSCB ADDR IS VALID *HMD 08/81*
         BNE   ABENDXX            NO, ABEND                 *HMD 08/81*
         OI    JSCBOPTS,JSCBAUTH  TURN ON AUTHORIZATION     *HMD 08/81*
         DROP  R2,R15
*                                 IGNORED FOR HARDCOPY TERMS*HMD 08/81*
         MODESET KEY=NZERO        GET OUT OF KEY ZERO       *HMD 08/81*
         STFSMODE ON              RESTORE FULL SCREEN MODE  *HMD 08/81*
         DELETE  EP=IKJSCAN       SCAN NO LONGER NEEDED     *HMD 07/81*
         LA    R1,CSOABLOK        R1 = ADDR OF CMD SCAN OUTPUT AREA
         USING CSOA,R1
         TM    CSOAFLG,CSOANOC    IS THE BUFFER EMPTY?
         BO    AXSETR             YES; JUST GO REFRESH      *HMD 04/83*
         B     CWAIT              CHECK FOR WAIT            *HMD 08/81*
         DROP  R1
ABENDXX  MODESET KEY=NZERO        TURN OFF KEY ZERO         *HMD 08/81*
         TM    AUTHFLG,X'FF'      DID WE TURN ON AUTH?      *HMD 07/82*
         BNE   ABENDXY            NO, THEN DON'T TURN OFF   *HMD 07/82*
* INSERT YOUR MAGIC SVC HERE TO TURN OFF APF AUTHORIZATION.
ABENDXY  ABEND 30,DUMP            DUMP THIS TASK            *HMD 08/81*
         EJECT                                              *HMD 07/81*
***********************************************************************
*                                                                     *
*                       W  --  ENTER WAIT MODE                        *
*                                                                     *
***********************************************************************
CWAIT    DS    0H                                           *HMD 07/81*
         CLI   REPLY,C'W'         DO WE SHIFT TO WAIT MODE?
         BNE   CDELAY             NO, SO CONTINUE
         XI    WAITFLG,X'FF'      TURN ON WAIT FLAG
         LA    R2,30              SET DEFAULT VALUE = 30
         LA    R15,CONVBIN        BRANCH TO CONVERSION RTN
         BALR  R14,R15            EBCDIC TO BINARY
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   TIME(3),SCRATCH+1  MOVE TIME LEFT INTO PLACE
         ST    R2,TIMER           STORE STARTING TIMER VALUE
         B     NEXTPAGE           ALL SET - GO DISPLAY NEXT PAGE
***********************************************************************
*                                                                     *
*            D  --  SET TIMER DELAY IN TENTHS OF A SECOND             *
*                                                                     *
***********************************************************************
CDELAY   CLI   REPLY,C'D'         ARE WE CHANGING THE TIME DELAY?
         BNE   CCONSOLE           NO, SO CONTINUE
         LA    R2,10              SET DEFAULT VALUE = 10 TENTHS SECOND
         LA    R15,CONVBIN        BRANCH TO CONVERSION RTN
         BALR  R14,R15            EBCDIC TO BINARY
         MVC   SCRATCH(5),DPATTRN MOVE IN EDIT PATTERN
         ED    SCRATCH(5),WORK+6  EDIT IN DELAY TIME
         MVC   PAUSE(3),SCRATCH+2 MOVE TIME LEFT INTO PLACE
         MH    R2,=H'10'          CONVERT TO 100THS OF A SECOND
         ST    R2,DELAY           STORE WAIT DELAY VALUE
         B     NEXTPAGE           ALL SET - GO DISPLAY NEXT PAGE
***********************************************************************
*                                                                     *
*                    C  --  SET CONSOLE NUMBER                        *
*                                                                     *
***********************************************************************
CCONSOLE CLI   REPLY,C'C'         DO WE CHANGE CONSOLES?
         BNE   BYE                NO, SO CONTINUE
         L     R2,CONSOLE         SET DEFAULT CONSOLE
         ST    R2,OLDCONS         SAVE OLD CONSOLE #
         L     R2,MCONS           GET MASTER CONS. NUMBER   *HMD 07/81*
         LTR   R2,R2              IS MASTER VALID?          *HMD 07/81*
         BNZ   CALLCVB            KEEP MASTER AS DEFAULT    *HMD 07/81*
         LA    R2,1(R2)           SET DEFAULT CONSOLE       *HMD 12/79*
CALLCVB  LA    R15,CONVBIN        BRANCH TO
         BALR  R14,R15            EBCDIC->BINARY CONVERTOR
         ST    R2,CONSOLE         STORE R2 AWAY AS CONSOLE NUMBER
         B     NEXTPAGE           CONTINUE
         EJECT
***********************************************************************
*                                                                     *
*                        B  --  TERMINATE SPY                         *
*                                                                     *
***********************************************************************
BYE      CLI   REPLY,C'B'         IS IT A 'B'?
         BE    DONE               YES, SO QUIT
***********************************************************************
*                                                                     *
*                        E  --  TERMINATE SPY                         *
*                                                                     *
***********************************************************************
END      CLI   REPLY,C'E'         IS IT AN 'E'?
         BE    DONE               YES, SO QUIT
***********************************************************************
*                                                                     *
*               F  --  FREEZE DISPLAY ON CURRENT PAGE                 *
*                                                                     *
***********************************************************************
F        CLI   REPLY,C'F'         IS IT AN 'F'?
         BNE   R                  NO, SO CONTINUE ON
         MVI   FREEZE,C'F'        TURN ON FREEZE INDICATOR
         B     NEXTPAGE           CONTINUE
***********************************************************************
*                                                                     *
*           R  --  RELEASE FREEZE ON CURRENT PAGE DISPLAY             *
*                                                                     *
***********************************************************************
R        CLI   REPLY,C'R'         IS IT AN 'R'?
         BNE   MODE1              NO, SO CONTINUE ON
         MVI   FREEZE,C'R'        TURN OFF FREEZE INDICATOR
         B     NEXTPAGE           CONTINUE
***********************************************************************
*                                                                     *
*                1  --  SHIFT TO MODE 1 TYPE DISPLAY                  *
*                                                                     *
***********************************************************************
MODE1    CLI   REPLY,C'1'         DO WE SHIFT TO MODE 1 DISPLAY?
         BNE   MODE2              NO, SO CONTINUE
         MVI   MODE,C'1'          SET MODE INDICATOR
         B     NEXTPAGE
***********************************************************************
*                                                                     *
*                2  --  SHIFT TO MODE 2 TYPE DISPLAY                  *
*                                                                     *
***********************************************************************
MODE2    CLI   REPLY,C'2'         DO WE SHIFT TO MODE 2 DISPLAY?
         BNE   GETHELP            NO, SO CONTINUE
         MVI   MODE,C'2'          SET MODE INDICATOR
         B     NEXTPAGE
         EJECT
***********************************************************************
*                                                                     *
*           ?  --  LIST HELP FOR SPY COMMANDS ON TERMINAL             *
*                                                                     *
***********************************************************************
GETHELP  CLI   REPLY,C'?'         IS HE ASKING FOR HELP?
         BNE   GETSTAT            NO,SO CONTINUE
         CLI   CRTFLAG,X'FF'      IS THIS A DISPLAY TERM?   *HMD 12/79*
         BE    DO3270             YES, DISPLAY HELP ON 3270 *HMD 12/79*
         XR    R8,R8              R8 = COUNTER = 0
         LA    R1,HELP2           GET HELP FOR HARDCOPY     *HMD 12/79*
         ICM   R1,8,EDITFLG       EDIT MODE
         L     R0,=F'78'          R0 LENGTH OF HEADER LINE  *HMD 12/79*
NEXTHLP  CLI   ATTNFLG,X'FF'      DID WE HAVE ATTN INT??    *HMD 12/79*
         BE    NEXTPAGE           YES, BACK TO ASK FOR INPT *HMD 07/81*
         LR    R3,R1              SAVE R1 OVER TPUT         *HMD 12/79*
         TPUT  (1),(0),R          PUT OUT HEADER            *HMD 12/79*
         CLI   ATTNFLG,X'FF'      DID WE HAVE ATTN INT?     *HMD 07/81*
         BE    NEXTPAGE           YES, BACK TO ASK FOR INPT *HMD 07/81*
         LA    R8,1(R8)           ADD 1 TO COUNTER          *HMD 12/79*
         C     R8,=F'21'          HAVE WE PRINTED LAST LINE?
         BE    DOLASTH            YES, CONTINUE
         LA    R1,80(R3)          NOPE, POINT TO NEXT LINE
         L     R0,=F'78'          LOAD LENGTH OF LINE
         ICM   R1,8,EDITFLG       EDIT MODE
         B     NEXTHLP            PRINT NEXT LINE
DOLASTH  MVC   ERROR,BLANKS       CLEAR ERROR INDICATOR     *HND 09/81*
         TPUT  LASTHL,L'LASTHL,EDIT                         *HMD 07/81*
         B     READCHAR           READ A CHAR. FROM TERM    *HMD 12/79*
DO3270   LA    R1,HELP            R1 = ADDR OF HELP PAGE
         LA    R0,HLENGTH         R0 = LENGTH OF HELP PAGE
         ICM   R1,8,FULLSCR       INSERT ASIS CNTL CHARS
         TPUT  (1),(0),R          DISPLAY HELP
         B     READCHAR
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*           S  --  LIST STATUS PAGE FOR ALL DEFINED CONSOLES          *
*                                                                     *
***********************************************************************
GETSTAT  CLI   REPLY,C'S'         IS HE ASKING FOR STATUS REPORT?
         BNE   SWAPLINK           NO; CONTINUE
         BAL   R14,CLRBUF         CLEAR BUFFER
         LH    R5,NUMUCMS         R5 = NUMBER OF CONSOLES
         LA    R2,UCMTAB+UCMTABL  R2 = ADDR OF UCM TABLE    *HMD 07/81*
         XR    R4,R4              CLEAR R4
         LA    R4,BUF             R4 = ADDR OF TPUT BUFFER
         LA    R3,22              ASSUME 3277 TERMINAL
         CLI   MOD4FLG,X'FF'      IS THIS A MOD4?           *HMD 07/81*
         BNE   STLOOP             GET RIGHT NO OF LINES     *HMD 07/81*
         LA    R3,41              R3 = NUMBER OF LINES 3278-4
STLOOP   LR    R1,R2              R1 = ADDR OF UCMTAB ENTRY
         BAL   R14,CONSDATA       CALL CONSOLE DATA ROUTINE
         MVC   0(45,R4),CONNUM    SAVE RESULTS IN BUFFER
         LR    R1,R4              R1 = BUFFER ADDRESS       *HMD 07/81*
         LA    R4,80(R4)          R4 = ADDR OF NEXT LINE IN BUFFER
         LA    R2,16(R2)          R2 = ADDR OF NEXT UCMTAB ENTRY
         CLI   CRTFLAG,X'00'      IS THIS A HARDCOPY?       *HMD 07/81*
         BNE   COUNT01            NO, FILL BUFFER FIRST     *HMD 07/81*
         L     R0,=F'45'          LOAD BUFFER LENGTH        *HMD 07/81*
         ICM   R1,B'1000',EDITFLG EDIT MODE                 *HMD 07/81*
         TPUT  (1),(0),R          PUT DISPLAY TO HARDCOPY   *HMD 07/81*
         CLI   ATTNFLG,X'FF'      DID USER HIT ATTN?        *HMD 07/81*
         BE    NEXTPAGE           YES, GO ASK FOR INPUT     *HMD 07/81*
COUNT01  BCT   R3,NOTFULL         BRANCH IF THE SCREEN NOT FULL
FULL     LA    R1,HEADER          R1 = ADDR OF TPUT BUF
         L     R6,=F'22'          NUMBER OF LINES/SCREEN    *HMD 07/81*
         CLI   MOD4FLG,X'FF'      IS THIS A 3278-4?         *HMD 07/81*
         BNE   CALC01             NO, CALCULATE LENGTH      *HMD 07/81*
         L     R6,=F'41'          LENGTH OF FULL SCREEN     *HMD 07/81*
CALC01   DS    0H                                           *HMD 07/81*
         MH    R6,=H'80'          MULTIPLY BY LENGTH OF LINE*HMD 07/81*
         AH    R6,STATLGN         ADD HEADER LENGTH         *HMD 07/81*
         LA    R6,TRLLEN(R6)      INCREMENT FOR LAST LINE   *HMD 07/81*
         LR    R0,R6              SAVE LENGTH OF TPUT       *HMD 07/81*
         S     R4,=F'35'          TACK TRAILER ON END       *HMD 07/81*
         MVC   0(TRLLEN,R4),TRALR INSERT SPECIAL TRAILER    *HMD 07/81*
         CLI   CRTFLAG,X'00'      IS THIS A HARDCOPY?       *HMD 07/81*
         BE    CALC02             DO NOT DO FULLSCRN TPUT   *HMD 07/81*
         ICM   R1,B'1000',FULLSCR INSERT ASIS CNTL CHARS
         TPUT  (1),(0),R          DO A TPUT; DISPLAY STATUS PAGE
         BAL   R14,CLRBUF         CLEAR BUFFER
         LA    R4,BUF             R4 = ADDR OF TPUT BUFFER
         LA    R3,22              R3 = NUMBER OF LINES ON SCREEN
         CLI   MOD4FLG,X'FF'      IS THIS A 3278-4?         *HMD 07/81*
         BNE   MOREPRE            NO, JUST A NORMAL 3277    *HMD 07/81*
         LA    R3,41              LOAD CORRECT LENGTH       *HMD 07/81*
         B     MOREPRE            SEE IF ANY MORE ENTRIES   *HMD 07/81*
CALC02   TPUT  LASTHL,L'LASTHL,EDIT                         *HMD 07/81*
         CLI   ATTNFLG,X'FF'      DID USER HIT ATTN?        *HMD 07/81*
         BE    NEXTPAGE           FORGET REST OF DISPLAY    *HMD 07/81*
MOREPRE  BCT   R5,MORE            MORE ENTRIES?
         B     NEXTPAGE           NO; CONTINUE
MORE     MVC   REPLY(79),BLANKS   CLEAR REPLY AREA          *HMD 07/81*
         TGET  REPLY,1            PAUSE FOR ENTER           *HMD 07/81*
         CLI   REPLY,X'6E'        VTAM RESHOW COMMAND?      *HMD 07/81*
         BE    STLOOP             CONTINUE WITH DISPLAY     *HMD 07/81*
         CLI   REPLY,C' '         ANYTHING ENTERED?         *HMD 07/81*
         BE    STLOOP             CHECK FOR NEW COMMAND     *HMD 07/81*
         B     INPUT1             IF NOT BLANK              *HMD 07/81*
NOTFULL  BCT   R5,STLOOP          LOOP IF MORE
         L     R6,=F'22'          LOAD SCREEN SIZE          *HMD 07/81*
         CLI   MOD4FLG,X'FF'      IS THIS A 3278-4?         *HMD 07/81*
         BNE   CALC03             NO, CONTINUE              *HMD 07/81*
         L     R6,=F'41'          LOAD MOD4 SCREEN SIZE     *HMD 07/81*
CALC03   SLR   R6,R3              CALCULATE NO. OF LINES    *HMD 07/81*
         MH    R6,=H'80'          CALCULATE TPUT LENGTH     *HMD 07/81*
         AH    R6,STATLGN         FIX LENGTH OF TPUT        *HMD 07/81*
         LA    R6,TRLLEN(R6)      INCREMENT FOR LAST LINE   *HMD 07/81*
         LR    R0,R6              LOAD TPUT LENGTH          *HMD 07/81*
         LA    R1,HEADER          R1 = ADDR OF TPUT BUF
         S     R4,=F'35'          TACK TRAILER ON END       *HMD 07/81*
         MVC   0(TRLLEN,R4),TRALR INSERT SPECIAL TRAILER    *HMD 07/81*
         CLI   CRTFLAG,X'00'      IS THIS A HARDCOPY?       *HMD 07/81*
         BE    HARD02             YES, HANDLE DIFFERENTLY   *HMD 07/81*
         ICM   R1,8,FULLSCR       INSERT ASIS CNTL CHARS
         TPUT  (1),(0),R          DO A TPUT; DISPLAY STATUS PAGE
         B     CALC04             GET REPLY                 *HMD 07/81*
HARD02   TPUT  LASTHL,L'LASTHL,EDIT                         *HMD 07/81*
         CLI   ATTNFLG,X'FF'      DID USER HIT ATTN?        *HMD 07/81*
         BE    NEXTPAGE           FORGET REST OF DISPLAY    *HMD 07/81*
CALC04   DS    0H                                           *HMD 07/81*
         MVC   REPLY(79),BLANKS   BLANK REPLY AREA          *HMD 07/81*
         TGET  REPLY,1            PAUSE FOR ENTER
         CLI   REPLY,X'6E'        VTAM RESHOW COMMAND?      *HMD 07/81*
         BE    NEXTPAGE           YES, IGNORE               *HMD 07/81*
         CLI   REPLY,C' '         DID USER ENTER ANYTHING   *HMD 07/81*
         BNE   INPUT1             YES, HANDLE AS NORMAL INPT*HMD 07/81*
         B     NEXTPAGE           DISPLAY THE CURRENT CONSOLE
         EJECT                                              *HMD 07/81*
***********************************************************************
*                                                                     *
*                    L  --  LINK TO SWAP PROGRAM                      *
*                                                                     *
***********************************************************************
SWAPLINK CLI   REPLY,C'L'         SHOULD WE XCTL TO SWAP?
         BNE   VIP                NO, SO CONTINUE
         CLI   AUTHFLG,X'FF'      DID WE TURN ON AUTH       *HMD 01/82*
         BNE   DOXCTL             NO, JUST DO XCTL          *HMD 01/82*
* INSERT YOUR MAGIC SVC HER TO TURN OFF AUTHORIZATION.      *HMD 01/82*
         MVI   AUTHFLG,X'00'      INDICATE AUTH TURNED OFF  *HMD 01/82*
DOXCTL   L     R13,SAVE+4         CALLERS SAVE AREA POINTER.
         XCTL  (2,12),EP=SWAP     XCTL TO SWAP
         SPACE 1                                            *HMD 12/79*
***********************************************************************
*                                                                     *
*                       CHECK FOR VIP PASSWORD                        *
*                                                                     *
***********************************************************************
VIP      CLC   REPLY(3),VIPWORD   WAS VIP PASSWORD ENTERED?
         BNE   BADCMD             NO, CONTINUE
         XI    VIPFLG,X'FF'       TOGGLE VIP FLAG
         B     NEXTPAGE
         SPACE 1                                            *HMD 12/79*
***********************************************************************
*                                                                     *
*                      COMMAND WAS INVALID                            *
*                                                                     *
***********************************************************************
BADCMD   MVC   ERROR(26),ERRMSG3  COMMAND WAS INVALID
         B     NEXTPAGE
         SPACE 2                                            *HMD 01/82*
NOTAUTH  DS    0H                                           *HMD 01/82*
         TPUT  NOTAMSG,L'NOTAMSG                            *HMD 01/82*
         B     ALLDONE1                                     *HMD 01/82*
NOTAMSG  DC    C'You are not authorized to issue the SPY command'
         EJECT
***********************************************************************
*                                                                     *
*                 ALL DONE, RETURN TO MVS                             *
*                                                                     *
***********************************************************************
DONE     DS    0H                                           *HMD 01/80*
         CLI   CRTFLAG,X'00'      IS THIS A HARDCOPY?
         BNE   CLEARSCN           NO, CLEAR THE SCREEN      *HMD 07/81*
         STSIZE SIZELOC=SAVESIZE  RESTORE HC LINESIZE       *HMD 07/81*
         B     ALLDONE            FORGET SCREEN CLEAR       *HMD 07/81*
CLEARSCN TPUT  CLR,CLRLEN,FULLSCR NO, LETS CLEAR THE SCREEN FIRST
         STFSMODE OFF             TURN OFF FS MODE
         AIF   (NOT &SP3).L017                              *HMD 07/82*
ALLDONE  CLI   SP3FLAG,X'00'      RUNNING MVS/SP 1.3?       *HMD 07/82*
         BE    ALLDONEY           NOPE..BRANCH AROUND CODE  *HMD 07/82*
         MODESET MODE=SUP,RELATED=('GET SUPV STATE')        *HMD 07/82*
         SLR   R1,R1              CLEAR REGISTER 1          *HMD 07/82*
         ICM   R1,B'1111',LOCDCM  PREPARE TO FREEMAIN TDCM  *HMD 07/82*
         BZ    ALLDONEX           NOT THERE? - QUE PASA?    *HMD 07/82*
         FREEMAIN R,LV=DCMEND-DCMSTRT,A=(1)                 *HMD 07/82*
ALLDONEX DS    0H                 ALL DONE HERE             *HMD 07/82*
         AXSET AX=AXRESET         SET AUTH INDEX TO ZERO    *HMD 07/82*
         MODESET MODE=PROB,RELATED=('SWITCH TO PROB STATE') *HMD 07/82*
ALLDONEY CLI   AUTHFLG,X'FF'      DID WE TURN ON AUTH?      *HMD 07/82*
         AGO   .L018                                        *HMD 07/82*
.L017    ANOP                                               *HMD 07/82*
ALLDONE  CLI   AUTHFLG,X'FF'      DID WE TURN ON AUTH?      *HMD 01/82*
.L018    ANOP                                               *HMD 07/82*
         BNE   ALLDONE1           NO, JUST TERMINATE        *HMD 01/82*
* INSERT YOUR MAGIC SVC HERE TO TURN OFF AUTHORIZATION.
ALLDONE1 L     R13,SAVE+4         RESTORE POINTER TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)    RESTORE REGISTERS
         LA    R15,0
         BR    R14                RETURN TO SYSTEM
         EJECT
***********************************************************************
*                                                                     *
*            CONVERT EBCDIC NUMBERS FROM USER INTO BINARY             *
*                                                                     *
***********************************************************************
CONVBIN  CVD   R2,WORK            CONVERT TO DECIMAL.
         CLI   REPLY+1,C' '       DID HE ENTER A NUMBER?
         BE    RTRN               NO, USE THE DEFAULT
         CLI   REPLY+1,C'0'       IS THE HEX CODE < 'F0' ?
         BL    BADCHAR            YES, ERROR
         CLI   REPLY+1,C'9'       IS THE HEX CODE > 'F9' ?
         BH    BADCHAR            YES, ERROR
         PACK  WORK(8),REPLY+1(1) PACK EBCDIC (ASSUME 1 DIGIT)
         CLI   REPLY+2,C' '       DID HE ENTER 2 DIGITS?
         BE    CVB                NO, DONT DO THE 2 DIGIT PACK
         CLI   REPLY+2,C'0'       IS THE HEX CODE < 'F0' ?
         BL    BADCHAR            YES, ERROR
         CLI   REPLY+2,C'9'       IS THE HEX CODE > 'F9' ?
         BH    BADCHAR            YES, ERROR
         PACK  WORK(8),REPLY+1(2) PACK AGAIN, WITH 2 DIGITS THIS TIME
CVB      CVB   R2,WORK            GET BINARY
RTRN     BR    R14                RETURN TO MAINLINE
BADCHAR  MVC   ERROR(26),ERRMSG2  CONSOLE NUMBER ERROR
         B     RTRN
         EJECT                                              *HMD 07/81*
***********************************************************************
*                            C L R B U F                              *
*               CLEAR THE ENTIRE BUF AREA TO HEX ZEROS                *
***********************************************************************
CLRBUF   STM   R2,R5,SAVE25       SAVE REGS 2 - 5
         LA    R2,BUF             R2 = ADDR OF BUFFER
         LA    R3,M4BUFLEN        R3 = LENGTH OF BUFFER (35 LINES)
         LA    R4,BLANKS          R4 = ADDR OF BLANKS
         LA    R5,1               R5 = LENGTH OF BLANK
         ICM   R5,8,PAD           SETUP PAD CHAR
         MVCL  R2,R4              M O V E    L O N G
         LM    R2,R5,SAVE25       RESTORE REGS
         BR    R14                RETURN TO CALLER
SAVE25   DS    4F
         EJECT
***********************************************************************
*                       C O N S D A T A                               *
*                                                                     *
*           FILL IN THE CONSOLE DATA FOR A GIVEN CONSOLE              *
*                                                                     *
***********************************************************************
CONSDATA STM   R0,R15,SAVECDAT       SAVE REGS
         L     R5,0(R1)              R5 = ADDR OF UCM
         USING UCMLIST,R5
         L     R6,UCMXB              R6 = ADDR OF RDCM
         USING DCMTSRT,R6
         MVC   CONNUM(48),BLANKS     BLANK OUT CONSOLE TYPE FIELD
         XR    R3,R3                 CLEAR R3
         ICM   R3,1,UCMID            LOAD THE CONSOLE NUMBER
         CVD   R3,WORK               CONVERT TO DECIMAL IN WORK
         MVC   SCRATCH(4),PATTERN    MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6     EDIT IN CONSOLE NUMBER
         MVC   CONNUM(2),SCRATCH+2   MOVE CONSOLE NUMBER INTO PLACE
         MVC   LOCATION(12),4(R1)    MOVE IN CONSOLE DESCRIPTION
         MVI   SLASH,C'/'
         MVC   STATUS(6),=CL6'Inactv' SET DEFAULT = 'INACTV'
         TM    UCMATR,UCMUF          IS THIS DEVICE ACTIVE?
         BNO   NOTACT                NO
         MVC   STATUS(6),=CL6'Active' SET STATUS = 'ACTIVE'
NOTACT   EQU   *
         TM    UCMDISP1,UCMDISPA     IS THIS A MASTER CONSOLE?
         BNO   AUTH                  NO
         MVC   STATUS(6),=CL6'MASTER' YES
AUTH     TM    UCMAUTHA,UCMAUTH1     CONSOLE SYSTEM AUTHORIZED?
         BNO   AUTH1                  NO
         MVC   SYS(3),=CL3'SYS'       YES
AUTH1    TM    UCMAUTHA,UCMAUTH2     IS IT I/O AUTHOZRIZED?
         BNO   AUTH2                  NO
         MVC   IO(3),=CL3'I/O'        YES
AUTH2    TM    UCMAUTHA,UCMAUTH3     IS IT CONS AUTHORIZED?
         BNO   AUTHDONE               NO
         MVC   CONS(3),=CL3'CON'      YES
AUTHDONE EQU   *
         LTR   R6,R6                 IS THIS A GRAPHICS CONSOLE?
         BP    NOTHARD                YES
         MVC   STATUS(6),=CL6'Hrdcpy'
NOTHARD  EQU   *
         USING UCBOB,R7                                     *HMD 07/81*
         L     R7,UCMUCB             R7 = ADDR OF UCB
         MVC   UNIT(3),UCBNAME       MOVE UNIT ADDR INTO LINE
         L     R5,UCMALTEN           R5 = ADDR OF ALTERNATE UCM
         L     R7,UCMUCB             R7 = ADDR OF UCB
         MVC   ALTUNIT(3),UCBNAME    MOVE UNIT ADDR INTO LINE
         LM    R0,R15,SAVECDAT       RESTORE REGS
         BR    R14
         DROP  R5,R6
SAVECDAT DS    16F
         EJECT
***********************************************************************
*                                                                     *
*          SETUP AND INITIALIZE PARAMETER LIST FOR IKJSCAN            *
*                                                                     *
***********************************************************************
INITSCAN STM   R15,R4,SAVE154     SAVE REGS
         L     R3,CVTPTR          R3 = ADDR OF CVT
         L     R3,0(R3)           R3 = ADDR OF ADDR OF TCB
         USING TCBLIST,R3         USE SYMBOLIC LABELS       *HMD 07/81*
         L     R3,TCBCUR          R3 = ADDR OF TCB
         DROP  R3                                           *HMD 07/81*
         USING TCB,R3             ADDRESS THE DSCET
         ICM   R3,7,TCBJSCBB      R3 = ADDR OF JSCB
         ST    R3,SAVEJSCB        SAVE ADDR OF JSCB
         DROP  R3
         USING IEZJSCB,R3
         L     R4,JSCBPSCB        R4 = ADDR OF PSCB
         DROP  R3
         USING PSCB,R4
         L     R3,PSCBUPT         R3 = ADDR OF UPT
         ST    R3,SAVEUPT         SAVE ADDR OF UPT
         LA    R2,CSPLSAVE        R2 = ADDR OF CMD SCAN PARM LIST
         USING CSPL,R2
         ST    R3,CSPLUPT         SAVE UPT ADDR
         L     R3,PSCBRLGB        R3 = ADDR OF RELOGON BUFFER
         L     R3,256(R3)         R3 = ADDR OF ECT
         ST    R3,CSPLECT         SAVE ECT ADDR
         LA    R3,CPECB           R3 = ADDR OF FAKE ECB FOR THIS CP
         ST    R3,CSPLECB         SAVE ECB ADDR
         LA    R3,FLAGWORD        R3 = ADDR OF CSPL FLAG WORD
         ST    R3,CSPLFLG         SAVE FLAG ADDR
         LA    R3,CSOABLOK        R3 = ADDR OF CMD SCAN OUTPUT AREA
         ST    R3,CSPLOA          SAVE OUTPUT AREA ADDR
         LA    R3,CBUF            R3 = ADDR OF 'CMD BUFFER'
         ST    R3,CSPLCBUF        SAVE CBUF ADDR
         DROP  R2,R4
         LOAD  EP=IKJSCAN
         ST    R0,ADDRSCAN
         LM    R15,R4,SAVE154     RESTORE REGS 15 - 4
         BR    R14
         SPACE 2
         DS    0F
SAVE154  DS    6F
         SPACE 1
SAVEUPT  DS    F                   SAVE ADDR OF UPT
SAVEJSCB DS    F                   SAVE ADDR OF JSCB
CSPLBLOK DS    6F                  CMD SCAN PARAMETER LIST
CSPLSAVE DS    6F
CPECB    DC    F'0'                FAKE ECB FOR THIS CP
FLAGWORD DC    F'0'
CSOABLOK DS    2F                  CMD SCAN OUTPUT AREA
CBUF     DC    AL2(84),AL2(0)
REPLY    DC    CL80' '             USER'S COMMAND INPUT BUFFER
ADDRSCAN DS    A
         DROP  R12                                          *HMD 07/81*
         EJECT
***********************************************************************
*                                                                     *
*                          A T T N E X I T                            *
*                                                                     *
*         TRAP USERS ATTENTION INTERRUPTS AND FLAG FOR RESET          *
*                                                                     *
***********************************************************************
ATTNEXIT LR    R7,R15             ESTABLISH
         USING ATTNEXIT,R7        ADDRESSABILITY.
         MVI   ATTNFLG,X'FF'      SET ATTN FLAG
         BR    R14                RETURN TO CALLER
         DROP  R7
         EJECT
***********************************************************************
*                                                                     *
*                         C O N S T A N T S                           *
*                                                                     *
***********************************************************************
         DS    0D
WORK     DS    D                   WORK AREA FOR PACKS
SCRATCH  DS    D                   SCRATCH AREA FOR CHAR. MANIP
SYNCHPRM SYNCH MF=L                GENERATE SYNCH PARM LIST *HMD 07/82*
SYNCHL   EQU   *-SYNCHPRM          LENGTH OF SYNCH PARM     *HMD 07/82*
OLDCONS  DC    F'0'                PREVIOUS CONSOLE NUMBER
CONSOLE  DC    F'0'                DEFAULT CONSOLE TO LOOK AT
MCONS    DC    F'0'                MASTER CONSOLE NUMBER    *HMD 07/81*
SAVESIZE DC    F'0'                TERMINAL LINESIZE        *HMD 07/81*
ADDRPSCB DC    F'0'                SAVED PSCB ADDRESS       *HMD 08/81*
STATLGN  DC    A(STATLEN)          LENGTH OF STATUS HEADER  *HMD 07/81*
TPUTLEN  DC    A(MOD2LEN)          LENGTH OF MOD 2 TPUT
MOD4TPUT DC    A(MOD4LEN)          LENGTH OF MOD4 TPUT
MOD2TPUT DC    A(MOD2LEN)          LENGTH OF MOD2 TPUT
LPSCREEN DC    H'0'                LINES PER SCREEN
CPLINE   DC    H'0'                CHARACTERS PER LINE
NUMUCMS  DS    H                   NUMBER OF CONSOLES DEFINED
TIMER    DC    F'30'               SECONDS LEFT ON TIMER
DELAY    DC    F'100'              DELAY FOR 100 HUNDREDTHS OF A SECOND
MOD4FLG  DC    X'00'               X'FF' INDICATES 3278-4 IN USE
ATTNFLG  DC    X'00'               X'FF' INDICATES ATTN WAS TRAPPED
CRTFLAG  DC    X'FF'               X'FF' INDICATES CRT IN USE
WAITFLG  DC    X'00'               X'00' INDICATES NOT IN WAIT MODE
VIPFLG   DC    X'00'               X'FF' INDICATES VIP MODE
INTEGFLG DC    X'00'               INTEGRATED CONSOLE FLAG
FULLSCR  DC    X'03'               TPUT ASIS FLAG
EDITFLG  DC    X'00'               TPUT EDIT FLAG
AUTHFLG  DC    X'00'               AUTHORIZED FLAG BYTE     *HMD 01/82*
SAVEWTP  DC    X'00'               SAVED UPTSWS VALUE       *HMD 07/82*
* THE FOLLOWING FIELDS ARE FOR MVS/SP 1.3 SUPPORT           *HMD 07/82*
SP3FLAG  DC    X'00'               WE ARE RUNNING MVS/SP    *HMD 07/81*
PCAUTHN  DC    CL8'PCAUTH'         PCAUTH JOBNAME           *HMD 07/82*
SASID    DC    F'0'                SECONDARY ASID NUMBER    *HMD 07/82*
LOCDCM   DC    F'0'                LOCATION OF TDCM COPY    *HMD 07/82*
AXIX     DC    H'1'                TO SET AX 1              *HMD 07/82*
AXRESET  DC    H'0'                TO SET AX 0 (RESET)      *HMD 07/82*
CONSASID DC    H'0'                ASID OF CONSOLE ADDRSPC  *HMD 07/82*
MOVEBSAV DC    7F'-1'              SAVE AREA FOR MOVEBUFF   *HMD 07/82*
* END OF MVS/SP 1.3 SUPPORT FIELDS                          *HMD 07/82*
VIPWORD  DC    C'GAK'              VIP PASSWORD
NULLS    DC    80X'00'             JUST NULLS
R41C1    DC    X'11F240'           3278-4  --  ROW 41, COL 1
R42C1    DC    X'11F350'           3278-4  --  ROW 42, COL 1
R43C1    DC    X'11F460'           3278-4  --  ROW 43, COL 1
R43C23   DC    X'11F4F6'           3278-4  --  ROW 43, COL 23
R43C80   DC    X'11F56F'           3278-4  --  ROW 43, COL 80
PATTERN  DC    X'40202020'         EDIT PATTERN FIELD
DPATTRN  DC    X'4021204B20'       EDIT PATTERN FIELD
PAD      DC    C' '                PAD CHARACTER FOR MOVEBUFF MVC
STAXLIST STAX  ATTNEXIT,MF=L
USERID   DC    CL8' '              SAVED USER IDENTIFICATION
BLANKS   DC    CL80' '
         SPACE 1                                            *HMD 07/81*
OPERCN   DC    F'0'                CONSOLE ID               *HMD 07/81*
OPERCMDL DC    H'0'                LENGTH OF COMMAND        *HMD 07/81*
OPERCMD  DC    CL80' '             OPERATOR COMMAND AREA    *HMD 07/81*
TABLE    DC    CL64' '
*     0123456789ABCDEF0123456789ABCDEF0123456789ABCDEF0123456789ABCDEF
 DC C'          .<(+&&         !$*);^-/         ,%_>?          :#@'''
 DC C'="' CONTINUANCE OF PREVIOUS LINE BECAUSE OF DUPLICATION
 DC C' abcdefghi       jklmnopqr        stuvwxyz                      '
 DC C' ABCDEFGHI       JKLMNOPQR        STUVWXYZ      0123456789      '
         EJECT
***********************************************************************
*                                                                     *
*               327X SCREEN CLEAR CONTROL CHARACTERS                  *
*                                                                     *
***********************************************************************
CLR      DC    X'C1'              WCC - CLEAR SCREEN
         DC    X'115D7E'          SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'3C404000'        FILL SCREEN WITH NULLS
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'13'              INSERT CURSOR
CLRLEN   EQU   *-CLR
         EJECT
***********************************************************************
*                                                                     *
*                  DISPLAY SCREEN - HEADER SECTION                    *
*                                                                     *
***********************************************************************
HEADER   EQU   *
CLEAR    DC    X'C1'              WCC
CLEARX   DC    X'115D7F'          SBA TO ROW 24, COL 80 (FSE 5.0)
*                                 OR ROW 43 COL 80 FOR 3278-4
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'3C404000'        FILL SCREEN WITH NULLS
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'1D60'            ATTR BYTE - PROTECTED FIELD
*        DC    X'40'
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*               DISPLAY SCREEN - IMAGE BUFFER SECTION                 *
*                                                                     *
***********************************************************************
BUF      DC    21CL80' '          OPERATORS SCREEN BUFFER
         DC    14CL80' '          PLUS EXTRA FOR 3278-4
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*               DISPLAY SCREEN - TRAILER SECTION                      *
*                                                                     *
***********************************************************************
TRAILER  EQU   *
CMDCTRL  DC    X'115A50'          SBA TO ROW 22, COL 1
         DC    X'1DE8'            ATTR BYTE - PROTECTED, HIGH INTENSITY
LASTLINE DC    CL79' '            OPERATORS COMMAND INPUT LINE
PHEADING DC    X'115B60'          SBA TO ROW 23, COL 1
         DC    X'1DE8'            ATTR BYTE - PROTECTED, HIGH INTENSITY
HEADING  DC    CL8'Console '
CONNUM   DC    CL2' 1'            CONSOLE NUMBER
         DC    CL1' '
LOCATION DC    CL12' '            CONSOLE LOCATION          *HMD 07/81*
         DC    CL2' '                                       *HMD 07/81*
UNIT     DC    CL3' '             UNIT ADDR OF CONSOLE
SLASH    DC    CL1'/'                                       *HMD 07/81*
ALTUNIT  DC    CL3' '                                       *HMD 07/81*
         DC    CL2' '                                       *HMD 07/81*
STATUS   DC    CL6' '             MASTER CONSOLE
         DC    CL2' '                                       *HMD 07/81*
SYS      DC    CL4' '             SYS  AUTHORIZATION
IO       DC    CL4' '             I/O  AUTHORIZATION
CONS     DC    CL3' '             CONS AUTHORIZATION
         DC    CL2' '
TIME     DC    CL3' '             SECONDS REMAINING ON TIMER
         DC    CL1'/'
PAUSE    DC    CL3'1.0'           DELAY IN SECONDS
         DC    CL1' '
HEADING2 DC    CL6'Mode:'
FREEZE   DC    C'F'               FREEZE/RELEASE MODE
MODE     DC    CL1'2'             DISPLAY MODE 2/1
         DC    CL2' '
         DC    CL5'Page '
PAGE     DC    CL1'2'             PAGE NUMBER
R24C1    DC    X'115CF0'          SBA TO ROW 24, COL 1
         DC    X'1D40'            ATTR BYTE - UNPROTECTED, LOW INTENS.
         DC    X'13'              INSERT CURSOR
USERLINE DC    CL13' '            USERS COMMAND INPUT LINE
ERROR    DC    CL66' '            ERROR MSG FIELD
ENDTRAIL EQU   *
         SPACE 2
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*                  ERROR AND WARNING MESSAGES                         *
*                                                                     *
***********************************************************************
         SPACE 2
ERRMSG1  DC    CL26'ERROR - Console not active'
ERRMSG2  DC    CL26'ERROR - Non-numeric value '
ERRMSG3  DC    CL26'ERROR - Invalid command   '
ERRMSG4  DC    CL26'ERROR - Non-CRT console   '
ERRMSG5  DC    CL26'ERROR - UCM table full    '             *HMD 01/81*
ERRMSG6  DC    CL26'ERROR - non-supported CRT '             *HMD 07/82*
         EJECT
***********************************************************************
*                                                                     *
*                           USER HELP PAGE                            *
*                                                                     *
***********************************************************************
HELP     DC    X'C1'               WCC
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
         DC    X'114040',X'1DE8',C'S P Y   ---   Version 4.0'  ** HMD**
         DC    C' (GTEDS-HMD '                              *HMD 07/81*
         DC    C'&DATE'                                     *HMD 07/81*
         DC    X'11C260',X'1D60',C'Command  Description'    *HMD 07/81*
         DC    X'11C3F0'                                    *HMD 07/81*
         DC    X'11C540',C'   B     End SPY'
         DC    X'11C650',C'   C     Switch monitor to MASTER console'
         DC    X'11C760',C'   CXX   Switch monitor to console XX'
         DC    X'11C8F0',C'   DXX   Set delay to XX tenths seconds'
         DC    X'114A40',C'   E     End SPY'
         DC    X'114B50',C'   F     Freeze display on current page'
         DC    X'114C60',C'   L     Link to SWAP program'
         DC    C' (if available)'
         DC    X'114DF0',C'   R     Release Display'
         DC    X'114F40',C'   S     Status of all consoles'
         DC    X'115050',C'   W     Start timer mode for 30 seconds'
         DC    X'11D160',C'   WXX   Start timer mode for XX seconds'
         DC    X'11D2F0',C'   W0    Start timer mode until '
         DC    C'attention'
         DC    X'11D440',C'   ?     Display this page'
         DC    X'11D550',C'   1     Display mode 1 (top 22, bottom 12)'
         DC    X'11D660',C'   2     Display mode 2 (top 22, bottom 22)'
         DC    X'11D7F0'
         DC    X'11D940',C'Hitting INTERRUPT will stop the wait timer'
         DC    X'115A50'
         DC    X'115B60'
TRALR    DC    X'115CF0'        ROW 24, COL 1
TRALRSS  DC    C'Hit ENTER to continue'
TRALX    DC    X'115DC6'        ROW 24, COL 23
         DC    X'1D40'
         DC    X'1340'
         DC    X'1DE8'
TRLLEN   EQU   *-TRALR          LENGTH OF SPECIAL TRAILER   *HMD 07/81*
MARKER1  EQU   *
HLENGTH  EQU   MARKER1-HELP     LENGTH OF HELP TPUT
         EJECT
***********************************************************************
*                                                                     *
*          HELP TEXT FOR HARDCOPY TERMINALS                           *
*                                                                     *
***********************************************************************
HELP2    DC    CL80'S P Y   ---   Version 4.0 (GTEDS-HMD &DATE'
         DC    CL80'Command  Description'
         DC    CL80' '
         DC    CL80'   B     End SPY'
         DC    CL80'   C     Switch monitor to MASTER console'
         DC    CL80'   CXX   Switch monitor to console XX'
         DC    CL80'   DXX   Set delay to XX tenths seconds'
         DC    CL80'   E     End SPY'
         DC    CL80'   F     Freeze display on current page'
         DC    CL80'   L     Link to SWAP program (if available)'
         DC    CL80'   R     Release display'
         DC    CL80'   S     Status of all consoles'
         DC    CL80'   W     Start timer mode for 30 seconds'
         DC    CL80'   WXX   Start timer mode for XX seconds'
         DC    CL80'   W0    Start timer mode until attention'
         DC    CL80'   ?     Display this page'
         DC    CL80'   1     Display MODE 1 (top 22, bottom 12)'
         DC    CL80'   2     Display MODE 2 (top 22, bottom 22)'
         DC    CL80' '                                      *HMD 12/79*
         DC    CL80'Hitting interrupt will stop the wait timer'
         DC    CL80' '
LASTHL   DC    CL50'Hit ENTER to continue'
         EJECT
***********************************************************************
*                                                                     *
*                            U C M T A B                              *
*                                                                     *
*        THE UCMTAB TABLE IS BUILT AT SPY INITIALIZATION TIME.        *
*        ONE ENTRY IS FILLED IN FOR EACH CONSOLE THAT HAS BEEN        *
*        IOGENED INTO THE SYSTEM. THE 12 BYTE DESCRIPTION FIELD       *
*        ALLOWS YOU TO TAG EACH CONSOLE WITH SOME MNEMONIC THAT       *
*        IS EASIER TO REMEMBER THAN CONSOLE NUMBERS. IF UCMTAB IS     *
*        NOT DEFINED LARGE ENOUGH TO HOLD ALL THE CONSOLES, A         *
*        WARNING IS ISSUED BUT SPY CONTINUES ANYWAY.                  *
*                                                                     *
***********************************************************************
         DS    0F
UCMTABE  DC    A(TABEND)                                    *HMD 07/81*
UCMTAB   DC    A(0),CL12' '               CONSOLE 0
TABSTART DC    A(0),CL12'3033 Main   '    CONSOLE 1
         DC    A(0),CL12'Amdahl Main '    CONSOLE 2
         DC    A(0),CL12'3033 CE Cons'    CONSOLE 3
         DC    A(0),CL12'4th Fl Rje  '    CONSOLE 4
         DC    A(0),CL12'Res. Sched  '    CONSOLE 5
         DC    A(0),CL12'LA Tapepool '    CONSOLE 6         *HMD 03/82*
         DC    A(0),CL12'Hardcopy Log'    CONSOLE 7
         DC    A(0),CL12'Spurt Pool  '    CONSOLE 8
         DC    A(0),CL12'Paper Tape  '    CONSOLE 9
         DC    A(0),CL12'NCC Console '    CONSOLE 10        *HMD 03/82*
         DC    A(0),CL12'SAC Tapepool'    CONSOLE 11        *HMD 03/82*
         DC    A(0),CL12'Staging     '    CONSOLE 12        *HMD 03/82*
         DC    A(0),CL12'Transmittals'    CONSOLE 13        *HMD 03/82*
         DC    A(0),CL12'Telegenix   '    CONSOLE 14        *HMD 03/82*
         DC    A(0),CL12'Sched 5th fl'    CONSOLE 15        *HMD 03/82*
         DC    A(0),CL12'Destaging   '    CONSOLE 16        *HMD 03/82*
         DC    A(0),CL12'UCC-1 labels'    CONSOLE 17        *HMD 03/82*
         DC    A(0),CL12'Spurt Pool  '    CONSOLE 18        *HMD 03/82*
         DC    A(0),CL12'Sched 5th fl'    CONSOLE 19        *HMD 03/82*
         DC    A(0),CL12'Apollo - GTC'    CONSOLE 20        *HMD 03/82*
         DC    A(0),CL12'NDU Techs   '    CONSOLE 21        *HMD 03/82*
         DC    A(0),CL12'Output Cntl '    CONSOLE 22        *HMD 03/82*
         DC    A(0),CL12'Destaging   '    CONSOLE 23        *HMD 03/82*
         DC    A(0),CL12'Expansion   '    CONSOLE 24        *HMD 03/82*
         DC    A(0),CL12'Transmittals'    CONSOLE 25        *HMD 03/82*
         DC    A(0),CL12'Expansion HC'    CONSOLE 26        *HMD 03/82*
         DC    A(0),CL12'Expansion   '    CONSOLE 27        *HMD 03/82*
         DC    A(0),CL12'Expansion   '    CONSOLE 28        *HMD 03/82*
         DC    A(0),CL12'Expansion HC'    CONSOLE 29        *HMD 03/82*
         DC    A(0),CL12'            '    CONSOLE 30
         DC    A(0),CL12'            '    CONSOLE 31
         DC    A(0),CL12'            '    CONSOLE 32
         DC    A(0),CL12'            '    CONSOLE 33
         DC    A(0),CL12'            '    CONSOLE 34
         DC    A(0),CL12'            '    CONSOLE 35
         DC    A(0),CL12'            '    CONSOLE 36
         DC    A(0),CL12'            '    CONSOLE 37
         DC    A(0),CL12'            '    CONSOLE 38
         DC    A(0),CL12'            '    CONSOLE 39
         DC    A(0),CL12'            '    CONSOLE 40
         DC    A(0),CL12'            '    CONSOLE 41
         DC    A(0),CL12'            '    CONSOLE 42
         DC    A(0),CL12'            '    CONSOLE 43
         DC    A(0),CL12'            '    CONSOLE 44
         DC    A(0),CL12'            '    CONSOLE 45
         DC    A(0),CL12'            '    CONSOLE 46
         DC    A(0),CL12'            '    CONSOLE 47
         DC    A(0),CL12'            '    CONSOLE 48
         DC    A(0),CL12'            '    CONSOLE 49
         DC    A(0),CL12'            '    CONSOLE 50
         DC    A(0),CL12'            '    CONSOLE 51
         DC    A(0),CL12'            '    CONSOLE 52
         DC    A(0),CL12'            '    CONSOLE 53
         DC    A(0),CL12'            '    CONSOLE 54
         DC    A(0),CL12'            '    CONSOLE 55
         DC    A(0),CL12'            '    CONSOLE 56
         DC    A(0),CL12'            '    CONSOLE 57
         DC    A(0),CL12'            '    CONSOLE 58
         DC    A(0),CL12'            '    CONSOLE 59
         DC    A(0),CL12'            '    CONSOLE 60
         DC    A(0),CL12'            '    CONSOLE 61
         DC    A(0),CL12'            '    CONSOLE 62
         DC    A(0),CL12'            '    CONSOLE 63
         DC    A(0),CL12'            '    CONSOLE 64
         DC    A(0),CL12'            '    CONSOLE 65
         DC    A(0),CL12'            '    CONSOLE 66
         DC    A(0),CL12'            '    CONSOLE 67
         DC    A(0),CL12'            '    CONSOLE 68
         DC    A(0),CL12'            '    CONSOLE 69
         DC    A(0),CL12'            '    CONSOLE 70
         DC    A(0),CL12'            '    CONSOLE 71
         DC    A(0),CL12'            '    CONSOLE 72
         DC    A(0),CL12'            '    CONSOLE 73
         DC    A(0),CL12'            '    CONSOLE 74
         DC    A(0),CL12'            '    CONSOLE 75
         DC    A(0),CL12'            '    CONSOLE 76
         DC    A(0),CL12'            '    CONSOLE 77
         DC    A(0),CL12'            '    CONSOLE 78
         DC    A(0),CL12'            '    CONSOLE 79
         DC    A(0),CL12'            '    CONSOLE 80
         DC    A(0),CL12'            '    CONSOLE 81
         DC    A(0),CL12'            '    CONSOLE 82
         DC    A(0),CL12'            '    CONSOLE 83
         DC    A(0),CL12'            '    CONSOLE 84
         DC    A(0),CL12'            '    CONSOLE 85
         DC    A(0),CL12'            '    CONSOLE 86
         DC    A(0),CL12'            '    CONSOLE 87
         DC    A(0),CL12'            '    CONSOLE 88
         DC    A(0),CL12'            '    CONSOLE 89
         DC    A(0),CL12'            '    CONSOLE 90
         DC    A(0),CL12'            '    CONSOLE 91
         DC    A(0),CL12'            '    CONSOLE 92
         DC    A(0),CL12'            '    CONSOLE 93
         DC    A(0),CL12'            '    CONSOLE 94
         DC    A(0),CL12'            '    CONSOLE 95
         DC    A(0),CL12'            '    CONSOLE 96
         DC    A(0),CL12'            '    CONSOLE 97
         DC    A(0),CL12'            '    CONSOLE 98
         DC    A(0),CL12'            '    CONSOLE 99
TABEND   DS    0F                                           *HMD 07/81*
UCMTABL  EQU   TABSTART-UCMTAB                              *HMD 07/81*
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                    E Q U A T E S                                    *
*                                                                     *
***********************************************************************
LEN9     EQU   9*80             NUMBER OF BYTES IN  9 LINES
LEN21    EQU   21*80            NUMBER OF BYTES IN 21 LINES
LEN22    EQU   22*80            NUMBER OF BYTES IN 22 LINES
LEN13    EQU   13*80            NUMBER OF BYTES IN 13 LINES
LEN14    EQU   14*80            NUMBER OF BYTES IN 14 LINES
M2BUFLEN EQU   21*80            LENGTH OF BUFFER 3278-2
M4BUFLEN EQU   35*80            LENGTH OF BUFFER 3278-4
STATLEN  EQU   BUF-CLEAR        LENGTH OF 'STATUS' PAGE HEADER
HEADLEN  EQU   BUF-HEADER       LENGTH OF HEADER
TRAILEN  EQU   ENDTRAIL-TRAILER LENGTH OF TRAILER
MOD4LEN  EQU   ENDTRAIL-HEADER  LENGTH OF TPUT FOR MOD4
MOD2LEN  EQU   MOD4LEN-LEN14    LENGTH OF TPUT FOR MOD2     *HMD 07/81*
ENDMOD2  EQU   BUF+M2BUFLEN     ADDR OF TRAILER FOR 3278-2
         EJECT
***********************************************************************
*      S Y S T E M    C O N T R O L    B L O C K   D S E C T S        *
*                                                                     *
*      THESE MACROS CAN BE FOUND IN 'SYS1.AMODGEN', 'SYS1.MACLIB',    *
*      OR THE OPTIONAL MATERIAL FOR THE MVS SYSTEM PRODUCTS.          *
***********************************************************************
*        DSECT FOR TCB LIST --
*        THIS LIST IS POINTED TO BY THE CVTTCBP FIELD OF THE CVT
TCBLIST  DSECT
TCBNEW   DS    A              TCB TO BE DISPATCHED
TCBCUR   DS    A              CURRENT TCB
ASCBNEW  DS    A              NEW ASCB
ASCBCUR  DS    A              CURRENT ASCB
         EJECT
         CVT   DSECT=YES,LIST=YES    COMMUNICATIONS VECTOR TABLE
         EJECT
         IEERDCM ,                   RESIDENT DISPLAY CONTROL MODULE   X
                                     *** COPYRIGHTED ***               X
                                     OBTAIN FROM THE SYSTEM PRODUCT    X
                                     PVTMACS DATASET OR USE THE 'RDCM' X
                                     MACRO IN 'SSU.MACLIB'             X
                                     NOTE: 'RDCM' IS NON-LICENSED AND  X
                                           DOWN-LEVEL
         EJECT
         IEETDCM ,                   PAGABLE  DISPLAY CONTROL MODULE   X
                                     *** COPYRIGHTED ***               X
                                     OBTAIN FROM THE SYSTEM PRODUCT    X
                                     PVTMACS DATASET OR USE THE 'TDCM' X
                                     MACRO IN 'SSU.MACLIB'             X
                                     NOTE: 'TDCM' IS NON-LICENSED AND  X
                                           DOWN-LEVEL
         EJECT
         IEZJSCB ,                   JOB STEP CONTROL BLOCK
         EJECT
         IKJCSOA ,                   COMMAND SCAN OUTPUT AREA
         EJECT
         IKJCSPL ,                   COMMAND SCAN PARAMETER LIST
         EJECT
         IKJUPT ,                    USER PROFILE TABLE
         EJECT
CSCB     DSECT
         IEECHAIN ,                  COMMAND SCHED CNTL BLOCK
         EJECT
         IKJPSCB ,                   PROTECTED STEP CONTROL BLOCK
         EJECT
         IKJTCB  LIST=YES            TASK CONTROL BLOCK
         EJECT
         IEECUCM FORMAT=NEW          UNIT CONTROL MODULE
         EJECT
         IEFUCBOB LIST=YES           UNIT CONTROL BLOCK
         EJECT
         IHAASVT                     ADDRESS SPACE VECTOR TABLE
         EJECT
         IHAASCB                     ADDRESS SPACE CONTROL BLOCK
         END  SPY
