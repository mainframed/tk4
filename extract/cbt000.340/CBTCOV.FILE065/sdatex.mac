SDATEX   TITLE 'GREGORIAN DATE SUBROUTINE (MM/DD/YY)'
*
*      THIS PROGRAM OBTAINS THE GREGORIAN DATE ("MM/DD/YY"). IF IT
* IS OBTAINED SUCCESSFULLY, 0 IS RETURNED IN REGISTER 15. IF THE DATE
* IS NOT OBTAINED SUCCESSFULLY, 12 IS RETURNED IN REGISTER 15.
* THE CALLING SEQUENCE FOR "SDATEX" IS AS FOLLOWS
*           1. THE OUTPUT GREGORIAN DATE IN THE FORMAT "MM/DD/YY"
*
*      THE "SDATEX" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
SDATEX   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING WORKAREA,R13        ADDRESS WORKAREA DSECT
         MVC   CALLIST,CCALLST     MOVE PARAMETER LIST TO WORK AREA
         LA    R4,GDATE            POINT TO GREG DATE FIELD
         LOAD  EP=DATEBX           LOAD ROUTINE TO OBTAIN GREG DATE
         LR    R15,R0              SAVE "DATEBX" ENTRY POINT ADDRESS
         CALL  (15),((R4)),       CALL "DATEBX"                        X
               MF=(E,CALLIST)
         LTR   R15,R15             IF THE SUBROUTINE FAILED
         BNZ   ERROR                  THEN FLAG FOR ERROR
         L     R2,0(,R2)           POINT TO PARAMETER FROM CALLER
         MVC   0(2,R2),GDATE       MOVE IN "MM"
         MVI   2(R2),C'/'
         MVC   3(2,R2),GDATE+2     MOVE IN "DD"
         MVI   5(R2),C'/'
         MVC   6(2,R2),GDATE+4     MOVE IN "YY"
         B     EXIT                   AND LEAVE
ERROR    DS    0H
         LA    R15,12              INDICATE ERROR IN INPUT
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
         EJECT
CCALLST  CALL  ,(,),MF=L            GENERATE PARAMETER LIST FOR CALL
CALLEN   EQU   *-CCALLST           COMPUTE PARAMETER LIST LENGTH
         SPACE
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
WORKSTRT EQU   *
         DS    18A                 REGISTER SAVE AREA
CALLIST  DS    CL(CALLEN)          PARAMETER LIST FOR CALL MACRO
GDATE    DS    CL6                 GREG DATE
WORKEND  EQU   *
WORKSIZE EQU   WORKEND-WORKSTRT    COMPUTE SIZE OF WORKAREA
         END
