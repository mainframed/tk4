XACFLGTM TITLE 'ACF LOGON PROMPT TIMER ROUTINE'
***********************************************************************
*                                                                     *
*    THIS ROUTINE SETS A TIMER FOR ACF2 LOGON EXITS AND DOES THE      *
*    TGET FOR THE CALLING PROGRAM. IT ALSO WILL WRITE A MESSAGE IF    *
*    THERE WAS A PROBLEM WITH THE TGET OR IF THE USER HAS TIMED OUT.  *
*                                                                     *
* INPUT PARMS- R1 POINTS TO THE BUFFER ADDRESS ,THE BUFFER SIZE,      *
*              AND THE TYPE OF TGET TO BE DONE.                       *
*                                                                     *
* OUTPUT-      THE UPDATED TGET BUFFER IN THE FIRST PARM.             *
*              THE LENGTH OF DATA FROM TGET IN THE THIRD PARM.        *
*              THE BUFFER SIZE PARM IS NOT MODIFIED.                  *
*                                                                     *
* RETURN CODE- R15                                                    *
*              0 IF SUCCESSFUL                                        *
*              8 IF TIMED OUT OR TGET ERROR                           *
***********************************************************************
XACFLGTM ENTER REGEQUS=YES,            REGISTER EQUATES                +
               PARMREG=2,              PARM REGISTER                   +
               GETMAIN=(WORKSIZE,0)    AND SOME CORE
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
         USING ACCVT,R9                ESTABLISH ADDRESSABILITY
         LM    R4,R6,0(R2)             GET PARM ADDRESSES
         MVC   INPARM,0(R6)            SAVE TGET PARM
         ACFINCVT R9,NONE=ERROR1       GET THE ADDR OF THE ACF2 CVT
         XR    R2,R2                   ZERO R2
         ICM   R2,1,ACCTLWTM           INSERT THE WAIT LIMIT
         BZ    NOLIMIT                 NO WAIT LIMIT
         SLL   R2,1                    MUTIPLY BY 2
         B     TGETLOOP                BYPASS FIRST STIMER
*
*     GET THE DATA - ENFORCE TIME LIMIT
*
WAIT     STIMER WAIT,BINTVL=HALFSEC    WAIT FOR 1/2 SECOND
*
TGETLOOP LR    R1,R4                   INPUT BUFFER ADDR
         ICM   R1,8,INPARM             SET FLAGS FOR TGET
         LR    R0,R5                   BUFFER LENGTH
         TGET  (1),(0),R               ISSUE TGET SVC
         ST    R1,0(R6)                SAVE TGET SIZE
         LA    R0,4                    LOAD CONSTANT
         CR    R15,R0                  CHECK TGET RETURN CODE
         BL    RETURN0                 USER HAS REPLIED
         BH    ERROR2                  TGET ERROR
         BCT   R2,WAIT                 GO WAIT AND CHECK AGAIN
         B     ERROR3                  TIME IS UP - CANCEL SESSION
*
*     GET THE DATA - NO TIME LIMIT
*
NOLIMIT  LR    R1,R4                   INPUT BUFFER ADDR
         NI    INPARM,X'EF'            CLEAR NOWAIT FLAG
         ICM   R1,8,INPARM             SET FLAGS FOR TGET
         LR    R0,R5                   BUFFER LENGTH
         TGET  (1),(0),R               ISSUE TGET SVC
         ST    R1,0(R6)                SAVE TGET SIZE
*
RETURN0  XR    R15,R15                 SET RETURN CODE TO ZERO
         B     EXIT                    --> EXIT
*
*     ISSUE ERROR MESSAGES AND SET RETURN CODE
*
ERROR1   DS    0H
         STLINENO LINE=14
         TPUT  MSG1,L'MSG1,,NOWAIT,NOHOLD
         B     RETURN                        LEAVE
ERROR2   DS    0H
         STLINENO LINE=14
         TPUT  MSG2,L'MSG2,,NOWAIT,NOHOLD
         B     RETURN                        LEAVE
ERROR3   DS    0H
         STLINENO LINE=14
         TPUT  MSG3,L'MSG3,,NOWAIT,NOHOLD
RETURN   LA    R15,8                         SET RETURN CODE
*
*     EXIT HERE ----->
*
EXIT     LEAVE GETMAIN=(WORKSIZE,0),   GIVE BACK THE CORE              +
               RETCODE=(15)            AND GO
         TITLE 'DATA AREAS'
HALFSEC  DC    F'50'
MSG1     DC    C'ACFLUOR2 ACF2, ERROR LOCATING ACF2 CVT'
MSG2     DC    C'ACFLUOR3 ACF2, ATTENTION INTERRUPT OR TGET ERROR'
MSG3     DC    C'ACFLUOR4 ACF2, WAIT TIME LIMIT EXCEEDED'
WORKAREA DSECT
         DS    18F
INPARM   DS    X
WORKSIZE EQU   *-WORKAREA
         TITLE 'ACF2 CVT DSECT'
         ACCVT ,
         END
