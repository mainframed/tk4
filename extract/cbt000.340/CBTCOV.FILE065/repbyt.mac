REPBYT   TITLE 'SUBROUTINE WHICH REPLACES A GIVEN CHARACTER'
*
*      THIS PROGRAM REPLACES ALL OCCURRENCES OF A GIVEN CHARACTER IN A
* CHARACTER STRING WITH A SPECIFIED REPLACEMENT CHARACTER. IF THE
* PARAMETERS PASSED THE PROGRAM ARE VALID, REGISTER 15 WILL CONTAIN 0
* UPON RETURN; OTHERWISE, IT WILL CONTAIN 12. THE CALLING SEQUENCE FOR
* "REPBYT" IS AS FOLLOWS:
*           1. THE CHARACTER STRING TO BE SEARCHED
*           2. THE LENGTH OF THE CHARACTER STRING (4-BYTE BINARY)
*           3. THE BYTE TO BE REPLACED
*           4. THE REPLACEMENT BYTE
*
*
*      THE "REPBYT" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
REPBYT   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(72,0)
         REGISTER                  REGISTER EQUATES
         LM    R4,R7,0(R2)         LOAD PARAMETER POINTERS
         L     R5,0(,R5)           LOAD LENGTH
         LTR   R5,R5               IF THE LENGTH IS NOT POSITIVE
         BNP   ERROR                  THEN FLAG FOR ERROR
LOOP     DS    0H
         CLC   0(1,R4),0(R6)       IF THIS IS NOT THE ONE
         BNE   CONTINUE               THEN KEEP LOOKING
         MVC   0(1,R4),0(R7)          ELSE REPLACE
CONTINUE DS    0H
         LA    R4,1(,R4)           INCREMENT STRING POINTER
         BCT   R5,LOOP                        AND KEEP LOOKING
         LA    R15,0               INDICATE SUCCESSFUL EXECUTION
         B     EXIT                   AND LEAVE
ERROR    DS    0H
         LA    R15,12              INDICATE INVALID PARAMETERS
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(72,0)
         END
