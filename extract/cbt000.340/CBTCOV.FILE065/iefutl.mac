IEFUTL   TITLE 'SDTIF USER TIME LIMIT  '
***********************************************************************
*                                                                     *
*             MODULE NAME = IEFUTL                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  SDTIF SUPPLIED USER TIME LIMIT EXIT *
*                                                                     *
*             COPYRIGHT = NONE                                        *
*                                                                     *
*             FUNCTION =                                              *
*                ALLOW JOBS TO BE EXTENDED IFF THEY APPEAR IN A       *
*                   PRIVILEGED LIST.                                  *
*                ALLOW PRIVILEGED TSO SESSIONS TO HAVE EXTENDED WAIT  *
*                   TIME.  THE TIME IS EASILY CHANGED.                *
*                                                                     *
*                OPERATION =                                          *
*                   ANALYZE SMF COMMON EXIT PARAMETER AREA.           *
*                   IF WAIT TIME LIMIT EXCEEDED SEE IF STARTED TASK   *
*                      STARTED TASK                                   *
*                      SEARCH PRIVILEGED LIST FOR A MATCH ON          *
*                      JOBNAME.  IF  MATCH IS FOUND, EXTEND THE WAIT  *
*                      TIME ACCORDING TO THE EXTENTION TIME FOUND FOR *
*                      THAT JOBNAME IN THE LIST.                      *
*                      CODE OF 8 IN REGISTER 15 AND THE NUMBER        *
*                      OF SECONDS  OF WAIT TIME EXTENSION IN          *
*                      REGISTER 1.  INCREMENT THE USER COMMUNICA-     *
*                      TION FIELD WORK AREA TO SHOW TIME EXTENSIONS.  *
*                      TSO CHECK.                                     *
*                      SEARCH PRIVILEGED LIST FOR A MATCH ON          *
*                      USERID.   IF  MATCH IS FOUND, EXTEND THE WAIT  *
*                      TIME ACCORDING TO THE EXTENTION TIME FOUND FOR *
*                      THAT USERID  IN THE LIST, PROVIDED THE USER    *
*                      IS IN SPF NON SPLIT EDIT MODE.                 *
*                      CODE OF 8 IN REGISTER 15 AND THE NUMBER        *
*                      OF SECONDS  OF WAIT TIME EXTENSION IN          *
*                      REGISTER 1.  INCREMENT THE USER COMMUNICA-     *
*                      TION FIELD WORK AREA TO SHOW TIME EXTENSIONS.  *
*                                                                     *
*             NOTES =                                                 *
*                                                                     *
*                DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE  *
*                   IF A DIFFERENT CHARACTER SET IS NEEDED.           *
*                                                                     *
*                RESTRICTIONS = NONE                                  *
*                                                                     *
*                REGISTER CONVENTIONS = STANDARD CONVENTIONS.         *
*                   REGISTERS 0 TO 2  = WORK REGISTERS                *
*                   REGISTERS 3       = ORING REGESTER ONLY           *
*                   REGISTERS 4       = WORK  LINK LIST ===>          *
*                   REGISTERS 5       = ADDRESSIBILITY TO SMF         *
*                                       CONTROL AREA                  *
*                   REGISTERS 6 TO 9  = WORK REGISTERS                *
*                   REGISTER  10      = ADDRESSABILITY TO IEFUTL CSECT*
*                   REGISTER  11      = ADDRESSABILITY TO COMMON      *
*                                       EXIT PARAMETER AREA           *
*                   REGISTER  12      = ADDRESSABILITY TO USER COMM   *
*                   REGISTER  13      = SAVE AREA REGISTER            *
*                   REGISTER  14      = RETURN ADDRESS                *
*                   REGISTER  15      = RETURN CODE                   *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                  'ZAP*'S)                                           *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 0, REFRESHABLE,           *
*                   SUPERVISOR STATE, ENABLED                         *
*                                                                     *
*             ENTRY POINTS = IEFUTL (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM INITIATOR VIA MODULE IEATLEXT                *
*                                                                     *
*             INPUT = ASYNCHRONOUS EXIT ROUTINE HAS SET THE           *
*                'STEP MUST COMPLEET' STATUS BEFOUR IEFUTL            *
*                RECEIVES CONTROL.  ERGO WE WILL AVOID ALL            *
*                IMPLICIT WAITS, I.E. TPUT TGET LOAD                  *
*                THIS MAY ONLY HAVE PROBLEMS IN SESSION MANAGER       *
*                WHICH WE DO NO HAVE.  HOPEFULLY, LEVEL II WIL GET    *
*                RESPOND.  IN THE MEAN TIME I WILL USE CAUTION ON ALL *
*                MACROS.  I HAVE ALSO ASKED THEM TO IDENTIFY A FIELD  *
*                IN SOME CONTROL BLOCK WHICH WOULD INDICATE ISPF      *
*                SPLIT MODE.  THIS PROBABLY IS IN AN ISPF CONTROL     *
*                BLOCK WHICH IS NOT USER ALLOWED.  I HAVE DECIDED     *
*                TO USE THE MODULE USE COUNT IN CDE.                  *
*                                                                     *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO A LIST OF FULL WORDS       *
*                THE FIRST OF WHICH POINTS TO THE SMF COMMON          *
*                EXIT PARAMETER AREA MAPPED BY IEFJMR DSECT           *
*                USE THIS WORK AREA RATHER THAN THE REAL JMR          *
*                WHICH IS NOT READ BY SMF MANAGER                     *
*                                                                     *
*             INPUT = REGISTER 0 CONTAINS A BINARY VALUE              *
*                     0  INDICATES THE JOB CPU TIME LIMIT EXPIRED     *
*                     4  INDICATES THE STEP CPU TIME LIMIT EXPIRED    *
*                     8  INDICATES THE CONTINUOUS WAIT TIME LIMIT     *
*                        FOR THE JOB EXPIRED                          *
*                                                                     *
*             OUTPUT = REGISTER 15 - RETURN CODE                      *
*                0 INDICATES JOB PROCESSING TO BE CANCELLED           *
*                4 INDICATES JOB PROCESSING TO BE CONTINUED           *
*                  WITH A TIME EXTENSION IN TIMER UNITS               *
*                8 INDICATES JOB PROCESSING TO BE CONTINUED           *
*                  WITH A TIME EXTENSION IN SECONDS                   *
*                                                                     *
*                REGISTER 1 - TIME LIMIT EXTENSION                    *
*                USER COMMUNICATION FIELD - EXTENSION COUNT           *
*                SMF WILL OVERLAY THIS VALUE IN THE ASCBSWTL (X'C4')  *
*                ON YOUR BEHALF                                       *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = SEE FUNCTION ABOVE                     *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             ASSUMPTIONS:                                            *
*                                                                     *
*                THE PSAAOLD ===>   INTERRUPTED ASCB                  *
*                THE PSATOLD ===>   CURRENT TASK OF THAT ASCB         *
*                                   SOME OF THE TIME                  *
*             CONFIRMATIONS                                           *
*                RESETTING REGISTER 1 DOES OVERRIDE THE JWT           *
*                FOR THAT ASCB                                        *
*                                                                     *
*                                                                     *
*             EXTERNAL REFERENCES = NONE                              *
*                ROUTINES = NONE                                      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = SEE LAST PAGE                       *
*                                                                     *
*             TABLES = NONE                                           *
*                                                                     *
*             MACROS = SDEXEMPT SDEXTENT                              *
*                      SDGUCOM                                        *
*                      SAVE REGEQU                                    *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             MESSAGES = NONE                                         *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
***    DEVELOPED BY AL MACMORRES                                    ***
***                 UNISYS                                          ***
***                 SYSTEMS DEVELOPMENT GROUP (SDTIF)               ***
***                 5151 CAMINO RUIZ     M.S.   G110                ***
***                                      ROOM   G 1426              ***
***                 CAMARILLO CALIFORNIA        93011-6004          ***
***                                             (805) 987-6811      ***
***                                             EXT   7532          ***
***    ATTRIBUTES: REFRESHABLE,REENTRANT,SERIALLY REUSEABLE         ***
***                                                                 ***
***********************************************************************
         EJECT
IEFUTL   CSECT
         SAVE  (14,12),,IEFUTL.SDG.&SYSTIME_&SYSDATE SAVE REGISTERS
         LR    R10,R15                 LOAD REGISTER 10 FROM 15
         USING IEFUTL,R10              ESTABLISH ADDRESSABILITY TO
         REGEQU                        ANY STANDARD EQUATE MACRO
IEFUTL   AMODE 31
IEFUTL   RMODE ANY
*                                      PROGRAM CSECT
*
***********************************************************************
*                                                                     *
*        ESTABLISH ADDRESSABILITY TO SMF PARAMETER AREA DSECT         *
*        EVALUATE REGISTER 0 = 0 - JOB CPU TIME EXCEEDED              *
*                            = 4 - STEP CPU TIME EXCEEDED             *
*                            = 8 - JOB WAIT TIME EXCEEDED             *
*                                                                     *
***********************************************************************
*
         L     R11,0(R1)               LOAD POINTER TO PARAMETER AREA
         USING JMR,R11          ADDRESSABILITY TO SMF PARAMETER AREA
*                                      COMMON EXIT PARAMETER AREA
         C     R0,FULL8                JOB OR STEP TIME EXCEEDED
         BE    WAITTIME                GO TO WAIT TIME ROUTINES
         B     CANCEL                  NO EXTENT FOR JOB/STEP CPU TIME
***********************************************************************
***                                                                   *
***      THE USER IS TIME SHARING AND THE POSSIBILITY OF              *
***      BEING IN NON SPLIT SPF EDIT OR IPCS.                         *
***                                                                   *
***********************************************************************
****************************************************************      *
***                                                                   *
***      TO MODIFY THE TIME LIMIT THE USER NEED ONLY CHANGE           *
***      THE VALUE OF THE TIME TO BE EXTENDED IN THE EXTENT           *
***      LIST.                                                        *
***                                                                   *
****************************************************************      *
***                                                                   *
***      IEATLEXT INFORMS US THAT REGISTER 4 POINTS TO THE            *
***      ASCB.  HOWEVER, OUR I B M  S E  HAS WARNED US NOT            *
***      TO USE IT, SINCE IT MAY BE TEMPORARY.  I B M LEVEL 1         *
***      HAS TOLD US NOT TO USE THE PSATOLD SINCE IT COULD,           *
***      AT ANY POINT IN TIME, POINT TO THE CURRENT TCB FOR           *
***      ANOTHER ADDRESS SPACE. THIS SEEMS DOUBTFUL SINCE             *
***      IEFUTL TOOK CONTROL WHEN THAT ADDRESS SPACE EXPERI-          *
***      ENCED A TIME OUT THEREBY HAVING THE ADDRESS OF THE           *
***      ASID AND TCB PLACED IN THE PSAAOLD AND PSATOLD               *
***      RESPECTIVELY.  BUT, WE WILL ANCHOR OFF THE PSAAOLD,          *
***      REGARDLESSLY.                                                *
***********************************************************************
WAITTIME DS    0H
***********************************************************************
***      CONTINOUS WAIT TIME EXCEEDED                               ***
***********************************************************************
         USING PSA,R0           ADDRESSABILITY TO PSA
         L     R6,PSAAOLD       ADDRESS SPACE CONTROL BLOCK
         DROP  R0               FREE INDEX REGISTER
         USING ASCB,R6          ADDRESSABILITY TO ASCB
         L     R7,ASCBJBNI      PTR TO BATCH JOB NAME OR ZERO
         LTR   R7,R7            IS THIS ASCB INITIATED (BATCH)
         BNZ   CANCEL           BATCH OF NO INTETEST AT THIS TIME
         TM    JMROPT,JMRFIND   SEE IF TSO SESSION
         BO    TSOCHECK         GO CHECK TSO IDS
         L     R7,ASCBJBNS      JOBNAME FIELD FOR START/MOUNT/LOGON
***************                 BUT NOT A LOGON
         LTR   R7,R7            IS THIS ASCB START/MOUNT/LOGON
         BNZ   STCCHECK         GO CHECK FOR THE PRIVELEDGED STC
******** DC    X'0C1'           TROUBLE IN RIVER CITY
         B     CANCEL           IGNORE ANYTHING ELSE
***********************************************************************
***            WE HAVE A TSO SESSION                                ***
***********************************************************************
TSOCHECK DS    0H
***********************************************************************
***            IS HE ON THE PRIVILEGED LIST                         ***
***********************************************************************
         LA    R4,PRIVHEAD     POSITION R4 WITH PRIVILEDGED LIST
PRIVSRCH DS    0H                                                     *
         L     R4,0(,R4)       BUMP R4 DOWN THE LINKED LIST           *
         C     R4,=F'0'        END OF LIST  ?                         *
         BE    ASCBSRCH        NOT PRIVALEDGED USERID CONTINUE CHECK  *
         LA    R9,5(,R4)       R9 ===>  PROGRAM NAME                  *
         IC    R3,4(,R4)       STUFF R3  WITH LENGTH OF CHARACTER
         LA    R7,JMRJOB       TSO USER ID
****************************   R3  STILL CONTAINS EXEC LENGTH         *
         EX    R3,CLCINS       COMPARE THE NAMES                      *
****************************************************************      *
***                            CLCINS   CLC   0(0,R9),0(R7)           *
****************************************************************      *
         BNE   PRIVSRCH        CONTINUE THROUGH THE LIST              *
         LA    R12,JMRUCOM     USER COMMUNICATION FILED
         USING USERCOM,R12     ADDRESSABILITY
         TM    SDGFLAG0,SDGFLAG8 HAS JWT BEEN EXTENDED ONCE
         BO    CANCEL          ALREADY EXTENDED
PRIVEXT  DS    0H              BRANCH ADDRESS
         L     R1,12(,R4)      TIME TO EXTEND
         L     R15,FULL8       TIME IS IN SECONDS
         OI    SDGFLAG0,SDGFLAG8  CPU EXTENDED ONCE
         B     RETURN          GO TO RETURN ROUTINE
ASCBSRCH DS    0H
***********************************************************************
***      TSO FOREGROUND BUT NOT ON PRIVILEGED LIST                  ***
***********************************************************************
         L     R6,ASCBASXB      ADDRESS SPACE EXTENSION BLOCK
         USING ASXB,R6          ADDRESSABILITY TO ASXB
         L     R6,ASXBFTCB      ASXB   TO ===> FIRST   TCB
         SPACE 2
***********************************************************************
***      USE THE SDEXTENT MACRO AND ITS LINKED LIST OF ENTRIES        *
***      SCANNING THE RB CHAIN FOR A PROGRAM NAME 'IE SPFEDIT'        *
***      IN THE PRB'S CDE/LPDE NAME FIELD                             *
***      R6  ===> THE FIRST TCB (TCBFTCB)                             *
***********************************************************************
TCBSCAN  DS    0H
         XR    R8,R8           CLEAR REG 8
         USING TCB,R6          ADDRESSABILITY TO TCB
*        ICM   R8,B'0111',TCBRBP+1  R8 ===  LAST RB
         L     R8,TCBRBP       R8 ===  LAST RB
*        USING RBPRFX,R8       ADDRESSABILITY TO PRB
         USING RBBASIC,R8       ADDRESSABILITY TO PRB
         TM    RBSTAB1,B'11100000' TEST FOR PRB
         BNZ   NEXTTCB           TEST ANOTHER TCB
*        ICM   R8,B'0111',FLCDE+1(R8) PRB POINTER
*        L     R8,FLCDE(,R8)   R8 ===> FL-CDE IF A PRB (NO NAME)
         ICM   R8,B'0111',RBCDE1 R8 ===> FL-CDE IF A PRB (NO NAME)
         USING CDENTRY,R8      ADDRESSABILITY TO CDE
         LA    R7,CDNAME       R7 ===> CDE / LPDE NAME IF EXISTS
***********************************************************************
PGMLIST  DS    0H              WILL BE BROUGHT IN BY LOAD
         LA    R4,LISTHEAD     POSITION R4 WITH TOP OF EXTENT LIST
LISTSRCH DS    0H
         L     R4,0(,R4)       BUMP R4 DOWN THE LINKED LIST
         C     R4,=F'0'        END OF LIST  ?
         BE    NEXTTCB         NOT A PRIVALEDGED PROGRAM
         LA    R9,13(,R4)      R9 ===>  PROGRAM NAME
         IC    R3,12(,R4)      LENGTH FOR COMPARE
****************************   R3 STILL CONTAINS EXEC LENGTH
         EX    R3,CLCINS       COMPARE THE NAMES
***********************************************************************
***                            CLCINS   CLC   0(0,R9),0(R7)           *
***********************************************************************
         BE    BLDUCOM         CHECK HIS EXTENSION                    *
         B     LISTSRCH        CONTINUE THROUGH THE LIST              *
         EJECT
*******  USING TCB,R6          ADDRESSABILITY TO TCB (NOT DROPPED)
NEXTTCB  L     R8,TCBTCB       R8 ===> NEXT TCB                       *
         LTR   R8,R8           IS THIS THE LAST TCB?                  *
         BZ    CANCEL          PRB NAME NOT FOUND ON TCB CHAIN OR END *
         LR    R6,R8           KEEP TCB POINTER IN R6                 *
         B     TCBSCAN         RUN THORUGH THE LIST AGAIN             *
***********************************************************************
*        BUILD JMR USER COMMUNICATION AREA.                         ***
*        AT THIS POINT A MATCH ON CDNAMES FOUND IN THE EXTENT       ***
*        LIST HAS OCCURED.  I.E. TAST, EDIT OR IPCS                 ***
***********************************************************************
BLDUCOM  DS    0H                                                   ***
*                                                                   ***
*        WTO   'EDIT MODE BLDUCOM          ',ROUTCDE=1
         LA    R12,JMRUCOM            ADDRESS OF USER EXIT AREA
         USING USERCOM,R12            ADDRESSABILITY TO UCOM FIELD
         TM    SDGFLAG0,SDGFLAG8      HAS HE BEEN EXTENDED
         BO    CANCEL                 HE'S ALREADY HAD HIS EXTENSION
CONTCHEK DS    0H
*        WTO   'EDIT MODE CONTCHEK         ',ROUTCDE=1
         CLC   5(7,R4),=C'SPFEDIT'    IS THIS EDIT MODE
         BNE   IPCSTEST               NO
         OI    SDGFLAG0,SDGFLAG2      INDICATE EDIT MODE
***********************************************************************
***      EDIT MODE IS ESTABLISHED.  BUT IF HE IS SPLITTING HIS      ***
***      SCREEN, HE'S CHEATING AND SHOULD NOT GET AN EXTENSION.     ***
***      IF EDIT / SPLIT IS DEEMED NOT CHEATING COMMENT THE         ***
***      THE FOLLOWING LINES                                        ***
***      R8 ===> CDE OF THE LAST RB                                 ***
***      USE R8 TO FOLLOW CDE CHAIN AND LOOK FOR ISPTASK            ***
***      THE FOLLOWING BAS INS MAY BE COMMENTED FOR THOSE           ***
***      NOT PENALIZING EDIT SPLIT MODE.                            ***
***********************************************************************
         BAS   R14,SPLITEST           GO TEST FOR EDIT SPLIT MODE
         B     GRANTEXT               GRANT EXTENSION
         B     CANCEL                 BLOE HIM AWAY
IPCSTEST DS    0H
         CLC   5(4,R4),=C'IPCS   '    IS THIS EDIT MODE
         BNE   NEXTTCB                NO
         OI    SDGFLAG0,SDGFLAG4      INDICATE IPCS MODE
         B     NEXTTCB                IPCS SINGLE SCREEN MODE
********                              STIPULATED (AT THIS LEVEL)
         EJECT
         SPACE 2
STCCHECK DS    0H
***********************************************************************
*                                                                     *
*        EXTEND JOB ACCORDING TO THE TIME FOR THAT BATCH JOB NAME     *
*                                                                     *
***********************************************************************
***********************************************************************
***            IS HE ON THE PRIVILEGED LIST                         ***
***********************************************************************
***********************************************************************
***                                                                 ***
***********************************************************************
         LA    R4,STCHEAD      POSITION R4 WITH PRIVILEDGED LIST
MONTHEAD DS    0H                                                     *
         L     R12,JMRUCOM     ADDRESS OF USER EXIT AREA              *
         LTR   R12,R12         IS THIS THE FIRST WAIT TIMER POP
         BNZ   CANCEL          HE'S ALREADY HAD HIS EXTENSION
         L     R4,0(,R4)       BUMP R4 DOWN THE LINKED LIST           *
         C     R4,=F'0'        END OF LIST  ?                         *
         BE    CANCEL          NOT A PRIVALEDGED PROGRAM              *
         LA    R9,5(,R4)       R9 ===>  PROGRAM NAME                  *
         IC    R3,4(,R4)       LENGTH FOR COMPARE
         LA    R7,JMRJOB       R7 ===> JMRJOB
****************************   R3 STILL CONTAINS EXEC LENGTH          *
         EX    R3,CLCINS      COMPARE THE NAMES                       *
****************************************************************      *
***                            CLCINS   CLC   0(0,R9),0(R7)           *
****************************************************************      *
         BE    STCEXTEN        CONTINUE CHECKING                      *
         B     MONTHEAD        CONTINUE THROUGH THE LIST              *
         SPACE 2
STCEXTEN DS    0H              GRANT BATCH EXTENSION                  *
         L     R15,FULL8       EXTENSION IN SECONDS
         L     R1,DUMPVAL      GIVE HIN THREE DAYS
         B     RETURN
         EJECT
GRANTEXT DS    0H
*        WTO   'EDIT MODE GRANTEXT         ',ROUTCDE=1
         TM    SDGFLAG0,SDGFLAG8       HAS EXTENSION BEEN GRANTED
         BO    CANCEL                  YOU ONLY GET TO GO AROUND ONCE
***********************************************************************
***      WE HAVE NOT BEEN EXTENDED AND OUR NAME IN ON THE EXTENT    ***
***      LIST.  IF WE ARE IN SPLIT SCREEN BLOW THE CHEATER AWAY.    ***
***      IF IN SINGLE SCREEN EDIT MODE, GRANT HIM AN EXTENSION.     ***
***      WITH THE TCB CHAIN EXHAUSTED WE CAN INTERROGATE THE        ***
***      JMRUCOM AND DECIDE WHETHER TO EXTEND OR NOT.               ***
***                                                                 ***
***********************************************************************
         TM    SDGFLAG0,SDGFLAG1       TEST FOR SPLIT SCREEN
         BO    CANCEL                  BLOW HIM AWAY
         TM    SDGFLAG0,SDGFLAG2       TEST FOR SPF EDIT
         BO    SPFEXTNT                GO TO SPF EXTENTION
         TM    SDGFLAG0,SDGFLAG3       TEST FOR IPCS
         BO    IPCEXTNT                GO TO IPCS EXTENTION
         B     CANCEL                  BLOW HIM AWAY
*                                      EXTEND WAIT TIME IN SECONDS
SPFEXTNT DS    0H                      ISPF EXTENTION
         L     R15,FULL8               INDICATE TIME IN SECONDS
         L     R1,EDITEXT              LOAD SECONDS TO EXTEND
         OI    SDGFLAG0,SDGFLAG8       EXTENT GRANTED
         B     RETURN                  GO RETURN TO CALLER
IPCEXTNT DS    0H                      ISPF EXTENTION
         L     R15,FULL8               INDICATE TIME IN SECONDS
         L     R1,IPCSEXT              LOAD SECONDS TO EXTEND
         OI    SDGFLAG0,SDGFLAG8       EXTENT GRANTED
         B     RETURN                  GO RETURN TO CALLER
*
SPLITEST DS    0H                     START OF LOOP
*        WTO   'EDIT MODE SPLITEST         ',ROUTCDE=1
*        USING CDENTRY,R8             CDE ADDRESSABILITY
         L     R8,CDCHAIN             POINT BACK TO CDE
         LTR   R8,R8                  END OF CHAIN
         BZ    CANCEL                 SHOULD ENCOUNTER ISPTASK FIRST
         CLC   CDNAME(7),=C'ISPTASK'     ARE YOU THE TASKER
         BNE   SPLITEST               OH WELL
         LH    R3,CDUSE               WHAT IS MODULE USE COUNT
         CH    R3,=H'+1'              ARE YOU SINGLE TASKING
         BH    DBLTASK                YOU'RE CHEATING
         BR    R14                    WE COULD HAVE JUST CANCELLED
DBLTASK  DS    0H                     TEST FOR DOUBLE TASKING
         OI    SDGFLAG0,SDGFLAG1      DOUBLE DIPPING
         LA    R14,4(,R14)            BUMP RETURN ADDRESS
         BR    R14                    RETURN TO CANCEL
***********************************************************************
*                                                                     *
*        ZERO REGISTER 2 TO INDICATE CANCEL                           *
*                                                                     *
***********************************************************************
*
CANCEL   DS    0H
         XR    R15,R15                 INDICATE RETURN CODE OF ZERO -
         XR    R1,R1                   INDICATE ZERO SECONDS
*                                      CONTINUE (CANCEL)
*
***********************************************************************
*                                                                     *
*        NORMAL END PROCESSING                                        *
*                                                                     *
***********************************************************************
*
RETURN   DS    0H
         L     R14,12(,R13)            LOAD ADDRESS FOR RETURN
         L     R0,20(,R13)             RESTORE REG ZERO
         LM    R2,R12,28(R13)          RESTORE REGISTERS 2 TO 12
         BR    R14                     RETURN TO CALLER IGNORE REG 1
         EJECT
**************************** CONSTANTS ********************************
FULL8    DC    F'8'
         SPACE 2
         DS    0F
         EJECT
         SPACE 5
CLCINS   CLC   0(0,R9),0(R7)            GENERAL COMPARE
         EJECT
         SDEXTALL
         IHARB DSECT=YES,LIST=NO
         PRINT NOGEN
         PUSH PRINT
         IEFJMR
         IHAPSA
         IHAASCB
         IHAASXB
         IHACDE
         IEESMCA
         IKJTCB LIST=YES
         POP PRINT
         MACRO
         SDEXTALL
.**********************************************************************
.***                                                                ***
.***     THESE ARE THE PROGRAM NAMES WHICH ARE TO BE EXTENDED       ***
.***     BEYOND THE NORMAL JOB WAIT TIME.                           ***
.***     THE LIST MAY BE INCREASED, INSERTING THREE ADCONS, ONE     ***
.***     FOR EACH NEW PROGRAM NAME.                                 ***
.***     AT THE PRESENT THEY THE ARE ORDERED BY EXPECTATION  OF     ***
.***     OCCURANCE IN THE JOB PACK QUEUE.                           ***
.**********************************************************************
.***     IBM LEVEL II HAS STATED THAT THERE IS NO EASY WAY TO       ***
.***     TEST THE EXISTENCE OF SPLIT MODE.  I.E. A SPLIT MODE       ***
.***     BIT IN SOME CONTROL BLOCK.                                 ***
.***     THE METHOD HERE WILL BE TO NOTE THE EXISTENCE OF A MODULE  ***
.***     USE COUNT EXCEEDING ONE.                                   ***
.***                                                                ***
.**********************************************************************
***********************************************************************
         EJECT
LISTHEAD DC    A(SPFEDIT)         WHERE TO START
SPFEDIT  DC    A(IPCS)            WHERE TO NEXT
         DC    AL1(6),C'SPFEDIT'  ISPF EDIT MODE
         DC    AL1(6),C'ISREDIT'  CDNAME
EDITEXT  DC    F'1800'            30 MINUTES
IPCS     DC    A(BOTTOM)          WHERE TO NEXT
         DC    AL1(6),C'IPCS   '  INTERACTIVE PROBLEM CONTROL SYSTEM
         DC    AL1(6),C'IPCS   '  CDNAME
IPCSEXT  DC    F'2700'            45 MINUTES
BOTTOM   DC    A(0)               END OF LIST
         DC    AL1(6),C'BOTTOM '  NOT USED
         DC    AL1(6),C'BOTTOM '  NOT USED
         DC    F'0000'            00 MINUTES
.**********************************************************************
.***                                                                ***
.***     THESE ARE THE STARTED TASKS WHICH ARE ELIGIBLE FOR         ***
.***     EXEMPTION FROM CONTINUOUS WAIT TIME CANCELLATION.          ***
.***     THE LIST MAY BE INCREASED, INSERTING TWO ADCONS, ONE       ***
.***     FOR EACH NEW STARTED TASKS AND THE TIME EXTENSION IN       ***
.***     SECONDS.                                                   ***
.***     AT THE PRESENT TIME THE LIST IS RESTRICTED TO ONE TASK.    ***
.***     THIS STARTED TASK GENERATES A TAPE MOUNT TO DUMP A SPECIFIC***
.***     SMF DATA SET.  THE PURPOSE OF EXEMPTION IS TO MAINTAIN     ***
.***     THE MOUNT PENDING OVER THE WEEKEND WHEN THE OPERATIONS     ***
.***     ROOM IS UNATTENDED.  ADDITIONS TO THIS LIST SHOULD BE      ***
.***     SCRUTINIZED VERY CAREFULLY.                                ***
.**********************************************************************
.***                                                                ***
.***                                                                ***
.**********************************************************************
***********************************************************************
         EJECT
STCHEAD  DC    A(TASK1)
TASK1    DC    A(TASKEND)          FIRST ON THE LIST
         DC    AL1(7),C'DUMPSMF '  SMF ARCHIVAL STARTED TASK
DUMPVAL  DC    F'259200'           THREE DAYS
TASKEND  DC    A(0)                END OF LIST
         DC    AL1(7),C'TASKEND '  FOR FUTURE POSSIBLE USE
         DC    F'0000'             CONSISTENCY
         EJECT
.**********************************************************************
.***                                                                ***
.***     THESE ARE THE USERID PREFIXES WHICH ARE ELIGIBLE           ***
.***     FOR EXTENSION.                                             ***
.***     THE LIST MAY BE INCREASED, INSERTING THREE ADCONS, ONE     ***
.***     FOR EACH NEW USERID PREFIX.                                ***
.***     AT THE PRESENT TIME THE LIST IS RESTRICTED TO IN HOUSE     ***
.***     PERSONNEL (BUILDING G) FOR SECURITY PURPOSES.  THESE       ***
.***     NAMES ARE PROJECT DEPENDANT AND SHOULD BE ADDED OR STRUCK  ***
.***     AS THE PROJECTS ARISE AND FINISH.                          ***
***********************************************************************
PRIVHEAD DC    A(USRID001)
USRID001 DC    A(USRID002)         FIRST ON THE LIST
         DC    AL1(2),C'TOP    '   ALL OPERATIONS
         DC    F'1800'             30 MINUTE EXTENTION
USRID002 DC    A(USRID003)         FIRST ON THE LIST
         DC    AL1(6),C'DSMY002'   JERRY CONCLIN 'DARTS' ASSUMED
         DC    F'12600'            THREE HOURS THIRTY MINUTES
USRID003 DC    A(USEREND)          END OF LIST
         DC    AL1(6),C'DSMY006'   GEORGE INGRAM 'DARTS' ASSUMED
         DC    F'12600'            THREE HOURS 30 MINUTES
USEREND  DC    A(0)                END OF LIST
         DC    AL1(6),C'USEREND'   FOR FUTURE POSSIBLE USE
         DC    F'0000'             CONSISTENCY
         EJECT
***********************************************************************
***     THE FOLLOWING DSECT MAPS SDTIF'S USER COMMUNICATION WORK AREA *
***     IT IS STORED IN THE JMRUCOM FILED OF THE COMMON EXIT          *
***     PARAMETER AREA                                                *
***********************************************************************
USERCOM  DSECT
SDGFLAG0 DS    BL1           USER FLAGS CORRESPONDING --
*                            TO SMFUSER IN SMFEPLA  OFFSET 92
*                            X X X X   X X X X
*                            0 1 2 3   4 5 6 7
*              X'00'         × × × ×   × × × ×              B'00000000'
SDGFLAG1 EQU   X'01'         × × × ×   × × × ISPTASK/DOUBLE B'00000001'
SDGFLAG2 EQU   X'02'         × × × ×   × × * SPFEDIT        B'00000010'
SDGFLAG3 EQU   X'04'         × × × ×   × ***                B'00000100'
SDGFLAG4 EQU   X'08'         × × × ×   ***** IPCS           B'00001000'
SDGFLAG5 EQU   X'10'         × × × ****                     B'00010000'
SDGFLAG6 EQU   X'20'         × × ******                     B'00100000'
SDGFLAG7 EQU   X'40'         × ********                     B'01000000'
SDGFLAG8 EQU   X'80'         CPU TIME EXTENDED ONCE         B'10000000'
*
***********************************************************************
***********************************************************************
UCOMLEN  EQU   *-USERCOM     CORE SIZE IF NEEDED
         EJECT
         MEND
         MACRO
         REGEQU
*        REGEQU                      STANDARD EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEND
         EJECT
         END   IEFUTL
