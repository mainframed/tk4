         TITLE 'CVTVTOC - VTOC MODIFIER DRIVER ROUTINE'
*FUNCTION:  THIS PROGRAM, ALONG WITH ITS COMPANION SERVICE
*           SUBROUTINE VTOCREAD, READS VTOCS AND MODIFYS FORMAT
*           1 DSCB'S.
*
*OPERATION:  THE PROGRAM SEARCHES THE TIOT FOR DDNAMES BEGINNING
*            WITH THE CHARACTERS 'VOL'. WHEN FOUND, THE SERIAL NUMBER
*            IS OBTAINED FROM THE JFCB, THE DEVICE TYPE IS DETERMINED,
*            DEVICE CONSTANTS ARE OBTAINED FROM AN INCORE TABLE,
*            A RESERVE IS ISSUED, AND THE VTOC IS OPENED FOR OUTPUT
*            AND PROCESSED. THE PROGRAM WILL PROCESS ANY NUMBER OF
*            'VOL' DD CARDS IN ONE PASS.
*
*            THIS PROGRAM HAS 3 OPERATING MODES WHICH ARE SELECTED
*            BY PARM VALUE:
*
*               A) LIST MODE - PRODUCES A LISTING OF THE DSNAMES FROM
*                              ALL FORMAT 1 DSCB'S ENCOUNTERED.
*               B) CHANGE MODE - CHANGES THE DSCB'S AND CAUSES THEM
*                                TO BE REWRITTEN TO DISK.
*               C) TEST MODE - FORCES LIST MODE, BYPASSES THE RESERVE
*                              AND DEQ CODE, AND CAUSES A CORE DUMP
*                              IF A WRITE ERROR OCCURS.
*
*             THE PERMISSABLE PARM VALUES ARE:
*
*                A) PARM='LI'  -  CAUSES THE PROGRAM TO RUN IN LIST
*                                 MODE ONLY.
*                B) PARM='LC'  -  CAUSES THE PROGRAM TO RUN IN LIST
*                                 AND CHANGE MODES.
*                C) PARM='CH'  -  CAUSES THE PROGRAM TO RUN IN CHANGE
*                                 MODE ONLY.
*                D) PARM='CT'  -  CAUSES THE PROGRAM TO RUN IN CHANGE
*                                 AND TEST MODES.
*                E) NO PARM    -  CAUSES THE PROGRAM TO RUN IN LIST
*                                 AND TEST MODES.
*
*        EXECUTION JCL SHOULD LOOK SOMETHING LIKE THIS:
*            //CVT EXEC PGM=CVTVTOC,PARM='LC'
*            //SYSPRINT DD SYSOUT=A
*            //SYSUDUMP DD SYSOUT=A
*            //VOLABC   DD UNIT=SYSDA,DISP=OLD,VOL=SER=123456
*            /*
*
*ENTRY POINTS:  ENTRY IS ALWAYS TO CVTVTOC.
*
*ATTRIBUTES:    MUST BE LINKED AUTHORIZED (SETCODE AC(1)) AND
*               RESIDE IN AN AUTHORIZED LIBRARY BECAUSE THE
*               RESERVE OPTIONS USED ARE RESTRICTED.
*
*
CVTVTOC  CSECT
         SAVE  (14,12),,CVTVTOC
         LR    R11,R15        ESTABLISH BASE
         USING CVTVTOC,R11
         LR    R4,13
         LA    13,SAVEAREA    LOCAL SAVE AREA
         ST    R4,SAVEAREA+4  FWD CHAIN
         ST    13,8(R4)       BKWD POINTER
         ST    R1,SAVER1      SAVE PARM POINTER
         L     R1,SAVEAREA+4  ADDR OF SAVE AREA WITH R1
         L     R1,24(R1)      R1 WITH PARM POINTER
         L     R1,0(R1)       ADDR OF PARM FIELD
         LH    R2,0(R1)       PARM LENGTH
         LTR   R2,R2          TEST FOR ZERO LENGTH
         BZ    NOPARM
         CLC   2(1,R1),=C'C'  CHANGE OPTION REQUESTED?
         BE    SETCHG         YES
         OI    OPTSW,LIST     SET LIST OPTION
         CLC   3(1,R1),=C'C'  LIST AND CHANGE OPTIONS REQUESTED?
         BNE   OPENSYS        NO
SETCHG   OI    OPTSW,CHGE     YES. SET CHANGE OPTION
         CLC   3(1,R1),=C'T'  IF TEST, ALSO SET LIST AND TEST BITS
         BE    NOPARM
         B     OPENSYS
NOPARM   OI    OPTSW,LIST     DEFAULT TO LIST ONLY
         OI    OPTSW,TEST     SET PGM INTO TEST MODE
OPENSYS  EQU   *
         OPEN  (SYSPRINT,(OUTPUT))
         LA    R7,SYSPRINT    DCB ADDRESS
         LA    R8,PTL         PRINT LINE ADDRESS
         MVC   PTL(31),=C'VTOC CONVERSION PROGRAM STARTED'
         WRITE MYECB,SF,(7),(8),MF=E
         MVC   PTL(80),BLANKS CLEAR PRINT LINE
* NOW BUILD AN AREA WITH TODAYS DATE
* FOR USE DURING CONVERSION
         TIME
         STH   R1,DAYWK+6          ISOLATE DAY VALUE
         SRL   R1,12          ISOLATE YEAR VALUE
         O     R1,=X'0000000F'     SET SIGN
         ST    R1,YRWK+4
         CVB   R1,DAYWK       CONVERT DAY TO BINARY
         STH   R1,CURRDAT+1   SAVE CONVERTED DAY
         CVB   R1,YRWK        CONVERT YEAR TO BINARY
         STC   R1,CURRDAT     SAVE CONVERTED YEAR
* LOCATE TIOT
         EXTRACT TIOTLOC,FIELDS=TIOT
         L     R4,TIOTLOC
         USING TIOT,R4
         LA    R4,TIOENTRY
         ST    R4,TIOTLOC     SAVE TIOT ENTRY ADDRESS
         DROP  R4
         B     VOLJOIN        GO PROCESS THIS VOL
         SPACE 2
***********************
* BEGIN PROCESSING NEXT VOLUME
************************
* SEARCH TIOT FOR DDNAME BEGINNING WITH 'VOL'
************************
         SPACE 1
NEWVOL   DS    0H
         L     R4,TIOTLOC     GET TIOT ENTRY ADDRESS
         USING TIOENTRY,R4    DSECT ADDRESSABILITY
         SR    R1,R1
         IC    R1,TIOELNGH    LENGTH OF CURRENT TIOT ENTRY
         AR    R4,R1          BUMP TO NEXT ENTRY
         ST    R4,TIOTLOC     SAVE NEW ENTRY ADDRESS
VOLJOIN  EQU   *
         CLI   TIOELNGH,X'00' ENTRY LENGTH ZERO ?
         BE    ALLDONE        GO FINISH UP
         CLC   TIOEDDNM(3),=C'VOL'   LOOK FOR 'VOL' DDNAMES
         BNE   NEWVOL         GET NEXT TIOT ENTRY IF NOT.
         MVC   DDNAME(8),TIOEDDNM    SAVE DDNAME
         MVC   UCBADDR+1(3),TIOEFSRT   UCB ADDR
         DROP  R4
* GET DEVICE TYPE AND VERIFY DASD
         DEVTYPE DDNAME,DEVCHAR
         LTR   R15,R15        DDNAME FOUND?
         BNZ   NODDCARD       NO. ERROR MSG.
         CLI   DEVCHAR+2,X'20'  IS IT DASD ?
         BNE   NOTDACC        NO.
* NOW READ JFCB AND GET VOL SERIAL NUMBER
         LA    R3,PDSDCB      GET DCB ADDRESS
         USING IHADCB,R3      USE TO GET ACCESS TO STD NAMES
         MVC   DCBDDNAM(8),DDNAME   STORE DDNAME IN DCB
         DROP  R3
         RDJFCB (PDSDCB)
         L     R4,JFCBADR     SET UP JFCB BASE ADDR
         USING JFDUMMY,R4     ESTABLISH ADDRESSABILITY
         MVC   VOLUME(6),JFCBVOLS  SAVE VOL SERIAL
* NOW GO OPEN THIS VTOC
         TM    OPTSW,TEST     IF IN TEST MODE,
         BO    ENDRSV         BYPASS THE RESERVE
         RESERVE (MAJQNM,VOLUME,E,6),UCB=UCBADDR
ENDRSV   LA    R0,1           FUNCTION CODE FOR OPEN
         LA    R1,DDNAME      DD TO BE OPENED
         L     R15,VTOCREAD   GO OPEN AND READ 1ST TRACK
         BALR  R14,R15
         B     *+4(R15)       BRANCH ON RETURN CODE
         B     VOPENED        0, OK
         B     CANTOPEN       4, UNABLE TO OPEN
         B     NOTDACC        8, NOT DASD DEVICE
* AT THIS POINT, THE VTOC HAS BEEN OPENED
* AND THE FIRST TRACK OF DSCB'S IS IN CORE
VOPENED  EQU   *
         LR    R6,R1          DSCB BUFFER ADDRESS
         LR    R2,R0          NUMBER OF DSCBS PER TRACK
         ST    R6,SAVER6      SAVE BUFF ADDR
         ST    R2,SAVER2      SAVE DSCB COUNT
         MVC   PTL(24),=C'BEGIN PROCESSING VOLUME '
         MVC   PTL+24(6),VOLUME    VOL SERIAL TO MSG
         XC    MYECB(16),MYECB     CLEAR ECB
         WRITE MYECB,SF,(7),(8),MF=E
         MVC   PTL(80),BLANKS     CLEAR PRINT LINE
         USING DS1,R6         ADDR OF BUF RETURNED FROM OPEN
NXTTRK   LA    R6,8(R6)       INCR PAST CCHHR IN BUF
F1LOOP   EQU   *
         CLI   DS1FMTID,X'F1' TEST FOR FORMAT 1
         BE    PROCF1         GO PROCESS
BUMP     LA    R6,148(R6)     BUMP TO NEXT DSCB
         BCT   R2,F1LOOP      GO TEST NEXT DSCB
* FALL THRU HERE WHEN ALL DSCB'S ON A TRACK
* HAVE BEEN PROCESSED
         TM    OPTSW,MODIFIED ANY DSCB'S CHANGED?
         BNO   READNXT        NO. GO GET NEXT TRACK
* MUST NOW WRITE BACK THE MODIFIED DSCB'S
         NI    OPTSW,X'C4'   SET OFF MODIFIED TAG
         LA    R0,3           FUNCTION CODE FOR WRITE
         L     R15,VTOCREAD   ADDR OF SERVICE SUBR
         BALR  R14,R15        WRITE BACK MODIFIED DATA
*              CHECK WRITE RETURN CODES
         B     *+4(R15)       BRANCH ON RETURN CODE
         B     READNXT        0,  OK
         B     WTERR          4,  NG
READNXT  SR    R0,R0          FUNCTION CODE FOR READ
         L     R15,VTOCREAD   ADDR OF SERVICE SUBR
         BALR  R14,R15        READ NEXT VTOC TRACK
         L     R6,SAVER6      ADDR OF DSCB BUFFER
         L     R2,SAVER2      NUMBER OF DSCB'S PER TRACK
         B     *+4(R15)       BRANCH ON RETURN CODE
         B     NXTTRK         0, OK
         B     CLOSEVOL       4, END OF FILE
         B     PERMIO         8, IO ERROR
* WRITE ERROR. EXCP ECB HAS SOMETHING BESIDES A 7F IN IT.
* THIS CAUSED WRITE SUBR TO ISSUE A RETURN CODE 4.
WTERR    DS    0H
         XC    MYECB(16),MYECB      CLEAR THE ECB
         MVC   PTL(27),=C'*** WRITE ERROR ON VTOC ***'
         WRITE MYECB,SF,(7),(8),MF=E
         TM    OPTSW,TEST     IF WE ARE IN TEST MODE,
         BNO   GO             GET A DUMP ON WRT ERR. ELSE NEXT VOLUME
         ABEND 111,DUMP          TAKE A DUMP
GO       MVC   PTL(80),BLANKS     CLEAR THE PRINT LINE
         XC    MYECB(16),MYECB     CLEAR SYSPRINT ECB
         MVC   PTL(28),=C'*** WILL TRY NEXT VOLUME ***'
         WRITE MYECB,SF,(7),(8),MF=E
* CLOSE OUT THIS VOL AND TRY THE NEXT ONE
         B     CLOSEVOL
*PROCESS FORMAT 1 DSCB'S
PROCF1   EQU   *
         TM    OPTSW,LIST     LIST REQUESTED?
         BNO   CKCHG          NO
         MVC   PTL+4(44),DS1DSNAM    MOVE DSN TO PRINT LINE
         XC    MYECB(16),MYECB
         WRITE MYECB,SF,(7),(8),MF=E
         MVC   PTL(80),BLANKS
CKCHG    TM    OPTSW,CHGE     CHANGE OPTION REQUESTED?
         BNO   BUMP           NO. GET NEXT DSCB
USECURR  MVC   DS1DSSN+3(3),CURRDAT   USE CURRENT DATE
MVZERO   MVC   DS1DSSN(3),VOLZERO     ZERO THE CNT FIELD
         OI    OPTSW,MODIFIED         SET ON 'DSCB MODIFIED' TAG
         B     BUMP           GO GET NEXT DSCB
         SPACE 3
* ENTIRE VTOC FOR THIS VOL HAS BEEN PROCESSED
* CLOSE IT AND GO SEE IF MORE VOLS TO BE PROCESSED.
CLOSEVOL EQU   *
         TM    OPTSW,TEST     IF IN TEST MODE,
         BO    ENDEQ          BYPASS THE DEQ.
         DEQ   (MAJQNM,VOLUME,6),UCB=UCBADDR
ENDEQ    LA    R0,2           FUNCTION CODE FOR CLOSE
         L     R15,VTOCREAD   ADDR OF SERVICE SUBR
         BALR  R14,R15
         B     NEWVOL         GO SEE IF ANY MORE VOLS
* PROCESSING COMPLETE. PRINT MESSAGE, CLEANUP, GET OUT.
ALLDONE  EQU   *
         SR    R15,R15        CLEAR RETURN CODE
NG       MVC   PTL(32),DONEMSG    CAN ENTER HERE IF BAD RC
         XC    MYECB(16),MYECB
         WRITE MYECB,SF,(7),(8),MF=E
         CLOSE (SYSPRINT)
         L     R13,SAVEAREA+4
         RETURN (14,12),T,RC=(15)
         EJECT
********************
*ERROR MSG PRINTING*
********************
         SPACE
NODDCARD MVC   PTL+1(19),=C'CANNOT FIND DD CARD'
         B     PRINT
NOTDACC  MVC   PTL+1(27),=C'DEVICE IS NOT DIRECT ACCESS'
         B     PRINT
UNKNDEV  MVC   PTL+1(19),=C'UNKNOWN DEVICE TYPE'
         B     PRINT
CANTOPEN MVC   PTL+1(27),=C'ATTEMPT TO OPEN VTOC FAILED'
PRINT    DS    0H
         XC    MYECB(16),MYECB
         WRITE MYECB,SF,(7),(8),MF=E
         MVC   PTL(80),BLANKS
         B     NEWVOL
PERMIO   EQU   *              IO ERROR READING VTOC
         MVC   PTL+1(26),=C'PERM IO ERROR READING VTOC'
         XC    MYECB(16),MYECB
         WRITE MYECB,SF,(7),(8),MF=E
         MVC   PTL(80),BLANKS
         B     CLOSEVOL
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
SAVEAREA DC    18F'0'
SAVER1   DS    F              SAVE PARM REG
SAVER2   DS    F              SAVE NUMBER OF DSCB'S PER TRACK
SAVER6   DS    F              SAVE DSCB BUFFER ADDR
OPTSW    DC    X'00'          OPTIONS SELECTED
LIST     EQU   X'80'          LIST OPTION SELECTED
CHGE     EQU   X'40'          CHANGE OPTION REQUESTED
MODIFIED EQU   X'08'          DSCB HAS BEEN MODIFIED
TEST     EQU   X'04'          SET PGM INTO TEST MODE
TIOTLOC  DC    A(0)           CURR TIOT ENTRY LOC
LASTFMT1 DC    44X'FF'
DEVCHAR  DC    2F'0'
DEVNAMAD DC    A(0)
DDNAME   DC    CL8'VOLUME00'
JFCBADR  DS    0F
         DC    X'87'
         DC    AL3(JFCB)
DSN1PTR  DC    A(DSNJFCB)     DSN FROM JCL
VTOCREAD DC    V(VTOCREAD)    ADDR OF SERVICE SUBR
VOLZERO  DC    6X'00'
OLDDATE  DC    XL2'40F7'      OLD FORMAT TEST
         DS    0F
         DS    X
CURRDAT  DC    XL3'000000'    CURR DATE IN NEW FORMAT
PAKWK    DC    D'0'
         DS    0F             * THIS ALIGNMENT REQD
         DS    X              * TO FORCE RIGHT 2 BYTES OF
BINDATE  DC    XL3'000000'    * BINDATE TO HALF WD BOUND
PTL      DS    CL80           PRINT LINE
HOLD     DC    F'0'
DAYWK    DC    2F'0'
YRWK     DC    2F'0'
MYECB    DC    2D'0'
BLANKS   DC    CL80' '
UCBADDR  DC    A(0)           UCB ADDRESS
MAJQNM   DC    CL8'SYSVTOC '
VOLUME   DC    CL6'000000'
DONEMSG  DC    CL32'*** VTOC CONVERSION COMPLETE ***'
PATCH    DC    20F'0'
         DS    0D
JFCB     DS    CL176
         EJECT
SYSPRINT DCB   BLKSIZE=80,LRECL=80,DSORG=PS,MACRF=(W),                 X
               DDNAME=SYSPRINT,RECFM=F
         EJECT
PDSDCB   DCB   DSORG=PS,MACRF=(E),DDNAME=VOLUME01,                     X
               EXLST=JFCBADR
         SPACE 2
DSNJFCB  DS    CL44
         LTORG
         EJECT
* FORMAT 1 DSCB DSECT
DS1      DSECT
         IECSDSL1  1
         EJECT
* FORMAT 4 DSCB
DS4      DSECT
DS4DSNAM DS    11F
         IECSDSL1  4
         EJECT
* DCB DSECT
         DCBD  DSORG=PS,DEVD=DA
         EJECT
* JOB FILE CONTROL BLOCK DSECT
JFDUMMY  DSECT
         IEFJFCBN
         EJECT
* TASK IO TABLE (TIOT) DSECT
TIOT     DSECT
         IEFTIOT1
         END   CVTVTOC
