SUBSYS   TITLE '-----  LIST SUBSYSTEMS  -----'
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  SUBSYS                                             *
*                                                                     *
*   PURPOSE:       LIST ALL DEFINED SUBSYSTEMS                        *
*                                                                     *
*   AUTHOR:        LEONARD D. WOREN                                   *
*                                                                     *
*   DATE:          JANUARY 28, 1982                                   *
*                                                                     *
*   ATTRIBUTES:    PROBLEM STATE, NOT RE-ENTRANT                      *
*                                                                     *
*   UPDATES:                                                          *
*      12/13/83 LDW  DISPLAY 4 BYTES OF SSVT ADDR                     *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
SUBSYS   OSENTER  EXIT=LEAVE,RC=0
LINK     EQU   R11
         L     R2,16                    -> CVT
         USNGX CVT,R2
         MVC   BUFFER(4),=C'CVT='
         HEX   BUFFER+4,16,LEN=4
         MVC   BUFFER+15(6),=C'JESCT='
         HEX   BUFFER+21,CVTJESCT,LEN=4
         L     R2,CVTJESCT              -> JESCT
         DROPX R2
         USNGX JESCT,R2
         MVC   BUFFER+32(12),=C'PRIMARY_JES='
         MVC   BUFFER+44(4),JESPJESN
         BAL   LINK,TPUT
         L     R2,JESSSCT               -> FIRST SSCT
         DROPX R2
         USNGX SSCT,R2
         SPACE 2
LOOP     MVC   BUFFER(6),=C'SSCVT='
         ST    R2,WORK
         HEX   BUFFER+6,WORK,LEN=4
         MVC   BUFFER+16(5),=C'NAME='
         MVC   BUFFER+21(4),SSCTSNAM
         MVC   BUFFER+27(5),=C'SSVT='
         HEX   BUFFER+32,SSCTSSVT,LEN=4
         MVC   BUFFER+42(5),=C'FLG1='
         HEX   BUFFER+47,SSCTFLG1,LEN=1
         MVC   BUFFER+51(5),=C'SUSE='
         HEX   BUFFER+56,SSCTSUSE,LEN=4
         L     R3,SSCTSSVT
         USNGX SSVT,R3
         MVC   BUFFER+66(5),=C'FNUM='
         HEX   BUFFER+71,SSVTFNUM,LEN=2
         BAL   LINK,TPUT
         ICM   R2,B'1111',SSCTSCTA      -> NEXT SSCT
         BNZ   LOOP
         B     LEAVE
         SPACE 2
TPUT     TPUTX BUFFER,80
         MVC   BUFFER,BLANKS
         BR    LINK
         LTORG ,
WORK     DC    F'0'
         HEXTAB  DUAL=NO
BLANKS   DC    CL80' '
BUFFER   DC    CL80' '
         EJECT ,
         CVT   DSECT=YES
         EJECT ,
         IEFJESCT  ,
         EJECT ,
         IEFJSCVT  ,
         EJECT ,
         IEFJSSVT  ,
         SPACE 3
         END
