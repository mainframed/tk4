REMARK   TITLE 'FORTRAN WTO SUBROUTINE'
*
*      THIS PROGRAM WRITES A MESSAGE TO THE OPERATOR'S CONSOLE AND
* TO THE SYSTEM LOG. THE CALLING SEQUENCE FOR "REMARK" IS AS FOLLOWS:
*           1. THE LENGTH OF THE MESSAGE (FULL WORD BINARY) - MUST
*                 BE GREATER THAN 0 AND LESS THAN OR EQUAL TO 80
*           2. THE TEXT OF THE MESSAGE
*
*      THE "REMARK" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
REMARK   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING WORKAREA,R13        ADDRESS WORKAREA DSECT
         LM    R4,R5,0(R2)         LOAD PARAMETER POINTERS
         L     R4,0(,R4)           LOAD LENGTH OF TEXT
         LTR   R4,R4               IF LENGTH IS NOT POSITIVE
         BNP   ERROR                  THEN FLAG AS AN ERROR
         C     R4,=F'80'           IF LENGTH IS GREATER THAN 80
         BH    ERROR                  THEN FLAG AS AN ERROR
         MVC   TEXT,BLANKS         CLEAR THE TEXT WORK AREA
         BCTR  R4,R0               DECREMENT LENGTH FOR EXECUTE
         EX    R4,MOVETEXT         MOVE THE TEXT TO WORK AREA
         MVC   CALLIST,CCALLST     MOVE PARAMETER LIST TO WORK AREA
         LOAD  EP=MESSAGE          LOAD ROUTINE TO PERFORM THE WTO
         LR    R15,R0              SAVE "MESSAGE" ENTRY POINT ADDRESS
         CALL  (15),(TEXT),        CALL "MESSAGE"                      X
               MF=(E,CALLIST)
         B     EXIT                   AND LEAVE
ERROR    DS    0H
         LA    R15,12              SET RETURN CODE TO INDICATE ERROR
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
         SPACE
MOVETEXT DS    0H
         MVC   TEXT(0),0(R5)       MOVE MESSAGE TO WORK AREA
         EJECT
CCALLST  CALL  ,(0),MF=L           GENERATE PARAMETER LIST FOR CALL
CALLEN   EQU   *-CCALLST           COMPUTE PARAMETER LIST LENGTH
         SPACE
BLANKS   DC    CL80' '
         EJECT
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
WORKSTRT EQU   *
         DS    18A                 REGISTER SAVE AREA
CALLIST  DS    CL(CALLEN)          PARAMETER LIST FOR CALL MACRO
TEXT     DS    CL80                TEXT OF MESSAGE
WORKEND  EQU   *
WORKSIZE EQU   WORKEND-WORKSTRT    COMPUTE SIZE OF WORKAREA
         END
