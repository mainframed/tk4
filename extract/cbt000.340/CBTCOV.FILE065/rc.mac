RC       TITLE 'DISPLAY RETURN CODE FROM PREVIOUS COMMAND'
*---------------------------------------------------------------------*
* UPDATES:                                                            *
*    05/14/80 SDM2 - CHANGE TO HALFWORD RETURN CODE                   *
*    07/23/79 SDM1 - ADD HEXADECIMAL DISPLAY                          *
*---------------------------------------------------------------------*
RC       CSECT
         SAVE  (14,12),,RC_&SYSDATE_&SYSTIME
         LR    BASE,R15
         USING RC,BASE
         LA    R15,SAVEAREA
         ST    R15,8(,R13)
         ST    R13,4(,R15)
         LR    R13,R15
         LR    R2,R1
         USING CPPL,R2
         L     R3,CPPLUPT               FOR PUTLINE
         L     R4,CPPLECT
         USING ECT,R4
         UNPK  HEXWORK(7),ECTRTCD(4)                               SDM1
         TR    HEXWORK(6),HEXTAB                                   SDM1
         CLI   ECTRCDF,X'80'            LAST ONE ABEND?
         BE    ABEND                    YES
         SR    R5,R5                                               SDM2
         ICM   R5,B'0011',ECTRTCD+1     GET 2 BYTE RETURN CODE     SDM2
         BZ    ZEROIT                   IT WAS ZERO                SDM2
         CVD   R5,DWORK                 CONVERT IT TO DECIMAL      SDM1
         MVC   EDWORK(L'EDMASK),EDMASK                             SDM2
         EDMK  EDWORK(L'EDMASK),DWORK+5                            SDM2
         MVC   MRC2,0(R1)               MOVE LEFT JUSTIFIED        SDM1
         MVC   MRC1,HEXWORK+2           MOVE THE HEX VALUE         SDM2
         B     PUTRC
ZEROIT   MVC   MSG(2),=Y(MZEROL)                                   SDM1
PUTRC    PUTLINE PARM=PUTPARM,UPT=(3),ECT=(4),ECB=ECB,                 X
               OUTPUT=(MSG,TERM,SINGLE,DATA),MF=(E,IOPLADS)
         B     ENDIT
ABEND    MVC   MSG2CC,HEXWORK                                      SDM1
         PUTLINE PARM=PUTPARM,UPT=(3),ECT=(4),ECB=ECB,                 X
               OUTPUT=(MSG2,TERM,SINGLE,DATA),MF=(E,IOPLADS)
ENDIT    L     R13,4(,R13)
         RETURN (14,12),RC=0
         EJECT
ZEROSUP  CLI   0(R6),X'F0'
         BNER  R8
         MVI   0(R6),X'40'
         LA    R6,1(,R6)
         BCT   R7,ZEROSUP
         BR    R8
         EJECT
SAVEAREA DC    9D'0'
DWORK    DC    D'0'
HEXTAB   EQU   *-C'0'                                              SDM1
         DC    C'0123456789ABCDEF'
HEXWORK  DC    CL7' '                                              SDM1
         SPACE 1                                                   SDM1
* THE FOLLOWING THREE LINES MUST REMAIN TOGETHER                   SDM1
EDWORK   DS    0CL12                                               SDM1
         DC    C' XX,XXX DECIMAL)'                                 SDM2
         DC    CL8' '                   LEAVE EXTRA BLANKS FOR     SDM2
*                                          LEFT JUSTIFIED MVC      SDM2
EDMASK   DC    X'4020206B202020'                                   SDM2
         SPACE 1                                                   SDM1
MSG      DC    Y(MSGL,0)
         DC    CL16'RETURN CODE WAS'
MRC1     DC    CL4'ZERO'                                           SDM2
MZEROL   EQU   *-MSG                                               SDM1
         DC    C' ('                                               SDM1
MRC2     DC    C' XX,XXX DECIMAL)'                                 SDM2
MSGL     EQU   *-MSG                                               SDM1
         SPACE 1                                                   SDM1
MSG2     DC    Y(MSG2L,0)
         DC    C'SYSTEM COMPLETION CODE WAS '
MSG2CC   DC    CL3' '
MSG2L    EQU   *-MSG2                                              SDM1
         SPACE 1                                                   SDM1
PUTPARM  PUTLINE MF=L
IOPLADS  DC    4F'0'
ECB      DS    F'0'
         EJECT
         IKJCPPL
         EJECT
         IKJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
BASE     EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
         END   RC
