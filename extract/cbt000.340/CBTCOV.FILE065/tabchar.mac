*          DATA SET TABCHAR    AT LEVEL 002 AS OF 04/08/78
TABCHAR   START
*                        EDIT SUBCOMMAND PROCESSOR TO SET THE LOGICAL
*                        TAB CHARACTER FOR USE IN EDIT.
*
*                        THIS SUBCOMMAND MUST BE INSTALLED WITH LOCAL
*                        FIX ZT00012 IN ORDER FOR THE TAB CHARACTER
*                        TO TAKE EFFECT.
*
*                        WRITTEN BY JOHN A. NORDTVEDT
*                                   DEPT EXOKF
*                                   UNITED AIR LINES
*                                   P.O. BOX 66100
*                                   CHICAGO, ILL. 60666
*                                   (312) 952-5410
*
*                        LAST REVISED    02/09/76
*
*
*
*              REGISTER ASSIGNMENTS
REG0     EQU   0
REG1     EQU   1
REG2     EQU   2                   WORK REGISTER
REG3     EQU   3                   WORK REGISTER
REG4     EQU   4                   WORK REGISTER
REG5     EQU   5                   WORK REGISTER
CABASE   EQU   8                   BASE FOR EDIT COMMUNICATIONS TABLE
BASE     EQU   12                  BASE FOR MAIN PROGRAM
SAVEREG  EQU   13                  ADDRRESS OF SAVEAREA
REG14    EQU   14
REG15    EQU   15
*
*
         SAVE  (14,12),,TABCHAR
         LR    BASE,REG15
         USING TABCHAR,BASE
         LR    CABASE,REG1         SAVE POINTER TO COMMUNICATIONS TABLE
         USING IKJEBECA,CABASE
         GETMAIN   R,LV=WLNGTH     GET CORE FOR WORKAREA
         ST    REG1,8(SAVEREG)     SET UP SAVEAREA LINKAGE
         ST    SAVEREG,4(REG1)       *
         LR    SAVEREG,REG1          *
         USING WORKAREA,SAVEREG      *
         LA    REG3,$PPL
         USING PPL,REG3            PPL ADDRESSABILITY                 U
*
***********************************************************************
*
**    INITIALIZE PARSE PARAMETER LIST - PARSE TABCHAR COMMAND         *
*
***********************************************************************
*
         SPACE 1
         MVC   PPLUPT,CAPTUPT      UPT PTR
         MVC   PPLECT,CAPTECT      ECT PTR
         SR    REG4,REG4           GET A ZERO
         ST    REG4,$ECB           INIT CP ECB
         LA    REG5,$ECB           ECB PTR
         ST    REG5,PPLECB         TO PPL
         MVC   PPLPCL,APARSPCL     PCL PTR
         LA    REG5,$PARSANS       ANS PLACE PTR
         ST    REG5,PPLANS         TO PPL
         MVC   PPLCBUF,CAPTIBFR    COMMAND BUFFER PTR
         ST    REG4,PPLUWA         ZERO UWA PTR
         LA    REG1,PPL            ADDR OF PPL FOR PARSE
         SPACE 1
         LINK  EP=IKJPARS          LINK TO PARSE ROUTINE
         SPACE 1
         LTR   REG15,REG15         TEST PARSE RETURN CODE
         BNZ   PARSERR             GO TO ERROR ROUTINE IF ^= 0
*
*
***********************************************************************
*
*
*
         IC    REG4,DEFAULT        LOAD TAB DEFAULT CHARACTER
         STC   REG4,CAPINST+1      STORE IN COMMUNICATIOMS TABLE
         L     REG4,$PARSANS       READ REPLY FROM PARS
         USING IKJPARMD,REG4          *
         TM    TABPDE+6,X'80'         *   WAS OPERAND ENTERED ?
         BZ    EXIT                   *   NO
         L     REG5,TABPDE            *   YES, COPY IT TO INSTRUCTION
         IC    REG5,0(REG5)                       *
         STC   REG5,CAPINST+1                     *
         B     EXIT
*
*
PARSERR  LA    REG0,L'MESSAGE      LOAD LENGTH OF MESSAGE
         LA    REG1,MESSAGE        LOAD ADDRESS OF MESSAGE
         TPUT  (1),(0),R           PRINT ERROR MESSAGE
*
*
*                        EXIT
*
EXIT     LR    REG1,SAVEREG        LOAD ADDRESS OF WORK AREA
         L     SAVEREG,SAVEAREA+4  RESTORE SAVEAREA POINTER
         FREEMAIN R,LV=WLNGTH,A=(1)
         RETURN   (14,12),T,RC=0
*
*
*                        MESSAGE FORMAT AREA
*
APARSPCL DC    A(PARSEPCL)
DEFAULT  DC    XL1'05'
MESSAGE  DC    C' PARS ERROR '
*
*
*
**********************************************************************
*
**    DEFINE THE TABCHAR COMMAND SYNTAX FOR PARSE                     *
*
**********************************************************************
*
PARSEPCL IKJPARM
TABPDE  IKJIDENT 'TAB CHARACTER +',ASIS,MAXLNTH=1,                     X
               FIRST=ANY,HELP=('SINGLE CHARACTER ONLY+','VALID TAB CHARX
               ACTER, ONE CHARACTER AND ONLY ONE CHARACTER','NULL LINE X
               FOR STANDARD TAB CHARACTER, AND GIVE UP')
         IKJENDP
*
**********************************************************************
*
**    DEFINE THE DUMMY CONTROL SECTIONS USED                          *
*
**********************************************************************
*
*
IKJEBECA DSECT
         ORG    IKJEBECA+4
CAPINST  DS    XL4'9505C003'
         ORG    IKJEBECA+528
CAPTUPT  DS    F                   POINTER TO USER PROFILE TABLE
         ORG    IKJEBECA+532
CAPTECT  DS    F                   POINTER TO ENVIRONMENT CONTROL TAB
         ORG    IKJEBECA+40
CAPTIBFR DS    F                   POINTER TO SUBCOMMAND INPUT BUFFER
*
         IKJPPL
LPPL     EQU   *-PPL
*
*
**********************************************************************
*
**    DEFINE PROGRAM SAVE AND WORK AREAS                             **
*
**********************************************************************
*
WORKAREA DSECT
SAVEAREA DS    18F                 REGISTER SAVEAREA
$PPL     DS    (LPPL)C,0F   PARSE PARAM LIST          ( IKJPPL  )
$PARSANS DS    F            PARSE ANSWER PLACE ADDR
$ECB     DS    F            ECB FOR COMMAND PROCESSOR
WLNGTH   EQU   *-WORKAREA          LENGTH OF WORKAREA
*
*
         END
++ PTF (ZT00012)
                 /*
                    THIS MODIFICATION WILL ALLOW THE USE OF A
                    USER SPECIFIED TAB CHARACTER IN EDIT.
                    THE COMMAND PROCESSOR "TABCHAR" MUST ALSO
                    BE PLACED IN CMDLIB FOR THIS MODIFICATION
                    TO TAKE EFFECT.

                    THE MODIFICATION TO IKJEBELE WILL RELPLACE THE
                    CLI OF THE TAB CHARACTER WITH AN EXECUITE OF
                    A CLI IN THE EDIT COM TABLE.
                    THE MODIFICATION TO IKJEBMA9 WILL MAKE TABCHAR
                    KNOW TO EDIT AS A VALID SUBCOMMAND.
                    THE MODIFICATION TO SGIKJ0E2 WILL LOAD THE
                    INITIAL CLI INSTRUCTION IN THE EDIT COM TABLE.

                    THIS MODIFICATION WAS KF00097 ON MVT.

                    THE FOLLOWING UCLIN MUST BE RUN BEFORE
                    INSTALLING THIS MODIFICATION.
                       UCLIN.
                       ADD MOD(IKJEBMA9) DLIB(ACMDLIB) LMOD(IKJEBEMA).
                       ENDUCL.


                 */.
++ VER (Z037) PRE (UZ81100).
++ ZAP (IKJEBELE).
NAME IKJEBELE
VER 021A 9505C003       CLI   TEXTBFR-1(@12),X'05'
VER 0270 1E6C           ALR   @06,@12
VER 0272 95056003       CLI   TEXTBFR-1(@06),X'05'
VER 0516 9505C003       CLI   GETTXT-1(@12),X'05'
VER 06FE 9505C003       CLI   TEXTBFR-1(@12),X'05'
REP 021A 44009004       EX    CAPINST+4             EXECUITE CLI FOR TAB
REP 0270 1EC6           AR    @12,@06
REP 0272 44009004       EX    CAPINST+4             EXECUITE CLI FOR TAB
REP 0516 44009004       EX    CAPINST+4             EXECUITE CLI FOR TAB
REP 06FE 44009004       EX    CAPINST+4             EXECUITE CLI FOR TAB
++ MOD (IKJEBMA9) TXLIB(SMPOBJ)  /* THE SOURCE IS IN 'SYS1.PANLIB' */
                                 /* AND IS LISTED BELOW.           */
      /*
         IKJEBEST  (TABCHAR,TABC,TABCHAR),CSECT=USER
*                  MACRO "IKJEBEST" IS IN "PVTMACS"
         END
      */.
++ UPDTE (SGIKJ0E2)  /*
         LINE NUMBERS FOR THE UPDTE ARE 06257600,36760500
  */.
./    CHANGE   NAME=SGIKJ0E2
        MVC   4(4,9),$INST             PLACE INST IN COM TABLE
$INST   DC    XL4'9505C003'            TM FOR TAB CHAR
