UNPBIT   TITLE 'SUBROUTINE WHICH UNPACKS BITS INTO BYTES'
*
*      THIS PROGRAM UNPACKS A NUMBER OF BYTES INTO A CORRESPONDING
* NUMBER OF 8-BYTE STRINGS SUCH THAT EACH BIT IN THE INPUT BYTE
* CORRESPONDS TO A BYTE IN THE OUTPUT STRING. THE CALLING SEQUENCE
* FOR "UNPBIT" IS AS FOLLOWS:
*           1. THE SOURCE FIELD TO BE UNPACKED.
*           2. THE TARGET FIELD TO BE UNPACKED INTO.
*           3. THE NUMBER OF BYTES TO BE UNPACKED (IF THIS VALUE IS
*              NOT POSITIVE, 12 IS RETURNED IN REGISTER 15)
*
*      THE "UNPBIT" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
UNPBIT   ENTER PARMREG=2,          INITIALIZE                          X
               GETMAIN=(72,0)
         REGISTER                  REGISTER EQUATES
         LM    R4,R6,0(R2)         LOAD PARAMETER POINTERS
         L     R6,0(,R6)           LOAD STRING COUNT VALUE
         LTR   R6,R6               IF THE STRING COUNT IS NOT POSITIVE
         BNP   ERROR                  THEN FLAG AS AN ERROR
LOOP1    DS    0H
         LA    R7,8
         LA    R9,1                INITIALIZE MASK
         MVC   0(8,R5),=C'00000000' INITIALIZE TARGET STRING
LOOP2    DS    0H
         LR    R8,R5               LOAD ADDRESS OF TARGET STRING
         BCTR  R8,0                DECREMENT BY ONE
         AR    R8,R7               POINT TO THE BYTE IN TARGET STRING
         EX    R9,TM               IF SOURCE BIT IS OFF
         BZ    TESTOK                 THEN LEAVE ZERO IN TARGET BYTE
         MVI   0(R8),C'1'             ELSE MOVE ONE TO TARGET BYTE
TESTOK   DS    0H
         SLL   R9,1                MODIFY THE TM MASK
         BCT   R7,LOOP2            FINISH UP THIS 8-BYTE STRING
         LA    R5,8(,R5)           POINT TO NEXT 8-BYTE STRING
         LA    R4,1(,R4)           POINT TO NEXT TARGET BYTE
         BCT   R6,LOOP1            GO DO THE NEXT STRING
         LA    R15,0               INDICATE SUCCESSFUL OPERATION
         B     EXIT                   AND EXIT
TM       DS    0H
         TM    0(R4),X'00'         INSTRUCTION USED ONLY BY EXECUTE
ERROR    DS    0H
         LA    R15,12              SET RETURN CODE TO INDICATE ERROR
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(72,0)
         END
