ZAP      TITLE 'ZAP --- DISPLAY AND MODIFY DATA SETS   ---   TSO'
***********************************************************************
* UPDATES:                                                            *
*                                                                     *
*  07/08/82  U026  WJN                                                *
*          . CHANGE CSOUT TO USE SVC 99 TO ALLOCATE SYSOUT FILES      *
*          .     (FOR SP1.3)                                          *
*          . TURN ON BIT IN JSCB TO BYPASS PASSWORD CHECKING          *
*  08/31/81  U025  WJN                                                *
*          . ADD SPNB SECURITY AND APF SVC.  ONLY SCP PROGRAMMERS     *
*          .     CAN USE THIS NOW                                     *
*  08/17/81  U024  SDM                                                *
*          . FIX SEQ READ BUG - EOF IN RECORD 1 OF TRACK              *
*          . CHANGE MACRO=YES TO GETMAIN=(DSALEN,DSA)                 *
*          . FIX STFSMODE OFF LOGIC TO REDUCE SWAPPING                *
*          . KILL BEEPS ON ENTERING AND LEAVING ZAP                   *
*          . START ZAP SESSION WITH 3270 ERASE WRITE COMMAND          *
*          . MODIFY 'ZAP LOG DELETED' MESSAGE LOGIC                   *
*          . FIX MISSING VOLSER IN 'VTOC FOR ...' TITLE LOST IN T002  *
*          . MAKE 3270 LOGIC INDEPENDENT OF WINDOW VALUES             *
*          . ADD NOTCRT COMMAND (NOT3270 ALIAS) - NO HELP             *
*          . CHANGE ZAP LOG TITLE BACK TO GIBRALTAR                   *
*          . REPLACE CCW ADDRESS RESOLUTION LOGIC                     *
*          . CHANGE TO VERSION 3.0A                                   *
*  08/12/81  G001  EMS                                                *
*          . ERIC DIDN'T BOTHER TO MENTION THESE CHANGES -- SDM       *
*  04/19/81  U023  LDW                                                *
*          . INSTALL FIX "MAS1" (12/18/80) VSAPL ZCODE TABLE          *
*          . INSTALL FIX "MAS2" (12/18/80) FIX NAME(E) COMMAND FOR VS *
*               AND "MTTR" COMMAND (MAS USED "J")                     *
*          . INSTALL FIX "MAS3" (12/18/80) ASK IF ZAP LOG REQUIRED    *
*  04/19/81  T003  EMS                                                *
*          . REMOVE MISC USE OF SAVEAREA FOR FUNCTIONS                *
*          . ADD MORE VSAM CODE FOR KEYSEQUENCED FILES                *
*          . FIX PROBLEMS WITH F/L NOT STOPPING AT EOF                *
*  03/20/81  U022  LDW                                                *
*          . FIX LOG (PUT IN DSECT)                                   *
*  03/20/81  T002  EMS                                                *
*          . ADD VSAM SUPPORT                                         *
*          . CHANGED ALL USINGS, DROPS TO USNGX, DROPX                *
*          . MADE EXP A SEPARATE CSECT                                *
*          . ADDED NEW OPCODES TO ASMGASM AND UPPED OPCODE LEN TO 6   *
*          . CHANGED INCREASED LENGTH OF JOBQUEUE FOR VS1             *
*          . OVERLAYED CODE FOR VSAM, EXCP WORKAREAS                  *
*          . IMPLEMENTED SCHEME FOR CHECKING FOR INVALID COMMANDS     *
*          . PUT VSAM SWITCH IN FOR INCLUSION OF VSAM CODE            *
*          . FIX ERROR IN EBCIDIC TRANSLATE TABLE FOR X'FA'-'FF'      *
*          . IMPLEMENT SEPARATE LINK REG SAVEAREAS                    *
*  11/26/80  U021  LDW                                                *
*          . ADD 'NOSTAX' KEYWORD                                     *
*          . SHOW DSNAME ON 'UNAVAILABLE OR NON-EXISTENT' MSG         *
*          . REMOVE ALL CCN CODE                                      *
*          . CHANGE LOGIC TO DETERMINE IF 327X DEVICE                 *
*          . CORRECTLY DETERMINE NUMBER OF HELP SCREENS (FULLSCREEN)  *
*          . CLEANUP, REORGANIZE FOR IMPROVED BASE REGISTERS          *
*          . STRIP OFF LEADING BLANKS IN FRONT OF COMMAND             *
*  09/22/80  U020  LDW                                                *
*          . CHANGE LOG TITLE FOR HARTFIELD-ZODY'S                    *
*          . CHANGE "DO" SUBCOMMAND TO "TSO"                          *
*  09/02/80  T001  EMS                                                *
*          . ADD VS1/TONE SUPPORT CODE                                *
*          . CLEAN UP MISC CONDITIONAL ASM STUFF                      *
*          . FIX CHECKPT FUNCTION CORRECTLY                           *
*          . IMPLEMENT SIMPLE DSN CHECK FACILITY                      *
*  08/05/80  U019  LDW                                                *
*          . USE SEARCH KEY TO LOCATE CORRECT DIRECTORY BLOCK         *
*          . ADD 'FINDEOF' COMMAND                                    *
*          . CHANGE CURRENT LOC SYMBOL FROM '*' TO '$'                *
*  07/30/80  U018  LDW                                                *
*          . USE UNIT=SYSALLDA WHEN VOLUME NAME SUPPLIED              *
*          . FIX DSNAME LOGIC SO ALIASES DON'T 213                    *
*          . UPDATE 'ENTR' FOR CURRENT OPERANDS                       *
*          . FIX CHECK FOR PDS (DSORG=POU DIDN'T WORK)                *
*          . REMOVE 'DOING', 'DONE' MESSAGES FROM 'DO' SUBCOMMAND     *
*  03/10/80  U017  LDW                                                *
*          . DON'T REWRITE JFCB WHEN OPENING TYPE=J                   *
*  02/14/80  U016  LDW                                                *
*          . FIX BUG IN "SET" CAUSED BY BASE REGISTER SWAP            *
*  12/31/79  U015  LDW                                                *
*          . MODIFY FULLSCREEN LOGIC ("DO"; STFSMODE INIT=YES)        *
*          . FIX MVT CONDITIONAL ASSEMBLY PROBLEMS                    *
*          . REMOVE STCMX MACRO, USE CONDITIONAL ASM INSTEAD          *
*  12/27/79  U014  LDW                                                *
*          . MODIFY FULLSCREEN FLAGS LOGIC (M3270F FSMODE)            *
*          . SIMPLIFY "DO" COMMAND CODE                               *
*          . DELETE "RESHOW" COMMAND                                  *
*          . FIX "WINDOW" COMMAND BUG                                 *
*          . CHANGE VERSION TO 2.3                                    *
*          . CHANGE SENSITIVE DATASET CRITERIA FOR GIBRALTAR          *
*          . MERGE "INITSHOW" FLAG INTO "RESHOWF"                     *
*          . PRE-BLANK REPLY BUFFER FOR PUTGET                        *
*  12/27/79  U013  SDM                                                *
*          . FIX COMSCAN TO HANDLE ZERO LENGTH REPLIES CORRECTLY      *
*          . FIX SHORT RESPONSE LOGIC FOR FULLSCR SUPPORT             *
*  11/29/79  U012  LDW                                                *
*          . FIX IDR & LOG USERID                                     *
*          . PAUSE 'F' AT DS1LSTAR                                    *
*          . ADD 'WM' ALIAS FOR WHATMEM COMMAND                       *
*          . MAKE JCJ HAPPY: SAME CMD TWICE IN A ROW WILL GET OUT     *
*                 OF A MODIFIED BLOCK WITHOUT "ZAP"ING IT             *
*                 ("NOZAP" COMMAND REMOVED)                           *
*          . CHANGE TO VERSION 2.2B                                   *
*          . CHANGE MESSAGE ID'S FROM CSM3XX TO ZAP0XX                *
*          . FORCE LOG FOR ANY SYSN. DATASET                          *
*          . CHANGE TITLE ON LOG FROM UCLA-CCN TO GIS                 *
*          . ADD USID TO ALLOCDSN PARSE ENTRY                         *
*          . ROUND UP BY 50 BYTES THE SIZE OF THE GETMAIN'D BUF       *
*          . MOVE LASTDS1 TO ITS OWN FIELD                            *
*          . DIDDLE CODE IN 'FORMAT' TO PREVENT 0C4'S                 *
*  11/28/79  U011  SDM                                                *
*          . CHANGE LINE01 LENGTH TO NORMAL                           *
*          . IMPROVE PARTIAL SCREEN DISPLAY CODE                      *
*          . IMPROVE TGET ASIS CODE                                   *
*          . MOVE BASE FROM R7 TO R9                                  *
*  11/27/79  U010  SDM                                                *
*          . ELIMINATE 'UNLOCK KEYBOARD' FROM 3270 OUTPUT             *
*          . FIX ERROR MESSAGE BLANKING BUG                           *
*          . FIX HELP PROMPT FIELD CLEAR LENGTH BUG                   *
*          . FIX HELP PROMPT FIELD MDT BUG                            *
*  11/09/79  U009  SDM                                                *
*          . FIX INTERMITTENT 0C4 IN 'L' COMMAND                      *
*  09/26/79  U008  SDM                                                *
*          . ADD ROUTCDE=9 (SECURITY) TO WTO                          *
*  09/05/79  U007  LDW                                                *
*          . DELETE ALL SSL CODE                                      *
*  08/07/79  U006  SDM                                                *
*          . CHANGE TGET WHEN IN FULLSCREEN MODE TO ASIS              *
*          . ADD ROUTCDE=2 (MASTER CONSOLE) TO WTO                    *
*  06/27/79  U005  LDW                                                *
*          . CHANGE FULLSCREEN I/O TO ONLY TPUT CHANGED LINES         *
*          . FIX BUG IN FULLSCREEN HELP                               *
*          . ADD HELP INFO                                            *
*  05/05/79  U004  LDW  THIS IS A MAJOR CLEANUP/REWRITE THAT KEPT     *
*                       MAINLY THE ORIGINAL FRAMEWORK, BUT ALMOST     *
*                       EVERYTHING WAS CHANGED SOMEWHAT.              *
*          . CHANGE ACCESS METHOD FROM BDAM TO EXCP                   *
*          . FIX SO "X" WORKS IF LOG IS ON                            *
*          . FIX SO "ZAP REQ'D" WORKS IF LOG IS OFF                   *
*          . CHANGE TPUT TO PUTLINE                                   *
*          . MAKE SINGLE ASSEMBLY                                     *
*          . INCLUDE CHAR FORM OF DATA AND ABS LOC IN LOG             *
*          . VARIOUS RE-ARRANGING TO CONSERVE PAPER                   *
*          . FULLSCREEN 3270 & SSL SUPPORT                            *
*          . JUST "M" GIVES SAME MEMBER AGAIN (NOT DIRECTORY)         *
*          . DELETE LOGTEST MACRO, PUT CODE IN SUBROUTINE             *
*          . UPDATE IDR FOR ALL ENTRY POINTS SELECTED                 *
*          . MISC CODE CLEANUP, EG "BLANKS", EQU'S FOR TTR+3          *
*          . ADD DUMPE COMMAND TO DUMP CURRENT REC TO EOF             *
*          . ADD LINELEN=80 FEATURE/OPTION                            *
*          . DIDDLE BASE REGISTERS & ADD ONE                          *
*          . VTOC SUPPORT                                             *
*          . CHANGE CSOUT TO USE QSAM, LARGE BLKSIZE, FIX BUGS        *
*          . FIX EXPR HANDLER TO MULT/DIV                             *
*          . NEW CMDS:  WHATMEM, F, V, LASTDS1, U, DISP, %            *
*          . SET LOG DEFAULT, FIX SO DELETED IF UNUSED                *
*          . REDESIGN "CHKPT" OPERATION                               *
*          . USE 'HEX' MACRO                                          *
*          . FIX 15 NIBBLE HEX STRING BUG                             *
*          . CONDITIONAL ASSEMBLY FOR MVT/MVS, SSL/NOSSL, CCN/^CCN    *
*  05/20/78  U003  JCJ                                                *
*          . PROPAGATE LPR URSA ZAP FIXES TO TSO ZAP                  *
*          . PROPAGATE ABS COMMAND FROM URSA ZAP TO TSO ZAP           *
*          . FIX LOST SP 0 CORE AT EODAD BUG.                         *
*          . FIX VERBOSE MODE MSG OVERLAY BUG.                        *
*  12/20/75  U002  VIC                                                *
*          . ADD 'NAME' COMMAND TO LOCATE ENTRY POINTS/CSECTS WITHIN  *
*            LOAD MODULES                                             *
*  09/12/75  U001  WDD                                                *
*          . GET USERID FOR ZAP UPDATE RECORD, ALLOW USERS TO USE ZAP *
*          . ALLOCATE DATASET PERM=NO, AND SET CORRECT DSNAME LENGTH  *
*                                                                     *
***********************************************************************
         SPACE 2
*  POSSIBLE NEW FEATURES;  BUGS;  THINGS TO CHECK
*  1) COMMAND STACKING
*  2) ENQ ON SPZAPLIB/SYSVTOC/SYSCTLG   (SPZAPLIB L'RNAME=44)
*  3) STORE TTR IN DEFINE TABLE, ADD P= COMMAND
*  4) OFFSET COMMAND?
*  5) RECORD MAY BE > 32K BYTES... BUT O/S WON'T LET IT, SO NO PROBLEM.
*  6) EXP IS SCREWED
*  7) NOTE WHERE IDR'S ARE WHILE LOOKING FOR ENTRY POINT
*  8) DO 256 READ COUNTS
*  9) RN GOES TO NEXT TRACK IF CURR TRK DOESN'T HAVE N RECORDS
* 10) OFFSET IN LOG WILL BE FISHY IF NOT ADJUSTED FOR DISPC/K/D
* 11) AUTO "M" IF MEMBER GIVEN?
* 12) WSHORT & DELETE ALL 40 CHAR CODE?
* 13) DON'T POSITION CURSOR UNLESS RMOD
* 14) MAKE HELP USE LONGER SCREEN LINES
* 15) DON'T DISP COUNT AS PART OF REC, HAVE SET COUNT CMD
* 16) USE 'MSG' FOR HELPS.
* 17) MOVE CSOUT MF=L TO DSA
* 18) "FORMAT" COMMAND
* 19) SCAN OFF BLANKS BETWEEN COMMAND AND OPERAND
* 20) LC/UC COMMANDS
* 21) BLKSIZE 0 SHOULD USE DEVTYPE FOR BUFLEN (WHAT ABOUT TRK OFLO?)
* 22) SLIDE THE "ON VOLSER" LEFT VARIABLY
* 23) LOG(CLASS) / HOLD
* 24) 'FORMAT4.DSCB' - ALLOC THE VOLUME
* 25) NO OPERANDS --> FULLSCREEN PROMPT
* 26) FIX NO ABEND AFTER DOUBLE ATTN IN PUTGET RTN
* 27) < LETS YOU OUT OF THE BLK W/O ZAP, LATER ASKS ZAP?
* 28) SAME COMMAND TWICE IN A ROW MAY GET YOU OUT OF BLK
* 29) TURN OFF ZAP REQD BIT AFTER DECIDING SAME CMD TWICE IN A ROW.
* 30) IF NOT ATTEMPTING TO LEAVE BLK, KILL PREV CMD.
         SPACE 6
         MACRO
&LBL     MSG   &C
         LCLA  &A
&A       SETA  K'&C
&A       SETA  &A-2
&LBL     DC    AL1(&A),CL&A.&C
         MEND
         SPACE 2
         MACRO
&N       OP    &M,&F,&O
         GBLA  &#                                                  U004
         GBLC  &$                                                  T002
         LCLC  &F$,&##                                             T002
&F$      SETC  '&F'
         AIF   ('&F'(1,1) NE '''').OK
&F$      SETC  '&F'(2,1)
.OK      ANOP
&#       SETA  &#+1                                                U004
@&O      EQU   &#                                                  U004
         AIF   (K'&O EQ 2).S360OP                                  T002
&N       DC    CL6'&M',C'&F$',X'&O'                                T002
         AIF   ('&O'(1,2) NE '&$').S370FST                         T002
         MEXIT ,                                                   T002
.S360OP  ANOP  ,                                                   T002
&N       DC    CL6'&M',C'&F$',X'&O.00'                             T002
         MEXIT ,                                                   T002
.S370FST ANOP  ,                                                   T002
&$       SETC  '&O'(1,2)                                           T002
@&$      EQU   &#                                                  T002
         MEND
         SPACE 2
         MACRO                                                     T002
&NFS     VSAMTST &OP                                               T002
         BXLE  R15,R15,*+8              TEST RETURN CODE           T002
         BAL   R14,VSAMERR              IF NON-ZERO, FLAG ERROR    T002
         MEND                                                      T002
         EJECT
         GBLB  &MVS                     1 = THIS IS MVS            U004
         GBLB  &MVT                     1 = THIS IS MVT            U004
         GBLB  &INST370                 1 = THIS IS A 370 (0=360)  U015
         GBLB  &VS1                     1 = THIS IS VS1            T001
         GBLB  &TONE                    1 = THIS IS TONE (NOT TSO) T001
         GBLB  &SEC                     1 = INCLUDE SECURITY CHECK T001
         GBLB  &VSAM                    1 = INCLUDE VSAM CODE      T002
         SPACE 1
         ACTR  100                      TRY TO CATCH FATAL ERRORS  T002
         SPACE 3
&MVS     SETB  1                        IT TAKES A 3033 TO RUN MVS U004
&MVT     SETB  0                        FINALLY...                 U004
&INST370 SETB  1                        3033'S ARE 370'S           U015
&VS1     SETB  0                        TELECREDIT RUNS VS1        T001
&TONE    SETB  0                        TONE SUPPORTS TSO (SORTA)  T001
&SEC     SETB  1                        ONLY OPER CAN UPDATE ALL   T001
&VSAM    SETB  1                        GIVE US VSAM PLEASE        T002
         AIF   (&MVS+&MVT+&VS1 GT 1).BADSYS                        T001
         AIF   (&TONE AND &MVT).BADSYS                             T001
         AIF   (&INST370+&MVT EQ 0).BADSYS                         T001
         AIF   (&VSAM+&MVT EQ 2).BADSYS                            T002
         AGO   .OK1                                                T001
.BADSYS  ANOP  ,                                                   T001
***********************************************************************
***********************************************************************
%%%%%% ERROR---CONDITIONAL ASSEMBLY SWITCHES SET INCORRECTLY %%%%%%%%%%
***********************************************************************
***********************************************************************
.OK1     SPACE 4
*  SEQUENCE SYMBOLS IN USE:                                        U004
*
*  .MVS**   01 02 03 04 05 06 07 08 09 10 11 12
*  .NMVS**  01                                                     T001
*  .MVT**   01 02 03 04 05 06 07                                   T002
*  .VS1**   01                                                     T002
*  .TONE**  01 02 03 04 05                                         T003
*  .NTONE** 01 02 03 04 05                                         T003
*  .NSEC**  01                                                     T001
*  .VSAM**     02 03 04 05 06 07 08                                T002
*  .NVSAM** 01 02 03 04 05    07 08 09 10 11 12 13 14 15 16 17     T002
*           18 19 20 21 22 23 24 25 26 27 28 29 30 31 32 33 34     T003
*           35 36 37 38 39 40 41 42 43 44 45 46 47 48 49 50 51     T003
*           52 53                                                  T003
         EJECT
**********************************************************************
*                                                                    *
*  PROGRAM:  ZAP    (UNDER TSO)                                      *
*                                                                    *
*                                                                    *
*  THIS PROCESSOR IS RE-ENTRANT AND SERIALLY REUSABLE                *
*                                                                    *
*                                                                    *
*                                                                    *
*                                                                    *
*  FOR FULL DOCUMENTATION OF COMMANDS AND SO ON, SEE THE LISTING     *
*  OF THE URSA VERSION IN BINDER 'URSA MISCELLANEOUS VOLUME 3'       *
*                                                                    *
*                                                                    *
*                                                                    *
*                                                                    *
*  PROGRAMMERS:  LEONARD D. WOREN (LDW)                              *
*                STEVE MCGINTY    (SDM)                              *
*                ERIC SCHINDLER   (EMS)                              *
*                VIC TOLOMEI      (VIC)                              *
*                                                                    *
*                                                                    *
*  WRITTEN FOR URSA:         5/22/73                                 *
*  CHANGE TO TSO VERSION:    1/22/75                                 *
*                                                                    *
**********************************************************************
         SPACE 3
**********************************************************************
*                                                                    *
*  ZAP                                                               *
*                                                                    *
*  GENERAL ZAP OUTLINE:                                              *
*                                                                    *
*  THIS SERVICE WILL DISPLAY ANY TYPE OF DATA SET (EXCLUDING         *
*  ISAM) IN DUMP FORMAT, ACCEPT VARIOUS DISPLAY FORMATTING           *
*  AND MODIFYING COMMANDS, AND APPLY INTERACTIVE CHANGES             *
*  AS DOES IBM'S SERVICE AID, IMASPZAP (SUPERZAP).                   *
*  HARDCOPY LINES ARE PRINTED FOR ALL DATA SET MODIFICATIONS         *
*  AND 'POTENTIAL MODIFICATIONS'  (THOSE NOT YET APPLIED) AS WELL AS *
*  COMMENTS AND DUMPED BLOCKS.  THE FOLLOWING CONDITIONS CAUSE       *
*  A LINE TO BE WRITTEN INTO THE PRINT FILE:  ACCESSING A DATA SET,  *
*  'ZAPPING' A BLOCK, STORING IN THE BUFFER (S,X,O,N, OR SET),       *
*  SELECTING A MEMBER, CREATING A COMMENT, EJECTING A PAGE, OR       *
*  DUMPING A BLOCK OR BLOCKS.  THE FILE WILL BE PRINTED ONLY IF AN   *
*  EXPLICIT DUMP, COMMENT, OR PERMANENT CHANGE IS MADE.  THE FILE    *
*  IS ACTUALLY RELEASED FOR PRINTING UPON EXIT FROM THE DATA SET.    *
*  IDR'S ARE UPDATED IN LOAD MODULE DATA SETS IN THE FORMAT SUPERZAP *
*  USES (WITH 'TSO--III' AS THE ID DATA FIELD).                      *
*  END-OF-FILES MAY BE JUMPED TO VIEW DATA BEYOND UP TO THE EXTENT   *
*  OF THE DATA SET  (USEFUL IF AN INADVERTANT EOF WAS WRITTEN IN THE *
*  MIDDLE OF A DATA SET).                                            *
*                                                                    *
**********************************************************************
         EJECT
*  THE OLD SCREEN FORMAT WAS AS FOLLOWS:                           U004
         SPACE 2
*               ##########################################
*               # *MSG*  *TALK*  Z  A  P  *BULL*  *OUT* >#
*               #L'NOTHING'                              #
*               #ENTER VALID COMMAND ABOVE OR ? FOR HELP #
*               # ***** I/O ERROR: XXXXXXXXXXXXXXX ***** #
*               #000000  F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #000008  F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #000010 >F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #000018  F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #000020  F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #000028  F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #000030  F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #000038  F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #000040  F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #000048  F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #000050  F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #000058  F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #000060  F1F2 F3F4  F5F6 F7F8  ×12345678×#
*               #        *****  SCAN MATCH  *****        #
*               #DSN: CPC013.LDW.SCREEN.SAMPLE OFF:  0010#
*               #TTR: 000001 CCHHR: 0022000301 LEN:   400#
*               ##########################################
         SPACE 3
*  THE NEW FORMAT IS AS FOLLOWS:                                   U004
*  (SOME BLANK COLUMNS HAVE BEEN OMITTED SO IT WILL FIT)           U004
         SPACE 2
*                             Z  A  P
*(TYPE HERE)
*ENTER VALID COMMAND ABOVE OR ? FOR HELP
*L'12345678'
*             ***** I/O ERROR: XXXXXXXXXXXXXXX *****
*00000  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*00010 >F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*00020  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*00030  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*00040  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*00050  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*00060  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*00070  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*00080  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*00090  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*000A0  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*000B0  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*000C0  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*000D0  F1F2 F3F4  F5F6 F7F8  F9C1 C2C3  C4C5 C6F0   ×123456789ABCDEF0×
*                    *****  SCAN MATCH  *****
*OFF: 0010 (    16) ADDR: 00010 (     16) DSN: CPC013.LDW.SCREEN.SAMPLE
*LEN: 0190 (   400) BASE: 00000 (      0) CCHHR: 0022003301 TTR: 000001
         EJECT
ZAP      CSECT
         SPACE 5
**********************************************************************
*                                                                    *
*  REGISTER USAGE:  R0-R7...WORK REGS                                *
*                   R8......PRIMARY BAL REG                          *
*                   R9......SUBCOMMAND LOCAL BASE REGISTER           *
*                   R10.....THIRD BASE REGISTER                      *
*                   R11.....SECOND BASE REGISTER                     *
*                   R12.....FIRST BASE REGISTER (BASE FOR EXP)       *
*                   R13.....TS WORKAREA (SAVEAREA, 'CSAREA', ETC)    *
*                   R14.....WORK AND RET ADDR                        *
*                   R15.....WORK AND ENTRY POINT ADDR                *
*                                                                    *
**********************************************************************
         TITLE 'ZAP --- INITIALIZATION'
*ZAP     TSENTRY BASE=R12,SYMREG=NO,PL=(PARSE,DAIR,MESSAGE,IO),    U004$
               MACRO=YES,LOAD=(IKJPTGT,IKJPUTL)                    U004
         AIF   (&TONE).TONE02                                      T001
ZAP      OSENTER  PL=(PARSE,DAIR,IO),LOAD=(IKJPTGT,IKJPUTL),       U004$
               ENV=(CP,SIMULATE),GETMAIN=(DSALEN,DSA),PARMREG=R5,  U024$
               BASE=R12,BASE2=R11,BASE3=R10,F=F                    U021
         AGO   .NTONE02                                            T001
.TONE02  ANOP  ,                                                   T001
ZAP      OSENTER  PL=(PARSE,DAIR,IO),LOAD=(IKJPTGT,IKJPUTL),       T001$
               ENV=CP,GETMAIN=(DSALEN,DSA),PARMREG=R5,             U024$
               BASE=R12,BASE2=R11,BASE3=R10                        U021
.NTONE02 ANOP  ,                                                   T001
         EJECT
CSREG    EQU   R13                                                 T003
*U004    LA    R11,4095(,R12)           BASE #2                    U004
*U004    LA    R11,1(,R11)                                         U004
*U004    LA    R10,4095(,R11)           BASE #3                    U004
*U004    LA    R10,1(,R10)                                         U004
*U004    USNGX ZAP+4096,R11,R10                                    U021
         LA    R6,4095(,R13)            TEMP POINT TO EXTRA        T002
         USNGX DSA+4095,R6                                         T002
         SPACE 2
         AUTH  ON      CALL SECURITY SVC                           U025
         LTR   R15,R15                                             U025
         BZ    SECOK                                               U025
         TPUTX 'USERID NOT AUTHORIZED FOR THIS COMMAND'            U025
         L     R13,4(R13)         GET                              U025
         LM    R14,R12,12(R13)        OUT                          U025
         XR    R15,R15                    GET                      U025
         BR    R14                            OUT                  U025
         SPACE 2
SECOK    MODESET KEY=ZERO               GET KEY ZERO               U026
         L     R1,16                    CVT ADDRESS                U026
         L     R1,0(R1)                 ADR OF CUR/NEXT TCB ADRS.  U026
         L     R1,4(R1)                 CURRENT TCB                U026
         L     R1,180(R1)               JSCB ADDRESS               U026
         OI    243(R1),X'80'            TURN ON SKIP PSWD. BIT     U026
         MODESET KEY=NZERO              BACK TO OLD KEY            U026
         SPACE 2
         MVI   BLANKS,C' '              INIT                       U004
         MVC   BLANKS+1(L'BLANKS-1),BLANKS                         U004
         MVC   VERSION,BLANKS           INIT                       U004
         MVC   VERSION(12),=C'VERSION=3.0A'                        U024
         MVC   VERSION+13(7),ZAP+10     DATE                       U004
         MVC   VERSION+21(3),=C'WJN'                               U024
         L     R1,CPPLPSCB              -> PSCB                    T001
         MVC   USERID,0(R1)             GET THE TSO USER ID        T001
*%       MVC   LINELEN,=H'80'           DEFAULT LINE LENGTH        U004
         L     R1,=A(DSPCON80)          -> CONSTANTS               T002
         MVC   DISPCONS(DISPCONL),0(R1) CONSTANTS FOR LEN=80       T002
         MVC   CTL3270,CTL3270C         INIT 3270 CONTROL BYTES    U004
         MVC   END3270,END3270C         FINISH IT                  U004
         LA    R0,SCREENL               GET TPUT LENGTH            U005
         LA    R1,SCREEN                GET TPUT ADDR              U011
         STM   R0,R1,REGS3270           SAVE FOR FULLSCR TPUT      U004
         MVI   REGS3270+4,X'03'         SET FULLSCR TPUT FLAG      U011
         XC    CURPOS,CURPOS            CLEAR CURSOR POSITION      U004
         LA    R1,JFCB                  -> JFCB                    U004
         ST    R1,EXLST                                            U004
         MVI   EXLST,X'87'              CALL IT A JFCB             U004
         SPACE 1
         LA    R0,IDEFTAB                                          U007
         ST    R0,AIDEFTAB                                         U004
         LA    R1,ITRTAB                                           U004
         ST    R1,AITRCTAB                                         U004
         MVI   0(R1),X'FE'              SET TO INITIAL VALUE       U004
         MVC   1(ITRLEN*ITRCNT-1,R1),0(R1)  SET THE WHOLE THING    T002
         ST    R1,ITRAVAL               -> FIRST POS               U004
         ST    R1,CURRITR               -> CURRENT POS             U004
         MVI   ENDITAB,X'FF'            MARK THE END OF THE TABLE  U004
         MVI   ITREND,X'FF'             DITTO                      U004
         LA    R0,ITREND                POINT TO IT                U004
         ST    R0,AITREND               SAVE                       U004
         LA    R1,SCRWORK               -> WORK AREA               U005
         ST    R1,ASCRWORK              SAVE ADDR                  U005
         LA    R1,OLDSCR                -> SCREEN SAVE AREA        U011
         ST    R1,AOLDSCR               SAVE ADDRESS               U011
         DROPX R6                                                  U004
         L     R1,=A(DCBMSK)            GET ADDR OF PATTERN DCB    U004
         MVC   DCBU(DCBUL),0(R1)        SET UP DCB FOR INPUT       U004
         XC    IOB(40),IOB              INIT IOB                   U004
         MVI   IOB,X'42'                IOBFLAGS                   U004
         LA    R0,ECB                   -> EXCP ECB                U004
         ST    R0,IOB+4                                            U004
         LA    R0,DCBU                                             U004
         ST    R0,IOB+20                                           U004
         MVI   FLAGS2,0                 INIT FLAGS                 U015
         MVI   FLAGS3,0                 INIT FLAGS                 U014
         MVI   FLAGVSAM,0               INIT FLAGS                 T002
         MVI   FLAGCOM,0                INIT FLAGS                 T002
         MVI   FLAGS4,0                 INIT FLAGS                 T002
         SPACE 2
*  GUESS IF THIS IS A 3270                                         U004
         GTSIZE ,
         LTR   R0,R0                    3270 DEVICE CLASS?(SORTA)  U021
         BZ    *+16                     NO - NOT 3270              U021
         AIF   (&TONE).TONE01           TONE SUPPORTS 133 WIDE     T001
         CH    R1,=H'80'                80 COLUMNS?                U004
         AGO   .NTONE01                                            T001
.TONE01  CH    R1,=H'133'               TONE GIVES 133 WE USE 80   T001
.NTONE01 ANOP  ,                                                   T001
         BNE   *+8                      NO - NOT 3270              U004
         OI    FLAGS3,M3270F            24 LNS & 80 COLS ==> 3270  U015
         EJECT
*
*  PARSE THE INPUT LINE:
*
*       ZAP  'DSN'  VOL(VOLSER) CRT LOG TERSE/VERBOSE
*
         TSPARSE CBUF=(CPPLCBUF,I),PCL=(=V(PCL),I)
         SPACE 1
         BXLE  R15,R15,OKPARSE          IT'S OK
         SPACE 2
         LA    R1,=CL40'INVALID INPUT'  OOOPS
         B     CSIO#                    SHOW AND QUIT
         SPACE 3
OKPARSE  L     R7,TSPARANS              POINT TO PARSE BLOCK
         USNGX IKJPARMD,R7              TEMP TO GET DSN AND VOL
         SPACE 3
         CLI   NWK+1,2                  SEE IF TERSE
         BNE   *+8                      IF NOT NO FLAG
         OI    FLAGS2,TERSEF            SUPRESS WHERE
         SPACE 1
         CLI   CRTK+1,1                 SEE IF CRT SPECIFIED
         BNE   PARMLOG                  NO, DON'T SET FLAG         U024
         OI    FLAGS3,CRTF              YES, SET FLAG              U014
         NI    FLAGS3,255-M3270F        AND TURN OFF 3270 FLAG     U024
         MVC   WIDTHS(8),=2F'4'         SET UP AND DOWN DISTANCES  U024
         NI    FLAGS2,255-TERSEF        AND SET VERBOSE MODE       U024
         SPACE 1
PARMLOG  CLI   LOGK+1,1                 SEE IF LOG SPECIFIED
         BNE   *+8                      IF NOT NO LOG
         OI    FLAGS2,LOGF
         SPACE 1
         CLI   NOT3270K+1,1             NOT3270 SPECIFIED?         U004
         BNE   *+8                      NO - SKIP                  U004
         NI    FLAGS3,255-M3270F        YES - TURN OFF FLAG        U014
         SPACE 1
         CLI   NOSTAXK+1,1              NOSTAX SPECIFIED?          U021
         BNE   *+8                      NO - SKIP                  U021
         OI    FLAGS3,NOSTAXF           YES - SET FLAG             U021
         SPACE 1
         AIF   (NOT &VSAM).NVSAM49      NO VSAM, OPTION NOT AVAIL  T002
         CLI   NOACBK+1,0               VALUE SPECIFIED?           T002
         BE    *+8                      NO - SKIP                  T002
         OI    FLAGS4,NOACBF            YES - SET FLAG             T002
.NVSAM49 SPACE 1
         XC    WIDTHS(4+4),WIDTHS       DEFAULT = CARET LINE ONLY  U004
         SPACE 2
*
*  CHECK OUT THE USER.  IF HE CAN'T GET IN, OR CAN'T TOUCH THE
*  DATASET, KICK HIM OUT.
*
         L     R1,PDEPTR-PDE+PDLDSN     POINT TO DSN SUPPLIED
         LH    R15,PDELEN-PDE+PDLDSN    POINT TO ITS LEN (<= 45)
         STH   R15,DSNAMEL              SET DSNAME LENGTH          U001
         BCTR  R15,0                    EX LEN
         MVC   DSNAME(44),BLANKS        CLEAR TARGET               U004
         MVC   VOLSER(6),BLANKS         BLANK VOL TOO
         MVC   UNITNAME(8),BLANKS       AND THE UNITNAME           U018
         MVC   PASSWORD(8),BLANKS       AND THE PASSWORD           U004
         MVC   DSNAME(0),0(R1)          <<< EXECUTED >>>
         EX    R15,*-6                  MOVE PARSE DSN TO FULL TARGET
         MVC   DISPDSN,DSNAME           COPY FOR DISPLAYING        U004
         CLC   DSNAME(7),=CL7'SYSCTLG'  IS IT CATALOG              U003
         BNE   *+8                      IF NOT LET IT BY
SENSTIVE OI    FLAGS2,SENSF+LOGF        MARK AS SENSITIVE
         SPACE 1
         TM    PDLDSN+PDEFLAG3-PDE,PDEFPRES  PSWD PRESENT?         U004
         BNO   NOPASS                   NO                         U004
         L     R1,PDLDSN+PDEPTR3-PDE    -> PSWD                    U004
         LH    R15,PDLDSN+PDELEN3-PDE   LENGTH                     U004
         BCTR  R15,0                    -1 FOR EX                  U004
         MVC   PASSWORD(0),0(R1)        << EXECUTED >>             U004
         EX    R15,*-6                                             U004
NOPASS   TM    PDEFLAG-PDE+VOLVOL,PDEFPRES   ANY VOL SPEC?         U004
         BNO   NOVOL                    NO, BLANK (=DAIR CATALOG)
         L     R1,PDEPTR-PDE+VOLVOL     GET PTR TO VOL SER
         LH    R15,PDELEN-PDE+VOLVOL    YES, GET ITS LEN
         BCTR  R15,0                    GET EX LEN
         MVC   VOLSER(0),0(R1)          <<< EXECUTED >>>
         EX    R15,*-6                  MOVE VOL SPEC TO VOLSER
         MVC   UNITNAME,=C'SYSALLDA'    USE MVS ALL-INCLUSIVE NAME U018
         SPACE 1
NOVOL    MVC   ALLOCDSN,BLANKS          CLEAR FIELD                U004
         TM    PDEFLAG-PDE+PDLALCDS,PDEFPRES   GIVEN?              U004
         BNO   NOALCDSN                 NO - SKIP                  U004
         L     R1,PDEPTR-PDE+PDLALCDS   POINT TO DSN               U004
         LH    R15,PDELEN-PDE+PDLALCDS  GET LENGTH                 U004
         STH   R15,DSNAMEL              SET LENGTH FOR ALLOC       U004
         BCTR  R15,0                    -1 FOR EX                  U004
         MVC   ALLOCDSN(0),0(R1)        << EXECUTED >>             U004
         EX    R15,*-6                  MOVE IN DSN                U004
NOALCDSN CLC   =C'FORMAT4.DSCB',DSNAME  VTOC?
         BNE   NOTVTOC3
         SPACE 1
         MVC   DSNAMEL(2),=H'44'                                   U001
         MVI   DSNAME,X'04'             YES, CHANGE DSN
         MVC   DSNAME+1(43),DSNAME      TO 44X'04'
         MVC   DISPDSN,BLANKS           RESET FIELD                U004
         MVC   DISPDSN(8),=C'VTOC FOR'  SHOW SOMETHING GOOD        U004
*                                       VOLSER MOVED AFTER ALLOC   U024
         OI    FLAGS2,SENSF+LOGF        MARK AS SENSITIVE          U004
         CLI   ALLOCDSN,C' '            ALLOCDSN GIVEN?            U004
         BNE   NOTVTOC3                 YES - VOLSER IS IMPLIED    U004
         CLI   VOLSER,C' '              CATALOG FOR VTOC?
         BE    BADDSN                   YES, WHAT DOES THAT MEAN?
         SPACE 2
NOTVTOC3 OI    GODFLAG,GOD              ASSUME HE CAN UPDATE       U004
         AIF   (NOT &SEC).NSEC01        INSTALL DSN CHECKING?      T001
         L     R1,CPPLPSCB              -> PSCB                    T001
         TM    16(R1),X'80'             OPER? PSCBATR1,PSCBOPER    T001
         BO    OKDSN5                   OPER UPDATES ANYTHING      T001
         XR    R15,R15                  CLEAR IT                   T001
         IC    R15,7(,R1)               LEN OF USERID  PSCBUSRL    T001
         LA    R14,DSNAME(R15)          -> CHAR AFTER USERID       T001
         CLI   0(R14),C'.'              USERID INDEX LEVEL?        T001
         BNE   NOTGOD                   NO, READ ONLY ACCESS       T001
         BCTR  R15,0                    - 1 FOR EXECUTE            T001
         CLC   DSNAME(*-*),0(R1)        DSN PREFIXED WITH USERID?  T001
         EX    R15,*-6                  TEST THE FULL USERID       T001
         BE    OKDSN5                   YES, USER CAN UPDATE TOO.  T001
NOTGOD   MVI   GODFLAG,NOGOD            READ ONLY                  T001
.NSEC01  ANOP  ,                                                   T001
         B     OKDSN5                   CONTINUE                   T001
         SPACE 2
*
*  INVALID DSN OR INVALID ACCESS. SORRY.
*
BADDSN   LA    R15,BADDSNMS             -> TO DEFAULT MESSAGE      T003
BADDSNI  MVC   LINEBUFF,BLANKS          CLEAR BUFFER               U021
         MVC   LINEBUFF(7),=C'DATASET'  FIRST PART OF MSG          U021
         MVC   LINEBUFF+8(44),DSNAME    DSNAME                     U021
         LA    R1,LINEBUFF+9            -> START OF DSNAME + 1     U021
         AH    R1,DSNAMEL               -> END OF DSNAME           U021
         XR    R14,R14                  CLEAR FOR LEN              T003
         IC    R14,0(,R15)              GET LEN OF STRING          T003
         BCTR  R14,0                    -1 FOR EXECUTE             T003
         MVC   0(0,R1),1(R15)           MOVE 1 BYTE OF MSG         T003
         EX    R14,*-6                  MOVE THE REST OF MESSAGE   T003
         MVC   LINEDESC(4),=Y($L+4,0)   SET LINE WIDTH FOR MSG     T002
*U021    B     CSIO#                    INVALID DSN
         BAL   R8,KILL3270              TURN OFF 3270 MODE IF ON   U021
         BAL   R14,PUTLINEX             PUT THE SHORT LINE         U021
         L     R9,=A(END)               POINT TO SUBCOMMAND        U021
         BR    R9                       QUIT THIS CP               U021
         SPACE 2
         DROPX R7                       NO NEED FOR IKJPARMD NOW
         SPACE 2
OKDSN5   DC    0H'0'
         SPACE 2
*
*  SET UP DEFAULTS AND INITIALS COND'S
*
         TM    FLAGS3,NOSTAXF           BYPASS STAX?               U021
         BO    NOSTAX                   YES - SKIP IT              U021
         XC    STAXLIST(STAXL),STAXLIST INITIALIZE STAX LIST
         L     R14,=A(STAXEXIT)         POINT TO STAX EXIT
         STAX  (R14),USADDR=(R13),MF=(E,STAXLIST)                  U004
         SPACE 2
NOSTAX   L     R1,=A(TRCHARE)           INITIALIZE PTR TO...       T002
         ST    R1,TRTABADD              TR TABLE (THE EBCDIC ONE)
         LA    R1,LINE06                PT TO FIRST AVAIL SCREEN LOC
         ST    R1,FIRSTSCR              SAVE IT
         LA    R1,LINE19                PT TO LAST AVAIL SCREEN LOC
         ST    R1,MAXSCR                SAVE IT
         XC    LOOKFOR(3*18),LOOKFOR    CLEAR SCAN STRNG SAVE AREA U004
         XC    MEMTTR(3),MEMTTR         NO MEMBER YET
         XC    TXTTTR(3),TXTTTR         NO TEXT FOR MEMBER YET     U002
         XC    ESDTTR(3),ESDTTR         NO ESD PTR YET             U004
         MVC   ESDID(2),=H'1'           ESDID DEFAULTS TO 1 FOR IBM
         MVI   CONTINUE,0               NO SCAN, STORES OR SETS YET
         MVI   CPUTFLAG,NOCSERR         CSOUT WILL WORK FOR SURE
         BAL   R8,CLEARDEF              INITIALIZE IDEF TABLE AND PTRS
         MVC   TTR(4),=XL4'00000100'    INITIALIZE TTRN= 00000100
         TITLE 'ZAP --- OPEN AND INITIALIZE I/O FILE'
         CLI   ALLOCDSN,C' '            ALLOC DSN GIVEN?           U007
         BE    GO$ALLOC                 NO - GO DO IT              U004
         MVC   LINE07(44),DSNAME        SAVE MOMENTARILY           U004
         MVC   DSNAME,ALLOCDSN          CHANGE DSN'S               U004
*
*  ALLOC THE FILE AND OPEN IT FOR INPUT.
*
         SPACE 1
GO$ALLOC ALLOC DSN=DSNAMEL,VOL=VOLSER,DISP=(SHR,KEEP,KEEP),        U001$
               PERM=NO,PASSWD=PASSWORD,UNIT=UNITNAME               U018
         SPACE 1
         BXH   R15,R15,BADDSN
         CLI   ALLOCDSN,C' '            WAS THIS ALLOCDSN?         U004
         BE    *+10                     NO - OK                    U004
         MVC   DSNAME,LINE07            RESTORE DSN                U004
         EJECT
         LA    R0,DCBU                  POINT TO DCB               T002
         ST    R0,DCBLIST               PUT IN OPEN/CLOSE LIST     T002
         MVI   DCBLIST,X'80'            INPUT                      T002
         MVC   DCBDDNAM-IHADCB+DCBU(8),DA08DDN-DAPB08+DAPBAREA DDN T002
         LA    R0,EXLST                 SET UP FOR RDJFCB          T002
         ST    R0,DCBEXLST-1+DCBU-IHADCB  SET IN DCB               T002
         SPACE 1
         RDJFCB  MF=(E,DCBLIST)         GET THE JFCB               T002
         SPACE 1
         OI    JFCBTSDM-JFCDSECT+JFCB,JFCNWRIT   DON'T RE-WRITE    T002
         SPACE 1
         LA    R1,DA08DDN-DAPB08+DAPBAREA   -> DDNAME ALLOCATED    T002
         BAL   R8,FINDVOL               GET VOLSER                 T002
         MVC   VOLSER,0(R1)             SAVE VOLSER OF DATASET     T002
         CLI   DSNAME,X'04'             VTOC?                      U024
         BNE   *+10                     NO - OK                    U024
         MVC   DISPDSN+9(6),VOLSER      YES - FILL IN THE VOLSER   U024
         AIF   (NOT &VSAM).NVSAM04                                 T002
         TM    FLAGS4,NOACBF            DCB REQUESTED?             T002
         BO    SKIPVSAM                 YES, GET DCB               T002
         CLI   JFCDSRG2-INFMJFCB+JFCB,JFCORGAM   VSAM ??           T002
         BE    OPENVSAM                 YES, GET ACB               T002
SKIPVSAM DC    0H'0'                    USE A DCB                  T002
.NVSAM04 ANOP  ,                                                   T002
*                                                                  T002
*  FIND THE LAST TTR FROM THE FORMAT 1 DSCB OF THE DATASET, OBTAINED,
*  CLEVERLY ENOUGH, BY AN 'OBTAIN' (ASSUMING IT'S NOT A VTOC)      T002
*                                                                  T002
         XC    ENDTTR,ENDTTR            DEFAULT TO NO LAST TTR     T002
         XC    DSCB(256),DSCB           CLEAR OUT DEFAULT DSCB     T002
*DSCBLIST CAMLST SEARCH,DSNAME,VOLSER,DSCB                         T002
         MVC   DSCBLIST(4),=AL1(193,0,0,0) MOVE IN OPTIONS         T002
         LA    R14,DSNAME               DSNAME FOR OBTAIN          T002
         LA    R15,VOLSER               PT TO VOLSER               T002
         LA    R0,DSCB+44               DSCB BUFFER                T002
         STM   R14,R0,DSCBLIST+4        STORE ALL 3 POINTERS       T002
         OBTAIN DSCBLIST                GET THE FMT1 DSCB          T002
         BXLE  R15,R15,GETLTTR1         IF OK USE INFO             T002
         AIF   (&MVS).MVS03                                        T002
         CLI   DSNAME,X'04'             IS THIS A VTOC?            T002
         BNE   OBTFAIL                  NO - IT REALLY DIDN'T WORK T002
         CH    R15,=H'16'               RC = 8?                    T002
         BE    GOTVTOC                  YES - GO SAVE INFO         T002
.MVS03   SPACE 2
OBTFAIL  LA    R1,=CL40'OBTAIN FAILED - "LAST" WILL BE INVALID'    T002
         B     NOLAST                   SKIP AROUND                T002
         SPACE 3
GOTVTOC  MVC   DS1BLKL,=H'96'           SET BLKSIZE OF VTOC        T002
         MVI   DS1KEYL,44               SET KEYLEN OF VTOC         T002
         MVC   LASTFMT1(5),DSCB+45      SAVE PTR TO LAST FMT1 USED T002
         B     NOLAST                   CONTINUE                   T002
         SPACE 3
GETLTTR1 DC    0H'0'                                               T002
         AIF   (&MVT).MVT01                                        T002
         CLI   DSNAME,X'04'             VTOC?                      T002
         BE    GOTVTOC                  YES - GET INFO             T002
.MVT01   TM    DS1DSORG,DSORGPO         PARTIONED?                 T002
         BZ    *+8                      NO, DON'T SAY SO           T002
         OI    FLAGCOM,COMPO            YES, INDICATE FOR LATER    T002
         MVC   ENDTTR,DS1LSTAR          SAVE IT                    T002
         CLI   DS1NOEPV,0               ANY EXTENTS?               T002
         BE    BADDSNZP                 NO - CAN'T ACCESS          T002
NOLAST   CLI   DSNAME,X'04'             VTOC?                      T002
         BNE   NOTVTOC4                 NO - SKIP                  U018
         MVC   JFCBDSNM-JFCDSECT+JFCB,DSNAME  YES - CHANGE DSN     T002
         B     GO$OPEN                  CONTINUE                   U018
         SPACE 1
BADDSNZP LA    R15,ZEROSPMS             ZERO SPACE MSG INSERT      T003
         B     BADDSNI                  INSERT THIS MESSAGE        T003
         SPACE 2
NOTVTOC4 MVC   DSNAME,JFCBDSNM-JFCDSECT+JFCB  CHANGE THE DSN MAYBE T002
         MVC   DISPDSN,JFCBDSNM-JFCDSECT+JFCB   IN DISPLY ALSO     T002
         SPACE 1
GO$OPEN  OPEN  MF=(E,DCBLIST),TYPE=J    OPEN THE DATASET           U004
         SPACE 1
         TM    DCBU+48,X'10'            DID IT OPEN?
         BNO   BADDSN                   NO DARN
         OI    FLAGCOM,COMEXCP          USING EXCP ACCESS METHOD   T002
         EJECT
*
*  BUY BUFFER
*
         LH    R0,DS1BLKL               PICK IT UP                 U004
         LTR   R0,R0                    ZERO BLKSIZE (LIKE JOBQ)?
         BP    *+8                      NO - GO ON
         LH    R0,=AL2(20*1024)         USE 20K FOR DEFAULT IN CASE
         CH    R0,=H'264'               BELOW MIN (ESP PDS DIRECTORY)
         BNL   *+8                      NO, LEAVE BLKSIZE AS IS
         LH    R0,=H'264'               YES, SET MIN=PDS DIRECTORY SIZE
         MVI   KEYLEN,0                 CLEAR FOR COPY             U004
         MVC   KEYLEN+1(1),DS1KEYL      COPY KEYLEN                U004
         AH    R0,KEYLEN                ADD TO BUFFERSIZE
         AH    R0,=Y(8+1)               GET ROOM FOR COUNT         U004
*  THE +1 IS SO WILL ALWAYS HAVE RESIDUAL                          U004
         LR    R1,R0                    COPY VALUE
         AH    R0,=AL2(LCSOUT)          WORKAREA SIZE
         STH   R1,BUFFSIZE              STORE AWAY FOR LATER USE
         AH    R0,=Y(50)                SO NO 0C4'S NEAR BUFF. END U012
         GETMAIN R,LV=(0)               MAKE IT UNCONDITIONAL      U004
         ST    R1,ADDRWORK              SAVE ADDR FOR LATER        U004
         SPACE 1
         AH    R1,=AL2(LCSOUT)          ADD WORKAREA SIZ TO PT TO BUFF
         ST    R1,ADDRCNT               SAVE ADDR OF COUNT         U004
         LA    R1,8(,R1)                -> KEY FIELD               U004
         ST    R1,ADDRKEY               SAVE ADDR OF KEY           U004
         ST    R1,ADDRBUFF              DEFAULT DISPLAY IS KEY     U004
         L     R1,=A(CCWLIST)           -> CCWS                    T002
         MVC   CCWS(CCWL),0(R1)         INIT CCWS                  T002
         LH    R1,BUFFSIZE              BUFFER SIZE                U024
         STH   R1,CCW#R#R+6             SET FOR MAX READ           U004
         STH   R1,CCW##R0+6             DITTO FOR R0               U004
         LA    R1,CCWS+CCWL-8           BXLE LIMIT                 U024
         LA    R14,CCWS                 LOOP POINTER               U024
CCWREL   L     R15,0(,R14)              GET OFFSET ASMED INTO CCW  U024
         CLI   0(R14),X'08'             TIC CCW?                   U024
         BE    CCWR01A                  YES, RELOCATE TO CCW'S     U024
CCWR01   TM    0(R14),X'60'             SEARCH INSTRUCTION?        U024
         BNZ   CCWR02                   YES, RELOCATE TO DSA       U024
         AL    R15,ADDRCNT              RELOC I/O INSTR TO BUFFER  U024
         B     CCWR03                   GO UPDATE CCW              U024
CCWR02   ALR   R15,R13                  CALCULATE ADDRESS IN DSA   U024
         B     CCWR03                   GO UPDATE CCW              U024
CCWR01A  LA    R0,0(,R15)               GET OFFSET WITHOUT OPCODE  U024
         XR    R15,R0                   ZERO THE OFFSET            U024
         OR    R15,R14                  MOVE IN TIC CCW ADDRESS    U024
         SLR   R15,R0                   SUBTRACT OFFSET            U024
CCWR03   ST    R15,0(,R14)              SAVE UPDATED ADDRESS       U024
         LA    R0,8                     BXLE INCREMENT             U024
         BXLE  R14,R0,CCWREL            INCREMENT AND LOOP         U024
         AIF   (NOT &VSAM).NVSAM05                                 T002
         B     TOPENLOG                 OPEN LOG FILE              T002
         EJECT
OPENVSAM L     R1,=A(RPLMSK)            GET ADDR OF PATTERN RPL    T002
         MVC   RPLV(RPLVL),0(R1)        SET UP RPL FOR INPUT       T002
         L     R1,=A(EXLMSK)            GET ADDR OF PATTERN EXLST  T002
         MVC   EXLV(EXLVL),0(R1)        SET UP EXLST INPUT         T002
         L     R1,=A(ACBMSK)            NO, TREAT AS ESDS          T002
         MVC   ACBV(ACBVL),0(R1)        MOVE IN CORRECT ACB        T002
         LA    R1,ACBV                  -> ACB                     T002
         ST    R1,DCBLIST               SAVE IN ACB LIST           T002
         MVI   DCBLIST,X'80'            OPEN IT FOR INPUT          T002
         MVC   ACBV+ACBDDNM-IFGACB(8),DA08DDN-DAPB08+DAPBAREA      T002
         LA    R1,EXLV                  -> EXITLIST                T002
         ST    R1,ACBV+ACBEXLST-IFGACB  SAVE IN ACB                T002
         OPEN  MF=(E,DCBLIST)           OPEN ACB                   T002
         TM    ACBV+ACBOFLGS-IFGACB,ACBOPEN   DID IT OPEN?         T002
         BZ    BADDSN                   NO, CLAIM ITS INVALID      T002
         OI    FLAGVSAM,VSAMOPEN        YES, SAY SO                T002
         OI    FLAGCOM,COMVSAM          YES, SAY SO                T002
         SPACE 1
         SHOWCB  AM=VSAM,ACB=(S,ACBV),AREA=(S,VSAMCBA),            T002$
               LENGTH=L'VSAMCBA,MF=(G,VSAMCB),                     T002$
               FIELDS=(CINV,LRECL,RKP,KEYLEN)                      T002
         SPACE 1
         LM    R0,R3,VSAMCBA            GET THE RESULTS            T002
         STH   R0,BUFFSIZE              SAVE SIZE OF BUFFER        T002
         STH   R0,DS1BLKL               SAVE SIZE OF BUFFER        T002
         STH   R0,VSAMCISZ              SAVE CONTROLINTERVAL SIZE  T002
         STH   R1,DS1LRECL              SAVE RECORD LENGTH         T002
         STH   R2,VSAMRKP               SAVE RKP FOR READ RTN.     T002
         STH   R3,KEYLEN                SAVE KEYLENGTH             T002
         AR    R0,R0                    EXTRA BUFFER FOR FIRST ZAP T002
         AH    R0,KEYLEN                KEY OF CONTROLINTERVAL     T002
         AH    R0,KEYLEN                LOW KEY OF CI ALSO         T002
         AH    R0,=AL2(LCSOUT)          WORKAREA SIZE + BUFF SIZE  T002
         AH    R0,=Y(50)                + SPARE FOR THIS AND THAT  T002
         GETMAIN  R,LV=(0)              BUY DA BUFFER              T002
         ST    R1,ADDRWORK              SAVE PTR TO CSWORKAREA     T002
         AH    R1,=AL2(LCSOUT)          -> TO BUFFER AREA          T002
         XC    ADDRCNT,ADDRCNT          CLEAR FOR NOT SETPNT00     T002
         ST    R1,ADDRKEYL              SAVE ADDRESS OF KEY        T002
         ST    R1,ADDRKEY               SAVE ADDRESS OF KEY        T003
         AH    R1,KEYLEN                -> DATA ADDR               T002
         ST    R1,ADDRKEYH              SAVE ADDRESS OF KEY FOR CI T002
         AH    R1,KEYLEN                -> LOW KEY VALUE           T002
         ST    R1,ADDRDATA              SAVE FOR DISPLAY           T002
         ST    R1,ADDRBUFF              SAVE CURRENT DISPLAY ADDR  T002
         LR    R2,R1                    SAVE FOR MODCB             T002
         LH    R4,DS1BLKL               GET MAX RECORD SIZE        T002
         LA    R1,ACBV                  -> ACB                     T002
         ST    R1,RPLV+RPLDACB-IFGRPL   SAVE IN RPL                T002
         ST    R2,RPLV+RPLAREA-IFGRPL   SAVE -> BUFFER IN RPL      T002
         ST    R4,RPLV+RPLBUFL-IFGRPL   SAVE LEN OF BUFFER IN RPL  T002
         LA    R1,TTR                   -> RBA                     T002
         ST    R1,RPLV+RPLARG-IFGRPL    SAVE IN RPL                T002
         VERIFY  RPL=RPLV               FIND MAX RBA               T002
         VSAMTST ,                                                 T002
         SHOWCB  AM=VSAM,ACB=(S,ACBV),AREA=(S,ENDTTR),             T002$
               LENGTH=L'ENDTTR,MF=(G,VSAMCB),FIELDS=ENDRBA         T002
         VSAMTST ,                                                 T002
         L     R1,ENDTTR                GET HIGH RBA VALUE         T003
         XR    R0,R0                    CLEAR FOR DIVISION         T003
         LH    R15,VSAMCISZ             GET CONTROL INTERVAL SIZE  T003
         DR    R0,R15                   GET CI NUMBER OF MAX       T003
         MR    R0,R15                   GET RBA OF MAX CI          T003
         ST    R1,ENDTTR                SAVE FOR LATER             T003
         XC    TTR,TTR                  START AT RBA 0             T002
         OC    KEYLEN,KEYLEN            ANY KEY?                   T002
         BZ    TOPENLOG                 NO, DONE HERE              T002
         L     R1,=A(ACBMSK)            -> TO ACB FOR KEY ACCESS   T002
         MVC   ACBVK(ACBVL),0(R1)       MOVE IN ACB                T002
         L     R1,=A(RPLMSK)            ANOTHER RPL TOO.           T002
         MVC   RPLVK(RPLVL),0(R1)       MOVE IT IN                 T002
         LA    R1,ACBVK                 POINT TO ACB               T002
         ST    R1,OPENKVSM              SAVE IN NEW OPEN LIST      T002
         MVI   OPENKVSM,X'80'           LAST AND ONLY ENTRY        T002
         MVC   ACBVK+ACBDDNM-IFGACB(8),DA08DDN-DAPB08+DAPBAREA     T002
         LA    R1,EXLV                  -> EXIT LIST               T002
         ST    R1,ACBVK+ACBEXLST-IFGACB SAVE IN ACB                T002
         MVI   ACBVK+ACBMACR1-IFGACB,ACBKEY+ACBSEQ+ACBDIR+ACBIN    T002
         LA    R1,ACBVK                 -> ACB                     T002
         ST    R1,RPLVK+RPLDACB-IFGRPL  SAVE IN RPL                T002
         ST    R2,RPLVK+RPLARG-IFGRPL   SAVE KEY ADDR              T002
         ST    R3,RPLVK+RPLKEYLE-IFGRPL SAVE KEY LEN               T002
         MVI   RPLVK+RPLOPT1-IFGRPL,RPLLOC+RPLKGE+RPLSEQ   FIX RPL T002
         MVI   RPLVK+RPLOPT2-IFGRPL,RPLKEY+RPLNSP   FIX RPL ATTR   T002
         OPEN  MF=(E,OPENKVSM)          OPEN KEYSEQ ACB            T002
         TM    ACBVK+ACBOFLGS-IFGACB,ACBOPEN                       T002
         BO    TOPENLOG                 IF IT WORKED, CONTINUE     T002
         EX    0,*                      OTHERWISE, DIE HORRIBLY    T002
.NVSAM05 ANOP  ,                                                   T002
         EJECT
TOPENLOG TM    FLAGS2,LOGF              ARE WE LOGGING?            T002
         BNO   *+8                      IF NOT SKIP OPEN
         BAL   R8,OPENOUT               IF SO DO THE OPEN
         SPACE 2
*  GET FREE DEFINE TABLE SYMBOLS FOR 'LRECL', 'KEYLEN', AND 'BLKSIZE'
         SPACE 1
         L     R3,AIDEFTAB              PT TO DEFINE TABLE
         MVI   0(R3),C'L'               GIVE SYMBOL 'L' FOR 'LRECL'
         MVC   8(2,R3),DS1LRECL         GET LRECL, PUT IN TAB
         MVC   10(2,R3),=C'BL'          GIVE 'BL' FOR BLKSIZE (MAX)
         MVC   18(2,R3),DS1BLKL         GET BLKSIZE, PUT IN TAB    U004
         MVI   20(R3),C'K'              GIVE 'K' FOR 'KEYLEN' SYMBOL
         MVC   28(2,R3),KEYLEN          GET KEYLEN AND PUT IN TABLE
         LA    R3,30(,R3)               UPDATE DEFINE TABLE PTR
         ST    R3,IDEFAVAL              SAVE IT AS PTR TO CURR ENTRY
         MVC   REP,BLANKS               CLEAR REPLY OUT            U004
         MVC   LINE19,BLANKS            CLEAR ERR MSG PART OF SCRN U004
         AIF   (&MVS).MVS04                                        U004
         MVI   JQFLAG,0                 NO MASTER QCR YET         *LPR*
.MVS04   XC    BASEVAL,BASEVAL          BASE = 0                   U004
         SPACE 1
         TM    FLAGS3,M3270F            3270 MODE?                 U014
         BO    NEW$READ                 YES - DON'T TPUT VERSION   U015
         SPACE 1
         MVC   LINEBUFF(40),BLANKS      INIT                       U014
         MVC   LINEBUFF(L'VERSION),VERSION                         U004
         BAL   R14,PUTLINE$             SAY WHAT VERSION           U004
         TITLE 'ZAP --- CREATE THE SCREEN DISPLAY AND GET COMMAND'
*  HERE IF A NEW BLOCK IS TO BE READ AND THE SCREEN IS TO BE BUILT
*  FROM IT
         SPACE 1
NEW$READ XC    NOWSTUFF(NOWLEN-L'BASEVAL),NOWSTUFF RESET BUFF OFF  U004
         BAL   R8,READBLK               GET A BLK FROM DS (BDAM)
         AIF   (&MVS).MVS05                                        U004
         TS    JQFLAG                   MASTER QCR SAVED YET?     *LPR*
         BNZ   NEW$DISP                 YES                       *LPR*
         CLC   DSNAME(13),=C'SYS1.SYSJOBQE'  DOING A JOB Q?       *LPR*
         BNE   NEW$DISP                 NO                        *LPR*
         L     R8,ADDRDATA              LOAD DATA ADDRESS          U004
         MVC   JQQMHDA(JQQMHDAL),0(R8)  SAVE MASTER QCR            T002
.MVS05   B     NEW$DISP                 SKIP AROUND                U004
*
*  HERE IF BUILD SCREEN WITHOUT READING A NEW BUFFER IN
*
NEWDSPNT MVI   TRACE,NOTRACE            SET FLAG                   U004
         SPACE 1
NEW$DISP BAL   R8,CLEAR                 CLEAR THE SCREEN
         OC    BLKLEN(2),BLKLEN         0 READ BEFORE?
         BZ    DISPLAY                  YES - THERE'S NOTHING TO DISP
         LH    R8,OLDPOINT              GET CARET OFFSET           U004
         N     R8,NOWMASK               GET HIGH PART              U004
         LR    R5,R8                    COPY                       U004
         SH    R8,SCRBYTES              BACK UP TO SCREEN START    U004
         BNM   *+6                      CONTINUE IF OK             U004
         SR    R8,R8                    SCR START = BLK START      U004
         A     R8,ADDRBUFF              GET ADDRESS INTO BUFFER
         A     R5,ADDRBUFF              GET DWD ADDRESS INTO BUFFER
         L     R15,FIRSTSCR             PT TO FIRST AVAIL SCREEN POS
*
*  FORMAT ONE LINE OF SCREEN DISPLAY
*
FORMAT   LR    R0,R8                    COPY BUFFER PTR            U004
         S     R0,ADDRBUFF              OFFSET INTO BUFFER FOR SCR U004
         ST    R0,TEMP                  SAVE FOR UNPK              U004
         HEX   (0,R15),TEMP+1,LEN=3,DIGITS=5,HEXTAB=TRHEX          U004
         MVC   TEMP(16),0(R8)           COPY CHAR FORM OF DATA     U004
         UNPK  TEMP2+00(14+1),TEMP+00(7+1)   BYTES 01-07           U004
         UNPK  TEMP2+14(14+1),TEMP+07(7+1)   BYTES 08-14           U004
         UNPK  TEMP2+28(04+1),TEMP+14(2+1)   BYTES 15-16           U004
         TR    TEMP2(32),TRHEX          MAKE EBCDIC                U004
         L     R1,TRTABADD              GET RIGHT TRTAB PTR        U012
         TR    TEMP(16),0(R1)           MAKE IT VALID CHARS        U012
         LR    R1,R0                    COPY BLK OFFSET            U004
         AH    R1,LINEHEX               GET NEXT OFFSET            U004
         SH    R1,BLKLEN                COMPUTE AMOUNT SHORT       U004
         BNP   FORMATOK                 SKIP IF THIS LINE FULL     U004
         LA    R14,TEMP2                -> AREA                    U004
         AH    R14,LINEHEX2             -> PAST END                U004
         LA    R2,0(R1,R1)              GET LENGTH OF HEX GARBAGE  U004
         SR    R14,R2                   -> GARBAGE HEX             U004
FMTBLANK MVC   0(0,R14),BLANKS          << EXECUTED >>             U004
         EX    R2,*-6                   BLANK THE GARBAGE HEX      U004
*  NOTE THAT TEMP2 IS LONG ENOUGH THAT BLANKING 1 CHAR TOO MANY    U004
*  DOESN'T HURT ANYTHING                                           U004
         LA    R14,TEMP                 -> CHARS                   U004
         AH    R14,LINEHEX              -> PAST END                U004
         SR    R14,R1                   -> GARBAGE CHARS           U004
         BCTR  R1,0                     -1 FOR EX                  U004
         EX    R1,FMTBLANK              CHARS ALSO GET TREATMENT   U004
         SPACE 2
FORMATOK MVC   TEMP2+32(2),=C' ×'       MOVE IN CONSTANTS FOR TR   U004
         CLI   LINELEN+1,40             WHICH ONE?                 U004
         BE    FORMAT40                 SHORT                      U004
         L     R1,=A(DISPTR80)          -> TRANSLATE TABLE TO USE  T002
         MVC   7(65,R15),0(R1)          MOVE IN "TRANSLATE" TABLE  T002
         TR    7(65,R15),TEMP           DISTRIBUTE HEX TO SCREEN   U004
         B     FORMATXX                 CONTINUE                   U004
FORMAT40 L     R1,=A(DISPTR40)          -> TRANSLATE TABLE TO USE  T002
         MVC   7(32,R15),0(R1)          MOVE IN SHORT TABLE        T002
         TR    7(32,R15),TEMP           DISTRIBUTE HEX TO SCREEN   U004
         SPACE 1
FORMATXX CR    R8,R5                    BUFF PTR = DATA PTR?       U004
         BNE   NO$CARET                 NO - SKIP CARET            U004
         LR    R1,R15                   COPY LINE ADDR             U004
         BCTR  R1,0                     BACK UP TO ATTR BYTE       U004
         MVI   0(R1),X'E8'              HIGH INTENSITY PROTECTED   U004
         LH    R1,OLDPOINT              GET CARET OFFSET           U004
         N     R1,BITMASK               GET LOW ORDER PART         U004
         LA    R3,BYTES(R1)             PT TO COLUMN OF SCRN       U004
         IC    R1,0(,R3)                GET SCREEN COL FOR CARET   U004
         LA    R14,0(R15,R1)            PT TO WHERE IT GOES        U004
         MVI   0(R14),C'>'              ASSUME EVEN                U004
         TM    OLDPOINT+1,1             IS IT?                     U004
         BNO   *+8                      YES - SKIP                 U004
         MVI   0(R14),C'<'              ODD POINTS THE OTHER WAY   U004
         ST    R14,MIDLINE              SAVE PTR TO CARET LINE     U004
         SPACE 1
NO$CARET AH    R8,LINEHEX               UPDATE BUFFER PTR          U004
         LA    R15,$I(,R15)             UPDATE SCREEN PTR          U004
         AH    R0,LINEHEX               NEXT OFFSET                U004
         CH    R0,BLKLEN                PAST END OF BLOCK?         U004
         BNL   DISPLAY                  YES - GO DISPLAY           U004
         C     R15,MAXSCR               OUT OF SCREEN?             U004
         BL    FORMAT                   NO - DO ANOTHER LINE       U004
         B     DISPLAY                  SKIP (WE WILL TRACE)       U004
         EJECT
DISPLYNT MVI   TRACE,NOTRACE            SET NOTRACE FLAG           U004
         SPACE 2
DISPLAY  NI    FLAGS2,X'FF'-NOEOF       SET TO RECOGNIZE EOF'S     T002
         NI    FLAGVSAM,X'FF'-SEQREAD   TURN OFF SEQ READ          T003
         TM    FLAGS3,M3270F            FULLSCREEN?                U014
         BO    DS01                     YES, IGNORE NODISPF, ETC.  U024
         TM    FLAGS3,NODISPF           SUPPRESS REDISPLAY?        U013
         BO    NOTWHERE                 YES, GO DIRECT TO OUTPUT   U013
         TM    FLAGS2,NOWHEREF+TERSEF   SEE IF WHERE SUPPRESSED
         BNZ   NOTWHERE                 IF ON SUPPRESS IT
DS01 MVC LINE03(40),=CL40'ENTER VALID COMMAND ABOVE OR ? FOR HELP' U004
         MVC   LINE03+72-L'VERSION(L'VERSION),VERSION              U004
         MVC   LINE03+61(7),ZAP+10      PUT IN THE ASM DATE        U004
         SPACE 1
WHEREAMI CLI   LINELEN+1,80             LONG LINES?                U004
         BE    WHERE80                  YES - FORMAT MORE INFO     U004
         MVC  LINE21+17(34),=C'TTR: XXXXXX CCHHR: XXXXXXXXXX LEN:' U004
         HEX   LINE21+36,CCHHR,LEN=5                               U004
         HEX   LINE21+22,TTR,LEN=3                                 U004
         MVC   LINE20+17(4),=C'DSN:'    MOVE IN MASK               U004
         MVC   LINE20+22(24),DISPDSN    SHOW THE DSNAME            U004
         MVC   LINE20+47(4),=C'OFF:'    MOVE IN MASK               U004
         HEX   LINE20+53,OLDPOINT,LEN=2 OFFSET                     U004
         MVC   LINE21+52(5),EDMASK+3    GET READY TO EDIT IN BLKLN U004
         LH    R1,BLKLEN                GET THE LENGTH READ        U004
         CVD   R1,TEMP                  CONV TO DECIMAL            U004
         ED    LINE21+51(6),TEMP+5      EDIT IT TO SCREEN          U004
         B     NOTWHERE                 CONTINUE                   U004
         SPACE 2
WHERE80  MVC   LINE20+00(4),=C'OFF:'                               U004
         LH    R15,OLDPOINT             GET OFFSET IN BUFFER       U004
         BAL   R14,NUMCONV              CONVERT IT                 U004
         MVC   LINE20+05(4),TEMP2+12+4  MOVE IN HEX PART           U004
         MVI   LINE20+10,C'('                                      U004
         MVC   LINE20+11(6),TEMP2+6     DECIMAL PART               U004
         MVC   LINE20+17(7),=C') ADDR:'                            U004
         L     R15,OFFSET               GET IT                     U004
         BAL   R14,NUMCONV              CONVERT                    U004
         MVC   LINE20+25(5),TEMP2+12+3  HEX PART                   U004
         MVI   LINE20+31,C'('                                      U004
         MVC   LINE20+32(8),TEMP2+4     DECIMAL PART               U004
         MVC   LINE20+40(6),=C') DSN:'                             U004
         MVC   LINE20+47($L-47),DISPDSN DSN                        U004
         SPACE 1
         MVC   LINE21+00(4),=C'LEN:'                               U004
         LH    R15,BLKLEN               GET LENGTH OF BLOCK        U004
         BAL   R14,NUMCONV              CONVERT                    U004
         MVC   LINE21+05(4),TEMP2+12+4  HEX PART                   U004
         MVI   LINE21+10,C'('                                      U004
         MVC   LINE21+11(6),TEMP2+6     DECIMAL PART               U004
         MVC   LINE21+17(7),=C') BASE:'                            U004
         L     R15,BASEVAL              BASE                       U004
         BAL   R14,NUMCONV              CONVERT                    U004
         MVC   LINE21+25(5),TEMP2+12+3  HEX PART                   U004
         MVI   LINE21+31,C'('                                      U004
         MVC   LINE21+32(8),TEMP2+4     DECIMAL PART               U004
         AIF   (NOT &VSAM).NVSAM07                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS DATASET VSAM?           T002
         BO    WHEREVSM                 YES                        T002
.NVSAM07 ANOP  ,                                                   T002
         MVC   LINE21+40(8),=C') CCHHR:'                           T002
         UNPK  LINE21+49(10+1),CCHHR(5+1)                          U004
         TR    LINE21+49(10),TRHEX      MAKE IT EBCDIC             U004
         MVC   LINE21+59(5),=C' TTR:'                              U004
         HEX   LINE21+66,TTR,LEN=3                                 U004
         AIF   (NOT &VSAM).NVSAM08                                 T002
         B     NOTWHERE                 WRITE SCREEN               T002
WHEREVSM MVC   LINE21+40(6),=C') RBA:'  INSERT VSAM STYLE TTR      T002
         L     R15,TTR                  GET CURRENT RBA            T002
         BAL   R14,NUMCONV              DECIMAL                    T002
         MVC   LINE21+47(8),TEMP2+12    MOVE IN HEX RBA VALUE      T002
         MVI   LINE21+47+8,C'('         LEFT PAREN FOR DEC RBA     T002
         MVC   LINE21+47+9(11),TEMP2+1  GET RBA                    T002
         MVI   LINE21+47+20,C')'        CLOSE DEC RBA              T002
.NVSAM08 ANOP  ,                                                   T002
         SPACE 1
NOTWHERE NI    FLAGS2,255-NOWHEREF      ENABLE WHERE LINE          U004
         BAL   R8,CSIO                  DISPLAY SCREEN JUST MADE   U013
         TITLE 'ZAP --- COMMAND CHECKING AND EXECUTION'
*
*  FIND REPLY LENGTH AND COPY IT TO TEMP2
*
COMSCAN  OC    REP,BLANKS               MAKE IT UPPER CASE         U004
         LH    R1,READLEN               GET READ LENGTH            U013
         CLC   REP,BLANKS               ANY REPLY AT ALL?          U021
         BE    NEWDSPNT                 NO - DISPLAY WITH NO TRACE U021
COMSCAN2 CLI   REP,C' '                 LEADING BLANK?             U021
         BNE   COMSCAN3                 NO - ALL DONE HERE         U021
         MVC   REP(L'REP-1),REP+1       YES - SHIFT IT LEFT        U021
         MVI   REP+L'REP-1,C' '         BLANK LAST CHAR            U021
         BCT   R1,COMSCAN2              KEEP SHIFTING              U021
COMSCAN3 LA    R1,REP(R1)               POINT TO LAST CHAR + 1     U013
COMGET   BCTR  R1,0                     DECREMENT POINTER          U013
         CLI   0(R1),C' '               END OF STRING?             U013
         BE    COMGET                   NO, GO TEST ANOTHER CHAR   U013
         LA    R0,REP-1                 GET LENGTH...              U013
         SR    R1,R0                    ... OF DATA                U013
         BNP   NEWDSPNT                 ZERO, IGNORE WITH NO TRACE U013
         STH   R1,REALRDLN              PHONY UP THE ACTUAL READ LEN
         MVC   TEMP2(40),REP            COPY START OF COMMAND      U004
*    (SOME ROUTINES MODIFY THE COMMAND IN TEMP2)                   U004
         EJECT
*  SEE IF THE COMMAND IS IN THE COMMAND TABLE
         SPACE 1
         LR    R0,R1                    GET THE TRUE READ LENGTH   U004
         SR    R1,R1                    CLEAR REG FOR IC'S
         L     R2,=A(COMTAB)            PT TO COMMAND TABLE        U004
         BAL   R14,*+8                  SET LOOP ADDR & SKIP       U004
         SPACE 1
         LA    R2,12(,R2)               -> NEXT TABLE ENTRY        U004
         CLI   0(R2),X'FF'              END OF TABLE
         BE    EXPR                     YES - NOT IN TAB, AN EXPRESS?
         IC    R1,0(,R2)                NO - PICK UP LEN OF COMMAND
         N     R1,=X'0000000F'          JUST THE LEN               T002
         EX    R1,COMCLC                COMMAND MATCH?
         BNER  R14                      NO - CHECK NEXT            U004
         IC    R15,0(,R2)               GET FLAGS FOR COMMAND      T002
         N     R15,=X'000000F0'         JUST THE FLAGS             T002
         EX    R15,COMTM                CHECK FOR VALID COMD. HERE T002
         BZ    COMMERR                  INVALID, GET CORRECT MSG   T002
         L     R9,8(,R2)                PICK UP ROUTINE BASE REG   U021
         BCTR  R0,0                     COMPUTE LENGTH OF ...      U004
         SR    R0,R1                    ... REMAINING INPUT        U004
         LA    R1,TEMP2+1(R1)           -> START OF OPERANDS       U004
         BR    R9                       GO TO SUBCOMMAND           U021
         SPACE 1
*  NOTE:  CC IS SET FOR OPERAND LENGTH = 0 OR ^=0                  U004
COMCLC   CLC   REP(0),1(R2)             << EXECUTED >>             U004
COMTM    TM    FLAGCOM,0                << EXECUTED >>             T002
         SPACE 1
         AIF   (NOT &VSAM).NVSAM09                                 T002
COMMERR  LA    R2,INVVSAM               ASSUME INVALID FOR VSAM    T002
         TM    FLAGCOM,COMVSAM          IS IT VSAM?                T002
         BO    BOTCH                    YES, GOTEM                 T002
         LA    R2,INVNVSAM              ASSUME INVAL FOR NON-VSAM  T002
         TM    FLAGCOM,COMEXCP          IS IT VSAM?                T002
         BO    BOTCH                    YES, GOTEM                 T002
         LA    R2,NOMCOM                ONLY THING LEFT IS EXCP-PO T002
         AGO   .VSAM02                                             T002
.NVSAM09 ANOP  ,                                                   T002
COMMERR  LA    R2,NOMCOM                DATASET NOT PO             T002
.VSAM02  ANOP  ,                                                   T002
         B     BOTCH                    YES, GOTEM                 T002
         TITLE 'ZAP --- COMMAND EXECUTION --- EXPRESSION'
*
*****  NO COMMAND (EXPRESSION)  *****
*
EXPR     LA    R1,REP                   GET EXPRESSION PTR         U004
         LH    R0,REALRDLN              GET EXPRESSION LEN
         SPACE 2
PARSE    L     R15,OFFSET               GET '*' (CURRENT VALUE)    U004
         MVI   EXPOPT,YESSYMB           SAY 'LOOK AT THE SYMBOL TABLE'
         BAL   R8,CALLEXP               GO PARSE                   U004
         S     R15,BASEVAL              GET OFFSET IN BUFFER       U004
         BNM   SETPOINT                 IT'S NON-NEG, SO WE'RE OK  U004
         SPACE 2
SETPNT00 XR    R15,R15                  CAN'T HAVE NEGATIVE
         CLC   ADDRBUFF,ADDRCNT         DISPLAYING COUNT FIELD?    U004
         BNE   SETPOINT                 NO - OK                    U004
         LA    R15,8                    YES - PUT CARET AT KEY     U004
         SPACE 1
SETPOINT LH    R2,BLKLEN                GET BLKLENGTH              U004
         BCTR  R2,0                     -1 FOR COMPARE
         CR    R15,R2                   ARE WE STILL WITHIN THE BLK?
         BNH   *+6                      YES - WE'RE OK
         LR    R15,R2                   NO - MAKE IT THE END OF BLK
         STH   R15,OLDPOINT             SAVE OFFSET FOR CURR LOC
         A     R15,BASEVAL              RELOCATE IT                U004
         ST    R15,OFFSET               SAVE ADDR                  U004
         B     NEW$DISP                 GO DO IT
         SPACE 2
INVEXPER LA    R2,INVEXP                YES - SAY INVALID SYNTAX
*  NOTE:  R1 WILL PT TO THE COLUMN OF THE ERROR
         B     BOTCH                    AND GO TELL HIM WITH NO TRACE
         SPACE 6                        SPLIT HERE
ZAPCMDS  CSECT                          ALL SUBCOMMANDS HERE       U021
         TITLE 'ZAP --- COMMAND EXECUTION --- HELP, ?'
*
*****  HELP (?, ?1, ?2, ?3, ?4, ?5, ?6, ?7, HELP, H, H1,...,H7)    U004
*
         USNGX *,R9                                                U021
HELPHELP TM    FLAGS3,M3270F            FULLSCREEN?                U014
         BZ    HELPALL                  NO - DO IT OLD WAY         U007
         SR    R3,R3                    HE WANTS HELP #1 ("HELP")  U005
         B     HELPOK                   AND PROCESS                U004
         SPACE 1
         DROPX R9                                                  U021
         SPACE 2
HELP     L     R9,=A(HELPHELP)          FUDGE BASE REGISTER        U021
         USNGX HELPHELP,R9                                         U021
         BNP   HELPHELP                 JUST "?" - GIVE HIM "?1"   U004
         TM    FLAGS3,M3270F            FULLSCREEN?                U014
         BZ    HELPALL                  NO - DO IT OLD WAY         U007
         XR    R15,R15                  * = 0                      U004
         BAL   R8,CALLEXP               PARSE OPERAND              U004
         LTR   R3,R15                   GET THE ANSWER             U005
         BNP   INVEXPER                 NO GOOD                    U004
         BCTR  R3,0                     -1 FOR OFFSET              U005
         CH    R3,=H'15'                GREATER THAN 9?            U005
         BL    *+8                      NO - SKIP                  U004
         SH    R3,=H'6'                 EXP THOUGHT IT WAS HEX     U004
         SPACE 2
HELPOK   LA    R2,1+C'1'(,R3)           GET NEXT PROMPT            U004
         BAL   R8,CLEAR                 CLEAR THE SCREEN FIRST     U004
         L     R4,=V(ZAPHELP)           POINT TO STUFF             U021
         CH    R3,2(,R4)                TOO BIG?                   U021
         BH    INVEXPER                 YES, BYE BYE               U004
         BL    *+8                      NO, USE IT, WE HAVE PROMPT U004
         LA    R2,C'1'                  AT END, WRAP TO BEGIN      U004
         LR    R1,R3                    COPY # TO BETTER REG       U004
         OI    REP-1,X'01'              SET THE MDT FLAG           U010
         MVC   REP,BLANKS               BLANK THE PROMPT           U010
         MVI   REP,C'?'                 SET UP PROMPT              U004
         STC   R2,REP+1                 DO IT                      U004
         CLI   REP+1,X'FA'              TOO BIG?                   U004
         BL    HELPOK02                 NO - OK                    U004
         MVI   REP+1,C'1'               SET FIRST DIGIT            U004
         SH    R2,=H'10'                FIX OTHER DIGIT            U004
         STC   R2,REP+2                 SET IT                     U004
HELPOK02 MH    R1,=H'520'               * LENGTH OF EACH           U004
         A     R1,4(,R4)                POINT TO IMAGE             U021
         LA    R2,LINE06                FIRST SCREEN LINE FOR HELP U004
         LA    R14,$I                   BXLE INCR                  U004
         LA    R15,LINE18+20            LAST SCREEN LINE FOR HELP  U004
         LA    R0,40                    INCR FOR HELP              U004
         CLI   LINELEN+1,80             LONG LINES?                U004
         BNE   *+8                      NO - SKIP                  U004
         LA    R2,LINE06+17             YES - CENTER IT            U004
         SPACE 1
         MVC   0(40,R2),0(R1)           MOVE 1 LINE                U004
         AR    R1,R0                    INCR SOURCE PTR            U004
         BXLE  R2,R14,*-8               MOVE 520 BYTES             U004
         B     DISPLYNT                 GIVE HIM SCR (NOTRACE)     U004
         SPACE 6
*
*****  HELP (NON FULLSCREEN TERMINALS)  *****
*
HELPALL  OI    FLAGS3,NODISPF           DON'T REDISPLAY AT END     U011
         L     R4,=V(ZAPHELP)           POINT TO HELP              U021
         LH    R3,0(,R4)                NUMBER OF LINES TO TPUT    U021
         LA    R8,8(,R4)                -> INDICATOR BYTES         U021
         L     R2,4(,R4)                -> FIRST LINE              U021
         SPACE 1
HELPLOOP CLI   0(R8),0                  DUMP THIS LINE?            U004
         BE    HELPSKIP                 NO - SKIP IT               U004
         MVC   LINEBUFF(40),0(R2)       MOVE TO TEMP BUFFER        U004
         BAL   R14,PUTLINE$             PRINT IT                   U004
         TM    FLAGS2,ATTNHIT           SEE IF USER INTERRUPTED IT U014
         BO    HELPINT                  I DON'T BLAME HIM
HELPSKIP LA    R2,40(,R2)               POINT TO NEXT LINE         U004
         LA    R8,1(,R8)                NEXT INDICATOR BYTE        U004
         BCT   R3,HELPLOOP              GIVE HIM NEXT LINE
         B     DISPLYNT                 GO BACK TO USER            U013
HELPINT  NI    FLAGS2,255-ATTNHIT       TURN OFF INTERRUPT         U014
         B     DISPLYNT                 GO BACK TO USER            U013
         SPACE 1
         DROPX R9                                                  U021
   TITLE 'ZAP --- COMMAND EXECUTION --- LOG, CRT, TERSE, VERBOSE, DISP'
*
***** LOG
*
LOG      BAL   R8,LOGTEST               OPEN THE DS IF NOT OPEN    U004
         LA    R2,LOGONMSG              POINT TO MSG               U004
         B     BOTCH                    DISPLAY MSG                U004
         SPACE 2
*
***** CRT
*
CRT      OI    FLAGS3,CRTF              SET FLAG                   U014
         NI    FLAGS3,255-M3270F        TURN OFF FLAG              U024
         BAL   R8,KILL3270              TURN OFF 3270 MODE IF ON   U004
         MVC   WIDTHS(8),=2F'4'         SET UP AND DOWN DISTANCES  U024
         NI    FLAGS2,255-TERSEF        AND SET VERBOSE MODE       U024
         OI    FLAGS3,NODISPF           DON'T REDISPLAY            U013
         B     DISPLYNT                 GO BACK TO USER, NO TRACE  U013
         SPACE 2
*
***** TERSE/VERBOSE
*
TERSE    OI    FLAGS2,TERSEF            SUPPRESS WHERE
         OI    FLAGS3,NODISPF           DON'T REDISPLAY            U013
         B     DISPLYNT                 GO BACK TO USER, NO TRACE  U013
         SPACE 1
VERBOSE  NI    FLAGS2,255-TERSEF        ALLOW WHERE
         OI    FLAGS3,NODISPF           DON'T REDISPLAY            U013
         B     DISPLYNT                 GO BACK TO USER, NO TRACE  U013
         SPACE 3
*
*****  DISP  *****
*
DISPC    MVC   ADDRBUFF,ADDRCNT         -> COUNT                   T002
         LH    R14,LENKEYDA             GET KL+DL                  T002
         LA    R14,8(,R14)              INCLUDE LEN OF COUNT       U004
         STH   R14,BLKLEN               SET DISPLAY BLOCK LENGTH   U004
         LA    R15,8                    DISPLAY OFFSET             U004
         B     SETPOINT                 RE-DISPLAY NEAR TOP        U004
         SPACE 1
DISPK    MVC   ADDRBUFF,ADDRKEY         -> KEY                     U004
         MVC   BLKLEN,LENKEYDA          SET DISPLAY BLOCK LENGTH   T002
         B     SETPNT00                 RE-DISPLAY AT TOP          U004
         SPACE 1
         USNGX *,R9                                                T003
DISPD    MVC   ADDRBUFF,ADDRDATA        -> DATA                    U004
         LH    R14,LENKEYDA             GET KL+DL                  T002
         AIF   (NOT &VSAM).NVSAM52                                 T003
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T003
         BZ    DISPDNVS                 NO, JUST SINGLE KEY        T003
         LH    R14,VSAMCISZ             YES, GET CI SIZE           T003
         B     DISPDST                  AND SAVE FOR DISPLAY       T003
.NVSAM52 ANOP  ,                                                   T003
DISPDNVS SH    R14,KEYLEN               DON'T DISPLAY KEY          T003
DISPDST  STH   R14,BLKLEN               SET DISPLAY BLOCK LENGTH   T003
         B     SETPNT00                 RE-DISPLAY AT TOP          U004
         DROPX R9                                                  T003
         TITLE 'ZAP --- COMMAND EXECUTION --- END'                 U007
*
*****  END  *****
*
         SPACE 2
         USNGX *,R9                                                U021
END      BAL   R8,CHKPT                 CHECK TO SEE IF ZAP REQ
         BAL   R8,KILL3270              TURN OFF 3270 MODE         U007
         BAL   R8,CLOSE                 CLOSE THE FILE IF ANY
         TM    FLAGS2,LOGF              SEE IF LOGGING
         BNO   EXITOUT                  IF NOT NO CLOSE
         L     R1,CSOUTWK               GET WORKAREA
         LTR   R1,R1                    IF ANY
         BZ    EXITOUT                  NONE, CAN'T BE OPEN THEN
         CLI   ENQIT,0                  WAS ANYTHING IMPORTANT SAID?
         BE    EXITSCR                  NO, GO DELETE LOG IMMED    U024
         SPACE 1
         LA    R1,N5                    POINT TO DIGIT             U024
         BAL   R8,SETLINE               SET UP A LINE
         B     EXITOUT                  NOT PRINTING?
         USNGX LOGLINE,R2                                          U022
         MVC   LL@TEXT(33),=C'FILE CLOSED - PROCESSING COMPLETE'   U022
         MVI   LL@CC,C'-'               TRIPLE SPACE IT            U022
         DROPX R2                                                  U022
         BAL   R8,CPUT                  DUMP IT
EXITASK  TPUTX 'ZAP LOG REQUIRED? (YES/NO)',ASIS                   U023
         LM    R0,R1,REGS3270           SCREENL, @ SCREEN          U023
         LA    R1,0(,R1)                CLEAR TPUT FULLSCR FLAG    U023
         O     R1,=X'80000000'          MAKE IT A TGET             U023
         SVC   93                       DO THE TGET SVC            U023
         LTR   R15,R15                  CHECK RC FROM TGET         U023
         BNZ   EXITENQ                  GO ENQ IF ERROR ON INPUT   U023
         LTR   R1,R1                    ANY ANSWER?                U023
         BNP   EXITASK                  NO - ASK AGAIN             U023
         OI    SCREEN,C' '              MAKE IT UPPER CASE         U023
         CLI   SCREEN,C'N'              DELETE IT?                 U023
         BE    EXITSCRM                 YES - DO SO                U024
         CLI   SCREEN,C'Y'              PRINT IT?                  U023
         BNE   EXITASK                  NO - WHAT THEN????         U023
EXITENQ  TPUTX 'ZAP LOG QUEUED FOR PRINTING'                       U024
         L     R1,CSOUTWK               RELOAD WORKAREA PTR        U023
*U004    LA    R14,HISBIN
*U004    CSOUT ENQ,(14),=H'1',CALL=CALL,MF=(E,(1))
         MVI   0(R1),X'40'          (+) FLAG AS ENQUEUE CALL
*U004    ST    R14,4(,R1)           (+) STORE FIRST PARM
*U004    LA    R0,=H'1'             (+)
*U004    ST    R0,8(,R1)            (+) STORE SECOND PARM
         LR    R2,R1                (+) COPY WORKAREA PTR
         L     R15,=V(CSOUT)        (+) POINT TO ROUTINE
         LR    R1,CSREG             (+) GET CSAREA PTR
         BALR  R14,R15              (+) CALL IT
         B     EXITOUT                  THEN LEAVE
         SPACE 1
EXITSCRM TPUTX 'ZAP LOG DELETED'        TELL HIM                   U024
EXITSCR  L     R1,CSOUTWK               GET WORKAREA PTR           U024
*U004    CSOUT SCR,MF=(E,(1)),CALL=CALL DELETE FILE
         MVI   0(R1),X'02'          (+) INDICATE SCRATCH
         LR    R2,R1                (+) COPY WORKAREA PTR
         L     R15,=V(CSOUT)        (+) POINT TO ROUTINE
         LR    R1,CSREG             (+) GET CSAREA PTR
         BALR  R14,R15              (+) CALL IT
         SPACE 1
EXITOUT  L     R13,4(,R13)              RESTORE THE REG STUPID
         LM    R14,R12,12(R13)          RESTORE
         XR    R15,R15                  RC=0
         BR    R14                      BYE BYE
         SPACE 1
         DROPX R9                                                  U021
 TITLE 'ZAP --- COMMAND EXECUTION --- EBCDIC, ASCII, ZCODE, //DEBUG' LW
*
*****  EBCDIC  *****
*
         USNGX *,R9                                                U021
EBCDIC   L     R1,=A(TRCHARE)           -> EBCDIC TRTAB            T002
         LA    R2,EBCDCMSG              TELL HIM WHAT JUST HAPPENED
         B     NEWTRANS                 AND DO IT FROM NOW ON
         SPACE 1
         DROPX R9                                                  U021
         SPACE 4
*
*****  ASCII  *****
*
         USNGX *,R9                                                U021
ASCII    L     R1,=A(TRCHARA)           PICK UP PTR TO ASCII TRTAB T002
         LA    R2,ASCIIMSG              TELL HIM WHAT JUST HAPPENED
         B     NEWTRANS                 GO FINISH THE JOB
         SPACE 1
         DROPX R9                                                  U021
         SPACE 4
*
*****  ZCODE  *****
*
         USNGX *,R9                                                U021
ZCODE    L     R1,=A(TRCHARZ)           PICK UP PTR TO ZCODE TRTAB T002
         LA    R2,ZCODEMSG              SAY WHAT TYPE OF TRANS
NEWTRANS ST    R1,TRTABADD              SAVE FOR LATER DISPLAYS
         OI    FLAGS2,NOWHEREF          SUPRESS WHERE
         B     BOTCH                    GO DO IT AND DISPLAY
         SPACE 1
         DROPX R9                                                  U021
         SPACE 4
*
*****  //DEBUG  *****
*
DIEFAST  NI    FLAGS3,255-M3270F        TURN IT OFF FOR REAL       U015
         BAL   R8,KILL3270              TURN OFF 3270 MODE         U004
         DC    X'00C1'                  "HALT..."                  U004
         LA    R2,OUCH                  PT TO MSG                  U004
         B     BOTCH                    RESUME IF "GO" FROM TEST   U004
         TITLE 'ZAP --- COMMAND EXECUTION --- #, LAST, LASTDS1'    U004
*
*****  # (CALC)  *****
*
CALC     L     R15,OFFSET               GET '*' = CURRENT OFFSET   U004
         MVI   EXPOPT,YESSYMB           TELL HIM SCAN SYMB TAB
         BAL   R8,CALLEXP               PARSE & CHECK              U004
         MVC   LINE19+17(20),=CL20'CALCULATE VALUE:'    MOVE IN MASK
         BAL   R14,NUMCONV              CONVERT                    U004
         MVC   LINE19+34(12),TEMP2      DECIMAL PART               U004
         MVC   LINE19+48(8),TEMP2+12    HEX PART                   U004
         B     NEWDSPNT                 GIVE IT TO HIM
         SPACE 4
*
*****  LAST  *****
*
         USNGX *,R9                                                U021
LAST     BAL   R8,CHKPT                 CHECK TO SEE IF ZAP REQ    T002
         AIF   (NOT &VSAM).NVSAM10                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    LASTV                    YES, GET LAST RECORD       T002
.NVSAM10 ANOP  ,                                                   T002
         CLI   DSNAME,X'04'             IS THERE A LAST TTR (NOT VTOC)?
         LA    R2,LASTINV               PT TO MESSAGE IN CASE
         BE    BOTCH                    NO - TELL HIM              U004
LASTV    MVC   TTR,ENDTTR               PICK UP LAST TTR           T002
         B     NEW$READ                 GO GIVE IT TO HIM
         SPACE 1
         DROPX R9                                                  U021
         SPACE 4
*
*****  LASTDS1  *****                                              U004
*
         USNGX *,R9                                                U021
LASTDS1  CLI   DSNAME,X'04'             IS THIS THE VTOC?          T002
         LA    R2,NOTVTOC               PT TO MESSAGE IN CASE      U004
         BNE   BOTCH                    NO - TELL HIM              U004
         BAL   R8,CHKPT                 CHECK TO SEE IF ZAP REQ    T002
         XC    DWD,DWD                  CLEAR AREA FOR 'ABS'       U004
*  ASSUME VTOC IS 1 EXTENT ==> M=0                                 U004
         MVC   DWD+3(5),LASTFMT1        SET CCHHR OF LAST FMT1     U004
         B     ABSGOT1                  GO FAKE AN 'ABS' COMMAND   U004
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- D, U'                U004
*
*****  D  *****
*
         USNGX *,R9                                                U021
DISPLAY# BNZ   PARSE                    MORE THAN A 'D'            U004
         BAL   R8,DU$COM                GET # OF BYTES             U004
         AR    R15,R14                  WHERE TO POINT TO NOW (OFFSET)
         CH    R15,BLKLEN               PAST BLK?
         BL    SETPOINT                 NO - GO ON
         SRL   R14,1                    TRY HALF WAY THEN
         SR    R15,R14                  MAYBE NOW?
         CH    R15,BLKLEN               STILL ICKY?
         BNL   OFLOD                    YES - GIVE UP AND NEXT BLK
         LH    R15,BLKLEN               NO - PT TO END OF BLK + 1
         B     SETPOINT                 GO GIVE IT TO HIM
         SPACE 2
OFLOD    BAL   R8,CHKPT                 CHECK TO SEE IF ZAP REQ
         BAL   R8,READNBLK              GET THE NEX BLK            U004
         B     SETPNT00                 GO GIVE HIM OFFSET 0       U004
         SPACE 1
         DROPX R9                                                  U021
         SPACE 3
*
*****  U  *****                                                    U004
*
         USNGX *,R9                                                U021
UP       BAL   R8,DU$COM                GET # OF BYTES             U004
         SR    R15,R14                  BACK UP                    U004
         BNM   SETPOINT                 USE IT IF OK               U004
         B     SETPNT00                 TOO FAR, USE 0             U004
         SPACE 1
         DROPX R9                                                  U021
         SPACE 5
DU$COM   LH    R15,OLDPOINT             GET CURRENT OFFSET         U004
         LA    R14,13                   3270 # OF LINES            U024
         MH    R14,LINEHEX              GET # OF BYTES DISPLAYED   U024
         TM    FLAGS3,M3270F            3270 MODE?                 U024
         BOR   R8                       YES, RETURN THIS VALUE     U024
         L     R14,WIDTHD               GET # OF LINES BELOW       U004
         A     R14,WIDTHU               ADD NUMBER OF LINES ABOVE  U004
         LA    R14,1(,R14)              ADD CARET LINE             U004
         MH    R14,LINEHEX              GET # OF BYTES DISPLAYED   U004
         BR    R8                       RETURN TO "D" OR "U"       U004
         TITLE 'ZAP --- COMMAND EXECUTION --- =, NODEF'            U004
*
*****  =  *****
*
         USNGX *,R9                                                U021
EQUALS   BNP   INVEXPER                 ERROR IF JUST '='          U004
         LR    R7,R1                    -> STRING                  U004
         LR    R1,R0                    LENGTH                     U004
         BAL   R8,HEXCHECK              SEE IF VALID NUMBER        U004
         B     EQUALBAD                 CAN'T USE #'S FOR SYMBOLS  U004
         L     R4,IDEFAVAL              GET PTR TO 1ST AVAIL TABL POS
         CLI   0(R4),X'FF'              END OF TABLE?
         LA    R2,TABFULL               GET MSG ADDR IN CASE       U004
         BE    BOTCH                    NO ROOM...                 U004
         L     R3,AIDEFTAB              PT TO BEGINNING            U004
         SPACE 1
*  SEE IF THE SYMBOL IS ALREADY DEFINED
         SPACE 1
ICLOOP   CR    R3,R4                    UP TO FIRST AVAIL POS YET?
         BNL   OKIDEF                   YES - NO DUPLICATE, SO DEFINE
         CLC   0(8,R3),REP+1            NO - SYMBOL ALREADY HERE?  U004
         BE    REDEF                    YES - SO REDEFINE IT
         LA    R3,10(,R3)               NO - PT TO NEXT SPOT IN TABLE
         B     ICLOOP                   KEEP GOING TILL OUT
         SPACE 1
REDEF    MVC   0(8,R3),REP+1            MOVE IN THE SYMBOL         U004
         MVC   8(2,R3),OFFSET+2         MOVE IN THE OFFSET         U004
         LA    R2,REDEFMSG              PT TO MSG SAYING REDEFINED SYM
         B     BOTCH                    DONT RESET FIRST AVAIL - LEAVE
         SPACE 1
OKIDEF   MVC   0(8,R4),REP+1            MOVE IN THE SYMBOL TO TAB  U004
*U004    MVC   8(2,R4),OLDPOINT         MOVE IN OFFSET INTO BLK
         MVC   8(2,R4),OFFSET+2         MOVE IN THE OFFSET         U004
         LA    R4,10(,R4)               POINT TO NEXT AVAIL
         ST    R4,IDEFAVAL              SAVE FOR NEXT TIME
         OI    FLAGS2,NOWHEREF          SUPRESS WHERE LINES
         B     NEWDSPNT                 DISPLAY (NOTRACE)
         SPACE 2
EQUALBAD LA    R2,BADEQUAL              PT TO MSG                  U004
         B     BOTCH                    MAS CAN'T COMPLAIN ANYMORE U004
         SPACE 1
         DROPX R9                                                  U021
         SPACE 7
*
*****  NODEF  *****
*
NODEF    BAL   R8,CLEARDEF              YES - LET HIM HAVE IT
         LA    R2,DEFRESET              TELL HIM WE RESET THE TABLE
         OI    FLAGS2,NOWHEREF          SUPRESS WHERE LINES
         B     BOTCH                    GO BACK TO DISPLAY (NOTRACE)
         TITLE 'ZAP --- COMMAND EXECUTION --- IDEF'
*
*****  IDEF  *****
*
         USNGX *,R9                                                U021
IDEF     BAL   R8,CLEAR                 CLEAR THE SCREEN
         MVC   LINE06(16),=C' SYMBOL   OFFSET'  MOVE MASK FOR COL 1
         MVC   LINE06+20(3*20-4),LINE06  MAKE 3 MORE COLUMNS       U004
         LA    R1,LINE08                PT TO DISPLAY              U004
         CLI   LINELEN+1,80             4 COLUMNS OK?              U004
         BE    *+14                     YES - LEAVE IT             U004
         MVC   LINE06+40($L-40),BLANKS  NO - KILL LAST 2           U004
         LA    R1,LINE07                PT TO DISPLAY              U004
         L     R2,IDEFAVAL              PT PAST LAST LOC TO FMT    U004
         L     R3,AIDEFTAB              PT TO TABLE                U004
         OI    FLAGS2,NOWHEREF          SUPRESS WHERE LINES
         SPACE 1
IDEFNEWL LA    R4,2                     ASSUME 2 COLUMNS           U004
         CLI   LINELEN+1,80             RIGHT?                     U004
         BNE   *+6                      YES - OK                   U004
         AR    R4,R4                    NO - MAKE IT 4 COLUMNS     U004
IFMTLOOP CR    R3,R2                    DONE WITH FORMATTABLE STUFF?
         BNL   DISPLYNT                 YES - DISPLAY WITH NO TRACE
         MVC   0(8,R1),0(R3)            MOVE SYMBOL TO DISPLAY
         HEX   (11,R1),(8,R3),LEN=2     MOVE IN OFFSET             U004
         LA    R1,20(,R1)               UPDATE DISPLAY PTR
         LA    R3,10(R3)                UPDATE TABLE PTR
         BCT   R4,IFMTLOOP              DO ANOTHER                 U004
         LA    R1,$I-4*20(,R1)          INCREMENT A LITTLE MORE    U004
         CLI   LINELEN+1,80             WAS THAT ENOUGH MORE?      U004
         BE    IDEFNEWL                 YES - DO ANOTHER LINE      U004
         LA    R1,40(,R1)               NO - FIX IT                U004
         B     IFMTLOOP                 KEEP UP THE GOOD WORK
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- ITRACE'
*
*****  ITRACE  *****
*
         USNGX *,R9                                                U021
ITRACE   L     R2,AITRCTAB              PT TO BEGINNING OF TABLE   U004
         OI    FLAGS2,NOWHEREF          SUPRESS WHERE LINES
         BAL   R8,CLEAR                 CLEAR THE SCREEN
         MVC   LINE06+1(13),=C'  TTR    OFFS'   MOVE IN TITLE COL1 T002
         AIF   (NOT &VSAM).NVSAM11                                 T002
         TM    FLAGVSAM,VSAMOPEN        VSAM?                      T002
         BZ    *+10                     NO, KEEP TTR               T002
         MVC   LINE06+3(3),=C'RBA'      VSAM USES RBA              T002
.NVSAM11 ANOP  ,                                                   T002
         MVC   LINE06+1+15(15*4-2),LINE06+1   MAKE 4 MORE COLUMNS  T002
         LA    R3,LINE08                FIRST AVAIL SCREEN POSIT   U004
         CLI   LINELEN+1,80             6 COLUMNS OK?              U004
         BE    *+14                     YES - LEAVE IT             U004
         MVC   LINE06+40($L-40),BLANKS  NO - KILL LAST 2           U004
         LA    R3,LINE07                FIRST AVAIL SCREEN POSIT   U004
NEWLINE  LA    R4,2                     CNT FOR GOOD OFFSET DSP LP T002
         CLI   LINELEN+1,80             NEED 5 COLUMNS?            T002
         BNE   *+8                      NO - LEAVE IT              U004
         LA    R4,5                     YES - MAKE IT 5            T002
ITRFMTLP CLI   0(R2),X'FF'              END OF TABLE
         BE    DISPLYNT                 GO DISPLAY (NOTRACE & NO REBLD)
         AIF   (NOT &VSAM).NVSAM12                                 T002
         HEX   (1,R3),(0,R2),LEN=4      MOVE IN TTR/RBA            T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    *+10                     YES                        T002
         MVC   1+6(2,R3),BLANKS         CLEAR N OF TTRN            T002
         AGO   .VSAM03                                             T002
.NVSAM12 ANOP  ,                                                   T002
         HEX   (1,R3),(0,R2),LEN=3      MOVE IN TTR                T002
.VSAM03  ANOP  ,                                                   T002
         HEX   (10,R3),(4,R2),LEN=2     MOVE IN OFFSET             T002
         C     R2,CURRITR               IS THIS THE CURRENT TRACE TAB
         BNE   *+8                      NO - GO ON WITH FORMATTING
         MVI   0(R3),C'>'               MOVE IN THE PTR TO CURR ENTRY
         LA    R3,15(R3)                UPDATE SCREEN PTR          T002
         LA    R2,ITRLEN(,R2)           UPDATE TABLE PTR           T002
         BCT   R4,ITRFMTLP              DO IT THRICE (OR 6)
         LA    R3,$I-5*15(,R3)          MAKE UP FOR ODDBALL SCREEN T002
         CLI   LINELEN+1,80             5 COLUMNS?                 U004
         BE    NEWLINE                  YES - DO NEXT LINE         U004
         LA    R3,39(,R3)               MAKE UP FOR ODDBALL SCREEN
         B     NEWLINE                  DO IT ALL OVER
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- >, <'
*
*****  >  *****
*
         USNGX *,R9                                                U021
FORWARD  L     R3,ITRAVAL               PICK UP PTR TO NEXT TRACE
         MVI   TRACE,NOTRACE            DON'T TRACE > FOR PETE'S SAKE
         CLI   0(R3),X'FF'              END OF TABLE?
         BNE   *+8                      NO - NO PROBLEM
         L     R3,AITRCTAB              YES - SO WRAP TO TOP       U004
         CLI   0(R3),X'FE'              EXIST YET?                 U004
         LA    R2,INVFMSG               GET MSG ADDR IN CASE       U004
         BE    BOTCH                    NO - TELL HIM NO GOOD      U004
         LA    R4,ITRLEN(,R3)           PT TO NEXT ONE AFTER THAT  T003
         L     R9,=A(BACKWARD)                                     U021
         DROPX R9                                                  U021
         USNGX BACKWARD,R9                                         U021
         B     CARET                    FINISH ALL THE REST
         SPACE 1
         DROPX R9                                                  U021
         SPACE 2
*
*****  <  *****
*
         USNGX *,R9                                                U021
BACKWARD MVI   TRACE,NOTRACE            DON'T TRACE A < EITHER
         L     R2,AITRCTAB              PT TO TRACE TABLE          U004
         L     R3,ITRAVAL               PT TO NEXT CURRENT ENTRY
         CR    R3,R2                    CURRENT ENTRY = FIRST ENTRY?
         BNE   *+8                      NO - NO PROBLEM (YET)
         L     R3,AITREND               YES - MUST LOOP TO TAB END U004
         SH    R3,=Y(ITRLEN)            BACKTRACK ONE ENTRY        T002
         LR    R4,R3                    SAVE FOR LATER ITRAVAL
         CR    R3,R2                    IS THAT THE BEGINNING NOW?
         BNE   *+8                      NO - NO MORE WORRIES
         L     R3,AITREND               YES - WE LOOP TO ENDTAB    U004
         SH    R3,=Y(ITRLEN)            BACKTRACK ONE ENTRY        T002
         CLI   0(R3),X'FE'              DOES THAT ENTRY EXIST?     U004
         BNE   CARET                    YES - WE'RE OK
         LA    R2,INVBMSG               NO - TELL HIM SO AND
         B     BOTCH                    GO TELL HIM
         SPACE 2
*
*  DO THE CARET.  IF LEAVING BLOCK, CHECK TO SEE OF BLK REPLACED.
*
CARET    CLC   TTR,0(R3)                NEW BLOCK NEEDED?          T002
         BE    RDNOTNEC                 NO, DON'T BOTHER WITH CHECK
         BAL   R8,CHKPT                 YES, MAKE SURE THIS ONES OK
         MVC   TTR,0(R3)                NEW BLK - GET CORRECT TTR  T002
         ST    R3,TEMP                  SAVE PTR TO THE ENTRY
         BAL   R8,READBLK               READ THE BLOCK IN
         L     R3,TEMP                  PICK UP THE PTR BACK
         SPACE 1
RDNOTNEC ST    R4,ITRAVAL               NEW TRACE TABLE PTR
         ST    R3,CURRITR               SAVE CURRENT TRACE TAB ENTRY
         MVC   TEMP(2),4(R3)            GET OFFSET FROM TRC TAB    T002
         LH    R15,TEMP                 INTO RIGHT REG             U004
         B     SETPOINT                 GO FAKE A '+' COMMAND
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- T, P, %'             U004
*
*****  T  *****
*
         USNGX *,R9                                                U021
TRACK    BAL   R8,CHKPT                 ANY ZAP NEEDED?            T002
         LTR   R0,R0                    TEST OPERAND LENGTH        U004
         BNZ   MORET                    NOT JUST 'T'
         LH    R15,TTR                  JUST A T (T=T+1)
         LA    R15,1(R15)               POINT TO NEXT TRACK
         B     POSTRACK                 GO DO IT
MORET    LH    R15,TTR                  PICK UP '*' TRACK
         BAL   R8,CALLEXP               PARSE & CHECK              U004
         BM    TTRLT1                   NEGATIVE IS BAD            U004
POSTRACK STH   R15,TTR                  SAVE TRACK #
         MVI   TTR+2,X'01'              GIVE HIM REC=01
         B     NEW$READ                 READ AND DISPLAY
         SPACE 1
         DROPX R9                                                  U021
         SPACE 7
*
*****  P  *****
*
         USNGX *,R9                                                U021
POINT    BAL   R8,CHKPT                 ANY ZAP NEEDED?
         LTR   R0,R0                    TEST OPERAND LENGTH        U004
         BNZ   MOREP                    MORE THAN P - GET TTR
         AIF   (NOT &VSAM).NVSAM13                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    POINTV                   YES                        T002
.NVSAM13 ANOP  ,                                                   T002
         MVC   TTR(4),=XL4'00000100'    JUST 'P', SET TTR=000001   T002
         B     NEW$READ                 DO IT
MOREP    BAL   R6,TTRPARSE              GET THE TTR SPECIFIED      U004
         AIF   (NOT &VSAM).NVSAM14                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    MOREPV                   YES                        T002
.NVSAM14 ANOP  ,                                                   T002
         MVC   TTR(3),TEMP+1            MOVE IN TTR
         B     NEW$READ                 GO GET IT
         AIF   (NOT &VSAM).NVSAM15                                 T002
POINTV   XC    TTR,TTR                  RESET RBA TO ZERO          T002
         B     NEW$READ                 SKIP UPDATE OF RBA         T003
MOREPV   MVC   TTR,TEMP                 GET RBA OF EXP             T002
         B     NEW$READ                 GO GET IT                  T002
.NVSAM15 ANOP  ,                                                   T002
         SPACE 1
         DROPX R9                                                  U021
         SPACE 4
*
*****  MTTR  *****
*
         USNGX *,R9                                                U023
MTTR     BAL   R8,CHKPT                 ANY ZAP NEEDED?            U023
         LTR   R0,R0                    TEST OPERAND LENGTH        U023
         BNZ   MOREMTTR                 MORE THAN MTTR - GET TTR   U023
         MVC   TTR,=XL4'00000100'       JUST "MTTR" - GO TO START  U023
         B     NEW$READ                 DO IT                      U023
         SPACE 1
MOREMTTR BAL   R6,TTRPARSE              GET THE OPERAND            U023
         MVC   TTR(3),TEMP+1            MOVE IN TTR                U023
         L     R14,DCBDVTBL-1+DCBU-IHADCB  -> DEV CHAR TBL ENTRY   U023
         L     R15,DCBDEBAD+DCBU-IHADCB @ DEB                      U023
         USNGX DEBDS,R15                                           U023
         LH    R0,DEBSTRCC              FIRST EXTENT CYL BEGIN     U023
         MH    R0,2(,R14)               * # OF TRKS/CYL            U023
         AH    R0,DEBSTRHH              + 1ST EXTENT TRK BEGIN     U023
         DROPX R15                                                 U023
         LH    R1,TTR                   GET TRACK SPECIFIED        U023
         SR    R1,R0                         ON VOLUME             U023
         STH   R1,TTR                           (FOR JES2 SPOOL)   U023
         B     NEW$READ                 GO GET IT                  U023
         SPACE 1
         DROPX R9                                                  U023
         SPACE 4
*
*****  %  *****                                                    U004
*
         USNGX *,R9                                                U021
INDPOINT LH    R15,OLDPOINT             GET BUFFER OFFSET          U004
         MVI   EXPOPT,YESSYMB           WANT SYM TAB LOOKUP        U004
         BAL   R8,CALLEXP               EVALUATE OPERAND           U004
         BM    INVEXPER                 NEG IS INVALID             U004
         LH    R0,BLKLEN                GET BLOCK LENGTH           U004
         SH    R0,=H'3'                 LAST POSSIBLE TTR          U004
         AIF   (NOT &VSAM).NVSAM16                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BZ    *+6                      NO WE CAN GO FURTHER       T002
         BCTR  R0,0                     RBA'S ARE 4 BYTES LONG     T002
.NVSAM16 ANOP  ,                                                   T002
         CR    R15,R0                   PAST END?                  T002
         BH    INVEXPER                 YES - ERROR                U004
         A     R15,ADDRBUFF             GET ADDR IN BUFFER         U004
         BAL   R8,CHKPT                 CAN HE LEAVE?              U004
         MVC   TTR,0(R15)               SET NEW TTR                T002
         B     NEW$READ                 GO READ IT & DISPLAY       U004
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- ABS'
*
*****  ABS *****
*
         USNGX *,R9                                                U021
ABS      BAL   R8,CHKPT                 SEE IF HE CAN LEAVE        T002
         LTR   R15,R0                   TEST OPERAND LENGTH        T002
         BNP   INVEXPER                 IT'S BAD                   U004
         CH    R15,=H'10'               CCHHR TOO LONG?
         BNH   *+8                      NO, OK
         LH    R15,=H'10'               YES, TRUNCATE
         EX    R15,ABSTR                TRANSLATE TO "HEX", NO EX LEN
         EX    R15,ABSPACK              GET 000CCHHR        NO EX LEN
         BAL   R8,ABSCONV               FIND 'M'                   U019
         B     NEW$READ                 GO READ/DISPLAY            U019
         SPACE 2
*
*****  SUBROUTINE TO CONVERT TO TTR
*
*  FIRST FIND 'M' FOR MBBCCHHR
         SPACE 1
ABSCONV  L     R15,DCBDEBAD-IHADCB+DCBU GET DEB
         XR    R1,R1                    START WITH M=0
         XR    R0,R0                    FOR IC
         IC    R0,DEBNMEXT-DEBDS(,R15)  GET NUMBER OF EXTENTS
ABSLOOP  CLC   DWD+3(4),DEBSTRCC-DEBDS(R15)   CCHH : START CCHH
         BL    ABSNEXT                  NOT IN THIS EXTENT
         CLC   DWD+3(4),DEBENDCC-DEBDS(R15)   CCHH : END   CCHH
         BNH   ABSGOT                   NOT IN THIS EXTENT
ABSNEXT  LA    R15,16(,R15)             NEXT EXTENT
         LA    R1,1(,R1)                NEXT 'M'
         BCT   R0,ABSLOOP               NEXT EXTENT
         BAL   R8,CLEAR                 CLEAR THE SCREEN FIRST     U004
         MVC   TTR(3),=X'FFFFFFFFFF'    SET UP A BAD TTR           U004
         MVC   CCHHR(5),=X'FFFFFFFFFF'  HERE TOO                   U004
         MVI   IOERROR,YESSYN           SAY "IOERROR"              U004
         XC    BLKLEN,BLKLEN            NOTHING TO DISPLAY         U004
         MVC   LINE05+17(40),IOERRMSG   MOVE IN MASK               U004
         MVC   LINE05+35(15),=CL15'NOT IN DATA SET'                U004
         B     DISPLYNT                 GO SAY BOTCHED             U004
         SPACE 1
         DROPX R9                                                  U021
         SPACE 1
ABSGOT   STC   R1,DWD                   SET 'M'
ABSGOT1  STM   R9,R12,56(R13)           SAVE REGS CONVERT KILLS
         L     R1,DCBDEBAD-IHADCB+DCBU  GET DEB AGAIN
         LA    R2,DWD                   GET 'MBBCCHHR'
         LR    R3,R13                   SAVE R13 TOO
         L     R15,CVTPTR               CVT
         L     R15,CVTPRLTV-CVT(,R15)   GET ABS=>REL CONVERT
         BALR  R14,R15                  GO GET TTRZ
         LR    R13,R3                   RESTORE R13
         LM    R9,R12,56(R13)           RESTORE OTHER REGS
         ST    R0,TTR                   SAVE TTR
         BR    R8                       RETURN TO CALLER           U019
         SPACE 1
ABSTR    TR    TEMP2+3(0),TRHEX         << EXECUTED >>             U004
ABSPACK  PACK  DWD(9),TEMP2+3(0)        << EXECUTED >>
         TITLE 'ZAP --- COMMAND EXECUTION --- B, R'                U004
*
*****  B  *****
*
         USNGX *,R9                                                U021
BACK     BAL   R8,CHKPT                 ANY ZAP NEEDED?
         AIF   (NOT &VSAM).NVSAM17                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    BACKV                    YES, BACK VSAM STYLE       T002
.NVSAM17 ANOP  ,                                                   T002
         OI    FLAGS2,NOEOF             TREAT EOF'S AS DATA BLKS   T002
         XR    R15,R15                  CLEAR FOR IC               U004
         IC    R15,TTR+2                GET R                      U004
         SH    R15,=H'1'                BACK IT UP 1               U004
         L     R9,=A(RECORD)            GET HIS LOCAL BASE         U021
         DROPX R9                                                  U021
         USNGX RECORD,R9                                           U021
         B     RBACK                    GO CHECK IT                U004
         AIF   (NOT &VSAM).NVSAM18                                 T002
         DROPX R9                                                  U021
         USNGX BACK,R9                                             U021
BACKV    LH    R15,=H'-1'               DEFAULT IS ONE BACK        T002
         L     R9,=A(RECORD)            GET HIS LOCAL BASE         U021
         DROPX R9                                                  U021
         USNGX RECORD,R9                                           U021
         B     RECBACKI                 GO GET LAST BLOCK          T002
.NVSAM18 SPACE 1
         DROPX R9                                                  U021
         SPACE 6
*
*****  R  *****
*
         USNGX *,R9                                                U021
RECORD   BAL   R8,CHKPT                 ANY ZAP NEEDED?
         AIF   (NOT &VSAM).NVSAM19                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    RECORDV                  YES, MOVE VSAM STYLE       T002
.NVSAM19 ANOP  ,                                                   T002
         TM    FLAGCOM,COMPO            ARE WE IN A PDS?           T002
         BNO   *+8                      NO - TELL HIM ABOUT EOF'S  U018
         OI    FLAGS2,NOEOF             YES - TREAT EOF'S AS DATA  T002
         LTR   R0,R0                    TEST OPERAND LENGTH        U004
         BP    MORER                    NOT JUST 'R'
         OI    FLAGVSAM,SEQREAD         GO TO NEXT RECORD          T002
         B     NEW$READ                 READ AND DISPLAY
         SPACE 1
MORER    XR    R15,R15                  CLEAR FOR IC
         IC    R15,TTR+2                GET CURRENT RECORD
         BAL   R8,CALLEXP               PARSE & CHECK              U004
         SPACE 1
RBACK    XR    R1,R1                    CLEAR FOR IC
         IC    R1,TTR+2                 GET CURRENT RECORD
         LR    R3,R15                   PRESERVE ABSOLUTE FOR BACK
         SR    R3,R1                    FIND OFFSET
         LR    R5,R15                   SAVE IT
* R3 IS RELATIVE OFFSET
* R5 IS ABSOLUTE VALUE
         BZ    NEW$READ                 IF STILL HERE, REREAD
         BM    RGOBACK                  IF NEGATIVE, BACK UP
         SPACE 2
*
*  OFFSET IS POSITIVE: READ FORWARD ENOUGH TIMES
*
RGOFORW  NI    FLAGS2,X'FF'-NOEOF       WE WANT TO STOP AT EOF FOR T003
         BAL   R8,READNBLK              READ NEXT BLOCK            U004
         CLI   IOERROR,YESSYN           WAS THERE AN I/O ERROR?
         BE    NEWDSPNT                 YES - CUT THIS OUT
         BCT   R3,RGOFORW               GO FORWARD
         B     NEW$READ                 WHEN DONE, DISPLAY IT
*        TITLE 'ZAP --- COMMAND EXECUTION --- R'                   T002
*
*  OFFSET IS NEGATIVE: READ BACKWARD ENOUGH TIMES
*
RGOBACK  LTR   R5,R5                    WAS ABSOLUTE ON THIS TRACK?
         STC   R5,TTR+2                 ASSUME YES - SET NEW RECORD
         BP    NEW$READ                 YES - READ IT              U004
*  BACK UP A TRACK
         LH    R0,TTR                   GET TRACK NUMBER
         BCTR  R0,0                     MINUS ONE
         LTR   R0,R0                    BACK TO ZERO?
         BNM   RCOUNT                   ITS GOOD, COUNT TRACK
         MVC   TTR(4),=XL4'00000100'    BACKED UP TOO FAR
         BAL   R8,SETMSG                MOVE MSG TO RIGHT PLACE    U004
         DC    Y(TTRSMALL-ZAP)          'TTR < 000001 INVALID'     U004
         B     NEW$READ                 DISPLAY FIRST RECORD
RCOUNT   STH   R0,TTR                   SAVE FOR COUNT
         MVI   TTR+2,0                  IT WILL INCREMENT
         MVC   TEMP(3),TTR              SAVE TTR
         SPACE 1
BCKCNT   BAL   R8,READNBLK              GET A BLK                  U004
         CLC   TEMP(2),TTR              STILL THIS TRACK?
         BNE   RNEWTRK                  NO
         CLI   IOERROR,YESSYN           I/O ERROR?
         BE    NEWDSPNT                 GET OUTA HERE
         MVC   TEMP+2(1),TTR+2          SAVE THIS RECORD
         B     BCKCNT                   CONTINUE DOWN TRACK
RNEWTRK  MVC   TTR(2),TEMP              RESTORE PREVIOUS TRACK
         XR    R1,R1                    CLEAR FOR PICKUP
         IC    R1,TEMP+2                GET NUMBER OF RECS
         AR    R5,R1                    WE NOW ARE THIS FAR BACK
         B     RGOBACK                  GO SEE IF ITS ON THIS TRK
         SPACE 1
         AIF   (NOT &VSAM).NVSAM20                                 T002
RECORDV  OI    FLAGVSAM,SEQREAD         ASSUME FORWARD FOR EASE    T002
         BAL   R8,VSAMTSEQ              START FORWARD PLEASE       T003
         LTR   R0,R0                    ANY EXP TO PARSE?          T002
         BZ    NEW$READ                 NO, GO READ NEXT SEQ BLOCK T002
         XR    R15,R15                  NO INITIAL OFFSET          T002
         BAL   R8,CALLEXP               ANALYZE EXPRESSION         T002
RECBACKI LTR   R3,R15                   GET COUNT OF RECORDS       T002
         BZ    NEW$READ                 NONE, GET NEXT RECORD      T002
         TM    FLAGVSAM,VSAMKEY         KEY SEQUENCE?              T002
         BZ    RECORDC                  NO, GET THERE DIRECTLY     T002
         BL    RBACKV                   IF NEGATIVE, GO BACKWARDS  T002
         BCT   R3,*+8                   -1 FOR NEW$READ TO GET IT  T002
         B     NEW$READ                 GET THE NEXT BLOCK         T002
         BAL   R8,READNBLK              SKIP THIS BLOCK            T002
         BCT   R3,*-4                   SKIP N-1 BLOCKS            T002
         OI    FLAGVSAM,SEQREAD         READ THE NEXT SEQ          T002
         B     NEW$READ                 GET THE BLOCK TO DISP      T002
         SPACE 3
RBACKV   LPR   R3,R15                   SAVE POS COUNT FROM MODCB  T002
         BAL   R8,VSAMTSQB              SEQ BACKWARDS?             T002
         BCT   R3,*+8                   LET NEW$READ GET LAST REC  T002
         B     NEW$READ                 JUST ONE TO BACKSPACE      T002
         BAL   R8,READBLK               SKIP A RECORD              T002
         BCT   R3,*-4                   LESS RECORD JUST READ      T002
         B     NEW$READ                 GO READ THE LAST ONE       T002
         SPACE 1
RECORDC  L     R1,TTR                   GET CURRENT RBA            T002
         MH    R3,VSAMCISZ              GET RBA OFFSET             T002
         AR    R1,R3                    GET NEW RBA TO GET TO.     T002
         BM    RECORDNR                 LESS THAN ZERO, NOT HERE   T002
         ST    R1,TTR                   SAVE NEW RBA               T002
         CLC   TTR,ENDTTR               OUT OF CLUSTER?            T002
         BH    RECORDNR                 YES, FAKE BAD POINT        T002
         B     NEW$READ                 NO, GET THE RECORD         T002
         SPACE 3
RECORDNR LA    R1,=CL15'NOT IN CLUSTER'                            T002
         B     EXCPERR                  FAKE I/O ERROR             T002
.NVSAM20 ANOP  ,                                                   T002
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- L, F'
*
*****  F  *****
*
FIND     OI    FLAGS2,NOEOF             SET SO EOF'S COME BACK     T002
         LA    R9,LOCATE-FIND(,R9)      FUDGE BASE REGISTER        U021
         SPACE 3
*
*****  L  *****
*
         USNGX *,R9                                                U021
LOCATE   LTR   R3,R0                    TEST OPERAND LENGTH        U004
         BZ    LGO                      JUST L, GO CONTINUE        U004
         LR    R4,R1                    PT TO STRING               U004
         LA    R5,LOOKFOR               POINT TO STRING SAVEAREA
         BAL   R6,GETSTRNG              GET THE HEX OR CHAR STRING
         B     AROUND1
*
*  CONTINUE SCAN
*
LGO      TM    CONTINUE,YESCONTL        CAN WE CONTINUE SCAN?
         LA    R2,LINMSG                ASSUME NO - PT TO MSG
         BNO   BOTCH                    NO - NO SCAN WAS STARTED   U004
*  CONTINUE THE SCAN
         LH    R3,LOOKFOR               YES - GET THE SCAN LEN     U004
*
*  SET UP SCAN
*
AROUND1  XR    R2,R2                    CLEAR REG FOR IC (WHAT ELSE?)
         IC    R2,LOOKFOR+2             GET 1ST CHAR TO FIND       U004
         LA    R2,TEMPTRT(R2)           PT TO OFFSET IN TRTAB
         XC    TEMPTRT(256),TEMPTRT     CLEAR TRTAB
         MVI   0(R2),X'FF'              MOVE IN FF THERE
         OI    CONTINUE,YESCONTL        SAY HE CAN CONTINUE SCAN NOW
         LH    R1,OLDPOINT              GET CURRENT OFFSET         U004
         CLI   REALRDLN+1,1             JUST 'L' ENTERED?          U004
         BNE   *+8                      NO - START FROM HERE       U004
         LA    R1,1(,R1)                YES - START FROM NEXT BYTE U004
         B     LOOK8                    GO ON
         EJECT
*
*  BLOCK SCAN LOOP
*
LOOK7    SR    R1,R1                    RESET BUFFER PTR
LOOK8    A     R1,ADDRBUFF              PT TO OFFSET IN BUFFER
         BCTR  R1,0                     -1
         LH    R5,BLKLEN                GET BLK LENGTH
         A     R5,ADDRBUFF              PT TO END OF BLK
*
*  DO THE SCAN
*
LOOKLOOP LR    R2,R5                    GET END                    U009
         SR    R2,R1                    COMPUTE LENGTH             U009
         SH    R2,=H'2'                 GET LENGTH OF REMAINDER    U009
         BM    TOOFAR                   NOT ENOUGH                 U009
         EX    R2,SCANTRT               FIND INTERESTING STRING    U009
         BZ    LOOK1                    NOT YET - KEEP TRYING
         CR    R1,R5                    YES - BUT IS IT PAST THE BLK?
         BNL   TOOFAR                   YES - SO GET A NEW ONE
         EX    R3,SCANCLC               NO - IS ALL OF IT THERE?
         BNE   LOOKLOOP                 NO - KEEP CHECKING
         MVC   LINE19+17(40),=CL40'        *****  SCAN MATCH  *****'
         S     R1,ADDRBUFF              YES - GET WHERE IT IS AND...
         LR    R15,R1                   INTO RIGHT REG             U004
         B     SETPOINT                 FAKE A '+' COMMAND TO IT
SCANTRT  TRT   1(0,R1),TEMPTRT          <<< EXECUTED >>>           U009
SCANCLC  CLC   0(0,R1),LOOKFOR+2        <<< EXECUTED >>>           U004
LOOK1    N     R2,=XL4'FF'              GET CLEAN SINGLE BYTE      U009
         LA    R1,1(R1,R2)              SET LOCATION TO TRY AGAIN  U009
         CR    R1,R5                    PAST BLK?
         BL    LOOKLOOP                 NO - KEEP SCANNING
         SPACE 1
TOOFAR   BAL   R8,CHKPT                 ANY ZAP NEEDED?
TOOFAR2  BAL   R8,READNBLK              GET NEXT BLK FOR SCANNING  U004
         CLI   IOERROR,YESSYN           I/O ERROR?
         BE    NEWDSPNT                 YES - GET OUT
         CLI   DSNAME,X'04'             VTOC?                      U012
         BE    TOOFAR4                  YES - DIFFERENT CHECK      U012
         AIF   (NOT &VSAM).NVSAM31                                 T002
         TM    FLAGVSAM,VSAMOPEN        VSAM CLUSTER?              T002
         BO    TOOFARV                  YES - DIFFERENT CHECK      T003
.NVSAM31 CLC   TTR(3),ENDTTR            HAVE WE ARRIVED?           U012
         BE    TOOFAR3                  YES - PAUSE                U012
NOT2FAR  OC    BLKLEN,BLKLEN            ANYTHING HERE? (EOF = 'F') U004
         BZ    TOOFAR2                  NO - GET NEXT BLOCK        U004
         B     LOOK7                    GO SCAN IT THEN
         SPACE 1
TOOFAR3  MVC   LINE19+22(33),=C'*****  HOLDING AT DS1LSTAR  *****' U012
         B     SETPNT00                 DISP OFF 0, CURR BLK       U012
         SPACE 2
TOOFAR4  CLC   CCHHR(5),LASTFMT1        IS THIS THE LAST FMT1?     U012
         BNE   NOT2FAR                  NO - CONTINUE SCAN         U012
         MVC   LINE19+22(33),=C'*****  HOLDING AT LASTFMT1  *****' U012
         B     SETPNT00                 DISP OFF 0, CURR BLK       U012
         SPACE 1
         AIF   (NOT &VSAM).NVSAM51                                 T002
TOOFARV  L     R1,ENDTTR                GET HIGH RBA               T002
         XR    R0,R0                    CLEAR FOR DIVISION         T002
         LH    R15,VSAMCISZ             GET CISIZE                 T002
         DR    R0,R15                   CI NUMBER                  T002
         MR    R0,R15                   NOW HAVE CI CONTAINING MAX T002
         C     R1,TTR                   IS IT THIS RBA?            T002
         BNE   NOT2FAR                  NO - CONTINUE SCAN         T002
         MVC   LINE19+22(33),=C'*****  HOLDING AT HIGH RBA  *****' T002
         B     SETPNT00                 DISP OFF 0, CURR BLK       T002
         SPACE 1
.NVSAM51 DROPX R9                                                  T002
         TITLE 'ZAP --- COMMAND EXECUTION --- S, X, O, N'
         SPACE 3
*
*****  N  *****
*
AND      LA    R9,SXON-*(,R9)           FUDGE BASE REGISTER        U021
         BALR  R2,R9                    PT TO EX INSTR, GO DO IT   U004
         NC    0(0,R5),ZAPSTRNG+2       <<< EXECUTED >>>    'N'    U004
         SPACE 3
*
*****  O  *****
*
OR       LA    R9,SXON-*(,R9)           FUDGE BASE REGISTER        U021
         BALR  R2,R9                    PT TO EX INSTR, GO DO IT   U004
         OC    0(0,R5),ZAPSTRNG+2       <<< EXECUTED >>>    'O'    U004
         SPACE 3
*
*****  X  *****
*
EXOR     LA    R9,SXON-*(,R9)           FUDGE BASE REGISTER        U021
         BALR  R2,R9                    PT TO EX INSTR, GO DO IT   U004
         XC    0(0,R5),ZAPSTRNG+2       <<< EXECUTED >>>    'X'    U004
         SPACE 3
*
*****  S  *****
*
STORE    LA    R9,SXON-*(,R9)           FUDGE BASE REGISTER        U021
         BALR  R2,R9                    PT TO EX INSTR, GO DO IT   U004
         MVC   0(0,R5),ZAPSTRNG+2       <<< EXECUTED >>>    'S'    U004
         EJECT
*  ALLOW HIM TO USE PREVIOUS STRING IF HE JUST ENTERS THE COMMAND
*  WITH NO SUBSEQUENT STRING OPERAND.
         SPACE 5
         USNGX *,R9                                                U021
SXON     ST    R2,INSTRSAV              SAVE THE EX INSTR PTR      T003
         LTR   R3,R0                    TEST OPERAND LENGTH        U004
         BP    SXONNEW#                 HAVE AN OPERAND - USE IT   U004
         SPACE 2
*  USE AN OLD STRING FOR S,X,O,N WITH NO OPERANDS
         SPACE 1
         TM    CONTINUE,YESCONTS        JUST 'S'.  OLD STRING YET?
         BO    SXONNEW                  YES, GO ON
         B     INVEXPER                 SAY SYNTAX ERROR COL 2     U004
         SPACE 2
*  USE SPECIFIED STRING
         SPACE 1
SXONNEW# LR    R4,R1                    POINT TO STRING OPERAND    U004
         LA    R5,ZAPSTRNG              POINT TO STRING WORK AREA
         BAL   R6,GETSTRNG              GET AL2(LEN),C'STRING'
         SPACE 1
*  AT THIS POINT, ZAPSTRNG CONTAINS THE INFO NECESSARY TO STORE:
*        AL2(STRING_LENGTH),C'STRING'
         SPACE 1
SXONNEW  OC    BLKLEN(2),BLKLEN         ANYTHING TO CHANGE HERE?
         BZ    INVALCOM                 NO - TELL HIM              U004
         CLI   GODFLAG,NOGOD            CAN HE EVER UPDATE?
         BNE   LETSTORE                 YES - SO GO ON
         BAL   R8,SETMSG                MOVE MSG TO RIGHT PLACE    U004
         DC    Y(STORMSG-ZAP)           TELL HIM NOT UPDATING      U004
LETSTORE LH    R4,ZAPSTRNG              GET LEN                    U004
         LH    R5,OLDPOINT              PT TO ZAPPED OFFSET
         EJECT
*
*  CHECK TO SEE IF STRING WILL FIT IN THE REMAINING PART OF THE BLK
*  WHERE HE IS POINTING
*
         SPACE 2
         LH    R6,BLKLEN                GET LENGTH OF BLK
         SR    R6,R5                    GET LENGTH OF PIECE REMAINING
         CR    R6,R4                    IS IT SMALLER THAN THE STRING?
         LA    R2,LENERR                ASSUME STRING IS INVALID LENGTH
         BNH   BOTCH                    YES - SO GO TELL HIM       U004
*  STRING WILL FIT
         A     R5,ADDRBUFF              PT TO ACTUAL ZAPPED DATA
         OI    CONTINUE,YESCONTS        SAY HE CAN DO 'SXON' NO OPRND
         LA    R1,N1                    MESSAGE NUMBER
         BAL   R8,SETLINE               SET UP THE LINE
         B     NOMSGEX                  NOT PRINTING
         USNGX LOGLINE,R2                                          U022
         CLI   CHNGED,0                 VIRGIN BUFFER?
         BNE   *+8                      NO, SINGLE SPACE
         MVI   LL@CC,C'0'               ELSE, DOUBLE SPACE 1ST CHG U022
         MVC   LL@OFFC6,=C'OFFSET'                                 U022
******%% MVC   ??(27),=C'OFFSET XXXX ADDR XXXXX DATA'    SOMEDAY
         HEX   LL@OFF,OLDPOINT,LEN=2    CONVERT OFFSET TO THE LINE U022
         MVC   LL@DATC4,=C'DATA'                                   U022
         BAL   R8,SETSTRNG              CONVERT STRING TO LINE AS WAS
         AIF   (NOT &VSAM).NVSAM21                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    SXONPUT                  YES, SKIP CCHHR            T002
.NVSAM21 ANOP  ,                                                   T002
         MVC   LL@CHRC5,=C'CCHHR'       MOVE IN IDENTIFIER         U022
         HEX   LL@CCHHR,CCHHR,LEN=5     GET THE CCHHR              U022
SXONPUT  BAL   R8,CPUT                                             T002
         LA    R1,N1                                               U004
         BAL   R8,SETLINE                                          U004
         EX    0,*                                                 U004
         MVC   LL@TTRC3(12),LL@TTRC3-1  BLANK THE TTR THIS TIME    U022
         MVC   LL@2BC2(16),=C'TO BE CHANGED TO'                    U022
         L     R15,INSTRSAV             RESTORE EX INS PTR         T003
         EX    R4,0(,R15)               CHANGE DATA IN BUFFER      T003
         BAL   R8,SETSTRNG              RECORD THE CHANGE
         BAL   R8,CPUT                  PUT THE LINE
         DROPX R2                                                  U022
         MVI   CHNGED,255               MARK AS CHANGED FOR LATER
         OI    FLAGS2,MUSTZAP           BLK NEEDS A ZAP NOW
         MVC   OLDTTR(3),TTR            COPY FOR PRINTING
         B     NEWDSPNT                 THEN RETURN TO HIM         U004
         SPACE 2
NOMSGEX  L     R2,INSTRSAV              RESTORE EX INSTRUCTION PTR T003
         EX    R4,0(,R2)                MAKE THE CHANGE            T003
         OI    FLAGS2,MUSTZAP           BLK NEEDS A ZAP NOW        U004
         B     NEWDSPNT                 GIVE IT TO HIM
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- SET'
*
*****  SET  *****
*
         USNGX *,R9                                                U021
SET      LTR   R3,R0                    TEST OPERAND LENGTH        U004
         BP    SETNEW#                  THERE IS ONE               U004
         SPACE 1
*  USE OLD STRING FOR SET NO OPERANDS
         SPACE 1
         TM    CONTINUE,YESCONTT        IS THERE AN OLD ONE TO USE?
         BO    SETNEW                   YES - USE IT               U004
         B     INVEXPER                 INVALID SYNTAX COL 4       U004
         SPACE 2
INVALCOM LA    R2,INVCOM                PT TO MSG                  U021
         B     BOTCH                    AND TELL HIM               U021
         SPACE 2
*  USE SPECIFIED STRING
         SPACE 1
SETNEW#  LR    R4,R1                    -> TO THE STRING OPERAND   U004
         LA    R5,SETSTR                WHERE TO SAVE THE INFO
         BAL   R6,GETSTRNG              GET AL2(LEN),C'STRING'
         SPACE 3
*  NOW SETSTR CONTAINS:
*        AL2(SET_LEN),C'SET_STRING'
         SPACE 1
SETNEW   OC    BLKLEN(2),BLKLEN         IS THERE ANYTHING TO SET?
         BZ    INVALCOM                 NO - SO DON'T AND TELL HIM
         CLI   GODFLAG,NOGOD            CAN HE EVER UPDATE?
         BNE   LETSET                   YES - WE HAVE NOTHING TO SAY
         BAL   R8,SETMSG                MOVE MSG TO RIGHT PLACE    U004
         DC    Y(STORMSG-ZAP)           TELL HIM NOT UPDATING      U004
LETSET   LH    R4,SETSTR                GET BCTR'D STRING LEN      U004
         LA    R5,SETSTR+2              POINT TO STRING            U004
         LA    R1,N2                    MSG NUMBER
         BAL   R8,SETLINE               SET UP LINE
         B     NOSETMSG                 OOPS, NOT PRINTING
         USNGX LOGLINE,R2                                          U022
         CLI   CHNGED,0                 VIRGIN BUFFER?
         BNE   *+8                      NO, SINGLE SPACE
         MVI   LL@CC,C'0'               ELSE, DOUBLE SPACE 1ST CHG U022
         MVC   LL@XMSG(28),=C'FOLLOWING BLOCK TO BE SET TO'        U022
         DROPX R2                                                  U022
         BAL   R8,SETSTRNG              CONVERT THE STRING
         BAL   R8,CPUT                  DUMP THE LINE NOW
         MVI   CHNGED,255               ITS CHANGED NOW
         MVC   OLDTTR(3),TTR            SAVE TTR
         IC    R7,ENQIT                 PRESERVE FLAG              U004
         BAL   R6,PREPDMPX              GET READY FOR THE DUMP     U004
         BAL   R6,DUMPER                DUMP THE BLOCK             U004
         STC   R7,ENQIT                 RESTORE FLAG               U004
         LH    R4,SETSTR                RESTORE LENGTH             U004
         SPACE 1
NOSETMSG OI    CONTINUE,YESCONTT        SAY HE CAN SET NO OPRND NOW
         OI    FLAGS2,MUSTZAP           BLK NEEDS A ZAP NOW        U004
         L     R5,ADDRBUFF              PT TO BUFFER
         LH    R7,BLKLEN                GET LEN OF BLK IN BUFFER
         LA    R1,1(,R4)                GET REAL STRING LENGTH
         XR    R6,R6                    CLEAR REM REG FOR DIVIDE   U016
         DR    R7-1,R1                  GET # STRINGS FIT IN BLK   U016
         LTR   R7,R7                    BLK SHORTER THAN STRING?
         BZ    JUSTSPEW                 YES - JUST MOVE IN REMAINDER
*  R7 HAS BCT COUNT OF HOW MANY STRINGS CAN FIT IN THIS
*  PARTICULAR BLK, WHILE R6 HAS THE LENGTH OF THE PIECE LEFT OVER. U016
SETLOOP  EX    R4,SETMVC                MOVE 1 STRING IN
         LA    R5,1(R4,R5)              UPDATE BUFFER PTR FOR NEXT
         BCT   R7,SETLOOP               FILL AS MUCH OF BUFFER AS CAN
JUSTSPEW LTR   R6,R6                    IS A SMALL PIECE LEFT?     U016
         BZ    NEWDSPNT                 NO - WE ARE ALL DONE
         BCTR  R6,0                     YES - GET EXECUTE LEN      U016
         EX    R6,SETMVC                MOVE AS MUCH AS WILL FIT   U016
         B     NEWDSPNT                 AND GO DISPLAY WHAT WE DID
         SPACE 1
SETMVC   MVC   0(0,R5),SETSTR+2         <<< EXECUTED >>>           U004
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- ZAP/SAVE'
*
*****  ZAP/SAVE  *****
*
         USNGX *,R9                                                U021
REPLACE  CLI   IOERROR,YESSYN           WAS THERE AN I/O ERROR ON BLK?
         BE    WRTNONO                  YES - CAN'T ZAP THAT
         OC    BLKLEN(2),BLKLEN         NO - BUT IS THERE A BLK TO ZAP?
         BZ    WRTNONO                  NO - SO DON'T ZAP ALREADY
         TM    DCBLIST,X'0F'            UPDATING ALREADY?
         BNZ   THERE                    YES, CONTINUE
         CLI   GODFLAG,NOGOD            CAN HE CHANGE FROM INPUT TO UP
         BE    WRTNONO                  NO - TOUGH BEANS FOR HIM
         MVC   REP,BLANKS               CLEAR REPLY                U004
         MVC   REP(3),=C'ZAP'           RE-PROMPT THE RIGHT THING  U004
         CLOSE MF=(E,DCBLIST)           CLOSE THE DATASET SO THAT....
         MVI   DCBLIST,X'84'            WE CAN OPEN FOR UPDATE
         AIF   (NOT &VSAM).NVSAM22                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    REPACBOP                 YES, IT IS SPECIAL         T002
.NVSAM22 ANOP  ,                                                   T002
         OPEN  MF=(E,DCBLIST),TYPE=J    THEN DO IT ALREADY         U004
         TM    DCBU+48,X'10'            OPEN?
         BO    THERE#                   YES, OK
REPDIE   BCR   0,1                      DRAIN PIPELINE             T002
         EX    0,*                      THIS BETTER NEVER HAPPEN
         AIF   (NOT &VSAM).NVSAM23                                 T002
REPACBOP OI    ACBV+ACBMACR1-IFGACB,ACBOUT   OPEN FOR UPDAT        T002
         OPEN  MF=(E,DCBLIST)           REOPEN ACB                 T002
         TM    ACBV+ACBOFLGS-IFGACB,ACBOPEN  DID IT REOPEN?        T002
         BZ    REPDIE                   NEVER SHOULD HAPPEN        T002
         L     R2,ADDRDATA              -> DATA RECORD TO REPLACE  T002
         LR    R3,R2                    COPY PTR TO DUMMY AREA     T002
         AH    R3,BUFFSIZE              -> TO DUMMY RECORD AREA    T002
         ST    R3,RPLV+RPLAREA-IFGRPL   SAVE DUMMY ADDR FOR GET    T002
         OI    RPLV+RPLOPT2-IFGRPL,RPLUPD   RPL NOW DOES UPDATES   T002
         POINT RPL=RPLV                 -> TO RECORD TO ZAP        T002
         VSAMTST ,                                                 T002
         GET   RPL=RPLV                 GET RECORD TO UPDATE OURS  T002
         VSAMTST ,                                                 T002
         ST    R2,RPLV+RPLAREA-IFGRPL   RESTORE CORRECT ADDR       T002
.NVSAM23 ANOP  ,                                                   T002
         SPACE 1
THERE#   TM    FLAGS2,LOGF              SEE IF LOGGING
         BNO   THERE                    IF NOT, THEN NO MSG
         L     R1,CSOUTWK               POINT TO WORK AREA
         LTR   R1,R1                    SEE IF THERE
         BZ    THERE                    IF NOT, NO MSG
*        BAL   R8,OPENMSG               THE NEXT 3 INSTRS DO IT    T003
         LA    R8,THERE                 -> RETURN ADDR             T003
         ST    R8,LINKLOG               SAVE FOR OPENMSG RETURN    T003
         B     OPENMSG                  GO OPEN THE FILE           T003
THERE    BAL   R8,WRITE                 WRITE THE BUFFER OUT
         CLI   IOERROR,YESSYN           I/O ERROR?
         BE    NEWDSPNT                 YES - IGNORE TELLING HIM OK
         MVI   CHNGED,0                 BUFFER HAS BEEN CHECKPOINTED
         NI    FLAGS2,255-MUSTZAP       BLK NO LONGER NEEDS ZAP
         SPACE 2
*  UPDATE THE IDR IF APPLICABLE
*  IDR FOR SUPERZAP (AND THEREFORE ZAP) TAKES THE FOLLOWING FORM:
*        BYTE  0.....'80' FOR ID
*        BYTE  1.....LENGTH OF IDR (USUALLY X'FA' = 250.)
*        BYTE  2.....WHO'S IDR IT IS (X'01' FOR SUPERZAP AND ZAP)
*        BYTE  3.....NUMBER OF 13-BYTE IDR DATA ENTRIES (BITS 2-7)
*        BYTES 4-16, 17-29,... IDR DATA ENTRY AS FOLLOWS:
*              BYTES 0-1 --- ESD ID (DEFAULT 1)
*              BYTES 2-4 --- DATE ('YYDDDF')
*              BYTES 5-12 -- INFO ('TSO--UUU, WHERE UUU=ZAPPER'S UID)
         SPACE 2
         TM    FLAGCOM,COMPO            IS THIS A PDS?             T002
         BNO   FINZAP                   NO - FORGET THE IDR        U018
         CLI   DCBRECFM-IHADCB+DCBU,RECFMU  IS THIS A LIBRARY?
         BNE   FINZAP                   NO - DON'T UPDATE THE 'IDR'
         TM    FLAGESD,CHGIDR           ALREADY UPDATED THIS IDR   T002
         BO    FINZAP                   YES, DON'T DO IT AGAIN NOW T002
         MVC   TTRSAVE,TTR              LET'S SAVE WHERE HE WAS    T003
         OC    MEMTTR(3),MEMTTR         WAS THERE A 'M' COMMAND?
         BZ    ENDIDR                   NO - FORGET IT
         MVC   TTR(3),MEMTTR            PICK UP MEMBER PTR
         BAL   R8,READBLK               READ 1ST BLK OF MEMBER
         SPACE 1
IDRFIND  L     R1,ADDRBUFF              GET BUFFER PTR
         TM    0(R1),X'01'              CONTROL RECORD OF SOME SORT?
         BO    ENDIDR                   YES - WE ARE ALL DONE LOOKING
         CLI   0(R1),X'80'              IDR?
         BNE   NEWBLK                   NO - GET ANOTHER BLK
         TM    2(R1),X'01'              YES - BUT DONE BY SUPERZAP?
         BNO   NEWBLK                   NO - FORGET THIS ONE
         XR    R2,R2                    CLEAR WORK REG
         IC    R2,3(R1)                 PICK UP # OF IDR DATA ENTRIES
         LR    R4,R2                    SAVE IT FOR LATER
         N     R2,=XL4'0000003F'        WE JUST WANT BITS 2-7
         MH    R2,=H'13'                EACH ENTRY IS 13 BYTES LONG
         LA    R2,3(R2)                 IDR HDR IS 3 BYTES LONG
*  SO NOW R2 HAS AN OFFSET TO THE NEXT AVAILABLE POSITION IN THE IDR
         XR    R3,R3                    CLEAR WORK REG
         IC    R3,1(R1)                 PICK UP IDR LENGTH
         CR    R3,R2                    ARE WE AT END OF IDR?
         BH    UPDATIDR                 NO - SO UPDATE THE IDR
*
NEWBLK   OI    FLAGS2,NOEOF             TREAT EOF'S AS DATA BLKS   T002
         BAL   R8,READNBLK              READ ON ANOTHER BLK        U004
         CLC   LINE05+27(20),EOFMSG     DID I HIT END OF FILE?     U004
         BE    ENDIDR                   YES - STOP THIS STUPID THING
         CLI   IOERROR,YESSYN           I/O ERROR?
         BE    ENDIDR                   YES - LEAVE
         B     IDRFIND                  NO - CHECK THIS BLK
*
UPDATIDR AR    R2,R1                    R2 PTS TO NEXT IDR SPOT
         LA    R4,1(R4)                 INCREMENT # OF IDR ENTRIES...
         STC   R4,3(R1)                 AND PUT IT BACK
         MVC   1(2,R2),ESDID            MOVE IN ESDID (IF ANY)
*
         TIME  ,                        GET THE DATE IN R1
*
         ST    R1,DWD                   SAVE IT IN TEMP WORD
         MVC   3(3,R2),DWD+1            MOVE YYDDDF TO IDR
         MVI   6(R2),C'/'               INDICATE TSO USERID        U012
         MVC   7(7,R2),USERID           MOVE IN THE CULPRIT        U012
         BAL   R8,WRITE                 GO WRITE THE RECORD BACK   U012
         CLI   IOERROR,YESSYN           I/O ERROR?
         BE    ENDIDR                   YES - SAY WE DIDN'T UPDATE
         OI    FLAGESD,CHGIDR           NO - WE DID UPDATE THE IDR T002
         SPACE 1
ENDIDR   DC    0H'0'
         LA    R1,N6                    ASSUME NOT UPDATED
         TM    FLAGESD,CHGIDR           WAS THE IDR UPDATED?       T002
         BO    *+8                      NO - WE WERE RIGHT         T002
         LA    R1,N7                    DID UPDATE
         BAL   R8,SETLINE               SET UP OUTPUT LINE
         B     PUTBACK                  NO PRINT FILE - LEAVE
         USNGX LOGLINE,R2                                          U022
         MVI   LL@CC,C'0'               DOUBLE SPACE               U022
         MVC   LL@XMSG(25),=C'IDR FOR CSECT NOT UPDATED'           U022
         TM    FLAGESD,CHGIDR           WAS IT UPDATED?            T002
         BNO   PUTITOUT                 NO - MSG IS OK             T002
         MVC   LL@XMSG+14(3),EDMASK+9   YES - SO GIVE ESDID        U022
         LH    R0,ESDID                 GET ESDID (USUALLY)        U002
         CVD   R0,DWD                   IN DEC                     U002
         ED    LL@XMSG+13(4),DWD+6      ESDID TO MSG               U022
         DROPX R2                                                  U022
PUTITOUT BAL   R8,CPUT                  PUT MSG OUT
PUTBACK  CLC   TTR(3),TTRSAVE           HAS HE MOVED ANY?          T003
         BE    FINZAP                   YES - DON'T BOTHER PUTTING BACK
         NI    FLAGVSAM,X'FF'-SEQREAD   GET THE RIGHT ONE          T003
         MVC   TTR(3),TTRSAVE           LET'S PUT HIM BACK         T003
         BAL   R8,READBLK               HE'LL NEVER KNOW WHAT HAPPENED
FINZAP   MVC   LINE19+17(40),=CL40'      *****  BLOCK REPLACED  *****'
         MVI   ENQIT,255                FORCE ENQ NOW THAT ITS ZAPPED
         LA    R1,N3                    MSG NUMBER
         BAL   R8,SETLINE               SETUP THE LINE
         B     NEWDSPNT                 NOT PRINTING
         USNGX LOGLINE,R2                                          U022
         MVC   LL@XMSG(14),=C'BLOCK REPLACED'                      U022
         MVI   LL@CC,C'0'               DOUBLE SPACE THIS ONE      U022
         DROPX R2                                                  U022
         BAL   R8,CPUT                  DUMP THE LINE
*  TRUNCATE CURRENT LOG BLOCK, IN CASE OF SYSTEM/PROGRAM FAILURE
         L     R2,CSOUTWK               IT'S GOTTA BE HERE         U004
         MVI   0(R2),X'01'              INDICATE TRUNC             U004
         BAL   R8,CPUTRUNC              TRUNCATE THIS LOG BLOCK    U004
         B     NEWDSPNT                 YES - DISPLAY WITH NO TRACE
         SPACE 1
WRTNONO  LA    R2,WRITERR               TELL HIM WRITE NOT ALLOWED
         MVC   REP,BLANKS               CLEAR OUT REPLY BUFF AND   U004
*                                       SO DON'T TEMPT HIM TO RE-'ZAP'
         B     BOTCH                    AND DISPLAY
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- DISASM, ASM'         U004
*
*****  DISASM
*
DISASM   LH    R15,OLDPOINT             CURR OFFSET
         MVI   EXPOPT,YESSYMB           WANT SYM TAB LOOKUP
         BAL   R8,CALLEXP               PARSE & CHECK              U004
         BM    INVEXPER                 NEG IS BAD                 U004
         LH    R0,BLKLEN                GET LENGTH BLK
         SH    R0,=H'2'                 LAST POSS INSTRUCTION
         CR    R15,R0                   PAST END OF BLK?
         BH    INVEXPER
         A     R15,ADDRBUFF             GET ADDR IN BUFFER
         LNR   R1,R15                   NEG FOR 'DISASM'
         LA    R0,LINE19+24             GIVE HIM 33 BYTES TO WORK WITH
         LA    R2,TEMP2                 GIVE HIM SOME WORKAREA TOO
         L     R15,=V(ASMGASM)          POINT TO 'ASSEMBLER' HAR HAR
         BALR  R14,R15                  DISASSEMBLE IT
         LA    R2,INVOPCOD              ASSUME BOTCHUP
         BXH   R15,R15,BOTCH            YUP
         SPACE 1
         MVC   LINE19+17(7),=C'INSTR:'  NO, MOVE MASK
         B     NEWDSPNT                 AND SHOW IT OFF
         SPACE 6
*
*****  ASM
*
ASM      BNP   INVEXPER                 NO OPERAND - BAD           U004
         L     R15,=V(ASMGASM)          PT TO SUB
         BALR  R14,R15                  GET OBJECT
         SPACE 1
         LA    R2,INVOPCOD              ASSUME NO
         BXH   R15,R15,BOTCH
         MVC   LINE19+17(8),=C'OP CODE:'  MASK
         HEX   LINE19+27,(1,R1),LEN=1                              T002
         CLI   0(R1),C'2'               EXTENDED S370 OPCODE?      T002
         BNE   NEWDSPNT                 NO, JUST DISPLAY IT        T002
         HEX   LINE19+29,(2,R1),LEN=1   YES, GET SECOND BYTE       T002
         B     NEWDSPNT                 SHOW IT
         TITLE 'ZAP --- COMMAND EXECUTION --- V, BASE, FINDEOF'    U019
*
*****  V  *****
*
INDIRECT LH    R15,OLDPOINT             * = CURRENT OFFSET         U004
         MVI   EXPOPT,YESSYMB           WANT SYM TAB LOOKUP        U004
         BAL   R8,CALLEXP               PARSE & CHECK              U004
         BM    INVEXPER                 NEG IS INVALID             U004
         LH    R0,BLKLEN                GET BLOCK LENGTH           U004
         SH    R0,=H'2'                 LAST POSSIBLE RDW          U004
         CR    R15,R0                   PAST END?                  U004
         BH    INVEXPER                 YES - BYE                  U004
         A     R15,ADDRBUFF             GET ADDR IN BUFFER         U004
         MVC   DWD(2),0(R15)            REC LEN TO ALIGNED PLACE   T003
         LH    R15,DWD                  GET RECORD LENGTH          T003
         AH    R15,OLDPOINT             BUMP OFFSET                U004
         B     SETPOINT                 GO SET IT                  U004
*  DO WE WANT TO GO TO THE NEXT BLK IF NECESSARY?                  U004
         SPACE 7
*
*****  BASE  *****                                                 U004
*
BASE     L     R15,OFFSET               * = CURRENT OFFSET         U004
         MVI   EXPOPT,YESSYMB           WANT SYM TAB LOOKUP        U004
         BAL   R8,CALLEXP               PARSE & CHECK              U004
         ST    R15,BASEVAL              SET NEW BASE VALUE         U004
         AH    R15,OLDPOINT             ADD OLD BUFFER OFFSET      U004
         ST    R15,OFFSET               SET NEW OFFSET             U004
         B     NEWDSPNT                 GO SHOW IT OFF             U004
         SPACE 7
*
*****  FINDEOF  *****                                              U019
*
         USNGX *,R9                                                U021
FINDEOF  BAL   R8,CHKPT                 ANY ZAP NEEDED?            U019
         NI    FLAGS2,X'FF'-NOEOF       SET NEW$DISP IF EOF        T003
         BAL   R8,READNBLK              READ THE NEXT BLOCK        U019
         B     *-4                      KEEP READING               U019
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- TSO'                 U020
*
*****  TSO                                                         U020
*
         USNGX *,R9                                                U021
TSO      LA    R2,INVEXP                'INVALID COMMAND'          U020
         LTR   R3,R0                    TEST OPERAND LENGTH        U007
         BNP   INVEXPER                 ERROR
         SPACE 1
TSO1     CLI   0(R1),C' '               SCAN OFF LEADING BLNKS
         BNE   TSO2                     FOUND A NON BLNK
         LA    R1,1(,R1)                LOOK AT NXT CHAR
         BCT   R3,TSO1                  - THE COUNT
         B     INVEXPER
         SPACE 1
TSO2     LR    R2,R1                    PUT IN RIGHT REG
         BAL   R8,KILL3270              TURN OFF 3270 MODE IF ON   U007
         SPACE 1
         XC    TMPLIST(TMPLISTL),TMPLIST CLEAR THE LIST
         SPACE 1
         TMPMAC CMD=((R2),(R3)),MF=(E,TMPLIST)  DO THE COMMAND
         SPACE 1
         BXH   R15,R15,TSO3
         SPACE 1
TSO3     BAL   R8,TGET                  BRANCH ENTRY               U004
         B     COMSCAN                  PARSE COMMAND
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- WHERE'
*
*****  WHERE
*
WHERE    MVI   TRACE,NOTRACE            DONT TRACE
         BAL   R8,CLEAR                 CLEAR SCREEN
         B     WHEREAMI                 FORMAT LINE                U004
         SPACE 4
*
*****  NOT3270  *****                                              U004
*
NOT3270  NI    FLAGS3,255-CRTF-M3270F   TURN OFF FLAGS             U024
         BAL   R8,KILL3270              TURN OFF 3270 MODE IF ON   U004
         B     NEWDSPNT                 DISPLAY AGAIN, NOTRACE     U004
         SPACE 4
*
*****  YES3270  *****                                              U005
*
YES3270  OI    FLAGS3,M3270F            TELL THE WORLD             U015
         NI    FLAGS3,255-CRTF          NOT A NON-3270 CRT         U024
         B     NEWDSPNT                 DISPLAY AGAIN, NOTRACE     U004
         SPACE 4
*
*****  LINE80  *****                                               U004
*
LINE80   L     R1,=A(DSPCON80)          -> APPROPRIATE CONSTANTS   T002
         MVC   DISPCONS(DISPCONL),0(R1) CONSTANTS FOR LEN=80       T002
         B     NEWDSPNT                 GO DISPLAY, NOTRACE        U004
         SPACE 4
*
*****  LINE40  *****                                               U004
*
LINE40   L     R1,=A(DSPCON40)          -> APPROPRIATE CONSTANTS   T002
         MVC   DISPCONS(DISPCONL),0(R1) CONSTANTS FOR LEN=40       T002
         B     NEWDSPNT                 GO DISPLAY, NOTRACE        U004
         TITLE 'ZAP --- COMMAND EXECUTION --- W (WINDOW)'
*
*****  W   (WINDOW)
*
         USNGX *,R9                                                U021
WINDOW   BP    OKWIND                   GO PROCESS OPERANDS
         XC    WIDTHS(4+4),WIDTHS       ONLY 'W', SET DEFAULTS
         NI    FLAGS3,255-CRTF          TURN OFF CRT MODE          U004
         OI    FLAGS2,TERSEF            SUPRESS WHERE LINES        U024
         B     NEWDSPNT                 AND QUIT
         SPACE 2
OKWIND   CLC   =C'FULL',REP+1           FULL WINDOW SPECIFIED?     U004
         BNE   OKWIND#                  NO, GO ON
         L     R6,MAXWIDTH              YES, SAY FULL              U013
         LR    R7,R6                    SAY FULL HERE TOO          U013
         B     WINDSAVE                 WE ARE DONE
         SPACE 2
OKWIND#  LM    R6,R7,WIDTHS             ASSUME WIND=CURR WIND      U013
         LR    R4,R0                    SAVE LEN OP(S)             U013
         LR    R5,R1                    SAVE PTR TO OPERANDS       U013
         SPACE 1
*
*  W<DOWN>,<UP>
*
WINDLOOP CLI   0(R5),C','               END 1ST OP?                U013
         LA    R5,1(,R5)                ASSUME YES                 U013
         BE    *+8                      RIGHT, DO 1ST
         BCT   R4,WINDLOOP              TRY TRY                    U013
         SPACE 1
         SR    R0,R4                    LEN 1ST                    U013
         BZ    WIND2                    NO 1ST, DO 2ND
         BAL   R3,WINDEXP               PARSE, R1 SET, *=0         U013
         LR    R6,R15                   ALL OK, SAVE FOR <DOWN>    U013
         SPACE 2
WIND2    LR    R0,R4                    LEN LEFT = ALMOST LEN 2ND  U013
         SH    R0,=H'1'                 -1 FOR COMMA (IF ANY)
         BNP   WINDSAVE                 NO 2ND OP, LEAVE ALONE
         LR    R1,R5                    SET OP PTR FOR EXP         U013
         BAL   R3,WINDEXP               GO PARSE 2                 U013
         LR    R7,R15                   ALL OK, SAVE <UP>          U013
         SPACE 2
WINDSAVE STM   R6,R7,WIDTHS             SAVE THE 2 WIDTHS          U013
         B     NEWDSPNT
         SPACE 2
WINDEXP  XR    R15,R15                  * = 0                      U004
         BAL   R8,CALLEXP               PARSE & CHECK              U004
         BM    INVEXPER                 NEG IS BAD                 U004
         C     R15,MAXWIDTH             TOO BIG?                   U004
         BNHR  R3                       NO - RETURN                U013
         B     INVEXPER                 YES - COMPLAIN             U004
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- FLOAT'
*
*****  FLOAT
*
*  (ORIGINAL INTX SVCX 6 CODE WRITTEN BY STEVE SILVER.  MODIFIED
*   FOR ZAP USE BY JCJ AND VIC - IE:  WE DON'T UNDERSTAND)
         SPACE 1
         USNGX *,R9                                                U021
FLOAT    LTR   R2,R0                    TEST OPERAND LENGTH        U004
         BP    FLOAT$                   GO USE IT                  U004
         LH    R15,OLDPOINT             GET CURRENT OFFSET         U004
         LR    R14,R15                  COPY                       U004
         AH    R14,=H'8'                POINT PAST END OF OPERAND  U004
         CH    R14,BLKLEN               IN BLOCK?                  U004
         BNL   INVEXPER                 NO - ERROR                 U004
         A     R15,ADDRBUFF             -> OPERAND                 U004
         MVC   TEMP(8),0(R15)           GET IN ALIGNED PLACE       U004
         B     FLOAT#                   CONTINUE WITH IT           U004
         SPACE 1
FLOAT$   BCTR  R2,0                     GET EXECUTE LENGTH         U004
         CH    R2,=H'15'                WAS THE OPERAND TOO LONG?
         BH    LENERROR                 YES - TELL HIM AND QUIT
         LR    R7,R1                    POINT TO THE STRING        U004
         LA    R1,1(,R2)                PICK UP LENGTH OF STRING
         BAL   R8,HEXCHECK              IS IT VALID HEX?
         B     OKFLOAT                  YES - SO LET'S DO IT
         LR    R1,R7                    NO - POINT TO THE ERROR
         B     INVEXPER                 AND SAY SYNTAX ERROR
         SPACE 1
OKFLOAT  EX    R2,TRFLOAT               START TRANSLATE TO REAL HEX
         PACK  TEMP(8),TEMP2+5(15)      MAKE IT HONEST-TO-GOODNESS HEX
         MVN   TEMP+7(1),TEMP2+20       GET THE LAST NIBBLE TOO
FLOAT#   LD    F0,TEMP                  LOAD IT IN FLOAT REG 0     U004
         LA    R7,LINE19+36             PT TO START OF SCRN RESULT AREA
         SPACE 1
*  SET THE PROGRAM MASK TO MASK OUT FIX PT OFLO, DEC OFLO,
*  EXPON UNFLO, AND SIGNIFICANCE.  SAVE IT IN R6 TO RESET IT LATER.
         SPACE 1
         XR    R2,R2                    0 R2 FOR SPM
         BALR  R6,0                     SAVE PGM MASK FOR RESTORE
         SPM   R2                       0 THE PGM MASK (NO INTERR)
         SPACE 1
         MVI   16(R7),C'0'              ASSUME ANSWER IS 0
         AD    F0,=D'0'                 NORMALIZE BY ADDING 0
         LTDR  F0,F0                    CHECK THE SIGN
         BZ    CVFCI3                   ZERO (WE'RE ALMOST DONE)
         BP    CVFC00                   PLUS - GO ON
         LPDR  F0,F0                    MINUS - MAKE IT PLUS...
         MVI   0(R7),C'-'               INSERT SIGN AND GO ON LIKE PLUS
CVFC00   LA    R7,1(R7)                 UPDATE OUTPUT PTR EITHER CASE
         CD    F0,=X'7FFFFFFFFFFFFF00'  DATA WILL CAUSE EXP OVERFLOW?
         BNH   *+8                      NO - SKIP PREVENT LOAD
         LD    F0,=X'7FFFFFFFFFFFFF00'  ELSE GET MAX ALLOWABLE
* MAX ALLOWABLE IS X'7FFFFFFFFFFFFF00'
         MD    F0,=X'4110000000000010'  MULT BY FUDGE FACTOR
         EJECT
*
*  GENERATE THE NUMBER IN D-FORMAT
*
         SPACE 2
         XR    R2,R2                    INITIALIZE EXPON REG
         LD    F4,=D'10'                SET F4 TO 10
         LD    F6,=D'1'                 SET F6 TO 1
         LD    F2,=X'401999999999999A'  SET F2 TO .1
         MVI   0(R7),C'.'               SET UP OUTPUT
         LA    R7,1(R7)                 INCREMENT LINE POINTER
         MVI   14(R7),C'D'              FORM THE OUTLINE OF EXPONENT
         MVI   15(R7),C'+'              MOVE IN EXPONENT SIGN
*  GET THE EXPONENT IN R2 (>=1)
CVFC1A   CDR   F0,F6                    SEE IF # < 1
         BL    CVFC2                    IF SO, GO TO NEXT PART
         DDR   F0,F4                    NO - DIVIDE BY 10
         LA    R2,1(R2)                 INCREMENT EXPONENT
         B     CVFC1A                   CONTINUE
*  GET THE EXPONENT (<.1)
CVFC2    CDR   F0,F2                    SEE IF >= .1
         BNL   CVFC3                    IF SO, GO TO NEXT PART
         BCTR  R2,0                     DECREMENT EXPONENT
         MDR   F0,F4                    MULT NUMBER BY 10
         B     CVFC2                    CONTINUE
*  GET EXPONENT SIGN
CVFC3    LTR   R2,R2                    SEE IF EXPONENT +
         BNM   CVFC3A                   IF SO, SKIP CODE
         LPR   R2,R2                    MAKE IT PLUS BUT.....
         MVI   15(R7),C'-'              WRITE OUT A MINUS
CVFC3A   LA    R0,10                    DIVISER FOR BELOW
         SRDL  R2,32                    SET UP FOR DIVIDE
         DR    R2,R0                    DO THE DIVIDE
         LA    R2,C'0'(R2)              GET THE EBCDIC VALUE
         LA    R3,C'0'(R3)              GET THE EBCDIC VALUE
         STC   R2,17(R7)                AND STORE IT
         STC   R3,16(R7)                AND STORE IT
         LA    R0,14                    GET NUMBER OF DIGITS FOR BCT
         LD    F6,=X'4100000000000000'  LOAD F6 WITH UN-NORMALIZER
*  GET THE MANTISSA
CVFC4    MDR   F0,F4                    MULT NUMBER BY 10
         AWR   F0,F6                    UN-NORMALIZE
         STD   F0,TEMP                  STORE RESULT
         SR    R4,R4                    ZERO FOR IC
         IC    R4,TEMP+1                GET FIRST HEX DIGIT
         SRA   R4,4                     KILL LOW NIBBLE (LAST HEX DIG)
         LA    R3,C'0'(R4)              CONVERT TO CHAR
         STC   R3,0(R7)                 MOVE RESULT TO OUTPUT
         XC    TEMP(8),TEMP             SET UP DWD
         LA    R7,1(R7)                 POINT TO NEXT OUTPUT COL
         LTR   R4,R4                    IS R4 0?
         BZ    CVFC4#                   YES - WE HAVE NOTHING TO DO
         MVI   TEMP,X'41'               MAKE DWD INTERNAL FLOATING PT
         SLL   R4,4                     MOVE NUMBER TO HIGH ORDR NIBBLE
         STC   R4,TEMP+1                AND MOVE IT TO DWD
*
*  WE HAVE NOW BUILT A DWD WHICH MAY CONTAIN D'0' THROUGH D'10'
*
         SD    F0,TEMP                  SUBTRACT THE INTEGER PART
CVFC4#   BCT   R0,CVFC4                 DO TILL END
         B     CVFCI3                   CLEAN UP AND GO AWAY
         SPACE 4
*  IF NUMBER IS ZERO TO BEGIN WITH, THERE'S NOTHING TO DO BUT.......
         SPACE 1
         MVI   16(R7),C'0'              SET UP ZERO RESULT
         SPACE 2
*  RESTORE PROGRAM MASK TO WHAT IT WAS
         SPACE 2
CVFCI3   SPM   R6                       RESET PROGRAM MASK
         SPACE 2
         MVC   LINE19+17(15),=CL15'FLOATING POINT:'
         B     NEWDSPNT                 RETURN TO CALLER
TRFLOAT  TR    TEMP2+5(0),TRHEX         <<< EXECUTED >>>           U004
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- DUMP, DUMPE, DUMPF'  U004
*
*****  DUMP, DUMPE  *****                                          U004
*
         USNGX *,R9                                                U021
DUMP     BAL   R8,LOGTEST                                          U004
         BAL   R8,CHKPT                 SEE IF HE CAN LEAVE        U004
         CLI   CPUTFLAG,YESCSERR        CAN CSOUT ENQ NOW?
         BE    BADCSOUT                 NO - WHY BOTHER TO DUMP THEN?
         L     R7,=XL4'7FFFFFFF'        NUMBER OF BLOCKS TO READ (HAH)
         AIF   (NOT &VSAM).NVSAM24                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    DUMPV                    YES, RBA'S ARE DIFFERENT   T002
.NVSAM24 ANOP  ,                                                   T002
         MVC   TTRSAVE,TTR              SAVE WHERE WE ARE          T003
         CLI   REP+4,C'E'               IS THIS REALLY DUMPE?      U004
         BE    PREPDMPF                 YES - DUMP FROM HERE       U004
         MVC   TTR(4),=XL4'00000100'    POINT TO BEGINNING OF DATASET
         B     PREPDMPF                 START DUMPING TILL END OF DS
         AIF   (NOT &VSAM).NVSAM25                                 T002
DUMPV    MVC   TTRSAVE,TTR              SAVE WHERE WE ARE          T003
         CLI   REP+4,C'E'               IS THIS REALLY DUMPE?      T002
         BE    PREPDMPF                 YES - DUMP FROM HERE       T002
         XC    TTR,TTR                  POINT TO BEGINNING OF DS   T002
         B     PREPDMPF                 START DUMPING TO END OF DS T002
.NVSAM25 SPACE 1
         DROPX R9                                                  U021
         SPACE 3
*
*****  DUMPF  *****
*
DUMPF    L     R9,=A(DUMP)              FUDGE BASE REGISTER        U021
         USNGX DUMP,R9                                             U021
         BAL   R8,LOGTEST                                          U004
         BAL   R8,CHKPT                 SEE IF HE CAN LEAVE        U004
         CLI   CPUTFLAG,YESCSERR        CAN CSOUT ENQ IT TO PRINT?
         BE    BADCSOUT                 NO - SO WHY DUMP MAY I ASK?
         LH    R0,REALRDLN              GET LEN OF COMMAND
         SH    R0,=H'5'                 -5 FOR 'DUMPF' ACTUAL LEN
         BNZ   DUMPFOR                  HE GAVE AN OPERAND, PROCESS IT
JUSTDUMP LA    R7,1                     DUMP R* ONLY - R7 = # TO DUMP
         B     STARTDF                  ALL SET, START THE DUMP-FOR
         SPACE 1
*  PROCESS OPERAND OF DUMPF COMMAND (NUMBER OF BLOCKS TO DUMP)
         SPACE 1
DUMPFOR  XR    R15,R15                  LOAD *, CURRENT LOC FOR 'EXP'
         LA    R1,REP+5                 POINT TO OPERAND TO PARSE  U004
         BAL   R8,CALLEXP               GO PARSE                   U004
         BM    NOBACK                   NEG - WE CAN'T DUMP BACKWARDS
         BZ    INVEXPER                 ZERO - INVALID SYNTAX
         SPACE 1
         LR    R7,R15                   NUMBER OF BLOCKS TO DUMP
STARTDF  MVC   TTRSAVE,TTR              SAVE WHERE WE ARE          T003
PREPDMPF NI    FLAGVSAM,X'FF'-SEQREAD   1ST BLK - READ IT FIRST    T003
DMPFLOOP BAL   R6,PREPDUMP              GET READY FOR THE DUMP
         BAL   R6,DUMPER                DUMP A BLOCK
         OI    FLAGVSAM,SEQREAD         SAY NEXT PASS,NOW READ BLK T002
         BCT   R7,DMPFLOOP              DO ALL THE BLOCKS HE WANTS
         SPACE 2
DUMPEND  MVC   TTR(4),TTRSAVE           PICK UP WHERE WE WERE      T003
         AIF   (NOT &VSAM).NVSAM26                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BZ    DUMP$END                NO, JUST RE-READ BLOCK      T002
         NI    FLAGVSAM,X'FF'-SEQREAD   DON'T PUT HIM IN NEXT BLK  T003
.NVSAM26 ANOP  ,                                                   T002
DUMP$END BAL   R8,READBLK               SO THAT HE'LL NEVER KNOW   T002
         LA    R2,DUMPWORK              IT WORKED SO TELL HIM
         B     BOTCH                    DISPLAY (NOT REALLY A BOTCH)
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- DUMPT'
*
*****  DUMPT  *****
*
DUMPT    L     R9,=A(DUMP)              FUDGE BASE REGISTER        U021
         USNGX DUMP,R9                                             U021
         BAL   R8,LOGTEST                                          U004
         BAL   R8,CHKPT                 SEE IF HE CAN LEAVE        U004
         CLI   CPUTFLAG,YESCSERR        CAN CSOUT ENQ TO PRINT?
         BE    BADCSOUT                 NO - DON'T BOTHER WITH DUMP
         LH    R0,REALRDLN              GET LEN OF READ
         SH    R0,=H'5'                 -5 FOR 'DUMPT' ACTUAL LEN
         BZ    JUSTDUMP                 NO OPERAND, JUST DUMP THIS BLK
         SPACE 1
*  PARSE OUT THE TTR OPERAND OF THE DUMPT COMMAND
         SPACE 1
         LA    R1,REP+5                 POINT TO THE OPERAND       U004
         BAL   R6,TTRPARSE              GO PARSE (USE POINT'S RTN) U004
         CLC   TEMP,TTR                 RESULT < CURRENT TTR?      T002
         BNL   OKDUMPT                  NO - HIS WISH IS MY COMMAND
NOBACK   LA    R2,BACKDUMP              YES - WE CAN'T DUMP BACKWARDS
         B     BOTCH                    WHAT DOES HE THINK WE ARE?
         SPACE 1
OKDUMPT  MVC   STOPDUMP,TEMP+1          TTR TO STOP AT             T002
         MVC   TTRSAVE,TTR              SAVE WHERE WE ARE          T003
         SPACE 1
         NI    FLAGVSAM,X'FF'-SEQREAD   1ST TIME READ BLK FOR DUMP T003
DMPTLOOP BAL   R6,PREPDUMP              NO - SO PREPARE TO DUMP
         BAL   R6,DUMPER                AND DUMP WHAT WE HAVE
         OI    FLAGVSAM,SEQREAD         NOW WE CAN READ SEQ BLKS   T002
         CLC   TTR(3),STOPDUMP          ARE WE THERE OR PAST YET?
         BNL   DUMPEND                  YES - WE ARE ALL DONE THEN
         B     DMPTLOOP                 GO TILL WE ARE AT HIS TTR
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- WHATMEM'             U004
*
*****  WHATMEM  *****
*
         USNGX *,R9                                                U021
WHATMEM  BAL   R8,CHKPT                 SEE IF HE CAN LEAVE        T002
         MVC   TTRSAVE,TTR              SAVE CURRENT TTR           T003
         XC    STOPDUMP(3),STOPDUMP     SAVE CLOSEST TTR HERE      U004
         XC    TTR(3),TTR               START AT BEGINNING OF DIR  U004
         SPACE 1
WHATLOOP BAL   R8,READNBLK              GET NEXT DIRECTORY BLOCK   U004
         CLI   IOERROR,YESSYN           DID IT WORK?               U004
         BE    NEWDSPNT                 NO - ABORT                 U004
         L     R3,ADDRDATA              -> BUFFER                  U004
         LH    R2,0(,R3)                BLK LEN USED               U004
         LA    R3,2(,R3)                -> REAL BEGIN LOC          U004
         BCTR  R2,0                     -1                         U004
         BCTR  R2,0                     AND -2 FOR REAL LEN USED   U004
         SPACE 1
WMLOOP2  CLC   0(4,R3),=XL4'FFFFFFFF'   END OF DIRECTORY?          U004
         BE    WMEND                    YES                        U004
         CLC   8(3,R3),TTRSAVE          THIS MEM PAST CURRENT LOC? T003
         BH    WMNEXT                   YES - TRY NEXT             U004
         CLC   8(3,R3),STOPDUMP         NO - HIGHER THAN PREVIOUS? U004
         BL    WMNEXT                   NO - PREVIOUS IS CLOSER    U004
         MVC   STOPDUMP(3),8(R3)        SAVE THIS TTR              U004
         MVC   LINE19+35(8),0(R3)       SAVE MEMBER NAME           U004
         SPACE 1
WMNEXT   BAL   R8,NXTDIRNT              INCR TO NEXT ENTRY         U004
         BP    WMLOOP2                  CHECK NEXT                 U004
         B     WHATLOOP                 GET ANOTHER DIR BLK        U004
         SPACE 1
WMEND    MVC   TTR(3),TTRSAVE           RESTORE WHERE HE WAS       T003
         OC    STOPDUMP(3),STOPDUMP     DID I FIND ANYTHING?       U004
         BNZ   WMENDOK                  YES - DISPLAY IT           U004
         MVC   LINE19,BLANKS            RESET THE LINE             U004
         BAL   R8,SETMSG                MOVE IN MSG                U004
         DC    Y(NOMEMFND-ZAP)          'NONE FOUND'               U004
         B     WMDONE                   SKIP OTHER                 U004
         SPACE 1
WMENDOK  MVC   LINE19+17(17),=C'CLOSEST MEMBER IS'                 U004
         MVC   LINE19+44(6),=C'AT TTR'                             U004
         HEX   LINE19+51,STOPDUMP,LEN=3 GIVE HIM THE START         U004
         SPACE 1
WMDONE   BAL   R8,READBLK               PUT HIM BACK WHERE HE WAS  U004
         B     NEWDSPNT                 AND GO DISPLAY             U004
*  SEE NOTE AT 'MNF' (NEXT PAGE)                                   U004
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- M'                   U004
*
*****  M  *****
*
         USNGX *,R9                                                U021
MEMBER#  BAL   R8,CHKPT                 NEED ZAP?                  T002
         MVC   TTRSAVE,TTR              SAVE TTR IN CASE NO FIND   T003
         MVC   TTR(3),MEMTTR            RESTORE START OF MEMBER    U019
         LTR   R0,R0                    TEST OPERAND LENGTH        U004
         BNP   NEW$READ                 NONE - USE LAST MEM        U019
         MVC   FINDMBR,REP+1            MOVE MEMBER NAME TO FIELD  U019
*
*  FIND THE DIRECTORY BLOCK CONTAINING THE MEMBER                  U019
*
         L     R15,DCBDEBAD-IHADCB+DCBU -> DEB                     U019
         XC    MBBCCHHR(3),MBBCCHHR     CLEAR MBB                  U019
         MVC   CCHHR(4),DEBSTRCC-DEBDS(R15)  SET CCHH OF FIRST EXT U019
         MVI   CCHHR+4,0                SET R0                     U019
         LA    R0,CCW##D                -> CCWS                    U019
         ST    R0,IOBCCWA               SET CCW ADDR IN IOB        U019
         SPACE 1
         EXCP  IOB                      DO IT                      U019
         SPACE 1
         WAIT  ECB=ECB                  WAIT FOR IT                U019
         SPACE 1
         CLI   ECB,X'7F'                DID IT WORK?               U019
         BNE   REALERR                  NO - GIVE MESSAGE          U019
*
*  SEARCH THE DIRECTORY BLOCK FOR THE MEMBER                       U019
*
         L     R3,ADDRKEY               GET BUFFER PTR             U004
         LH    R2,0(,R3)                GET BLK LEN USED           U019
         BCTR  R2,0                     -1
         BCTR  R2,0                     AND -2 FOR REAL LEN USED
         LA    R3,2(,R3)                POINT TO REAL BEGIN LOC    U019
MLOOP2   CLC   0(8,R3),FINDMBR          MEMBER HERE?               U019
         BE    FOUNDM                   YES - GO WORK ON IT
         BH    MNF                      IF PAST ALPHA SEQ GIVE UP *PCN*
         BAL   R8,NXTDIRNT              INCR TO NEXT ENTRY         U004
         BP    MLOOP2                   MORE TO GO - KEEP LOOKING  U019
         EX    0,*                      LOGIC ERROR IN ZAP OR PDS  U019
MNF      MVC   TTR(3),TTRSAVE           PICK UP WHERE HE WAS       T003
*
*  NOTE:  READBLK WILL RETURN ONLY IF THE USER IS NOT AT AN EOF.
*         IF EOF, SYNAD==>EODAD==>DISPLAY GET EXECUTED, SO WE MUST
*         PRE-SET THE MESSAGE HERE.
*
         BAL   R8,SETMSG                MOVE MSG TO RIGHT PLACE    U004
         DC    Y(INVMEM-ZAP)            SAY INV MEMBER (NOT FOUND) U004
         BAL   R8,READBLK               PUT HIM BACK WHERE HE WAS
         B     NEWDSPNT                 AND GO DISPLAY
         SPACE 2
FOUNDM   MVC   MEMBER(8),FINDMBR        SET MEMBER NAME FOR NOW ON U019
         MVC   TTR(3),8(R3)             PICK UP MEMBER'S TTR
         MVC   MEMTTR(3),8(R3)          SAVE IT FOR LATER USE
         MVC   ESDID,=H'1'              RESET ESDID TO DEFAULT     U002
         NI    FLAGESD,X'FF'-CHGIDR     HAVEN'T CHANGED IDR YET    T003
         XC    TXTTTR(3),TXTTTR         ASSUME NOT A LOAD MOD      U002
         XC    CTL1TTR,CTL1TTR          NO 1ST CTL REC YET         U023
         MVC   TEMP(1),11(R3)           FLAGS/LEN FROM DIR ENTRY   U023
         NI    TEMP,B'01100000'         # TTR'S IN USER DATA FIELD U023
         CLI   TEMP,B'00100000'         AT LEAST ONE USER TTR?     U023
         BL    FOUNDM1                  NO - NOT LOAD MODULE       U023
         MVC   TEMP(1),11(R3)           FLAGS/LEN FROM DIR ENTRY   U023
         NI    TEMP,B'00011111'         # H-WORDS USER DATA        U023
         CLI   TEMP,12                  AT LEAST 12 HALFWORDS?     U023
         BL    FOUNDM1                  NO - NOT LOAD MODULE       U023
         CLI   DCBRECFM-IHADCB+DCBU,RECFMU  A LOAD MODULE?         U002
         BNE   *+10                     NO, NO TEXT THEN           U002
         MVC   TXTTTR(3),12(R3)         YES, SAVE 1ST TEXT BLK TTR U002
FOUNDM1  LA    R1,N0                    POINT TO PROPER NUMBER     U023
         BAL   R8,SETLINE               SET UP LINE TO PUT
         B     NEW$READ                 NOT PRINTING, EXIT
         USNGX LOGLINE,R2                                          U022
         MVI   LL@CC,C'0'               DOUBLE SPACE IT            U022
         MVC   LL@XMSG(6),=C'MEMBER'                               U022
         MVC   LL@XMSG+7(8),MEMBER      FILL IN MEMBER IN QUESTION U022
         MVC   LL@XMSG+16(8),=C'SELECTED'                          U022
         DROPX R2                                                  U022
         BAL   R8,CPUT                  NOW DUMP THE LINE OUT
         B     NEW$READ                 GO DO IT
         SPACE 1
         DROPX R9                                                  U021
         SPACE 4
*
*  SUBROUTINE TO INCREMENT TO NEXT ENTRY IN DIRECTORY BLOCK
*
         SPACE 3
NXTDIRNT IC    R4,11(,R3)               GET INDICATORS             U004
         N     R4,=X'0000001F'          JUST # USER DATA HALFWORDS U004
         LA    R4,12(R4,R4)             ENTRY LENGTH               U004
         AR    R3,R4                    -> NEXT ENTRY              U004
         SR    R2,R4                    DECR LENGTH REMAINING      U004
         BR    R8                       RETURN WITH CC SET         U004
         TITLE 'ZAP --- COMMAND EXECUTION --- NAME'                U002
*                                                                  U002
*****  NAME  *****                                                 U002
*                                                                  U002
         USNGX *,R9                                                U021
NAME#    BAL   R8,CHKPT                 NEED CHKPT?                T002
         OC    TXTTTR(3),TXTTTR         IS THERE ANY TEXT?         U002
         BZ    NAMENFN#                 NO, CAN'T HAVE ESD'S THEN  U002
         SPACE 1
         MVC   NAME(8),MEMBER           ASSUME CSECT IS MODULE    *LPR*
         LTR   R0,R0                    IS THERE AN ARGUMENT?      U004
         BNP   *+10                     NO - OK THEN               U004
         MVC   NAME(8),0(R1)            MOVE NAME TO FIELD         U004
         MVI   FLAGESD,0                NO FLAGS YET               U002
         SPACE 2
*                                                                  U002
*  FIND THE ESD'S FIRST                                            U002
*                                                                  U002
         MVC   TTRSAVE,TTR              REMEMBER WHERE WE ARE NOW  T003
         MVC   TTR(3),MEMTTR            START AT BEGIN MEMBER      U002
         BAL   R8,READBLK               GET 1ST BLK                U002
         SPACE 1
NAMEFIND L     R1,ADDRDATA              POINT TO RECORD            U004
         TM    0(R1),X'01'              CONTROL REC?               U002
         BNO   NAMECHKE                 NO, POSSIBLE ESD OR SYM    U002
NAMENFND CLC   TTR(3),TTRSAVE           NOT FOUND. HAVE WE MOVED?  T003
         BE    NAMENFN#                 NO, NO RE-POS'N NECESSRY   U002
         NI    FLAGVSAM,X'FF'-SEQREAD   YES, NO SEQUENTIAL READ    T003
         MVC   TTR(3),TTRSAVE           RESET WHERE WE WERE        T003
         BAL   R8,READBLK               PUT US BACK                U002
NAMENFN# LA    R2,NAMEMNF               'INVALID' MSG              U002
         B     BOTCH                    OH WELL                    U002
         SPACE 1
NAMECHKE CLI   0(R1),X'20'              ESD?                       U002
         BE    NAMEESD                  YES, HAVE ONE, TRY IT      U002
NAMEENXT OI    FLAGS2,NOEOF             EOF'S ARE DATA NOW         T002
         BAL   R8,READNBLK              GET NEXT BLK               U004
         CLC   LINE05+27(20),EOFMSG     HIT EOF HERE?              U004
         BE    NAMENFND                 YES, NOT FOUND             U002
         CLI   IOERROR,YESSYN           I/O ERROR                  U002
         BE    NAMENFND                 YES, NOT FOUND             U002
         B     NAMEFIND                 NONE, KEEP LOOKING         U002
         SPACE 1
NAMEESD  LH    R15,6(,R1)               GET LEN ESD DATA THIS BLK  U002
         LA    R1,8(,R1)                PT PAST ESD PREFIX TO DATA U002
         AR    R15,R1                   PT PAST LAST ESD DATA      U002
         USNGX ESDDATA,R1               ESD ENTRIES                U002
         SPACE 1
NAMECHK  CR    R1,R15                   PAST END THIS BLK?         U002
         BNL   NAMEENXT                 YES, NEXT ESD BLK          U002
         CLC   ESDDNAME(8),NAME         NO, NAME MATCH?            U002
         BE    NAMEGOT                  YES, WE MIGHT HAVE IT      U002
NAMEBNXT LA    R1,ESDDL(,R1)            NEXT ENTRY IN BLK          U002
         B     NAMECHK                  KEEP SCANNING BLK          U002
         SPACE 2
*                                                                  U002
*  THE ENTRY POINT EXISTS.  FIND THE PLACE IN THE MODULE TEXT.     U002
*                                                                  U002
NAMEGOT  NI    ESDDTYPE,255-X'F0'       JUST WANT LOW NIBBLE       U002
         CLI   ESDDTYPE,X'02'           EXT REF ENTRY  (ER)?       U002
         BE    NAMEBNXT                 YES, IGNORE, NEXT ENTRY    U002
         CLI   ESDDTYPE,X'0A'           WEAK EXT REF?  (WEX)?      U002
         BE    NAMEBNXT                 YES, IGNORE, NEXT ENTRY    U002
         L     R3,ESDDADDR-1            NO, GET LKED ADDR OF EP    U002
         LA    R3,0(,R3)                GET RID OF TYPE BYTE       U002
         DROPX R1                       NO MORE ENTRIES            U002
         SPACE 1
         L     R15,ADDRDATA             PT TO ESD BLK PREFIX       U004
         LH    R14,4(,R15)              ESDID 1ST NTRY THIS BLK    U002
         LA    R15,8(,R15)              PT PAST PRFX TO 1ST ENTRY  U002
         SR    R1,R15                   OFFSET MTHC NTRY FROM PRFX U002
         XR    R0,R0                    FOR DIVIDE                 U002
         D     R0,=A(ESDDL)             GET ESDID REL THIS BLK     U002
         AR    R1,R14                   ESDID MATCH ENTRY POINT    U002
         STH   R1,ESDID                 SAVE FOR IDR UPDATE        U002
         NI    FLAGVSAM,X'FF'-SEQREAD   NO MORE SEQ READ           T003
         MVC   TTR(3),CTL1TTR           START AT FIRST CTL REC     U023
         OC    TTR(3),TTR               ANY FIRST CTL TTR?         U023
         BNZ   *+10                     YES - START THERE          U023
         MVC   TTR(3),MEMTTR            NO - START AT START OF MBR U023
         BAL   R8,READBLK               GET ESD, CTL, IDR RECORD   U023
         NI    FLAGESD,255-FESDTEXT     NEXT BLK IS NOT TEXT       U023
         SPACE 1
NAME$L1  TM    FLAGESD,FESDTEXT         IS THIS TEXT BLOCK?        U023
         BO    NAME$T1                  YES - PROCESS              U023
         L     R1,ADDRDATA              @ NON-TEXT RECORD          U023
         TM    0(R1),X'01'              CONTROL RECORD?            U023
         BZ    NAME$NX                  NO - TRY NEXT              U023
         OI    FLAGESD,FESDTEXT         NEXT BLK IS TEXT           U023
         OC    CTL1TTR,CTL1TTR          HAVE FOUND 1ST CTL TTR?    U023
         BNZ   *+10                     YES - SKIP                 U023
         MVC   CTL1TTR,TTR              SAVE TTR OF 1ST CTL REC    U023
         MVC   TXTORIG,8(R1)            LKED ORIG NEXT (TEXT) BLK  U023
         MVI   TXTORIG,0                CLEAR HIGH BYTE            U023
         SPACE 1
*U023    MVC   TTR(3),TXTTTR            START AT 1ST TEXT BLK      U002
*U023    BAL   R8,READBLK               GET 1ST TEXT BLK           U002
*U023    SPACE 2
*AMEOFFS SH    R3,BLKLEN                DECREMENT BY TEXT BLK LEN  U002
*U023    BM    NAMEGOTB                 WE HAVE OFFSET, DONE       U002
*AME#NXT NI    FLAGESD,255-FESDTEXT     NEXT BLOCK IS NOT TEXT     U002
         SPACE 1
*AMEONXT OI    FLAGS2,NOEOF             EOF'S ARE DATA             T002
NAME$NX  OI    FLAGS2,NOEOF             EOF'S ARE DATA             U023
         BAL   R8,READNBLK              GET NEXT LOADMOD BLK       U004
         CLC   LINE05+27(20),EOFMSG     EOF?                       U004
         BE    NAMENFND                 OOPS, CAN'T HAPPEN         U002
         B     NAME$L1                                             U023
*U023    TM    FLAGESD,FESDTEXT         CONTROL REC BEFORE?        U002
*U023    BO    NAMEOFFS                 YES, THIS MUST BE TEXT BLK U002
*U023    L     R1,ADDRDATA              NO, FIND BLK               U004
*U023    TM    0(R1),X'03'              CONTROL/RLD SOME SORT?     U002
*U023    BNO   NAMENOT3                 NO, KEEP CHECKING          U002
*AMETNXT OI    FLAGESD,FESDTEXT         YES, REMEMBER NEXT=TEXT    U002
*U023    B     NAMEONXT                 NEXT REC NOW               U002
*U023    SPACE 2
*AMENOT3 TM    0(R1),X'01'              JUST CONTROL REC?          U002
*U023    BO    NAMETNXT                 YES, TREAT AS CONTROL/RLD  U002
*U023    B     NAME#NXT                 NEXT REC IS NOT TEXT       U002
         SPACE 2
NAME$T1  NI    FLAGESD,255-FESDTEXT     NEXT BLK IS NOT TEXT       U023
         LR    R0,R3                    COPY ENTRY ORIGIN          U023
         S     R0,TXTORIG               SUBTRACT TXT BLOCK ORIG    U023
         BM    NAME$NX                  NOT IN NEXT TXT BLK, LOOP  U023
         CH    R0,BLKLEN                OFFSET IN BLOCK :: BLKLEN  U023
         BNL   NAME$NX                  NOT IN THIS TXT BLK, LOOP  U023
         SPACE 5
*                                                                  U002
*  WE HAVE THE BLOCK AND OFFSET OF THE ENTRY POINT. NOTIFY/DISPLAY U002
*                                                                  U002
NAMEGOTB NI    FLAGVSAM,X'FF'-SEQREAD   NO MORE SEQ READS          T003
         LR    R3,R0                    THIS IS THE BLOCK          T003
         LA    R1,N8                    MSG/WTO                    U002
         BAL   R8,SETLINE               SET IT UP                  U002
         B     NAMEDONE                 NOT PRINTING               U002
         USNGX LOGLINE,R2                                          U022
         MVI   LL@CC,C'0'               DBL SPACE                  U002
         MVC   LL@XMSG(6),=C' ENTRY'    MEMBER=ENTRY               U002
         MVC   LL@XMSG+7(8),NAME        MOVE ENTRY NAME IN         U002
         MVC   LL@XMSG+16(8),=C'SELECTED'                          U022
         DROPX R2                                                  U022
         BAL   R8,CPUT                  DO IT                      U002
         SPACE 1
NAMEDONE LR    R15,R3                   GET OFFSET INTO BLOCK      U002
         NI    FLAGESD,X'FF'-CHGIDR     HAVEN'T CHANGED IDR YET    T003
         B     SETPOINT                 SIMULATE A '+' COMMAND     U002
         SPACE 1
         DROPX R9                                                  U021
         TITLE 'ZAP --- COMMAND EXECUTION --- RBASEQ, KEYSEQ'      T002
*
*****  RBASEQ  *****                                               T002
*
RBASEQ   NI    FLAGVSAM,VSAMSEQM        TURN OFF SEQ BITS          T002
         OI    FLAGVSAM,VSAMSEQR        NOW RBA SEQ                T002
         B     NEWDSPNT                 NEXT COMMAND               T002
         SPACE 5
*
*****  KEYSEQ  *****                                               T002
*
         USNGX *,R9                                                U021
KEYSEQ   LA    R2,INVKEY                -> ASSUME NO KEY           T002
         OC    KEYLEN,KEYLEN            ANY HERE?                  T002
         BZ    BOTCH                    NO, USER BLEW IT           T002
         NI    FLAGVSAM,VSAMSEQM        TURN OFF SEQ BITS          T002
         OI    FLAGVSAM,VSAMSEQK        NOW KEY SEQ                T002
         B     NEWDSPNT                 NEXT COMMAND               T002
         SPACE 1
         DROPX R9                                                  T003
.NVSAM28 ANOP  ,                                                   T002
         AIF   (&MVS).MVS06                                        U004
         TITLE 'ZAP --- COMMAND EXECUTION --- NN'
*
*****  NN  *****
*
         USNGX *,R9                                                U021
NN       BAL   R8,CHKPT                 NEED ZAP?
         CLC   =C'SYS1.SYSJOBQE',DSNAME ARE WE IN RIGHT DSN (JOBQ)?
         LA    R2,NOTJQMSG              POINT TO MESSAGE IN CASE
         BNE   BOTCH                    NO - EXIT TO DISPLAY       U004
         XR    R15,R15                  '*'=0
         BAL   R8,CALLEXP               PARSE & CHECK              U004
*
*  WE HAVE AN 'NN' VALUE IN R15.  BEGIN ALGORITHM TO FIND TTR OF
*  JOB
*
         USNGX QMRCAR,R3
*LPR*    L     R3,CVTPTR                PICK UP CVT PTR
*LPR*    L     R3,CVTJOB-CVT(R3)        PT TO QMANAGER WORK AREA
         LA    R3,JQQMHDA-QMHDA+QMRCAR  PT TO QMANAGER WORK AREA  *LPR*
         LR    R1,R15                   SAVE NN VAL
         SH    R1,QMFQR                 REL 'NN' OF FIRST JOB Q RECRD
         BNM   POSNN                    IF NOT ON MIXED TRK, CONTINUE
         LH    R0,QMKTT                 GET 'TT' OF FIRST NON-MIXED
         BCTR  R0,0                     FIND MIXED TRACK
         STH   R0,TTR                   SAVE IT IN TTR
         AH    R15,QMNHM                NUM OF HANDLES (QCRS) IN MIX
         STC   R15,TTR+2                THAT'S THE REC
         B     NEW$READ                 GO DISPLAY IT FOR HIM
*
POSNN    XR    R0,R0                    CLEAR REM REG
         LH    R14,QMRPT                GET RECS/TRACK
         DR    R0,R14                   GET NUMBER OF TRACKS
         AH    R1,QMKTT                 ADD MIXED TRKS TO THAT
         AH    R0,=H'1'                 ADJUST REC FOR OFFSET
         STH   R1,TTR                   SAVE TT
         STC   R0,TTR+2                 SAVE REC
         B     NEW$READ                 GO DISPLAY
         DROPX R9,R3                                               U021
.MVS06   TITLE 'ZAP --- COMMAND EXECUTION --- NOTE, EJECT, FCME'   T001
*
*****  NOTE  *****
*
NOTE     BAL   R8,LOGTEST                                          U004
         OI    FLAGS2,NOWHEREF          SUPRESS WHERE LINES
         L     R2,CSOUTWK               FIND WORKAREA
         LTR   R2,R2                    ANY THERE?
         BZ    BADCSOUT                 NO - TELL HIM SORRY        U004
         L     R2,0(,R2)                FIND THE NEXT LINE IMAGE
         USNGX LOGLINE,R2                                          U022
         MVC   LL@MSGID($L-4),REP+4     COPY NOTATION TO BUFFER    U022
         MVI   ENQIT,255                FORCE IT TO BE ENQUEUED
         MVI   LL@CC,C'0'               DOUBLE SPACE NOTES         U022
         DROPX R2                                                  U022
         BAL   R8,CPUT                  DUMP THIS LINE NOW
         B     NEWDSPNT                 THEN CONTINUE ON MERRY WAY
         SPACE 5
*
*****  EJECT  *****
*
EJECT    BAL   R8,LOGTEST                                          U004
         OI    FLAGS2,NOWHEREF          SUPRESS WHERE LINES
         L     R2,CSOUTWK               GET THE WORKAREA PTR
         LTR   R2,R2                    IS THERE ONE?
         BZ    BADCSOUT                 NO - GIVE UP ON THIS ENDEAVOR
         L     R2,0(,R2)                FIND THE NEXT LINE IMAGE
         USNGX LOGLINE,R2                                          U022
         MVI   LL@CC,C'1'               PAGE EJECT                 U022
         MVI   ENQIT,255                FORCE ENQ TO PRINT
         DROPX R2                                                  U022
         BAL   R8,CPUT                  DO IT
         B     NEWDSPNT                 ALL DONE
         SPACE 4
*
*****  FCME  *****
*
*FCME    TM    CSALTFLG,X'0A'           ONLY STAFF CAN DO THIS ONE
*U021    BNO   *+8                      JUST A USER - BYE BYE
FCME     MVI   GODFLAG,GOD              LET HIM BE GOD IF HE WANTS IT
         BCTR  R1,0                     PT TO 'BAD' COLUMN...      U004
         LA    R2,INVEXP                AND FAKE AN 'INVALID COMMAND'
         B     BOTCH                    ALL DONE
         TITLE 'ZAP --- UTILITY BAL ROUTINES'
         SPACE 3
ZAP      CSECT                          RESUME MAIN CSECT          U021
         SPACE 3
*  ROUTINES INCLUDE:
*
*  CLEAR........CLEAR SCREEN
*  READBLK......GET A DATA BLOCK
*  CALLEXP......CALL EXP TO PARSE EXPR, CHECK ERROR RETURNS        U004
*  HEXCHECK.....VALIDITY CHECK HEX
*  GETSTRNG.....VALIDITY CHECK AND PARSE HEX/CHAR STRING
*  NUMCONV......CONVERT A NUMBER TO HEX AND SIGNED DECIMAL         U004
*  CSIO.........PRIMARY SCREEN DISPLAY
*  CSIO#........SECONDARY SCREEN DISPLAY
*  PUTLINE......PUTLINE 1 LINE TO TERMINAL                         U004
*  TTRPARSE.....PARSE OUT SPECIFIED TTR
*  PREPDUMP.....PREPARE FOR DUMP - GET BLOCK, PRINT HEADER LINE
*  DUMPER.......DUMP A BLOCK IN DUMP FORMAT
*  CLEARDEF.....RESET DEFINE TABLE
*  CLOSE........CLOSE DATASET
*  WRITE........EXCP WRITE OUT A BLK
*  OPENMSG......GIVE PRINT FILE MSG OF DATASET OPEN
*  CPUT.........ENQ TO PRINT AN OUTPUT LINE
*  SETLINE......SET UP THE OUTPUT LINE FOR CPU
*  SETSTRNG.....CONVERT VARIOUS CONSTANTS FOR CPUT
*  SETMSG.......MOVE INFO MSG TO CORRECT PLACE ON SCREEN           U004
*  BOTCH........DISPLAY ERROR MESSAGES
*  EODAD........END OF FILE EXIT (SYNAD)
*  CHKPT........CHECK IF ZAP NEEDED.  IF SO IGNORE COMMAND PENDING
*  KILL3270.....TURN OFF 3270 FULLSCREEN MODE CONSOLE I/O          U004
*  FINDVOL......FIND VOLSER ON WHICH DSN RESIDES                   T002
*  TTRCONV......CONVERT TTR TO MBBCCHHR USING CVT RTN              T002
         AIF   (NOT &VSAM).NVSAM29                                 T002
*  VSAMTSEQ.....READ SEQ FORWARD BY RBA                            T002
*  VSAMTSQB.....READ SEQ BACKWARDS BY RBA                          T002
*  VSAMTKEY.....READ DIRECT BY KEY                                 T002
*  VSAMERR......DISPLAY VSAM ERROR CODES FOR DEBUGGING             T002
.NVSAM29 EJECT
**********************************************************************
********************************  CLEAR  *****************************
**********************************************************************
         SPACE 1
*  CLEAR THE SCREEN BUFFER EXCEPT FOR ERROR AND I/O ERROR LINES
         SPACE 1
CLEAR    LA    R15,LINE01-2             -> START                   U011
         LA    R0,3                     NUMBER OF LINES            U011
         BAL   R14,CLEARX               CLEAR 1-3                  U011
         LA    R15,LINE06-2             -> START                   U004
         LA    R0,13                    NUMBER OF LINES            U004
         BAL   R14,CLEARX               CLEAR 6-18                 U004
         LA    R15,LINE20-2             -> START                   U004
         LA    R0,2                     NUMBER OF LINES            U004
         BAL   R14,CLEARX               CLEAR 20-21                U004
         MVI   LINE02-1,X'C8'           UNPROT HIGH                U004
         XC    LINE02,LINE02            NULLS FOR THIS ONE         U011
         MVC   LINE05-2(2),=X'1DE8'     I/O ERRMSG LINE (PROT BRT) U011
         MVC   LINE19-2(2),=X'1DE8'     AND OTHER ERRMSG LINE      U011
         MVC   REP-2(2),LINE02-2        UNPROT HIGH                U004
         BR    R8                       RETURN TO CALLER
         SPACE 2
*  X... ....  SET TO MAKE VALID CHAR                               U004
*  .1.. ....  MUST BE 1                                            U004
*  ..1. ....  PROT                                                 U004
*  ...1 ....  NUMERIC                                              U004
*  ..11 ....  AUTOSKIP                                             U004
*  .... 00..  DISPLAY / NOT SELECTOR PEN DETECTABLE                U004
*  .... 01..  DISPLAY / DETECTABLE                                 U004
*  .... 10..  HIGH INTENSITY / DETECTABLE                          U004
*  .... 11..  NO DISPLAY / NOT DETECTABLE                          U004
*  .... ..0.  MUST BE 0                                            U004
*  .... ...1  MDT FLAG                                             U004
         SPACE 1
CLEARX   MVC   0(2,R15),=X'1D60'        PROT AUTOSKIP LOW          U004
         MVC   2($L,R15),BLANKS         BLANK THE LINE             U004
         LA    R15,$I(,R15)             -> NEXT LINE               U004
         BCT   R0,CLEARX                CONTINUE                   U004
         BR    R14                      DONE THIS SECTION          U004
         EJECT
**********************************************************************
********************************  READBLK  ***************************
**********************************************************************
         SPACE 3
*  EXCP READ A BLOCK (PHYSICAL RECORD) INCLUDING KEY (IF ANY).
*  VSAM READ A RECORD (LOGICAL RECORD) INCLUDING KEY (IF ANY).     T002
*  IF TTR+3 CONTAINS A 1 (FLAG) THEN READ SEQUENTIALLY, SIMULATING
*  BSAM
         SPACE 1
READNBLK OI    FLAGVSAM,SEQREAD         SET NEXT BLOCK FLAG        T002
READBLK  NI    FLAGS2,255-ATTNHIT       NO ATTN YET                U004
READBLK2 ST    R8,LINKREAD              SAVE RETURN ADDR           T002
READBLK3 MVI   IOERROR,NOSYN            START WITH NO I/O ERROR    T002
         BAL   R8,CHKPT                 SEE IF HE CAN LEAVE        U004
         TM    FLAGVSAM,SEQREAD         DO BLOCK +1?               T002
         BZ    READBLK#                 NO                         T002
         AIF   (NOT &VSAM).NVSAM30                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    READBLK#                 YES, DON'T UPDATE TTR YET  T002
.NVSAM30 ANOP  ,                                                   T002
         IC    R15,TTR+2                GET RECORD NO
         LA    R15,1(R15)               NEXT RECORD
         STC   R15,TTR+2                NEXT ONE
         SPACE 1
READBLK# MVC   LINE05,BLANKS            CLEAR I/O ERR MSG IF ANY   U004
         CLI   CHNGED,0                 BUFFER TAMPERED WITH W/O CHKPT?
         BE    NORDBMSG                 NO, ITS OK TO READ THEN
         MVI   CHNGED,0                 WELL IT'LL BE CLEAN NOW
         NI    FLAGS2,255-MUSTZAP       NEW BLOCK, NO ZAP NEEDED YET
         LA    R1,N4                    MSG NUMBER
         BAL   R8,SETLINE               SETUP THE LINE
         B     NORDBMSG                 NOT PRINTING               U004
         USNGX LOGLINE,R2                                          U022
         MVC   LL@XMSG(18),=C'BLOCK NOT REPLACED'                  U022
         MVI   LL@CC,C'0'               DOUBLE SPACE               U022
         HEX   LL@TTR,OLDTTR,LEN=4  %%  DIFF TTR THIS TIME         U022
         DROPX R2                                                  U022
         BAL   R8,CPUT                  DUMP THE LINE
NORDBMSG XC    BLKLEN(2),BLKLEN         ASSUME NOTHING READ        T002
         AIF   (NOT &VSAM).NVSAM32                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    READVSM                  YES READ IT THEN           T002
.NVSAM32 ANOP  ,                                                   T002
         LA    R15,CCW##R               -> CCW CHAIN               U004
         CLI   TTR+2,0                  R0?                        U004
         BNE   *+8                      NO - OK                    U004
         LA    R15,CCW##R0              READ R0                    U004
         ST    R15,IOBCCWA              SET IN IOB                 U004
         BAL   R8,TTRCONV               TTR -> MBBCCHHR            T002
         LTR   R15,R15                  OUT OF DS?                 U004
         BNZ   EXCP42                   YES - DON'T BOTHER READING U004
         IC    R1,CCHHR+4               GET "R"                    U004
         BCTR  R1,0                     -1 FOR SEARCH ID           U004
         STC   R1,CCHHR+4               SET BACK                   U004
         SPACE 1
         EXCP  IOB                                                 U004
         SPACE 1
         WAIT  ECB=ECB                                             U004
         SPACE 1
         MVC   CCHHR+4(1),TTR+2         RESTORE "R"                U004
         LH    R2,BUFFSIZE              BUFSIZ (MAX READ=BLKSI)    U004
         SH    R2,CSW+6                 =AMOUNT READ               U004
         LR    R0,R2                    COPY 8+KL+DL               U004
         SH    R0,=H'8'                 GET KEYLEN+DATALEN         U004
         STH   R0,CCW#W#W+6             SAVE IT FOR LATER USE      U004
         STH   R0,LENKEYDA              SAVE FOR DISP? CMDS        T002
         L     R1,ADDRCNT               -> COUNT                   U004
         MVC   KEYLEN+1(1),5(R1)        SET UP KEYLEN              U004
         LH    R15,KEYLEN               GET KEYLEN                 U004
         LA    R0,8(R1,R15)             -> DATA                    U004
         ST    R0,ADDRDATA              SET ADDR OF DATA           U004
         CLC   ADDRBUFF,ADDRKEY         HOW MUCH DO WE DISPLAY?    U004
         BL    SAVE$LEN                 COUNT + KEY + DATA         U004
         BE    *+6                      KEY + DATA                 U004
         SR    R2,R15                   JUST DATA                  U004
         SH    R2,=H'8'                 NOT COUNT FIELD            U004
         SPACE 1
SAVE$LEN STH   R2,BLKLEN                SAVE IT FINALLY            U004
         SPACE 1
*  NOW CHECK THE READ, FINALLY...                                  U004
         CLI   ECB,X'7F'                OK?                        U004
         BE    EXCPOK                                              U004
         CLI   ECB,X'42'                END OF EXTENT              U004
         BE    EXCP42                                              U004
         CLI   ECB,X'41'                PERM ERROR?                U004
         BE    EXCP41                                              U004
         B     REALERR                                             T002
         AIF   (NOT &VSAM).NVSAM33                                 T002
         EJECT
READVSM  TM    FLAGVSAM,SEQREAD         SEQ READ?                  T002
         BZ    GETVSM                   NO, JUST GET RECORD        T002
         TM    FLAGVSAM,VSAMSEQK        READING IN KEY SEQ?        T002
         BO    GETVKEY                  YES, HANDLE KEYS WIERD     T002
         LH    R1,VSAMCISZ              GET CI SIZE                T002
         TM    FLAGVSAM,VSAMSEQB        BACKWARDS?                 T002
         BZ    *+6                      NO - CONTINUE FORWARDS     T002
         LNR   R1,R1                    YES - BACK UP 1 CI         T002
         A     R1,TTR                   GET NEXT TTR TO READ       T003
         ST    R1,TTR                   SAVE FOR VSAM              T002
         B     GETVSM                                              T002
         SPACE 1
GETVKEY  TM    DCBLIST,X'0F'            UPDATING YET?              T002
         BZ    READNEND                 NO, THEN JUST FIND CI      T002
         ENDREQ  RPL=RPLV               YES, FREE CI JIC           T002
         VSAMTST  ,                     ANY ERRORS?                T002
         SPACE 3
READNEND L     R1,ADDRKEYH              ASSUME FORWARD READING     T003
         TM    FLAGVSAM,VSAMSEQB        BACKWARD?                  T003
         BZ    *+8                      NO, KEEP LOOKING           T003
         L     R1,ADDRKEYL              YES, GET THE OTHER ONE     T003
         ST    R1,RPLVK+RPLARG-IFGRPL   SAVE FOR POINT             T003
         POINT  RPL=RPLVK               FIND THAT RECORD           T003
         VSAMTST  ,                     DID IT WORK?               T003
         GET   RPL=RPLVK                YES, GET THAT RECORD       T003
         VSAMTST  ,                     THAT SHOULD WORK ALSO      T003
         GET   RPL=RPLVK                FIND THE NEXT RECORD       T003
         VSAMTST  ,                     CHECK IT                   T002
         SHOWCB  RPL=(S,RPLVK),AREA=(S,TTR),LENGTH=L'TTR,          T002$
               FIELDS=(RBA),MF=(G,VSAMCB)                          T002
         VSAMTST  ,                                                T002
         XR    R0,R0                                               T002
         L     R1,TTR                   GET CURRENT TTR            T002
         LH    R15,VSAMCISZ             AND CI SIZE                T002
         DR    R0,R15                   RELATIVE CI NUMBER         T002
         MR    R0,R15                   AND NOW THE RBA OF CI      T002
         ST    R1,TTR                   SAVE FOR GET               T002
         SPACE 3
GETVSM   CLC   TTR,ENDTTR               PAST MAX VALUE?            T003
         BH    GETVSAMH                 YES, NO RECORD THERE       T003
         POINT RPL=RPLV                 WHERE TO GO                T002
         VSAMTST  ,                     WELL???                    T002
         GET   RPL=RPLV                 READ A BLOCK               T002
         VSAMTST  ,                     ERRORS?                    T002
         LH    R1,KEYLEN                GET KEYLEN                 T002
         LTR   R1,R1                    ANY HERE?                  T002
         BZ    READVNKY                 NO, THEN DONE FOR NOW      T002
         L     R15,ADDRKEYL             WHERE TO STICK THE KEY     T003
         BCTR  R1,0                     -1 FOR EXECUTE             T002
         L     R14,ADDRDATA             -> DATA RECORD             T002
         AH    R14,VSAMRKP              -> KEY IN DATA RECORD      T002
         MVC   0(0,R15),0(R14)          MOVE 1 BYTE                T002
         EX    R1,*-6                   AND GET THE REST           T002
         L     R15,ADDRDATA             -> DATA                    T002
         LR    R14,R15                                             T002
         AH    R15,VSAMCISZ             -> PAST CI                 T002
         SH    R15,=H'4'                -> CIDF                    T002
         AH    R14,0(,R15)              -> FREE PORTION OF CI      T002
         LH    R15,2(,R15)              LEN OF FREE                T002
         AR    R15,R14                  -> LAST RDF                T002
         TM    0(R15),X'7C'             ANY FLAGS ON?              T002
         BZ    *+8                      NO, USE THIS RDF           T002
         LA    R15,3(,R15)              YES, USE PREVIOUS RDF      T002
         SH    R14,1(,R15)              -> START OF LAST REC IN CI T002
         AH    R14,VSAMRKP              -> KEY WITHIN RECORD       T002
         L     R15,ADDRKEYH             -> WHERE IT GOES           T002
         MVC   0(0,R15),0(R14)          MOVE 1 BYTE                T002
         EX    R1,*-6                   MOVE THE REST TOO.         T002
         LA    R1,2(R1,R1)              GET KEYLEN*2               T003
         SPACE 1
READVNKY LH    R2,VSAMCISZ              GET RECLEN                 T002
         AR    R2,R1                    GET DATA+MIN AND MAX KEY   T003
         STH   R2,LENKEYDA              SAVE FOR USE BY DISP? CMDS T002
         CLC   ADDRBUFF,ADDRDATA        DISPKEY?                   T003
         BNE   *+6                      YES, JUST DATA LEN         T003
         SR    R2,R1                    JUST DATA LEN              T003
         STH   R2,BLKLEN                SAVE LEN OF BUFFER         T002
         B     EXCPOK$$                 EXIT OK                    T002
         EJECT
GETVSAMH LA    R1,=CL15'NOT IN CLUSTER' GET MESSAGE ADDR           T003
         B     EXCPERR                  FLAG AS ERROR              T003
         SPACE 2
.NVSAM33 ANOP  ,                                                   T002
REALERR  LA    R1,=CL15' TYPE UNKNOWN  '  GET MSG ADDR             U004
         SPACE 1
EXCPERR  SR    R0,R0                    GET A ZERO                 U004
         STH   R0,BLKLEN                NOTHING WAS READ           U004
         STH   R0,CCW#W#W+6             AND NOTHING TO WRITE.      U004
EXCPERR$ MVI   IOERROR,YESSYN           AND IT WAS IN ERROR        U004
         MVC   LINE05+17(40),IOERRMSG   MOVE IN MASK               U004
         MVC   LINE05+35(15),0(R1)      MOVE IN ERROR MSG          U004
         NI    FLAGVSAM,X'FF'-SEQREAD   TURN OFF SEQ READ FLAG     T003
         B     READEXIT                 RESTORE R8, AND RETURN     T002
         SPACE 2
EXCP42   LA    R1,=CL15'NOT IN DATA SET'  GET MSG ADDR             U004
         B     EXCPERR                                             U004
         SPACE 2
EXCP41   TM    CSW+4,X'01'              UNIT EXCEPTION?            U004
         BO    EXCPOK                   YES, EOF - TREAT AS DATA   U024
         TM    IOB+3,X'08'              NRF?                       U004
         BNO   NOT$NRF                  NO                         U004
         SPACE 1
EXCP$NRF TM    FLAGVSAM,SEQREAD         NEXT BLOCK REQ?            T002
         LA    R1,=CL15'NO RECORD FOUND'  GET MSG ADDR IN CASE     U004
         BZ    EXCPERR                                             T002
         LH    R15,TTR                  GET TRACK                  U004
         LA    R15,1(,R15)              NEXT TRACK                 U004
         STH   R15,TTR                  SAVE IT                    U004
         MVI   TTR+2,1                  RECORD 1                   U004
         NI    FLAGVSAM,X'FF'-SEQREAD   RESET SEQ READ FLAG        T003
         B     READBLK3                 GO REREAD                  T002
         SPACE 2
NOT$NRF  TM    CSW+5,X'40'              INCORRECT LENGTH?          U004
         BNO   REALERR                  NO - SOMETHING ELSE        U004
         LA    R1,=CL15'INCORR. LENGTH '  GET MSG ADDR             U004
         B     EXCPERR                  GO SAY INCORRECT LENGTH    U004
         SPACE 2
EXCPOK   L     R15,ADDRCNT              -> COUNT                   U004
         CLC   CCHHR(5),0(R15)          RIGHT RECORD?              U004
         BE    EXCPOK$                  YES - CONTINUE             U004
         LA    R1,=CL15'CCHHR INCORRECT'  GET MSG ADDR IN CASE     U004
         CLC   CCHHR(4),0(R15)          RIGHT CCHH?                U004
         BNE   EXCPERR$                 NO - ARRGH                 U004
         CLI   4(R15),1                 IS IT R1?                  U004
         BE    EXCP$NRF                 YES - IT'S REALLY NRF      U004
         B     EXCPERR$                 NO - ARRGH                 U004
         SPACE 2
EXCPOK$  TM    CSW+4,X'01'              UNIT EXCEPTION?            U024
         BO    EODAD                    YES - EOF                  U024
         NI    FLAGVSAM,X'FF'-SEQREAD   TURN OFF SEQ READ FLAG     T003
         OC    CSW+6(2),CSW+6           ANY RESIDUAL?              U004
         LA    R1,=CL15'BLKLEN > BUFFSZ'   -> MSG IN CASE          U004
         BZ    EXCPERR$                 NO - ERROR                 U004
EXCPOK$$ TM    FLAGS2,ATTNHIT           WAS ATTN SIGNALED LATELY?  T002
         BNO   READEXIT                 NO, ALL DONE, RETURN BLK   T002
         BAL   R8,CLEAR                 YES, CLEAR SCREEN
         MVC   LINE19+17(40),=CL40'         *****  HOLDING  ******'
         L     R8,LINKREAD              RESTORE R8                 T002
         B     SETPNT00                 DISP OFF 0, CURR BLK       U004
         SPACE 3
READEXIT L     R8,LINKREAD              RESTORE R8                 T002
         BR    R8                       AND RETURN TO CALLER       T002
         EJECT
**********************************************************************
*******************************  CALLEXP  ************************ U004
**********************************************************************
         SPACE 3
*  CALL EXP, CHECK ERROR RETURNS                                   U004
*                                                                  U004
*  R1 -> STRING                                                    U004
*  R0  = LENGTH                                                    U004
*  R8  = RETURN ADDR - NOTE: CC IS SET ACCORDING RESULT'S SIGN     U004
         SPACE 1
CALLEXP  LR    R2,R15                   COPY "$" VALUE             U004
         L     R15,=A(EXP)              -> ROUTINE                 U004
         BALR  R14,R15                  CALL IT                    U004
         LTR   R1,R1                    WORK?                      U004
         BNM   INVEXPER                 NO                         U004
         LTR   R15,R15                  SET CC                     U004
         BR    R8                       RETURN TO CALLER           U004
         SPACE 5
**********************************************************************
*******************************  HEXCHECK  ***************************
**********************************************************************
         SPACE 3
*  VALIDITY CHECK FOR HEXADECIMAL, DECIMAL, OR CHARACTER
*
*  R7 = POINTER TO STRING
*  R1 = LENGTH OF STRING
*  R8 = RETURN ADDR - NOTE: 0(R8)=GOOD HEX, 4(R8)=BAD HEX AT 0(R7)
*  R15= 0 IF PURE DECIMAL UP TO EXIT, 4 IF HEX
         SPACE 1
HEXCHECK XR    R15,R15                  ASSUME DECIMAL
HEXCHK   TM    0(R7),X'F0'              NUMBER?
         BO    OKHEX                    YES - ITS OK
         CLI   0(R7),C'A'               LESS THAN ALPHA?
         BL    4(R8)                    YES, MUST BE CHARACTER
         CLI   0(R7),C'F'               'F' OR SMALLER?
         BH    4(R8)                    NO - MUST BE CHARACTER
         LA    R15,4                    INDICATE HEX (NOT DECIMAL)
OKHEX    LA    R7,1(R7)                 SO FAR SO GOOD, UPDATE HEX PTR
         BCT   R1,HEXCHK                DO THE WHOLE STRING
         BR    R8                       WE'RE OK - ALL DONE
         EJECT
**********************************************************************
********************************  GETSTRNG  **************************
**********************************************************************
         SPACE 3
*  PARSE OUT HEX OR CHARACTER STRING FOR S, X, O, N, L AND SET COMMS.
*  DETERMINE STRING LENGTH AND STRING ITSELF, AND SAVE BOTH IN AREA.
*
*  IF DECIMAL - A FULLWORD IS STORED (< 2147483647.)
*  IF HEXADECIMAL - UP TO 16 DIGITS (DOUBLEWORD) MAY BE STORED
*  IF CHARACTER - ANY DELIMITERS MAY SURROUND THE STRING (EXCEPT
*                 HEX DIGITS) AND UP TO 16 CHAR (2 DOUBLEWORDS) MAY
*                 MAY BE STORED.
*
*  R3 = ACTUAL LENGTH OF STRING TO BE PARSED
*  R4 = PTR TO THE STRING ITSELF
*  R5  =  PTR TO STRING SAVEAREA  (1ST BYTE = LEN)
         SPACE 1
GETSTRNG LA    R1,1                     LEN OF HEX TO BE CHECKED
         LR    R7,R4                    LOC OF HEX TO BE CHECKED
         BAL   R8,HEXCHECK              DO IT
         B     HEXSTR                   FIRST CHAR HEX - SO HEX STRING
*
*  CHARACTER STRING
*
         CH    R3,=H'18'                STRING LONGER THAN 16 CHARS?
         BH    LENERROR                 YES - SAY LENGTH ERROR     U004
         XC    TEMPTRT(256),TEMPTRT     CLEAR OUT TRTAB
         SR    R1,R1                    CLEAR OUT WORK REG
         IC    R1,0(,R4)                GET DELIM FOR CHAR STRING
         STC   R1,TEMPTRT(R1)           PUT IT IN RIGHT PLACE IN TRTAB
         SH    R3,=H'2'                 GET ADJUST STRING LEN (DELIMS)
         BNP   LENERROR                 HE ENTERED S'' OR LESS - ERROR
DELIMTRT TRT   1(0,R4),TEMPTRT          <<< EXECUTED >>>
         EX    R3,DELIMTRT              FIND THE OTHER DELIMITER
         LA    R2,=CL20'NO ENDING DELIMITER'   GET MSG ADDR IN CASE
         BZ    BOTCH                    NO - TELL HIM              U004
         LA    R3,1(,R4)                PT TO BEGINNING OF STRING
         SR    R1,R3                    GET STRING LEN (NO DELIMS)
         LR    R3,R1                    PUT IN RIGHT REG
         BCTR  R3,0
         LTR   R3,R3                    NULL INPUT?
         BM    LENERROR                 YES - SAY LENGTH ERROR
CHARMVC  MVC   2(0,R5),1(R4)            <<< EXECUTED >>>           U004
         EX    R3,CHARMVC               MOVE STRING CHARS TO SAVE AREA
         B     FINSTR                   GO FINISH THE JOB
         SPACE 2
LENERROR LA    R2,LENERR                YES - SAY LENGTH ERROR
         B     BOTCH                    GO TELL HIM
*
*  HEX STRING
*
HEXSTR   CH    R3,=H'16'                HEX STRING LONGER THAN 16?
         BH    LENERROR                 YES - SAY LENGTH ERROR
         LR    R1,R3                    PUT IT IN RIGHT REG FOR CHECK
         LR    R7,R4                    POINT TO LOC TO CHECK
         BAL   R8,HEXCHECK              CHECK THE HEX
         B     OK1#                     ITS GOOD
         CLI   0(R7),C' '               IS IT HEX?
         BE    OK1#                     YES, GO DO IT
         CLI   0(R7),C'.'               DECIMAL?
         BE    DECMAYBE                 MAYBE SO
         LA    R2,=CL20'INVALID HEXADECIMAL'   IT'S BAD - PT TO MSG
         B     BOTCH                    TELL HIM
OK1#     TR    0(16,R4),TRHEX           YES - START TO CONVERT TO HEX
         CH    R3,=H'15'                IS THIS THE MAX CASE?
         BL    HEXPACK                  NO, DO JUST ENOUGH
         PACK  TEMP(5),0(9,R4)          PACK FIRST GROUP
         PACK  TEMP+4(5),8(9,R4)        PACK SECOND GROUP
         BH    HEX16                    SKIP IF 16 DIGITS          U004
         LM    R14,R15,TEMP             GET THE RESULT             U004
         SRDL  R14,4                    DOWN 1 NIBBLE              U004
         STM   R14,R15,TEMP             RESTORE IT CORRECTLY       U004
HEX16    LA    R3,7                     NUMBER OF BYTES (EX LEN)
         MVC   2(8,R5),TEMP             GET HEX                    U004
         B     FINSTR                   AND THEN RETURN TO CALLER
* R3 IS NOT BCTRED BECAUSE WE NEED A FLIP BYTE ON THE END
HEXPACK  PACK  TEMP(8),0(0,R4)          <<< EXECUTED >>>
         EX    R3,HEXPACK               PACK TO MAKE REAL HEX
         LA    R3,1(,R3)                ROUND UP ...
         SRL   R3,1                     AND FIND NUMBER OF BYTES
         LA    R15,TEMP+7               POINT TO GORF BYTE
         SR    R15,R3                   POINT TO FIRST BYTE
         BCTR  R3,0                     GET EXECUTE LENGTH
MOVEHEX  MVC   2(0,R5),0(R15)           <<< EXECUTED >>>           U004
         EX    R3,MOVEHEX               MOVE HEX TO OUTPUT LOCATION
FINSTR   STH   R3,0(,R5)                SAVE THE LEN IN SCAN AREA  U004
         BR    R6                       RET TO CALLER
*
*  DECIMAL STRING
*
DECMAYBE BXLE  R15,R15,DECOK            IF REALLY DECIMAL - JUMP
         LA    R2,INVDEC                NO - TELL HIM SO BECAUSE...
         B     BOTCH                    ITS GOT A-F IN IT
DECOK    CH    R3,=H'10'                APPROACHING LEN ERR (BCTR'D)?
         BL    DECOK#                   NO - DON'T WORRY ABOUT IT
         CLC   0(11,R4),=C'2147483647.' TOO BIG FOR FULLWORD?
         BH    LENERROR                 YES - TELL HIM AND GET OUT
DECPACK  PACK  TEMP(8),0(0,R4)          <<< EXECUTE >>>
DECOK#   LA    R0,1(R4)                 POINT TO STRING + 1 FOR TO....
         SR    R7,R0                    GET EXECUTE LENGTH
         EX    R7,DECPACK               PACK UP YOUR DECIMALS IN ...
*        COMMENT TYPE=CONTINUED         IN YOUR OLD DOUBLE WORD
         CVB   R1,TEMP                  AND CONVERT, CONVERT, CONVERT
         ST    R1,TEMP                  SAVE IN ALIGNED PLACE
         MVC   2(4,R5),TEMP             COPY TO OUTPUT STRING      U004
         MVI   1(R5),3                  EXECUTE LENGTH             U004
         BR    R6                       RETURN
         SPACE 5
**********************************************************************
*********************************  NUMCONV  ********************** U004
**********************************************************************
         SPACE 2
*  R15 = NUMBER TO BE CONVERTED                                    U004
*  R14 = RETURN ADDR                                               U004
*  NUMBER IS CONVERTED TO 12 SIGNED DECIMAL DIGITS AT TEMP2,       U004
*  AND TO HEX AT TEMP2+12 (8 CHARS)                                U004
         SPACE 1
NUMCONV  ST    R15,TEMP                 SET FOR UNPK               U004
         UNPK  TEMP2+12(8+1),TEMP(4+1)                             U004
         TR    TEMP2+12(8),TRHEX        MAKE EBCDIC                U004
         CVD   R15,TEMP                 GET IN PACKED FORM         U004
         LA    R1,TEMP2+11              INIT LOC FOR FLOATING SIGN U004
         MVC   TEMP2(12),EDMASK         MOVE IN EDIT MASK          U004
         EDMK  TEMP2(12),TEMP+2         GET 11 DECIMAL DIGITS      U004
         LTR   R15,R15                  IS IT NEGATIVE?            U004
         BNMR  R14                      NO - ALL DONE              U004
         BCTR  R1,0                     YES - SO BACK UP AND...    U004
         MVI   0(R1),C'-'               ...PUT IN THE SIGN         U004
         BR    R14                      NOW DONE                   U004
         SPACE 5
**********************************************************************
*********************************  CSIO  *****************************
**********************************************************************
         SPACE 3
*  MAIN SCREEN DISPLAY.
*  CREATE TRACE TABLE ENTRY IF NECESSARY.
*  DISPLAY WITH TPUTS FOR NOW
         SPACE 1
CSIO     MVC   PREVREP,REP              SAVE FOR TWICE TEST        U012
         TM    TRACE,NOTRACE            TRACE FOR THIS GUY?
         BO    NOTRACIT                 NO - SO DONT
*
*  MAKE THE TRACE TABLE ENTRY
*
         L     R1,ITRAVAL               PICK UP NEXT SPOT IN TABLE
         CLI   0(R1),X'FF'              END?
         BNE   MORETAB                  NO - THERE'S MORE
         L     R1,AITRCTAB              YES - START OVER AT TOP    U004
         ST    R1,ITRAVAL               AND RESET AVAIL POS PTR
MORETAB  MVC   0(4,R1),TTR              MOVE TTR/RBA TO TABLE      T002
         MVC   4(2,R1),OLDPOINT         AND OFFSET TOO             T002
         ST    R1,CURRITR               SAVE PTR TO CURR TRACE ENTRY
         LA    R1,ITRLEN(,R1)           UPDATE AVAIL PTR           T002
         ST    R1,ITRAVAL               SAVE IT
NOTRACIT MVI   TRACE,YESTRACE           MAKE IT TRACE AGAIN
         TM    FLAGS3,NODISPF           DISPLAY SUPPRESSED ?       U013
         BO    NOTSHORT                 YES, DON'T FIX STUFF TWICE U013
         SPACE 1
         CLI   LINELEN+1,80             SHORT LINES?               U004
         BE    NOTSHORT                 NO - OK AS IS              U004
         MVC   LINE01($L-17),LINE01+17  SHIFT INFO LINES OVER      U011
         MVC   LINE05($L-17),LINE05+17  SHIFT INFO LINES OVER      U004
         MVC   LINE19($L-17),LINE19+17  SHIFT INFO LINES OVER      U004
         MVC   LINE20($L-17),LINE20+17  SHIFT INFO LINES OVER      U004
         MVC   LINE21($L-17),LINE21+17  SHIFT INFO LINES OVER      U004
NOTSHORT TM    FLAGS3,M3270F            FULLSCREEN?                U014
         BO    CSIO3270                 YES, GO TO HIS STUFF       U013
         TM    FLAGS3,NODISPF           DISPLAY SUPPRESSED ?       U013
         BO    TGET                     MAKE IT SIMPLE, STUPID     U013
         TM    FLAGS3,CRTF              CRT MODE?                  U014
         BZ    NOFORMFD                 NO - DON'T CLEAR THE SCRN  U004
         TPUTX =X'0C',1,CONTROL         CLEAR THE SCREEN           U004
         SPACE 2
NOFORMFD LA    R6,$I                    BXLE INCR                  U004
         LA    R1,LINE05                TPUT I/O ERROR MSG
         BAL   R14,PUTLINE              (IF ANY)                   U004
         SPACE 1
         L     R5,MIDLINE               ADDR OF CARET
         LTR   R5,R5                    ANY CARET (BLK DISPLAY?)
         BNZ   DOWINDOW                 YES, WINDOW SCREEN
         LA    R3,LINE06                NO, FULL SCR, BEGIN
         LA    R5,LINE18                END (BXLE)
         B     DUMPSCR                  DUMP IT
         SPACE 2
*  CREATE TSO SCREEN WINDOW. DEFAULT SIZE IS ONLY CARET LINE. THE
*  'W' COMMAND CHANGES IT TILL NEXT 'W' COMMAND.  LINES ABOVE AND
*  BELOW 'CARET' LINE ARE SPECIFIED.  THIS ALLOWS FEWER LINES FOR
*  HARDCOPY TERMINALS.  EG:
*
*  D,U ... U LINES ABOVE, CARET LINE, D LINES BELOW
*  ,U .... U LINES ABOVE, CARET LINE, PREVIOUS D LINES BELOW
*  D×D, .. PREVIOUS U LINE ABOVE, CARET LINE, D LINES BELOW
*
         SPACE 1
DOWINDOW S     R5,FIRSTSCR              OFFSET TO SCREEN CARET
         XR    R4,R4                    DIVIDE
         DR    R4,R6                    LINE NUMBER CARET
         LR    R3,R5                    SAVE IT
         S     R3,WIDTHU                BACK UP 'U' LINES <UP>
         BNM   *+6                      SKIP IF OK
         XR    R3,R3                    TOO FAR, MAKE IT TOP OF BUFF
         A     R5,WIDTHD                GO DOWN 'D' LINES <DOWN>
         LA    R0,(LINE18-LINE06)/$I    HIGHEST VALID LINE #       U024
         CR    R5,R0                    BOTTOM TOO HIGH
         BNH   *+6                      NO
         LR    R5,R0                    YES, MAKE IT MAX LINE#
         MR    R2,R6                    GET OFFSET TO TOP LINE
         MR    R4,R6                    GET OFFSET TO BOTT LINE
         A     R3,FIRSTSCR              POINT TO TOP
         A     R5,FIRSTSCR              POINT TO BOTTOM
DUMPSCR  LR    R4,R6                    GET LENGTH IN BXLE REG
         SPACE 1
*  R3=BEGIN, R4=LEN, R5=END
         SPACE 1
TPUTL    LR    R1,R3                    COPY LINE ADDR             U004
         BAL   R14,PUTLINE              PRINT IT IF NOT BLANK      U004
         BXLE  R3,R4,TPUTL
         SPACE 2
         LA    R2,LINE19                POSSIBLE MSGS BELOW WINDOW
         LA    R3,3                     3 LINES
TPUTL2   LR    R1,R2                    COPY LINE ADDR             U004
         BAL   R14,PUTLINE              PRINT IT IF NOT BLANK      U004
         AR    R2,R6                    DO ALL
         BCT   R3,TPUTL2
         SPACE 2
TGET     MVI   TSECB,0                  RESET ECB                  U004
         MVC   PGPB(PGPBDCL),PGPBDC     INIT PARM BLOCK
         NI    FLAGS2,255-ATTNHIT       RESET FLAG                 U004
         L     R15,IKJPTGT              ADDR OF LOADED PUTGET
         PUTGET PARM=PGPB,MF=(E,IOPL),ENTRY=(15)
         LA    R14,8                                               U004
         CR    R15,R14                                             U004
         BL    *+16       >==========+                             U004
         BE    TGET                  ×  ATTN, TRY AGAIN            U004
         EX    0,*                   ×                             U004
         B     TGET                  ×  RESUME IF TEST RESTARTS ME U004
         NI    FLAGS3,255-NODISPF  <=+  STOP SUPPRESSING DISPLAY   U013
         L     R1,PGPBIBUF              POINT TO INPUT LINE
         LH    R15,0(,R1)               GET LENGTH OF BUFFER
         SH    R15,=H'4'                GET LENGTH OF REPLY
         STH   R15,READLEN              SAVE REPLY LNTH
         BZ    TGET1                    IF NO REPLY SKIP MOVE TO REPLY
         CH    R15,=Y($L)               COMPARE WITH MAX           U004
         BNH   TGET0                    OK
         LH    R15,=Y($L)               USE MAX                    U004
         STH   R15,READLEN              SAVE IT
TGET0    DC    0H'0'
         MVC   REP,BLANKS               PRE-BLANK THE BUFFER       U014
         BCTR  R15,0                    DECREMENT FOR EXECUTE
         MVC   REP(*-*),4(R1)           EXECUTED
         EX    R15,*-6                  MOVE IN THE REPLY
TGET1    LH    R15,0(,R1)               GET LNTH AGAIN
         LA    R0,1                     GET SP
         SLL   R0,24                    SHIFT INTO PLACE
         AR    R0,R15                   ADD IN LENGTH
         FREEMAIN R,LV=(0),A=(1)
         LH    R1,READLEN               GET REPLY LENGTH
         MVC   LINE19,BLANKS            CLEAR ERROR MSG IF ANY     U004
         XC    MIDLINE,MIDLINE          NO CARET LINE NOW
         BR    R8                       YES, PARSE IT
         SPACE 3
*  THIS HUNK OF CODE MOVED DOWN HERE FROM SOMEWHERE IN "DISPLAY"   U013
CSIO3270 TM    FLAGS3,FSMODE            IS FSMODE ON ?             U014
         BO    CSIOINIT                 YES, THAT'S GOOD           U014
         SPACE 1
         AIF   (&MVT).MVT03                                        U014
         STFSMODE  ON,INITIAL=YES       SET 3270 FULLSCREEN MODE   U015
         SPACE 1
.MVT03   OI    FLAGS3,FSMODE+RESHOWF    IT'S ON NOW                U015
         MVI   CTL3270+1,X'F5'          CHANGE TO ERASE WRITE      U024
         SPACE 1
CSIOINIT MVC   LINE01,BLANKS            CLEAR THE TOPLINE          U013
         MVC   LINE01+33(7),=C'Z  A  P' MOVE TOPLINE               U011
         SPACE 2
         LA    R1,REP+$L-1              END OF REPROMPT BUFFER     U013
CSIOCLR  CLI   0(R1),C' '               TRAILING BLANK?            U011
         BNE   CSIOCLRX                 NO, LEAVE THE LOOP         U011
         MVI   0(R1),X'00'              CHANGE TO TRAILING NULL    U011
         BCT   R1,CSIOCLR               GO BACK AND CHECK AGAIN    U011
CSIOCLRX L     R2,AOLDSCR               -> SAVED LINE1             U013
         LA    R4,LINE01-2              -> CURRENT LINE1           U013
         TM    FLAGS3,RESHOWF           FULL SCREEN TPUT?          U014
         BNO   CSIOCHNG                 NO, PROCESS CHANGED LINES  U014
         LA    R3,L'OLDSCR              GET SAVE AREA LENGTH       U013
         LR    R5,R3                    COPY LENGTH FOR MVCL       U013
         MVCL  R2,R4                    SAVE INITIAL SCREEN IMAGE  U013
CSIOFULL LM    R0,R1,REGS3270           GET THE TPUT REGS          U011
         B     CSIOTPUT                 GO WRITE THE WHOLE SCREEN  U011
CSIOCHNG L     R3,ASCRWORK              -> WORK AREA               U011
         MVC   0(CTL3270L,R3),CTL3270   COPY CONTROL INFO          U024
         LA    R3,CTL3270L(,R3)         BUMP PTR                   U024
         SR    R5,R5                    INITIAL SCREEN ADDRESS     U011
         SPACE 1
         MVI   $I+2(R2),X'FE'           FORCE LINE02 MISMATCH      U011
         MVI   3*$I+2(R2),X'FE'         FORCE LINE04 MISMATCH      U011
         LA    R6,21                    NUMBER OF LINES TO TEST    U011
         BAL   R14,CSIOTMOD             GO CHECK ONE               U005
         BCT   R6,*-4                   CHECK ALL 21               U011
         SPACE 1
         MVC   0(END3270L,R3),END3270   MOVE IN ENDING CTL STUFF   U024
         LA    R0,END3270L(,R3)         GET END+1 ADDR             U024
         L     R1,ASCRWORK              -> START OF WORK AREA      U005
         SR    R0,R1                    GET LENGTH TO TPUT         U005
         BP    *+8                      CONTINUE IF OK             U005
         EX    0,*                      DIE IF LOGIC ERROR         U005
         ICM   R1,B'1000',=X'03'        GET FULLSCR TPUT FLAG      U005
         SPACE 2
CSIOTPUT TPUT  (1),(0),R                                           U004
         SPACE 1
         NI    FLAGS3,255-RESHOWF-NODISPF   REFRESH IS DONE        U013
         LA    R1,REPX                  BUFFER ADDR                U004
         ICM   R1,B'1000',=X'81'        MAKE IT A TGET ASIS        U006
         LA    R0,L'REPX                LENGTH OF INPUT BUFFER     U004
         TGET  (1),(0),R                                           U011
         SPACE 1
         MVI   CTL3270+1,X'F1'          CHANGE TO NORMAL WRITE     U024
         NI    CTL3270+2,255-X'04'      CHANGE WCC TO NO ALARM     U024
         CLI   REPX,X'6E'               PA2?  (RESHOW?)            U006
         BE    CSIOFULL                 YES - GO DO IT             U013
         LA    R2,REP                   THIS IS WHERE IT WILL GO   U011
         LA    R3,REPX+3                POINT SRC PAST CURSOR ADDR U006
         LA    R4,REPX-1(R1)            END OF REPLY DATA          U006
         CR    R3,R4                    TOO FAR?                   U011
         BH    ASIS08                   YES, NO DATA CAME IN       U011
         CLI   0(R3),X'11'              SBA SEQUENCE?              U011
         BE    ASIS02                   YES, SCREEN IS FORMATTED   U011
         OI    FLAGS3,RESHOWF           NO, FORCE FULL REWRITE     U011
ASIS02   CR    R3,R4                    TOO FAR?                   U006
         BH    ASIS08                   YES, NO DATA CAME IN       U006
         CLI   0(R3),X'11'              SBA SEQUENCE?              U011
         BNE   ASIS05                   NO, DON'T WORRY ABOUT IT   U011
         CLC   1(2,R3),=X'C1D1'         FIRST LINE INPUT?          U011
         BE    ASIS04                   YES, ALL'S COOL SO FAR     U011
         CLC   1(2,R3),=X'C3F1'         SECOND LINE INPUT?         U011
         BE    ASIS03                   YES, GO CLEAR REP          U011
         TPUTX '** SCREEN FORMAT ERROR **'   TOO BAD               U006
         B     CSIOFULL                 GIVE HIM BACK WHOLE SCREEN U013
         SPACE 1
ASIS03   MVC   REP,BLANKS               CLEAR REMEMBERED INPUT     U011
ASIS04   LA    R3,3(,R3)                SKIP THE SBA SEQUENCE      U011
         B     ASIS02                   AND TRY SOME MORE          U011
         SPACE 1
ASIS05   CLI   0(R3),C' '               LEADING BLANK?             U011
         BNE   ASIS06                   NO, FOUND DATA             U011
         LA    R3,1(,R3)                SKIP THE BLANK             U011
         B     ASIS02                   GO LOOK FOR MORE           U011
         SPACE 1
ASIS06   MVC   REP,BLANKS               FORCE CLEAR REPLY          U011
ASIS07   CR    R3,R4                    TOO FAR?                   U006
         BH    ASIS08                   YES, FILL IT UP NOW        U006
         CLI   0(R3),X'11'              SBA?                       U006
         BE    ASIS08                   YES, IGNORE THE 2ND LINE   U006
         MVC   0(1,R2),0(R3)            MOVE ONE MORE BYTE         U006
         LA    R2,1(,R2)                OK, UP THIS ONE            U006
         LA    R3,1(,R3)                AND THE FROM POINTER, TOO  U006
         B     ASIS07                   CONTINUE THIS TRAVESTY     U006
         SPACE 1
ASIS08   LA    R4,REP                   COMPUTE THE REAL           U006
         SR    R2,R4                      LENGTH OF THE REPLY      U006
         STH   R2,READLEN                   AND SAVE IT FOR LATER  U006
         MVC   LINE19,BLANKS            CLEAR THE ERROR MSG IF ANY U004
         BR    R8                       GO PARSE IT                U004
         SPACE 2
CSIOTMOD CLC   0($I,R2),0(R4)           DOES IT MATCH ?            U011
         BE    CSIONMOD                 YES - SKIP                 U005
         MVI   0(R3),X'11'              SBA                        U011
         LR    R0,R5                    COPY ADDRESS               U011
         STC   R0,2(,R3)                STORE LOW 8 BITS           U011
         NI    2(R3),B'00111111'        MAKE THAT LOW 6 BITS       U011
         SRL   R0,6                     DOWNSHIFT                  U005
         STC   R0,1(,R3)                STORE SOME MORE BITS       U011
         NI    1(R3),B'00111111'        MAKE THAT HIGH 6 BITS      U011
         TR    1(2,R3),TR3270           MAKE VALID CHARS           U011
         MVC   0($I,R2),0(R4)           UPDATE SAVED SCREEN        U011
         MVC   3($I,R3),0(R4)           MOVE IN NEW DATA           U011
         LA    R3,$I+3(,R3)             -> NEXT SCRWORK AREA       U011
         SPACE 1
CSIONMOD LA    R2,$I(,R2)               -> NEXT SCREEN LINE        U011
         LA    R4,$I(,R4)               -> NEXT SCREEN LINE        U011
         LA    R5,$I-1(,R5)             =NEXT SCREEN OFFSET        U011
         BR    R14                      RETURN TO ABOVE            U005
         EJECT
**********************************************************************
*********************************  CSIO#  ****************************
**********************************************************************
         SPACE 3
*  SECONDARY DISPLAY.
*  SHORT SCREEN MSGS
*
*  R1 = ADDR OF BUFFER (40 BYTES ONLY)
         SPACE 1
CSIO#    MVC   LINEBUFF(40),0(R1)       COPY THE LINE              U004
         BAL   R8,KILL3270              TURN OFF 3270 MODE IF ON   U004
         BAL   R14,PUTLINE$             PUT THE SHORT LINE         U004
         SPACE 2
         L     R9,=A(END)               POINT TO SUBCOMMAND        U021
         BR    R9                       QUIT THIS CP               U021
         SPACE 5
*********************************************************************
**********************************  PUTLINE  ********************* U004
*********************************************************************
         SPACE 3
PUTLINE$ MVC   LINEDESC(4),=H'44,0'                                U004
         B     PUTLINEX                                            U004
         SPACE 2
PUTLINE  CLC   0($L,R1),BLANKS          BLANK LINE?                U004
         BER   R14                      YES - DON'T TPUT IT        U004
         MVC   LINEBUFF($L),0(R1)       COPY OUTPUT LINE           U004
         MVC   LINEDESC(4),=Y($L+4,0)   SET DESCRIPTOR WORD        U004
         SPACE 1
PUTLINEX TM    FLAGS2,ATTNHIT           STOP???                    U014
         BOR   R14                      YES.                       U004
         ST    R14,TERMSAVE             SAVE RETURN ADDR           U004
         MVC   PTPB(PTPBDCL),PTPBDC     INIT PARM BLOCK            U004
         LA    R0,LINEDESC              POINT TO O.L.D.            U004
         ST    R0,PTPB+4                SET ADDR IN PARM BLOCK     U004
         L     R15,IKJPUTL                                         U004
         MVI   TSECB,0                  RESET ECB                  U004
         SPACE 1
         PUTLINE  PARM=PTPB,MF=(E,IOPL),ENTRY=(15)                 U004
         SPACE 1
         LA    R14,4                                               U004
         CR    R15,R14                                             U004
         L     R14,TERMSAVE             RESTORE RETURN ADDR        U004
         BNHR  R14                                                 U004
         EX    0,*                                                 U004
         SPACE 5
**********************************************************************
**********************************  TTRPARSE  ************************
**********************************************************************
         SPACE 3
*  DETERMINES AND VALIDITY CHECKS TTR SPECIFIED.
*  R1 POINTS TO THE TTR;  R0 HAS ITS LENGTH;  OUTPUT IS IN TEMP+1(3)
         SPACE 1
TTRPARSE L     R15,TTR                  GET '*' = CURRENT TTR
         AIF   (NOT &VSAM).NVSAM34                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    *+8                      YES, RBA'S ARE 4 BYTES     T002
.NVSAM34 ANOP  ,                                                   T002
         SRL   R15,8                    RIGHT JUSTIFY
         BAL   R8,CALLEXP               GO PARSE                   U004
         BL    TTRLT1                   NEGATIVE IS INVALID        T002
         ST    R15,TEMP                 SAVE THE VALUE RETURNED    U004
         AIF   (NOT &VSAM).NVSAM35                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BOR   R6                       YES, RBA'S ARE ABSOLUTE    T002
.NVSAM35 ANOP  ,                                                   T002
         CLI   TEMP+3,0                 REC 0?                     U004
         BNER  R6                       NO - GIVE IT               T002
         LA    R2,REC0INV               YES - TELL HIM
         B     BOTCH                    WITH A MSG
         SPACE 2
TTRLT1   LA    R2,TTRSMALL              POINT TO ERROR MSG
         AIF   (NOT &VSAM).NVSAM53                                 T003
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T003
         BZ    BOTCH                    NO, MESSAGE IS CORRECT     T003
         LA    R2,RBASMALL              POINT TO CORRECT MSG       T003
.NVSAM53 ANOP  ,                                                   T003
         B     BOTCH                    DISPLAY ERROR STYLE
         EJECT
**********************************************************************
**********************************  PREPDUMP  ************************
**********************************************************************
         SPACE 3
*  THIS ROUTINE IS THE 'PREPROCESSOR' TO THE DUMPER.  IT GETS THE
*  BLOCK TO BE DUMPED, PREPARES AND WRITES OUT THE BLOCK HEADER
*  LINE (WITH TTR, LENGTH, AND CCHHR), AND IF THERE IS AN EOF OR
*  I/O ERROR AT THE BLOCK, AND MESSAGE IS PUT OUT TO THE RECORDING
*  FILE AND THE 'BLOCK' IS NOT DUMPED.
*  RETURNS:  0(R6) - NORMAL RETURN WITH DUMP OF BLOCK TO FOLLOW.
*            4(R6) - I/O ERROR OR EOF, RETURN WITH NO DUMP, OR EXIT
*                    FROM ENTIRE DUMPING LOOP IF I/O ERROR WAS
*                    'OUT OF DATASET', MEANING THERE IS NO MORE
*                    DATASET, NO MORE EXTENT.
         SPACE 1
PREPDUMP OI    FLAGS2,NOEOF             SAY ALSO EOF'S ARE DATA    T002
         BAL   R8,READBLK               GET THE BLOCK, SUCH AS IT IS
PREPDMPX TM    FLAGS2,LOGF              ARE WE LOGGING
         BNO   NEWDSPNT                 IF NOT FORGET THIS
         L     R2,CSOUTWK               POINT TO THE OUTPUT WORKAREA
         LTR   R2,R2                    IS THERE ONE?
         BZ    NEWDSPNT                 NO - FORGET THIS WHOLE THING
         L     R2,0(,R2)                GET ADDR IN REG
         USNGX LOGLINE,R2                                          U022
         AIF   (NOT &VSAM).NVSAM36                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    PREPDMPV                 YES, INDICATE RBA'S        T002
.NVSAM36 ANOP  ,                                                   T002
         MVC   LOGLINE+1(40),=C'LENGTH=XXXXX TTR=XXXXXX CCHHR=XXXXXXXXX$
               X'                                                  U022
         LH    R1,BLKLEN                GET THE LENGTH OF THE BLK
         CVD   R1,TEMP                  PACKED DEC
         UNPK  LOGLINE+1+7(5),TEMP+5(3) PUT LENGTH INTO MSG        U022
         OI    LOGLINE+1+11,X'F0'       FIX FLIP BYTE              U022
         HEX   LOGLINE+1+17,TTR,LEN=3   CONVERT TTR                U022
         HEX   LOGLINE+1+30,CCHHR,LEN=5 CONVERT CCHHR              U022
         AIF   (NOT &VSAM).NVSAM37                                 T002
         B     PREPDMPC                 RESUME  PREPROCESSING      T002
         SPACE 2
PREPDMPV MVC   LOGLINE+1(18),=C'LENGTH=XXXXX RBA='                 U022
         LH    R1,BLKLEN                GET LEN OF BLOCK           T002
         CVD   R1,TEMP                  DECIMAL                    T002
         UNPK  LOGLINE+1+7(5),TEMP+5(3) GET LEN                    U022
         OI    LOGLINE+1+11,C'0'        NUMERIC                    U022
         L     R15,TTR                  GET RBA OF RECORD          T002
         BAL   R14,NUMCONV              DECIMAL                    T002
         MVC   LOGLINE+1+18(11),TEMP2+1 GET VALUE                  U022
PREPDMPC MVI   LL@CC,C'-'               TRIPLE SPACE BEFORE HDR    U022
         AGO   .VSAM05                                             T002
.NVSAM37 ANOP  ,                                                   T002
         MVI   LL@CC,C'-'               TRIPLE SPACE BEFORE HDR    U022
.VSAM05  ANOP  ,                                                   T002
         DROPX R2                                                  U022
         MVI   ENQIT,255                FORCE ENQ TO PRINT
         BAL   R8,CPUT#                 PUT OUT THE LINE
         CLI   IOERROR,YESSYN           I/O ERROR IN READ BEFORE?
         BE    OOPSDUMP                 YES - DON'T DUMP, JUST MSG
         CLC   LINE05+27(20),EOFMSG     NO - BUT WAS THERE AN EOF? U004
         BNER  R6                       NO - RETURN NORMALLY AND DUMP
         SPACE 1
*  EITHER THE BLOCK HAS AN I/O ERROR WAS AN END-OF-FILE, SO WE CANNOT
*  DUMP.  INSTEAD, PRINT OUT, IF POSSIBLE, THE TYPE OF CONDITION
*  WHICH HAS OCCURRED, AND RETURN BEYOND DUMP CODE - 4(R6).
*  IF THE I/O ERROR 'NOT IN DATASET' HAS OCCURRED, THEN STOP THE
*  PROCESS ALTOGETHER.  WE ARE AT THE REAL END OF THE DATASET.
         SPACE 1
OOPSDUMP TM    FLAGS2,LOGF              ARE WE LOGGING      %%NEED%%
         BNO   NEWDSPNT                 IF NOT FORGET IT    %%THIS?%%
         L     R2,CSOUTWK               PT TO OUTPUT BUFFER PTR
         LTR   R2,R2                    AHA.  IS THERE ONE, HE ASKS
         BZ    NEWDSPNT                 NOPE - TOUGH LUCK, FELLA
         L     R2,0(,R2)                GET ADDR OF BUFFER
         USNGX LOGLINE,R2                                          U022
         MVC   LOGLINE+1(40),LINE05+17  COPY MESSAGE FROM SCREEN   U022
         MVI   LL@CC,C' '               CARRIAGE CONTROL           U022
         MVI   ENQIT,255                FORCE ENQ
         DROPX R2                                                  U022
         BAL   R8,CPUT#                 WRITE IT OUT
         CLC   LINE05+35(15),=CL15'NOT IN DATA SET'   END OF EXTENT?
         BE    NEW$DISP                 YES - GET OUT OF LOOP AND DISP
         CLC   LINE05+27(20),EOFMSG     WAS IT EOF?                U004
         BE    NEW$DISP                 YES - GET OUT AND DISPLAY LOC
         B     4(R6)                    NO - BUT DON'T DUMP THIS 'BLK'
         EJECT
**********************************************************************
*********************************  DUMPER  ***************************
**********************************************************************
         SPACE 3
*  THIS ROUTINE DUMPS ANY SIZE BLOCK WHICH IS IN THE BUFFER TO
*  THE OUTPUT RECORDING FILE.  THE HEX OFFSETS, THE ACTUAL HEX DATA,
*  AND THE TRANSLATION OF THAT DATA IS PRINTED OUT.
*  NOTE:  (1) GARBAGE DATA IS NOT PRINTED.  THAT IS, DATA IS DISPLAYED
*             ONLY UP TO THE LAST BYTE, AND NO FARTHER.
*         (2) TRANSLATED DATA IS EITHER IN EBCDIC OR IN ASCII
*             TRANSLATION, DEPENDING ON WHICH COMMAND IS IN EFFECT
*             AT THE TIME OF THE DUMP, 'EBCDIC' OR 'ASCII'.
*         (3) THE 'PREPROCESSOR' ROUTINE 'PREPDUMP' MUST BE CALLED
*             FIRST TO GET THE BLOCK, WRITE OUT THE BLOCK HEADER
*             LINE, AND TO CHECK FOR SYNAD AND EODAD ON THE BLOCK.
         SPACE 1
DUMPER   L     R3,ADDRBUFF              PT TO INPUT BUFFER (THE BLOCK)
         ST    R6,TEMP2+20              SAVE RETURN BAL ADDR
         XR    R4,R4                    CLEAR REM REG FOR DIVIDE
         LH    R5,BLKLEN                GET LEN OF BLK
         D     R4,=F'32'                HOW MANY 4 DWD LINES IN BLK
*  R4 HAS THE LENGTH OF THE OFLO 'SHORT' LINE.  R5 HAS THE NUMBER OF
*  LINES TO DUMP OUT (THE NUMBER OR 1 SHORT).
         LTR   R4,R4                    IS THERE A SHORT LAST LINE?
         BZ    *+8                      NO - HAVE NUMBER OF LINES NOW
         LA    R5,1(R5)                 YES - CORRECT NUM OF LINES
         XC    TEMP(4),TEMP             INITIALIZE OFFSET COUNTER
         SPACE 1
DMPFMTLP TM    FLAGS2,LOGF              ARE WE LOGGING
         BNO   BADCSOUT
         L     R2,CSOUTWK               PT TO CSOUT OUTPUT BUFFER
         LTR   R2,R2                    IS THERE ONE?
         BZ    BADCSOUT                 NO - TELL HIM UNABLE TO RECORD
         L     R2,0(,R2)                YES - PICK UP BUFFER PTR
         USNGX LOGLINE,R2                                          U022
         MVI   LL@CC,C' '               CARRIAGE CONTROL           U022
         HEX   LOGLINE+1,TEMP+1,LEN=3   GET OFFSET                 U022
         MVC   LOGLINE+1+91(32),0(R3)   MOVE DATA TO DUMP SECTION  U022
         L     R15,TRTABADD             PICK UP CURRENT TRANSLATE TAB
         TR    LOGLINE+1+91(32),0(R15)  TRANSLATE IT IN PLACE      U022
         MVI   LOGLINE+1+90,C'*'        MOVE IN DELIMITER          U022
         DROPX R2                                                  U022
         MVI   1+123(R2),C'*'           MOVE IN DELIMITER
         SPACE 1
*  FORMAT THE LINE
*  THE LINE IS COMPOSED OF 4 COMPLETE DOUBLEWORDS.  EACH DOUBLEWORD
*  IS FORMATTED INTO FULLWORDS SEPARATED BY A BLANK, AND EACH PAIR
*  OF DOUBLEWORDS IS SEPARATED BY 3 BLANKS.  THUS DUMPER LOOPS TWICE
*  A LOOP OF 4 PASSES, ONE PER FULLWORD.
         SPACE 1
         LA    R14,1+9(R2)              POINT TO 1ST AVAIL LINE POS
         LA    R6,2                     BCT OUTER LOOP (2 PAIRS DWD'S)
*
LINELP1  LA    R4,4                     BCT INNER LOOP (4 FULLWORDS)
*
LINELP2  HEX   (0,R14),(0,R3),LEN=4     GET ONE FULLWORD           U004
         LA    R3,4(,R3)                UPDATE INPUT BUFFER PTR
         LA    R14,9(,R14)              UPDATE OUTPUT LINE PTR
         BCT   R4,LINELP2               DO IT FOR 4 FULLWORDS
         LA    R14,3(,R14)              FOR NEXT PAIR OF DWD'S, SPACE
         BCT   R6,LINELP1               DO IT FOR 2 PAIRS OF DWD'S
         SPACE 1
         MVI   ENQIT,255                FORCE ENQ TO PRINT
         CH    R5,=H'1'                 LAST LINE NOW?
         BE    LASTLINE                 YES - CHECK FOR SHORT LAST LINE
         L     R15,TEMP                 NO - UPDATE OFFSET BY GETTING
         LA    R15,X'20'(R15)           IT IN THE REG, ADDING INCR,
         ST    R15,TEMP                 AND PUTTING IT BACK
         BAL   R8,CPUT#                 WRITE IT OUT
         BCT   R5,DMPFMTLP              DO ALL THE LINES EXCEPT LAST
         SPACE 2
*  LAST LINE ROUTINE
*  IF THE LINE IS A SHORT LAST LINE, ONLY WRITE OUT THE CORRECT
*  AMOUNT OF DATA.
         SPACE 1
LASTLINE LH    R1,BLKLEN                GET LEN OF BLK
         N     R1,=F'31'                TURN OFF ALL BUT LOW ORDR BITS
         LTR   R1,R1                    ARE THERE ANY LOW ORDER BITS?
         BZ    BYELAST                  NO - FULL LAST LINE, DUMP IT
         SPACE 1
*  GET RID OF THE JUNK IN THE DUMP (TRANSLATED) PART FIRST
         SPACE 1
         LA    R14,32-1-1               MAX WIDTH -1 FOR MVI -1 EX LEN
         SR    R14,R1                   GET LEN OF JUNK
         LA    R3,1+91(R2,R1)           POINT TO BEGINNING OF JUNK
         MVI   0(R3),C' '               KILL THE FIRST BYTE
         LTR   R14,R14                  ANY MORE STUFF TO BLANK?
         BM    *+8                      NO - GO ON AND FIX THE LINE NOW
         EX    R14,DMPBKMVC             KILL THAT JUNK
         SPACE 1
         XR    R0,R0                    CLEAR REM REG FOR DIVIDE
         D     R0,=F'4'                 HOW MANY FLWRD GRPS ARE THERE?
*  R0 HAS SLOPOVER WITHIN 1 FULLWORD, R1 HAS NUMBER OF FULLWORDS
*  ON THE LAST LINE
         LA    R4,DUMPOFFS(R1)          POIN TO THE TAB TABLE OFFSET
         XR    R3,R3                    CLEAR REG FOR IC
         IC    R3,0(R4)                 GET THE TAB TO THE COLUMN
         SLL   R0,1                     *2 SLOPOVER FOR CHARACTER BYTES
         AR    R3,R0                    OFFSET TO FIRST GARBAGE CHAR
         LA    R4,86-1                  GET MAX LEN  OF LINE (-1 MVI)
         SR    R4,R3                    GET LEN OF GARBAGE TO BLANK
         LA    R3,1(R3,R2)              POINT TO 1ST GARBAGE BYTE
         MVI   0(R3),C' '               BLANK OUT AT LEAST ONE
         BCTR  R4,0                     GET EXECUTE LEN
         LTR   R4,R4                    IS THERE ANY MORE TO GET RID OF
         BM    BYELAST                  NO - GOOD BYE LAST LINE
DMPBKMVC MVC   1(0,R3),0(R3)            <<< EXECUTED >>>
         EX    R4,DMPBKMVC              GET RID OF THE REST
BYELAST  BAL   R8,CPUT#                 WRITE OUT LAST LINE
         L     R6,TEMP2+20              RESTORE RET ADDR
         BR    R6                       RETURN TO CALLER
         SPACE 5
**********************************************************************
********************************  CLEARDEF  **************************
**********************************************************************
         SPACE 3
*  RESET AND/OR INITIALIZE THE DEFINE TABLE
         SPACE 1
CLEARDEF L     R2,AIDEFTAB              PT TO BEGINNING OF TABLE   U004
         ST    R2,IDEFAVAL              MAKE THAT THE 1ST AVAIL POSIT
         MVI   0(R2),C' '               CLEAR OUT THE TABLE ALSO   U021
         MVC   1(ENDITAB-IDEFTAB-1,R2),0(R2)  ...                  U021
         BR    R8                       RET TO CALLER
         SPACE 5
**********************************************************************
**********************************  CLOSE  ***************************
**********************************************************************
         SPACE 3
*  CLOSE THE DATASET
         SPACE 1
CLOSE    TM    DCBU+48,X'10'            IS IT OPEN?
         BNOR  R8                       NO, WHY CLOSE THEN?
         CLOSE MF=(E,DCBLIST)           GOODBYE DATASET
         AIF   (NOT &VSAM).NVSAM48                                 T002
         TM    FLAGVSAM,VSAMOPEN        VSAM CLUSTER?              T002
         BZR   R8                       NO, RETURN TO CALLER       T002
         OC    KEYLEN,KEYLEN            ANY KEY HERE?              T002
         BZR   R8                       NO, RETURN TO CALLER       T002
         CLOSE MF=(E,OPENKVSM)          YES, CLOSE KEYSEQ ACB      T002
.NVSAM48 ANOP ,                                                    T002
         BR    R8                       RET TO CALLER
         SPACE 5
**********************************************************************
*********************************  WRITE  ****************************
**********************************************************************
         SPACE 2
         AIF   (NOT &VSAM).NVSAM38                                 T002
WRITE    TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    WRITEV                   YES, DO IT                 T002
         LA    R14,CCW##W               -> CCW CHAIN               T002
         AGO   .VSAM06                                             T002
.NVSAM38 ANOP  ,                                                   T002
WRITE    LA    R14,CCW##W               -> CCW CHAIN               T002
.VSAM06  ANOP  ,                                                   T002
         ST    R14,IOBCCWA              SET IN IOB                 U004
         SPACE 1
         EXCP  IOB                                                 U004
         SPACE 1
         WAIT  ECB=ECB                                             U004
         SPACE 1
         CLI   ECB,X'7F'                                           U004
         BER   R8                                                  U004
         B     REALERR                                             U004
         AIF   (NOT &VSAM).NVSAM39                                 T002
         SPACE 1
WRITEV   PUT   RPL=RPLV                 REWRITE THE RECORD         T002
         BXLE  R15,R15,0(R8)            ERROR? NO, RETURN          T002
         BAL   R14,VSAMERR              YES, TELL USER             T002
.NVSAM39 SPACE 5
***********************************************************************
***************************  OPENMSG  *********************************
***********************************************************************
         SPACE 2
*  IF WE MUST OPEN A DATA SET, CREATE A MESSAGE ABOUT IT
         SPACE 2
LOGTEST  TM    FLAGS2,LOGF              ALREADY LOGGING?           U004
         BOR   R8                       YES - ALL DONE             U004
         SPACE 2
OPENOUT  ST    R8,LINKLOG               SAVE FOR LATER             T002
         L     R1,CSOUTWK               FIND CSOUT'S WORKAREA
         LTR   R1,R1                    ANY WORKAREA?
         BZR   R8                       NO, EXIT NOW
         OI    FLAGS2,LOGF              WE ARE NOW LOGGING         U004
*U004    CSOUT OPN,=CL6'SYSDA3',=H'10',CALL=CALL,MF=(E,(1))  (10,10)
         MVI   0(R1),X'80'          (+) FLAG AS OPEN CALL
*U004    LA    0,=CL6'SYSDA3'       (+)
*U004    ST    0,4(,R1)             (+) STORE FIRST PARM
*U004    LA    R0,=H'10'            (+)
*U004    ST    R0,8(,R1)            (+) STORE SECOND PARM
         LR    R2,R1                (+) COPY WORKAREA PTR
         L     R15,=V(CSOUT)        (+) POINT TO ROUTINE
         LR    R1,CSREG             (+) GET CSAREA PTR
         BALR  R14,R15              (+) CALL IT
         LTR   R0,R0                    ERROR?
         BZ    SAYOPEN                  NO
         NI    FLAGS2,255-LOGF          TURN OFF FLAG              U004
         LA    R1,=CL40'UNABLE TO ALLOCATE PRINT FILE'             U004
         BAL   R8,CSIO#                 DISPLAY IT
***  CSIO# DOESN'T RETURN ??????                                   U004
         XC    CSOUTWK,CSOUTWK (NOOUT)  NO MORE TRIES AT IT
         B     LOCRET                   RETURN TO CALLER
SAYOPEN  DC    0H'0'
         MVI   ENQIT,0                  MAYBE
         MVI   CHNGED,0                 VIRGIN BUFFER
         L     R1,CSOUTWK               POINT TO WORK AREA
OPENMSG  L     R1,0(,R1)                FIND LINE TO USE
         USNGX LOGLINE,R1                                          U022
         MVC   LL@MSGID-1(8),=C'0ZAP009I'                          U022
         CLI   DSNAME,X'04'             VTOC?
         BNE   *+8                      NO - INFORMATION ONLY
         MVI   LL@MSGID+6,C'A' (ACTION) MAKE OPR SIT UP AND NOTICE U022
         MVC   LL@UID,USERID            CULPRIT                    U022
         MVC   LL@TEXT(20),=C'HAS OPENED FOR INPUT'                U022
         TM    DCBLIST,X'0F'            NOW, WAS IT OPEN FOR INPUT?
         BZ    DSNWTO                   YES - SO THERE'S NOTHING TO DO
         MVI   LL@MSGID+6,C'A'          IF UPDATING, ACTION MSG    U022
         MVC   LL@TEXT+15(6),=CL6'UPDATE'  AND SAY UPDATE          U022
DSNWTO   CLI   DSNAME,X'04'             VTOC HERE?
         MVC   LL@TEXT+22(44),DISPDSN   MOVE IN DISPLAYABLE DSN    U022
         BE    WTOIT                    YES - VOLSER ALREADY SET   U004
         MVC   LL@TEXT+67(2),=C'ON'     ADD                        U022
         MVC   LL@TEXT+70(6),VOLSER       VOLSER                   U022
         DROPX R1                                                  U022
WTOIT    BAL   R8,CPUT                  DUMP THE LINE
LOCRET   L     R8,LINKLOG               PICK UP RET ADDR           T002
         BR    R8                       ALL OK - RETURN NORMALLY
         EJECT
**********************************************************************
********************************  CPUT  ******************************
**********************************************************************
         SPACE 3
*  PASS A LINE TO THE RECORDING SUBROUTINE
*
*  ENTRY CPUT IS ENQUEUE TO PRINT WITH POSSIBLE WTO'S
*  ENTRY CPUT# IS ENQUEUE BUT WITH NO WTO'S (FOR DUMP COMMANDS)
         SPACE 1
CPUT     TM    FLAGS2,LOGF              SEE IF LOGGING
         BNOR  R8                       IF NOT RETURN TO CALLER
         L     R2,CSOUTWK               GET WA ADDR
         LTR   R2,R2                    ANY THERE?
         BZR   R8                       NO, EXIT NOW
         MVI   0(R2),X'20'              SET INITIAL VALUE (WTO)
         TM    FLAGS2,SENSF             IS DS SYS OR URSA?
         BNO   CPUT#                    IF NOT NO WTO
         CLI   GODFLAG,GOD              CAN HE EVER UPDATE?        U004
         BE    ISWTOCPT                 YES - DO THE WTO           U004
*  IF HE CAN NEVER UPDATE, DON'T WTO.  (USER LOOKING AT SYS1. DS)  U004
CPUT#    L     R2,CSOUTWK               DUMP ENTRY - GET WORKAREA PTR
         MVI   0(R2),0                  DON'T ISSUE WTO
ISWTOCPT CLI   CPUTFLAG,YESCSERR        WAS THERE A CSOUT ENQ ERROR?
         BER   R8                       YES - ACT LIKE I ENQUEUED IT
CPUTRUNC LR    R1,CSREG                 GET CSAREA PTR             U004
         L     R15,=V(CSOUT)            FIND SUBROUTINE
         BALR  R14,R15                  CALL IT
         LTR   R0,R0                    DID IT WORK?
         BZR   R8                       YES - RETURN TO CALLER
         MVI   CPUTFLAG,YESCSERR        RECORD THAT THERE WAS AN ERROR
*U021    B     BADCSOUT                 AND TELL THE USER OF HIS FATE
         SPACE 2
BADCSOUT LA    R2,NOCSOUT               TELL HIM CAN'T ENQ         U021
         B     BOTCH                    BYE                        U021
         SPACE 5
**********************************************************************
********************************  SETLINE  ***************************
**********************************************************************
         SPACE 3
*  SET UP LINE FOR CPUT
*        INPUT: R1 POINTS TO SINGLE MESSAGE NUMBER IN CHAR FORM
*               R8 RETURN ADDRESS IF NOT PRINTING, +4 IF PRINTING
*        OUTPT: R2 POINTS TO LINE
         SPACE 2
SETLINE  TM    FLAGS2,LOGF              ARE WE LOGGING?
         BNOR  R8                       IF NOT RETURN
         L     R2,CSOUTWK               FIND WORKAREA
         LTR   R2,R2                    ANY THERE?
         BZR   R8                       NO, EXIT ERROR
         L     R2,0(,R2)                FIND THE LINE
         USNGX LOGLINE,R2                                          U022
         MVC   LL@MSGID,=C'ZAP01XI'     MOVE IN MESSAGE NUMBER     U022
         MVC   LL@MSGID+5(1),0(R1)      COPY CORRECT DIGIT         U022
         MVC   LL@UID,USERID            CULPRIT                    U022
         AIF   (NOT &VSAM).NVSAM40                                 T002
         TM    FLAGVSAM,VSAMOPEN        IS IT VSAM?                T002
         BO    SETLINEV                 YES, USE RBA, NOT TTR      T002
.NVSAM40 ANOP  ,                                                   T002
         MVC   LL@TTRC3,=C'TTR'                                    U022
         HEX   LL@TTR,TTR,LEN=3         GET THIS TTR               U022
         B     4(,R8)                   NOW RETURN TO CALLER
         AIF   (NOT &VSAM).NVSAM41                                 T002
SETLINEV MVC   LL@RBAC3,=C'RBA'         USE RBA'S HERE             U022
         HEX   LL@RBA,TTR,LEN=4         RBA'S ARE 4 BYTES LONG     U022
         B     4(,R8)                   RETURN TO CALLER           T002
.NVSAM41 SPACE 2
         DROPX R2                                                  U022
         SPACE 5
**********************************************************************
********************************  SETSTRNG  **************************
**********************************************************************
         SPACE 3
*  CONVERT STRING FOR CPUTING
*        INPUT: R1 POINTER TO OUTPUT LINE
*               R8 RETURN ADDRESS
*               R4 EXECUTE LENGTH OF STRING
*               R5 ADDRESS OF DATA TO BE CONVERTED
         SPACE 3
         USNGX LOGLINE,R2                                          U022
SETSTRNG UNPK  SETAREA(8+1),0(4+1,R5)   CONVERT FIRST WORD         T003
         UNPK  SETAREA+8(8+1),4(4+1,R5) CONVERT SECOND             T003
         UNPK  SETAREA+16(8+1),8(4+1,R5)   CONVERT THIRD           T003
         UNPK  SETAREA+24(8+1),12(4+1,R5)  CONVERT FOURTH AND LAST T003
         TR    SETAREA,TRHEX            FIX UP HEX                 T003
         LTR   R4,R4                    ANY?
         BMR   R8                       NO, LEAVE IT BLANK
         LA    R15,1(R4,R4)             GET EXECUTE LENGTH OF HEX
         EX    R15,CPMVC                MOVE CONVERTED DATA
         MVI   LL@Q1,C''''                                         U004
         EX    R4,SETSTRMV              MOVE CHAR FORM TO BUFFER   U004
         L     R15,TRTABADD             GET RIGHT TRTAB PTR        U004
         EX    R4,SETSTRTR              TRANSLATE TO RIGHT CHARSET U004
         LA    R15,LL@DATAC+1(R4)                                  U022
         MVI   0(R15),C''''                                        U004
         BR    R8                       RETURN
         SPACE 1
CPMVC    MVC   LL@DATAX(*-*),SETAREA    << EXECUTED >>             T003
SETSTRMV MVC   LL@DATAC(*-*),0(R5)      << EXECUTED >>             U022
SETSTRTR TR    LL@DATAC(*-*),0(R15)     << EXECUTED >>             U022
         SPACE 1
         DROPX R2                                                  U022
         SPACE 5
**********************************************************************
********************************  SETMSG  ************************ U004
**********************************************************************
         SPACE 3
*  MOVE MSG AND HYPHENS TO CORRECT POS ON SCREEN, DEPENDING ON     U004
*  WHETHER IN FULLSCREEN MODE                                      U004
*        INPUT:  R1 POINTS TO CL20'MSG'                            U004
         SPACE 1
SETMSG   LH    R1,0(,R8)                GET MSG OFFSET             U004
         AR    R1,R12                   GET ADDR                   U004
SETMSGX  TM    FLAGS3,M3270F            FULLSCREEN?                U014
         BZ    SETMSG1                  NO - SHORTER MSG           U007
         MVC   LINE19+27(20),0(R1)      MOVE IT TO CENTER          U004
         MVC   LINE19+17(9),=9C'='      MAKE IT FANCY              U004
         MVC   LINE19+48(9),=9C'='      BUT DON'T GET CARRIED AWAY U004
         B     2(,R8)                   RETURN PAST OFFSET         U004
         SPACE 1
SETMSG1  MVC   LINE19+23(20),0(R1)      MOVE IT TO LEFT OF CENTER  U004
         MVC   LINE19+17(3),=9C'='      MAKE IT FANCY              U004
         B     2(,R8)                   RETURN PAST OFFSET         U004
         EJECT
**********************************************************************
********************************  BOTCH  *****************************
**********************************************************************
         SPACE 3
*  DISPLAY ERROR MESSAGES
*  MOVE IN MSG AND HYPHENS AROUND IT TO ERROR MSG LINE ON SCREEN.
*  TRACE WILL BE DISABLED.  IF SYNTAX ERROR MSG IS TO BE DISPLAYED,
*  THE COLUMN IN QUESTION IS COMPUTED AND DISPLAYED WITH THE MSG.
*
*  R2 = PTR TO MESSAGE
*  R1 = PTR TO INVALID LOC IN EXPRESSION (IF APPLICABLE)
         SPACE 1
BOTCH    XR    15,15                    CLEAR R15
         IC    R15,0(,R2)               GET LENGTH
         CH    R15,=H'20'               SEE IF WE ARE USING MSG
         BH    BOTCH0                   IF NOT ASSUME LNTH 20
         MVC   TEMP2(20),BLANKS         CLEAR AN AREA              U004
         BCTR  R15,0                    DECREMENT FOR EXECUTE
         MVC   TEMP2(0),1(R2)           << EXECUTED >>             U004
         EX    15,*-6                   MOVE IN MSG
         LA    R2,TEMP2                 POINT TO MSG               U004
         SPACE 1
         CLC   INVEXP+1(16),TEMP2       IS THIS AN INV EXPR MSG?   U004
         BNE   BOTCH0                   NO - GO ON                 U004
         LA    R3,REP                   PT TO REPLY                U004
         SR    R1,R3                    GET OFFSET TO BAD HEX
         LA    R1,1(R1)                 MAKE THE OFFSET A COLUMN #
         CVD   R1,TEMP                  YES - GET OFFSET OF ERROR INTO
         UNPK  TEMP2+18(2),TEMP+6(2)    THE EXPRESSION AND         U004
         OI    TEMP2+19,C'0'            MOVE IT TO THE SCREEN      U004
         BCTR  R1,0                     GET CURSOR POSITION        U004
         STH   R1,CURPOS                SET IT                     U004
         SPACE 1
BOTCH0   LR    R1,R2                    GET MSG ADDR               U004
         LA    R8,NEWDSPNT-2            FAKE THE RETURN POINT      U004
         B     SETMSGX                  SPECIAL ENTRY PT           U004
         SPACE 5
**********************************************************************
********************************  EODAD  *****************************
**********************************************************************
         SPACE 3
*  END-OF-FILE ROUTINE                                             U004
*  IF EOF'S ARE NOT COUNTED AS BLOCKS (AS IN A PDS), THEN DISPLAY
*  END OF FILE;  OTHERWISE HOP THEM
         SPACE 2
EODAD    BAL   R8,CLEAR                 CLEAR SCREEN               T002
         MVC   LINE05,BLANKS            CLR TOP ERR MSG SO CAN SAY EOF
*  LENGTH OF EOF RECORD IS ALREADY SET                             U004
         MVC   LINE05+27(20),EOFMSG     TELL HIM END-OF-FILE       U004
         NI    FLAGVSAM,X'FF'-SEQREAD   TURN OFF SEQ READ          T003
         TM    FLAGS2,NOEOF             EOF ALLOWED HERE?          T002
         BZ    NEW$DISP                 YES - SO EOF ALREADY       T002
         L     R8,LINKREAD              RESTORE LINK REG           T002
         BR    R8                       NO - EXIT FROM READBLK     T002
*
*  THUS UNDER CERTAIN CIRCUMSTANCES EOF'S ARE COUNTED AS RECORDS.
*
         EJECT
**********************************************************************
*********************************  CHKPT  ****************************
**********************************************************************
         SPACE 2
CHKPT    TM    FLAGS2,MUSTZAP           BLOCK NEED A ZAP 1ST?
         BNOR  R8                       NO, LET COMMAND THRU
*U004    NI    FLAGS2,255-MUSTZAP       YES, WE TOLD HIM ONCE
*U004    LA    R2,=CL20'**** ZAP NEEDED ****' TELL HIM, IGNORE COMMAND
*U004    B     BOTCH                    HE CAN ALWAYS DO IT AGAIN
         CLC   REP,PREVREP              SAME CMD TWICE IN A ROW?   U012
         BE    CHKPTRST                 YES - LET HIM DO IT        T001
         MVC   LINE19+11(58),=C'******** ENTER "ZAP" OR RE-ENTER PREVIO$
               US COMMAND ********'                                U012
         B     NEWDSPNT                 TELL HIM HE CAN'T LEAVE    U004
CHKPTRST NI    FLAGS2,X'FF'-MUSTZAP     RESET FLAG SAYING MUST ZAP T001
         BR    R8                       RETURN TO CALLER           T001
         SPACE 5
**********************************************************************
**********************************  KILL3270  ******************** U004
**********************************************************************
         SPACE 2
KILL3270 TM    FLAGS3,FSMODE            FULL SCREEN MODE ON?       U014
         BNOR  R8                       NO - JUST RETURN           U004
         SPACE 1
         AIF   (&MVT).MVT04                                        U014
         STFSMODE  OFF                  KILL VTAM FULLSCREEN       U004
         LTR   R15,R15                  TSO/VTAM ACTIVE?           U024
         BZ    KFS01                    YES, WE'RE FINISHED HERE   U024
.MVT04   ANOP                                                      U024
         SPACE 1
         TPUT  CLR3270,CLR3270L,FULLSCR  NO, KILL SPF FULLSCREEN   U024
         SPACE 1
KFS01    NI    FLAGS3,255-FSMODE        IT'S OFF NOW               U014
         BR    R8                       RETURN TO CALLER           U004
         EJECT
**********************************************************************
*********************************  FINDVOL  ********************** T002
**********************************************************************
         SPACE 3
FINDVOL  L     R15,CVTPTR               -> CVT                     T002
         L     R15,0(,R15)              -> TCB WORDS               T002
         L     R15,4(,R15)              -> MY TCB                  T002
         L     R15,12(,R15)             -> MY TIOT                 T002
         LA    R0,24                    LEN OF JOBNAME ENTRY       T002
         BALR  R14,0                    LOOP BACK HERE             T002
         AR    R15,R0                   NEXT ENTRY PLEASE          T002
         LTR   R0,R0                    ANY LEFT?                  T002
         BZ    FINDVOLE                 NO, CAN'T HAPPEN           T002
         IC    R0,0(,R15)               LEN OF THIS ENTRY          T002
         CLC   4(8,R15),0(R1)           THIS OUR DDNAME?           T002
         BNER  R14                      NO, TRY NEXT ENTRY         T002
         L     R15,16(,R15)             GET THE UCB                T002
         USNGX UCB,R15                                             T002
         CLI   UCBTYP+2,X'20'           DASD?                      T002
         BNE   FINDVOLE                 NO, STRANGE                T002
         LA    R1,SRTEVOLI              GET THE VOLSER             T002
         DROPX R15
         BR    R8                       RETURN  VICTORIOUS         T002
FINDVOLE LA    R1,=CL40'  INVALID DEVICE TYPE FOR DATASET'         T002
         B     CSIO#                    AND EXIT FOREVER           T002
         SPACE 5
**********************************************************************
*********************************  TTRCONV  ********************** T002
**********************************************************************
         SPACE 3
TTRCONV  L     R0,TTR                   GIVE HIM THE TTRN          U004
         N     R0,=X'FFFFFF00'          MAKE IT A TTRN             U004
         L     R1,DCBDEBAD+DCBU-IHADCB  GIVE HIM THE DEB           U004
         LA    R2,MBBCCHHR              GIVE HIM A WORKAREA FOR IT U004
         STM   R8,R12,52(R13)           SAVE SOME REGISTERS        U004
         LR    R8,R13                   SAVE PTR TO SAVEAREA       U004
         L     R15,CVTPTR               CVT PTR                    U004
         L     R15,CVTPCNVT-CVT(,R15)   POINT TO THE ROUTINE       U004
         BALR  R14,R15                  GO DO IT                   U004
         LR    R13,R8                   RESTORE PTR TO SAVE AREA   U004
         LM    R8,R12,52(R13)           RESTORE THE DEAD REGS      U004
         BR    R8                       RETURN TO CALLER           T002
         AIF   (NOT &VSAM).NVSAM42                                 T002
         EJECT
**********************************************************************
*********************************  VSAMTSEQ  ********************* T002
**********************************************************************
         SPACE 3
VSAMTSEQ TM    FLAGVSAM,VSAMSEQ         ALREADY DOING IT?          T002
         BOR   R8                       YES, RETURN NOW            T002
         NI    RPLVK+RPLOPT1-IFGRPL,X'FF'-RPLDIR  RESET DIR BITS   T002
         NI    RPLVK+RPLOPT2-IFGRPL,X'FF'-RPLBWD  RESET DIR BITS   T002
         NI    FLAGVSAM,VSAMMASK        RESET DIR BITS             T002
         OI    FLAGVSAM,VSAMSEQ         NOW GOING FORWARD          T002
         BR    R8                       RETURN TO CALLER           T002
         SPACE 5
**********************************************************************
*********************************  VSAMTSQB  ********************* T002
**********************************************************************
         SPACE 3
VSAMTSQB TM    FLAGVSAM,VSAMSEQB        ALREADY DOING IT?          T002
         BOR   R8                       YES, RETURN NOW            T002
         NI    RPLVK+RPLOPT1-IFGRPL,X'FF'-RPLDIR  RESET DIR BITS   T002
         OI    RPLVK+RPLOPT2-IFGRPL,RPLBWD   AND GO BACKWARDS      T002
         NI    FLAGVSAM,VSAMMASK        RESET DIR BITS             T002
         OI    FLAGVSAM,VSAMSEQB        NOW GOING FORWARD          T002
         BR    R8                       RETURN TO CALLER           T002
         EJECT
**********************************************************************
*********************************  VSAMERR   ********************* T002
**********************************************************************
         SPACE 3
VSAMERR  MVC   LINE19+14(52),=C'VSAM ERROR RPLFDBK=XXXXXX, OFFSET=XXXX,$
                R8 OFFS=XXXX'           ERROR MESSAGE LESS INSERTS T002
         HEX   LINE19+14+19,RPLV+RPLFDBK-IFGRPL,LEN=3,BYTE=C','    G001
*                                       GET ERROR FLAGS            G001
         SR    R14,R12                  GET OFFSET OF ERROR        T002
         SH    R14,=H'4'                -> ADDR OF BAL             T002
         STH   R14,DWD                  STASH IT SOMEWHERE         T002
         HEX   LINE19+14+34,DWD,LEN=2,BYTE=C',' GET OFFSET OF CALL G001
         SR    R8,R12                   GET OFFSET OF LINK REG     T002
         STH   R8,DWD                   STASH IT SOMEWHERE         T002
         HEX   LINE19+14+48,DWD,LEN=2   CONVERT ERROR CODE         T002
         B     NEWDSPNT                 GIVE ME A NEW SCREEN       T002
.NVSAM42 TITLE 'ZAP --- CONSTANTS, DATA,FLAGS, ETC'
*%       PRINT NOGEN                                               U004
IOERRMSG DC    CL40' ***** I/O ERROR: XXXXXXXXXXXXXXX *****'
INVMEM   DC    CL20'MEMBER NOT FOUND'
WRITERR  MSG   'WRITE NOT ALLOWED'
EOFMSG   DC    CL20'*** END OF FILE ***'
ASCIIMSG MSG   'ASCII TRANSLATION'
ZCODEMSG MSG   'ZCODE TRANSLATION'
EBCDCMSG MSG   'EBCDIC TRANSLATION'
INVFMSG  MSG   'NO FORWARD CHAIN'
INVBMSG  MSG   'NO BACKWARD CHAIN'
INVEXP   MSG   'SYNTAX ERROR COL. '                                U004
LINMSG   MSG   'SCAN NOT IN EFFECT'
TABFULL  MSG   'DEFINE TABLE FULL'
REDEFMSG MSG   'SYMBOL REDEFINED'
LASTINV  MSG   'INVALID IN VTOC'
NOTVTOC  MSG   'DATASET NOT VTOC'                                  U004
DEFRESET MSG   'DEFINE TABLE RESET'
REC0INV  MSG   'RECORD 0 INVALID'
INVCOM   MSG   'INVALID COMMAND'
NOMCOM   MSG   'NOT PARTITIONED'
INVOPCOD MSG   'INVALID OP CODE '
INVDEC   MSG   'INVALID DECIMAL'
TTRSMALL DC    CL20'TTR < 000001 INVALID'                          U004
RBASMALL DC    CL20'RBA < 0 INVALID'                               T003
LENERR   MSG   'LENGTH INVALID'
NOTJQMSG MSG   'DATA SET NOT JOBQ'
STORMSG  DC    CL20'NOTE: NOT UPDATING'
BACKDUMP DC    CL20'REVERSE DUMP INVALID'                          U004
NOCSOUT  MSG   '  UNABLE TO RECORD'
DUMPWORK MSG   '  DUMP SUCCESSFUL'
NAMEMNF  MSG   'NAME INVALID'                                      U002
LOGONMSG MSG   'LOGGING'                                           U004
NOMEMFND DC    CL20'*NO DIRECTORY ENTRY*'                          U004
BADEQUAL MSG   'INVALID SYMBOL'                                    U004
OUCH     MSG   'OUCH!!'                                            U004
         AIF   (NOT &VSAM).NVSAM43                                 T002
INVVSAM  MSG   'CMD REQ NON-VSAM'                                  T002
INVNVSAM MSG   'CMD REQ VSAM CLUSTER'                              T002
VERREQ   MSG   'VERIFY WAS SUCCESSFUL'                             T002
.NVSAM43 ANOP  ,                                                   T002
INVKEY   MSG   'CMD REQ KEYLEN > 0'                                T002
BADDSNMS MSG   'UNAVAILABLE OR NON-EXISTENT'                       T003
ZEROSPMS MSG   'HAS NO EXTENTS, CANNOT BE ZAPPED'                  T003
*        PRINT GEN                                                 U007
         SPACE 8    (EJECT)
*        LITERAL POOL
         SPACE 2
         LTORG ,
         EJECT
*                                                                  U005
*  TABLE TO GENERATE VALID 3270 CHARS FROM 6 BIT CODE              U005
*                                                                  U005
TR3270   DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'                 U005
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'                 U005
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'                 U005
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'                 U005
         SPACE 2
*
*  HEX TO CHAR HEX AND CHAR TO HEX TRTAB
*
         DC    X'0A0B0C0D0E0F'          FOR CHAR TO HEX            U004
         DC    41C'.'                   WASTED SPACE               U004
NUMBERS  DC    C'0123456789ABCDEF'
TRHEX    EQU   *-256
N0       EQU   NUMBERS+0
N1       EQU   NUMBERS+1
N2       EQU   NUMBERS+2
N3       EQU   NUMBERS+3
N4       EQU   NUMBERS+4
N5       EQU   NUMBERS+5
N6       EQU   NUMBERS+6
N7       EQU   NUMBERS+7
N8       EQU   NUMBERS+8
N9       EQU   NUMBERS+9
         SPACE 5
EDMASK   DC    X'402020202020202020202120'
*
DUMPOFFS DC    AL1(9,18,27,36,48,57,66,75)
         SPACE 2
MAXWIDTH DC    A((LINE19-LINE06-$I)/$I/2)                          U004
         SPACE 2
OLD      DC    F'1',A(HERALD)
HERALD   DC    H'10',H'0',CL6' ZAP: '
         SPACE 2
PTPBDC   PUTLINE  OUTPUT=(LINEDESC-DSA,TERM,SINGLE,DATA),          U004$
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L             U004
PTPBDCL  EQU   *-PTPBDC                                            U004
         SPACE 1
PGPBDC   PUTGET OUTPUT=(OLD,SINGLE,MODE),TERMGET=(EDIT,WAIT),          $
               TERMPUT=(ASIS,WAIT,NOHOLD,NOBREAK),MF=L
PGPBDCL  EQU   *-PGPBDC
         SPACE 3
CTL3270C DC    X'27F1'                  ESC, NORMAL WRITE          U024
         DC    X'C1'                    WCC:  RST MDT              U024
         DC    X'115D7F114040'          SPF/TCAM PREFIX            U004
CTL3270L EQU   *-CTL3270C                                          U024
         SPACE 1
END3270C DC    X'11C1D113'              INSERT CURSOR AT 2,2       U024
END3270L EQU   *-END3270C                                          U024
         SPACE 1
CLR3270  DC    X'27F5'                  ESC, ERASE WRITE           U024
         DC    X'C1'                    WCC:  RST MDT              U024
         DC    X'115D7E114040'          RESET SPF FULLSCREEN       U004
         DC    X'13'                    INSERT CURSOR AT TOP       U004
CLR3270L EQU   *-CLR3270                LENGTH                     U011
         SPACE 2
         DC    3A(PATCH)                -> TO PATCH SPACE          T002
         SPACE 1
PATCH    DC    10S(*)                   SCONS FOR A WHILE          T002
         DC    190H'0'                  ZEROS FOR THE REST         T002
         SPACE 2
*  CONSTANTS FOR DISPLAY ROUTINE
DSPCON80 DC    F'-16'                   MASK FOR HIGH ORDER BITS   U004
         DC    F'15'                    MASK FOR LOW  ORDER BITS   U004
         DC    H'80'                    LINE LENGTH                U004
         DC    H'96'                    (HEX/SCREEN)/2             U004
         DC    H'16'                    HEX BYTES / LINE           U004
         DC    H'32'                    TWICE THE ABOVE            U004
         DC    AL1(7,12,12,17,18,23,23,28,30,35,35,40,41,46,46,51) U004
DISPTR80 DC    AL1(48)                  BLANK               +08    U004
         DC    AL1(16,17,18,19,48)      BYTES 01, 02        +09    U004
         DC    AL1(20,21,22,23,48,48)   BYTES 03, 04        +14    U004
         DC    AL1(24,25,26,27,48)      BYTES 05, 06        +20    U004
         DC    AL1(28,29,30,31,48,48,48)      07, 08        +25    U004
         DC    AL1(32,33,34,35,48)            09, 10        +32    U004
         DC    AL1(36,37,38,39,48,48)         11, 12        +37    U004
         DC    AL1(40,41,42,43,48)            13, 14        +43    U004
         DC    AL1(44,45,46,47)               15, 16        +48    U004
         DC    AL1(48,48,48,49)         '   ×'              +52    U004
         DC    AL1(0,1,2,3,4,5,6,7)     FIRST 8 CHARS       +56    U004
         DC    AL1(8,9,10,11,12,13,14,15)  LAST 8 CHARS     +64    U004
         DC    AL1(49)                                      +72    U004
         SPACE 2
DSPCON40 DC    F'-8'                    MASK FOR HIGH ORDER BITS   U004
         DC    F'7'                     MASK FOR LOW  ORDER BITS   U004
         DC    H'40'                    LINE LENGTH                U004
         DC    H'48'                    (HEX/SCREEN)/2             U004
         DC    H'8'                     HEX BYTES / LINE           U004
         DC    H'16'                    TWICE THE ABOVE            U004
         DC    X'060B0B101116161B'      TABS FOR CARET ON SCREEN
*%       DC    8X'00'                   PADDING                    U004
DISPTR40 DC    AL1(16,17,18,19,48)      BYTES 01, 02        +08    U004
         DC    AL1(20,21,22,23,48,48)   BYTES 03, 04        +13    U004
         DC    AL1(24,25,26,27,48)      BYTES 05, 06        +19    U004
         DC    AL1(28,29,30,31)         BYTES 07, 08        +24    U004
         DC    AL1(48,48,49)            '  ×'               +28    U004
         DC    AL1(0,1,2,3,4,5,6,7)     CHARACTERS          +31    U004
         DC    AL1(49)                  '×'                 +39    U004
*%       DC    34AL1(48)
         EJECT
CCWLIST  DC    0D'0'                                               U024
         CCW   X'31',CCHHR-DSA,X'40',5  SEARCH ID EQ               U024
         CCW   X'08',8,X'00',0          TIC *-8                    U024
         CCW   X'1E',0,X'20',*-*        READ CKD                   U024
         SPACE 1
         CCW   X'31',CCHHR-DSA,X'40',5  SEARCH ID EQ               U024
         CCW   X'08',8,X'00',0          TIC *-8                    U024
         CCW   X'0D',8,X'20',*-*        WRITE KD                   U024
         SPACE 1
         CCW   X'16',0,X'20',*-*        READ R0                    U024
         SPACE 1
         CCW   X'16',0,X'70',1          READ R0 TO ORIENT          U019
         CCW   X'E9',FINDMBR-DSA,X'40',8  SEARCH KEY EQ/HI MT      U024
         CCW   X'08',8,X'00',0          TIC *-8                    U024
         CCW   X'06',8,X'00',256        READ D                     U024
         SPACE 2
         DROPX ,                                                   T002
         EJECT
DCBMSK   DCB   MACRF=E,DDNAME=SYSLIB,DEVD=DA                       U004
         DC    5F'0'                    STUPID MACRO & SYSTEM      U004
DCBUL    EQU   *-DCBMSK                                            T002
         AIF   (NOT &VSAM).NVSAM44                                 T002
ACBMSK   ACB   AM=VSAM,DDNAME=SYSLIB,MACRF=(CNV,DIR,IN)            T002
ACBVL    EQU   *-ACBMSK                                            T002
ACBMSKK  ACB   AM=VSAM,DDNAME=SYSLIB,MACRF=(KEY,SEQ,DIR,IN)        T002
RPLMSK   RPL   AM=VSAM,ACB=0,OPTCD=(CNV,SEQ,MVE)                   T002
RPLVL    EQU   *-RPLMSK                                            T002
RPLMSKK  RPL   AM=VSAM,ACB=0,OPTCD=(SEQ,LOC,FKS,KGE,KEY)           T003
EXLMSK   EXLST AM=VSAM,EODAD=EODAD                                 T002
EXLVL    EQU   *-EXLMSK                                            T002
.NVSAM44 SPACE 2
*
*  DUMP FORMAT TRTAB (*** ASCII ***)
*
TRCHARA  DC    256C'.'
         ORG   TRCHARA+X'20'
         DC    C' !"#$%&&''()*+,-./0123456789:;<=>?'
         ORG   TRCHARA+X'40'
         DC    C'@ABCDEFGHIJKLMNOPQRSTUVWXYZ...._'
         ORG   TRCHARA+X'60'
         DC    X'79818283848586878889919293949596'
         ORG   TRCHARA+X'70'
         DC    X'979899A2A3A4A5A6A7A8A94B4F4B5F4B'
         ORG   TRCHARA+X'A0'
         DC    C' !"#$%&&''()*+,-./'
         DC    C'0123456789'
         DC    C':;<=>?'
         ORG   TRCHARA+X'C0'
         DC    C'@ABCDEFGHIJKLMNOPQRSTUVWXYZ...._'
         ORG   TRCHARA+X'E0'
         DC    X'79818283848586878889919293949596'
         ORG   TRCHARA+X'F0'
         DC    X'979899A2A3A4A5A6A7A8A94B4F4B5F4B'
         SPACE 3
*
*  DUMP FORMAT TRTAB (*** ZCODE ***)
*
TRCHARZ  DC    256C'.'
*U023    ORG   TRCHARZ+4
*U023    DC    C'$'
*U023    ORG   TRCHARZ+14
*U023    DC    C'??();/'
*U023    ORG   TRCHARZ+26
*U023    DC    C'+-*/*...?V<.=.         '
*U023    ORG   TRCHARZ+47
*U023    DC    C',?'
*U023    ORG   TRCHARZ+54
*U023    DC    C'^'
*U023    ORG   TRCHARZ+61
*U023    DC    C'_'
*U023    ORG   TRCHARZ+82
*U023    DC    C'??'
*U023    ORG   TRCHARZ+86
*U023    DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ-'
*U023    ORG   TRCHARZ+113
*U023    DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ='
*U023    ORG   TRCHARZ+140
*U023    DC    C'0123456789.. '':'
         ORG   TRCHARZ+X'18'                                       U023
         DC    X'C0D0'                                             U023
         ORG   TRCHARZ+X'25'                                       U023
         DC    C'$'                                                U023
         ORG   TRCHARZ+X'40'                                       U023
         DC    C' ABCDEFGHIJKLMNOPQRSTUVWXYZ'                      U023
         DC    C'_ABCDEFGHIJKLMNOPQRSTUVWXYZ'                      U023
         DC    C'=0123456789'                                      U023
         ORG   TRCHARZ+X'90'                                       U023
         DC    C'+-'                                               U023
         ORG   TRCHARZ+X'9E'                                       U023
         DC    C'<'                                                U023
         ORG   TRCHARZ+X'A0'                                       U023
         DC    C'='                                                U023
         ORG   TRCHARZ+X'A2'                                       U023
         DC    C'>'                                                U023
         ORG   TRCHARZ+X'A4'                                       U023
         DC    C'-',X'5A'                                          U023
         ORG   TRCHARZ+X'B5'                                       U023
         DC    C',?'                                               U023
         ORG   TRCHARZ+X'C4'                                       U023
         DC    C';:'                                               U023
         ORG   TRCHARZ+X'F9'                                       U023
         DC    C'#'                                                U023
         ORG   ,
         SPACE 3
*
*  DUMP FORMAT TRTAB (*** EBCDIC ***)
*
TRCHARE  DC    256C'.'
         ORG   TRCHARE+C' '
         DC    C' '
         ORG   TRCHARE+X'4A'            CENT SIGN
         DC    X'4A',C'.<(+×&&'
         ORG   TRCHARE+X'5A'            EXCLAMATION PT
         DC    X'5A',C'$*);^-/'
         ORG   TRCHARE+C','
         DC    C',%_>?'
         ORG   TRCHARE+C':'
         DC    C':#@''=',X'7F'          DOUBLE QUOTE
         ORG   TRCHARE+X'81'
         DC    9AL1(*-TRCHARE)          LOWER CASE A-I
         ORG   TRCHARE+X'91'
         DC    9AL1(*-TRCHARE)          LOWER CASE J-R
         ORG   TRCHARE+X'A2'
         DC    8AL1(*-TRCHARE)          LOWER CASE S-Z
         ORG   TRCHARE+C'A'
         DC    9AL1(*-TRCHARE)          UPPER CASE A-I
         ORG   TRCHARE+C'J'
         DC    9AL1(*-TRCHARE)          UPPER CASE J-R
         ORG   TRCHARE+C'S'
         DC    8AL1(*-TRCHARE)          UPPER CASE S-Z
         ORG   TRCHARE+C'0'
         DC    10AL1(*-TRCHARE)         NUMBERS
         ORG   ,                                                   T002
         EJECT
*
*  COMMAND TABLE
*
         SPACE 1
*        X'80' COMMAND IS VALID FOR VSAM CLUSTERS                  T002
*        X'40' COMMAND IS VALID FOR EXCP ACCESS                    T002
*        X'20' COMMAND IS VALID FOR PDS                            T002
COMTAB   DC    0F'0'                             * = NO HELP INFO
         DC    AL1(X'40'+6),CL8'FINDEOF ',AL3(FINDEOF)           * T002
         DC    AL1(X'C0'+6),CL8'VERBOSE ',AL3(VERBOSE)             T002
         DC    AL1(X'C0'+5),CL8'NOT3270 ',AL3(NOT3270)           * T002
         DC    AL1(X'C0'+5),CL8'YES3270 ',AL3(YES3270)           * T002
         DC    AL1(X'60'+6),CL8'WHATMEM ',AL3(WHATMEM)             T002
         DC    AL1(X'C0'+6),CL8'//DEBUG ',AL3(DIEFAST)           * T002
         DC    AL1(X'40'+6),CL8'LASTDS1 ',AL3(LASTDS1)             T002
         DC    AL1(X'00'+6),CL8'X X X X ',AL3(*)   ZAP SPACE     * T002
         DC    AL1(X'C0'+5),CL8'EBCDIC  ',AL3(EBCDIC)              T002
         DC    AL1(X'C0'+5),CL8'ITRACE  ',AL3(ITRACE)              T002
         DC    AL1(X'C0'+5),CL8'DISASM  ',AL3(DISASM)              T002
         DC    AL1(X'C0'+5),CL8'LINE80  ',AL3(LINE80)              T002
         DC    AL1(X'C0'+5),CL8'LINE40  ',AL3(LINE40)              T002
**%%     DC    AL1(X'00'+5),CL8'FORMAT  ',AL3(FORMAT)            * T002
         DC    AL1(X'80'+5),CL8'KEYSEQ  ',AL3(KEYSEQ)              T002
         DC    AL1(X'80'+5),CL8'RBASEQ  ',AL3(RBASEQ)              T002
         DC    AL1(X'C0'+5),CL8'NOTCRT  ',AL3(NOT3270)           * T002
         DC    AL1(X'C0'+4),CL8'NODEF   ',AL3(NODEF)               T002
         DC    AL1(X'C0'+4),CL8'ASCII   ',AL3(ASCII)               T002
         DC    AL1(X'C0'+4),CL8'FLOAT   ',AL3(FLOAT)               T002
         DC    AL1(X'C0'+4),CL8'ZCODE   ',AL3(ZCODE)               T002
         DC    AL1(X'C0'+4),CL8'WHERE   ',AL3(WHERE)               T002
         DC    AL1(X'C0'+4),CL8'TERSE   ',AL3(TERSE)               T002
         DC    AL1(X'C0'+4),CL8'EJECT   ',AL3(EJECT)               T002
         DC    AL1(X'C0'+4),CL8'DUMPF   ',AL3(DUMPF)               T002
         DC    AL1(X'C0'+4),CL8'DUMPT   ',AL3(DUMPT)               T002
         DC    AL1(X'40'+4),CL8'DISPC   ',AL3(DISPC)               T002
         DC    AL1(X'C0'+4),CL8'DISPK   ',AL3(DISPK)               T002
         DC    AL1(X'C0'+4),CL8'DISPD   ',AL3(DISPD)               T002
         DC    AL1(X'C0'+3),CL8'BASE    ',AL3(BASE)                T002
         DC    AL1(X'C0'+3),CL8'DUMP    ',AL3(DUMP)                T002
         DC    AL1(X'C0'+3),CL8'NOTE    ',AL3(NOTE)                T002
         DC    AL1(X'C0'+3),CL8'LAST    ',AL3(LAST)                T002
         DC    AL1(X'C0'+3),CL8'HELP    ',AL3(HELPHELP)          * T002
         DC    AL1(X'60'+3),CL8'NAME    ',AL3(NAME#)               T002
         DC    AL1(X'40'+3),CL8'MTTR    ',AL3(MTTR)                U023
         DC    AL1(X'C0'+3),CL8'IDEF    ',AL3(IDEF)                T002
         DC    AL1(X'C0'+3),CL8'SAVE    ',AL3(REPLACE)           * T002
         DC    AL1(X'C0'+3),CL8'ZSYM    ',AL3(ZCODE)             * T002
         DC    AL1(X'00'+3),CL8'FCME    ',AL3(FCME)              * U021
**%%     DC    AL1(X'00'+3),CL8'VTOC    ',AL3(VTOCCMD)           * T002
         DC    AL1(X'C0'+2),CL8'ASM     ',AL3(ASM)                 T002
         DC    AL1(X'C0'+2),CL8'CRT     ',AL3(CRT)                 T002
         DC    AL1(X'C0'+2),CL8'LOG     ',AL3(LOG)                 T002
         DC    AL1(X'C0'+2),CL8'ZAP     ',AL3(REPLACE)             T002
         DC    AL1(X'C0'+2),CL8'TSO     ',AL3(TSO)                 U020
         DC    AL1(X'C0'+2),CL8'END     ',AL3(END)                 T002
         DC    AL1(X'C0'+2),CL8'SET     ',AL3(SET)                 T002
         DC    AL1(X'40'+2),CL8'ABS     ',AL3(ABS)                 T002
**%%     DC    AL1(X'C0'+2),CL8'DSN     ',AL3(DSNCMD)            * T002
         DC    AL1(X'60'+1),CL8'WM      ',AL3(WHATMEM)             T002
         AIF   (&MVS).MVS07
         DC    AL1(X'40'+1),CL8'NN      ',AL3(NN)                  T002
*.MVS07  DC    AL1(X'C0'+1),CL8'DO      ',AL3(DO)                  U020
.MVS07   DC    AL1(X'C0'+0),CL8'L       ',AL3(LOCATE)              T002
         DC    AL1(X'C0'+0),CL8'S       ',AL3(STORE)               T002
         DC    AL1(X'C0'+0),CL8'O       ',AL3(OR)                  T002
         DC    AL1(X'C0'+0),CL8'X       ',AL3(EXOR)                T002
         DC    AL1(X'C0'+0),CL8'N       ',AL3(AND)                 T002
         DC    AL1(X'C0'+0),CL8'F       ',AL3(FIND)                T002
         DC    AL1(X'C0'+0),CL8'>       ',AL3(FORWARD)             T002
         DC    AL1(X'C0'+0),CL8'<       ',AL3(BACKWARD)            T002
         DC    AL1(X'60'+0),CL8'M       ',AL3(MEMBER#)             T002
         DC    AL1(X'60'+0),CL8'E       ',AL3(NAME#)               T002
         DC    AL1(X'C0'+0),CL8'B       ',AL3(BACK)                T002
         DC    AL1(X'C0'+0),CL8'D       ',AL3(DISPLAY#)            T002
         DC    AL1(X'C0'+0),CL8'V       ',AL3(INDIRECT)            T002
         DC    AL1(X'40'+0),CL8'T       ',AL3(TRACK)               T002
         DC    AL1(X'C0'+0),CL8'R       ',AL3(RECORD)              T002
         DC    AL1(X'C0'+0),CL8'P       ',AL3(POINT)               T002
         DC    AL1(X'C0'+0),CL8'U       ',AL3(UP)                  T002
         DC    AL1(X'C0'+0),CL8'=       ',AL3(EQUALS)              T002
         DC    AL1(X'C0'+0),CL8'?       ',AL3(HELP)                T002
         DC    AL1(X'C0'+0),CL8'H       ',AL3(HELP)                T002
         DC    AL1(X'C0'+0),CL8'#       ',AL3(CALC)                T002
         DC    AL1(X'C0'+0),CL8'W       ',AL3(WINDOW)              T002
         DC    AL1(X'C0'+0),CL8'%       ',AL3(INDPOINT)            T002
         DC    X'FF'
         TITLE 'STAXEXIT ----- STAX EXIT'
*
*  GIVEN PTR TO DSA IN STAXLIST, TURN ON ATTENTION INDICATOR
*  AND RETURN QUICKLY
*
         SPACE 2
         USNGX STAXEXIT,R15                                        U004
STAXEXIT L     R3,8(,R1)                GET USADDR FIELD (R13)     U004
         USNGX DSA,R3                                              U004
         LR    R2,R14                   SAVE RETURN ADDR
         TM    FLAGS2,ATTNHIT           ALREADY SET?               U014
         BNO   STAXFRST                 NO - OK                    T003
         AIF   (&TONE).TONE03                                      T003
         LA    R1,1000                  ABEND CODE                 U004
         SVC   13                       LET HIM OUT QUICK          U004
         AGO   .NTONE03                                            T003
.TONE03  ANOP  ,                                                   T003
         XR    R15,R15                  CLEAR RETURN CODE          T003
         SVC   3                        EXIT TO SYSTEM ???         T003
.NTONE03 ANOP  ,                                                   T003
STAXFRST OI    FLAGS2,ATTNHIT           INDICATE ATTN SIGNALED     T003
         NI    FLAGS3,255-FSMODE        ATTN BREAKS FULLSREEN MODE U024
         POST  TSECB                    POST ATTN ECB
         XR    R15,R15                  RC=0 TO STAX
         BR    R2                       RETURN
         DROPX R15,R3                                              U004
         TITLE 'ZAP --- TSO PARSE DSECTS/CSECTS  -  PDL, PCL, PDE, ETC'
         PRINT NOGEN
PCL      IKJPARM DSECT=IKJPARMD
         AIF   (&MVS OR &TONE).MVS08                               T001
         MNOTE 0,'*** USERID WILL NOT BE PREFIXED TO DSN ***'      U004
PDLDSN   IKJPOSIT DSNAME,PROMPT='DATA SET NAME'
         AGO   .MVT05                                              U004
.MVS08   ANOP  ,                                                   T002
PDLDSN   IKJPOSIT DSNAME,USID,PROMPT='DATA SET NAME'               U004
.MVT05   ANOP  ,                                                   T002
PDLVOL   IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=VOLUME
PDLALC   IKJKEYWD  DEFAULT=                                        U004
         IKJNAME  'ALLOCDSN',SUBFLD=PDLALCSF                       U004
CRTK     IKJKEYWD
         IKJNAME 'CRT'
NOT3270K IKJKEYWD DEFAULT=                                         U004
         IKJNAME 'NOT3270'                                         U004
LOGK     IKJKEYWD DEFAULT='LOG  '                                  U004
         IKJNAME 'LOG'
         IKJNAME 'NOLOG'                                           U004
NWK      IKJKEYWD DEFAULT='TERSE'
         IKJNAME 'VERBOSE'
         IKJNAME 'TERSE'
NOSTAXK  IKJKEYWD DEFAULT=                                         U021
         IKJNAME 'NOSTAX'                                          U021
         AIF   (NOT &VSAM).NVSAM50                                 T002
NOACBK   IKJKEYWD DEFAULT=                                         T002
         IKJNAME 'NOACB'                                           T002
         IKJNAME 'NOVSAM'                                          T002
         IKJNAME 'DCB'                                             T002
.NVSAM50 ANOP  ,                                                   T002
VOLUME   IKJSUBF
VOLVOL   IKJIDENT 'VOLUME SERIAL',FIRST=ALPHA,OTHER=ALPHANUM,MAXLNTH=6
PDLALCSF IKJSUBF
         AIF   (&MVS OR &TONE).MVS09                               T001
         MNOTE 0,'*** USERID WILL NOT BE PREFIXED TO ALLOCDSN ***' U015
PDLALCDS IKJPOSIT  DSNAME,PROMPT='NAME OF DATASET TO ALLOC'        U015
         AGO   .MVT06                                              U015
.MVS09   ANOP  ,                                                   T002
PDLALCDS IKJPOSIT  DSNAME,USID,PROMPT='NAME OF DATASET TO ALLOC'   U012
.MVT06   ANOP  ,                                                   T002
         IKJENDP
         EJECT
         PRINT GEN
         SPACE 4
DSA      DSECT                          RESUME STORAGE DEFINITION  U024
         SPACE 2
*U024    MACRO
*U024 &N TSWORK &DUMMY
*U024    GBLB  &MVS,&MVT,&INST370                                  U021
*U024    GBLB  &VS1,&TONE,&SEC,&VSAM                               T002
*
*  OLD CS DSECT SORTA
*
GODFLAG  DS    X                        MOST USEFUL FLAG
GOD      EQU   X'FF'                    ONLY HE CAN CHANGE HIS MIND
NOGOD    EQU   X'00'                    TOUGH BANANAS
         AIF   (&MVS).MVS01                                        U004
JQFLAG   DS    X                        .                         *LPR*
.MVS01   ANOP  ,                                                   T001
USERID   DS    CL7                      TSO USERID                 U021
         SPACE 2
DISPCONS DS    0F                       DISPLAY CONSTANTS          U004
NOWMASK  DS    F'-8'                                               U004
BITMASK  DS    F'7'                                                U004
LINELEN  DS    H'40'                    LENGTH OF LINES TO USE     U004
SCRBYTES DS    H'96'                    (BYTES/SCREEN)/2           U004
LINEHEX  DS    H'16'                    NUMBER OF BYTES/LINE HEX   U004
LINEHEX2 DS    H'32'                    TWICE THE ABOVE            U004
BYTES    DS    16AL1                    TRTAB FOR CARET            U004
DISPCONL EQU   *-NOWMASK                LEN OF DISPLAY CONSTANTS   U004
         SPACE 2
OVERLAY1 DS    0D                                                  T002
TEMPTRT  DS    XL256                    TEMP TRTAB FOR STRING ANAL
         ORG   OVERLAY1                 OVERLAY FOLLOWING DATA     T002
         SPACE 2
TMPLIST  TMPMAC MF=L                    FOR 'TSO'
TMPLISTL EQU   *-TMPLIST                LEN
         SPACE 2
         ORG   OVERLAY1                 OVERLAY FOLLOWING DATA     T002
         SPACE 2
*
*  ZAP'S DATA AREA FOR EXP
*
GORF     DS    F                        SAVE AREA FOR PTR
EXPOPT   DS    X                        WHETHER TO SCAN LABLE TAB
YESSYMB  EQU   X'FF'                    EXP LOOK AT SYMBOL TAB
NOSYMB   EQU   X'00'                    EXP LOOK AT SYMBOL TAB
         SPACE 1
DWD      DS    2D                       SOME TURKEY SLOPS OVER     U015
EXPWORK  DS    D
EXPARMS  DS    0F                       'SAVEAREA' FOR 'EXP'
EXPFLAG  DS    3F
         DS    A
EXPSTART DS    A
         DS    F                        LENGTH
EXPPTR   DS    A
         SPACE 2
         AIF   (NOT &VSAM).NVSAM01                                 T002
         ORG   OVERLAY1                 OVERLAY FOLLOWING DATA     T002
VSAMCB   DS    XL128                    AREA FOR SHOWCB, MODCB,    T002
VSAMCBA  DS    XL128                    OUTPUT AREA FOR ABOVE      T002
.NVSAM01 ORG   OVERLAY1                 BACK OUT OF OVERLAY1       T003
SETAREA  DS    CL32                     SETSTRNG UNPACK WORK AREA  T003
         ORG   ,                                                   T003
         SPACE 3
*
*  ZAP'S DATA AREA
*
NOWSTUFF DS    0F                                                  U004
*  FOLLOWING 3 LINES *MUST*BE*TOGETHER*
OLDPOINT DS    H                        OFFSET INTO BUFFER
OFFSET   DS    F                        (OLDPOINT + BASEVAL)       U004
BASEVAL  DS    F                        BASE VALUE                 U004
NOWLEN   EQU   *-NOWSTUFF                                          U004
         SPACE 2
AIDEFTAB DS    A                        ADDR OF DEFINE TABLE
AITRCTAB DS    A                        ADDR OF TRACE TABLE        U004
AITREND  DS    A                        -> END OF TRACE TABLE      U004
FIRSTSCR DS    A                        ADDR OF FIRST SCREEN LOC
MAXSCR   DS    A                        ADDR OF END OF AVAIL SCR
ADDRWORK DS    A                        ADDRESS OF MSG AND WTO WORK
CSOUTWK  EQU   ADDRWORK                 CSOUT WORKAREA IS FIRST
LINKREAD DS    A                        LINK REG STORAGE FOR READ  T002
LINKLOG  DS    A                        LINK REG STORAGE FOR LOG   T002
INSTRSAV DS    A                        -> TO INSTRUCTION TO EX    T003
         SPACE 2
$L       EQU   79                       LENGTH OF DISPLAY LINE     U004
$I       EQU   81                       LINE INCREMENT (IN SCREEN) U004
SCREEN   EQU   *                                                   U011
CTL3270  DS    XL(CTL3270L)             WHERE THE 3270 STUFF GOES  U024
         DS    X'1D60'                  PROT NORMAL                U011
LINE01   DS    CL79,X'1DC8'             SCREEN BUFFER...           U011
LINE02   DS    CL79,X'1D60'             .                          U004
LINE03   DS    CL79,X'1DC8'             .                          U004
REP      DS    CL79,X'1DF8'             .                          U004
LINE05   DS    CL79,X'1D60'             .                          U004
LINE06   DS    CL79,X'1D60'             .                          U004
LINE07   DS    CL79,X'1D60'             .                          U004
LINE08   DS    CL79,X'1D60'             .                          U004
LINE09   DS    CL79,X'1D60'             .                          U004
LINE10   DS    CL79,X'1D60'             .                          U004
LINE11   DS    CL79,X'1D60'             .                          U004
LINE12   DS    CL79,X'1D60'             .                          U004
LINE13   DS    CL79,X'1D60'             .                          U004
LINE14   DS    CL79,X'1D60'             .                          U004
LINE15   DS    CL79,X'1D60'             .                          U004
LINE16   DS    CL79,X'1D60'             .                          U004
LINE17   DS    CL79,X'1D60'             .                          U004
LINE18   DS    CL79,X'1DF8'             .                          U004
LINE19   DS    CL79,X'1D60'             .                          U004
LINE20   DS    CL79,X'1D60'             .                          U004
LINE21   DS    CL79                     .                          U011
END3270  DS    XL(END3270L)             WHERE THE REST GOES        U024
SCREENL  EQU   *-SCREEN                 LENGTH OF SCREEN DATA      U011
REGS3270 DS    A(SCREEN,SCREENL)        TPUT FULLSCR REGS          U011
ASCRWORK DS    A(SCRWORK)               -> SCREEN WORK AREA        U005
AOLDSCR  DS    A(OLDSCR)                -> SCREEN SAVE AREA        U011
REPX     DS    CL(1+2+(3+79)*2)         REPLY BUFF FOR TGET ASIS   U006
PREVREP  DS    CL79                     THE PREVIOUS REPLY         U012
VERSION  DS    C'VERSION=V.MC DDMMMYY LDW'                         U004
CURPOS   DS    H                        CURSOR POSITION            U004
TERMSAVE DS    A                        SAVE FOR R14 IN PUTLINE    U004
LINEDESC DC    Y($L+4,0)                THE "OLD" FOR PUTLINE      U004
LINEBUFF DS    CL80                     PUTLINE'S BUFFER           U004
DSNAMEL  DS    H                        LENGTH OF DSN
DSNAME   DS    CL44                     DSN
PASSWORD DS    CL8                      GUESS WHAT?                U004
DISPDSN  DS    CL44                     DISPLAYABLE DSN            U004
ALLOCDSN DS    CL44                     DSN TO ALLOCATE            U004
LENKEYDA DS    H                        COMBINED LEN OF KEY + DATA T002
TTR      DS    F                        TTR OF BLK
TTRSAVE  DS    F                        HOLDING TANK FOR TTR       T003
OLDTTR   DS    F                        TTR OF LAST STORE OR SET CMD
STOPDUMP DS    F                        TTR WHERE TO STOP DUMP     T002
ENDTTR   DS    XL4                      TTR OF LAST BLK IN DATASET T003
         SPACE 2
IDEFAVAL DS    A                        1ST AVAL POSIT IN DEFINE TAB
ITRAVAL  DS    A                        1ST AVAIL TRACE TAB POSIT
CURRITR  DS    F                        CURRENT TRACE TABLE ENTRY PTR
         SPACE 2
TRTABADD DS    A                        PTR TO TRTAB FOR DUMP DISPLAY
TEMP     DS    2D                       WORK AREA
TEMP2    DS    5D                       WORK AREA
         SPACE 1
MIDLINE  DS    A                        CARET
WIDTHS   DS    0F                       FOR WINDOWS       ×**********×
WIDTHD   DS    F                        FOR DOWN          ×CONTIGUOUS×
WIDTHU   DS    F                        UP                ×**********×
         SPACE 2
REALRDLN DS    H                        ACTUAL COMND READ LENGTH
READLEN  DS    H                        READLENGTH FROM TGET
         SPACE 1
TRACE    DS    X                        FLAG-TRACE IN EFFECT OR NOT
YESTRACE EQU   X'00'                    TRACE IN EFFECT
NOTRACE  EQU   X'FF'                    NO TRACE
         SPACE 1
CONTINUE DS    X                        STORES, SCAN, SET IN EFFECT
YESCONTL EQU   X'80'                    SCAN CONTINUE IS OK
YESCONTS EQU   X'40'                    S,X,O,N NO OPERANDS OK
YESCONTT EQU   X'20'                    SET NO OPERAND OK
         SPACE 1
IOERROR  DS    X                        FLAG-SYNAD I/O ERROR OR NOT
YESSYN   EQU   X'FF'                    SYNAD I/O ERROR HAS OCCURRED
NOSYN    EQU   X'00'                    NO ERROR HAS OCCURRED
         SPACE 1
CPUTFLAG DS    X                        WHETHER CSOUT CAN ENQ OR NOT
YESCSERR EQU   X'FF'                    CSOUT CANNOT ENQ IN THIS DS
NOCSERR  EQU   X'00'                    CSOUT HAS NO PROBLEMS
         SPACE 1
ENQIT    DS    X                        WHETHER THERES ANYTHING TO ENQ
CHNGED   DS    X                        SET IF BUFFER NEEDS CHKPOINTING
         SPACE 1
FLAGS2   DS    X                        MISC FLAGS FOR TSO VERSION
MUSTZAP  EQU   X'80'                    BLK NOT REP, ENTER SAVE/EN
ATTNHIT  EQU   X'40'                    ATTN HIT SINCE LAST TGET   U014
SENSF    EQU   X'20'                    URSA OR SYS DS
LOGF     EQU   X'10'                    IN LOGGING STATE
NOWHEREF EQU   X'08'                    WHERE NOT NEEDED           U013
TERSEF   EQU   X'04'                    PERMANENTLY SUPPRESS WHERE U013
NOEOF    EQU   X'02'                    DON'T STOP ON EOF'S        T002
         SPACE 1
FLAGS3   DS    X                        CRT AND 3270 FLAGS         U005
RESHOWF  EQU   X'80'                    REFRESH WHOLE SCR NXT TPUT U005
NODISPF  EQU   X'40'                    DON'T DISP SCRN THIS TIME  U013
M3270F   EQU   X'10'                    3270 OPTION ON             U014
FSMODE   EQU   X'08'                    FULLSCR MODE ACTIVE        U014
CRTF     EQU   X'04'                    CRT (NON-3270) MODE        U014
NOSTAXF  EQU   X'02'                    NOSTAX KEYWORD GIVEN       U021
         SPACE 1
FLAGESD  DS    X                        FLAGS                      T002
FESDTEXT EQU   X'80'                    NEXT BLOCK IS TEXT         T002
CHGIDR   EQU   X'40'                    WE HAVE CHANGED THE IDR    T002
         SPACE 1
FLAGVSAM DS    X                        STAUS FLAGS FOR VSAM       T002
         AIF   (NOT &VSAM).NVSAM02                                 T002
VSAMSEQ  EQU   X'80'                    LAST I/O WAS SEQ READ      T002
VSAMKEY  EQU   X'40'                    LAST I/O WAS SEARCH KEY    T002
VSAMRBA  EQU   X'20'                    LAST I/O WAS SKIP SEQ RBA  T002
VSAMSEQB EQU   X'10'                    LAST I/O WAS SEQ READ BWD  T002
VSAMMASK EQU   X'0F'                    MASK TO RESET VSAM DIR.    T002
VSAMSEQR EQU   X'08'                    SEQ GETS IN RBA SEQ        T002
VSAMSEQK EQU   X'04'                    SEQ GETS IN KEY SEQ        T002
VSAMSEQM EQU   X'FF'-VSAMSEQR-VSAMSEQK  MASK TO SEQUENTIAL SEQ     T002
VSAMOPEN EQU   X'02'                    USING VSAM INSTEAD OF EXCP T002
.NVSAM02 ANOP  ,                                                   T002
SEQREAD  EQU   X'01'                    NEXT I/O IS SEQ READ       T002
         SPACE 1
FLAGS4   DS    X                                                   T002
NOACBF   EQU   X'80'                    FORCE NON-VSAM ACCESS      T002
         SPACE 1
FLAGCOM  DS    X                        FLAGS FOR VALID COMMANDS   T002
*        NOTE THAT ONLY BITS 0-3 ARE VALID HERE BECAUSE OF         T002
*           CHECKING LOGIC                                         T002
*        X'0F'  RESERVED                                           T002
COMVSAM  EQU   X'80'                    IN VSAM CLUSTER            T002
COMEXCP  EQU   X'40'                    USING EXCP ACCESS METHOD   T002
COMPO    EQU   X'20'                    DATASET DSORG = PO,POU     T002
*        X'10'  UNUSED                                             T002
         SPACE 2
LOOKFOR  DS    H,CL16                   SCAN WORK AREA (LEN+STRING)
ZAPSTRNG DS    H,CL16                   S,X,O,N WORK AREA (LEN+STRING)
SETSTR   DS    H,CL16                   SET WORK AREA (LEN+STRING)
         SPACE 2
ADDRCNT  DS    A                        ADDRESS OF RECORD COUNT    U004
ADDRKEY  DS    A                        ADDRESS OF RECORD KEY      U004
ADDRDATA DS    A                        ADDRESS OF RECORD DATA     U004
ADDRBUFF DS    A                        ONE OF THE ABOVE 3 ADDRS   U004
BUFFSIZE DS    H                        MAX BUFFER SIZE            U002
KEYLEN   DS    H                        CURRENT KEY LENGTH         U002
BLKLEN   DS    H                        ACTUAL BLK LEN AFTER READ  U002
         SPACE 2
ESDID    DS    H                        ESD ID
MEMTTR   DS    XL3                      SAVE MEMBER'S TTR
TXTTTR   DS    XL3                      LOAD MOD'S FIRST TEXT BLK  U002
CTL1TTR  DS    XL3                      TTR OF FIRST CTL REC OR 0  U023
TXTORIG  DS    F                        ORIG OF NEXT TEXT BLOCK    U023
MEMBER   DS    CL8                      CURRENT MEMBER NAME
NAME     DS    CL8                      ENTRY NAME FOR EPA SEARCH  U002
FINDMBR  DS    CL8                      MEMBER FOR PDS SEARCH      U019
LASTFMT1 DS    XL5                      CCHHR OF LAST FMT1 DSCB    U012
ESDTTR   DS    XL3                      START OF ESD               U004
         SPACE 2
DCBLIST  DS    F                        DCB PTR MF=L OPEN AND CLOSE
DSCBLIST EQU   TEMP2+16                                            U004
VOLSER   DS    CL6                      VOL=SER OF DSN
UNITNAME DS    CL8                      UNITNAME TO ALLOC          U018
         SPACE 2
OVERLAY2 DS    0F                                                  T002
         SPACE 1
         DS    F                        TURKEY SYSTEM NEED 4 BYTES T002
DCBU     DCB   MACRF=E,DDNAME=DDNAME,DEVD=DA                       U004
         DS    5F                       TURKEY SYSTEM NEED 72 BYTE U004
         SPACE 2
IOB      DC    X'42000000'              IOBFLAG                    U004
         DC    A(ECB)                   IOBECBA                    U004
CSW      DS    XL8                      IOBCSW                     U004
IOBCCWA  DC    A(CCWS,DCBU,0,0)         IOBCCWA,IOBDCBA,?,?        U004
MBBCCHHR DS    0XL8,XL3                 IOBSEEK                    U004
CCHHR    DS    XL5                      INTERESTING PART           U004
         SPACE 2
ECB      DS    A                        ECB FOR ECXP               U004
         SPACE 2
JFCB     DS    XL176                    OBVIOUS                    T002
EXLST    DS    A(JFCB)                  DITTO                      T002
         SPACE 2
CCWS     DS    0D                       HANDLE FOR ALL CCWS        U004
BUFF     EQU   0                                                   U024
CCW##R   CCW   X'31',CCHHR,X'40',5      SEARCH ID EQ               U024
         CCW   X'08',*-8,X'00',0        TIC *-8                    U024
CCW#R#R  CCW   X'1E',BUFF+0,X'20',*-*   READ CKD                   U024
         SPACE 1
CCW##W   CCW   X'31',CCHHR,X'40',5      SEARCH ID EQ               U024
         CCW   X'08',*-8,X'00',0        TIC *-8                    U024
CCW#W#W  CCW   X'0D',BUFF+8,X'20',*-*   WRITE KD                   U024
         SPACE 1
CCW##R0  CCW   X'16',BUFF+0,X'20',*-*   READ R0                    U024
         SPACE 1
CCW##D   CCW   X'16',BUFF+0,X'70',1     READ R0 TO ORIENT          U019
         CCW   X'E9',FINDMBR,X'40',8    SEARCH KEY EQ/HI MT        U024
         CCW   X'08',*-8,X'00',0        TIC *-8                    U024
CCW#D#R  CCW   X'06',BUFF+8,X'00',256   READ D                     U024
         SPACE 1
CCWL     EQU   *-CCWS                   LENGTH OF CCW PATTERNS     U004
         SPACE 1
         AIF   (&MVS).MVS02                                        T002
         SPACE 2
         AIF   (&VS1).VS101             VS1 HAS LONGER JOBQUEUE    T002
JQQMHDA  DS    9F                       MASTER QCR FROM JOBQUEUE   T002
         AGO   .MVT02                                              T002
.VS101   ANOP  ,                                                   T002
JQQMHDA  DS    52F                      MASTER QCR FROM JOBQUEUE   T002
.MVT02   SPACE 1
.MVS02   ANOP  ,                                                   T002
         AIF   (NOT &VSAM).NVSAM03                                 T002
         ORG   OVERLAY2                 OVERLAY VSAM OVER NON-VSAM T002
         SPACE 1
ACBV     ACB   AM=VSAM,DDNAME=DDNAME,MACRF=(CNV,SEQ,DIR,IN)        T002
         SPACE 2
RPLV     RPL   AM=VSAM,ACB=ACBV,OPTCD=(CNV,SEQ,MVE)                T002
         SPACE 2
EXLV     EXLST AM=VSAM,EODAD=0                                     T002
         SPACE 2
OPENKVSM DC    A(ACBVK)                                            T002
         SPACE 2
ACBVK    ACB   AM=VSAM,DDNAME=DDNAME,MACRF=(KEY,SEQ,DIR,IN)        T002
         SPACE 2
RPLVK    RPL   AM=VSAM,ACB=ACBV,OPTCD=(KEY,SEQ,LOC)                T002
         SPACE 2
ADDRKEYH DS    A                        -> MAX KEY IN CI           T002
ADDRKEYL DS    A                        -> MIN KEY IN CI           T002
VSAMRKP  DS    H                        RELATIVE KEY POS FOR VSAM  T002
VSAMCISZ DS    H                        SIZE OF A CI FOR CLUSTER   T002
VSAMKYSV DS    H                        SAVE AREA FOR KEYLEN       T003
         ORG   ,                        BACK OUT OF OVERLAY2       T002
.NVSAM03 SPACE 5
STAXLIST STAX  0,MF=L
STAXL    EQU   *-STAXLIST
         SPACE 5
PTPB     DS    0F,XL12                  FOR PUTLINE                U004
         PGPB  DSECT=NO
         SPACE 2
BLANKS   DS    CL133                    CONSTANT FOR CLEARING      U021
         SPACE 2
         DS    0F                                                  U021
IDEFTAB  DS    24XL10                   24 ENTRY DEFINE TABLE
ENDITAB  DS    X                        END OF TABLE (X'FF')
*
*  EACH ENTRY ABOVE IS OF THE FORM (8C,H), CORRESP TO 'SYMBOL,OFFSET'
*
         SPACE 2
ITRLEN   EQU   6                        LEN OF TRACE ENTRY         T002
ITRCNT   EQU   24                       COUNT OF TRACE ENTRIES     T002
         DS    0F                                                  U021
ITRTAB   DS    (ITRCNT)XL(ITRLEN)       TRACE TABLE                T002
ITREND   DS    X                        END OF TABLE (X'FF')
*
*  EACH ENTRY ABOVE IS OF THE FORM (4X,2X), 'TTR,OFFSET'
*                                           'RBA,OFFSET'           T003
         SPACE 3
OLDSCR   DS    CL(21*$I)                SCREEN SAVE AREA           U011
SCRWORK  DS    CL(SCREENL+3*21)         JUST PARTS THAT CHANGED    U005
         SPACE 2
*U024    MEND
DSALEN   EQU   *-DSA                    CALCULATE STORAGE LENGTH   U024
         EJECT
PDE      DSECT ,                        PARSE DESCRIPTOR ELEMENT
PDEPTR   DS    A                        TEXT PTR
PDELEN   DS    H                        LEN
PDEFLAG  DS    X                        FLAGS
PDEFPRES EQU   X'80'                    PARM PRESENT
         DS    X                        RESERVED
PDEPTR2  DS    A                                                   U004
PDELEN2  DS    H                                                   U004
PDEFLAG2 DS    X                                                   U004
         DS    X                                                   U004
PDEPTR3  DS    A                                                   U004
PDELEN3  DS    H                                                   U004
PDEFLAG3 DS    X                                                   U004
         DS    X                                                   U004
PDEL     EQU   *-PDE
         TITLE 'ZAP --- DSECT MASK EXPANSIONS'
*U004    DCBD  DSORG=DA                 DCB EXPANSION
IHADCB   DSECT                                                     U004
         ORG   IHADCB+X'D'                                         U023
DCBDVTBL DS    AL3                                                 U023
         ORG   IHADCB+X'24'                                        U004
DCBRECFM DS    X                                                   U004
RECFMU   EQU   X'C0'                    RECFM=U                    T002
DCBEXLST DS    AL3                                                 U004
DCBDDNAM DS    CL8                                                 U004
         ORG   IHADCB+X'2C'                                        U004
*CBIFLGS DS    0X                                                  U004
DCBDEBAD DS    A                                                   U004
         SPACE 6    (EJECT)
CVT      DSECT
*U004    CVT                            CVT EXPANSION
CVTPTR   EQU   16                                                  U004
         ORG   CVT+X'1C'                                           U004
CVTPCNVT DS    A                                                   U004
CVTPRLTV DS    A                                                   U004
         SPACE 6    (EJECT)
UCB      DSECT
*U004    IEFUCBOB                       UCB EXPANSION
         ORG  UCB+X'10'                                            T002
UCBTYP   DS   XL4'00'                                              T002
         ORG   UCB+X'1C'                                           U004
SRTEVOLI DS    CL6                                                 U004
         SPACE 6    (EJECT)
*U004    DEBDS                          DEB EXPANSION
DEBDS    DSECT ,                                                   U004
         ORG   DEBDS+X'10'                                         U004
DEBNMEXT DS    C                        # OF EXTENTS               U004
         ORG   DEBDS+X'20'                                         U004
DEBUCBAD DS    F                                                   U004
         DS    H                                                   U004
DEBSTRCC DS    H                        STARTING CYLINDER          U004
DEBSTRHH DS    H                        STARTING HEAD              U023
DEBENDCC DS    H                        ENDING CYLINDER            U004
         EJECT
*DSCB    DSECT
DSA      DSECT                          BACK TO THERE, MOMENTARILY U004
         ORG   LINE07                                              U004
DSCB     DS    0D                                                  U004
         IECSDSL1 1                     FORMAT 1 DSCB EXPANSION
DS1CCHHR DS    XL5                      ADDR OF CCHHR OF REAL DSCB T002
DSORGPO  EQU   X'02'                    DSORG=PO,POU OF DS1DSORG   T002
         SPACE 1
         ORG   ,                                                   U004
         SPACE 6
LOGLINE  DSECT                          ZAP LOG OUTPUT LINE FORMAT U022
LL@CC    DS    C                   +0   CARRIAGE CONTROL           U022
LL@MSGID DS    C'ZAPNNNI',C        +1   MESSAGE ID                 U022
LL@UID   DS    CL7,C               +9   CULPRIT'S TSO USERID       U022
LL@TEXT  DS   0C                   +9   MESSAGE TEXT STARTS HERE   U022
LL@TTRC3 DS   0C'TTR'              +9   CONSTANT                   U022
LL@RBAC3 DS    C'RBA',C            +9      "                       U022
LL@TTR   DS   0CL6                 +13  CURRENT TTR                U022
LL@RBA   DS    CL8,C               +13  CURRENT RBA                U022
LL@2BC2  DS   0C'TO BE CHANGED TO' +22  CONSTANT                   U022
LL@XMSG  DS   0C'FOLLOWING BLOCK TO BE SET TO'                     U022
*        DS   0C'IDR FOR CSECT NNN UPDATED'                        U022
*        DS   0C'MEMBER XXXXXXXX SELECTED'                         U022
*        DS   0C' ENTRY XXXXXXXX SELECTED'                         U022
*        DS   0C'BLOCK NOT REPLACED'                               U022
LL@OFFC6 DS    C'OFFSET',C         +22     "                       U022
LL@OFF   DS    CL4,C               +29  CURRENT OFFSET             U022
LL@DATC4 DS    C'DATA',C           +35  CONSTANT                   U022
LL@DATAX DS    CL32,C              +40  OLD/NEW DATA (HEX)         U022
LL@Q1    DS    C''''               +73  STARTING QUOTE             U022
LL@DATAC DS    CL16                +74  OLD/NEW DATA (CHAR)        U022
         DS    C''''               +90  SPACE FOR ENDING QUOTE     U022
LL@CHRC5 DS    C'CCHHR',C          +91  CONSTANT                   U022
LL@CCHHR DS    CL10                +97  CURRENT CCHHR              U022
         SPACE 6
JFCDSECT DSECT ,                                                   T002
*        IEFJFCBN ,                                                T002
INFMJFCB EQU   *                        JFCB ORIGIN                T002
JFCBDSNM DS    CL44                     DSNAME                     T002
         ORG   JFCDSECT+52                                         T002
JFCBTSDM DS    X                        JOB MANAGEMENT INTERFACE   T002
JFCNWRIT EQU   X'08'                    DON'T REWRITE JFCB         T002
         AIF   (NOT &VSAM).NVSAM45                                 T002
         ORG   JFCDSECT+98                                         T002
JFCDSORG DS    0XL2                     DSORG FLAGS                T002
JFCDSRG1 DS    X                                                   T002
JFCDSRG2 DS    X                                                   T002
JFCORGAM EQU   X'08'                    DSORG = AM (VSAM)          T002
         SPACE 3
*        IFGACB AM=VSAM                                            T003
IFGACB   DSECT ,                                                   T003
         ORG   IFGACB+12                                           T003
ACBMACF  DS    0BL2                     MACRF PARTS 1,2            T003
ACBMACR1 DS    B                        FIRST BYTE                 T003
ACBKEY   EQU   X'80'                    KEY SEQUENCE?              T003
ACBSEQ   EQU   X'10'                    SEQUENTIAL PROCESSING      T003
ACBDIR   EQU   X'08'                    DIRECT PROCESSING          T003
ACBIN    EQU   X'04'                    INPUT ONLY                 T003
ACBOUT   EQU   X'02'                    OPENED FOR OUTPUT?         T003
         ORG   IFGACB+36                                           T003
ACBEXLST DS    A                        EXIST LIST PTR             T003
ACBDDNM  DS    CL8                      ACB DDNAME                 T003
ACBOFLGS DS    B                        OPEN FLAGS                 T003
ACBOPEN  EQU   X'10'                    IS IT OPEN?                T003
         SPACE 3
*        IFGRPL AM=VSAM                                            T003
IFGRPL   DSECT ,                                                   T003
         ORG   IFGRPL+13                                           T003
RPLFDBK  DS    XL3                      RPL FEEDBACK CODES         T003
         ORG   IFGRPL+16                                           T003
RPLKEYLE DS    H                        GENERIC KEY LEN            T003
         ORG   IFGRPL+24                                           T003
RPLDACB  DS    A                        PTR TO ACB                 T003
         ORG   IFGRPL+32                                           T003
RPLAREA  DS    A                        PTR TO DATA AREA           T003
RPLARG   DS    A                        PTR TO SEARCH ARGUMENT     T003
RPLOPTCD DS    0BL4                     OPTION CODES               T003
RPLOPT1  DS    B                        OPTION BYTE 1              T003
RPLLOC   EQU   X'80'                    LOCATE MODE (0 = MOVE MODE)T003
RPLDIR   EQU   X'40'                    DIRECT ACCESS              T003
RPLSEQ   EQU   X'20'                    SEQUENTIAL ACCESS          T003
RPLKGE   EQU   X'04'                    KEY GE                     T003
RPLOPT2  DS    B                        OPTION BYTE 2              T003
RPLKEY   EQU   X'80'                    KEY SEQUENCE               T003
RPLBWD   EQU   X'10'                    BACKWARD SEQUENCE          T003
RPLUPD   EQU   X'02'                    UPDATE ACCESS              T003
RPLNSP   EQU   X'01'                    NOTE CCW STRING POS        T003
         ORG   IFGRPL+52                                           T003
RPLBUFL  DS    A                        LEN OF BUFFER              T003
.NVSAM45 AIF   (&MVS).MVS10                                        T002
         EJECT
QMRCAR   DSECT                          QUEUE MANAGER RESIDENT AREA
         IEFQMRES
JQQMHDAL EQU   *-QMRCAR                 LEN TO MOVE                T002
.MVS10   EJECT
WORKAREA DSECT ,                                                   T002
*U004    CSOUT MF=L,COUNT=YES
         DS    0D                   (+)
         DS    X,AL3                (+) FLAGS/ADDR OF LINE BUFFER
         DS    860X                 (+) REMAINING DATA
         SPACE 1
         DS    0D     (BUFFER)
LCSOUT   EQU   *-WORKAREA
         SPACE 6  (EJECT)
ESDDATA  DSECT ,                        ESD DATA ENTRY             U002
ESDDNAME DS    CL8                      ENTRY PT NAME OF 8X'00'    U002
ESDDTYPE DS    X                        TYPE                       U002
ESDDADDR DS    AL3                      LKED ADDR OF ENTRY PT      U002
ESDDSEG  DS    AL1                      SEGMENT NUMBER             U002
ESDDLEN  DS    AL3                      LENGTH OF ENTRY            U002
ESDDL    EQU   *-ESDDATA                LENGTH ESD DATA ENTRY      U002
         TITLE 'ZAP --- EXP: EXPRESSION ANALYZER'
         SPACE 1
*        EXP: THIS ROUTINE WILL ANALYZE AN EXPRESSION PASSED TO IT
*        AND RETURN ITS VALUE CONCATENATED WITH A STARTING VALUE.
*
*        INPUT: R13 - SAVEAREA
*               R14 - RETURN ADDRESS
*                     IF EXPOPT IS ZERO - TABLE NOT SCANNED
*                     ELSE SYMBOL TABLE IS SCANNED.
*               R2  - STARTING VALUE OF '*'                        U004
*               R1  - ADDRESS OF EXPRESSION TO PARSE
*               R0  - LENGTH OF EXPRESSION TO PARSE
*        OUTPUT:R1  - RETURN CODE
*                     IF NEGATIVE: NO ERRORS DETECTED
*                     IF ZERO OR POSITIVE AN ERROR OCCURED AT ADDRESS
*                     GIVEN IN R1
*               R15 - IF NO ERROR, THE NEW VALUE CALCULATED.
         SPACE 1
*  THIS ROUTINE WAS WRITTEN BY DON WORTH.
         SPACE 1
*  MODIFIED BY LEONARD D. WOREN TO DO MULT & DIV    12-27-78       U004
*  CHANGE CURRENT LOC SYMBOL FROM '*' TO '$'        07-31-80       U019
         SPACE 1
         USNGX DSA,R13
EXP      CSECT ,                                                   T002
         STM   R14,R1,EXPARMS+12        SAVE CALLER'S REGS HERE    T002
         STM   R2,R12,12+(4*4)(R13)     SAVE REST IN OTHER PLACE
         LR    R12,R15                  GET BASE                   U004
         USNGX EXP,R12                                             U004
         LR    R4,R2                    ALWAYS BEGIN WITH $        U004
         ST    R2,EXPSTART              SAVE VALUE OF "$"          U004
         LR    R3,R1                    START SCAN
         BCTR  R3,0                     HE WILL ADD ONE FIRST
         LR    R2,R0                    BCT INDEX
         MVI   EXPFLAG+1,0              RESET FLAGS                U004
         LA    R2,1(,R2)                ALLOW FOR FIRST PASS
         ST    R1,GORF                  FIRST BAD SPOT
         B     EXPBACK                  START AFTER ITEM
         SPACE 1
*        SEE WHAT NEXT ITEM IS
         SPACE 1
EXPITEM  LTR   R2,R2                    ANYTHING LEFT?
         BP    EXPON                    YES
         ST    R4,EXPSTART              NEW STARTER
         OI    EXPPTR,X'80'             NO ERROR
         B     EXPRET                   RETURN
EXPON    CLI   0(R3),C' '               BLANK?
         BNE   EXPCHECK                 NO, WE CAN USE IT
EXPNEXT  LA    R3,1(,R3)                NEXT SPOT TO CHECK
         BCT   R2,EXPITEM               CONTINUE
         MVI   EXPFLAG+1,0              NOTHING FOR NEXT TIME
         B     EXPCONV                  WHEN AT END, USE LAST IF ANY
EXPCHECK ST    R3,GORF                  KEEP TRACK OF WHERE WE ARE
         CLI   0(R3),C'$'               CURRENT LOC?               U019
         BE    EXPCURR                  YES, GO LOAD IT            U019
         CLI   0(R3),C'+'               ADD?
         BE    EXPADD                   YES
         CLI   0(R3),C'-'               SUBTRACT?
         BE    EXPSUB                   YES
         CLI   0(R3),C'.'               DECIMAL DONE?
         BE    EXPDEC                   YES
         CLI   0(R3),C'*'               MULTIPLY?                  U019
         BE    EXPMULT                  YES                        U019
         CLI   0(R3),C'/'               DIVIDE?                    U004
         BE    EXPDIV                   YES                        U004
*U019 %  OI    EXPFLAG+1,EXPEXOPR       EXPECTING OPERATOR NOW     U004
         SPACE 1
*        IF NO TERMINATOR IS FOUND, SAVE UP FOR LATER
         SPACE 1
         LA    R0,EXPWORK+8             POINT PAST IT
         CR    R0,R5                    TOO FAR?
         BNH   EXPERR                   YES
         MVC   0(1,R5),0(R3)            COPY IT TO WORKAREA
         LA    R5,1(,R5)
         B     EXPNEXT                  CONTINUE
         SPACE 1
*        IF AN ERROR IS DETECTED, PASS BACK ADDR OF IT
         SPACE 1
EXPERR   MVC   EXPPTR(4),GORF           SAVE POINTER FOR RETURN
EXPRET   MVI   EXPOPT,NOSYMB            RESET NO SYMB TAB LOOKUP
         LM    R14,R1,EXPARMS+X'C'      GET RETURN REGS
         LM    R2,R12,12+(4*4)(R13)     RESTORE REST OF REGS FROM SAVE
         BR    R14
         SPACE 2
*  IF '$' WAS ENTERED, USE CURRENT VALUE                           U019
         SPACE 1
EXPCURR  L     R15,EXPSTART             GET STARTING VALUE
*U019 %  MVI   EXPFLAG+1,EXPEXOPR       EXPECTING OPERATOR NOW     U004
         B     EXPUSE                   AND GO USE IT
         SPACE 2
*  IF '+' WAS ENTERED, TERMINATE PREVIOUS AND SET FLAG
         SPACE 1
EXPADD   MVI   EXPFLAG+1,EXPFPLUS       INDICATE ADD NEXT TIME
         B     EXPCONV                  GO CONVERT IT
         SPACE 2
*  IF '-' WAS ENTERED, TERMINATE PREVIOUS AND SET FLAG
         SPACE 1
EXPSUB   MVI   EXPFLAG+1,EXPFMINS       INDICATE ADD NEXT TIME
         B     EXPCONV                  GO CONVERT IT
         SPACE 2
*  IF '*' WAS ENTERED, TERMINATE PREVIOUS AND SET FLAG             U019
         SPACE 1
EXPMULT  MVI   EXPFLAG+1,EXPFMULT       INDICATE MULT NEXT TIME    U004
         B     EXPCONV                  GO CONVERT IT              U004
         SPACE 1
*  IF '/' WAS ENTERED, TERMINATE PREVIOUS AND SET FLAG             U019
         SPACE 1
EXPDIV   MVI   EXPFLAG+1,EXPFDIV        INDICATE DIV NEXT TIME     U004
         B     EXPCONV                  GO CONVERT IT              U004
         SPACE 1
*  IF '.' WAS ENTERED, TERMINATE PREVIOUS AS DECIMAL
         SPACE 1
EXPDEC   OI    EXPFLAG,EXPFDEC          INDICATE DECIMAL THIS TIM
*        B     EXPCONV                  GO CONVERT IT
         SPACE 1
*  WHEN UP TO 8 CHARS SCANNED OUT, CONVERT THEM APPROPRIATELY
         SPACE 1
EXPCONV  LA    R15,EXPWORK+1            POINT TO START
         SR    R5,R15                   FIND EXECUTE LENGTH
         LR    R15,R5                   SAVE IT                    U004
         BNM   EXPCOK                   IF SOMETHINGS THERE, CONTINUE
         TM    EXPFLAG,EXPFTERM         SOMETHING EXPECTED?
         BZ    EXPBACK                  NO, IGNORE THIS TIME
         B     EXPERR                   OTHERWISE ITS NO GOOD
EXPCOK   MVC   DWD(0),EXPWORK           <<<  EXECUTED  >>>
         MVI   DWD,C' '
         MVC   DWD+1(7),DWD             CLEAR AN AREA
         EX    R15,EXPCOK               MOVE TO IT
         SPACE 1
*        IF SYMBOL TABLE SCAN REQUESTED, DO SO
         SPACE 1
         CLI   EXPOPT,NOSYMB            SCAN LABEL TABLE?
         BE    EXPNTAB                  NO, GO ON
         TM    EXPFLAG,EXPFDEC          DECIMAL?
         BO    EXPNTAB                  THEN CAN'T BE THIS
         L     R14,AIDEFTAB             POINT TO TABLE
EXPIDSCN CLI   0(R14),255               END OF TABLE?
         BE    EXPNTAB                  THEN WE DIDN'T FIND IT
         CLC   0(8,R14),DWD             SEE IF THIS IS HIS LABEL
         BE    EXPGLAB                  YES, IT IS
         LA    R14,10(,R14)             NEXT ENTRY IN TABLE
         B     EXPIDSCN                 GO ON
EXPGLAB  LH    R15,8(,R14)              GET VALUE OF IT
         B     EXPUSE                   AND USE IT
         SPACE 1
*        VALIDITY CHECK FOR NUMERICS
         SPACE 1
EXPNTAB  LA    R0,1(,R15)               GET LENGTH OF THINGY
         LA    R14,EXPWORK              FIND IT
EXPCCHK  CLI   0(R14),C'0'              IF ITS NUMERIC I ALWAYS LIKE IT
         BNL   EXPCGOOD                 GO
         TM    EXPFLAG,EXPFDEC          DECIMAL?
         BO    EXPERR                   THEN NO GOOD
         CLI   0(R14),C'A'              OTHERWISE NOT LESS THAN A
         BL    EXPERR                   GO
         CLI   0(R14),C'F'              OR GREATER THAN F
         BH    EXPERR                   GO
         IC    R1,0(,R14)               GET IT
         AH    R1,EXPFUDGE              MAKE FA-FF
         STC   R1,0(,R14)               REPLACE IT
EXPCGOOD LA    R14,1(,R14)              NEXT
         BCT   R0,EXPCCHK               CONTINUE CHECKING
         SPACE 1
*        CONVERT DECIMAL
         SPACE 1
         TM    EXPFLAG,EXPFDEC          DECIMAL?
         BZ    EXPNDEC                  NO
EXPPACK  PACK  DWD(8),EXPWORK(0)        <<< EXECUTED >>>
         EX    R15,EXPPACK              PACK IT
         CVB   R15,DWD                  CONVERT IT
         B     EXPUSE                   AND USE IT
         SPACE 1
*        CONVERT FOR HEX
         SPACE 1
EXPNDEC  LA    R15,1(,R15)              DO AN EXTRA CHAR
         EX    R15,EXPPACK              PACK IT
         LM    R14,R15,DWD              GET OFFSET
         SRDL  R14,8                    SHIFT OUT BAD BYTE (FLIP)
*        B     EXPUSE                   GO USE IT
         SPACE 1
*        WHEN VALUE OBTAINED, ADD, SUB OR WHATEVER
         SPACE 1
EXPUSE   TM    EXPFLAG,EXPFPLUS         ADDING?
         BO    EXPUADD                  YES
         TM    EXPFLAG,EXPFMINS         SUBTRACTING?
         BO    EXPUSUB                  YES
         TM    EXPFLAG,EXPFMULT         MULTIPLYING?               U004
         BO    EXPUMULT                 YES                        U004
         TM    EXPFLAG,EXPFDIV          DIVIDING?                  U004
         BO    EXPUDIV                  YES                        U004
         LR    R4,R15                   ELSE, OVERLAY
         SPACE 1
EXPBACK  LA    R5,EXPWORK               START AGAIN
         MVC   EXPFLAG(1),EXPFLAG+1     NEW FLAGS
         MVI   EXPFLAG+1,0              NO MORE
         B     EXPNEXT                  SKIP LAST ONE
EXPUADD  AR    R4,R15
         B     EXPBACK                  GO ON
EXPUSUB  SR    R4,R15                   SUBTRACT
         B     EXPBACK                  GO
         SPACE 1
EXPUMULT LR    R5,R4                    GET INTO ODD REG           U004
         MR    R5-1,R15                 DO THE MULTIPLY            U004
         LR    R4,R5                    GET RESULT IN CORRECT REG  U004
         B     EXPBACK                  GO ON                      U004
         SPACE 1
EXPUDIV  LR    R5,R4                    GET INTO ODD REG           U004
         XR    R4,R4                    CLEAR FOR DIVIDE           U004
         LTR   R15,R15                  DIVIDE BY 0?               U004
         BZ    EXPERR                   YES - ERROR                U004
         DR    R5-1,R15                 DO THE MULTIPLY            U004
         LR    R4,R5                    GET RESULT IN CORRECT REG  U004
         B     EXPBACK                  GO ON                      U004
         SPACE 5
*
*  EXP'S DATA
*
         SPACE 1
EXPFUDGE DC    0H'0',XL2'0039'
EXPFPLUS EQU   X'80'                    ADD NEXT VALUE
EXPFMINS EQU   X'40'                    SUBTRACT NEXT VALUE
EXPFDEC  EQU   X'20'
EXPFMULT EQU   X'10'                    MULT NEXT VALUE            U004
EXPFDIV  EQU   X'08'                    DIV NEXT VALUE             U004
*XPEXOPR EQU   X'04' %                  EXPECTING OPERATOR NEXT    U004
*  SOMETHING REQUIRED FLAG:
EXPFTERM EQU   EXPFPLUS+EXPFMINS+EXPFDEC+EXPFMULT+EXPFDIV          U004
         SPACE 5
         PRINT GEN
         LTORG ,
         SPACE 2
         DROPX ,                                                   U004
         TITLE 'C S O U T -- URSA PROCESSOR PRINTING ROUTINE'
***********************************************************************
CSOUT    CSECT                                                        *
*        CSOUT: THIS ROUTINE WHEN CXCTLED TO OR CALLED BY AN URSA     *
*               PROCESSOR WILL ALLOCATE A PRINT FILE, WRITE OUTPUT    *
*               LINES INTO IT (OPTIONALLY WTOING THEM ALSO), AND      *
*               ENQUEUE THE FILE TO BE PRINTED BY THE MOVE SERVICE.   *
*               IT IS INVOKED BY THE CSOUT MACRO IN ONE OF THREE      *
*               TYPES OF CALLS:                                       *
*        INPUTS: R1 ==> CSAREA ADDRESS                                *
*                R2 ==> CSOUT WORKAREA ADDRESS                        *
*                                                                     *
*               CSOUT  OPN,VOLUME,TRKS,CALL=TYPE,MF=(TYPE,WORKAREA)   *
*                                                                     *
*               'OPN' REQUESTS THE FILE TO BE ALLOCATED AND OPENED.   *
*               'VOLUME' IS THE ADDRESS OF A 6 BYTE FIELD CONTAINING  *
*                        THE VOLUME NAME TO BE USED FOR THE FILE      *
*               'TRKS' IS THE ADDRESS OF A HALFWORD CONTAINING THE    *
*                        NUMBER OF TRACKS TO BE ALLOCATED             *
*               FOR CALL= 'TYPE' IS EITHER 'CXCTL' OR 'CALL'. IF      *
*               'CXCTL' IS SPECIFIED CSOUT IS INVOKED VIA CXCTL.      *
*               OTHERWISE IT IS VIA CALL.                             *
*               MF=(TYPE,WORKAREA) WHERE TYPE IS B (SET UP FOR CALL)  *
*                        OR E (SET UP FOR CALL AND CALL). 'WORKAREA'  *
*               IS THE ADDRESS OF A ??? BYTE AREA ALIGNED ON A        *
*               DOUBLE WORD TO BE USED BY CSOUT FOR WORK SPACE.       *
*                                                                     *
*               RETRN: R0 ==> 0 -- NORMALL, FILE OPEN, PUTS ACCEPTED  *
*                            ^0 -- ERROR RETURN FROM CSLOC/CSOPN      *
*                                                                     *
*               CSOUT  PUT,CALL=,MF=,WTO=OPTION                       *
*                                                                     *
*               WHERE WTO=OPTION MAY BE WTO=YES OR WTO=NO TO CAUSE    *
*                      THE LINE TO BE WTOED ALSO.                     *
*               THE LINE BUFFER IS PRINTED. (SEE BELOW FOR WORKAREA   *
*               DEFINITIONS)                                          *
*               OUTPUT: R0 ==> 0 NORMAL RETURN                        *
*                             ^0 I/O ERROR                            *
*                                                                     *
*               CSOUT  ENQ,BIN,COPIES,MF=,CALL=                       *
*                                                                     *
*               ENQUEUES THE FILE TO PRINT, RETURNS TO START STATE.   *
*               BIN IS ADDRESS OF 4 BYTE BIN NUMBER                   *
*               COPIES IS ADDRESS OF HALFWORD CONTAINING COPIES       *
*               MF AND CALL AS WITH OPN.                              *
*               OUTPUT: R0 ==> 0 NORMAL, DATA SET ENQUEUED TO PRINT   *
*                             ^0 ENQUEUE FAILED FOR SOME REASON       *
*                                                                     *
*               THE FIRST WORD OF THE WORKAREA CONTAINS THE ADDRESS   *
*               OF THE INTERNAL LINE BUFFER TO BE FILLED BY THE       *
*               CALLER EACH TIME HE DOES A PUT. IT IS SET AT 'OPN'    *
*               TIME. THE BUFFER IS CLEARED AFTER EACH CALL.          *
*                                                                     *
*        PROGRAMMER: DON D WORTH                                      *
*                                                                     *
*        ALTERED BY PCN FOR USE WITH TSO ZAP 1/30/77
*                                                                     *
*        MODIFIED BY LDW TO USE QSAM        12/26/78                  *
*                                                                     *
***********************************************************************
         SPACE 7
DATA     DSECT
OFLAGS   DS    0BL1
OPNCALL  EQU   X'80'                    THIS IS AN OPEN CALL
ENQCALL  EQU   X'40'                    THIS IS AN ENQUEUE CALL
PUTCALL  EQU   X'C0'                    THIS IS A PUT CALL (IF ZEROS)
DOWTO    EQU   X'20'                    ISSUE AS A WTO ALSO
OIOERROR EQU   X'04'                    AN I/O ERROR HAS OCCURED
SCRCALL  EQU   X'02'                    THIS IS A SCRATCH CALL
TRNCALL  EQU   X'01'                    THIS IS A TRUNC   CALL     U004
LINELOC  DS    A                        ADDRESS OF LINE BUFFER
*OVOLSER DS    A                        ADDRESS OF VOLUME NAME
*BINNO   EQU   OVOLSER                  ADDRESS OF BIN NUMBER
*SPACE   DS    A                        ADDRESS OF SPACE
*COPIES  EQU   SPACE                    ADDRESS OF COPIES
ODBLW    DS    D                        SCRATCH DOUBLE WORD
PAGE     DS    H                        PAGE COUNTER
LINE     DS    H                        LINE COUNTER
SAVEAREA DS    6F                       REGISTERS CHANGED GO HERE
RETREG   DS    F
         PRINT NOGEN
DCB      DCB   DDNAME=X,MACRF=PM,DSORG=PS,BLKSIZE=133,LRECL=133,   U004$
               RECFM=FBA,SYNAD=0
ODSN     DS    CL44
*OVOL    DS    CL6
WTOBUFF  DS    F                                                   U004
         ORG   *-1                      OVERLAY CC IN WTO PREFIX   U004
OBUF     DS    CL133                    BUFFER ITSELF              U004
         DS    XL4                      ROOM FOR ROUTCDE AND DESC  U006
TTLBUFF  DS    CL133                    BUFFER FOR TITLE LINE      U004
DATALEN  EQU   *-DATA
         PRINT GEN
         TITLE 'C S O U T -- MAIN ENTRY, DETERMINE CALL TYPE'
         SPACE 1
*        HOUSEKEEPING
         SPACE 1
CSOUT    CSECT
         USNGX DSA,R13                                             U004
         TM    FLAGS2,LOGF              ARE WE LOGGING?
         BNOR  R14                      IF NOT RETURN
         LTR   R2,R2                    ANY WORKAREA?
         BZR   R14                      NO, ICKY
         STM   R9,R12,SAVEAREA-DATA(R2) SAVE SOME REGS
         ST    R14,RETREG-DATA(R2)      SAVE WHERE TO GO BACK
*U004    LR    R13,R1                   COPY DSA PTR TO R13
         LR    R12,R15                  GET BASE REG
         LR    R10,R2                   GET WORKAREA ADDRESS
*U004    USNGX DSA,R13
         USNGX DATA,R10
         USNGX CSOUT,R12
         SPACE 1
*        DETERMINE CALL TYPE
         SPACE 1
         TM    OFLAGS,OPNCALL            OPEN?
         BO    OPEN                     YES, GO OPEN
         TM    OFLAGS,ENQCALL            ENQUEUE?
         BO    ENQUEUE                  YES
         TM    OFLAGS,SCRCALL            SCRATCH?
         BO    SCRATCH                  YES                        U004
         TM    OFLAGS,TRNCALL           TRUNCATE?                  U004
         BO    TRUNC                    YES                        U004
         B     PUTOUT                   NO, MUST BE PUT THEN       U004
         SPACE 1
*        WHEN DONE WITH WHATEVER, CLEAR LINE, AND PASS IT BACK
         SPACE 1
EXIT     MVC   OBUF,BLANKS              CLEAR LINE BEFORE RETURN   U004
         NI    OFLAGS,255-PUTCALL-DOWTO-TRNCALL  REMOVE CALL TYPE  U004
         LR    R0,R15                   GET RETURN CODE
         L     R14,RETREG               RESTORE RETURN REG
         LM    R9,R12,SAVEAREA          RESTORE OTHER REGS
         BR    R14                      RETURN TO CALLER
         TITLE 'C S O U T -- OPEN ROUTINE'
         SPACE 1
*        OPEN -- PREPARE FOR CSLOC, CREATE A DATASET NAME
         SPACE 1
OPEN     LA    R0,OBUF                  START IT OUT
         IC    R14,LINELOC              SAVE FLAGS
         ST    R0,LINELOC               STORE FIRST LINE ADDR
         STC   R14,LINELOC              RESTORE FLAGS
         AIF   (&TONE).TONE04                                      T001
         LA    R1,PTRRB                                            U026
         DYNALLOC                       ALLOCATE SYSOUT FILE       U026
         AGO   .NTONE04                                            T001
.TONE04  ALLOC SYSOUT='A',DDNAME==CL8'SYSZAPIT'                    T001
.NTONE04 ANOP  ,                                                   T001
         BXH   R15,R15,EXIT             RETURN WITH ERROR CODE
         MVC   ODSN(8),DDRET+6          COPY DDN                   U026
NOSYSOUT DS    0H                                                  U004
         XC    PAGE(4),PAGE             CLEAR PAGE AND LINE
         LA    R0,DCB                   POINT TO DCB
         ST    R0,ODBLW                 STORE IN DCB LIST
         MVI   ODBLW,X'8F'              MARK END OF LIST & OUTPUT
         MVC   DCB(DCBLEN),ODCBMSK      COPY IN MASK
         MVC   DCBDDNAM-IHADCB+DCB(8),ODSN COPY IN DDNAME          U007
         OPEN  MF=(E,ODBLW)
         TM    DCB+48,X'10'             SEE IF OPEN
         BO    RC00                     IF SO RETURN OK
         LA    R15,4                    IF NOT ERROR
         B     EXIT
         SPACE 5
TRUNC    TRUNC DCB                      WRITE OUT A SHORT BLOCK    U004
         SPACE 1
         B     RC00                     THAT WAS EASY              U004
         TITLE 'C S O U T -- PUT ROUTINE'
*        PUT -- CALCULATE LINE POSITION
         SPACE 1
PUTOUT   LH    R0,LINE                  GET LINE NUMBER            U004
         CLI   OBUF,C'1'                SKIP TO PAGE?              U004
         BE    PUTPAGE                  YES, FORCE PAGE SKIP
         CLI   OBUF,C'0'                DOUBLE SPACE?              U004
         BE    SKIP2                    YES
         CLI   OBUF,C'+'                SUPPRESS SKIP?             U004
         BE    SKIP0                    YES
         CLI   OBUF,C'-'                TRIPLE SPACE?              U004
         BE    SKIP3                    NO, ANYTHING ELSE IS ONE SPACE
         MVI   OBUF,C' '                ELSE FORCE SINGLE          U004
         B     SKIP1                    AND COUNT ONE
SKIP3    BCTR  R0,0
SKIP2    BCTR  R0,0
SKIP1    BCTR  R0,0
SKIP0    LTR   R0,R0                    ANYTHING LEFT ON PAGE?
         BNP   PUTPAGE                  NO, EJECT
         STH   R0,LINE                  SAVE NEW LINE COUNTER
         SPACE 1
*        PRINT THE LINE NOW
         SPACE 1
         PUT   DCB,OBUF                 DUMP OUT HIS LINE THEN     U004
         SPACE 1
*        IF WTOING, DO IT
         SPACE 1
         TM    OFLAGS,DOWTO              DO WTO?
         BZ    RC0                      NO, ALL DONE NOW
*U006    XC    WTOBUFF(4),WTOBUFF       GIVE LENGTH/MCS FLAGS
         MVC   WTOBUFF+2(2),=X'8000'    INDICATE ROUTCDE PRESENT   U006
         LA    R1,WTOBUFF+123+4         LAST POSSIBLE
         CLI   0(R1),C' '               END OF IT?
         BNE   *+8                      YES
         BCT   R1,*-8                   KEEP LOOKING
         MVC   1(4,R1),=X'00004080'     SET ROUTCDE=(2,9)          U008
         LA    R0,WTOBUFF-1             BEGINNING
         SR    R1,R0                    LENGTH
         BNP   RC0                      NULL
         STH   R1,WTOBUFF               SAVE FOR WTO
         WTO   MF=(E,WTOBUFF)           DO IT
RC0      TM    OFLAGS,OIOERROR           IOERROR?
         BZ    RC00                     NO
         NI    OFLAGS,255-OIOERROR       TURN OFF FLAG
         LA    R15,8                    INDICATE IT
         B     EXIT                     LEAVE
RC00     XR    R15,R15                  SAY ALL IS WELL
         B     EXIT                     THEN LEAVE
         EJECT
*        PUT OUT A TITLE IF AT TOP OF PAGE
         SPACE 1
PUTPAGE  MVI   WTOBUFF+3,C'-'           TRIPLE SPACE FROM TITLE
         MVC   LINE(2),=H'60'           RESET LINE COUNTER
         LH    R1,PAGE                  GET PAGE COUNTER
         LA    R1,1(R1)                 COUNT LAST PAGE
         STH   R1,PAGE                  SAVE FOR LATER
         CVD   R1,ODBLW                 CONVERT THE PAGE NUMBER
         MVC   TTLBUFF,H1P              MOVE IN THE PATTERN        U004
         ED    TTLBUFF+113(6),ODBLW+5    EDIT IN THE PAGE NUMBER   U004
         SR    R1,R1                    GET CURRENT DATE/TIME      U004
         LINK  EP=DATETIME              CALL THE ROUTINE           U004
         SPACE 1
*  NOTE:  R1=0 RETURNS A PARMLIST POINTED TO BY R1 WITH THE       *VIC*
*         CURRENT DATE AND TIME IN THE FORMS DESCRIBED IN THE     *VIC*
*         'NOWPARM' DSECT BELOW.                                  *VIC*
         SPACE 1
         USNGX NOWPARM,R1                                         *VIC*
         SPACE 1
         MVC   TTLBUFF+82(2),DAY        PICK UP DAY   (DD)         U004
         CLI   TTLBUFF+82,C'/'          HAD THE DAY BEEN SQUISHED  U004
         BNE   *+8                      NO - LEAVE IT             *VIC*
         MVI   TTLBUFF+82,C' '          YES - BLANK IT OUT         U004
         MVC   TTLBUFF+85(3),CHARDATE   PICK UP MONTH (MMM)        U004
         MVC   TTLBUFF+89(2),YEAR       PICK UP YEAR  (YY)         U004
         SPACE 1
         MVC   TTLBUFF+96(8),TIME24     PICK UP TIME HH:MM:SS      U004
         SPACE 1
         DROPX R1                                                 *VIC*
         SPACE 1
         PUT   DCB,TTLBUFF              GO DUMP THE TITLE          U004
         B     PUTOUT                   NOW TRY THAT AGAIN
         SPACE 1
*        SYNAD: AND I/O ERROR HAS OCCURED
         SPACE 1
OSYNAD   OI    OFLAGS,OIOERROR           SIGNAL ERROR
         BR    R14                      AND RETURN
         EJECT
*  NOWSVC SSVC0  PARMLIST DSECT                                   *VIC*
         SPACE 1
NOWPARM  DSECT                          NOWSVC (SSVC0) PARMLIST   *VIC*
         SPACE 2
TIME24   DS    0CL12                    TIME 24-HR....HH:MM:SS PST*VIC*
TIME24HR DS    2C,C                     HH:                       *VIC*
TIME24MN DS    2C,C                     MM:                       *VIC*
TIME24SC DS    2C                       SS                        *VIC*
         DS    C                        BLANK                     *VIC*
TIME24TZ DS    3C                       PST OR PDT                *VIC*
         DS    2C                       BLANKS                    *VIC*
         SPACE 1
NUMDATE  DS    0CL8                     THE DATE: (M)M/(D)D/YY    *VIC*
MO       DS    2C,C                     (M)M/                     *VIC*
DAY      DS    2C,C                     (D)D/                     *VIC*
YEAR     DS    2C                       YY                        *VIC*
         DS    2C                       BLANKS                    *VIC*
         SPACE 1
WEEKDAY  DS    9C                       THE WEEKDAY               *VIC*
         DS    C                        BLANK                     *VIC*
         SPACE 1
CHARDATE DS    13C                      DATE: MMM(M) (D)D, 19YY   *VIC*
         DS    2C                       BLANKS                    *VIC*
         SPACE 1
TIME12   DS    0CL15                    TIME 12-HR...HH:MM:SS AM PST
TIME12HR DS    2C,C                     HH:                       *VIC*
TIME12MN DS    2C,C                     MM:                       *VIC*
TIME12SC DS    2C,C                     SS + BLANK                *VIC*
AMPM     DS    2C                       AM OR PM                  *VIC*
         DS    C                        BLANK                     *VIC*
TIME12TZ DS    3C                       PDT OR PST                *VIC*
         DS    2C                       BLANKS                    *VIC*
         SPACE 1
JULDATE  DS    0CL6                     THE DATE:  YY.DDD         *VIC*
JULYEAR  DS    2C,C                     YY.                       *VIC*
JULDAY   DS    3C                       DDD                       *VIC*
         SPACE 2
PARMLEN  EQU   *-NOWPARM                LEN OF PARMLIST (72)      *VIC*
CSOUT    CSECT                                                   *VIC*
         TITLE 'C S O U T -- ENQUEUE THE DATASET TO PRINT'
*        CLOSE THE OUTPUT DATA SET AND FIND MTABLE
         SPACE 1
ENQUEUE  LA    R0,DCB                   FIND DCB                   U004
         ST    R0,ODBLW                 SAVE IT
         MVI   ODBLW,X'80'              CLOSE JUST THIS ONE
         SPACE 1
         CLOSE MF=(E,ODBLW)             CLOSE THE DS
         SPACE 2
         FREE  DDNAME=ODSN,SYSOUT='A'   AND FREE IT
         SPACE 1
         B     RC00                     NOW RETURN TO CALLER
         EJECT
SCRATCH  LA    R0,DCB                   POINT TO DCB               U004
         ST    R0,ODBLW                 STORE IN LIST
         MVI   ODBLW,X'80'              MARK AS END OF LIST
         SPACE 1
         CLOSE MF=(E,ODBLW)             CLOSE IT
         SPACE 1
         AIF   (&TONE).TONE05                                      T001
         FREE  DDNAME=ODSN,DISP=DELETE                             U004
         AGO   .NTONE05                                            T001
.TONE05  FREE  DDNAME=ODSN,SYSOUT='Z'                              T001
.NTONE05 ANOP  ,                                                   T001
         SPACE 1
         B     RC00                     RETURN
         TITLE 'C S O U T -- CONSTANTS'
         SPACE 1
H1P      DC    CL133'1G I B R A L T A R   INFORMATION SYSTEMS *** TSO Z$
               AP  SESSION RECORD          *** DD MMM YY *** HH:MM:SS *$
               ** PAGE _____'                                      U024
*H1P     DC    CL133'1HARTFIELD-ZODY''S INFORMATION RESOURCE MGNT. DIV.$
                 *** TSO ZAP SESSION RECORD *** DD MMM YY *** HH:MM:SS $
               *** PAGE _____'                                     U020
*H1P     DC    CL133'1T E L E C R E D I T                    *** TONE Z$
               AP  SESSION RECORD          *** DD MMM YY *** HH:MM:SS *$
               ** PAGE _____'                                      T001
         ORG   H1P+114
         DC    X'2020202120'
         ORG   ,
         SPACE 1
*        DCB MASK
         SPACE 1
         PRINT NOGEN
ODCBMSK  DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133,  $
               SYNAD=OSYNAD,BLKSIZE=4123,BUFNO=1                   U004
DCBLEN   EQU   *-ODCBMSK
         PRINT GEN
         EJECT
         DS    0F
PTRRB    DC    X'80',AL3(RB)
RB       DC    AL1(LENRB)           LENGTH OF RB
         DC    AL1(01)              VERB
         DC    AL2(0)               FLGS1
RBERR    DC    AL2(0)               ERROR CODE
RBINFO   DC    AL2(0)               INFO CODE
RBTXTA   DC    A(TXTPTRA)           ADDRESS OF TEXT POINTERS
         DC    A(0)                 RESERVED
         DC    XL4'00000000'        FLGS2
LENRB    EQU   *-RB
         SPACE 2
*        ALLOCATE TEXT POINTERS
TXTPTRA  DC    X'00',AL3(SOUT)
         DC    X'80',AL3(DDRET)
         SPACE
*        TEXT UNITS FOR ALLOCATE AND UNALLOCATE
         DS    0F
SOUT     DC    X'0018',X'0001',X'0001',C'A'      SYSOUT ALLOC CLASS A
DDRET    DC    X'0055',X'0001',X'0008',C'        ' DDNAME RETURN
         DC    C'-1'
         EJECT
         SPACE 1
*        LITERAL POOL
         SPACE 1
         LTORG ,
         SPACE 3
         DROPX ,
         TITLE 'ZAP --- HELP FOR ZAP' (HLP)                        U004
ZAPHELP  CSECT
         DC    Y(NUMHELPL,NUMHELPS-1)                              U021
         DC    A(FIRST)                                            U004
         DC    AL1(0,0,0,0,0,0,0,0,0,0,0,0,0)    1                 U004
         DC    AL1(1,0,1,1,1,1,1,1,1,1,1,1,0)    2                 U005
         DC    AL1(0,0,1,1,1,1,1,1,0,0,0,0,0)    3                 U012
         DC    AL1(1,0,1,1,1,1,1,1,1,1,1,1,1)    4                 U004
         DC    AL1(0,0,1,1,1,1,1,1,1,1,1,0,0)    5                 U012
         AIF   (&VSAM).VSAM07                                      T002
         DC    AL1(0,0,1,1,1,0,0,0,0,0,0,0,0)    6                 T002
         AGO   .NVSAM46                                            T002
.VSAM07  DC    AL1(0,0,1,1,1,1,1,0,0,0,0,0,0)    6                 T002
.NVSAM46 DC    AL1(1,0,1,1,1,1,1,1,1,1,1,0,0)    7                 T002
         DC    AL1(1,0,1,1,1,1,1,1,1,1,1,0,1)    8                 U007
         DC    AL1(1,0,1,1,1,1,1,1,1,1,1,1,1)    9                 U004
         DC    AL1(1,0,1,1,1,1,1,1,1,1,1,1,1)    10                U004
         AIF   (&MVS).MVS11                                        U004
         DC    AL1(1,0,1,1,1,1,1,1,1,1,1,1,0)    11                U012
         AGO   .MVT07                                              U004
.MVS11   DC    AL1(0,0,1,1,1,1,1,1,1,1,1,0,0)    11                U012
.MVT07   DC    AL1(0,0,1,1,1,1,1,1,1,1,0,0,0)    12                U012
         DC    AL1(1,1,1,1,1,1,1,1,1,1,1,1,1)    13                U004
         DC    AL1(1,0,1,1,1,1,1,1,1,1,1,1,1)    14                U005
         SPACE 2
FIRST    DC    0A(0)                                               U004
*
*  IMAGE # 1                                                       U004
*
         DC    CL40'                CONTENTS                '      U004
         DC    CL40'                                        '      U004
         DC    CL40'?1      - THIS SCREEN                   '      U005
         DC    CL40'?2, ?3  - GENERAL INFO                  '      U005
         DC    CL40'?4 - ?6 - INPUT/OUTPUT COMMANDS         '      T002
         DC    CL40'?7      - SESSION CONTROL COMMANDS      '      T002
         DC    CL40'?8      - DUMP COMMANDS                 '      T002
         DC    CL40'?9      - BYTE COMMANDS                 '      T002
         DC    CL40'?10     - STRING COMMANDS               '      T002
         DC    CL40'?11, ?12- MISCELLANEOUS COMMANDS        '      T002
         DC    CL40'?13     - EXPLANATION OF SYMBOLS        '      T002
         DC    CL40'?14     - SAMPLE ZAP SESSION            '      T002
         DC    CL40'                                        '      U005
*
*  IMAGE # 2                                                       U004
*
         DC    CL40'              GENERAL INFO              '      U005
         DC    CL40'                                        '      U005
         DC    CL40'--OPERANDS ARE ENTERED IMMEDIATELY AFTER'      U005
         DC    CL40'THE COMMAND, WITH NO INTERVENING BLANKS.'      U005
         DC    CL40'--THE BLOCK IS READ INTO A WORK BUFFER, '      U005
         DC    CL40'WHERE YOU MODIFY IT.  WHEN YOU ENTER THE'      U005
         DC    CL40'"ZAP" COMMAND, THE BLOCK IS WRITTEN BACK'      U005
         DC    CL40'TO THE DATASET FROM THE BUFFER.  IF ANY '      U005
         DC    CL40'OTHER JOB OR USER HAS UPDATED THAT BLOCK'      U005
         DC    CL40'IN BETWEEN WHEN YOU FIRST DISPLAYED IT  '      U005
         DC    CL40'AND WHEN YOU "ZAPPED" IT, THE OTHER     '      U005
         DC    CL40'CHANGES WILL BE LOST.                   '      U005
         DC    CL40'                                        '      U005
*
*  IMAGE # 3                                                       U005
*
         DC    CL40'        GENERAL INFO (CONTINUED)        '      U005
         DC    CL40'                                        '      U005
         DC    CL40'--IF YOU MAKE CHANGES TO A BLOCK, TO GO '      U005
         DC    CL40'TO ANOTHER BLOCK YOU MUST EITHER "ZAP"  '      U005
         DC    CL40'THE BLOCK (RE-WRITE IT TO THE DATASET), '      U005
         DC    CL40'OR ENTER THE COMMAND TWICE IN A ROW     '      U012
         DC    CL40'WHICH WILL CAUSE YOU TO LEAVE THE       '      U012
         DC    CL40'CURRENT MODIFIED BLOCK.                 '      U012
         DC    CL40'                                        '      U012
         DC    CL40'                                        '      U012
         DC    CL40'                                        '      U005
         DC    CL40'                                        '      U005
         DC    CL40'                                        '      U005
*                                                                  U004
*  IMAGE # 4                                                       U004
*                                                                  U004
         DC    CL40'         INPUT/OUTPUT COMMANDS          '      U004
         DC    CL40'                                        '      U004
         DC    CL40'P<EXP> - POINT TO REC WHOSE TTR IS <EXP>'
         DC    CL40'P      - POINT TO BEGINNING OF DATA SET '
         DC    CL40'T<EXP> - POINT TO TRACK <EXP>, RECORD 1 '
         DC    CL40'T      - POINT TO NEXT TRACK, RECORD 1  '
         DC    CL40'R<EXP> - SHOW BLK <EXP> REL. TO CURRENT '      U002
         DC    CL40'R      - POINT TO NEXT PHYSICAL RECORD  '      U002
         DC    CL40'B      - POINT TO PREVIOUS RECORD       '      U002
         DC    CL40'B<EXP> - BACK <EXP> RECORDS             '      T002
         DC    CL40'LAST   - POINT TO LAST RECORD (DS1LSTAR)'      U002
         DC    CL40'LASTDS1- POINT TO LAST FMT1 IN VTOC     '      U004
         DC    CL40'ABS<EXP> - POINT TO REC WITH CCHHR = EXP'      U003
*
*  IMAGE # 5                                                       U004
*
         DC    CL40'       MORE INPUT/OUTPUT COMMANDS       '      U004
         DC    CL40'                                        '      U004
         DC    CL40'%<EXP> - POINT TO TTR @ LOC <EXP> IN BLK'      T002
         DC    CL40'M<NAME>- FOR PDS, POINT TO MEMBER <NAME>'      U002
         DC    CL40'M      - POINT TO START OF CURRENT MEMBR'      U012
         DC    CL40'NAME<N>- AFTER ''M'', SHOW ENTRY POINT <N>'    U002
         DC    CL40'NAME   - SELECT ENTRY POINT NAMED MEMBER'      U004
         DC    CL40'E<N>   - SAME AS NAME<N>                '      U002
         DC    CL40'E      - SAME AS NAME                   '      U002
         DC    CL40'FINDEOF- SCAN FORWARD LOOKING FOR AN EOF'      U019
         DC    CL40'ZAP    - REPLACE BLOCK FROM CURRENT BUFF'
         DC    CL40'                                        '      T002
         DC    CL40'                                        '      T002
*
*  IMAGE # 6                                                       T002
*
         DC    CL40'       MORE INPUT/OUTPUT COMMANDS       '      T002
         DC    CL40'                                        '      T002
         DC    CL40'KEY<N> - FIND KEY <N> IN DATASET        '      T002
         DC    CL40'KEYP<N>- FIND KEY <N> IN DATASET FROM   '      T002
         DC    CL40'         BEGINNING                      '      T002
         AIF   (NOT &VSAM).NVSAM47                                 T002
         DC    CL40'RBASEQ - VSAM KSDS IN RBA SEQ           '      T002
         DC    CL40'KEYSEQ - VSAM KSDS IN KEY SEQ           '      T002
.NVSAM47 DC    CL40'                                        '      T002
         DC    CL40'                                        '      T002
         DC    CL40'                                        '      T002
         DC    CL40'                                        '      T002
         DC    CL40'                                        '      T002
         DC    CL40'                                        '      T002
         AIF   (&VSAM).VSAM08                                      T002
         DC    CL40'                                        '      T002
         DC    CL40'                                        '      T002
.VSAM08  ANOP  ,                                                   T002
*
*  IMAGE # 7                                                       T002
*
         DC    CL40'        SESSION CONTROL COMMANDS        '      U004
         DC    CL40'                                        '      U004
         DC    CL40'LOG      - START LOGGING THIS SESSION   '
         DC    CL40'CRT      - SET WIDTH 4,4 AND VERBOSE    '
         DC    CL40'TERSE    - DO NOT DO AUTO WHERE         '
         DC    CL40'VERBOSE  - ALWAYS GIVE WHERE INFO       '
         DC    CL40'W<D>,<U> - SET WINDOW DOWN/UP SIZE(S)   '
         DC    CL40'WFULL    - SET SIZE TO URSA SCREEN SIZE '
         DC    CL40'LINE40, LINE80 - SET LINE LENGTH 40 × 80'      U007
         DC    CL40'NOTE<COMNT>-WRITE <COMNT> INTO LOG FILE '      U004
         DC    CL40'EJECT    - EJECT A PAGE IN THE LOG FILE '      U004
         DC    2CL40'                                        '     U007
*                                                                  U004
*  IMAGE # 8                                                       T002
*                                                                  U004
         DC    CL40'             DUMP COMMANDS              '      U004
         DC    CL40'                                        '      U004
         DC    CL40'DUMP       - DUMP ALL RECORDS IN THE    '
         DC    CL40'             DATA SET IN ABDUMP FORMAT  '
         DC    CL40'DUMPT<EXP> - DUMP TO TTR <EXP> FROM     '
         DC    CL40'             CURRENT RECORD             '
         DC    CL40'DUMPT      - DUMP CURRENT RECORD ONLY   '
         DC    CL40'DUMPF<EXP> - DUMP FOR <EXP> RECORDS FROM'
         DC    CL40'             CURRENT RECORD             '
         DC    CL40'DUMPF      - DUMP CURRENT RECORD        '
         DC    CL40'DUMPE      - DUMP CURRENT RECORD TO EOF '      U004
         DC    CL40'                                        '
         DC    CL40'EOF, I/O ERROR, AND END-OF-DS HALT DUMP '
*                                                                  U004
*  IMAGE # 9                                                       T002
*                                                                  U004
         DC    CL40'             BYTE COMMANDS              '      U004
         DC    CL40'                                        '      U004
         DC    CL40'D<EXP> - DISPLAY BYTE AT OFFSET <EXP>   '
         DC    CL40'<EXP>  - SAME AS D<EXP>                 '
         DC    CL40'D      - PAGE TO NEXT SCREEN (D+#SCREEN)'      U004
         DC    CL40'U      - PAGE TO PREV SCREEN (D-#SCREEN)'      U004
         DC    CL40'>      - GO FORWARD IN TRACE TABLE      '
         DC    CL40'<      - GO BACKWARD IN TRACE TABLE     '
         DC    CL40'EBCDIC - EBCDIC TRANSLATION (DEFAULT)   '
         DC    CL40'ASCII  - ASCII-8 TRANSLATION            '
         DC    CL40'ZCODE  - ZCODE TRANSLATION              '
         DC    CL40'=<LAB> - DEFINE <LAB> TO CURR LOCATION  '
         DC    CL40'NODEF  - CLEAR ALL DEFINED LABELS FROM ='
*                                                                  U004
*  IMAGE # 10                                                      T002
*                                                                  U004
         DC    CL40'             STRING COMMANDS            '      U004
         DC    CL40'                                        '      U004
         DC    CL40'S<STR>   - STORE <STR> AT CURRENT LOC   '
         DC    CL40'X<STR>   - EXCLUSIVE OR AT CURRENT LOC  '
         DC    CL40'O<STR>   - OR AT CURRENT LOC            '
         DC    CL40'N<STR>   - AND AT CURRENT LOC           '
         DC    CL40'SET<STR> - SET ENTIRE RECORD TO <STR>   '
         DC    CL40'           S,X,O,N,SET MAY BE ENTERED   '
         DC    CL40'           ALONE TO USE PREVIOUS <STR>  '
         DC    CL40'L<STR>   - LOOK FOR <STR> FROM NEXT BYTE'
         DC    CL40'L        - CONTINUE LOOKING FOR <STR>   '
         DC    CL40'F<STR>   - LIKE L<STR>, CONT''S PAST EOFS'     U004
         DC    CL40'F        - LIKE L, CONTINUES PAST EOFS  '      U004
*                                                                  U004
*  IMAGE # 11                                                      T002
*                                                                  U004
         DC    CL40'         MISCELLANEOUS COMMANDS         '      U004
         DC    CL40'                                        '      U004
         DC    CL40'END    - EXIT ZAP                       '
         DC    CL40'#<EXP> - FIND HEX/DECIMAL VALUE OF <EXP>'
         DC    CL40'FLOAT<F>-FIND FLOATING PT VALUE OF <F>  '
         DC    CL40'IDEF   - DISPLAY DEFINE LABEL TABLE     '
         DC    CL40'ITRACE - DISPLAY TRACE TABLE            '
         AIF   (&MVS).MVS12                                        U004
         DC    CL40'NN<EXP>- FOR JOBQ, FIND <EXP>TH RECORD  '
.MVS12   DC    CL40'?N     - VIEW NTH HELP DISPLAY (1-13)   '      U004
         DC    CL40'WHERE  - GIVE CURRENT LOCATION ETC.     '
         DC    CL40'ASM<OP> - GIVE OBJECT CODE FOR MNEMONIC '
         DC    CL40'DISASM<EXP> - DISASSEMBLE INSTR AT <EXP>'      U004
         AIF   (NOT &MVS).NMVS01                                   T001
         DC    CL40'                                        '      U004
.NMVS01  DC    CL40'                                        '      T001
*
*  IMAGE # 12                                                      T002
*
         DC    CL40'      MORE MISCELLANEOUS COMMANDS       '      U004
         DC    CL40'                                        '      U004
         DC    CL40'WHATMEM- FIND DIR ENTRY CLOSEST TO REC *'      U012
         DC    CL40'WM       - SAME AS WHATMEM              '      U012
         DC    CL40'TSO<CMD> - EXECUTE TSO <CMD> AS AT READY'      U020
         DC    CL40'V<EXP>   - ADD CONTENTS OF 2 BYTES AT   '      U014
         DC    CL40'           <EXP> TO CURR VALUE OF LOC   '      U014
         DC    CL40'BASE<EXP>- ADD <EXP> TO BUFFER OFFSET   '      U004
         DC    CL40'           WHEN COMPUTING ADDR          '      U004
         DC    CL40'DISP<TYPE>-SET RECORD DISPLAY START POS '      U004
         DC    CL40'           <TYPE> IS COUNT × KEY × DATA '      U004
         DC    CL40'                                        '      U014
         DC    CL40'                                        '      U014
*                                                                  U004
*  IMAGE # 13                                                      T002
*                                                                  U004
         DC    CL40'<EXP> IS COMPOSED OF OPERATORS (+,-,*,/)'      U004
         DC    CL40'      DEFINE SYMBOLS (''='' COMMAND) AND'
         DC    CL40'HEX OR DECIMAL CONSTANTS. THE FOLLOWING '
         DC    CL40'SYMBOLS ARE PREDEFINED: $=CURRENT OFFSET'      U019
         DC    CL40'L=LOGICAL RECORD LENGTH, K=KEY LENGTH   '
         DC    CL40'BL=MAXIMUM BLOCK SIZE.                  '
         DC    CL40'<STR> MAY BE EITHER UP TO 16 CHARACTERS '
         DC    CL40'      ENCLOSED IN ANY DELIMITER, OR UP  '
         DC    CL40'      TO 16 HEX DIGITS, OR A DECIMAL VAL'
         DC    CL40'      ENDING WITH ''.'' (LENGTH 4 BYTES)  '
         DC    CL40'<LAB> MAY ANY 1 TO 8 CHARACTERS         '
         DC    CL40'<F>   IS ANY FLOATING POINT CONSTANT IN '
         DC    CL40'      INTERNAL FORM (IE: FORTRAN A FORM)'
*
*  IMAGE # 14                                                      T002
*
         DC    CL40'           SAMPLE ZAP SESSION           '      U005
         DC    CL40'                                        '      U005
         DC    CL40'ZAP LOAD   READY LEVEL COMMAND ENTERED  '      U005
         DC    CL40'MTEST      SELECT MEMBER "TEST"         '      U005
         DC    CL40'EFIRST     SELECT ENTRY POINT "FIRST"   '      U005
         DC    CL40'4C         GO TO OFFSET 4C IN RECORD    '      U005
         DC    CL40'S00        STORE 1 BYTE HEX             '      U005
         DC    CL40'+4         ADVANCE PTR 4 BYTES IN BLOCK '      U005
         DC    CL40'NFF0F      TURN OFF 1 NIBBLE            '      U005
         DC    CL40'ZAP        REWRITE THAT BLOCK           '      U005
         DC    CL40'R+3        GO TO 3RD BLOCK FROM HERE    '      U005
         DC    CL40'L/HELLO/   FIND SOME TEXT               '      U005
         DC    CL40'END        ALL DONE                     '      U005
         SPACE 3
NUMHELPL EQU   (*-FIRST)/40             NUMBER OF HELP LINES       U021
NUMHELPS EQU   NUMHELPL/13              NUMBER OF HELP SCREENS     U021
         TITLE 'ZAP --- ASMGASM --- OP CODE PARSE' (ASM)           U004
         SPACE 3
*
*  ASSEMBLE OR DISASSEMBLE.
*
         SPACE 2
ASMGASM  CSECT ,
         SPACE 1
*U004    REGEQU
         SPACE 1
         SAVE  (14,12),,*
         SPACE 1
         LR    R12,R15                  BASE
         USNGX ASMGASM,R12
         SPACE 2
         LTR   R1,R1                    ASM OR DISASM?
         BM    DISASMIT                 GUESS...                   U004
         EJECT
*  ASM  ---  R1 = PTR TO CL6'MNEMONIC'                             T002
         SPACE 2
         LA    R2,OPCODES               TABLE
         LA    R15,OPEND                END BXLE                   T002
         LA    R14,OPLEN                LENGTH
         SPACE 1
OPLOOP   CLC   0(6,R1),0(R2)            THIS ONE (KLUGE)           T002
         BE    GOTOP                    YES
         BXLE  R2,R14,OPLOOP            TRY ALL POSSIBLES
         SPACE 1
BADRET   LA    R15,4                    INVALID OP CODE
         B     RETURN                   BYE
         SPACE 2
GOTOP    LA    R1,6(,R2)                POINT TO FLAG              T002
GOODRET  XR    R15,R15                  RC=0
         SPACE 1
RETURN   L     R14,12(,R13)             RESTORE R14  **** NOTE R0 NOT
         LM    R2,R12,12+16(R13)        REST REGS         RESTORED ****
         BR    R14                      BYE
         EJECT
*  DISASM  ---  R1 = PTR TO BUFF LOC OF OBJECT CODE
*               R0 = PTR TO OUTPUT BUFFER  (33 BYTES)
*               R2 = PTR TO 5 DOUBLEWORDS WORKAREA
*  OUTPUT:      R15= RC
*               R1 = INSTR LEN IF R15=0
         SPACE 2
DISASMIT LPR   R1,R1                    GET POS 1ST                U004
         LR    R7,R2                    SAVE WORK BASE
         LR    R9,R0                    SAVE OUTPUT BUFF PTR
         SPACE 3
JCJWORK  DSECT
DOUBLE   DS    D
DBL      DS    D                                                   U004
EDITWORK DS    6C                                                  U004
FLAGS    DS    X
         SPACE 3
ASMGASM  CSECT ,
         SPACE 2
         USNGX JCJWORK,R7
*
*  THIS ROUTINE WRITTEN BY JCJ 1/75
*
         SPACE 2
         MVC   DBL(8),0(R1)             ALIGN POSSIBLE INSTRUCTION U004
         LA    R6,DBL                   POINT TO INSTR             U004
         SPACE 2
***********************************************************************
*                                                                     *
* DISASSEM                                                            *
* RETURNS:                                                            *
*     R15 - LENGTH OF INSTRUCTION OR ZERO IF NON-VALID OPCODE         *
*     FILLED IN SCREEN BUFFER IF OPCODE WAS VALID                     *
*                                                                     *
*                                                                     *
* NOTE WE GO THROUGH SOME CONTORTIONS IN ORDER TO BE ABLE TO SPECIFY  *
* THE OPCODE FLAG FIELDS IN EBCDIC CHARACTER FORM (IMAGE-ENTERABLE).  *
*                                                                     *
***********************************************************************
         SPACE 2
         XR    R15,R15                  READY FOR IC
         XR    R2,R2                    CLEAR TRT REG
         TRT   0(1,R6),OPTAB            TRANSLATE THE OPCODE
         BZ    BADRET                   BRANCH IF NON-VALID OPCODE
         SPACE 2
         LA    R4,1                     GET F'1' FOR LATER
         SLR   R2,R4                    FIX FOR PROPER TABLE INDEX
         MH    R2,=Y(OPLEN)             GET OFFSET INTO TABLE      T002
         LA    R1,OPCODES(R2)           PNT TO PROPER MNENOMIC
         CLI   6(R1),C'2'               370 EXTENDED OPCODE        T002
         BE    OP370                    YES, DO IT SPECIAL         T002
         MVC   0(6,R9),0(R1)            MOVE IN MNENOMIC           T002
         MVC   FLAGS(1),6(R1)           GET THE FLAG BYTE          T002
         IC    R15,1(,R6)               GET THE REG BYTE
         LR    R2,R15                   GET IT IN R2 FOR LATER
         SPACE 1
* NOW CHECK IF IT IS AN EXTENDED MNENOMIC INSTRUCTION
         SPACE 1
         TM    FLAGS,X'04'              EXTENDED MNENOMIC FLAG ON?
         BZ    GETTYPE                  NO - GO ON WITH THE REST
         TM    FLAGS,X'02'              INDEX REG FLAG ON?
         BZ    GETMNE                   NO - GO GET THE MMENOMIC
         SPACE 2
* GETMNE RETURNS TO GETINST - WE DON'T CARE IF WE GO THROUGH THIS CODE
* FOR NON-MNENOMICS THOUGH
         SPACE 2
GETINST  CLI   1(R9),C'C'               GOT AN EXTENDED MNENOMIC?
         BNE   *+8                      YES - SKIP THE BIT RESET
         OI    FLAGS,X'02'              NO - FAKE OUT A RR(OR RX) TYPE
         SPACE 2
GETTYPE  CLC   0(6,R9),OPCODES          IS IT SPM?                 T002
         LA    R9,9(,R9)                INCREMENT LINE PNTR        T002
         BNE   GETTYPE#                 NO - SKIP FLAG RESET/BRANCH
         MVI   FLAGS,C'<'               RESET FLAG FOR LATER TEST
         B     GETRR#                   AND GO PROCESS SPM
         SPACE 3
OP370    LA    R1,OPTAB370              -> EXTENDED OPCODE TABLE   T002
         XR    R2,R2                    CLEAR FOR IC               T002
OP370LP  CLC   DBL(1),0(R1)             THIS THE MAIN OPCODE?      T002
         BE    OP370GOT                 YES, USE IT                T002
         IC    R2,1(,R1)                GET MAX VALUE              T002
         LA    R1,3(R2,R1)              -> NEXT TABLE ENTRY        T002
         B     OP370LP                  TRY IT                     T002
OP370GOT CLC   DBL+1(1),1(R1)           OVER MAX SUB OPCODE?       T002
         BH    BADRET                   YES, EXIT ERRORS           T002
         IC    R2,DBL+1                 GET SUB OPCODE             T002
         IC    R2,2(R2,R1)              GET OPCODE VALUE           T002
         LTR   R2,R2                    VALID OPCODE?              T002
         BZ    BADRET                   NO, EXIT                   T002
         BCTR  R2,0                     -1 FOR OFFSET INTO TBLE    T002
         MH    R2,=Y(OPLEN)             GET TRUE OFFSET            T002
         LA    R2,OPCODES(R2)           -> OPTABLE ENTRY           T002
         MVC   0(6,R9),0(R2)            MOVE IN OPCODE             T002
         MVC   FLAGS(1),6(R2)           AND NEW FLAGS              T002
         LA    R9,9(,R9)                -> NEXT FIELD              T002
         B     RXDISP                   GO GET B1(D1)              T002
         SPACE 2
GETTYPE# TM    FLAGS,X'90'              IS LENGTH FOUR BYTES?
         BZ    GETRR                    NO - GET 2 BYTE INSTRUCTION
         BM    GETSS                    NO - GET 6 BYTE INSTRUCTION
         SPACE 2
*
* HAVE AN FOUR BYTE TYPE WHEN WE FALL THROUGH TO HERE
*
         SPACE 2
         CLI   FLAGS,C'M'  (GETRX)      DO WE WANT A REGISTER?
         BNH   RXDISP                   NO - GO GET DISP FIELD
         LR    R2,R15                   GET REG BYTE BACK
         SRDL  R2,4                     SHIFT TO GET FIRST REG
         BAL   R14,GETNUM               PUT CHAR VALUE IN SCREEN
         BAL   R14,COMMA                PUT IN A COMMA
         CLI   FLAGS,C'R'               TWO REGISTERS NEEDED?
         BNE   RXDISP                   NO - GET DISPLACEMENT
         SLDL  R2,4                     GET THE OTHER REG
         BAL   R14,GETNUM               AND PUT IT IN SCREEN
         BAL   R14,COMMA                MOVE IN A COMMA
RXDISP   BAL   R5,GETDISP               GET THE DISP FIELD
         TM    FLAGS,X'04'              NEED AN INDEX REG?
         BZ    RXNOIND                  NO - GET THE BASE ONLY
         SLDL  R2,4                     AND PICK UP THE INDEX REG
         BAL   R14,GETNUM               PUT IT IN SCREEN
         BAL   R14,COMMA                MOVE IN A COMMA
RXNOIND  BAL   R5,GETBASE               GET THE BASE BYTE
         CLI   FLAGS,C'L'               IMMEDIATE TYPE?
         BNE   DISLEAVE                 NO - LEAVE
         SPACE 1
* WE HAVE AN SI TYPE HERE
         SPACE 1
         MVC   0(3,R9),SICHAR           MOVE IN ,X'
         HEX   (3,R9),(1,R6),LEN=1,HEXTAB=HEXTAB,BYTE=C''''        U004
         B     DISLEAVE                 AND LEAVE
         SPACE 2
*
* TWO BYTE INSTRUCTIONS
*
GETRR    LR    R2,R15                   GET REG BYTE BACK
         CLI   FLAGS,C'+'               SVC?
         BE    RRSVC                    YES - GO PROCESS IT
         BL    MNENT                    EXTENDED MNENOMIC HERE
         SPACE 1
GETRR#   SRDL  R2,4                     GET FIRST REGISTER VALUE
         BAL   R14,GETNUM               MOVE IN THE CHAR VALUE
         CLI   FLAGS,C'<'               SPM INST?
         BE    DISLEAVE                 YES - LEAVE
         BAL   R14,COMMA                MOVE IN A COMMA
MNENT    XR    R2,R2                    ZERO PARM REG
         SLDL  R2,4                     GET OTHER REG
RRSVC    BAL   R14,GETNUM               MOVE IN THE CHAR AGAIN
         B     DISLEAVE                 GO AWAY
         SPACE 2
*
* THIS SECTION HANDLES 6 BYTE INSTRUCTIONS
*
         SPACE 2
GETSS    LR    R0,R2                    SAVE THE REG BYTE (LENGTH)
         BAL   R5,GETDISP               GEN FIRST DISPLACEMENT
         LR    R2,R0                    GET THE REG BYTE BACK
         TM    FLAGS,X'01'              ONLY ONE LENGTH FIELD?
         BO    *+8                      YES - SKIP THE REG SHIFT
         SRDL  R2,4                     GET THE FIRST LENGTH FIELD
         BAL   R14,GETNUMI              MOVE IN THE CHAR VALUE
         BAL   R14,COMMA                MOVE IN A COMMA
         BAL   R5,GETBASE               GET THE BASE FIELD
         BAL   R14,COMMA                MOVE IN A COMMA
         LH    R2,4(,R6)                GET SECOND DISP FIELD
         BAL   R5,GETDISP#              MOVE IT TO THE SCREEN
         TM    FLAGS,X'01'              SINGLE LENGTH?
         BO    SSL1                     YES - SKIP 2ND LEN PROCESSING
         SLDL  R2,4                     GET 2ND LENGTH
         BAL   R14,GETNUMI              AND MOVE IT TO SCREEN
         BAL   R14,COMMA                A DELIMITER OR TWO
SSL1     IC    R2,4(,R6)                GET SECOND BASE FIELD
         BAL   R5,GETBASE#              GET THE BASE IN SCREEN
         SPACE 2
DISLEAVE TM    FLAGS,X'10'              DO WE HAVE A BAD LENGTH?
         BZ    *+8                      NO - HOP OVER THE BIT RESET
         NI    FLAGS,255-X'40'          FIX FOR CORRECT LENGTH
         XR    R1,R1                    SET UP FOR IC
         IC    R1,FLAGS                 GET THE LENGTH
         SRL   R1,5                     WIPE OUT FLAG BITS  = LENGTH
         B     GOODRET                  EXIT OK
         SPACE 2
*
* EXTENDED MNEMONIC ROUTINE
*
         SPACE 2
GETMNE   SRDL  R2,4                     SHIFT TO CLEAR OUT REG NIBBLE
         MH    R2,H6                    INDEX INTO TABLE
         LA    R1,MNEOPS(R2)            PNT TO TABLE ENTRY
         MVC   0(3,R9),0(R1)            MOVE IN PRIMARY MNEMONIC
         SPACE 1
         CLI   3(R1),C' '               SECONDARY MNENOMIC PRESENT?
         BE    GETMNE#                  NO - CONTINUE ON
         SPACE 1
         MVC   5(3,R9),3(R1)            GET SECONDARY ONE
         LR    R14,R9                   SAVE CURR BUFFER PNTR
         AH    R9,H6                    AND UP WORK BUFFER PNTR
         SPACE 1
GETMNE#  TM    FLAGS,X'80'              NON 2 BYTE INSTRUCTION?
         BO    GETINST                  YES - LEAVE
         SPACE 1
         CLI   3(R1),C' '               TWO MNENOMICS NEEDED?
         BE    ONEMNE                   NO - SO DO ONE DUMMY...
         BAL   R5,RLOOP                 PUT IN A 'R' (RR TYPE)
         SPACE 1
ONEMNE   LR    R14,R9                   PNT TO CURR MNEMONIC
         LA    R5,GETINST               FAKE OUT A BAL AND RETURN
         SPACE 2
RLOOP    ALR   R14,R4                   PNT TO NEXT CHAR
         CLI   0(R14),C' '              A BLANK WAITING FOR 'R'?
         BNE   RLOOP                    NO - INCREMENT
         MVI   0(R14),C'R'              YES - PUT IN THE 'R'
         BR    R5                       AND GO HOME
         TITLE 'U T I L I T Y   R O U T I N E S'
*
* DISASSEMBLER BAL ROUTINES - USED TO SAVE BYTES HERE AND THERE
*
         SPACE 2
* THIS SECTION GETS THE BASE REG
         SPACE 1
GETBASE  IC    R2,2(,R6)                GET THE BASE
GETBASE# SRL   R2,4                     SHIFT OUT GARBAGE
         BAL   R14,GETNUM               GO MOVE TO SCREEN
         MVI   0(R9),C')'               PUT IN A R-PAREN
         ALR   R9,R4                    INCREMENT LINE PNTR
         BR    R5                       GO TO CALLER
         SPACE 1
* THIS SECTION GETS THE DISPLACEMENT FIELD
         SPACE 1
GETDISP  LH    R2,2(,R6)                LOAD THE DISPLACEMENT
GETDISP# N     R2,DISPMASK              WIPE OUT BASE BITS
         BAL   R14,GETNUM               DO THE MOVE TO SCREEN
         CLI   FLAGS,C'J'               IS IT SVCX OR DIAGNOSE?
         BE    DISLEAVE                 YES - GO AWAY
         MVI   0(R9),C'('               ELSE MOVE IN A L-PAREN
         ALR   R9,R4                    INCREMENT LINE PNTR
         BR    R5                       GO TO CALLER
         SPACE 1
COMMA    MVI   0(R9),C','               MOVE IN A COMMA
         ALR   R9,R4                    INCREMENT PNTR
         BR    R14                      GO TO CALLER
         SPACE 2
*
* THIS ROUTINE PUTS THE CHAR VALUE OF THE CONTENTS OF R2 INTO THE
* SCREEN(VIA R9) AND UPDATES R9 (POINT TO THE NEXT AVAILABLE POSITION)
*
         SPACE 2
GETNUMI  ALR   R2,R4                    INCREMENT FOR A LENGTH FIX
GETNUM   CVD   R2,DOUBLE                GET IT IN DECIMAL
         MVC   EDITWORK(6),EDITMASK     SET UP FOR EDIT
         LA    R15,EDITWORK+5           PNT TO END OF AREA
         LR    R1,R15                   SET UP R1 FOR EDMK
         EDMK  EDITWORK(6),DOUBLE+5     DO THE EDIT
         SR    R15,R1                   GET THE EXECUTE LENGTH
         EX    R15,NUMMOVE              MOVE THE VALUE TO THE SCREEN
         LA    R9,1(R15,R9)             UPDATE THE SCREEN PNTR
         XR    R2,R2                    ZERO OUR WORK REG
         BR    R14                      GO HOME
NUMMOVE  MVC   0(0,R9),0(R1)            <<< EXECUTED >>>
         TITLE 'D A T A'
DISPMASK DC    0F'0',XL4'00000FFF'
H6       DC    H'6'
EDITMASK DC    X'402020202120'
SICHAR   DC    C',X'''
         DC    C'0123456789ABCDEF'
HEXTAB   EQU   *-256
         SPACE 3
         LTORG ,                                                   T002
         SPACE 5
*        TITLE 'E X T E N D E D   M N E N O M I C   T A B L E'
         SPACE 2
MNEOPS   DC    CL6'NOP   '
         DC    CL6'BO    '
         DC    CL6'BH BP '
         DC    CL6'BC    '
         DC    CL6'BL BM '
         DC    CL6'BC    '
         DC    CL6'BC    '
         DC    CL6'BNEBNZ'
         DC    CL6'BE BZ '
         DC    CL6'BC    '
         DC    CL6'BC    '
         DC    CL6'BNLBNM'
         DC    CL6'BC    '
         DC    CL6'BNHBNP'
         DC    CL6'BNO   '
         DC    CL6'B     '
         TITLE 'O P C O D E   T R A N S L A T E   T A B L E'
*  THE FOLLOWING OPTAB REPLACES A GIANT ABORTION THAT WAS          U004
*  IMPOSSIBLE TO MODIFY BECAUSE IT HAD THE ABSOLUTE OFFSET INTO    U004
*  THE OPCODE TABLE FOR EACH OPCODE.                               U004
         SPACE 3
OPTAB    DC    AL1(000,000,000,000,@04,@05,@06,@07)                U004
         DC    AL1(@08,@09,@0A,000,000,@0D,@0E,@0F)                G001
         DC    AL1(@10,@11,@12,@13,@14,@15,@16,@17)                U004
         DC    AL1(@18,@19,@1A,@1B,@1C,@1D,@1E,@1F)                U004
         DC    AL1(@20,@21,@22,@23,@24,@25,@26,@27)                U004
         DC    AL1(@28,@29,@2A,@2B,@2C,@2D,@2E,@2F)                U004
         DC    AL1(@30,@31,@32,@33,@34,@35,@36,@37)                U004
         DC    AL1(@38,@39,@3A,@3B,@3C,@3D,@3E,@3F)                U004
         DC    AL1(@40,@41,@42,@43,@44,@45,@46,@47)                U004
         DC    AL1(@48,@49,@4A,@4B,@4C,@4D,@4E,@4F)                G001
         DC    AL1(@50,@51,000,000,@54,@55,@56,@57)                U004
         DC    AL1(@58,@59,@5A,@5B,@5C,@5D,@5E,@5F)                U004
         DC    AL1(@60,000,000,000,000,000,000,@67)                U004
         DC    AL1(@68,@69,@6A,@6B,@6C,@6D,@6E,@6F)                U004
         DC    AL1(@70,000,000,000,000,000,000,000)                U004
         DC    AL1(@78,@79,@7A,@7B,@7C,@7D,@7E,@7F)                U004
         DC    AL1(@80,000,@82,@83,@84,@85,@86,@87)                U004
         DC    AL1(@88,@89,@8A,@8B,@8C,@8D,@8E,@8F)                U004
         DC    AL1(@90,@91,@92,@93,@94,@95,@96,@97)                U004
         DC    AL1(@98,000,000,000,@9C,@9D,@9E,@9F)                U004
         DC    AL1(000,000,000,000,000,000,000,000)                U004
         DC    AL1(000,000,000,000,@AC,@AD,@AE,@AF)                U004
         DC    AL1(000,@B1,@B2,000,000,000,@B6,@B7)                U004
         DC    AL1(000,000,@BA,@BB,000,@BD,@BE,@BF)                U004
         DC    AL1(000,000,000,000,000,000,000,000)                U004
         DC    AL1(000,000,000,000,000,000,000,000)                U004
         DC    AL1(000,@D1,@D2,@D3,@D4,@D5,@D6,@D7)                U004
         DC    AL1(000,@D9,@DA,@DB,@DC,@DD,@DE,@DF)                G001
         DC    AL1(000,000,000,000,000,@E5,000,000)                T002
         DC    AL1(@E8,000,000,000,000,000,000,000)                T002
         DC    AL1(@F0,@F1,@F2,@F3,000,000,000,000)                U004
         DC    AL1(@F8,@F9,@FA,@FB,@FC,@FD,000,000)                U004
         SPACE 2
OPTAB370 DC    AL1(X'9C',X'01',@9C00,@9C01)                        T002
         DC    AL1(X'9D',X'01',@9D00,@9D01)                        T002
         DC    AL1(X'9E',X'01',@9E00,@9E01)                        T002
         DC    AL1(X'9F',X'01',@9F00,@9F01)                        T002
         DC    AL1(X'B2',X'2C',@B200,@B201,@B202,@B203)            T002
         DC                AL1(@B204,@B205,@B206,@B207)            T002
         DC                AL1(@B208,@B209,@B20A,@B20B)            T002
         DC                AL1(00000,@B20D,00000,00000)            T002
         DC                AL1(@B210,@B211,@B212,@B213)            T002
         DC                AL1(00000,00000,00000,00000)            T002
         DC                AL1(@B218,@B219,00000,00000)            G001
         DC                AL1(00000,00000,00000,00000)            T002
         DC                AL1(00000,@B221,00000,@B223)            G001
         DC                AL1(@B224,@B225,@B226,@B227)            G001
         DC                AL1(@B228,00000,00000,00000)            G001
         DC                AL1(@B22C)                              G001
         DC    AL1(X'E5',X'01',@E500,@E501)                        G001
         TITLE 'O P C O D E   T A B L E'
*
* RR TYPE:
*    C'(',X'4D' - EXTENDED MNEMONIC
*    C'+',X'4E' - SVC       IMED
*    C'×',X'4F' - NORMAL RR TYPE
*    C'&',X'50' - SPM TYPE
*
* RX TYPE:
*    C'O',X'D6' - INDEX REGISTER USED   R1,D2(X2,B2)
*    C'M',X'D4' - EXTENDED MNENOMIC     D2(X2,B2)
*    C'L',X'D3' - SI TYPE               D1(B1),I2
*    C'J',X'D1' - DIAGNOSE,SVCX   IMED  I
*    C'K',X'D2' - DISP(BASE) TYPE       D1(B1)
*    C'R',X'D9' - BXLE,BXH,STM,LM       R1,R3,D2(B2)
*    C'Q',X'D8' - REG SHIFT TYPE        R1,D2(B2)
*    C'2',X'F2' - 370 EXTENDED OPCODE   D1(B1)                     T002
*
* SS TYPE:
*    C'S',X'E2' - TWO LENGTHS REQUIRED
*    C'T',X'E3' - ONLY ONE LENGTH
*
*
*  TABLE FORMAT:
*
*        CL6'MNEMONIC',C'FLAG',X'OPCODE'                           T002
*
         PRINT NOGEN
OPCODES  OP    SPM,×,04
OPLEN    EQU   *-OPCODES                LEN OF AN OPCODE           T002
         OP    BALR,×,05
         OP    BCTR,×,06
         OP    BCR,'(',07
         OP    SSK,×,08
         OP    ISK,×,09
         OP    SVC,+,0A
         OP    BASR,×,0D                                           G001
         OP    MVCL,×,0E                                           U004
         OP    CLCL,×,0F                                           U004
         OP    LPR,×,10
         OP    LNR,×,11
         OP    LTR,×,12
         OP    LCR,×,13
         OP    NR,×,14
         OP    CLR,×,15
         OP    OR,×,16
         OP    XR,×,17
         OP    LR,×,18
         OP    CR,×,19
         OP    AR,×,1A
         OP    SR,×,1B
         OP    MR,×,1C
         OP    DR,×,1D
         OP    ALR,×,1E
         OP    SLR,×,1F
         OP    LPDR,×,20
         OP    LNDR,×,21
         OP    LTDR,×,22
         OP    LCDR,×,23
         OP    HDR,×,24
         OP    LRDR,×,25
         OP    MXR,×,26
         OP    MXDR,×,27
         OP    LDR,×,28
         OP    CDR,×,29
         OP    ADR,×,2A
         OP    SDR,×,2B
         OP    MDR,×,2C
         OP    DDR,×,2D
         OP    AWR,×,2E
         OP    SWR,×,2F
         OP    LPER,×,30
         OP    LNER,×,31
         OP    LTER,×,32
         OP    LCER,×,33
         OP    HER,×,34
         OP    LRER,×,35
         OP    AXR,×,36
         OP    SXR,×,37
         OP    LER,×,38
         OP    CER,×,39
         OP    AER,×,3A
         OP    SER,×,3B
         OP    MER,×,3C
         OP    DER,×,3D
         OP    AUR,×,3E
         OP    SUR,×,3F
         OP    STH,O,40
         OP    LA,O,41
         OP    STC,O,42
         OP    IC,O,43
         OP    EX,O,44
         OP    BAL,O,45
         OP    BCT,O,46
         OP    BC,M,47
         OP    LH,O,48
         OP    CH,O,49
         OP    AH,O,4A
         OP    SH,O,4B
         OP    MH,O,4C
         OP    BAS,O,4D                                            G001
         OP    CVD,O,4E
         OP    CVB,O,4F
         OP    ST,O,50
         OP    SVCX,J,51
         OP    N,O,54
         OP    CL,O,55
         OP    O,O,56
         OP    X,O,57
         OP    L,O,58
         OP    C,O,59
         OP    A,O,5A
         OP    S,O,5B
         OP    M,O,5C
         OP    D,O,5D
         OP    AL,O,5E
         OP    SL,O,5F
         OP    STD,O,60
         OP    MXD,O,67
         OP    LD,O,68
         OP    CD,O,69
         OP    AD,O,6A
         OP    SD,O,6B
         OP    MD,O,6C
         OP    DD,O,6D
         OP    AW,O,6E
         OP    SW,O,6F
         OP    STE,O,70
         OP    LE,O,78
         OP    CE,O,79
         OP    AE,O,7A
         OP    SE,O,7B
         OP    ME,O,7C
         OP    DE,O,7D
         OP    AU,O,7E
         OP    SU,O,7F
         OP    SSM,K,80
         OP    LPSW,K,82
         OP    DIAG,J,83
         OP    WRD,L,84
         OP    RDD,L,85
         OP    BXH,R,86
         OP    BXLE,R,87
         OP    SRL,Q,88
         OP    SLL,Q,89
         OP    SRA,Q,8A
         OP    SLA,Q,8B
         OP    SRDL,Q,8C
         OP    SLDL,Q,8D
         OP    SRDA,Q,8E
         OP    SLDA,Q,8F
         OP    STM,R,90
         OP    TM,L,91
         OP    MVI,L,92
         OP    TS,K,93
         OP    NI,L,94
         OP    CLI,L,95
         OP    OI,L,96
         OP    XI,L,97
         OP    LM,R,98
         OP    SIO,2,9C00                                          T002
         OP    SIOF,2,9C01                                         T002
         OP    TIO,2,9D00                                          T002
         OP    CLRIO,2,9D01                                        T002
         OP    HIO,2,9E00                                          T002
         OP    HDV,2,9E01                                          G001
         OP    TCH,2,9F00                                          T002
         OP    CLRCH,2,9F01                                        T002
         OP    STNSM,L,AC                                          U004
         OP    STOSM,L,AD                                          U004
         OP    SIGP,R,AE                                           U004
         OP    MC,L,AF                                             U004
         OP    LRA,O,B1                                            U004
         OP    CONCS,2,B200                                        T002
         OP    DISCS,2,B201                                        T002
         OP    STDIP,2,B202                                        T002
         OP    STDIC,2,B203                                        T002
         OP    SCK,2,B204                                          T002
         OP    STCK,2,B205                                         T002
         OP    SCKC,2,B206                                         T002
         OP    STCKC,2,B207                                        T002
         OP    SPT,2,B208                                          T002
         OP    STPT,2,B209                                         T002
         OP    SPKA,2,B20A                                         T002
         OP    IPK,2,B20B                                          T002
         OP    PTLB,2,B20D                                         T002
         OP    SPX,2,B210                                          T002
         OP    STPX,2,B211                                         T002
         OP    STAP,2,B212                                         T002
         OP    RRB,2,B213                                          T002
         OP    PC,2,B218                                           G001
         OP    SAC,2,B219                                          G001
         OP    IPTE,2,B221                                         T002
         OP    IVSK,2,B223                                         G001
         OP    IAC,2,B224                                          G001
         OP    SSAR,2,B225                                         G001
         OP    EPAR,2,B226                                         G001
         OP    ESAR,2,B227                                         G001
         OP    PT,2,B228                                           G001
         OP    TB,2,B22C                                           G001
         OP    STCTL,R,B6                                          U004
         OP    LCTL,R,B7                                           U004
         OP    CS,R,BA                                             U004
         OP    CDS,R,BB                                            U004
         OP    CLM,R,BD                                            U004
         OP    STCM,R,BE                                           U004
         OP    ICM,R,BF                                            U004
         OP    MVN,C,D1
         OP    MVC,C,D2
         OP    MVZ,C,D3
         OP    NC,C,D4
         OP    CLC,C,D5
         OP    OC,C,D6
         OP    XC,C,D7
         OP    MVCK,C,D9                                           G001
         OP    MVCP,C,DA                                           G001
         OP    MVCS,C,DB                                           G001
         OP    TR,C,DC
         OP    TRT,C,DD
         OP    ED,C,DE
         OP    EDMK,C,DF
         OP    LASP,2,E500                                         G001
         OP    TPROT,2,E501                                        T002
         OP    MVCIN,C,E8                                          T002
         OP    SRP,S,F0                                            U004
         OP    MVO,B,F1
         OP    PACK,B,F2
         OP    UNPK,B,F3
         OP    ZAP,B,F8
         OP    CP,B,F9
         OP    AP,B,FA
         OP    SP,B,FB
         OP    MP,B,FC
OPEND    OP    DP,B,FD                                             T002
         SPACE 3
         END
