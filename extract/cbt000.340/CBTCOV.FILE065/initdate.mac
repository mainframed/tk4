         TITLE 'I N I T D A T E     CONVERT DATE TO EBCDIC'
INITDATE CSECT
         SPACE 1
***********************************************************************
*                                                                     *
*FUNCTION  -  THIS ROUTINE WILL TAKE A DATE GIVEN IN PACKED FORMAT    *
*        AND CONVERT IT INTO FORMS THAT ARE PRINTABLE.                *
*        FORM 1  CHARACTER LENGTH 8  MM/DD/YY.                        *
*        FORM 2  CHARACTER LENGTH 8  YY.DDD                           *
*                                                                     *
*INPUT  -  THREE VARIABLES ARE PASSED.                                *
*        1)  PL4  -  DATE TO BE CONVERTED(PACKED FORMAT 00YYDDDF).    *
*        2)  CL8  -  WILL CONTAIN THE CONVERTED DATE MM/DD/YY         *
*        3)  CL8  -  WILL CONTAIN THE DATE IN THE FORM YY.DDD         *
*                                                                     *
***********************************************************************
         EJECT
         SAVE  (14,12),,*
         LR    12,15
         USING INITDATE,12
         LR    3,13
         ST    13,MYSAVE+4
         LA    13,MYSAVE
         ST    13,8(3)
         SPACE 3
         LM    3,5,0(1)             GET PARMS
         ZAP   MONTH,=PL4'1'        INITIALIZE
         MVC   MM,=C'  '
         MVC   DD,=C'  '
         MVC   YY,=C'  '
         MVC   DATE,0(3)            SAVE DATE
         MVC   DAY,DATE             X
         NC    DAY,MASK             REMOVE YY
         MVC   EDIT,PATTERN         X
         ED    EDIT,DATE            PROCESS FOR JULIAN
         MVC   0(8,5),EDIT+1        RETURN JULIAN
         ZAP   TEMPD,DATE           INIT. FOR YEAR
         DP    TEMPD,=PL4'1000'     YYDDDF TO 000YYF
         MVC   YEAR,TEMPD           X
         ZAP   TEMPD,YEAR           X
         CVB   10,TEMPD
         LR    11,10
         SRL   11,2                 CHK FOR LEAP YEAR
         SLL   11,2                 MULTIPLE OF 4
         CR    11,10
         BNE   NOTLEAP              NO
         LA    6,LEAP               YES
         B     LOOP                 GO FORMAT MM/DD/YY
NOTLEAP  LA    6,TAB                X
LOOP     CP    DAY,2(2,6)           Q. GREATER THAN NEXT MONTH
         BL    LOOP1                A. IN THIS MONTH.
         BE    LOOP1                A. IN THIS MONTH
         LA    6,2(,6)              TRY NEXT MONTH
         AP    MONTH,=PL4'1'        BUMP FOR NEXT MONTH
         CP    MONTH,=PL4'13'       END OF YEAR
         BE    ERROR                X
         B     LOOP                 X
LOOP1    ZAP   LOWER,0(2,6)         GET JULIAN FOR LAST MONTH
         SP    DAY,LOWER            GET REMAINDER
         MVC   EDIT1,PATTERN1       X
         ED    EDIT1,DAY+2
         MVC   DD,EDIT1+2           SET FOR RETURN
         MVC   EDIT1,PATTERN1       X
         ED    EDIT1,MONTH+2        MAKE 0MMF
         MVC   MM,EDIT1+2           RETURN
         MVC   EDIT1,PATTERN1       X
         ED    EDIT1,YEAR+2         X
         MVC   YY,EDIT1+2           X
         MVC   0(8,4),MM            RETURN MM/DD/YY TO CALLER
         L     13,MYSAVE+4
         RETURN (14,12),,RC=0
ERROR    ABEND 99,DUMP
         SPACE  4
         DS     0D
TEMPD    DS      PL8
MYSAVE   DC    18F'0'
YEAR     DS      PL4
MONTH    DS      PL4
DATE     DS      PL4
DAY      DS      PL4
MASK     DC      XL4'0000FFFF'
TEMP     DS      PL4
LOWER    DS      PL4
PATTERN  DC      XL9'40202120204B202020'
PATTERN1 DC      XL4'40212020'
EDIT     DS      CL9
EDIT1    DS      CL4
MM       DC      CL2' ',C'/'
DD       DC      CL2' ',C'/'
YY       DC      CL2' '
         SPACE   3
TAB      DC      PL2'00',PL2'31',PL2'59',PL2'90',PL2'120',PL2'151',PL2'X
               181',PL2'212',PL2'243',PL2'273',PL2'304',PL2'334',PL2'40X
               0'
LEAP     DC      PL2'00',PL2'31',PL2'60',PL2'91',PL2'121',PL2'152',PL2'X
               182',PL2'213',PL2'244',PL2'274',PL2'305',PL2'335',PL2'40X
               0'
         LTORG
         END
