         MACRO
&NAME    CLEAR &AREA
         LCLC  &L
&L       SETC  'L'''
&NAME    DS    0H
         MVI   &AREA,C' '
         MVC   &AREA+1(&L&AREA-1),&AREA
         MEND
         EJECT
MASKLIST CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SAVE  (14,12)
         LR    R12,R15
         USING MASKLIST,R12
         CNOP  0,4
         BAL   R15,START
         DC    18F'0'
START    ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         SPACE 2
         L     R1,16
         L     R8,40(R1)
         LR    R1,R8
UCBSCAN  LH    R0,0(,R1)
         LTR   R0,R0
         LA    R1,2(,R1)
         BNM   UCBSCAN
         SR    R1,R8                    NUMBER OF UCB SLOTS+1
         SRL   R1,1
         BCTR  R1,0
         LA    R0,31
         AR    R1,R0                    ROUND COUNT TO WORD BOUNDARY
         OR    R1,R0
         XR    R1,R0
         SRL   R1,3                     TOTAL WORD COUNT
         LR    R9,R1                    SAVE COUNT
         EJECT
LOAD     DS    0H
         LOAD  EP=DEVNAMET
         LR    R10,R0
         LOAD  EP=DEVMASKT
         LR    R11,R0
         OPEN  (SYSPRINT,OUTPUT,SYSIN,INPUT)
GET      MVI   LINE-1,C'1'
GET0     TPUT  MSGENTER,L'MSGENTER
GET1     GET   SYSIN
         LR    R2,R1
         CLI   0(R1),C' '
         BNE   GET2
         CLC   1(71,R1),0(R1)
         BE    GET1
GET2     DS    0H
         CLEAR LINE
         MVC   LINE(80),0(R1)
         PUT   SYSPRINT,LINE-1
         MVI   LINE-1,C' '
         CLI   0(R2),C'*'
         BE    GET1
         LA    R14,1
         LR    R1,R2
         LA    R15,71(R2)
FIND     CLI   0(R1),C' '
         BNE   FIND1
         BXLE  R1,R14,FIND
         B     GET1
FIND1    LR    R2,R1
FIND2    CLI   0(R1),C' '
         BE    FIND3
         BXLE  R1,R14,FIND2
FIND3    SR    R1,R2
         CLEAR UNITNAME
         BCTR  R1,0
         MVC   UNITNAME(0),0(R2)
         EX    R1,*-6
         CLC   END,UNITNAME
         BE    EODAD
         L     R0,0(,R10)
         LA    R15,4(,R10)
NAMESCAN CLC   UNITNAME,0(R15)
         BE    HAVENAME
         LA    R15,12(,R15)
         BCT   R0,NAMESCAN
         PUT   SYSPRINT,=CL121' ******* INVALID UNIT GROUP'
         B     GET
HAVENAME LH    R0,0(,R11)               GET NUMBER OF MASK ENTRIES
         LA    R1,4(,R11)
FINDMASK CLC   8(4,R15),4(R1)
         BE    HAVEMASK
         LA    R1,8(R1,R9)
         BCT   R0,FINDMASK
         PUT   SYSPRINT,=CL121' ******* NO MASK ENTRY FOUND'
         B     GET
HAVEMASK DS    0H
         MVI   LINE-1,C'-'
         LA    R2,7(R1,R9)              END OF MASK ENTRY
         L     R3,0(,R1)                NUMBER OF BITS THIS ENTRY
         LA    R4,8(,R1)
         SR    R7,R7
NEXTBYTE LA    R5,8
         LA    R6,X'80'
NEXTBIT  TM    0(R4),0
         EX    R6,NEXTBIT
         BZ    NOTTHIS
         CLEAR LINE
         LA    R15,0(R7,R8)             DOUBLE INDEX
*                                       TO LOOKUP ENTRY
         LH    R15,0(,R15)
         MVC   LINE+4(3),13(R15)
         TM    18(R15),X'20'            DIRECT ACCESS?
         BZ    NOTDA                    NO
         TM    3(R15),X'80'             ONLINE?
         BZ    NOTDA
         CLI   28(R15),X'00'            YES, VOLID KNOWN?
         BE    NOTDA                    NO
         MVI   LINE+8,C'-'
         MVC   LINE+10(6),28(R15)
         SPACE 2
NOTDA    DS    0H
         PUT   SYSPRINT,LINE-1
         BCT   R3,NOTTHIS
         B     GET
NOTTHIS  DS    0H
         LA    R7,2(,R7)
         SPACE 2
         SRL   R6,1
         BCT   R5,NEXTBIT
         LA    R4,1(,R4)
         CR    R4,R2                    MASK BIT COUNT ERROR?
         BH    TOOMANY                  YES
         B     NEXTBYTE
TOOMANY  PUT   SYSPRINT,=CL121'0TOO MANY BITS IN MASK COUNT'
         B     GET
EODAD    DS    0H
         CLOSE (SYSIN,,SYSPRINT)
         L     R13,4(,R13)
         RETURN (14,12),T,RC=0
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,LRECL=81,BLKSIZE=81,RECFM=FBA,          X
               DSORG=PS,MACRF=PM
SYSIN    DCB   DDNAME=SYSIN,LRECL=80,RECFM=FB,MACRF=GL,                X
               EODAD=EODAD,DSORG=PS
         LTORG
UNITNAME DC    CL8' '
         DC    C' '
LINE     DC    CL80' '
MSGENTER DC    C'ENTER UNIT GROUP NAME'
END      DC    C'END'
         END
