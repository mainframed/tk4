IGC0025D TITLE 'IGC0025D - GTEDS SYSTEMS INTERFACE ROUTINE '
         @     IGC0025D    MODULE NAME IN BLOCK LETTERS     *HMD 12/81*
***********************************************************************
* THIS ROUTINE IS A NON-AUTHORIZED TYPE 3 SVC AND ALLOWS
* THE FOLLOWING DEPENDING ON PARM PASSED IN REG 1:
*
*            - R1 = 0   BYPASS PASSWORD PROTECT IN JSCB     *HMD 02/82*
*            - R1 = 4   SET 'ON'  AUTH IN JSCB              *HMD 02/82*
*            - R1 = 8   SET 'OFF' AUTH IN JSCB              *HMD 02/82*
*            - R1 = 12  RESET PASSWORD PROTECT IN JSCB      *HMD 02/82*
*            - R1 = 16  SET 'OFF' CANCELABLE FLAG IN CSCB   *HMD 10/81*
*            - R1 = 20  SET 'ON'  CANCELABLE FLAG IN CSCB   *HMD 10/81*
*            - R1 = 24  BYPASS JOBSTEP TIMING IN ASCB       *HMD 02/82*
*            - R1 = 28  RESET  JOBSTEP TIMING IN ASCB       *HMD 02/82*
*            - R1 > MAX JUST EXIT                           *HMD 10/81*
*                                                           *HMD 02/82*
* THE ROUTINE ALSO CHECKS FOR AUTHORIZED USERS AS FOLLOWS:  *HMD 12/81*
*                                                           *HMD 12/81*
*     - IF CODE IN FLOATING POINT REGISTER 0 IS EQUAL TO    *HMD 12/81*
*       THE CODE IN FETCH PROTECTED STORAGE POINTED TO BY   *HMD 12/81*
*       THE GTE CVT USER AREA, THEN ACCESS TO THE SVC IS    *HMD 12/81*
*       ALLOWED.  THIS ALLOWS THIS SVC TO BE USED BY        *HMD 12/81*
*       CERTAIN TSO COMMANDS UNDER SPF BY EVERYONE. THE     *HMD 12/81*
*       CODE CAN BE CHANGED BY RE-ASSEMBLING THE 'INTICVTU' *HMD 12/81*
*       PROGRAM, ZEROING OUT THE CVTUSER FIELD IN THE CVT,  *HMD 12/81*
*       AND THEN STARTING THE 'INITCVTU' TASK. IF CHANGED,  *HMD 12/81*
*       HOWEVER, THE INVOKING PROGRAM WILL HAVE TO BE       *HMD 12/81*
*       REASSEMBLED.                                        *HMD 12/81*
*                                                           *HMD 12/81*
*     - IF CODE IN R0 IS NOT EQUAL TO CODE IN PROTECTED     *HMD 12/81*
*       STORAGE (I.E., NO BYPASS CODE PROVIDED), THEN       *HMD 12/81*
*       USE ACF2 SECURITY FACILITY TO LOOK UP LOGONID       *HMD 12/81*
*       ASSOCIATED WITH CALLER. IF LOGONID MATCHES          *HMD 12/81*
*       TABLE OF 'AUTHORIZED' SYSTEMS PROGRAMMER PREFIXES,  *HMD 12/81*
*       THEN ALLOW THIS SVC TO OPERATE. OTHERWISE JUST EXIT *HMD 12/81*
*       WITH RETURN CODE 8 IN R15. THIS ALLOWS THE STANDARD *HMD 12/81*
*       APF AUTHORIZATION MECHANISM TO BE USED IN WITH THIS *HMD 12/81*
*       SVC. THAT IS, A TSO COMMAND CAN BE WRITTEN WHICH    *HMD 12/81*
*       USES THIS SVC TO OBTAIN AUTHORIZATION.  WHEN THE    *HMD 12/81*
*       COMMAND IS ADDED TO THE IKJEFTE2 TABLE, THIS SVC    *HMD 12/81*
*       CALL NEED NOT BE REMOVED. IT WILL SIMPLY 'NOT'      *HMD 12/81*
*       INVOKE AUTHORIZATION FOR UNAUTHORIZED USERS.        *HMD 12/81*
*                                                           *HMD 12/81*
*                                                           *HMD 12/81*
* WRITTEN AND MODIFIED BY:
*                                                           *HMD 12/81*
*         HOWARD M. DEAN (GTE DATA SERVICES)                *HMD 10/81*
*         SYSTEMS SUPPORT UNIT                              *HMD 10/81*
*         4750 LINCOLN BLVD.                                *HMD 12/81*
*         MARINA DEL REY, CA 90291                          *HMD 12/81*
*         (213) - 821-0511                                  *HMD 12/81*
*                                                           *HMD 12/81*
***********************************************************************
         EJECT                                              *HMD 10/81*
IGC0025D CSECT
         ENTRY SWITCH                                       *HMD 02/82*
         USING IGC0025D,R6
         USING PSA,R0                                       *HMD 12/81*
         USING CVTMAP,R3                                    *HMD 12/81*
         USING TCB,R4                                       *HMD 12/81*
         USING RBBASIC,R5                                   *HMD 12/81*
         USING IGC0025D,R6                                  *HMD 12/81*
         USING ASCB,R7        ADDRESS SPACE CONTROL BLOCK   *HMD 02/82*
         USING CHAIN,R8       ADDRESS THE CSCB CNTL BLOCK   *HMD 12/81*
         USING ACCVT,R9                                     *HMD 12/81*
         USING LIDREC,R10                                   *HMD 12/81*
$OFF     EQU   4                                            *HMD 12/81*
D0       EQU   0              DISPLACEMENT OF ZERO          *HMD 12/81*
D1       EQU   1              DISPLACEMENT OF ONE           *HMD 12/81*
D2       EQU   2              DISPLACEMENT OF TWO           *HMD 12/81*
FP0      EQU   0              FLOATING POINT REG. ZERO      *HMD 12/81*
M7       EQU   7              MASK OF BINARY SEVEN          *HMD 01/82*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         LR    R11,R1         SAVE PARM REG. OVER ACF CALL  *HMD 12/81*
SWITCH   NOP   SETUP          BYPASS SWITCH FOR TESTING     *HMD 03/82*
         L     R12,CVTUSER    LOAD CVT USER ADDRESS         *HMD 12/81*
         N     R12,CLRHI      CLEAR HIGH-ORDER BITS         *HMD 01/82*
         BZ    ACFLOOK        HAS NOT BEEN INITIALIZED      *HMD 01/82*
         CLC   PROT,0(R12)    CHECK FOR VALID CVT USER AREA *HMD 01/82*
         BNE   ACFLOOK        NOT VALID, USE ACF CHECKING   *HMD 01/82*
         L     R12,$OFF(R12)  LOAD SECRET CODE ADDRESS      *HMD 12/81*
         N     R12,CLRHI      CLEAR HIGH-ORDER BITS         *HMD 12/81*
         BZ    ACFLOOK        SECRET CODE NOT INITIALIZED   *HMD 01/82*
         CD    FP0,0(,R12)    IS  FP REGISTER EQUAL TO CODE *HMD 12/81*
         BE    SETUP          YES, ALLOW SVC ACTION         *HMD 12/81*
ACFLOOK  DS    0H             GET SET TO LOOK UP USERIDS    *HMD 12/81*
         SER   FP0,FP0        CLEAR CODE JUST IN CASE       *HMD 12/81*
         ACFINCVT 9,NONE=ABENDXXX,TYPE=INLINE,DISPL=SSCVT   *HMD 04/83*
*                             FIND ACF2 CVT ADDRESS         *HMD 04/83*
         ACFGUCB 10,NONE=ABENDXXX,INLINE=YES,SYS=AOS2       *HMD 04/83*
*                             FIND ACUCB                    *HMD 04/83*
         L     R10,ACULRECP-ACUCB(R10)                      *HMD 12/81*
         LA    R12,UIDTBL     GET USERID TABLE ADDR         *HMD 12/81*
         SLR   R15,R15        CLEAR R15 FOR IC              *HMD 12/81*
         TM    LIDCFLAG,LIDCCAN+LIDCSUS                     *HMD 12/81*
         BO    ABENDXXX       CANCELED OR SUSPENDED         *HMD 12/81*
* ADD TEST HERE FOR OTHER LOGONID ATTRIBUTES
* USERID FAILED ABOVE TESTS, CHECK FOR AUTH USER IN TABLE   *HMD 12/81*
TBLLOOK  CLI   D0(R12),ENDFLG CHECK FOR TABLE END FIRST     *HMD 12/81*
         BE    ABENDXXX       END OF TABLE..NOT AUTHORIZED  *HMD 12/81*
         IC    R15,D0(R12)    INSERT LENGTH TO CHECK        *HMD 12/81*
         EX    R15,COMPUID    DOES LID MATCH TABLE?         *HMD 12/81*
         BE    SETUP          YES. THIS GUY (OR GAL) IS OK  *HMD 12/81*
         LA    R12,D2(R12,R15) LENGTH OF NAME+LENGTH BYTE   *HMD 12/81*
         B     TBLLOOK        LOOK UP NEXT USERID           *HMD 12/81*
COMPUID  CLC   D1(D0,R12),LIDLID                            *HMD 12/81*
*                             CHECK FOR LOGON ID MATCH      *HMD 12/81*
         EJECT                                              *HMD 12/81*
SETUP    DS    0H             SETUP FOR SVC ACTION          *HMD 12/81*
         L     R12,TCBJSCB    LOAD JSCB ADDRESS
         USING IEZJSCB,R12    ADDRESS THE CONTROL BLOCK     *HMD 12/81*
         L     R12,JSCBACT    POINT TO ACTIVE CONTROL BLOCK *HMD 12/81*
         LA    R8,MAX         IS THE PARM > MAX?            *HMD 10/81*
         CR    R11,R8         IS IT?
         BH    EXIT           YEP, FOR GIT IT
         B     BRTAB(R11)     USE R11 AS BRANCH OFFSET
BRTAB    B     PASSOF         TURN OFF PASSWORD PROTECT
         B     AUTHON         TURN ON AUTH FLAG
         B     AUTHOFF        TURN OFF AUTH FLAG
         B     PASSON         RESET PASSWORD PROTECT
         B     NOCAN          SET JOB-STEP NON-CANCELLABLE  *HMD 10/81*
         B     CANDO          SET JOB STEP CANCELLABLE      *HMD 10/81*
         B     NOTIME         SET JOBSTEP TIMING OFF        *HMD 02/82*
         B     TIME           SET JOBSTEP TIMING ON         *HMD 02/82*
MAX      EQU   *-BRTAB-4      MAX NUMBER OF USABLE ENTRIES  *HMD 12/81*
         SPACE 2
PASSOF   OI    JSCBSWT1,JSCBPASS          TURN OFF PASSWORD PROTECT
         B     EXIT
         SPACE 2
AUTHON   OI    JSCBOPTS,JSCBAUTH          TURN AUTH 'ON'
         B     EXIT
         SPACE 2
AUTHOFF  NI    JSCBOPTS,X'FF'-JSCBAUTH    TURN AUTH 'OFF'
         B     EXIT
PASSON   NI    JSCBSWT1,X'FF'-JSCBPASS    TURN ON PASSWORD PROTECT
         B     EXIT
NOCAN    L     R8,JSCBCSCB       GET CSCB ADDR              *HMD 10/81*
         NI    CHACT,X'FF'-CHCL-CHAIFX                      *HMD 10/81*
         B     EXIT              SET JOB NON-CANCELLABLE    *HMD 10/81*
CANDO    L     R8,JSCBCSCB       GET CSCB ADDR              *HMD 10/81*
         OI    CHACT,CHCL+CHAIFX SET 'CANCELLABLE'          *HMD 10/81*
         B     EXIT                                         *HMD 10/81*
NOTIME   OI    ASCBRCTF,ASCBTOFF DONT TIME THIS ASCB        *HMD 02/82*
         B     EXIT                                         *HMD 02/82*
TIME     NI    ASCBRCTF,X'FF'-ASCBTOFF TURN TIMING ON       *HMD 02/82*
         B     EXIT                                         *HMD 02/82*
         SPACE 2
ABENDXXX LA    R15,8             INDICATE NOT AUTHORIZED    *HMD 12/81*
         SVC   3                 RETURN TO CALLER           *HMD 01/82*
EXIT     DS    0H
         SER   FP0,FP0           CLEAR SECRET CODE          *HMD 12/81*
         LA    R15,0             RETURN CODE 0              *HMD 01/82*
SVC3     SVC   3
         SPACE 2                                            *HMD 12/81*
         EJECT                                              *HMD 12/81*
ALIGN    DS    0F                                           *HMD 03/82*
PROT     DC    CL4'PROT'        CONTROL BLOCK EYE-CATCHER   *HMD 01/82*
CLRHI    DC    F'16777215'      TO CLEAR HI-ORDER BITS      *HMD 12/81*
UIDTBL   DS    0F               LOGONID TABLE START         *HMD 04/83*
         DC    X'07',C'$$$$$$$$' BLANKED OUT FOR SECURITY   *HMD 04/83*
*                                                           *HMD 04/83*
* ADD MORE NAMES HERE                                       *HMD 04/83*
* 1ST NUMBER IS LENGTH OF LOGONID MINUS ONE                 *HMD 04/83*
* 2ND IS CHARACTER STRING OF LOGONID                        *HMD 04/83*
*                                                           *HMD 04/83*
ENDOTAB  DC    X'FF',C'TBLEND'  MARKS END OF TABLE          *HMD 12/81*
ENDFLG   EQU   X'FF'            END OF TABLE FLAG EQUATE    *HMD 12/81*
         LTORG
         EJECT                                              *HMD 12/81*
         CVT   DSECT=YES,LIST=YES                           *HMD 12/81*
         EJECT                                              *HMD 12/81*
         IKJTCB LIST=YES                                    *HMD 12/81*
         EJECT                                              *HMD 12/81*
         IEZJSCB ,                                          *HMD 12/81*
         EJECT                                              *HMD 12/81*
         IEECHAIN                                           *HMD 12/81*
         EJECT                                              *HMD 12/81*
         IHAPSA                                             *HMD 12/81*
         EJECT                                              *HMD 12/81*
         IHAASCB                                            *HMD 12/81*
         EJECT                                              *HMD 12/81*
         IHAASVT                                            *HMD 12/81*
         EJECT                                              *HMD 12/81*
         IHARB                                              *HMD 12/81*
         EJECT                                              *HMD 12/81*
         LIDREC  DSECT=YES                                  *HMD 12/81*
         EJECT                                              *HMD 12/81*
         ACFASVT DSECT=YES                                  *HMD 12/81*
         EJECT                                              *HMD 12/81*
         ACCVT   DSECT=YES                                  *HMD 12/81*
         EJECT                                              *HMD 12/81*
         ACUCB   DSECT=YES                                  *HMD 12/81*
         END   IGC0025D
