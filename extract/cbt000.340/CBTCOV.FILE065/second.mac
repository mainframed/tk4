SECOND   TITLE 'SUBROUTINE/FUNCTION TO GET ACCUMLATED STEP CPU TIME'
*
*     THIS PROGRAM RETRIEVES THE JOB STEP ACCUMULATED CPU TIME FROM
* THE ASCB (ASCBEJST), CONVERTS IT INTO SECONDS(IN FLOATING POINT
* NUMBER). IT CAN BE USED EITHER AS A FUNCTION OR AS A SUBROUTINE.
* USER'S PARAMETER AREA (SERVING AS A SUBROUTINE). THE CALLING SEQUENCE
* THE CALLING SEQUENCE  FOR "SECOND" IS AS FOLLOWS:
*          1. THE ACCUMULATED JOB STEP TCB TIME IN SECONDS
*                 (A FLOATING POINT NUMBER)
*
*     "SECOND" IS REENTRANT AND REUSEABLE.
*
SECOND   ENTER PARMREG=2,          INITIALIZATION                      X
               GETMAIN=(WORKSIZE,0)
         REGISTER                 REGISTER EQUATES
         USING WORKAREA,R13        ADDRESS WORK AREA DSECT
         USING ASCB,R4            ADDRESS ASCB DSECT
         L     R2,0(,R2)          POINT TO CPU TIME FIELD
         L     R4,16              POINT TO CVT
         L     R4,0(,R4)          POINT TO TCB/ASCB WORD LIST
         L     R4,12(,R4)         POINT TO CURRENT ASCB
         LM    R6,R7,ASCBEJST     LOAD ACCUMULATED STEP TCB TIME
         SRDA  R6,22              SHIFT TO GET 1024*MICROSECONDS
         BINTIME 6                CONVERT TO HUNDREDTHS OF SECONDS
         ST    R7,ADR
         MVC   CALLIST,CCALLST
         LOAD  EP=COAD01
         ST    R0,COAD01EP
         L     R15,COAD01EP
         CALL  (15),                                                   X
               (ADR,ADR1),MF=(E,CALLIST)
         LE    R0,ADR1
         DE    R0,=E'100'         CONVERT TO SECONDS,FUNCTION CALL
         STE   R0,0(,R2)          PUT INTO PARM1 FOR SUBR CALL
         LEAVE RETCODE=0,          RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
CCALLST  CALL  ,(0,0),MF=L
CCALLEN  EQU   *-CCALLST
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
         DS    18A                 REGISTER SAVE AREA
CALLIST  DS    CL(CCALLEN)
ADR      DS    F
ADR1     DS    F
COAD01EP DS    F
WORKSIZE EQU   *-WORKAREA
         EJECT
         IHAASCB
         END
