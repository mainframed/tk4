         TITLE 'CICS FORWARD RECOVERY JOB'
         PRINT NOGEN
**********************************************************************
*                                                                    *
*        IS028                                                       *
*                                                                    *
**********************************************************************
*                                                                    *
* FUNCTION: THIS JOB WILL PERFORM A FORWARD RECOVERY ON VSAM FILES   *
*           WHO'S UPDATES ARE RECORDED ON THE CICS JOURNAL FILE      *
*           SUPPLIED TO THIS PROGRAM AS INPUT.                       *
*                                                                    *
* INPUT:    CICS JOURNAL FILE.                                       *
*                                                                    *
* OUTPUT:   UPDATED VSAM FILE(S).                                    *
*           MESSAGES ARE PRINTED INDICATING PROGRAM INITIATION,      *
*                PROGRAM TERMINATION, UPDATE COUNTS PER FILE AND     *
*                ANY UNUSUAL CONDITIONS.                             *
*                                                                    *
* ENTRY CONDITIONS: A PARAMETER IS PASSES TO THIS PROGRAM VIA JCL    *
*           WHO'S FORMAT IS AS FOLLOWS.                              *
*                                                                    *
*              PARM='DATE=YY.DDD,DDNAME=(DDNAME1,DDNAME2,DDN3)'      *
*                                                                    *
*           WHERE DATE IS THE DATE OF THE UPDATES TO START RECOVERING*
*                          FROM OR THE CHARACTER STRING ALL WHICH    *
*                          WILL CAUSE A RECOVERY OF ALL THE RECORDS  *
*                          FROM THE RECOVERY LOG (THE DEFAULT IS     *
*                          THE CURRENT DATE).                        *
*                 DDNAME IS EITHER A SINGLE DDNAME OR A LIST OF      *
*                          DDNAMES IN PARENTHASIS OF THOSE DATA SETS *
*                          TO RECOVER.                               *
*                                                                    *
* EXIT CONDITIONS: A RETURN CODE IS SET TO INDICATE THE STATUS OF    *
*           ALL EXECUTIONS.  THE RETURN CODE VALUES AND THEIR        *
*           MEANINGS ARE AS FOLLOWS:                                 *
*                 0 = JOB RUN SUCCESSFULLY AND ALL FILES RECOVERED.  *
*                 4 = INPUT JCL PARM(S) WAS NOT VALID.  NO RECOVERY  *
*                     WAS ATTEMPTED.                                 *
*                 8 = ERROR FOUND WHEN ATTEMPTING TO RECOVER.  VSAM  *
*                     FILE STATUS IS UNKNOWN.                        *
*                                                                    *
* NOTES AND LIMITATIONS:                                             *
*           IF THE PROGRAM IS UNSUCCESSFUL IN OPENING THE SYSPRINT   *
*           FILE (USED TO PRINT ALL MESSAGES), THE PROGRAM WILL      *
*           ABEND WITH A USER CODE 1311.                             *
*                                                                    *
*               IF THIS PROGRAM RECEIVES A NON-ZERO RETURN CODE      *
*           FROM ANY OF IT'S SUB ROUTINES, IT WILL NOT CONTINUE      *
*           WITH THE RECOVERY.  IF THIS PROGRAM ENDS IN A NON-ZERO   *
*           RETURN CODE, THE STATUS OF THE RECOVERED FILES IS IN     *
*           DOUBT.                                                   *
*                                                                    *
**********************************************************************
         EJECT
**********************************************************************
*                                                                    *
*  THE LOGIC OF THE IS028 CICS VSAM RECOVER RTN IS AS FOLLOWS        *
*                                                                    *
*  1)  OPEN THE PROGRAM MESSAGE FILE                                 *
*  2)  IF THE OPEN WAS NOT SUCCESSFUL                                *
*  3)     ABEND WITH A USER CODE OF 1311                             *
*      ENDIF                                                         *
*  4)  CALL THE IS029 PROGRAM TO PARSE THE INPUT PARMS               *
*  5)  IF THE INPUT PARMS WERE IN ORDER                              *
*  6)     CALL THE IS030 PROGRAM TO OPEN ALL SPECIFIED VSAM FILES    *
*  7)     IF ALL VSAM FILES WERE OPENED SUCCESSFULLY                 *
*  8)        CALL IS031 TO RECOVER SPECIFIED VSAM FILES              *
*         ENDIF                                                      *
*  9)     DO WHILE ALL VSAM FILES ARE NOT CLOSED                     *
* 10)        IF FILE IS OPEN                                         *
* 11)           PRINT COUNTS OF ACTIONS TAKEN ON FILE                *
*               CLOSE THE FILE                                       *
*            ENDIF                                                   *
* 12)        GET THE NEXT FILE ENTRY IN THE TABLE                    *
*         ENDDO                                                      *
*      ENDIF                                                         *
* 13)  CLOSE THE MESSAGE FILE                                        *
*      RETURN                                                        *
*                                                                    *
**********************************************************************
         EJECT
IS028    ENTER
         L     R1,0(R1)                  R1 --> INPUT PARMS
         ST    R1,PARSLIST               STORE ADDRESS OF INPUT PARMS
         SPACE
* < 1 > *
         OPEN  (SYSPRINT,OUTPUT)         OPEN SYSPRINT FILE
         LA    R3,SYSPRINT               R3 --> SYSPRINT DCB
         USING IHADCB,R3                 ESTABLISH LABELS ON DCB
         SPACE
* < 2 > *
         IF    (DCBOFLGS,Z,X'10',TM),THEN IF OPEN NOT SUCCESSFULL
         SPACE
* < 3 > *
            ABEND 1311,DUMP              *** ABEND *** USER 1311
         EIF
         SPACE
* < 4 > *
         LA    R1,PARSLIST               R1 --> PARM FOR PARMPARS
         L     R15,=V(IS029)             R15 --> IS029, THE PARM PARSER
         BALR  R14,R15                   BRANCH TO PARMPARS ROUTINE
         LR    R12,R15                   SAVE RETURN CODE
         SPACE
* < 5 > *
         IF (R15,Z,R15,LTR),THEN
         SPACE
* < 6 > *
            LA    R1,GNRLPARM            R1 --> OPEN RTN'S PARM LIST
            L     R15,=V(IS030)          R15 --> IS030, THE VSAM OPENR
            BALR  R14,R15                BRANCH TO THE VSAM OPEN RTN
         SPACE
* < 7 > *
            IF (R15,Z,R15,LTR),THEN
         SPACE
* < 8 > *
               LA    R1,GNRLPARM         R1 --> OPEN RTN'S PARM LIST
               L     R15,=V(IS031)       R15 --> IS031, THE RECOVER RTN
               BALR  R14,R15             BRANCH TO THE VSAM OPEN RTN
            EIF
            LR    R12,R15                SAVE RETURN CODE
            LA    R4,RECVENTS            R4 --> FILE ENTRIES IN TABLE
            USING $DDENTRY,R4            ESTABLISH LABELS ON TABLE
         EJECT
* < 9 > *
            DOWHILE ($DDNAME,NE,X'FF')
         SPACE
* < 10 > *
               IF ($DDACBAD,NE,=F'-1'),THEN
         SPACE
* < 11 > *
                  MVC   FILENAME,$DDNAME MOVE DDNAME TO PRINT LINE
                  MVC   FILEADD,=X'40206B2020206B202120'
                  ED    FILEADD,$DDADD   MOVE ADD COUNT TO PRINT LINE
                  MVC   FILEUPDT,=X'40206B2020206B202120'
                  ED    FILEUPDT,$DDUPDT MOVE UPDT COUNT TO PRINT LINE
                  MVC   FILEDEL,=X'40206B2020206B202120'
                  ED    FILEDEL,$DDDEL   MOVE DELET COUNT TO PRINT LINE
                  PUT   SYSPRINT,FILEMSG PRINT COUNT'S FOR FILE
                  L     R5,$DDACBAD      R5 --> ACB
                  CLOSE ((R5))           CLOSE THE ACB
               EIF
         SPACE
* < 12 > *
               LA    R4,$DDNEXT          POINT TO NEXT FILE ENTRY
            EDO
         EIF
         SPACE
* < 13 > *
         CLOSE (SYSPRINT)                CLOSE SYSPRINT FILE
         LR    R15,R12                   RESTORE RETURN CODE
         SPACE
         EXIT
         TITLE 'IS028 DATA PORTION'
PARSLIST DS    A                         ADDR(INPUT PARMS)
GNRLPARM DC    A(RECVTABL)               ADDR(RECOVERY TABLE)
         DC    A(SYSPRINT)               ADDR(SYSPRINT DCB)
         SPACE
FILEMSG  DS   0CL80                      THIS IS THE FILE UPDATE MSG
         DC    CL12'0 FILE NAME '
FILENAME DS    CL8                       FIELD FOR FILE NAME
         DC    CL8'   ADDED'
FILEADD  DS    CL10                      FIELD FOR FILE ADD COUNT
         DC    CL10'   UPDATED'
FILEUPDT DS    CL10                      FIELD FOR FILE UPDATE COUNT
         DC    CL10'   DELETED'
FILEDEL  DS    CL10                      FIELD FOR FILE DELETE COUNT
         DC    CL2' '
         SPACE
RECVTABL DS   0F                         THIS IS THE RECOVERY TABLE
RECVDATE DC    F'-1'                     THIS IS THE DATE TO RECOVER
RECVENTS DC  20XL28'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFX
               FFFFF'
         DC    X'FF'                     THIS IS THE 12 ENTRY TABLE
         SPACE
         LTORG
         SPACE
SYSPRINT DCB   DDNAME=SYSPRINT,          DDNAME FOR PROGRAM MSG FILE   X
               DSORG=PS,                 DATA SET ORG. IS PHYSICAL SEQ.X
               MACRF=PM,                 MACRO FORMAT IS GET LOCATE    X
               LRECL=80,                 LOGICAL RECORD LENGTH IS 80   X
               BLKSIZE=3120,             BLOCK SIZE IS 3120            X
               RECFM=FBA                 RECORD FORMAT IS FIXED BLK
         SPACE
         DDENTRY
         SPACE
         IHADCB DSORG=PS,DEVD=DA
         SPACE
         END   IS028
