*/********************************************************************/
*/*                                                                  */
*/*  SALVAGE : A PROGRAM WHICH WILL RECOVER AS MUCH DATA AS POSSIBLE */
*/*            FROM A PDS WHEN THE DIRECTORY HAS BEEN CLOBBERED.     */
*/*                                                                  */
*/* MEMBER DATA IS WRITTEN OUT TO A SEQUENTIAL DATASET WITH          */
*/* MEMBERS BEING DELIMITED BY AN IEBUPDTE TYPE CONTROL CARD         */
*/*./   ADD   LIST=ALL,NAME=MEM#NNNN                                 */
*/* WHICH YOU CAN THEN DO A) USE IEBUPDTE TO RE-CREATE THE PDS       */
*/*                          (80 BYTE RECS ONLY)                     */
*/*                                                                  */
*/*                       B) USE TSO ISPF TO MANUALLY MOVE MEMBERS   */
*/* (NOTE THE DATASET CREATED WILL BE TOO LARGE PROBABLY TO EDIT)    */
*/*                                                                  */
*/*                       C) INVOKE PGM PLIFIX FROM ISPF WHICH WILL  */
*/* CALL THIS PROGRAM AND REBUILD YOUR PDS WITH WHATEVER SIZE        */
*/* OF RECORD YOUR PDS HAS.---SOURCE IS ON THE TAPE.                 */
*/*                                                                  */
*/*                                                                  */
*/* SINCE THE DIRECTORY HAS BEEN BROKEN GAS MEMBERS ARE RECOVERED    */
*/* AS WELL AS I CANNOT TELL WHAT IS GOOD AND NOT GOOD.              */
*/*                                                                  */
*/* IF THE DIRECTORY IS PARTIALLY BROKEN AND YOUR LRECL=256          */
*/* YOU WILL GET SOME DIRECTORY BLOCKS IN A MEMBER. SIMPLY           */
*/* DISCARD THESE.                                                   */
*/*                                                                  */
*/*                                                                  */
*/* IF YOU WANT 3350 CODE THEN SET  SYSTEM VARIABLE TO 1             */
*/* DEFAULT IS 3380 AS WE DON'T HAVE ANY 3350'S                      */
*/*                                                                  */
*/* IF YOU HAVE OTHER DEVICES E.G 3330 / 3375 THEN YOU WILL NEED TO  */
*/* ALTER THE EXCP CODE FOR MAX TRACK AND CYL NUMBERS BUT IT IS VERY */
*/* EASY IF YOU KNOW THE DEVICE CHARACTERISTICS.                     */
*/* AS A CHANNEL PROGRAM IS USED TO READ THE DATA FROM THE DEVICE    */
*/* THIS PROGRAM UNFORTUNATELY HAS TO BE DEVICE DEPENDANT (ACTUAL    */
*/* CC-HH-R ADDRESSES USED). YOU COULD ASSEMBLE DIFFERENT VERSIONS   */
*/* FOR EACH DEVICE TYPE.                                            */
*/*                                                                  */
*/*                                                                  */
*/* CHANGED FIXED LENGTH 80 BYTE RECS TO ANY SIZE FIXED REC LENGTH   */
*/* RECS. (IF YOU USE OTHER THAN 80 THEN YOU WILL NEED TO USE        */
*/* SOME PROGRAM TO RE-CREATE THE PDS AS IEBUPDTE ONLY HANDLES       */
*/* 80 BYTE RECORDS. PROGRAM PLIFIX ON THE TAPE IS A TYPICAL ONE YOU */
*/* CAN USE.
*/* I CHECK A PARMFIELD WHICH IS A 1-BYTE FULL WORD BINARY NUMBER    */
*/* IF NOT SUPPLIED THEN THE DEFAULT RECORD SIZE IS 80               */
*/* SPECIFY THE BLOCKSIZE OF THE OUTPUT DATASET AT RUN TIME          */
*/*                                                                  */
*/*                                                                  */
*/* IF YOU HAVE 3330'S/3375'S THE CODE CAN BE CHANGED IN THE         */
*/* SAME MANNER. I SUPPOSE A MOD COULD BE MADE TO ISSUE A DEVTYPE    */
*/* MACRO AND DYNAMICALLY GENERATE THE CORRECT CODE.                 */
*/* (OR ADD IT AS ANOTHER VARAIBLE TO THE SPF PANEL)                 */
*/*                                                                  */
*/*                                                                  */
*/* IF THE DIRECTORY HAS BEEN COMPLETELY OVERLAYED WITH MEMBER DATA  */
*/* (E.G YOU OPENED THE DATASET AS OUTPUT WITH A DSORG OF PS AND     */
*/* THEN UPDATED IT) THE FIRST FEW MEMBERS MAY CONTAIN GARBAGE.      */
*/*                                                                  */
*/* REPAIRED ABEND S400 FOR XA (DEBS ARE MORE CAREFULLY PROTECTED)   */
*/*                (STILL WORKS ON MVS EVEN WITH CHANGE)             */
*/*                                                                  */
*/* CHANGED MEMBER NAMING TO GENERATE MEM#NNNN (UP TO 9999)          */
*/*                                                                  */
*/* ORIGINAL AUTHOR ANON BUT I HAVE FOUND THIS PROGRAM USEFUL        */
*/*          THANKS !  (IF THE ORIGINAL AUTHOR IS STILL AROUND       */
*/*                     PLEASE CALL ME).                             */
*/*                                                                  */
*/*                                                                  */
*/*  MIKE HAWTHORNE  TTI CITICORP                                    */
*/*                  3100 OCEAN PK BLVD                              */
*/*                  SANTA MONICA CA                                 */
*/*                                                                  */
*/*                                                                  */
*/*  THIS VERSION IS USED FOR ANY FIXED LENGTH RECORD DATA SET       */
*/*  (UP TO 32760)                                                   */
*/*                                                                  */
*/*                                                                  */
*/********************************************************************/
         MACRO  ,
&NAME    EQUATE ,
         LCLC  &SYMBOL
         GBLA  &SYSTEM
&NAME    EQU   &SYSLIST(&SYSTEM)      SYSTEM=&SYSTEM
         MEND
         SPACE 1
*******************************************************************
* NOTE: SET SYMBOL &SYSTEM TO 1 FOR 3350 DEVICES
*                          TO 2 FOR 3380 DEVICES
*******************************************************************
         GBLA   &SYSTEM
&SYSTEM  SETA   2            DEFAULT TO 3380 DEVICES
*
*******************************************************************
SALVAGE  CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
READDATA EQU   6                   COMMAND CODE FOR READ DATA
SERCHIDE EQU   X'31'               COMMAND CODE FOR SEARCH ID EQUAL
TIC      EQU   8                   COMMAND CODE FOR TRANSFER-IN-CHANNEL
CC       EQU   X'40'               COMMAND CHAINING
SLI      EQU   X'20'               SUPPRESS LENGTH INDICATION
         EJECT
         SAVE  (14,12),,(JAR-3350-EXCP-SKELETON)
         LR    R12,R15
         USING SALVAGE,R12         CSECT ADDRESSABILITY
         ST    R13,SAVEAREA+4
         LA    R2,SAVEAREA
         ST    R2,8(,R13)
         LR    R13,R2
         LR    R7,R1               PARM DATA FROM USER
         L     R7,0(R7)            GET DATA
         MVC   PARML,0(R7)
         CLC   PARML,=X'0000'
         BNE   GOTPARM
         MVC   WORD(4),=F'80'      DEFAULT 80 BYTE RECORDS
         B     GOTOIT
GOTPARM  MVC   WORD(4),2(R7)       GET LENGTH FROM USER
GOTOIT   DS    0H
         AIF   (&SYSTEM EQ 2).$1
         OPEN  (DATA3350,,QSAMDCB,(OUTPUT))
         L      R2,DATA3350+44     DEB ADDRESS
         AGO   .$2
.$1      ANOP
         OPEN  (DATA3380,,QSAMDCB,(OUTPUT))
         L     R2,DATA3380+44      DEB ADDRESS
.$2      ANOP
         LA    R3,16(,R2)          A(DEB-EXTENSION) LESS 16   *JMH  XA
NEWXTNT  LA    R3,16(,R3)
         SLR   R15,R15
         IC    R15,IOBEXTNT        # OF EXTENTS THET I'M FINISHED WITH
         LA    R15,1(,R15)         BUMP BY 1
         STC   R15,IOBEXTNT        AND STORE BACK IN THE IOB
         EX    R15,CMPRINST        COMPARE TO # OF EXTENTS FROM DEB
         BNH   ENDOFILE            IF HIGH, THIS IS EODAD
         MVC   IOBCYL(4),6(R3)     MOVE CC-HH INTO IOB
RECORD0  MVI   IOBREC,X'00'        REC#0 (UPDATED BEFORE READ)
READLOOP SLR   R15,R15
         IC    R15,IOBREC          BUMP
         LA    R15,1(,R15)         TO NEXT
         STC   R15,IOBREC          RECORD
         XC    ECB(4),ECB          CLEAR THE ECB
         EXCP  IOB                 ISSUE THE EXCP
         WAIT  1,ECB=ECB                AND WAIT FOR COMPLETION
         CLI   ECB,X'7F'           NORMAL COMPLETION?
         BE    GETLENTH                 YES - PROCESS
IOERROR  EQU   *              I/O ERROR - WHAT KIND?
         CLI   ECB,X'42'           EXTENT VIOLATION?
         BE    NEWXTNT                  YES - GO SET UP.
         CLI   ECB,X'41'           PERMANANT I/O ERROR?
         BE    HANDLE                   YES - SEE WHAT CAUSED IT.
S001     ABEND 1,DUMP,,SYSTEM,DUMPOPT=SNAPOPT     SYSTEM 001 ABEND
HANDLE   TM    IOB+12,X'01'        UNIT EXCEPTION?
         BO    ENDOFMEM                 YES - END-OF-MEMBER
         TM    IOBSENSE+1,X'08'    NO RECORD FOUND?
         BO    NEXTRACK                 YES - LOOK ON NEXT TRACK.
         TM    IOBCSW+5,X'40'      INCORRECT LENGTH?
         BNO   S001                     NO - ABEND.
GETLENTH DS    0H
         LH    R9,=H'32760'
         SLR   R0,R0
         ICM   R0,B'0011',IOBCSW+5
         SR    R9,R0
         SLR   R8,R8
*        D     R8,=F'80'
*        D     R8,=F'133'                FOR A 133 BYTE PDS
         D     R8,WORD                   GET BLOCKSIZE
         LTR   R8,R8
         BNZ   READLOOP
         LTR   R9,R9
         BZ    READLOOP
         LA    R8,BUF
WRITLOOP PUT   QSAMDCB,(8)
*        LA    R8,80(,R8)
*        LA    R8,133(,R8)         FOR A 133 BYTE FILE
         A     R8,WORD             INCREMENT BY LRECL
         BCT   R9,WRITLOOP
         B     READLOOP            AND GO READ THE FILE AGAIN
ENDOFMEM DS    0H
         MVC   TEMP,=X'F02020202120'              *JMH
         ED    TEMP(6),MEMNUMBR                   *JMH
         MVC   CTLCARD+29(4),TEMP+2               *JMH
         LA    R8,CTLCARD
         PUT   QSAMDCB,(R8)                       *JMH
         AP    MEMNUMBR,=P'1'                     *JMH
         B     READLOOP                           *JMH
NEXTRACK SLR   R15,R15
*/********************************************************************/
*/*                                                                  */
*/*  CHANGE CODE HERE TO MAX TRACKS YOUR DEVICE HAS PER CYLINDER.    */
*/*                                                                  */
*/********************************************************************/
         AIF   (&SYSTEM EQ 2).$2A
         CLI   IOBHEAD+1,X'1D'     AM I AT TRACK 30? (THIS IS A 3350).
         AGO   .$2B
.$2A     ANOP
         CLI   IOBHEAD+1,X'0E'     AM I AT TRACK 15? (THIS IS A 3380).
.$2B     ANOP
         BNL   NEXTCYL                  YES - BUMP CYLINDER.
         IC    R15,IOBHEAD+1            NO -
         LA    R15,1(,R15)              BUMP TRACK
         STC   R15,IOBHEAD+1                 AND STORE BACK.
         B     RECORD0
NEXTCYL  ICM   R15,3,IOBCYL
*/********************************************************************/
*/*                                                                  */
*/*  CHANGE CODE HERE TO MAX CYLS YOUR DEVICE HAS PER PACK.          */
*/*                                                                  */
*/********************************************************************/
         LA    R15,1(,R15)         BUMP CYLINDER
         AIF   (&SYSTEM EQ 2).$3
         CH    R15,=H'555'  3350     AM I AT EOP?  (END-OF-PACK).
         AGO   .$4
.$3      ANOP
         CH    R15,=H'885'  3380     AM I AT EOP?  (END-OF-PACK).
.$4      ANOP
         BH    NEWXTNT               YES - DON'T TRY ANYTHING FANCY
         STCM  R15,3,IOBCYL             AND STORE BACK
         MVI   IOBHEAD+1,X'00'
         B     RECORD0
         AIF   (&SYSTEM EQ 2).$5
ENDOFILE CLOSE (DATA3350,,QSAMDCB)
         AGO   .$6
.$5      ANOP
ENDOFILE CLOSE (DATA3380,,QSAMDCB)
.$6      ANOP
         L     R13,SAVEAREA+4
         RETURN     (14,12),T,RC=0
         AIF   (&SYSTEM EQ 2).$7
DATA3350 DCB   DDNAME=EXCPDD,DSORG=PS,MACRF=(E)
         AGO   .$8
.$7      ANOP
DATA3380 DCB   DDNAME=EXCPDD,DSORG=PS,MACRF=(E)
.$8      ANOP
QSAMDCB  DCB   DDNAME=QSAMDCB,DSORG=PS,MACRF=(PM),BUFNO=5
SAVEAREA DS    9D
IOB      DS    0CL40               I..O..B
         DC    X'4200'   FLAGS - COMMAND CHAINING & UNRELATED
IOBSENSE DC    X'0000'             SENSE BYTES
         DC    AL4(ECB)            ECB ADDRESS
IOBSFLAG DC    X'00'               SYSTEM FLAGS
IOBCSW   DC    XL7'00'             LAST 7 BYTES OF CSW
IOBSIOCD DC    X'00'               CONDITION-CODE FROM SIO OPERATION
IOBCPADR DC    AL3(CHANPROG)       ADDRESS OF CHANNEL PROGRAM
         DC    X'0'
*        DC    AL3(DATA3350)       DCB ADDRESS
         DC    AL3(DATA3380)       DCB ADDRESS
         DC    XL8'00'
IOBEXTNT DC    X'FF'               M    EXTENT
IOBBIN   DC    XL2'0000'           BB   BIN-BIN
IOBCYL   DC    XL2'0000'           CC   CYL-CYL
IOBHEAD  DC    XL2'0000'           HH   HEAD-HEAD
IOBREC   DC    X'00'               R    RECORD
         SPACE 3
ECB      DC    F'0'
CHANPROG DS    0D
         CCW   SERCHIDE,IOBCYL,CC,5     SEARCH-ID-EQUAL
         CCW   TIC,*-8,CC,0             TRANSFER-IN-CHANNEL
         CCW   READDATA,BUF,SLI,32760 READ BIG BLOCK INTO BUF (&SLI)
CMPRINST CLI   16(R2),*-*
SNAPOPT  SNAP  SDATA=(CB,ERR,TRT,DM,IO),PDATA=(PSW,REGS,SA),MF=L
DBL      DS    D
WORD     DC    F'0'
CTLCARD  DC    CL80'./   ADD   LIST=ALL,NAME=MEM#'       *JMH
PARML    DC    H'0'
MEMNUMBR DC    PL3'0'                                    *JMH
TEMP     DC    CL6' '
         LTORG
BUF      DS    128CL256    32768 BYTE BUFFER
         END   SALVAGE
