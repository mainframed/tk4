         TITLE 'DISPLAY CURRENT WRITE-TO-LOG QUEUE    '
****************************************************************
* THIS PROGRAM TRACES AND PRINTS THE CONTENTS OF THE
*     CURRENT WTL QUEUE AND PRINTS THE LAST N OPERATOR MESSAGES
*     WHICH ARE WAITING OUTPUT TO THE LOG DATA SETS.
*
* ENHANCEMENT BY MAC FRANKS - NOV. 1978.
*     AN OPTIONAL PARM IS PASSED ALLOWING A SEARCH ON THE
*     PARM INPUT.  IF NO PARM IS SUPPLIED THE DEFAULT DISPLAY
*     IS PRODUCED.
****************************************************************
         PRINT NOGEN
SCAN     CSECT
         USING *,R12
         SAVE  (14,12),,*               SAVE REGS
         LR    R12,R15                  SET BASE
         ST    R13,SAVEAREA+4           BACKWARD SAVE CHAIN
         LA    R11,SAVEAREA             ADDR OF OUR SAVE
         ST    R11,8(,R13)              FORWARD  SAVE CHAIN
         LR    R13,R11                  SET R13
         L     R1,0(R1)                 GET ADDR OF PARM INFO
         LH    R4,0(R1)                 GET LEN OF EVERYTHING
         CH    R4,=H'9'                 4+4 =/XXYYSCAN/
         BL    GETSTOR                  NO, ASSUME ALL WTLS
         AH    R1,2(R1)                 OFFSET TO PARM INFO
         LA    R1,4(R1)                 COUNTING LENGTH OF THE PARM
         SH    R4,=H'9'                 GET PARM LENGTH
         CH    R4,=H'16'                PARM GT 16 BYTES
         BL    STORIT                   NO STORIT
         LH    R4,=H'16'                YES, MAX IS 16 BYTES
STORIT   STH   R4,PARMLEN               GET PARM LENGTH
         LTR   R4,R4                    PARM LEN ZEROS
         BZ    GETSTOR                  YES  -  BYPASS MOVE
         SH    R4,=H'1'                 PARM LEN - 1 FOR MVC
         EX    R4,MOVIT                 MVC  JOBNM,2(R1)
         TR    JOBNM(16),TRTABLE        TRANSLATE MSG TO UPPER CS
GETSTOR  GETMAIN R,LV=WKLEN             GET WRK AREA FOR 125 WTLS
         ST    R1,VIRTSTOR              SAVE GETMAIN AREA ADDR
         LR    R10,R1                   REG 10 FOR BASE
         USING MSGAREA,R10              ADDRESS MSG AREA
AGAIN    L     R10,VIRTSTOR             REESTABLISH GETMAIN ADDR
         CALL  CLEAR                    CLEAR SCREEN
         TPUT  MSG,L'MSG                MESSAGE TO USER
START    L     R2,16                    CVT
         L     R2,X'94'(R2)             MSRDA
         L     R3,X'1C'(R2)   SCAN CONTROL AREA
         MODESET KEY=ZERO
         LR    R2,R10         SAVE ADDR(MSGAREA) FOR END OF SCAN
         ICM   R9,7,5(R3)     ADDR(FIRST MESSAGE)
         BZ    EXIT           NONE TO DISPLAY
LOOP     LH    R1,8(,R9)      GET LENGTH OF MESSAGE
         S     R1,=F'8'       DECREMENT FOR LEN FIELD AND MCS BYTES
         STH   R1,MSGLEN      STORE IN MSGAREA
         MVC   MSGTEXT(0),16(R9) MOVE MESSAGE TEXT TO WORKAREA
         EX    R1,*-6
         ICM   R9,7,1(R9)     GO TO NEXT BUFF
         BZ    PUTIT          END OF CHAIN - PUT IT OUT
         LA    R10,132(,R10)  BUMP MSGAREA POINTER
         B     LOOP           AND PROCESS NEXT MESSAGE
         SPACE 2
PUTIT    MODESET KEY=NZERO    BACK TO PP STATE
PUTIT2   LH    R0,MSGLEN      LENGTH OF WTL
         LA    R1,MSGTEXT     MESSAGE TEXT
         LH    R4,PARMLEN     RESTORE PARM ADDR
         LTR   R4,R4          TEST ADDR FOR ZEROS
         BZ    TSOMAC         IF ZEROS PUT MSG
         S     R4,=F'1'       MINUS ONE FOR EXEC
         LA    R5,MSGTEXT     STARTING ADDR FOR SCAN
         LR    R7,R5          ENDING ADDR(LIMIT) FOR SCAN
         AR    R7,R0          PLUS LENGTH OF MESSAGE
         LA    R6,1           INCREMENT REG FOR SCAN
TESTIT   EX    R4,COMPIT      CLC 0(0,R5),JOBNM
         BE    TSOMAC         EQUAL, FOUND
         BXLE  R5,R6,TESTIT   BRANCH LOOP
         B     BACKUP         NOT FOUND
TSOMAC   TPUT  (R1),(R0),R    TO USER
BACKUP   S     R10,=F'132'    BACK UP TO NEXT MESSAGE
         CR    R10,R2         ANY MORE ?
         BNL   PUTIT2         YES
EXIT     MODESET KEY=NZERO    BACK TO PP STATE
         TPUT  SPACES,L'SPACES
         TPUT  CONTMSG,L'CONTMSG
         TGET  NEWNM,L'NEWNM            GET NEW STRING
         TR    NEWNM(16),TRTABLE        TRANSLATE MSG TO UPPER CS
         CLC   NEWNM,SPACES             SAME AS BEFORE
         BE    AGAIN                    YES, CONTINUE
         CLC   NEWNM(4),=C'END '        END OF SCAN-END
         BE    EXITL                    YES, EXIT
         CLC   NEWNM(4),=C'ALL '        SCAN WHOLE LOG-ALL
         BNE   MOVNM                    NO, CONTINUE
         MVC   JOBNM,SPACES             SPACE SCAN STRING
         LA    R9,0                     AND ZERO
         STH   R9,PARMLEN                       PARM LENGTH
         B     AGAIN
MOVNM    MVC   JOBNM,NEWNM              MOVE NEW STRING TO JOBNM
SETLEN   LA    R8,JOBNM+16              SET ADDR FOR SCAN
         LA    R9,15                    SET COUNT FOR BCT
SCANJOB  S     R8,=F'1'                 MINUS ONE FOR LOOP
         CLI   0(R8),C' '               IS IT A SPACE
         BNE   STORLEN                  NO, END OF STRING
         MVI   0(R8),C' '               MOVE A SPACE
         BCT   R9,SCANJOB               YES, CONTINUE
STORLEN  LA    R9,1(R9)                 TRUE LEN RELATIVE TO ONE
         STH   R9,PARMLEN               SAVE AT PARMLEN
         B     AGAIN                    SNAPSHOT CONSOLE AND CONTINUE
EXITL    FREEMAIN R,LV=WKLEN,A=(R2)
         SPACE 2
         L     R13,4(,R13)
         RETURN (14,12),T,RC=0
         SPACE 4
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
SAVEAREA DC    18F'0'
VIRTSTOR DC    F'0'
PARMLEN  DC    H'0'
TRTABLE  EQU   *
         DC    XL16'000102030405060708090A0B0C0D0E0F'
         DC    XL16'101112131415161718191A1B1C1D1E1F'
         DC    XL16'202122232425262728292A2B2C2D2E2F'
         DC    XL16'303132333435363738393A3B3C3D3E3F'
         DC    XL16'404142434445464748494A4B4C4D4E4F'
         DC    XL16'505152535455565758595A5B5C5D5E5F'
         DC    XL16'606162636465666768696A6B6C6D6E6F'
         DC    XL16'707172737475767778797A7B7C7D7E7F'
         DC    XL16'80C1C2C3C4C5C6C7C8C98A8B8C8D8E8F'
         DC    XL16'90D1D2D3D4D5D6D7D8D99A9B9C9D9E9F'
         DC    XL16'A0A1E2E3E4E5E6E7E8E9AAABACADAEAF'
         DC    XL16'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    XL16'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    XL16'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    XL16'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    XL16'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
CONTMSG  DC    CL40' HIT ENTER, NEW STRING, "ALL" OR "END"'
MSG      DS    0CL35
         DC    CL19' SCANNING FOR ===> '
JOBNM    DC    CL16' '
NEWNM    DC    CL16' '
SPACES   DC    CL16' '
MOVIT    MVC   JOBNM,0(R1)
COMPIT   CLC   0(0,R5),JOBNM
MSGAREA  DSECT
         DS    0F
MSGLEN   DS    H
MSGTEXT  DS    CL130
MSGALEN  EQU   *-MSGAREA
WKLEN    EQU   (125*MSGALEN)
         END
