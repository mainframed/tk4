***********************************************************************
*                                                                     *
*   THIS PROGRAM DELETES DUPLICATED PDS MEMBERS FROM TWO DIFFERANT    *
*   DATA SETS AND REPORTS WHICH MEMBERS HAVE BEEN DELETED AND WHICH   *
*   MEMBERS HAVE BEEN RETAINED.                                       *
*                                                                     *
*                                                                     *
* INPUT: A PDS WHICH IS TO BE COMPARED.              DDNAME=SYSUT1    *
*        A PDS THAT IS TO HAVE THE MEMBERS DELETED.  DDNAME=SYSUT2    *
*                                                                     *
* OUTPUT: A SYSOUT DATA SET FOR THE REPORT.          DDNAME=SYSPRINT  *
*                                                                     *
***********************************************************************
DELETER  ENTER REGEQUS=YES            SAVE REGISTERS, ETC.
         OPEN  (SYSUT2,(OUTPUT))      OPEN THE TARGET PDS
         OPEN  (SYSPRINT,(OUTPUT))    OPEN REPORT FILE
         LA    R4,SYSUT1              GET ADDRESS OF DCB
         BAL   R14,DIRLIST            DO THE REAL WORK
         CLOSE (SYSUT2)               CLOSE TARGET PDS
         OPEN  (SYSUT2A,(INPUT))      OPEN SYSUT2 FOR INPUT
         MVI   STOWSW,NOSTOW          REPORT THE REMAINING MODULES
         MVC   MSG,KEEPLINE           MOVE IN PROPER MSG
         LA    R4,SYSUT2A             GET ADDRESS OF DCB
         BAL   R14,DIRLIST            READ THE DIRECTORY
         CLOSE (SYSPRINT)             CLOSE THE REPORT FILE
         LEAVE                        AND GO
*
*      MAIN ROUTINE THAT READS THE DIRECTORY OF THE INPUT PDS
*
DIRLIST  DS    0H
         STM   R0,R15,SAVEALL         SAVE ALL REGISTERS
         OPEN  ((R4),(INPUT))         OPEN DIRECTORY OF SOURCE PDS
DIRLIST1 DS    0H
         GET   (R4),BUFFER            GET A DIRECTORY BLOCK
         CLC   BUFFER,=H'12'          SEE IF BLOCK IS UNUSED
         BL    CLOSE                  YES....GO
         LA    R9,BUFFER+2            LOAD POINTER TO FIRST DATAY
         SLR   R7,R7                  ZERO REGISTER 7 FOR INSERT
         ICM   R7,3,BUFFER            INSERT LENGTH USED OF THIS BLOC K
         LA    R7,BUFFER-1(R7)        LOAD LAST USED BYTE OF BLOCK
UNBLOCK  DS    0H
         CLC   EOF,0(R9)              LAST ENTRY ?
         BE    CLOSE                  YEP, -->
         MVC   MEMBER,0(R9)           PUT NAME IN TABLE
         CLI   STOWSW,NOSTOW          IS IT REPORT ONLY?
         BE    REPORT                 YEP, -->
         STOW  SYSUT2,MEMBER,D        DELETE MEMBER
         LTR   R15,R15                WAS THE MEMBER OUT THERE?
         BNZ   NOREPORT               NOPE, -->
REPORT   DS    0H
         PUT   SYSPRINT,MSGLINE       PRINT REPORT LINE
         MVI   CC,C' '                SET CARRIAGE CONTROL
         L     R15,LINECNT            GET LINE COUNT
         LA    R15,1(R15)             ADD ONE TO IT
         C     R15,MAXLINES           IS IT A FULL PAGE
         BNH   SAVECNT                NOPE, -->
         MVI   CC,C'1'                SET TOP OF PAGE
         LA    R15,0                  SET LINE COUNT TO ZERO
SAVECNT  DS    0H
         ST    R15,LINECNT            SAVE LINE COUNT
NOREPORT DS    0H
         IC    R6,11(R9)              INSERT CFIELD INTO REGISTER 6
         SLL   R6,27                  SHIFT OUT HIGH ORDER BITS
         SRL   R6,26                  SHIFT RIGHT TO GET LENGTH OF DI
         LA    R6,12(R6)              ADD STANDARD ENTRY LENGTH
NEXTNTRY DS    0H
         BXLE  R9,R6,UNBLOCK          INCREMENT REGISTER 9 WITH
         B     DIRLIST1               NEXT READ...
CLOSE    CLOSE ((R4))                 CLOSE INPUT FILE
         LM    R0,R15,SAVEALL         RESTORE ALL REGISTERS
         BR    R14                    RETURN TO CALLER
*
*
         EJECT
*
LINECNT  DC    F'0'
MAXLINES DC    F'55'
SAVEALL  DC    16F'0'
EOF      DC    XL8'FFFFFFFFFFFFFFFF'
STOWSW   DC    C' '
NOSTOW   EQU   C'X'
KEEPLINE DC    C' HAS BEEN RETAINED.             '
*
MSGLINE  DS    0H
CC       DC    C'1'
         DC    CL2' '
MEMBER   DC    CL8' '
MSG      DC    C' HAS BEEN DELETED SUCCESSFULLY. '
LREP     EQU   *-MSGLINE
         DC    CL(133-LREP)' '
*
BUFFER   DC    256C' '
*
         EJECT
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GM,EODAD=CLOSE,            +
               RECFM=U,LRECL=256,BLKSIZE=256
*
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PO,MACRF=W,EODAD=CLOSE
*
SYSUT2A  DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=GM,EODAD=CLOSE,            +
               RECFM=U,LRECL=256,BLKSIZE=256
*
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      X
               LRECL=133,BLKSIZE=2600,RECFM=FBA
         END
