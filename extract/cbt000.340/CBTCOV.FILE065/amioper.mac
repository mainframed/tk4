***********************************************************************
*
*      THIS PROGRAM DETERMINES WHETHER OR NOT THE LOGONID OF THE
*      CURRENT JOB HAS OPERATOR AUTHORITY.
*
*      RETURN CODES:  0 - HAS OPER AUTHORITY
*                     4 - NO OPER AUTHORITY
*                     8 - COULDN'T LOCATE THE ACF2 CVT
*                    12 - COULDN'T LOCATE THE ACF2 ACUCB
*
***********************************************************************
AMIOPER  CSECT                         DEFINE CSECT
*
*     REGISTER EQUATES AND USAGE
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                      BASE REG
R13      EQU   13                      WORKAREA BASE REG AND SAVE AREA
R14      EQU   14                      RETURN ADDR
R15      EQU   15                      RETURN CODE
*
*     HOUSEKEEPING
*
         STM   R14,R12,12(R13)         SAVE REGISTERS
         LR    R12,R15                 SET REG 12 TO ENTRY ADDR
         USING AMIOPER,R12             ESTABLISH ADDRESSABILITY
         LR    R10,R1                  SAVE R1
         LR    R2,R13                  SAVE ADDR OF CALLING SAVE AREA
         GETMAIN RU,LV=WORKSIZE        GET SOME CORE
         LR    R13,R1                  SAVE ADDR OF CORE OBTAINED
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
         ST    R2,SAVEAREA+4           STORE ADDR OF CALLING SAVE AREA
         ST    R13,8(R2)               STORE ADDR OF MY SAVE AREA
*
*     LOCATE THE USER'S LOGONID RECORD
*
         USING PSA,R0                  ADDRESSABILITY FOR PSA
         ACFINCVT R6,NONE=ERROR1       GET THE ADDR OF THE ACF2 CVT
         USING ACCVT,R6                ADDRESSABILITY FOR ACCVT
         ACFGUCB R4,NONE=ERROR2        GET THE ADDR OF THE ACF2 UCB
         USING ACUCB,R4                ADDRESSABILITY FOR ACUCB
         L     R5,ACULRECP             LOAD ADDR OF LOGONID RECORD
         USING LIDREC,R5               ADDRESSABILITY FOR THE LOGONID
*
*     NOW SEE IF HE HAS OPER
*
         TM    LIDTFLG3,LIDT3OPR       TEST OPER BIT
         BO    HASOPER                 HE'S OK
         LA    R15,4                   SET RETURN CODE
         B     RETURN
HASOPER  LA    R15,0                   SET RETURN CODE
         B     RETURN
*
*     ERROR CONDITIONS
*
ERROR1   LA    R15,8
         B     RETURN
ERROR2   LA    R15,12
         B     RETURN
*
*     TERMINATE
*
RETURN   EQU   *
         LR    R1,R13                  SAVE PTR TO GETMAINED SAVE AREA
         LR    R2,R15                  SAVE RETURN CODE
         L     R13,SAVEAREA+4          LOAD ADDR OF CALLING SAVE AREA
         FREEMAIN R,LV=WORKSIZE,A=(R1) FREE THE CORE WE GOT
         LR    R15,R2                  PUT RETURN CODE IN R15
         L     R14,12(R13)             RESTORE R14
         LM    R0,R12,20(R13)          RESTORE R0 THROUGH R12
         BR    R14                     RETURN
*
*     DSECTS
*
WORKAREA DSECT
SAVEAREA DS    18F
WORKSIZE EQU   *-WORKAREA
         IHAPSA                        MAP THE PSA
         IHAASCB                       MAP THE ASCB
         ACCVT                         THE ACF2 CVT
         ACFASVT                       MAP THE ACF2 ASVT
         ACUCB                         MAP THE ACF2 UCB
         LIDREC                        LOGONID RECORD DSECT
         END   AMIOPER
