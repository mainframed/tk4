VOLS     TITLE  'TSO COMMAND TO LIST ATTRIBUTES OF ONLINE VOLUMES'
         LCLB   &DFDS                                       *HMD 06/83*
&DFDS    SETB   1             DF/DS MACROS ARE AVAILABLE    *HMD 06/83*
*                                                           *SRH 07/84*
*    FIXED :                                                *SRH 07/84*
*       1) ERROR RETURN WHEN LSPACE R15 NOT ZERO.           *SRH 07/84*
*       2) UCB LOOKUP - EQU CVTILK2 UNDER XA - NOT IN MACRO *SRH 07/84*
*                                                           *SRH 07/84*
*                                                           *SRH 07/84*
USPACE   CSECT
         TSOENTER REGS=YES,PL=(CPPL,IO,PARSE)               *HMD 06/83*
         TM    0(R2),X'80'              CALLED AS PROGRAM?  *HMD 06/83*
         BO    END                      THEN GET OUT FAST   *HMD 06/83*
         XC    TSOECB,TSOECB            CLEAR THE ECB       *HMD 06/83*
         XC    FLAGS,FLAGS              CLEAR FLAG BYTES    *HMD 06/83*
         MVC   PPLPCL,=V(PARSEPCL)      MOVE THE PCL ADDR   *HMD 06/83*
         SPACE 1                                            *HMD 06/83*
         CALLTSSR EP=IKJPARS,MF=(E,PPL)                     *HMD 06/83*
         SPACE 1                                            *HMD 06/83*
         LTR   R15,R15                  CHECK THE RETURN    *HMD 06/83*
         BZ    PARSOK                   YES, WAS OK         *HMD 06/83*
         XC    TSOECB,TSOECB            CLEAR TSO ECB       *HMD 06/83*
         LA    R1,PARSER                GET ERROR MESSAGE   *HMD 06/83*
         ST    R1,OLDADDR               SAVE IN O.L.D.      *HMD 06/83*
P1       PUTLINE PARM=PLPB,MF=(E,IOPL),                     *HMD 06/83*X
               OUTPUT=(OLD,TERM,SINGLE,INFOR)               *HMD 06/83*
         LA    R2,8                      RC=8 PARSE ERROR   *HMD 06/83*
         B     END
PARSOK   DS    0H                        PARSE WAS OK       *HMD 06/83*
         MVI   INPUT,C' '                CLEAR INPUT AREA   *HMD 06/83*
         MVC   INPUT+1(L'INPUT-1),INPUT                     *HMD 06/83*
         XC    LEN,LEN                   CLEAR LENGTH       *HMD 06/83*
         L     R2,PPLRANS                LOAD ANSWER PLACE  *HMD 06/83*
         LUSE  IKJPARMD,R2               ADDRESS THE AREA   *HMD 06/83*
         LA    R2,VOL                    POINT TO PARM      *HMD 06/83*
         LDROP R2                                           *HMD 06/83*
         TM    6(R2),X'80'               PARM PRESENT?      *HMD 06/83*
         BZ    BEGIN                     NO, START PROCESS  *HMD 06/83*
         SPACE 1                                            *HMD 06/83*
         LH    R3,4(,R2)                 GET LENGTH         *HMD 06/83*
         STH   R3,LEN                    LENGTH OF PARM     *HMD 06/83*
         L     R2,0(,R2)                 GET PARM TEXT      *HMD 06/83*
         BCTR  R3,R0                     BUMP DOWN FOR EX   *HMD 06/83*
         EX    R3,HAVEMVC                MOVE PARM          *HMD 06/83*
         B     BEGIN                                        *HMD 06/83*
         TITLE  'INPUT / OUTPUT AND MISC COSTANTS'
HAVEMVC  MVC   INPUT(0),0(R2)           <<EXECUTED>>
SP3      OI    FLAGS,FLAGSP3            <<EXECUTED>>        *HMD 06/83*
ALL      OI    FLAGS,FLAGALL            <<EXECUTED>>        *HMD 06/83*
FIND     OI    FLAGS,FLAGFND            <<EXECUTED>>        *HMD 06/83*
ERROR    OI    FLAGS,FLAGERR            <<EXECUTED>>        *HMD 06/83*
FLAGALL  EQU   X'80'
FLAGFND  EQU   X'40'
FLAGSP3  EQU   X'20'             WE ARE RUNNING MVS/SP 1.3  *HMD 06/83*
FLAGNF   EQU   X'10'             FIRST TIME SWITCH          *HMD 06/83*
FLAGERR  EQU   X'08'             ERROR IN LSPACE SVC OR CVAF*HMD 06/83*
INDEXED  EQU   X'01'             WE HAVE AN INDEXED VTOC    *HMD 06/83*
SEARCH   EQU   1076              UCB SEARCH OFFSET IN CVT   *HMD 06/83*
DOUBLE   DC    D'0'
LEN      DC    H'0'
FLAGS    DC    H'0'
PLPB     PUTLINE MF=L            PUTLINE LIST FORM          *HMD 06/83*
SPACE    DC    C'SPACE='
FREECYL  DC    C'CCCC,'
FREETRK  DC    C'TTTT,'
EXTENT   DC    C'AAAA/'
CNTGCYL  DC    C'CCCC,'
CNTGTRK  DC    C'TTTT'
INPUT    DC    CL12' '                                      *HMD 06/79*
LINE     DC    H'83,0'                                      *HMD 06/79*
LINETX   DC    CL79' '                                      *HMD 06/79*
TITLE    DC    H'83,0'                                      *HMD 06/79*
TITLETX  DC    CL79' '                                      *HMD 06/79*
         ORG   TITLETX
         DC    C'ADDR  '
         DC    C'VOLUME  '
         DC    C'FREE CYLS,TRKS  '
         DC    C'EXTENTS    '
         DC    C'MAX CYLS,TRKS  USECNT'
         DC    C' STATUS   MOUNT'
         ORG   LINETX
ADDR     DC    C'ADDR  '
VOLUME   DC    C'VOLUME       '
CYLS     DC    C'CCCC,TTTT    '
EXTENTS  DC    C'AAAA         '
MAX      DC    C'CCCC,TTTT    '
USECNT   DC    CL5' '
STATUS   DC    C' '
         DC    CL6' '
         DC    CL3' '
MOUNT    DC    CL3' '
         ORG   TITLETX+79
EOTBL    DC    F'65535'            END OF UCB TABLE         *HMD 04/80*
LSPRC    DC    F'0'                LSPACE RETURN CODE SAVE  *HMD 06/83*
OLD      DC    F'1'                                         *HMD 06/83*
OLDADDR  DC    A(0)                ADDRESS OF MESSAGE       *HMD 06/83*
LSPCERR  DC    CL9'LSPC ERR='      ERR MSG                  *SRH 07/84*
LSPCERC  DC    CL5' '                                       *SRH 07/84*
PARSER   DC    AL2(PARSLEN),AL2(0)
         DC    C'HMD546I PARSE ERROR - CANNOT INTERPRET OPERAND'
PARSLEN  EQU   *-PARSER                                     *HMD 06/83*
         SPACE 1                                            *HMD 06/83*
NMNT     DC    AL2(NMNTLEN),AL2(0)
         DC    C'HMD547I REQUESTED VOLUME(S) NOT MOUNTED'   *HMD 06/83*
NMNTLEN  EQU   *-NMNT                                       *HMD 06/83*
         SPACE 1                                            *HMD 06/83*
*  UCB LOOKUP WORK AREA AND PARM LIST VARIABLES             *HMD 05/83*
*  FOR DF/DS SUPPORT ONLY                                   *HMD 06/83*
UWORK    DC   XL100'00' WORK AREA, MUST BE ZEROED           *HMD 05/83*
DEVCLASS DC   X'20'     DEVICE CLASS TO SEARCH FOR (DASD)   *HMD 05/83*
ADDRUCB  DC   F'0'      UCB ADDRESS (FULL WORD IN LENGTH)   *HMD 05/83*
         SPACE 1                                            *HMD 06/83*
         TITLE 'TSO COMMAND TO LIST ATTRIBUTES OF ONLINE VOLUMES'
BEGIN    DS    0H                                           *HMD 06/83*
         SP3CHECK  ,                    CHECK OPER SYSTEM   *HMD 06/83*
         BNE   BEGINX                                       *HMD 06/83*
         EX    R0,SP3                   WE ARE RUNNING SP1.3 OR HIGHER
BEGINX   TM    FLAGS,FLAGSP3            SP3 SYSTEM?         *HMD 06/83*
         BO    BEGINZ                   YES, NO LOOKUP TBL  *HMD 06/83*
         L     R8,CVTPTR                GET CVT ADDRESS
CVTILK2  EQU   CVT+X'28'                                    *SRH 07/84*
         L     R8,CVTILK2-CVT(,R8)
BEGINZ   DS    0H                                           *HMD 06/83*
         XR    R6,R6
         CLC   =CL6' ',INPUT            BLANK INPUT LINE    *HMD 06/83*
         BE    SETALL
         CLC   =C'ALL',INPUT            ALL VOLUMES WANTED  *HMD 06/83*
         BNE   LOOP                     NO, FIND SPECIFIC   *HMD 06/83*
         LH    R15,LEN                  GET LENGTH          *HMD 06/83*
         CH    R15,=H'3'                LENGTH OF THREE?    *HMD 06/83*
         BNE   LOOP                     NO SPECIFIC VOLUME  *HMD 06/83*
SETALL   DS    0H
         EX    R0,ALL                   SET ALL FLAG
         XC    LEN,LEN                  CLEAR LENGTH        *HMD 06/83*
LOOP     DS    0H                                           *HMD 06/83*
         NI    FLAGS,255-INDEXED        CLEAR INDEXED FLAG  *HMD 06/83*
         MVC   INPUT+7(2),=C'  '        CLEAR IX INDICATOR  *HMD 06/83*
         TM    FLAGS,FLAGSP3            IS THIS MVS/SP 1.3  *HMD 06/83*
         BZ    LOOPX                    NO, PRE-MVS/SP      *HMD 06/83*
         L     R15,CVTPTR               GET CVT ADDRESS     *HMD 06/83*
         L     R15,SEARCH(,R15)         GET SEARCH ADDRESS  *HMD 06/83*
         CALL  (15),(UWORK,DEVCLASS,ADDRUCB),VL             *HMD 06/83*
         L     R7,ADDRUCB               GET UCB ADDRESS     *HMD 06/83*
         LTR   R15,R15                  CHECK RETURN CODE   *HMD 06/83*
         BZ    PROC01                   ZERO - PROCESS      *HMD 06/83*
         B     DONE                     NON-ZERO, DONE      *HMD 06/83*
         SPACE 1                                            *HMD 06/83*
LOOPX    SLR   R7,R7                    CLEAR FOR ICM       *HMD 04/80*
         ICM   R7,3,0(R8)               INSERT UCB ADDRESS  *HMD 04/80*
         LA    R8,2(,R8)                POINT TO NEXT
         USING UCB,R7
         BZ    LOOP                     DUMMY UCB           *HMD 04/80*
         C     R7,EOTBL                 Q. END OF TABLE     *HMD 04/80*
         BE    DONE                     A. YES, DONE        *HMD 04/80*
**                                                          *HMD 06/83*
** PROCESS A UCB ADDRESS                                    *HMD 06/83*
**                                                          *HMD 06/83*
         CLI   UCBTBYT3,UCB3DACC        DIRECT ACCESS?
         BNE   LOOP                     NO
         CR    R7,R6                    TEST ADDR
         BNH   LOOP                     NO
         LR    R6,R7
PROC01   TM    SRTESTAT,SRTEONLI        ONLINE?             *HMD 06/83*
         BNO   LOOP                     NO
         CLI   SRTEVOLI,X'00'           ANYTHING MOUNTED?
         BE    LOOP                     NO
         TM    FLAGS,FLAGALL            ALL VOLUMES?
         BZ    CKVOL                    NO
DOVOL    MVI   FLAGS+1,X'FF'            HAVE A VOLUME
         LR    R0,R7                    UCB ADDRESS
         LA    R1,SPACE                 LOAD PLACE
         SVC   78                       ISSUE LSPACE
         ST    R15,LSPRC                SAVE RETURN CODE    *HMD 06/83*
         AIF   (NOT &DFDS).L010         NO DF/DS SUPPORT    *HMD 06/83*
         SPACE 2                                            *HMD 06/83*
***********************************************************************
**                                                                   **
**  TEST FOR INDEXED VTOC - R7 POINTS TO UCB                         **
**                                                                   **
***********************************************************************
         CVAFTST  UCB=(R7)              TEST  VTOC          *HMD 06/83*
         B     BRTBL(R15)               TEST  RETURN CODE   *HMD 06/83*
BRTBL    B     RC0                      DF/DS NOT ON SYSTEM *HMD 06/83*
         B     RC4                      VTOC NOT INDEXED    *HMD 06/83*
         B     RC8                      VTOC IS INDEXED     *HMD 06/83*
         B     RC12                     VIB NOT INITIALIZED *HMD 06/83*
         B     RC16                     NOT A DASD DEVICE   *HMD 06/83*
         SPACE 2                                            *HMD 06/83*
RC12     DS    0H                                           *HMD 06/83*
         ST    R15,LSPRC                INDICATE ERROR      *HMD 06/83*
         MVC   CYLS(30),=CL30'CVAF - VIB NOT INITIALIZED'   *HMD 06/83*
         B     DOLINE                   PUT OUT LINE        *HMD 06/83*
RC16     DS    0H                                           *HMD 06/83*
         ST    R15,LSPRC                INDICATE ERROR      *HMD 06/83*
         MVC   CYLS(30),=CL30'CVAF - I/O ERROR OR NOT DASD' *HMD 06/83*
         B     DOLINE                   PUT OUT LINE        *HMD 06/83*
         SPACE 2                                            *HMD 06/83*
RC8      MVC   INPUT+7(2),=C'IX'        INDICATE INDEXED    *HMD 06/83*
         OI    FLAGS,INDEXED            SET FLAG            *HMD 06/83*
RC4      DS    0H                                           *HMD 06/83*
RC0      DS    0H                                           *HMD 06/83*
         SPACE 2                                            *HMD 06/83*
DOLINE   DS    0H                                           *HMD 06/83*
.L010    ANOP                                               *HMD 06/83*
         MVI   LINETX,C' '              CLEAR LINE          *HMD 06/79*
         MVC   LINETX+1(78),LINETX      ALL THE WAY         *HMD 06/79*
         MVC   VOLUME(6),SRTEVOLI       MOVE IN VOLUME
         MVC   VOLUME+8(2),INPUT+7      MOVE INDEX INDICATOR*HMD 06/83*
         MVC   ADDR(3),UCBNAME          NAME
         L     R15,LSPRC                TEST LSPACE RETURN  *HMD 06/83*
         LTR   R15,R15                  TEST THE RETURN CODE*HMD 06/83*
         BZ    MOVESTAT                 OK
         CVD   R15,DOUBLE               MAKE DECIMAL        *SRH 07/84*
         UNPK  LSPCERC(3),DOUBLE+6(2)   MAKE PRINTABLE      *SRH 07/84*
         OI    LSPCERC+2,C'0'           MAKE PRINTABLE      *SRH 07/84*
         MVC   CYLS(L'LSPCERR),LSPCERR  MOVE ERR MSG        *SRH 07/84*
         MVC   CYLS+L'LSPCERR(L'LSPCERC),LSPCERC            *SRH 07/84*
         B     TPUTTST                  PUT IT OUT          *HMD 06/83*
MOVESTAT XR    R1,R1                    CLEAR REG
         TM    SRTESTAT,SRTEONLI
         BZ    XVARY
         MVI   STATUS,C'O' ONLINE
XVARY    TM    SRTESTAT,SRTECHGS
         BZ    XRESV
         MVI   STATUS+1,C'V' VARY PENDING
XRESV    TM    SRTESTAT,SRTERESV
         BZ    XUNLD
         MVI   STATUS+2,C'R' MARKED RESERVED
XUNLD    TM    SRTESTAT,SRTEUNLD
         BZ    XALLOC
         MVI   STATUS+3,C'U' UNLOAD PENDING?
XALLOC   TM    SRTESTAT,SRTEALOC
         BZ    XRES
         MVI   STATUS+4,C'A' ALLOCATED
XRES     TM    SRTESTAT,SRTEPRES
         BZ    XINDEX
         MVI   STATUS+5,C'P' PERM RESIDENT
XINDEX   TM    FLAGS,INDEXED             INDEXED VTOC       *HMD 06/83*
         BZ    YPRIV                                        *HMD 06/83*
         MVI   STATUS+6,C'I' INDEXED VTOC                   *HMD 06/83*
YPRIV    TM    SRTESTAB,SRTEBPRV
         BZ    YPUB
         MVC   MOUNT(3),=CL3'PRV' PRIVATE
YPUB     TM    SRTESTAB,SRTEBPUB
         BZ    YSTRG
         MVC   MOUNT(3),=CL3'PUB' MOUNTED PUBLIC
YSTRG    TM    SRTESTAB,SRTEBVQS
         BZ    YLAST
         MVC   MOUNT(3),=CL3'STG' MOUNTED STORAGE
YLAST    DS    0H
         IC    R1,SRTEUSER              GET USE COUNT
         CVD   R1,DOUBLE                CONVERT IT
         UNPK  USECNT(3),DOUBLE+6(2)
         OI    USECNT+2,C'0'
         MVC   CYLS(9),FREECYL
         MVC   EXTENTS(4),EXTENT
         MVC   MAX(9),CNTGCYL
TPUTTST  TM    FLAGS,FLAGNF            NOT FIRST TIME?      *HMD 06/83*
         BO    TPUTX                   YES, NO TITLE        *HMD 06/83*
         SPACE 1                                            *HMD 06/83*
TPUTTIT  DS    0H                      GET READY TO TPUT    *HMD 06/83*
         XC    TSOECB,TSOECB            CLEAR TSO ECB       *HMD 06/79*
         PUTLINE PARM=PLPB,MF=(E,IOPL),                     *HMD 06/79*X
               OUTPUT=(TITLE,TERM,SINGLE,DATA)              *HMD 06/79*
         OI    FLAGS,FLAGNF            NOT THE FIRST TIME   *HMD 06/83*
         SPACE 2                                            *HMD 06/83*
TPUTX    XC    TSOECB,TSOECB           CLEAR ECB            *HMD 06/79*
         PUTLINE PARM=PLPB,MF=(E,IOPL),                     *HMD 06/79*X
               OUTPUT=(LINE,TERM,SINGLE,DATA),              *HMD 06/79*
         B     LOOP
DONE     SLR   R2,R2                    CLEAR RETURN CODE   *HMD 06/83*
         CLI   FLAGS+1,X'FF'            ANYTHING FOUND
         BE    END                      YES, EXIT           *HMD 06/83*
         XC    TSOECB,TSOECB            CLEAR ECB           *HMD 06/79*
         LA    R1,NMNT                                      *HMD 06/83*
         ST    R1,OLDADDR                                   *HMD 06/83*
         PUTLINE PARM=PLPB,MF=(E,IOPL),                     *HMD 06/79*X
               OUTPUT=(OLD,TERM,SINGLE,INFOR)               *HMD 06/79*
         LA    R2,4                     RETURN CODE         *HMD 06/83*
         B     END                      RETURN CODE         *HMD 06/79*
CKVOL    LH    R15,LEN                  GET LENGTH
         LTR   R15,R15                  CHECK FOR ALL       *HMD 06/83*
         BZ    DOVOL                    YES, DO IT          *HMD 06/83*
         BCTR  R15,R0                   DOWN FOR EX         *HMD 06/83*
         EX    R15,VOLCLC
         BE    DOVOL
         B     LOOP
         SPACE 2                                            *HMD 06/83*
VOLCLC   CLC   INPUT(0),SRTEVOLI        <<EXECUTED>>        *HMD 06/83*
         SPACE 2                                            *HMD 06/83*
END      TM    FLAGS,FLAGERR            DID WE HAVE ERROR   *HMD 06/83*
         BZ    ENDX                     NO, RETURN          *HMD 06/83*
         LA    R2,8                     SET RETURN CODE     *HMD 06/83*
ENDX     TSOLEAVE ISN=LTORG,RETCODE=(2)  RETURN CODE IN R2  *HMD 06/83*
         TITLE 'USPACE - PARSE CONTROL BLOCKS'              *HMD 06/83*
PARSEPCL IKJPARM  DSECT=IKJPARMD                            *HMD 06/83*
         SPACE 2                                            *HMD 06/83*
VOL      IKJIDENT 'VOLUME PREFIX',UPPERCASE,MAXLNTH=6,FIRST=ALPHANUM,  X
               OTHER=ALPHANUM                               *HMD 06/83*
         SPACE 2                                            *HMD 06/83*
         IKJENDP
         TITLE 'USPACE - MAPPING DSECTS'
         CVT   DSECT=YES
         EJECT
UCB      DSECT
         IEFUCBOB
         PRINT GEN
         END
