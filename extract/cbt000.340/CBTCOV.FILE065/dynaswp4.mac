DYNASWAP TITLE 'PROGRAM TO MODIFY ELIGIBLE DEVICE TABLE'
***********************************************************************
*    CHANGE DATE: FEB  1992  MVS 4.2 VERSION ONLY (ALSO ESA)          *
*                                                                     *
*                                                                     *
*     ABSTRACT                                                        *
*                                                                     *
*     DYNASWAP PROVIDES THE ABILITY TO DYNAMICALLY SWAP TWO DEVICES   *
*     WITHIN THE EDT,  BOTH DEVICES MUST BE OFFLINE DASD DEVICES.     *
*     THIS PROVIDES A WAY TO REPLACE A BAD VOLUME WITH A RECOVERY     *
*     VOLUME FOR ALL RELATED ESOTARIC NAMES WITHIN THE EDT.           *
*     IT CAN ALSO BE USED TO ADD NEW DEVICES TO A SET OF RELATED      *
*     ESORTARIC NAMES BY SWAPPING A DEVICE FOR A NEW VOLUME FOR       *
*     A NOT CONNECTED DEVICE ADDRESS ADDED TO THESE ESORTARICS        *
*     FOR THIS PURPOSE.                                               *
*                                                                     *
*     DYNASWAP SUPPORTS THE FOLLOWING RELEASES:                       *
*                                                                     *
*     MVS/XA 2.2.0 - ESA 3.1.3                                        *
*                                                                     *
*     INQUIRES:                                                       *
*                                                                     *
*     SEND INQUIRES TO:                                               *
*              STEVEN D. SMITH   G12-99                               *
*              SECURITY PACIFIC AUTOMATION COMPANY                    *
*              611 NORTH BRAND                                        *
*              GLENDALE, CA, 91203                                    *
*                                                                     *
*     OR PHONE: (818) 507-3014                                        *
*                                                                     *
*                                                                     *
*     INSTALLATION                                                    *
*                                                                     *
*     DYNASWAP IS A SINGLE MODULE AND MUST BE LINK EDITED INTO        *
*     AN AUTHORIZED LIBRARY WITH ATTRIBUTES AC=1 RENT REUS REFR       *
*     ASSEMBLY USING ASMH AND XA MACRO LIBRARY                        *
*                                                                     *
*                                                                     *
*     A PARM VALUE MUST BE INCLUDED TO PROVIDE THE TWO DEVICE         *
*     ADDRESSES TO BE SWAPPED.                                        *
*                                                                     *
*      //DYNASWAP PROC A=NON,B=NON                                    *
*      //DYNASWAP EXEC PGM=DYNASWAP,PARM='&A,&B'                      *
*                                                                     *
*     RESTRICTIONS                                                    *
*                                                                     *
*     THE DEVICES MUST BE OFFLINE.                                    *
*     THE DEVICES MUST BE DASD                                        *
*     THE DEVICES MUST BE THE SAME DEVICE TYPE                        *
*                                                                     *
*     NOTES                                                           *
*                                                                     *
*     THERE ARE NO ENQUE INVOLVED OR NECESSARY AND THERE ARE NO       *
*     INTEGRITY PROBLEMS WITH ALLOCATION BECAUSE THE DEVICES ARE      *
*     OFFLINE WHEN THE SWAP IS DONE.  THE GROUP STRUCTURE OF THE      *
*     EDT IS NOT CHANGES AND THEREFOR DOES NOT AFFECT MSX (MIM)       *
*     OR STC 4400 HSC SOFTWARE                                        *
*                                                                     *
*     ALL MESSAGES ARE OF THE FORM TSDXXX WHICH CAN BE EDITTED        *
*     TO LOCAL MESSAGE ID STANDARDS                                   *
*                                                                     *
*    ATTRIBUTES - REENTRANT                                           *
*    TAB SETTINGS - 10 16 36 41 72                                    *
*                                                                     *
***********************************************************************
***********************************************************************
         SPACE 3                                               DM JUN83
         SPACE 3                                               DM JUN83
         PRINT OFF                                             DM JUN83
         MACRO ,                                               DM JUN83
         EDT   &TYPE=SP                                        DM NOV83
.* WRITTEN BY DOUG MERCER, JUNE 1983, FOR THE LOCKHEED-CALIFORNIA CO.
.* CHANGED BY DOUG MERCER, NOVEMBER 1983, FOR XA DIFFERENCES   DM NOV83
.* CHANGED BY STEVE SMITH, JANUARY  1984, FOR XA DIFFERENCES   SS JAN84
.* CHANGED BY STEVE SMITH, FEBRUARY 1988, FOR XA2.2            SS FEB88
EDT      DSECT ,                   ELIGIBLE DEVICES TABLE      DM JUN83
EDTNAME  DS    CL3                   EDT NAME                  SS FEB88
EDTLEVEL DS    C                     LEVEL                     SS FEB88
EDTCREAT DS    0CL8                EDT CREATION ID             SS FEB88
EDTCBNAM DS    CL6                 EDT CB NAME                 SS FEB88
EDTIDNUM DS    CL2                 EDT VER ID                  SS FEB88
EDTDATE  DS    CL8                   CREATION DATE (MM/DD/YY)  SS FEB88
EDTTIME  DS    CL5                     TIME (HH.MM)            SS FEB88
EDTFLAGS DS    XL1                 EDT FLAGS                  JAH JUL91
EDTIODF  EQU   B'10000000'         EDT BUILT AT NIP TIME      JAH JUL91
EDTGLINX EQU   B'01000000'         GROUP LOCKING INDEX        JAH JUL91
EDTDLOCK EQU   B'00100000'         LOCKING DEADLOCK           JAH JUL91
EDTSP    DS    XL1                 EDT SUBPOOL                JAH JUL91
EDTKEY   DS    XL1                 EDT STORAGE KEY            JAH JUL91
EDTLKUP  DS    A                   POINTER TO LOOK-UP SECTION  DM JUN83
EDTGNRC  DS    A                   POINTER TO GENERIC SECTION  DM JUN83
EDTGRP   DS    A                   POINTER TO GROUP SECTION    DM JUN83
EDTUCB   DS    A                   POINTER TO UCB SECTION      DM JUN83
EDTMSK   DS    A                   POINTER TO MASK SECTION     DM JUN83
EDTGRPT  DS    A                   POINTER TO GROUP POINTERS   DM JUN83
EDTPREF  DS    A                   POINTER TO PREFERENCE TABLE DM JUN83
EDTTAPE  DS    A                   POINTER TO TAPE DEVICE EL   SS FEB88
* THE FOLLOWING ADDED TO HEADER FOR VERSION 3 UPDATES         JAH JUL91
EDTLUBL  DS    CL4                 LENGTH OF LUVSECT          JAH JUL91
EDTGENL  DS    CL4                 LENGTH OF GENSECT          JAH JUL91
EDTGRPL  DS    CL4                 LENGTH OF GRPSECT          JAH JUL91
EDTUCBL  DS    CL4                 LENGTH OF EUCBSECT         JAH JUL91
EDTMSKTL DS    CL4                 LENGTH OF GRMSKTAB         JAH JUL91
EDTGRPPL DS    CL4                 LENGTH OF GRPPSECT         JAH JUL91
EDTPREFL DS    CL4                 LENGTH OF PREFTAB          JAH JUL91
EDTTAPEL DS    CL4                 LENGTH OF TAPETAB          JAH JUL91
EDTRESV1 DS    CL16                RESERVED                   JAH JUL91
EDTUPLP  DS    A                   POINTER TO UCB POINTER LISTJAH JUL91
EDTUPLL  DS    CL4                 LENGTH OF UPL              JAH JUL91
EDTGMCTP DS    A         POINTER TO GROUP MASK CONVERTION TABLEJAHJUL91
EDTGMCTL DS    CL4                 LENGTH OF GMCT             JAH JUL91
EDTRESV2 DS    CL8                 RESERVED                   JAH JUL91
* THE FOLLOWING ADDED TO HEADER FOR DYNAMSK2                   SS FEB88
EDTGENE  DS    A                   MVSCP GENERATED EDT ADDRESS SS FEB88
EDTSIZE  DS    A                   SUBPOOL AND SIZE OF EDT     SS FEB88
EDTHDRLX EQU   *-EDT               LENGTH OF THE HEADER        DM JUN83
         SPACE 2                                               DM JUN83
* EACH OF THE FOLLOWING SECTIONS IS PREFIXED BY                SS FEB88
*   A HEADER  - MOST ARE 12 BYTES LONG EXCEPT FOR LOOKUP HDR   SS FEB88
*   FIRST 8 BYTES ARE HEADER ID                                SS FEB88
*   NEXT  4 BYTES ARE THE NUMBER OF ENTRYS                     SS FEB88
*   NEXT  4 BYTES ARE FOR LOOKUP SECTION ONLY                  SS FEB88
*         2 BYTES ARE FOR LOOKUP INDEX TO FIRST ENTRY          SS FEB88
*         2 BYTES ARE RESERVED                                 SS FEB88
         SPACE ,                                               DM JUN83
LUVHDR   DSECT ,                   LOOK-UP SECTION            JAH JUL91
LUVHDRNM DS    CL8                 SECTION HEADER NAME        JAH JUL91
LUVENTNO DS    XL4                 NO ENTRIES THIS SECTION    JAH JUL91
LUVFIRST DS    XL2                 INDEX TO FIRST GENERIC/ESO JAH JUL91
LUVRSV1  DS    XL2                 RESERVED                   JAH JUL91
LUVHDRL  EQU   *-LUVHDR            HEADER LENGTH              JAH JUL91
         SPACE ,                                               DM JUN83
LKPSECT  DSECT ,                   LOOK-UP SECTION             DM JUN83
LKPNAME  DS    CL8                 UNITNAME                    DM JUN83
LKPDEVT  DS    XL4                 DEVICE TYPE (AS IN CATALOG) DM JUN83
LKPMSKPT DS    A                   INDEX TO GROUP MASK ENTRY  JAH JUL91
LKPALTPT DS    A                   ALTERNATE MASK INDEX        SS FEB88
LKPGNRC# DS    F                   NUMBER OF GENERICS          DM JUN83
LKPFLAGS DS    XL2                 FLAGS                       DM JUN83
LKPVIO   EQU   X'80'               ELIGIBLE FOR VIO            SS JAN84
LKPAGMSK EQU   X'40'               GENERIC HAS ALT MASK        SS JAN84
LKPMGEN  EQU   X'20'               ALLOC OF MUTL GEN OK        SS JAN84
LKPGENR  EQU   X'10'               THIS IS A GENERIC: FULL DEV SS JAN84
LKPESOTR EQU   X'08'               THIS IS A ESOTERIC          SS JAN84
LKPGGERN EQU   X'04'               THIS IS A GENERATED GEN     SS JAN84
LKPGESOR EQU   X'08'               THIS IS A GENERATED ESOTR   SS FEB88
LKPAFFIX DS    H                   AFFINITY INDEX              SS FEB88
LKPGNRC1 DS    H                   INDEX   TO FIRST GENERIC    SS FEB88
LKPNXT   DS    H                   INDEX TO NEXT ENTRY         SS FEB88
LKPGRPNO DS    XL4                 TOTAL # GRPS FOR LUV       JAH JUL91
LKPUCBNO DS    XL4                 TOTAL # DEVICES FOR LUV    JAH JUL91
LKPRESV2 DS    CL12                RESERVED                   JAH JUL91
LKPEND   EQU   *                        *                      SS JAN92
LKPLNG   EQU   *-LKPSECT           LENGTH OF SECTION           DM JUN83
         SPACE ,                                               DM JUN83
GNRCSECT DSECT ,                   GENERIC SECTION             DM JUN83
GNRCDEVT DS    XL4                 DEVICE TYPE (AS IN CATALOG) DM JUN83
GNRCGRP# DS    F                   NUMBER OF GROUPS            DM JUN83
GNRCGRP1 DS    H                   INDEX   TO FIRST GROUP      SS FEB88
GNRCNXT  DS    H                   INDEX TO NEXT ENTRY         SS FEB88
GNRCLNG  EQU   *-GNRCSECT          LENGTH OF SECTION           DM JUN83
         SPACE ,                                               DM JUN83
GRPTSECT DSECT                     GROUP POINTER SEC           SS JAN84
GRPTPTR  DS    H                   A GROUP INDEX               SS FEB88
GRPTNXT  DS    H                   INDEX TO NEXT ENTRY         SS FEB88
GRPTLNG  EQU   *-GRPTSECT          LENGTH OF SECTION           SS FEB88
         SPACE ,                                               SS JAN84
GRPSECT  DSECT ,                   GROUP SECTION               DM JUN83
GRPID    DS    H                   GROUP ID NUMBER             SS FEB88
GRPDEV#  DS    H                   NUMBER OF DEVICES           SS FEB88
GRPUCB1  DS    A                   INDEX   TO FIRST UCB        SS FEB88
GRPNXT   DS    H                   INDEX TO NEXT ENTRY         SS FEB88
         DS    H                   RESERVED                    SS FEB88
GRPLNGX  EQU   *-GRPSECT           LENGTH OF SECTION XA SECT   DM JUN83
         SPACE ,                                               DM JUN83
MSKSECT  DSECT ,                   GROUP MASK SECTION          DM JUN83
MSKMASK  DS    X                   GROUP MASK (LENGTH VARIABLE)DM JUN83
MSKLNG   EQU   *-MSKSECT           LENGTH OF SECTION           DM JUN83
         SPACE ,                                               DM JUN83
UCBSECT  DSECT ,                   UCB POINTER SECTION         DM JUN83
DEV#     DS    CL3                 DEVICE NUMBER IN EBCDIC     DM NOV83
         DS    C                   EXTRA BYTE                  SS FEB88
DEVNXT   DS    F                   INDEX TO NEXT ENTRY         SS FEB88
UCBLNGX  EQU   *-UCBSECT           LENGTH OF SECTION XA SECT   DM JUN83
         SPACE ,                                               DM JUN83
         MEND  ,                                               DM JUN83
         PRINT ON                                              DM JUN83
         SPACE 3
DYNASWAP CSECT                          *                      SS JAN92
*
*  EQUATES FOR REGISTERS AND FLAGS
*
CVTPT    EQU   16                  PSA POINTER TO CVT
CVTILK2  EQU   40                  CVT POINTER TO UCB LOOK-UP  DM JUN83
CVTJESCT EQU   X'128'              JESCT ADDR                  SS JAN84
CVTDCB   EQU   X'74'               CVT SYSTEM TYPE OFFSET      SS JAN84
CVTMVSE  EQU   X'80'               XA MODE                     SS JAN84
CVTOSLV0 EQU   X'4F0'              LEVEL FLAGS                 SS JAN92
CVTOS41  EQU   X'08'               LEVEL SP 4.1                SS JAN92
CVTOS42  EQU   X'40'               LEVEL SP 4.2                SS JAN92
DACA     EQU   X'78'                                          JAH JUL91
EDTL     EQU   X'60'               EDTLIST                    JAH JUL91
EDTT     EQU   X'10'               PRIMARY EDT POINTER        JAH JUL91
EDTT2    EQU   X'14'               SECONDARY EDT POINTER       SS JAN92
EDTLFG   EQU   X'08'               EDT STATUS FLAG IN EDTL     SS JAN92
JESUPLP  EQU   X'5C'               UCB POINTER LIST            SS JAN84
JESEDTA  EQU   X'34'               EDT POINTER                 SS JAN84
UPLUCBA  EQU   X'10'               FIRST POINTER IN LIST       SS JAN84
UCBSTAT  EQU   3,1,C'X'            UCB STATUS                  DM NOV83
UCBONLI  EQU   128                 DEVICE IS ONLINE            DM NOV83
UCBCHGS  EQU   64                  DEVICE IS TO BE CHANGED TO  DM NOV83
*               OFFLINE & ALLOCATION IS ENQUEUED OR DEVICE     DM NOV83
*               IS ALLOCATED                                   DM NOV83
UCBUNLD  EQU   16                  UNLOAD IN PROCESS           DM NOV83
UCBNAME  EQU   13,3,C'C'           UCB NAME                    DM NOV83
UCBNUM   EQU   4,2,C'X'            UCB DEVICE NUMBER           SS MAR87
UCBTYPE  EQU   16,4,C'X'           DEVICE TYPE                 DM NOV83
UCBDVCLS EQU   18,1,C'X'           DEVICE CLASS                DM NOV83
UCB3DACC EQU   32                  DASD CLASS                  DM NOV83
UCBVOLI  EQU   28,6,C'C'           VOLUME SERIAL               DM NOV83
R0       EQU   0
R1       EQU   1
R2       EQU   2                   USED FOR COUNTER
R3       EQU   3                   USED AS POINTER IN LOOP
R4       EQU   4                   UCB, POINTER  NEXT IN NEW EDT
R5       EQU   5                   COUNTER OR POINTER  NEW PTXREF
R6       EQU   6                   COUNTER OR POINTER  OLD PTXREF
R7       EQU   7                   COUNTER TO TOTAL FOR EACH SEC
R8       EQU   8                   POINTER TO MODEL EDT
R9       EQU   9                   SIZE OF GROUP - UNIT XREF
R10      EQU   10                  POINTER TO NEW EDT
R11      EQU   11                  PROGRAM BASE
R12      EQU   12                  PROGRAM BASE TWO
R13      EQU   13                  SAVE AREA/WORK BASE
R14      EQU   14
R15      EQU   15
         SPACE 3                                               SS JUN90
         SAVE  (14,12),T,DYNASWAP                              SS JUN90
         LR    R11,R15             SET UP BASE                 SS JUN90
         USING DYNASWAP,R11                                    SS JUN90
         L     R2,CVTPT                                        SS JUN90
         TM    CVTDCB(R2),CVTMVSE                              SS FEB88
         BZ    XA0                                             SS FEB88
         LA    R2,IN31J                 *                      SS FEB88
         O     R2,=X'80000000'          *                      SS FEB88
         BSM   0,R2                     *                      SS FEB88
IN31J    DS    0H                       *                      SS FEB88
CHECKEDT DS    0H                       *                      SS JAN92
         LR    R6,R1               SAVE PARM ADDR              SS JAN92
         L     R2,CVTPT                 LOAD CVT ADDR          SS FEB88
         TM    CVTDCB(R2),CVTMVSE       XA                     SS JAN92
         BZ    ESANO                    NO                     SS JAN92
         TM    CVTOSLV0(R2),CVTOS41     SP 41                  SS JAN92
         BZ    ESANO                    NO                     SS JAN92
*        TM    CVTOSLV0(R2),CVTOS42     SP 42                  SS JAN92
*        BZ    XA0                      NO                     SS JAN92
         L     R2,CVTJESCT(R2)          LOAD JESCT ADDR        SS FEB88
         L     R2,DACA(R2)                                    JAH JUL91
         L     R2,EDTL(R2)                                    JAH JUL91
         TM    EDTLFG(R2),X'08'         SECONDARY ACTIVE       SS JAN92
         BZ    USEEDT1                  NO USE PRIMARY         SS JAN92
         L     R2,EDTT2(R2)             GET SECONDARY EDT      SS JAN92
         B     USEEDT                   CONTINUE               SS JAN92
USEEDT1  DS    0H                       *                      SS JAN92
         L     R2,EDTT(R2)              LOAD EDT ADDRESS      JAH JUL91
USEEDT   DS    0H                       *                      SS JAN92
         LR    R8,R2                    HOLD THIS EDT          SS JAN92
         CLC   0(3,R2),=C'EDT'          ESA 4.2                SS JAN92
         BE    XA420                    YES                    SS JAN92
ESANO    DS    0H                                              SS JAN92
         LA    R2,XA0                   *                      SS FEB88
         BSM   0,R2                     *                      SS FEB88
XA0      DS    0H                                              SS FEB88
         DC    X'00'                    *                      SS JUN90
XA420    DS    0H                       *                      SS JUN90
         LA    R2,IN24J                 *                      SS JUN90
         BSM   0,R2                     *                      SS JUN90
IN24J    DS    0H                                              SS JUN90
         L     R0,GETMLEN          LENGTH OF GETMAIN           SS JUN90
         LR    R6,R1               SAVE PARM ADDR              SS JUN90
         GETMAIN R,LV=(0)          STORAGE FOR WORK AND TABLES SS JUN90
         LR    R2,R1               AREA TO ZERO                SS JUN90
         L     R3,GETMLEN          LENGTH TO ZERO              SS JUN90
         SR    R4,R4               FROM ADDRESS NULL           SS JUN90
         SR    R5,R5               FILL CHAR & FROM LEN        SS JUN90
         MVCL  R2,R4               ZERO GETMAIN AREA           SS JUN90
         ST    R1,8(R13)           FORWARD POINTER             SS JUN90
         ST    R13,4(R1)           BACK POINTER                SS JUN90
         LR    R13,R1              SET BASE/SAVE AREA          SS JUN90
         USING SAVE,R13                                        SS JUN90
         LTR   R5,R6               ANY PARM PASSED             SS JUN90
         BZ    MSG1                NO                          SS JUN90
         L     R5,0(R5)            PARM ADDRESS                SS JUN90
         LTR   R5,R5               ANY                         SS JUN90
         BZ    MSG1                BAD                         SS JUN90
         LH    R1,0(R5)            GET LENGTH                  SS JUN90
         LTR   R1,R1               ANY LENGTH                  SS JUN90
         BZ    MSG1                BAD                         SS JUN90
         CH    R1,=H'7'            EXACT SIZE                  SS JUN90
         BNE   MSG1                BAD                         SS JUN90
         MVC   SAVEPARM(7),2(R5)        EXECUTED MOVE          SS JUN90
         OC    SAVEPARM(7),BLK8         FORCE UPPER CASE       SS JUN90
*                                                              SS JUN90
*  FIND UNITS IN EDT THAT MATCH PARM CUUS                      SS JUN90
*                                                              SS JUN90
         LA    R1,IN31A                 START OF 31 BIT MODE   SS JUN90
         O     R1,=X'80000000'          MODE BIT ON            SS JUN90
         BSM   0,R1                     SWITCH MODES TO 31     SS JUN90
IN31A    DS    0H                                              SS JUN90
         ST    R8,EDTPTR                SAVE SYSGEN EDT        SS JUN90
         USING EDT,R8                   *                      SS JAN92
         L     R1,EDTUPLP-EDT(R8)       UCB PT LIST            SS JAN92
         LA    R1,UPLUCBA-4(R1)         OFFSET FOR INDEX       SS JUN90
         L     R3,EDTGRP                GRP PT SEC             SS JUN90
         USING GRPSECT,R3                                      SS JUN90
         L     R2,8(R3)                 NUM OF PT              SS JUN90
         LA    R3,12(R3)                OVER HDR               SS JUN90
BLDGX2   LH    R5,GRPID                 GRP ID                 SS JUN90
         LH    R6,GRPDEV#               UCBS IN GRP            SS JUN90
         L     R7,GRPUCB1               FIRST DEV INDEX        SS JUN90
         LR    R14,R7                   SAVE INDEX             SS JUN90
         SLA   R7,3                     TIMES 8                SS JUN90
         AL    R7,EDTUCB                ADD SECTION ADDRESS    SS JUN90
         LA    R7,12-8(,R7)             ADJUST FOR TRUE ADDR   SS JUN90
         USING UCBSECT,R7                                      SS JUN90
BLDGXE2  DS    0H                                              SS JUN90
         CLC   0(3,R7),SAVEPARM         SAME AS FIRST          SS JUN90
         BNE   NOTFIRST                                        SS JUN90
         ST    R7,SAVEDP1               SAVE IT                SS JUN90
         LR    R7,R14                   GET DEVICE ENTRY       SS JUN90
         SLA   R7,2                     TIMES 4 (FULL WORD)    SS JUN90
         ALR   R7,R1                    INDEX INTO UCB LIST    SS JUN90
         ST    R7,SAVEUP1               SAVE IT                SS JUN90
NOTFIRST DS    0H                                              SS JUN90
         CLC   0(3,R7),SAVEPARM+4       SAME AS SECOND         SS JUN90
         BNE   NOTSECD                                         SS JUN90
         ST    R7,SAVEDP2               SAVE IT                SS JUN90
         LR    R7,R14                   GET DEVICE ENTRY       SS JUN90
         SLA   R7,2                     TIMES 4 (FULL WORD)    SS JUN90
         ALR   R7,R1                    INDEX INTO UCB LIST    SS JUN90
         ST    R7,SAVEUP2               SAVE IT                SS JUN90
NOTSECD  DS    0H                                              SS JUN90
         LR    R7,R14                   GET DEVICE ENTRY       SS JUN90
         SLA   R7,3                     TIMES 8                SS JUN90
         AL    R7,EDTUCB                ADD SECTION ADDRESS    SS JUN90
         LA    R7,12-8(,R7)             ADJUST FOR TRUE ADDR   SS JUN90
         L     R7,DEVNXT                GET NEXT INDEX         SS JUN90
         LR    R14,R7                   SAVE THIS INDEX        SS JUN90
         SLA   R7,3                     TIMES 8                SS JUN90
         AL    R7,EDTUCB                ADD SECTION ADDRESS    SS JUN90
         LA    R7,12-8(,R7)             ADJUST FOR TRUE ADDR   SS JUN90
         BCT   R6,BLDGXE2               FOR EACH IN GRP        SS JUN90
         LA    R3,GRPLNGX(R3)           NEXT GRP               SS JUN90
         BCT   R2,BLDGX2                FOR ALL GRPS           SS JUN90
         DROP  R3,R7,R8                                        SS JUN90
         SR    R10,R10                  ZERO FLAG              SS JUN90
         LA    R15,SAVEPARM             THIS UCB               SS JUN90
         L     R1,SAVEDP1                ADDR                  SS JUN90
         LTR   R1,R1                     ANY ADDR              SS JUN90
         BZ    ERR2                      NO, ERROR             SS JUN90
         L     R2,SAVEUP1                                      SS JUN90
         L     R6,0(R2)                  UCB PTR               SS JUN90
         CLI   UCBTYPE+2(R6),X'20'      DSAD                   SS JUN90
         BNE   ERR4                     NO                     SS JUN90
         TM    UCBSTAT(R6),UCBONLI      ONLINE                 SS JUN90
         BZ    CKU2                     NO                     SS JUN90
         BAL   R14,MSG3                 *                      SS JUN90
         B     CKU2                     *                      SS JUN90
ERR2     DS    0H                       *                      SS JUN90
         BAL   R14,MSG2                 *                      SS JUN90
         B     CKU2                     *                      SS JUN90
ERR4     DS    0H                       *                      SS JUN90
         BAL   R14,MSG5                 *                      SS JUN90
         B     CKU2                     *                      SS JUN90
CKU2     DS    0H                       *                      SS JUN90
         LA    R15,SAVEPARM+4           THIS UCB               SS JUN90
         L     R3,SAVEDP2                                      SS JUN90
         LTR   R3,R3                     ANY ADDR              SS JUN90
         BZ    ERR3                      NO, ERROR             SS JUN90
         L     R4,SAVEUP2                                      SS JUN90
         L     R8,0(R4)                  UCB PTR               SS JUN90
         CLI   UCBTYPE+2(R8),X'20'      DSAD                   SS JUN90
         BNE   ERR5                     NO                     SS JUN90
         TM    UCBSTAT(R8),UCBONLI      ONLINE                 SS JUN90
         BZ    CKALL                    NO                     SS JUN90
         BAL   R14,MSG3                 *                      SS JUN90
         B     EXIT                     *                      SS JUN90
ERR3     DS    0H                       *                      SS JUN90
         BAL   R14,MSG2                 *                      SS JUN90
         B     EXIT                     *                      SS JUN90
ERR5     DS    0H                       *                      SS JUN90
         BAL   R14,MSG5                 *                      SS JUN90
         B     EXIT                     *                      SS JUN90
CKALL    DS    0H                       *                      SS JUN90
         LTR   R10,R10                  FLAG                   SS JUN90
         BNZ   EXIT                     SOME ERROR             SS JUN90
         CLC   UCBTYPE+2(2,R6),UCBTYPE+2(R8) SAME TYPE         SS NOV90
         BNE   MSG6                                            SS NOV90
         CLC   UCBTYPE(1,R6),UCBTYPE(R8) SAME TYPE             SS NOV90
         BNE   MSG6                                            SS NOV90
         L     R7,0(R3)                  VALUE                 SS JUN90
         L     R5,0(R1)                  VALUE                 SS JUN90
         NOP   EXIT        TEST , DONT SWAP                    SS JUN90
         LR    R10,R1              SAVE R1                     SS JUN90
         MODESET KEY=ZERO               *                      SS JUN90
         ST    R5,0(R3)            SWAP  VALUE                 SS JUN90
         ST    R6,0(R4)            SWAP  UCB PTR               SS JUN90
         ST    R7,0(R10)           SWAP  VALUE                 SS JUN90
         ST    R8,0(R2)            SWAP  UCB PTR               SS JUN90
         MODESET KEY=NZERO              *                      SS JUN90
         MVC   WTOE(WTO4LN),WTO4                               SS JUN90
         MVC   WTOE+28(7),SAVEPARM                             SS JUN90
         WTO   MF=(E,WTOE)                                     SS JUN90
EXIT     LR    R1,R13                                          SS JUN90
         L     R0,GETMLEN          LENGTH TO FREE              SS JUN90
         L     R13,4(R13)                                      SS JUN90
         FREEMAIN R,A=(1),LV=(0)                               SS JUN90
         RETURN (14,12),RC=0                                   SS JUN90
         EJECT ,                                               SS JUN90
MSG1     WTO   'TSDXXXI PARM MISSING OR MORE THAN 7 CHARACTORS',       X
               ROUTCDE=(2,11)
         B     EXIT
MSG6     WTO   'TSDXXXI UNITS ARE NOT THE SAME DEVICE TYPE    ',       X
               ROUTCDE=(2,11)                                  SS NOV90
         B     EXIT                                            SS NOV90
MSG2     MVC   WTOE(WTO2LN),WTO2                               SS JUN90
         MVC   WTOE+17(3),0(R15)                               SS JUN90
         WTO   MF=(E,WTOE)                                     SS JUN90
         LA    R10,4             ERROR FLAG                    SS JUN90
         BR    R14               RETURN                        SS JUN90
MSG3     MVC   WTOE(WTO3LN),WTO3                               SS JUN90
         MVC   WTOE+17(3),0(R15)                               SS JUN90
         WTO   MF=(E,WTOE)                                     SS JUN90
         LA    R10,4             ERROR FLAG                    SS JUN90
         BR    R14               RETURN                        SS JUN90
MSG5     MVC   WTOE(WTO5LN),WTO5                               SS JUN90
         MVC   WTOE+17(3),0(R15)                               SS JUN90
         WTO   MF=(E,WTOE)                                     SS JUN90
         LA    R10,4             ERROR FLAG                    SS JUN90
         BR    R14               RETURN                        SS JUN90
WTO2     WTO   'TSDXXXI UNIT XXX NOT FOUND               ',            X
               ROUTCDE=(2,11),                                         X
               MF=L
WTO2LN   EQU   *-WTO2
WTO3     WTO   'TSDXXXI UNIT XXX NOT OFFLINE             ',            X
               ROUTCDE=(2,11),                                         X
               MF=L
WTO3LN   EQU   *-WTO3
WTO4     WTO   'TSDXXXI SWAP DONE                        ',            X
               ROUTCDE=(2,11),                                         X
               MF=L
WTO4LN   EQU   *-WTO4
WTO5     WTO   'TSDXXXI UNIT XXX NOT DASD TYPE           ',            X
               ROUTCDE=(2,11),                                         X
               MF=L
WTO5LN   EQU   *-WTO5
MSG10    ABEND 10,DUMP
GETMLEN  DC    A(SAVEEND-SAVE)          FOR TEMPORARY WORK AREA
BLK8     DC    CL8' '                   *                      SS JUN90
         LTORG
         EJECT
SAVE     DSECT
SAVEAREA DS    18F
DBLWRD   DS    D                                               SS JUN90
UPLSTR   DS    F                   SAVE POINTER TO UCB PT LIST SS JUN90
NUMUNIT  DS    F                   NUMBER OF UNITS             SS JUN90
UCBLU    DS    F                                               SS JUN90
GXRLNG2  DS    H                   SAVE LENGTH GXREF           SS JUN90
SAVEGRPT DS    A                        *                      SS JUN90
EDTPTR   DS    A                                               SS JUN90
SAVEDP1  DS    A                                               SS JUN90
SAVEUP1  DS    A                                               SS JUN90
SAVEDP2  DS    A                                               SS JUN90
SAVEUP2  DS    A                                               SS JUN90
FLAGS    DS    X                    READ FLAGS                 SS JUN90
BFLAG    DS    X                    SORT FLAG                  SS JUN90
FLAG2    DS    X                    MISC FLAGS                 SS JUN90
SAVEPARM DS    CL8                  SAVE PARM                  SS JUN90
WTOE     DS    CL133                                           SS JUN90
         SPACE 2                                               SS JUN90
SAVEEND  EQU   *                                               SS JUN90
         EJECT ,                                               DM JUN83
         EDT   ,                                               DM JUN83
         END
