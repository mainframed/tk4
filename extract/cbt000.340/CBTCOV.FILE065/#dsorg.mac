#DSORG   TITLE 'DETERMINE DATASET ORGANIZATION FOR CLIST PROCESSING'
         SPACE 4
*
* WRITTEN BY LEONARD WOREN (?), 1978
* UPDATE BY DWIGHT HARM 5/17/79
* UPDATED BY LDW 10/17/79
*
* THIS IS A TSO COMMAND PROCESSOR WHICH TAKES ONE PARAMETER, A
* DATASET NAME, AND SETS ITS RETURN CODE BASED ON THE ORGANIZATION
* OF THE DATASET:
*   0 = DATASET NOT CATALOGUED, OR NOT ON VOLUME INDICATED
*       IN CATALOGUE. ("CAMLST LOCATE" OR "CAMLST SEARCH" FAILED.)
*   1 = DATASET IS PHYSICAL SEQUENTIAL (PS), OR DATASET IS PARTITIONED
*       AND A MEMBER NAME WAS SPECIFIED AS PART OF THE PARAMETER.
*   2 = DATASET IS PARTITIONED, AND NO MEMBER WAS SPECIFIED.
*   3 = INDEXED SEQUENTIAL (ISAM).
*   4 = VSAM.
*   5 = DIRECT ACCESS (DA).
*   6 = OTHER(?).
*
* USEFUL PRIMARILY IN CLISTS FOR TAKING ACTION BASED ON
* DATASET TYPE.
*
         TITLE 'INITIALIZATION'
#DSORG   ENTR  EXIT=LEAVE,RC=(R11),                                    X
               ENV=CP,TSFAKE=FAKECPPL,PARMREG=R2,ADDRREG=R3,LENREG=R4
         SPACE 4
         SR    R11,R11                  ASSUME IT WON'T WORK.
         LTR   R4,R4                    ANY OPERAND?
         BNP   LEAVE                    NO - ERROR
*
         L     R8,CPPLUPT-CPPL(,R2)     -> USER PROFILE TABLE
         USING UPT,R8
         TITLE 'MAIN PROCESSING'
*                                       CONSTRUCT QUALIFIED DSNAME
         QUALIFY DSN=(R3),LEN=(R4),NEW=DSN
*
         BCTR  R15,0                    DECREMENT LENGTH
         LR    R3,R1                    START OF DSNAME
         EX    R15,TRT                  LOOK FOR LEADING PAREN.
         BZ    NOPAREN                  ...NONE, CONTINUE.
*
         OI    FLAG,MEMBER              SET FLAG INDICATING MEMBER NAME
         SR    R1,R3                    COMPUTE LENGTH W/0 MBR NAME
         BCTR  R1,0                     MINUS ONE FOR EXEC
         LR    R15,R1                   AND PUT BACK IN R15
NOPAREN  LR    R1,R3                    THEN RESTORE STARTING POINT
*
         EX    R15,MOVEDSN              MOVE DSN TO NEW AREA
         OC    DSN2,=CL44' '            MAKE IT UPPER CASE
*
         LOCATE   CAMLST                SEARCH CATALOG FIRST
         LTR   R15,R15
         BNZ   LEAVE                    ERROR
*
         OBTAIN  CAMLST2                THEN GET DSCB INFORMATION
*
         LTR   R15,R15                  DID IT WORK?
         BNZ   LEAVE                    NO - LEAVE WITH BAD RC.
*
         LA    R11,1                    ASSUME PS
         TM    DSORG,ORGPS              IS IT?
         BO    LEAVE                    YES
*
         LA    R11,3                    ASSUME ISAM.
         TM    DSORG,ORGISAM            IS IT?
         BO    LEAVE                    ...YES, EXIT.
*
         LA    R11,4                    ASSUME VSAM.
         TM    DSORG+1,ORGVSAM          IS IT?
         BO    LEAVE                    ...YES, EXIT.
*
         LA    R11,5                    ASSUME DIRECT.
         TM    DSORG,ORGDA              IS IT?
         BO    LEAVE                    ...YES, EXIT.
*
         LA    R11,6                    ASSUME UNKNOWN
         TM    DSORG,ORGPO              IS IT PO?
         BZ    LEAVE                    ...NO, THEREFORE UNKNOWN
*
         LA    R11,2                    ASSUME PO AND NO MEMBER.
         TM    FLAG,MEMBER              WAS MEMBER GIVEN?
         BZ    LEAVE                    ...NO, EXIT WITH RC=2, PO.
         LA    R11,1                    ...YES, EXIT WITH RC=1, PS.
         B     LEAVE
         SPACE 2
MOVEDSN  MVC   DSN2(0),0(R1)            << EXECUTED >>
TRT      TRT   0(0,R3),TABLE            << EXECUTED >>
         TITLE 'CAMLST"S AND WORK AREAS'
CAMLST   CAMLST   NAME,DSN2,,LOCAREA
         SPACE 2
CAMLST2  CAMLST  SEARCH,DSN2,LOCAREA+6,DSCB
         SPACE 2
DSN      DC    CL44' '
DSN2     DC    CL44' '
*
DSCB     DC    44D'0'
*
DSORG    EQU   DSCB+82-44      LOCATION OF DATASET ORGANIZATION
*    DSORG FIELD VALUES:
ORGISAM  EQU   X'80'           ISAM
ORGPS    EQU   X'40'           PHYSICAL SEQUENTIAL
ORGDA    EQU   X'20'           DIRECT ACCESS
ORGPO    EQU   X'02'           PARTITIONED
ORGVSAM  EQU   X'08'           AT DSORG+1 - VSAM
*
LOCAREA  DC    0D'0',CL256' ',CL9' '
*
FLAG     DC    X'00'               FLAG BYTE
MEMBER   EQU   X'80'               MEMBER WAS SPECIFIED
*
TABLE    DC    256X'00'            TRANSLATE-AND-TEST TABLE
         ORG   TABLE+C'('          LOOK FOR LEADING PAREN
         DC    X'01'
         ORG   ,                   RESTORE ORIGIN
*
FAKECPPL DC    4F'0',CL256' '
*
         LTORG
         TITLE 'TSO CONTROL BLOCK DSECTS'
         TSCPPL
         SPACE 1
         ORG   CPPL
         DC    (CPPLL)X'00'
         SPACE 2
         IKJUPT
         SPACE 2
         END   #DSORG
