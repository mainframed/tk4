DSMF     TITLE '-----  DISPLAY STATUS OF VSAM SMF DATASETS  -----'
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  DSMF                                               *
*                                                                     *
*   PURPOSE:       DISPLAY STATUS OF VSAM SMF DATASETS                *
*                                                                     *
*   AUTHOR:        LEONARD D. WOREN                                   *
*                                                                     *
*   DATE:          MARCH 26, 1982                                     *
*                                                                     *
*   INSTALLATION:  TITLE INSURANCE AND TRUST                          *
*                                                                     *
*   UPDATES:                                                          *
*      00/00/00 LDW  NONE                                             *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
DSMF     OSENTER  EXIT=LEAVE,RC=0
CVTPTR   EQU   X'10'                    OFFSET OF CVT ADDRESS
CVTSMCA  EQU   196                      OFFSET OF SMCA PTR IN CVT
         EJECT ,
*
* SEE IF THIS SYSTEM HAS VSAM SMF DATASETS (SE2 & SP)
*
         L     R11,CVTPTR               -> CVT
         TM    CVTSMCA(R11),X'80'       CHECK FOR MVS/SP  INSTD
         BO    VSAMSMF                  NO - ERROR
         SPACE 1
         TPUTX '*** NOT VSAM SMF ***'
         B     LEAVE
         SPACE 3
VSAMSMF  L     R11,CVTSMCA(,R11)        GET ADDRESS OF SMCA
         USING SMCABASE,R11             GET SMCA BASE ADDRESS
*
* LIST ALL VSAM SMF DATA SETS
*
         ICM   R10,B'1111',SMCAFRDS     POINT TO FIRST RDS
         BNZ   SMFOK                    CONTINUE IF ACTIVE
         TPUTX '*** SMF NOT ACTIVE ***'
         B     LEAVE
         USING IEEMBRDS,R10
SMFOK    TPUTX LINE,L'LINE
         MVC   LINE,BLANKS
         MVC   L@TYPE(3),=C'PRI'        FIRST DATASET IS PRIMARY
         TPUTX DASHES,79
VSAMLOOP TM    RDSFLG2,RDSLOST          DUMMY RDS FOR LOST DATA?
         BO    VSAMNEXT                 YES - SKIP
         MVC   L@DSN(L'RDSNAME),RDSNAME MOVE IN DSNAME
         MVC   L@VOLID(6),RDSVOLID      MOVE IN VOLUME SERIAL
         L     R15,RDSCAPTY             GET SIZE IN BLKS
         CVD   R15,DWD
         MVC   L@SIZE(7),=X'4020206B202120'   MOVE IN EDIT MASK
         ED    L@SIZE(7),DWD+5
         L     R1,RDSNXTBL              GET NUMBER OF BLKS IN USE
         BCTR  R1,0                     DON'T ASK ME WHY
         CVD   R1,DWD
         MVC   L@USED(7),=X'4020206B202120'   MOVE IN EDIT MASK
         ED    L@USED(7),DWD+5
         M     R1-1,=F'10000'           SCALE, CLEAR R0 FOR DIVIDE
         LTR   R15,R15                  DIVISOR = ZERO?
         BNZ   DIVIDE                   NO - GO DO IT
         SR    R1,R1                    NO - FAKE THE QUOTIENT
         B     ZERO                     AND CONTINUE
DIVIDE   DR    R1-1,R15                 COMPUTE
ZERO     CVD   R1,DWD
         MVC   L@PCENT(8),=X'402021204B20206C'  MOVE IN EDIT MASK
         ED    L@PCENT(7),DWD+5
         CL    R10,SMCASVCR             IS THIS THE ACTIVE RDS?
         BNE   NOTACT                   NO - SKIP
         MVC   L@STAT(6),=C'ACTIVE'
         B     PRINT
NOTACT   CLC   RDSNXTBL,=F'1'           ANYTHING IN DATASET?
         BNH   ALT                      NO - SKIP
         MVC   L@STAT(13),=C'DUMP REQUIRED'
         B     PRINT
ALT      MVC   L@STAT(9),=C'ALTERNATE'
PRINT    TPUTX LINE,L'LINE              PRINT LINE FOR THIS DATASET
         MVC   LINE,BLANKS              CLEAR DISPLAY LINE
         MVC   L@TYPE(3),=C'SEC'        REST OF DATASETS ARE SECONDARY
VSAMNEXT L     R10,RDSNEXT              -> NEXT RDS
         CL    R10,SMCAFRDS             BACK TO THE FIRST ONE?
         BNE   VSAMLOOP                 NO - CONTINUE WITH NEXT
         B     LEAVE                    LEAVE IF BACK TO FIRST
         SPACE 2
         LTORG ,
         SPACE 2
DWD      DC    D'0'
BLANKS   DC    CL80' '
LINE     DC    CL80' '
         ORG   LINE
L@TYPE   DC    C'TYPE  '
L@DSN    DC    C'DSNAME      '
L@VOLID  DC    C'VOLSER   '
L@SIZE   DC    C'SIZE(BLKS)   '
L@USED   DC    C'BLKS IN USE        '
L@PCENT  EQU   *-11
L@STAT   DC    C'STATUS              '
         ORG   ,
DASHES   DC    70C'-',CL9' '
         EJECT
         IEESMCA ,                      SMCA DSECT IN SYS1.AMODGEN
         EJECT ,
IEEMBRDS DSECT
RDSID    DS    CL4                      CONTROL BLOCK ID
RDSNEXT  DS    F                        FORWARD CHAIN POINTER
RDSPREV  DS    F                        BACKWARD CHAIN POINTER
*
RDSFLG1  DS    X
RDSFREE  EQU   X'10'                    1 - NEED TO FREE DATASET
RDSDUMP  EQU   X'08'                    1 - OPER HAS BEEN TOLD TO
*                                           DUMP DS
RDSREADY EQU   X'04'                    1 - DS IS OPEN AND READY
RDSCLOSE EQU   X'02'                    1 - DS NEEDS TO BE CLOSED
RDSNDOP  EQU   X'01'                    1 - DS NEEDS TO BE OPENED
*
RDSFLG2  DS    X
RDSLOST  EQU   X'80'                    DUMMY RDS FOR LOST DATA
RDSPHYER EQU   X'40'                    1 - PHYSICAL I/O ERROR
RDSLOGER EQU   X'20'                    1 - LOGICAL I/O ERROR
*
RDSFLG3  DS    X                        RESERVED
RDSFLG4  DS    X                        RESERVED
RDSNAME  DS    CL10                     DATASET NAME
RDSVOLID DS    CL6                      VOLUME SERIAL
RDSDDN   DS    CL8                      DDNAME RETND BY DYNALLOC
RDSCAPTY DS    F                        CAPACITY IN # OF BLOCKS
RDSNXTBL DS    F                        NUMBER OF NEXT AVAIL BLOCK
RDSBQE   DS    F                        ADDR OF BQE BEING WRITTEN
RDSAVSPC DS    F                        AVAIL SPACE FROM SHOWCB
RDSENRBA DS    F                        ENDRBA FROM SHOWCB
RDSCINV  DS    F                        CONTROL INTERVAL SIZE
RDSDOMID DS    F                        MSG # OF DUMP MSG
RDSRPL   DS    CL76                     VSAM RPL
RDSACB   DS    CL76                     VSAM ACB
         SPACE 2
         END
