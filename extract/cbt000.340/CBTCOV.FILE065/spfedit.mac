         PRINT NOGEN
*----------------------------------------------------------------------
*
*        FULL SCREEN EDIT DIALOG MANAGER
*
*----------------------------------------------------------------------
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                 BASE REG #1
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
SPFEDIT  CSECT
*----------------------------------------------------------------------
*        INITIALIZATION
*----------------------------------------------------------------------
         USING *,R15
         SAVE  (14,12),,SPFEDIT_&SYSDATE._&SYSTIME.
         DROP  R15
         BALR  R12,0
         USING *,R12              INFORM ASSEMBLER OF BASE REGS
         SPACE
         LR    R10,R1             SAVE CPPL ADDRESS
         USING CPPL,R10           CPPL ADDRESSABILITY
         GETMAIN RU,LV=@DATAL     GET STORAGE FOR WORKAREA
         LR    R2,R1              ADDRESS OF 'TO' FIELD
         LA    R3,@DATAL          LENGTH OF 'TO' FIELD
         XR    R5,R5              ZERO PAD CHAR AND 'FROM' LENGTH
         MVCL  R2,R4              CLEAR WORK AREA
         ST    R13,4(R1)          SAVE ADDR OF CALLERS SAVEAREA IN OURS
         ST    R1,8(R13)          SAVE ADDR OF OUR SAVEAREA IN CALLERS
         LR    R13,R1             SET WORKAREA POINTER
         USING @DATA,R13          ADDRESSABILITY TO WORKAREA
         SPACE 2
*----------------------------------------------------------------------
*        SET UP TSO ENVIRONMENT
*----------------------------------------------------------------------
ENTRY010 DS    0H
         ST    R10,@CPPLPTR       STORE THE CPPL PTR
         MVC   @ECT,CPPLECT    MOVE ENVIRONMENT CONTROL TABLE PTR
         SPACE
         LA    R1,@PPL            LOAD ADDRESS OF OUR PARSE PARM LIST
         USING PPL,R1
         MVC   PPLUPT,CPPLUPT     MOVE USER PROFILE TABLE PTR
         MVC   PPLECT,CPPLECT     MOVE ENVIRONMENT CONTROL TABLE PTR
         LA    R0,@ECB            ADDRESS OF ECB
         ST    R0,PPLECB          STORE IT IN THE PARSE PARM LIST
         L     R0,PCLPTR          ADDRESS OF IKJPARM CSECT
         ST    R0,PPLPCL          STORE IT IN THE PARSE PARM LIST
         LA    R0,@PPLANS         ADDRESS OF THE PARSE ANSWER AREA
         ST    R0,PPLANS          STORE IT IN THE PARSE PARM LIST
         MVC   PPLCBUF,CPPLCBUF   MOVE COMMAND BUF POINTER
         ST    R13,PPLUWA         STORE THE ADDRESS OF THE WORKAREA
         DROP  R1
EXIT010  EQU   *
         EJECT
*----------------------------------------------------------------------
*        CHECK FOR OPTIONS
*----------------------------------------------------------------------
ENTRY020 DS    0H
         CALLTSSR EP=IKJPARS,MF=(E,@PPL)
         LTR   R15,R15
         BZ    AAA020
         TPUT  ERRPARS,ERRPARSL
         B     EOJ
AAA020   EQU   *
         L     R10,@PPLANS        GET POINTER TO PARSE ANSWER
         USING IKJPARMD,R10       GAIN ADDRESSABILITY TO ANSWER
         SPACE
         LA    R15,IDDSN          ADDR OF PDE
         XR    R2,R2              CLEAR REG
         ICM   R2,3,4(R15)        INSERT LENGTH OF DSNAME
         BZ    ZZZ020             NO DSNAME
         L     R3,0(R15)          ADDR OF PARSE DSNAME
         LA    R4,@DSNAME
         MVI   0(R4),X'7D'
         LA    R4,1(R4)
         MVI   0(R4),C' '
         MVC   1(54,R4),0(R4)
         BCTR  R2,0               DECREMENT FOR EXECUTE
         EX    R2,MOVOPT          MOVE IN SPECIFIED OPTION
         LA    R4,1(R2,R4)
         SPACE
         LA    R15,8(R15)         ADDR OF PDE+8
         XR    R2,R2              CLEAR REG
         ICM   R2,3,4(R15)        INSERT LENGTH OF MEMBER
         BZ    BBB020             NO MEMBER
         MVI   0(R4),C'('
         LA    R4,1(R4)
         L     R3,0(R15)          ADDR OF PARSE MEMBER
         BCTR  R2,0               DECREMENT FOR EXECUTE
         EX    R2,MOVOPT          MOVE IN SPECIFIED OPTION
         LA    R4,1(R2,R4)
         MVI   0(R4),C')'
         LA    R4,1(R4)
BBB020   EQU   *
         MVI   0(R4),X'7D'
         SPACE
         LA    R15,SFIDVOL        ADDR OF PDE
         XR    R2,R2              CLEAR REG
         ICM   R2,3,4(R15)        INSERT LENGTH OF VOLSER
         BZ    ZZZ020             NO VOLSER
         L     R3,0(R15)          ADDR OF PARSE VOLSER
         LA    R4,@VOLSER
         MVI   0(R4),C' '
         MVC   1(5,R4),0(R4)
         BCTR  R2,0               DECREMENT FOR EXECUTE
         EX    R2,MOVOPT          MOVE IN SPECIFIED OPTION
         LA    R4,1(R2,R4)
ZZZ020   EQU   *
         IKJRLSA @PPLANS
         DROP  R10                DROP ADDRESSABILITY
EXIT020  EQU   *
         B     ENTRY030
         SPACE
MOVOPT   MVC   0(0,R4),0(R3)
         EJECT
*----------------------------------------------------------------------
*        SET FOR RETURN OF ERRORS
*----------------------------------------------------------------------
ENTRY030 DS    0H
         LA    R15,CONTROL         ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,ERRORS          ADDRESS OF ERRORS
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         LA    R15,RETURN          ADDRESS OF RETURN
         ST    R15,@LKPARM3        UPDATE DYNAMIC STORAGE
         OI    @LKPARM3,X'80'      SET THE VL BIT
         BAL   R14,ISPCALL
         SPACE
         LA    R15,VDEFINE         ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,NAMELIST        ADDRESS OF NAMELIST
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         LA    R15,@ZEDCMD         ADDRESS OF 1ST VARIABLE
         ST    R15,@LKPARM3        UPDATE DYNAMIC STORAGE
         LA    R15,CHAR            ADDRESS OF CHAR
         ST    R15,@LKPARM4        UPDATE DYNAMIC STORAGE
         LA    R15,F1              ADDRESS OF 1
         ST    R15,@LKPARM5        UPDATE DYNAMIC STORAGE
         OI    @LKPARM5,X'80'      SET THE VL BIT
         BAL   R14,ISPCALL         EFFECT EDIT
EXIT030  EQU   *
         SPACE 2
*----------------------------------------------------------------------
*        EDIT RECOVERY INIT
*----------------------------------------------------------------------
ENTRY040 DS    0H
         LA    R15,EDREC           ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,INIT            ADDRESS OF 'INIT'
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         OI    @LKPARM2,X'80'      SET THE VL BIT
         BAL   R14,ISPCALL         EFFECT EDREC INIT
EXIT040  EQU   *
         EJECT
*----------------------------------------------------------------------
*        EDIT RECOVERY QUERY
*----------------------------------------------------------------------
ENTRY050 DS    0H
         LA    R15,EDREC           ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,QUERY           ADDRESS OF 'QUERY'
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         OI    @LKPARM2,X'80'      SET THE VL BIT
         BAL   R14,ISPCALL         EFFECT EDREC QUERY
         LTR   R15,R15             EDIT RECOVERY PENDING?
         BZ    ENTRY100            NO - GO INVOKE EDIT SERVICE
         C     R15,F4              NORMAL EDIT RECOVERY?
         BE    EXIT050             YES - CONTINUE
         TPUT  ERRMSG1,ERRMSG1L    INFORM USER OF SEVERE ERROR
         B     EOJ
EXIT050  EQU   *
         SPACE 2
*----------------------------------------------------------------------
*        DISPLAY THE EDIT RECOVERY SCREEN
*----------------------------------------------------------------------
ENTRY060 DS    0H
         LA    R15,DISPLAY         ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,ISREDM02        ADDRESS OF PANEL NAME
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         OI    @LKPARM2,X'80'      SET THE VL BIT
         BAL   R14,ISPCALL         EFFECT EDIT
         LTR   R15,R15             RETURN CODE OK?
         BZ    AAA060              NO - USER HIT(RETURN/END)
         OI    @FLAG,@EOJ          SET EOJ FLAG
         B     ENTRY090            SPECIFY DEFER
AAA060   EQU   *
         CLI   @ZEDCMD,C' '        DID USER SPECIFY PROCESS?
         BE    ENTRY070            YES - PROCESS RECOVERY
         CLI   @ZEDCMD,C'C'        DID USER SPECIFY CANCEL?
         BE    ENTRY080            YES - CANCEL RECOVERY
         CLI   @ZEDCMD,C'D'        DID USER SPECIFY DEFER?
         BE    ENTRY090            YES - DEFER RECOVERY
EXIT060  EQU   *
         B     EOJ                 THIS SHOULD NEVER HAPPEN!
         EJECT
*----------------------------------------------------------------------
*        EDIT RECOVERY PROCESS
*----------------------------------------------------------------------
ENTRY070 DS    0H
         LA    R15,EDREC           ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,PROCESS         ADDRESS OF PROCESS
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         OI    @LKPARM2,X'80'      SET THE VL BIT
         BAL   R14,ISPCALL         EFFECT EDREC
EXIT070  EQU   *
         B     ENTRY050
         SPACE 2
*----------------------------------------------------------------------
*        EDIT RECOVERY CANCEL
*----------------------------------------------------------------------
ENTRY080 DS    0H
         LA    R15,EDREC           ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,CANCEL          ADDRESS OF CANCEL
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         OI    @LKPARM2,X'80'      SET THE VL BIT
         BAL   R14,ISPCALL         EFFECT EDREC
EXIT080  EQU   *
         B     ENTRY050
         SPACE 2
*----------------------------------------------------------------------
*        EDIT RECOVERY DEFER
*----------------------------------------------------------------------
ENTRY090 DS    0H
         LA    R15,EDREC           ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,DEFER           ADDRESS OF CANCEL
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         OI    @LKPARM2,X'80'      SET THE VL BIT
         BAL   R14,ISPCALL         EFFECT EDREC
         TM    @FLAG,@EOJ          WAS EOJ FLAG SET?
         BO    EOJ                 YES - EOJ
EXIT090  EQU   *
         B     ENTRY050
         EJECT
*----------------------------------------------------------------------
*        LINK TO THE EDIT SERVICE
*----------------------------------------------------------------------
ENTRY100 DS    0H
         CLI   @DSNAME,X'00'
         BE    ENTRY200
         LA    R15,EDIT            ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,@DSNAME         ADDRESS OF THE DSN
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         CLI   @VOLSER,X'00'       WAS A VOLSER SPECIFIED ?
         BE    AAA100              NO - GO SET THE VL BIT
         LA    R15,@VOLSER         ADDRESS OF THE VOLSER
         ST    R15,@LKPARM3        UPDATE DYNAMIC STORAGE
         OI    @LKPARM3,X'80'      SET THE VL BIT
         B     BBB100
AAA100   EQU   *
         OI    @LKPARM2,X'80'      SET THE VL BIT
BBB100   EQU   *
         BAL   R14,ISPCALL         EFFECT EDIT
         C     R15,F16
         BNE   CCC100
         TPUT  ERRMSG2,ERRMSG2L
CCC100   EQU   *
         C     R15,F20
         BNE   EXIT100
         TPUT  ERRMSG3,ERRMSG3L
EXIT100  EQU   *
         B     EOJ
         SPACE 2
*----------------------------------------------------------------------
*        DISPLAY THE EDIT SCREEN
*----------------------------------------------------------------------
ENTRY200 DS    0H
         LA    R15,SELECT          ADDRESS OF SERVICE REQUEST
         ST    R15,@LKPARM1        UPDATE DYNAMIC STORAGE
         LA    R15,BUFLEN          LENGTH OF CMD BUFFER
         ST    R15,@LKPARM2        UPDATE DYNAMIC STORAGE
         LA    R15,CMDBUF          ADDRESS OF CMD BUFFER
         ST    R15,@LKPARM3        UPDATE DYNAMIC STORAGE
         OI    @LKPARM3,X'80'      SET THE VL BIT
         BAL   R14,ISPCALL         EFFECT ISREDIT
EXIT200  EQU   *
         EJECT
*----------------------------------------------------------------------
*        RETURN TO THE CALLER
*----------------------------------------------------------------------
EOJ      DS    0H
         ICM   R15,15,@ISPEPA
         BZ    EOJ1
         DELETE EP=ISPLINK
EOJ1     EQU   *
         LR    R1,R13                   GET ADDRESS TO FREE
         L     R13,4(,R13)              GET PREVIOUS SAVE AREA
         FREEMAIN RU,A=(1),LV=@DATAL    FREE THE GOTTEN AREA
         RETURN (14,12),,RC=0           RETURN TO CALLER
         SPACE 2
*----------------------------------------------------------------------
*        LOAD ISPLINK ROUTINE
*----------------------------------------------------------------------
ISPCALL  DS    0H
         ST    R14,@SAV14         SAVE RETURN ADDR
         ICM   R15,15,@ISPEPA     DYNAMIC ALLOCATION ROUTINE LOADED ?
         BNZ   ISP010             YES - USE IT
         SPACE
         LOAD  EP=ISPLINK         LOAD ISPLINK ROUTINE
         ST    R0,@ISPEPA         SAVE THE ENTRY POINT
         LR    R15,R0
         LA    R1,@LKPARM1
         ST    R1,@ISPADDR
ISP010   EQU   *
         LA    R1,@LKPARM1
         BALR  R14,R15
         L     R14,@SAV14         LOAD RETURN ADDR
         BR    R14
         EJECT
*----------------------------------------------------------------------
*        CONSTANTS
*----------------------------------------------------------------------
F1       DC    F'1'
F4       DC    F'4'
F16      DC    F'16'
F20      DC    F'20'
         SPACE
CONTROL  DC    CL8'CONTROL'
ERRORS   DC    CL8'ERRORS'
RETURN   DC    CL8'RETURN'
EDREC    DC    CL8'EDREC'
INIT     DC    CL8'INIT'
QUERY    DC    CL8'QUERY'
DISPLAY  DC    CL8'DISPLAY'
ISREDM02 DC    CL8'ISREDM02'
PROCESS  DC    CL8'PROCESS'
DEFER    DC    CL8'DEFER'
CANCEL   DC    CL8'CANCEL'
VDEFINE  DC    CL8'VDEFINE'
CHAR     DC    CL8'CHAR'
EDIT     DC    CL8'EDIT'
SELECT   DC    CL8'SELECT'
NAMELIST DC    C'(ZEDCMD)'
CMDBUF   DC    C'PGM(ISREDIT) PARM(P,ISREDM01)'
CMDBUFL  EQU   *-CMDBUF
BUFLEN   DC    A(CMDBUFL)
         SPACE
ERRPARS  DC    C'*** PARSE FAILED.'
ERRPARSL EQU   *-ERRPARS
ERRMSG1  DC    C'*** SEVERE ERROR DURING ERROR RECOVERY.'
ERRMSG1L EQU   *-ERRMSG1
ERRMSG2  DC    C'*** NO MEMBERS IN LIBRARY.'
ERRMSG2L EQU   *-ERRMSG2
ERRMSG3  DC    C'*** DATASET NOT FOUND.'
ERRMSG3L EQU   *-ERRMSG3
         SPACE
         LTORG
         SPACE 2
PCLPTR   DC    V(PCL)
PCL      IKJPARM
IDDSN    IKJPOSIT DSNAME,USID
KWVOL    IKJKEYWD
         IKJNAME 'VOLSER',SUBFLD=SFVOL,ALIAS='VOLUME'
*
SFVOL    IKJSUBF
SFIDVOL  IKJPOSIT DSNAME,VOLSER,PROMPT='VOLUME SERIAL NUMBER'
         IKJENDP
         EJECT
@DATA    DSECT
         DS    18F
@SAV14   DS    F
@ISPEPA  DS    F
@ISPADDR DS    F
@CPPLPTR DS    F
@ECT     DS    F
@ECB     DS    F
@PPL     DS    7F
@PPLANS  DS    F
@LKPARM1 DS    F
@LKPARM2 DS    F
@LKPARM3 DS    F
@LKPARM4 DS    F
@LKPARM5 DS    F
@ZEDCMD  DS    CL1
@DSNAME  DS    CL56
@VOLSER  DS    CL6
@FLAG    DS    X
@EOJ     EQU   X'01'
@TRTAB   DS    XL256
@DATAL   EQU   *-@DATA
         SPACE
         CVT   DSECT=YES
         SPACE
         IKJCPPL
         SPACE
         IKJECT
         SPACE
         IKJPPL
         END
