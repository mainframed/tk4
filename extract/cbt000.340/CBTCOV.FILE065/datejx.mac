DATEJX   TITLE 'GREGORIAN TO JULIAN DATE CONVERSION SUBROUTINE'
*
*      THIS PROGRAM CONVERTS A GREGORIAN DATE TO A JULIAN DATE. UPON
* COMPLETION, REGISTER 15 CONTAINS 0 IF THE CONVERSION WAS SUCCESSFUL
* OR 12 IF THE CONVERSION FAILED (DUE TO INVALID INPUT JULIAN DATE).
* THE CALLING SEQUENCE FOR "DATEJX" IS AS FOLLOWS
*           1. THE INPUT GREGORIAN DATE IN THE FORMAT "MMDDYY"
*           2. THE OUTPUT JULIAN DATE IN THE FORMAT "YYDDD"
*
*      THE "DATEJX" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
DATEJX   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING INPUT,R4            ADDRESS INPUT PARAMETER DSECT
         USING OUTPUT,R5           ADDRESS OUTPUT PARAMETER DSECT
         USING WORKAREA,R13        ADDRESS WORKAREA DSECT
         LM    R4,R5,0(R2)         LOAD PARAMETER POINTERS
         TRT   YEARIN,NUMERIC      IF THE GREGORIAN YEAR IS NUMERIC
         BC    8,TESTDAY              THEN GO TEST THE GREGORIAN DAY
         B     ERROR                  ELSE FLAG FOR ERROR
TESTDAY  DS    0H
         TRT   DAYIN,NUMERIC       IF THE GREGORIAN DAY IS NUMERIC
         BC    8,TESTMNT              THEN GO TEST MONTH
         B     ERROR                  ELSE FLAG FOR ERROR
TESTMNT  DS    0H
         TRT   MONTHIN,NUMERIC     IF THE GREGORIAN MONTH IS NUMERIC
         BC    8,INPUTNUM             THEN GO TRY TO CONVERT
         B     ERROR                  ELSE FLAG FOR ERROR
INPUTNUM DS    0H
         MVC   MONTHTBL,MTBL       MOVE THE DAYS/MONTH TABLE TO WORK
         MVC   YEARDAYS,YDAYS      MOVE DAYS/YEAR TO WORK AREA
         CLC   YEARIN,ZERO         IF INPUT YEAR IS ZEROES
         BE    NOTLEAP                THEN NOT A LEAP YEAR
         PACK  DBLWORD,YEARIN      CONVERT INPUT YEAR TO PACKED DECIMAL
         CVB   R6,DBLWORD          CONVERT INPUT YEAR TO BINARY
         ST    R6,SNGLWORD         SAVE BINARY INPUT YEAR
         TM    SNGLWORD+3,X'03'    IF THE INPUT YEAR NOT DIVISIBLE BY 4
         BNZ   NOTLEAP                THEN NOT A LEAP YEAR
         AP    MONTHTBL+4(4),ONE   ADD A DAY TO FEBRUARY
         AP    YEARDAYS,ONE           AND TO THE YEAR
NOTLEAP  DS    0H
         PACK  DBLWORD,MONTHIN     CONVERT INPUT MONTH TO PACKED DECIMA
         CVB   R8,DBLWORD          CONVERT INPUT MONTH TO BINARY
         C     R8,=F'01'           IF MONTH IS LESS THAN 01
         BL    ERROR                  THEN FLAG FOR ERROR
         C     R8,=F'12'           IF MONTH IS GREATER THAN 12
         BH    ERROR                  THEN FLAG FOR ERROR
         BCTR  R8,R0               DECREMENT MONTH BY ONE
         PACK  SNGLWORD,DAYIN      CONVERT INPUT DAY TO PACKED DECIMAL
         CP    SNGLWORD,PZERO      IF INPUT DAY IS ZERO
         BE    ERROR                  THEN FLAG FOR ERROR
         CP    SNGLWORD,=PL4'31'   IF INPUT DAY IS TOO LARGE
         BH    ERROR                  THEN FLAG FOR ERROR
         ZAP   JDAY,PZERO          INITIALIZE JULIAN DAY
         LA    R7,MONTHTBL         POINT TO START OF MONTH TABLE
         LTR   R8,R8               IF THIS IS THE FIRST MONTH
         BZ    GOTMONTH               THEN BYPASS LOOP
LOOP     DS    0H
         AP    JDAY,0(4,R7)        ADD TO THE JULIAN DAY
         LA    R7,4(,R7)           INCREMENT THE TABLE POINTER
         BCT   R8,LOOP             CONTINUE
GOTMONTH DS    0H
         AP    SNGLWORD,JDAY       COMPUTE THE JULIAN DAY
         UNPK  DAYOUT,SNGLWORD     CONVERT THE OUTPUT DAY
         OI    DAYOUT+2,X'F0'         TO CHARACTER FORMAT
         MVC   YEAROUT,YEARIN      MOVE INPUT INPUT YEAR TO OUTPUT YEAR
         LA    R15,0               INDICATE SUCCESSFUL CONVERSION
         B     EXIT                   AND LEAVE
ERROR    DS    0H
         LA    R15,12              INDICATE ERROR IN INPUT
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
         EJECT
MTBL     DS    0F
         DC    PL4'31'             JANUARY
         DC    PL4'28'             FEBRUARY
         DC    PL4'31'             MARCH
         DC    PL4'30'             APRIL
         DC    PL4'31'             MAY
         DC    PL4'30'             JUNE
         DC    PL4'31'             JULY
         DC    PL4'31'             AUGUST
         DC    PL4'30'             SEPTEMBER
         DC    PL4'31'             OCTOBER
         DC    PL4'30'             NOVEMBER
         DC    PL4'31'             DECEMBER
MTBLEND  EQU   *
MTBLLEN  EQU   MTBLEND-MTBL        COMPUTE LENGTH OF MONTH TABLE
         SPACE
NUMERIC  DS    0F
         DC    256X'FF'
         ORG   NUMERIC+C'0'
         DC    10X'00'
         ORG
         SPACE
PZERO    DC    PL4'0'
ZERO     DC    CL2'00'
ONE      DC    PL4'1'
YDAYS    DC    PL4'365'
         EJECT
OUTPUT   DSECT ,                   MAP OF OUTPUT JULIAN DATE
YEAROUT  DS    CL2                 OUTPUT JULIAN YEAR
DAYOUT   DS    CL3                 OUTPUT JULIAN DAY
         SPACE
INPUT    DSECT ,                   MAP OF INPUT GREGORIAN DATE
MONTHIN  DS    CL2                 INPUT GREGORIAN MONTH
DAYIN    DS    CL2                 INPUT GREGORIAN DAY
YEARIN   DS    CL2                 INPUT GREGORIAN YEAR
         SPACE
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
WORKSTRT EQU   *
         DS    18A                 REGISTER SAVE AREA
DBLWORD  DS    D                   DOUBLEWORD WORK AREA
SNGLWORD DS    F                   SINGLEWORD WORK AREA
YEARDAYS DS    PL4                 DAYS PER YEAR
JDAY     DS    PL4                 COMPUTE AREA FOR JULIAN DAY
MONTHTBL DS    CL(MTBLLEN)         DAYS PER MONTH TABLE WORK AREA
WORKEND  EQU   *
WORKSIZE EQU   WORKEND-WORKSTRT    COMPUTE SIZE OF WORKAREA
         END
