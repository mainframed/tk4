         TITLE '***** AUTOOPER - ISSUE OPERATOR COMMANDS'
AUTOOPER CSECT
***********************************************************************
*                                                                     *
*     THIS PROGRAM IS DESIGNED TO ISSUE OPERATOR COMMANDS THAT ARE    *
*     READ IN FROM THE 'GOFORIT' FILE.                                *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        STANDARD REGISTER EQUATES                                    *
*                                                                     *
***********************************************************************
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
R00      EQU   00
R01      EQU   01
R02      EQU   02
R03      EQU   03
R04      EQU   04
R05      EQU   05
R06      EQU   06
R07      EQU   07
R08      EQU   08
R09      EQU   09
         EJECT
***********************************************************************
*                                                                     *
*        PROGRAM INITIALLIZATION AND HOUSEKEEPING.                    *
*                                                                     *
***********************************************************************
         SPACE 2
         STM   R14,R12,12(R13)     *****
         LR    R12,R15             * STANDARD
         USING AUTOOPER,R12        *    LINKAGE
         LA    R15,SAVEAREA        *     CONVENTIONS AND
         ST    R13,4(,R15)         *       SAVE AREA
         ST    R15,8(,R13)         *         CHAINING.
         LR    R13,R15             *****
         SPACE 2
         OPEN  (GOFORIT,(INPUT))
         LA    R1,GOFORIT     SET ADDRESSABILITY TO DCB
         USING IHADCB,R1
         TM    DCBOFLGS,X'10' DID THE FILE OPEN?
         DROP  R1
         BNO   RETURN         ...NO, THEN GET OUT, NO MESSAGES
         SPACE 2
         MODESET KEY=ZERO,MODE=SUP
         EJECT
***********************************************************************
*                                                                     *
*        GET MASTER CONSOLE UCMID.                                    *
*                                                                     *
***********************************************************************
         SPACE 2
         L     R2,16         --> CVT
         L     R2,100(R2)    --> UCM
         S     R2,=F'4'      --> PREFIX
         L     R3,0(R2)      --> MCS PREFIXTO UCM
         L     R3,0(R3)      --> MASTER CONS UCM ENTRY
         MVC   UCMID,26(R3)  SAVE MASTER CONSOLE ID
         SPACE 2
***********************************************************************
*                                                                     *
*        READ THE 'GOFORIT' FILE AND BEGIN TO ISSUE THE COMMANDS.     *
*                                                                     *
***********************************************************************
         SPACE 2
GETCMND  DS    0H
         SPACE 2
         MODESET KEY=NZERO,MODE=PROB
         SPACE 2
         GET   GOFORIT,COMMAND GET A CARD IMAGE
         SPACE 2
         MODESET KEY=ZERO,MODE=SUP
         SPACE 2
         LA    R1,CMD         POINT TO COMMAND
         LA    R0,0           INDICATE MASTER CONSOLE
         IC    R0,UCMID       MOVE IN CONSOLE ID
         SVC   34             ISSUE THE COMMAND
         B     GETCMND        GO CREATE A NEW SCREEN
         EJECT
***********************************************************************
*                                                                     *
*        END-OF-FILE ON 'GOFORIT'.                                    *
*                                                                     *
***********************************************************************
CLOSEIT  DS    0H             END-OF-FILE ON GOFORIT
         SPACE 2
         MODESET KEY=NZERO,MODE=PROB
         SPACE 2
         CLOSE (GOFORIT)
         SPACE 2
RETURN   DS    0H
         L     R13,4(,R13)    RESTORE REGISTERS AND RETURN
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         XR    R15,R15
         BR    R14            RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*        PROGRAM CONSTANTS                                            *
*                                                                     *
***********************************************************************
         SPACE 2
         DS    0D
SAVEAREA DC    18F'0'
         SPACE 2
CMD      DC    H'80',H'0'
COMMAND  DC    CL80' '
UCMID    DC    X'00'                 CONSOLE ID
         SPACE 2
         LTORG
         EJECT
GOFORIT  DCB   DSORG=PS,MACRF=(GM),EODAD=CLOSEIT,DDNAME=GOFORIT,       *
               LRECL=80
         EJECT
         DCBD  DSORG=PS,DEVD=DA
         END   AUTOOPER
