DSP      TITLE 'DISPLAY AVAILABLE SPACE ON DISK DRIVE'      *HMD 05/83*
***********************************************************************
*                                                                     *
*      PROGRAM TO PRINTOUT AVAILABLE SPACE ON A DISK DRIVE            *
*      USES LSPACE (SVC 78)  FUNCTION                                 *
*                                                                     *
*      ALSO DETERMINES IF PACK HAS INDEXED VTOC                       *
*      USES MVS/XA COMPATIBLE UCB LOOKUP ROUTINE            *HMD 05/83*
*                                                                     *
***********************************************************************
DSPACE   CSECT                                              *HMD 05/83*
         TSOENTER REGS=YES,PL=(IO,PARSE),WORK=WORKLEN       *HMD 05/83*
         TM    0(R2),X'80'             RUN AS PROGRAM?      *HMD 05/83*
         BO    DONE1                   YES, GET OUT FAST    *HMD 05/83*
         XC    TSOECB,TSOECB           ZERO ECB             *HMD 05/83*
         MVC   PPLPCL,ADDPCL           GET ADD OF PCL
         CALLTSSR EP=IKJPARS,MF=(E,PPL)
*                                      CALL IKJPARS         *HMD 05/83*
         LTR   R15,R15                 TEST  RETURN CODE
         BNZ   ERROR                   ERROR GO TELL USER
         XC    VOLLEN,VOLLEN           CLEAR VOLUME LENGTH  *HMD 05/83*
         SLR   R2,R2                   CLEAR RETURN REGISTR *HMD 05/83*
         L     R6,PPLRANS              GET ADDRESS OF ANSWER AREA
         LUSE  IKJPARMD,R6             ADDRESS THE PDE      *HMD 05/83*
         LA    R6,VOL                  PDL  DSECT           *HMD 05/83*
         ST    R6,PDLENTRY             SAVE ENTRY ADDRESS   *HMD 05/83*
         LDROP R6                      DROP REGISTER NOW    *HMD 05/83*
         TM    6(R6),X'80'             IS IT REQUST FOR SPECIFIC
*                                      VOLUME INDEX         *HMD 05/83*
         BZ    GETCVT                  SKIP
         INUSE ,                       CHECK REGISTERS      *HMD 05/83*
         EJECT                                              *HMD 05/83*
**                                                          *HMD 05/83*
** PROCESS VOLSER PARAMETER                                 *HMD 05/83*
**                                                          *HMD 05/83*
GETVOL   MVC   VOLSER,BLANKS     CLEAR VOLSER FIELD QUICK   *HMD 05/83*
         L     R7,0(R6)          GET ADDRESS OF VOLUME
         LH    R8,4(R6)          GET LENGTH OF VOLUME
         STH   R8,VOLLEN         SAVE LENGTH OF VOLUME      *HMD 05/83*
         BCTR  R8,R0             MACHINE LENGTH
         LA    R9,VOLSER         ADDRESS OF MESSAGE
         EX    R8,EXMVC          MOVE VOLUME IN
GETCVT   L     R3,CVTPTR         LOAD CVT ADDRESS           *HMD 05/83*
         LUSE  CVT,R3                                       *HMD 05/83*
         LUSE  UCBOB,R7                                     *HMD 05/83*
         SPACE 1                                            *HMD 05/83*
         XC    UWORK,UWORK      CLEAR WORK AREA             *HMD 05/83*
         XC    ADDRUCB,ADDRUCB  CLEAR UCB ADDRESS           *HMD 05/83*
         MVI   DEVCLASS,UCB3DACC                            *HMD 05/83*
*                               INDICATE DASD DEVICE CLASS  *HMD 05/83*
         MVI   FLAG,X'00'       CLEAR FLAG BYTE             *HMD 05/83*
UCBLOOP  L     R15,CVTUCBSC     LOAD UCB SCAN ADDRESS       *HMD 05/83*
         CALL  (15),(UWORK,DEVCLASS,ADDRUCB),VL,MF=(E,CALLIST)
         LTR   R15,R15                                      *HMD 05/83*
         BNZ   UCBEND           END OF UCBS                 *HMD 05/83*
         L     R7,ADDRUCB       GET UCB ADDRESS             *HMD 05/83*
         CLI   UCBID,UCBSTND    IS UCB ADDRESS VALID        *HMD 05/83*
         BNE   UCBSKP           NO
         TM    UCBSTAT,UCBONLI  IS UCB ONLINE?              *HMD 05/83*
         BNO   UCBSKP           NO  SKIP                    *HMD 05/83*
         TM    UCBSTAT,UCBCHGS  CHANGING STATUS?            *HMD 05/83*
         BO    UCBSKP           YES SKIP
         LH    R6,VOLLEN        LOAD LENGTH OF PARM         *HMD 05/83*
         LTR   R6,R6            TEST IF VOLUME PRESENT      *HMD 05/83*
         BZ    ALLVOLS          NO, PROCESS ALL VOLUMES     *HMD 05/83*
         BCTR  R6,R0            BUMP DOWN FOR EX            *HMD 05/83*
         EX    R6,CLCVOL        COMPARE PARTIAL VOLSER      *HMD 05/83*
         BNE   UCBSKP           NOT EQUAL - SKIP            *HMD 05/83*
ALLVOLS  MVC   INFLAG,=C'  '                                *HMD 05/83*
         MVC   UNIT,UCBNAME     MOVE IN NAME                *HMD 05/83*
         MVC   VOLSER,UCBVOLI   MOVE IN VOLSER NAME         *HMD 05/83*
         LR    R0,R7            SAVE UCB ADDRESS            *HMD 05/83*
         LA    R1,WORKA         POINT TO WORK AREA          *HMD 05/83*
         SVC   78               ISSUE LSPACE SVC            *HMD 05/83*
         SPACE 1                                            *HMD 05/83*
         MVC   OUTMESS,VOLMESS
         MVC   OUTMESS+4(6),VOLSER
         MVC   OUTMESS+16(3),UNIT
         EJECT                                              *HMD 05/83*
         CVAFTST  UCB=(R7)           TEST  VTOC             *HMD 05/83*
         B     BRTBL(R15)            TEST  RETURN CODE
BRTBL    B     RC0
         B     RC4
         B     RC8
         B     RC12
         B     RC16
RC8      MVC   INFLAG,=C'I '
RC4      DS    0H
RC0      DS    0H
         SPACE 2                                            *HMD 05/83*
         LA    R1,OUTMESSL          LOAD MESSAGE LENGTH     *HMD 05/83*
         SLL   R1,16                SHIFT TO HIGH ORDER     *HMD 05/83*
         ST    R1,OUTMESSA          SAVE IN LINE DESCRIPTOR *HMD 05/83*
         XC    TSOECB,TSOECB        CLEAR ECB               *HMD 05/83*
         PUTLINE PARM=PTPB,OUTPUT=(OUTMESSA,TERM,SINGLE,DATA),         X
               MF=(E,IOPL)                                  *HMD 05/83*
         MVI   FLAG,X'FF'           INDICATE UCB DISPLAYED  *HMD 05/83*
         SPACE 2                                            *HMD 05/83*
UCBSKP   B     UCBLOOP     CONTINUE SEARCHING UCB TABLE
         SPACE 1                                            *HMD 05/83*
UCBEND   CLI   FLAG,X'FF'   DID WE FIND A UCB?              *HMD 05/83*
         BE    SECVOL       YES - CHECK FOR ANOTHER PREFIX  *HMD 05/83*
**                                                          *HMD 05/83*
** PROCESS TWO SEGMENT PUTLINE MESSAGE                      *HMD 05/83*
**                                                          *HMD 05/83*
         MVC   OLD2,=F'2'   SAVE NUMBER OF MESSAGE SEGS     *HMD 05/83*
         MVC   SEG1(NVOLEN),NOVOLA                          *HMD 05/83*
*                           MOVE IN FIRST SEGMENT           *HMD 05/83*
         LA    R1,SEG1      GET MESSAGE ADDRESS             *HMD 05/83*
         ST    R1,OLD2+4    SAVE IN O.L.D. NUMBER 2         *HMD 05/83*
         LA    R1,SEG2      POINT TO MESSAGE SEGMENT        *HMD 05/83*
         ST    R1,OLD2+8    SAVE IN O.L.D. NUMBER 2         *HMD 05/83*
         LH    R2,VOLLEN    GET VOLUME PARM LENGTH          *HMD 05/83*
         LTR   R2,R2        DID WE HAVE A PARAMETER         *HMD 05/83*
         BZ    ENDOPUT      NO, JUST GIVE RETURN CODE       *HMD 05/83*
         BCTR  R2,R0        DECREMENT FOR EXECUTE           *HMD 05/83*
         EX    R2,MOVEVOL   MOVE VOLSER SEGMENT TO AREA     *HMD 05/83*
         LA    R1,SEG2VOL   LOAD BEGINNING OF FIELD         *HMD 05/83*
         LA    R1,2(R1,R2)  POINT TO BEGINNING OF FIELD     *HMD 05/83*
         MVC   0(L'NOVOLE,R1),NOVOLE                        *HMD 05/83*
*                           MOVE IN SECOND PART OF SEGMENT  *HMD 05/83*
         LA    R1,L'NOVOLE(,R1)                             *HMD 05/83*
*                           POINT TO END OF SEGMENT         *HMD 05/83*
         LA    R2,SEG2      GET SEGMENT 2 ADDRESS           *HMD 05/83*
         SR    R1,R2        GET SEGMENT LENGTH              *HMD 05/83*
         STH   R1,SEG2      SAVE SEGMENT LENGTH             *HMD 05/83*
         LA    R1,SEG1LEN   LOAD SEGMENT ONE LENGTH         *HMD 05/83*
         STH   R1,SEG2+2    SAVE OFFSET IN SEGMENT          *HMD 05/83*
         XC    TSOECB,TSOECB                                *HMD 05/83*
*                           CLEAR EVENT CNTL BLOCK          *HMD 05/83*
         PUTLINE PARM=PTPB,OUTPUT=(OLD2,TERM,SINGLE,INFOR), *HMD 05/83*X
               MF=(E,IOPL)                                  *HMD 05/83*
ENDOPUT  LA    R2,8         GET RETURN CODE                 *HMD 05/83*
         SPACE 2                                            *HMD 05/83*
         INUSE ,            CHECK REGISTER USAGE            *HMD 05/83*
         EJECT                                              *HMD 05/83*
*************************************************************HMD 05/83*
**  PROCESS NEXT PARAMETER GIVEN ON INVOCATION OF COMMAND   *HMD 05/83*
*************************************************************HMD 05/83*
SECVOL   L     R6,PDLENTRY
         CLC   =X'FF000000',8(R6)                           *HMD 05/83*
*                             CHAIN WORD PRESENT            *HMD 05/83*
         BE    DONE           NO, END OF LIST               *HMD 05/83*
         L     R6,8(,R6)      GET CHAIN WORD                *HMD 05/83*
         ST    R6,PDLENTRY    SAVE ADDRESS OF PARM          *HMD 05/83*
         B     GETVOL         YES, END OF JOB               *HMD 05/83*
         SPACE 2                                            *HMD 05/83*
DONE     IKJRLSA   PPLRANS       FREE PARSE AREA            *HMD 05/83*
         SLR   R2,R2          CLEAR RETURN CODE             *HMD 05/83*
DONE1    TSOLEAVE ISN=LTORG,RETCODE=(2)                     *HMD 05/83*
         EJECT                                              *HMD 05/83*
*************************************************************HMD 05/83*
**  C O N S T A N T S   A N D   V A R I A B L E S           *HMD 05/83*
*************************************************************HMD 05/83*
EXMVC    MVC   0(0,9),0(7)        << EXECUTED >>            *HMD 05/83*
CLCVOL   CLC   VOLSER(0),UCBVOLI  << EXECUTED >>            *HMD 05/83*
MOVEVOL  MVC   SEG2VOL(0),VOLSER  << EXECUTED >>            *HMD 05/83*
ADDPCL   DC    V(PARSEPCL)        ADDRESS OF PCL            *HMD 05/83*
BLANKS   DC    CL8' '             FOR CLEARING AREAS        *HMD 05/83*
*************************************************************HMD 05/83*
**  E R R O R    R O U T I N E S                            *HMD 05/83*
*************************************************************HMD 05/83*
RC12     LA    R1,NOVIBA                                    *HMD 05/83*
         MVC   OLD,=F'1'                                    *HMD 05/83*
         ST    R1,OLD+4                                     *HMD 05/83*
         XC    TSOECB,TSOECB                                *HMD 05/83*
         PUTLINE PARM=PTPB,OUTPUT=(OLD,TERM,SINGLE,INFOR),  *HMD 05/83*X
               MF=(E,IOPL)                                  *HMD 05/83*
         LA    R2,12                                        *HMD 05/83*
         B     UCBSKP                                       *HMD 05/83*
         SPACE 2                                            *HMD 05/83*
RC16     LA    R1,NOTDAA                                    *HMD 05/83*
         MVC   OLD,=F'1'                                    *HMD 05/83*
         ST    R1,OLD+4                                     *HMD 05/83*
         XC    TSOECB,TSOECB                                *HMD 05/83*
         PUTLINE PARM=PTPB,OUTPUT=(OLD,TERM,SINGLE,INFOR),  *HMD 05/83*X
               MF=(E,IOPL)                                  *HMD 05/83*
         LA    R2,16                                        *HMD 05/83*
         B     UCBSKP                                       *HMD 05/83*
         SPACE  2                                           *HMD 05/83*
ERROR    LA    R1,ERRMESSA                                  *HMD 05/83*
         MVC   OLD,=F'1'                                    *HMD 05/83*
         ST    R1,OLD+4                                     *HMD 05/83*
         XC    TSOECB,TSOECB                                *HMD 05/83*
         PUTLINE PARM=PTPB,OUTPUT=(OLD,TERM,SINGLE,INFOR),  *HMD 05/83*X
               MF=(E,IOPL)                                  *HMD 05/83*
         LA    R2,20                                        *HMD 05/83*
         B     DONE1                                        *HMD 05/83*
         SPACE 2                                            *HMD 05/83*
NOVIBA   DC    AL2(NVLEN),AL2(0)                            *HMD 05/83*
NOVIB    DC    C'HMD0234I DSPACE - VIB NOT INITIALIZED'     *HMD 05/83*
NVLEN    EQU   *-NOVIBA                                     *HMD 05/83*
         SPACE 1                                            *HMD 05/83*
NOTDAA   DC    AL2(NDLEN),AL2(0)                            *HMD 05/83*
NOTDA    DC    C'HMD0235I DSPACE - VOLUME NOT DASDI   '     *HMD 05/83*
NDLEN    EQU   *-NOTDAA                                     *HMD 05/83*
         SPACE 1                                            *HMD 05/83*
ERRMESSA DC    AL2(EMLEN),AL2(0)                            *HMD 05/83*
ERRMESS  DC    C'HMD0236I DSPACE - PARSE FAILED'            *HMD 05/83*
EMLEN    EQU   *-ERRMESSA                                   *HMD 05/83*
         SPACE 1                                            *HMD 05/83*
VOLMESS  DC    C'VOL VVVVVV '
UNITMESS DC    C'UNIT UUU '
         SPACE 1                                            *HMD 05/83*
NOVOLA   DC    AL2(NVOLEN),AL2(0)                           *HMD 05/83*
NOVOL    DC    C'HMD0237I VOLUME(S) WITH PREFIX '           *HMD 05/83*
NVOLEN   EQU   *-NOVOLA                                     *HMD 05/83*
         SPACE  1                                           *HMD 05/83*
NOVOLE   DC    C'NOT FOUND'                                 *HMD 05/83*
         EJECT                                              *HMD 05/83*
PARSEPCL IKJPARM  DSECT=IKJPARMD
         SPACE 2                                            *HMD 05/83*
VOL      IKJIDENT 'VOLUME PREFIX',UPPERCASE,MAXLNTH=6,FIRST=ALPHANUM,  X
               OTHER=ALPHANUM,LIST                          *HMD 05/83*
         IKJENDP
         EJECT                                              *HMD 05/83*
TSODSECT DSECT          CONTINUE DUMMY SECTION              *HMD 05/83*
*        WORK AREA FOR  DSPACE CP
WORK     DS   0F        NAME FOR USING
VOLLEN   DS   H                                             *HMD 05/83*
ECB      DS   F
PDLENTRY DS   F         ADDRESS OF CURRENT PDL              *HMD 05/83*
PTPB     PUTLINE MF=L                                       *HMD 05/83*
OLD      DS   2F                                            *HMD 05/83*
OLD2     DS   F         NUMBER OF SEGMENTS                  *HMD 05/83*
         DS   F         POINTER TO FIRST  SEGMENT           *HMD 05/83*
         DS   F         POINTER TO SECOND SEGMENT           *HMD 05/83*
         DS   F         POINTER TO THIRD  SEGMENT           *HMD 05/83*
FLAG     DS   X         FLAG TO INDICATE IF UCB DISPLAYED   *HMD 05/83*
         SPACE 1                                            *HMD 05/83*
SEG1     DS    F         SEGMENT NUMBER ONE                 *HMD 05/83*
SEG1TXT  DC    C'HMD0237I VOLUME(S) WITH PREFIX '           *HMD 05/83*
SEG1LEN  EQU   *-SEG1TXT                                    *HMD 05/83*
         SPACE 1                                            *HMD 05/83*
SEG2     DS    F         SEGMENT TWO LENGTH AND OFFSET      *HMD 05/83*
SEG2VOL  DS    CL6       SEGMENT TWO VOLSER PREFIX          *HMD 05/83*
SEG2TXT  DC    C'NOT FOUND'                                 *HMD 05/83*
SEG2VLN  EQU   *-SEG2VOL                                    *HMD 05/83*
SEG2LEN  EQU   *-SEG2                                       *HMD 05/83*
         SPACE 1                                            *HMD 05/83*
OUTMESSA DS   F         LENGTH OF MESSAGE AREA              *HMD 05/83*
OUTMESS  DS   CL20      MESSAGE AREA
INFLAG   DS   CL2       INDEXED VTOC FLAG AREA
WORKA    DS   CL30      AREA FOR LSPACE RETURN
OUTMESSL EQU  *-OUTMESSA                                    *HMD 05/83*
         SPACE 1                                            *HMD 05/83*
VOLSER   DS   CL6       VOLSER
UNIT     DS   CL3       UNIT
         SPACE 1                                            *HMD 05/83*
*  UCB LOOKUP WORK AREA AND PARM LIST VARIABLES             *HMD 05/83*
UWORK    DS   XL100     WORK AREA, MUST BE ZEROED           *HMD 05/83*
DEVCLASS DS   X         DEVICE CLASS TO SEARCH FOR (DASD)   *HMD 05/83*
ADDRUCB  DS   F         UCB ADDRESS (FULL WORD IN LENGTH)   *HMD 05/83*
         SPACE 1                                            *HMD 05/83*
**                                                          *HMD 05/83*
* CALL MACRO LIST FORM FOR UCB LOOKUP ROUTINE               *HMD 05/83*
**                                                          *HMD 05/83*
CALLIST  CALL ,(,,,),MF=L                                   *HMD 05/83*
         SPACE 1                                            *HMD 05/83*
WORKLEN  EQU  *-WORK    LENGTH OF WORK AREA                 *HMD 05/83*
         EJECT                                              *HMD 05/83*
         CVT  LIST=YES,DSECT=YES                            *HMD 05/83*
         EJECT                                              *HMD 05/83*
         IEFUCBOB LIST=YES                                  *HMD 05/83*
         END  DSPACE
