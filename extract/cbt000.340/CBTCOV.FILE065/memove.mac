MEMOVE   TITLE 'BLOCK TRANSFER SUBROUTINE'
*  THIS PROGRAM IS A FORTRAN CALLABLE SUBROUTINE WHICH TRANSFERS
* A BLOCK OF N CONSECUTIVE WORDS FROM LOCATION A TO LOCATION B.
* THE DESTRUCTIVE OVERLAP CONDITION IS SET IF A WORD IS USED AS A
* SOURCE AFTER IT IS USED AS A DESTINATION. A MESSAGE IS WRITTEN TO
* THE PROGRAMMER'S LOG WHEN THE SUBROUTINE IS CALLED BY A BATCH JOB
* .THE MESSAGE IS WRITTEN TO THE USER'S TERMINAL IF THE SUBROUTEINE
* IS CALLED BY A LOGON.
*          CALL MEMOVE( A, B, N)
*
MEMOVE   ENTER PARMREG=2,             SAVE REGISTERS AND INITIALIZE    X
               GETMAIN=(WORKSIZE,0)
         REGISTER                     REGISTER EQUATES
         USING WORKAREA,R13
         L     R6,0(R2)            LOAD THE ADDRESS OF PARM1
         L     R4,4(R2)            LOAD THE ADDRESS OF PARM2
         L     R8,8(R2)            LOAD THE ADDRESS OF PARM3
         L     R9,0(R8)            LOAD N TO R9
         SR    R8,R8               ZERO OUT R8
         M     R8,FOUR             MULTIPLY N BY 4
         SR    R7,R7
         LR    R7,R9
         O     R7,MSK              SET BLANK FOR PADDING CHARACTER
         LR    R5,R9
         MVCL  R4,R6               MOVE N*4 BYTES  FROM R6 TO R4
         BC    1,ERROR             DESTRUCTIVE OVERLAP ERROR CONDITION
         SR    R15,R15
         B     LEV
ERROR    MVC   CALLST,CCALLST      MOVE CALL PARAMETER TO WORK
         LOAD  EP=AMIINT           LOAD SUBROUTINE TO GET JOB STATUS
         LR    R15,R0              SAVE ENTRY POINT ADDRESS
         CALL  (15),(WORK),        CALL  " AMIINT"                     X
               MF=(E,CALLST)
         CLC   WORK,ONE            IF RETURN VALUE FROM "AMIINT"<= 1
         BNH   BATCH                 THEN PROCESS WITH WTO
LOGON    DS    0H
         TPUT  WTOMSG1,L'WTOMSG1   WRITE MESSAGE 1 TO TERMINAL
         B     EXIT
BATCH    DS    0H
         WTO   MF=(E,WTOPRM1)      WRITE MESSAGE 1 TO DAYFILE LOG
EXIT     DS    0H
         ABEND 900                 ABEND THE STEP
LEV      LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
ONE      DC    F'1'
FOUR     DC    F'4'
MSK      DC    X'40000000'
CCALLST  CALL  ,(),MF=L            CONSTANT CALL PARAMETER LIST
CCALLEN  EQU   *-CCALLST
WTOPRM1  WTO   ' ERROR, DESTRUCTIVE OVERLAP CONDITION ',               X
               ROUTCDE=11,MF=L
WTOLEN1  EQU   *-WTOPRM1
         ORG   WTOPRM1+4
WTOMSG1  DS    CL(WTOLEN1-4)
         ORG
WORKAREA DSECT ,
         DS    18A
WORK     DS    F
CALLST   DS    CL(CCALLEN)         CALL PARAMETER LIST
WORKSIZE EQU   *-WORKAREA
         END
