**********************************************************************
**                                                                  **
**  RELATED USERMOD : SFTMS02                                       **
**  DOC: REFER TO SYS5.TMS.TMSGEN(OMODVOL1).                        **
**                                                                  **
**          THIS MODULE REPLACES THE ORIGINAL IBM'S MODULE.         **
**          IT EDITS THE TAPE LABEL FOR TMS FORMAT.                 **
**                                                                  **
**********************************************************************
OMOD     TITLE 'OMODVOL1 LABEL EDITOR FOR TMS INTERFACE'          40159
         COPY  TMSSVCNO
         EJECT ,
OMODVOL1 CSECT ,
         TMSVCEQU SVC=X
         TITLE 'INITIALIZATION'
*
         USING IEC,R4
         USING SVCXWORK,R6
         USING UCB,R8
         USING TMVT,R10
*
*                                                                 40159
*                                                                 40173
*                                                                 40231
*                                                                 40364
ENTRY    DS    0H
         BALR  R3,0               GET ADDRESSABILITY
         USING *,R3               MAP PROGRAM
         L     R0,SPFDWORK        GET LENGTH OF WORK AREA
         GETMAIN R,LV=(0)         GET WORK AREA
         DROP  R6
         USING SVCXWORK,R1        MAP GOTTON CORE
         XC    SVCXWORK(LXWORK),SVCXWORK  CLEAR
         STM   R14,R12,SAVE+12    SAVE REGS R14-R12
         ST    R13,SAVE+4         SAVE R13
         LA    R15,SAVE           LOAD SAVE AREA ADDR
         ST    R15,SAVEAREA+4     SAVE
         LA    R13,SAVEAREA
         ST    R13,SAVE+8
         XC    SAVEAREA+8(4),SAVEAREA+8
         XC    DXCCW12,DXCCW12    CLEAR XCTL PARM AREA
         ST    R6,DXCCW12         POST MOD NAME ADDR
         LA    R4,32(,R6)         POINT TO FIRST ENTRY
         LR    R7,R4              SAVE POINTER TO WTG SEGMENT
         LR    R9,R6              SAVE WTG TABLE POINTER
         LR    R6,R1              LOAD SVCXWORK BASE
         DROP  R1
         USING SVCXWORK,R6        MAP SVCXWORD
         MVI   IOAREA2,X'00'      CLEAR  FIRST DCB SWITCH
         XC    8(4,R9),8(R9)       CLEAR ADDR IN WTG
         MVC   0(8,R9),OPENRET    POST OPEN RETURN MODNAME
CKACTIVE DS    0H
*
*              DETERMINE IF TMS IS ACTIVE
         SPACE
         MVI   TMSVCRTN,X'00'     INDICATE SVC ROUTINE            40364
         MVI   TMOPCODE,12        POST OP-CODE TO OBTAIN 'TMVT' ADDR
         BAL   R2,CALLSVCY        ISSUE SVC Y
         LTR   R10,R1             Q. TMS ACTIVE
         BNP   INACTIVE           A. NOT POSITIVE - NO
         MVC   TMSHASH,0(R1)      POST ADDR OF TMSHASH
*
         BAL   R11,DATERTN        GET DATE
         TITLE 'MAIN LOOP CONTROL'
*
PROCESS  DS    0H
         L     R12,0(,R5)         LOAD DCB ADDRESS                40173
         USING IHADCB,R12         MAP DCB                         40173
         TM    DCBOFLGS,X'03'     Q. ACTION REQUIRED              40173
         BNO   NEXTDCB            A. BITS NOT ON - NO             40173
         L     R4,4(,R7)          POINT TO IEC WORK AREA
         L     R8,DXDEBUCB        POINT TO UCB
         TM    JFCBFLG1,JFCSTAND  Q. LABEL ERROR                  40173
         BO    LPROCESS           A. BIT ON - YES                 40173
         TM    JFCBFLG1,JFCSLCRE  Q. CREATE LABEL
         BO    LPROCESS           A. BIT ON - YES
         TM    JFCBFLG1,JFCSLDES  Q. DESTROY - LABEL
         BO    LPROCESS           A. BIT ON - YES
NEXTDCB  DS    0H
         MVI   IOAREA2,X'FF'      SET ONE TIME SWITCH
         TM    0(R5),X'80'        Q. LAST ENTRY
         BO    RETURN             A. BIT ON - YES
         LA    R5,4(,R5)          POINT TO NEXT DCB
         LA    R7,8(,R7)          POINT TO NEXT WTG SEGMENT
         B     PROCESS            DO NEXT DCB
         TITLE 'RETURN'
INACTIVE DS    0H
RETURN   DS    0H
         L     R0,SPFDWORK        GET LENGTH OF WORK AREA
         LR    R1,R6              GET ADDR OF WORKAREA
         DROP  R6
         USING SVCXWORK,R1
         LM    R2,R12,SAVE+28     LOAD REGS
         L     R13,SAVE+4         LOAD R13
         L     R14,SAVE+12        RESTORE R14
         FREEMAIN R,LV=(0),A=(1)  FREE WORKAREA
         LA    R15,DXCCW12        POINT AT PARM LIST
         LR    R1,R15             INIT XCTL PARMS
         XCTL  MF=(E,(1)),SF=(E,(15))  RETURN
         DROP  R1
         USING SVCXWORK,R6        RESET MAP
         TITLE 'DETERMINE TYPE OF LABEL CONFLICT'
***********************************************************************
*                   GET VOLSER FROM OPERATOR                          *
***********************************************************************
LPROCESS DS    0H
         AIF   ('&OMOD' EQ 'YES').OMODY01
         B     NOMSG              DISMOUNT ALL LABEL CKS
.OMODY01 ANOP
         SPACE 3
         TM    UCBSTAT,UCBDADI    Q. SL MOUNTED
         BO    DPROCESS           A. BIT ON - YES
         TM    UCBSTAB,UCBBSTR    Q. AL MOUNTED
         BO    DPROCESS           A. BIT ON - YES
         TM    JFCBFLG1,JFCSTAND  Q. SL BUT NOT SL ERROR          40173
         BO    GETVOL1            A. BIT ON - YES TREAT AS NL     40173
         TM    JFCBFLG1,JFCSLDES  Q. DESTROY - LABEL
         BO    NOMSG              A. BIT ON - YES  TMS DISMOUNT
GETVOL1  DS    0H
         MVC   WTO(LMSG1),IECTMS1 POST 'ENTER VSN'
         MVC   WTO+12+8(3),UCBNAME
         BAL   R11,ASKOPR          GET VSN FROM OPERATOR
CHECKVSN DS    0H
         MVC   SVCXTEMP(6),THEREPLY  SAVE REPLY
         BAL   R11,ASKOPR           GET SECOND REPLY FROM OPERATOR
         CLC   THEREPLY,SVCXTEMP   Q. SAME RESPONSE
         BNE   CHECKVSN            A. NOT EQUAL-COME ON DO IT RIGHT
         LA    R1,THEREPLY         POINT TO VSN
         B     CHECKTMC            CHECK TMC
         SPACE 3
***********************************************************************
*              TAPE REQUIRES LABEL DESTRUCT
***********************************************************************
         SPACE 3
DPROCESS DS    0H
         AIF   ('&OMOD' EQ 'YES').OMODY02
         B     NOMSG              DISMOUNT ALL LABEL CKS
.OMODY02 ANOP
         CLC   =C'VOL1',VOLLABI   Q. IS THIS A VOLUME LABEL
         BNE   GETVOL1            A. UNEQUAL - NO
         LA    R1,VOLSERNO        POINT TO VOLSER
         B     CHECKTMC           GO CHECK
         TITLE 'CHECK THE TMC FOR LABEL CHANGE APPROVAL'
***********************************************************************
*
*              CHECK VOLUMN IN TMC
*
***********************************************************************
         SPACE 3
CHECKTMC DS    0H
         XC    TMRECORD(TMCLRECL),TMRECORD  CLEAR I/O AREA
         MVC   TMVOLSER,0(R1)     SAVE VOLSER
         MVI   REJECT,68          SET REJECT CODE
         CLC   JFCBFLSQ,=H'1'     Q. FILE SEQ 1
         BH    DISMOUNT           ABEND BY OS
NOFILCK  DS    0H
         L     R15,TMSHASH        TMSHASH ADDRESS
         BALR  R14,R15            VERIFY VOLSER
         LTR   R1,R1              Q. VALID VOLSER
         BZ    NOTINTMC           A. ZERO - NO
         ST    R1,TMRELBLK        POST REL RECORD ADDRESS
         LA    R1,TMRECORD        GET I/O AREA ADDRESS
         ST    R1,TMSPARM         STORE
         MVI   TMOPCODE,24        POST READ TMC OP-CODE
         MVI   TMOPBITS,X'00'     POST NO ENQUEUE
         BAL   R2,CALLSVCY        READ THE TMC RECORD
         CLC   JFCBXPDT,=X'620000' Q. 98000 VOL
         BNE   INTMC              A. UNEQUAL - NO
         CLI   TMDSSIF,X'00'      Q. PASSWORD PROTECTED
         BNE   CKPSWRD            A. UNEQUAL - YES
GETREPLY DS    0H
         MVC   WTO(LMSG2),IECTMS2       MOVE  VERIFY NOT IN TMC
         MVC   WTO+12+08(3),UCBNAME     POST UNIT ADDRESS
         MVC   WTO+12+12(6),TMVOLSER    POST VOLSER
ISSUE2   DS    0H
         BAL   R11,ASKOPR         VERIFY 98000 VOLUMN
         CLI   THEREPLY,C'U'      Q. OPERATOR APPROVE VOL
         BE    NOTINTMC           A. EQUAL - YES
         CLI   THEREPLY,C'M'      Q. OPERATOR DISAPPROVE VOL
         BNE   ISSUE2             A. UNEQUAL - LOOP UNTIL CORRECT
*
         MVI   REJECT,04          SET REJECT CODE
         B     DISMOUNT           DISMOUNT VOLUMN
*
NOTINTMC DS    0H
         MVI   REJECT,08          SET REJECT CODE
         CLC   JFCBXPDT,=X'620000' Q. 98000 SPECIFIED
         BNE   DISMOUNT            A. UNEQUAL - NO
         B     NEXTDCB            GO TO NEXT ENTRY
INTMC    DS    0H
         TM    TMFLAG1,TMDELET    Q. DELETED VOLUME
         BO    GETREPLY           A. BIT ON - YES
*
         MVI   REJECT,20
         CP    TMEXPDT,=P'99365'  Q. PERMANENT HOLD TAPE
         BNL   DISMOUNT           A. EQUAL - YES
*
         MVI   REJECT,24          SET REJECT CODE
         CLI   TMOUTAR,C' '       Q. OUT OF AREA
         BH    DISMOUNT           A. HIGH - YES
*
CHKEXPDT DS    0H
         MVI   REJECT,32          SET REJECT CODE
         TM    TMFLAG1,TMSCRTCH   Q. VOLUMN SCRATCHED
         BNO   DISMOUNT           A. BIT OFF - NO
         CP    TMEXPDT,DATE       Q. VALID - DATE
         BH    DISMOUNT           A. HIGH - NO
CKPSWRD  DS    0H
         MVI   REJECT,72          SET REJECT CODE
         CLI   TMDSSIF,X'00'      Q. ANY PASSWORD PROTECTION
         BNE   DISMOUNT           A. UNEQUAL - YES
         AIF   ('&OSREL' GE 'VS202.0').MVSVOL
         CLC   DXVOLMT1,=C'SCRTCH'     Q. SCRATCH REQUEST
         AGO   .ENDVOL
.MVSVOL  ANOP
         CLC   DXVOLSR2,=C'SCRTCH' Q. SCRTCH REQUASTED
.ENDVOL  ANOP
         BNE   NOMSG              A. UNEQUAL - , DISMT NO MSG
ISSUE6   DS    0H
         MVC   WTO(LMSG6),IECTMS6 POST LABEL CHANGE APPROVED
         MVC   WTO+04+8(3),UCBNAME      POST UNIT NAME
         MVC   WTO+04+12(6),TMVOLSER    POST VOLSER
         BAL   R11,WTOMSG              WRITE APPROVED MESSAGE
         B     NEXTDCB                 PROCESS NEXT DCB
DISMOUNT DS    0H
         SR    R1,R1              CLEAR
         IC    R1,REJECT          INSERT REJECT CODE
         CVD   R1,SVCXTEMP        COHVERT TO DECIMAL
         OI    SVCXTEMP+7,X'0F'   SET SIGN
         MVC   WTO(LMSG3),IECTMS3       POST NOT SCRATCH MESSAGE
         MVC   WTO+4+08(03),UCBNAME     POST UNIT ADDRESS
         MVC   WTO+04+12(6),TMVOLSER    POST VOLSER
         UNPK  WTO+4+34(02),SVCXTEMP(8) POST 'REJECT CODE'
         BAL   R11,WTOMSG               WRITE TO OPERATOR
NOMSG    DS    0H
         AIF   ('&OSREL' GE 'VS202.0').MVS1
         LA    R15,4              SET OPEN DISMOUNT RETURN
         MVC   SAVE+40(4),SAVE+4  RESET RETURN REG 5
         ST    R15,DXREGE         POST
         ST    R4,SVCXTEMP        STORE WA ADDRESS                40231
         CLC   SVCXTEMP+1(3),SAVE+37    Q. FIRST DCB              40231
         BNE   NOTFIRST           A. UNEQUAL - NO
         ST    R15,SAVE+12        POST IN REG 14
NOTFIRST DS    0H
.MVS1    ANOP
*
         OI    UCBDMCT,UCBMOUNT         POST FOR REMOUNT
*
         B     NEXTDCB            PROCESS NEXT DCB
         TITLE 'SUBROUTINES'
***********************************************************************
*                                                                     *
*                       SUB-ROUTINES                                  *
*                                                                     *
***********************************************************************
DATERTN  DS    0H
         TIME  DEC                GET DATE
         SRL   R0,12              SET TIME TO '0HHMMF'
         STM   R0,R1,TIMEDATE     POST DATE AND TIME
         BR    R11                AND RETURN
*
WTOMSG   DS    0H
         WTO   MF=(E,WTO)         ISSUE MESSAGE
         BR    R11                RETURN
         SPACE 3
*
ASKOPR   DS    0H
         MVI   WTORECB,X'00'      CLEAR ECB
         XC    THEREPLY,THEREPLY  CLEAR REPLY
         WTOR  ,THEREPLY,6,WTORECB,MF=(E,WTO)
         WAIT  ECB=WTORECB
         OC    THEREPLY,=CL6' '   FORCE UPPER CASE
         CLC   =C'J ',THEREPLY    Q. RE-ISSUE MESSAGE
         BE    ASKOPR             A. EQUAL - YES
         BR    R11                RETURN
*
*
CALLSVCY DS    0H
         LA    R1,TMSPARM         POINT TO PARAMLIST
         SVC   SVCYYY             SVC Y
         BR    R2                 RETURN
*
         TITLE 'CONSTANTS AND MESSAGES'
***********************************************************************
*                       CONSTANTS AND MESSAGES                        *
***********************************************************************
LTORG1   LTORG ,
         DS    0F
SPFDWORK DC    X'FD',AL3(LSVCXWRK+72)
         AIF   ('&OSREL' GE 'VS202.0').MVSRET
OPENRET  DC    CL8'IFG0193C'      NON-MVS OPEN RETURN MODNAME
         AGO   .ENDRET
.MVSRET  ANOP
OPENRET  DC    CL8'IGG0190A'      MVS OPEN RETURN MODNAME
.ENDRET  ANOP
         DC    0F'0'
IECTMS1  WTOR  'IECTMS1 DDD,      ,ENTER VSN',MF=L,                    *
               ROUTCDE=3
LMSG1    EQU   *-IECTMS1
         SPACE 3
IECTMS2  WTOR  'IECTMS2 DDD,VVVVVV,VERIFY TAPE FROM OUTSIDE LIBRARY',  *
               MF=L,ROUTCDE=3
LMSG2    EQU   *-IECTMS2
         SPACE 3
IECTMS3  WTO   'IECTMS3 DDD,VVVVVV,IS NOT SCRTCH (XX)',MF=L,           *
               ROUTCDE=3
LMSG3    EQU   *-IECTMS3
         SPACE 3
IECTMS6  WTO   'IECTMS6 DDD,VVVVVV,IS APPROVED FOR LABEL CHANGE',      *
               MF=L,ROUTCDE=3
LMSG6    EQU   *-IECTMS6
         EJECT ,
         PADSVC SVC=01X
         TITLE 'DSECTS'
SVCXWORK DSECT ,
         SVCXWORK ,
SAVE     DS    18F                ENTRY REG SAVE AREA
         EJECT ,
         PRINT &PRINT
TMVT     DSECT ,
         TMVT  TYPE=DSECT,TMVTEXT=NO
         EJECT ,
IEC      DSECT ,
         TMSIEC ,
         ORG   DXLBL+X'1BC'
DXREGE   DS    F
         EJECT
IHADCB   DCBD  DSORG=(PS),DEVD=(TA)                               40173
         EJECT ,                                                  40173
UCB      DSECT ,
         IEFUCBOB ,
         END
