MOVBYT   TITLE 'SUBROUTINE WHICH MOVES BYTES WITHIN WORDS'
*
*      THIS PROGRAM MOVES A BYTE STRING FROM A SOURCE FIELD TO A
* TARGET FIELD. OFFSETS FROM THE BEGINNING OF THE SOURCE AND TARGET
* FIELDS MAY BE SPECIFIED. THE LENGTH OF THE BYTE STRING TO BE MOVED
* MUST BE BETWEEN 1 AND 256, INCLUSIVE. UPON COMPLETION OF THIS
* SUBROUTINE, REGISTER 15 CONTAINS 0 IF THE MOVE WAS SUCCESSFUL AND
* 12 IF THE MOVE FAILED. THE CALLING SEQUENCE FOR "MOVBYT" IS AS
* FOLLOWS
*           1. THE SOURCE FIELD
*           2. THE OFFSET FROM THE BEGINNING OF THE SOURCE FIELD
*                  (4-BYTE BINARY)
*           3. THE TARGET FIELD
*           4. THE OFFSET FROM THE BEGINNING OF THE TARGET FIELD
*                  (4-BYTE BINARY)
*           5. THE LENGTH OF THE STRING TO BE MOVED (4-BYTE BINARY)
*
*     THE "MOVBYT" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
MOVBYT   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(72,0)
         REGISTER                  REGISTER EQUATES
         LM    R4,R8,0(R2)         LOAD PARAMETER POINTERS
         L     R8,0(,R8)           LOAD LENGTH OF MOVE
         L     R5,0(,R5)           LOAD SOURCE OFFSET
         L     R7,0(,R7)           LOAD TARGET OFFSET
         AR    R4,R5               COMPUTE STARTING ADDRESS OF SOURCE
         AR    R6,R7               COMPUTE STARTING ADDRESS OF TARGET
         C     R8,=F'256'          IF LENGTH OF MOVE IS GREATER 256
         BH    ERROR                  THEN FLAG AS ERROR
         LTR   R8,R8               IF LENGTH OF MOVE IS NOT POSITIVE
         BNP   ERROR                  THEN FLAG AS ERROR
         BCTR  R8,R0               DECREMENT LENGTH FOR EXECUTE
         EX    R8,MOVEDATA         PERFORM THE MOVE
         LA    R15,0               SET SUCCESSFUL RETURN CODE
         B     EXIT                   AND LEAVE
MOVEDATA DS    0H
         MVC   0(0,R6),0(R4)       MOVE THE DATA PER SPECIFICATION
ERROR    DS    0H
         LA    R15,12              SET RETURN CODE FOR ERROR CONDITION
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(72,0)
         END
