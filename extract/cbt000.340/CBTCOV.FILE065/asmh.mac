*/********************************************************************/
*/*                                                                  */
*/* FOREGROUND ASSEMBLER H PROCESSOR. USE THE MACLIB TO ASSEMBLE     */
*/* AS IT HAS THOSE DYNAM TYPE MACROS IN IT.                         */
*/*                                                                  */
*/********************************************************************/
ASMH     SUPER
         PARSEX
         POSITION MEM
         KEYWORD (MAC,AC,ALIAS)
         KEYWORD (LIB,SYSLMOD),TYPE=DSN
         PARSEEND
         TITLE 'ASM'
FR1      DYNAM FREE,F=(SYSIN,SYSPRINT,SYSUT1,SYSLIB,SYSLIN)
AL0      DYNAM F=SYSTERM,DA=*
AL1      DYNAM F=SYSUT1,SP=(10,2),TYPESP=CYL,UNIT=SYSDA
         TM    TRIG,HVMAC     INSTREAM MACRO?
         BZ    AL2
         MVI   AL2DDP+11,C'2'
         MVI   AL2DDP+5,X'06' NEW LENGTH
ALM      DYNAM F=SYSIN,DA=SYSUID.MACLIB.ASM,MEM=DUMMY
AL2      DYNAM F=SYSIN,DA=SYSUID.LIB.ASM,MEM=X
         TM    TRIG,HVMAC
         BZ    ARNCON2
C2       DYNAM CONCAT,F=(SYSIN,SYSIN2)
ARNCON2  DS    0H
AL3      DYNAM F=SYSLIB,                                               X
               DA=(SYSUID.MACLIB.ASM,SYS1.MACLIB)
AL4      DYNAM F=SYSPRINT,SP=(30,10),TYPESP=TRK,UNIT=SYSDA,            X
               RECFM=FBM,LRECL=121,BLKSIZE=6050
AL6      DYNAM F=SYSLIN,SP(20,10),TYPESP=TRK,DISP=MOD,UNIT=SYSDA
         LA    1,ASMPARM
         LINK  EP=IEV90
         STH   15,SV15
         MOVE  RCASM,SV15
         TPUT  ASMRC,10
         LH    15,SV15
         C     15,=F'4'
         BNH   CKPRT
         B     GOERRASM
ASMRC    DC    C'ASM RC='
RCASM    DC    CL3' '
SV15     DC    H'0'
CKPRT    DS    0H
         TM    TRIG,HVPRT
         BO    GOPRT
         TPUT  WANTPRT,4
         TGET  ANS,80
         OI    ANS,C' '       SHIFT UP
         CLI   ANS,C'Y'
         BE    GOPRT
RETPRT   TM    TRIG,NOLINK
         BZ    GOLINK
         B     FR8
GOPRT    DS    0H
         LH    R1,AL2MP+4     MEMBER LENGTH
         BCTR  1,0            MACH LENGTH
         EX    1,MVCMOUT      MOVE IT INTO PARM
         LA    2,PPRT1+3(1)   POINT PAST NAME
         MVC   0(29,2),RESTPARM
         LA    1,35(1)
         STH   1,PPRT1
         LA    1,PRTPARM
         CALL  PRT
         B     RETPRT
         DS    0F
PRTPARM  DC    X'80',AL3(PPRT1)
PPRT1    DC    H'0',CL44' '
WANTPRT  DC    C'PRT?'
ANS      DC    CL80' '
MVCMOUT  MVC   PPRT1+2(1),AL2MP+6
RESTPARM DC    C' DDN(SYSPRINT) BLKP DATE TIME'
         TITLE 'ASM ERROR'
GOERRASM DS    0H
         OI    TRIG,NOLINK
         OPEN  PRINT
GS       GET   PRINT
         LA    4,1(1)
         CLC   15(5,4),=C'ERROR'
         BE    PUTPUT
         LR    5,4
         B     GS
ENDS     DS    0H
         CLOSE PRINT
         OI    TRIG,ASMERR
         OI    TRIG,NOLINK
         B     CKPRT
FR8      DYNAM FREE,F=SYSLIN
         B     FR2
PUTPUT   TPUT (5),119
         TPUT  (4),119
         B     GS
PRINT    DCB   DDNAME=SYSPRINT,EODAD=ENDS,MACRF=GL,DSORG=PS
         TITLE 'LKED'
GOLINK   DS    0H
FR2      DYNAM FREE,F=(SYSIN,SYSLIB,SYSPRINT,SYSTERM)
         TM    TRIG,ASMERR
         BO    ALL
AL5      DYNAM F=SYSLIB,DA=SYSUID.LIB.LOAD
ALS      DYNAM F=SYSLMOD,DA=SYSUID.LIB.LOAD
AL9      DYNAM F=SYSPRINT,DA=SYSUID.PRT2.DATA
         OPEN  (SYSLIN,OUTPUT)
         PUT   SYSLIN,PAGE
         PUT   SYSLIN,ENTRY
         PUT   SYSLIN,SETCODE
         CLI   SETALIAS,C' '
         BE    NOALIAS
         PUT   SYSLIN,ALIASCRD
NOALIAS  DS    0H
         PUT   SYSLIN,NAME
         CLOSE SYSLIN
         LA    1,LKEDPARM
         LINK  EP=IEWL
         STH   15,SV15
         MOVE  LKRC,SV15
         TPUT  RCLK,11
FR5      DYNAM FREE,F=(SYSLMOD,SYSLIN,SYSPRINT,SYSLIB)
ALL      DYNAM F=(SYSIN,SYSPRINT),DA=*
ENDUP    SUPRET RC=(15)
         TITLE 'SET MEMBER NAME'
MEM      DS    0H
         LM    3,4,0(1)       GET NAME AND LENGTH
         STH   R4,AL2MP+4     LENGTH OF MEMBER
         BCTR  R4,0
         EX    R4,MVCMEM2
         EX    R4,MVCPAGE
         EX    R4,MVCENTRY
         EX    R4,MVCNAME
         LA    R1,NAME+7(R4)
         MVC   0(3,R1),=C'(R)'
         BR    R14
MVCMEM2  MVC   AL2MP+6(1),0(3)
MVC63    MVC   0(1,6),0(3)
MVCPAGE  MVC   PAGE+6(1),0(R3)
MVCENTRY MVC   ENTRY+7(1),0(R3)
MVCNAME  MVC   NAME+6(1),0(R3)
MVCPRE2  MVC   0(1,6),PREFIX
SAVENAME DC    F'0'
         SPACE 3
SYSLMOD  DS    0H
         LM    3,4,0(1)
         STH   4,ALSDAP+4     LENGTH
         BCTR  4,0
         EX    4,MVCSYSLM
         BR    14
MVCSYSLM MVC   ALSDAP+6(1),0(3)
         SPACE 3
LIB      DS    0H
         LM    3,4,0(1)       SYSLIB FOR LKED
         STH   4,AL5DAP+4
         BCTR  4,0
         EX    4,MVCLIB
         BR    14
MVCLIB   MVC   AL5DAP+6(1),0(3)
         BR    14
         SPACE 3
MAC      DS    0H
         LM    3,4,0(1)
         STH   4,ALMMP+4
         BCTR  4,0
         EX    4,MVCMPM
         OI    TRIG,HVMAC
         BR    14
MVCMPM   MVC   ALMMP+6(1),0(3)
TRIG     DC    H'0'
HVMAC    EQU   X'80'
ASMERR   EQU   X'40'
HVPRT    EQU   X'20'
NOLINK   EQU   X'10'
         SPACE 3
PRT      DS    0H
         OI    TRIG,HVPRT
         BR    14
AC       LM    R3,R4,0(R1)
         MVC   SETCODE+12(1),0(R3)
         BR    R14
         SPACE 3
ALIAS    LM    R3,R4,0(R1)
         BCTR  R4,0
         EX    R4,MVCALIAS
         BR    R14
MVCALIAS MVC   SETALIAS(1),0(R3)
         TITLE 'DATA AREAS'
SYSLIN   DCB   DDNAME=SYSLIN,DSORG=PS,MACRF=PM
         DS    0F
ASMPARM  DC    X'80',AL3(P1)
LKEDPARM DC    X'80',AL3(P2)
P1       DC    H'18',C'TERM,NODECK,OBJECT'
P2       DC    H'9',C'XREF,CALL'
PAGE     DC    CL80' PAGE'
ENTRY    DC    CL80' ENTRY'
ALIASCRD DC   CL80' ALIAS '
         ORG   ALIASCRD+7
SETALIAS DC    C' '
         ORG   ALIASCRD+80
SETCODE  DC    CL80' SETCODE AC(0)'
NAME     DC    CL80' NAME'
RCLK     DC    C'LKED RC='
LKRC     DC    CL3' '
DOUBLE   DC    D'0'
         SUPEND
