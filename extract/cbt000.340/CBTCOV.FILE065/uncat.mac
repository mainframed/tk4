*          DATA SET TSOUNCAT   AT LEVEL 001 AS OF 04/03/73
         TITLE 'UNCAT - UNCATALOG TSO COMMAND PROCESSOR'
***********************************************************************
*
* UNCATALOG - TSO COMMAND - 11/28/72 BY W. S. FINKELSTEIN
*
* SYNTAX:
*              UNCAT  'DSNAME'  CVOL(CVOLID)
*
*              DSNAME - MAY BE A LIST OF DATASET NAMES OF STD FORM
*              CVOL   - OPTIONAL CONTROL VOLUME SERIAL
*
***********************************************************************
UNCAT    TSOENTER REGS=YES,PL=(CPPL,PARSE)
         TITLE 'UNCAT - CALL PARSE SERVICE ROUTINE.'
         XC    PPLRANS(4),PPLRANS .     ZERO PARSE ANS AREA
         XC    TSOECB(4),TSOECB .       ZERO TSO ECB
         L     R15,CPPLCBUF .           LOAD COMMAND BUFF
         TSOPARSE CBUF=(15),PCL==V(PCL) CALL PARSE ROUTINE
         LTR   R15,R15 .                MAKEUM BOOBOO?
         BNZ   HOBOY
         TITLE 'UNCAT - PROCESS THE OUTPUT OF TSOPARSE'
         L     R11,PPLRANS .            ESTAB ADDR OF ANSWER FLDS
         LUSE  PDL,R11
         SPACE 1
* LOOK AT PDLCVOL RETURN FIELD (FOR CVOL SPECIFICATION)
         SPACE 1
         TM    PDLCVOL+6,X'80' .        PARAMETER PRESENT?????
         BZ    NOCVOL
         SPACE 1
* THE FOLLOWING CODE SETS UP THE CVOL PNTR IN THE CAMLST
         SPACE 1
         OI    CAMLST,X'80' .           TURN ON WEGOTACVOL FLAG
         LH    R1,PDLCVOL+4 .           LOAD CVOL LENGTH
         BCTR  R1,0 .                   DEC BY ONE FOR MVC
         L     R2,PDLCVOL .             PNTR TO CHAR STRING
         EX    R1,MVCCVOL
MVCCVOL  MVC   CVOLID(1),0(R2) .        * * EXECUTED MVC * *
         LA    R1,CVOLID .              CVOL STRING
         ST    R1,CAMLST+8 .            STASHEM INTO CAMLST
         TITLE 'UNCAT - DATASET NAME ANALYSIS ROUTINE.'
NOCVOL   LA    R10,PDLDSN .             POINT TO FIRST DSN
         SPACE 1
DSNLOOP  TM    6(R10),X'80' .           IS DSN PRESENT
         BZ    HOBOY .                  NO, WE GOT TROUBLES
         MVI   DSNAME,C' '
         MVC   DSNAME+1(L'DSNAME-1),DSNAME . RIPPLE BLANK
         LA    R4,DSNAME .              TARGET OF MVC
         TM    6(R10),X'40' IS FULLY QUAL....
         BO    FQNAME .                 YA, DON'T APPEND T.S. ID
         SPACE 1
* MUST PREFIX WITH ID
         SPACE 1
         L     R5,CPPLPSCB .            POINT TO PSCB
         SR    R6,R6 .                  FOR ID LENGTH
         IC    R6,7(R5) .               LOAD LENGTH
         LA    R4,1(R4,R6) .            REM, OF DSN
         BCTR  R6,0 .                   FOR EX
         EX    R6,IDMVC
IDMVC    MVC   DSNAME(1),0(R5) .        MOVE IN DSNAME
         LA    R6,DSNAME+1(R6) .        FOR DOTPLACE
         MVI   0(R6),C'.' .             FOR ID PRE INDEX
         SPACE 1
FQNAME   LH    R1,4(R10) .              LOAD LENGTH OF DSN
         BCTR  R1,0 .                   DEC BY ONE FOR MVC
         L     R2,0(R10) .              LOAD PNTR TO DSNAME STR
         EX    R1,DSNMVC .              DOEM MOVE
DSNMVC   MVC   0(1,R4),0(R2)
         SPACE 1
* CHECK FOR GDG GEN NUMBER (MEMBER NAME)
         SPACE 1
         TM    14(R10),X'80' .          IS IT THERE???
         BZ    GOCHECK .                CHECK VALIDITY OF USE
         SPACE 1
* IF GDG NUM IS THERE, CONCAT TO END OF DSN IN PARENS
         SPACE 1
         LA    R2,DSNAME .              FIND PLACE TO CONCAT
         AH    R2,4(R10) .              PNT TO END OF DSNAME
         MVI   0(R2),C'(' .             OPEN PAREN
         LH    R1,12(R10) .             LENGTH OF MEMNAME
         BCTR  R1,0 .                   DEC BY ONE FOR MVC
         L     R3,8(R10) .              POINT R3 TO MEMNAME
         EX    R1,GDGMVC .              MOVE IN GDG NAME
GDGMVC   MVC   1(1,R2),0(R3)
         AH    R2,12(R10) .             POINT TO LAST CHAR OF GDG NAME
         MVI   1(R2),C')' .             CLOSE PARENS
         TITLE 'UNCAT - CHECK ACCESS AND TRY ROUTINE'
GOCHECK  BCR   0,0                  NOP DSNAME CHECK
         SPACE 1
* TRY TO DO THE UNCATLG
         SPACE 1
CAT      CATALOG CAMLST .               TRY UNCATALOG
         CH    R15,=H'28' .             BIG ERROR
         BE    HOBOY .                  YA,REALLY GRIPE
         B     BRTAB(R15) .             ELSE USE BRANCH TABLE
BRTAB    B     GOODY .                  RC=0 A.O.K
         B     NOCVOLM .                RC=4 NO CVOL
         B     NOTTHERE .               RC=8 IN CONSIS. DSN
         B     HOBOY .                  RC=12 UNUSED
         B     INCON .                  RC=16 MISSING INDEX
         B     HOBOY .                  RC=20 OUT OF SPACE IN CAT
         B     HOBOY .                  RC=24 CATLG BAD GDG
         B     HOBOY .                  RC=28 PERM I/O ERROR
         TITLE 'UNCAT - YE OLD NEXT VOL CHECK ROUTINE'
GOODY    CLI   24(R10),X'FF' .          NULL LINK???
         BE    LEAVE .                  YUP, TAKE IT ON LAMB
         L     R10,24(R10) .            LOAD PNTR TO NEXT DSNPDL
         LA    R10,0(R10) .             OFF UPPER BYTE
         B     DSNLOOP .                ROUND AND ROUND
         TITLE 'UNCAT - ERROR ROUTINES'
HOBOY    TPUT  M1,L'M1,ASIS
         B     TONEXT
         SPACE 1
NOCVOLM  TPUT  M2,L'M2,ASIS
         B     TONEXT
         SPACE 1
INCON    TPUT  M3,L'M3,ASIS
         B     TONEXT
         SPACE 1
NOTTHERE TPUT  M4,L'M4,ASIS
         SPACE 3
TONEXT   TPUT  DSNAME,L'DSNAME .        OUTPUT DSNAME
         B     GOODY .                  CHECK NEXT DATASET
         SPACE 1
LEAVE    IKJRLSA PPLRANS .              FREEUM WORK AREA
         TSOLEAVE
         EJECT
CAMLST   CAMLST UCATDX,DSNAME .         CAMLIST FOR CATLG
         SPACE 1
DSNAME   DC    CL56' '
CVOLID   DC    CL6' '
         TITLE 'UNCAT - ERROR MESSAGES AND OTHER STUFF'
M1       DC    C'FATAL ERROR PROCESSING DATASET - '
M2       DC    C'CONTROL VOLUME NOT MOUNTED FOR DATASET - '
M3       DC    C'INCONSISTANT OR INVALID DATASET NAME - '
M4       DC    C'DATASET NOT FOUND IN CATALOG - '
         TITLE 'UNCAT - LITERALS'
         LTORG
         TITLE 'UNCAT - PARSE PARAMETER LIST'
PCL      IKJPARM DSECT=PDL .            DEFINE PARSE PARM LIST
PDLDSN   IKJPOSIT DSNAME,LIST,PROMPT='DATASET NAME'
PDLCVK   IKJKEYWD
         IKJNAME 'CVOL',SUBFLD=PDLCVN
         IKJNAME 'CONTROL',SUBFLD=PDLCVN
         SPACE 1
PDLCVN   IKJSUBF
PDLCVOL  IKJIDENT 'CVOL',FIRST=ALPHANUM,OTHER=ALPHANUM,MAXLNTH=6,      *
               PROMPT='CONTROL VOLUME SERIAL'
         SPACE 1
         IKJENDP
         CVT   DSECT=YES
         END   UNCAT
