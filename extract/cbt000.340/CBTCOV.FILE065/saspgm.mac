*  THIS SAS PROGRAM IS DESIGNED TO READ IN THE OUTPUT
   TO DDNAME VTOCOUT FROM THE VTOC COMMAND WITH
   THE FOLLOWING OPTIONS.
    VTOC  VOLUMES  NOSORT PRINT(ADD (UNUSED USED)) NOHEAD

   THEN REFORMAT AND REPORT SOME STATISTICS, BREAK OUT
   REPORTS BY THE FIRST THREE CHARACTERS OF THE DSNAME.
   IT ALSO GIVES DISTRIBUTIONS OF DATA SETS, SPACE USED,
   AND SPACE ALLOCATED FOR THESE SAME GROUPS, AND LISTS
   THE LARGEST DATA SETS AND THOSE WITH THE MOST UNUSED
   SPACE.                   ;
DATA DSN ; *   READ THE VTOC COMMAND INPUT AND REPORT IT  ;
INFILE  VTOC  ;
INPUT   @1 CHARCON $8.  @  ;  IF CHARCON  = '0 TOTALS'  THEN DELETE ;
DROP    CHARCON        ;
INPUT   @2 ALLOC 6. +1 UNUSED 6. +1 USED 6. +1 PCT 3. +1
        EXT 3. +1 DSORG $5. +1 RECFM $5. +1 BLKSZ 5. +1
        LRECL 5. +1 CDATE 5. +1 LSTUS 5. +1 APPLIC $3.
        @67 DSNAME $44. +1 VOLUME $6. +1
        SECQ 5. +1 SECT $4. +1 ROUND $5. +1 PASS $4. +1
        ACTION $8.   ;
IF ALLOC  = . THEN ALLOC  = 0  ;
IF UNUSED = . THEN UNUSED = 0  ;
IF USED   = . THEN USED   = 0  ;
IF BLKSZ  = . THEN BLKSZ  = 0  ;
IF LRECL  = . THEN LRECL  = 0  ;
PROC MEANS DATA=DSN N MEAN STD MIN MAX SUM ;
  TITLE  CHARACTERISTICS OF THE DATA SET POPULATION;
PROC SORT  DATA=DSN ;  BY APPLIC DSNAME ;
DATA _NULL_  ;
SET DSN  END=ENDODATA;  BY APPLIC  ;
IF NOT FIRST.APPLIC THEN GO TO NOTFIRST  ;
   SUBTOT = 0  ;
   SUBUSED = 0  ;
   SUBUNUSD = 0 ;
NOTFIRST:
SUBTOT+ALLOC  ;  SUBUSED+USED ;   SUBUNUSD+UNUSED;
TOTAL + ALLOC ;
FILE PRINT  LINE=LINECT ;
IF LINECT = 1 THEN
   PUT ' DSNAME    '     @46
       ' ALLOC  USED   UNUSED LRECL BLKSZ VOLUME CDATE'
       ' LSTUS EXT DSORG RECFM SECQ  SECT ROUND  '        ;
   PUT @2 DSNAME @47 ALLOC @54 USED @61 UNUSED @68 LRECL
       @74 BLKSZ @80 VOLUME @87 CDATE @93 LSTUS
       @99 EXT @103 DSORG @109 RECFM @115 SECQ @121 SECT
       @127 ROUND                   ;
IF NOT LAST.APPLIC THEN GO TO NOTLASTA ;
   PUT // ' APPLICATION CODE ' APPLIC ' HAS ALLOCATED ' SUBTOT
          ' KILOBYTES OF DATA, USING ' SUBUSED ' KILOBYTES, WITH '
          SUBUNUSD   ' KILOBYTES UNUSED '  ;
   PUT _PAGE_  ;
   PUT ' DSNAME    '     @46
       ' ALLOC  USED   UNUSED LRECL BLKSZ VOLUME CDATE'
       ' LSTUS EXT DSORG RECFM SECQ  SECT ROUND  '        ;
NOTLASTA:
IF ENDODATA THEN PUT ///// TOTAL ' KILOBYTES TOTAL FOR ALL DATA SETS' ;
TITLE  DATA SETS, SORTED BY APPLICATION CODES, DSNAMES ;
TITLE2 ALLOC AND USED ARE SPECIFIED IN KILOBYTES;
PROC FREQ DATA=DSN;
  TABLES EXT DSORG;
   TITLE  EXTENTS AND DATA SET ORGANIZATIONS  ;
PROC FREQ DATA=DSN  ;
   TABLES APPLIC    ;
   TITLE  DATA SETS FOR EACH APPLICATION   ;
PROC FREQ DATA=DSN  ;
   TABLES APPLIC    ; WEIGHT ALLOC         ;
   TITLE  KILOBYTES ALLOCATED FOR EACH APPLICATION  ;
PROC FREQ DATA=DSN  ;
   TABLES APPLIC    ; WEIGHT USED          ;
   TITLE  KILOBYTES USED FOR EACH APPLICATION ;
PROC SORT DATA=DSN ; BY DESCENDING ALLOC;
PROC PRINT DATA=DSN(OBS=100)   ;
   VAR ALLOC USED PCT DSNAME VOLUME CDATE LSTUS EXT DSORG RECFM
       BLKSZ LRECL ;
   TITLE  THE 100   LARGEST DATA SETS ;
PROC SORT DATA=DSN; BY DESCENDING UNUSED;
PROC PRINT DATA=DSN(OBS=100)   ;
   VAR UNUSED ALLOC PCT DSNAME VOLUME CDATE LSTUS EXT DSORG RECFM
       BLKSZ LRECL ;
  TITLE  THE 100   DATA SETS WITH MOST UNUSED SPACE (APPROX);
