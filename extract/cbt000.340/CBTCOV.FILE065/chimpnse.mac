         TITLE 'CHIMP - MAIN STORAGE MONITOR FOR TSO - 3270S'
CHIMP    CSECT
* THIS IS THE NON-SE VERSION OF CHIMP
***********************************************************************
* IN CASE YOU ARE INTERESTED, THE SOURCE OF THE NAME (OR RATHER THE
* JUSTIFICATION FOR THE NAME) OF THIS SILLY PROGRAM IS/WAS:
*
*  COMPUTERIZED HEURISTIC INFORMATION MONITORING PROGRAM  (WHEW!)
*
* THIS COMMAND HAS SEVERAL DIFFERENT MODES OF DISPLAY:
*
*     I   - THIS MODE WILL DISPLAY ONLY THOSE MEMORIES WHICH ARE EITHER
*           SWAPPED IN AND RUNNING OR SWAPPED OUT BUT READY TO RUN.
*     O   - THIS MODE WILL DISPLAY ALL ACTIVE MEMORIES IN THE SYSTEM,
*           REGARDLESS OF LOCATION.
*
*   TO SWITCH BETWEEN MODES, TYPE EITHER I OR O.
*
*
*      YOU MAY ALSO ENTER OPTIONS TO RESTRICT THE OUTPUT TO ONLY
*      TSO MEMORIES (IN EITHER IN/OUT OR WAITING STATUS ACCORDING
*      TO YOUR USE OF THE I AND O OPTIONS ABOVE) OR ONLY STARTED
*      TASK/BATCH JOB MEMORIES:
*
*     T  - RESTRICTS THE OUTPUT TO ONLY TSO MEMORIES
*
*     B  - RESTRICTS THE OUTPUT TO STARTED TASK/BATCH MEMORIES
*
*     A  - RESETS THE TSO/STARTED TASK/BATCH JOB SELECTION.
*
*   TO EXIT FROM THIS COMMAND, SIMPLY TYPE S
*
*  GLOSSARY OF TERMS FOR OUTPUT DISPLAY:
*
*    JOB       JOB NAME OF MEMORY
*    STEP      FOR BATCH JOBS WILL ALWAYS BE INIT.
*              FOR TSO JOBS AND STARTED TASKS WILL BE ACTUAL STEP NAME.
*    P         REASON CODE FOR SWAP OUT FROM OUCBPFL.
*              THE FOLLOWING ARE THE REASON CODES DISPLAYED:
*                R  RESET VALUE.
*                W  MEMORY IS IN LONG WAIT OR INPUT TERMINAL WAIT.
*                T  MEMORY IS IN OUTPUT TERMINAL WAIT.
*                E  AN ENQHOLD HAS BEEN RECEIVED FOR THIS MEMORY.
*                V  MEMORY WAS SWAPPED OUT BY MS6 LONG WAIT DETECTION.
*                O  MEMORY WAS SWAPPED OUT TO MAKE ROOM FOR TSO TRIV.
*                P  INVOLUNTARY SWAPOUT USUALLY DUE TO PARTIAL ANAL.
*                S  SWAPOUT CAUSED BY REAL STORAGE SHORTAGE.
*                ?  UNABLE TO DETERMINE SWAPOUT REASON.
*    MEMORY    CURRENT AMOUNT OF MEMORY ALLOCATED TO THIS MEMORY.
*    WSS       SRM'S VIEW OF THE WORKING SET SIZE FOR THIS MEMORY.
*    T         TYPE OF MEMORY (S=STC OR BATCH, T=TSO USER)
*    LOC       CURRENT LOCATION OF THIS MEMORY:
*                IN  SWAPPED IN AND ELIGIBLE TO RUN.
*                OUT SWAPPED OUT BUT READY TO RUN.
*                WT  SWAPPED OUT AND NOT READY TO RUN.
*                N/S SWAPPED IN AND V=R OR NON-SWAPPABLE STATUS.
*                <-> TRANSITIONING BETWEEN STATES.
*    DP        DISPATCHING PRIORITY OF MEMORY (IN HEX).
*    PG        PERFORMANCE GROUP.
*    SC        NUMBER OF TIMES THE MEMORY HAS BEEN SWAPPED IN THE
*              CURRENT TRANSACTION.
*    WMR       CURRENT SRM WORKLOAD MANAGER RECOMMENDATION VALUE.
*              (ACTUALLY THE WORKLOAD LEVEL AT WHICH THIS MEMORY IS
*               OPERATING).
*    SRV       SERVICE ACCUMULATED IN THE LAST SWAPPED IN PERIOD.
*    CPU       CPU TIME IN SECONDS USED BY THE MEMORY (SRB PLUS TASK).
***********************************************************************
*
         SAVE  (14,12)          SAVE THE CALLERS REGISTERS
         LR    12,15            ESTABLISH
         USING CHIMP,12           ADDRESSABILITY.
         LA    11,SAVE          SET-UP
         ST    13,SAVE+4          SAVE
         ST    11,8(,13)             AREA
         LR    13,11                  CHAINING.
FINDCVT  L     2,16             LOAD PTR TO CVT.
         L     2,556(,2)        TO ASVT
         L     3,516(,2)        MAXIMUM NUMBER OF ENTRIES
         LA    4,524(,2)        FIRST ENTRY MINUS 4.
         LA    1,BUFFER         LOAD ADDRESS OF OUTPUT BUFFER.
ASCBLOOP LA    4,4(,4)          BUMP BY FOUR.
         ICM   5,B'1111',0(4)   LOAD THE PTR.
         BM    NOGOOD           BRANCH IF BAD PTR.
         TM    102(5),X'04'     TEST FOR SWAPPED OUT.
INOROUT  BO    NOGOOD           BRANCH IF OUT AND NOT READY TO RUN.
         L     6,144(,5)         LOAD PTR TO OUCB FOR QUICK TSO CHK
         TM    18(6),X'20'      CHECK FOR TSO.
TSOORNO  BC    0,NOGOOD         TO BE FILLED IN IF BATCH ONLY.
BATCHORN BC    0,NOGOOD         TO BE FILLED IN LATER.
         MVI   LINE+2,C' '      CLEAR OUTPUT LINE TO BLANKS.
         MVC   LINE+3(77),LINE+2 DITTO.
         MVC   JOB(8),=CL8'STARTING' TO BYPASS GARBAGE
         ICM   6,B'1111',172(5) LOAD PTR TO JOBNAME.
         BZ    NOTJOB           BRANCH IF ZERO POINTER.
         MVC   JOB,0(6)         OTHERWISE, MOVE IN JOBNAME.
         B     JOBOK            BYPASS FURTHER JOBNAME PROCESSING.
NOTJOB   ICM   6,B'1111',176(5) PTR IF S/M/L.
         BZ    JOBOK            BYPASS THIS MOVE IF POINTER IS ZERO.
         MVC   JOB,0(6)         MOVE IN JOBNAME FOR S/M/L MEMORIES.
JOBOK    DS    0H
         ICM   7,B'1111',56(5)  LOAD POINTER TO THE TOP CSCB.
         BZ    STEPOK           BRANCH IF CSCB POINTER IS ZERO.
         CLI   4(7),X'04'       CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    STORMT           JOB IS START OR MOUNT
         CLI   4(7),X'0C'       CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    STORMT           JOB IS START OR MOUNT
         MVC   STEP(8),16(7)    PICK UP CHCLS.
         B     STEPOK           BYPASS FURTHER CSCB PROCESSING.
STORMT   MVC   STEP(8),8(7)     PICK UP CHKEY.
STEPOK   DS    0H
         LH    6,152(,5)         LOAD ALLOC FRAME COUNT.
         SLL   6,2              MULTIPLY BY FOUR.
         CVD   6,WORK           CONVERT TO DECIMAL FORMAT.
         MVC   SIZE(6),=X'2020202120D2' GET READY FOR EDIT.
         ED    SIZE-1(6),WORK+5 EDIT THE RESULT.
         L     6,144(,5)        PTR TO OUCB.
         LH    7,58(,6)          LOAD OUCBWSS
         SLL   7,2              TIMES FOUR FOR K.
         CVD   7,WORK           CONVERT TO DECIMAL.
         MVC   OUCBWSS(6),=X'2020202120D2' MOVE IN THE EDIT MASK.
         ED    OUCBWSS-1(6),WORK+5 EDIT THE RESULT.
         TM    18(6),X'20'      TEST FOR TSO MEMORY
         BZ    NOTTSO           BRANCH IF NOT TSO.
         MVI   TYPE,C'T'        MOVE IN TSO MEMORY INDICATION.
         B     TYPEOK           BYPASS REST OF TYPE PROCESSING.
NOTTSO   MVI   TYPE,C'S'        MOVE IN S/M/L INDICATION.
TYPEOK   DS    0H
         MVC   WHERE,=CL3'IN'   DEFAULT MEMORY POSITION IS IN.
         MVI   LINE+1,X'E8'     FOR FULL SCREEN EDIT.
         TM    17(6),X'80'      TEST FOR NON-SWAP
         BZ    TRYWAIT          BRANCH IF NOT NON-SWAP.
         MVC   WHERE,=CL3'N/S'  MOVE IN NON-SWAP INDICATION.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYWAIT  DS    0H
         TM    16(6),X'08'      CHECK FOR WAIT
         BZ    TRYOUT           BRANCH IF NOT.
         MVC   WHERE,=CL3'WT'   MOVE IN WAIT POSITION INDICATION.
         MVI   LINE+1,X'60'     MOVE IN 3270 HI-INTENSITY ATTR BYTE.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYOUT   TM    16(6),X'04'      TRY OUT QUEUE
         BZ    TRYTRIV          IF NOT, GO TRY TRIVIAL QUEUE STATUS.
         MVC   WHERE,=CL3'OUT'  MOVE IN OUT QUEUE POSITION.
         MVI   LINE+1,X'60'     MOVE IN 3270 HI-INTENSITY ATTR BYTE.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYTRIV  TM    16(6),X'02'      CHECK FOR TRIVIAL QUEUE POSITION.
         BZ    WHEREOK          IF NOT, EXIT POSITION PROCESSING.
         MVC   WHERE,=CL3'TRV'  MOVE IN TRIVIAL QUEUE POSITION.
WHEREOK  DS    0H
         TM    16(6),X'E0'      CHECK FOR TRANSITIONING STATUS
         BZ    NOTTRANS         BRANCH IF MEMORY NOT TRANSITIONING.
         MVC   WHERE(3),=CL3'<->' INDICATE TRANSITIONING.
NOTTRANS DS    0H
         SR    7,7              PREPARE FOR THE INSERT CHARACTERS.
         IC    7,43(,5)         LOAD ASCBDP
         SRL   7,4              SHIFT OUT LOW ORDER NIBBLE.
         IC    8,HEX(7)         PICK THE EBCDIC CHARACTER.
         STC   8,DP             STORE INTO OUTPUT FIELD.
         IC    7,43(,5)         LOAD THE DISPATCHING PRIORITY AGN.
         N     7,=F'15'         TURN OFF HIGH ORDER NIBBLE.
         IC    8,HEX(7)         LOAD THE EBCDIC CHARACTER.
         STC   8,DP+1           STORE INTO OUTPUT LINE.
         SR    7,7              PREPARE FOR THE IC.
         IC    7,24(,6)         NEW PGN
         CVD   7,WORK           CONVERT IT TO DECIMAL.
         MVC   OUCBPGN(3),=X'202120' MOVE IN THE EDIT MASK.
         ED    OUCBPGN-1(4),WORK+6 EDIT THE OUTPUT FIELD.
         LH    7,30(,6)         LOAD OUCBSWC (SWAP COUNT)
         CVD   7,WORK           CONVERT IT TO DECIMAL.
         MVC   OUCBSWC(3),=X'202120' MOVE IN THE EDIT MASK.
         ED    OUCBSWC-1(4),WORK+6 EDIT THE OUTPUT FIELD.
         LH    7,72(,6)         LOAD OUCBWMR
         LTR   7,7              SEE IF HI-ORDER BIT IS ON
         BNM   WMROK             INDICATING WMR INVALID.
         MVC   OUCBWMR(5),=C'*N/A*' IF SO, MOVE IN NOT AVAIL INDIC.
         MVI   OUCBWMS+4,C'-'   AND BLANK OUT THE SERVICE COUNTER.
         B     NOWMS            BY-PASS THE SERVICE PROCESSING.
WMROK    DS    0H
         SRL   7,8              DIVIDE BY 256
         CVD   7,WORK           CONVERT IT TO DECIMAL.
         MVC   OUCBWMR(5),=X'2020202120' MOVE IN EDIT MASK.
         ED    OUCBWMR-1(6),WORK+5 EDIT THE RESULT.
WMROKA   DS    0H
         MVC   STATUS(1),71(6)  OUCBPFL (SWAP REASON CODE)
         TM    16(6),X'0E'      SEE IF OUT SOMEHOW
         BC    9,PFLDONE        MUST BE IN.
         TM    21(6),X'20'      CHECK FOR TERMINAL OUTPUT WAIT.
         BNO   NOTOUTW          BR IF NOT.
         MVI   STATUS,C'T'      INDICATE OUTPUT WAIT.
         B     PFLDONE
NOTOUTW  DS    0H
         CLI   71(6),C'R'       SEE IF R WHEN OUT.
         BNE   PFLDONE          SKIP REST OF PFL PROCESSING.
         TM    21(6),X'01'      SEE IF MS6 DETECTED WAIT.
         BNO   NOTMS6           BR IF NOT
         MVI   STATUS,C'V'      MOVE IN MS6 REASON CODE.
         B     PFLDONE
NOTMS6   TM    21(6),X'08'      SEE IF ENQHOLD AGAINST THIS MEMORY.
         BNO   NOTENQ           BR IF NOT
         MVI   STATUS,C'E'      MOVE IN ENQHOLD REASON CODE.
         B     PFLDONE
NOTENQ   TM    18(6),X'08'      SEE IF CTL URGES SWAP BIT IS ON.
         BNO   NOTCTL
         MVI   STATUS,C'C'      MOVE IN CTL REASON CODE
         B     PFLDONE
NOTCTL   MVI   STATUS,C'?'      MOVE IN DONT KNOW INDICATION.
PFLDONE  DS    0H
         L     7,44(,6)         OUCBWMS LOAD SERVICE UNITS THIS XACTION
         CVD   7,WORK           CONVERT TO DECIMAL.
         MVC   OUCBWMS(5),=X'2020202120' MOVE IN THE EDIT MASK.
         ED    OUCBWMS-1(6),WORK+5 EDIT THE RESULT.
NOWMS    LM    8,9,64(5)        LOAD UP EJST (CPU TIME - TASK TYPE)
         SRDL  8,12             TO GET MICROSEC
         LM    10,11,200(5)     LOAD UP SRB TIME.
         SRDL  10,12            TO GET MICROSEC
         AR    9,11             ADD TOGETHER.
         CVD   9,WORK           CONVERT IT TO DECIMAL.
         MVC   CPUTIME(8),=X'20202021204B2020' MOVE IN THE EDIT MASK.
         ED    CPUTIME-1(9),WORK+2 EDIT THE RESULT.
         MVC   0(81,1),LINE     MOVE THE LINE TO THE 3270 BUFFER.
         LH    7,SCRSIZE        BUMP BUFFER COUNTER.
         LA    7,81(,7)
         STH   7,SCRSIZE
         LA    1,81(,1)         SEE IF THE BUFFER IS FULL YET.
         C     1,ADBUF
         BE    PUTIT            IF YES, THE GO PUT THIS SCREEN.
NOGOOD   BCT   3,ASCBLOOP       BRANCH BACK TO LOOK AT NEXT MEMORY.
PUTIT    LA    1,CLEAR          PREPARE
         LA    0,LENGTH            FOR
         AH    0,SCRSIZE              THE
         ICM   1,8,TPUTFLG               FULL SCREEN
         TPUT  (1),(0),R                    TPUT
         TGET  BUFFER,1         READ THE INPUT DATA FROM USER.
         OI    BUFFER,X'40'     GET TO UPPER CASE BEFORE COMPARE.
         CLI   BUFFER,C'S'      CHECK IF TERMINATION REQUESTED.
         BE    EODAD            BYE BYE BABY...
         CLI   BUFFER,C'T'      CHECK FOR TSO ONLY.
         BNE   BATCHCHK         IF NOT, TRY FOR BATCH ONLY MODE.
         MVI   BATCHORN+1,X'80' RESET THE BRANCHES TO GET
         MVI   TSOORNO+1,X'00'     ONLY TSO MEMORIES.
         B     GOODGRIF         BY-PASS FURTHER INPUT CHECKING.
BATCHCHK CLI   BUFFER,C'B'      CHECK FOR BATCH ONLY
         BNE   OTHERCHK         BRANCH TO CHECK OTHER OPTIONS.
         MVI   TSOORNO+1,X'10'  RESET THE BRANCHES TO GET
         MVI   BATCHORN+1,X'00'    ONLY BATCH (STC) MEMORIES.
         B     GOODGRIF         BY-PASS FURTHER INPUT CHECKING.
OTHERCHK CLI   BUFFER,C'A'      CHECK FOR EVERYTHING
         BNE   XYZ              IF NOT, TRY FOR IN OR OUT MODES.
         MVI   TSOORNO+1,X'00'  IF EVERYTHING, NOP ALL THE
         MVI   BATCHORN+1,X'00'    SELECTION BRANCHES.
XYZ      DS    0H
         CLI   BUFFER,C'O'      CHECK FOR OUT MODE
         BNE   WHATHECK
         MVI   INOROUT+1,X'00'  NOP THE BRANCH
         MVI   MODESW,X'FF'     INDICATE CHANGE OF MODES.
         B     GOODGRIF
WHATHECK CLI   BUFFER,C'I'      CHECK FOR IN OR READY MODE
         BNE   GOODGRIF
         MVI   INOROUT+1,X'10'  TAKE THE BRANCH
         MVI   MODESW,X'FF'     INDICATE MODE SWITCH.
GOODGRIF DS    0H
         MVC   SCRSIZE,=H'0'    RESET SCREEN SIZE.
         LA    1,BUFFER         PREPARE
         LA    6,BUFFER            FOR
         ICM   6,8,C' '               THE
         LA    7,1863                    MOVE
         LA    8,BUFFER                     CHARACTER
         SR    9,9                             LONG
         MVCL  6,8                                TO CLEAR BUFFER.
         CLI   MODESW,X'FF'     CHECK FOR CHANGE OF MODES.
         BNE   MODEOK           BRANCH IF SAME MODE.
         MVI   MODESW,X'00'     RESET MODE CHANGE.
         B     FINDCVT          START OVER FROM BEGINNING.
MODEOK   DS    0H
         LTR   3,3              CHECK TO SEE IF MORE MEMORIES TO PROCES
         BNZ   NOGOOD           BRANCH IF YES.
         B     FINDCVT          OTHERWISE, START OVER FROM THE TOP.
EODAD    DS    0H
BYEBY    LA    1,CLR            PREPARE FOR THE
         LA    0,L'CLR             FULL SCREEN
         ICM   1,8,TPUTFLG            TPUT TO
         TPUT  (1),(0),R                 CLEAR THE SCREEN.
         L     13,SAVE+4        LOAD CALLERS SAVE AREA POINTER.
         RETURN (14,12),RC=0    AND AWAY WE GO...
MODESW   DC    X'00'  MODE CHANGE.
WORK     DS    D
HEX      DC    C'0123456789ABCDEF'
SAVE     DS    18F
X        DC    CL80' '
         ORG   X
LINE     DC    X'1D60'
JOB      DC    CL8' '
         DC    C' '
         DC    C' '
STEP     DC    CL8' '
         DC    C' '
STATUS   DC    C' '
         DC    C' '
SIZE     DC    CL5' '
         DC    CL2' '
OUCBWSS  DC    CL5' '
         DC    CL2' '
TYPE     DC    C' '
         DC    C' '
WHERE    DC    CL3' '
         DC    C' '
DP       DC    CL2' '
         DC    C' '
OUCBPGN  DC    CL3' '
         DC    C' '
OUCBSWC  DC    CL3' '
         DC    C' '
         DC    C' '
         DC    C' '
OUCBWMR  DC    CL5' '
         DC    C' '
OUCBWMS  DC    CL5' '
         DC    C' '
CPUTIME  DC    CL8' '
         DC    C' '
         ORG X+90
SCRSIZE  DC    H'0'
TPUTFLG  DC    X'0B'
ADBUF    DC    A(Z)
CLR      DC    X'C71140403C40400011404013'
CLEAR    DC    X'C71140403C4040001140401D4013'
         DC    X'401DE8'
HEADING DC CL77'  JOB     STEP   P MEMORY   WSS  T LOC DP  PG  SC     WX
               MR   SRV     CPU'
BUFFER   DC    23CL81' '
LENGTH   EQU   BUFFER-CLEAR
Z        DS    X
         END   CHIMP
