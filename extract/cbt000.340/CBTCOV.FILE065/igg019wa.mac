 TITLE 'END OF EXTENT APPENDAGE FOR SQUISH VOL PROGRAM              '
IGG019WA CSECT
         USING *,15
         L     13,CODE         GET CODE WORD
         C     13,40(2)        SEE IF IT IS IN THE PROGRAM
         BNER  14              IF NOT, THEN JUST RETURN
         CLI   44(2),X'FF'     SEE IF WE'VE TRIED BEFORE
         BER   14              IF SO, THEN JUST GET OUT
         XC    38(4,3),38(3)   WIPE OUT EXTENT START
         MVI   41(3),1         START AT TRACK 1
         L     13,44(2)        GET FMT4 ADDRESS
         LH    10,48(2)        GET # OF CYLS
         LH    11,64(13)       GET # OF TRACKS / CYL IN 11
         LR    13,10           PUT # CYLS IN 13 TOO
         MR    12,11           GET TOTAL # OF TRACKS IN 13
         BCTR  13,0            REDUCE FOR TRACK 0
         STH   13,46(3)        SAVE # OF TRACKS IN DEB
         BCTR  10,0            REDUCE TO LAST CYL ADDRESS
         STH   10,42(3)        SAVE IN DEB
         BCTR  11,0            REDUCE TO LAST TRACK ADDRESS
         STH   11,44(3)        SAVE IN DEB
         MVI   44(2),X'FF'     TURN ON SWITCH TO SAY WE WERE HERE
         B     8(14)           GO RE-TRY CHANNEL PROGRAM
         DS    0F
CODE     DC    C'SQUV'         CODE WORD FOR MODULE EXECT
         END
