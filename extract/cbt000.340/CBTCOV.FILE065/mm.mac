MM       TITLE 'DCMS MASTER MONITOR ROUTINE'
         SPACE 4
*
*     THIS PROGRAM ALLOWS EASY SWITCHING BETWEEN VARIOUS DCMS
* COMMANDS.  INITIAL DISPLAY GIVES LIST OF COMMANDS AVAILABLE.
* AS MANY CHARACTERS AS ARE ENTERED ARE MATCHED AGAINST COMMAND
* TABLE.  AMBIGUOUS ENTRIES GET THE FIRST ONE THAT MATCHES.
*
*     THE TERMIO2 MODULE (TSO 3270 HANDLER) HAS BEEN MODIFIED
* SO THAT IT CAN DETECT THE 'PRESENCE' OF MM.  WHEN THE USER
* ENTERS A SINGLE INPUT TO ANY DCMS COMMAND WHICH BEGINS WITH
* A '=' (EQUALS), THE REMAINDER OF THE STRING IS MOVED TO MM'S
* CONTROL AREA (POINTED TO BY FSA+40) AND A PA1 INDICATION IS
* PASSED TO THE DCMS COMMAND (WHICH SHOULD EXIT, ACCORDING TO
* DCMS STANDARDS).
*
*     MM THEN REGAINS CONTROL, TAKES THE CONTROL AREA INPUT
* AS A NEW COMMAND, AND THE CYCLE CONTINUES.
*
*  PFK USAGE :
         SPACE 3
*
*       ENTER = READ INPUT STRING AS COMMAND
*        ATTN = EXIT (UNLESS SIMULATED PA1 VIA TERMIO2)
*           3 = EXIT                                               LDW1
*          12 = EXIT
*    (DEFAULT = NO CHANGE)
         SPACE 3
*  UPDATES:
*    11/25/81 LDW4 - RE-WORK MENU GENERATION TO ALLOW SUPPRESSION OF
*                       PRIVILEGED COMMANDS
*                  - ADD MORE COMMANDS
*    03/11/81 LDW3 - RE-ARRANGE "LINK" CODE
*                  - ADD STLINENO MACROS
*                  - UPDATE MENU
*    06/26/80 LDW2 - FIX 'END', GENERAL CLEANUP
*    06/02/80 LDW1 - ADD PF3, 'END' COMMAND
         TITLE 'MAIN LINE CODE'
MM       DCMSTART R,LV=4096,STAE=NO,IOCB=YES
         SPACE 2
         MVC   CPPLSAVE(16),0(R1)       SAVE CPPL FOR 'SUB'COMMANDS
*LDW3    MVC   COPYCODE(CODELEN),CODE   MOVE LINK CODE TO WORK AREA
*
         MVI   FLAGS,0                  INITIALIZE FLAGS           LDW4
         L     R2,16                    -> CVT
         L     R2,0(,R2)                -> DISPATCH
         L     R2,4(,R2)                -> TCB
         L     R15,180(,R2)             -> JSCB                    LDW4
         L     R15,264(,R15)            -> PSCB                    LDW4
***      TM    PSCBATR1,PSCBCTRL        PRIVILEGED USER?           LDW4
         TM    16(R15),X'80'            PRIVILEGED USER?           LDW4
         BNO   *+8                      NO - SKIP                  LDW4
         OI    FLAGS,FPRIVUSR           YES - REMEMBER             LDW4
         L     R2,112(,R2)              FIRST SAVE AREA (FSA),
*                                       USED BY TERMIO2
*
         MVC   44(4,R2),=C'MM  '        FLAG TO TERMIO2
         LA    R3,RETCMD                RETURNED COMMAND WORK AREA
         ICM   R3,8,=AL1(L'RETCMD)      LENGTH OF WORK AREA
         ST    R3,40(,R2)               SAVE FOR TERMIO2
*
* ANALYZE COMMAND INPUT STRING
*
         MVC   MSGLINE,BLANKS           (BUT FIRST CLEAR MESSAGE LINE)
*
         L     R1,0(,R1)                ADDR OF COMMAND STRING
         LH    R2,0(,R1)                COMMAND STRING LENGTH
         LH    R3,2(,R1)                OFFSET OF SUBCOMMANDS
         SR    R2,R3                    SUBTRACT OFFSET FROM TOTAL LEN
         LA    R1,4(R1,R3)              MOVE R1 TO BEGINNING OF SUBCMD
         SH    R2,=H'5'                 SUB LEN OF HEADER + 1 FOR EX
         BNM   CMD                      GOT ONE...PROCESS IT.
         SPACE 3
INITDISP LA    R14,SCREEN               -> DEST                    LDW3
         LA    R15,SCREENL              LENGTH                     LDW3
         LA    R0,=C'MM - MASTER MONITOR - ENTER COMMAND, PF3 TO EXIT'
         LA    R1,48                    LENGTH                     LDW4
         MVCL  R14,R0                   COPY HEADER, BLANK REST    LDW3
         MVC   COLHDRS(17),=C'COMMAND   DISPLAY'                   LDW4
         SR    R3,R3                    OFFSET OF FIRST MENU COL   LDW4
         SPACE 1
MENU$COL LA    R0,2                     NUMBER OF COLUMNS IN MENU  LDW4
         LA    R1,FIRST(R3)             -> FIRST MENU LINE         LDW4
         LA    R15,LAST+L'LAST          -> LAST+1 MENU LINE        LDW4
         SR    R15,R1                   = # OF MENU LINES * 79     LDW4
         SR    R15-1,R15-1              CLEAR FOR DIVIDE           LDW4
         D     R15-1,=F'79'             = # OF MENU LINES          LDW4
         LA    R14,CMDTABLE             -> COMMAND TABLE           LDW4
         SPACE 1
MENU$LP  CLI   0(R14),C' '              RESTRICTED OR INVISIBLE?   LDW4
         BE    MENU$GO                  NO - SKIP                  LDW4
         TM    FLAGS,FPRIVUSR           PRIVILEGED USER?           LDW4
         BNO   MENU$NXT                 NO - SKIP THIS ONE         LDW4
MENU$GO  MVC   0(8,R1),1(R14)           MOVE COMMAND NAME          LDW4
         MVC   10(CMDTBLEN-17,R1),17(R14)  AND DESCRIPTION         LDW4
         LA    R1,79(,R1)               -> NEXT DISPLAY LINE       LDW4
MENU$NXT LA    R14,CMDTBLEN(,R14)       -> NEXT TABLE ENTRY        LDW4
         CLI   0(R14),X'FF'             END OF COMMAND TABLE?      LDW4
         BE    DISPLAY                  YES - GO DISPLAY MENU      LDW4
         BCT   R15,MENU$LP              FILL THIS COLUMN           LDW4
         LA    R3,40(,R3)               -> NEXT COLUMN             LDW4
         MVC   COLHDRS+40(39),COLHDRS   PUT IN 2ND COLUMN HEADER   LDW4
         B     MENU$COL                 DO NEXT COLUMN             LDW4
         SPACE 3
DISPLAY  NI    IOCBFLGS,255-IOCBFNRD    TURN OFF 'NOREAD'
         NI    IOCBFLGD,255-IOCBDYON-IOCBDFRC
         OI    IOCBFLGC,IOCBCCLR+IOCBCRAO+IOCBCFRA
         MVI   RETCMD,X'00'             TO TELL IF TERMIO2 HAS SET IT
         TRMIO IOCB,FORMAT=FORMAT1,IMAGE=SCREEN
*
         MVC   MSGLINE,BLANKS           CLEAR MSGS BEFORE PROCEEDING.
*
         PFKEY DEF=DCMSOVER,ATTN=ATTN,K12=EXIT,ENTER=ENTER,K3=EXIT LDW1
         SPACE 5
*  TEST FOR PSEUDO ATTENTION
ATTN     CLI   RETCMD,X'00'             DID TERMIO2 GIVE US DATA?
         BNE   CHKNEW                   ...YES, USER INPUT '=...'
         B     EXIT                     ...NO, MUST BE REAL ATTENTION.
         SPACE 3
ENTER    LA    R1,ENTERLN               START OF INPUT LINE
ENTERNEW LA    R5,78(,R1)               END OF LINE
         LA    R2,78                    MAXIMUM SCAN LENGTH
LEADING  CLI   0(R1),C' '               SKIP LEADING BLANKS
         BNE   ENTER2
         LA    R1,1(,R1)
         BCT   R2,LEADING               TO A CERTAIN EXTENT
         B     INITDISP                 ...THEN RESTART
*
*  R1 = BEG OF CMD
ENTER2   LR    R3,R1                    CURRENT POS
SUBLOOP  LA    R3,1(,R3)                LOOK FOR BLANK -
         CLI   0(R3),C' '               ...END OF CMD
         BE    ENTER3                   GOT IT.
         BCT   R2,SUBLOOP               NO, CONTINUE
         B     CMDINVAL
*
ENTER3   LR    R2,R5                    LAST CHAR OF INPUT LINE
         LA    R4,79                    MAXIMUM LENGTH
TRAIL    CLI   0(R2),C' '               FIND LENGTH
         BNE   ENTER4                   ...GOT LAST NON-BLANK
         BCTR  R2,0
         BCT   R4,TRAIL
         B     INITDISP                 VERY STRANGE.
*
ENTER4   SR    R3,R1                    LENGTH UP TO FIRST BLANK
         CH    R3,=H'8'                 IS IT TOO LONG?
         BH    CMDINVAL                 ...YES, ERROR.
*
         SR    R2,R1                    TOTAL LEN - 1 FOR EXEC
         MVC   CMDBUFF,BLANKS           CLEAR WORK AREA FOR CMD
         EX    R2,MOVENEW               MOVE WHOLE INPUT TO COMMAND BUF
         EX    R2,UPTRAN1               AND TRANSLATE TO UPPER CASE
         LA    R4,5(,R2)                TOTAL LEN + 4 FOR HEADER
         STH   R4,NEWCMD                BUILD NEW COMMAND BUFFER
*
         CR    R3,R2                    IF R3<R2, THEN SUBCMDS EXIST
         BL    SUBS
         LA    R4,1(,R2)                LENGTH OF COMMAND NAME
         STH   R4,NEWCMD+2              STORE IN CMD BUFF OFFSET FIELD
         B     CMD
*
SUBS     LR    R2,R3                    RESET R2 FOR EXEC OF COMPARE
         BCTR  R2,0                     ( -1 FOR EXEC )
         AR    R3,R1                    -> OF INTERVENING BLANK(S)
SUBS2    LA    R3,1(,R3)
         CLI   0(R3),C' '               FIRST NON-BLANK
         BNE   SUBS3                    ...MEANS FOUND SUB-CMD START
         B     SUBS2
*
SUBS3    SR    R3,R1
         STH   R3,NEWCMD+2              SUBCOMMAND OFFSET
*
CMD      EX    R2,UPTRAN
         CLC   =C'END ',0(R1)           WANT OUT?                  LDW1
         BE    EXIT                     YES - LET HIM OUT          LDW1
         LA    R3,CMDTABLE              START OF COMMAND LIST
*
CMDLOOP  CLI   0(R3),C'*'               RESTRICTED COMMAND?        LDW4
         BNE   CMDTRY                   NO - TRY IT                LDW4
         TM    FLAGS,FPRIVUSR           PRIVILEGED USER?           LDW4
         BNO   CMDSKIP                  NO - DON'T TRY IT          LDW4
CMDTRY   EX    R2,CMDCOMP               IS IT THIS ONE?
         BE    CMDMATCH                 ...YES
CMDSKIP  LA    R3,CMDTBLEN(,R3)         NO...TRY NEXT ONE          LDW4
         CLI   0(R3),X'FF'              LAST ONE?
         BNE   CMDLOOP                  ...NO, CONTINUE.
*
CMDINVAL MVC   MSGLINE(L'BADCMD),BADCMD ...YES, GAVE BAD INPUT
         B     INITDISP                 TELL HIM, AND RETRY.
*
CMDCOMP  CLC   0(*-*,R1),1(R3)          *EXECUTED*                 LDW4
MOVENEW  MVC   CMDBUFF(*-*),0(R1)       *EXECUTED*
UPTRAN   TR    0(*-*,R1),UPTRT          *EXECUTED*
UPTRAN1  TR    CMDBUFF(*-*),UPTRT       *EXECUTED*
*
CMDMATCH LA    R3,9(,R3)                -> ENTRY POINT NAME        LDW4
         LA    R2,NEWCMD                NEW COMMAND BUFFER
         ST    R2,CPPLSAVE              UPDATE CPPL
         MVI   RETCMD,X'00'             SO CAN TELL IF THERES SOMETHING
*
*LDW3    BAL   R9,COPYCODE              GO LINK
*                                                                  LDW3
         STLINENO  MODE=OFF,LINE=3      GET OUT OF FULLSCREEN      LDW4
*
         XC    LINKSFL(LINKSFLL),LINKSFL  CLEAR SF=L AREA          LDW3
         LA    R1,CPPLSAVE              PARAMETER LIST FOR COMMAND LDW3
*
         LINK  EPLOC=(R3),ERRET=LINKFAIL,SF=(E,LINKSFL)            LDW3
*
         LR    R9,R15                   SAVE RETURN CODE           LDW3
*
         STLINENO  MODE=ON,LINE=23      SET FULLSCREEN BACK ON     LDW3
*
         LTR   R15,R9                   ZERO RETURN CODE?          LDW3
         BZ    CHKNEW                   ...YES, GO CHECK FOR NEW CMD
*
         ST    R15,HEXWORK1             FOR SUBRTN
         BAL   R14,@HEX                 CONVERT TO PRINT
         MVC   MSGLINE(L'BADRC),BADRC
         MVC   MSGLINE+L'BADRC(L'RETCODE),HEXWORK2+8-L'RETCODE
*
*  LOOK FOR NEW CMD IN TERMIO2 BUFFER
CHKNEW   LA    R1,RETCMD                INITIALIZE FOR CMD SCAN
         CLI   RETCMD,X'00'             ANYTHING PUT IN?
         BE    INITDISP                 ...NO, CARRY ON.
*
         CLC   RETCMD,BLANKS            ANYTHING ENTERED?
         BNE   ENTERNEW                 ...YES, GOT COMMAND
         B     INITDISP                 ...NO, NO NEW COMMAND.
         SPACE 3
LINKFAIL ST    R1,HEXWORK1
         BAL   R14,@HEX
         MVC   MSGLINE(L'BADLINK),BADLINK
         MVC   MSGLINE+L'BADLINK(L'RETCODE),HEXWORK2+8-L'RETCODE
*                                                                  LDW3
         STLINENO  MODE=ON,LINE=23      GET OUT OF FULLSCREEN      LDW3
*
         B     INITDISP
         SPACE 3
* IF FIRST CHAR OF TABLE ENTRY IS '*', THE COMMAND IS RESTRICTED.  LDW4
*    TO PRIVILEGED USERS.                                          LDW4
* IF FIRST CHAR OF TABLE ENTRY IS '$', THE COMMAND IS INVISIBLE    LDW4
*    TO UN-PRIVILEGED USERS.                                       LDW4
CMDTABLE DC    C' CHANNEL CHANNEL CHANNEL UTILIZATION         '
CMDTBLEN EQU   *-CMDTABLE               LENGTH OF TABLE ENTRY      LDW4
         DC    C' CHIMP   CHIMP   JOB/USER STATUS (DETAILED)  '
         DC    C'*CONS    CONS    SPY ON MCS CONSOLE          '    LDW4
         DC    C'$CORE    CORE    DISPLAY VIRTUAL STORAGE     '    LDW4
         DC    C' CSAMON  CSAMON  CSA USAGE                   '
         DC    C'$DPAGE   DPAGE   PAGE DATASET USAGE          '    LDW4
         DC    C' DUCB    DUCB    UCB STATUS                  '    LDW3
         DC    C' ENQ     ENQ     SYSTEM AND JOB ENQUEUES     '
         DC    C'$LPALIST LPALIST CONTENTS OF LPA             '    LDW4
         DC    C' MS      DMS     MASTER SUMMARY              '
         DC    C'$PAGE    PAGE    PAGING RATE                 '    LDW4
*LDW4    DC    C' PERFORM PERFORM PERFORMANCE GROUP DATA      '
         DC    C' RS      FRAMES  REAL STORAGE USAGE          '
         DC    C' TOP20   TOP20   JOBS WITH > 1% CPU USAGE    '
         DC    C'$T1      T1      TEST SLOT #1                '
         DC    C'$T2      T2      TEST SLOT #2                '
         DC    C'$WIZARD  WIZARD  SRM, DOMAIN, SWAPPING DATA  '
         DC    X'FF'
*
BADCMD   DC    C'  **ERROR** INVALID COMMAND ENTERED'
BADRC    DC    C'  **WARNING** COMMAND ENDED WITH NON-ZERO RC='
BADLINK  DC    C'  **ERROR** LINK OF COMMAND MODULE FAILED, RC='
RETCODE  DC    CL4' '
*
*CODE    DC    0H'0'                    CODE GETS COPIED INTO WORK AREA
*LDW3    BALR  R14,0
*LDW3    USING *,R14
*LDW3    STM   R0,R13,CODESV            IN CASE CALLED ROUTINE CLOBBERS
*
*LDW3    STLINENO  MODE=OFF,LINE=4      GET OUT OF FULLSCREEN      LDW3
*
*LDW3    LINK  EPLOC=(3),ERRET=LINKFAIL
*
*LDW3    BALR  R14,0                    ESTABLISH
*LDW3    USING *,R14                    ....NEW ADDRESSABILITY
*LDW3    LM    R0,R13,CODESV            TO GET OUR REGS BACK.
*
*LDW3    STLINENO  MODE=ON,LINE=23      SET FULLSCREEN BACK ON     LDW3
*
*LDW3    BR    R9                       AND RETURN TO MAINLINE CODE
*
*CODESV  DC    16F'0'
*CODELEN EQU   *-CODE
*LDW3    DROP  R14
*
UPTRT    DC    256X'40'
         ORG   UPTRT+X'81' LOWER CASE A
         DC    C'ABCDEFGHI',7C' ',C'JKLMNOPQR',8C' ',C'STUVWXYZ'
         ORG   UPTRT+C'A'
         DC    C'ABCDEFGHI',7C' ',C'JKLMNOPQR',8C' ',C'STUVWXYZ'
         ORG   UPTRT+C'0'
         DC    C'0123456789'
         ORG
         TITLE 'SCREEN FORMAT DEFINITION'
FORMAT1  DCMSFMT
SCREEN   DCMSFLD 79                     TITLE
ENTERLN  DCMSFLD 79,ALPHA,INTEN=LO,CURSOR=YES
MSGLINE  DCMSFLD 79                     ERROR MESSAGES
COLHDRS  DCMSFLD 79                     COLUMN HEADERS             LDW4
         DCMSFLD 79,INTEN=LO
FIRST    DCMSFLD 79,INTEN=LO                                       LDW4
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
LAST     DCMSFLD 79,INTEN=LO                                       LDW4
         DCMSFEND
         TITLE 'EXIT ROUTINE AND DSECTS'
EXIT     DCMSTOP RC=(15)
         TITLE 'SCREEN IMAGE AREA (DSECT)'
         DSGEN  FORMAT1
SCREENL  EQU   *-SCREEN                                            LDW3
         SPACE 4
*
FLAGS    DS    X                        MISCELLANEOUS FLAG         LDW4
FPRIVUSR EQU   X'80'                    PRIVILEGED USER            LDW4
*
NEWCMD   DS    2H                       CBUF HDR FOR 'SUB'-COMMAND
CMDBUFF  DS    CL79                     COMMAND STRING
RETCMD   DS    CL79                     CMD AREA FOR RETRN FROM TERMIO2
CPPLSAVE DS    4F                       FOR COPY OF CPPL ON ENTRY TO MM
*OPYCODE DS    XL(CODELEN)              FOR COPY OF LINK CODE.
LINKSFL  LINK  ERRET=*-*,SF=L                                      LDW3
LINKSFLL EQU   *-LINKSFL                                           LDW3
         END
