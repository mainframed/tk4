***********************************************************************
*                                                                     *
*   PROGRAM TO MAKE ALL DIRECT ACCESS VOLUMES MATCH VATLST            *
*   WHEN CHANGES ARE MADE AFTER IPL TIME                              *
*                                                                     *
*   WRITTEN BY AL ARAUJO, JUNE 1976,                                  *
*      FOR THE LOCKHEED-CALIFORNIA COMPANY                            *
*                                                                     *
*   MODIFIED BY DOUG MERCER, JULY 1980,                               *
*      TO BE REENTRANT                                                *
*                                                                     *
*   MODIFIED BY DOUG MERCER, AUGUST 1980,                             *
*      TO BINARY SEARCH THE VATLST TABLE (IF IN SEQUENCE)             *
*                                                                     *
*   MODIFIED BY DOUG MERCER, JANUARY 1984,                            *
*      FOR MVS/XA (IOSVSUCB RATHER THAN UCB LOOKUP TABLE)             *
*                                                                     *
*      THIS PROGRAM IS BEING CALLED IN BY GENERIC AND LOADS IN THE    *
*         VATLIST TO BE COMPARED WITH THE DIRECT ACCESS VOLUMES.      *
*      1.THE CORRESPONDING 'USE' ATTRIBUTE IS ALTERED IN ITS UCB      *
*      2.THE CORRESPONDING 'SHARE' ATTRIBUTE IS ALTERED IN ITS UCB    *
*      3.THE CORRESPONDING 'MOUNT' ATTRIBUTE IS ALTERED IN ITS UCB    *
*                                                     A.R.A. DEC 76   *
*      4.ANY DIRECT ACCESS DEVICE NOT FOUND IN THE VATLST TABLE WILL  *
*        BE MARKED AS PRIVATE AND RESERVE.            A.R.A. JAN 80   *
*                                                                     *
*        ATTRIBUTES - REENTRANT                                       *
*        TAB SETTINGS - 10 16 31 72                                   *
*                                                                     *
***********************************************************************
         SPACE 1
VATUCB   CSECT ,
         USING VATUCB,R12
         USING EXSA,R8
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 1
         SAVE  (14,12)        *SAVE THE REGISTERS
         LR    R12,R15
         SPACE 3                                               DM JUL80
         GETMAIN R,LV=SAVSIZ
         L     R14,=A(SAVEAREA)                                DM JUL80
         LR    R15,R0                                          DM JUL80
         LR    R2,R1          INITIALIZE                       DM JUL80
         LR    R3,R0               THE WORK                    DM JUL80
         MVCL  R2,R14                   AREA                   DM JUL80
         ST    R1,8(,R13)
         ST    R13,4(,R1)
         LR    R13,R1
         USING SAVEAREA,R13                                    DM JUL80
         SPACE 3
         L     R3,VATTBLE                                      DM JUL80
         USING VATLST,R3
         OPEN  VATIN,MF=(E,OCLIST)                             DM JUL80
         BAL   R14,GETVAT     GET A TABLE AREA                 DM JUL80
         SPACE 4
********************************************************************
*   LOAD VATLST TABLE                                              *
INPUT    DS    0H
         C     R3,VATLAST     SEE IF THIS IS LAST SLOT         DM JUL80
         BNH   *+8            NO                               DM AUG80
         BAL   R14,GETVAT     GET A LARGER AREA                DM JUL80
         GET   VATIN          BRING IN VATLST ENTRY            DM JUL80
         LA    R0,1           COUNT                            DM AUG80
         AH    R0,VATCNT           TABLE                       DM AUG80
         STH   R0,VATCNT                ENTRY                  DM AUG80
         MVC   VATLST(VATLNG),0(R1)                            DM JUL80
         CLC   SEQCHK,VOLSER  CHECK SEQUENCE                   DM AUG80
         MVC   SEQCHK,VOLSER       UPDATE SEQUENCE CHECK       DM AUG80
         BL    NEXTBL         OK - IN SEQUENCE                 DM AUG80
         MVC   SEQSER,VOLSER  OUT OF SEQUENCE                  DM AUG80
         LA    R1,SEQWTO      SEND ERROR MESSAGE               DM AUG80
         WTO   MF=(E,(1))                                      DM AUG80
         OI    BITS1,OUTSEQ   INDICATE SEQUENCE ERROR          DM AUG80
NEXTBL   LA    R3,VATLNG(,R3) SET PTR TO NEXT TABLE ENTRY      DM JUL80
         B     INPUT          GO LOAD ANOTHER CARD IN TABLE
END      DS    0H
         MODESET KEY=ZERO                                      DM AUG80
         SPACE 3                                               DM AUG80
********************************************************************
*   BEGIN UCB SEARCH IN VATLST TABLE                               *
         SR    R3,R3          ZERO VATTBLE REGISTER
         XC    UCBWORK,UCBWORK    ZERO WORK AREA               DM JAN84
         LA    R0,UCBWORK         INITIALIZE PARAMETER LIST    DM JAN84
         ST    R0,UCBPARM                                      DM JAN84
         LA    R0,=AL1(UCB3DACC)  DASD DEVICE TYPE             DM JAN84
         ST    R0,UCBPARM+4                                    DM JAN84
         LA    R0,UCBADDR         RETURN ADDRESS               DM JAN84
         ST    R0,UCBPARM+8                                    DM JAN84
         OI    UCBPARM+8,X'80'    SET END OF LIST              DM JAN84
         L     R4,CVTPTR
         USING CVTMAP,R4
         L     R4,CVTUCBSC    ADDR OF UCB SCAN ROUTINE         DM JAN84
         DROP  R4
A0100    DS    0H
         SPACE ,                                               DM AUG80
         LR    R15,R4             CVTUCBSC                     DM JAN84
         LA    R1,UCBPARM                                      DM JAN84
         BALR  R14,R15            SCAN                         DM JAN84
         LTR   R15,R15                                         DM JAN84
         BNZ   ENDSRCH            LAST UCB                     DM JAN84
         SPACE ,                                               DM JAN84
         L     R10,UCBADDR                                     DM JAN84
         USING UCBOB,R10
         CLI   UCBVOLI,X'0'         IS THE VOL ID ZERO?
         BE    A0400
         L     R3,VATTBLE     SET POINTER BACK TO VATTBLE TOP  DM JUL80
         USING VATLST,R3
         LH    R0,VATCNT      SEARCH FOR VOLUME                DM AUG80
         TM    BITS1,OUTSEQ                                    DM AUG80
         BZ    B0100          IN SEQUENCE - BINARY SEARCH      DM AUG80
         SPACE ,                                               DM AUG80
A0300    CLC   VOLSER,UCBVOLI       IS UCB IN VATLST?          DM AUG80
         BE    A0900                YES, GO HAVE 'USE' ATTRIB ALTER'D
         LA    R3,VATLNG(R3)  SET POINTER TO NEXT VATLST ENTRY DM JUL80
         BCT   R0,A0300       GO TRY NEXT VATLST ENTRY         DM AUG80
         B     A1700          NOT FOUND                        DM AUG80
         SPACE 2                                               DM AUG80
A0400    DS    0H
         B     A0100          CONTINUE UCB SEARCH
         SPACE 3
* BINARY SEARCH VATLST TABLE                                   DM AUG80
B0100    SH    R3,=Y(VATLNG)  BACK UP TO ENTRY(0)              DM AUG80
         SPACE ,                                               DM AUG80
B0200    LR    R15,R3         SAVE THIS AS LAST LOW            DM AUG80
B0300    LR    R3,R15         RESET FROM PRIOR LOW             DM AUG80
         LR    R14,R0         DEVELOP ADDRESS INCREMENT        DM AUG80
         LA    R14,1(,R14)         FROM COUNT                  DM AUG80
         SRA   R14,1          HALVE (ROUNDED)                  DM AUG80
         BZ    A1700          NOT FOUND                        DM AUG80
         SR    R0,R14         REMAINING COUNT                  DM AUG80
         MH    R14,=Y(VATLNG)     * ENTRY LENGTH               DM AUG80
         AR    R3,R14         NEXT ADDRESS                     DM AUG80
         CLC   VOLSER,UCBVOLI      SEE IF THIS IS VOLUME       DM AUG80
         BL    B0200          NO - TOO LOW                     DM AUG80
         BH    B0300          NO - TOO HIGH                    DM AUG80
         B     A0900          FOUND                            DM AUG80
         SPACE 2
**********************************************************************
*   CHECK VATLST FOR THE 'USE' ATTRIBUTE OF VOLUME                   *
A0900    DS    0H
         NI    UCBSTAB,255-(UCBBPRV+UCBBSTR+UCBBPUB)   ZERO OUT UCB
         CLI   USE,USESTOR    SHOULD IT BE A STORAGE VOL?      DM JUL80
         BE    A1000          GO ALTER ITS UCB
         CLI   USE,USEPUB     SHOULD IT BE A PUBLIC VOL?       DM JUL80
         BE    A1100          GO ALTER ITS UCB
         CLI   USE,USEPRIV    SHOULD IT BE A PRIVATE VOL?      DM JUL80
         BE    A1200          GO ALTER ITS UCB
         B     A0100          GO GET ANOTHER UCB
         SPACE 2
**********************************************************************
*   ALTER THE UCB TO THE CORRECT 'USE' ATTRIBUTE                     *
A1000    DS    0H
         OI    UCBSTAB,UCBBSTR  ALTER ITS UCB TO EQUAL STORAGE VOL
         B     A1300            GO CHECK SHARE STATUS
         SPACE 1
A1100    DS    0H
         OI    UCBSTAB,UCBBPUB  ALTER ITS UCB TO EQUAL  PUBLIC VOL
         B     A1300            GO CHECK SHARE STATUS
         SPACE 1
A1200    DS    0H
         OI    UCBSTAB,UCBBPRV  ALTER ITS UCB TO EQUAL  PRIVATE VOL
         SPACE 3
***********************************************************************
*   ALTER THE UCB IF 'SHARE' OR 'UNSHR' ATTRIBUTE IS REQUIRED         *
A1300    DS    0H
         CLC   SHRFLAG,=C'UNSHR'  IS THIS VOL UNSHARABLE
         BNE   A1400              CONTINUE SHARE CHECK
         NI    UCBTBYT2,255-UCBRR  ALTER UCB TO UNSHARED STATUSDM JAN84
A1400    DS    0H
         CLC   SHRFLAG,=C'SHARE'  IS THIS VOL SHARABLE
         BNE   A1500              CONTINUE UCB SCAN
         OI    UCBTBYT2,UCBRR     ALTER UCB TO SHARE STATUS
         SPACE 3
******************************************************************
*    CHECK AND ALTER 'MOUNT' UCB ATTRIBUTE AS REQUIRED            *
A1500    DS    0H
         CLI   MOUNT,MNTRSDNT SHOULD IT BE A PERMANENT RESIDENT VOL? DM
         BNE   A1600          NO. CONTINUE CHECKING
         NI    UCBSTAT,255-UCBRESV      REMOVE RESERVE STATUS  DM JAN84
         OI    UCBSTAT,UCBPRES  ALTER THE UCB TO PERM. RESIDENT
         B     A1800          CONTINUE UCB SCAN
         SPACE 1
A1600    DS    0H
         CLI   MOUNT,MNTRSERV SHOULD IT BE A RESERVED VOL?     DM JUL80
         BNE   A1700          NO.NO ATTRIBUTES FOUND IN VATLST
         NI    UCBSTAT,255-UCBPRES      REMOVE PERM. RESIDENT  DM JAN84
         OI    UCBSTAT,UCBRESV  ALTER THE UCB TO RESERVE
         B     A1800          CONTINUE THE UCB SCAN
         EJECT
********************************************************************
*   ASSIGNING A DEFAULT VALUE TO A UCB NOT FOUND IN VATLST         *
A1700    DS    0H                                              ARA 1/80
         TM    UCBTBYT2,UCBRVDEV       IS IT A VIRT UCB?       ARA 1/80
         BO    A0400                   YES/ GO GET ANOTHER UCB ARA 1/80
         MVC   DFLTVOL,UCBVOLI     MOVE VOLID INTO WTO MSG     DM AUG80
         LA    R1,DFLTWTO          SET 'DEFAULT' MSG ADDR      DM AUG80
*        WTO   MF=(E,(1))                                      DM SEP81
         SPACE 3
         NI    UCBSTAB,255-(UCBBPRV+UCBBSTR+UCBBPUB)  CLEAR UCBDM NOV81
         OI    UCBSTAB,UCBBPRV         ALTER TO PRIVATE        ARA 1/80
         NI    UCBSTAT,255-UCBPRES     REMOVE PERM. RESIDENT   DM JAN84
         OI    UCBSTAT,UCBRESV         ALTER TO RESERVED       ARA 1/80
         SPACE 4
A1800    DS    0H
         B     A0400                   GO GET ANOTHER UCB      ARA 1/80
         SPACE 4                                               DM JUL80
************************************************************** DM JUL80
* GETMAIN AN AREA FOR THE VATLST ENTRIES -                   * DM JUL80
* MOVE THE CURRENT LIST TO IT (ZEROS IF FIRST TIME)          * DM JUL80
GETVAT   STM   R14,R5,12(R13)                                  DM JUL80
         L     R2,VATTBLE                                      DM JUL80
         L     R3,VATGMLNG                                     DM JUL80
         LH    R0,=H'4096'    INCREMENT OVER PREVIOUS          DM JUL80
         AR    R0,R3          ADD PREVIOUS LENGTH (0 IF FIRST) DM JUL80
         GETMAIN R,LV=(0)                                      DM JUL80
         STM   R0,R1,VATGMLNG SAVE                             DM JUL80
         AR    R0,R1          BYTE PAST END                    DM JUL80
         SH    R0,=Y(VATLNG)  LAST AVAILABLE SLOT              DM JUL80
         ST    R0,VATLAST                                      DM JUL80
         LR    R4,R2          SAVE OLD GETMAIN DATA            DM JUL80
         LR    R5,R3               ACROSS MVCL                 DM JUL80
         LR    R0,R1          MOVE PRIOR DATA                  DM JUL80
         L     R1,VATGMLNG         TO NEW AREA                 DM JUL80
         MVCL  R0,R2                    (0 IF FIRST TIME)      DM JUL80
         L     R3,VATTBLE                                      DM JUL80
         LTR   R0,R5          PRIOR LENGTH                     DM JUL80
         BZ    GETVAT1        ZERO - FIRST TIME                DM JUL80
         FREEMAIN R,LV=(0),A=(R4)  FREE OLD                    DM JUL80
         A     R3,32(,R13)    CALCULATE WHERE CURRENT          DM JUL80
         SR    R3,R4               ENTRY HAS MOVED TO          DM JUL80
         SPACE ,                                               DM JUL80
GETVAT1  ST    R3,32(,R13)    SET NEW CURRENT (AFTER LM)       DM JUL80
         LM    R14,R5,12(R13)                                  DM JUL80
         BR    R14            RETURN                           DM JUL80
         EJECT
ENDSRCH  DS    0H
         MODESET KEY=NZERO                                     DM AUG80
         LA    R5,ENDVAT      SET NORMAL 'END' MSG ADDR
         CLOSE VATIN,MF=(E,OCLIST)                             DM JUL80
         WTO   MF=(E,(R5))
         LM    R0,R1,VATGMLNG FREE THE TABLE AREA              DM JUL80
         FREEMAIN R,LV=(0),A=(1)                               DM JUL80
         FREEPOOL             VATIN                            DM JUL80
         LR    R1,R13
         L     R13,4(,R13)
         DROP  R13                                             DM JUL80
         FREEMAIN R,LV=SAVSIZ,A=(1)
         RETURN (14,12),T,RC=(15)
         EJECT
ENDVAT   WTO   'END UCB ALTERATIONS',MF=L,ROUTCDE=(2,11)
         SPACE 2
         LTORG
         EJECT
         CSECT ,              WORK AREA DEFINITION AND         DM JUL80*
                              INITIALIZATION AREA - R13 POINTS DM JUL80*
                              TO A GETMAIN AREA                DM JUL80
SAVEAREA DC    18A(0)
         SPACE 2
         DC    0D'0'                                           DM JAN84
UCBWORK  DC    XL100'0'           IOSVSUCB WORK AREA           DM JAN84
         SPACE ,                                               DM JAN84
UCBPARM  DC    3A(0)    (UCBWORK,=X'20',UCBADDR+X'80000000')   DM JAN84
UCBADDR  DC    A(0)                                            DM JAN84
         SPACE 2                                               DM AUG80
VATCNT   DC    H'0'           COUNT OF VATLST ENTRIES          DM AUG80
         SPACE ,                                               DM AUG80
DFLTWTO  DC    Y(DFLTWTOL),X'8000'                             DM AUG80
         DC    C'A DEFAULT VALUE WAS ASSIGNED FOR '            DM AUG80
DFLTVOL  DC    CL6' '                                          DM AUG80
DFLTWTOL EQU   *-DFLTWTO                                       DM AUG80
         DC    X'00004020'    ROUTCDE=(2,11)                   DM AUG80
         SPACE ,                                               DM AUG80
BITS1    DC    X'0'                                            DM AUG80
OUTSEQ   EQU   128            VATLST IS OUT OF SEQUENCE        DM AUG80
         SPACE ,                                               DM AUG80
SEQWTO   DC    Y(SEQWTOL),X'8000'                              DM AUG80
         DC    C'VATLST OUT OF SEQUENCE AT '                   DM AUG80
SEQSER   DC    CL6' '                                          DM AUG80
SEQWTOL  EQU   *-SEQWTO                                        DM AUG80
         DC    X'40004020'    DESC=2,ROUTCDE=(2,11)            DM AUG80
         SPACE ,                                               DM AUG80
SEQCHK   DC    XL6'0'         SEQUENCE CHECK FOR VATLST        DM AUG80
         SPACE 3
VATIN    DCB   DDNAME=VATIN,DSORG=PS,MACRF=GL,RECFM=FB,EODAD=END
         SPACE 3
OCLIST   OPEN  0,MF=L
VATGMLNG DC    A(0)           LENGTH OF VATLST GETMAIN         DM JUL80
VATTBLE  DC    A(0)           ADDRESS OF VATLST GETMAIN        DM JUL80
VATLAST  DC    A(0)           LAST SLOT IN VATLST GETMAIN      DM JUL80
SAVSIZ   EQU   *-SAVEAREA
         SPACE 2
VATLST   DSECT
VOLSER   DS    CL6            VOLUME SERIAL
         DS    CL1
MOUNT    DS    CL1            MOUNT ATTRIBUTE
MNTRSDNT EQU   C'0'                RESIDENT                    DM JUL80
MNTRSERV EQU   C'1'                RESERVED                    DM JUL80
         DS    CL1
USE      DS    CL1            USE ATTRIBUTE
USESTOR  EQU   C'0'                STORAGE                     DM JUL80
USEPUB   EQU   C'1'                PUBLIC                      DM JUL80
USEPRIV  EQU   C'2'                PRIVATE                     DM JUL80
         DS    CL1
DEVT     DS    CL8            DEVICE TYPE
         DS    CL1
N        DS    CL1            MOUNT MESSAGE
         DS    CL1
SHRFLAG  DS    CL5            SHARE ATTRIBUTE
* THIS IS A USER-DEFINED FIELD                                 DM JUL80
*        OUR VALUES ARE 'UNSHR' TO TURN OFF THE SHARED         DM JUL80
*            ATTRIBUTE IN THE UCB DEVTYPE AND 'SHARE'          DM JUL80
*            TO TURN IT ON                                     DM JUL80
VATLNG   EQU   *-VATLST       PORTION THAT WE WILL STORE       DM JUL80
         DS    CL53
         SPACE 2
EXSA     DSECT
         IEFUCBOB LIST=NO
         CVT DSECT=YES
         END
