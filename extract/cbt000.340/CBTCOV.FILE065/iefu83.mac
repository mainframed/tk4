IEFU83 TITLE 'IPO-SUPPLIED SMF RECORD EXIT'
***********************************************************************
*                                                                     *
*             MODULE NAME = IEFU83                                    *
*                                                                     *
*             DESCRIPTIVE NAME = IPO SUPPLIED SMF RECORD EXIT         *
*                                                                     *
*             COPYRIGHT = NONE                                        *
*                                                                     *
*             STATUS = RELEASE 01.0 OF IPO                            *
*                                                                     *
*             FUNCTION =                                              *
*             THIS MODULE RECEIVES CONTROL FROM SVC83(IEEMB830 ALIAS  *
*             IGC0008C) BEFORE EACH RECORD IS WRITTEN TO THE SMF DATA *
*             SET. THE FUNCTION OF THIS MODULE IS TO DETERMINE WHICH  *
*             SMF RECORDS ARE TO BE WRITTEN TO OR DELETED FROM THE MAN*
*             DATA SET. THIS SPECIFIC MODULE HAS A 256 BIT AREA CALLED*
*             BITMAP SET UP WITH THE 41ST BIT SET ON(1) SPECIFYING    *
*             THAT THE SMF RECORD TYPE 40 RECORD IS TO BE DELETED. AN *
*             INSTALLATION CAN CHANGE THE BITMAP AREA TO DELETE OR    *
*             KEEP ANY SMF RECORD TYPE FROM 0 TO 255 BY USING THE IBM *
*             SERVICE AID SUPERZAP TO SET THE APROPRIATE BITS TO SUIT *
*             ITS NEEDS.                                              *
***********************************************************************
*   DATE ----  77.137                                                 *
*   MODIFIED - TO REMOVE DELETION OF TYPE 40 RECORD                   *
*   MODIFIED - TO DELETE TYPE 14 AND 15 RECORDS ONLY IF               *
*              THE RECORD IS FOR A TEMPORARY DATA SET ON              *
*              EITHER DISK OR TAPE.  THIS ROUTINE CHECKS FOR          *
*              X'10' AT OFFSET +X'26'(+RDW) IN 14 & 15 RECORDS.       *
*              (X'10' ON MEANS DSNAME HAS A SYSTEM GENERATED NAME.)   *
***********************************************************************
*   DATE ----  80.325                                                 *
*   MODIFIED - TO DELETE ALL TYPE 14 AND 15 RECORDS                   *
*   MODIFIED - TO EXAMINE TYPE 21 RECORDS AND IF TEMPORARY READ/WRITE *
*              ERRORS ARE 15 OR GREATER ISSUE A WTO TO INFORM THE     *
*              OPERATOR TO CLEAN THE TAPE DRIVE.                      *
***********************************************************************
*   DATE ----  81.183                                                 *
*   MODIFIED - CHANGED FIXED GETMAIN TO A VARIABLE GETMAIN            *
*              THIS WILL ALLOW FOR THE LONGER RECORDS GENERATED BY    *
*              SP1.                                                   *
***********************************************************************
*                                                                     *
*             OPERATION =                                             *
*             UPON ENTRY REGISTER 1 POINTS TO THE RECORD DESCRIPTOR   *
*             WORD(RDW) OF THE SMF RECORD TO BE WRITTEN. THE SMF      *
*             RECORD NUMBER IS GOTTEN FROM THE RECORD AND TESTED      *
*             AGAINST THE BITMASK. IF THE BITMASK FOR THAT SPECIFIC   *
*             RECORD IS ON, THAT RECORD IS DELETED BY PUTTING A RETURN*
*             CODE OF 4 IN REGISTER 15 AND RETURNING. IF THE BITMAP   *
*             FOR THAT RECORD IS OFF A RETURN CODE OF 0 IS PLACED IN  *
*             REGISTER 15 AND RETURNED TO THE CALLER.                 *
*                                                                     *
*             DEPENDENCIES = NONE                                     *
*                                                                     *
*             RESTRICTIONS = NONE                                     *
*                                                                     *
*             REGISTER CONVENTIONS = STANDARD CONVENTIONS.            *
*                REGISTERS 0,2,6,8,9,10,11 = WORK REGISTERS           *
*                REGISTERS 3,4,5,7 = UNUSED                           *
*                REGISTER 1 = PARAMETER REGISTER                      *
*                REGISTER 12 = BASE REGISTER                          *
*                REGISTER 13 = SAVEAREA REG                           *
*                REGISTER 14 = RETURN REGISTER                        *
*                REGISTER 15 = ENTRY, TEMP BASE, RETURN CODE REGISTER *
*                                                                     *
*             MODULE TYPE = EXECUTABLE
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = 174 BYTES                              *
*                                                                     *
*                ATTRIBUTES = PROTECTION KEY 0, REENTRANT, ENABLED    *
*                                                                     *
*                                                                     *
*             ENTRY POINTS = IEFU83 (ONLY ENTRY POINT)                *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO A 4 BYTE ADDRESS OF THE    *
*                RECORD DESCRIPTOR WORD (RDW) OF THE SMF RECORD TO    *
*                BE PROCESSED BY THE EXIT.                            *
*                                                                     *
*             OUTPUT= REGISTER 15 MUST CONTAIN ONE OF THE FOLLOWING:  *
*                0 - WRITE THE SMF RECORD (KEEP)                      *
*                4 - DO NOT WRITE THE SMF RECORD (DELETE)             *
*                                                                     *
*             EXTERNAL REFERENCES = NONE                              *
*                                                                     *
*             EXITS - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14  *
*                OUTPUT = NONE                                        *
*                RETURN CODE = SEE FUNCTION ABOVE                     *
*                                                                     *
*             EXITS - ERROR = NONE                                    *
*                OUTPUT = NONE                                        *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             TABLES/WORK AREAS =                                     *
*                BITMAP - 256 BIT SWITCHES, 1 FOR EACH SMF RECORD TYPE*
*                MASKS - MASKS FOR TESTING BITMAP                     *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             CHARACTER CODE DEPENDENCY = NONE                        *
*                                                                     *
*             NOTES = GETMAIN FREEMAIN MACROS USED                    *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
         EJECT
IEFU83   CSECT
R00      EQU   0
R01      EQU   1
R02      EQU   2
R03      EQU   3
R04      EQU   4
R05      EQU   5
R06      EQU   6
R07      EQU   7
R08      EQU   8
R09      EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  BASE REG
R13      EQU   13                  SAVEAREA REG
R14      EQU   14                  RETURN ADDRESS
R15      EQU   15                  ENTRY ADDRESS & TEMPORARY BASE
D0       EQU   0                   DI
D3       EQU   3                     SP
D4       EQU   4                       LA
D5       EQU   5                         CEM
D12      EQU   12                           ENT
D20      EQU   20                  VAL
D29      EQU   29                     UES
D42      EQU   42                  HEX '2A'                  **77.137*
TEMPDS   EQU   16                  HEX '10'                  **77.137*
ZERO     EQU   0                   ZERO FOR BITMAP TESTING
RC0      EQU   0                   RETURN CODE OF 0
RC4      EQU   4                   RETURN CODE OF 4
BACKWARD EQU   4                   SAVEAREA BACKWARD POINTER OFFSET
FORWARD  EQU   8                   SAVEAREA FORWARD POINTER OFFSET
KEEP     EQU   X'40'
         EJECT
         USING *,R15               TEMPORARY BASE FOR
         B     START               BRANCHING AROUND
         DC    CL8'IEFU83  '       MODULE IDENTIFIER
START    EQU   *
         STM   R14,R12,D12(R13)    SAVE CALLERS REGISTERS
         DROP  R15                 DROP TEMPORARY BASE REGISTER
         BALR  R12,R00             SETUP R12
         USING *,R12                 AS MAIN BASE REGISTER
         LR    R10,R01             SAVE PARAMETER LIST ADDRESS
         SR    R00,R00             CLEAR REG. 0               **81.183*
         LA    R00,U83LEN          LENGTH OF GETMAIN AREA     **81.183*
         GETMAIN R,LV=(0)          GET CORE FOR SAVEAREA      **81.183*
         XC    0(U83LEN,R01),R01   CLEAR SAVE AREA            **81.183*
         ST    R01,FORWARD(R13)    SETUP FORWARD SAREA CHAIN  **80.325*
         ST    R13,BACKWARD(R01)   SETUP BACKWARD SAREA CHAIN **80.325*
         LR    R13,R01             PUT ADDR OF THIS SAVEAREA IN REG13
         USING U83WORK,R13         ADDRESSABILITY FOR DSECT   **80.325*
         MVI   WERRMIN,X'0F'       MIN VALUE FOR TEMP WRITE   **80.325*
         MVI   RERRMIN,X'0F'       MIN VALUE FOR TEMP READ    **80.325*
         L     R02,D0(R10)         GET ADDRESS OF SMF RECORD
         LA    R11,BITMAP          PUT ADDR OF BITMAP IN WORKREG
         CLI   D5(R02),X'0E'       Q. TYPE 14 RECORD?         **80.325*
         BE    DELETE              A. YES.                    **80.325*
         CLI   D5(R02),X'0F'       Q. TYPE 15 RECORD?         **80.325*
         BE    DELETE              A. YES                     **80.325*
TSTSMF21 EQU   *                                              **80.325*
         USING SMF21,R02           SET UP ADDRESSABILITY      **80.325*
         CLI   SMF21TYP,X'15'      Q. IS A TYPE 21 RECORD     **80.325*
         BNE   NOTYPE              A. NO, WRAP IT UP          **80.325*
         CLC   SMF21TR(1),RERRMIN  TEMP READ ERRORS           **80.325*
         BNL   WTOPER              YES GO INFORM THE DUMMY    **80.325*
         CLC   SMF21TW(1),WERRMIN  TEMP WRITE ERRORS          **80.325*
         BL    NOTYPE              NO, RETURN                 **80.325*
WTOPER   EQU   *                                              **80.325*
         MVC   WTOLIST(WTOLEN),WTODUMMY MOVE IN MODEL WTO     **80.325*
         UNPK  WORKCUU(5),SMF21CUU(3)  UNPACK CHANNEL/UNIT    **80.325*
         TR    WORKCUU(4),TRTBL        MAKE IT PRINTABLE      **80.325*
         MVC   WTOLIST+34(3),WORKCUU+1 MOVE TO WTO            **80.325*
         MVC   WTOLIST+60(3),WORKCUU+1 MOVE IT AGAIN          **80.325*
         LA    R01,WTOLIST             LOAD ADDRESS OF WTO    **80.325*
         WTO   MF=(E,(1))              TELL THE DUMMY         **80.325*
NOTYPE   EQU   *                                              **77.137*
         XR    R08,R08             CLEAR WORKREG
         IC    R08,D5(R02)         GET SMF RECORD NUMBER FROM RECORD
         SRDA  R08,D3              ISOLATE WHICH BYTE IT'S IN
         SRL   R09,D29             ISOLATE THE BIT
         IC    R09,MASKS(R09)      GET THE APPROPRIATE MASK
         LA    R10,BITMAP(R08)     GET THE BYTE ADDRESS
         EX    R09,TM              TEST TO SEE IF BIT IN BITMAP ON
         BO    DELETE              ON-SET RETURN CODE TO 4 FOR NO WRITE
         LA    R06,RC0             OFF-SET RETURN CODE TO 0 FOR WRITE
FINISH   EQU   *
         LR    R01,R13             PUT AREA TO BE FREED IN PARM REG
         L     R13,D4(R13)         RESET SAVEAREA
         SR    R00,R00             CLEAR REG                  **81.183*
         LA    R00,U83LEN          LENGTH OF GETMAIN AREA     **81.183*
         FREEMAIN R,LV=(0),A=(R01) FREE SAVEAREA STORAGE      **81.183*
         L     R14,D12(R13)        RESTORE RETURN ADDRESS
         LR    R15,R06             PUT RETURN CODE IN RETURN CODE REG
         LM    R00,R12,D20(R13)    RESTORE REGS ZERO THRU 12
         BR    R14                 RETURN
DELETE   EQU   *
         LA    R06,RC4             SET RETURN CODE TO 4 - NO WRITE
         B     FINISH              GO FREE SAVEAREA AND RETURN
MASKS    DC    X'8040201008040201' BITMAP  TESTING
TM       TM    0(R10),ZERO         TO TEST THE BITMAP FOR 0 OR 1
***********************************************************************
* IN THIS SPECIFIC EXAMPLE THE BITMAP AREA IS SET TO DELETE SMF RECORD
* TYPE 40 FROM BEING PRINTED. YOU MAY SET THE BITMAP INDICATOR FOR ANY
* SMF RECORD TYPE YOU DO NOT WANT PRINTED BY SETTING THE APPROPRIATE
* SWITCH TO 1.
***********************************************************************
BITMAP   DC    B'00000000'         BITMAP FOR SMF RECORD 0 THRU 7
         DC    B'00000000'         BITMAP FOR SMF RECORD 8 THRU 15
         DC    B'00000000'         BITMAP FOR SMF RECORD 16 THRU 23
         DC    B'00000000'         BITMAP FOR SMF RECORD 24 THRU 31
         DC    B'00000000'         BITMAP FOR SMF RECORD 32 THRU 39
         DC    B'00000000'         BITMAP FOR SMF RECORD 40 THRU 47
         DC    B'00000000'         BITMAP FOR SMF RECORD 48 THRU 55
         DC    B'00000000'         BITMAP FOR SMF RECORD 56 THRU 63
         DC    B'00000000'         BITMAP FOR SMF RECORD 64 THRU 71
         DC    B'00000000'         BITMAP FOR SMF RECORD 72 THRU 79
         DC    B'00000000'         BITMAP FOR SMF RECORD 80 THRU 87
         DC    B'00000000'         BITMAP FOR SMF RECORD 88 THRU 95
         DC    B'00000000'         BITMAP FOR SMF RECORD 96 THRU 103
         DC    B'00000000'         BITMAP FOR SMF RECORD 104 THRU 111
         DC    B'00000000'         BITMAP FOR SMF RECORD 112 THRU 119
         DC    B'00000000'         BITMAP FOR SMF RECORD 120 THRU 127
         DC    B'00000000'         BITMAP FOR SMF RECORD 128 THRU 135
         DC    B'00000000'         BITMAP FOR SMF RECORD 136 THRU 143
         DC    B'00000000'         BITMAP FOR SMF RECORD 144 THRU 151
         DC    B'00000000'         BITMAP FOR SMF RECORD 152 THRU 159
         DC    B'00000000'         BITMAP FOR SMF RECORD 160 THRU 167
         DC    B'00000000'         BITMAP FOR SMF RECORD 168 THRU 175
         DC    B'00000000'         BITMAP FOR SMF RECORD 176 THRU 183
         DC    B'00000000'         BITMAP FOR SMF RECORD 184 THRU 191
         DC    B'00000000'         BITMAP FOR SMF RECORD 192 THRU 199
         DC    B'00000000'         BITMAP FOR SMF RECORD 200 THRU 207
         DC    B'00000000'         BITMAP FOR SMF RECORD 208 THRU 215
         DC    B'00000000'         BITMAP FOR SMF RECORD 216 THRU 223
         DC    B'00000000'         BITMAP FOR SMF RECORD 224 THRU 231
         DC    B'00000000'         BITMAP FOR SMF RECORD 232 THRU 239
         DC    B'00000000'         BITMAP FOR SMF RECORD 240 THRU 247
         DC    B'00000000'         BITMAP FOR SMF RECORD 248 THRU 255
         SPACE 1
WTODUMMY WTO   'IEFU8301 - CLEAN TAPE DRIVE * CUU *  CLEAN TAPE DRIVE *+
                CUU *',ROUTCDE=(1,2,11),DESC=(2),MF=L         **80.325*
WTOLEN   EQU    *-WTODUMMY                                    **80.325*
         SPACE  1                                             **80.325*
TRTBL    EQU    *-X'F0'                                       **80.325*
         ORG    TRTBL+X'F0'                                   **80.325*
HEXDIG   DC     C'0123456789ABCDEF'                           **80.325*
         ORG    ,                                             **80.325*
         SPACE  1                                             **80.325*
U83WORK  DSECT                                                **80.325*
SAVEAREA DS     18F                REGISTER SAVE AREA         **80.325*
RERRMIN  DS     CL1                ALLOWABLE # OF READ ERRORS **80.325*
WERRMIN  DS     CL1                ALLOWABLE # OF WRITE ERRORS**80.325*
WORKCUU  DS     CL5                                           **80.325*
WTOLIST  WTO   'IEFU8301 - CLEAN TAPE DRIVE * CUU *  CLEAN TAPE DRIVE *+
                CUU *',ROUTCDE=(1,2,11),DESC=(2),MF=L         **80.325*
U83LEN   EQU    *-U83WORK                                     **81.183*
         SPACE  1                                             **80.325*
SMF21    DSECT                                                **80.325*
SMF21RDW DS     XL4               RECORD DESCRIPTOR WORD      **80.325*
SMF21FLG DS     XL1               SYSTEM INDICATOR            **80.325*
SMF21TYP DS     XL1               RECORD TYPE                 **80.325*
SMF21TME DS     XL4               TIME OF DAY                 **80.325*
SMF21DTE DS     PL4               DATE                        **80.325*
SMF21SID DS     CL4               SYSTEM ID                   **80.325*
SMF21LN  DS     XL2               LENGTH OF REST OF RECORD    **80.325*
SMF21VOL DS     CL6               VOLSER                      **80.325*
SMF21CUU DS     XL2               CHANNEL/UNIT                **80.325*
SMF21UT  DS     XL4               UNIT TYPE                   **80.325*
SMF21TR  DS     XL1               TEMPORARY READ ERRORS       **80.325*
SMF21TW  DS     XL1               TEMPORARY WRITE ERRORS      **80.325*
SMF21SIO DS     XL2               NUMBER OF START IO          **80.325*
SMF21PR  DS     XL1               # OF PERMANENT READ ERRORS  **80.325*
SMF21PW  DS     XL1               # OF PERMANENT WRITE ERRORS **80.325*
SMF21NB  DS     XL1               NOISE BLOCKS                **80.325*
SMF21ERG DS     XL2               ERASE GAPS                  **80.325*
SMF21CLN DS     XL2               CLEANER ACTIONS             **80.325*
SMF21DEN DS     XL1               TAPE DENSITY                **80.325*
SMF21BLS DS     CL2               BLOCK SIZE                  **80.325*
SMF21RVO DS     XL2               RESERVED                    **80.325*
         END
