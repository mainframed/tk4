         MACRO
&NAME    TPT   &A,&B
         LCLC  &X
&X       SETC  'L'''
&NAME    LA    1,&A
         AIF   ('&B' NE '').GENB
         LA    0,&X&A
         AGO   .TPT
.GENB    LA    0,&B
.TPT     TPUT  (1),(0),R
         MEND
         SPACE 5
         MACRO
&NAME    CLEAR &FIELD,&LEN,&TO=C' '
         LCLC  &L
&L       SETC  'L'''
         AIF (('&LEN' EQ '') OR ('&TO' NE '0')).L0
         XC    &FIELD.(&LEN.),&FIELD .CLEAR IT
         MEXIT
.L0      ANOP
&NAME    MVI   &FIELD,&TO               START TO CLEAR FIELD
         AIF ('&LEN' EQ '').L1
         MVC   &FIELD+1(&LEN-1),&FIELD .CLEAR IT
         MEXIT
.L1      MVC   &FIELD+1(&L&FIELD-1),&FIELD CLEAR IT
         MEND
         EJECT
FREALL   TSOENTER WORK=160,PL=(PARSE,DAIR)
         MVI   TSOECB,0
         L     R15,CPPLECT
         XR    R4,R4                    CLEAR PARM REG
         TM    28(R15),X'80'            ANY OPERANDS?
         BNO   PARSER                   YES
         XC    PPLRANS(4),PPLRANS       CLEAR SO IKJRLSA DON'T DIE
         B     FREEALL                  GO
PARSER   TSOPARSE PCL==V(PCL),CBUF=CPPLCBUF
         LTR   R15,R15                  TEST RETURN CODE
         L     R4,PPLRANS               GET ANSWER PLACE
         BZ    PARSEOK                  OK
         LR    R2,R15                   GET IT
         TPT   BADPARSE
         B     LEAVE1                   DO IT
PARSEOK  CLI   EXCEPT-PDL+1(R4),0       SPECIFIED?
         BE    PARSE0                   NO
         LA    R4,DDNAMES-PDL(,R4)      GET PLACE
         TM    6(R4),X'80'              EXIST?
         BO    FREEALL                  YES
PARSE0   XR    R4,R4                    ZERO IT
         B     FREEALL
LEAVE    LR    R2,R15                   SAVE RC
LEAVE1   IKJRLSA PPLRANS                FREE UP CORE
         TSOLEAVE RETCODE=(R2)          DIE
LEAVE0   XR    R15,R15                  CLEAR RC
         B     LEAVE                    END PROGRAM
         EJECT
PCL      IKJPARM DSECT=PDL
EXCEPT   IKJKEYWD
         IKJNAME 'EXCEPT',SUBFLD=DDNAME$
DDNAME$  IKJSUBF
DDNAMES  IKJIDENT 'DDNAME(S) TO NOT BE FREED',                         +
               LIST,                                                   +
               MAXLNTH=8,                                              +
               PROMPT='FILE NAME(S) TO NOT BE FREED',                  +
               HELP='ONE OR MORE DDNAMES WHICH SHOULD NOT BE FREED',   +
               OTHER=ALPHANUM
         IKJENDP
         EJECT
         IKJDAP18
         EJECT
TSODSECT DSECT
         ORG   TSOUSER
@MSGS    DC    XL4'00'                  ADDRESS OF DAIRMSGS
PDLSTART DC    XL4'00'                  START OF PDL LIST
LINE     DC    CL40' '                 AREA TO CLEAR
         DS    0D
DAIRWORK DC    XL100'00'
         ORG
         EJECT
FREALL   CSECT
*
*        FREEALL - THIS ROUTINE FREES DATA SETS
*
         SPACE 1
FREEALL  ST    R4,PDLSTART              SAVE FOR LATER
         L     R15,16                   GET CVT ADDRESS
         L     R15,0(,R15)              GET DISPATCH D-WORD
         L     R15,4(,R15)              MY TCB ADDRESS
         L     R3,12(,R15)              MY TIOT ADDRESS
         LA    R3,X'18'(,R3)            START OF DDNAME ENTRIES
FREENEXT CLI   0(R3),X'14'              VALID ENTRY?
         BNE   BUMPTIOT
         CLI   4(R3),X'00'              NULL ENTRY?
         BE    BUMPTIOT
         CLC   X'18'(8,R3),=CL8' '      CONCATENATION?
         BE    BUMPTIOT
         CLC   4(8,R3),=CL8' '          PART OF CONCATENATION?
         BE    BUMPTIOT
         CLC   4(4,R3),=CL8'SYSPROC'    SPECIAL TYPE?
         BE    BUMPTIOT
         CLC   4(4,R3),=CL8'SYSTERM'    SPECIAL TYPE?
         BE    BUMPTIOT
         CLC   4(4,R3),=CL8'STEPLIB'    SPECIAL TYPE?
         BE    BUMPTIOT
         CLC   4(4,R3),=CL8'SYSIN'      SPECIAL TYPE?
         BE    CKTERM
         CLC   4(4,R3),=CL8'SYSPRINT'   SPECIAL TYPE?
         BE    CKTERM
         TM    3(R3),X'08'              QNAME ENTRY?
         BO    BUMPTIOT
         BAL   R8,EXCEPTCK               GO CHECK EXCEPTIONS
         BAL   R8,FREEDS                 GO FREE THE FILE
         SPACE 2
BUMPTIOT SR    R2,R2
         IC    R2,0(,R3)                 GET LENGTH OF TIOT ENTRY
         AR    R3,R2                     POINT TO NEXT ENTRY IN TIOT
         CLI   0(R3),X'00'               CHECK FOR TIOT END
         BNE   FREENEXT
         B     LEAVE0
         SPACE 3
EXCEPTCK L     R4,PDLSTART              GET START
LISTALL3 LTR   R4,R4                    TEST IT
         BNPR  R8                       OK, CONTINUE
         CLEAR DAIRWORK,8,TO=C' '       CLEAR IT
         L     R15,0(,R4)               GET PTR TO NAME
         LH    R14,4(,R4)               GET LENGTH
         BCTR  R14,0
         MVC   DAIRWORK(0),0(R15)       EXECUTED MOVE
         EX    R14,*-6                  MOVE TO WORK AREA
         CLC   DAIRWORK(8),4(R3)        LEAVE ALONE??
         BE    BUMPTIOT
LISTALL4 L     R4,8(,R4)                GET PTR TO NEXT
         B     LISTALL3                 TEST IT
         SPACE 2
CKTERM   TM    3(R3),X'20'              TERMINAL DEFINED?
         BO    BUMPTIOT                 YES,DON'T FREE.
         LA    R8,BUMPTIOT
         EJECT
*
*        FREEDS - FREES THE DATA SETS FOUND
*
         SPACE 1
FREEDS   EQU   *
GETCLEAR CLEAR DAIRWORK,100,TO=0        CLEAR IT
GETBLOCK LA    R9,DAIRWORK              GET IT
         LUSE  DAPB18,R9                TELL ASSEMBLER
         INUSE
         MVI   DA18CD+1,X'18'           INDICATE CODE
         MVC   DA18DDN(8),4(3)          COPY DDNAME
         CLEAR DA18MNM,8,TO=C' '
         MVC   DA18SCLS(2),DA18MNM      CLEAR MEMBER/CLASS
         OI    DA18CTL,X'10'            FREE EVEN IF PERM ALLOC
         MVC   DA18JBNM(8),DA18MNM      CLEAR JOBNAME TO BLANKS
CALLDAIR TSODAIR BLOCK=(R9),PL=DAPL     DO IT
RETDAIR  LTR   R15,R15                  TEST RETURN CODE
         BZR   R8                       RETURN TO CALLER
IKJ18PRM EQU   DAIRWORK+X'30'
MSGPARM  EQU   DAIRWORK+X'50'
         LA    R1,MSGPARM
         LA    R14,DAPL
         ST    R14,IKJ18PRM             DAPL ADDR
         LA    R1,4(,R1)
         ST    R1,IKJ18PRM+4            DAIR ERROR CODE PTR
         ST    R15,0(,R1)               DAIR ERROR CODE
         LA    R1,4(,R1)
         ST    R1,IKJ18PRM+8            MSG ROUTINE PTR
         SR    R14,R14                  NO ADDR GIVEN
         ST    R14,0(,R1)
         LA    R1,4(,R1)
         ST    R1,IKJ18PRM+12           CALLER REQUEST PTR
         LA    R14,1
         STH   R14,0(,R1)               CALLER ID = 1
         LA    R1,IKJ18PRM              PARAMETER PTR
         LINK  EP=IKJEFF18              LINK TO DAIR ERROR MSG RTN
         BR    R8                       RETURN TO CALLER
         EJECT
BADPARSE DC    C' UNABLE TO PARSE, FREEALL NOT EXECUTED'
A        LTORG
         CVT   DSECT=YES
         END
