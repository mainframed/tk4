MBRLIST  TITLE '-----  BUILD LIST OF MEMBER NAMES  -----'
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  MBRLIST                                            *
*                                                                     *
*   PURPOSE:       TO BUILD A LIST OF MEMBER NAMES IN A GIVEN         *
*                  LIBRARY, FOR HIGHER LEVEL LANGUAGE PROGRAMS TO     *
*                  READ                                               *
*                                                                     *
*   AUTHOR:        LEONARD D. WOREN                                   *
*                                                                     *
*   DATE:          AUGUST 23, 1982                                    *
*                                                                     *
*   ATTRIBUTES:    PROBLEM STATE, NOT RE-ENTRANT                      *
*                                                                     *
*   UPDATES:                                                          *
*      08/24/83 U001 LDW  FIX TO WORK ON LOAD MODULE LIBRARIES        *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
MBRLIST  OSENTER  EXIT=LEAVE,RC=0
         OPEN  MF=(E,OPENLIST)
         TM    SYSLIB+48,X'10'          DID IT OPEN?
         BNO   ABEND99                  NO
         TM    SYSPUNCH+48,X'10'        DID IT OPEN?
         BO    OPENOK                   YES
         SPACE 1
ABEND99  LA    R1,99                    GET ABEND CODE
         SVC   13                       AND AWAY WE GO
         SPACE 2
OPENOK   L     R1,16                    -> CVT
         L     R1,0(,R1)                -> DISPATCH
         L     R1,4(,R1)                -> MY TCB
         L     R1,12(,R1)               -> TIOT
         AH    R1,SYSLIB+40             ADD TIOT OFFSET
         ICM   R1,B'0111',12(R1)        -> JFCB PREFIX
         MVC   CARD+9(44),16+0(R1)      MOVE DSNAME TO OUTPUT CARD
         SR    R9,R9                    CLEAR RECORD COUNTER
         SPACE 3
LOOP1    READ  DECB,SF,SYSLIB,BLOCK     READ A DIRECTORY BLOCK
         SPACE 1
         CHECK DECB                     WAIT FOR COMPLETION
         SPACE 1
         LH    R2,BLOCK                 GET LENGTH USED IN BLOCK
         LA    R2,BLOCK(R2)             -> FIRST UNUSED BYTE IN BLOCK
         LA    R3,BLOCK+2               -> FIRST DIR ENTRY IN BLOCK
         SPACE 1
LOOP2    CLC   =X'FFFFFFFFFFFFFFFF',0(R3)  END OF DIRECTORY?
         BE    DONE                     YES - STOP
         MVC   CARD(8),0(R3)            MOVE MEMBER NAME TO OUTPUT
         LA    R9,100(,R9)              INCREMENT SPF STYLE
         CVD   R9,DWD
         OI    DWD+7,X'0F'              DAMN THE SIGN
         UNPK  CARD+72(8),DWD           PUT SEQUENCE NUMBER ON OUTPUT
*
         PUT   SYSPUNCH,CARD            OUTPUT THE RECORD
*
         IC    R1,11(,R3)               GET # HALF   WORDS OF USERDATA
         N     R1,=A(B'11111')          GET JUST THE NUMBER        U001
         AR    R1,R1                    GET IT IN BYTES
         LA    R3,12(R1,R3)             -> NEXT DIRECTORY ENTRY
         CR    R3,R2                    AT END OF BLOCK?
         BL    LOOP2                    NO - PROCESS REST OF BLOCK
         B     LOOP1                    YES - READ ANOTHER BLOCK
         SPACE 2
DONE     CLOSE MF=(E,OPENLIST)
         SPACE 1
         B     LEAVE                    ALL DONE
         SPACE 2
         LTORG ,
         SPACE 2
DWD      DC    D'0'
BLOCK    DC    XL256'00'
CARD     DC    CL80' '
         SPACE 2
OPENLIST OPEN  (SYSLIB,,SYSPUNCH,(OUTPUT)),MF=L
         SPACE 2
SYSLIB   DCB   DDNAME=SYSLIB,DSORG=PS,MACRF=R,BLKSIZE=256,RECFM=F
         SPACE 2
SYSPUNCH DCB   DDNAME=SYSPUNCH,DSORG=PS,LRECL=80,RECFM=FB,MACRF=PM
         SPACE 2
         END
