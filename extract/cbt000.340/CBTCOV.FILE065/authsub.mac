AUTH     TITLE 'SUBROUTINE SET TO PERFORM AUTHORIZED FUNCTIONS'
AUTHSUB  CSECT
         BR    14
         IHAASCB
         EJECT                                              *HMD 04/82*
         IHAASVT
         EJECT                                              *HMD 04/82*
         IRAOUCB
         EJECT                                              *HMD 04/82*
         IHAPVT
         EJECT                                              *HMD 04/82*
         CVT   DSECT=YES,LIST=YES
         EJECT                                              *HMD 04/82*
PGDAT    CSECT
         STM   14,12,12(13)
         LR    10,15
         USING PGDAT,10
         LR    2,1     SAVE REG 1
         MODESET KEY=ZERO,MODE=SUP
         LR    1,2     RESTORE REG 1
         L     2,0(1)    ADDRESS OF ASID FIELD
         L     3,4(1)    ADDRESS OF DATA FIELD
         LH    0,0(2)    ASID IN REG 0
         LR    1,3       ADDRESS OF DATA FIELD IN REG 1
         SYSEVENT REQPGDAT,ASID=(0),TYPE=INTERVAL
         SLL   15,24
         SRL   15,24
         ST    15,16(13) SAVE RETURN CODE
         MODESET KEY=NZERO,MODE=PROB
         LM    14,12,12(13)
         BR    14
         LTORG
         DROP  10
         EJECT                                              *HMD 04/82*
FIX      CSECT
         STM   14,12,12(13)
         LR    10,15
         USING FIX,10
         L     2,0(1)    ADDRESS OF BEGIN FIELD
         L     3,4(1)    ADDRESS OF LENGTH FIELD
         LR    1,2       ADDRESS OF BEGIN FIELD
         SR    0,0       ZERO ECB ADDRESS
         L     15,0(3)   GET LENGTH
         AR    15,1      GET ADDRESS OF END FIELD+1
         PGFIX R,A=(1),EA=(15),ECB=(0),RELATED=(X,'GET STORAGE')
         ST    15,16(13) SAVE RETURN CODE
         LM    14,12,12(13)
X        BR    14
         LTORG
         DROP  10
         EJECT                                              *HMD 04/82*
FREE     CSECT
         STM   14,12,12(13)
         LR    10,15
         USING FREE,10
         L     2,0(1)    ADDRESS OF BEGIN FIELD
         L     3,4(1)    ADDRESS OF LENGTH FIELD
         LR    1,2       ADDRESS OF BEGIN FIELD
         SR    0,0       ZERO ECB ADDRESS
         L     15,0(3)   GET LENGTH
         AR    15,1      GET ADDRESS OF END FIELD+1
         PGFREE R,A=(1),EA=(15),RELATED=(Y,'FREE STORAGE')
         ST    15,16(13) SAVE RETURN CODE
         LM    14,12,12(13)
Y        BR    14
         LTORG
         DROP  10
         EJECT                                              *HMD 04/82*
NOSWAP   CSECT
         STM   14,12,12(13)
         LR    10,15
         USING NOSWAP,10
         MODESET KEY=ZERO,MODE=SUP
         SR    0,0
         SYSEVENT DONTSWAP,ASID=(0)
         SLL   1,24
         SRL   1,24
         ST    1,16(13) SAVE RETURN CODE
         MODESET KEY=NZERO,MODE=PROB
         LM    14,12,12(13)
         BR    14
         LTORG
         DROP  10
         EJECT                                              *HMD 04/82*
LASCB    CSECT
         STM   14,12,12(13)
         LR    10,15
         USING LASCB,10
         L     2,CVTPTR
         USING CVT,2
         L     7,CVTASVT    GET ADDRESS SPACE VECTOR TABLE
         L     11,CVTPVTP   GET ADDRESS OF PAGE VECTOR TABLE
         L     4,0(1)     GET ADDRESS OF JOBNAME
         USING ASVT,7
         L     8,ASVTMAXU   GET MAX ADDRESS SPACES
         LA    9,ASVTENTY   GET FIRST ASCB
CHKASID  TM    0(9),ASVTAVAL ASID ASSIGNED?
         BZ    CHKNAME YES, BRANCH
LOOP     LA    9,ASVTEND-ASVTENTY(9) BUMP TO NEXT ENTRY
         BCT   8,CHKASID
NOASCB   DS    0H
         LA    15,4    ERROR RET CODE
         B     SAVE15
         USING ASCB,6
CHKNAME  L     6,0(9)      GET ASCB ADDRESS
         CLC   ASCBJBNI,=XL4'00000000'
         BE    STCNAME
         L     5,ASCBJBNI
         B     GOON
STCNAME  CLC   ASCBJBNS,=XL4'00000000'
         BE    LOOP
         L     5,ASCBJBNS
GOON     CLC   0(8,5),0(4)   COMPARE NAMES
         BE    FOUND
         B     LOOP
         DROP  2,7
FOUND    DS    0H
         L     3,4(1)   GET ADDRESS OF ASID FIELD
         MVC   0(2,3),ASCBASID  MOVE ASCB ASID
         L     3,8(1)   GET ADDRESS OF FRAME COUNTS
         MVC   0(2,3),ASCBFMCT MOVE PAGE FRAME COUNT
         L     3,12(1)  GET ADDRESS OF TCB TIME
         MVC   0(4,3),ASCBEJST MOVE FIRST PART TCB TIME
         L     3,16(1)
         MVC   0(4,3),ASCBEJST+4 MOVE RIGHT HAND SIDE
         NC    0(4,3),=X'FFFFF000' GET RID OR LOW ORDER BITS
         L     3,20(1)  GET ADDRESS OF SRB TIME
         MVC   0(4,3),ASCBSRBT  MOVE FIRST PART SRB TIME
         L     3,24(1)
         MVC   0(4,3),ASCBSRBT+4 MOVE RIGHT HAND SIDE
         NC    0(4,3),=X'FFFFF000' GET RID OR LOW ORDER BITS
         L      2,ASCBOUCB  GET OUCB ADDRESS
         USING  OUCB,2
         L      3,28(1)
         MVC    0(2,3),=XL2'0000'
         LTR    2,2
         BZ     NOOUCB
         MVC    0(2,3),OUCBTWSS  TARGET WORKING SET
NOOUCB   EQU   *
         USING PVT,11
         L     3,32(1)
         MVC   0(4,3),PVTCAIN   SYSTEM COMMON (LPA+CSA) PAGE-INS
         DROP  11
         DROP   6
         SR    15,15
SAVE15   ST    15,16(13)
         LM    14,12,12(13)  RESTORE REGS
         BR    14     RETURN
         LTORG
         DROP  10
         END AUTHSUB
