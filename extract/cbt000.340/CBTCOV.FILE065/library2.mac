LIB2     TITLE 'LIBRARY2 - AUTOMATIC SCRATCH - SECOND PHASE'
* AUTOMATIC SCRATCH - PART 2
*
* THIS PROGRAM READS THE SELECT CARDS FROM PART 1 AND PERFORMS THE
*  SCRATCH OF THE PDS MEMBERS USING A STOW MACRO.
* A LISTING OF SCRATCHED IS PRODUCED.
*
* JCL REQUIREMENTS-
*  // EXEC PGM=LIBRARY2
*  //LIBRARY DD ---PDS TO BE AUTO-SCRATCHED
*  //SYSPRINT DD --LISTING OF SCRATCHED MEMBERS
*  //SYSIN DD -----SELECT CARDS FROM PART 1
         EJECT
LIBRARY2 START 0
* STANDARD LINKAGE
         USING *,R13
         B     72(R15)
         DC    17F'0'
         STM   R14,R12,12(R13)
         ST    R15,8(R13)
         ST    R13,4(R15)
         LR    R13,R15
         SPACE
* OPEN FILES, SKIP FIRST SELECT RECORD
         OPEN  (PDS,OUTPUT,SYSOUT,OUTPUT,SYSIN)
*  C I=INPUT,O=OUTPUT
         GET   SYSIN,INREC
         EJECT
* READ SELECT RECORD, SCRATCH MEMBER
SCR      GET   SYSIN,INREC
         STOW  PDS,INMBR,D
         LTR   R15,R15
         BZ    SCR2
* SHOULD NOT FAIL. R15 MUST BE ZERO
         ABEND 1,DUMP
         SPACE
* PRINT SCRATCH LISTING
SCR2     CLI   SCRLAST,C' '
         BE    SCR3
         PUT   SYSOUT,SCRLINE
         MVC   SCRLAST(131),SCRBLANK
SCR3     MVC   SCRLAST(120),SCRNXT
         MVC   SCRNEW,INMBR
         B     SCR
         EJECT
* CLOSE UP SHOP
EODSYSIN PUT   SYSOUT,SCRLINE
         CLOSE (PDS,,SYSOUT,,SYSIN)
* STANDARD EXIT
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         EJECT
SCRLINE  DS    0CL132
SCRBLANK DC    C' '
SCRLAST  DC    CL8' '
         DC    CL2' '
SCRNXT   DC    CL8' '
         DC    CL102' '
SCRNEW   DC    CL8' '
         DC    CL3' '
         SPACE
INREC    DS    0CL80
         DC    C' S M='
INMBR    DC    CL8' '
         DC    CL67' '
         EJECT
PDS      DCB   DDNAME=LIBRARY,DSORG=PO,MACRF=W
SYSOUT   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=132,   -
               BLKSIZE=132
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,EODAD=EODSYSIN
         EJECT
         REQUATE
         END
