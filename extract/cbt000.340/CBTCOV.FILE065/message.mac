MESSAGE  TITLE 'SUBROUTINE WHICH WRITES TO CONSOLE AND LOG'
*
*      THIS PROGRAM WRITES A MESSAGE TO THE OPERATOR'S CONSOLE AND
* TO THE SYSTEM LOG. THE CALLING SEQUENCE FOR "MESSAGE" IS AS
* FOLLOWS
*           1. THE MESSAGE TO BE WRITTEN (80 CHARACTERS)
*
*      THE "MESSAGE" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
MESSAGE  ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING WORKAREA,R13        ADDRESS WORK AREA DSECT
         L     R2,0(R2)            POINT TO MESSAGE
         MVC   WTOPARM,CWTOPARM    MOVE WTO PARM LIST TO WORK AREA
         MVC   WTLPARM,CWTLPARM    MOVE WTL PARM LIST TO WORK AREA
         MVC   WTOMSG,0(R2)        MOVE MESSAGE INTO WTO PARM LIST
         MVC   WTLMSG,0(R2)        MOVE MESSAGE INTO WTL PARM LIST
         WTL   MF=(E,WTLPARM)      WRITE MESSAGE TO LOG
         WTO   MF=(E,WTOPARM)      WRITE MESSAGE TO CONSOLE
         LEAVE RETCODE=0,          RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
         EJECT
CWTOPARM WTO   '1234567890123456789012345678901234567890123456789012345X
               6789012345678901234567890          ',MF=L
WTOLEN   EQU   *-CWTOPARM
CWTLPARM WTL   '1234567890123456789012345678901234567890123456789012345X
               6789012345678901234567890          ',MF=L
WTLLEN   EQU   *-CWTLPARM
         EJECT
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
         DS    18A                 REGISTER SAVE AREA
WTOPARM  DS    CL(WTOLEN)
         ORG   WTOPARM+4
WTOMSG   DS    CL80                WTO MESSAGE
         ORG
WTLPARM  DS    CL(WTLLEN)
         ORG   WTLPARM+4
WTLMSG   DS    CL80                WTL MESSAGE
         ORG
WORKSIZE EQU   *-WORKAREA
         END
