DUCB     TITLE 'D U C B - DISPLAY UNITS COMMAND FROM TSO/DCMS'
*---------------------------------------------------------------------*
*   UPDATES:                                                          *
*      03/24/83 U016 LDW  ADD CHECK FOR SUSPENDED CHANNEL PROGRAM     *
*      10/25/82 U015 LDW  ADD NEW DEVICE TYPES                        *
*                         FIX TAPE DENSITY LOGIC                      *
*                         REMOVE DEF=NO FOR NEW PFKEY MACRO           *
*      07/31/81 U014 SDM  FIX TO MAKE INPUT PARM WORK AGAIN           *
*      07/23/81 U013 LDW  DISPLAY 'SCRTCH' IF MOUNT PENDING VOLSER IS *
*                            BAD                                      *
*      07/10/81 U012 SDM  CHANGE POSITION LOGIC TO UCBNAME FROM       *
*                            UCBCHAN                                  *
*                         FIX PARM LENGTH SENSITIVE BUG               *
*                         FIX HELP TEXT FOR UNPRIVILEGED USER (&GIS)  *
*      07/08/81 U011 SDM  COMPLETELY REORGANIZE SCROLLING LOGIC       *
*                         FIX 0C4 ABENDS WITH 'ON' OR 'OFF' OPERANDS  *
*                         DISPLAY QUEUED I/O MODE                     *
*      06/25/81 U010 SDM  CHANGE DEFAULT MODE TO ONLINE TAPES         *
*                         IMPLEMENT GIBRALTAR COND ASSEMBLY (&GIS)    *
*                         FORCE NORMAL USERS TO ONLINE TAPES (&GIS)   *
*      06/09/81 U009 SDM  FIX MOUNT PENDING LOGIC SOME MORE           *
*                         RECOMPUTE FIRST AUTO UPDATE SCREEN          *
*                         FIX BUG BACKING UP PAST FIRST UCB           *
*      03/23/81 U008 SDM  FIX MOUNT PENDING LOGIC                     *
*      01/26/81 U007 LDW  BUILD TABLE OF DEVICE ADDRESSES DISPLAYED,  *
*                            SO THAT WE CAN SHOW ALL DEVICES EVEN IF  *
*                            A SWAP HAS OCCURRED, WHICH GETS THE      *
*                            UCB'S OUT OF ORDER, (THE OLD OPTCHAN     *
*                            CODE SKIPPED THEM)                       *
*      01/12/81 U006 LDW  CHANGE 3277 TO 3278                         *
*                         ADD DEVICE TYPES: 3203, 3290, 3800,         *
*                                           3838, 3851, 3886          *
*                         DISPLAY 'C' INSTEAD OF 'A' FOR MCS CONSOLES *
*      01/08/81 U005 SDM  RECOGNIZE MOUNT PENDING CONDITION           *
*                         MOVE CVTA TO DSECT (FIX REENTERABILITY)     *
*      08/12/80 U004 LDW  STANDARDIZE PF KEY USAGE TO CONFORM TO SPF  *
*                         CHANGE ENTER KEY TO REDISPLAY RATHER THAN   *
*                            SCROLL DOWN                              *
*                         ADD "+", "-", "." COMMANDS IN CASE NO PFK'S *
*                         ADD HELP COMMAND                            *
*                         ACCEPT "END" TO EXIT                        *
*      04/29/80 U003 LDW  SHOW FILE SEQUENCE NUMBER FOR TAPES         *
*      04/14/80 U002 LDW  SHOW OWNER FOR TAPE DRIVES                  *
*                         DEFINE PF3 TO BE EXIT                       *
*                         MISCELLANEOUS CHANGES FOR NEW TERMIO$       *
*      10/12/79 U001 LDW  ADD SOME UNIT TYPES                         *
*                         CHANGE DEFAULT TO 'ONLINE' FROM 'ALL'       *
*---------------------------------------------------------------------*
         SPACE 4
         MACRO
         DX    &HEXTYP,&CHAR
         DC    X'&HEXTYP'
         DC    CL7&CHAR
         MEND
         SPACE 3
         MACRO
&NFS     $     &TEXT
&NFS     DC    CL79&TEXT
         MEND
         SPACE 3
         LCLB  &GIS                     GIBRALTAR INFO SERVICES    U010
&GIS     SETB  1                                                   U010
*              IF NOT FOR GIBRALTAR SET ABOVE SWITCH TO 0
*              IF YOU ARE ASSEMBLING FOR GIBRALTAR SET SWITCH TO 1
         EJECT
         SPACE 3
*.
* DUCB  -
*   DISPLAY UNITS COMMAND FROM TSO/DCMS
*   ALL SYSGENNED UNITS ARE DISPLAYED STARTING FROM THE WORD ZERO
*
*   INPUT PARAMETERS ALLOWED -
*      D - DISPLAY DASD ONLY
*      T - DISPLAY TAPE ONLY
*      AL - DISPLAY ALL DEVICES
*      Q - DISPLAY ONLY DEVICES WITH QUEUE LENGTH > 0
*      CUU - DISPLAY BEGINNING AT UNIT 'CUU'
*
* PF KEY DEFINITIONS -
*
*    -----
*   × PF2 ×   AUTO UPDATE MODE - REDISPLAY CURRENT SCREEN          U004
*   ×     ×
*    -----
*
*    -----
*   × PF3 ×   EXIT                                                 U002
*   ×     ×
*    -----
*
*    -----
*   × PF4 ×   SWITCH MODES - (A) DISPLAY ALL SYSGEN'D UNITS        U004
*   ×     ×                  (B) DISPLAY ONLY ONLINE UNITS
*    -----
*
*    -----
*   × PF7 ×   SCROLL UP 20 UNITS
*   ×     ×
*    -----
*
*    -----
*   × PF8 ×   SCROLL DOWN 20 UNITS
*   ×     ×
*    -----
*
*    -----
*   × PF9 ×   RESTART AT FIRST UCB, CLEAR ALL FLAGS
*   ×     ×
*    -----
*
*
* REGISTER USAGE
*
*    R2 - POINTER TO CURRENT UCB LOOKUP TABLE ENTRY
*    R3 - POINTER TO CURRENT UCB NAME TABLE ENTRY
*    R4 - POINTER TO CURRENT SCREEN LINE ENTRY
*    R5 - POINTER TO THE CURRENT UCB
*    R7 - POINTER TO LAST UCB DISPLAYED
*    R9 - SUBROUTINE LINKAGE REGISTER
*   R11 - BASE+4096
*   R12 - BASE
*
*  THIS PROGRAM DOES NOT ATTEMPT TO FIND THE 'OPTCHAN' UNITS SO YOU
*  MAY SEE SOME UNITS APPEARING MORE THAN ONCE.
*
*  *SDC* CODE ATTEMPTS TO SKIP OPTCHAN DEVICE UCB'S, BUT MAY SKIP
*  OTHER DEVICES IF THERE HAS BEEN A 'SWAP' SINCE THE LAST IPL.
*
* IF YOU HAVE ANY QUESTIONS PLEASE CALL:
*
*     FRED LUDDY
*     AMDAHL CORP.
*     1250 E ARQUES
*     SUNNYVALE CA.
*   PHONE:
*     (408) 735 4011
*
*.
         EJECT
DUCB     DCMSTART RENT,LV=DCMSWLEN
         SPACE 1
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         USING DUCB+4096,R11
         SPACE 2
* INITIALIZE AREAS IN DSECT
         SPACE 1
         NI    IOCBFLGD,255-IOCBDYON+IOCBDFRC                      U002
         OI    IOCBFLGC,IOCBCRAO+IOCBCFRA                          U002
         SPACE 1
         MVC   DEVCLAS,CLASTAPE         DEFAULT = TAPE             U010
         MVI   FLAG,FLAGON+FLAGTA       SET TO ONLINE TAPES ONLY   U010
         SPACE 1
         L     R2,CVTPTR                AND IN THE BEGINNING...    U011
         L     R2,CVTILK2-CVT(,R2)      -> UCB WORDS               U011
         ST    R2,UCBLTBL               SAVE POINTER TO THE BEGIN  U011
         SR    R5,R5                    CLEAR FOR ICM (LATER)
         SPACE 1
         L     R1,0(,R1)                ADDRESS OF COMMAND STRING
         LH    R2,0(,R1)                LOAD COMMAND STRING LENGTH
         LH    R3,2(,R1)                LOAD OFFSET
         LA    R6,4(R1,R3)              BEGINNING OF SUBCOMMAND DATA
         SPACE 1
         AIF   (NOT &GIS).GIS01                                    U012
         TSUSRCHK  (SYSPGMR,NO,INIT1)   ALLOW SYSPGMR FULL CONTROL U012
         OI    FLAG,FLAGPRIV            INDICATE PRIVILEGED USER   U012
         SPACE 1
.GIS01   ANOP                                                      U012
INIT1    SR    R2,R3                    SUBTRACT OFFSET OF 'SUBCMDS'
         SH    R2,=H'4'                 SUB LEN OF HEADER          U012
         BNP   RESTART                  NO SUBCOMMANDS             U012
         SPACE 1
INSHIFT  EQU   *
         LR    R1,R6                    -> FIRST BYTE              U012
IS01     CLI   0(R1),C' '               ENDING BLANK?              U012
         BE    IS02                     YES, END OF DATA           U012
         LA    R1,1(,R1)                NO, INCREMENT POINTER      U012
         BCT   R2,IS01                  REDUCE COUNT AND LOOP      U012
*U014    B     RESTART                  NO DATA, GO DISPLAY        U012
IS02     LR    R2,R1                    -> END OF SUBCMD + 1       U012
         SR    R2,R6                    LENGTH OF SUBCMD           U012
         BNP   RESTART                  NO SUBCMD, IGNORE          U012
         BCTR  R2,0                     REDUCE FOR EX              U012
         SPACE 1
         EX    R2,UPSHIFT               SHIFT TO UPPERCASE
         SPACE 1
         CLC   =C'END',0(R6)            WANT OUT?                  U012
         BE    END1                     YES - LET HIM OUT          U004
         CLI   0(R6),C'H'               NEED HELP?                 U012
         BE    HELP                     YES - POOR GUY             U004
         CLI   0(R6),C'?'               REALLY NEED HELP?          U012
         BE    HELP                     YES - POOR GUY             U004
         CLC   =C'DA',0(R6)             DASD ONLY?                 U011
         BE    PARMDA                   YES - SET IT               U011
         CLI   0(R6),C'T'               TAPE ONLY?                 U011
         BE    PARMTA                   YES - SET IT               U011
         CLI   0(R6),C'Q'               DEVICES WITH I/O QUEUED?   U011
         BE    PARMQ                    YES - SET IT               U011
         CLC   =C'ON',0(R6)             ONLINE DEVICES ONLY?       U004
         BE    PARMON                   YES - SET IT               U011
         CLC   =C'OF',0(R6)             INCLUDE OFFLINE DEVICES?   U004
         BE    PARMOF                   YES - SET IT               U011
         CLC   0(2,R6),=C'AL'           ALL DEVICES?
         BE    DOALL                                               U011
         B     SETFIRST                                            U011
         SPACE 2
DOALL    MVC   DEVCLAS,CLASALL          SET CLASS MESSAGE
         NI    FLAG,255-FLAGDA-FLAGTA-FLAGQ CLEAR DASD, TAPE, + Q FLAGS
         B     RESTART
         SPACE 1
PARMDA   OI    FLAG,FLAGDA              ...YES, SET FLAG
         MVC   DEVCLAS,CLASDASD         SET CLASS MESSAGE
         NI    FLAG,255-FLAGTA          CLEAR TAPE FLAG
         B     RESTART
         SPACE 1
PARMTA   OI    FLAG,FLAGTA              ...YES, SET FLAG
         MVC   DEVCLAS,CLASTAPE         SET CLASS MESSAGE
         NI    FLAG,255-FLAGDA          CLEAR DASD FLAG
         B     RESTART                  AND PROCEED
         SPACE 1
PARMQ    OI    FLAG,FLAGQ+FLAGON        SET ONLINE AND QUEUED I/O
         B     RESTART
         SPACE 1
PARMON   OI    FLAG,FLAGON              SET ONLINE ONLY            U011
         NI    FLAG,255-FLAGQ           RESET QUEUED I/O FLAG      U011
         B     RESTART                                             U011
         SPACE 1
PARMOF   NI    FLAG,255-FLAGON-FLAGQ    SET OFFLINE AND ONLINE     U011
         B     RESTART                                             U011
         SPACE 1
SETFIRST EQU   *
         MVC   FIRSTUCB,=3C'0'          CLEAR WORK AREA            U012
         CH    R2,=H'2'                 TOO LONG?                  U012
         BNH   *+8       >=====+        NO, USE IT AS IS           U012
         LH    R2,=H'2'        ×        YES, CUT IT OFF            U012
         EX    R2,MOVEUCB  <===+        MOVE INPUT PARM TO PACK W.A.
         LA    R0,L'FIRSTUCB            LOOP COUNT                 U012
         LA    R1,FIRSTUCB              FIRST BYTE TO CHECK        U012
SF01     CLI   0(R1),C'A'               IS IT HIGH ENOUGH FOR HEX? U012
         BL    RESTART                  NO, IGNORE INPUT           U012
         CLI   0(R1),C'F'               IS IT A-F ?                U012
         BNH   SF02                     YES, IT'S OKAY             U012
         CLI   0(R1),C'0'               IS IT HIGH ENOUGH FOR NUM? U012
         BL    RESTART                  NO, IGNORE INPUT           U012
         CLI   0(R1),C'9'               IS IT 0-9 ?                U012
         BH    RESTART                  NO, IGNORE INPUT           U012
SF02     LA    R1,1(,R1)                -> NEXT BYTE               U012
         BCT   R0,SF01                  DECREMENT COUNT AND LOOP   U012
         XC    FIRSTUCB,=3C'0'          FIX COLLATING SEQUENCE     U012
         OI    FLAG,FLAGPOS             INDICATE POSITION REQUEST  U011
         B     RESTART                  AND PROCEED
         SPACE 1
MOVEUCB  MVC   FIRSTUCB(0),0(R6)        EXECUTED                   U012
UPSHIFT  OC    0(0,R6),BLANKS           EXECUTED
         SPACE 2
*   START DISPLAY WITH FIRST UCB
         SPACE 1
RESTART  MVC   UCBNBRT,=H'1'            DISPLAY FIRST UCB FOUND    U011
         SPACE 1
*   START DISPLAY WITH 'UCBNBRT' UCB
         SPACE 1
REDISP   XC    UCBNBR,UCBNBR            NO UCB'S FOUND YET         U011
         L     R2,UCBLTBL               INIT UCB LOOKUP POINTER    U011
         LA    R3,NAMELIST              INIT NAME TABLE POINTER    U011
DU01     EQU   *
         AIF   (NOT &GIS).GIS02                                    U010
         TM    FLAG,FLAGPRIV            PRIVILEGED USER?           U012
         BO    DU02                     YES, SKIP RESTRICTIONS     U012
*                                       NORMAL TSO USERS GET ONLY  U010
*                                          ONLINE TAPES WITHOUT    U010
*                                          AUTO UPDATE CLASS       U010
         NI    FLAG,255-FLAGDA-FLAGAUTO  CLEAR FLAGS               U010
         NI    IOCBFLGS,255-IOCBFNRD           CLEAR AUTO FLAGS    U010
         NI    IOCBFLGD,255-IOCBDYON-IOCBDFRC  CLEAR AUTO FLAGS    U010
         OI    FLAG,FLAGON+FLAGTA       FORCE ONLINE TAPES ONLY    U010
         MVC   DEVCLAS,CLASTAPE         SET TAPES MESSAGE          U010
DU02     EQU   *                                                   U010
.GIS02   ANOP                                                      U010
*                                       STARTING AT UCB ENTRY POINTED
*                                       TO BY R2
         LA    R8,20                    NUMBER OF UNITS TO DISPLAY
         LA    R0,FORMAT1               CLEAR SCREEN
         LA    R1,L1                        WORK AREA
         BAL   R14,@CLEAR                       USING DCMS SUBROUTINE
         MVC   L1,TITLE                 INITIALIZE TITLE LINE
         MVC   MODECLH,CLASMSG             AND LAST LINE           U011
         MVC   MODECLAS,DEVCLAS                                    U011
         MVC   MODEMH,MODEMSG                                      U011
         MVC   MODEMODE,MODEQ                                      U011
         TM    FLAG,FLAGQ               QUEUED I/O ONLY?           U011
         BO    DU03                     YES, CONTINUE WITH DISPLAY U011
         MVC   MODEMODE,MODEONLI                                   U011
         TM    FLAG,FLAGON              ONLINE ONLY?               U011
         BO    DU03                     YES, CONTINUE WITH DISPLAY U011
         MVC   MODEMODE,MODEONOF                                   U011
         SPACE 1
DU03     EQU   *
         LA    R4,L1                    GET ADDRESS OF CURRENT ENTRY
         USING OUTLINE,R4
         SPACE 1
DU04     EQU   *                        BCT LOOP HERE...
*                                       ...FOR EACH LINE OF OUTPUT.
         LA    R4,79(,R4)               GET NEXT LINE ADDRESS
         SPACE 1
DU05     EQU   *                        LOOP TO HERE FOR NEXT UCB.
         CLC   0(2,R2),=X'FFFF'         END OF UCB TABLE?
         BE    PUTOUT                   ...YES, STOP HERE.
         ICM   R5,3,0(R2)               SEE IF WE HAVE A LIVE ONE
         BNZ   DU07                     THIS UNIT DOES EXIST
         USING UCBOB,R5                                            U011
         SPACE 1
DU06     EQU   *
         LA    R2,2(,R2)                GET NEXT
         B     DU05
         SPACE 1
DU07     EQU   *
         SPACE 1
*    HAVE A POSSIBLE CANDIDATE.  CHECK FLAGS FOR RESTRICTIONS
*    INDICATED BY USER, AND VERIFY THAT DEVICE MATCHES REQUEST.
         SPACE 1
*  BUT FIRST LET'S SEE IF WE ALREADY DID THIS ONE                  U007
         LA    R14,NAMELIST             -> FIRST                   U007
         LA    R0,3                     INCREMENT                  U007
PREVLOOP CR    R14,R3                   END OF LIST?               U011
         BNL   SHOWNOW                  YES, DISPLAY UNIT          U011
         CLC   UCBNAME(3),0(R14)        THIS IT?                   U007
         BE    DU06                     YES - DON'T DISPLAY AGAIN  U007
         AR    R14,R0                   -> NEXT SLOT               U007
         B     PREVLOOP                 KEEP LOOKING               U007
*
*
SHOWNOW  MVC   0(3,R14),UCBNAME         SAVE DEVICE ADDRESS        U007
         AR    R3,R0                    -> NEXT SLOT               U011
*
         TM    FLAG,FLAGON              ONLINE ONLY ?
         BZ    DU08                     NO..
         TM    UCBSTAT,UCBONLI
         BZ    DU06                     NOT ONLINE, IGNORE
*
DU08     EQU   *
         TM    FLAG,FLAGDA              LIST DASD ONLY?
         BZ    CHKTP                    ...NO
         TM    UCBTBYT3,UCB3DACC        ...YES, CHECK FOR DASD
         BZ    DU06                     NOT DASD, SKIP
         B     GOTONE                   ...OK, FORMAT IT
*
CHKTP    TM    FLAG,FLAGTA              LIST TAPE ONLY?
         BZ    GOTONE                   ...NO, PROCEED
         TM    UCBTBYT3,UCB3TAPE        ...YES, CHECK FOR TAPE
         BZ    DU06                     NOT TAPE, SKIP
*
GOTONE   EQU   *                        PASSED DASD/TAPE CRITERIA
*   NOW CONTROL PAGING BY BYPASSING ACCEPTABLE UCB'S UNTIL THE
*   CONTROL COUNTER IS EQUAL TO THE TOP OF SCREEN TARGET.
         LA    R1,1                                                U011
         AH    R1,UCBNBR                                           U011
         STH   R1,UCBNBR                                           U011
         CH    R1,UCBNBRT               HAVE WE HIT TOP OF SCREEN? U011
         BL    DU06                     NO, SKIP THIS ONE          U011
         SPACE 1
*   TEST FOR POSITION BY NAME
CKPOS    TM    FLAG,FLAGPOS             POSITIONING BY NAME?       U011
         BNO   TESTQ                    NO, SKIP THIS TEST         U011
         MVC   DWORK(3),UCBNAME         GET UCB IDENTIFIER         U012
         XC    DWORK(3),=3C'0'          FIX COLLATING SEQUENCE     U012
         CLC   DWORK(3),FIRSTUCB        IS IT PAST FIRST REQ'D UCB?U012
         BL    DU06                     ...NO, SKIP IT.
         NI    FLAG,255-FLAGPOS         RESET POSITIONING FLAG     U011
         MVC   UCBNBRT,UCBNBR           SET TOP OF SCREEN NUMBER   U011
         SPACE 1
*   NOW TEST FOR I/O QUEUED RESTRICTIONS
TESTQ    TM    FLAG,FLAGQ               ONLY DEVICES W/ I/O QUEUED?
         BZ    MOVEIT                   ...NO, THEN THIS ONE IS OK.
         CLI   UCBCNT,X'00'             ANY QUEUE?
         BZ    DU06                     ...NO, SKIP THIS ONE.
*
*  THIS UCB HAS PASSED ALL THE SELECTION CRITERIA.
*  BEGIN FORMATTING OUTPUT INFORMATION.
*
MOVEIT   EQU   *
         MVC   OUTUNIT(3),UCBNAME       MOVE THE EBCDIC NAME
*
* DETERMINE UNIT TYPE..
*
         CLC   UCBTBYT1(4),=F'0'        DUMMY?                     U015
         BNE   DU09A                    NO - TRY NEXT              U015
         MVC   OUTTYPE,=CL8'DUMMY'      YES - SHOW IT              U015
         B     DU14                     AND PRESS ON               U015
DU09A    EQU   *                                                   U015
         CLI   UCBTBYT3,UCB3TAPE        IS THIS A TAPE
         BNE   DU09                     NO, TRY NEXT
         BAL   R9,ITSATAPE              GO DO TAPE UNIT STUFF
         B     DU14                     AND PRESS ON
DU09     EQU   *
         CLI   UCBTBYT3,UCB3COMM        IS THIS A TERMINAL
         BNE   DU10                     NO, TRY NEXT
         BAL   R9,ITSACOMM              GO DO TERMINAL
         B     DU14                     AND PRESS ON
DU10     EQU   *
         CLI   UCBTBYT3,UCB3CTC         IS THIS A CTC
         BNE   DU11                     NO, THANK GOD IF THIS A 470
         BAL   R9,ITSACTC               GO DO CTC STUFF
         B     DU14                     AND PRESS ON
DU11     EQU   *
         CLI   UCBTBYT3,UCB3DACC        IS THIS DASD ?
         BNE   DU12                     NO, WHATS UP DOC ?
         BAL   R9,ITSADASD              GO DO DASDSTUFF
         B     DU14                     AND PRESS ON
DU12     EQU   *
         CLI   UCBTBYT3,UCB3DISP        IS THIS GRAPHICS
         BNE   DU13                     NO.. HOPE FOR UR
         BAL   R9,ITSATUBE              GO DOWN THE TUBES
         B     DU14                     AND PRESS ON
DU13     EQU   *
         CLI   UCBTBYT3,UCB3UREC        IS THIS UNIT RECORD ?
         BNE   DU14                     NO.. LEAVE THE DAMN THING BLANK
         BAL   R9,ITSAUR                GO DO THE UNIT RECORD STUFF
*
DU14     EQU   *
*
* ANALYZE THE DEVICES CURRENT STATUS, ONLINE OR OFF, ALLOCATED, OR NOT
* BUSY OR NOT, ETC...
*
         MVC   OUTSTAT(7),OFFLINE       DEFAULT TO OFFLINE
         TM    UCBSTAT,UCBONLI          IS THIS DEVICE ONLINE ?
         BZ    DU15                     NO, PRESS ON
         MVC   OUTSTAT(7),BLANKS        RESET A MINUTE
         MVI   OUTSTAT,C'O'             SHOW IT'S ONLINE
*
* SEE WHATS HAPPENING WITH ONLINE DEVICES
*
         TM    UCBSTAT,UCBALOC          IS IT ALLOCATED
         BZ    *+8                      NO.. SKIP MVI
         MVI   OUTSTAT,C'A'             SHOW ALLOCATED
         CLI   UCBTBYT3,UCB3DACC        IS IT A DISK?              U006
         BE    NOTCONS                  YES.. SKIP NEXT TEST       U006
         TM    UCBSTAT,UCBSYSR          IS IT A CONSOLE?           U006
         BZ    NOTCONS                  NO.. SKIP MVI              U006
         MVI   OUTSTAT,C'C'             SHOW CONSOLE               U006
NOTCONS  TM    UCBFLA,UCBNRY            NOT READY ?
         BZ    NOTMTP                   NO, AND SKIP MTP LOGIC TOO U009
         MVC   OUTSTAT+1(5),NOTREADY
         CLI   UCBTBYT3,UCB3DACC        DIRECT ACCESS DEVICE?      U008
         BNE   MP01                     NO, SKIP DISK MTP TEST     U008
         TM    UCBDMCT,UCBMOUNT         MOUNT PENDING?             U008
         BO    MP02                     YES, GO SAY SO             U008
         B     NOTMTP                   LEAVE QUIETLY              U008
MP01     CLI   UCBTBYT3,UCB3TAPE        TAPE DEVICE?               U008
         BNE   NOTMTP                   NO, SKIP TAPE MTP TEST     U008
         TM    UCBSTAB,UCBBNUL          MESSAGES OUTSTANDING?      U008
         BNO   NOTMTP                   NO, ASSUME NO MOUNT        U008
MP02     MVC   OUTSTAT+1(5),MTP         OVERLAY NOTREADY IN OUTPUT U008
         CLC   OUTVOL(6),UCBVOLI        WAS VOLSER READABLE?       U013
         BE    *+10                     YES - OK                   U013
         MVC   OUTVOL(6),=C'SCRTCH'     NO - MTP MUST BE 'SCRTCH'  U013
NOTMTP   TM    UCBFLA,UCBBSY            IS IT BUSY ?
         BZ    DU17                     NO..
         MVC   OUTSTAT+1(5),BUSY        SHOW BUSY
*
* ATTEMPT TO FIND OUT WHY
*
         TM    UCBFLA,UCBPSNS           PENDING SENSE ?
         BZ    DU15                     NO
         MVC   OUTNOTE(13),PENDSNSE     SHOW REASON                U003
         B     DU17                     GET OUT
DU15     EQU   *
         TM    UCBFLA,UCBCUB            IS THE CONTROL UNIT BUSY ?
         BZ    DU16                     NO, SEE IF CHANNEL PGM ACTIVE
         MVC   OUTNOTE(17),CUB          SHOW CONTROL UNIT BUSY     U003
         B     DU17                     AND SPLIT
DU16     EQU   *
         TM    UCBFLA,UCBACTV           CHAN PGM ACTIVE
         BZ    DU17                     SORRY.. I GIVE UP
         MVC   OUTNOTE(22),CHPGMACT     SHOW CHANNEL PROGRAM ACT   U002
         L     R1,UCBEXTPT              -> COMMON EXTENSION        U016
         USING UCBCMEXT,R1                                         U016
         TM    UCBFL7,UCBSSPND          CHANNEL PGM SUSPENDED?     U016
         BZ    DU17                     NO - SKIP                  U016
         MVC   OUTNOTE+16(9),=C'SUSPENDED'  CHANGE 'ACTIVE' ...    U016
         DROP  R1                                                  U016
DU17     EQU   *
         LR    R1,R5                    GET THE ADDRESS OF THE UCB
         SR    R6,R6                    CLEAR FOR IC
         IC    R6,UCBCNT                GET CURRENT QUEUE LENGTH
         CVD   R6,DWORK                 TO DECIMAL
         UNPK  DWORK(3),DWORK+6(2)
         OI    DWORK+2,X'F0'            SET ZONE
         MVC   OUTQLEN+2(3),DWORK       MOVE TO OUTPUT FIELD
         LA    R2,2(,R2)                GET PTR TO NEXT LOOK UP
         BCT   R8,DU04                  LOOP FOR THE NEXT
         TITLE 'WRITE SCREEN AND (OPTIONALLY) READ INPUT'
PUTOUT   EQU   *
         NI    FLAG,255-FLAGPOS         CLEAR POSITION REQUEST     U011
*
PUTBAL   EQU   *                        DOIT JACK !!!
         TM    FLAG,FLAGAUTO            AUTO UPDATE CLASS?
         BO    WRITE                    ...YES, DO WRITE ONLY
*
PUTHELP  TRMIO IOCB,FORMAT=FORMAT1,IMAGE=L1    WRITE/READ
*
         PFKEY DEF=REDISP,ATTN=END1,ENTER=ENTER,                   LDW4$
               K1=HELP,K2=AUTO,K3=END1,K4=FLIPMODE,                LDW4$
               K7=BACK,K8=FORWARD,K9=DOALL,K12=END1                U004
*
WRITE    EQU   *                            WRITE ONLY
         TRMIO IOCB,WRITE,FORMAT=FORMAT1,IMAGE=L1
         SPACE 1
         PFKEY ATTN=AUTOOFF                                        U015
         TITLE 'INPUT HANDLING SUBROUTINES'
AUTO     EQU   *                        AUTO UPDATE CLASS REQUESTED
         OI    FLAG,FLAGAUTO
         OI    IOCBFLGS,IOCBFNRD        NO READ
         OI    IOCBFLGD,IOCBDYON+IOCBDFRC
         B     REDISP                                              U009
         SPACE 1
AUTOOFF  EQU   *                        TURN OFF AUTO UPDATE
         NI    FLAG,255-FLAGAUTO
         NI    IOCBFLGS,255-IOCBFNRD
         NI    IOCBFLGD,255-IOCBDYON-IOCBDFRC
         B     PUTBAL
         SPACE 1
FORWARD  EQU   *                        GET NEXT 20 UNITS
         CLC   0(2,R2),=X'FFFF'         ARE WE AT END ALREADY?
         BE    RESTART                  ...YES, SO GO BACK TO ZERO.U011
         LA    R1,20                                               U011
         AH    R1,UCBNBRT               NEW CURRENT UCB            U011
         STH   R1,UCBNBRT               SET NEW TOP OF SCREEN      U011
         B     DU01                     GO GET NEXT SET OF UCBS.
         SPACE 1
ENTER    EQU   *                        HIT ENTER KEY
         LA    R0,L'ENTRLN              LENGTH OF DATA             U012
         LA    R6,ENTRLN                FIRST INPUT BYTE           U012
ENTER1   CLI   0(R6),C' '               LEADING BLANK?             U012
         BNE   ENTER2                   NO, TEST THE DATA          U012
         LA    R6,1(,R6)                -> NEXT BYTE               U012
         BCT   R0,ENTER1                DECREMENT LENGTH AND LOOP  U012
         B     REDISP                   NO DATA, GO REDISPLAY      U012
ENTER2   CLI   0(R6),C'.'               PAGE FORWARD?              U012
         BE    FORWARD                  YES - DO IT                U004
         CLI   0(R6),C'+'               PAGE FORWARD?              U012
         BE    FORWARD                  YES - DO IT                U004
         CLI   0(R6),C'-'               PAGE BACKWARD?             U012
         BE    BACK                     YES - DO IT                U004
         LR    R2,R0                    DATA LENGTH FOR 'INSHIFT'  U012
         B     INSHIFT                  JUST GO TO PARM ANALYSIS
         SPACE 1
FLIPMODE EQU   *                        SWITCH ONLINE/OFFLINE MODES
         TM    FLAG,FLAGQ               QUEUED I/O MODE?           U011
         BO    PARMON                   YES, GO SET ONLINE MODE    U011
         TM    FLAG,FLAGON              ONLINE MODE?               U011
         BO    PARMOF                   YES, GO SET OFFLINE MODE   U011
         B     PARMON                   OTEHRWISE, SET ONLINE      U011
         SPACE 1
*
* COUNT BACK UP 20 UNITS PRIOR TO *FIRST* ONE ON SCREEN
*
BACK     EQU   *
         LH    R1,UCBNBRT               TOP OF SCREEN UCB NUMBER   U011
         SH    R1,=H'20'                REDUCE BY TWENTY           U011
         BNP   RESTART                  NOT OKAY, START AT TOP     U011
         STH   R1,UCBNBRT               SAVE NEW TOP OF SCREEN     U011
         B     REDISP                   AND GO FORMAT DISPLAY      U011
         SPACE 3
HELP     LA    R14,L1                   DEST ADDR                  U004
         LH    R15,FORMAT1              DEST LEN                   U004
         LA    R0,HELPIMG               SOURCE ADDR                U004
         LA    R1,HELPLEN               SOURCE LEN                 U004
         AIF   (NOT &GIS).GIS03                                    U012
         TM    FLAG,FLAGPRIV            PRIVILEGED USER?           U012
         BO    HELP1                    YES, WE HAVE RIGHT IMAGE   U012
         LA    R0,HELPIMG1              NO, POINT TO NEW IMAGE     U004
         LA    R1,HELPLEN1                 AND NEW IMAGE LENGTH    U004
.GIS03   ANOP                                                      U012
HELP1    ICM   R1,B'1000',BLANKS        PAD CHAR                   U004
         MVCL  R14,R0                   COPY THE HELP SCREEN       U004
         B     PUTHELP                  GO WRITE IT                U004
         TITLE 'DEVICE TYPE SPECIFIC SUBROUTINES'
ITSACTC  EQU   *                        UCBTYPE3=X'41'
         MVC   OUTTYPE,CTCTYPE
         BR    R9
         SPACE 2
ITSAUR   EQU   *                        UCBTYPE3=X'08'
         LA    R1,URTYPES               GET DEVICE TYPES ADDRESSES
UR0      EQU   *
         CLC   UCBUNTYP,0(R1)           IS IT THIS ?
         BE    UR1                      SHORE IS
         LA    R1,TYPESIZE(R1)          GET NEXT IF NOT
         CLI   0(R1),255                STOPPER ?
         BNE   UR0
UR1      EQU   *
         MVC   OUTTYPE,1(R1)            MOVE THE DESCRIPTOR
         BR    R9
         SPACE 2
ITSACOMM EQU   *                        UCBTYPE3=X'40'
         LA    R1,COMMTYPS              GET DEVICE TYPES ADDRESSES
         MVC   DWORK(1),UCBTBYT1
         NI    DWORK,15                 GET MODEL CODE
CO0      EQU   *
         CLC   DWORK(1),0(R1)           IS IT THIS ?
         BE    CO1                      SHORE IS
         LA    R1,TYPESIZE(,R1)         GET NEXT IF NOT
         CLI   0(R1),255                STOPPER ?
         BNE   CO0
         MVC   DWORK(1),UCBUNTYP
         NI    DWORK,15                 GET CONTROL UNIT-TYPE
         LA    R1,COMMCUS
CO00     EQU   *
         CLC   DWORK(1),0(R1)           IS IT THIS ?
         BE    CO1                      SHORE IS
         LA    R1,TYPESIZE(,R1)         GET NEXT IF NOT
         CLI   0(R1),255                STOPPER ?
         BNE   CO00
CO1      EQU   *
         MVC   OUTTYPE,1(R1)            MOVE THE DESCRIPTOR
         BR    R9
         SPACE 2
ITSATUBE EQU   *                        UCBTYPE3=X'10'
         LA    R1,TUBETYPS              GET DEVICE TYPES ADDRESSES
TU0      EQU   *
         CLC   UCBUNTYP,0(R1)           IS IT THIS ?
         BE    TU1                      SHORE IS
         LA    R1,TYPESIZE(,R1)         GET NEXT IF NOT
         CLI   0(R1),255                STOPPER ?
         BNE   TU0
TU1      EQU   *
         MVC   OUTTYPE,1(R1)            MOVE THE DESCRIPTOR
         BR    R9
         SPACE 2
ITSATAPE EQU   *                        UCBTYPE3=X'80'
         LA    R1,TAPETYPS              GET DEVICE TYPES ADDRESSES
TA0      EQU   *
         CLC   UCBUNTYP,0(R1)           IS IT THIS ?
         BE    TA1                      SHORE IS
         LA    R1,TYPESIZE(,R1)         GET NEXT IF NOT
         CLI   0(R1),255                STOPPER ?
         BNE   TA0
TA1      EQU   *
         MVC   OUTTYPE,1(R1)            MOVE THE DESCRIPTOR
         TM    UCBSTAT,UCBONLI          IS IT ONLINE ?
         BZ    TA10                     NO.. MOVE ON
         MVC   OUTVOL(6),UCBVOLI        MOVE THE VOL-SER
         TR    OUTVOL(6),BLANKTAB       MAKE SURE IT IS READABLE
*
* CHECK  OUT THE DENSITIES AVAILABLE
*
TA10     EQU   *
*U015    TM    UCBTBYT2,UCBDUDN1+UCB2OPT1 DUAL  800/1600 ?
         TM    UCBTBYT2,UCBDUDN1        DUAL  800/1600 ?           U015
         BZ    TA2                      NO..
         MVC   OUTNOTE(9),DD1600
         MVC   OUTTYPE+4(2),=C'-4'      3400-4                     U004
         TM    UCBTBYT2,UCB2OPT1        SUPPORT ASCII ?
         BZ    TA7                      NO..
         MVC   OUTNOTE+9(5),ASCII
         B     TA7
TA2      EQU   *
         TM    UCBTBYT2,UCBDUDN2        1600/6250 ?
         BZ    TA3                      NO..
         MVC   OUTNOTE(9),DD6250
         MVC   OUTTYPE+4(2),=C'-6'      3400-6                     U004
         B     TA7
TA3      EQU   *
         TM    UCBTBYT2,UCB2OPT0        7-TRACK ?
         BZ    TA4                      NO..
         MVC   OUTNOTE(8),TRACK7
*U015    MVC   OUTNOTE+8(9),DD1600
         MVC   OUTTYPE+4(2),=C'-2'      3400-2                     U004
         B     TA7
TA4      EQU   *
         TM    UCBTBYT1,UCBD1600        1600 BPI ?
         BZ    TA5
         MVC   OUTNOTE(4),D1600
         MVC   OUTTYPE+4(2),=C'-3'      3400-3                     U004
         B     TA7
TA5      EQU   *
         TM    UCBTBYT1,UCBD6250        6250 BPI ?
         BZ    TA7                      NO..
         MVC   OUTNOTE(4),D6250
         MVC   OUTTYPE+4(2),=C'-5'      3400-5                     U004
TA7      EQU   *
         TM    UCBSTAT,UCBALOC          IS IT ALLOCATED?           U002
         BNO   TA9                      NO - SKIP                  U002
         MVC   OUTNOTE+26(5),=C'FILE='                             U003
         LH    R0,UCBFSEQ               GET FILE SEQUENCE NUMBER   U003
         CVD   R0,DWORK                                            U003
         OI    DWORK+7,X'0F'            REMOVE SIGN                U003
         UNPK  OUTNOTE+31(3),DWORK+6(2) PUT ON SCREEN              U003
         L     R1,UCBEXTPT              -> COMMON EXTENSION        U002
         LH    R15,UCBASID-UCBETI(,R1)  GET ASID OF OWNER          U002
         BCTR  R15,0                    APPLY FUDGE FACTOR         U002
         SLL   R15,2                    * 4 BYTES / ENTRY          U002
         L     R1,16                    -> CVT                     U002
         L     R1,CVTASVT-CVT(,R1)      -> ASVT                    U002
         L     R1,ASVTENTY-ASVT(R15,R1) -> ASCB                    U002
         MVC   OUTSTATE(3),=C'-->'      FLAG IT                    U002
         L     R15,ASCBJBNI-ASCB(,R1)   -> JOBNAME                 U002
         LTR   R15,R15                  OK?                        U002
         BNZ   TA8                      YES - CONTINUE             U002
         L     R15,ASCBJBNS-ASCB(,R1)   -> JOBNAME FOR S/M/L       U002
         LTR   R15,R15                  OK?                        U002
         BZ    TA9                      NO - BYPASS                U002
TA8      EQU   *                                                   U002
         MVC   OUTSTATE+3(8),0(R15)     MOVE IN JOBNAME            U002
TA9      EQU   *                                                   U002
         BR    R9
         SPACE 2
ITSADASD EQU   *                        UCBTYPE3=X'20'
         TM    UCBWGT,UCBMTPXP          IS THIS THE DOLDRUMS ?
         BZ    DA00                     NO..
         TM    UCBUA,15                 IS THIS THE BASE EXPOSURE ?
         BNZ   DU06                     NOPE.. GOTO NEXT
DA00     EQU   *
         LA    R1,DASDTYPS              GET DEVICE TYPES ADDRESSES
DA0      EQU   *
         CLC   UCBUNTYP,0(R1)           IS IT THIS ?
         BE    DA1                      SHORE IS
         LA    R1,TYPESIZE(,R1)         GET NEXT IF NOT
         CLI   0(R1),255                STOPPER ?
         BNE   DA0
DA1      EQU   *
         MVC   OUTTYPE,1(R1)            MOVE THE DESCRIPTOR
         TM    UCBTBYT2,UCBRVDEV        IS THIS VIO
         BZ    DA10                     NO..
         MVC   OUTVOL,VIOUNIT           INDICATE SO..
DA10     EQU   *
         TM    UCBSTAT,UCBONLI          IS IT ONLINE ?
         BZR   R9                       NO.. RETURN
         TM    UCBSTAB,UCBPGFL          IS THERE PAGING ON THIS PACK
         BZ    *+10
         MVC   OUTNOTE(13),PAGEPACK     TELL THE USER
         MVC   OUTVOL(6),UCBVOLI        MOVE THE VOL-SER
         TR    OUTVOL(6),BLANKTAB       MAKE SURE IT IS READABLE
         TM    UCBSTAT,UCBSYSR          IS THIS THE SYSRES ?
         BZ    DA2                      NO... PRESS ON
         MVC   OUTSTATE(10),SYSRES      INDICATE SO
         BR    R9                       AND RETURN
         SPACE
DA2      EQU   *
         TM    UCBSTAB,UCBBPRV          IS THIS VOL PRIVATE ?
         BZ    DA3                      NO..
         MVC   OUTSTATE,PRIVATE         SHOW IT IS SO
DA3      EQU   *
         TM    UCBSTAB,UCBBPUB          IS IT PUBLIC ?
         BZ    DA4                      NO..
         MVC   OUTSTATE,PUBLIC
DA4      EQU   *
         TM    UCBSTAB,UCBBSTR          STORAGE ?
         BZ    DA5                      NO...
         MVC   OUTSTATE,STORAGE
DA5      EQU   *
         TM    UCBSTAT,UCBRESV          IS IT RESERVED ?
         BZ    DA6                      NO..
         MVC   OUTSTAT2,RESERVED
DA6      EQU   *
         TM    UCBSTAT,UCBPRES          PERMANENTLY RESIDENT
         BZ    DA7                      NO..
         MVC   OUTSTAT2,PRES
DA7      EQU   *
         BR    R9
         TITLE 'D U - DATA AREAS AND THE LIKE'
FORMAT1  DCMSFMT
L1       DCMSFLD  79
L2       DCMSFLD  79,INTEN=LO
L3       DCMSFLD  79,INTEN=LO
L4       DCMSFLD  79,INTEN=LO
L5       DCMSFLD  79,INTEN=LO
L6       DCMSFLD  79,INTEN=LO
L7       DCMSFLD  79,INTEN=LO
L8       DCMSFLD  79,INTEN=LO
L9       DCMSFLD  79,INTEN=LO
L10      DCMSFLD  79,INTEN=LO
L11      DCMSFLD  79,INTEN=LO
L12      DCMSFLD  79,INTEN=LO
L13      DCMSFLD  79,INTEN=LO
L14      DCMSFLD  79,INTEN=LO
L15      DCMSFLD  79,INTEN=LO
L16      DCMSFLD  79,INTEN=LO
L17      DCMSFLD  79,INTEN=LO
L18      DCMSFLD  79,INTEN=LO
L19      DCMSFLD  79,INTEN=LO
L20      DCMSFLD  79,INTEN=LO
L21      DCMSFLD  79,INTEN=LO
ENTRLN   DCMSFLD  79,ALPHA,CURSOR=AFTER
MODELN   DCMSFLD  79
         DCMSFEND
CHPGMACT DC    C'CHANNEL PROGRAM ACTIVE'                           U002
CUB      DC    C'CONTROL UNIT BUSY'                                U003
PENDSNSE DC    C'SENSE PENDING'                                    U003
OFFLINE  DC    C'OFFLINE'
BUSY     DC    C'-BUSY'
NOTREADY DC    C'-NRDY'
MTP      DC    C'-MTP '                                            U005
VIOUNIT  DC    CL7'V-I-O'
DD6250   DC    CL9'1600/6250'
DD1600   DC    CL9'800/1600'
D1600    DC    CL4'1600'
D6250    DC    CL4'6250'
ASCII    DC    CL5'ASCII'
*TRACK7  DC    CL8'7-TRACK,'
TRACK7   DC    CL8'7-TRACK'                                        U015
PAGEPACK DC    CL13'<PAGING-PACK>'
CLASMSG  DC    C'CLASS ='
CLASALL  DC    C'ALL '
CLASDASD DC    C'DASD'
CLASTAPE DC    C'TAPE'
MODEMSG  DC    C'MODE ='
MODEONLI DC    C'ONLINE ONLY        '
MODEONOF DC    C'ONLINE AND OFFLINE '
MODEQ    DC    C'QUEUED I/O ONLY    '                              U011
ZEROES   DC    CL8'00000000'
*
TITLE    DC    CL79'UNIT TYPE   STATUS  VOLSER  VOLSTATE  Q-LEN  ______$
               ____NOTES_____________'
         LTORG
BLANKTAB DC    CL256' '
         ORG   BLANKTAB+C'Ö'
         DC    C'Ö.<(+×',X'50'
         ORG   BLANKTAB+C'!'
         DC    C'!$*);^-/'
         ORG   BLANKTAB+C','
         DC    C',%_>?'
         ORG   BLANKTAB+C':'
         DC    C':#@''="'
         ORG   BLANKTAB+C'A'
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+C'J'
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+C'S'
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'0'
         DC    C'0123456789'
         ORG
TYPESIZE EQU   8                        SIZE OF A 'DX' ENTRY
PUBLIC   DC    CL4'PUBL'
PRIVATE  DC    CL4'PRIV'
STORAGE  DC    CL4'STRG'
PRES     DC    CL7'/RSDNT'
RESERVED DC    CL7'/RSERV'
SYSRES   DC    CL10'<-SYSRES->'
CTCTYPE  DC    CL7'CTC'
         PRINT NOGEN                                               U002
*  THIS TABLE IS BY LOW ORDER NIBBLE OF 4TH UCBTYPE BYTE           U002
*  (UCB+X'13')                                                     U002
COMMCUS  EQU   *              XXXX40XN  UCBTYP3=X'40' (IF NOT COMMTYP)
         DX    1,'2702'
         DX    2,'2701'
         DX    3,'2703'
         DX    4,'2955'                 FEDAU                      U001
         DX    5,'3704/5'
         DX    FF,'COMM'
TAPETYPS EQU   *              XXXX80NN  UCBTYP3=X'80'
         DX    1,'2400'
         DX    3,'3400'
         DX    FF,'TAPE'
DASDTYPS EQU   *              XXXX20NN  UCBTYP3=X'20'
         DX    6,'2305-1'
         DX    7,'2305-2'
         DX    8,'2314'
         DX    9,'3330'
         DX    A,'3340'
         DX    B,'3350'
         DX    C,'3375'                                            U015
         DX    D,'3330-1'
         DX    E,'3380'                                            U015
         DX    FF,'DASD'
TUBETYPS EQU   *              XXXX10NN  UCBTYP3=X'10'
         DX    2,'2250'                                            U015
         DX    8,'3066'
         DX    9,'3278'                                            U006
         DX    A,'3284'
         DX    B,'3286'
         DX    C,'3158'                 158 CONSOLE                U001
         DX    D,'3036'                 3033 CONSOLE               U001
         DX    FF,'TUBE'
COMMTYPS EQU   *              XNXX40XX  UCBTYP3=X'40'
         DX    1,'TWX'
         DX    3,'2740'
         DX    4,'2740'
         DX    5,'2740C'
         DX    6,'2741P'
         DX    7,'BSC3'
         DX    8,'2740X'
         DX    9,'2740B'
         DX    FF,'COMM'
URTYPES  EQU   *              XXXX08NN  UCBTYP3=X'08'
         DX    1,'2540-R'
         DX    2,'2540-P'
         DX    3,'1442-R'
         DX    4,'2501'
         DX    5,'2520'
         DX    6,'3505'
         DX    8,'1403'
         DX    9,'3211'
         DX    A,'1443'
         DX    B,'3203'                                            U006
         DX    C,'3525'
         DX    E,'3800'                                            U006
         DX    10,'2671'                PAPER TAPE READER
         DX    16,'3890'                DOCUMENT PROCESSOR         U006
         DX    17,'3886'                OPTICAL CHARACTER READER   U006
         DX    18,'2495'
         DX    1B,'1287'
         DX    1C,'1288'
         DX    20,'1052'
         DX    22,'3210'
         DX    23,'3215'
         DX    30,'3213'
         DX    3D,'7443'                SRF                        U002
         DX    42,'3851'                MSF                        U006
         DX    44,'3540'
         DX    4C,'3838'                ARRAY PROCESSOR            U006
         DX    FF,'U/R'
         PRINT GEN                                                 U004
         TITLE 'D U - TERMINATION AND REENTRANT DATA AREAS'
END1     DCMSTOP
         SPACE 2
DUCB     CSECT                          RESUME                     U004
         PRINT NOGEN                                               U006
HELPIMG  $     '          * * *  D U  * * *        HELP'           U004
         $     ' '
 $ 'THE PF KEYS ARE DEFINED AS FOLLOWS:         COMMANDS:'
 $ ' '
 $ '  _____________________________      HELP - THIS SCREEN'
 $ '  ×PF1   13×PF2   14×PF3    15×      END  - TERMINATE DU'
 $ '  ×  HELP  × UPDATE ×   END   ×      ON   - SELECT ONLINE DEVICES O$
               NLY'
 $ '  ×________×________×_________×      OFF  - INCLUDE OFFLINE DEVICES$
               '
 $ '  ×PF4   16×PF5   17×PF6    18×      DA   - SELECT DASD ONLY'
 $ '  × ON/OFF ×        ×         ×      T    - SELECT TAPE ONLY'
 $ '  ×________×________×_________×      Q    - SELECT ONLY DEVICES WIT$
               H I/O QUEUED'
 $ '  ×PF7   19×PF8   20×PF9    21×      ALL  - RESET DA/T/Q'
 $ '  ×  DOWN  ×   UP   × RESTART ×      '
 $ '  ×________×________×_________×      +    - SCROLL DOWN'
 $ '  ×PF10  22×PF11  23×PF12   24×      .    - SCROLL DOWN'
 $ '  ×        ×        ×         ×      -    - SCROLL UP'
 $ '  ×________×________×_________×      '
HELPLEN  EQU   *-HELPIMG                                           U004
         AIF   (NOT &GIS).GIS04                                    U012
         SPACE 2
HELPIMG1 $     '          * * *  D U  * * *        HELP'
         $     ' '
 $ 'THE PF KEYS ARE DEFINED AS FOLLOWS:         COMMANDS:'
 $ ' '
 $ '  _____________________________      HELP - THIS SCREEN'
 $ '  ×PF1   13×PF2   14×PF3    15×      END  - TERMINATE DU'
 $ '  ×  HELP  ×        ×   END   ×      ON   - SELECT ONLINE DEVICES O$
               NLY'
 $ '  ×________×________×_________×      Q    - SELECT ONLY DEVICES WIT$
               H I/O QUEUED'
 $ '  ×PF4   16×PF5   17×PF6    18×      ALL  - RESET Q'
 $ '  ×        ×        ×         ×      '
 $ '  ×________×________×_________×      '
 $ '  ×PF7   19×PF8   20×PF9    21×      '
 $ '  ×  DOWN  ×   UP   × RESTART ×      '
 $ '  ×________×________×_________×      +    - SCROLL DOWN'
 $ '  ×PF10  22×PF11  23×PF12   24×      .    - SCROLL DOWN'
 $ '  ×        ×        ×         ×      -    - SCROLL UP'
 $ '  ×________×________×_________×      '
HELPLEN1 EQU   *-HELPIMG1
.GIS04   SPACE 1
         PRINT GEN                                                 U006
         SPACE 2
DCMSWORK DSECT                          RESUME                     U004
DWORK    DS    D
         DSGEN FORMAT1                  GENERATE SCREEN WORK AREA
         SPACE 1
         ORG   MODELN                   REDEFINE MODELN AREA       U011
         DS    CL1                                                 U011
MODECLH  DS    CL(L'CLASMSG)                                       U011
         DS    CL1                                                 U011
MODECLAS DS    CL(L'CLASALL)                                       U011
         DS    CL3                                                 U011
MODEMH   DS    CL(L'MODEMSG)                                       U011
         DS    CL1                                                 U011
MODEMODE DS    CL(L'MODEONLI)                                      U011
         ORG
         SPACE 2
UCBLTBL  DS    A                        START OF UCB LOOKUP TABLE  U011
UCBNBRT  DS    H                        NUMBER OF FIRST DSPLY UCB  U011
UCBNBR   DS    H                        NUMBER OF CUR DSPLY UCB    U011
         SPACE 1
FLAG     DS    X                        DEFAULT 'ALL'
FLAGON   EQU   X'80'                    USE ONLY ONLINE PEOPLE
FLAGDA   EQU   X'40'                    ONLY DASD DEVICES
FLAGTA   EQU   X'20'                    ONLY TAPE DEVICES
FLAGQ    EQU   X'10'                    ONLY DEVICES WITH I/O QUEUED
FLAGAUTO EQU   X'08'                    AUTO UPDATE CLASS
FLAGPOS  EQU   X'04'                    POSITION TO UCB BY NAME    U011
FLAGPRIV EQU   X'02'                                               U012
*
FIRSTUCB DS    CL3                      DEFAULT FIRST UCB TO PRINT U012
*
DEVCLAS  DS    CL6                      CURRENT CLASS (ALL/DASD/TAPE)
*
NAMELIST DS    (65535/40)CL3            NAMES OF UCB'S DONE        U007
         DS    0D
DCMSWLEN EQU   *-DCMSWORK               FOR GETMAIN
         SPACE 4
OUTLINE  DSECT ,
OUTUNIT  DS    CL5
OUTTYPE  DS    CL7
OUTSTAT  DS    CL8
OUTVOL   DS    CL7
OUTSTATE DS    CL4
OUTSTAT2 DS    CL7
OUTQLEN  DS    CL7
OUTNOTE  DS    CL34                                                U002
         TITLE 'MVS UCB DEFINITION '
         IEFUCBOB  ,
         TITLE 'ASVT DEFINITION'
         IHAASVT  ,                                                U002
         TITLE 'ASCB DEFINITION'
         IHAASCB  ,                                                U002
         TITLE 'MVS CVT DEFINITION'
         CVT   DSECT=YES
         END
