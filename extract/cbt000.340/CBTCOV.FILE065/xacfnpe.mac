XACFNPE  CSECT ,
********************************************************************
*        THIS IS THE ACF2 NEW PASSWORD EXIT.
*        ITS PURPOSE IS TO REJECT NEW PASSWORDS WHICH ARE
*        COMPRISED OF A PART OF THE USER'S NAME.
*
*        CODED BY:      MARK ELCONIN
*        MODIFIED BY:   R.L. WELKER
*        LAST MODIFIED: 06/07/82
*
********************************************************************
*
*     REGISTER EQUATES AND USAGE
*
         ACFREGS LINK=R14,        DEFINE REGISTER EQUATES              X
               PREFIX=ACFLOUR
         GBLC  &PFX
*
*     DSECTS
*
WORKAREA DSECT ,
SAVEAREA DS    18F
SPACE    DS    CL21
WORKSIZE EQU   *-WORKAREA
         ACVALD ,                      PARAMETER LIST DSECT
         LIDREC ,                      LOGONID RECORD DSECT
*
*     HOUSEKEEPING
*
XACFNPE  CSECT ,
         ENTER GETMAIN=WORKSIZE,  SET UP LINKAGE                       X
               BASEREG=R12,                                            X
               PARMREG=R2
         LM    R9,R11,0(R2)            LOAD ADDRESSES OF PARMS
         USING WORKAREA,R13            SET ADDRESSABILITY TO WORKAREA
         USING ACVALD,R9               SET ADDRESSABILITY TO ACVALD
         USING LIDREC,R11              SET ADDRESSABILITY TO LIDREC
         XR    R15,R15                 SET RETURN CODE TO ZERO
*
*     FIRST REMOVE IMBEDDED BLANKS FROM THE NAME
*
         MVC   SPACE(20),LIDNAME       COPY THE NAME
         MVI   SPACE+20,C' '           PLANT A TRAILING BLANK
         LA    R2,SPACE                POINT TO FIRST BYTE
         LA    R3,19                   THIS MANY SPOTS TO CHECK
LOOP1    CLI   0(R2),C' '              HAVE A BLANK?
         BNE   SOLID                   NO
         LR    R4,R3                   COPY LENGTH
         BCTR  R4,0                    DECREMENT LENGTH FOR EX
         EX    R4,COMPARE              REMAINDER ALL BLANKS?
         BE    ENDCMPRS                YEP, WE'RE DONE
         EX    R4,SQUEEZE              ELIMINATE THE BLANK
         B     LOOP1                   KEEP GOING
SOLID    LA    R2,1(R2)                POINT TO NEXT CHAR
         BCT   R3,LOOP1                KEEP GOING IF NOT AT END
ENDCMPRS EQU   *
*
*     COMPUTE LENGTH OF PASSWORD
*
         LR    R2,R10                  POINT TO FIRST BYTE OF PASSWORD
         LA    R4,0                    COUNT OF BYTES
COUNT    CLI   0(R2),C' '              IS BYTE BLANK?
         BE    HAVELEN                 YEP, WE'RE THROUGH
         LA    R2,1(R2)                POINT TO NEXT CHAR
         LA    R4,1(R4)                INCREMENT COUNT
         CH    R4,=H'8'                AT MAX?
         BL    COUNT                   NO, KEEP GOING
HAVELEN  EQU   *
*
*     IS THE NEW PASSWORD CONTAINED IN THE USER'S NAME?
*
         LA    R2,SPACE                POINT TO FIRST BYTE OF NAME
         BCTR  R4,0                    DECREMENT LENGTH FOR EX
         LA    R3,20                   THIS MANY POSITIONS TO CHECK
         SR    R3,R4                   MINUS (LENGTH-1)
SCAN     EX    R4,COMPARE2             PASSWORD PART OF NAME?
         BE    OBVIOUS                 YEP, DON'T ALLOW IT
         LA    R2,1(R2)                POINT TO NEXT CHAR IN NAME
         BCT   R3,SCAN                 AND KEEP CHECKING
         B     RETURN                  NO MATCH FOUND, ALLOW PASSWORD
OBVIOUS  EQU   *
         L     R2,ACVMSG               POINT TO MESSAGE BUFFER
         MVC   0(MSGLEN,R2),MESSAGE    MOVE MESSAGE INTO MESSAGE AREA
         LA    R15,4                   SET RETURN CODE
*
*     TERMINATE
*
RETURN   EQU   *
         LEAVE GETMAIN=WORKSIZE,  RETURN TO CALLER                     X
               RETCODE=(15)
*
*     INSTRUCTIONS USED IN EXECUTE STATEMENTS
*
COMPARE  CLC   1(0,R2),=CL19' '        REMAINDER ALL BLANKS?
SQUEEZE  MVC   0(0,R2),1(R2)           MOVE WHAT'S LEFT 1 BYTE LEFT
COMPARE2 CLC   0(0,R2),0(R10)          PASSWORD PART OF NAME?
*
*     CONSTANTS
*
MESSAGE  DC    AL2(L'MSGTEXT+4)
         DC    X'0000'
MSGTEXT  DC    C'ACF01255 &PFX..XACFNPE NEW PASSWORD CANNOT BE CONTAINEX
               D IN YOUR NAME'
         DC    CL(124-L'MSGTEXT)' '
MSGLEN   EQU   *-MESSAGE
         LTORG
         END   XACFNPE
