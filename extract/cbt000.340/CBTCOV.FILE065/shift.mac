SHIFT    TITLE 'FUNCTION FOR SHIFTING BITS WITHIN A WORD'
*
*     THIS PROGRAM SHIFTS BITS WITHIN A WORD. IT CAN SHIFT EITHER TO
* THE RIGHT OR TO THE LEFT. THE FORMAT OF THE ARGUMENTS IS AS FOLLOWS
*          1. THE WORD UPON WHICH THE SHIFT OPERATION IS TO BE
*             PERFORMED
*          2. THE NUMBER OF BITS WHICH THE WORD IS TO BE SHIFTED
*             (FULL WORD BINARY), WHERE A NEGATIVE VALUE INDICATES A
*             SHIFT TO THE RIGHT AND A POSITIVE VALUE INDICATES A
*             SHIFT TO THE LEFT.
*     "SHIFT" IS REENTRANT AND REUSEABLE.
*
SHIFT    ENTER PARMREG=2,          INITIALIZATION                      X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING WORKAREA,R13        ADDRESS WORK AREA DSECT
         MVC   SHIFTR,DSHIFTR      MOVE SHIFT RIGHT SUBROUTINE TO WORK
         MVC   SHIFTL,DSHIFTL      MOVE SHIFT LEFT SUBROUTINE TO WORK
         LM    R4,R5,0(R2)         LOAD PARAMETER POINTERS
         L     R6,0(,R4)           LOAD CONTENTS OF WORD TO BE SHIFTED
         L     R5,0(,R5)           LOAD NUMBER OF BITS TO SHIFT
         LTR   R5,R5               TEST THE NUMBER OF BITS TO SHIFT
         BM    SRA                    NEGATIVE - SHIFT RIGHT
         BP    SLA                    POSITIVE - SHIFT LEFT
         BZ    EXIT                   ZERO     - NO SHIFT NECESSARY
SRA      DS    0H
         LPR   R5,R5               MAKE NUMBER OF BITS POSITIVE
         STC   R5,SHIFTR+3         PUT NUMBER OF BITS IN INSTRUCTION
         BAL   R14,SHIFTR          GO PERFORM THE SHIFT RIGHT
         L     R4,4(,R13)          POINT TO REGISTER SAVE AREA
         ST    R6,20(,R4)          STORE RESULT IN REGISTER 0 AREA
         LE    R0,20(,R4)          LOAD INTO FLOATING POINT REG 0
         B     EXIT                   AND LEAVE
SLA      DS    0H
         STC   R5,SHIFTL+3         PUT NUMBER OF BITS IN INSTRUCTION
         BAL   R14,SHIFTL          GO PERFORM THE SHIFT LEFT
         L     R4,4(,R13)          POINT TO REGISTER SAVE AREA
         ST    R6,20(,R4)          STORE RESULT IN REGISTER 0 AREA
         LE    R0,20(,R4)          LOAD INTO FLOATING POINT REG 0
         B     EXIT                   AND LEAVE
EXIT     DS    0H
         LEAVE RETCODE=0,          RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
         EJECT
DSHIFTR  DS    0H
         SRL   R6,0                SHIFT TO THE RIGHT
         BR    R14                 RETURN TO CALLER
DSHIFTRL EQU   *-DSHIFTR
DSHIFTL  DS    0H
         SLL   R6,0                SHIFT TO THE LEFT
         BR    R14                 RETURN TO CALLER
DSHIFTLL EQU   *-DSHIFTL
         EJECT
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
         DS    18A                 REGISTER SAVE AREA
SHIFTR   DS    CL(DSHIFTRL)        SHIFT RIGHT SUBROUTINE
SHIFTL   DS    CL(DSHIFTLL)        SHIFT LEFT SUBROUTINE
WORKSIZE EQU   *-WORKAREA
         END
