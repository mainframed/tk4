CONSPGM  CSECT
*/******************************************************************/
*/* THIS SILLY PROGRAM SIMPLY CHECKS THE CVT TO FIND OUT IF WE ARE */
*/* RUNNING XA OR MVS/SP. IF WE ARE RUNNING XA THEN THE XA VERSION */
*/* OF THE TSO OPERATOR CONSOLE PROGRAM WILL BE ATTACHED OTHERWISE */
*/* THE MVS/SP VERSION WILL BE LOADED.                             */
*/*                                                                */
*/*  MAKE THIS PROGRAM RE-ENTRANT AS WE CAN HAVE MULTIPLE USERS    */
*/*  CONCURRENTLY.                                                 */
*/*                                                                */
*/*                                                                */
*/******************************************************************/
         STM   14,12,12(13) SAVE REGS
         BALR  10,0
         USING *,10
         GETMAIN R,LV=MEMORY
         ST    13,4(1)         LETS MAKE OURSELVES RE-ENTRANT
         ST    1,8(13)         CHAIN SAVE AREAS
         LR    13,1                OUR SAVE AREA                    JMH
         LR    11,13
         USING CORE,11
         L     2,CVTPTR            CVT ADDRESS
         USING CVT,2               GET CVT ADDRESSABILITY
         TM    CVTDCB,CVTMVSE  IF THIS BIT = 1 THEN WEVE GOT XA
         BO    XAFOLKS
         MVC   MESS(38),=C'MVS/SP VERSION OF TSO OPERATOR CONSOLE'
         MVC   STUPID(8),=C'SPCONS  '
         B     DOIT
XAFOLKS  MVC   MESS(38),=C'MVS/XA VERSION OF TSO OPERATOR CONSOLE'
         MVC   STUPID(8),=C'XACONS  '
DOIT     EQU   *
         LA    1,MESS
         L     0,=F'38'
         TPUT  (1),(0)
         LA    7,STUPID
         LOAD EPLOC=(7)
         LR   15,0
         BALR 14,15
         LA   7,STUPID
         DELETE EPLOC=(7)
         LR    1,13              GET MAINED STORAGE
         L     13,4(13)          PREVIOUS SAVE AREA
         FREEMAIN R,LV=MEMORY,A=(1)
         LM    14,12,12(13)
         XR    15,15
         BR    14
         CVT  DSECT=YES
CORE     DSECT
         DS   18F        FOR OUR SAVE AREA
STUPID   DS    CL8
MESS     DS    CL38
MEMORY   EQU  *-CORE
         PRINT NOGEN
 END
