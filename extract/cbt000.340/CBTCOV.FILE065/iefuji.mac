**********************************************************************
**                                                                  **
** RELATED USERMOD: SFSMF01             LAST UPDATE: 11/09/82       **
**                                                                  **
**        THIS PROGRAM IS A SMF JOB VALIDATION ROUTINE WITH         **
**        ACF2 INTERFACE.    THIS PROGRAM IS INSTALLED IN THE       **
**        SYSTEM WITH SMP. (SFSMF01 IS USERMOD).                    **
**                                                                  **
**********************************************************************
AUJI     TITLE 'SAMPLE BATCH ACCOUNT AND JOBCLASS VALIDATION'   *102081
WORKAREA DSECT
SAVEAREA DS    9D                      SAVE AREA
         ACGRSRC DSECT=NO              RESOURCE PARM BLOCK
MESSAGE  DS    CL128                   MESSAGE AREA
WORKLEN  EQU   *-WORKAREA              WORK AREA LENGTH
         PRINT NOGEN
         IHAPSA
         IHAASCB
         ACFASVT
         PRINT GEN
         ACCVT
         ACUCB
         LIDREC
         ACFREGS
         USING PSA,R0
         EJECT
IEFUJI   CSECT    , REENTRANT
         SAVE  (14,12),,*              SAVE REGISTERS
         LR    R12,R15                 COPY ENTRY PT
         USING IEFUJI,R12              ADDRESSABILITY
         LR    R11,R1                  COPY PARM LIST ADDR
         GETMAIN RU,LV=WORKLEN         GET WORKING STORAGE
         ST    R13,4(,R1)              CHAIN SAVE AREAS
         ST    R1,8(,R13)
         LR    R13,R1                  NEW SAVE AREA
         USING WORKAREA,R13            GET ADDRESSABILITY
*
*        GET ADDRESSABILITY TO ACF2 RELATED CONTROL BLOCKS
*
         ACFGACVT R10,NONE=NOACF2      GET ACF2 CVT ADDRESS
         USING ACCVT,R10               ADDRESS IT
         ACFGUCB R8,NONE=NOACF2        GET USERS ACF UCB ADDR
*        ACFGUCB R8,NONE=NOACF2,INLINE=YES  USERS ACF UCB ADDR    -IK-
         USING ACUCB,R8                ADDRESS ACUCB
*            NOTE:  AFTER ACFGUCB ISSUED R15 HAS ADDR OF ACFASVT ENTRY
         LR    R9,R15                  GET ACF ASVT ENTRY ADDRESS
         USING ACFASVT,R9              ADDRESS IT
         ICM   R7,15,ACULRECP           GET USERS LIDREC ADDR
         BZ    NOACF2                  PUNT IF NONE
         USING LIDREC,R7               ADDRESS LIDREC
*
*        THE FOLLOWING CODE SEQUENCE TESTS FOR TSO AND EXITS IF SO.
*        MODIFY IF ALL JOBS (BATCH AND TSO) ARE TO BE TESTED.
*        NOTE:  TSO NORMALLY WOULD BE TESTED BY ACF2 LOGON PROCESS.
*
         CLI   ASVJID,C'T'             TSO SESSION?
         BE    ALLOW                   YES, ALREDY CHECKED
         CLI   ASVJID,C'J'             BATCH JOB?
         BNE   ALLOW                   NO, STC OR SUBSYS
*
*        THE FOLLOWING CODE SEQUENCE TESTS IF THE VLD-ACCT ATTRIBUTE
*        IS ACTIVE, AND IF NOT, SKIPS THE ACCOUNT VALIDATION.
*
CHKVLD   TM    LIDTFLG4,LIDT4ACC       TEST VLD-ACCT BIT
         BZ    CHKCLS     OFF, NO ACCT VALIDATION-CHECK JOBCLS  *102081
*
*        DROP ADDRESSABILITY TO ACF2 RELATED CONTROL BLOCKS
*
         DROP  R7,R8,R9,R10            DROP ADDRESSABILITY
*
*        SET UP RESOURCE VALIDATION CONTROL BLOCK
*
         XC    ACGRSRC(ACGRSLEN),ACGRSRC  ZERO RESOURCE PARM BLOCK
         MVI   ACGFCN,4                SET FUNCTION CODE
         MVI   ACGSFCN,ACGSINP         SET SUB FUNCTION - INTERPRET
         LA    R1,MESSAGE              GET MESSAGE AREA ADDR
         ST    R1,ACGMSG               STORE INTO PARM BLOCK
         MVC   ACGRTYPE,=CL4'RTAC'     RESOURCE TYPE
         MVI   ACGFLGS,ACGFNRES        DONT MAKE RULES RESIDENT
         MVC   MESSAGE(LINVALID),INVALID  SET INVALID ACCT MESSAGE
         L     R1,12(,R11)             GET ACCOUNT FIELD ADDR
         CLI   0(R1),0                 WAS IT SPECIFIED?
         BE    ERROR                   NO, ERROR
         SR    R15,R15                 CLEAR R15 FOR ICM
*
*        NOTE:  THE FOLLOWING CODE SEQUENCE OBTAINS THE FIRST
*              ACCOUNTING FIELD.  IF THIS IS NOT WHAT IS DESIRED,
*              INSERT CODE TO POINT AT ANOTHER FIELD.  SEE THE
*              IBM SPL: SMF FOR SPECIFICATIONS AS TO THE FORMAT
*              OF THIS PARAMETER LIST.
*
         ICM   R15,1,1(R1)             GET LENGTH OF FIELD
         BZ    ERROR                   ERROR, NULL SPEC
         LA    R0,L'ACGRNAME           MAX LENGTH
         CR    R15,R0                  WILL IT FIT?
         BH    ERROR                   NO, ERROR - TOO BIG
         MVI   ACGRNAME,C' '           PRE-BLANK FIELD
         MVC   ACGRNAME+1(L'ACGRNAME-1),ACGRNAME
         BCTR  R15,0                   GET 360 LENGTH OF ACCOUNT
         EX    R15,MVCACCT             MOVE ACCOUNT TO BLOCK
*
*        ISSUE THE ACF2 RESOURCE VALIDATION CALL
*
         ACFSVC ACGRSRC,NONE=NOACF2    CALL ACF2 TO VALIDATE ACCT
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   ERROR                   NO, ERROR - ACF2 RET MESSAGE
*                                                               *102081
*        VALIDATE USE OF JOB CLASS                              *102081
*                                                               *102081
CHKCLS   XC    ACGRSRC(ACGRSLEN),ACGRSRC  ZERO RSRC PARM BLOCK  *102081
         MVI   ACGFCN,4                SET FUNCTION CODE        *102081
         MVI   ACGSFCN,ACGSINP      SET SUBFUNCTION - INTERPRET *102081
         LA    R1,MESSAGE              GET MESSAGE AREA ADDRESS *102081
         ST    R1,ACGMSG               STORE IN PARM BLOCK      *102081
         MVC   ACGRTYPE,=CL4'RCLS'     RESOURCE TYPE            *102081
         MVI   ACGFLGS,ACGFNRES        DON'T MAKE RULES RESIDENT*102081
         MVC   MESSAGE(LINVCLS),INVCLASS  SET INVALID CLS MSG   *102081
         L     R1,0(R11)       GET SMF COMMON EXIT PARM AREA    *102081
         MVI   ACGRNAME,C' '           PRE BLANK THE FIELD      *102081
         MVC   ACGRNAME+1(L'ACGRNAME-1),ACGRNAME                *102081
         MVC   ACGRNAME(1),31(R1)      GET JOB CLASS            *102081
         ACFSVC ACGRSRC,NONE=NOACF2   CALL ACF2 TO VALIDATE CLS *102081
         LTR   R15,R15                 WAS IT GOOD?             *102081
         BNZ   ERROR                   NO, ERROR-ACF2 RET MSG   *102081
*
*        ALLOW  THE USE OF THE DESCRIBED ACCOUNTING FIELD
*
ALLOW    SR    R2,R2                   ZERO RETURN CODE
         B     RETURN                  AND ENTER RETURN SEQUENCE
         SPACE 2
*
*        CONVERT THE MESSAGE TO WTO FORMAT AND ISSUE WTO
*
ERROR    LH    R1,MESSAGE              GET MESSAGE LENGTH
         LA    R1,MESSAGE(R1)          NEXT AVAILABLE BYTE IN MSG
         MVC   0(4,R1),ROUTCDE         ADD ROUTING CODES
         MVI   MESSAGE+1,X'80'         SET MCS FLAG
         WTO   MF=(E,MESSAGE)          WRITE IT OUT
         LA    R2,4                    SET REUTRN CODE - CAN JOB
         B     RETURN                  AND RETURN
         SPACE 2
*
*        ACF2 IS NOT UP, ISSUE NON-ROLLABLE WARNING MESSAGE
*        TO THE OPERATOR AND ALLOW JOB TO CONTINUE
*
NOACF2   WTO   'INST001 WARNING - ACF2 NOT STARTED',                   X
               ROUTCDE=(1,9,11),DESC=2
         SR    R2,R2                   CLEAR RETURN CODE
*        B     RETURN                  AND RETURN
         SPACE 2
*
*        ENTER RETURN SEQUENCE TO RETURN CONTROL
*        TO CALLING ROUTINE
*
RETURN   LR    R1,R13                  COPY WORKAREA ADDR
         L     R13,4(,R13)             GET OLD SAVE AREA ADDR
         FREEMAIN RU,LV=WORKLEN,A=(1)  FREE STORAGE
         LR    R15,R2                  COPY RETURN CODE
         RETURN (14,12),RC=(15)        AND RETURN
         SPACE 2
*
*        CONSTANTS, EXECUTED INSTRUCTIONS, ETC.
*
INVALID  DC    AL2(LINVALID,0)         INVALID ERROR MSG HEADER
         DC    CL80'INST002 ACCOUNT SYNTAX INVALID OR NOT SPECIFIED'
LINVALID EQU   *-INVALID               MESSAGE LENGTH
         SPACE 1
INVCLASS DC    AL2(LINVCLS,0)         INVALID JOB CLASS MSG HDR *102081
         DC    CL80'INST003 JOBCLASS INVALID OR NOT ALLOWED'    *102081
LINVCLS  EQU   *-INVCLASS              MESSAGE LENGTH           *102081
         SPACE 1                                                *102081
MVCACCT  MVC   ACGRNAME(0),2(R1)       COPY ACCOUNT FIELD
ROUTCDE  DC    XL4'000080A0'           DESC=0,ROUT=(1,9,11)
         END
