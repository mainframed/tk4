FNDBYT   TITLE 'SUBROUTINE WHICH SEARCHES FOR A GIVEN CHARACTER'
*
*     THIS PROGRAM SEARCHES A CHARACTER STRING FOR THE FIRST OCCURRENCE
* OF A GIVEN CHARACTER AND RETURNS THE POSITION OF THAT CHARACTER (OR
* ZERO, IF THE CHARACTER IS NOT FOUND). UPON COMPLETION, REGISTER 15
* CONTAINS ZERO IF THE SUBROUTINE EXECUTED SUCCESSFULLY OR 12 IF IT
* EXECUTED UNSUCCESSFULLY. THE CALLING SEQUENCE FOR "FNDBYT" IS AS
* FOLLOWS
*           1. THE CHARACTER STRING TO BE SEARCHED
*           2. THE OFFSET WITHIN THAT STRING (4-BYTE BINARY)
*           3. THE CHARACTER TO BE SEARCHED FOR
*           4. THE LENGTH OF THE CHARACTER STRING TO BE SEARCHED
*                  (4-BYTE BINARY)
*           5. THE POSITION WITHIN THE CHARACTER STRING OF THE
*                  FIRST OCCURRENCE OF THE OBJECT OF THE SEARCH
*                  (4-BYTE BINARY)
*
*      THE "FNDBYT" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
FNDBYT   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(72,0)
         REGISTER                  REGISTER EQUATES
         LM    R4,R8,0(R2)         LOAD PARAMETER POINTERS
         L     R5,0(,R5)           LOAD OFFSET
         L     R7,0(,R7)           LOAD LENGTH
         LTR   R5,R5               IF THE OFFSET IS NEGATIVE
         BM    ERROR                  THEN FLAG FOR ERROR
         LTR   R7,R7               IF THE LENGTH IS NOT POSITIVE
         BNP   ERROR                  THEN FLAG FOR ERROR
         AR    R4,R5               ADD OFFSET TO STRING ADDRESS
         LA    R9,1                INITIALIZE POSITION OF OBJECT
LOOP     DS    0H
         CLC   0(1,R4),0(R6)       IF THIS IS THE ONE
         BE    FOUNDIT                THEN DONE LOOKING
         LA    R4,1(,R4)              ELSE INCREMENT STRING POINTER
         LA    R9,1(,R9)                             AND POSITION
         BCT   R7,LOOP                               AND KEEP LOOKING
         LA    R9,0                INDICATE CHARACTER NOT FOUND
FOUNDIT  DS    0H
         ST    R9,0(,R8)           SAVE POSITION OF CHARACTER
         LA    R15,0               INDICATE SUCCESSFUL EXECUTION
         B     EXIT                   AND LEAVE
ERROR    DS    0H
         LA    R15,12              INDICATE INVALID PARAMETERS
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(72,0)
         END
