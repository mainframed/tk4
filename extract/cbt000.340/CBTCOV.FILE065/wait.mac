WAIT     TITLE 'ROUTINE TO WAIT AMOUNT SPECIFIED IN PARM'
WAIT     CSECT ,
         ENTER BASEREG=R12,        SET UP LINKAGE                      X
               PARMREG=R2,                                             X
               GETMAIN=WORKLEN,                                        X
               REGEQUS=YES
         USING WORKAREA,R13       SET WORK AREA ADDRESSABILITY
         L     R2,0(,R2)          GET ADDRESS OF PARM
         XR    R3,R3              CLEAR R3
         ICM   R3,B'0011',0(R2)   GET LENGTH
         BNP   RETURN             IF NO PARM RETURN
         BCTR  R3,0               DECREMENT R3
         CH    R3,=H'15'          CHECK FOR VALID LENGTH
         BH    RETURN             NO, RETURN
         EX    R3,PACK            EXECUTE PACK
         CVB   R1,DOUBLE          CONVERT TO BINARY
         ST    R1,TIME            STORE TIME
         STIMER WAIT,BINTVL=TIME  WAIT
RETURN   EQU   *
         LEAVE RETCODE=0,         RETURN TO CALLER                     X
               GETMAIN=WORKLEN
PACK     PACK  DOUBLE,2(0,R2)     PACK INSTRUCTION
         LTORG ,
WORKAREA DSECT ,
         DS    18F                SAVE AREA
DOUBLE   DS    D                  DOUBLE WORD WORK AREA
TIME     DS    F                  FULL WORD TIME VALUE IN .01 SECONDS
WORKLEN  EQU   *-WORKAREA
         END   WAIT
