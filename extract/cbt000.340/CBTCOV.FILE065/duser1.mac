DUSER1   CSECT
         PRINT GEN
         SAVE  (14,12),,*_&SYSDATE_&SYSTIME
         LR    R12,R15
         USING DUSER1,R12
         LA    R0,GETMAINL             GET READY FOR GETMAIN'G SAV AREA
         GETMAIN R,LV=(R0)
         USING GETMAIND,R13            INITIALIZE GETMAIN AREA
         LR    R6,R13                  NORMAL
         LR    R13,R1                       SAVE
         ST    R6,SAVEAREA+4                        CHAINING
         ST    R13,8(0,R6)
         XC    ACALT(ACACFLMU-ACALT+2),ACALT
         MVC   ACAUCB,=X'FFFFFFFF'         7-20-82 SPWMP ACF 3.1 FIX
         MVI   ACAFCN,X'02'
         MVI   ACASFCN,ACASIRT         RETURN TERSE ID-UID-NAME
         LA    R6,MSG128
         ST    R6,ACAMSG                  AND MSG AREA
         LA    R6,ACASMAP
         ST    R6,ACARETB
         LA    R6,ACASLEN
         ST    R6,ACARETL
         SPACE 2
CVTASVT  EQU   556
ASVTMAXU EQU   516
ASVTFRST EQU   524
ASCBOUCB EQU   144
ASCBASID EQU   36
ASCBJBNS EQU   176
OUCBYFL  EQU   18
         LA    R8,WORK
         LA    R7,WORKLEN
         WTO   MF=(E,HEADER)           PRINT HEADINGS
         L     R10,16                  LOAD CVT ADDRESS
         L     R10,CVTASVT(,R10)       GET ASVT ADDR
         L     R9,ASVTMAXU(,R10)       LOAD MAX USERS
         LA    R10,ASVTFRST(,R10)      POINT TO FIRST
         USING ACCVT,5
         ACFINCVT 5,NONE=RETURN
         L     R6,ACCASVT
         USING ACFASVT,R6
LOOP     ICM   R8,15,0(R10)
         BNP   NEXT
         L     R11,ASCBOUCB(,R8)       GET OUCB
         TM    OUCBYFL(R11),X'20'      TSO USER ?
         BNO   NEXT
         ICM   R4,15,ASCBJBNS(R8)      LOAD USERID NAME POINTER
         BZ    NEXT
         MVC   ACALID,0(R4)
         MVC   WLID,0(R4)              7-20-82 SPWMP
         ACFSVC ACALT,TYPE=A,NONE=RETURN,CVT=HAVE
         LTR   R15,R15
         BNZ   SKIPIT
SKIPIT   MVC   WTIDNT,ASVSRC
         LA    R4,TABLEV               VTAM ID LOOK UP
         LA    R2,ENTRYLEV             TABLE LENGTH
         LA    R3,ENDTABLV             ADDRESS OF END OF TABLE
LOOKUPV EQU *
         CLC   WTIDNT,0(R4)            DOES THE IDS MATCH
         BE    FOUNDITV                YES THEY DO
         BXLE  R4,R2,LOOKUPV
NOTFND   EQU *
         MVC   WTRMINAL(26),=C' **TERMINAL NOT FOUND** '
         B     WRITEIT
FOUNDITV EQU *
         MVC   WTRMINAL,9(R4)
WRITEIT  WTO   MF=(E,WORKX)
NEXT     LA    R10,4(R10)              LOOP THRU TABLE
         LA    R6,32(R6)
         BCT   R9,LOOP
RETURN   DS    0H
         LR    R1,R13
         L     R13,SAVEAREA+4
         LA    R0,GETMAINL
         FREEMAIN R,LV=(0),A=(1)
         RETURN (14,12),,RC=0
         LTORG
         EJECT
*********************************************************************
********************************************************************
*                                                                  *
****   NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE *
****   NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE *
****                                                               *
****   THIS TABLE IS FOR THE VTAM TERMINAL IDS.                    *
****   KEEP THIS TABLE IN ASCENDING ORDER BY TERMINAL IDS!!!!!!!   *
*                                                                  *
********************************************************************
TABLEV   DS    0F
********************************************************************
*                VTAMLINE                       PHONE              *
*                XXXXXXXX CUA LOCATION          EXT.               *
********************************************************************
         DC    C'AAAAAAAA CUU XXXXXXXXXXXXXXXXX NNNN '
ENTRYLEV EQU   *-TABLEV
         DC    C'L100010  B40 COMPUTER ROOM     2521 '
         DC    C'L100011  B40 COMPUTER ROOM     2521 '
         DC    C'L100012  B40 COMPUTER ROOM     2521 '
         DC    C'L100013  B40 COMPUTER ROOM     2521 '
         DC    C'TERM322  322 COMPUTER ROOM     2521 '
         DC    C'TERM323  323 06/07             2646 '
         DC    C'TERM324  324 06/07             2642 '
         DC    C'TERM360  360 COMPUTER ROOM     2521 '
         DC    C'TERM361  360 COMPUTER ROOM     2521 '
         DC    C'TERM365  365 06/07             2645 '
         DC    C'TERM367  367 06/07                  '
         DC    C'TERM36A  36A 06/07             2640 '
         DC    C'TERM36E  36E 06/07                  '
         DC    C'TERM381  381 06/07             2683 '
         DC    C'TERM383  383 06/07             2906 '
ENDTABLV EQU   *
*
****     END OF TERMINAL ADDRESS AND LOCATION TABLE                   *
*********************************************************************
TABNAMEN EQU   *
         EJECT
HEADER   WTO   ' LOGONID  TERMINAL CUA LOCATION          EXTN',        +
               ROUTCDE=2,MF=L
HDRLEN   EQU   *-HEADER                LENGTH OF HEADER MESSAGE
WORKX    DC    Y(WORKEND-*)
         DC    B'0000000000000000'
WORK     DC    CL1' '
WLID     DC    CL8' '
         DC    CL1' '
WTIDNT   DC    CL8' '
         DC    C' '
WTRMINAL DC    CL26' '
WORKLEN  EQU   *-WORK
WORKEND  EQU   *
         DC  B'0000000000000000' DESC
         DC  B'0100000000000000' ROUT
         ACCVT
         ACFASVT
GETMAIND DSECT
SAVEAREA DS    18F
         ACALT   DSECT=NO
         ACASMAP DSECT=NO
MSG128   DC    CL128' '
LOGONID  DC    CL8' '
GETMAINL EQU   *-GETMAIND
R0       EQU   0    FOR GETMAIN AND FREEMAIN
R1       EQU   1    FOR GETMAIN AND FREEMAIN
R2       EQU   2    VTAM TABLE LENGTH
R3       EQU   3    VTAM TABLE LENGTH END
R4       EQU   4    LOAD USERID NAME
R5       EQU   5    USED
R6       EQU   6    REG FOR ACF
R7       EQU   7    FOR WTO
R8       EQU   8    FOR WTO
R9       EQU   9    MAX USERS
R10      EQU   10   CVT POINTER
R11      EQU   11   OCB ADDRESS
R12      EQU   12   BASE REGISTER
R13      EQU   13   RETURN REGISTER
R14      EQU   14   *********************  OPEN *******
R15      EQU   15   TEST COND CODE
         END   DUSER1
