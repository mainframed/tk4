K        TITLE 'CLEAR CRT SCREEN - TSO  "K" COMMAND'               SDM1
***********************************************************************
*                                                                     *
*  PROGRAM:  K                                                        *
*                                                                     *
*  AUTHOR:   LEONARD D. WOREN                                         *
*                                                                     *
*  PURPOSE:  CLEAR THE SCREEN ON ANY TYPE OF CRT TERMINAL             *
*                                                                     *
*  UPDATES:                                                           *
*       SDM4 07/22/80  IMPROVE VTAM SUPPORT BY REMOVING TPUT (HOLD)   *
*                      MAKE THIS REENTERABLE BY FORGETTING SAVE AREA  *
*       LDW2 06/23/80  REMOVE "MODID" MACRO                           *
*       SDM3 07/05/79  IMPROVE VTAM SUPPORT                           *
*       SDM2 06/27/79  FIX UP FOR TCAM WITH SPF SUPPORT               *
*       SDM1 06/25/79  ELIMINATE NON-STANDARD MACROS                  *
*       LDW1 06/21/79  ADD STLINENO                                   *
*                                                                     *
***********************************************************************
         SPACE 2
K        CSECT
         SAVE  (14,12),,K_&SYSDATE_&SYSTIME                        LDW2
         LR    R12,R15                                             SDM1
         USING K,R12                                               SDM1
         GTSIZE
         SPACE 1
         LTR   R0,R0                    NON-ZERO SCREEN LENGTH?    SDM3
         BZ    NOT3270                  NO - DO IT NORMALLY        SDM3
         SPACE 1                                                   SDM3
         STLINENO MODE=OFF,LINE=1       SET UP LINE POINTER.       SDM4
         LTR   R15,R15                  WUZZIT A VTAM TERMINAL?    SDM3
         BZ    RETURN                   YES, LEAVE NOW             SDM4
         SPACE 1
         TPUT  T3270,T3270L,FULLSCR     CLEAR IT WITH TCAM.        SDM4
         B     RETURN                                              SDM3
         SPACE 2                                                   SDM1
NOT3270  TPUT  KK,1,CONTROL             JUST DO A FORMFEED
         SPACE 1
RETURN   RETURN (14,12),RC=0                                       SDM1
         SPACE 3                                                   SDM3
         LTORG                                                     SDM3
         SPACE 3
V3270    DC    X'40'                    WCC=NOTHING SPECIAL        SDM3
T3270    DC    X'40'                    NOTHING SPECIAL            SDM2
         DC    X'115D7E114040'          LAST SPF FULLSCRN          SDM2
         DC    X'3C404000'              CLEAR ENTIRE SCREEN        SDM2
T3270L   EQU   *-T3270
KK       DC    X'0C'
         SPACE 2
R0       EQU   0                                                   SDM1
R1       EQU   1                                                   SDM1
R2       EQU   2                                                   SDM1
R3       EQU   3                                                   SDM1
R4       EQU   4                                                   SDM1
R5       EQU   5                                                   SDM1
R6       EQU   6                                                   SDM1
R7       EQU   7                                                   SDM1
R8       EQU   8                                                   SDM1
R9       EQU   9                                                   SDM1
R10      EQU   10                                                  SDM1
R11      EQU   11                                                  SDM1
R12      EQU   12                                                  SDM1
R13      EQU   13                                                  SDM1
R14      EQU   14                                                  SDM1
R15      EQU   15                                                  SDM1
         SPACE 3                                                   SDM1
         END   K
