MASK     TITLE 'FORTRAN FUNCTION WHICH RETURNS A BIT MASK'
*
*     THIS PROGRAM RETURNS A BIT MASK, LEFT-JUSTIFIED, WITH THE
* NUMBER OF BITS SPECIFIED BY THE CALLER HAVING BEEN TURNED ON.
* FOR EXAMPLE, THE FORTRAN STATEMENT
*                 X=MASK(16)
* WILL RESULT IN THE LEFTMOST 16 BITS OF X TURNED ON AND THE
* RIGHTMOST 16 BITS TURNED OFF.
*
*     "MASK" IS REENTRANT AND REUSEABLE.
*
MASK     ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(72,0)
         REGISTER                  REGISTER EQUATES
         L     R2,0(,R2)           LOAD ADDRESS OF BIT COUNT
         L     R2,0(,R2)           LOAD BIT COUNT
         XR    R0,R0               CLEAR RESULT REGISTER
         L     R1,=X'80000000'     INITIALIZE MASK REGISTER
         LTR   R2,R2               IF BIT COUNT IS NOT POSITIVE
         BNP   EXIT                   THEN LEAVE
LOOP     DS    0H
         OR    R0,R1               TURN ON A BIT
         SRL   R1,1                ADJUST MASK FOR NEXT BIT
         BCT   R2,LOOP             CONTINUE UNTIL DONE
EXIT     DS    0H
         L     R4,4(,R13)          POINT TO SAVED REGISTER AREA
         ST    R0,20(,R4)          CHANGE REGISTER 0 (RESULT REGISTER)
         LE    R0,20(,R4)          CHANGE F.P. REG 0 (RESULT REGISTER)
         LEAVE RETCODE=0,          RETURN TO CALLER                    X
               GETMAIN=(72,0)
