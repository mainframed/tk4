LSTB     TITLE '-----  READ TSO MESSAGES WITHOUT REMOVING THEM  -----'
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  LSTB                                               *
*                                                                     *
*   AUTHOR:        LEONARD D. WOREN                                   *
*                                                                     *
*   DATE:          MARCH 10, 1980                                     *
*                                                                     *
*   PURPOSE:       READ TSO USER MESSAGES (MAIL) FROM THE BRODCAST    *
*                  WITHOUT REMOVING THEM.                             *
*                                                                     *
*   ATTRIBUTES:    RE-ENTERABLE                                       *
*                                                                     *
*   UPDATES:                                                          *
*      05/18/84 LDW3  DISPLAY TOTALS FOR "USAGE"                      *
*      07/26/83 LDW2  DISPLAY 4 DIGITS FOR "USAGE"                    *
*                     MAKE PROGRAM RE-ENTERABLE                       *
*      02/19/82 LDW1  ADD "= USAGE" AND "= ALL" OPTIONS               *
*                     RENAME PROGRAM FROM "LOOK" TO "LSTB"            *
*                     REMOVE "TSFIND" MACRO, FIND PSCB THE LONG WAY   *
*                                                                     *
*   NOTE:          THE IKJZT30N DSECTS ARE FROM THE OPTIONAL          *
*                  MATERIAL.  THE AREAS ARE DESCRIBED IN THE FICHE    *
*                  UNDER "BCDIR", "BCMSG", "USDIR", "USMSG".          *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
LSTB     OSENTER  EXIT=LEAVE,RC=0,PARMREG=R2,LENREG=R4,ADDRREG=R5,     $
               ENV=(CP,SIMULATE),GETMAIN=(WORKLEN,WORKD),              $
               PL=DAIR
LINK     EQU   R11
         SPACE 2
         MVI   BLANKS,C' '              INIT FIELD                 LDW2
         MVC   BLANKS+1(L'BLANKS-1),BLANKS  ...                    LDW2
         MVC   WHO,BLANKS                                          LDW2
         SPACE 2
         ALLOC DSN=DSNL,DISP=(SHR,KEEP,KEEP),PERM=NO
         SPACE 2
         LTR   R15,R15
         BZ    ALLOC$OK
         TPUTX '*** SYS1.BRODCAST COULD NOT BE ALLOCATED ***'
         ABEND 88
         SPACE 2
ALLOC$OK MVC   SYSLBC(SYSLBCXL),SYSLBCX COPY DCB MODEL             LDW2
         MVC   SYSLBC+40(8),DA08DDN+DAPBAREA-DAPB08   GET DDNAME
         LA    R1,SYSLBC                -> DCB                     LDW2
         ST    R1,OPENMFL               STORE FOR OPEN MF=L        LDW2
         MVI   OPENMFL,X'80'            SET END OF LIST INDICATOR  LDW2
         SPACE 2
         OPEN  MF=(E,OPENMFL)           OPEN SYSLBC                LDW2
         SPACE 2
         TM    SYSLBC+48,X'10'
         BO    OPEN$OK
         ABEND 99
         SPACE 2
OPEN$OK  LTR   R4,R4                    ANY OPERAND?
         BNP   DFLTUSER                 NO - USE SELF
         CH    R4,=H'7'                 TOO BIG?
         BNH   OK$USER                  NO - USE IT
         TPUTX 'INVALID USERID'
         BAL   R11,PROMPT
         B     NEWUSER
         SPACE 2
OK$USER  BCTR  R4,0                     -1 FOR EX
         OC    WHO(*-*),0(R5)           << EXECUTED >>
         EX    R4,*-6                   COPY USERID FROM PARM
         B     NEWUSER
         SPACE 2
*FLTUSER TSFIND  PSCB,R1
DFLTUSER L     R1,16                    -> CVT                     LDW1
         L     R1,0(,R1)                -> DISPATCH                LDW1
         L     R1,4(,R1)                -> TCB                     LDW1
         L     R1,180(,R1)              -> JSCB                    LDW1
         L     R1,264(,R1)              -> PSCB                    LDW1
         MVC   WHO,0(R1)                USE PSCB USERID
         SPACE 3
NEWUSER  XC    DECB(DECBL),DECB         INIT DECB
         XC    RBA,RBA                  SET WHICH BLOCK TO READ
         BAL   LINK,READ                READ THE CONTROL BLOCK
         MVC   DIRRBA,R1USPTR           START AT TOP OF MAIL DIRECTORY
         LA    R2,TERMBUFF              -> START OF BUFFER
         MVC   TERMBUFF,BLANKS          CLEAR IT
         LA    R0,TERMBUFF+L'TERMBUFF-7 -> LAST USABLE BUFF SLOT   LDW1
         ST    R0,MAXBUFF               SAVE FOR LOOP CHECK        LDW1
         CLC   =C'= LIST',WHO           SPECIAL REQUEST?
         BE    LIST$TTL                 YES - PRINT TITLE
         CLC   =C'= USAGE',WHO          SPECIAL REQUEST?           LDW1
         BE    USAG$TTL                 YES - PRINT TITLE          LDW1
         CLC   =C'= ALL',WHO            SPECIAL REQUEST?           LDW1
         BE    DIRSCAN1                 YES - START                LDW1
         CLC   =C'= INDEX',WHO          SPECIAL REQUEST?
         BNE   DIRSCAN1                 NO - START
         TPUTX '=== TSO USERS WITH MAIL ==='
         B     DIRSCAN1
         SPACE 2
USAG$TTL TPUTX '=== NUMBER OF MAIL MESSAGES BY TSO USER ==='       LDW1
         SPACE 1
         LA    R0,TERMBUFF+L'TERMBUFF-13 -> LAST USABLE BUFF SLOT  LDW1
         ST    R0,MAXBUFF               SAVE FOR LOOP CHECK        LDW1
         SR    R6,R6                    CLEAR GRAND TOTAL          LDW3
         B     DIRSCAN1
         SPACE 2
LIST$TTL TPUTX '=== TSO USERS LISTED IN BRODCAST DATASET ==='
         SPACE 1
DIRSCAN1 CLC   DIRRBA,=F'0'             ANY MORE DIRECTORY BLOCKS?
         BE    NOUSER                   NO - NOT FOUND
         MVC   RBA,DIRRBA               GET RBA TO READ
         BAL   LINK,READ                READ THE DIRECTORY BLOCK
         MVC   RECORD4,RECORD           MOVE TO CORRECT PLACE      LDW1
* NOW LOOP THROUGH DIRECTORY LOOKING FOR USER
         LA    R3,9                     NUMBER OF ENTRIES
         LA    R4,RECORD4               -> FIRST                   LDW1
LIST01   CLC   =C'= LIST',WHO           SPECIAL REQUEST?
         BE    LIST02                   YES - PROCESS
         CLC   =C'= USAGE',WHO          SPECIAL REQUEST?           LDW1
         BE    USAGE                    YES - PROCESS              LDW1
         CLC   =C'= ALL',WHO            SPECIAL REQUEST?           LDW1
         BE    ALL                      YES - PROCESS              LDW1
         CLC   =C'= INDEX',WHO          SPECIAL REQUEST?
         BNE   DIRSCAN2                 NO - DO NORMAL SEARCH
         CLC   7(3,R4),=F'0'            RBA FIRST MSG PRESENT?
         BE    NEXTUSER                 NO - SKIP
         SPACE 1
LIST02   MVC   0(7,R2),0(R4)            MOVE A USERID INTO BUFFER
         LA    R2,9(,R2)                -> BUFFER POSITION
         SPACE 2
LIST03   C     R2,MAXBUFF               ANY MORE ROOM IN TERMBUFF? LDW1
         BNH   NEXTUSER                 YES - CONTINUE
         TPUTX TERMBUFF,L'TERMBUFF      PRINT IT
         MVC   TERMBUFF,BLANKS          CLEAR IT
         LA    R2,TERMBUFF              RESET POINTER
         SPACE 1
NEXTUSER LA    R4,13(,R4)               -> NEXT DIRECTORY INDEX SLOT
         BCT   R3,LIST01                LOOP THRU WHOLE DIRECTORY BLOCK
         MVC   DIRRBA,USDNEXT           GET RBA OF NEXT DIRECTORY BLOCK
         B     DIRSCAN1                 GO SCAN IT
         SPACE 3
USAGE    CLC   7(3,R4),=F'0'            RBA FIRST MSG PRESENT?     LDW1
         BE    NEXTUSER                 NO - SKIP                  LDW1
         MVC   6(7,R2),0(R4)            MOVE USERID INTO BUFFER    LDW2
         SR    R5,R5                    CLEAR RECORD COUNTER       LDW1
         SPACE 1
USAGE01  MVC   RBA,7(R4)                SET RBA TO READ            LDW1
         SPACE 1
USAGE02  LA    R5,1(,R5)                COUNT THIS MAIL RECORD     LDW1
         LA    R6,1(,R6)                INCREMENT GRAND TOTAL      LDW3
         BAL   LINK,READ                READ NEXT MAIL RECORD      LDW1
         MVC   RBA,USMNEXT              GET RBA OF NEXT MAIL BLOCK LDW1
         CLC   RBA,=F'0'                ANY MORE?                  LDW1
         BNE   USAGE02                  NO - KEEP READING          LDW1
         SPACE 1
*---  FORMAT MAIL RECORD COUNT
         CVD   R5,DWD                                              LDW1
         MVC   EDITWORK(6),=X'402020202020'                        LDW2
         ED    EDITWORK(6),DWD+5                                   LDW2
         MVC   0(5,R2),EDITWORK+2       MOVE NUMBER OF MAIL RECS   LDW2
         LA    R2,15(,R2)               -> NEXT BUFFER SLOT        LDW2
         B     LIST03                   SEE IF BUFFER FULL         LDW1
         SPACE 3
ALL      CLC   7(3,R4),=F'0'            RBA FIRST MSG PRESENT?     LDW1
         BE    NEXTUSER                 NO - SKIP                  LDW1
         MVC   TERMBUFF(79),=C'*-*-*-*-*-*-*-*-*-*-*-*-*-*-*  MAIL FOR $
               XXXXXXX *-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*'            LDW1
         MVC   TERMBUFF+40(7),0(R4)     MOVE USERID TO MESSAGE     LDW1
         TPUTX TERMBUFF,79              PRINT HEADER               LDW1
         MVC   RBA,7(R4)                SET RBA TO READ            LDW1
         SPACE 1
ALL01    BAL   LINK,READ                READ NEXT MAIL RECORD      LDW1
         SR    R0,R0                    CLEAR FOR IC               LDW1
         IC    R0,USMLNG                GET LENGTH OF TEXT         LDW1
         TPUTX USMTEXT,(0)              PRINT THE MSG              LDW1
         MVC   RBA,USMNEXT              GET RBA OF NEXT MAIL BLOCK LDW1
         CLC   RBA,=F'0'                ANY MORE?                  LDW1
         BNE   ALL01                    NO - KEEP READING          LDW1
         TPUTX ' '                      PUT OUT A BLANK LINE       LDW1
         B     NEXTUSER                 DONE WITH THIS USER        LDW1
         SPACE 3
DIRSCAN2 CLC   WHO,0(R4)                THIS HIM?
         BE    FOUNDHIM                 YES
         LA    R4,13(,R4)               -> NEXT ENTRY
         BCT   R3,DIRSCAN2              SCAN THIS WHOLE DIRECTORY BLOCK
* NOT IN THAT DIRECTORY BLOCK... TRY NEXT
         MVC   DIRRBA,USDNEXT           GET RBA OF NEXT DIRECTORY BLOCK
         B     DIRSCAN1                 GO SCAN IT
         SPACE 3
NOUSER   CLC   =C'= ',WHO               SPECIAL REQUEST?
         BNE   NOUSER2                  NO - GIVE MSG
         CLC   =C'= ALL',WHO            LISTING ALL MAIL?          LDW1
         BE    QUIT                     YES - NO PARTIAL BUFFER    LDW1
         CLC   TERMBUFF,BLANKS          ANYTHING HERE?
         BE    QUIT                     NO - JUST LEAVE QUIETLY
         TPUTX TERMBUFF,L'TERMBUFF      PRINT THE PARTIAL LINE
*---  FORMAT GRAND TOTAL                                           LDW3
         MVC   TERMBUFF(GRANDLEN),GRANDLIN  COPY MODEL             LDW3
         CVD   R6,DWD                                              LDW3
         ED    GRANDNUM-GRANDLIN+TERMBUFF(6),DWD+5                 LDW3
         TPUTX TERMBUFF,GRANDLEN        PRINT THE GRAND TOTAL      LDW3
         B     QUIT                     LEAVE
         SPACE 2
NOUSER2  MVC   TERMBUFF,BLANKS
         MVC   TERMBUFF(8),=C'*** USER' START THE MSG
         MVC   TERMBUFF+9(7),WHO        MOVE IN USERID
         LA    R1,TERMBUFF+8            -> START
         LA    R1,1(,R1)                -> NEXT CHAR
         CLI   0(R1),C' '               FIND END?
         BNE   *-8                      NO - KEEP LOOKING
         MVC   1(14,R1),=C'NOT LISTED ***'  FINISH MSG
         TPUTX TERMBUFF,31
         B     QUIT
         SPACE 3
* FOUND USER. NOW FOLLOW DOWN MESSAGE CHAIN
FOUNDHIM MVC   RBA,7(R4)                GET FIRST RBA
         CLC   RBA,=F'0'                ANYTHING HERE?
         BNE   HASMAIL                  YES - SHOW IT
         MVC   TERMBUFF(24),=C'*** NO TSO MAIL FOR USER'
         MVC   TERMBUFF+25(7),WHO       MOVE IN USERID
         TPUTX TERMBUFF,32
         B     QUIT
         SPACE 3
HASMAIL  BAL   LINK,READ                READ A MAIL BLOCK
         SR    R0,R0                    CLEAR FOR IC
         IC    R0,USMLNG                GET LENGTH OF TEXT
         TPUTX USMTEXT,(0)              PRINT THE MSG
         MVC   RBA,USMNEXT              GET RBA OF NEXT MAIL BLOCK
         CLC   RBA,=F'0'                ANY?
         BNE   HASMAIL                  YES - CONTINUE
         TPUTX '*** NO MORE TSO MAIL ***'
         SPACE 3
QUIT     CLOSE MF=(E,OPENMFL)           CLOSE SYSLBC               LDW2
         SPACE 1
         B     LEAVE
         SPACE 3
PROMPT   TPUTX 'ENTER USERID OR "END":',ASIS
         SPACE 3
         LA    R1,REPLY                 STUPID TGET MACRO...       LDW2
         SPACE 1
         TGET  (1),80                                              LDW2
         SPACE 1
         MVC   WHO,REPLY                ASSUME IT'S GOOD
         CLC   REPLY+7(80-7),BLANKS     TOO LONG?
         BER   R11                      NO - RETURN TO CALLER
         TPUTX 'INVALID USERID'
         B     PROMPT
         SPACE 3
READ     READ  DECB,DI,SYSLBC,RECORD,'S',KEY,RBA,MF=E
         SPACE 1
         CHECK DECB
         SPACE 1
         BR    LINK                     RETURN TO CALLER
         EJECT
DSNL     DC    H'13',C'SYS1.BRODCAST'   NAME OF BRODCAST DATASET
GRANDLIN DC    C'====== TOTAL NUMBER OF MAIL RECORDS ='            LDW3
GRANDNUM DC    X'402020202120'                                     LDW3
         DC    C' ======'                                          LDW3
GRANDLEN EQU   *-GRANDLIN                                          LDW3
         LTORG ,
         SPACE 3
WORKD    DSECT                          RESUME
DWD      DS    D                        CVD WORK AREA
EDITWORK DS    CL8                      ED WORK AREA               LDW2
TERMBUFF DS    CL80                     TPUT OUTPUT BUFFER
REPLY    DS    CL80                     TGET INPUT BUFFER
OPENMFL  DS    F                        OPEN MF=L PARMLIST         LDW2
BLANKS   DC    CL131' '                 CONSTANT
WHO      DS    CL7                      WHO ARE WE SPYING ON?
RBA      DS    FL3                      BLOCK # TO BE READ
DIRRBA   DS    FL3
MAXBUFF  DS    A                        -> LAST USABLE POS IN BUF  LDW1
         DS    0F,CL3                   "RECORD" MUST BE ON FULLWORD
KEY      DS    C                        THE KEY GOES HERE (WHOOPIE!)
RECORD   DS    CL129                    BUFFER USED TO READ BRODCAST
         SPACE 1
         DS    0F                       "RECORD" MUST BE ON FWD    LDW1
RECORD4  DS    CL129                    SAVEAREA FOR MAIL DIR REC  LDW1
         SPACE 2
         READ  DECB,DI,*-*,RECORD,,KEY,RBA,MF=L
DECBL    EQU   *-DECB
         SPACE 2
SYSLBC   DCB   DSORG=DA,MACRF=RIC,DDNAME=*-*,OPTCD=R,                  $
               RECFM=F,KEYLEN=1,BLKSIZE=129
         SPACE 2
WORKLEN  EQU   *-WORKD                  LENGTH TO GETMAIN
         SPACE 3
         EJECT
LSTB     CSECT                          RESUME
         SPACE 1
         PRINT NOGEN                                               LDW2
SYSLBCX  DCB   DSORG=DA,MACRF=RIC,DDNAME=*-*,OPTCD=R,                  $
               RECFM=F,KEYLEN=1,BLKSIZE=129
SYSLBCXL EQU   *-SYSLBCX                LENGTH OF MODEL DCB        LDW2
         EJECT
WORKD    DSECT ,                        RESUME
         SPACE 2
         ORG   RECORD
         SPACE 2
         IKJZT301  DSECT=NO             MAP OF RECORD 1 OF BRODCAST
         EJECT
         ORG   RECORD4                                             LDW1
         SPACE 2
         IKJZT304  DSECT=NO             MAP OF MAIL DIRECTORY RECORD
         EJECT
         ORG   RECORD
         SPACE 2
         IKJZT305  DSECT=NO             MAP OF MAIL MESSAGE RECORD
         SPACE 2
         END
