U2J2     TITLE 'XACFU2J2 - ACF2 JES2 EXIT 2 MODULE FOR LINKLIB' IV02300
*************************************************************** IV02300
*                                                               IV02300
* LOAD MODULE NAME = XACFU2J2  ATTRIBUTE = RENT  LIBRARY = LINK IV02300
*                                                               IV02300
* THIS LOAD MODULE PROVIDES JES2 EXIT CODE TO SUPPORT THE       IV02300
* FOLLOWING ACF2 FUNCTIONS:                                     IV02300
*                                                               IV02300
* CSECT XACFXXEQ - JOB POST-VALIDATION EXIT (JES2 EXIT 6)       IV02300
*                                                               IV02300
*     REGS - R0  -      - 0 = INTERNAL TEXT EXIT                IV02300
*                         4 = WHOLE JOB CONVERTED               IV02300
*            R1  -      - CONTAINS PARM LIST ADDRESS.           IV02300
*            R2/R7      - N A                                   IV02300
*            R8         - ADDRESS OF THE PCE.                   IV02300
*            R9         - N A                                   IV02300
*            R10 -      - JCT ADDRESS.                          IV02300
*            R11 -      - HCT ADDRESS.                          IV02300
*            R12 -      - N A                                   IV02300
*            R13 -      - OS SAVE AREA POINTER.                 IV02300
*            R14 -      - RETURN ADDRESS.                       IV02300
*            R15 -      - ENTRY ADDRESS.                        IV02300
*                         RETURN CODE ON EXIT -                 IV02300
*                         0= CONTINUE PROCESSING NORMALLY.      IV02300
*                         8= ABORT FURTHER JOB PROCESSING.      IV02300
*                                                               IV02300
*                                                               IV02300
*              M A I N T E N A N C E   L O G                    IV02300
*              ---------------------   -----                    IV02300
*                                                               IV02300
*        CODED BY          : R.L. WELKER                        IV02300
*        DATE CREATED      : 06/17/82                           IV02300
*        MODIFIED BY       : R.L. WELKER                        IV02300
*        DATE LAST MODIFIED: 06/17/82                           IV02300
*                                                               IV02300
*************************************************************** IV02300
         TITLE ' LOCAL MACRO DEFINITIONS'                       IV02300
         MACRO                                                  IV02300
         $JESCB  ,                                              IV02300
         PUSH  PRINT                                            IV02300
         PRINT OFF                                              IV02300
         $PSA  LIST=NO                                          IV02300
         $CVT  LIST=NO                                          IV02300
         $JQE  DOC=NO                                           IV02300
         $TEXT DOC=NO                                           IV02300
         $KEYS DOC=NO                                           IV02300
         $PCE  DOC=NO                                           IV02300
         $BUFFER DOC=NO                                         IV02300
         $JCT  DOC=NO                                           IV02300
         $CAT  DOC=NO                                           IV02300
         $HCT  DOC=NO                                           IV02300
         $DCT  DOC=NO                                           IV02300
         $TAB   DOC=NO                                          IV02300
         $CMB  DOC=NO                                           IV02300
         $NHD  DOC=NO                                           IV02300
         $RAT  DOC=NO                                           IV02300
         $SVT   DOC=NO                                          IV02300
         $LRC   DOC=NO                                          IV02300
         PRINT ON                                               IV02300
         $RDRWORK DOC=YES                                       IV02300
         PRINT OFF                                              IV02300
         $MITETBL DOC=NO                                        IV02300
         $MIT  DOC=NO                                           IV02300
         $XIT  DOC=YES                                          IV02300
         $EXITPL DOC=YES                                        IV02300
         PRINT ON                                               IV02300
         TITLE ' ACCVT - ACF2 CVT MAP'                          IV02300
         ACCVT  ,                                               IV02300
         TITLE ' ACUCB - ACF2 USER CONTROL BLOCK MAP'           IV02300
         ACUCB   ,                                              IV02300
         TITLE ' #ACFDCTX - INTRDR EXTENSION BLOCK MAP'         IV02300
         #ACFDCTX  ,                                            IV02300
         TITLE ' #ACFJCTX - JCT EXTENSION BLOCK MAP'            IV02300
         #ACFJCTX  ,                                            IV02300
         POP   PRINT                                            IV02300
         MEND                                                   IV02300
         SPACE 2                                                IV02300
         GBLC  &SKK                                             IV02300
         GBLC  &ACF2XIT,&ACF2DCT,&ACF2JCT,&OFFSET               IV02300
         SPACE 5                                                IV02300
XACFU2J2 START 0                   ACF2 LINKLIB EXIT MODULE     IV02300
         PUSH PRINT                                             IV02300
         PRINT OFF                                              IV02300
         COPY  $HASPGEN                                         IV02300
         POP PRINT                                              IV02300
         TITLE ' ACF2 JES2 MODULE INFORMATION TABLE'            IV02300
XACFU2J2 $MODULE NOTICE=NO                                      IV02300
         $JESCB   ,                                             IV02300
         TITLE ' #ACFJES2 - JES2 SPECIFICATIONS '               IV02300
         #ACFJES2  ,                                            IV02300
         TITLE 'XACFU2J2 - CSECT XACFXXEQ - JOB CHECK EXIT'     IV02300
************************************************************    IV02300
*                                                               IV02300
*     XACFXXEQ -- ACF2 POST-VALIDATION FOR STARTED TASK         IV02300
*                 JOBS.  RUNS UNDER CONVERTER SUBTASK           IV02300
*                                                               IV02300
************************************************************    IV02300
XACFXXEQ $ENTRY CSECT=YES                                       IV02300
         @ID   C=NO                                             IV02300
         SAVE  (14,12)             SAVE REGISTER CONTENTS       IV02300
         LR    BASE3,R15           COPY TO PROPER BASE          IV02300
         XR    R15,R15             SET RETURN CODE              IV02300
         USING JCTDSECT,R10        JCT ADDRESSING               IV02300
         USING HCTDSECT,R11        HCT ADDRESSING               IV02300
         ICM   R9,15,&ACF2JCT      TEST, GET ACF2 AREA OFFSET   IV02300
         BZ    #ACFXXIT            NOT PRESENT, SKIP ALL CODE   IV02300
         LA    R9,0(R10,R9)        CALCULATE ADDRESS            IV02300
         USING #ACFJCTX,R9         ADDRESSING                   IV02300
         CLC   JCTACFJ,=CL4'ACFJ'  IS IT AN ACF JCT             IV02300
         BNE   #ACFXXIT            NO, SKIP ALL PROCESSING      IV02300
         TM    JCTAC2F,JCTAC2FP    WAS THIS JCT PROCESSED?      IV02300
         BNO   #ACFXXIT            YES, EXIT QUICKLY            IV02300
         TM    JCTJOBFL,JCTSTCJB   STC JOB??                    IV02300
         BZ    #ACFXXIT            YES, SKIP CKS OR READ HERE   IV02300
*************************************************************** IV02300
         LTR   R0,R0              Q. JCL INTERNAL TEXT HERE?    IV02300
         BNZ   NOTEXT             A. NO, SKIP MSGCLASS          IV02300
         L     R2,4(,R1)          GET ADDRESS OF INTERNAL TEXT  IV02300
         USING TXTDSECT,R2        INTERNAL TEXT ADDRESSABILITY  IV02300
         XR    R3,R3              CLEAR R1                      IV02300
         ICM   R3,B'0011',STRLTH  Q. TEXT HERE                  IV02300
         BZ    NOTEXT             A. NO, SKIP MSGCLASS          IV02300
         CLI   STRINDCS,JOBSTR    Q. JOB STATEMENT?             IV02300
         BNE   NOTEXT             A. NO, SKIP MSGCLASS          IV02300
         LA    R3,0(R3,R2)        GET END OF TEXT               IV00230
         S     R3,=F'4'           PREVENT S0C4                  IV00230
         LA    R4,STRJKEY         GET ADDRESS OF KEY            IV02300
         USING STRJKEY,R4         SET KEY ADDRESSABILITY        IV02300
         LA    R2,1               GET INCREMENT                 IV00230
MSGCLOOP EQU   *                                                IV00230
         CLC   =X'B20101',STRJKEY Q. MSGCLASS FOUND             IV00230
         BE    MSGCLASS           A. YES, MOVE IN MSGCLASS      IV00230
         BXLE  R4,R2,MSGCLOOP     LOOP TILL FOUND               IV00230
         B     NOTEXT             SKIP MSGCLASS                 IV00230
MSGCLASS EQU   *                                                IV02300
         MVC   STRJKEY+3,LIDTSUBM  MOVE MSGCLASS                IV02300
NOTEXT   EQU   *                                                IV02300
         MVC   JCTPNAME,LIDNAME    MOVE IN NAME FIELD           IV02300
         MVC   JCTMCLAS,LIDTSUBM   MOVE MSGCLASS                IV02300
         MVC   JCTROOMN,LIDBAY     MOVE BAY NUMBER              IV02300
         MVC   JCTWORK(L'LIDACCT),LIDACCT MOVE ACCT NUMBER      IV02300
         LA    R15,4              SET NEVER RETURN CODE         IV02300
*************************************************************** IV02300
#ACFXXIT DS    0H                                               IV02300
         RETURN (14,12),RC=(15)    RETURN TO SSSM               IV02300
         LTORG                                                  IV02300
         DROP  R9,R10                                           IV02300
         TITLE ' MODULE SUMMARY'                                IV02300
         $MODEND ,                                              IV02300
XACFU2J2 CSECT  ,                  BACK TO ROOT CSECT           IV02300
         @ID    ,                  ID FOR BASE LOADMOD          IV02300
         END   XACFU2J2                                         IV02300
