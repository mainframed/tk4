R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5    ............ PARM POINTER AND DATE STORE
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12   ............ BASE
R13      EQU   13
R14      EQU   14
R15      EQU   15
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*   THE PURPOSE OF THIS PROGRAM IS TO FORMAT A TAPE FOR PRODUCTION    *
* OF MICROFICHE BY THE COM MACHINES.  IT READS TWO FILES --           *
*   1.  THE TITLE FILE                                                *
*   2.  THE DATA FILE                                                 *
* IT PRODUCES ONE FILE --                                             *
*   1.  THE COM OUTPUT FILE                                           *
*                                                                     *
*   ONLY ONE RECORD OF THE TITLE FILE IS READ.  THE FIRST 65 BYTES    *
* OF THIS 80 BYTE RECORD ARE USED FOR THE TITLE OF THE MICROFICHE.    *
* THE MICROFICHE TITLE IS PLACED ON THE TOP OF EACH MICROFICHE        *
* PRODUCED.  IT ALSO APPEARS AT THE TOP OF EACH FRAME IN THE FICHE.   *
* IF A PARM OF 'D' IS PROVIDED FOR THE EXECUTION OF THIS PROGRAM,     *
* THE LAST 8 BYTES OF THE 65 WHICH ARE USED FOR THE TITLE ARE         *
* OVERLAID WITH THE EXECUTION DATE.                                   *
*                                                                     *
*  ALL RECORDS OF THE DATA FILE ARE READ.  EVERY TIME A CARRAIGE      *
* CONTROL CHARACTER OF '1' IS DETECTED, THE TITLE IS PLACED ON        *
* THE TAPE JUST PRIOR TO WRITING THAT RECORD ONTO THE TAPE.           *
* THE TITLE HAS A CONTROL CHARACTER OF '1', AND THE LINE WHICH IS     *
* WRITTEN AFTER THE TITLE HAS IT'S CONTROL CHARACTER OVERLAID WITH    *
* A '0', WHICH MEANS 'SKIP A LINE'.  THE DATA INPUT FILE CAN HAVE     *
* ANY LRECL BETWEEN 2 AND 133.  IT MUST BE FIXED BLOCK.  IT SHOULD    *
* USE ASA CARRAIGE CONTROL CHARACTERS FOR FORMS CONTROL.  IT IS BEST  *
* NOT TO PRINT OVER 63 LINES PER PAGE.                                *
*                                                                     *
* THE OUTPUT FILE IS FB WITH LRECL OF 133.  IT'S FIRST RECORD WILL    *
* ALWAYS BY THE TITLE (SO THE COM MACHINE WILL HAVE A TITLE FOR THE   *
* FIRST FICHE).                                                       *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
FICHE1   CSECT                           EVERYONE LOVES HIS OWN CODE
         USING *,R12                     BASE (FIRST BASE)
         STM   R14,R12,12(R13)
         LR    R12,R15                   BASE FIDDLE
         ST    R13,SAVEAREA+4
         LA    R5,SAVEAREA               SORRY - NO SECOND BASE
         ST    R5,8(0,R13)
         LR    R13,R5
         B     STARTUP
         SPACE
SAVEAREA DC    18F'0'
         SPACE 3
STARTUP  DS    0H                      MAINLINE STARTS HERE
         SPACE 3
         L     R5,0(0,R1)              ADDRESS OF THE PARM FIELD
         CLC   0(2,R5),=H'0'           LOOK AT THE LENGTH
         BE    DEFAULT                 THERE IS NO PARM FIELD
         CLC   2(1,R5),=C'D'           Q. IS PARM = 'D'
         BNE   DEFAULT                 NO. TAKE THE DEFAULT.
         SPACE 3
         TIME
         ST    R1,JULIANDT             SAVE IT IN MEMORY
         ZAP   GREGDECI,JULIANDT+1(3)  PACKED DATE TO DECIMAL
         DP    GREGDECI,GREG1000       FORM YEAR & MONTH FIELDS
         OI    GREGDECI+7,15           FORCE DAYS TO BE POSITIVE
         OI    GREGDECI+4,15           FORCE YEARS TO BE POSITIVE
         UNPK  GREGYEAR,GREGDECI+3(2)  FORM YEARS IN CHAR FORMAT
         DP    GREGDECI(5),GREGPAK4    TEST FOR LEAP YEAR
         MVI   GREGDECI+5,X'0C'        RESET LEAP YEAR ADJUST FIELD
         CP    GREGDECI+4(1),GREGPAK0  Q. IS REMAINDER ZERO
         BNE   *+8                     IF NOT IT IS NOT A LEAP YEAR
         MVI   GREGDECI+5,X'1C'        YES MAKE ADJUST FIELD ONE
         ZAP   GREGDECI(2),GREGPK12    INIT MONTH FIELD TO 12
         LA    R1,GREGMNTB             POINT TO MONTH TABLE
GREGTBIN ZAP   GREGDECI+2(2),0(2,R1)   MOVE TABLE VALUE IN
         CP    GREGDECI(2),GREGPAK2    Q. IS IT FEBRUARY YET
         BNH   *+10                    YES = DO NOT ADJUST FOR LEAP-YR
         SP    GREGDECI+2(2),GREGDECI+5(1)    NO - ADJUST FOR LEAP-YR
         AP    GREGDECI+2(2),GREGDECI+6(2)    ADD DAYS TO TABLE VALUE
         BP    GREGFMDT                       Q. IS RESULT POSITIVE
         SP    GREGDECI(2),GREGPAK1           NO REDUCE MONTH BY ONE
         LA    R1,2(R1)                       POINT TO NEXT TBL ENTRY
         B     GREGTBIN                       REPEAT
GREGFMDT OI    GREGDECI+1,15
         OI    GREGDECI+3,15
         UNPK  GREGMNTH,GREGDECI(2)     FORM MONTHS IN CHARACTER FORMAT
         UNPK  GREGDAY,GREGDECI+2(2)    FORM DAYS IN CHARACTER FORMAT
         MVC   TITLDAY,GREGDAY          PUT DAY IN TITLE
         MVI   SLASH1,C'/'
         MVC   TITLMNTH,GREGMNTH        PUT MONTH IN TITLE
         MVI   SLASH2,C'/'
         MVC   TITLYEAR,GREGYEAR        PUT YEAR IN TITLE
DEFAULT  DS    0H                      WE HAVE NO VALID PARM
         SPACE 3
         OPEN  (TITLE,(INPUT))         OPEN TITLE DATA SET
         GET   TITLE,DATAPART          GET THE TITLE
         CLI   SLASH1,C'/'             Q. WILL WE SUPPLY THE DATE
         BNE   SKIPIT                  NO
         MVC   TITLEDTE,DATESTOR       YES
SKIPIT   DS    0H
         OPEN  (SYSPRINT,(OUTPUT))     OPEN MAIN OUTPUT DATASET
         PUT   SYSPRINT,TITLAREA       PUT OUT THE FIRST ONE
TITLEND  CLOSE (TITLE)                 CLOSE THE TITLE DATA SET
         SPACE 2
         OPEN  (SYSIN,(INPUT))         OPEN MAIN INPUT DATASETS
         GET   SYSIN,INAREA            GET THE FIRST RECORD
         MVI   INAREA,C'0'             DO NOT SKIP TO CHANNEL 1
*                                      (SKIP ONE LINE INSTEAD)
         B     TITLEDON                PUT OUT THE FIRST DATA LINE
         SPACE 2
READ     DS    0H                      READ ROUTINE STARTS HERE
         SPACE
         GET   SYSIN,INAREA            GET DATA TO BE PRINTED
         CLI   INAREA,C'1'             Q. IS THIS SKIP TO CHANNEL 1
         BE    TITLEPUT                YES. GO MAKE A TITLE
TITLEDON PUT   SYSPRINT,INAREA         PUT RECORD ON MICROFICHE
         B     READ                    PROCESS NEXT RECORD
         SPACE 3
TITLEPUT DS    0H
         PUT   SYSPRINT,TITLAREA       PUT OUT THE TITLE
         MVI   INAREA,C'0'             DO NOT SKIP TO CHANNEL 1
*                                      (JUST SKIP ONE LINE INSTEAD)
         B     TITLEDON                GO BACK WHERE YOU CAME FROM
         SPACE 3
ENDIN    DS    0H                      NO MORE INPUT
         CLOSE (SYSIN)
         CLOSE (SYSPRINT)
         SPACE 3
LINKOUT  DS    0H                      LINK BACK TO CALLER
         L     R13,SAVEAREA+4
         LM    R14,R12,12(R13)
         LA    R15,0
         BR    R14
         SPACE 2
JULIANDT DS    F            STORAGE FOR JULIAN DATE FROM TIME MACRO
GREGDECI DS    PL8'0'       WORK AREA FOR JULIAN TO GREGORIAN CONVERTOR
GREG1000 DC    P'1000'      THESE ARE CONSTANTS FOR
GREGPAK4 DC    P'4'         DOING THE JULIAN TO
GREGPAK0 DC    P'0'         GREGORIAN DATE
GREGPK12 DC    P'12'        CONVERSION
GREGMNTB DC    PL2'-334,-304,-273,-243,-212,-181,-151,-120,-90,-59,-31,-
               0'           SO
GREGPAK2 DC    P'2'         ARE
GREGPAK1 DC    P'1'         THESE
GREGEND  DS    0H           -------------
GREGMNTH DS    CL2          GREGORIAN MONTH
GREGDAY  DS    CL2          GREGORIAN DAY
GREGYEAR DS    CL2          GREGORIAN YEAR
         SPACE 2
INAREA   DC    CL133' '     INPUT AND OUTPUT AND WORK AREA
         SPACE 2
TITLAREA DS    CL133        PLACE FOR THE TITLE TO RESIDE
         ORG   TITLAREA     REDEFINE IT
ASACNTL  DC    C'1'         ASA CHARACTER (TOP OF FORM)
DATAPART DC    CL132' '     REST OF RECORD
         ORG   DATAPART     REDEFINE DATAPART
FIRST56  DS    CL56
TITLEDTE DS    CL8          PLACE FOR DATE IN TITLE
         ORG   TITLAREA+133 END OF REDEFINES
         SPACE 2
DATESTOR DC    CL8' '
         ORG   DATESTOR     REDEFINE DATE STORAGE PLACE
TITLMNTH DS    CL2          MONTH MONTH
SLASH1   DS    C            SLASH
TITLDAY  DS    CL2          DAY DAY
SLASH2   DS    C            SLASH
TITLYEAR DS    CL2          YEAR YEAR
         SPACE 2
         PRINT NOGEN
TITLE    DCB   DDNAME=TITLE,DSORG=PS,MACRF=(GM),EODAD=TITLEND
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),EODAD=ENDIN
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),LRECL=133,RECFM=FB
         PRINT GEN
         LTORG
         END   FICHE1
