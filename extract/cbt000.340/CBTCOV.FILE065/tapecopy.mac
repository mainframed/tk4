*          DATA SET CBT990     AT LEVEL 002 AS OF 01/27/82
   TITLE 'COPYMODS -- TAPE COPY PROGRAM'
**************************************************************
**************************************************************
**                                                          **
**                                                          **
**  MODULE: COPYMODS.   WRITTEN BY PAUL TOKHEIM, AFAFC.     **
**                                                          **
**  05/05/87            MODIFIED BY RONALD CRUZ, CADAM INC  **
**                                                          **
**                                                          **
**  PURPOSE: COPY ALL FILES OF AN INPUT TAPE TO ONE TO TEN  **
**     OUTPUT TAPES.  THE ORIGINAL PURPOSE OF THIS PROGRAM  **
**     IS FOR COPYING THE SHARE MVS MODS TAPE.              **
**                                                          **
**                                                          **
**  METHOD: COPYMODS FIRST DETERMINES WHICH OUTPUT DD'S ARE **
**    PRESENT. THE INPUT TAPE IS READ USING EXCP AND THE    **
**    APPROPRIATE OUTPUT TAPES ARE WRITTEN USING EXCP.      **
**    ANY LABELS ON THE INPUT WILL BE COPIED. ANY LABELS ON **
**    THE OUTPUT TAPES WILL BE DESTROYED.  IF AN ERROR      **
**    OCCURS ON THE INPUT TAPE, THE PROGRAM WILL ABEND WITH **
**    A USER 69 ABEND.  IF AN ERROR OCCURS ON AN OUTPUT     **
**    TAPE, THE PROGRAM WILL QUIT WRITEING TO THE TAPE      **
**    WHICH ENCOUNTERED THE ERROR.                          **
**                                                          **
***************** MODIFICATION LOG ***************************
** R. CRUZ 05/05/87  MODIFIED TO DO THE FOLLOWING:          **
**                 - THE NUMBER OF TAPE COPIES IS SPECIFIED **
**                   VIA A WTOR. A VALID NUMBER OF COPIES IS**
**                   FROM ONE TO SIX.                       **
**                 - DYNAMICALLY ALLOCATE OUTPUT DD'S USING **
**                   SVC99.                                 **
**                 - ACCEPT OUTPUT TAPE DENSITY FROM WTOR,  **
**                   YOU MAY SPECIFY 1600 OR 6250 BPI.      **
**                 - SPECIFY UNIT TYPE VIA WTOR, LENGTH OF  **
**                   FROM 1 - 8  CHARACTERS MAY BE SPECIFIED.*
**                                                          **
**************************************************************
**************************************************************
         EJECT
         MACRO
&TAG     PRIME &REG,&AREA,&POOL,&LEN,&SAVE=,&R=,&RMK=
         GBLC  &CR0,&CR1,&CR12,&CR13,&CR14,&CR15
         LCLC  &C1,&C2
*******************   PROGRAM INITIATION ROUTINE   ********************
         AIF   ('&RMK' NE 'YES').PR10  JUMP IF REMARKS NOT DESIRED
* THIS MACRO WILL CREATE A SAVEAREA, SAVE THE REGISTERS, AND PERFORM  *
* THE NECESSARY ENTRY LINKAGE.  IT HAS THE FOLLOWING OPTIONS:         *
*                                                                     *
* (TAG)  PRIME (REG)(,AREA)(,POOL)(,LEN)(,SAVE=)(,R=)(,RMK=)          *
*                                                                     *
*    TAG = OPTIONAL - SYMBOLIC NAME IDENTIFIABLE TO 1ST GENERATED INST*
*  PRIME = REQUIRED - OPERATION CODE.                                 *
*    REG = OPTIONAL - INDICATE DESIRED BASE REGISTER OR REGISTERS.    *
*          IF OMITTED REGISTER 13 ASSUMED.  IF MULTIPLE BASES DESIRED *
*          SEPARATE BY COMMAS AND ENCLOSE IN PARENTHESIS (13,11,12).  *
*          IF REG 13 IS SPECIFIED IT MUST BE FIRST.                   *
*          DO NOT USE REG 13 AS A BASE IF RE-ENTERABLE CODING DESIRED.*
*   AREA = OPTIONAL - SYMBOLIC NAME WHICH IF SPECIFIED WILL CAUSE A   *
*          SAVEAREA TO BE GENERATED AND TAGGED WITH THIS NAME. IF REG *
*          13 IS USED AS A BASE A SAVEAREA IS ALWAYS GENERATED AND    *
*          WILL BE GIVEN THIS NAME. THIS AREA WILL NOT BE RELEASED IN *
*          THE 'TERME' MACRO.  THIS IS DESIRABLE IF THE ROUTINE USING *
*          THE 'PRIME & TERME' MACROS IS ENTERED SEVERAL TIMES. IF REG*
*          13 IS NOT USED AS A BASE AND THIS OPTION IS NOT SPECIFIED  *
*          PRIME WILL EXECUTE A 'GETMAIN' TO OBTAIN A SAVEAREA AND    *
*          TERME WILL EXECUTE A 'FREEMAIN' TO RELEASE THE AREA.       *
*          DO NOT SPECIFY THIS OPTION IF RE-ENTERABLE CODING DESIRED. *
*   POOL = OPTIONAL - SPECIFIES THE SUBPOOL FROM WHICH THE SAVEAREA IS*
*          TO BE OBTAINED. IF OMITTED SUBPOOL ZERO IS ASSUMED. THIS   *
*          OPERAND IS IGNORED IF REG 13 IS USED AS A BASE OR IF THE   *
*          AREA OR SAVE OPERANDS ARE SPECIFIED. IF THIS OPTION IS USE *
*          THE SAME SUBPOOL MUST BE SPECIFIED IN THE 'TERME' MACRO.   *
*    LEN = OPTIONAL - MAY BE USED TO SPECIFY THE AMOUNT OF STORAGE TO *
*          BE OBTAINED FOR THE SAVEAREA.  THE OPERAND MUST BE NUMERIC *
*          AND MUST NOT BE LESS THAN 72. THE FIRST 72 BYTES OF THE    *
*          AREA ARE USED BY PRIME TO STORE THE REGISTERS. THE USER MAY*
*          USE THE EXTRA AREA THAT WAS OBTAIN BEYOND 72 PASSED REG 13 *
*          FOR A WORK AREA. THIS OPTION IS USEFUL FOR RE-ENTERABLE    *
*          CODE AND IS IGNORED IF REG 13 IS USED AS A BASE, OR IF THE *
*          AREA OR SAVE OPERANDS ARE SPECIFIED.                       *
*   SAVE = OPTIONAL - SPECIFIES THE NAME OF AN 18 FULLWORD AREA WHERE *
*          THE REGISTERS ARE TO BE SAVED.                             *
*      R = OPTIONAL (KEY WORD) - IF (R=YES) IS SPECIFIED R0 THRU R15  *
*          WILL BE EQUATED TO 0 THRU 15 TO SYMBOLICALLY IDENTIFY REGS.*
*    RMK = OPTIONAL (KEYWORD) - IF (RMK=YES) IS SPECIFIED THIS REMARK *
*          BLOCK WILL BE LISTED IN THE MACRO EXPANSION.               *
***********************************************************************
.PR10    AIF   ('&CR0' NE '').PR40     JUMP IF REGS ALREADY EQUATED
         AIF   ('&R' EQ '').PR20
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.PR20    ANOP
         AIF   ('&R' EQ '').PR30       JUMP IF NO EQUATE
&CR0     SETC  'R0'
&CR1     SETC  'R1'
&CR12    SETC  'R12'
&CR13    SETC  'R13'
&CR14    SETC  'R14'
&CR15    SETC  'R15'
         AGO   .PR40
.PR30    ANOP
&CR0     SETC  '0'
&CR1     SETC  '1'
&CR12    SETC  '12'
&CR13    SETC  '13'
&CR14    SETC  '14'
&CR15    SETC  '15'
.PR40    DS    0F -------------------- VER.1 REL.6 --------------------
         AIF   ('&REG' NE '').PR50     JUMP IF REG PRESENT
         USING *,&CR13
         AGO   .PR100
.PR50    AIF   (N'&REG GT 1).PR60           JUMP IF MORE THAN 1 REG
         USING *,&REG(1)
         AGO   .PR100
.PR60    AIF   (N'&REG GT 2).PR70           JUMP IF MORE THAN 2 REGS
         USING *,&REG(1),&REG(2)
         AGO   .PR100
.PR70    AIF   (N'&REG GT 3).PR80           JUMP IF MORE THAN 3 REGS
         USING *,&REG(1),&REG(2),&REG(3)
         AGO   .PR100
.PR80    ANOP
         USING *,&REG(1),&REG(2),&REG(3),&REG(4)
         AIF   (N'&REG LT 5).PR100          JUMP IF LESS THAN 5 REGS
      MNOTE 8,'** MAXIMUM NUMBER OF REGISTERS IS 4 - OTHER IGNORED **'
.PR100   AIF   ('&REG' EQ '').PR110    JUMP IF REG NOT PRESENT
         AIF   ('&REG(1)' EQ 'R13' OR '&REG(1)' EQ '13').PR110
         AGO   .PR120
.PR110   AIF   ('&AREA' EQ '').PR115
&AREA    DS    0F
.PR115   ANOP
&TAG     BAL   &CR15,76(&CR15)
         DC    17F'0',H'4',H'4096'
         SH    &CR15,68(&CR15)
         STM   &CR14,&CR12,12(&CR13)
         LR    &CR1,&CR15
         AIF   (N'&REG LT 2).PR170
         LR    &REG(2),&CR1
         LA    &CR15,74(&CR1)
         AGO   .PR150
.PR120   ANOP
&TAG     BALR  &CR15,0
         BCTR  &CR15,0
         BCTR  &CR15,0
         STM   &CR14,&CR12,12(&CR13)
         LR    &REG(1),&CR15
         AIF   ('&AREA' EQ '').PR130   JUMP IF AREA NOT DESIRED
         BAL   &CR1,*+78
&AREA    DC    18F'0',H'4096'
         LA    &CR15,72(&CR1)
         AGO   .PR140
.PR130   AIF   (N'&REG LT 2).PR160
         BAL   &CR15,*+6
         DC    H'4096'
.PR140   AIF   (N'&REG EQ 1).PR160     JUMP IF 1 REG
         LR    &REG(2),&REG(1)
.PR150   AH    &REG(2),0(&CR15)
         AIF   (N'&REG EQ 2).PR160     JUMP IF 2 REGS
         LR    &REG(3),&REG(2)
         AH    &REG(3),0(&CR15)
         AIF   (N'&REG EQ 3).PR160     JUMP IF 3 REGS
         LR    &REG(4),&REG(3)
         AH    &REG(4),0(&CR15)
.PR160   AIF   ('&REG(1)' EQ 'R13' OR '&REG(1)' EQ '13').PR170
         AIF   ('&AREA' NE '').PR170   JUMP IF AREA
         AIF   ('&SAVE' EQ '').PR180   JUMP IF NO SAVE
         LA    &CR1,&SAVE
.PR170   ST    &CR1,8(&CR13)
         MVI   8(&CR13),X'A3'
         AGO   .PR240
.PR180   AIF   ('&POOL' EQ '').PR190   JUMP IF NO POOL
&C1      SETC  '&POOL'
         AGO   .PR200
.PR190   ANOP
&C1      SETC  '0'
.PR200   AIF   ('&LEN' EQ '').PR220
         AIF   ('&LEN' LT '72').PR210
&C2      SETC  '&LEN'
         AGO   .PR230
.PR210   MNOTE 4,'SAVEAREA LENGTH SPECIFIED LESS THAN 72 - 72 ASSUMED'
.PR220   ANOP
&C2      SETC  '72'
.PR230   GETMAIN R,LV=&C2,SP=&C1
         XC    0(72,&CR1),0(&CR1)
         ST    &CR1,8(&CR13)
         MVI   8(&CR13),X'DC'
.PR240   ST    &CR13,4(&CR1)
         LM    &CR0,&CR1,20(&CR13)
         L     &CR13,8(&CR13)
         MEND
         MACRO
&TAG     TERME &OP,&POOL,&LEN,&RMK=
         GBLC  &CR0,&CR1,&CR12,&CR13,&CR14,&CR15
         LCLA  &A
         LCLC  &C1,&C2
*******************   PROGRAM TERMINATION ROUTINE   *******************
         AIF   ('&RMK' NE 'YES').TR1   JUMP IF REMARKS NOT DESIRED
* THIS MACRO WILL RESTORE THE REGISTERS, RELEASE THE SAVEAREA IF      *
* OBTAIN BY 'PRIME' MACRO, AND RETURN TO NEXT HIGHER LEVEL.           *
* UPON RETURN REGS. 0 & 1 WILL REMAIN THE SAME AS BEFORE THE TERME.   *
*                                                                     *
* (TAG)  TERME  (OP)(,POOL)(,LEN)(,RMK=NO)                            *
*                                                                     *
*    TAG = OPTIONAL - SYMBOLIC NAME IDENTIFIABLE TO 1ST GENERATED INST*
*  TERME = REQUIRED - OPERATION CODE.                                 *
*     OP = OPTIONAL - IF 'XCTL' IS SPECIFIED INSTRUCTIONS WILL BE     *
*          GENERATED WHICH WILL ALLOW THE 'XCTL' MACRO TO BE ISSUED   *
*          IMMEDIATLY FOLLOWING THE TERME MACRO. CONTROL WILL NOT BE  *
*          GIVEN TO THE NEXT HIGHER LEVEL.                            *
*   POOL = OPTIONAL - IT MUST BE PRESENT IF THE POOL OPTION IN 'PRIME'*
*          WAS SPECIFIED, AND MUST INDICATE THE SAME SUBPOOL.         *
*    LEN = OPTIONAL - IT MUST BE PRESENT IF THE LEN OPTION IN 'PRIME' *
*          WAS SPECIFIED, AND MUST INDICATE THE SAME LENGTH.          *
*    RMK = OPTIONAL (KEYWORD) - IF (RMK=YES) IS SPECIFIED THIS REMARK *
*          BLOCK WILL BE LISTED IN THE MACRO EXPANSION.               *
***********************************************************************
.TR1     ANOP
         AIF   ('&CR0' NE '').TR2      JUMP IF REG EQU IN PRIME
&CR0     SETC  '0'
&CR1     SETC  '1'
&CR12    SETC  '12'
&CR13    SETC  '13'
&CR14    SETC  '14'
&CR15    SETC  '15'
.TR2     ANOP
&A       SETA  &SYSNDX
&TAG     L     &CR14,4(&CR13)
         STM   &CR15,&CR1,16(&CR14)
         XI    8(&CR14),X'DC'
         BC    7,IXI5&A
         L     &CR1,8(&CR14)
         AIF   ('&POOL' EQ '').TR10
&C1      SETC  '&POOL'
         AGO   .TR20
.TR10    ANOP
&C1      SETC  '0'
.TR20    AIF   ('&LEN' EQ '').TR40
         AIF   ('&LEN' LT '72').TR30
&C2      SETC  '&LEN'
         AGO   .TR50
.TR30    MNOTE 4,'SAVEAREA LENGTH SPECIFIED LESS THAN 72 - ASSUME 72'
.TR40    ANOP
&C2      SETC  '72'
.TR50    ANOP
         FREEMAIN R,LV=&C2,A=(1),SP=&C1
IXI5&A   LR    &CR13,&CR14
         L     &CR14,16(&CR13)
         SPM   &CR14
         LM    &CR14,&CR12,12(&CR13)
         XC    8(4,&CR13),8(&CR13) ----- VER.1 REL.6 -----
         AIF   ('&OP' EQ 'XCTL').C
         BR    &CR14
         MEXIT
.C       ANOP
         BALR  &CR15,0
         USING *,&CR15
         MEND
         EJECT
COPYMODS CSECT
         PRINT NOGEN
         PRIME R=YES,RMK=NO
         SR    R9,R9          ZERO FOR BLOCK COUNT
         LA    R8,1           INITIALIZE FOR FILE NUMBER
ASKOPER  DS    0H
         XC    OUTECB,OUTECB
         XC    REPLY2,REPLY2
         WTOR  'ENTER NUMBER OF TAPE DUPES TO BE DONE, FROM 1 - 6',    X
               REPLY2,1,OUTECB,ROUTCDE=(3),DESC=(2)
         WAIT  ECB=OUTECB
         LA    R10,REPLY2
         CLI   0(R10),C'1'    GET NUMBER OF O/P FILES
         BL    ASKOPER        NOT ALLOWED
         CLI   0(R10),C'6'    INSURE VALID NUMERICS ONLY
         BH    ASKOPER
         IC    R7,0(R10)      GET READY FOR SHIFT
         SLL   7,28           CHOP OFF ZONE
         SRL   7,28           AND KEEP WHOLE NUMBER
         LR    R6,R7          SAVE IT
ASK4TYPE DS    0H
         MVC   REPLY8(8),=CL8' '
         XC    OUTECB,OUTECB
         WTOR  'ENTER OUTPUT DEVICE TYPE - SPECIFY FROM 1 - 8 CHARACTERX
               S',REPLY8,8,OUTECB,ROUTCDE=(3),DESC=(2)
         WAIT  ECB=OUTECB
         SR    R2,R2
         LA    R5,REPLY8         ADDRESS OF REPLY AREA
         LA    R3,8              MAXIMUM LENGTH ALLOWED
LOOP1    DS    0H
         CLI   0(R5),C' '        END OF UNIT NAME
         BE    LOOP2             YES, GO MOVE IT
         LA    R2,1(R2)          BUMP LENGTH COUNTER
         LA    R5,1(R5)          BUMP TO NEXT CHARACTER
         BCT   R3,LOOP1          LOOP TILL DONE
LOOP2    DS    0H
         STH   R2,UNITLEN        STORE TRUE LENGTH IN TEXT
         BCTR  R2,0              DECREMENT FOR S370 MOVE
         LA    R5,UNTNAME        TARGET ADDRESS
         LA    R3,REPLY8         SENDING ADDRESS
         EX    R2,MVCUNIT        MOVE IT
         CLC   REPLY8(8),=CL8'CTAPE'     OUR GENERIC UNITNAME FOR 3480
         BE    NODEN                     YES, INDICATE NO DENSITY
         CLC   REPLY8(8),=CL8'3480'      UNIT TYPE OF 3480
         BNE   ASK4DEN                   NO, ASSUME 3420 TYPE DEVICE
NODEN    DS    0H
*
* TURN ON END OF LIST INDICATOR, NO DENSITY REQUIRED FOR 3480 UNIT
*
         OI    TXTPTRS+24,X'80'
         LA    R2,4
         STH   R2,UNITLEN
         MVC   UNTNAME(8),=CL8'3480'     INDICATE 3480 UNIT TYPE
         B     OPENS
ASK4DEN  DS    0H
         XC    REPLY4,REPLY4
         XC    OUTECB,OUTECB
         WTOR  'ENTER DENSITY OF OUTPUT TAPE 1600 OR 6250 BPI',        X
               REPLY4,4,OUTECB,ROUTCDE=(3),DESC=(2)
         WAIT  ECB=OUTECB
         CLC   REPLY4(4),=CL4'6250'   DEFAULT DESITY
         BE    OPENS                  YES, CONTINUE
         CLC   REPLY4(4),=CL4'1600'   DENSITY OF 1600 SPECIFIED
         BNE   ASK4DEN                NO, GO ASK AGAIN
         MVI   DCBDENS,X'C3'          YES
         EJECT
*
*  OPEN ALL FILES
*
OPENS    OPEN  (IN,,SYSPRINT,OUTPUT)
* ALWAYS OPEN OUTPUT FILE 1
         BCTR  R6,0           DECREMENT O/P FILE COUNTER
         OI    FLAG1,OP1      INDICATE OUPUT 1 PRESENT
         BAL   R3,DYNALOC     DYNAMICALLY ALLOCATE OUT1 DD
         OPEN  (OUT1,OUTPUT)
OPEN1    LTR   R6,R6          DO WE OPEN O/P FILE 2
         BZ    READINA        NO, LETS GO READ AND WRITE
         BCTR  R6,0           DECREMENT O/P FILE COUNTER
         OI    FLAG1,OP2      INDICATE OUPUT 2 PRESENT
         MVI   DSNAME+7,C'2'  CHANGE TO MODTAPE2 DSNAME
         MVI   DDNM+3,C'2'    CHANGE TO OUT2 DDNAME
         BAL   R3,DYNALOC     DYNAMICALLY ALLOCATE OUT2 DD
         OPEN  (OUT2,OUTPUT)
OPEN2    LTR   R6,R6          DO WE OPEN O/P FILE 3
         BZ    READINA        NO, LETS GO READ AND WRITE
         BCTR  R6,0           DECREMENT O/P FILE COUNTER
         OI    FLAG1,OP3      INDICATE OUTPUT 3 PRESENT
         MVI   DSNAME+7,C'3'  CHANGE TO MODTAPE3 DSNAME
         MVI   DDNM+3,C'3'    CHANGE TO OUT3 DDNAME
         BAL   R3,DYNALOC     DYNAMICALLY ALLOCATE OUT3 DD
         OPEN  (OUT3,OUTPUT)
OPEN3    LTR   R6,R6          DO WE OPEN O/P FILE 4
         BZ    READINA        NO, LETS GO READ AND WRITE
         BCTR  R6,0           DECREMENT O/P FILE COUNTER
         OI    FLAG1,OP4      INDICATE OUTPUT 4 PRESENT
         MVI   DSNAME+7,C'4'  CHANGE TO MODTAPE4 DSNAME
         MVI   DDNM+3,C'4'    CHANGE TO OUT4 DDNAME
         BAL   R3,DYNALOC     DYNAMICALLY ALLOCATE OUT4 DD
         OPEN  (OUT4,OUTPUT)
OPEN4    LTR   R6,R6          DO WE OPEN O/P FILE 5
         BZ    READINA        NO, LETS GO READ AND WRITE
         BCTR  R6,0           DECREMENT O/P FILE COUNTER
         OI    FLAG1,OP5      INDICATE OUTPUT 5 PRESENT
         MVI   DSNAME+7,C'5'  CHANGE TO MODTAPE5 DSNAME
         MVI   DDNM+3,C'5'    CHANGE TO OUT5 DDNAME
         BAL   R3,DYNALOC     DYNAMICALLY ALLOCATE OUT5 DD
         OPEN  (OUT5,OUTPUT)
OPEN5    LTR   R6,R6          DO WE OPEN O/P FILE 6
         BZ    READINA        NO, LETS GO READ AND WRITE
         BCTR  R6,0           DECREMENT O/P FILE COUNTER
         OI    FLAG1,OP6      INDICATE OUTPUT 6 PRESENT
         MVI   DSNAME+7,C'6'  CHANGE TO MODTAPE6 DSNAME
         MVI   DDNM+3,C'6'    CHANGE TO OUT6 DDNAME
         BAL   R3,DYNALOC     DYNAMICALLY ALLOCATE OUT6 DD
         OPEN  (OUT6,OUTPUT)
READINA  DS    0H
         MVC   FLAG3(2),FLAG1 SAVE INDICATION OF WHICH FILES ARE HERE
         SPACE 3
*
*  READ INPUT
*
READIN   LR    R6,R7          REFRESH WITH NUMBER OF O/P FILES
         MVI   INECB,X'00'    INITIALIZE ECB
         EXCP  INIOB          READ
         WAIT  ECB=INECB      WAIT FOR READ
         TM    INCSW+4,X'01'  WAS TM READ?
         BO    READIN1        YES
         CLI   INECB,X'7F'    DID READ COMPLETE NORMALLY?
         BNE   INERR          NO -- ERROR
         MVI   OUTCCW,X'01'   SET OUTPUT CCW TO WRITE
         LH    R2,INCCW+6     R2=READ LENGTH
         LH    R3,INCSW+6     R3=RESIDUAL BYTE COUNT
         SR    R2,R3          R2=ACTUAL LENGTH READ
         STH   R2,OUTCCW+6    STORE FOR WRITE
         LA    R9,1(,R9)      ADD 1 TO COUNT OF DATA BLOCKS
         B     WRITE1         CONTINUE
READIN1  CVD   R8,DWORD       CONVERT FILE NUMBER TO DECIMAL
         OI    DWORD+7,X'0F'  MAKE SIGN READABLE
         UNPK  FILEDONE+6(5),DWORD+5(3)  UNPACK FILE NUMBER
         CVD   R9,DWORD       CONVERT BLOCK COUNT TO DECIMAL
         OI    DWORD+7,X'0F'  MAKE SIGN READABLE
         UNPK  FILEDONE+21(7),DWORD+4(4)  UNPACK BLOCK COUNT
         PUT   SYSPRINT,FILEDONE  WRITE IT
         SR    R9,R9          ZERO BLOCK COUNT FOR NEW FILE
         LA    R8,1(,R8)      ADD 1 TO FILE NUMBER
         CLI   OUTCCW,X'1F'   2ND TM IN A ROW?
         BE    READIN2        YES - END OF VOLUME
         MVI   OUTCCW,X'1F'   SET OUTPUT CCW TO WRITE TM
         B     WRITE1         GO WRITE TM TO OUTPUTS
READIN2  OI    FLAG2,EOV      INDICATE EOV ON INPUT
         SPACE 3
*
*  WRITE TO OUTPUT TAPES
*
WRITE1   LTR   R6,R6          IS THIS OUTPUT OPENED
         BZ    WRITE11        NO, BYPASS WRITE
         BCTR  R6,0
         TM    FLAG1,OP1      POSSIBLE I/O ERROR
         BZ    WRITE2         YES, DON'T WRITE
         LA    R2,OUT1        R2=ADDRESS OF CURRENT DCB
         ST    R2,OUTDCBPT    PUT IT IN IOB
         MVI   OUTECB,X'00'   INITIALIZE ECB
         EXCP  OUTIOB         WRITE IT
         WAIT  ECB=OUTECB     WAIT FOR IT
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?
         BNE   OUTERR         NO - ERROR
WRITE2   LTR   R6,R6          IS THIS OUTPUT OPENED
         BZ    WRITE11        NO, BYPASS WRITE
         BCTR  R6,0
         TM    FLAG1,OP2      POSSIBLE I/O ERROR
         BZ    WRITE3         YES, DON'T WRITE
         LA    R2,OUT2        R2=ADDRESS OF CURRENT DCB
         ST    R2,OUTDCBPT    PUT IT IN IOB
         MVI   OUTECB,X'00'   INITIALIZE ECB
         EXCP  OUTIOB         WRITE IT
         WAIT  ECB=OUTECB     WAIT FOR IT
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?
         BNE   OUTERR         NO - ERROR
WRITE3   LTR   R6,R6          IS THIS OUTPUT OPENED
         BZ    WRITE11        NO, BYPASS WRITE
         BCTR  R6,0
         TM    FLAG1,OP3      POSSIBLE I/O ERROR
         BZ    WRITE4         YES, DON'T WRITE
         LA    R2,OUT3        R2=ADDRESS OF CURRENT DCB
         ST    R2,OUTDCBPT    PUT IT IN IOB
         MVI   OUTECB,X'00'   INITIALIZE ECB
         EXCP  OUTIOB         WRITE IT
         WAIT  ECB=OUTECB     WAIT FOR IT
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?
         BNE   OUTERR         NO - ERROR
WRITE4   LTR   R6,R6          IS THIS OUTPUT OPENED
         BZ    WRITE11        NO, BYPASS WRITE
         BCTR  R6,0
         TM    FLAG1,OP4      POSSIBLE I/O ERROR
         BZ    WRITE5         YES, DON'T WRITE
         LA    R2,OUT4        R2=ADDRESS OF CURRENT DCB
         ST    R2,OUTDCBPT    PUT IT IN IOB
         MVI   OUTECB,X'00'   INITIALIZE ECB
         EXCP  OUTIOB         WRITE IT
         WAIT  ECB=OUTECB     WAIT FOR IT
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?
         BNE   OUTERR         NO - ERROR
WRITE5   LTR   R6,R6          IS THIS OUTPUT OPENED
         BZ    WRITE11        NO, BYPASS WRITE
         BCTR  R6,0
         TM    FLAG1,OP5      POSSIBLE I/O ERROR
         BZ    WRITE6         YES, DON'T WRITE
         LA    R2,OUT5        R2=ADDRESS OF CURRENT DCB
         ST    R2,OUTDCBPT    PUT IT IN IOB
         MVI   OUTECB,X'00'   INITIALIZE ECB
         EXCP  OUTIOB         WRITE IT
         WAIT  ECB=OUTECB     WAIT FOR IT
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?
         BNE   OUTERR         NO - ERROR
WRITE6   LTR   R6,R6          IS THIS OUTPUT OPENED
         BZ    WRITE11        NO, BYPASS WRITE
         BCTR  R6,0
         TM    FLAG1,OP6      POSSIBLE I/O ERROR
         BZ    WRITE11        YES, DON'T WRITE
         LA    R2,OUT6        R2=ADDRESS OF CURRENT DCB
         ST    R2,OUTDCBPT    PUT IT IN IOB
         MVI   OUTECB,X'00'   INITIALIZE ECB
         EXCP  OUTIOB         WRITE IT
         WAIT  ECB=OUTECB     WAIT FOR IT
         CLI   OUTECB,X'7F'   WRITE COMPLETE NORMALLY?
         BNE   OUTERR         NO - ERROR
WRITE11  TM    FLAG2,EOV      END OF INPUT?
         BO    ENDINPUT       YES - QUIT
         B     READIN         NO - LOOP
         SPACE 3
*
*  ERROR ON INPUT -- TELL OPERATOR AND ABEND
*
INERR    TM    INCSW+4,X'02'  UNIT CHECK?
         BZ    INERR1         NO - ASSUME DATA CHECK
         MVC   LINE(133),INEQC  SET ERROR MESSAGE
         MVC   INERR3+16(25),INEQC+12  MOVE ERROR MESSAGE TO WTOR
         B     INERR2         CONTINUE
INERR1   MVC   LINE(133),INDCK  SET ERROR MESSAGE
         MVC   INERR3+16(20),INDCK+12  MOVE ERROR MESSAGE TO WTOR
INERR2   PUT   SYSPRINT,LINE  WRITE ERROR MESSAGE
         MVI   INECB,X'00'    RESET ECB
INERR3   WTOR  '                          -- ACKNOWLEDGE',REPLY,       X
               6,INECB,ROUTCDE=(3)
         WAIT  ECB=INECB      WAIT FOR OPERATOR TO SEE THE MESSAGE
*                             WHO CARES WHAT THE REPLY IS
         ABEND 69             QUIT
         SPACE 3
*
*  ERROR ON OUTPUT -- TELL OPERATOR THEN TERMINATE THIS OUTPUT ONLY
*
OUTERR   TM    OUTCSW+4,X'02' UNIT CHECK?
         BZ    OUTERR1        NO - ASSUME DATA CHECK
         MVC   LINE(133),OUTEQC  SET ERROR MESSAGE
         B     OUTERR2        CONTINUE
OUTERR1  MVC   LINE(133),OUTDCK  SET ERROR MESSAGE
OUTERR2  LH    R4,40(,R2)     R4=TIOT OFFSET
         L     R5,16          R5=CVT
         L     R5,0(,R5)      R5=TCB WORDS
         L     R5,4(,R5)      R5=MY TCB
         L     R5,12(,R5)     R5=TIOT
         AR    R4,R5          R4=TIOT ENTRY FOR THIS OUTPUT
         MVC   LINE+35(5),4(R4)  MOVE DDNAME TO MESSAGE
         L     R4,16(,R4)     R4=UCB FOR THIS OUTPUT
         MVC   LINE+47(3),13(R4)  MOVE UNIT ADDRESS TO MESSAGE
         MVC   LINE+59(6),36(R4)  MOVE VOLSER TO MESSAGE
         PUT   SYSPRINT,LINE  WRITE MESSAGE
         MVC   OUTERR4+16(53),LINE+12  MOVE ERROR MESSAGE TO WTOR
OUTERR3  MVI   OUTECB,X'00'   RESET ECB
OUTERR4  WTOR  '                                                       X
               -- REPLY GO TO CONTINUE',REPLY,2,OUTECB,ROUTCDE=(3)
         WAIT  ECB=OUTECB     WAIT FOR OPERATOR TO REPLY
         CLC   REPLY(2),=C'GO'  CORRECT REPLY?
         BNE   OUTERR3        NO - TRY AGAIN
         ST    R2,REWDCBPT    PUT DCB ADDRESS IN REWIND IOB
         EXCP  REWIOB         REWIND TAPE WITH THE ERROR
*                             DON'T BOTHER WAITING FOR REWIND
         OI    FLAG2,ERR      INDICATE THAT THERE WAS AN ERROR
         LA    R3,OUT1        R3=DCB FOR OUTPUT 1
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?
         BNE   OUTERR5        NO
         NI    FLAG1,FF-OP1   SHOW THIS OUTPUT NOT PRESENT ANY MORE
         B     WRITE2         CONTINUE WITH OTHER OUTPUT
OUTERR5  LA    R3,OUT2        R3=DCB FOR OUTPUT 2
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?
         BNE   OUTERR6        NO
         NI    FLAG1,FF-OP2   SHOW THIS OUTPUT NOT PRESENT ANY MORE
         B     WRITE3         CONTINUE WITH OTHER OUTPUT
OUTERR6  LA    R3,OUT3        R3=DCB FOR OUTPUT 3
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?
         BNE   OUTERR7        NO
         NI    FLAG1,FF-OP3   SHOW THIS OUTPUT NOT PRESENT ANY MORE
         B     WRITE4         CONTINUE WITH OTHER OUTPUT
OUTERR7  LA    R3,OUT4        R3=DCB FOR OUTPUT 4
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?
         BNE   OUTERR8        NO
         NI    FLAG1,FF-OP4   SHOW THIS OUTPUT NOT PRESENT ANY MORE
         B     WRITE5         CONTINUE WITH OTHER OUTPUT
OUTERR8  LA    R3,OUT5        R3=DCB FOR OUTPUT 5
         CR    R2,R3          WAS THIS THE DCB WITH THE ERROR?
         BNE   OUTERR9        NO
         NI    FLAG1,FF-OP5   SHOW THIS OUTPUT NOT PRESENT ANY MORE
         B     WRITE6         CONTINUE WITH OTHER OUTPUT
OUTERR9  NI    FLAG1,FF-OP6   SHOW THIS OUTPUT NOT PRESENT ANY MORE
         B     WRITE11        CONTINUE WITH OTHER OUTPUT
         SPACE 3
*
*  END OF INPUT -- FINISH UP
*
ENDINPUT MVC   LINE(133),SUCCESS  MOVE MESSAGE TO LINE
         TM    FLAG2,OP10     OUTPUT 10 COMPLETED?
         BO    ENDIN1         YES - CONTINUE
         MVC   LINE+87(5),LINE+92  NO - DON'T SHOW IT
ENDIN1   TM    FLAG2,OP9      OUTPUT 9 COMPLETED?
         BO    ENDIN2         YES - CONTINUE
         MVC   LINE+81(11),LINE+87  NO - DON'T SHOW IT
ENDIN2   TM    FLAG1,OP8      OUTPUT 8 COMPLETED?
         BO    ENDIN3         YES - CONTINUE
         MVC   LINE+75(17),LINE+81  NO - DON'T SHOW IT
ENDIN3   TM    FLAG1,OP7      OUTPUT 7 COMPLETED?
         BO    ENDIN4         YES - CONTINUE
         MVC   LINE+69(23),LINE+75  NO - DON'T SHOW IT
ENDIN4   TM    FLAG1,OP6      OUTPUT 6 COMPLETED?
         BO    ENDIN5         YES - CONTINUE
         MVC   LINE+63(29),LINE+69  NO - DON'T SHOW IT
ENDIN5   TM    FLAG1,OP5      OUTPUT 5 COMPLETED?
         BO    ENDIN6         YES - CONTINUE
         MVC   LINE+57(35),LINE+63  NO - DON'T SHOW IT
ENDIN6   TM    FLAG1,OP4      OUTPUT 4 COMPLETED?
         BO    ENDIN7         YES - CONTINUE
         MVC   LINE+51(41),LINE+57  NO - DON'T SHOW IT
ENDIN7   TM    FLAG1,OP3      OUTPUT 3 COMPLETED?
         BO    ENDIN8         YES - CONTINUE
         MVC   LINE+45(47),LINE+51  NO - DON'T SHOW IT
ENDIN8   TM    FLAG1,OP2      OUTPUT 2 COMPLETED?
         BO    ENDIN9         YES - CONTINUE
         MVC   LINE+39(53),LINE+45  NO - DON'T SHOW IT
ENDIN9   TM    FLAG1,OP1      OUTPUT 1 COMPLETED?
         BO    ENDIN10        YES - CONTINUE
         MVC   LINE+33(59),LINE+39  NO - DON'T SHOW IT
ENDIN10  PUT   SYSPRINT,LINE  WRITE IT
         SPACE 3
*
*  CLOSE ALL FILES
*
         CLOSE (IN,,SYSPRINT)
         TM    FLAG3,OP1      OUTPUT 1 OPEN?
         BZ    CLOSE1         NO
         CLOSE (OUT1)         CLOSE OUTPUT 1
CLOSE1   TM    FLAG3,OP2      OUTPUT 2 OPEN?
         BZ    CLOSE2         NO
         CLOSE (OUT2)         CLOSE OUTPUT 2
CLOSE2   TM    FLAG3,OP3      OUTPUT 3 OPEN?
         BZ    CLOSE3         NO
         CLOSE (OUT3)         CLOSE OUTPUT 3
CLOSE3   TM    FLAG3,OP4      OUTPUT 4 OPEN?
         BZ    CLOSE4         NO
         CLOSE (OUT4)         CLOSE OUTPUT 4
CLOSE4   TM    FLAG3,OP5      OUTPUT 5 OPEN?
         BZ    CLOSE5         NO
         CLOSE (OUT5)         CLOSE OUTPUT 5
CLOSE5   TM    FLAG3,OP6      OUTPUT 6 OPEN?
         BZ    THEEND         NO
         CLOSE (OUT6)         CLOSE OUTPUT 6
         SPACE 3
*
*  THE END
*
THEEND   EQU   *
         SR    R15,R15        ZERO RETURN CODE
         TM    FLAG2,ERR      WAS THERE AN ERROR?
         BZ    THEEND1        NO
         LA    R15,16         SET RETURN CODE
THEEND1  TERME RMK=NO
         SPACE 2
DYNALOC  DS    0H
         LA    R1,RB              LOAD ADDRESS OF MY RB
         USING S99RB,R1           GIVE THEIR RB ADDRESSABILITY
         LA    R2,S99RBEND-S99RB  GET LENGTH OF REQUEST BLOCK
         STC   R2,S99RBLN         AND STORE IT
         OI    S99VERB,S99VRBAL   INDICATE ALLOCATION TYPE CALL
         OI    S99FLG11,S99NOCNV  DO NOT USE AN EXISTING ALLOCATION
         LA    R2,TXTPTRS         LOAD ADDRESS OF OUR TEXT POINTERS
         ST    R2,S99TXTPP        AND STORE IT
         DROP  R1
         LA    R1,RBPTR
         DYNALLOC
         LTR   R15,R15            EVERTHING GO OKAY ?
         BZ    0(,R3)             YES, PROCEED
         LA    R1,RB              LOAD ADDRESS OF MY RB
         USING S99RB,R1           GIVE THEIR RB ADDRESSABILITY
*
* ROUTINE TO CONVERT SVC99 ERROR TO PRINTABLE
*
         LA    R9,2               MAX LENGTH OF ERROR CODE
         LA    R3,S99ERROR        GET ERROR CODE
         LA    R6,DECMAL          TARGET ADDRESS
HEXLOOP  DS    0H
         SLR   R4,R4              CLEAR REGISTER 4
         IC    R4,0(R3)           GET CHARACTER
         SRDL  4,4                OFFSET IT
         SLL   4,4                CHOP OFF LEFT MOST BITS
         SLDL  4,4                AND PLACE IT BACK
         STCM  4,B'0011',0(6)     STORE CHARACTER
         TR    0(2,R6),CONVT      TRANSLATE IT TO UPPERCASE
         LA    R3,1(R3)           BUMP TO NEXT CHARACTER
         LA    R6,2(R6)           BUMP OUTPUT AREA BY 2 BYTES
         BCT   R9,HEXLOOP
         MVC   ERRWTO+19(4),DECMAL  MOVE THE ERROR CODE TO THE WTO
ERRWTO   WTO   'S99ERROR -      TAPECOPY FAILED DURING DYNAMIC ALLOCATIX
               ON',ROUTCDE=(3),DESC=(2)
         ABEND 2469               SCREW IT, DON'T GO ANY FARTHER
*
MVCUNIT  MVC   0(0,R5),0(R3)      * EXECUTED INSTRUCTION *
         EJECT
DWORD    DC    D'0'
         CNOP  0,8
INCCW    DC    X'02',AL3(INAREA),X'2000',H'32760'
OUTCCW   DC    X'01',AL3(INAREA),X'2000',H'32760'
REWCCW   DC    X'07',AL3(INAREA),X'2000',H'32760'
INECB    DC    F'0'
INIOB    DC    X'02000000'
         DC    A(INECB)
INCSW    DC    2F'0'
         DC    A(INCCW)
         DC    A(IN)
         DC    F'0'
         DC    X'00010000'
OUTECB   DC    F'0'
OUTIOB   DC    X'02000000'
         DC    A(OUTECB)
OUTCSW   DC    2F'0'
         DC    A(OUTCCW)
OUTDCBPT DC    A(OUT1)
         DC    F'0'
         DC    X'00010000'
REWECB   DC    F'0'
REWIOB   DC    X'02000000'
         DC    A(REWECB)
REWCSW   DC    2F'0'
         DC    A(REWCCW)
REWDCBPT DC    A(OUT1)
         DC    F'0'
         DC    X'00010000'
         SPACE 3
FLAG1    DC    X'00'
OP1      EQU   X'80'
OP2      EQU   X'40'
OP3      EQU   X'20'
OP4      EQU   X'10'
OP5      EQU   X'08'
OP6      EQU   X'04'
OP7      EQU   X'02'
OP8      EQU   X'01'
FLAG2    DC    X'00'
OP9      EQU   X'80'
OP10     EQU   X'40'
ERR      EQU   X'02'
EOV      EQU   X'01'
FF       EQU   X'FF'
FLAG3    DC    X'00'
FLAG4    DC    X'00'
         SPACE 3
INEQC    DC    C' ********** EQUIPTMENT CHECK ON INPUT -- COPY '
         DC    C'TERMINATED '
         DC    76C'*'
INDCK    DC    C' ********** DATA CHECK ON INPUT -- COPY TERMINATED '
         DC    82C'*'
OUTEQC   DC    C' ********** EQUIPTMENT CHECK ON DD XXXXX, UNIT '
         DC    C'XXX, VOLUME XXXXXX -- THIS OUTPUT TERMINATED '
         DC    41C'*'
OUTDCK   DC    C' **********       DATA CHECK ON DD XXXXX, UNIT '
         DC    C'XXX, VOLUME XXXXXX -- THIS OUTPUT TERMINATED '
         DC    41C'*'
FILEDONE DC    C' FILE 00000 CONTAINS 0000000 DATA BLOCKS'
         DC    93C' '
SUCCESS  DC    C'0COPY SUCCESSFULLY COMPLETED FOR OUT1, OUT2, OUT3,'
         DC    C' OUT4, OUT5, OUT6                         '
         DC    41C' '
LINE     DC    133C' '
REPLY    DC    C'      '
REPLY2   DC    CL1' '
REPLY4   DC    CL4' '
REPLY8   DC    CL8' '
DECMAL   DC    F'0'
CONVT    DC    CL16'0123456789ABCDEF'
         EJECT
RBPTR    DC    X'80',AL3(RB)
RB       DC    20X'00'
TXTPTRS  DC    A(DSN,STAT,DISP,DEVT,LABL,EXPDT,DDN)
         DC    X'80',AL3(TDEN)
         SPACE 3
DSN      DC    X'0002'                INDICATE DSNAME POINTER
         DC    AL2(1)
         DC    AL2(8)                 LENGTH OF DSNAME
DSNAME   DC    CL8'MODTAPE1'          DEFAULT DSNAME
         SPACE 2
STAT     DC    X'0004'                INDICATE
         DC    AL2(1)                          DISP
         DC    AL2(1)                               =
         DC    X'04'                                  NEW
         SPACE 2
DISP     DC    X'0005'
         DC    AL2(1)
         DC    AL2(1)
         DC    X'08'                       KEEP IT WHEN DONE
         SPACE 2
DEVT     DC    X'0015'                SPECIFY UNIT TEXT
         DC    AL2(1)
UNITLEN  DC    AL2(4)                 LENGTH
UNTNAME  DC    CL8'TAPE'              UNIT NAME
         SPACE 2
LABL     DC    X'001E'                SPECIFY LABEL TEXT
         DC    AL2(1)
         DC    AL2(1)
         DC    X'10'                  INDICATE (,BLP)
         SPACE 2
EXPDT    DC    X'0022'                SPECIFY EXPIRATION DATE
         DC    AL2(1)
         DC    AL2(5)
         DC    CL5'98000'             BYPASS TMS
         SPACE 2
TDEN     DC    X'003B'                SPECIFY TAPE DENSITY
         DC    AL2(1)
         DC    AL2(1)
DCBDENS  DC    X'D3'                  DEFAULT DESITY OF 6250 BPI
         SPACE 2
DDN      DC    X'0001'                SPECIFY DDNAME TEXT
         DC    AL2(1)
         DC    AL2(4)
DDNM     DC    CL4'OUT1'              DEFAULT DDNAME
         LTORG
         PRINT NOGEN
IN       DCB   MACRF=(E),DDNAME=IN,DEVD=TA,DSORG=PS,RECFM=U
OUT1     DCB   MACRF=(E),DDNAME=OUT1,DEVD=TA,DSORG=PS,RECFM=U
OUT2     DCB   MACRF=(E),DDNAME=OUT2,DEVD=TA,DSORG=PS,RECFM=U
OUT3     DCB   MACRF=(E),DDNAME=OUT3,DEVD=TA,DSORG=PS,RECFM=U
OUT4     DCB   MACRF=(E),DDNAME=OUT4,DEVD=TA,DSORG=PS,RECFM=U
OUT5     DCB   MACRF=(E),DDNAME=OUT5,DEVD=TA,DSORG=PS,RECFM=U
OUT6     DCB   MACRF=(E),DDNAME=OUT6,DEVD=TA,DSORG=PS,RECFM=U
SYSPRINT DCB   MACRF=PM,DDNAME=SYSPRINT,DSORG=PS,RECFM=FA,LRECL=133,   X
               BLKSIZE=133
INAREA   DS    CL32768
         PRINT GEN
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         END
