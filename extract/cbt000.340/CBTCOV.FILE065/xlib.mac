XLIB     TITLE 'JOBLIB/STEPLIB FUNCTION FOR TSO'            *HMD 12/79*
         AGO   .START
         MACRO
         PCFCVT
.*                    THIS MACRO IS USED IF PCF MACROS NOT AVAILABLE
PCFCVT   DSECT
         DS    4F
PCFTMPW2 DS    A                                            *HMD 10/80*
         MEND
.START   ANOP
         EJECT                                              *HMD 01/80*
XLIB     START
*                        XLIB:  COMMAND PROCESSOR TO ALLOCATE A
*                                STEPLIB FOR USE ON TSO.
*
*                        THIS COMMAND WILL ALLOCATE THE REQUESTED
*                        LIBRARY, OPEN IT, AND THEN ATTACH THE
*                        TERMINAL MONITOR PROGRAM USING THIS OPEN
*                        LIBRARY AS A TASK LIBRARY.  AFTER CONTROL
*                        IS RETURNED FROM THE TMP, CERTAIN CONTROL
*                        BLOCK POINTERS ARE RESTORED BEFORE CLOSING
*                        AND DEALLOCATING THE LIBRARY AND TERMINATING.
*
*                        NOTES:   1. THIS COMMAND REQUIRES PCF II.
*                                 2. THIS CODE IS NOT REENTERABLE.
*                                 3. THIS COMMAND MUST BE LINKED
*                                    AS AC(1), MUST BE INVOKED FROM
*                                    AN AUTHORIZED LIBRARY, AND
*                                    MUST BE KNOWN TO THE TMP AS
*                                    AN AUTHORIZED COMMAND (IN
*                                    CSECT IKJEFTE2).
*
*                        WRITTEN BY JOHN A. NORDTVEDT   EXOKF
*                                   DEPARTMENT EXOKF
*                                   UNITED AIR LINES
*                                   P.O.BOX 66100
*                                   CHICAGO, ILLINOIS 60666
*                                   (312) 952-5410
*
*                        TO ASSEMBLE, USE :
*                        LIB('SYS1.MACLIB' 'SYS3.PP113.SOURCE').
*                        'SYS3.PP113.SOURCE' CONTAINS THE PCF MACROS:
*                                                       PCFCVT.
*
*                        WRITTEN      02/13/76
*                        LAST REVISED 07/09/78
*
*                                                           *HMD 09/82*
*  ** HISTORY LOG **                                        *HMD 09/82*
*                                                           *HMD 09/82*
*  UPDATED 09/15/82 TO PROVIDE SUPPORT FOR UZ32172          *HMD 09/82*
*  INTEGRITY PTF                                            *HMD 09/82*
*                                                           *HMD 09/82*
*        HOWARD M. DEAN                                     *HMD 09/82*
*        GTE DATA SERVICES                                  *HMD 09/82*
*        SYSTEMS SUPPORT UNIT                               *HMD 09/82*
*        4750 LINCOLN BLVD.                                 *HMD 09/82*
*        MARINA DEL REY, CA 90291                           *HMD 09/82*
*        (213) 821-0511 X330                                *HMD 09/82*
*                                                           *HMD 09/82*
               EJECT                                        *HMD 12/79*
*              REGISTER ASSIGNMENTS
REG0     EQU   0
REG1     EQU   1
REG2     EQU   2                   WORK REGISTER
REG3     EQU   3                   WORK REGISTER
REG4     EQU   4                   WORK REGISTER
REG5     EQU   5                   WORK REGISTER
PSCBBASE EQU   6                   BASE FOR PSCB
PARSBASE EQU   7                   BASE FOR PARS
CPPLBASE EQU   8                   BASE FOR CPPL
ECTBASE  EQU   9                   BASE FOR ECT
DCBBASE  EQU   10                  BASE FOR DATA CONTROL BLOCK
BASE     EQU   12                  BASE FOR MAIN PROGRAM
SAVEREG  EQU   13                  ADDRRESS OF SAVEAREA
REG14    EQU   14
REG15    EQU   15
LWAPIOWA EQU   160       LOGON WORK AREA IOWA POINTER       *HMD 09/82*
*
*
         EJECT                                              *HMD 12/79*
         SAVE  (14,12),,XLIB_&SYSDATE_COPYRIGHT_(C)_HOWARD_M_DEAN
         LR    BASE,REG15          ESTABLISH BASE
         USING XLIB,BASE             *
         LR    CPPLBASE,REG1       SAVE POINTER CPPL
         USING CPPL,CPPLBASE         *   AND ESTABLISH BASE
         GETMAIN   R,LV=WLNGTH     GET CORE FOR WORKAREA
         ST    REG1,8(SAVEREG)     SET UP SAVEAREA LINKAGE
         ST    SAVEREG,4(REG1)       *
         LR    SAVEREG,REG1          *
         TM    CPPLCBUF,X'80'      INVOKED BY BATCH JOB?    *HMD 12/79*
         BO    EXIT                YEP, EXIT                *HMD 12/79*
         USING WORKAREA,SAVEREG      *
         L     PSCBBASE,CPPLPSCB   PSCB ADDRESSABILITY
         USING PSCB,PSCBBASE         *
         CLI   PSCBUSRL,X'00'      RUN UNDER TMP IN BATCH?  *HMD 12/79*
         BE    EXIT                YUP,EXIT                 *HMD 12/79*
         TM    PSCBATR1,PSCBCTRL+PSCBACCT     IS USER OK ?
         BC    7,OKTOUSE                        *  YES
         TM    PSCBATR2,X'F0'                   *
         BNZ   ERROR8                           *  NOPE
OKTOUSE  EQU   *                                *
*
         EJECT                                              *HMD 12/79*
***********************************************************************
*
**    INITIALIZE PARSE PARAMETER LIST - PARSE STEPLIB COMMAND         *
*
***********************************************************************
*
*
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         MVC   PPLCBUF,CPPLCBUF
         LA    REG1,PPLUPT             POINT TO IT
         CALLTSSR EP=IKJPARS                                *HMD 09/82*
         B     *+4(REG15)              BRANCH ON RETURNED CODE
         B     PARSOK
         B     ERROR2
         B     EXIT                    USER ATTNED OUT
         B     ABEND1                  PROGRAM BUG
         B     ERROR3                  SPACE NOT AVAILABLE IN REGION
         B     ERROR4                  INVALID PARM
*
PARSOK   L     PARSBASE,PPLANS         CHECK ANSWER ADDRESS
         LTR   PARSBASE,PARSBASE
         BZ    ABEND1                  PARSE ERROR
         USING IKJPARMD,PARSBASE
*
         EJECT
***********************************************************************
*
*        SET UP DAIR BLOCK TO ALLOCATE FILE
*
***********************************************************************
*
         TM    DSNAM+6,X'80'           IS DSNAME THERE
         BZ    ERROR2                     *  NO
         TM    DSNAM+6,X'40'           CHECK FOR QUOTES
         BO    SKIPUID                 YES, FORGET USERID   *HMD 12/79*
ADDUSER  OI    DA08FLG1,FLG1UID        SET FLAGS IN ALLOCATE BLOCKS
*        OI    DA18FLG,FLG1UID         SET FLAGS IN DEALLOCATE BLOCKS
*        OI    DA18FLG2,FLG1UID
SKIPUID  LH    REG1,DSNAM+4            SET UP DSNAME
         STH   REG1,DDSNAML            SAVE LENGTH FOR LATER
         BCTR  REG1,0                  GET MACH. LENGTH     *HMD 09/82*
         L     REG2,DSNAM              GET ADDRESS
         EX    REG1,MOVED              MOVE IT TO BLOCK
         TM    DSNAM+14,X'80'          SET UP FOR MEMBER NAME ALSO
         BO    NOMEMBER
         TM    DSNAM+22,X'80'          TEST FOR PASSWORD
         BZ    NOPASS
         LH    REG1,DSNAM+20           LENGTH
         L     REG2,DSNAM+16           ADDRESS
         BCTR  REG1,0
         EX    REG1,MOVEP              MOVE PASSWORD
NOPASS   EQU   *
*
         EJECT                                              *HMD 12/79*
**********************************************************************
*
*              USE DAIR TO ALLOCATE  DATASET
*
**********************************************************************
*
         MVC   DAPLUPT,CPPLUPT
         MVC   DAPLECT,CPPLECT
         MVC   DAPLPSCB,CPPLPSCB
         LA    REG1,DAIRBLK
         ST    REG1,DAPLDAPB
         LA    REG1,DAPLUPT
         CALLTSSR EP=IKJDAIR                                *HMD 09/82*
         LTR   REG15,REG15
         BNZ   ERROR7
*
         EJECT                                              *HMD 12/79*
***********************************************************************
*
*                          SAVE POINTERS PRIOR TO ATTACH
*
***********************************************************************
*
         MVC   SAVPCVPT(4),PSCBU         SAVE PCFCVT ADDRESS FROM PSCB
         L     REG1,PSCBU                SAVE ENTIRE PCFCVT
         MVC   SAVEPCVT(32),0(REG1)         *
*
*************************************************************HMD 09/82*
* SAVE LOGON WORK AREA POINTER PRIOR TO ATTACH              *HMD 09/82*
* TO AVOID ERROR CAUSED BY PTF UZ32172                      *HMD 09/82*
*************************************************************HMD 09/82*
*
         USING PSA,REG0                                     *HMD 09/82*
         L     REG1,PSAAOLD               GET CURRENT ASCB  *HMD 09/82*
         USING ASCB,REG1                  ADDRESS THE ASCB  *HMD 09/82*
         L     REG1,ASCBASXB              GET ASXB POINTER  *HMD 09/82*
         USING ASXB,REG1                  ADDRESS THE ASXB  *HMD 09/82*
         L     REG1,ASXBLWA               GET LWA POINTER   *HMD 09/82*
         DROP  REG1                                         *HMD 09/82*
         ST    REG1,SAVELWA               SAVE LWA POINTER  *HMD 09/82*
         MVC   SAVEIOWA,LWAPIOWA(REG1)    SAVE THE IOWA ADDR*HMD 09/82*
*
***********************************************************************
*
*              ATTACH NEW TERMINAL MONITOR PROGRAM
*
***********************************************************************
*
         TESTAUTH FCTN=1       ARE WE AUTHORIZED VIA TMP?   *HMD 01/82*
         LTR   REG15,REG15     ARE WE?                      *HMD 01/82*
         BNZ   ERRORA          YES..SET MODE                *HMD 01/82*
MODE01   MODESET  MODE=SUP               GO TO SUP MODE TO GET SP 230
         GETMAIN  R,LV=100,SP=230        NOW GET NON FETCH PROT STORAGE
         LR    DCBBASE,REG1              SAVE ADDRESS OF NEW CORE
         LR    REG2,REG15                SAVE RETURN CODE FROM GETMAIN
         MODESET  MODE=PROB              RETURN TO NORMAL MODE
         LTR   REG2,REG2                 WAS GETMAIN SUCCESSFUL
         BNZ   ERROR3                         *  NO
         MVC   0(100,DCBBASE),TASKDCB    MOVE IN DCB FORMAT
         MVC   40(8,DCBBASE),DA08DDNM    MOVE NEW DDNAME FOR SYSUT1
         OPEN  ((DCBBASE),(INPUT))
         TM    48(DCBBASE),X'10'
         BZ    ERROR7
         TM    36(DCBBASE),X'C0'         RECFM=U?           *HMD 12/79*
         BNO   ERROR9                    NOT A LOAD LIB     *HMD 12/79*
         MVC   ONMSGDD(44),DDSNAM        MOVE DDNAME TO FIRST COMMAND
         TPUT  ONMSGST,ONMSGL            WRITE MESSAGE WE ARE IN XLIB
*                                        FOR SHARE TAPE ONLY.
         MODESET  KEY=ZERO
         LA    REG2,STAXLIST
         STAX  STAXEXIT,REPLACE=NO,MF=(E,(REG2))
         L     REG1,SAVELWA              POINT TO ASXB      *HMD 09/82*
         XC    LWAPIOWA(4,REG1),LWAPIOWA(REG1) CLEAR IOWA   *HMD 09/82*
         LA    REG1,LISTPARM
         ATTACH EP=IKJEFT01,TASKLIB=(DCBBASE),ECB=MYECB1,PURGE=HALT,   X
               STAI=(STAIEXIT,(PSCBBASE)),SZERO=NO,RSAPF=YES,          X
               KEY=PROP
         LTR   REG15,REG15                  WAS ATTACH SUCESSFUL
         BNZ   ERROR5                          *
L2       ST    1,SUBTASK
L3       WAIT  1,ECBLIST=ECBLIST
*
         EJECT                                              *HMD 12/79*
***********************************************************************
*                      DETACH AND RESTORE
*
***********************************************************************
*
L1       DETACH SUBTASK
*
         STAX
         L     REG2,CPPLPSCB             LOAD ADDRESS OF PSCB
         L     REG2,48(REG2)             LOAD ADDRESS OF RLGB
         MVC   256(4,REG2),CPPLECT       RESTORE ECT ADDRESS
         L     REG1,SAVPCVPT             RESTORE PCFCVT ADDR IN PSCB
         ST    REG1,PSCBU                    *
         MVC   0(32,REG1),SAVEPCVT       RESTORE ENTIRE PCFCVT
         L     REG1,SAVELWA              LOAD ASCB PTR      *HMD 09/82*
         MVC   LWAPIOWA(4,REG1),SAVEIOWA RESTORE IOWA PTR   *HMD 09/82*
RESETKEY MODESET  MODE=SUP               GO TO SUP MODE TO FREE SP
         MODESET  KEY=NZERO
*
         EJECT                                              *HMD 12/79*
*********************************************************************
*
*                       CLOSE AND DEALLOCATE LIBRARY
*
*********************************************************************
*
         CLOSE ((DCBBASE))
         FREEMAIN  R,SP=230
         MODESET  MODE=PROB              RETURN TO NORMAL MODE
         LA    REG2,DA18CD               PLACE DAPB ADDR IN DAPL
         ST    REG2,DAPLDAPB                *
         MVC   DA18DDN(8),DA08DDNM
         LA    REG1,DAPLUPT   POINT TO DAIR PARM LIST
         CALLTSSR EP=IKJDAIR
         TPUT  ENDMSG,L'ENDMSG                              *HMD 12/79*
         EJECT                                              *HMD 12/79*
*
*                        EXIT
*
EXIT     DS    0H                  EXIT ROUTINE             *HMD 12/79*
         DS    0H                                           *HMD 12/79*
         LR    REG1,SAVEREG        LOAD ADDRESS OF WORK AREA
         L     SAVEREG,SAVEAREA+4  RESTORE SAVEAREA POINTER
         FREEMAIN R,LV=WLNGTH,A=(1)
         RETURN   (14,12),T,RC=0
         EJECT                                              *HMD 12/79*
*
*
***********************************************************************
*
*        DAIR PARAMETERS
*
***********************************************************************
*
PARMLIST IKJPARM
DSNAM    IKJPOSIT DSNAME,PROMPT='LOAD LIBRARY NAME'
         IKJENDP
         EJECT                                              *HMD 12/79*
ECBLIST  DC    A(MYECB1)
         DC    XL1'80'
         DC    AL3(MYECB2)
MYECB1   DC    F'0'
MYECB2   DC    F'0'
STAIECB  DC    A(MYECB2)
LISTPARM DC    XL1'80'                   PARAMETER LIST FOR TMP
         DC    AL3(ONMSG)                   *
ONMSG    DC    AL2(0)                    PLACE ZERO LENGTH HERE AND
*                                        ADD ONMSG TPUT IF UAPR OR
*                                        EQUIVALENT IS NOT AVAILABLE.
ONMSGST  DC    C'YOUR STEPLIB IS NOW '
ONMSGDD  DC    CL44' '
ONMSGL   EQU   *-ONMSGST                                    *HMD 12/79*
         EJECT                                              *HMD 12/79*
TASKDCB  DCB    DSORG=PO,DDNAME=XXXXXXXX,MACRF=(R)
         EJECT                                              *HMD 12/79*
*
***********************************************************************
*
*              EXIT ROUTINES
*
***********************************************************************
*
STAXEXIT LR    REG4,REG14
         STATUS START
         LR    REG14,REG4
         BR    14
*
STAIEXIT STM   14,12,12(13)              SAVE REGISTERS
         USING STAIEXIT,REG15
         LR    REG4,REG1                 SAVE REGISTER ONE
         LA    REG3,12                   WAS "SDWA" PROVIDED
         CR    REG0,REG3                    *
         BNE   STAI01                       *    YES
         TPUT  STAIMSG,L'STAIMSG            *    NO, TELL THE WORLD
         ABEND 711,DUMP                           *
STAIMSG  DC    C' STAIEXIT ERROR '
STAIMSG2 DC    C' STAIEXIT ENTERED '
STAI01   TPUT  STAIMSG2,L'STAIMSG2
         LR    REG1,REG4                 RESTORE REGISTER ONE
         L     REG2,0(REG1)              LOAD ADDR OF PSCB FROM SDWA
         L     PSCBBASE,0(REG2)              *
         L     REG3,PSCBU                LOAD ADDR OF PCFCVT
         USING PCFCVT,REG3
         L     REG4,PCFTMPW2              LOAD ADDR OF TMP WORK AREA
         L     REG5,232(REG4)            LOAD ADDR OF RESTART TMP WORK
         ST    REG5,0(REG2)              STORE THIS ADDR IN SDWA
         LM    14,12,12(13)              RESTORE REGISTERS
         XCTL  EP=IKJEFT04               GO TO TMP RETRY ROUTINE
         DROP  REG15,REG3
*
STAXLIST STAX  STAXEXIT,OBUF=(ATTNMSG,L'ATTNMSG),REPLACE=NO,MF=L
*
         CNOP  0,4
*
         EJECT
*
***********************************************************************
*
*        SOME EXECUTED INSTRUCTIONS
*
***********************************************************************
*
MOVED   MVC   DDSNAM(0),0(2)
MOVEP   MVC   DA08PSWD(0),0(2)
*
*
        EJECT                                               *HMD 12/79*
***********************************************************************
*
*        PARSE PARAMETER LIST
*
***********************************************************************
*
PPLUPT   DC    A(0)
PPLECT   DC    A(0)
PPLECB   DC    A(PPLECBS)
PPLPCL   DC    A(PARMLIST)
         DC    A(PPLANS)
PPLCBUF  DC    A(0)
PPLUWA   DC    A(0)
PPLECBS  DC    A(0)
PPLANS   DC    A(0)  ANSWER PLACE
*
         EJECT                                              *HMD 12/79*
***********************************************************************
*
*        DAIR ALLOCATE BY DSNAME BLOCK
*
***********************************************************************
*
DAIRBLK  DS    0F
DA08CD   DC    X'0008'
DA08FLG  DC    H'0'
DA08DARC DC    H'0'
DA08CTRC DC    H'0'
DA08DSN  DC    A(DDSNAML)
DA08DDNM DC    CL8' '  LET DAIR FILL IN DDNAME
         DC    2CL8' '
         DC    4F'0'
DA08MNAM DC    CL8' '
DA08PSWD DC    CL8' '
         DC    X'080808' SHR,KEEP,KEEP
DA08FLG1 DC    X'00'
FLG1UID  EQU   X'20'  OR IN TO CONCAT USERID
         DC    3X'00'
DA08FLG2 DC    XL1'00'
DA08ALN  DC    CL8'        '
         EJECT                                              *HMD 12/79*
*
*
***********************************************************************
*
*              DAIR PARAMETER LIST
*
***********************************************************************
*
DAPLUPT  DC    A(0)
DAPLECT  DC    A(0)
DAPLECB  DC    A(DAPLECBS)
DAPLPSCB DC    A(0)
DAPLDAPB DC    A(DAIRBLK)
DAPLECBS DC    A(0)
         EJECT                                              *HMD 12/79*
*
***********************************************************************
*
*        DAIR DEALLOCATE BY DSNAME BLOCK
*
***********************************************************************
*
         DS    0F
DA18CD   DC    X'0018'
DA18FLG  DC    H'0'
DA18DARC DC    H'0'
DA18CTRC DC    H'0'
*A18DSN  DC    A(DDSNAML)
DA18DSN  DC    F'0'
DA18DDN  DC    CL8'        '
DA18MEM  DC    CL8' '
         DC    CL2' '
         DC    X'08'
DA18FLG2 DC    X'10'
         DC    CL8' '
*
DDSNAML  DC    H'0'                LENGTH OF DSNAME
DDSNAM   DC    CL44' '             DSNAME
         EJECT                                              *HMD 12/79*
***********************************************************************
*
*              ERROR MESSAGES
*
***********************************************************************
*
MSG2     DC    C'INCOMPLETE PARAMETERS'
MSG3     DC    C'NOT ENOUGH SPACE IN REGION'
MSG4     DC    C'INVALID PARAMETERS'
MSG5     DC    C'ATTACH ERROR CODE XXXX'
MSG6     DC    C'MEMBER NAME NOT ALLOWED'
MSG7     DC    C'UNABLE TO ALLOCATE OR OPEN DATASET'
MSG8     DC    C'COMMAND XXXXXXXX IS NOT AUTHORIZED FOR XXXXXXX '
MSG9     DC    C'DATA SET IS NOT A LOAD LIBRARY DUMMY'      *HMD 12/79*
ATTNMSG  DC    C'ATTENTION IGNORED'
ENDMSG   DC    C'STEPLIB FUNCTION ENDED '                   *HMD 12/79*
AUTHMSG  DC    C'XXXXXXXX MUST BE APF AUTHORIZED'           *HMD 09/82*
*
ERROR2   TPUT  MSG2,L'MSG2
         B     EXIT
ERROR3   TPUT  MSG3,L'MSG3
         B     EXIT
ERROR4   TPUT  MSG4,L'MSG4
         B     EXIT
ERROR5   CVB   REG15,PACKED
         UNPK  MSG6+18(4),PACKED
         OI    MSG6+21,X'F0'
         TPUT  MSG5,L'MSG5
         B     RESETKEY
NOMEMBER TPUT  MSG6,L'MSG6
         B     EXIT
ERROR7   TPUT  MSG7,L'MSG7
         B     EXIT
ERROR8   L     ECTBASE,CPPLECT               FIND COMMAND NAME
         USING ECT,ECTBASE                      *    IN ECT
         L     PSCBBASE,CPPLPSCB                            *HMD 12/79*
         USING PSCB,PSCBBASE             ADDRESS THE PSCB   *HMD 12/79*
         MVC   MSG8+39(7),PSCBUSER       MOVE IN USERID     *HMD 12/79*
         MVC   MSG8+8(8),ECTPCMD                *
         TPUT  MSG8,L'MSG8               WRITE MESSAGE
         B     EXIT
ERROR9   TPUT  MSG9,L'MSG9               TELL THE DUMMY     *HMD 12/79*
         B     EXIT                                         *HMD 12/79*
ERRORA   L     ECTBASE,CPPLECT               FIND COMMAND NAME
         USING ECT,ECTBASE                      *    IN ECT
         MVC   AUTHMSG(8),ECTPCMD               *
         TPUT  AUTHMSG,L'AUTHMSG         WRITE MESSAGE
         B     EXIT
ABEND1   ABEND 1004,DUMP
         B     EXIT
         EJECT                                              *HMD 12/79*
         LTORG
         EJECT                                              *HMD 12/79*
*
WORKAREA DSECT
SAVEAREA DS    18F
PACKED   DS    PL8
MISC     DS    XL3                       EXTRA FLAGS LATER  *HMD 12/79*
SUBTASK  DS    F
SAVPCVPT DS    F                         SAVE AREA FOR PCFCVT POINTER
SAVEPCVT DS    8F                        SAVE AREA FOR ENTIRE PCFCVT
SAVELWA  DS    F                                            *HMD 09/82*
SAVEIOWA DS    F                                            *HMD 09/82*
WLNGTH   EQU   *-WORKAREA
*
         EJECT                                              *HMD 12/79*
         IKJCPPL
         EJECT                                              *HMD 12/79*
         IKJPSCB
         EJECT                                              *HMD 12/79*
         IKJECT
         EJECT                                              *HMD 12/79*
         PCFCVT
         EJECT                                              *HMD 09/82*
         IHAASCB                                            *HMD 09/82*
         EJECT                                              *HMD 09/82*
         IHAASXB                                            *HMD 09/82*
         EJECT                                              *HMD 09/82*
         IHAPSA ,                                           *HMD 09/82*
         EJECT                                              *HMD 09/82*
         CVT   LIST=YES,DSECT=YES                           *HMD 09/82*
         END
