RACFGRES START 0
***********************************************************************
* THE PURPOSE OF THIS PROGRAM IS TO EXTRACT INFORMATION FROM ALL      *
* GENERAL RESOURCE PROFILES IN A RACF 1.7 DATASET. THE EXTRACTED DATA *
* IS PUT INTO A FILE WITH RECORDS FORMATTED AS FOLLOWS:               *
*                                                                     *
*           COLUMNS   1-8: RESOURCE NAME                              *
*           COLUMNS  9-16: RESOURCE CLASS (SEE NOTE BELOW)            *
*           COLUMNS 17-24: OWNER                                      *
*           COLUMN     25: UACC                                       *
*           COLUMN  26-33: AUTHORIZED USER                            *
*           COLUMN     34: AUTHORIZED USER'S ACCESS AUTHORITY         *
*                                                                     *
* THIS PROGRAM MAY PROCESS ONLY ONE RESOURCE CLASS AT A TIME, WHICH   *
* IS SPECIFIED IN A PARAMETER PASSED TO THE PROGRAM. THE RESOURCE     *
* CLASS NAME PASSED AS A PARAMETER SHOULD BE AT LEAST 4 BYTES LONG.   *
*                                                                     *
* NOTE: THIS PROGRAM MUST BE LINKEDITED INTO AND RUN FROM AN APF      *
*       AUTHORIZED LIBRARY.                                           *
*                                                                     *
* LAST UPDATE: 3/11/88 BY SCOTT WILSON (REMOVED UNNECESSARY MODESETS) *
***********************************************************************
R0       EQU   0                       USED BY SYS. MACROS; PARM LENGTH
R1       EQU   1                       USED BY SYS. MACROS; PARM ADDR.
R2       EQU   2                       1ST BASE REGISTER FOR WORKSPACE
R3       EQU   3                       USER COUNT
R4       EQU   4                       MULTIPLIER REGISTER
R5       EQU   5                       2ND BASE REGISTER FOR WORKSPACE
R6       EQU   6                       DUMMY REGISTER FOR MVCL;
*                                      3RD BASE REGISTER FOR WORKSPACE
R7       EQU   7                       ZERO INDICATOR FOR MVCL
R8       EQU   8                       ADDRESS OF WORKAREA
R9       EQU   9                       LENGTH OF WORKAREA
R10      EQU   10                      ADDRESS OF END OF WORKAREA
R11      EQU   11                      LENGTH OF RESOURCE NAME
R12      EQU   12                      PROGRAM BASE REGISTER
R13      EQU   13                      ADDRESS OF MVS SAVEAREA
R14      EQU   14                      RETURN ADDRESS
R15      EQU   15                      COMPLETION CODE
*
***********************************************************************
* START OF JOB HOUSEKEEPING                                           *
***********************************************************************
         STM   R14,R12,12(R13)         STORE MVS REGISTERS
         BALR  R12,0                   ESTABLISH R12 AS BASE REGISTER
         USING *,R12                   IDENTIFY BASE REGISTER
         ST    R13,SAVEAREA+4          STORE ADD. OF MVS REG. SAVEAREA
         LA    R13,SAVEAREA            LOAD ADDRESS OF PGM. SAVEAREA
*
***********************************************************************
* RETRIEVE PASSED PARAMETER AND STORE FOR ICHEINTY MACRO              *
***********************************************************************
         USING FULLWORD,R1             SET ADDRESSABILITY TO R1 VALUE
         L     R1,0(R1)                LOAD ADDRESS OF PARM DATA AREA
MOVEPARM MVC   GRESCLAS(8),2(R1)       MOVE PARM INTO CLASS FIELD
*
***********************************************************************
* SET ADDRESSABILITY TO A 32K WORKSPACE (MAXIMUM RACF PROFILE SIZE)   *
***********************************************************************
WORKADDR EQU   *
         DROP  R1
         LA    R2,WORKSPCE             LOAD ADDRESS OF WORKSPACE
         USING WORKSPCE,R2             SET ADDRESSABILITY TO WORKSPACE
*
***********************************************************************
* OPEN OUTPUT FILES                                                   *
***********************************************************************
         OPEN  (GRESFILE,(OUTPUT))     OPEN OUTPUT FILE
*
***********************************************************************
* SET THE LENGTH OF THE WORKAREA AND PROFILE ENTRY                    *
***********************************************************************
INITPROF MVI   PROFILE,X'01'           INITIAL PROFILE LENGTH = 1 BYTE
         MVC   WRKLNGTH(4),=XL4'00008000'  PUT WORKAREA LENGTH WHERE...
*                                          ...RACF CAN FIND IT
***********************************************************************
* GET A RACF PROFILE                                                  *
***********************************************************************
CLERWORK EQU   *
         L     R7,=XL4'00000000'       INDICATE CLEAR FUNCTION TO MVCL
         LA    R8,WORKDATA             LOAD ADDRESS OF WORK AREA
         L     R9,=XL4'00007FE4'       LOAD LENGTH OF DATA IN WORKAREA
         MVCL  R8,R6                   ZERO OUT THE WORK AREA
***********************************************************************
* THIS PROGRAM RUNS IN TWO "PHASES"; FIRST SCANNING FOR DISCRETE      *
* PROFILES, AND THEN FOR GENERIC PROFILES. THESE ARE DONE SEPARATELY  *
* BECAUSE DIFFERENT, NON-SUBSTITUTABLE PARAMETERS OF THE ICHEINTY     *
* MACRO ARE NECESSARY FOR EACH OF THE TWO TYPES OF PROFILES. A FLAG   *
* (MACTYPE) DETERMINES THE MACRO TO BE USED, AND THEREFORE THE        *
* CURRENT PHASE OF THE PROGRAM.                                       *
***********************************************************************
         CLC   MACTYPE(8),=CL8'DISCRETE'    ICHEINTY FOR DISCRETE?
         BNE   GENMAC                       IF NOT, EXECUTE FOR GENERIC
DISMAC   ICHEINTY NEXT,MF=(E,DISCRETE)      FIND NEXT DISCRETE PROFILE
         B     CHECKRC                      CHECK ICHEINTY RETURN CODE
GENMAC   ICHEINTY NEXT,MF=(E,GENERIC)       FIND NEXT GENERIC PROFILE
CHECKRC  LTR   R15,R15                 CHECK RETURN CODE FROM ICHEINTY
         BNZ   WRAPUP                  IF NON-ZERO, EXIT THIS PROGRAM
*
***********************************************************************
* WRITE THE RETURNED DATA                                             *
***********************************************************************
         MVI   OUTRECRD,C' '              PROPOGATE BLANK SPACE TO...
         MVC   OUTRECRD+1(36),OUTRECRD    ...OUTPUT RECORD IN TWO STEPS
         LA    R10,WORKDATA            LOAD ADDRESS OF RETURNED DATA
         A     R10,DATALNTH            ADD LENGTH OF RETURNED DATA
         L     R11,GRESLNTH            LOAD LENGTH OF GRES-ID
         S     R11,=XL4'00000001'      SUBTRACT 1 FROM GRES-ID LENGTH
         EX    R11,MOVEGRES            ALTER LENGTH OF GRES-ID FOR MVC
         B     MOVECLAS                DON'T EXECUTE MVC TWICE
MOVEGRES MVC   OUTGRES(0),ENTRYNAM     MOVE RESOURCE INTO OUTPUT FIELD
MOVECLAS MVC   OUTCLASS(8),GRESCLAS    MOVE CLASS INTO OUTPUT FIELD
         MVC   OUTOWNER(8),WORKDATA+2  MOVE OWNER INTO OUTPUT FIELD
         MVC   OUTUACC(1),WORKDATA+12  MOVE UACC TO OUTPUT FIELD
         LH    R3,WORKDATA+15          LOAD NUMBER OF USER-IDS
         SLL   R3,16                   TRUNCATE SIGN FIELD
         SRL   R3,16                   RIGHT JUSTIFY NUMBER OF USERS
         LA    R6,WORKDATA+19          LOAD ADDRESS OF FIRST USER-ID
         USING USER$ID,R6              SET ADDRESSABILITY TO USER-ID
         SR    R4,R4                   CLEAR OUT REGISTER 4
         C     R3,=XL4'00000000'       ARE THERE AUTHORIZED USERS?
         BNZ   FINDACES                IF SO, LOCATE ACCESS LIST
         L     R3,=XL4'00000001'       IF NOT, WRITE INDICATIVE...
         MVC   OUTUSER(8),=CL8'* NONE *'  ...RECORD CONTAINING...
         MVI   OUTACESS,X'00'             ...THIS INFO
         B     WRITEIT                 WRITE THE OUTPUT RECORD
FINDACES LR    R5,R3                   LOAD # OF USERIDS
         M     R4,=XL4'00000008'       MULTIPLY BY USER-ID LENGTH (8)
         A     R5,=XL4'00000002'       ADD OFFSET FOR ACCESS PREFIX
         AR    R5,R6                   ADD ADDRESS OF FIRST USER-ID
         USING ACCESS,R5               SET ADDRESSABILITY TO ACCESS
NEXTUSER EQU   *
*        CR    R6,R10                  ARE WE PAST THE END OF THE DATA?
*        BP    CLERWORK                IF SO, GET ANOTHER PROFILE
MOVEUSER EQU   *
         MVC   OUTUSER(8),0(R6)        MOVE USER-ID TO OUTPUT FIELD
*        CR    R5,R10                  ARE WE PAST THE END OF THE DATA?
*        BP    CLERWORK                IF SO, GET ANOTHER PROFILE
         MVC   OUTACESS(1),0(R5)       MOVE ACCESS TO OUTPUT FIELD
WRITEIT  PUT   GRESFILE,OUTRECRD       WRITE THE OUTPUT RECORD
         A     R6,=XL4'00000008'       POINT TO NEXT USER-ID
         USING USER$ID,R6              SET ADDRESSABILITY TO USER-ID
         A     R5,=XL4'00000001'       POINT TO NEXT USER ACCESS
         USING ACCESS,R5               SET ADDRESSABILITY TO ACCESS
         S     R3,=XL4'00000001'       DECREMENT NUMBER OF USERIDS
         C     R3,=XL4'00000000'       ANY MORE REMAINING USER-IDS?
         BZ    CLERWORK                IF NOT, GET NEXT PROFILE
         B     NEXTUSER                GET THE NEXT USER-ID
*
***********************************************************************
* END OF JOB HOUSEKEEPING                                             *
***********************************************************************
WRAPUP   EQU   *
         CLC   MACTYPE(8),=CL8'DISCRETE'     BEEN PROCESSING DISCRETES?
         BNE   CLOSING                       IF NOT, EXIT PROGRAM
         MVC   GRESLNTH(12),=XL12'000000000000000000000000'
         MVC   MACTYPE(8),=CL8'GENERIC '     INDICATE GENERIC ICHEINTY
         B     INITPROF                      BEGIN SEARCH FOR GENERICS
CLOSING  CLOSE (GRESFILE)              CLOSE THE OUTPUT FILE
         DROP  R2,R5,R6                RELEASE BASE REGISTERS
         L     R13,SAVEAREA+4          LOAD RETURN ADDRESS (MVS R13)
         L     R14,12(R13)             LOAD MVS R14 VALUE
         LM    R0,R12,20(R13)          RESTORE MVS REGISTERS 0-12
         SR    R15,R15                 GUARANTEE ZERO RETURN CODE
         BR    R14                     RETURN TO MVS
***********************************************************************
* STORAGE USED BY PROGRAM                                             *
***********************************************************************
SAVEAREA DS    18F                     SAVE AREA FOR SYSTEM REGISTERS
*
GENERIC  ICHEINTY NEXT,ENTRY=PROFILE,WKAREA=WORKAREA,MF=L,TYPE='GEN',  +
               ACTIONS=(GETOWNR,GETUACC,GETCNT,GETUSER,GETUACS),       +
               CLASS=GRESCLAS,GENERIC=UNCOND
*
DISCRETE ICHEINTY NEXT,ENTRY=PROFILE,WKAREA=WORKAREA,MF=L,TYPE='GEN',  +
               ACTIONS=(GETOWNR,GETUACC,GETCNT,GETUSER,GETUACS),       +
               CLASS=GRESCLAS,GENERIC=NO
*
MACTYPE  DC    CL8'DISCRETE'           ICHEINTY MACRO TYPE FLAG
*
GRESCLAS DC    CL8'        '           CLASS NAME USED BY ICHEINTY
*
GRESLNTH DC    XL3'000000'             PADDING FOR REGISTER LOADS
PROFILE  DS    AL1                     ENTRY LENGTH FIELD
ENTRYNAM DC    XL8'0000000000000000'   GENERAL RESOURCE NAME
*
GETCNT   ICHEACTN FIELD=ACLCNT,MF=L    RETREIVE # OF AUTHORIZED USERS
GETOWNR  ICHEACTN FIELD=OWNER,MF=L     RETREIVE PROFILE OWNER
GETUACC  ICHEACTN FIELD=UACC,MF=L      RETREIVE UACC
GETUSER  ICHEACTN FIELD=USERID,MF=L    RETREIVE AUTHORIZED USER
GETUACS  ICHEACTN FIELD=USERACS,MF=L   RETREIVE ACCESS AUTHORITY
*
ACLCNT   DC    CL8'ACLCNT  '           # OF AUTHORIZED USERS
OWNER    DC    CL8'OWNER   '           OWNER
UACC     DC    CL8'UACC    '           UACC
USERID   DC    CL8'USERID  '           AUTHORIZED USER-ID OR GROUP
USERACS  DC    CL8'USERACS '           AUTHORIZED USER ACCESS AUTHORITY
*                  *
OUTRECRD DS    0XL34                   OUTPUT RECORDS
OUTGRES  DS    XL8                     GENERAL RESOURCE NAME
OUTCLASS DS    XL8                     RESOURCE CLASS
OUTOWNER DS    XL8                     OWNER
OUTUACC  DS    XL1                     UACC
OUTUSER  DS    XL8                     AUTHORIZED USER-ID OR GROUP
OUTACESS DS    XL1                     AUTHORIZED USER ACCESS AUTHORITY
*
***********************************************************************
* OUTPUT FILE DEFINITION                                              *
***********************************************************************
GRESFILE DCB   DDNAME=GRESFILE,MACRF=PM,DSORG=PS
*
         LTORG
*
WORKSPCE EQU   *
WORKAREA DS    0F                      WORKAREA ALIGNMENT ON FULLWORD
WRKLNGTH DS    F                       WORK AREA LENGTH
RBA      DS    CL6                     RBA OF RETURNED ENTRY
         DS    CL2                     RESERVED (BY IBM)
         DS    F                       DUPLICATE DATASET NAME COUNT
         DS    2F                      RESERVED (BY IBM)
DATALNTH DS    F                       LENGTH OF RETURNED DATA
WORKDATA DS    CL32740                 32K MAXIMUM RETURNED DATA
ENDWORK  EQU   *                       END OF WORKAREA
*
ACCESS   DSECT
USRACESS DS    XL1
*
USER$ID  DSECT
USER     DS    CL8
*
FULLWORD DSECT
PARMADDR DS    F
PARM     DS    CL8
*
         END
