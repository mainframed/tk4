./       ADD   NAME=PLAYBACK
PLAYBACK CSECT
         SPACE
*   COPYRIGHT (C) 1980 BY SAS INSTITUTE INC., CARY NC
*
*   NAME:      PLAYBACK
*   SUPPORT:   CRAIGE HALES
*   PRODUCT:   GRAPH
*   TYPE:      PROC
*   ATTRIB:    NORENT
*   LANGUAGE:  ASM
*   PURPOSE:   PLAYBACK A FT14F001 FILE
*   USAGE:
*
*   HISTORY:
*   FUTURE:
*   MACROS:
*   AREAS:
*   EXTREF:
*   REF:
*   NOTES: FT14F001 WAS WRITTEN WITH THE OUTPUT FOR A
*          CONTINUOUS MODE DEVICE
*   KEYWORDS:
*   END
         SPACE
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         ST    13,SAVE+4
         LA    13,SAVE
         OPEN  (FT14F001,INPUT)
LOOP     GET   FT14F001
         LH    0,0(1)
         S     0,FOUR
         C     0,=F'1'              DATA LENGTH=1?
         BH    TPUT                 NO, PUT IT OUT
         CLI   4(1),C' '            ONLY ONE BLANK?
         BNE   TPUT                 NO, PUT IT OUT
         LA    0,16                 MUST BE FILE SEPARATOR,
         LA    1,BUFFER             SO GET A LINE FROM TERMINAL.
         O     1,TGET
         SVC   93
         B     LOOP                 THEN PROCESS NEXT RECORD.
TPUT     LA    1,4(1)               POINT TO DATA AND DO
         O     1,CNTL               CONTROL TPUT.
         SVC   93
         B     LOOP                 THEN PROCESS NEXT RECORD
EOD      CLOSE (FT14F001)
         FREEPOOL FT14F001
         L     13,SAVE+4
         LM    14,12,12(13)
         SR    15,15
         BR    14
FT14F001 DCB   DSORG=PS,RECFM=VB,LRECL=256,MACRF=GL,DDNAME=FT14F001,   +
               EODAD=EOD
FOUR     DC    F'4'
TGET     DC    X'80000000'
CNTL     DC    X'02000000'
SAVE     DS    10D
BUFFER   DC    CL16'****************'
         END   PLAYBACK
