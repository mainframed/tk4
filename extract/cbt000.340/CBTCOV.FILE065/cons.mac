CONSOLE  TITLE 'C O N S O L E - OPERATOR CONSOLE SUPPORT FOR DCMS '
         LCLA  &APFSVC                  NBR OF SVC TO SET APF AUTH U026
         LCLB  &SP3                     GENERATE SP1.3 SUPPORT     U023
         SPACE 2
&APFSVC  SETA  109                      SPFCOPY MAGIC SVC          U027
*                                       SET TO 0 IF NONE           U026
         SPACE 1
&SP3     SETB  1                        SET TO 0 IF SP3 MACROS     U023
*                                       ARE NOT AVAILABLE          U023
         SPACE 3
*.
* CONSOLE
*
* THIS PROGRAM WILL DO CONSOLE SIMULATION FOR A DCMS TERMINAL.
* IT GOES OUT AND FINDS A GRAPHICS (3277) ACTIVE CONSOLE AND USES
* THAT CONSOLES SCREEN IMAGE FOR OUTPUT AND UCM-ID FOR SVC 34.
* THIS PROGRAM REQUIRES AUTHORIZATION TO USE THE SVC 34 INTERFACE.
*
* TO USE IT SIMPLY SAY.. CONSOLE
*
* ERROR MESSAGES -
*  NO ACCEPTABLE (3277) CONSOLES FOUND
*
*
* A NULL INPUT CAUSES THE SCREEN TO BE UPDATED. (SO YOU CAN
* USE IT UN-AUTHORIZED SO LONG AS YOU DONT TYPE ANYTHING IN).
*  *** AS OF 09/16/80, THIS COMMAND ALWAYS REQUIRES AUTHORIZATION.  ***
*
* COMMANDS ARE NOT BROADCAST TO THE CONSOLE BEING USED SO YOU CAN
* HAVE SOME FUN WITH YOUR OPERATORS. THE COMMAND ENTERED IS PLACED
* ON THE LINE BELOW THE INPUT LINE.
*  *** AS OF 10/23/80, THE ABOVE IS NOT QUITE TRUE.  READ THE CODE. ***
*
*
* IF YOU HAVE ANY QUESTIONS PLEASE CALL:
*
*     FRED LUDDY
*     AMDAHL CORP.
*     1250 E ARQUES
*     SUNNYVALE CA.
*  PHONE:
*     (408) - 735 - 4011
*
*
*
*  CURRENT RESPONSIBLE PROGRAMMERS:
*
*  LDW = LEONARD D. WOREN
*        HUGHES AIRCRAFT COMPANY
*        BLDG S50 / MS X303
*        P.O. BOX 92919
*        LOS ANGELES, CA  90009
*        (213)  615-7993
*
*
*  SDM = STEVEN D. MCGINTY
*        (NO ADDRESS AVAILABLE)
*
*
*
*   UPDATES:
*      03/14/84 U029 LDW  USE TABLE LOOKUP FOR CONSOLE DESCRIPTION
*                            (INSTEAD OF INDEXING INTO THE TABLE)
*                         FIX BUG IN CONVERTING OPERAND OF 'S' COMMAND
*      12/29/83 U028 SDM  ADD 3278-4 SUPPORT (39 LINE SCREEN)
*                         INTERPRET CONS PARM AS COMMAND
*                         ADD 'S' COMMAND TO SELECT CONSOLE (LIKE PF9)
*                         DELETE OLD COMMENTED OUT CODE
*      05/10/83 U027 LDW  FIX FOR NON-SP3 (AGAIN)
*      01/18/83 U026 LDW  ADD USE OF SPECIAL APF SET SVC
*      01/04/83 U025 SDM  MAKE IT WORK AGAIN IN NON-SP3 ENVIRONMENT
*      12/06/82 U024 LDW  REQUIRE "/" IN FRONT OF MVS COMMANDS
*                         DELETE SOME OLD COMMENTED OUT CODE
*      11/24/82 U023 LDW  INSTALL SUPPORT FOR SP1.3 (THIS CODE ADAPTED
*                            FROM HOWARD DEAN'S MODIFIED "SPY")
*                         ONLY RUN KEY ZERO WHEN ACTUALLY NECESSARY
*                         RECOGNIZE MORE DEVICE TYPES IN "LISTCONS"
*                         ADD CONSOLE LOCATION TABLE
*                         CHANGE MESSAGES TO LOWER CASE
*      03/30/82 U022 LDW  CORRECT DISPLAY OF CONSOLE CMDAUTH
*      03/22/82 U021 LDW  MOVE LABEL "MENUBIG" TO MAKE LONG CANNED
*                            COMMANDS WORK AGAIN
*                         MINOR CHANGES TO CANNED COMMANDS LIST
*      03/21/82 U020 LDW  FIX U019 TO WORK ON NON-SP SYSTEM
*                         USING -> USNGX, DROP -> DROPX, INUSE
*                         MOVE TESTAUTH TO FIX "CONS ?" PROBLEM
*                         IMPROVE "D C" DEVICE TYPE DISPLAY
*                         CHANGE "CURSOR=AFTER" TO "CURSOR=YES"
*      03/18/82 U019 LDW  DISPLAY 20 LINES INSTEAD OF 19 FOR SP1.1
*      12/03/81 U018 LDW  ADD "DISPLAY CONSOLES" FUNCTION ("?" ON PF9)
*                         MINOR CHANGES TO CANNED COMMANDS
*      10/07/81 U017 LDW  ADD FLIP-FLOP LOGIC TO AUTO UPDATE IF 3066
*                         INSTALL TESTAUTH TO PREVENT SPF-6 SYSUDUMPS
*      08/03/81 U016 SDM  ALLOW PARM TO SPECIFY CONSOLE ADDRESS
*                         CHANGE INPUT LINE RESETING LOGIC
*                         ALLOW CONCURRENT COMMAND AND SWITCH TO MENU
*                         FIX 'OPERATOR NOT AUTHORIZED' DISPLAY BUG
*                         DISALLOW QUIESCE COMMAND
*      06/17/81 U015 LDW  FIX U014 UPDATE
*      06/16/81 U014 LDW  ADD SUPPORT FOR 3066 (168 CONSOLE)
*      03/25/81 U013 SDM  REMOVE TCLEARQ INPUT (VTAM WORKS AGAIN)
*                         SUPPRESS DISPLAY OF REPLY TEXT
*                         FIX MINOR BUGS IN CANNED COMMANDS
*      10/23/80 U012 LDW  WTO USERID & COMMAND ISSUED
*                         MAKE PROGRAM RE-ENTRANT
*                         IGNORE UNDEFINED PF KEYS
*      09/16/80 U011 LDW  MODESET KEY=ZERO FOR AFTER PUT7908
*      06/02/80 U010 LDW  MAKE PF5 TO PF5 WORK
*      05/27/80 U009 LDW  CANNED COMMANDS CAPABILITY (PF5)
*                         RE-WRITE ERROR MESSAGE LOGIC
*                         MOVE "TESTAUTH" OUT OF LOOP
*      05/22/80 U008 LDW  CHANGE UPDATE KEY FROM PF1 TO PF2
*                         MAKE PF1 "HELP" TO CONFORM TO SPF
*                         ADD CONSOLE SELECT CAPABILITY TO PF9
*                         MISCELLANEOUS CLEANUP
*      03/14/80 U007 LDW  ADD REPROMPT (PF6) FEATURE
*      02/08/80 U006 LDW  FIX "DCMSWARN" MSG & DISPLAY OF LAST CMD
*                         DEFINE PF3 AS "END" TO CONFORM TO SPF
*                         DISPLAY ACTION INDICATORS AT START OF LINE
*                         DISPLAY ONLY ACTION MSGS IN HIGH INTENSITY
*      02/04/80 U005 LDW  "TCLEARQ INPUT" TO GET AROUND VTAM RESHOW BUG
*      11/30/79 U004 SDM  ELIMINATE 'SYSTEM PROGRAMMER' RESTRICTION
*      11/30/79 U003 LDW  FIX LENGTH OF "CMND" PASSED TO SVC 34
*      11/12/79 U002 LDW  CHANGE CHECK FOR USER'S AUTHORIZATION
*                         FIX BUG IN DISPLAY OF SVC34 RETURN CODE
*      10/08/79 U001 LDW  FIND MSTCONS
*                         CHECK FOR OPERATOR BIT
*                         CHECK FOR "END" OPERATOR COMMAND TO
*                            LEAVE THIS PROCESSOR.
*                         INIT TO NO AUTO UPDATE
         EJECT
*
*
*    .  -----
*      × PA1 ×   WHEN IN AUTO-UPDATE : SWITCHES TO WAIT MODE
*      ×     ×   WHEN IN WAIT MODE : TERMINATES PROGRAM
*       -----
*
*    .  -----
*      × PF1 ×   DISPLAY HELP INFORMATION                          U008
*      ×     ×
*       -----
*
*    .  -----
*      × PF2 ×   PUTS CONSOLE IN AUTO-UPDATE MODE                  U008
*      ×     ×
*       -----
*
*    .  -----
*      × PF3 ×   TERMINATES PROGRAM                                U006
*      ×     ×
*       -----
*
*    .  -----
*      × PF5 ×   DISPLAY CANNED COMMANDS MENU                      U009
*      ×     ×
*       -----
*
*    .  -----
*      × PF6 ×   REPROMPT LAST COMMAND ON COMMAND LINE             U007
*      ×     ×
*       -----
*
*    .  -----
*      × PF7 ×   SELECT TOP OF LARGE SCREEN (DEFAULT)              U014
*      ×     ×
*       -----
*
*    .  -----
*      × PF8 ×   SELECT BOTTOM OF LARGE SCREEN                     U014
*      ×     ×
*       -----
*
*    .  -----
*      × PF9 ×   SELECT ANOTHER CONSOLE                            U008
*      ×     ×
*       -----
*
*    .  -----
*      × PF12×   TERMINATES PROGRAM
*      ×     ×
*       -----
*.
         EJECT ,
CONS     DCMSTART R,LV=WORKLEN                                     U012
         SPACE 3                                                   U023
         LA    R11,4095(,R12)           PREPARE                    U023
         LA    R11,1(,R11)                      SECOND             U023
         USING CONS+4096,R11                           REGISTER    U023
         EJECT
         LR    R2,R1                    SAVE CBUF POINTER          U018
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*  INITIALIZE MISCELLANEOUS DATA                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         MVI   XUCMID,0                                            U012
         MVI   FLAG,0                   DEFAULT = NO AUTO UPDATE   U001
         MVC   SAVECMD,BLANKS                                      U012
         MVC   DISPCMD,BLANKS                                      U013
         MVC   ERRMSG,BLANKS                                       U012
         MVC   UNITXXX(3+1+9),BLANKS                               U012
         XC    ADDRUCM,ADDRUCM          WE DON'T HAVE A CONSOLE    U028
         MVC   WTO(WTOL),WTOMASK        COPY PATTERN WTO           U012
         MVC   SCR1(SCR1L),SCR1X        COPY STANDARD SCRN DESCR   U012
         MVC   MENUSCR(256),MENUSCRX    COPY MENU SCRN DESCR       U012
         MVC   MENUSCR+256(MENUSCRL-256),MENUSCRX+256              U012
         SPACE 2
         LA    R1,79+4                  MGCR LENGTH                U012
         SLL   R1,16                                               U012
         ST    R1,CMND                  STORE IN WTO LIST          U012
         SPACE 1
         LA    R1,SCREEN                -> AREA TO CLEAR           U009
         LA    R0,SCR1                  -> LENGTH                  U009
         BAL   R14,@CLEAR               CLEAR THE SCREEN BUFFER    U009
         SPACE 1
         L     R0,=A(MENULIST)          -> SOURCE                  U009
         LA    R1,MENULEN               LENGTH                     U009
         ICM   R1,B'1000',BLANKS        GET PAD CHAR               U009
         LA    R14,MENUSAVE             -> DEST                    U009
         LH    R15,MENUSCR              LENGTH                     U009
         MVCL  R14,R0                   COPY THE MENU              U009
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*  CHECK FOR PCAUTH ADDRESS SPACE.  IF FOUND, WE ARE RUNNING MVS/SP1.3*
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         L     R1,CVTPTR                -> CVT                     U023
         L     R1,CVTASVT-CVT(,R1)      -> ASVT                    U023
         L     R1,ASVTENTY+4-ASVT(,R1)  -> 2ND ASCB                U023
         L     R1,ASCBJBNS-ASCB(,R1)    -> START/MOUNT JOBNAME     U023
         CLC   =CL8'PCAUTH',0(R1)       IS THIS 'PCAUTH'?          U023
         BNE   *+8                      NO - SKIP                  U023
         OI    FLAG,SP3                 YES - SET FLAG FOR SP3     U023
         SPACE 2
          AIF   (&SP3).SKIP001           SKIP IF SP1.3 SUPPORTED   U023
*---------------------------------------------------------------------*
*                                                                     *
*  IF SP1.3 IS NOT SUPPORTED, BUT WE ARE RUNNING ON SP1.3, COMPLAIN   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         TM    FLAG,SP3                 RUNNING SP3?               U023
         BNO   TESTAUTH                 NO - OK                    U023
         MVC   SCREEN(63),=C'*** THIS VERSION OF "CONS" NOT GENERATED W$
               ITH SP1.3 SUPPORT ***'                              U023
         BAL   R9,WRITE                 WRITE THE SCREEN           U023
         B     DCMSTOP                  FINI                       U023
         SPACE 1
TESTAUTH EQU   *,,C'I'                                             U023
         SPACE 1
.SKIP001  ANOP                                                     U023
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*  SEE IF WE HAVE APF AUTHORIZATION, IF NOT, CAN'T DO ANYTHING...     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         TESTAUTH  FCTN=1               ASK MVS IF WE ARE ARE      U020
         SPACE 1                                                   U020
         LTR   R15,R15                  WELL, ARE WE?              U020
          AIF   (&APFSVC NE 0).SVC01                               U026
         BNZ   NOTAPF                   NO - GET OUT               U020
          AGO   .SVC02                                             U026
.SVC01    ANOP                                                     U026
         BZ    YESAPF                   YES - CONTINUE             U026
         LA    R1,4                     REQUEST SET APF AUTH       U027
         LA    R15,16
         SVC   &APFSVC                  ISSUE MAGIC SVC            U026
         SPACE 1
         TESTAUTH  FCTN=1               SEE IF IT WORKED           U026
         SPACE 1
         LTR   R15,R15                  WELL, DID IT?              U026
         BNZ   NOTAPF                   NO - GET OUT               U026
         OI    FLAG,AUTHSET             YES - REMEMBER WE SET IT   U026
.SVC02    ANOP                                                     U026
YESAPF   EQU   *                                                   U026
         SPACE 2
          AIF   (NOT &SP3).SKIP002      SKIP IF SP3 UNSUPPORTED    U023
*---------------------------------------------------------------------*
*                                                                     *
*  SET UP CROSS MEMORY ADDRESS SPACE COMMUNICATION SO WE CAN TRANSFER *
*  DATA FROM CONSOLE ADDRESS SPACE                                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         TM    FLAG,SP3                 RUNNING SP3?               U023
         BZ    NOAXSET                  NO - SKIP AXSET            U023
         BAL   R9,MODESUP               GET SUPERVISOR STATE       U023
         SPACE 1
*---  THE STUPID MACRO FOLLOWING DOES NOT ALLOW COMMENTS           U023
*---  EXTRACT AUTHORIZATION INDEX FOR CURRENT PASID                U023
         AXEXT
         SPACE 1
         STH   R0,OLDAXVAL              SAVE FOR RESTORE           U023
         SPACE 1
         ESAR  R3                       GET SECONDARY ASID         U023
         SPACE 1
         STH   R3,OLDSASID              SAVE FOR RESTORE           U023
         SPACE 1
         LA    R0,1                     GET AX VALUE               U023
         SPACE 1
         AXSET AX=(0)                   SET AUTHORIZATION INDEX    U023
         SPACE 1
         BAL   R9,MODEPROB              RESTORE PROBLEM STATE      U023
NOAXSET  EQU   *,,C'I'                                             U023
.SKIP002  ANOP                                                     U023
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  COPY PARM INTO COMMAND BUFFER                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         MVC   CMDBUF,BLANKS            CLEAR CMD INTERPRET AREA   U028
         L     R1,0(,R2)                -> CBUF                    U018
         LR    R15,R1                   -> CBUF                    U028
         AH    R15,0(,R1)               -> END OF COMMAND + 1      U028
         LH    R14,2(,R1)               OFFSET TO PARAMETERS       U028
         LA    R14,4(R14,R1)            -> COMMAND PARAMETERS      U028
         SR    R15,R14                  LENGTH OF PARAMETERS       U028
         BNP   GET$PSCB                 NONE, DON'T MOVE PARM      U028
         BCTR  R15,0                    REDUCE BY ONE FOR 'EX'     U028
         MVC   CMDBUF(0),0(R14)         << EXECUTED >>             U028
         EX    R15,*-6                  COPY PARMS INTO AREA       U028
         OC    CMDBUF,BLANKS            FORCE CMD TO UPPER CASE    U028
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  FIND REAL PSCB, SEE IF USER IS AUTHORIZED TO BE HERE               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
GET$PSCB L     R1,16                    -> CVT                     U001
         L     R1,0(,R1)                -> DISPATCH                U001
         L     R1,4(,R1)                -> TCB                     U001
         L     R1,180(,R1)              -> JSCB                    U001
         L     R1,264(,R1)              -> PSCB                    U001
         TM    PSCBATR1-PSCB(R1),PSCBCTRL  OPERATOR?               U023
         BNO   BOUNCE                   NO - THROW HIM OUT         U002
         MVC   WTO+4+4(7),PSCBUSER-PSCB(R1)  MOVE IN TSO USERID    U023
         EJECT
GETMAST  XC    UNITXXX,UNITXXX          INDICATE NO SPECIFIC ADDR  U028
         L     R1,CVTPTR                AND IN THE BEGINNING...
         L     R1,CVTCUCB-CVT(,R1)      ...
         USNGX UCM,R1                                              U023
         LR    R15,R1                   COPY FOR ADJUSTMENT        U023
         SH    R15,=H'4'                POINT TO MCS PREFIX ADDR   U023
         L     R15,0(,R15)              GET MCS PREFIX             U023
         USNGX UCMPRFX,R15                                         U023
         MVC   CONSASID,UCMCTID         SAVE CONSOLE TASK ASID...  U023
*                                       ... FOR SSAR               U023
         SPACE 3
*---------------------------------------------------------------------*
*                                                                     *
*  TRY TO LOCATE THE MASTER CONSOLE                                   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         L     R3,UCMMCENT              GET MASTER CONSOLE ENTRY   U001
         DROPX R15                      UCMPRFX                    U028
         LTR   R3,R3                    IS THERE ONE?              U001
         BZ    SEARCH                   NO - SKIP                  U016
         USNGX UCMLIST,R3                                          U001
         L     R10,UCMXB                GET DCM                    U001
         LTR   R10,R10                  IS THERE ONE?              U001
         BZ    SEARCH                   SKIP IF NONE/NOT GRAPHICS  U016
         USNGX DCMTSRT,R10              POINT TO THE DCM           U001
         L     R6,DCMADTRN              GET PAGEABLE DCM (TDCM)    U001
         LTR   R6,R6                    IS THERE ONE?              U001
         BZ    SEARCH                   NO - SKIP                  U016
         BAL   R9,GETTDCM               COPY TDCM IF NECESSARY     U023
         USNGX DCMSTRT,R6                                          U001
         MVC   XUCMID,UCMID             SAVE THIS                  U001
         CLI   DCMIONDX,X'10'           IS IT A 3270 ?             U001
         BE    GOTMAST                  YES - USE IT               U014
         CLI   DCMIONDX,X'04'           IS IT A 3066 ?             U014
         BNE   SEARCH                   SKIP IF NEITHER            U016
GOTMAST  TM    UCMATR,UCMUF             IS IT ACTIVE ?             U001
         BO    GOTUCM                   YES - SAVE UCM ADDR        U008
         DROPX R3,R6,R10                DITCH ALL MY REGISTERS     U001
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  COULDN'T FIND MSTCONS, SO NOW WE DO IT THE OLD WAY --              *
*  JUST USE THE FIRST AVAILABLE GRAPHICS CONSOLE.                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
SEARCH   L     R1,CVTPTR                LOCATE UCM BASE AGAIN ...  U028
         L     R1,CVTCUCB-CVT(,R1)      ... AT THIS ENTRY POINT    U028
         LM    R3,R5,UCMVEA             R3 -> TO THE FIRST UCME
*                                         R4 CONTAINS UCME LENGTH
*                                           R5 -> TO THE LAST UCME
         DROPX R1                       UCM                        U023
         SPACE 1
DID0     EQU   *                        SEARCH FOR SOMETHING USEFUL
         USNGX UCMLIST,R3
***
         OC    UNITXXX,UNITXXX          LOOKING FOR SPECIFIC CONS? U028
         BZ    ANY                      NO - SKIP                  U016
         L     R1,UCMUCB                -> MVS UCB                 U028
         CLC   UNITXXX,13(R1)           YES - THIS IT?             U028
         BNE   DID1                     NO - TRY NEXT              U008
         SPACE 1
ANY      TM    UCMATR,UCMUF             IS IT ACTIVE ?
         BZ    DID1                     NO.. UNUSEABLE
         L     R10,UCMXB                IS IT GRAPHICS ?
         LTR   R10,R10                  ...
         BZ    DID1                     NO.. USELESS
         USNGX DCMTSRT,R10              POINT TO THE DCM
         L     R6,DCMADTRN              FIND THE PAGEABLE DCM (TDCM)
         LTR   R6,R6                    DOES IT EXIST ?
         BZ    DID1                     NO...
         BAL   R9,GETTDCM               COPY TDCM IF NECESSARY     U023
         USNGX DCMSTRT,R6               A LITTLE
         MVC   XUCMID,UCMID             SAVE THIS
         CLI   DCMIONDX,X'10'           IS IT A 3270 ?
         BE    GOTUCM                   YES BY DINGLES !
*                                          DINGLES ?
         CLI   DCMIONDX,X'04'           IS IT A 3066 ?             U014
         BE    GOTUCM                   YES - HOW UNFORTUNATE      U014
DID1     BXLE  R3,R4,DID0               GET THE NEXT ENTRY
         OC    UNITXXX,UNITXXX          LOOKING FOR SPECIFIC CONS? U028
         BNZ   NOTFOUND                 YES - DIDN'T FIND IT       U016
         MVC   SCREEN(47),=C'>>> NO ACCEPTABLE (3270 OR 3066) CONSOLES $
               FOUND'                                              U017
         BAL   R9,WRITE                 WRITE THE SCREEN
         B     DCMSEXIT                 FINI
         DROPX R3                       UCMLIST                    U023
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  ASSORTED ERROR MESSAGES                                            *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
NOTFOUND MVC   ERRMSG,BLANKS            CLEAR THE AREA             U009
         MVC   ERRMSG(3),UNITXXX        WHAT WE ARE LOOKING FOR    U028
         MVC   ERRMSG+3(24),=C' NOT FOUND OR NOT USABLE'           U009
         MVC   ENTRLNE,ERRMSG           COPY TO SCREEN             U009
         B     REFRESH                  BACK TO WHERE HE WAS       U009
         SPACE 1
SELERR   MVC   ERRMSG,BLANKS            CLEAR THE AREA             U028
         MVC  ERRMSG(37),=C'INVALID ADDRESS SPECIFIED FOR CONSOLE' U028
         MVC   ENTRLNE,ERRMSG           COPY TO SCREEN             U028
         B     REFRESH                  BACK TO WHERE HE WAS       U028
         SPACE 2
BOUNCE   MVC   SCREEN(33),=C'COMMAND NOT AUTHORIZED FOR USERID'    U002
         BAL   R9,WRITE                 WRITE THE SCREEN           U002
         B     DCMSEXIT                 LEAVE QUICKLY              U002
         SPACE 2
NOTAPF   MVC   SCREEN(31),=C'CONS REQUIRES APF AUTHORIZATION'      U017
         BAL   R9,WRITE                 WRITE THE SCREEN           U017
         B     DCMSTOP                  LEAVE EXTREMELY QUICKLY    U023
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  DISPLAY LIST OF CONSOLES AND THEIR STATUS (LIKE "D C")         U018*
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
LISTCONS LA    R1,SCREEN                -> AREA TO CLEAR           U018
         LA    R0,SCR1                  -> LENGTH                  U018
         BAL   R14,@CLEAR               CLEAR THE SCREEN BUFFER    U018
         SPACE 1
         L     R1,CVTPTR                AND IN THE BEGINNING...    U018
         L     R1,CVTCUCB-CVT(,R1)      ...                        U018
         USNGX UCM,R1
         LM    R3,R5,UCMVEA             R3 -> TO THE FIRST UCME    U018
*                                         R4 CONTAINS UCME LENGTH  U018
*                                           R5 -> TO THE LAST UCME U018
         LA    R8,SCREEN                -> OUTPUT LINE             U018
         USNGX LCLINE,R8                                           U018
         LA    R15,SCR1+6               -> FIRST LINE DESCRIPTOR   U018
******** MVC   0(55,R8),=C'<<< LOCATION >>> TYPE  ADDR  ID  STAT  CMDAU$
               TH  ROUTCDE'                                        U023
         MVC   0(L'MSGLOC,R8),MSGLOC    MOVE IN HEADER             U023
         MVI   1(R15),X'F8'             MAKE HEADER BRIGHT         U018
         LA    R8,79(,R8)               -> NEXT SCREEN LINE        U023
         LA    R15,8(,R15)              -> NEXT LINE DESCRIPTOR    U023
         MVI   0(R8),C'-'               SET UP FOR A LINE ...      U023
         MVC   1(79-1,R8),0(R8)         ... OF DASHES              U023
         MVI   1(R15),X'F4'             MAKE DASHES LOW INTENSITY  U023
         LA    R7,19                    MAX LINES                  U023
         SPACE 1
         USNGX UCMLIST,R3                                          U018
         INUSE ,                                                   U020
         SPACE 2
LC$01    LA    R8,79(,R8)               -> NEXT SCREEN LINE        U018
         LA    R15,8(,R15)              -> NEXT LINE DESCRIPTOR    U018
         MVI   1(R15),X'F4'             MAKE IT LOW INTENSITY      U018
*---  DEVICE ADDRESS
         L     R1,UCMUCB                                           U018
         MVC   LC@ADDR,13(R1)           COPY DEVICE ADDRESS        U018
*---  CONSOLE ID NUMBER
         SR    R2,R2                    CLEAR FOR IC               U018
         IC    R2,UCMID                 GET CONSOLE ID NUMBER      U018
         BAL   R14,@DECIML2             CONVERT TO DECIMAL         U018
         MVC   LC@ID,DECWORK4+2         MOVE TO DISPLAY LINE       U018
*---  CONSOLE LOCATION -- TABLE MUST BE UPDATED FOR YOUR INSTALLATION
*U029    MH    R2,=Y(L'LOCTABLE)        MULTIPLY BY TBL ENTRY LEN  U023
*U029    AL    R2,=A(NAMES-L'LOCTABLE)  -> LOC TABLE ENTRY         U027
*U029    MVC   LC@LOC(L'LOCTABLE),0(R2) MOVE IN LOCATION DESC      U029
         L     R1,=A(NAMES-L'LOCTABLE)  -> NAME LOC TABLE          U029
         BAL   R14,LOCFIRST             SET LOOP ADDR, SKIP FIRST  U029
         LA    R1,L'LOCTABLE(,R1)       -> NEXT TABLE ENTRY        U029
         CLC   0(3,R1),BLANKS           END OF TABLE?              U029
         BE    NONAME                   YES - LEAVE IT BLANK       U029
LOCFIRST CLC   LC@ADDR,0(R1)            IS THIS THE ENTRY?         U029
         BNER  R14                      NO - TRY NEXT              U029
         MVC   LC@LOC(L'LOCTABLE-3),3(R1)  YES - GET DESCRIPTION   U029
*---  CONSOLE STATUS
NONAME   MVI   LC@STAT,C'I'             ASSUME INACTIVE            U018
         TM    UCMATR,UCMUF             IS IT ACTIVE?              U018
         BNO   *+12                     NO - SKIP                  U023
         MVI   LC@STAT,C'A'             YES - INDICATE             U018
         MVI   1(R15),X'F8'             MAKE IT BRIGHT             U023
         TM    UCMDISP1,UCMDISPA        MASTER CONSOLE?            U018
         BNO   *+8                      NO - SKIP                  U018
         MVI   LC@STAT,C'M'             YES - INDICATE             U018
*---  DISPLAY MODE
         TM    UCMDISP2,UCMDISPI        $TOSCN,D=T ?               U018
         BNO   *+10                     NO - SKIP                  U018
         MVC   LC@STAT+1(2),=C',T'      YES - INDICATE             U018
         TM    UCMDISP2,UCMDISPJ        $TOSCN,D=J ?               U018
         BNO   *+10                     NO - SKIP                  U018
         MVC   LC@STAT+1(2),=C',J'      YES - INDICATE             U018
         TM    UCMATR,UCMAT04           STATUS TO CHANGE?          U018
         BNO   *+10                     NO - SKIP                  U018
         MVC   LC@STAT+3(2),=C',C'      YES - INDICATE             U018
*---  COMMAND AUTHORIZATION
         IC    R14,UCMAUTHA             PICK UP AUTHORIZATION BYTE U018
         SRL   R14,8-3                  SHIFT OUT JUNK             U018
         N     R14,=F'7'                REMOVE OTHER JUNK          U018
         MH    R14,=Y(L'AUTHTBL)        * TABLE ENTRY LENGTH       U018
         LA    R14,AUTHTBL(R14)         -> DESCRIPTOR              U018
         MVC   LC@AUTH,0(R14)           MOVE TO DISPLAY LINE       U018
*---  ROUTING CODES
         HEX   LC@ROUT,UCMRTCD,LEN=2    ROUTE CODES                U018
*---  DEVICE TYPE
         L     R1,UCMUCB                -> UCB                     U020
         MVC   LC@TYPE(4),=C'????'      ASSUME UNKNOWN             U023
         CLI   18(R1),X'10'             GRAPHICS DEVICE CLASS?     U023
         BNE   LC$02                    NO - TRY UNIT RECORD       U023
         CLI   19(R1),X'02'             IN RANGE?                  U023
         BL    LC$99                    NO - UNKNOWN GRAPHICS      U023
         CLI   19(R1),X'0E'             IN RANGE?                  U023
         BH    LC$99                    NO - UNKNOWN GRAPHICS      U023
         SR    R14,R14                  CLEAR FOR IC               U023
         IC    R14,19(,R1)              GET DEVICE TYPE CODE       U023
         SLL   R14,2                    MULTIPLY TYPE CODE BY 4    U023
         LA    R14,GRAFTYPE-(2*4)(R14)  -> DEVICE TYPE             U023
         MVC   LC@TYPE(4),0(R14)        MOVE IN DEVICE TYPE        U023
         B     LC$99                    DONE                       U023
         SPACE 1
LC$02    CLI   18(R1),X'08'             UNIT RECORD CLASS?         U023
         BNE   LC$99                    NO - UNKNOWN               U023
         LA    R14,URTYPE               -> UNIT RECORD TYPE TABLE  U023
         SPACE 1
LC$03    CLC   0(1,R14),19(R1)          IS THIS THE DEVICE TYPE?   U023
         BE    LC$04                    YES - MOVE IT IN           U023
         LA    R14,1+5(,R14)            -> NEXT TYPE TABLE ENTRY   U023
         CLI   0(R14),X'FF'             END OF TABLE?              U023
         BNE   LC$03                    NO - KEEP LOOKING          U023
*  THE FOLLOWING CLC IS INCORRECT, BUT I DON'T HAVE ANY 2740'S     U023
*  TO LOOK AT, AND 2740 IS NOT LISTED IN DEBUGGING HANDBOOK        U023
         CLC   =X'98765432',16(R1)      IS IT A 2740?              U023
         BNE   LC$99                    NO - UNKNOWN TYPE          U023
         LA    R14,=C' 2740 '           YES - FAKE THE STRING      U023
         SPACE 1
LC$04    MVC   LC@TYPE(5),1(R14)        MOVE IN DEVICE TYPE        U023
         SPACE 1
LC$99    BXLE  R3,R4,*+8   >========+   GET THE NEXT ENTRY         U018
         B     *+8    >========+    ×   GET OUT IF ALL DONE        U018
         BCT   R7,LC$01        × <==+   LOOP UNTIL FULL            U018
         B     DISPLAY      <==+        GO DISPLAY MY HANDIWORK    U018
         SPACE 1
         DROPX R8                                                  U018
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
* A CONSOLE HAS BEEN FOUND COPY THE SCREEN AND DISPLAY IT FOR THE USER*
*        R3 ->  UCME                                                  *
*        R10 -> RDCM                                                  *
*        R6 ->  TDCM                                                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
GOTUCM   ST    R3,ADDRUCM               SAVE UCM ADDR FOR LATER    U009
         NI    FLAG,255-LARGCON-DISPBOT  RESET FLAGS               U028
         LA    R1,80                    ASSUME 3066 LINE LENGTH    U028
         CLI   DCMIONDX,X'04'           IS IT A 3066 ?             U028
         BE    *+8                      YES, SETUP IS RIGHT        U028
         LA    R1,84                    3270'S HAVE ATTRIBUTES     U028
         STH   R1,LINELEN               SAVE MEMORY LINE LENGTH    U028
         LH    R1,DCMMSGAL              NBR OF LINES IN MSG AREA   U028
         SH    R1,=H'20'                NUMBER OF EXCESS LINES     U028
         BNP   EXECUTE                  SHORT SCREEN, DONE HERE    U028
         OI    FLAG,LARGCON             CONSOLE HAS LARGE SCREEN   U028
         MH    R1,LINELEN               OFFSET TO BOTTOM OF SCREEN U028
         A     R1,DCMASCRN              -> BOTTOM OF SCREEN        U028
         ST    R1,ADRBOT                SAVE FOR LATER             U028
         LH    R1,=H'20'                OFFSET TO LINE 21          U028
         MH    R1,LINELEN               OFFSET TO LINE 21          U028
         A     R1,DCMASCRN              -> LINE 21                 U028
         ST    R1,ADRL21                SAVE FOR LATER             U028
         B     EXECUTE                  FIRE IT UP                 U028
         SPACE 2
CHKATTN  TM    FLAG,FLASH               IN AUTO UPDATE MODE?       U008
         BZ    DCMSEXIT                 NO - TIME TO LEAVE         U008
         NI    FLAG,255-FLASH           RESET AUTO UPDATE MODE     U009
         SPACE 2
REFRESH  TM    FLAG,MENUFLAG            RETURN TO MENU?            U010
         BO    DISPMENU                 YES - GO TO IT             U010
         ICM   R3,B'1111',ADDRUCM       -> DESIRED UCME            U016
         BZ    DISPLAY                  NOT SET, TAKE SHORTCUT     U028
         L     R1,UCMUCB                -> UCB                     U018
         MVC   UNITXXX,13(R1)           GET DEVICE ADDRESS         U018
         MVC   UNITCONS,BLANKS          ASSUME NOT MASTER CONSOLE  U018
         TM    UCMDISP1,UCMDISPA        MSTCONS?                   U018
         BNO   *+10                     NO - SKIP                  U018
         MVC   UNITCONS,=C'*MSTCONS*'   YES - SHOW WE'RE SMART     U018
         BAL   R9,OPENCONS              ACCESS CONSOLE BUFFERS     U028
         L     R7,DCMASCRN              -> FIRST INPUT LINE        U017
         LA    R8,SCREEN                -> OUTPUT AREA             U028
         LH    R2,DCMMSGAL              NBR OF LINES IN MSG AREA   U028
         TM    FLAG,LARGCON             TOO BIG FOR ME?  (3278-2)  U028
         BZ    SIZEOK                   NO - CONTINUE              U028
         LH    R2,=H'20'                YES - USE MY MAX           U028
         TM    FLAG,FLASH               IN AUTO UPDATE MODE?       U017
         BNO   SIZEOK                   NO - LEAVE HIM WHEREVER... U017
         NI    FLAG,255-DISPBOT         ASSUME TOP OF SCREEN       U017
         L     R7,ADRL21                -> LINE 21 OF SCREEN       U028
         LA    R1,78                    LENGTH OF DATA IN LINE     U028
         BAL   R9,GETCONS               GET LINE FROM CONSOLE      U028
         CLC   0(78,R8),BLANKS          ANYTHING ON BOTTOM?        U028
         L     R7,DCMASCRN              -> TOP OF SCREEN           U028
         BE    SIZEOK                   NO - STAY ON TOP           U017
         OI    FLAG,DISPBOT             YES - GO TO BOTTOM         U017
         SPACE 1
SIZEOK   TM    FLAG,DISPBOT             BOTTOM OF SCREEN?          U014
         BNO   *+8                      NO - SKIP                  U014
         L     R7,ADRBOT                -> BOTTOM OF SCREEN        U028
         LA    R3,SCR1+6                -> FIRST LINE DESCRIPTOR   U028
GOT1     EQU   *
         LA    R1,78                    LENGTH OF DATA IN LINE     U028
         BAL   R9,GETCONS               GET LINE FROM CONSOLE      U028
         TR    0(78,R8),BLANKTAB        GET IT READABLE            U006
         MVI   1(R3),X'F4'              MAKE IT LOW INTENSITY      U028
         CLI   3(R8),C'*'               SYSTEM ACTION MESSAGE?     U006
         BNE   *+8                      NO - LEAVE AS IS           U006
         MVI   1(R3),X'F8'              YES - MAKE IT BRIGHT       U028
         CLI   3(R8),C'@'               USER ACTION MESSAGE?       U006
         BNE   *+8                      NO - LEAVE AS IS           U006
         MVI   1(R3),X'F8'              YES - MAKE IT BRIGHT       U028
         LA    R8,79(,R8)               OUR SIZE
         AH    R7,LINELEN               THEIRS                     U028
         LA    R3,8(,R3)                -> NEXT LINE DESCRIPTOR    U028
         BCT   R2,GOT1                  GET EM AL                  U028
         BAL   R9,CLOSCONS              DROP CONSOLE BUFFERS       U028
         MVI   SCR1+6+20*8+1,X'F8'      MAKE NEXT LINE BRIGHT      U018
         MVC   MSGLINE(45),MSGSIM       MOVE OUR INSTRUCTION LINE  U023
         MVC   MSGLINE+66(13),BLANKS    CLEAR RANGE INDICATOR      U020
         TM    FLAG,LARGCON             STANDARD SIZE SCREEN?      U028
         BZ    NOTLARG                  YES - SKIP SPECIAL CODE    U028
         CLI   DCMIONDX,X'04'           IS IT A 3066 ?             U028
         BNE   *+10                     NO, SKIP 3066 DEVICE TYPE  U028
         MVC   MSGLINE+29(4),=C'3066'   2250 REINCARNATED          U020
         MVC   MSGLINE+66(13),=C'(LINES 01-20)'                    U020
         TM    FLAG,DISPBOT             TOP OR BOTTOM?             U014
         BZ    NOTLARG                  TOP                        U014
         LH    R1,DCMMSGAL              NBR OF LINES IN MSG AREA   U028
         CVD   R1,DECWORK3              ENDING LINE IN DECIMAL     U028
         OI    DECWORK3+7,X'0F'         ELIMINATE PACKED SIGN      U028
         UNPK  MSGLINE+76(2),DECWORK3   FORMAT END LINE INTO MSG   U028
         SH    R1,=Y(20-1)              GET FIRST LINE NUMBER      U028
         CVD   R1,DECWORK3              STARTING LINE IN DECIMAL   U028
         OI    DECWORK3+7,X'0F'         ELIMINATE PACKED SIGN      U028
         UNPK  MSGLINE+73(2),DECWORK3   FORMAT START LINE INTO MSG U028
         SPACE 2
NOTLARG  MVC  MSGLINE+45(3+1+9),UNITXXX MOVE ADDR & MSTCONS INDIC. U020
         MVC   MODELINE(18),=C'IEE163I MODE= DCMS'                 U020
         TM    FLAG,QUIET               IN QUIET MODE?             U024
         BNO   *+10                     NO - SKIP                  U024
         MVC   MODELINE+18(6),=C'/QUIET'  YES - REMIND HIM         U024
         MVC   MODELINE+30(79-30),DISPCMD  SHOW HIM LAST COMMAND   U020
         SPACE 2
DISPLAY  BAL   R9,WRITE                 DO IT JACK !!!
         SPACE 1
         PFKEY K1=HELP,K2=SETMODE,K3=DCMSEXIT,K5=EXEMENU,          U009$
               K6=REPROMPT,K7=TOP,K8=BOTTOM,K9=SELPFK,             U028$
               K10=LISTCONS,K12=DCMSEXIT,                          U024$
               DEF=REFRESH,ATTN=CHKATTN,ENTER=EXECUTE              U012
         SPACE 2
EXEMENUX MVC   CMDBUF,BLANKS            KILL THE "MENU" SUBCOMMAND U024
EXEMENU  OI    FLAG,MENUFLAG            DISPLAY MENU NEXT TIME     U016
         SPACE 1                                                   U001
EXECUTE  CLC   CMDBUF,BLANKS            NULL INPUT ?               U016
         BE    REFRESH
         CLI   CMDBUF,C'/'              MVS COMMAND?               U024
         BE    SLASH                                               U024
         CLI   CMDBUF,C'E'              WANT OUT?                  U024
         BE    DCMSEXIT                                            U001
         CLI   CMDBUF,C'A'              START AUTO UPDATE?         U024
         BE    SETMODEX                                            U024
         CLI   CMDBUF,C'H'              HELP?                      U024
         BE    HELP                                                U024
         CLI   CMDBUF,C'?'              HELP?                      U024
         BE    HELP                                                U024
         CLI   CMDBUF,C'M'              MENU?                      U024
         BE    EXEMENUX                                            U024
         CLI   CMDBUF,C'U'              UP?                        U024
         BE    TOP                                                 U024
         CLI   CMDBUF,C'D'              DOWN?                      U024
         BE    BOTTOM                                              U024
         CLI   CMDBUF,C'S'              SELECT?                    U028
         BE    SELECT                                              U028
         CLI   CMDBUF,C'L'              LIST CONSOLES?             U024
         BE    LISTCONS                                            U024
         CLI   CMDBUF,X'80'             SPECIAL CHARACTER?         U024
         BL    JESCMD                   YES - MUST BE JES CMD      U024
         CLC   =C'CONS',CMDBUF          CHANGE "SNEAKY MODE"?      U016
         BE    SNEAKY                   YES - GO DO IT             U012
         MVC   ERRMSG,BLANKS                                       U024
         MVC   ERRMSG(61),=C'*** INVALID SUBCOMMAND (IF MVS COMMAND, PR$
               ECEDE WITH "/") ***'                                U024
         MVC   ENTRLNE,ERRMSG                                      U024
         B     REFRESH                                             U024
         SPACE 2
JESCMD   MVC   SAVECMD,CMDBUF           SAVE INPUT COMMAND         U009
         MVC   DISPCMD,CMDBUF           SAVE COMMAND FOR DISPLAY   U013
         B     EXECUTE2                 SKIP CHECK FOR "REPLY"     U024
         SPACE 2
SLASH    MVC   ENTRLNE(78),CMDBUF+1     COPY FOR REFORMAT          U028
*        MVI   ENTRLNE,C' '             PLUG THE SLASH             U028
         LA    R1,ENTRLNE               -> COPY                    U028
         BAL   R9,GETCMD                SCAN FOR THE MVS COMMAND   U024
         CLC   =C'QUIESCE',CMDBUF       DANGEROUS SYSTEM COMMAND?  U024
         BE    QUIESCE                  YES - DISALLOW             U024
         MVI   SAVECMD,C'/'             INDICATE MVS COMMAND       U028
         MVC   SAVECMD+1(L'CMDBUF-1),CMDBUF  SAVE FOR REPROMPT     U028
         MVC   DISPCMD,CMDBUF           SHOW WHAT WE WILL EXECUTE  U024
         SPACE 1
*---  CHECK FOR "REPLY" COMMAND
         LA    R1,DISPCMD               -> SAVED COMMAND           U013
         LA    R0,L'DISPCMD             LENGTH OF SAVED COMMAND    U013
         CLC   =C'R ',0(R1)             'R' COMMAND?               U013
         BNE   RL02                     NO, TEST ANOTHER           U013
         LA    R1,2(,R1)                SKIP COMMAND               U013
         SH    R0,=H'2'                 REDUCE LENGTH              U013
         B     RL03                     AND GO SKIP BLANKS         U013
RL02     CLC   =C'REPLY ',0(R1)         'REPLY' COMMAND?           U013
         BNE   RL05                     NO, LOOK FOR NUMBER        U013
         LA    R1,6(,R1)                SKIP COMMAND               U013
         SH    R0,=H'6'                 REDUCE LENGTH              U013
RL03     LTR   R0,R0                    ANY MORE DATA?             U013
         BNP   EXECUTE2                 NO, NOT A REPLY            U013
RL04     CLI   0(R1),C' '               LEADING BLANK?             U013
         BNE   RL05                     NO, LOOK FOR NUMBER        U013
         LA    R1,1(,R1)                INCREMENT POINTER          U013
         BCT   R0,RL04                  AND LOOP                   U013
RL05     LTR   R0,R0                    ANY MORE DATA?             U013
         BNP   EXECUTE2                 NO, NOT A REPLY            U024
         TM    0(R1),X'F0'              NUMERIC ZONE?              U013
         BNO   EXECUTE2                 NO, CAN'T BE REPLY         U024
         MVI   REPLID,C'0'              ASSUME ONLY ONE DIGIT      U013
         MVC   REPLID+1(1),0(R1)        SAVE REPLID FIRST DIGIT    U013
         CH    R0,=H'2'                 ROOM FOR SECOND DIGIT?     U013
         BL    RL06                     NO, USE ONLY ONE DIGIT     U013
         TM    1(R1),X'F0'              NUMERIC ZONE?              U013
         BNO   RL06                     NO, USE ONLY ONE DIGIT     U013
         MVC   REPLID,0(R1)             SAVE BOTH DIGITS           U013
RL06     MVC   DISPCMD,BLANKS           CLEAR SAVED COMMAND        U013
         MVC   DISPCMD(6),=C'REPLY '    FORMAT REPLY MESSAGE       U013
         MVC   DISPCMD+6(2),REPLID      MOVE IN REPLY NUMBER       U013
         SPACE 2
EXECUTE2 TM    FLAG,QUIET               SUPPRESS WTO?              U012
         BO    SKIPWTO                  YES                        U012
         MVC   WTO+4+16(79),DISPCMD     MOVE COMMAND TO WTO        U013
         SR    R0,R0                    CLEAR FOR IC               U012
         IC    R0,XUCMID                GET CONSOLE ID             U012
         WTO   MF=(E,WTO)               NO - SO TELL THE OPERATOR  U012
         SPACE 2
SKIPWTO  BAL   R9,KEYZERO               SO SVC34 WILL LIKE ME      U011
         SPACE 1
         SR    R15,R15                  PREPARE R15 BECAUSE...     U008
         BCTR  R15,0                    ...SVC34 USUALLY DOESN'T...U008
*                                       ...SET R15 IF IT DOESN'T...U008
*                                       ...EXECUTE THE COMMAND     U008
         LA    R1,CMND                  POINT TO COMMAND RDW
         XR    R0,R0
         IC    R0,XUCMID                OUR MAN IN THE COMM TASK
         SVC   34                       FIRE HIM THRU
         LR    R2,R15                   SAVE RETURN CODE
         SPACE 1
         BAL   R9,KEYNZERO              I HOPE SVC34 WAS HAPPY     U011
*
         LTR   R15,R2                   DID IT WORK?
         BZ    REFRESH                  YES - REFRESH SCREEN
         SPACE  1
***
* SEE IF  WE ARE HERE BY A PREVIOUS MARRIAGE ...
*
         SPACE 1
         MVC   ERRMSG,BLANKS            CLEAR THE AREA             U009
         MVC   ERRMSG(34),=C'*** ERROR *** SVC 34 RETURN CODE ='   U009
         ST    R15,DOUBLE               SET DOWN RETURN CODE       U012
         UNPK  ERRMSG+35(8+1),DOUBLE(4+1)                          U012
         TR    ERRMSG+35(8),HEXTAB      MAKE IT PRINTABLE          U009
         MVI   ERRMSG+35+8,C' '         FIX FLIP BYTE              U009
         MVC   ENTRLNE,ERRMSG           COPY ERROR MSG TO SCREEN   U009
         NI    FLAG,255-FLASH           INHIBIT AUTO UPDATING      U009
         B     REFRESH                  REFRESH THE SCREEN         U009
         SPACE 3
SETMODEX MVC   CMDBUF,BLANKS            KILL THE "AUTO" SUBCOMMAND U024
         NI    FLAG,255-MENUFLAG        RESET MENU FLAG            U024
SETMODE  OI    FLAG,FLASH               START AUTO UPDATING        U009
         B     EXECUTE                  EXECUTE OPR CMD, IF ANY    U009
         SPACE 3
SNEAKY   MVC   ERRMSG,BLANKS            CLEAR THE AREA             U012
         MVC   ERRMSG(24),=C'=== QUIET MODE RESET ==='             U012
         NI    FLAG,255-QUIET           ASSUME RESET               U012
         CLC   =C'=QUIET',CMDBUF+4      WANT IT SET?               U016
         BNE   SNEAKY2                  NO - SKIP                  U012
         OI    FLAG,QUIET               YES - SET IT               U012
         MVC   ERRMSG+15(9),ERRMSG+17   'QUIET MODE SET'           U012
SNEAKY2  MVC   ENTRLNE,ERRMSG           COPY ERROR MSG TO SCREEN   U012
         B     REFRESH                  REFRESH THE SCREEN         U012
         SPACE 3
QUIESCE  MVC   ERRMSG,BLANKS            CLEAR THE AREA             U016
         MVC   ERRMSG(29),=C'YOU CAN''T "QUIESCE" FROM HERE'       U016
         MVC   ENTRLNE,ERRMSG           COPY ERROR MSG TO SCREEN   U016
         B     REFRESH                  REFRESH THE SCREEN         U016
         SPACE 3
REPROMPT MVC   ENTRLNE,SAVECMD          MOVE BACK HIS LAST COMMAND U009
         B     REFRESH                  AND REFRESH THE SCREEN     U009
         SPACE 3
TOP      NI    FLAG,255-DISPBOT         TURN OFF BOTTOM FLAG       U014
         B     REFRESH                  DO IT AGAIN                U014
         SPACE 3                                                   U014
BOTTOM   TM    FLAG,LARGCON             IS THIS A LARGE CONSOLE?   U014
         BNO   REFRESH                  NO - IGNORE COMMAND        U014
         OI    FLAG,DISPBOT             TURN ON BOTTOM FLAG        U014
         B     REFRESH                  DO IT AGAIN                U014
         SPACE 3
*---------------------------------------------------------------------*
*                                                                     *
*  SELECT DIFFERENT CONSOLE ADDRESS                                   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
SELECT   LA    R1,CMDBUF                -> INPUT DATA              U016
         LA    R0,L'CMDBUF              MAXIMUM DATA LENGTH        U016
SEL01    CLI   0(R1),C' '               FOUND END OF COMMAND YET?  U028
         BE    SEL02                    YES, GO FIND ADDRESS       U028
         LA    R1,1(,R1)                NO, -> NEXT CHARACTER      U028
         BCT   R0,SEL01                 DECREMENT LENGTH AND LOOP  U028
         MVC   CMDBUF,BLANKS            AVOID UNFORTUNATE LOOP     U028
         B     GETMAST                  WHO KNOWS WHAT HAPPENED?   U028
         SPACE 1
SELPFK   LA    R1,CMDBUF                -> INPUT DATA              U028
         LA    R0,L'CMDBUF              MAXIMUM DATA LENGTH        U028
SEL02    CLI   0(R1),C' '               FOUND REQUEST DATA YET?    U028
         BNE   SEL03                    YES, GO FIND ADDRESS       U028
         LA    R1,1(,R1)                NO, -> NEXT CHARACTER      U028
         BCT   R0,SEL02                 DECREMENT LENGTH AND LOOP  U028
         MVC   CMDBUF,BLANKS            AVOID UNFORTUNATE LOOP     U028
         B     GETMAST                  WHO KNOWS WHAT HAPPENED?   U028
         SPACE 1
SEL03    XR    R14,R14                  CLEAR WORKING REGISTER     U028
SEL04    OI    0(R1),X'40'              FORCE UPPER CASE           U028
         CLI   0(R1),C' '               ENDING BLANK?              U028
         BE    SEL06                    YES, END OF DATA           U028
         ICM   R15,B'1000',0(R1)        GET WORKING CHARACTER      U028
         SLL   R15,4                    SHIFT OFF SIGN NYBBLE      U028
         SLDL  R14,4                    SHIFT NUMERIC INTO VALUE   U028
         CLI   0(R1),C'9'               TOO HIGH FOR NUM?          U028
         BH    SELERR                   YES, BAD VALUE             U028
         CLI   0(R1),C'0'               IS IT 0-9?                 U028
         BNL   SEL05                    YES, USE IT                U028
*U029    LA    R14,9(,R14)              ADJUST VALUE               U028
         CLI   0(R1),C'F'               IS IT TOO HIGH FOR A-F?    U028
         BH    SELERR                   YES, BAD VALUE             U028
         CLI   0(R1),C'A'               IS IT A-F?                 U028
         BL    SELERR                   NO, BAD VALUE              U028
         LA    R14,9(,R14)              YES, ADJUST VALUE          U028
SEL05    LA    R1,1(,R1)                INCREMENT POINTER          U028
         BCT   R0,SEL04                 REDUCE COUNT AND LOOP      U028
         SPACE 1
SEL06    ST    R14,HEXWORK1             STORE FOR CONVERSION       U028
         BAL   R14,@HEX                 CONVERT TO PRINTABLE HEX   U028
         CLC   HEXWORK2(5),=5C'0'       PROPER LEADING ZEROS?      U028
         BNE   SELERR                   NO, BAD VALUE              U028
         MVC   UNITXXX,HEXWORK2+5       INDICATE CONSOLE ADDRESS   U028
         MVC   CMDBUF,BLANKS            AVOID UNFORTUNATE LOOP     U028
         B     SEARCH                   AND GO TRY TO GET IT
         SPACE 3                                                   U014
*---------------------------------------------------------------------*
*                                                                     *
*  DISPLAY HELP SCREEN                                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
HELP     LA    R14,SCREEN               -> DESTINATION             U008
         LA    R15,20*79                LENGTH                     U008
         L     R0,=A(HELPIMG)           -> HELP IMAGE              U008
         LA    R1,HELPIMGL              LENGTH                     U008
         ICM   R1,B'1000',BLANKS        GET THE PAD CHAR           U008
         MVCL  R14,R0                   MOVE IT IN                 U008
         SPACE 1                                                   U008
         LA    R15,SCR1+6               -> FIRST ATTRIBUTE AREA    U008
         MVI   1(R15),X'F8'             MAKE TITLE LINE BRIGHT     U008
         LA    R0,20                    NUMBER OF LINES MORE       U019
         LA    R15,8(,R15)   <===+      -> NEXT ATTRIBUTE AREA     U008
         MVI   1(R15),X'F4'      ×      MAKE IT LOW INTENSITY      U008
         BCT   R0,*-8     >======+      DO THE WHOLE THING         U008
         B     DISPLAY                  GO REWRITE THE SCREEN      U016
         SPACE 3
*---------------------------------------------------------------------*
*                                                                     *
*  DISPLAY MENU OF CANNED COMMANDS                                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
DISPMENU LA    R5,DCMSWORK+4095         NEED MORE BASE REGISTER    U012
         USNGX DCMSWORK+4095,R5         TEMPORARILY                U012
         MVC   ENTRLNE2,ENTRLNE         GET ERROR MESSAGE, IF ANY  U010
         MVC   ENTRLNE,BLANKS                                      U016
         MVC   SHOWCMD2+30(79-30),DISPCMD   AND THE LAST COMMAND   U016
         DROPX R5                                                  U012
         NI    FLAG,255-MENUFLAG-FLASH  RESET FLAGS                U010
         NI    IOCBFLGD,255-IOCBDYON-IOCBDFRC                      U009
         SPACE 1
         TRMIO IOCB,FORMAT=MENUSCR,IMAGE=MENUSAVE                  U009
         SPACE 2
         PFKEY K1=HELP,K2=MENUPF2,K3=DCMSEXIT,K5=MENUMENU,         U008$
               K6=REPROMPT,K9=SELECT,K10=LISTCONS,K12=DCMSEXIT,    U024$
               DEF=DISPMENU,ATTN=CHKATTN,ENTER=MENU$GO             U012
         SPACE 3                                                   U009
MENUPF2  OI    FLAG,FLASH               SET AUTO UPDATE MODE       U009
         B     MENU$GO                  CONTINUE                   U010
         SPACE 2
MENUMENU OI    FLAG,MENUFLAG            SET RETURN INDICATOR       U010
         SPACE 2
MENU$GO  LA    R5,DCMSWORK+4095         NEED MORE BASE REGISTER    U012
         USNGX DCMSWORK+4095,R5         TEMPORARILY                U012
         LA    R1,ENTRLNE2              -> INPUT DATA              U016
         BAL   R9,GETCMD                MOVE DATA TO CMD BUF       U016
         CLC   CMDBUF,BLANKS            ANY COMMAND HERE?          U016
         BNE   EXECUTE                  YES - GO PROCESS           U016
         LH    R1,IOCBCUR               GET CURSOR OFFSET          U009
         LTR   R1,R1                    ANY?                       U009
         BNP   REFRESH                  NO - BACK TO NORMAL CODE   U009
         SR    R0,R0                    CLEAR FOR DIVIDE           U009
         CH    R1,=Y(MENUBIG-MENUSAVE)  CURSOR AT BIG COMMAND?     U013
         BNL   MENU01                   YES, GO USE THAT LOGIC     U013
         D     R1-1,=F'39'              COMPUTE COMMAND #          U009
         M     R1-1,=F'39'              GET OFFSET TO START OF CMD U009
         LA    R15,MENUSAVE(R1)         -> CORRECT COMMAND         U009
         MVC   ENTRLNE2(39),0(R15)      MOVE IN THE COMMAND        U016
         B     MENU02                   GO PROCESS                 U016
         SPACE 1
MENU01   SH    R1,=Y(MENUBIG-MENUSAVE)  SET OFFSET TO BIG BASE     U013
         CH    R1,=Y(ENTRLNE2-MENUBIG)  CURSOR GONE TOO FAR?       U013
         BNL   EXECUTE                  YES, GO PROCESS            U013
         D     R1-1,=F'79'              COMPUTE COMMAND #          U013
         M     R1-1,=F'79'              GET OFFSET TO START OF CMD U013
         LA    R15,MENUBIG(R1)          -> CORRECT COMMAND         U013
         MVC   ENTRLNE2(79),0(R15)      MOVE IN THE COMMAND        U016
         SPACE 1
MENU02   LA    R1,ENTRLNE2              -> INPUT DATA              U016
         BAL   R9,GETCMD                MOVE TO CMDBUF             U016
         DROPX R5                                                  U012
         B     EXECUTE                  GO PROCESS COMMAND         U016
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*  COPY PAGEABLE DCM (TDCM) TO ACCESSABLE STORAGE.                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
GETTDCM  ST    R9,SAVER9                PRESERVE RETURN REGISTER   U025
         BAL   R9,OPENCONS              GET ACCESS TO CONSOLE      U028
         LR    R7,R6                    -> SOURCE DATA             U028
         LA    R8,COPYTDCM              -> DESTINATION DATA        U028
         LA    R1,DCMEND-DCMSTRT        LENGTH TO MOVE             U028
         BAL   R9,GETCONS               COPY CONSOLE DATA          U028
         BAL   R9,CLOSCONS              DROP ACCESS TO CONSOLE     U028
         LA    R6,COPYTDCM              AND FAKE THE POINTER       U025
         L     R9,SAVER9                RESTORE RETURN REGISTER    U023
         BR    R9                       AND RETURN TO MAINLINE     U023
         SPACE 1
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  SUBROUTINE TO WRITE THE SCREEN                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
WRITE    ST    R9,SAVER9                SAVE RETURN ADDRESS        U028
         OI    IOCBFLGC,IOCBCATR        REFRESH ALL ATTR BYTES     U008
         TM    FLAG,FLASH               IN FLASH (AUTO-UPDATE) MODE?
         BZ    WRITER                   ...NO, DO WRITE/READ
*                                       ...YES, DO WRITE/NOREAD
         OI    IOCBFLGD,IOCBDYON+IOCBDFRC
*
         TRMIO IOCB,WRITE,FORMAT=SCR1,IMAGE=SCREEN
*
         B     WRITEOK                  CONTINUE                   U018
         SPACE 2
WRITER   NI    IOCBFLGD,255-IOCBDYON-IOCBDFRC
*
         TRMIO IOCB,FORMAT=SCR1,IMAGE=SCREEN
         SPACE 2
WRITEOK  MVC   CMDBUF,BLANKS            CLEAR PREVIOUS COMMAND     U028
         CLC   ERRMSG,ENTRLNE           ERRMSG COME BACK?          U018
         BE    WRITECLR                 YES, FORGET INPUT          U028
         LA    R1,ENTRLNE               -> INPUT AREA              U028
         BAL   R9,GETCMD                MOVE TO COMMAND BUFFER     U028
WRITECLR MVC   ERRMSG,BLANKS            CLEAR ERRMSG FOR NEXT TIME U018
         LA    R1,SCREEN                -> AREA TO CLEAR           U018
         LA    R0,SCR1                  -> LENGTH                  U018
         BAL   R14,@CLEAR               CLEAR THE SCREEN BUFFER    U018
         L     R9,SAVER9                RESTORE RETURN ADDRESS     U028
         BR    R9                       RETURN TO CALLER
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  MOVE INPUT DATA INTO COMMAND BUFFER                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
GETCMD   MVC   CMDBUF,BLANKS            CLEAR COMMAND BUFFER       U016
         CLC   0(L'ENTRLNE,R1),BLANKS   ANY INPUT AT ALL?          U016
         BER   R9                       NO, RETURN TO CALLER       U016
         CLC   ERRMSG,0(R1)             REALLY ERROR MSG HERE?     U016
         BE    GCMD03                   YES - GO IGNORE IT         U016
         LA    R14,0(,R1)               -> INPUT AREA              U016
         LA    R15,L'ENTRLNE            LENGTH OF INPUT AREA       U016
GCMD01   CLI   0(R14),C' '              LEADING BLANK?             U016
         BNE   GCMD02                   NO, PROCESS INPUT          U016
         LA    R14,1(,R14)              UPDATE DATA POINTER        U016
         BCT   R15,GCMD01               DECREMENT AND LOOP         U016
         EX    0,*                      SCAN LOGIC ERROR           U016
         SPACE 1
GCMD02   BCTR  R15,0                    LENGTH MINUS ONE FOR EX    U016
         EX    R15,GCMDMVC              MVC CMDBUF(0),0(R14)       U016
GCMD03   MVC   0(L'ENTRLNE,R1),BLANKS   CLEAR INPUT AREA           U016
         BR    R9                       RETURN TO CALLER           U016
         SPACE 2
GCMDMVC  MVC   CMDBUF(*-*),0(R14)       << EXECUTED >>             U016
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  ACQUIRE ACCESS TO CONSOLE BUFFERS                                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
OPENCONS ST    R9,DOUBLE                SAVE ROUTINE LINKAGE       U028
          AIF   (NOT &SP3).SKIP003      SKIP IF SP1.3 UNSUPPORTED  U028
         TM    FLAG,SP3                 ARE WE RUNNING SP3?        U028
         BZ    NOTSP3$1                 NO - JUST USE KEYZERO      U028
         BAL   R9,MODESUP               GET SUPERVISOR STATE       U028
         LH    R14,CONSASID             GET ASID OF 'CONSOLE' A.S. U028
         SSAR  R14                      SET 'CONSOLE' AS SECONDARY U028
         B     *+8                      SKIP AROUND KEYZERO        U028
NOTSP3$1 EQU   *,,C'I'                                             U028
.SKIP003  ANOP                                                     U028
         BAL   R9,KEYZERO               DO IT HIS WAY              U028
         L     R9,DOUBLE                RESTORE ROUTINE LINKAGE    U028
         BR    R9                       RETURN TO CALLER
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  DROP ACCESS TO CONSOLE BUFFERS                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
CLOSCONS ST    R9,DOUBLE                SAVE ROUTINE LINKAGE       U028
          AIF   (NOT &SP3).SKIP005      SKIP IF SP1.3 UNSUPPORTED  U028
         TM    FLAG,SP3                 ARE WE RUNNING SP3?        U028
         BZ    NOTSP3$2                 NO - JUST RESET KEY        U028
         LH    R14,OLDSASID             GET OLD SECONDARY ASID     U028
         SSAR  R14                      RESTORE OLD SECONDARY ASID U028
         BAL   R9,MODEPROB              RESTORE PROBLEM STATE      U028
         B     *+8                      SKIP AROUND KEYNZERO       U028
NOTSP3$2 EQU   *,,C'I'                                             U028
.SKIP005  ANOP                                                     U028
         BAL   R9,KEYNZERO              DO THE REST MY WAY         U028
         L     R9,DOUBLE                RESTORE ROUTINE LINKAGE    U028
         BR    R9                       RETURN TO CALLER           U028
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  GET DATA FROM CONSOLE BUFFERS                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
GETCONS  STM   R7,R8,DOUBLE             SAVE DATA ADDRESSES        U028
         LR    R0,R1                    LENGTH OF DATA             U028
GCONS01  DC    0H'0'                                               U028
          AIF   (NOT &SP3).SKIP004      SKIP IF SP1.3 UNSUPPORTED  U028
         TM    FLAG,SP3                 ARE WE RUNNING SP3?        U028
         BNO   GCONS02                  NO - NORMAL MOVE           U028
         SR    R14,R14                  CLEAR KEY                  U028
*  NOTE THAT WE ARE STILL SUPERVISOR, TO GET PAST THE KEY CHECK    U028
         MVCP  0(R0,R8),0(R7),R14       MOVE CONSOLE BUFFER LINE   U028
         LA    R15,256                  MAX LENGTH OF MOVE         U028
         B     GCONS03                  SKIP LOCAL ASN STUFF       U028
GCONS02  EQU   *,,C'I'                                             U028
.SKIP004  ANOP                                                     U028
         LR    R15,R0                   REMAINING LENGTH OF DATA   U028
         BCTR  R15,0                    LENGTH - 1                 U028
         N     R15,=XL4'7F'             LENGTH OF MOVE - 1         U028
         EX    R15,GCONSMVC             MOVE DATA                  U028
         LA    R15,1(,R15)              ACTUAL LENGTH MOVED        U028
GCONS03  AR    R7,R15                   NEXT DESTINATION LOCATION  U028
         AR    R8,R15                   NEXT SOURCE LOCATION       U028
         SR    R0,R15                   REMAINING LENGTH TO MOVE   U028
         BP    GCONS01                  MOVE NEXT HUNK             U028
GCONS04  LM    R7,R8,DOUBLE             RESTORE DATA ADDRESSES     U028
         BR    R9                       RETURN TO CALLER           U028
         SPACE 1
GCONSMVC MVC   0(0,R8),0(R7)            << EXECUTED >>             U028
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  SUBROUTINES TO CHANGE STATE                                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
KEYZERO  MODESET  KEY=ZERO                                         U011
         SPACE 1
         BR    R9                       RETURN TO CALLER           U011
         SPACE 4
KEYNZERO MODESET  KEY=NZERO                                        U011
         SPACE 1
         BR    R9                       RETURN TO CALLER           U011
         SPACE 4
MODESUP  MODESET  MODE=SUP                                         U023
         SPACE 1
         BR    R9                       RETURN TO CALLER           U023
         SPACE 4
MODEPROB MODESET  MODE=PROB                                        U023
         SPACE 1
         BR    R9                       RETURN TO CALLER           U023
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  RESTORE AX AND LEAVE                                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
          AIF   (NOT &SP3).SKIP007      SKIP IF SP3 UNSUPPORTED    U027
DCMSEXIT TM    FLAG,SP3                 RUNNING UNDER SP3?         U023
         BZ    EXIT02                   NO - SKIP                  U026
*  WE WILL NEVER FALL THRU IF NOT RUNNING SP3                      U023
         BAL   R9,MODESUP               GET SUPERVISOR STATE       U023
         SPACE 1
         AXSET AX=OLDAXVAL              RESTORE PREVIOUS AUTH INDX U023
         SPACE 1
         BAL   R9,MODEPROB              RESTORE PROBLEM STATE      U023
EXIT02   EQU   *                                                   U026
          AGO   .SKIP008                                           U027
.SKIP007  ANOP                                                     U027
DCMSEXIT EQU   *                                                   U027
.SKIP008  ANOP                                                     U027
          AIF   (&APFSVC EQ 0).SVC03                               U026
         TM    FLAG,AUTHSET             DID WE SET APF?            U026
         BNO   DCMSTOP                  NO - ALL DONE              U026
         LA    R1,8                     REQUEST RESET APF AUTH     U026
         LA    R15,16
         SVC   &APFSVC                  ISSUE MAGIC SVC            U026
.SVC03    ANOP                                                     U026
         B     DCMSTOP                  ALL DONE                   U023
         TITLE 'D I D O C S - LITERALS'                            U023
*  DCMSTART MACRO GENERATES A LITERAL WHICH MUST BE IN THE FIRST 4K
         LTORG ,                                                   U023
         TITLE 'D I D O C S - DATA AREAS AND THE LIKE'
SCR1X    DCMSFMT
SCREEN   DCMSFLD  79                    1
         DCMSFLD  79                    2
         PRINT NOGEN                                               U011
         DCMSFLD  79                    3
         DCMSFLD  79                    4
         DCMSFLD  79                    5
         DCMSFLD  79                    6
         DCMSFLD  79                    7
         DCMSFLD  79                    8
         DCMSFLD  79                    9
         DCMSFLD  79                    10
         DCMSFLD  79                    11
         DCMSFLD  79                    12
         DCMSFLD  79                    13
         DCMSFLD  79                    14
         DCMSFLD  79                    15
         DCMSFLD  79                    16
         DCMSFLD  79                    17
         DCMSFLD  79                    18
         DCMSFLD  79                    19
         DCMSFLD  79                    20
         PRINT GEN                                                 U020
MSGLINE  DCMSFLD  79                    21                         U020
ENTRLNE  DCMSFLD  79,ALPHA,CURSOR=YES   22                         U020
MODELINE DCMSFLD  79                    23                         U020
         DCMSFEND
SCR1L    EQU   *-SCR1X                  LENGTH OF SCREEN DESCRIPT. U012
         SPACE 3
MENUSCRX DCMSFMT  ,                                                U009
MENUSAVE DCMSFLD  39                    1                          U009
         PRINT NOGEN                                               U011
         DCMSFLD  39                                               U009
         DCMSFLD  39,ALPHA              2                          U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              3                          U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              4                          U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              5                          U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              6                          U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              7                          U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              8                          U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              9                          U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              10                         U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              11                         U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              12                         U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              13                         U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              14                         U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              15                         U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              16                         U009
         DCMSFLD  39,ALPHA                                         U009
         DCMSFLD  39,ALPHA              17                         U009
         DCMSFLD  39,ALPHA                                         U009
MENUBIG  DCMSFLD  79,ALPHA              18                         U021
         DCMSFLD  79,ALPHA              19                         U013
         DCMSFLD  79,ALPHA              20                         U018
         DCMSFLD  79                    21                         U009
         PRINT GEN                                                 U011
ENTRLNE2 DCMSFLD  79,ALPHA,CURSOR=YES   22                         U020
SHOWCMD2 DCMSFLD  79                    23                         U009
         DCMSFEND
MENUSCRL EQU   *-MENUSCRX               LENGTH OF SCREEN DESCRIPT. U012
         SPACE 2
HEXTAB   EQU   *-C'0'
         DC    C'0123456789ABCDEF'
         SPACE 2
WTOMASK  WTO   '*** USERIDX ==> 000000000011111111112222222222333333333$
               3444444444455555555556666666666777777777',          U012$
               MF=L,MCSFLAG=REG0                                   U012
WTOL     EQU   *-WTOMASK                                           U012
         SPACE 2
MSGSIM   LC    '/D/C/M/S SIMULATED CONSOLE USING 3270 CONSOLE AT ' U023
         SPACE 1
MSGLOC   LC    '<<< /LOCATION >>> /TYPE  /ADDR  /ID  /STAT  /CMDAUTH  /$
               ROUTCDE'                                            U023
         SPACE 2
NAMES    CSECT                                                     U027
LOCTABLE LC    '23/D/S/C/G3 /MAIN      ',L=3+15                    U029
         LC    '23/F/S/C/G3 /PRINT AREA',L=3+15                    U029
         LC    '220/S/C/G3 /USER /WIND ',L=3+15                    U029
         LC    '340/S/C/G3 /TAPE       ',L=3+15                    U029
         LC    '0164341 /MASTER        ',L=3+15                    U029
         LC    '0174341 /ALTERNATE     ',L=3+15                    U029
         LC    '34/E/S/C/G3 /COLOR     ',L=3+15                    U029
         LC    '640/S/C/G4 /MAIN       ',L=3+15                    U029
         LC    '641/S/C/G4 /TAPE       ',L=3+15                    U029
         LC    '/J/E/S** /TEST **      ',L=3+15                    U029
         DC    20CL15' '                PATCH SPACE                U023
         DC    CL3' '                   FLAG END OF TABLE          U029
         SPACE 2
CONS     CSECT                          RESUME                     U027
         SPACE 1                                                   U027
BLANKTAB DC    CL256' '
         ORG   BLANKTAB+C'Ö'
         DC    C'Ö.<(+×',X'50'
         ORG   BLANKTAB+C'!'
         DC    C'!$*);^-/'
         ORG   BLANKTAB+C','
         DC    C',%_>?'
         ORG   BLANKTAB+C':'
         DC    C':#@''="'
         ORG   BLANKTAB+X'81'
         DC    X'818283848586878889'                               U001
         ORG   BLANKTAB+X'91'      ...
         DC    X'919293949596979899'                               U001
         ORG   BLANKTAB+X'A2'      ...
         DC    X'A2A3A4A5A6A7A8A9'                                 U001
         ORG   BLANKTAB+C'A'
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+C'J'
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+C'S'
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'0'
         DC    C'0123456789'
         ORG
         SPACE 4
AUTHTBL  DC    C'INFO   '               0:  INFO ONLY              U022
         DC    C'CON    '               1:  UCMAUTH3               U018
         DC    C'I/O    '               2:  UCMAUTH2               U018
         DC    C'I/O+CON'               3:  UCMAUTH2+UCMAUTH3      U018
         DC    C'SYS    '               4:  UCMAUTH1               U018
         DC    C'SYS+CON'               5:  UCMAUTH1+UCMAUTH3      U018
         DC    C'SYS+I/O'               6:  UCMAUTH1+UCMAUTH2      U018
         DC    C'ALL    '               7:  UCMAUTH1+2+3           U018
         SPACE 4
GRAFTYPE DC    C'2250'                  UCBTYPE=XXXX1002           U023
         DC    C'2260'                  UCBTYPE=XXXX1003           U023
         DC    C'????'                  UCBTYPE=XXXX1004           U023
         DC    C'????'                  UCBTYPE=XXXX1005           U023
         DC    C'????'                  UCBTYPE=XXXX1006           U023
         DC    C'????'                  UCBTYPE=XXXX1007           U023
         DC    C'3066'                  UCBTYPE=XXXX1008           U023
         DC    C'3270'                  UCBTYPE=XXXX1009           U023
         DC    C'3284'                  UCBTYPE=XXXX100A           U023
         DC    C'3286'                  UCBTYPE=XXXX100B           U023
         DC    C'3158'                  UCBTYPE=XXXX100C           U023
         DC    C'3036'                  UCBTYPE=XXXX100D           U023
         DC    C'????'                  UCBTYPE=XXXX100E  PATCH    U023
         SPACE 2
URTYPE   DC    X'01',C'2540R'           UCBTYPE=XXXX0801           U023
         DC    X'04',C'2501 '           UCBTYPE=XXXX0804           U023
         DC    X'05',C'2520 '           UCBTYPE=XXXX0805           U023
         DC    X'06',C'3505 '           UCBTYPE=XXXX0806           U023
         DC    X'08',C'1403 '           UCBTYPE=XXXX0808           U023
         DC    X'09',C'3211 '           UCBTYPE=XXXX0809           U023
         DC    X'0A',C'1443 '           UCBTYPE=XXXX080A           U023
         DC    X'0B',C'3203 '           UCBTYPE=XXXX080B           U023
         DC    X'0C',C'3525 '           UCBTYPE=XXXX080C           U023
         DC    X'20',C'1052 '           UCBTYPE=XXXX0820           U023
         DC    X'22',C'3210 '           UCBTYPE=XXXX0822           U023
         DC    X'23',C'3215 '           UCBTYPE=XXXX0823           U023
         DC    X'30',C'3213 '           UCBTYPE=XXXX0830           U023
         DC    X'FF'                    END OF TABLE INDICATOR     U023
         SPACE 4
HELPIMG  CSECT                                                     U008
         DC    CL79'*** CONS - HELP ***            (ANY PFKEY NOT LISTE$
               D IS IGNORED)               '                       U024
         DC    CL79' '                                             U008
         DC    CL79'PF1  / PF13  - H    - HELP (THIS SCREEN)'      U024
         DC    CL79'PF2  / PF14  - A    - START AUTO UPDATING'     U024
         DC    CL79'PF3  / PF15  - E    - EXIT'                    U024
         DC    CL79'PF5  / PF17  - M    - DISPLAY MENU OF CANNED COMMAN$
               DS'                                                 U024
         DC    CL79'PF6  / PF18  -      - REPROMPT WITH LAST COMMAND EN$
               TERED'                                              U024
         DC    CL79'PF7  / PF19  - U    - SELECT TOP OF LARGE CONSOLE' $
                                                                   U028
         DC    CL79'PF8  / PF20  - D    - SELECT BOTTOM OF LARGE CONSOL$
               E'                                                  U028
         DC    CL79'PF9  / PF21  - S    - SELECT ANOTHER CONSOLE'  U028
         DC    CL79'PF10 / PF22  - L    - LIST CONSOLE STATUS'     U024
         DC    CL79'PF12 / PF24  - E    - EXIT'                    U024
         DC    CL79'ATTN / PA1   -        STOP AUTO UPDATING'      U024
HELPIMGL EQU   *-HELPIMG                LENGTH OF HELP SCREEN      U008
         SPACE 3
MENULIST CSECT                                                     U009
         DC    CL39'(ANY COMMAND BELOW CAN BE MODIFIED.  IT'    1  U009
         DC    CL39'WILL BE REMEMBERED UNTIL LEAVING CONS) '       U009
         DC    CL39'                                       '    2  U009
         DC    CL39'                                       '       U009
         DC    CL39'/V 451,ONLINE                          '    3  U024
         DC    CL39'/M 451,VOL=(SL,NEWRES),USE=PRIVATE     '       U024
         DC    CL39'/M ___,VOL=(SL,______),USE=PRIVATE      '   4  U024
         DC    CL39'/M 459,VOL=(SL,OLDRES),USE=PRIVATE      '      U024
         DC    CL39'/V ___,OFFLINE                         '   5   U024
         DC    CL39'                                       '       U009
         DC    CL39'                                       '    6  U009
         DC    CL39'                                       '       U009
         DC    CL39'/D NET,ID=___                          '    7  U024
         DC    CL39'/V NET,ACT,ID=___                      '       U024
         DC    CL39'/D NET,CLSTRS                          '    8  U024
         DC    CL39'/V NET,INACT,ID=___                    '       U024
         DC    CL39'/D NET,LINES                           '    9  U024
         DC    CL39'/F TSO,USERMAX=0                       '       U024
         DC    CL39'/D NET,APPLS                           '   10  U024
         DC    CL39'                                       '       U009
         DC    CL39'/D NET,BFRUSE                          '   11  U024
         DC    CL39'                                       '       U009
         DC    CL39'/D NET,MAJNODES                        '   12  U024
         DC    CL39'                                       '       U009
         DC    CL39'                                       '   13  U009
         DC    CL39'                                       '       U009
         DC    CL39'                                       '   14  U009
         DC    CL39'                                       '       U009
         DC    CL39'                                       '   15  U009
         DC    CL39'                                       '       U009
         DC    CL39'                                       '   16  U009
         DC    CL39'                                       '       U009
         DC    CL39'                                       '   17  U009
         DC    CL39'                                       '       U009
         DC    CL79'$VS,''V ___,CONSOLE'',''V NET,INACT,I,ID=___''' U18
         DC    CL79'/V ___,CONSOLE,ROUT=(1,2,3,4,5,6,7,8,9,10,12,13,14,$
               15)                          '                  19  U024
         DC    CL79'                                       '   20  U009
         LC    '/POSITION CURSOR AT COMMAND ABOVE, OR ENTER ANY COMMAND$
                OR SUBCOMMAND BELOW',L=79                      21  U024
         DC    CL79'                                       '   22  U009
         DC    CL79'IEE163I MODE= MENU                     '   23  U009
MENULEN  EQU   *-MENULIST               LENGTH OF MENU SCREEN      U009
         SPACE 3
         IEETDCM  ,                     TRANSIENT DCM (TDCM)       U025
         SPACE 3
CONS     CSECT                          RESUME MAIN CSECT          U008
         SPACE 4
DCMSTOP  DCMSTOP  RC=(15),LIT=NO                                   U023
SAVER9   EQU   PARMADDR                 REUSE IDLE FIELD           U028
ADDRUCM  DC    A(0)                     -> CURRENT UCM             U009
*
ADRBOT   DC    A(0)                     ADDRESS OF BOTT OF SCREEN  U028
ADRL21   DC    A(0)                     ADDRESS OF LINE 21         U028
CONSASID DC    H'0'                     ASID OF 'CONSOLE'          U023
OLDSASID DC    H'0'                     PREVIOUS SECONDARY ASID    U023
OLDAXVAL DC    H'0'                     PREVIOUS AUTH INDEX        U023
LINELEN  DC    H'0'                     LENGTH OF LINE IN MEMORY   U028
*  FOLLOWING 3 LINES *MUST* BE TOGETHER
CMND     DC    F'0'
CMDBUF   DC    CL79' '
CMNDLEN  EQU   *-CMND
WTO      DC    CL(WTOL)' '                                         U012
SAVECMD  DC    CL79' '                                             U012
         DC    CL1 ' '
DISPCMD  DC    CL79' '                                             U013
ERRMSG   DC    CL79' '                                             U012
REPLID   DC    CL2' '                                              U013
         SPACE 1
COPYTDCM DS    CL(DCMEND-DCMSTRT)       TDCM COPIED FROM ...       U023
*                                       ... 'CONSOLE' ADDR SPACE   U027
         SPACE 1
*  FOLLOWING 2 LINES *MUST* BE TOGETHER
UNITXXX  DC    CL3' ',C' '
UNITCONS DC    CL9' '                   MAYBE '*MSTCONS*'          U001
         SPACE 1
XUCMID   DC    X'00'
FLAG     DC    X'00'
FLASH    EQU   X'80'                    AUTO-UPDATE MODE
MENUFLAG EQU   X'40'                    RETURN TO MENU SCREEN      U010
QUIET    EQU   X'20'                    CURRENTLY IN QUIET MODE    U012
LARGCON  EQU   X'10'                    SELECTED CONSOLE IS LARGE  U014
DISPBOT  EQU   X'08'                    BOTTOM OF SCREEN SELECTED  U014
SP3      EQU   X'04'                    RUNNING ON SP3             U023
AUTHSET  EQU   X'02'                    SVC USED TO SET APF AUTH   U026
         SPACE 2
         DC    0H'0'                                               U012
SCR1     DC    XL(SCR1L)'00'            ATTRIBUTE BYTES            U012
         DC    0H'00'                                              U012
MENUSCR  DS    XL(MENUSCRL)             ATTRIBUTE BYTES            U012
         SPACE 2
         DSGEN SCR1X                                               U009
         SPACE 2
         DSGEN MENUSCRX                                            U009
         SPACE 2
WORKLEN  EQU   *-DCMSWORK                                          U012
         EJECT
LCLINE   DSECT                          FOR "DISPLAY CONSOLES"     U018
LC@LOC   DS    CL15'LOCATION DESCRIPTION'                          U023
         DS    CL2                                                 U023
LC@TYPE  DS    CL6'3066'                                           U018
         DS    CL1                                                 U020
LC@ADDR  DS    CL3'012'                                            U018
         DS    CL2                                                 U018
LC@ID    DS    CL2'01'                                             U018
         DS    CL2                                                 U018
LC@STAT  DS    CL5'M,J,P'                                          U018
         DS    CL1                                                 U020
LC@AUTH  DS    CL7'ALL'                                            U018
         DS    CL2                                                 U018
LC@ROUT  DS    CL4'FFFF'                                           U018
         DS    CL4                                                 U020
LCLINEL  EQU   *-LCLINE                 LENGTH OF DISPLAY LINE     U020
         TITLE 'MVS UCM DEFINITION'
         IEECUCM FORMAT=NEW
         TITLE 'MVS RESIDENT DCM DEFINITION (RDCM)'
         IEERDCM
         TITLE 'MVS CVT DEFINITION'
         CVT   DSECT=YES
         TITLE 'MVS ASVT DEFINITION'                               U023
         IHAASVT  ,                                                U023
         TITLE 'MVS ASCB DEFINITION'                               U023
         IHAASCB  ,                                                U023
         TITLE 'TSO PSCB DEFINITION'                               U001
         IKJPSCB ,                                                 U001
         SPACE 2                                                   U001
         END
