         MACRO
&NAME    TSOUSER &LIST
         LCLA  &A,&B,&C
         LCLC  &N,&X
&N       SETC  '&NAME'
&C       SETA  N'&SYSLIST
&B       SETA  1
.L1      AIF   (&B GT &C).MEND
&A       SETA  K'&SYSLIST(&B)
&X       SETC  '&SYSLIST(&B)'.'             '
         AIF   (&A LE 7).X1             OK
         MNOTE 8,'&SYSLIST(&B) TRUNCATED TO SEVEN CHARS'
&A       SETA  7
.X1      ANOP
&N       DC    AL1(&A),CL7'&X' .        TABLE ENTRY
&N       SETC  ''
&B       SETA  &B+1
         AGO   .L1
.MEND    MEND
         SPACE 3
         MACRO
&NAME    PROTDSN &LIST
         LCLA  &A,&B,&C
         LCLC  &N
&N       SETC  '&NAME'
&B       SETA  1
&C       SETA  N'&SYSLIST
.L1      AIF   (&B GT &C).MEND
         AIF   ('&SYSLIST(&B)' EQ '*').MENDX
&A       SETA  K'&SYSLIST(&B)-1
&N       DC    AL1(&A),C'&SYSLIST(&B)'
&B       SETA  &B+1
&N       SETC  ''
         AGO   .L1
.MENDX   AIF   (&B LT &C).MNOTE
.GEN     ANOP
&N       DC    X'FF' .                  END OF LIST
         MEXIT
.MNOTE   MNOTE 4,'''*''' BEFORE END OF LIST, TRUNCATED'
         AGO   .GEN
.MEND    MEND
         TITLE 'DSNCHECK - SPNB DATA SET NAME CHECK ROUTINE'
DSNCHECK CSECT
         REGS
         EJECT
*
*
*        ATTRIBUTES:   PROBLEM STATE, PROBLEM KEY, ENABLED, RE-ENTRANT
*
*
         SPACE 2
*
*        CALLING SEQUENCE:
*        LA    1,DSNAME
*        IF FOR OUTPUT, LCR 1,1
*        LINK  EP=DSNCHECK,PARAM=((1))
*        (OR ANYTHING THAT GOES TO THIS MODULE)
*        1 POINTS TO A 54 BYTE DATA SET NAME .DS(MEMBER)
*
         SPACE 3
*
*        RETURNS
*        (IF A PARTITIONED DATA SET, THE RETURN CODE IS NEGATED;
*          I.E.  DSN(MEMBER)     )
*        R15 = 0  - THIS IS THE USER'S DATA SET
*        R15 = -2 - THIS IS THE USER'S DATA SET WITH MEMBER NAME
*        R15 = 4  - THIS DATA BELONGS TO HIS GROUP (E.G.
*                   REL1 ACCESSING REL.SRCLIB)  ** OR **
*                   THIS IS A TECH. SUPPORT SPECIAL PERSON
*        R15 = 8  - THIS IS A SYSTEM DATA SET
*        R15 = 12 - USER MAY NOT USE THIS DATA SET
*        R15 = 16 - DATA SET NAME IS INVALID.
*        R15 = 20 - INVALID INPUT OR CATASTROPHIC ERROR.
*
         SPACE 3
         STM   14,12,12(13)             SAVE REGISTERS
         BALR  R12,0                    GENERATE A BASE
         USING *,R12                    TELL ASSEMBLER
         TITLE 'DSNCHECK - WRKAREA DSECT'
WRKAREA  DSECT
USERLEN  DC    AL1(0)                   LENGTH OF USERID
USERID   DC    CL7'IRVING'              USER ID
EXTRACT  EXTRACT *-*,'S',FIELDS=PSB,MF=L
PSB      DC    A(0)                     ADDRESS OF PSCB
BITS     DC    X'0000'                  USER PRIVS
FLAGS    DC    X'0000'                  MY CNTL FLAGS
         SPACE 1
OUTF     EQU   X'80'                    OUTPUT CALL
MEMF     EQU   X'40'                    MEMBER IN DATA SET
PARF     EQU   X'20'                    L PAREN FOUND
DOTF     EQU   X'10'                    LAST CHAR WAS '.'
SYSF     EQU   X'08'                    SYSTEM DATA SET
         SPACE 1
ACTBIT   EQU   X'40'                    ACCOUNTING USER
JCLBIT   EQU   X'20'                    MAY SUBMIT
OPRBIT   EQU   X'80'                    HAS OPR PRIV
WRKLEN   EQU   *-WRKAREA                LENGTH
         TITLE 'DSNCHECK - INITIALIZATION'
DSNCHECK CSECT
         LR    R3,R1                    SAVE PARM FIELD
         GETMAIN R,LV=WRKLEN            GET LENGTH
         LR    R10,R1                   SAVE ADDRESS
         XC    0(WRKLEN,R1),0(R1)       CLEAR FIELD
         USING WRKAREA,R10              TELL ASSEMBLER
         LTR   R3,R3                    TEST ENTRY CODE
         LA    R4,1                     CONSTANT INCREMENT
         BZ    RET20                    ERROR IN INPUT
         BP    *+10                     BRANCH AROUND GARBAGE
         OI    FLAGS,OUTF               SAY FOR OUTPUT
         LPR   R3,R3                    MAKE IT POSITIVE
         LR    R11,R3                   SAVE POINTER
         LA    R5,53(,R3)               GET END FOR BXLE
         SPACE 3
SETBLK   CLI   0(R3),C' '               FIND FIRST BLANK
         BNE   SETBXLE                  NOT IT
SETBLK1  MVI   0(R3),C' '               CLEAR REST TO BLANK
         BXLE  R3,R4,SETBLK1            CONTINUE
         B     BEGIN                    CONTINUE PROCESSING
SETBXLE  BXLE  R3,R4,SETBLK             TRY FOR NEXT CHAR
         TITLE 'DSNCHECK - GET USERID AND STATUS, SCAN NAME'
BEGIN    EXTRACT  PSB,'S',FIELDS=PSB,MF=(E,EXTRACT)
         L     R15,PSB                  GET ADDRESS
         LA    R15,0(,R15)              OFF HI-ORDER BITS
         LTR   R15,R15                  TEST TO SEE
         BNP   RET20                    INVALID CALLER
         MVC   USERID(7),0(R15)         MOVE IN ID
         MVC   USERLEN(1),7(R15)        SAME
         MVC   BITS(2),16(R15)          PRIV BITS
         SPACE 3
         TRT   0(54,R11),TABLE          CHECK IT
TRTX2    BNZ   RET16                    OOPS *BMD*
         SPACE 1
         LR    R3,R11                   SAVE POINTER
         LR    R14,R11                  BEGIN FOR CHEQUE
MAKEUP   OI    0(R3),X'40'              MAKE IT UC
         CLI   0(R3),C'.'
         BE    DOTS
         CLI   0(R3),C'('
         BE    LP
         CLI   0(R3),C')'
         BE    RP
         CLI   0(R3),C' '
         BE    DONE
         NI    FLAGS,255-DOTF           TURN OFF DOT LAST
MAKEBXLE BXLE  R3,R4,MAKEUP             TRY AGAIN
         B     DONE                     DONE
         SPACE 3
DOTS     TM    FLAGS,DOTF               CHECK DOT FLAG
         BO    RET16                    INVALID DATA SET NAME
         OI    FLAGS,DOTF               TURN IT ON
         LR    R15,R3                   SAVE POINTER
         SR    R15,R14                  GET LENGTH
         CH    R15,=H'9'                CHECK IT
         LR    R14,R3                   SAVE POINTER
         BH    RET16                    INDEX TOO LONG
         B     MAKEBXLE                 TRY AGAIN
         SPACE 2
LP       TM    FLAGS,PARF               ANYTHING
         BO    RET16                    NO-NO
         TM    FLAGS,DOTF               LAST A '.'?
         BO    RET16                    YES, ERROR
         OI    FLAGS,MEMF+PARF          MEMBER NAME IMPLIED
         LA    R9,1(,R3)                POINT AT NEXT
         B     MAKEBXLE                 TRY AGAIN
         SPACE 3
RP       TM    FLAGS,PARF               ANYTHING?
         BNO   RET16                    NOPE, ERROR
         LR    R15,R3                   GET POINTER
         SR    R15,R9                   GET LENGTH
         BNP   RET16                    BAD NAME
         CH    R15,=H'9'                CHECK IT
         BH    RET16                    ERROR
         CLI   1(R3),C' '               END OF NAME
         BE    CNTU                     TRY
         AR    R3,R4                    ADD ONE
         CR    R3,R5                    SEE IF END
         BH    CNTU
         B     RET16                    ERROR
         SPACE 3
DONE     TM    FLAGS,PARF+DOTF          CHECK END
         BNZ   RET16                    ERROR
         SR    R3,R14                   GET END
         BNP   RET16                    ERROR
         CH    R3,=H'9'                 CHECK LEN
         BH    RET16                    ERROR
         TITLE 'DSNCHECK - CHECK A POSSIBLY VALID DSN'
CNTU     XR    R2,R2                    CLEAR IT FOR LATER
         IC    R2,USERLEN               GET LENGTH FIELD
         BCTR  R2,0                     -1 FOR EXECUTE
         EX    R2,CLC1                  COMPARE
         BNE   CNTU1                    TRY AGAIN
         LA    R2,1(R2,R11)             GET A DOT
         CLI   0(R2),C'.'               IS IT?
         BE    RET0                     YES
         B     CK1                      NOT HIM EXACTLY
         SPACE 1
CNTU1    CLC   0(3,R11),USERID          HIS GROUP?
         BNE   CK1                      NO, CONTINUE CHECKING
         CLI   3(R11),C'.'              IS IT 'XXX.' ? *BMD*
         BE    CKUSERS                  YES. PROD DS, CK PUCT *BMD*
         LA    R14,RET4                 GET GROUP RETURN CODE
         B     CKDSN                    SEE IF RESTRICTED
         SPACE 3
CK1      CLC   =CL9'NULLFILE ',0(R11)   DUMMY DATA SET?
         BE    RET0                     YES, LET HIM HAVE IT
         CLI   0(R11),C'*'              REQUEST FOR TERMINAL?
         BNE   SYSCHK                   NO
         CLI   1(R11),C' '              IS IT?
         BE    RET0                     YES, IT'S OK
         B     RET16                    INVALID DSNAME
SYSCHK   TM    FLAGS,MEMF               MEMBER NAME SPECIFIED?
         BNO   SYSCHK1                  NO
         CLI   0(R11),C'('              JUST A MEMBER NAME?
         BE    RET16                    YES, FORGET IT
SYSCHK1  CLC   =CL3'SYS',0(R11)         SYSTEM DATA SET?
         BNE   CKDSNX                   NO, CHECK FOR RESTRICTED
         LA    R9,7(,R11)               END
         LR    R8,R4                    ONE
         LA    R7,3(,R11)               START OF NUMBERS
SYSDS    CLI   0(R7),C'0'               NUMBER?
         BL    CKUSERX                  CHECK USER ID
         BXLE  R7,R8,SYSDS              TRY AGAIN
         B     RET0                     TEMP DATA SET
         SPACE 2
CLC1     CLC   USERID(0),0(R11)         <<EXECUTED>>
         SPACE 2
CKUSERX  TM    FLAGS,OUTF               OUTPUT REQUEST?
         BO    CKUSERS                  YES, SYS DS
         BAL   R14,CKDSN                CHECK FOR RESTRICT
         B     RET8                     OK, JUST INPUT
         TITLE 'DSNCHECK - RESTRICT SUBROUTINES'
CKUSERS  OI    FLAGS,SYSF               TURN ON FLAG
CKUSER   LOAD  EPLOC=@PUT               GET PRIV USER TAB
         LR    R15,0                    GET ADDRESS HERE
         LM    R7,R9,0(R15)             GET PRIV DSN USERS
         SPACE 1
CQ1      CLC   0(8,R7),USERLEN          CHECK FOR MATCH
         BNE   CQ1BXLE                  NOT HIM
         TM    FLAGS,SYSF               SYSTEM DS?
         BO    BRRET8                   YES
         B     BRRET4                   NO
CQ1BXLE  BXLE  R7,R8,CQ1                TRY AGAIN
BRRET12  DELETE EPLOC=@PUT              DELETE THE TABLE
         B     RET12                    TUFF
         SPACE 3
CKDSNX   LA    R14,CKSUM                DEFAULT PLACE
         SPACE 1
CKDSN    LA    R3,DSNTAB                LOAD TABLE
         XR    R2,R2                    CLEAR FOR IC LATER <BMD>
CKD      CLI   0(R3),X'FF'              END INDICATOR?
         BER   R14                      DONE
         IC    R2,0(R3)                 GET LENGTH
         EX    R2,CLC2                  TRY IT
         LA    R3,2(R2,R3)              GET NEXT ENTRY
         BE    CKUSERS                  SYSTEM DATA SET, RESTRICTED
         B     CKD                      TRY AGAIN
         SPACE 3
*KSUM    TM    BITS,OPRBIT+ACTBIT+JCLBIT       CHECK IT *BMD*
*        BO    RET4                     OK, WE CAN'T STOP HIM *BMD*
CKSUM    LOAD  EPLOC=@PUT               LOAD PRIV USER TAB *BMD*
         LR    R15,0                    GET TABLE ADDRESS
         LM    R7,R9,0(R15)             GET INFO
FINCHK   CLC   0(8,R7),USERLEN          SAME?
         BE    BRRET4
         BXLE  R7,R8,FINCHK             TRY AGAIN
         B     BRRET12                  FORGET IT, HE'S NOT IN TABLE
BRRET8   LA    R2,1                     LOAD RET CODE INDIC
         B     BRRETC
BRRET4   XR    R2,R2                    CLEAR RET CODE INDIC
BRRETC   DELETE EPLOC=@PUT              DELETE TABLE
         LTR   R2,R2                    TEST INDICATOR
         BP    RET8
         BZ    RET4
         B     RET12
CLC2     CLC   0(0,R11),1(R3)           <<EXECUTED>>
         TITLE 'DSNCHECK - RETURN'
RET20    LA    R2,20
         B     CRET
RET16    LA    R2,16
         CR    R1,R11                   COMPARE POINTER *BMD*
         BNE   CRET                     NOT SPEC. CASE *BMD*
         CLI   0(R1),C'&&'              TEMP DSN?
         BNE   CRET                     NO. END *BMD*
         TRT   1(53,R11),TABLE          DO TRANSLATE *BMD*
         BZ    RET0                     VALID TEMP NAME *BMD*
         B     CRET                     FORGET IT *BMD*
*        B     CRET                     *BMD*
RET12    LA    R2,12
         B     CRET
RET8     LA    R2,8
         B     CRET
RET4     LA    R2,4
         B     CRET
RET0     XR    R2,R2                    CLEAR RET TO ZERO
CRET     TM    FLAGS,MEMF               MEMBER
         BNO   FREEMAIN                 NO
         LNR   R2,R2
         BNZ   FREEMAIN                 OK TO CONTINUE
         LA    R2,2                     LOAD CODE
         LNR   R2,R2                    SET IT NEGATIVE
FREEMAIN LR    1,R10                    WORKAREA ADDRESS
         FREEMAIN R,LV=WRKLEN,A=(1)     DO IT
         LR    15,R2                    LOAD RETURN CODE
         RETURN (14,12),,RC=(15)       RETURN TO CALLER
         TITLE 'DSNCHECK - LITERALS AND CONSTANTS'
IRVING   LTORG
TABLE    DC    256AL1(12)               MAKE ALL INVALID
         ORG   TABLE+C' '
         DC    AL1(0)
         ORG   TABLE+C'*'
         DC    AL1(0)
         ORG   TABLE+C'('
         DC    AL1(0)
         ORG   TABLE+C')'
         DC    AL1(0)
         ORG   TABLE+C'A'-X'40'
         DC    X'000000000000000000'
         ORG   TABLE+C'J'-X'40'
         DC    X'000000000000000000'
         ORG   TABLE+C'S'-X'40'
         DC    X'0000000000000000'
         ORG   TABLE+C'A'
         DC    X'000000000000000000'
         ORG   TABLE+C'J'
         DC    X'000000000000000000'
         ORG   TABLE+C'S'
         DC    X'0000000000000000'
         ORG   TABLE+C'#'
         DC    X'00'
         ORG   TABLE+C'-'
         DC    X'00'
         ORG   TABLE+C'$'
         DC    X'00'
         ORG   TABLE+C'@'
         DC    X'00'
         ORG   TABLE+C'0'
         DC    X'00000000000000000000'
         ORG   TABLE+C'+'
         DC    X'00'
         ORG   TABLE+C'.'
         DC    X'00'
         ORG
         SPACE 3
@PUT     DC    CL8'#SPBTAB#'
         SPACE 3
DSNTAB   DC    100X'FF'                 TABLE SPACE
         ORG   DSNTAB                   NOW OVERLAY WITH TABLE
         PROTDSN SYS1.UADS,SYS1.NUCLEUS,                               +
               SYS1.SVCLIB,SYS1.MAN,*
         ORG
         SPACE 3
         END
