JES2AUTO CSECT
***********************************************************************
*
*  AUTOCMD:
*      WRITTEN BY S. HANSEN
*      GTE LABORATORIES
*      40 SYLVAN ROAD
*      WALTHAM, MASSACHUSETTS
*      MARCH 1, 1978
*
*  REVISED FOR SANTA FE INTERNATIONAL BY:
*      GENE L. NOBLE
*      SANTA FE DRILLING COMPANY
*      505 SOUTH MAIN STREET
*      ORANGE, CALIFORNIA
*      APRIL 29, 1983
*
*  PURPOSE:
*      THIS PROGRAM WAS WRITTEN FOR THE PURPOSE OF ISSUING AUTOMATIC
*  JES2 COMMANDS BASED ON DAY OF WEEK OR WEEK OF MONTH.  WE FOUND THAT
*  SUPPORT FOR TIME OF DAY PROCESSING WAS NOT SUFFICIENT FOR OUR NEEDS
*  AT GTE LABS BECAUSE MANY JOBS NEEDED TO BE ISSUED ONLY ON SPECIFIC
*  DAYS.  AS A RESULT, THIS PROGRAM AND 'DAYDATE', THE PROGRAM WHICH
*  COMPUTES THE ACTUAL DAY OF WEEK AND WEEK OF MONTH, WERE WRITTEN TO
*  PROVIDE SUCH SUPPORT.
*
*  INPUT:
*     THE INPUT TO AUTOCMD IS A FILE OF STANDARD JES2 AUTOMATIC
*  COMMANDS WITH ADDITIONAL PARM BEGINNING IN COLUMN 60.
*
*  PARM OPTIONS:
*     TIME OF DAY: AS IN THE STANDARD AUTOMATIC COMMAND, TIME OF DAY
*  FOR ISSUING THE COMMAND MAY BE SPECIFIED BY 'T=HH.MM' IN COLUMN 6
*  WHERE HH.MM ARE THE HOUR AND MINUTES ON THE 24-HR CLOCK.  IF THE
*  TIME OF DAY SPECIFIED HAS ALREADY OCCURRED, PGM=AUTOCMD WILL NOT
*  ISSUE THAT COMMAND.
*
*     DAY OF WEEK: THE DAYS OF THE WEEK FOR ISSUING A COMMAND CAN BE
*  SPECIFIED BY ENTER 'DXXX' IN COL 60 OF THE COMMAND WHERE 'XXX' IS A
*  STRING OF LENGTH 1-6 COMPRISED OF DIGITS 1-7, (EX. D15 - ISSUE
*  COMMAND ONLY ON MONDAYS AND FRIDAYS). IF NOTHING IS GIVEN IN THE
*  COMMENT FIELD, THE COMMAND WILL BE ISSUED DAILY.
*
*     WEEK OF MONTH: THE WEEK OF THE MONTH FOR ISSUING A GIVEN COMMAND
*  CAN BE SPECIFIED BY ENTERING 'WYYY' IN COL 60 OF THE COMMAND WHERE
*  'YYY' IS STRING OF LENGTH 1-4 COMPRISED OF DIGITS 1-5, WHERE 1 IS
*  MONDAY, OR LETTERS 'A' OR 'L', WHERE 'A' SPECIFIES ALL WEEKS EXCEPT
*  THE LAST AND 'L' SPECIFIES ONLY THE LAST WEEK.  (EX1. W13 - ISSUE
*  COMMAND ONLY DURING THE FIRST AND THIRD WEEK OF THE MONTH.
*  EX2. WA - ISSUE COMMAND ON WEEKS 1-4 IN A 5-WEEK MONTH AND ON WEEKS
*  1-3 IN A 4-WEEK MONTH. EX3. WL - ISSUE COMMAND ON WEEK 5 IN A 5-WEEK
*  MONTH AND ON WEEK 4 IN A 4-WEEK MONTH.)
*
***********************************************************************
         EJECT
***********************************************************************
*
*     DAY OF MONTH: PARM FOR BOTH DAY AND WEEK CAN BE SPECIFIED FOR ANY
*  COMMAND BY ENTER 'DXXXWYYY' IN COL. 60.  IN THIS CASE, CONDITIONS
*  MUST BE SATISFIED FOR BOTH DAY OF WEEK AND WEEK OF MONTH IN ORDER
*  FOR THE COMMAND TO BE ISSUED.  (EX1. D1W13 - ISSUE COMMAND ONLY ON
*  MONDAY OF THE FIRST AND THIRD WEEKS OF THE MONTH.  EX2. D5WL - ISSUE
*  COMMAND ONLY ON FRIDAY OF THE LAST WEEK OF THE MONTH.)  SEE NOTE.
*
*  ***  NOTE: WHEN SPECIFYING DAY OF MONTH PROCESSING, CONSIDER
*  CAREFULLY THE ALGORITHM FOR DETERMINING THE CURRENT WEEK OF THE
*  MONTH. (I.E., THE FIRST WEEK IS THE WEEK ENDING ON SUNDAY WHICH
*  CONTAINS THE FIRST FRIDAY OF THE MONTH.)  WHEN THE FIRST OF THE
*  MONTH HAPPENS TO FALL NEAR THE END OF WEEK, BY THIS ALGORITHM,
*  MONDAY - THURSDAY MAY NOT EVEN OCCUR DURING W1. ***
*
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        GENE'S STANDARD REGISTER EQUATES                             *
*                                                                     *
***********************************************************************
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
R00      EQU   00
R01      EQU   01
R02      EQU   02
R03      EQU   03
R04      EQU   04
R05      EQU   05
R06      EQU   06
R07      EQU   07
R08      EQU   08
R09      EQU   09
         EJECT
***********************************************************************
*        COMMON OS ENTRY LINKAGE                                      *
***********************************************************************
         SPACE 2
         STM   14,12,12(13)  SAVE CALLERS REGISTERS
         LR    R12,R15       * * * * * * * *
         USING JES2AUTO,R12  *   STANDARD  *
         LA    R15,SAVEAREA  *   REGISTER  *
         ST    R15,8(R13)    *   SAVEAREA  *
         ST    R13,4(R15)    *   CHAINING  *
         LR    R13,R15       * * * * * * * *
         SPACE 2
***********************************************************************
*        LOAD THE DAYDATE ROUTINE                                     *
***********************************************************************
         SPACE 2
         LOAD  EP=DAYDATE
         ST    R0,DAYDATEA   ---> ADDRESS OF DAYDATE PROGRAM
         EJECT
***********************************************************************
*        OPEN THE FILES                                               *
***********************************************************************
         SPACE 2
         OPEN  (COMMANDS,(INPUT), JES2 AUTOMATIC COMMANDS              *
               INTRDR,(OUTPUT),   JES2 INTRDR                          *
               CMNDLIST,(OUTPUT), HARDCOPY                             *
               SYSPRINT,(OUTPUT)) HARDCOPY
         SPACE 2
         PUT   CMNDLIST,OUTCMND   MAKE A COMMENT
         PUT   SYSPRINT,OUTAREA   MAKE A COMMENT
         MVC   OUTAREA,=CL81' '   CLEAR BUFFER
         EJECT
***********************************************************************
*        GET THE CURRENT WEEK OF MONTH                                *
***********************************************************************
         SPACE 2
         LA    R3,WOM        ---> DAYDATE PARM FOR WEEK OF MONTH
         ST    R3,PARMADDR   SAVE IT IN PARMETER LIST
         LA    R1,PARMADDR   ---> PARAMETER LIST
         L     R15,DAYDATEA  ---> DAYDATE PROGRAM
         BALR  R14,R15       CALL THE DAYDATE PROGRAM
         L     R1,0(R1)      ---> PARAMETERS RETURNED
         LH    R2,0(R1)      LENGTH INTO R2
         BCTR  R2,0          DECREMENT FOR INSERTION
         EX    R2,MOVEWEEK   MOVE WEEK OF MONTH INTO FIELD
         OI    WEEK,X'F0'    INSURE IT IS A CHARACTER
         B     LSPARM        GOTO GET LAST WEEK OF CURRENT MONTH
MOVEWEEK MVC   WEEK(0),2(R1) MOVE WEEK OF MONTH INTO FIELD
         SPACE 2
***********************************************************************
*        GET THE LAST WEEK OF THE MONTH                               *
***********************************************************************
         SPACE 2
LSPARM   DS    0H
         LA    R3,LWM        ---> DAYDATE PARM FOR WEEK OF MONTH
         ST    R3,PARMADDR   SAVE IT IN PARAMETER LIST
         LA    R1,PARMADDR   ---> PARAMETER LIST
         L     R15,DAYDATEA  ---> DAYDATE PROGRAM ADDRESS
         BALR  R14,R15       CALL THE DAYDATE PROGRAM
         L     R1,0(R1)      ---> RETURNED PARAMETERS
         LH    R2,0(R1)      LENGTH OF PARAMETER
         BCTR  R2,0          DECREMENT FOR INSERTION
         EX    R2,MOVELAST   MOVE LAST WEEK OF MONTH INTO FIELD
         B     DYPARM        GOTO GET THE CURRENT DAY OF THE WEEK
MOVELAST MVC   LAST(0),2(R1) MOVE LAST WEEK OF MONTH INTO FIELD
         EJECT
***********************************************************************
*        GET THE CURRENT DAY OF THE WEEK                              *
***********************************************************************
         SPACE 2
DYPARM   DS    0H
         LA    R3,DOW        ---> DAYDATE PARM FOR DAY OF WEEK
         ST    R3,PARMADDR   SAVE IT IN PARAMETER LIST
         LA    R1,PARMADDR   ---> PARAMETER LIST
         L     R15,DAYDATEA  ---> DAYDATE PROGRAM
         BALR  R14,R15       CALL THE DAYDATE PROGRAM
         L     R1,0(R1)      ---> RETURNED PARAMETERS
         LH    R2,0(R1)      LENGTH OF PARAMETER
         BCTR  R2,0          DECREMENT FOR INSERTION
         EX    R2,MOVEDAY    MOVE CURRENT DAY OF WEEK INTO FIELD
         OI    DAY,X'F0'     INSURE CHARACTER
         B     MNPARM        GOTO GET THE CURRENT MONTH OF YEAR
MOVEDAY  MVC   DAY(0),2(R1)  MOVE CURRENT DAY OF WEEK INTO FIELD
         SPACE 2
***********************************************************************
*        GET THE CURRENT MONTH OF YEAR                                *
***********************************************************************
         SPACE 2
MNPARM   DS    0H
         LA    R3,MOY        ---> DAYDATE PARM FOR MONTH OF YEAR
         ST    R3,PARMADDR   SAVE IT IN PARAMETER LIST
         LA    R1,PARMADDR   ---> PARAMETER LIST
         L     R15,DAYDATEA  ---> DAYDATE PROGRAM
         BALR  R14,R15       CALL DAYDATE PROGRAM
         L     R1,0(R1)      ---> RETURNED PARAMETERS
         LH    R2,0(R1)      LENGTH
         BCTR  R2,0          DECREMENT FOR INSERTION
         EX    R2,MOVEMOY    MOVE CURRENT MONTH OF YEAR
         L     4,FULWORK     LOAD PARM INTO R4
         CVD   4,DBLWORK     CONVERT PARM TO DECIMAL
         UNPK  MONTH,DBLWORK UNPACK PARM INTO MONTH
         OI    MONTH+3,X'F0' INSURE LAST BYTE IS CHARACTER
         B     TMPARM        GOTO GET CURRENT LOCAL TIME
MOVEMOY  MVC   FULWORK+3(0),2(R1)
         EJECT
***********************************************************************
*        GET THE CURRENT TIME OF DAY                                  *
***********************************************************************
         SPACE 2
TMPARM   DS    0H
         TIME  DEC,ZONE=LT         GET TIME OF DAY
         SRL   0,12                SHIFT RIGHT THREE BYTES
         ST    0,TOD               SAVE TIME OF DAY
         OI    TOD+3,X'0F'         MAKE INTO PACKED DECIMAL
         UNPK  TME,TOD             UNPACK TIME
         EJECT
***********************************************************************
*        SCAN THE JES2 AUTO COMMANDS FILE                             *
***********************************************************************
         SPACE 2
CMDLOOP  GET   COMMANDS            READ COMMAND
         LR    R2,R1               SAVE RECORD ADDRESS AROUND PUT
         MVC   OUTREC,0(R1)        MOVE RECORD TO OUTPUT BUFFER
         PUT   CMNDLIST,OUTAREA    WRITE IT TO THE RECORDS READ FILE
         LR    R1,R2               RESTORE RECORD ADDRESS
         CLC   0(3,R1),=C'//*'     IS IT A COMMENT CARD?
         BE    CMDLOOP             ...YES, THEN GOTO READ NEXT CARD
         SPACE 2
***********************************************************************
*        CHECK FOR OUR SPECIAL KEYWORDS STARTING IN COLUMN 60         *
***********************************************************************
         SPACE 2
PARMKEY  DS    0H
         LA    R3,59(R1)     ---> CARD COLUMN 60
         LA    R4,1          INCREAMENT VALUE
         LA    R5,70(R1)     ---> LAST CARD COLUMN TO SCAN (COL 71)
KEYLOOP  DS    0H
         CLI   0(R3),C'D'    IS THIS A 'DAY RUN' SPECIAL COMMAND?
         BE    DAYRUN        ...YES, THEN GOTO PROCESS DAY COMMAND
         CLC   0(R3),C'W'    IS THIS A 'WEEK RUN' SPECIAL COMMAND?
         BE    WEEKRUN       ...YES, THEN GOTO PROCESS WEEK COMMAND
         BXLE  R3,R4,KEYLOOP ...NO, GOTO LOOK IN NEXT CARD COLUMN
         B     ALLRUN        THERE IS NO SPECIAL CASE SO ACCEPT IT
         EJECT
***********************************************************************
*        CHECK FOR DAY OF THE WEEK PARM                               *
***********************************************************************
         SPACE 2
DAYRUN   DS    0H
         BXH   R3,R4,CMDLOOP IF LAST COLUMN, GOTO GET NEXT CARD
         CLC   0(1,R3),DAY   IS IT TODAY?
         BE    WEEKCHEK      ...YES, GOTO CHECK FOR WEEK PARM ALSO
         B     DAYRUN        CONTINUE CHECKING DAY
         SPACE 2
***********************************************************************
*        CHECK FOR WEEK OF MONTH AFTER HIT ON DAY                     *
***********************************************************************
         SPACE 2
WEEKCHEK DS    0H
         BXH   R3,R4,ALLRUN  GOTO ACCEPT COMMAND BECAUSE OF DAY HIT
         CLI   0(R3),C'W'    IS THERE WEEK PARM?
         BE    WEEKTOO       ...YES, THEN GOTO CHECK FOR A HIT
         B     WEEKCHEK      ...NO, THEN CONTINUE SCAN
WEEKTOO  DS    0H
         BXH   R3,R4,CMDLOOP IF LAST COLUMN, GOTO GET NEXT CARD
         CLC   0(1,R3),WEEK  IS IT THIS WEEK?
         BE    ALLRUN        ...YES, THEN GOTO ACCEPT THIS COMMAND
         CLI   0(R3),C'A'    IS IT ALL WEEKS EXCEPT THE LAST WEEK
         BNE   WEEKTOOL      ...NO, GOTO SEE IF IT'S FOR THE LAST WEEK
         CLC   WEEK(1),LAST  IS THIS WEEK THE LAST WEEK OF MONTH?
         BNE   ALLRUN        ...YES, THEN 'ALL' DOES WORK - ACCEPT IT
         B     CMDLOOP       ...NO, THEN 'ALL' DOESN'T WORK
WEEKTOOL DS    0H
         CLI   0(R3),C'L'    IS IT FOR ONLY THE LAST WEEK?
         BNE   WEEKTOO       ...NO, GOTO CHECK NEXT BYTE IN RECORD
         CLC   WEEK(1),LAST  IS THIS WEEK THE LAST WEEK OF MONTH?
         BE    ALLRUN        ...YES, THEN ACCEPT THE COMMAND
         B     CMDLOOP       ...NO, THEN IGNORE THIS CARD
         EJECT
***********************************************************************
*        CHECK FOR WEEK OF MONTH PARM                                 *
***********************************************************************
         SPACE 2
WEEKRUN  DS    0H
         BXH   R3,R4,CMDLOOP IF LAST COLUMN, GOTO GET NEXT CARD
         CLC   0(1,R3),WEEK  IS IT THIS WEEK?
         BE    DAYCHEK       ...YES, GOTO CHECK FOR DAY PARM ALSO
         CLI   0(R3),C'A'    IS THIS FOR ALL WEEKS?
         BNE   WEEKRUNL      ...NO, GOTO HECK FOR LAST WEEK
         CLC   WEEK(1),LAST  IS THIS WEEK THE LAST WEEK?
         BE    CMDLOOP       ...YES, THEN REJECT THE COMMAND
         B     ALLRUN        ...NO, THEN ACCEPT THE COMMAND
WEEKRUNL DS    0H
         CLI   0(R3),C'L'    IS THIS FOR THE LAST WEEK?
         BNE   WEEKRUN       ...NO, THEN CONTINUE CHECKING
         CLC   WEEK(1),LAST  IS THIS WEEK THE LAST WEEK?
         BE    DAYCHEK       ...YES, GOTO CHECK FOR DAY PARM ALSO
         B     WEEKRUN       ...NO, CONTINUE TO LOOP THROUGH
         SPACE 2
***********************************************************************
*        CHECK FOR DAY OF WEEK AFTER WEEK OF MONTH                    *
***********************************************************************
         SPACE 2
DAYCHEK  DS    0H
         BXH   R3,R4,ALLRUN  GOTO ACCEPT BECAUSE WE HAD A WEEK HIT
         CLI   0(R3),C'D'    IS THERE DAY PARM?
         BE    DAYTOO        ...YES, GOTO CHECK FOR A HIT
         B     DAYCHEK       ...NO, GOTO CONTINUE CHECKING
DAYTOO   DS    0H
         BXH   R3,R4,CMDLOOP REJECT IF DAY IS NOT RIGHT
         CLC   0(1,R3),DAY   IS IT TODAY IN THIS WEEK
         BE    ALLRUN        ...YES, ACCEPT COMMAND
         B     DAYTOO        ...NO, THEN CONTINUE CHECKING
         EJECT
***********************************************************************
*        AT THIS POINT CHECK THAT IT TIME= HAS NOT BEEN PASSED        *
***********************************************************************
         SPACE 2
ALLRUN   MVC   59(12,R1),=CL12' '  BLANK OUT COMMENT AREA
         CLC   6(1,1),=C'T'        IS THERE A TIME SPECIFIED?
         BNE   SYMBOLS             ...NO, GOTO CHECK FOR SYMBOLICS
         CLC   8(2,1),TME          HAS HOUR SPECIFIED PASSED?
         BL    CMDLOOP             ...YES, GET NEXT COMMAND
         BH    SYMBOLS             ...NO, GOTO CHECK FOR SYMBOLICS
         CLC   11(2,1),TME+2       HAS MINUTE SPECIFIED PASSED?
         BL    CMDLOOP             ...YES, GET NEXT COMMAND
         B     SYMBOLS             ...NO, THEN GOTO CHECK FOR SYMBOLS
         SPACE 5
***********************************************************************
*        PERFORM SYMBOL SUBSTITUTION                                  *
***********************************************************************
         SPACE 2
SYMBOLS  DS    0H
         LA    R3,3(R1)      ---> BEGINING OF COMMAND(AFTER /*$)
         LA    R4,1          INCREAMENT VALUE
         LA    R5,70(R1)     ---> LAST DATA BYTE IN RECORD
SYMLOOP  DS    0H
         CLI   0(R3),C'&&'   IS THERE A SYMBOLIC STRING HERE?
         BE    DAYPARM       ...YES, GOTO TO EXAMINE IT
         BXLE  R3,R4,SYMLOOP ...NO, GOTO CHECK NEXT BYTE
         B     PUTIRDR       NO SUBSTITUTION, ACCEPT COMMAND
         EJECT
***********************************************************************
*        CHECK FOR DAY SUBSTITUTION                                   *
***********************************************************************
         SPACE 2
DAYPARM  DS    0H
         CLC   0(5,R3),=C'&&DAY.'  IS THERE DAY SUBSTITUTION?
         BNE   WEEKPARM            ...NO, CHECK FOR WEEK
         MVC   0(5,R3),=CL7' '     ...YES, CLEAR SYMBOL
         MVC   0(1,R3),DAY         MOVE IN DAY OF WEEK VALUE
         B     PUTIRDR             GOTO PLACE RECORD IN INTERNAL RDR
         SPACE 2
***********************************************************************
*        CHECK FOR WEEK SUBSTITUTION                                  *
***********************************************************************
         SPACE 2
WEEKPARM CLC   0(6,R3),=C'&&WEEK.' IS THERE WEEK SUBSTITUTION?
         BNE   MONPARM             ...NO, CHECK FOR MONTH PARM
         MVC   0(6,R3),=CL7' '     ...YES, CLEAR SYMBOL
         MVC   0(1,R3),WEEK        MOVE IN WEEK OF MONTH VALUE
         B     PUTIRDR             GOTO PLACE RECORD IN INTERNAL RDR
         SPACE 2
***********************************************************************
*        CHECK FOR WEEK SUBSTITUTION                                  *
***********************************************************************
         SPACE 2
MONPARM  DS    0H
         CLC   0(7,R3),=C'&&MONTH.' IS THERE MONTH SUBSTITUTION?
         BNE   CMDLOOP              ...NO, SYMBOL IS INVALID
         MVC   0(7,R3),=CL7' '      ...YES, CLEAR SYMBOL
         MVC   0(2,R3),MONTH+2      MOVE IN MONTH OF YEAR VALUE
         B     PUTIRDR             GOTO PLACE RECORD IN INTERNAL RDR
         EJECT
***********************************************************************
*        AT THIS POINT THE AUTOMATIC COMMAND IS ACCEPTABLE            *
***********************************************************************
         SPACE 2
PUTIRDR  DS    0H
         LR    R8,R1               PUT ADDRESS OF COMMAND IN R8
         PUT   INTRDR,(R8)         WRITE TO INTRDR
         PUT   SYSPRINT,OUTAREA    WRITE TO HARDCOPY
         B     CMDLOOP             GET NEXT COMMAND
         EJECT
***********************************************************************
*        END OF FILE HAS OCCURED ON THE COMMANDS FILE.                *
***********************************************************************
         SPACE 2
EOCMDS   DS    0H
         CLOSE (COMMANDS,,INTRDR,,SYSPRINT)
         SPACE 2
***********************************************************************
*        RESTORE CALLERS REGISTERS AND RETURN.                        *
***********************************************************************
         SPACE 2
         L     R13,SAVEAREA+4
         LM    R14,R12,12(R13)
         SR    R15,R15          ALWAYS SET RETURN CODE OF ZERO
         BR    R14
         EJECT
***********************************************************************
*        PROGRAM CONSTANTS AND WORK AREAS                             *
***********************************************************************
         SPACE 2
SAVEAREA DC    18F'0'
DBLWORK  DC    D'0'
FULLWRD1 DC    F'0'
DAYDATEA DC    A(0)
PARMADDR DC    A(0)
FULWORK  DC    F'0'
DAY      DC    F'0'
WEEK     DC    F'0'
LAST     DC    F'0'
MONTH    DC    F'0'
TOD      DC    F'0'
DOW      DC    H'3'
         DC    C'DOW '
WOM      DC    H'3'
         DC    C'WOM '
LWM      DC    H'3'
         DC    C'LWM '
MOY      DC    H'3'
         DC    C'MOY '
TME      DS    CL4
         SPACE 2
OUTAREA  DC    0CL81' '
         DC    CL01'1'
OUTREC   DC    CL80'  JES2 AUTOMATIC COMMANDS ACCEPTED:'
OUTCMND  DC    0CL81' '
         DC    CL01'1'
         DC    CL81'  JES2 AUTOMATIC COMMANDS READ:    '
         SPACE 2
         LTORG
         EJECT
***********************************************************************
*        DATA CONTROL BLOCKS                                          *
***********************************************************************
         SPACE 2
COMMANDS DCB   DDNAME=COMMANDS,MACRF=(GL),EODAD=EOCMDS,DSORG=PS
         EJECT
INTRDR   DCB   DDNAME=INTRDR,MACRF=(PM),DSORG=PS,BLKSIZE=80
         EJECT
CMNDLIST DCB   DDNAME=CMNDLIST,MACRF=(PM),DSORG=PS,BLKSIZE=81,         *
               LRECL=81,RECFM=FBA
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=(PM),DSORG=PS,BLKSIZE=81,         *
               LRECL=81,RECFM=FBA
         END   JES2AUTO
