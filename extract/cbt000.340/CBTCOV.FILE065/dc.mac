DC       TITLE 'DISPLAY ACTIVE CICS ADDRESS SPACES'
**********************************************************************
*                                                                    *
*    LIBRARY:  SYS5.PROD.ASM                                         *
*                                                                    *
*     MEMBER:  DC                                                    *
*                                                                    *
*     AUTHOR:  JEFF SNYDER                                           *
*                                                                    *
*DESCRIPTION:  THIS MODULE IS INTENDED TO SEARCH THE ADDRESS SPACE   *
*              VECTOR TABLE AND DISPLAY WHICH CICS**** ADDRESS       *
*              SPACES ARE ACTIVE.                                    *
*                                                                    *
*     MACROS:  SYS1.MACLIB (TPUT    )                                *
*              SYS1.AMODGEN(CVT     )                                *
*              SYS1.AMODGEN(IHAASVT )                                *
*              SYS1.AMODGEN(IHAASCB )                                *
*              SYS1.AMODGEN(IRAOUCB )                                *
*              SYS1.USERMAC(ENTER   )                                *
*              SYS1.USERMAC(COVER   )                                *
*              SYS1.USERMAC(LEAVE   )                                *
*                                                                    *
**********************************************************************
         TITLE 'INITIALIZE THE PROGRAM'
DC       ENTER REGEQUS=YES,BASEREG=12
         L     R1,16              POINT TO CVT
         USING CVT,R1             ADDRESS CVT
         L     R9,CVTASVT         POINT TO ASVT
         USING ASVT,R9            MAP ASVT
         L     R3,ASVTMAXU        LOAD MAXIMUM # OF ASVT ENTRIES
         MVC   TPUTMSG,=CL80' '   CLEAR OUTPUT FIELD
ASVTLOOP TM    ASVTENTY,ASVTAVAL  IF THIS ASID IS AVAILABLE
         BO    NEXTASCB           THEN KEEP LOOKING
         L     R10,ASVTENTY       POINT TO ASCB
         USING ASCB,R10           MAP ASCB
         L     R4,ASCBOUCB        POINT TO OUCB
         USING OUCB,R4            MAP OUCB
         LA    R8,5(R8)           POINT TO NEXT SPOT
         TITLE 'DETERMINE WHICH (IF ANY) CICS JOB'
PROD     CLC   OUCBTRXN,=C'CICSPROD'
         BNE   SUBA
         MVC   CICSPROD,=C'CICSPROD  '
         B     NEXTASCB
SUBA     CLC   OUCBTRXN,=C'CICSSUBA'
         BNE   SUBB
         MVC   CICSSUBA,=C'CICSSUBA  '
         B     NEXTASCB
SUBB     CLC   OUCBTRXN,=C'CICSSUBB'
         BNE   SUBC
         MVC   CICSSUBB,=C'CICSSUBB  '
         B     NEXTASCB
SUBC     CLC   OUCBTRXN,=C'CICSSUBC'
         BNE   SUBD
         MVC   CICSSUBC,=C'CICSSUBC  '
         B     NEXTASCB
SUBD     CLC   OUCBTRXN,=C'CICSSUBD'
         BNE   SUBT
         MVC   CICSSUBD,=C'CICSSUBD  '
         B     NEXTASCB
SUBT     CLC   OUCBTRXN,=C'CICSSUBT'
         BNE   SUBU
         MVC   CICSSUBT,=C'CICSSUBT  '
         B     NEXTASCB
SUBU     CLC   OUCBTRXN,=C'CICSSUBU'
         BNE   SUBX
         MVC   CICSSUBU,=C'CICSSUBU  '
         B     NEXTASCB
SUBX     CLC   OUCBTRXN,=C'CICSSUBX'
         BNE   NEXTASCB
         MVC   CICSSUBX,=C'CICSSUBX  '
NEXTASCB LA    R9,4(R9)           POINT TO NEXT ASCB
         BCT   R3,ASVTLOOP        AND KEEP GOING
         TITLE 'DISPLAY OUTPUT AND EXIT PROGRAM'
         TPUT  TPUTMSG,80         SHOW THE OUTPUT
RETURN   LEAVE RETCODE=0          SET RETURN CODE 0 AND EXIT PROGRAM
         TITLE 'PROGRAM CONSTANTS AND LITERALS'
         LTORG
TPUTMSG  DS    0CL80
CICSPROD DC    CL10' '
CICSSUBA DC    CL10' '
CICSSUBB DC    CL10' '
CICSSUBC DC    CL10' '
CICSSUBD DC    CL10' '
CICSSUBT DC    CL10' '
CICSSUBU DC    CL10' '
CICSSUBX DC    CL10' '
         TITLE 'SYSTEM DATA AREA  MAPPING MACROS'
         COVER ('SYSTEM  ','MAPPING ','MACROS  ')
         CVT   DSECT=YES          MAP OF CVT
         IHAASCB ,                MAP OF ASCB
         IHAASVT ,                MAP OF ASVT
         IRAOUCB ,                MAP OF OUCB
         END   DC
