TMP      SUPER SUP=NO
         ENTRY DIABLO
         REGDEF
         ST    R1,SAVEPARM
         LA    R9,GOODMORN
         BAL   R10,PUTGET
CMDPROC  DS    0H
         LA    R4,INAREA
CP2      TRT   0(20,R4),TBLDEL
         BZ    NOSP
         SR    R1,R4          CALC LENGTH
         LA    R5,COMMANDS
         L     R6,NUMCOMM
         BAL   R10,FINDCMD
         LTR   R5,R5          DID HE FIND IT?
         BNZ   FNDCMD
         LA    R5,EXTCMD      LOOK FOR EXTERNAL CMD
         L     R6,EXTCMD#
         BAL   R10,CYCCMD
         LTR   R5,R5          DID WE FIND IT?
         BNZ   FNDCMD2
         LA    R9,BADCMDM
         BAL   R10,PUTGET
         B     CMDPROC
         TITLE 'SEARCH COMMAND LISTS'
FINDCMD  LA    R1,1(R1)
         STH   R1,CMDL
         BCTR  R1,0
         BCTR  R1,0           MACH LENGTH
         STC   R1,CYCCMD+1
         EX    R1,OR4         SHIFT UP
CYCCMD   CLC   0(1,R4),0(R5)
         BER   R10
         LA    R5,12(R5)
         BCT   R6,CYCCMD
         SR    R5,R5          INDICATE NOT FOUND
         BR    R10
NOSP     DC    X'0000'
FNDCMD   DS    0H
         LA    R4,2(R4,R1)
         L     R15,8(R5)      COMMAND ADDRESS
         BALR  R14,R15
BACK     LA    R9,BACKMSG
         BAL   R10,PUTGET
         ST    R1,LENGTH
         B     CMDPROC
FNDCMD2  DS    0H             EXTERNAL COMMAND
         L     R1,SAVEPARM
         LA    R4,INAREAP
         ST    R4,0(R1)
         L     R15,8(R5)
         BALR  R14,R15
*        CLEANUP
         L     R6,OUTP        CHECK IS MORE TO OUTPUT
         C     R6,=A(OUTAREA)
         BE    CLUP2
         BAL   R10,PUTIT
CLUP2    DS    0H
         B     BACK
OR4      OC    0(1,R4),=CL256' '
COMMANDS DS    0D
         DC    CL8'SET',A(SET)
         DC    CL8'CLEAR',A(CLEAR)
         DC    CL8'RESET',A(RESETP)
         DC    CL8'ABEND',A(ABEND)
         DC    CL8'STOP',A(RC0)
         DC    CL8'TRACE',A(TRCON)
NUMCOMM  DC    A((NUMCOMM-COMMANDS)/12)
EXTCMD   DS    0F
         DC    CL8'LIST',V(LISTIT)
EXTCMD#  DC    A((EXTCMD#-EXTCMD)/12)
ABEND    DC    X'0000'
SAVEPARM DC    F'0'
         TITLE 'SUBROUTINES'
PUTGET   DS    0H
         L     R1,SPECCALL    SPECIAL CALL?
         LTR   R1,R1
         BNZR  R1
         LH    R2,0(R9)       LENGTH
         LA    R9,2(R9)       POINT AROUND LENGTH
TPUTC    TPUT  (R9),(R2),CONTROL
         TGET  INAREA,100
         LA    R14,INAREA(R1)
         MVI   0(R14),X'FF'   MARK END
         LA    R1,4(R1)       LENGTH + HEADER
         STH   R1,TOTL        SAVE IN HEADER
         BR    R10
SPECCALL DC    F'0'
TRCON    DS    0H
ALT      DYNAM F=TRACE,SYSOUT=A
         OPEN  (TRCDCB,OUTPUT)
         TM    TRCDCB+48,X'10'
         BZ    BACK
         OI    BCT+1,X'F0'    UNCONDITIONAL BRANCH
         B     BACK
         TITLE 'SET COMMAND'
SET      DS    0H
         TRT   0(10,R4),TBLDEL
         SR    R1,R4
         LA    R5,SETCMDS
         L     R6,NUMSC
         BAL   R10,FINDCMD
         LTR   R5,R5          DID WE GET IT?
         BNZ   GOSET
         LA    R9,BADSETM
         BAL   R10,PUTGET
         B     CMDPROC
GOSET    L     R15,8(R5)
         LA    R4,2(R4,R1)
         BR    R15
         SPACE 3
SETCMDS  DS    0F
         DC    CL8'MARGIN',A(MAR)
         DC    CL8'TABS',A(TABPGM)
         DC    CL8'VMI',A(SETVMIP)
         DC    CL8'HMI',A(SETHMIP)
         DC    CL8'LINESIZE',A(LSPGM)
         DC    CL8'PAGESIZE',A(PSPGM)
NUMSC    DC    A((NUMSC-SETCMDS)/12)
         SPACE 3
MAR      DS    0H
         LA    R10,MAR2       RETURN ADDRESS
CALC     SR    R1,R1
         TRT   0(10,R4),TBLDEL
         SR    R1,R4
         BNP   NODATA
         BCTR  R1,0
         EX    R1,TRTN         LOOK FOR NON NUMERIC
         BZ    GOPACK
NODATA   LA    R9,BADDATA
         BAL   R10,PUTGET
         B     CMDPROC
GOPACK   EX    R1,PACK4
         CVB   R3,DOUBLE
         BR    R10            GET BACK
MAR2     ST    R3,MARGIN
         LA    R3,TABLEN(R3)
         MVC   MARSET,0(R3)
         LA    R9,MARSETMSG
         CLC   TABS(2),=X'0000'    ANY TABS?
         BNE   HVTABS              B YES
         BAL   R10,PUTGET
         B     CMDPROC
HVTABS   LA    R9,MARSETMSG+2      WHERE TO TPUT FROM
         B     DOTABS
PACK4    PACK  DOUBLE,0(1,R4)
TRTN     TRT   0(1,R4),TBLN
         SPACE 3
TABPGM   DS    0H
         LA    R7,TABS
BALCALC  BAL   R10,CALC
         STH   R3,0(R7)          SAVE OFFSET
         LA    R7,2(R7)          POINT TO NEXT
         MVC   0(2,R7),=X'0000'  MAKE SURE ITS 0
         LA    R4,1(R1,R4)
         CLI   0(R4),X'FF'       END?
         BE    DNCALC
         TRT   0(8,R4),TBLNB     LOOK FOR NON BLANK
         BZ    DNCALC
         LR    R4,R1
         B     CALC
DNCALC   LA    R9,STAREA         POINT TO OUTAREA
DOTABS   LA    R8,STAREA+2
         LA    R7,TABS
CYCTABS  LH    R3,0(R7)
         LTR   R3,R3             DO WE HAVE TABS?
         BZ    DNTAB2
         A     R3,MARGIN
         LA    R3,TABLEN(R3)
         MVI   0(R8),ESC      INSURANCE
         MVC   2(1,R8),0(R3)     MOVE IN OFFSET
         LA    R7,2(R7)          POINT TO NEXT
         LA    R8,5(R8)
         B     CYCTABS
DNTAB2   MVI   0(R8),CR          CARR RETURN
         LA    R2,1(R8)          POINT TO LAST CHAR
         SR    R2,R9             CALC LENGTH
         BAL   R10,PUTGET         GO PUT IT
         B     CMDPROC           GO PROCESS NEXT COMMAND
         SPACE 3
SETVMIP  DS    0H
         LA    R9,SETVMIM
COMSMI   BAL   R10,CALC
         LA    R3,TABLEN(R3)
         MVC   4(1,R9),0(R3)
         BAL   R10,PUTGET
         B     CMDPROC
         SPACE 3
SETHMIP  DS    0H
         LA    R9,SETHMIM
         B     COMSMI
         SPACE 3
         SPACE 3
RC0      SUPRET RC=0
         SPACE 3
LSPGM    DS    0H
         BAL   R10,CALC
         ST    R3,LINESIZE
         B     CMDPROC
         SPACE 3
PSPGM    DS    0H
         BAL   R10,CALC
         ST    R3,PAGESIZE
         TITLE 'RESET AND CLEAR COMMANDS'
CLEAR    DS    0H
         SPACE 3
RESETP   DS    0H
         SR    R0,R0
         ST    R0,MARGIN
         STH   R0,TABS
         LA    R9,RESETMSG
         BAL   R10,PUTGET
         B     CMDPROC
         TITLE 'EXTERNAL ENTRY'
DIABLO   DS    0H
         STM   R14,R12,12(R13)
         USING DIABLO,R15
         L     R12,=A(TMP)
         DROP  R15
         ST    R13,SVAREA2+4
         L     R13,=V(TMPS2)
         ST    R1,SVR1
         LM    R3,R5,0(R1)
         LA    R10,RET10
         CLI   0(R3),X'00'    DOPE VECTOR?
         BE    DOPE
         CLC   0(3,R3),=C'PUT'
         BE    PUT
         CLC   0(3,R3),=C'GET'
         BE    GET
         CLC   0(3,R3),=C'CMD'
         BE    CONTROL
         DC    X'0000'
RET10    DS    0H
RC20     L     R13,SVAREA2+4
         SR    R15,R15
         ST    R15,16(R13)
         LM    R14,R12,12(R13)
         BR    R14
SVAREA2  DS    18F
         SPACE 3
SVR1     DC    F'0'
DOPE     DS    0H
         SPACE 3
         TITLE 'PUT'
PUT      DS    0H
         CLI   3(R3),C'J'     JUSTIFY?
         BE    PUTJ
         ST    R4,STARTADD
         L     R8,0(,R5)      LENGTH
         BCTR  R8,0           MACH LENGTH
         CLI   0(R4),C'Ï'       INSTREAM COMMAND?
         BE    HVCMD
         LA    R1,MVCCRLF
         ST    R1,ENDPGM
         L     R6,OUTP
         EX    R8,MVCSP       CLEAR OUTAREA
         SR    R1,R1
         TRT   0(1,R4),TBLCOM-C' '    ARE WE STARTING WITH A SPACE
         BZ    HVDATA         B NO
CKSP     EX    R8,TRTNB       LOOK FOR NON BLANK
         BZ    DNTRT
         SR    R1,R4
         C     R1,=F'3'       IS IT WORTH SKIPPING?
         BH    SKIP
         AR    R6,R1          POINT AROUND SPACES
         SR    R8,R1          RESIDUAL COUNT
         AR    R4,R1
HVDATA   DS    0H
         EX    R8,TRTB        LOOK FOR SPACE
         BNZ   HVSP
         LA    R1,1(R8,R4)
HVSP     SR    R1,R4          CALC LENGTH
         EX    R1,MVCIN
         AR    R6,R1
         AR    R4,R1
         SR    R8,R1
         BP    CKSP
DNTRT    DS    0H
         CLI   3(R3),C'H'     HOLD?
         BNE   PUTIT
         C     R6,=A(OUTAREA+2500)
         BH    PUTH
         ST    R6,OUTP
         BR    R10
PUTH     DS    0H
         SPACE 3
PUTIT    L     R1,ENDPGM
         BR    R1
MVCCRLF  MVC   0(2,R6),=X'0D25'     CR LF
         LA    R6,2(R6)
PUTIT2   S     R6,=A(OUTAREA)  CALC LENGTH
BCT      BC    0,TRACE
BKTRC    DS    0H
         TPUT  OUTAREA,(R6),CONTROL
         LA    R6,OUTAREA
         ST    R6,OUTP
         BR    R10
SKIP     DS    0H
         MVC   0(2,R6),=X'2705'   DIRECT TAB TO N
         AR    R4,R1          CALC WHERE WE SHOULD BE
         LR    R9,R4
         S     R9,STARTADD    HOW FAR IN?
         A     R9,MARGIN
         LA    R9,TABLEN(R9)
         MVC   2(1,R6),0(R9)
         LA    R6,3(,R6)
         SR    R8,R1
         B     HVDATA
TRTB     TRT   0(1,R4),TBLCOM-C' '
TRTNB    TRT   0(1,R4),TBLNB
MVCSP    MVC   0(1,R6),=CL256' '
MVCIN    MVC   0(1,R6),0(R4)
OUTP     DC    A(OUTAREA)
LINESIZE DC    F'60'
PAGESIZE DC    F'66'
ENDPGM   DC    A(MVCCRLF)
         SPACE 3
PUTJ     DS    0H
         SPACE 3
TRACE    DS    0H
         B     BKTRC
         SPACE 2
HVCMD    DS    0H
         LA    R4,1(R4)       POINT TO COMMAND
         LA    R2,BKCMD       RETURN ADDRESS
         ST    R2,SPECCALL
         EX    R8,MVCIN4
         LA    R8,INAREA(R8)
         MVI   0(R8),X'FF'
         ST    R10,SVR10
         B     CMDPROC        GO PROCESS COMMAND
MVCIN4   MVC   INAREA(1),0(R4)
BKCMD    LH    R2,0(R9)       LENGTH
         SR    R0,R0
         ST    R0,SPECCALL
         BCTR  R2,0           MACH LENGTH
         L     R6,OUTP
         EX    R2,MVC69
         LA    R6,1(R2,R6)    POINT PAST DATA
         ST    R6,OUTP
         L     R10,SVR10
         LA    R1,PUTIT2      NO CRLF
         ST    R1,ENDPGM
         B     DNTRT
MVC69    MVC   0(1,R6),2(R9)
SVR10    DC    F'0'
         TITLE 'GET'
GET      DS    0H
         ST    R4,INLOC
         TGET  INAREA,100
         LA    R9,INAREA
         LTR   R3,R1          LENGTH
         BZ    NODATAIN
         BCTR  R3,0           MACH LENGTH
CYCTAB2  EX    R3,TRTTAB      LOOK FOR TAB
         BZ    NOTAB
         LA    R8,TABS
         SR    R1,R9          CALC LENGTH
         A     R1,PRELOC
         BCTR  R1,0           MACH LENGTH
         EX    R1,MVC49       MOVE THIS DATA
         LA    R1,1(R1)       REAL LENGTH
CYCFT    CH    R1,0(R8)       IS THIS THE TAB?
         BNH   FNDTAB
         LA    R8,2(R8)
         B     CYCFT
FNDTAB   LH    R1,0(R8)
         L     R4,INLOC
         AR    R4,R1
         ST    R1,PRELOC
         B     CYCTAB2
         SPACE 3
NODATAIN DS    0H
         MVI   0(4),CR
         B     RC20
         SPACE 3
NOTAB    EX    R3,MVC4IN
         B     RC20
         SPACE 3
TRTTAB   TRT   0(1,R9),TBLCOM-X'05'
MVC4IN   MVC   0(1,R4),0(R9)
MVC49    MVC   0(1,R4),0(9)
PRELOC   DC    F'0'
INLOC    DC    F'0'
         TITLE 'CONTROL'
CONTROL  DS    0H
         CLI   0(R4),X'00'    DOPE VECTOR?
         BE    DOPECONT
         LR    R1,R5          LENGTH
         BCTR  R1,0
         B     NODOPE
         SPACE 3
DOPECONT DS    0H
         LH    R1,0(R4)       LENGTH
         S     R1,=F'3'
NODOPE   EX    R1,MVCDOPE
         LA    R1,INAREA+1(R1)
         MVI   0(R1),X'FF'
         LA    R1,BKCMD
         ST    R1,SPECCALL
         B     CMDPROC
MVCDOPE  MVC   INAREA(1),2(R4)
         TITLE 'DATA CONSTANTS'
LENGTH   DC    F'0'
STARTADD DC    F'0'
OUTLNTH  DC    F'80'
MARGIN   DC    F'0'           LOCATION OF MARGIN
TABS     DC    40H'0'         LOCATION OF TABS
DOUBLE   DC    D'0'
         LTORG
TMPS2    CSECT
TBLN     DC    XL256'04'
         ORG   TBLN+C'0'
         DC    XL10'0'
         ORG   TBLN+256
TBLDEL   DC    XL256'0'
         ORG   TBLDEL+C' '
         DC    X'04'
         ORG   TBLDEL+X'FF'
         DC    X'08'
BADDATA  DC    AL2(BADCMDM-BADDATA-2)
         DC    C'DATA IS INVALID',X'0D25'
BADCMDM  DC    AL2(BADSETM-BADCMDM-2)
         DC    C'INVALID COMMAND',X'0D25'
BADSETM  DC    AL2(RESETMSG-BADSETM-2)
         DC    C'INVALID SET COMMAND',X'0D25'
RESETMSG DC    AL2(SETVMIM-RESETMSG-2)
         DC    X'27F2270D27050127F9'
         DC    X'271F0D271E25'   SET HMI AND VMI
SETVMIM  DC    AL2(SETHMIM-SETVMIM-2)
         DC    X'271E00'
SETHMIM  DC    AL2(MARSETMSG-SETHMIM-2)
         DC    X'271F00'
MARSETMSG DC   AL2(STAREA-MARSETMSG-2)
         DC    X'2705'
MARSET   DC    X'0'
         DC    X'27F90D'
STAREA   DC    X'27F2'        CLEAR ALL TABS
         DC    30X'27050027F1' DIRECT TAB AND SET TAB
BACKMSG  DC    AL2(GOODMORN-BACKMSG-2)
         DC    C'MONITOR',X'0D25'
GOODMORN DC    AL2(ENDMSG-GOODMORN-2)
         DC    C'GOOD MORNING',X'0D25'
ENDMSG   DS    0C
TRCDCB   DCB   DDNAME=TRACE,DSORG=PS,MACRF=PM
         COPY DIABLODC
INAREAP  DS    0F
TOTL     DC    XL2'00'
CMDL     DC    XL2'0'
INAREA   DC    CL100' '
TBLNB    DC    256X'04'
         ORG   TBLNB+C' '
         DC    X'00'
         ORG   TBLNB+256
         TBLCOM
OUTAREA  DS    CL3000
         END
