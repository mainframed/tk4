QSHIFT   TITLE 'FUNCTION FOR SHIFTING BITS WITHIN A QWORD'
*
*     THIS PROGRAM SHIFTS BITS WITHIN A QWORD. IT CAN SHIFT EITHER TO
* THE RIGHT OR TO THE LEFT. THE FORMAT OF THE ARGUMENTS IS AS FOLLOWS
*          1. THE QWORD UPON WHICH THE SHIFT OPERATION IS TO BE
*             PERFORMED
*          2. THE NUMBER OF BITS WHICH THE QWORD IS TO BE SHIFTED
*             (FULL WORD BINARY), WHERE A NEGATIVE VALUE INDICATES A
*             SHIFT TO THE RIGHT AND A POSITIVE VALUE INDICATES A
*             SHIFT TO THE LEFT.
*     "QSHIFT" IS REENTRANT AND REUSEABLE.
*
QSHIFT   ENTER PARMREG=2,          INITIALIZATION                      X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING WORKAREA,R13        ADDRESS WORK AREA DSECT
         LM    R4,R5,0(R2)         LOAD PARAMETER POINTERS
         L     R5,0(,R5)           LOAD NUMBER OF BITS TO SHIFT
         LTR   R5,R5               TEST THE NUMBER OF BITS TO SHIFT
         BM    SRDA                   NEGATIVE - SHIFT RIGHT
         BP    SLDA                   POSITIVE - SHIFT LEFT
         BZ    EXIT                   ZERO     - NO SHIFT NECESSARY
SRDA     DS    0H
         LPR   R5,R5               MAKE NUMBER OF BITS POSITIVE
         BAL   R14,SHIFTR          PERFORM SHIFT RIGHT
         B     EXIT                   AND LEAVE
SLDA     DS    0H
         BAL   R14,SHIFTL          PERFORM SHIFT LEFT
         B     EXIT                   AND LEAVE
EXIT     DS    0H
         LD    R0,DWORD1           SAVE 1ST DWORD IN F.P. REGISTER 0
         LD    R2,DWORD2           SAVE 2ND DWORD IN F.P. REGISTER 2
         LEAVE RETCODE=0,          RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
         EJECT
SHIFTR   DS    0H
         LM    R6,R9,0(R4)         LOAD QWORD INTO REGISTERS
SHIFTRC  DS    0H
         MVI   FLAG,X'00'          INITIALIZE FLAG
         STM   R6,R7,DWORD1        SAVE 1ST DWORD
         STM   R8,R9,DWORD2        SAVE 2ND DWORD
         TM    DWORD1+7,X'01'      IF LOW ORDER BIT OF 1ST WORD OFF
         BZ    SHIFTRF                THEN DON'T SET FLAG
         MVI   FLAG,X'FF'             ELSE SET FLAG
SHIFTRF  DS    0H
         SRDL  R6,1                SHIFT 1ST WORD TO THE RIGHT
         SRDL  R8,1                SHIFT 2ND WORD TO THE RIGHT
         CLI   FLAG,X'FF'          IF FLAG IS NOT ON
         BNE   SHIFTRK                THEN BYPASS CARRYOVER BIT
         O     R8,=X'80000000'        ELSE TURN ON CARRYOVER BIT
SHIFTRK  DS    0H
         BCT   R5,SHIFTRC          GO SHIFT ANOTHER BIT
         STM   R6,R7,DWORD1        SAVE 1ST DWORD
         STM   R8,R9,DWORD2        SAVE 2ND DWORD
         BR    R14                 RETURN TO CALLER
         EJECT
SHIFTL   DS    0H
         LM    R6,R9,0(R4)         LOAD QWORD INTO REGISTERS
SHIFTLC  DS    0H
         MVI   FLAG,X'00'          INITIALIZE FLAG
         STM   R6,R7,DWORD1        SAVE 1ST DWORD
         STM   R8,R9,DWORD2        SAVE 2ND DWORD
         TM    DWORD2+0,X'80'      IF HIGH ORDER BIT OF 2ND WORD OFF
         BZ    SHIFTLF                THEN DON'T SET FLAG
         MVI   FLAG,X'FF'             ELSE SET FLAG
SHIFTLF  DS    0H
         SLDL  R6,1                SHIFT 1ST WORD TO THE LEFT
         SLDL  R8,1                SHIFT 2ND WORD TO THE LEFT
         CLI   FLAG,X'FF'          IF FLAG IS NOT ON
         BNE   SHIFTLK                THEN BYPASS CARRYOVER BIT
         O     R7,=X'00000001'        ELSE TURN ON CARRYOVER BIT
SHIFTLK  DS    0H
         BCT   R5,SHIFTLC          GO SHIFT ANOTHER BIT
         STM   R6,R7,DWORD1        SAVE 1ST DWORD
         STM   R8,R9,DWORD2        SAVE 2ND DWORD
         BR    R14                 RETURN TO CALLER
         EJECT
WORKAREA DSECT ,
         DS    18A
DWORD1   DS    D                   DOUBLEWORD WORK AREA
DWORD2   DS    D                   DOUBLEWORD WORK AREA
FLAG     DS    X                   CARRYOVER BIT INDICATOR
WORKSIZE EQU   *-WORKAREA
         END
