INITCVTU TITLE '  CVT USER INIT.MODULE .. MAR. 80  A.P.'
INITCVTU CSECT
         SAVE  (14,12),,*
         USING INITCVTU,R10
         LR    R10,R15            R10 IS BASE
         ST    R13,SAVEAREA+4     STD
         LA    R12,SAVEAREA        LINK-
         ST    R12,8(R13)           AGE
         LR    R13,R12               ***
         B     CONTINUE
         LCLC  &VMTMDT
&VMTMDT  SETC  '&SYSTIME'(1,2).'&SYSTIME'(4,2)
         DC    X'&VMTMDT'          ASSEMBLY TIME (HHMM) AND
&VMTMDT  SETC  '&SYSDATE'(1,2).'&SYSDATE'(4,2)
         DC    X'&VMTMDT'          DATE (MMDD) SAME AS LISTING
SAVEAREA DS    18F
         SPACE
CONTINUE DS    0H
         USING CVTMAP,R7           CVT DSECT
         L     R7,CVTPTR(0,0)      LOAD CVT ADDRESS
         L     R4,CVTUSER          LOAD CVT USER POINTER DATA
         LTR   R4,R4               Q. ANYONE USING IT
         BZ    MODESET             IF NOT MODESET AND CONTINUE
         WTO   'HMD030I INITCVTU: ERROR - CVT USER AREA OCCUPIED',     X
               ROUTCDE=(2,10),DESC=4                        *HMD 12/81*
         B     RETURN
MODESET  MODESET KEY=ZERO,MODE=SUP
         SR    R2,R2               GETMAIN RETURN SAVE ADDRESS REG
         EJECT                     REMOVED AFTER ESTAE PRGM'ED *----*
***      SET UP ESTAE IN CASE OF ABENDS
         ESTAE ESTAEXIT,CT,PARAM=PARAMETR
         LTR   R15,R15             Q.ESTAE OK ?
         BZ    ESTAE$OK
         WTO   'HMD030I INITCVTU: ERROR - UNABLE TO CREATE ESTAE ENVIROX
               NMENT',ROUTCDE=(2,10),DESC=4                 *HMD 12/81*
         B     RETURN
         EJECT
ESTAE$OK DS    0H
***      GLOBAL SPIN LOCK TO SERIALIZE RSM AND VSM
         SETLOCK OBTAIN,TYPE=SALLOC,MODE=UNCOND,REGS=USE,              *
               RELATED=GETMAIN
         LTR   R15,R15
         BNZ   ABEND001
         MVC   SETLOCKO,=H'-1'     SETLOCK OBTAINED FLAG TURNED ON
***      RU IS UNCONDITIONAL REGISTER REQUEST               *HMD 12/81*
         LA    R0,DSECTSIZ         DSECT SIZE               *HMD 12/81*
         GETMAIN RU,LV=(0),SP=245,BRANCH=(YES,GLOBAL)       *HMD 12/81*
         LR    R2,R1               *** SAVE OBTAINED AREA ADDRESS ***
         ICM   R2,B'1000',=X'00'   FIRST BYTE BIN. ZEROES FOR AUTOJOT
         MVC   0(DSECTSIZ,R2),DSECTHDR *** INIT AREA ***
         ST    R2,CVTUSER          *** STORE PURCHASED ADDRESS IN CVT
         ST    R2,GETMAINA         SAVE ADDRESS IN PARM LIST
         GETMAIN RU,LV=8,SP=239,BRANCH=(YES,GLOBAL)         *HMD 12/81*
         ST    R1,PROTMAIN         SAVE ADDR IN ESTAE PARMS *HMD 12/81*
         ST    R1,PROTADR-DSECTHDR(R2)  ADDR IN USER AREA   *HMD 12/81*
         MVC   0(8,R1),DC01        MOVE IN CONSTANT         *HMD 12/81*
         SETLOCK RELEASE,TYPE=SALLOC,REGS=USE,RELATED=GETMAIN
         LTR   R15,R15
         BNZ   ABEND004
         MODESET KEY=NZERO,MODE=PROB   CHANGE MODE          *HMD 12/81*
RETURN   DS    0H
         L     R13,SAVEAREA+4
         RETURN (14,12),RC=0
         EJECT
ESTAEXIT DS    0H                  AS DOC. IN  -SUPERVISOR-
         SAVE  (14,12),,ESTAE_EXIT
         LR    R12,R15             BASE REG 12
         USING ESTAEXIT,R12
         ST    R13,ESTASAVE+4
         LR    R12,R13
         LA    R13,ESTASAVE
         ST    R13,8(R12)
         LR    R2,R1               SDWA IN REG 1 ... NOW IN 2
         USING SDWA,R2
         L     R9,SDWAPARM         ADDRS OF PARAMETER LIST
         WTO   'HMD030I INITCVTU: ESTAE ROUTINE ENTERED',              X
               ROUTCDE=(2,10),DESC=4                        *HMD 12/81*
***
         CLC   =H'-1',4(R9)        Q.SET LOCK OBTAINED
         BE    RELSLOCK
         SETRP WKAREA=(2),         2 POINTS2 POINTS TO SDWA            *
               DUMP=IGNORE,        DO NOT CHNG DUMP OPTIONS            *
               RC=0,               CONTINUE TERMINATION                *
               REGS=(14,12)
***
RELSLOCK DS    0H
         SETRP WKAREA=(2),         2 POINTS2 POINTS TO SDWA            *
               DUMP=IGNORE,        DO NOT CHNG DUMP OPTIONS            *
               RC=0,               CONTINUE TERMINATION                *
               REGS=(14,12),                                           *
               FRELOCK=SALLOC
***
         EJECT
ABEND001 DS    0H           SETLOCK OBTAIN FAILED
         ABEND 1,DUMP       SETLOCK OBTAIN FAILED
ABEND003 DS    0H           CVTUSER IS IN USE
         ABEND 3,DUMP       CVTUSER IS IN USE
ABEND004 DS    0H           SETLOCK RELEASE FAILED
         ABEND 4,DUMP       SETLOCK RELEASE FAILED
         EJECT
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
*
* THE FOLLOWING COMPRISES A USER CVT -
* ROLL YOUR OWN HERE - THE PROTECTION ADDRESS IS FOR
* THE AUTHORIZATION SVC 254.
*
DSECTHDR DS    0D
         DC    C'PROT'             PROTECTED STORAGE CODE   *HMD 12/81*
PROTADR  DC    F'0'                PROTECTED STORAGE ADDR   *HMD 12/81*
         DC    C'BDAT'             BULK DATA TRANSFER
         DC    X'80',FL3'00'       * AUTOJOT-BDT COMMAREA   *HMD 12/80*
         DC    C'TEST'             BULK DATA TRANSFER/TEST  *HMD 12/80*
         DC    X'80',FL3'00'       * AUTOJOT0-BTD COMMAREA  *HMD 12/80*
         DC    F'0'                * RESERVED               *HMD 12/80*
         DC    F'0'                * RESERVED               *HMD 12/80*
         DC    F'0'                * RESERVED               *HMD 12/80*
         DC    F'0'                * RESERVED               *HMD 12/80*
DSECTSIZ EQU   *-DSECTHDR
DC01     DC    D'9.99999999999'    NOT THE REAL CODE
         SPACE
ESTASAVE DS    18F
         SPACE
PARAMETR DS    0F                  ESTAE PARAMETER LIST
         DC    A(SAVEAREA)
SETLOCKO DC    H'0'                SETLOCK OBTAIN YES(-1) NO(NULL)
         DC    H'0'                UNUSED
GETMAINA DC    F'0'                GETMAIN ADDRESS
PROTMAIN DC    F'0'                PROTECTED STORAGE ADDRESS*HMD 12/81*
         EJECT
         IHAPSA
         EJECT
         IHASDWA
         EJECT
         IHAWSAVT CLASS=GLOBAL
         EJECT
         CVT   DSECT=YES,LIST=YES
         END
