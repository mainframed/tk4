JDATEX   TITLE 'JULIAN TO GREGORIAN DATE CONVERSION SUBROUTINE'
*
*      THIS PROGRAM CONVERTS A JULIAN DATE TO A GREGORIAN DATE. UPON
* COMPLETION, REGISTER 15 CONTAINS 0 IF THE CONVERSION WAS SUCCESSFUL
* OR 12 IF THE CONVERSION FAILED (DUE TO INVALID INPUT JULIAN DATE).
* THE CALLING SEQUENCE FOR "JDATEX" IS AS FOLLOWS
*           1. THE INPUT JULIAN DATE IN THE FORMAT "YYDDD"
*           2. THE OUTPUT GREGORIAN DATE IN THE FORMAT "MMDDYY"
*
*      THE "JDATEX" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
JDATEX   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING INPUT,R4            ADDRESS INPUT PARAMETER DSECT
         USING OUTPUT,R5           ADDRESS OUTPUT PARAMETER DSECT
         USING WORKAREA,R13        ADDRESS WORKAREA DSECT
         LM    R4,R5,0(R2)         LOAD PARAMETER POINTERS
         TRT   YEARIN,NUMERIC      IF THE JULIAN YEAR IS NUMERIC
         BC    8,TESTDAY              THEN GO TEST THE JULIAN DAY
         B     ERROR                  ELSE FLAG FOR ERROR
TESTDAY  DS    0H
         TRT   DAYIN,NUMERIC       IF THE JULIAN DAY IS NUMERIC
         BC    8,INPUTNUM             THEN GO TRY TO CONVERT
         B     ERROR                  ELSE FLAG FOR ERROR
INPUTNUM DS    0H
         MVC   MONTHTBL,MTBL       MOVE THE DAYS/MONTH TABLE TO WORK
         MVC   YEARDAYS,YDAYS      MOVE DAYS/YEAR TO WORK AREA
         CLC   YEARIN,ZERO         IF INPUT YEAR IS ZEROES
         BE    NOTLEAP                THEN NOT A LEAP YEAR
         PACK  DBLWORD,YEARIN      CONVERT INPUT YEAR TO PACKED DECIMAL
         CVB   R6,DBLWORD          CONVERT INPUT YEAR TO BINARY
         ST    R6,SNGLWORD         SAVE BINARY INPUT YEAR
         TM    SNGLWORD+3,X'03'    IF THE INPUT YEAR NOT DIVISIBLE BY 4
         BNZ   NOTLEAP                THEN NOT A LEAP YEAR
         AP    MONTHTBL+4(4),ONE   ADD A DAY TO FEBRUARY
         AP    YEARDAYS,ONE           AND TO THE YEAR
NOTLEAP  DS    0H
         PACK  SNGLWORD,DAYIN      CONVERT INPUT DAY TO PACKED DECIMAL
         CP    SNGLWORD,PZERO      IF INPUT DAY IS ZERO
         BE    ERROR                  THEN FLAG FOR ERROR
         CP    SNGLWORD,YEARDAYS   IF INPUT DAY IS TOO LARGE
         BH    ERROR                  THEN FLAG FOR ERROR
         LA    R6,1                INITIALIZE OUTPUT MONTH
         LA    R7,MONTHTBL         POINT TO START OF MONTH TABLE
LOOP     DS    0H
         CP    SNGLWORD,0(4,R7)    IF THIS IS THE RIGHT MONTH
         BNH   GOTMONTH               THEN EXIT THE LOOP
         SP    SNGLWORD,0(4,R7)       ELSE ADJUST THE DAY
         LA    R6,1(,R6)                   AND MONTH
         LA    R7,4(,R7)                   AND TABLE POINTER
         B     LOOP                     AND KEEP LOOKING
GOTMONTH DS    0H
         UNPK  DAYOUT,SNGLWORD     CONVERT THE OUTPUT DAY
         OI    DAYOUT+1,X'F0'         TO CHARACTER FORMAT
         CVD   R6,DBLWORD          CONVERT THE OUTPUT MONTH
         UNPK  MONTHOUT,DBLWORD       TO CHARACTER
         OI    MONTHOUT+1,X'F0'          FORMAT
         MVC   YEAROUT,YEARIN      MOVE INPUT YEAR TO OUTPUT YEAR
         LA    R15,0               INDICATE SUCCESSFUL CONVERSION
         B     EXIT                   AND LEAVE
ERROR    DS    0H
         LA    R15,12              INDICATE ERROR IN INPUT
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
         EJECT
MTBL     DS    0H
         DC    PL4'31'             JANUARY
         DC    PL4'28'             FEBRUARY
         DC    PL4'31'             MARCH
         DC    PL4'30'             APRIL
         DC    PL4'31'             MAY
         DC    PL4'30'             JUNE
         DC    PL4'31'             JULY
         DC    PL4'31'             AUGUST
         DC    PL4'30'             SEPTEMBER
         DC    PL4'31'             OCTOBER
         DC    PL4'30'             NOVEMBER
         DC    PL4'31'             DECEMBER
MTBLEND  EQU   *
MTBLLEN  EQU   MTBLEND-MTBL
         SPACE
NUMERIC  DS    0F
         DC    256X'FF'
         ORG   NUMERIC+C'0'
         DC    10X'00'
         ORG
         SPACE
PZERO    DC    PL4'0'
ZERO     DC    CL2'00'
ONE      DC    PL4'1'
YDAYS    DC    PL4'365'
         EJECT
INPUT    DSECT ,                   MAP OF INPUT JULIAN DATE
YEARIN   DS    CL2                 INPUT JULIAN YEAR
DAYIN    DS    CL3                 INPUT JULIAN DAY
         SPACE
OUTPUT   DSECT ,                   MAP OF OUTPUT GREGORIAN DATE
MONTHOUT DS    CL2                 OUTPUT GREGORIAN MONTH
DAYOUT   DS    CL2                 OUTPUT GREGORIAN DAY
YEAROUT  DS    CL2                 OUTPUT GREGORIAN YEAR
         SPACE
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
WORKSTRT EQU   *
         DS    18A                 REGISTER SAVE AREA
DBLWORD  DS    D                   DOUBLEWORD WORK AREA
SNGLWORD DS    F                   SINGLEWORD WORK AREA
YEARDAYS DS    PL4                 DAYS PER YEAR
JDAY     DS    PL4                 COMPUTE AREA FOR JULIAN DAY
MONTHTBL DS    CL(MTBLLEN)         DAYS PER MONTH TABLE WORK AREA
WORKEND  EQU   *
WORKSIZE EQU   WORKEND-WORKSTRT    COMPUTE SIZE OF WORKAREA
         END
