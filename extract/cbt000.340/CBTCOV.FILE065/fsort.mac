FSORT    TITLE 'FORTRAN INTERFACE TO SORT UTILITY PROGRAM'
*
*      THIS PROGRAM PROVIDES A FORTRAN CALLABLE INTERFACE TO THE
* SORT UTILITY PROGRAM. PROVISIONS ARE MADE FOR THE E15 AND E35 EXITS,
* IF DESIRED. THE CALLING SEQUENCE FOR "FSORT" IS AS FOLLOWS:
*             1. TEXT OF "SORT" STATEMENT
*             2. LENGTH OF "SORT" STATEMENT (FULL WORD BINARY)
*                    IN BYTES
*             3. TEXT OF "RECORD" STATEMENT
*             4. LENGTH OF "RECORD" STATEMENT (FULL WORD BINARY)
*                    IN BYTES
*             5. E15 (INPUT EXIT) SUBROUTINE (OPTIONAL)
*                   (IF NO E15 EXIT IS DESIRED, THIS PARAMETER SHOULD
*                    POINT TO A FULLWORD OF BINARY ZEROS)
*             6. E35 (OUTPUT EXIT) SUBROUTINE (OPTIONAL)
*                   (IF NO E35 EXIT IS DESIRED, THIS PARAMETER SHOULD
*                    POINT TO A FULLWORD OF BINARY ZEROS)
*
*       THE E15 AND E35 EXITS AND THE "SORT" AND "RECORD" SORT
* CONTROL STATEMENTS ARE DESCRIBED IN THE "OS/VS SORT/MERGE
* PROGRAMMER'S GUIDE" (SC33-4035-6).
*
FSORT    ENTER PARMREG=2           SAVE REGISTERS AND INITIALIZE
         REGISTER                  REGISTER EQUATES
         LM    R4,R9,0(R2)         LOAD PARAMETER POINTERS
         L     R5,0(,R5)           LOAD LENGTH OF "SORT"
         AR    R5,R4               COMPUTE ENDING ADDRESS OF "SORT"
         L     R7,0(,R7)           LOAD LENGTH OF "RECORD"
         AR    R7,R6               COMPUTE ENDING ADDRESS OF "RECORD"
         XC    SORTAREA(SORTLEN),SORTAREA   CLEAR SORT PARAMETER AREA
         MVC   PARMLEN,H24         SET LENGTH OF SORT PARAMETER AREA
         ST    R4,SORT1            STORE START OF "SORT"
         ST    R5,SORT2            STORE END OF "SORT"
         ST    R6,RECORD1          STORE START OF "RECORD"
         ST    R7,RECORD2          STORE END OF "RECORD"
         TM    0(R7),X'80'         IS THIS THE LAST PARM?
         BZ    NOE35               IF SO, NO E15 OR E35 EXITS
         L     R10,0(,R8)          LOAD E15 CONTENTS
         LTR   R10,R10             IF THE ADDR IS BINARY ZEROS
         BZ    NOE15                  THEN DON'T USE THE E15 EXIT
         STCM  R8,7,E15            STORE E15 ADDRESS
NOE15    DS    0H
         TM    0(R8),X'80'         IS THIS THE LAST PARM?
         BZ    NOE35               IF SO, NO E35 EXIT
         L     R10,0(,R9)          LOAD E35 CONTENTS
         LTR   R10,R10             IF THE ADDR IS BINARY ZEROS
         BZ    NOE35                  THEN DON'T USE THE E35 EXIT
         STCM  R9,7,E35            STORE E35 ADDRESS
NOE35    DS    0H
         LA    R1,PARMLEN          LOAD ADDRESS OF PARAMETER
         ST    R1,PARMADDR            AND STORE IN POINTER AREA
         OI    PARMADDR,VLBIT      TURN ON HIGH ORDER BIT
         LA    R1,PARMADDR         POINT TO PARAMETER POINTER
         LINK  EP=SORT             LINK TO SORT UTILITY
         LEAVE RETCODE=(15)        RETURN TO CALLER
         EJECT
H24      DC    H'24'
VLBIT    EQU   X'80'
SORTAREA DS    0F
         DS    H
PARMLEN  DS    H                   LENGTH OF PARAMETER LIST
SORT1    DS    A
SORT2    DS    A
RECORD1  DS    A
RECORD2  DS    A
         DS    AL1
E15      DS    AL3
         DS    AL1
E35      DS    AL3
SORTLEN  EQU   *-SORTAREA
PARMADDR DS    A
         END
