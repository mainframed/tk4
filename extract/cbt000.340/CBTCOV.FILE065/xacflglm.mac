XACFLGLM TITLE 'PRE-LOGON EDITING PROGRAM'
*
*        THIS  PROGRAM IS THE PRE-LOGON LINE MODE EDITING PGM.
*
*        PARMS -FIVE ARGUMENTS ARE PASSED
*
*            1. ADDRESS OF PROGRAMMERS NAME FIELD    (20 BYTES).
*            2. ADDRESS OF PROGRAMMERS ACCT FIELD    (40 BYTES).
*            3. ADDRESS OF TSO REGION SIZE FIELD     (4 BYTES).
*            4. ADDRESS OF PROGRAMMERS ROOM NUMBER   (4 BYTES).
*            5. ADDRESS OF FIRST COMMAND FIELD       (20 BYTES).
*
*      CALLS: XACFLGTM - FOR TGETS AND TEST FOR TIME OUT
*
*        REGISTER 15 HAS RETURN CODE:
*                 0 - EVERYTHING IS OK.
*                 8 - TIME OUT OR TGET ERROR.
*
*        **** "XACFLGLM" IS REENTRANT AND REUSEABLE ****
*
*
XACFLGLM ENTER REGEQUS=YES,PARMREG=2,         START HERE W/REGISTERS   +
               GETMAIN=(WORKSIZE,0)           AND SOME CORE
         USING WORKAREA,R13                   TELL THE ASSEMBLER
         MVC   PARMSAVE(20),0(R2)             SAVE PARM ADDRESSES
         MVC   MOVEAREA,BLANKS                CLEAR WORK AREA
         MVC   MOVEAREA(L'STARTMSG),STARTMSG  MOVE IN FIRST MESSAGE
         BAL   R14,DOIT                       --> TELL USER
         CLI   IOAREA,C'Y'                    JUST LOGON?
         BNE   RETURN                         YEP, -->
         TPUT  PROMPTMS,L'PROMPTMS            GIVE HIM A PROMPT
         LM    R4,R8,PARMSAVE                 GET THE PARAMETERS
LOGLM00  MVC   MOVEAREA(L'L1),L1              MOVE IN CONSTANTS
         MVC   MOVEAREA+L'L1(20),0(R4)        MOVE IN DATA
         BAL   R14,DOIT                       DO REAL WORK
         CLC   IOAREA,BLANKS                  ANYTHING THERE?
         BE    LOGLM01                        NOPE, --> NEXT LINE
         MVC   0(20,R4),IOAREA                SAVE NEW DATA
LOGLM01  MVC   MOVEAREA(L'L2),L2              MOVE IN CONSTANTS
         MVC   MOVEAREA+L'L2(40),0(R5)        MOVE IN DATA
         BAL   R14,DOIT                       DO REAL WORK
         CLC   IOAREA,BLANKS                  ANYTHING THERE?
         BE    LOGLM02                        NOPE, --> NEXT LINE
         MVC   0(40,R5),IOAREA                SAVE NEW DATA
LOGLM02  MVC   MOVEAREA(L'L3),L3              MOVE IN CONSTANTS
         MVC   MOVEAREA+L'L3(4),0(R6)         MOVE IN DATA
         BAL   R14,DOIT                       DO REAL WORK
         CLC   IOAREA,BLANKS                  ANYTHING THERE?
         BE    LOGLM03                        NOPE, --> NEXT LINE
         MVC   0(4,R6),IOAREA                 SAVE NEW DATA
LOGLM03  MVC   MOVEAREA(L'L4),L4              MOVE IN CONSTANTS
         MVC   MOVEAREA+L'L4(4),0(R7)         MOVE IN DATA
         BAL   R14,DOIT                       DO REAL WORK
         CLC   IOAREA,BLANKS                  ANYTHING THERE?
         BE    LOGLM04                        NOPE, --> NEXT LINE
         MVC   0(4,R7),IOAREA                 SAVE NEW DATA
LOGLM04  MVC   MOVEAREA(L'L5),L5              MOVE IN CONSTANTS
         MVC   MOVEAREA+L'L5(20),0(R8)        MOVE IN DATA
         BAL   R14,DOIT                       DO REAL WORK
         CLC   IOAREA,BLANKS                  ANYTHING THERE?
         BE    RETURN                         NOPE, --> ALL DONE
         MVC   0(20,R8),IOAREA                SAVE NEW DATA
RETURN   LEAVE GETMAIN=(WORKSIZE,0),          GIVE BACK CORE           +
               RETCODE=(15)                   USE RETURN CODE
DOIT     BEGIN RENT=YES
         TPUT  MOVEAREA,LINESIZE              DISPLAY PARM STRING
         MVC   IOAREA,BLANKS                  CLEAR BUFFER
         MVI   TGETPRM,TGEDIT                 SET PROPER TGET FLAGS
         L     R15,@ACFLGTM                   GET ADDRESS OF TIMER
         CALL  (15),(IOAREA,LINESIZE,TGETPRM),  DO TGET AND TEST TIME  +
               MF=(E,PARMLIST)
         LTR   R15,R15                        GOOD RETURN CODE?
         BNZ   RETURN                         NOPE, --> EXIT
         OC    IOAREA,BLANKS                  MAKE IT UPPER CASE
         MVC   MOVEAREA,BLANKS                CLEAR WORK AREA
DOITX    GOBACK RENT=YES                      GO BACK TO CALLER
LINESIZE EQU   72
BLANKS   DC    CL72' '
TGEDIT   EQU   X'90'                          TGET EDIT,NOWAIT
STARTMSG DC    C'TO MODIFY DEFAULT ACCOUNTING INFORMATION ENTER "YES"'
PROMPTMS DC    C'KEY IN COMPLETE STRING OR JUST "ENTER"'
L1       DC    C'DEFAULT PROGRAMMER NAME ==> '
L2       DC    C'DEFAULT ACCOUNTING STRING ==> '
L3       DC    C'DEFAULT TSO REGION SIZE ==> '
L4       DC    C'DEFAULT ROOM NUMBER ==> '
L5       DC    C'DEFAULT FIRST COMMAND ==> '
*
@ACFLGTM DC    V(XACFLGTM)
WORKAREA DSECT
         DS    18F
RENTSAVE DS    F
PARMSAVE DS    5F
PARMLIST DS    3F
TGETPRM  DS    F
IOAREA   DS    CL(LINESIZE)
MOVEAREA DS    CL(LINESIZE)
WORKSIZE EQU   *-WORKAREA
         END
