TAPENUM  TITLE 'SUBROUTINE WHICH RETRIEVES VOLUME/SERIAL NUMBER'
*
*      THIS PROGRAM RETRIEVES THE FIRST VOLUME/SERIAL NUMBER ASSOCIATED
* WITH A GIVEN DDNAME. IF THE PROGRAM SUCCESSFULLY OBTAINS THE
* VOLUME/SERIAL NUMBER FROM THE JFCB, ZERO IS RETURNED IN REGISTER
* 15; OTHERWISE, THE REGISTER 15 VALUE FROM THE "RDJFCB" SVC IS
* RETURNED. THE CALLING SEQUENCE FOR "TAPENUM" IS AS FOLLOWS:
*           1. THE DDNAME (8-BYTE CHARACTER)
*           2. THE VOLUME/SERIAL NUMBER (6-BYTE CHARACTER)
*
*      THE "TAPENUM" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
TAPENUM  ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKLEN,0)
         REGISTER                  REGISTER EQUATES
         USING JFCBMAP,R4          ADDRESS JFCB DSECT
         USING IHADCB,R5           ADDRESS DCB DSECT
         USING WORKAREA,R13        ADDRESS WORK AREA DSECT
         LM    R6,R7,0(R2)         POINT TO PARAMETERS
         LA    R4,JFCB             POINT TO JFCB MAP
         LA    R5,DCB              POINT TO DCB MAP
         MVC   DCB,DUMMYDCB        MOVE DCB CONSTANTS TO WORK AREA
         MVC   JFCBPARM,RDJFCB     MOVE RDJFCB PARM TO WORK AREA
         ST    R4,EXLST            PUT JFCB ADDRESS IN EXIT LIST
         MVI   EXLST,X'07'         MARK EXIT LIST FOR RDJFCB
         LA    R8,EXLST            LOAD ADDRESS OF EXIT LIST
         STCM  R8,7,DCBEXLSA          AND STORE IT IN THE DCB
         MVC   DCBDDNAM,0(R6)      PUT DDNAME IN THE DCB
         RDJFCB ((R5)),            READ THE JFCB                       X
               MF=(E,JFCBPARM)
         LTR   R15,R15             IF THE RDJFCB FAILED
         BNZ   EXIT                   THEN LEAVE NOW
         MVC   0(6,R7),JFCBVOLS    MOVE VOLUME/SERIAL TO USER'S AREA
         LA    R15,0               SET RETURN CODE TO ZERO
         B     EXIT                   AND LEAVE
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(WORKLEN,0)
         EJECT
DUMMYDCB DCB   DDNAME=DUMMY,       CONSTANT DUMMY DCB                  X
               DSORG=PS,MACRF=GL
DCBLEN   EQU   *-DUMMYDCB          LENGTH OF DCB
         SPACE 2
RDJFCB   RDJFCB DUMMYDCB,MF=L      CONSTANT RDJFCB PARAMETER LIST
RDJFCBLN EQU   *-RDJFCB
         EJECT
JFCBMAP  DSECT ,                   MAP OF THE JFCB
         IEFJFCBN
         EJECT
         DCBD  ,                   MAP OF THE DCB
         EJECT
WORKAREA DSECT ,                   MAP OF THE GETMAINED WORK AREA
         DS    18A                 REGISTER SAVE AREA
EXLST    DS    A                   EXIT LIST FOR RDJFCB
JFCB     DS    CL176               JFCB WORK AREA
JFCBPARM DS    CL(RDJFCBLN)        RDJFCB PARAMETER LIST
DCB      DS    CL(DCBLEN)          DCB WORK AREA
WORKLEN  EQU   *-WORKAREA          LENGTH OF WORK AREA
         END
