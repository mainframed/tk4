*          DATA SET TSDCVTMDY  AT LEVEL 001 AS OF 02/12/73
MDY      TITLE 'CVTMDY - CONVERT JULIAN TO HUMAN DATE'
CVTMDY   CSECT
         SAVE  (14,12),T                SAVE CALLER'S REGS
         REGS
         BALR  R12,0                    GET A BASE
         LUSE  INIT,R12                 TELL ASSEMBLER
INIT     LR    R3,R1                    00YYDDDF AS FROM TIME
         LR    R2,R0                    POINTER TO OUTPUT AREA
         GETMAIN R,LV=WRKLEN            GET SOME CORE
         LR    R11,R1                   SAVE ADDRESS
         XC    0(WRKLEN,R11),0(R11)     CLEAR WORKAREA
         LUSE  WRKAREA,R11              TELL ASSEMBLER
         TITLE 'CVTMDY - CHECK PARMS AND CONVERT'
         INUSE
         ST    R3,INPARM                SAVE IT
         XC    0(8,R2),0(R2)            CLEAR OUTPUT FIELD
         CLI   INPARM,X'00'             HIGH BYTE ZERO?
         BNE   BADPARM
         CLC   INPARM+2(2),X366F        PRELIMINARY CHECK
         BH    BADPARM
         XR    R5,R5                    CLEAR SOME REGS FOR PROCESSING
         SR    R6,R6                    CONTINUE
         MVC   INPARM(1),INPARM+1
         NI    INPARM,X'F0'             JUST YEAR
         IC    R5,INPARM                GET IT
         SRL   R5,4                     TO 4 BITS
         MH    R5,H10                   * 10 FOR YEAR
         NI    INPARM+1,X'0F'           ONLY THAT
         MVI   INPARM,X'00'             CHEAT
         AH    R5,INPARM                HAVE YEAR IN BINARY
         STH   R5,INPARM                SAVE FOR LATER
         SRL   R5,2                     DIVIDE BY 4
         SLL   R5,2                     AND MULTIPLY BY 4
         EX    R5,CLI                   SAME?
         LA    R15,STDYEAR              ASSUME STD YEAR
         BNE   NOTLEAP                  OK
         LA    R15,LEAPYR               IT'S A LEAP YEAR, FIENDS
         B     GETDATE
NOTLEAP  CLC   INPARM+2(2),X366F        CHECK IT
         BNL   BADPARM                  FORGET IT... IT'S ILLEGAL
         SPACE 3
GETDATE  MVC   DOUBLE+6(2),INPARM+2     GET DATE
         CVB   R10,DOUBLE               GET IT
         SPACE 1
CVTL1    SH    R10,0(,R15)              SUBTRACT TAB ENTRY
         LA    R6,1(,R6)                UP MONTH NUMBER
         BNP   CVTL3                    END
         LA    R15,2(,R15)              GET NEXT ENTRY
         B     CVTL1                    TRY AGAIN
         SPACE 2
CVTL3    AH    R10,0(,R15)              GET NUMBER OF DAYS BACK AGAIN
         IC    R5,INPARM+1              GET YEAR
         CVD   R6,DOUBLE                CONVERT TO DECIMAL
         UNPK  0(2,R2),DOUBLE+6(2)      MM
         OC    1(2,R2),C0S              DO IT
         CVD   R10,DOUBLE               DO SAME TRIP FOR DAY
         UNPK  3(2,R2),DOUBLE+6(2)      DD
         OC    4(2,R2),C0S
         CVD   R5,DOUBLE                AND, LASTLY, YEAR
         UNPK  6(2,R2),DOUBLE+6(2)      YY
         OI    7(R2),C'0'               SET IT UP
         TITLE 'CVTMDY - RETURN, ERRORS AND CONSTANTS'
         INUSE
RETURN0  XR    R2,R2                    CLEAR RETURN CODE
         SPACE 1
RETURN   FREEMAIN R,LV=WRKLEN,A=(R11)   FREE CORE
         LR    R15,R2                   LOAD RETURN CODE
         RETURN (14,12),,RC=(15)        RETURN TO CALLER
         SPACE 3
BADPARM  MVC   0(8,R2),NULLFMT          GIVE HIM A FORMAT
         LA    R2,4                     ERROR RETURN
         B     RETURN
         SPACE 3
CLI      CLI   INPARM+1,0               <<EXECUTED>>
         SPACE 2
H10      DC    H'10'
X366F    DC    X'366F'
C0S      DC    C'0/'
NULLFMT  DC    CL8'00/00/00'
         DS    0H                       ALIGN
STDYEAR  DC    AL2(31,28,31,30,31,30,31,31,30,31,30,31)
LEAPYR   DC    AL2(31,29,31,30,31,30,31,31,30,31,30,31)
WRKAREA  DSECT
DOUBLE   DS    D                        FOR CONVERTS
INPARM   DS    F                        FOR MANIPULATION
WASTED   DS    F                        JUST WHAT IT SAYS
WRKLEN   EQU   *-WRKAREA
CVTMDY   CSECT
         LDROP R12,R11
         INUSE
         END
