00001  ID DIVISION.
00002
00003  PROGRAM-ID.    IDRLIST2.
00004  AUTHOR.        T R SOLOMON.
00005  DATE-COMPILED.
00006
00007  REMARKS.
00008
00009    *************************************************************
00010    *  THIS PROGRAM INVOKES THE SORT VERB ONCE TO SEQUENCE IDR  *
00011    *  DATA FOR THREE SEPARATE REPORTS -- BY IDR DATA, BY MOD-  *
00012    *  ULE, AND BY DATE.  TOTALS FOR THE UNIQUE ENTRIES IN THE  *
00013    *  MAJOR FIELDS OF THE FIRST TWO REPORTS ARE ACCUMULATED    *
00014    *  AND PRINTED AT THE END OF THE RESPECTIVE REPORTS.        *
00015    *************************************************************
00016
00017
00018
00019  ENVIRONMENT DIVISION.
00020
00021
00022  CONFIGURATION SECTION.
00023
00024  SOURCE-COMPUTER. IBM-370.
00025  OBJECT-COMPUTER. IBM-370.
00026
00027
00028  INPUT-OUTPUT SECTION.
00029
00030  FILE-CONTROL.
00031      SELECT INFILE ASSIGN TO UT-S-IDRINPUT.
00032      SELECT SORTFILE ASSIGN TO DA-S-SORTWORK.
00033      SELECT OUTFILE ASSIGN TO UT-S-REPTSOUT.
00034      EJECT
00035  DATA DIVISION.
00036
00037
00038  FILE SECTION.
00039
00040  FD  INFILE
00041          RECORDING MODE IS F
00042          BLOCK CONTAINS 0 RECORDS
00043          RECORD CONTAINS 84 CHARACTERS
00044          LABEL RECORDS ARE STANDARD
00045          DATA RECORD IS DATA-REC.
00046  01  DATA-REC.
00047      05 IDR-DATA.
00048        10 CHK-FLD       PIC X(3).
00049        10 FILLER        PIC X(37).
00050      05 SYS-LIB         PIC X(8).
00051      05 VOL-SER         PIC X(6).
00052      05 LNK-ZAP         PIC X.
00053      05 JUL-DATE        PIC S99V999.
00054      05 MODULE-ID       PIC X(8).
00055      05 CSECT-ID        PIC X(8).
00056      05 SSI-ID          PIC X(8).
00057
00058  SD  SORTFILE
00059          RECORD CONTAINS 147 CHARACTERS
00060          DATA RECORDS ARE SORT-REC-RPT1,
00061                           SORT-REC-RPT2,
00062                           SORT-REC-RPT3.
00063  01  SORT-REC-RPT1.
00064      02 CTL-FLDS-ASCENDING.
00065        05 RPT-CTL-CHAR  PIC X.
00066        05 IDR-DATA-SRT  PIC X(40).
00067        05 FILLER        PIC X(22).
00068      02 DATA-FLDS.
00069        05 IDR-DATA      PIC X(40).
00070        05 SYS-LIB       PIC X(8).
00071        05 VOL-SER       PIC X(6).
00072        05 LNK-ZAP       PIC X.
00073        05 JUL-DATE      PIC S99V999.
00074        05 MODULE-ID     PIC X(8).
00075        05 CSECT-ID      PIC X(8).
00076        05 SSI-ID        PIC X(8).
00077  01  SORT-REC-RPT2.
00078      02 CTL-FLDS-ASCDG.
00079        05 RPT-CTL-CHAR  PIC X.
00080        05 MODULE-ID-SRT PIC X(8).
00081        05 CSECT-ID-SRT  PIC X(8).
00082        05 JUL-DATE-SRT  PIC S99V999.
00083        05 FILLER        PIC X(125).
00084  01  SORT-REC-RPT3.
00085      02 CTL-FLDS-ASCDG.
00086        05 RPT-CTL-CHAR  PIC X.
00087        05 JUL-DATE-SRT  PIC S99V999.
00088        05 IDR-DATA-SRT  PIC X(40).
00089        05 MODULE-ID-SRT PIC X(8).
00090        05 CSECT-ID-SRT  PIC X(8).
00091      02 CTL-FLDS-DESCENDING.
00092        05 LNK-ZAP-SRT   PIC X.
00093        05 FILLER        PIC X(84).
00094
00095  FD  OUTFILE
00096          RECORDING MODE IS F
00097          BLOCK CONTAINS 0 RECORDS
00098          RECORD CONTAINS 133 CHARACTERS
00099          LABEL RECORDS ARE STANDARD
00100          DATA RECORD IS PRINT-REC.
00101  01  PRINT-REC          PIC X(133).
00102
00103
00104  WORKING-STORAGE SECTION.
00105
00106  77  TOT-CTR            PIC S9(4) COMP SYNC VALUE ZERO.
00107  77  PG-CTR             PIC S9(4) COMP SYNC VALUE +0001.
00108  77  REPORT-NO          PIC S9    COMP SYNC VALUE +1.
00109  77  LINE-CTR           PIC S9(2) COMP SYNC VALUE +99.
00110  77  LINE-MAX           PIC S9(2) COMP SYNC VALUE +59.
00111  77  IDR-PREV           PIC X(40) VALUE SPACE.
00112  77  MOD-PREV           PIC X(8)  VALUE SPACE.
00113  77  BLANK-REC          PIC X(133) VALUE SPACE.
00114
00115  01  RPT-TITLE-1.
00116      05 FILLER          PIC X.
00117      05 FILLER          PIC X(7) VALUE 'REPORT '.
00118      05 REPORT-ID       PIC X VALUE '1'.
00119      05 FILLER          PIC X(34) VALUE SPACE.
00120      05 FILLER          PIC X(50) VALUE
00121          'SYSTEM MAINTENANCE IDR REPORT'.
00122      05 FILLER          PIC X(27) VALUE SPACE.
00123      05 FILLER          PIC X(4) VALUE 'PAGE'.
00124      05 FILLER          PIC X(2) VALUE SPACE.
00125      05 PG-NO           PIC ZZZ9 VALUE ZERO.
00126      05 FILLER          PIC X(3) VALUE SPACE.
00127  01  RPT-TITLE-2.
00128      05 FILLER          PIC X VALUE SPACE.
00129      05 NUMERIC-DATE    PIC X(8) VALUE SPACE.
00130      05 FILLER          PIC X(51) VALUE SPACE.
00131      05 RPT-NAME-FLD    PIC X(16) VALUE 'LIST BY IDR DATA'.
00132      05 FILLER          PIC X(57) VALUE SPACE.
00133
00134  01  COLUMN-HDR-TABLE.
00135      03  HDR-RPT1.
00136          05 FILLER      PIC X(24) VALUE SPACE.
00137          05 FILLER      PIC X(8) VALUE 'IDR DATA'.
00138          05 FILLER      PIC X(21) VALUE SPACE.
00139          05 FILLER      PIC X(7) VALUE 'LIBRARY'.
00140          05 FILLER      PIC X(6) VALUE SPACE.
00141          05 FILLER      PIC X(6) VALUE 'VOLSER'.
00142          05 FILLER      PIC X(4) VALUE SPACE.
00143          05 FILLER      PIC X(3) VALUE 'L/Z'.
00144          05 FILLER      PIC X(5) VALUE SPACE.
00145          05 FILLER      PIC X(4) VALUE 'DATE'.
00146          05 FILLER      PIC X(7) VALUE SPACE.
00147          05 FILLER      PIC X(6) VALUE 'MODULE'.
00148          05 FILLER      PIC X(7) VALUE SPACE.
00149          05 FILLER      PIC X(5) VALUE 'CSECT'.
00150          05 FILLER      PIC X(9) VALUE SPACE.
00151          05 FILLER      PIC X(3) VALUE 'SSI'.
00152          05 FILLER      PIC X(8) VALUE SPACE.
00153      03  HDR-RPT2.
00154          05 FILLER      PIC X(8) VALUE SPACE.
00155          05 FILLER      PIC X(6) VALUE 'MODULE'.
00156          05 FILLER      PIC X(7) VALUE SPACE.
00157          05 FILLER      PIC X(5) VALUE 'CSECT'.
00158          05 FILLER      PIC X(8) VALUE SPACE.
00159          05 FILLER      PIC X(4) VALUE 'DATE'.
00160          05 FILLER      PIC X(22) VALUE SPACE.
00161          05 FILLER      PIC X(8) VALUE 'IDR DATA'.
00162          05 FILLER      PIC X(21) VALUE SPACE.
00163          05 FILLER      PIC X(7) VALUE 'LIBRARY'.
00164          05 FILLER      PIC X(6) VALUE SPACE.
00165          05 FILLER      PIC X(6) VALUE 'VOLSER'.
00166          05 FILLER      PIC X(4) VALUE SPACE.
00167          05 FILLER      PIC X(3) VALUE 'L/Z'.
00168          05 FILLER      PIC X(6) VALUE SPACE.
00169          05 FILLER      PIC X(3) VALUE 'SSI'.
00170          05 FILLER      PIC X(9) VALUE SPACE.
00171      03  HDR-RPT3.
00172          05 FILLER      PIC X(8) VALUE SPACE.
00173          05 FILLER      PIC X(4) VALUE 'DATE'.
00174          05 FILLER      PIC X(21) VALUE SPACE.
00175          05 FILLER      PIC X(8) VALUE 'IDR DATA'.
00176          05 FILLER      PIC X(23) VALUE SPACE.
00177          05 FILLER      PIC X(6) VALUE 'MODULE'.
00178          05 FILLER      PIC X(7) VALUE SPACE.
00179          05 FILLER      PIC X(5) VALUE 'CSECT'.
00180          05 FILLER      PIC X(6) VALUE SPACE.
00181          05 FILLER      PIC X(3) VALUE 'L/Z'.
00182          05 FILLER      PIC X(4) VALUE SPACE.
00183          05 FILLER      PIC X(7) VALUE 'LIBRARY'.
00184          05 FILLER      PIC X(6) VALUE SPACE.
00185          05 FILLER      PIC X(6) VALUE 'VOLSER'.
00186          05 FILLER      PIC X(7) VALUE SPACE.
00187          05 FILLER      PIC X(3) VALUE 'SSI'.
00188          05 FILLER      PIC X(9) VALUE SPACE.
00189  01  FILLER  REDEFINES COLUMN-HDR-TABLE.
00190      05  COLUMN-HDR  OCCURS 3 TIMES  PIC X(133).
00191
00192  01  DTL-RPT1.
00193      05 FILLER          PIC X(8) VALUE SPACE.
00194      05 IDR-DATA        PIC X(40) VALUE SPACE.
00195      05 FILLER          PIC X(5) VALUE SPACE.
00196      05 SYS-LIB         PIC X(8) VALUE SPACE.
00197      05 FILLER          PIC X(5) VALUE SPACE.
00198      05 VOL-SER         PIC X(6) VALUE SPACE.
00199      05 FILLER          PIC X(5) VALUE SPACE.
00200      05 LNK-ZAP         PIC X VALUE SPACE.
00201      05 FILLER          PIC X(5) VALUE SPACE.
00202      05 JUL-DATE        PIC 99.999 VALUE ZEROES.
00203      05 FILLER          PIC X(5) VALUE SPACE.
00204      05 MODULE-ID       PIC X(8) VALUE SPACE.
00205      05 FILLER          PIC X(5) VALUE SPACE.
00206      05 CSECT-ID        PIC X(8) VALUE SPACE.
00207      05 FILLER          PIC X(5) VALUE SPACE.
00208      05 SSI-ID          PIC X(8) VALUE SPACE.
00209      05 FILLER          PIC X(5) VALUE SPACE.
00210  01  DTL-RPT2.
00211      05 FILLER          PIC X(7) VALUE SPACE.
00212      05 MODULE-ID       PIC X(8) VALUE SPACE.
00213      05 FILLER          PIC X(5) VALUE SPACE.
00214      05 CSECT-ID        PIC X(8) VALUE SPACE.
00215      05 FILLER          PIC X(5) VALUE SPACE.
00216      05 JUL-DATE        PIC 99.999 VALUE ZEROES.
00217      05 FILLER          PIC X(5) VALUE SPACE.
00218      05 IDR-DATA        PIC X(40) VALUE SPACE.
00219      05 FILLER          PIC X(5) VALUE SPACE.
00220      05 SYS-LIB         PIC X(8) VALUE SPACE.
00221      05 FILLER          PIC X(5) VALUE SPACE.
00222      05 VOL-SER         PIC X(6) VALUE SPACE.
00223      05 FILLER          PIC X(5) VALUE SPACE.
00224      05 LNK-ZAP         PIC X VALUE SPACE.
00225      05 FILLER          PIC X(5) VALUE SPACE.
00226      05 SSI-ID          PIC X(8) VALUE SPACE.
00227      05 FILLER          PIC X(9) VALUE SPACE.
00228  01  DTL-RPT3.
00229      05 FILLER          PIC X(7) VALUE SPACE.
00230      05 JUL-DATE        PIC 99.999 VALUE ZEROES.
00231      05 FILLER          PIC X(5) VALUE SPACE.
00232      05 IDR-DATA        PIC X(40) VALUE SPACE.
00233      05 FILLER          PIC X(5) VALUE SPACE.
00234      05 MODULE-ID       PIC X(8) VALUE SPACE.
00235      05 FILLER          PIC X(5) VALUE SPACE.
00236      05 CSECT-ID        PIC X(8) VALUE SPACE.
00237      05 FILLER          PIC X(5) VALUE SPACE.
00238      05 LNK-ZAP         PIC X VALUE SPACE.
00239      05 FILLER          PIC X(5) VALUE SPACE.
00240      05 SYS-LIB         PIC X(8) VALUE SPACE.
00241      05 FILLER          PIC X(5) VALUE SPACE.
00242      05 VOL-SER         PIC X(6) VALUE SPACE.
00243      05 FILLER          PIC X(5) VALUE SPACE.
00244      05 SSI-ID          PIC X(8) VALUE SPACE.
00245      05 FILLER          PIC X(9) VALUE SPACE.
00246
00247  01  STATISTICS-LINE.
00248      05 FILLER          PIC X(8) VALUE SPACE.
00249      05 FILLER          PIC X(17) VALUE 'NUMBER OF UNIQUE '.
00250      05 TOT-DESC        PIC X(8) VALUE SPACE.
00251      05 FILLER          PIC X(12) VALUE ' ENTRIES  = '.
00252      05 TOT-COUNT       PIC ZZZ9 VALUE ZEROES.
00253      05 FILLER          PIC X(84) VALUE SPACE.
00254      EJECT
00255  PROCEDURE DIVISION.
00256
00257
00258  HSKPG SECTION.
00259      MOVE CURRENT-DATE TO NUMERIC-DATE.
00260      OPEN INPUT INFILE OUTPUT OUTFILE.
00261      MOVE +999999 TO SORT-CORE-SIZE.
00262      SORT SORTFILE
00263        ON ASCENDING KEY CTL-FLDS-ASCENDING
00264          DESCENDING KEY CTL-FLDS-DESCENDING
00265        INPUT  PROCEDURE IN-PROC
00266        OUTPUT PROCEDURE OUT-PROC.
00267      CLOSE INFILE OUTFILE.
00268      STOP RUN.
00269
00270
00271  IN-PROC SECTION.
00272
00273  READ-INFILE.
00274      READ INFILE AT END GO TO IPEND.
00275      IF CHK-FLD = 'RSI' GO TO READ-INFILE.
00276
00277  BUILD-SORTREC-1.
00278      MOVE CORR DATA-REC TO DATA-FLDS.
00279      MOVE SPACES TO CTL-FLDS-ASCENDING.
00280      MOVE 1 TO RPT-CTL-CHAR OF SORT-REC-RPT1.
00281      MOVE IDR-DATA OF DATA-REC TO IDR-DATA-SRT OF SORT-REC-RPT1.
00282      RELEASE SORT-REC-RPT1.
00283
00284  BUILD-SORTREC-2.
00285      MOVE CORR DATA-REC TO DATA-FLDS.
00286      MOVE SPACES TO CTL-FLDS-ASCENDING.
00287      MOVE 2 TO RPT-CTL-CHAR OF SORT-REC-RPT2.
00288      MOVE MODULE-ID OF DATA-REC TO MODULE-ID-SRT OF SORT-REC-RPT2.
00289      MOVE CSECT-ID OF DATA-REC TO CSECT-ID-SRT OF SORT-REC-RPT2.
00290      MOVE JUL-DATE OF DATA-REC TO JUL-DATE-SRT OF SORT-REC-RPT2.
00291      RELEASE SORT-REC-RPT2.
00292
00293  BUILD-SORTREC-3.
00294      MOVE CORR DATA-REC TO DATA-FLDS.
00295      MOVE SPACES TO CTL-FLDS-ASCENDING.
00296      MOVE 3 TO RPT-CTL-CHAR OF SORT-REC-RPT3.
00297      MOVE JUL-DATE OF DATA-REC TO JUL-DATE-SRT OF SORT-REC-RPT3.
00298      MOVE IDR-DATA OF DATA-REC TO IDR-DATA-SRT OF SORT-REC-RPT3.
00299      MOVE MODULE-ID OF DATA-REC TO MODULE-ID-SRT OF SORT-REC-RPT3.
00300      MOVE CSECT-ID OF DATA-REC TO CSECT-ID-SRT OF SORT-REC-RPT3.
00301      MOVE LNK-ZAP OF DATA-REC TO LNK-ZAP-SRT OF SORT-REC-RPT3.
00302      RELEASE SORT-REC-RPT3.
00303      GO TO READ-INFILE.
00304
00305  IPEND.
00306      EXIT.
00307
00308
00309  OUT-PROC SECTION.
00310
00311  RETURN-SORT-REC.
00312      RETURN SORTFILE AT END GO TO OPEND.
00313      IF RPT-CTL-CHAR OF CTL-FLDS-ASCENDING IS EQUAL TO 1
00314        GO TO BUILD-RPT1.
00315      IF RPT-CTL-CHAR OF CTL-FLDS-ASCENDING IS EQUAL TO 2
00316        GO TO BUILD-RPT2.
00317      IF RPT-CTL-CHAR OF CTL-FLDS-ASCENDING IS EQUAL TO 3
00318        GO TO BUILD-RPT3.
00319
00320  BUILD-RPT1.
00321      IF LINE-CTR IS GREATER THAN LINE-MAX
00322        PERFORM WRITE-TLE-HDRS.
00323      MOVE CORR DATA-FLDS TO DTL-RPT1.
00324      IF IDR-DATA OF DTL-RPT1 IS NOT EQUAL TO IDR-PREV
00325        THEN ADD +0001 TO TOT-CTR
00326        ELSE IF LINE-CTR IS NOT EQUAL TO +06
00327               MOVE SPACES TO IDR-DATA OF DTL-RPT1.
00328      MOVE IDR-DATA OF DATA-FLDS TO IDR-PREV.
00329      WRITE PRINT-REC FROM DTL-RPT1 AFTER POSITIONING 1.
00330      ADD +01 TO LINE-CTR.
00331      GO TO RETURN-SORT-REC.
00332
00333  BUILD-RPT2.
00334      ON 1 MOVE 'IDR DATA' TO TOT-DESC
00335        PERFORM PRINT-STATISTICS
00336        MOVE 2 TO REPORT-NO
00337        MOVE '2' TO REPORT-ID
00338        MOVE ' LIST BY MODULE ' TO RPT-NAME-FLD.
00339      IF LINE-CTR IS GREATER THAN LINE-MAX
00340        PERFORM WRITE-TLE-HDRS.
00341      MOVE CORR DATA-FLDS TO DTL-RPT2.
00342      IF MODULE-ID OF DTL-RPT2 IS NOT EQUAL TO MOD-PREV
00343        THEN ADD +0001 TO TOT-CTR
00344        ELSE IF LINE-CTR IS NOT EQUAL TO +06
00345               MOVE SPACES TO MODULE-ID OF DTL-RPT2.
00346      MOVE MODULE-ID OF DATA-FLDS TO MOD-PREV.
00347      WRITE PRINT-REC FROM DTL-RPT2 AFTER POSITIONING 1.
00348      ADD +01 TO LINE-CTR.
00349      GO TO RETURN-SORT-REC.
00350
00351  BUILD-RPT3.
00352      ON 1 MOVE ' MODULE ' TO TOT-DESC
00353        PERFORM PRINT-STATISTICS
00354        MOVE 3 TO REPORT-NO
00355        MOVE '3' TO REPORT-ID
00356        MOVE '  LIST BY DATE  ' TO RPT-NAME-FLD.
00357      IF LINE-CTR IS GREATER THAN LINE-MAX
00358        PERFORM WRITE-TLE-HDRS.
00359      MOVE CORR DATA-FLDS TO DTL-RPT3.
00360      WRITE PRINT-REC FROM DTL-RPT3 AFTER POSITIONING 1.
00361      ADD +01 TO LINE-CTR.
00362      GO TO RETURN-SORT-REC.
00363
00364  WRITE-TLE-HDRS.
00365      MOVE PG-CTR TO PG-NO.
00366      WRITE PRINT-REC FROM RPT-TITLE-1 AFTER POSITIONING 0.
00367      WRITE PRINT-REC FROM RPT-TITLE-2 AFTER POSITIONING 1.
00368      WRITE PRINT-REC FROM COLUMN-HDR (REPORT-NO)
00369          AFTER POSITIONING 2.
00370      WRITE PRINT-REC FROM BLANK-REC AFTER POSITIONING 1.
00371      MOVE +06 TO LINE-CTR.
00372      ADD +0001 TO PG-CTR.
00373
00374  PRINT-STATISTICS.
00375      ADD +05 TO LINE-CTR.
00376      IF LINE-CTR IS GREATER THAN LINE-MAX
00377        PERFORM WRITE-TLE-HDRS.
00378      WRITE PRINT-REC FROM BLANK-REC AFTER POSITIONING 3.
00379      MOVE TOT-CTR TO TOT-COUNT.
00380      WRITE PRINT-REC FROM STATISTICS-LINE AFTER POSITIONING 3.
00381      MOVE +0001 TO PG-CTR.
00382      MOVE +99 TO LINE-CTR.
00383      MOVE ZERO TO TOT-CTR.
00384
00385  OPEND.
           EXIT.
