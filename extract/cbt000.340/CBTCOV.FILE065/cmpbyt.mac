CMPBYT   TITLE 'SUBROUTINE WHICH COMPARES BYTE STRINGS'
*
*      THIS PROGRAM COMPARES TWO BYTE STRINGS AND SETS THE RETURN
* CODE TO INDICATE THE RESULTS OF THAT COMPARISON. UPON RETURN,
* REGISTER 15 CONTAINS ONE OF THE FOLLOWING VALUES:
*           0 IF THE TWO BYTE STRINGS ARE EQUAL
*           4 IF THE FIRST BYTE STRING IS GREATER THAN THE SECOND
*                BYTE STRING
*           8 IF THE SECOND BYTE STRING IS GREATER THAN THE FIRST
*                BYTE STRING
*          12 IF THE ARGUMENTS PASSED TO THE PROGRAM WERE INVALID
* THE CALLING SEQUENCE FOR "CMPBYT" IS AS FOLLOWS
*           1. THE FIRST OPERAND TO BE COMPARED
*           2. THE OFFSET IN BYTES FROM THE FIRST FIELD TO BE
*                COMPARED (4-BYTE BINARY)
*           3. THE SECOND OPERAND TO BE COMPARED
*           4. THE OFFSET IN BYTES FROM THE SECOND FIELD TO BE
*                COMPARED (4-BYTE BINARY)
*           5. THE LENGTH IN BYTES OF THE BYTE STRINGS TO BE
*                COMPARED (4-BYTE BINARY) - MUST BE GREATER THAN 0
*                AND LESS THAN 256
*
*      THE "CMPBYT" SUBROUTINE IS REENTRANT AND REUSEABLE
*
CMPBYT   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(72,0)
         REGISTER                  REGISTER EQUATES
         LM    R4,R8,0(R2)         LOAD PARAMETER POINTERS
         A     R4,0(,R5)           ADD OFFSET TO FIRST OPERAND
         A     R6,0(,R7)           ADD OFFSET TO SECOND OPERAND
         L     R8,0(,R8)           LOAD LENGTH OF COMPARE
         LTR   R8,R8               IF THE LENGTH IS NOT POSITIVE
         BNP   ERROR                  THEN FLAG FOR ERROR
         C     R8,=F'256'          IF THE LENGTH IS GREATER THAN 256
         BH    ERROR                  THEN FLAG FOR ERROR
         BCTR  R8,R0               DECREMENT FOR EXECUTE
         EX    R8,COMPARE          PERFORM THE COMPARISON
         BE    EQUAL
         BH    HIGH
         BL    LOW
ERROR    DS    0H
         LA    R15,12              INDICATE ERROR
         B     EXIT                   AND LEAVE
EQUAL    DS    0H
         LA    R15,0               INDICATE EQUAL
         B     EXIT                   AND LEAVE
HIGH     DS    0H
         LA    R15,4               INDICATE HIGH
         B     EXIT                   AND LEAVE
LOW      DS    0H
         LA    R15,8               INDICATE LOW
         B     EXIT                   AND LEAVE
COMPARE  DS    0H
         CLC   0(0,R4),0(R6)       COMPARE THE STRINGS
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(72,0)
         END
