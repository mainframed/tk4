PDSEXPDT CSECT
         PRINT NOGEN
*----------------D  O  C  U  M  E  N  T  A  T  I  O  N----------------*
*                                                                     *
*  TITLE      =  PDSEXPDT                                             *
*                                                                     *
*  THIS PROGRAM READS PDS MODULE DATA RECORDS AND EXEMPT CARDS.
*  BOTH OF THESE FILES ARE IN MODULE NAME SEQUENCE. WHEN AN           *
*  EXEMPT CARD IS PRESENT THE MODULE RECORD IS IGNORED. THE MODULE    *
*  RECORDS WITHOUT EXEMPTIONS ARE COMPARED BY LINK DATE TO A PARM     *
*  FIELD SUPPLIED DATE. ALL MODULE RECORDS WITH AN EARLIER DATE THAN  *
*  THE PARM DATE ARE USED TO GENERATE A DELETE RECORD. THE DELETE     *
*  RECORD IS USED AS INPUT TO AN IEBCOPY PROGRAM THAT COPIES THE      *
*  MODULE TO BE DELETED TO A BACKUP TAPE. THE DELETE RECORD IS THEN   *
*  USED AS INPUT TO THE DELETE PROGRAM.                               *
*                                                                     *
*  EXIT/NORMAL=  RETURN TO CALLER                                     *
*                                                                     *
*  EXIT/ERROR =  U100 - I/O ERROR OCCURRED                            *
*                U200 - ERROR OPENING SYSPRINT DD STATEMENT           *
*                                                                     *
*  ATTRIBUTES =  ***** AUTHORIZED *****                               *
*                                                                     *
*  AUTHOR     =  REX BIRCH                                            *
*                TRW DSSG, REDONDO BEACH, CALIFORNIA                  *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*     REGISTER  EQUATES                                               *
*---------------------------------------------------------------------*
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7  USED FOR CONTINUED ADDRESSABILITY OF PROGRAM CODE
R8       EQU   8  USED FOR BAL R8,SNAPIT
R9       EQU   9  USED FOR BAL R9,LISTALL
R10      EQU   10 USED FOR BAL R10,LINECK + BAL R10,ALLINECK
R11      EQU   11 USED FOR ADDRESSABILTY OF INPUT AREAS (PDS+EXEMPTS)
R12      EQU   12
RBASE    EQU   12 USED FOR ADDRESSABILITY OF THE PROGRAM CODE
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
*---------------------------------------------------------------------*
*        H O U S E K E E P I N G                                      *
*---------------------------------------------------------------------*
         SPACE 1
         SAVE  (14,12)                 SAVE CALLER'S REGS
         BALR  RBASE,R0                     ESTABLISH
         USING *,RBASE,R7                 ADDRESSABILITY
HERE     L     R7,BASE                 POINT R7 T0 HERE + 4096
         LA    R2,SAVEAREA             ADDRESS OF MY SAVEAREA
         ST    R13,4(R2)               CHAIN THE SAVE AREAS
         ST    R2,8(R13)                      AND
         LR    R13,R2                  ESTABLISH THE POINTER TO MINE
         SPACE 2
* MOVE PARM IN TO MY AREA
         L     R3,0(,R1)      PARAMETER ADDRESS
         MVC   PARMIN(8),0(R3)       PARM TO MY AREA
*---------------------------------------------------------------------*
*        OPEN OUTPUT FILE THAT WILL CONTAIN                           *
*        A LISTING OF THE MODULES TO BE DELETED.                      *
*---------------------------------------------------------------------*
         SPACE 1
         OPEN  (SYSPRINT,(OUTPUT))     OPEN OUTPUT FILE
         LA    R1,SYSPRINT             LOAD ADDR IN CASE OF ERROR
         TM    SYSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO, EXIT INDICATING ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        OPEN OUTPUT FILE THAT WILL CONTAIN                           *
*        A LISTING OF ALL MODULES WITH LINK DATES                     *
*---------------------------------------------------------------------*
         SPACE 1
         OPEN  (ALLDCB,(OUTPUT))     OPEN OUTPUT FILE
         LA    R1,SYSPRINT             LOAD ADDR IN CASE OF ERROR
         TM    SYSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO, EXIT INDICATING ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        OPEN OUTPUT FILE THAT WILL CONTAIN THE                       *
*        DELETION RECORDS.                                            *
*---------------------------------------------------------------------*
         SPACE 1
         OPEN  (DELDCB,(OUTPUT))     OPEN OUTPUT FILE
         LA    R1,SYSPRINT             LOAD ADDR IN CASE OF ERROR
         TM    SYSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO, EXIT INDICATING ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        OPEN OUTPUT FILE THAT WILL CONTAIN THE                       *
*        SCRATCH LIST RECORDS                                         *
*---------------------------------------------------------------------*
         SPACE 1
         OPEN  (SCRDCB,(OUTPUT))     OPEN OUTPUT SCRATCH LIST FILE
         LA    R1,SYSPRINT             LOAD ADDR IN CASE OF ERROR
         TM    SYSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO, EXIT INDICATING ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        OPEN THE SNAP FILE WHICH IS USED                             *
*        FOR DEBUG PURPOSES                                           *
*---------------------------------------------------------------------*
         SPACE 1
         OPEN  (SNAPP,(OUTPUT))     OPEN OUTPUT FILE
         LA    R1,SYSPRINT             LOAD ADDR IN CASE OF ERROR
         TM    SYSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO, EXIT INDICATING ERROR
*---------------------------------------------------------------------*
*        OPEN THE PDS MODULE DATA FILE                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         OPEN  (PDSDCB,(INPUTT))     OPEN PDS MODULE FILE
         LA    R1,SYSPRINT             LOAD ADDR IN CASE OF ERROR
         TM    SYSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO, EXIT INDICATING ERROR
*---------------------------------------------------------------------*
*        OPEN THE EXEMPT CARD FILE                                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         OPEN  (EXEMDCB,(INPUTT))     OPEN EXEMP CARD FILE
         LA    R1,SYSPRINT             LOAD ADDR IN CASE OF ERROR
         TM    SYSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO, EXIT INDICATING ERROR
         SPACE 2
*---------------------------------------------------------------------*
* PROVIDE ADDRESSABILTY FOR INPUT EXCEPTIONS + PDS RECORDS            *
         L     R5,CONGM      GET MAIN LENGTH
         BAL   R14,GETAREA
         USING EXCARD,R11   ESTABLISH ADDRESSABILTY
         LR    R11,R1
*  WRITE REPORT HEADING LINES
         MVC   RPTID(8),=C'SCRLIST '
         MVC   RPTTITLE(24),=C'SCRATCH CANDIDATE REPORT'
         PUT   SCRDCB,ASTERIK
         MVC   RPTID(8),=C'PDSLIST '
         MVC   RPTTITLE(29),=C'ALL LINKED MODULE NAME REPORT'
         PUT   ALLDCB,ASTERIK
* VALIDATE PARM INPUT DATE
         CLI   PARMIN+4,X'4B'        CHECK FOR PERIOD
         BNE   DATEERR               NOT PERIOD
         MVC   CNTLDATE(2),PARMIN+2  YEAR
         MVC   CNTLDATE+2(3),PARMIN+5     JULIAN DATE
         MVZ   CMPFLD(5),CNTLDATE    NUMERIC TEST
         CLC   CMPFLD,ZONECON
         BNE   DATEERR               NON NUMERIC
         PACK  HEXDATE,CNTLDATE      PACK THE PARM DATE
*  WRITE SCRATCH LIST HEADER
         MVC   SCRDATE(6),PARMIN+2    PARM IN DATE TO HEADER
*  WRITE FIRST SCRATCH CONTROL RECORD
         PUT   DELDCB,CARD1
* READ FIRST EXEMPTION RECORD
         GET   EXEMDCB,EXCARD
*  START OF INPUT READ LOOP
RDPDS    GET   PDSDCB,PDSREC       READ THE PDS RECORD FILE
         MVI   EXFLAG,X'D5'        TURN OFF EXEMPT FLAG
         CLC   MODULE(2),=C'YM'
         BNE   MVCDSN
         L     R6,=F'15'
*        BAL   R8,SNAPIT
MVCDSN   MVC   SCRLIB(25),LIBRARY  DSN TO SCRATCH REPORT HEADER
         MVC   ALLLIB(25),LIBRARY  DSN TO ALL REPORT HEADER
*        WTO   ' IST PDS READ',ROUTCDE=11
* R3 POINTS TO IDR DATA OF THE PDS RECORD
* R4 CONTAINS THE LENGTH OF THE IDR RECORD
NEWPDS   EQU   *
         LH    R3,IDRLEN           TEST FOR NO IDR DATA
         LTR   R3,R3
         BZ    RDPDS             NO IDR DATA
         LA    R3,PDSREC           ADDRESS OF PDS RECORD
         AH    R3,CSECTLEN         CSECTION LENGTH
         A     R3,=F'73' ******    FIXED LENGTH RECORD SIZE ********
NEXTIDR  SR    R4,R4               ZERO R4
         IC    R4,0(R3)            IDR LENGTH TO R4
*        ABEND 888,DUMP
         TM    1(R3),X'02'      ?  LINKAGE RECORD
         BNO   LASTTEST            NO
         MVC   PDSMOD(8),MODULE
         MVC   PDSDATE(3),14(R3)   DATE TO PROGRAM AREA
* TEST FOR EXEMPT RECORD HERE
CMPMOD   CLC   MODULE,EXMOD         ? EXEMPTION PRESENT
         BE    SETFLAG              YES
         BL    ALLLIST               PDS LOW
         CLI   EXEMFLAG,X'E8'      ? EXEMPTION FILE EOF
         BE    ALLLIST              YES
* TEST FOR EXEMPTION RECORD
         GET   EXEMDCB,EXCARD       READ THE EXEMPTION FILE
         B     CMPMOD       GO TO COMPARE MODULE NAMES
ALLLIST  BAL   R9,LISTALL   LIST ALL LINK MODULES BEFORE SCRATCH TEST
         CLI   EXFLAG,X'E8'   ? EXEMPT RECORD PRESENT
         BE    RDPDS            YES, READ NEXT PDS
         B     ERRTEST
*          EXEMPT RECORD PRESENT
SETFLAG  MVI   EXFLAG,X'E8'          TURN ON EXEMPTION PRESENT FLAG
         B     ALLLIST
* PDS RECORD < EXEMPTION RECORD
* PDS RECORD IS A DELETION CANDIDATE - CHECK LINKAGE DATE
ERRTEST  CLI   LMERROR,X'C5'     ? LOAD MODULE I/O ERROR
         BE    WRSCR               YES
         CLC   PDSDATE,HEXDATE     PDS DATE : PARM DATE
         BL    WRSCR               WRITE SCRATCH RECORD
LASTTEST TM    1(R3),X'80'      ?  LAST IDR ENTRY
         BO    RDPDS             YES
         AR    R3,R4               POINT TO NEXT IDR ENTRY
         B     NEXTIDR
*  GOT HERE WITH A BAL R8,SNAPIT
SNAPIT   EQU   *
         L     R1,SNAPLIM
         A     R1,=F'1'
         C     R1,=F'10'
         BE    SNAPEND
         ST    R1,SNAPLIM
         SNAP  DCB=SNAPP,STORAGE=(CONGM,ENDRPT),PDATA=REGS
         LA    R5,400(R11)        INPUT BUFFER
         SNAP  DCB=SNAPP,STORAGE=((R11),(R5)),PDATA=REGS
SNAPEND  BR    R8
* WRITE DELETE RECORD
WRSCR    MVC   PCHMBR(8),MODULE     MODULE NAME TO PUNCH RECORD
         PUT   DELDCB,SCRRCD        WRITE DELETION RECORD
         L     R1,=F'1'             ADD 1 TO DELETE COUNT
         A     R1,DELCNT
         ST    R1,DELCNT            STORE UPDATED DELETE COUNT
* WRITE SCRATCH REPORT                          * * * * * * * * * * *
PTRLINE  L     R1,LINEPTR           ESTABLISH PRINT POSITION
         C     R1,=F'1'
         BE    FIRST                FIRST SLOT
         C     R1,=F'2'
         BE    SECOND               SECOND SLOT
         C     R1,=F'3'
         BE    THIRD                THIRD SLOT
*    FILL FOURTH SLOT, WRITE AND REFRESH PRINT LINE
         BAL   R10,LINECK           NEED HEADER CHECK
         L     R1,=F'1'
         ST    R1,LINEPTR           NEXT SLOT
         MVC   DATE4(7),DATEMASK
         MVC   MEMB4(8),MODULE      MODULE NAME
         CLI   ALIAS,X'C1'        ? ALIAS
         BNE   DATE4MV              NO
         MVC   MEMB4+11(2),=C'-A'
DATE4MV  ED    DATE4(7),PDSDATE     LINK DATE
         CLI   LMERROR,X'C5'        TEST FOR LOAD MODULE I/O ERROR
         BNE   WRLINE
         MVC   DATE4+7(3),=C'I/O'
WRLINE   PUT   SCRDCB,SCRDET        WRITE LINE
         MVI   SCRDET,C' '          BLANK SCRATCH DETAIL LINE
         MVC   SCRDET+1(133),SCRDET  BLANK SCRATCH DETAIL LINE
         B     RDPDS
FIRST    L     R1,=F'2'
         ST    R1,LINEPTR           NEXT SLOT
         MVC   DATE1(7),DATEMASK
         MVC   MEMB1(8),MODULE      MODULE NAME
         CLI   ALIAS,X'C1'        ? ALIAS
         BNE   DATE1MV              NO
         MVC   MEMB1+11(2),=C'-A'
DATE1MV  ED    DATE1(7),PDSDATE     LINK DATE
         CLI   LMERROR,X'C5'        TEST FOR LOAD MODULE I/O ERROR
         BNE   RDPDS
         MVC   DATE1+7(3),=C'I/O'
         B     RDPDS
SECOND   L     R1,=F'3'
         ST    R1,LINEPTR           NEXT SLOT
         MVC   DATE2(7),DATEMASK
         MVC   MEMB2(8),MODULE      MODULE NAME
         CLI   ALIAS,X'C1'        ? ALIAS
         BNE   DATE2MV              NO
         MVC   MEMB2+11(2),=C'-A'
DATE2MV  ED    DATE2(7),PDSDATE     LINK DATE
         CLI   LMERROR,X'C5'        TEST FOR LOAD MODULE I/O ERROR
         BNE   RDPDS
         MVC   DATE2+7(3),=C'I/O'
         B     RDPDS
THIRD    L     R1,=F'4'
         ST    R1,LINEPTR           NEXT SLOT
         MVC   DATE3,DATEMASK
         MVC   MEMB3(8),MODULE      MODULE NAME
         CLI   ALIAS,X'C1'        ? ALIAS
         BNE   DATE3MV              NO
         MVC   MEMB3+11(2),=C'-A'
DATE3MV  ED    DATE3(7),PDSDATE     LINKDATE
         CLI   LMERROR,X'C5'        TEST FOR LOAD MODULE I/O ERROR
         BNE   RDPDS
         MVC   DATE3+7(3),=C'I/O'
         B     RDPDS                   READ NEXT PDS RECORD
* EOF ON EXEMPTION FILE
ENDEX    MVI   EXEMFLAG,X'E8'           TURN ON EOF FLAG
         B     ALLLIST             TEST FOR SCRATCH
* WRITE ALL MODULE NAMES AND DATES              * * * * * * * * * * *
* GOT HERE WITH A BAL R9,LISTALL
LISTALL  EQU   *
         L     R1,SLOTPTR           ESTABLISH PRINT POSITION
         C     R1,=F'1'
         BE    PRIMERO              FIRST SLOT
         C     R1,=F'2'
         BE    SEGUNDO              SECOND SLOT
         C     R1,=F'3'
         BE    TERCERO              THIRD SLOT
*    FILL FOURTH SLOT, WRITE AND REFRESH PRINT LINE
         BAL   R10,ALLINECK         NEED HEADER CHECK
         L     R1,=F'1'
         ST    R1,SLOTPTR           NEXT SLOT
         MVC   FECHA4(7),DATEMASK
         MVC   MEMBER4(8),MODULE      MODULE NAME
         CLI   EXFLAG,X'E8'           EXEMPTION PRESENT
         BNE   F4A                    NO
         MVC   MEMBER4+9(2),=C'EX'    YES
F4A      CLI   ALIAS,X'C1'        ? ALIAS
         BNE   FECHA4MV             NO
         MVC   MEMBER4+11(2),=C'-A'
FECHA4MV ED    FECHA4(7),PDSDATE     LINK DATE
         CLI   LMERROR,X'C5'        TEST FOR LOAD MODULE I/O ERROR
         BNE   WRALL
         MVC   FECHA4+7(3),=C'I/O'
WRALL    PUT   ALLDCB,ALLDET        WRITE LINE
         MVI   ALLDET,C' '          BLANK SCRATCH DETAIL LINE
         MVC   ALLDET+1(133),ALLDET  BLANK SCRATCH DETAIL LINE
         BR    R9
PRIMERO  L     R1,=F'2'
         ST    R1,SLOTPTR           NEXT SLOT
         MVC   FECHA1(7),DATEMASK
         MVC   MEMBER1(8),MODULE      MODULE NAME
         CLI   EXFLAG,X'E8'           EXEMPTION PRESENT
         BNE   F1A                    NO
         MVC   MEMBER1+9(2),=C'EX'    YES
F1A      CLI   ALIAS,X'C1'        ? ALIAS
         BNE   FECHA1MV             NO
         MVC   MEMBER1+11(2),=C'-A'
FECHA1MV ED    FECHA1(7),PDSDATE     LINK DATE
         CLI   LMERROR,X'C5'        TEST FOR LOAD MODULE I/O ERROR
         BNE   ALLEXIT
         MVC   FECHA1+7(3),=C'I/O'
ALLEXIT  BR    R9
SEGUNDO  L     R1,=F'3'
         ST    R1,SLOTPTR           NEXT SLOT
         MVC   FECHA2(7),DATEMASK
         MVC   MEMBER2(8),MODULE      MODULE NAME
         CLI   EXFLAG,X'E8'           EXEMPTION PRESENT
         BNE   F2A                    NO
         MVC   MEMBER2+9(2),=C'EX'    YES
F2A      CLI   ALIAS,X'C1'        ? ALIAS
         BNE   FECHA2MV             NO
         MVC   MEMBER2+11(2),=C'-A'
FECHA2MV ED    FECHA2(7),PDSDATE     LINK DATE
         CLI   LMERROR,X'C5'        TEST FOR LOAD MODULE I/O ERROR
         BNE   ALLEXIT
         MVC   FECHA2+7(3),=C'I/O'
         BR    R9
TERCERO  L     R1,=F'4'
         ST    R1,SLOTPTR           NEXT SLOT
         MVC   FECHA3,DATEMASK
         MVC   MEMBER3(8),MODULE      MODULE NAME
         CLI   EXFLAG,X'E8'           EXEMPTION PRESENT
         BNE   F3A                    NO
         MVC   MEMBER3+9(2),=C'EX'    YES
F3A      CLI   ALIAS,X'C1'        ? ALIAS
         BNE   FECHA3MV             NO
         MVC   MEMBER3+11(2),=C'-A'
FECHA3MV ED    FECHA3(7),PDSDATE     LINKDATE
         CLI   LMERROR,X'C5'        TEST FOR LOAD MODULE I/O ERROR
         BNE   ALLEXIT
         MVC   FECHA3+7(3),=C'I/O'
         BR    R9
*---------------------------------------------------------------------*
* PARM DATE IS BAD, CRASH THE PROGRAM
DATEERR  EQU   *
         SNAP  DCB=SNAPP,STORAGE=(GETADDR,ZONECON),PDATA=REGS
         ABEND 1,DUMP
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*---------------------------------------------------------------------*
*        THIS IS THE END, CONTROL WILL BE GIVEN BACK TO THE CALLER    *
*---------------------------------------------------------------------*
         SPACE 1
RETURN   EQU   *
         BAL   R10,LINECK         ? NEED HEADER
         LH    R1,SLOTPTR         ? PARTIAL LINE REMAINING
         CH    R1,=H'1'
         BE    SCRTEST            NO PARTIAL LINE
         PUT   ALLDCB,ALLDET
SCRTEST  LH    R1,LINEPTR         ? PARTIAL LINE REMAINING
         CH    R1,=H'1'
         BE    ENDMSG             NO PARTIAL LINE
         PUT   SCRDCB,SCRDET
ENDMSG   MVC   ENDID(8),=C' SCRLIST'
         MVC   ENDRPT(15),=C' END OF REPORT '
         PUT   SCRDCB,ENDRPT
         MVC   ENDID(8),=C' PDSLIST'
         MVC   ENDRPT(15),=C' END OF REPORT '
         PUT   ALLDCB,ENDRPT
         DROP  R11   ADDRESSABILTY TO INPUT AREAS
         BAL   R14,FREEAREA
         CLOSE (SYSPRINT)              CLOSE DELETE LISTING FILE
         CLOSE (EXEMDCB)              CLOSE EXEMPTION FILE
         CLOSE (PDSDCB)               CLOSE PDS  MODULE DCB
         CLOSE (DELDCB)               CLOSE THE DELETION RECORD FILE
         CLOSE (ALLDCB)             CLOSE THE MODULE LIST ALL FILE
         CLOSE (SNAPP)      * * * * * * DEBUG AID * * * * * * * * *
         L     R15,DELCNT           ? DELETE RECORDS WRITTEN  ?
         LTR   R15,R15                 SET CONDITION CODE
         BNZ   RESTOR15              YES DELETE RECORDS WERE WRITTEN
         LA    R15,4                 NO SET CC = 4
         B     EXITOUT
RESTOR15 SR    R15,R15               ZERO R15 CC = 0
EXITOUT  L     R13,4(R13)              RESTORE PTR TO CALLER'S SAVEAREA
         L     R14,12(R13)
         LM    R0,R12,14(R13)         RESTORE HIS REGISTERS
         BR    R14
         SPACE 2
*---------------------------------------------------------------------*
*        THIS ROUTINE IS RESPONSIBLE FOR ACQUIRING ALL AREA. CONTROL  *
*        IS PASSED TO IT VIA BAL R8.                                  *
*---------------------------------------------------------------------*
         SPACE 1
GETAREA  GETMAIN R,LV=(R5)             R5 = LENGTH OF AREA
         LTR   R15,R15                 ?/AREA ACQUIRED
         BNZ   GETERR                  NO, GO INDICATE AN ERROR
         LA    R1,0(R1)                CLEAR HI-ORDERED BYTE
         ST    R1,GETADDR              SAVE ADDR OF AREA
         ST    R5,GETLNGTH             SAVE LENGTH OF AREA
         BR    R14                     RETURN TO THE CALLER
GETERR   LA    R8,ERR4                 LOAD ADDR OF ERR MSG
         B     PROCERR                 GO PROCESS THE ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        THIS ROUTINE FREES ALL ACQUIRE AREA. CONTROL IS PASSED TO IT *
*        VIA BAL R8.                                                  *
*---------------------------------------------------------------------*
         SPACE 1
FREEAREA L     R5,GETLNGTH             GET AMOUNT TO FREE
         L     R6,GETADDR              GET BEGINNING ADDRESS
         FREEMAIN R,LV=(R5),A=(R6)     FREE THE AREA
         LTR   R15,R15                 ?/AREA FREED
         BCR   8,R14                   YES, RETURN TO THE CALLER
         LA    R8,ERR5                 LOAD ADDR OF ERROR MESSAGE
         B     PROCERR                 GO PROCESS THE ERROR
         SPACE 2
* LINE COUNTER CHECK                              * * * * * *
* GOT HERE WITH A BAL R10                          * * * * * *
* DO NOT USE R3 NOR R4
LINECK   EQU   *
         L     R8,LINECNT           CURRENT LINE COUNT
         A     R8,=F'1'             + 1
         C     R8,=F'54'             MAX COUNT
         BL    LINEEND               NOT MAX
         MVI   SCRCC,X'F1'
         PUT   SCRDCB,SCRHDR         TITLE LINE
         PUT   SCRDCB,BLKLINE        SPACER
         L     R8,=F'1'              RESET TO 1
LINEEND  ST    R8,LINECNT
         BR    R10
         SPACE 1
* LINE COUNTER CHECK FOR LIST ALL MODULES         * * * * * *
* GOT HERE WITH A BAL R10 - DO NOT USE R3 NOR R4    * * * * * *
ALLINECK EQU   *
         L     R8,ALLCNT            CURRENT LINE COUNT
         A     R8,=F'1'             + 1
         C     R8,=F'54'             MAX COUNT
         BL    ALLEND                NOT MAX
         MVI   ALLCC,X'F1'
         PUT   ALLDCB,ALLHDR         TITLE LINE
         PUT   ALLDCB,BLKLINE        SPACER
         L     R8,=F'1'              RESET TO 1
ALLEND   ST    R8,ALLCNT
         BR    R10
         SPACE 1
*---------------------------------------------------------------------*
*        ANY FAILURE TO OPEN A DCB CAUSES OPENERR TO RECEIVE CONTROL. *
*        IF SYSPRINT IS THE DCB THAT FAILED WE DESTRUCT  WITH A USER  *
*        200. OTHERWISE AN ERROR MESSAGE WILL BE WRITTEN AND WE WILL  *
*        TERMINATE NORMALLY.                                          *
*---------------------------------------------------------------------*
         SPACE 1
OPENERR  CLC   40(8,R1),=C'SYSPRINT'   ?/OPEN SYSPRINT FAILED
         BNE   SAVEDDN                 NO, SAVE DDNAME IN MSG
         ABEND 200,DUMP
SAVEDDN  LA    R8,ERR6                 LOAD ADDR OF ERROR MSG
         MVC   22(8,R8),40(R1)         INDICATE FILE IN ERROR
PROCERR  SR    R2,R2                   CLEAR R2
         IC    R2,0(R8)                GET ERROR MESSAGE LENGTH
         MVI   OUTLINE,X'40'           CLEAR OUTPUT BUFFER           V1
         MVC   OUTLINE+1(120),OUTLINE         ××                     V1
         EX    R2,ERRORMSG             MOVE MESSAGE TO OUTPUT LINE
         MVI   OUTLINE,X'60'           INDICATE TRIPLE SPACING
         PUT   SYSPRINT,OUTLINE        WRITE THE MESSAGE
         B     RETURN                  PREPARE FOR EXIT
ERRORMSG MVC   OUTLINE+1(0),1(R8)      MOVE MESSAGE TO BUFFER
         EJECT
*---------------------------------------------------------------------*
*        ERROR MESSAGES. THE FOLLOWING IS THE FORMAT:                 *
*                                                                     *
*     --------------------------------------------------------        *
*     × LENGTH ×              THE                            ×        *
*     × OF MSG ×            MESSAGE                          ×        *
*     --------------------------------------------------------        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
ERR4     DC    X'0C',C'GETMAIN ERROR'                                V1
ERR5     DC    X'0D',C'FREEMAIN ERROR'                               V1
ERR6     DC    X'1B',C'ERROR OPENING FILE',CL10' '                   V1
         SPACE 2
*---------------------------------------------------------------------*
*        HEADERS FOR THE PRINT FILE                                   *
*---------------------------------------------------------------------*
         SPACE 1
OUTLINE  DC    CL121' '
HDR      DC    CL121'-*                            '
LINE1    DC    CL121'0                            '
LINE2    DC    CL121'                             '
         SPACE 2
*---------------------------------------------------------------------*
*        DATA CONTROL BLOCKS                                          *
*---------------------------------------------------------------------*
         SPACE 1
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,BLKSIZE=121,RECFM=FA
SNAPP    DCB   DSORG=PS,RECFM=VBA,MACRF=(W),BLKSIZE=1632,LRECL=125,    X
               DDNAME=SNAPPED
PDSDCB   DCB   DSORG=PS,MACRF=GM,RECFM=VB,DDNAME=PDSFILE,EODAD=RETURN
EXEMDCB  DCB   DSORG=PS,MACRF=GM,RECFM=FB,DDNAME=EXEMFILE,             X
               EODAD=ENDEX
DELDCB   DCB   DDNAME=DELREC,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80,      X
               BLKSIZE=80
SCRDCB   DCB   DDNAME=SCRREC,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133,    X
               BLKSIZE=1330
ALLDCB   DCB   DDNAME=LISTALL,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133,   X
               BLKSIZE=1330
         SPACE 2
*--------------------------------------------------------------------*
*        CONSTANTS/WORK AREAS/FLAGS/ETC.                             *
*--------------------------------------------------------------------*
         SPACE 1
         DS    0D
         LTORG
GETADDR  DC    F'0'
GETLNGTH DC    F'0'
DELCNT   DC    F'0'        DELETE RECORDS WRITTEN COUNT
BASE     DC    A(HERE+4096)
SNAPLIM  DC    F'0'
SAVEAREA DC    18F'0'
CONGM    DC    F'26800'   EXEMPTION + PDS RECORDS LENGTH
EXEMFLAG DC    C'N'       EXEMPTION EOF FLAG
PARMIN   DC    C'0079.274'       PARAMETER SCRATCH DATE
         DS    0D
CNTLDATE DC    CL5'     '
HEXDATE  DC    PL3'000'
EXFLAG   DC    CL1'N'          EXEMPT FLAG OFF
PDSMOD   DC    CL8'        '
PDSDATE  DC    PL3'000'
CMPFLD   DC    5X'00'
ZONECON  DC    CL5'00000'
LINECNT  DC    F'54'          MAX LINE COUNT
ALLCNT   DC    F'54'          MAX LINE COUNT
LINEPTR  DC    F'1'           NEXT AVAILABLE POSITION IN OUTPUT RECORD
SLOTPTR  DC    F'1'           NEXT AVAILABLE POSITION IN OUTPUT RECORD
UNPACKDT DC    C'29.012'
DATEMASK DC    X'4020204B202021'
* OUTPUT SCRATCH CARDS                            *
CARD1    DC    CL80'  C  I=INPUT,O=OUTPUT '
         SPACE
SCRRCD   DS    0CL80
PCHREC   DC    C' S M='
PCHMBR   DC    C'XXXXXXXX'
         DC    CL67'     '
         SPACE 1
*---------------------------------------------------------------------*
*     EQUATES
SYSOFLAG EQU   SYSPRINT+48
         SPACE 2
* THE FOLLOWING RECORDS ARE USED TO PRINT THE SCRATCH REPORT * * * *
*  BLANK LINE
BLKLINE  DC    CL133' '
* SCRATCH REPORT TITLE RECORD                     * * * *
SCRHDR   DS    0CL133
SCRCC    DC    CL44'1  AUTOMATIC SCRATCH LIST - PURGE DATE    '
SCRDATE  DC    CL7'      '
         DC    CL10' '
SCRLIB   DC    C'LIBRARY.USED                '
         DC    CL47' '
* SCRATCH DETAIL LINE
SCRDET   DS    0CL133
         DC    C' '             CC
MEMB1    DC    CL8' '           MEMBER 1
         DC    CL5' '           SPACE
DATE1    DC    CL7' '            DATE I
         DC    CL4' '           SPACE
MEMB2    DC    CL8' '           MEMBER 2
         DC    CL5' '           SPACE
DATE2    DC    CL7' '            DATE 2
         DC    CL4' '           SPACE
MEMB3    DC    CL8' '           MEMBER 3
         DC    CL5' '           SPACE
DATE3    DC    CL7' '            DATE 3
         DC    CL4' '           SPACE
MEMB4    DC    CL8' '           MEMBER 4
         DC    CL5' '           SPACE
DATE4    DC    CL7' '            DATE 4
         DC    CL4' '           SPACE
         DC    CL36' '          SPACE
* THE FOLLOWING RECORDS ARE USED TO PRINT ALL MODULE NAMES   * * * *
* ALL     REPORT TITLE RECORD                     * * * *
ALLHDR   DS    0CL133
ALLCC    DC    CL44'1  MODULES LINKED WITH LINKAGE DATE       '
ALLDATE  DC    CL7'      '
         DC    CL10' '
ALLLIB   DC    C'LIBRARY.USED                '
         DC    CL47' '
* ALL     DETAIL LINE
ALLDET   DS    0CL133
         DC    C' '             CC
MEMBER1  DC    CL8' '           MEMBER 1
         DC    CL5' '           SPACE
FECHA1   DC    CL7' '            DATE I
         DC    CL4' '           SPACE
MEMBER2  DC    CL8' '           MEMBER 2
         DC    CL5' '           SPACE
FECHA2   DC    CL7' '            DATE 2
         DC    CL4' '           SPACE
MEMBER3  DC    CL8' '           MEMBER 3
         DC    CL5' '           SPACE
FECHA3   DC    CL7' '            DATE 3
         DC    CL4' '           SPACE
MEMBER4  DC    CL8' '           MEMBER 4
         DC    CL5' '           SPACE
FECHA4   DC    CL7' '            DATE 4
         DC    CL4' '           SPACE
         DC    CL36' '          SPACE
* REPORT HEADING   ASTERIK LINE
ASTERIK  DC    C'1@@@@@  '    CC + 5 ASTERIKS + 2 BLANKS          7
RPTID    DC    C'        '    REPORT ID                          15
         DC    C'  '                                             17
RPTTITLE DC    CL47' '        REPORT TITLE                       65
         DC    CL68' '                                           80
* REPORT ENDING LINE
ENDRPT   DC    CL15' END OF REPORT '
ENDID    DC    CL8'        '
         DC    CL110' '
*---------------------------------------------------------------------*
*        DSECTS                                                       *
*---------------------------------------------------------------------*
EXPDSDS  DSECT
* FOLLOWING IS THE EXEMPTION CARD READ IN AREA                        *
EXCARD   DS    0CL80
EXNAME   DC    C' EXEMPT '                             8
EXMOD    DS    CL8                                    16
         DC    C' '                                   17
EXDATE   DS    0CL8
EXMON    DS    CL2                                    19
         DC    C'/'                                   20
EXDAY    DS    CL2                                    23
         DC    C'/'                                   24
EXYR     DS    CL2                                    25
         DS    CL55                                   80
* FOLLOWING IS THE INPUT PDS RECORD
         SPACE 2
         DS    0D
PDSREC   DS    0CL73          73 + 16384 + 10240 = 26697 MAX LENGTH
LRECL    DS    XL2            COMPUTED LENGTH OF OUTPUT RECORD22
         DS    XL2            RESERVED FOR IBM                    4
LIBRARY  DS    CL25           JFCB BYTE 0                        29
VOLSER   DS    CL6            UCB BYTE 28                        35
MODULE   DS    CL8            DIRECTORY BYTE 2                   43
SSI      DS    CL4            DIRECTORY BYTE 34 OR 42            47
ENTRYPT  DS    XL3            DIRECTORY BYTE 27                  50
ATTR     DS    XL2            DIRECTORY BYTE 20                  52
STORAGE  DS    XL3            DIRECTORY BYTE 22                  55
IDRTYPE  DS    CL1            LOAD MODULE TYPE 80 BYTE 2 (L,R,Z) 56
ALIAS    DS    CL1            ALIAS = A, MEMBER = BLANK          57
TTR      DS    XL3            TRACK & RECORD OF LOAD MODULE      60
EXMODULE DS    CL8            EXECUTED MODULE FROM ALIAS         68
CSECTLEN DS    XL2            COMPUTED                           70
IDRLEN   DS    XL2            COMPUTED                           72
LMERROR  DS    CL1            BLANK = NO ERROR, E =  I/O ERROR   73
CESDOUT  DS    CL26624        LOAD MODULE TYPE 20-MAX OF 2048 26624
* CONTAINS 0 TO 2048 CSECT NAMES 8 CHARACTERS LONG MAX (8X2048=16384)
* AN ADDITIONAL 10240 BYTES ARE PROVIDED FOR IDR DATA
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
         END   PDSEXPDT
