DOM      TITLE 'ROCKWELL DOMAIN DISPLAY COMMAND'
         MACRO
&LABEL   @B    &FIELDB,&FIELDA
         MVC   &FIELDB,=X'40202020'   MOVE EDIT PATTERN
         SR    R11,R11                ZERO FIELD FOR INSERT
         IC    R11,&FIELDA            LOAD FIELD TO BE CONVERTED
         CVD   R11,DWK                CONVERT TO DECIMAL
         ED    &FIELDB,DWK+6          AND EDIT IT INTO WTO
         OI    &FIELDB+3,X'F0'        INSURE NUMERIC
         SPACE
         MEND
         EJECT
         MACRO
&LABEL   @M    &FIELDB,&FIELDA
         MVC   &FIELDB,=X'40202020'   MOVE EDIT PATTERN
         LH    R11,&FIELDA            LOAD FIELD TO BE CONVERTED
         CVD   R11,DWK                CONVERT TO DECIMAL
         ED    &FIELDB,DWK+6          AND EDIT IT INTO WTO
         OI    &FIELDB+3,X'F0'         INSURE NUMERIC
         SPACE
         MEND
         EJECT
         MACRO
&LABEL   @M1   &FIELDB,&FIELDA
         MVC   &FIELDB,=X'2020202020'   MOVE EDIT PATTERN
         L     R11,&FIELDA            LOAD FIELD TO BE CONVERTED
         CVD   R11,DWK                CONVERT TO DECIMAL
         ED    &FIELDB-1(5),DWK+5          AND EDIT IT INTO WTO
         OI    &FIELDB+4,X'F0'         INSURE NUMERIC
         SPACE
         MEND
         EJECT
         MACRO
&LABEL   @MD   &FIELDB,&FIELDA
         MVC   &FIELDB,=X'202020214B20'   MOVE EDIT PATTERN
         L     R11,&FIELDA            LOAD FIELD TO BE CONVERTED
         CVD   R11,DWK                CONVERT TO DECIMAL
         DP    DWK,=P'100'   DIVIDE BY 100
         CP    DWK(6),=P'99999'  > 9999.9 MINUTES
         BH    &LABEL.@AA    YES - BYPASS EDIT
         ED    &FIELDB-1(6),DWK+3          AND EDIT IT INTO WTO
         B     &LABEL.@BB
&LABEL.@AA MVC &FIELDB,=C'******'
&LABEL.@BB EQU *
         SPACE
         MEND
         EJECT
         MACRO
&LABEL   @TPUT &LEN
         XC    TSOECB,TSOECB                                *HMD 04/82*
&LABEL   PUTLINE PARM=PTPB,OUTPUT=(&LEN,TERM,SINGLE,DATA),MF=(E,IOPL)
         TM    REPORT+X'30',X'10'  DCB OPEN ?
         BZ    $&SYSNDX           NO
         PUT   REPORT,&LEN
$&SYSNDX EQU *
         SPACE
         MEND
         EJECT
DOMAIN   TSOENTER REGS=YES,PL=(CPPL,IO),BASE=12             *HMD 04/82*
START    CALL   CLR3270                                     *HMD 04/82*
         EJECT
         USING CVTMAP,R1
         LA   R1,CVTPTR           -> CVT POINTER
         L    R1,0(R1)            -> CVT
         L    R2,CVTOPCTP         -> RMCT
         USING RMCT,R2
         L    R3,RMCTCCT          -> CCT
         USING CCT,R3
         L    R4,RMCTRMCA         -> RMCA
         USING RMCA,R4
         SPACE
         L     R8,RMCTWMST        -> WMST
         USING WMST,R8
         MVC   IPSID(2),WMSTID     MOVE CURRENT IPS ID
         DROP  R8
         USING PVT,R8
         L    R8,CVTPVTP         -> PVT
         L    R5,RMCTDMDT   RMCTDMDT         -> START OF DMDT
         USING DMDT,R5
         LH   R7,RMCTDMNC   RMCTDMNC         -> COUNT OF DOMAINS
         LH   R6,=AL2(DMDTEND-DMDTNO)  INCRIMENT
         @M   CPU,CCVUTILP        CPU UTILIZATION
         @M   AFC,PVTAFC          AVAIL FRAME COUNT
         L     R8,RMCTRCT          ->RCT
         DROP  R8
         USING RCT,R8
         @M   PFR,RCVPTR          PAGE FAULT RATE
         @M   ASMQ,RCVASMQA      ASM Q
         @M   UIC,RCVUICA        UNREFERENCED INTERVAL COUNT
         EJECT
*
         TIME
         MVC  TIME(9),=X'4020217A20207A2020'
         ST   R0,DWK              STORE TIME
         ED   TIME(9),DWK         AND EDIT IT
         TM    REPORT+X'30',X'10' DCB OPEN
         BZ    TPUTS              NO
         PUT   REPORT,SKIP
TPUTS    @TPUT TITLE
         EJECT
         @TPUT DASH
         @TPUT CPUDATAD
         @TPUT DASH
         SPACE
         @TPUT DMNH
         EJECT
DMNLOOP  EQU  *
         MVC  DMNO(DMNE-DMNO),BLANKS  CLEAR DOMAIN LINE
         SPACE
         @B   DMNO,DMDTNO         DOMAIN NUMBER
         @B   DMNLO,DMDTLO        MIN MPL LEVEL
         @B   DMNHI,DMDTHI        MAX MPL LEVEL
         @B   DMNWT,DMDTWT        WEIGHT FACTOR OF DOMAIN
         @M   DMNMPL,DMDTMPLT     MPL TARGET
         @M   DMNGOOU,DMDTGOOU    USERS GOING OUT
         @M   DMNCMPL,DMDTCMPL    CURRENT MPL
         @M   DMNOUTU,DMDTOUTU    OUT USERS
         @M   DMNINCU,DMDTINCU    INCORE USERS
*
         @M   DMNCIDX,DMDTCIDX    CONTENTION INDEX
         @M   DMNNSW,DMDTNSW      NON SWAPPABLE IN USERS
         @M   DMNRUMX,DMDTRUMX    MAX READY USERS IN INTERVAL
         @M1  DMNTWSR,DMDTTWSR    WEIGHTED INTERVAL DMN SERVICE
         @MD  DMNTWET,DMDTTWET    RTO AVG TIME IN MILLI SECONDS
*
         LA    R2,8                INSURE AN INTEGER
         AH    R2,DMDTRUA          ADD RUA
         SRDA  R2,32
         D     R2,=F'16'
         CVD   R3,DWK
         MVC   DMNRUA(4),=X'40202020'
         ED    DMNRUA(4),DWK+6
         OI    DMNRUA+3,X'F0'    INSURE NUMERIC
*
         SPACE
         @TPUT DMND
         AR    R5,R6           INCR LOOP
         BCT   R7,DMNLOOP      CONTINUE LOOP
         @TPUT DASH
         EJECT
         XC    TSOECB,TSOECB       CLEAR ECB                *HMD 08/81*
         MVI   REPLY,C' '          CLEAR REPLY AREA         *HMD 04/82*
         GETLINE PARM=GTPB,TERMGET=(EDIT,WAIT),             *HMD 08/81*X
               MF=(E,IOPL)                                  *HMD 08/81*
         LTR   R15,R15            CHECK FOR ZERO CODE       *HMD 08/81*
         BNE   EXIT               ERROR, RETURN TO CALLER   *HMD 08/81*
         L     R1,GTPB+4          GET ADDRESS OF LINE       *HMD 08/81*
         LH    R10,0(R1)          GET LENGTH OF LINE        *HMD 08/81*
         CH    R10,=H'4'          ZERO LENGTH LINE?         *HMD 08/81*
         BE    GOFREE             YES, GO FREE BUFFER       *HMD 08/81*
         MVC   REPLY(1),4(R1)     RETURN CHAR               *HMD 08/81*
GOFREE   FREEMAIN R,LV=(10),A=(1),SP=1  FREE STORAGE        *HMD 08/81*
         TCLEARQ INPUT                                      *HMD 08/81*
         OI    REPLY,X'40'         INSURE NUMERIC
         CLI   REPLY,C'O'         OPEN REPORT ?
         BE    OPENR              YES
         CLI   REPLY,C'C'         CLOSE REPORT
         BE    CLOSER
         CLI   REPLY,C'Z'         END ?
         BE    EXIT
         CLI   REPLY,C'E'         END                       *HMD 04/82*
         BE    EXIT                                         *HMD 04/82*
         CLI   REPLY,C'M'         MENU
         BE    EXIT               YES
         CLI   REPLY,C'0'         UPPER 0
         BL    START
         CLI   REPLY,C'9'         UPPER 9
         BH    START
EXIT     TM    REPORT+X'30',X'10' REPORT OPEN?              *HMD 04/82*
         BZ    EXIT1              NOPE                      *HMD 04/82*
         CLOSE (REPORT)
EXIT1    L     R1,4(R13)          GET PREVIOUS SA           *HMD 04/82*
         MVC   56(1,R1),REPLY     SAVE REPLY CHARACTER      *HMD 04/82*
         TSOLEAVE ISN=LTORG                                 *HMD 04/82*
         EJECT
OPENR    OPEN  (REPORT,OUTPUT)
         B     START
CLOSER   CLOSE (REPORT)
         B     START
         LTORG
         EJECT
DWK      DC   D'0'
PTPB     PUTLINE MF=L                                       *HMD 04/82*
GTPB     GETLINE MF=L                                       *HMD 04/82*
         SPACE 2
         DS   0F
DASH     DC    AL2(DASHE-DASH),AL2(0)
         DC    77C'-'
DASHE    EQU   *
         DS    0F
SKIP     DC   AL2(SKIPE-SKIP),AL2(0)
         DC   C'1'                SKIP TO CH 1
BLANKS   DC   CL30' '
SKIPE    EQU   *
         DC   CL50' '
         SPACE 2
         DS   0F
REPLY    DC    C' '
         SPACE 2
         DS   0F
TITLE    DC   AL2(TITLEE-TITLE),AL2(0)
         DC   C'   ROCKWELL DOMAIN ANALYSIS DISPLAY '
TIME     DC   CL10' '
TITLEE   EQU  *
         DC    CL20' '
         DS   0F
SPACE    DC   AL2(80),AL2(0)
         DC   CL80' '
         SPACE
         DS    0F
CPUDATAD DC    AL2(CPUDATDE-CPUDATAD),AL2(0)
         DC    C'  CPU='
CPU      DS    CL4
         DC    C'  PFR='
PFR      DS    CL4
         DC    C'  ASMQ='
ASMQ     DS    CL4
         DC    C'  UIC='
UIC      DS    CL4
         DC    C'  AFC='
AFC      DS    CL4
         DC    CL5' '
         DC    C'IPS = IEAIPS'
IPSID    DC    CL2' '
CPUDATDE EQU   *
         DC    CL20' '
         EJECT
         DS   0F
DMNH     DC   AL2(DMNHE-DMNH),AL2(0)
         DC   C' DMN  MIN  MAX   WT MPLT CMPL  RUA RUMX'
         DC   C' GOOU OUTU INCU CIDX  NSW  TWSR   RTO '
DMNHE    EQU  *
         DS   0F
DMND     DC   AL2(DMNE-DMND),AL2(0)
DMNO     DS   CL4                 DOMAIN NUMBER
         DS   CL1
DMNLO    DS   CL4                 MIN MPL LEVEL
         DS   CL1
DMNHI    DS   CL4                 MAX MPL LEVEL
         DS   CL1
DMNWT    DS   CL4                 WEIGHT FACTOR OF DOMAIN
         DS   CL1
DMNMPL   DS   CL4                 MPL TARGET
         DS   CL1
DMNCMPL  DS   CL4                 CURRENT MPL
         DS   CL1
DMNRUA   DS   CL4                 READY USER AVERAGE
         DS   CL1
DMNRUMX  DS   CL4                 MAX READY IN INTERVAL
         DS   CL1
DMNGOOU  DS   CL4                 USERS GOING OUT
         DS   CL1
DMNOUTU  DS   CL4                 OUT USERS
         DS   CL1
DMNINCU  DS   CL4                 IN-CORE USERS
         DS   CL1
DMNCIDX  DS   CL4                 CONTENTION INDEX
         DS   CL1
DMNNSW   DS   CL4                 NON SWAPPABLE IN USERS
         DS   CL1
DMNTWSR  DS   CL5                 WEIGHTED INTERVAL DMN SERVICE
         DS   CL1
DMNTWET  DS   CL5                 AVG RTO FOR DMN SERVICE
DMNE     EQU  *
         EJECT
REPORT   DCB  DSORG=PS,MACRF=PM,DDNAME=REPORT,RECFM=VBA,LRECL=78,      *
               BLKSIZE=784
         EJECT
         CVT  DSECT=YES,LIST=YES
         EJECT
ICT      EQU   0                                            *HMD 08/81*
RMPT     EQU   0                                            *HMD 08/81*
RMEX     EQU   0                                            *HMD 08/81*
IRAEPPA  EQU   0                                            *HMD 08/81*
IRAEPPR  EQU   0                                            *HMD 08/81*
RMSB     EQU   0                                            *HMD 08/81*
EPDTSCN  EQU   0                                            *HMD 08/81*
EPATSCN  EQU   0                                            *HMD 08/81*
LSCT     EQU   0                                            *HMD 08/81*
IRAWTQE  EQU   0                                            *HMD 08/81*
IRAOUCB  EQU   0                                            *HMD 08/81*
IRAOUXB  EQU   0                                            *HMD 08/81*
IRACTMQE EQU   0                                            *HMD 08/81*
IRAOTQE  EQU   0                                            *HMD 08/81*
IRAINQE  EQU   0                                            *HMD 08/81*
IRAFLTM  EQU   0                                            *HMD 08/81*
IRASRBT  EQU   0                                            *HMD 08/81*
EPAT     EQU   0                                            *HMD 08/81*
RQSV     EQU   0                                            *HMD 08/81*
ICSP     EQU   0                                            *HMD 08/81*
RSPL     EQU   0                                            *HMD 08/81*
ENFPM    EQU   0                                            *HMD 08/81*
         MNOTE *,'***************************************************'
         MNOTE *,'SOME OF THE MACROS BELOW ARE LICENSED MATERIAL.'
         MNOTE *,'THEY ARE AVAILABLE WITH THE OPTIONAL MATERIAL FOR'
         MNOTE *,'MVS SYSTEM PRODUCT AND WILL NOT BE DISTRIBUTED'
         MNOTE *,'WITH THE GTEDS MACRO LIBRARIES -- HOWARD M. DEAN'
         MNOTE *,'***************************************************'
         EJECT                                              *HMD 04/82*
         IRACCT
         EJECT
         IRARCT         *** COPYRIGHTED WITH SP/1 PVTMACS ***HMD 04/82*
         EJECT
         IHAPVT         *** COPYRIGHTED WITH SP/1 PVTMACS ***HMD 04/82*
         EJECT
DMDT     IRADMDT        *** COPYRIGHTED WITH SP/1 PVTMACS ***HMD 04/82*
         EJECT
         IRARMCA        *** COPYRIGHTED WITH SP/1 PVTMACS ***HMD 04/82*
         EJECT
         IRARMCT        *** COPYRIGHTED WITH SP/1 PVTMACS ***HMD 04/82*
         EJECT
         IRAWMST        *** COPYRIGHTED WITH SP/1 PVTMACS ***HMD 04/82*
         EJECT
         IRAMCT         *** COPYRIGHTED WITH SP/1 PVTMACS ***HMD 04/82*
         END
