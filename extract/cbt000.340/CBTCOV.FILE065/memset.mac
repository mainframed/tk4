MEMSET   TITLE 'TRANSFER COPYIES OF A WORD INTO A BLOCK '
*
*      THIS PROGRAM STORES N COPIES OF IA INTO ARRAY IB.
* ENTRY POINTS AND ARGUMENTS FOR THE SUBROUTINE:
*         CALL MEMSET( IA,IB,N) FTN
* IF N IS NEGATIVE A MESSAGE WILL BE WRITTEN TO THE PROGRAMMER'S LOG
* WHEN THE SUBROUTINE IS CALLED BY A BATCH JOB. THE MESSAGE IS WRITTEN
* TO THE USER'S TERMINAL IF THE SUBROUTINEIS CALLED INTERACTIVELY.
*
*      THE "MEMSET" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
MEMSET   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING WORKAREA,R13        ADDRESS WORK AREA DSECT
         LM    R4,R6,0(R2)         R5 HAS POINTER OF IB
         L     R7,0(R4)            R7 HAS IA
         L     R9,0(R6)            R9 HAS N
         C     R9,ZERO
         BL    ERROR               IF COPY NO < 0 BRANCH TO ERROR
         S     R9,ONE              R9 CONTAINS N-1
         M     R8,FOUR             R9 HAS (N-1)*4
         SR    R10,R10             ZERO OUT R10
LP       CR    R10,R9
         BH    HERE                GO TO HERE IF R10 > R9
         ST    R7,0(R10,R5)        STORE IA WORD IN PARM2
         A     R10,FOUR            INCREMENT X REGISTER BY FOUR
         B     LP
ERROR    MVC   CALLST,CCALLST      MOVE CALL PARAMETER TO WORK
         LOAD  EP=AMIINT           LOAD SUBROUTINE TO GET JOB STATUS
         LR    R15,R0              SAVE ENTRY POINT ADDRESS
         CALL  (15),(WORK),        CALL  " AMIINT"                     X
               MF=(E,CALLST)
         CLC   WORK,ONE            IF RETURN VALUE FROM "AMIINT"<= 1
         BNH   BATCH                 THEN PROCESS WITH WTO
LOGON    DS    0H
         TPUT  WTOMSG1,L'WTOMSG1   WRITE MESSAGE 1 TO TERMINAL
         B     EXIT
BATCH    DS    0H
         WTO   MF=(E,WTOPRM1)      WRITE MESSAGE 1 TO DAYFILE LOG
EXIT     DS    0H
         ABEND 900                 ABEND THE STEP
HERE     LEAVE RETCODE=0,          RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
ZERO     DC    F'0'
ONE      DC    F'1'
FOUR     DC    F'4'
CCALLST  CALL  ,(),MF=L            CONSTANT CALL PARAMETER LIST
CCALLEN  EQU   *-CCALLST
WTOPRM1  WTO   ' ERROR, THE NUMBER OF COPIES IS NEGATIVE',             X
               ROUTCDE=11,MF=L
WTOLEN1  EQU   *-WTOPRM1
         ORG   WTOPRM1+4
WTOMSG1  DS    CL(WTOLEN1-4)
         ORG
WORKAREA DSECT ,
         DS    18A
WORK     DS    F
CALLST   DS    CL(CCALLEN)         CALL PARAMETER LIST
WORKSIZE EQU   *-WORKAREA
         END
