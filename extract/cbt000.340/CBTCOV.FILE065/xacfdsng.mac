DSNG  TITLE 'XACFDSNG ACF2 PSEUDO DATASET NAME GENERATION EXIT' IV19900
         GBLC  &CSECT                                           IV19900
&CSECT   SETC  'XACFDSNG'                                       IV19900
         GBLC  &PFX                                             IV19900
&PFX     SETC  'FLUOR.IRVINE.XACFDSNG'                          IV19900
&CSECT   CSECT ,                                                IV19900
*************************************************************** IV19900
*                                                             * IV19900
*        THIS IS THE ACF2 PSEUDO DATASET NAME GENERATOR EXIT; * IV19900
*        IT GAINS CONTROL WHENEVER A TAPE VOLUME IS ACCESED.  * IV19900
*                                                             * IV19900
*        CODED BY:      R.L. WELKER                           * IV19900
*        LAST MODIFIED: 03/21/83                              * IV19900
*                                                             * IV19900
*        ITS PURPOSE IS:                                      * IV19900
*                                                             * IV19900
*        1) IF AN ACF2 ERROR, CONTINUE NORMALLY.              * IV19900
*        2) IF NOT A TAPE VALIDATION, CONTINUE NORMALLY.      * IV19900
*        3) IF REENTRY, CONTINUE NORMALLY.                    * IV19900
*        4) IF VSN IS NONSPECIFIC AND IF LABEL=EXPDT=98000    * IV19900
*           ISSUE MESSAGE AND FORCE VIOLATION PROCESSING.     * IV19900
*        5) IF SPECIFIC VSN NOT IN TMC THEN ALLOW ANY ACCESS. * IV19900
*        6) IF VSN IS IN TMC AND EXPDT=98000 THEN IF LIDREC   * IV19900
*           HAS TMS-BYPS BIT ON, THEN ALLOW ACCESS AND LOG;   * IV19900
*        7) IF TMS-BYPS BIT IS NOT ON THEN CHECK PGM AND      * IV19900
*           LIBRARY FOR MATCH IN BLP AUTHORIZED PGM TABLE.    * IV19900
*        8) IF NO MATCH FOUND THEN ISSUE MESSAGE AND ABORT    * IV19900
*           REQUEST. ELSE, ALLOW REQUEST WITHOUT LOG.         * IV19900
*        9) IF EXIT NOT ONE IMMEDIATELY FOLLOWING MOUNT,      * IV19900
*           CONTINUE NORMALLY.                                * IV19900
*       10) IF FILE SEQUENCE GREATER THAN 1 AND EXPDT IN JFCB * IV19900
*           DOES NOT EQUAL 98000 THEN CHANGE DATASET NAME     * IV19900
*           FOR VALIDATION AFTER MOUNT TO DATASET NAME OF     * IV19900
*           FIRST FILE ON TAPE AND CAUSE VALIDATION TO BE     * IV19900
*           RESTARTED.                                        * IV19900
*                                                             * IV19900
*************************************************************** IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     REGISTER USAGE                                          * IV19900
*                                                             * IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     R2  - TMC VOLUME RECORD ADDRESS                         * IV19900
*     R5  - EXIT PARMLIST COMMON AREA POINTER                 * IV19900
*     R6  - LOGONID RECORD BASE REGISTER                      * IV19900
*     R8  - ACCVT BASE REGISTER                               * IV19900
*     R11 - ACSXP BASE REGISTER                               * IV19900
*     R12 - CSECT BASE REGISTER                               * IV19900
*     R13 - WORK AREA BASE REGISTER                           * IV19900
*     R15 - RETURN CODE                                       * IV19900
*                                                             * IV19900
*************************************************************** IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     RETURN CODE EQUATES                                     * IV19900
*                                                             * IV19900
*************************************************************** IV19900
NORMAL   EQU   0                  CONTINUE NORMALLY             IV19900
QUIET    EQU   4                  ALLOW ACCESS                  IV19900
ABORTNL  EQU   8                  ABORT ACCESS, NO LOG          IV19900
LOG      EQU   12                 ALLOW ACCESS, LOG             IV19900
ABORT    EQU   16                 ABORT ACCESS, LOG             IV19900
VIOLATE  EQU   20                 VIOLATION PROCESS             IV19900
RETRY    EQU   24                 RETRY VALIDATION PROCESS      IV19900
RETRYPV  EQU   28                 INVOKE PREVALIDATION EXIT     IV19900
RETRYDSG EQU   32                 INVOKE DATASET GENERATION     IV19900
WARN     EQU   36                 ALLOW ACCESS, WARN, LOG       IV19900
DUMPGO   EQU   40                 TAKE SVC DUMP AND CONTINUE    IV19900
DMPABEND EQU   255                TAKE SVC DUMP AND ABORT       IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     DSECTS                                                  * IV19900
*                                                             * IV19900
*************************************************************** IV19900
         ACCVT ,                  ACF CVT MAPPING MACRO         IV19900
         ACSXP ,                  PARAMETER LIST DSECT          IV19900
JFCB     DSECT ,                                                IV19900
         IEFJFCBN ,               JFCB MAPPING MACRO            IV19900
         LIDREC ,                 LIDREC MAPPING MACRO          IV19900
WORKAREA DSECT ,                                                IV19900
SAVEAREA DS    18F                SAVE AREA                     IV19900
WTOR     DS    0D                                               IV19900
         DS    AL1                REPLY LENGTH                  IV19900
         DS    AL3                REPLY ADDRESS                 IV19900
         DS    A                  WTOR ECB ADDRESS              IV19900
WTOMSG   DS    H,XL2,CL124,XL4    WTO MSG AREA                  IV19900
WTORECB  DS    F                  WTOR ECB                      IV19900
REPLY    DS    C                  REPLY AREA                    IV19900
TESTDATA DS    0CL52              PGM+LIBRARY                   IV19900
TESTPGM  DS    CL8                PGM                           IV19900
TESTDSN  DS    CL44               LIBRARY                       IV19900
         OPENTMC MF=L             TMC PARM LIST                 IV19900
         TMRECORD ,               TMC RECORD AREA               IV19900
WORKLEN  EQU   *-WORKAREA                                       IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     SUBROUTINE ENTRY AND LINKAGE                            * IV19900
*                                                             * IV19900
*************************************************************** IV19900
&CSECT   ENTER BASEREG=R12,       SET UP LINKAGE                IV19900X
               CSECT=YES,                                       IV19900X
               GETMAIN=WORKLEN,                                 IV19900X
               PARMREG=R11,                                     IV19900X
               REGEQUS=YES,                                     IV19900X
               REGZERO=R6                                       IV19900
         USING WORKAREA,R13       SET WORKAREA ADDRESSABILITY   IV19900
         ST    R12,SAVEAREA       SAVE ENTRY ADDRESS            IV19900
         USING ACSXP,R11          ACSXP ADDRESSABILITY          IV19900
         USING LIDREC,R6          LIDREC ADDRESSABILITY         IV19900
         L     R8,ACUUACVT        SETUP ACCVT BASE REGISTER     IV19900
         USING ACCVT,R8           ACCVT ADDRESSABILITY          IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     IF ACF2 PROBLEM, RETURN IMMEDIATELY                     * IV19900
*                                                             * IV19900
*************************************************************** IV19900
ACF2ABE  EQU   *                                                IV19900
         LA    R15,NORMAL         SET NORMAL RETURN CODE        IV19900
         TM    ACUUFLG1,ACUU1ABE  Q. ABNORMAL CONDITION FLAG ?  IV19900
         BO    RETURN             A. YES, RETURN                IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     IF NON-TAPE DATASET, RETURN IMMEDIATELY                 * IV19900
*                                                             * IV19900
*************************************************************** IV19900
NONTAPE  EQU   *                                                IV19900
         TM    ACUUFLG1,ACUU1TDS  Q. TAPE DATASET ?             IV19900
         BZ    RETURN             A. NO, RETURN                 IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     CHECK FOR PREVIOUS PASS THROUGH THIS EXIT TO PREVENT    * IV19900
*     RECURSION                                               * IV19900
*                                                             * IV19900
*************************************************************** IV19900
REENTRY  EQU   *                                                IV19900
         L     R5,ACUUCOMM        GET ADDRESS OF COMMON AREA    IV19900
         CLC   =C'TAPE',0(R5)     Q. PREVIOUSLY ENTERED?        IV19900
         BE    RETURN             A. YES, THEN RETURN           IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     CHECK TO SEE IF TMS IS ACTIVE                           * IV19900
*                                                             * IV19900
*************************************************************** IV19900
TMSCHK   EQU   *                                                IV19900
         OPENTMC MF=E,            Q. TMS ACTIVE?                IV19900X
               NOTACT=NOTMS       A. NO, PROMPT OPERATOR        IV19900
         B     CHKVOL             A. YES, GET VOLUME RECORD     IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     IF TMS IS NOT INITIALIZED OR INACTIVE THEN              * IV19900
*     ISSUE WTOR TO DETERMINE ACTION TO BE TAKEN.             * IV19900
*     IF REPLY IS 'U' THEN PROCEED NORMALLY WITHOUT           * IV19900
*     TMS.  IF REPLY IS 'M' ISSUE RETURN CODE TO CAUSE AN     * IV19900
*     IMMEDIATE ABEND.  IF REPLY IS 'R' THEN ASSUME           * IV19900
*     TMS HAS BEEN INITIALIZED AND RETRY TMS PROCESSING.      * IV19900
*                                                             * IV19900
*************************************************************** IV19900
NOTMS    EQU   *                                                IV19900
         MVI   REPLY,0            CLEAR REPLY FIRST             IV19900
         MVC   WTOMSG(LWTOMSG1),WTOMSG1 MOVE IN WTO MSG #1      IV19900
         WTO   MF=(E,WTOMSG)      ISSUE WTO                     IV19900
         MVC   WTOMSG(LWTOMSG2),WTOMSG2 MOVE IN WTOR MSG #2     IV19900
         XC    WTORECB,WTORECB    CLEAR WTOR ECB                IV19900
         WTOR  ,REPLY,1,          ISSUE WTOR                    IV19900X
               WTORECB,                                         IV19900X
               MF=(E,WTOR)                                      IV19900
         WAIT  ECB=WTORECB        WAIT ON REPLY                 IV19900
         OI    REPLY,C' '         SHIFT TO UPPER CASE           IV19900
         LA    R15,NORMAL         SET NORMAL RETURN CODE        IV19900
         CLI   REPLY,C'U'         Q. REPLY='U'                  IV19900
         BE    RETURN             A. YES, RETURN                IV19900
         LA    R15,ABORT          SET ABORT RETURN CODE         IV19900
         CLI   REPLY,C'C'         Q. REPLY='C'                  IV19900
         BE    RETURN             A. YES, RETURN                IV19900
         CLI   REPLY,C'R'         Q. REPLY='R'                  IV19900
         BE    TMSCHK             A. YES, RETRY TMS CHECK       IV19900
         B     NOTMS              ELSE, REISSUE WTOR            IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     CHECK FOR VALID TMS VSN                                 * IV19900
*                                                             * IV19900
*************************************************************** IV19900
CHKVOL   EQU   *                                                IV19900
         L     R1,ACUUVOL         LOAD ADDRESS OF VOL SER       IV19900
         TM    ACUUFLG4,ACUU4OUT  Q. ANY OUTPUT BITS ON?        IV19900
         BZ    SPECVSN            A. NO, VSN IS SPECIFIC        IV19900
         CLC   =CL6'SCRTCH',0(R1) Q. NONSPECIFIC REQUEST?       IV19900
         BE    CHK98000           A. YES, SKIP TMS VSN CHECK    IV19900
         CLC   =CL6' ',0(R1)      Q. NONSPECIFIC REQUEST?       IV19900
         BE    CHK98000           A. YES, SKIP TMS VSN CHECK    IV19900
         CLI   0(R1),C'L'         Q. NONSPECIFIC REQUEST?       IV19900
         BE    CHK98000           A. YES, SKIP TMS VSN CHECK    IV19900
         CLI   0(R1),C'/'         Q. NONSPECIFIC REQUEST?       IV19900
         BNE   SPECVSN            A. NO, CHECK FOR TMS VSN      IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     IF NONSPECIFIC VOLUME SERIAL NUMBER AND                 * IV19900
*     LABEL=EXPDT=98000 ARE REQUESTED THEN ISSUE MESSAGE      * IV19900
*     AND ABORT REQUEST.                                      * IV19900
*                                                             * IV19900
*************************************************************** IV19900
CHK98000 EQU   *                                                IV19900
         LA    R15,NORMAL         SET NORMAL RETURN CODE        IV19900
         L     R1,ACUUDATA        GET ADDRESS OF JFCB           IV19900
         CLC   JFCBXPDT-JFCB(3,R1),=AL1(98,0,0) Q. EXPDT=98000? IV19900
         BNE   RETURN             A. NO, CONTINUE NORMALLY      IV19900
         B     INV98000           A. YES, ISSUE MESSAGE RETURN  IV19900
SPECVSN  EQU   *                                                IV19900
         TESTVSN (R1),            Q. VOLSER IN TMC?             IV19900X
               RQUIET             A. NO, ALLOW ANY ACCESS       IV19900
         LR    R2,R1              SAVE TMC RECORD ADDRESS       IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     IF EXPDT=98000 IS USED FOR A VOLUME SERIAL NUMBER       * IV19900
*     IN THE TMC THEN CHECK FOR AUTHORIZATION.                * IV19900
*                                                             * IV19900
*************************************************************** IV19900
CHKSPEC  EQU   *                                                IV19900
         L     R1,ACUUDATA        GET ADDRESS OF JFCB           IV19900
         CLC   JFCBXPDT-JFCB(3,R1),=AL1(98,0,0) Q. EXPDT=98000? IV19900
         BNE   CHKEXIT            A. NO, CHECK EXIT TYPE        IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     IF TMS-BYPS BIT IS ON IN LOGONID RECORD THEN ALLOW      * IV19900
*     BUT LOG ACCESS ELSE CHECK FOR TMS-BYPS AUTHORIZED       * IV19900
*     PROGRAM.                                                * IV19900
*                                                             * IV19900
*************************************************************** IV19900
CHKTMBYP EQU   *                                                IV19900
         TM    LIDI2FLG,LIDI2F7   Q. IS TMS-BYPS BIT ON?        IV19900
         BZ    CKATHBYP           A. NO, CHECK PROGRAM          IV19900
         LA    R15,LOG            FORCE LOG                     IV19900
         B     RETURN             RETURN                        IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     CHECK FOR TMS-BYPS AUTHORIZED PROGRAM                   * IV19900
*                                                             * IV19900
*************************************************************** IV19900
CKATHBYP EQU   *                                                IV19900
         ICM   R1,B'1111',ACUUPGM Q. ADDRESS OF CURRENT PGM?    IV19900
         BZ    INV98000           A. NO, 98000 IS INVALID       IV19900
         MVC   TESTPGM,0(R1)      SAVE PGM                      IV19900
         ICM   R1,B'1111',ACUULIBP Q. ADDRESS OF CURRENT LIB    IV19900
         BZ    INV98000           A. NO, 98000 IS INVALID       IV19900
         MVC   TESTDSN,0(R1)      SAVE DSN                      IV19900
         ICM   R1,B'1111',ACCBLPT  Q. ADDRESS OF BLP TABLE?     IV19900
         BZ    INV98000           A. NO, 98000 IS INVALID       IV19900
         ICM   R3,B'1111',0(R1)   Q. NUMBER OF ENTRIES?         IV19900
         BNP   INV98000           A. '0', 98000 IS INVALID      IV19900
         BCTR  R3,0               DECREMENT BY 1                IV19900
         LA    R1,4(,R1)          INCREMENT ADDRESS PAST COUNT  IV19900
         MH    R3,=AL2(L'TESTDATA) GET LENGTH OF TABLE          IV19900
         AR    R3,R1              GET ADDRESS OF LAST ENTRY     IV19900
         LA    R2,L'TESTDATA      GET INCREMENT                 IV19900
         LA    R15,LOG            SET RETURN CODE               IV19900
CHKLOOP  EQU   *                                                IV19900
         CLC   TESTDATA,0(R1)     Q. MATCH FOUND?               IV19900
         BE    RETURN             A. YES, RETURN                IV19900
         BXLE  R1,R2,CHKLOOP      LOOP THROUGH TABLE            IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     ISSUE MESSAGE AND ABORT REQUEST                         * IV19900
*                                                             * IV19900
*************************************************************** IV19900
INV98000 EQU   *                                                IV19900
         MVC   WTOMSG(LWTOMSG3),WTOMSG3 MOVE IN WTO MSG #3      IV19900
         ICM   R1,B'1111',ACCWRNMS GET ADDRESS OF WARNING MSG   IV19900
         BZ    WTP                IF ZERO DEFAULT TO WTP        IV19900
         LH    R0,0(,R1)          GET LENGTH OF MESSAGE         IV19900
         AR    R1,R0              GET ADDRESS OF ROUTE CODE     IV19900
         MVC   WTOMSG+LWTOMSG3-4(4),0(R1) MOVE IN ROUTE CODE    IV19900
WTP      EQU   *                                                IV19900
         WTO   MF=(E,WTOMSG)      ISSUE WTO                     IV19900
         LA    R15,VIOLATE        SET VIOLATE RETURN CODE       IV19900
         B     RETURN             RETURN                        IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     CHECK FOR APPROPRIATE INTERCEPT                         * IV19900
*                                                             * IV19900
*************************************************************** IV19900
CHKEXIT  EQU   *                                                IV19900
         LA    R15,NORMAL         SET NORMAL RETURN CODE        IV19900
         CLI   ACUUFLG3,ACUU3TP0  Q. TAPE OPEN AFTER MOUNT      IV19901
         BE    CHKFILE            A. YES, CHK PREVIOUS ENTRY    IV19900
         CLI   ACUUFLG3,ACUU3TPE  Q. TAPE EOV AFTER MOUNT       IV19901
         BNE   RETURN             A. NO, RETURN                 IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     CHECK FOR FILE SEQUENCE GREATER THAN 1                  * IV19900
*                                                             * IV19900
*************************************************************** IV19900
CHKFILE  EQU   *                                                IV19900
         CLC   ACUUFILE,=H'1'     Q. FILSQ > 1                  IV19900
         BNH   RETURN             A. NO, LEAVE DSN ALONE        IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     GET TMC RECORD FOR VOLUME SERIAL NUMBER                 * IV19900
*                                                             * IV19900
*************************************************************** IV19900
GETVOLRE EQU   *                                                IV19900
         XC    TMSPARM,TMSPARM    CLEAR PARM TMS PARMLIST       IV19900
         ST    R2,TMRELBLK        STORE RELATIVE BLOCK ADDRESS  IV19900
         LA    R1,TMRECORD        POINT TO TMC IOAREA           IV19900
         STCM  R1,B'0111',TMIOADDR POST IOAREA ADDRESS          IV19900
         MVI   TMOPCODE,24        POST OP-CODE                  IV19900
         LA    R1,TMSPARM         GET ADDRESS OF PARM LIST      IV19900
         L     R15,TMSVCRTN       GET ADDRESS OF TMSSVC         IV19900
         BALR  R14,R15            CALL TMS TO GET TMC RECORD    IV19900
         LTR   R1,R1              Q. RETURN CODE OK?            IV19900
         BM    NOTMS              A. NO, PROMPT OPERATOR        IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     MOVE DSN FROM TMC RECORD TO DSN AREA POINTED TO BY EXIT * IV19900
*     PARM LIST, ALSO POST INDICATOR IN COMMON AREA TO PREVENT* IV19900
*     RECURSION.                                              * IV19900
*                                                             * IV19900
*************************************************************** IV19900
FIXDSN   EQU   *                                                IV19900
         L     R1,ACUUDSN         GET ADDRESS OF DSN AREA       IV19900
         MVC   0(44,R1),TMDSN     DSN FOR FIRST TAPE FILE       IV19900
         MVC   0(4,R5),=C'TAPE'   SET RETRY INDICATOR           IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     RETURN TO CALLER WITH RETRY RETURN CODE                 * IV19900
*                                                             * IV19900
*************************************************************** IV19900
REVALDAT EQU   *                                                IV19900
         LA    R15,RETRY          SET RETRY PROCESS RETURN CODE IV19900
         B     RETURN             SKIP LOAD OF SAVEAREA ADDR    IV19900
RQUIET   EQU   *                                                IV19900
         LA    R15,QUIET          SET QUIET PROCESS RETURN CODE IV19900
         B     RETURN             SKIP LOAD OF SAVEAREA ADDR    IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     RETURN TO CALLER                                        * IV19900
*                                                             * IV19900
*************************************************************** IV19900
RETURN   EQU   *                                                IV19900
         LEAVE RETCODE=(15),      RETURN TO CALLER              IV19900X
               GETMAIN=WORKLEN                                  IV19900
*************************************************************** IV19900
*                                                             * IV19900
*     CONSTANTS                                               * IV19900
*                                                             * IV19900
*************************************************************** IV19900
WTOMSG1  DC    AL2(L'LMSG1)       MESSAGE LENGTH                IV19900
         DC    X'8000'            MCS FLAGS                     IV19900
         DC    C'&PFX..017 ACF2, TMS IS NOT PROPERLY ACTIVATED' IV19900
LMSG1    DS    0CL(*-WTOMSG1)                                   IV19900
         DC    B'0100000000000000' DESCRIPTOR CODES             IV19900
         DC    B'1010100010100000' ROUTING CODES                IV19900
LWTOMSG1 EQU   *-WTOMSG1                                        IV19900
WTOMSG2  DC    AL2(L'LMSG2)       MESSAGE LENGTH                IV19900
         DC    X'8000'            MCS FLAGS                     IV19900
         DC    C'&PFX..017 ACF2, REPLY ''U'' TO CONTINUE, '     IV19900
         DC    C'''C'' TO CANCEL, OR '                          IV19900
         DC    C'''R'' TO RETRY AFTER ACTIVATING TMS'           IV19900
LMSG2    DS    0CL(*-WTOMSG2)                                   IV19900
         DC    B'0100000000000000' DESCRIPTOR CODES             IV19900
         DC    B'1010100010100000' ROUTING CODES                IV19900
LWTOMSG2 EQU   *-WTOMSG2                                        IV19900
WTOMSG3  DC    AL2(L'LMSG3)       MESSAGE LENGTH                IV19900
         DC    X'8000'            MCS FLAGS                     IV19900
         DC    C'&PFX..018 ACF2, '                              IV19900
         DC    C'UNAUTHORIZED USE OF LABEL=EXPDT=98000'         IV19900
LMSG3    DS    0CL(*-WTOMSG3)                                   IV19900
         DC    B'0000001000000000' DESCRIPTOR CODES             IV19900
         DC    B'0000000000100000' ROUTING CODES                IV19900
LWTOMSG3 EQU   *-WTOMSG3                                        IV19900
         LTORG ,                                                IV19900
         END   &CSECT                                           IV19900
