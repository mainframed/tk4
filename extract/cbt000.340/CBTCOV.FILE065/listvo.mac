LVO      TITLE 'LISTVO - COMMAND PROCESSOR'
*---------------------------------------------------------------------*
*                                                                     *
*   ATTRIBUTES:  REENTERABLE, NOT AUTHORIZED                          *
*                                                                     *
*   UPDATES:                                                          *
*      12/05/83 U018 LDW  ADD TOTALS LINE IF MORE THAN ONE VOLUME     *
*                            LISTED ("NOTOTALS" KEYWORD WILL SUPPRESS)*
*                         FIX CHECK FOR 'ALL'                         *
*      11/30/83 U017 LDW  USE GQSCAN TO CHECK FOR ENQ ON SYSVTOC      *
*                         MISC OTHER CLEANUP                          *
*      07/26/83 U016 LDW  IF VTOC INDEX EXISTS, CALL CVAF TO GET      *
*                            NUMBER OF AVAILABLE DSCBS                *
*                         UPDATE UNITNAME TABLE                       *
*      03/14/83 U015 LDW  DISPLAY 4 DIGITS OF # OF FREE EXTENTS       *
*      11/29/82 U014 EMS  FIX BUG IN U013                             *
*      11/19/82 U013 LDW  SHORTEN LINE LENGTH FOR STUPID OLD OLD TCAM *
*                         CHANGE HEADER LINES TO LOWER CASE           *
*                         PUT IN 'REMOV' INSTEAD OF LEAVING IT BLANK  *
*      11/15/82 U012 LDW  CHANGE UNIT DEFINITIONS FOR HUGHES AIRCRAFT *
*                         DON'T DEFAULT TO 'ALL' IF GUN NOT FOUND     *
*                         USE "PUTLINE" INSTEAD OF "TPUT"/"TPUTX"     *
*      08/30/82 U011 LDW  INDICATE INDEXED VTOC FOR PRIVILEGED USERS  *
*      08/09/82 U010 LDW  DEFAULT TO "STATUS" IF TUBE                 *
*                         ADD "NOHEADER" KEYWORD                      *
*                         UPDATE VOLUMES TABLE                        *
*      02/16/82 U009 LDW  RESTRICT 'ALL' TO SYSTEMS PROGRAMMERS       *
*                         CREATE 'UNITS' MACRO, INCREASE MAX TO 5     *
*                         SET FOR TITLE INSURANCE UNIT TYPES          *
*                         HANDLE BLANK GUN IN UNITNAME TABLE          *
*                         IMPROVE "NO VOLUMES FOUND ..." ERROR MSG    *
*                         USE 'UNITS' FOR PHONY GENERICS              *
*                         ADD PHONY GENERIC "PC"                      *
*      06/23/81 U008 SDM  SIMPLIFY UNIT TABLE LOGIC (SLIGHTLY)        *
*      06/08/81 U007 SDM  CONVERT 'MACRO=YES' TO 'GETMAIN='           *
*                         ADD THREE INDEX SUPPORT FOR 'SYS' REQUEST   *
*                         MAKE GENERIC NAME LENGTH LOGIC MORE FLEXIBLE*
*      01/06/81 U006 LDW  SCAN QCB'S TO CHECK FOR ENQ ON SYSVTOC      *
*      05/07/80 U005 LDW  ADD PHONY GENERIC 'SYS'                     *
*                         CHANGE TO LOCAL MACROS                      *
*      12/04/79 U004 LDW  INSERT MORE SPACES                          *
*                         CHANGE UNIT TABLE                           *
*                         ADD STATUS INFO                             *
*                         DO BOTH USE COUNTS                          *
*      11/16/79 U003 SDM  REPLACE 'SVC 3' WITH REAL EXIT CODE         *
*      10/01/79 U002 LDW  REPAIR FMT4 OBTAIN CODE                     *
*      1977     U001 LDW  OVERHAUL TITLE LOGIC                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
         MACRO                                                     U009
&NFS     UNITS &GENERIC,&VOLS                                      U009
         AIF   (N'&VOLS EQ 0).ERR1      NO VOLUME GROUPS GIVEN     U009
         AIF   (N'&VOLS LE 5).GEN       NOT TOO MANY GROUPS GIVEN  U009
         MNOTE 8,'TOO MANY VOLUME GROUPS SPECIFIED'                U009
         AGO   .GEN                                                U009
.ERR1    MNOTE 8,'NO VOLUME GROUPS SPECIFIED'                      U009
         AGO   .GEN                                                U009
.GEN     ANOP                                                      U009
&NFS     DC    CL8'&GENERIC '           GENERIC VOLUME IN PSCB
         DC    CL6'&VOLS(1) '           VOLUME GROUP # 1
         DC    CL6'&VOLS(2) '           VOLUME GROUP # 2
         DC    CL6'&VOLS(3) '           VOLUME GROUP # 3
         DC    CL6'&VOLS(4) '           VOLUME GROUP # 4
         DC    CL6'&VOLS(5) '           VOLUME GROUP # 5
         SPACE 1
         MEND  ,                                                   U009
         SPACE 3
         GBLB  &GRS                     IS SP1.2 GRS SUPPORTED?    U017
&GRS     SETB  1                        YES                        U017
         EJECT ,
LISTVO   OSENTER  ENV=(CP,SIM),PL=(PARSE,IO),PARMREG=R2,           U012+
               EXIT=RET,RC=0,GETMAIN=(WORKLEN,WORKD)               U017
         EJECT
         MVC   LINERDW(4),=Y(4+LINEL,0)   INITIALIZE PUTLINE RDW   U012
         MVC   XHDR1RDW(4),=Y(4+LINEL,0)  INITIALIZE PUTLINE RDW   U012
         MVC   XHDR2RDW(4),=Y(4+LINEL,0)  INITIALIZE PUTLINE RDW   U012
         LA    R0,PUTLNMFL              -> PUTLINE MF=L            U012
         ST    R0,IOPLIOPB              SET IN IOPL                U012
         SPACE 2
*        IF NO OPERAND GIVEN DEFAULT TO SEARCH INDEX BASED ON USER'S
*        GENERIC UNIT NAME
         SPACE 2
         MVI   FLAG,F@TOTALS            INIT ALL FLAGS.            U018
         XC    CLEAR(CLEARL),CLEAR      CLEAR SOME VARIABLES       U018
         ZAP   TOTCYL,=P'0'             ...                        U018
         ZAP   TOTTRK,=P'0'             ...                        U018
         ZAP   TOTNUM,=P'0'             ...                        U018
         SPACE 1
         GTSIZE  ,                      GET SCREEN SIZES           U010
         LTR   R0,R0                    TUBE?                      U010
         BNP   *+8                      NO - SKIP                  U010
         OI    FLAG,F@STAT              YES - DEFAULT TO STATUS    U010
         SPACE 1
         MVC   XHDR1,HEADER1            COPY HEADER LINE 1         U011
         MVC   XHDR2,HEADER2            COPY HEADER LINE 2         U011
         L     R1,CPPLPSCB              -> PSCB                    U011
         TM    PSCBATR1-PSCB(R1),PSCBCTRL  OPERATOR?               U011
         BO    OKIXVTOC                 YES - HE SEES IXVTOC IND.  U011
*                                       NO - CLEAR IT FROM ...     U011
         MVC   XHDR1+OLIXVTOC-1-LINE(4),BLANKS  ... HEADER ...     U011
         MVC   XHDR2+OLIXVTOC-1-LINE(4),BLANKS  ... LINES          U011
         SPACE 1
OKIXVTOC MVC   VOLLIST(VOLLISTL),BLANKS CLEAR SEARCH INDICES       U007
         L     R1,CPPLECT               POINT TO ECT
         TM    ECTSWS-ECT(R1),ECTNOPD   NO OPERAND?
         BO    UNIT                     NO, LEAVE IT BLANK
         SPACE 1
         TSPARSE CBUF=(CPPLCBUF,I),PCL=(=V(PCL),I)
         SPACE 1
         L     R15,TSPARANS             POINT TO DESCRIPTOR LIST   U004
         CLI   STAT+1-PDL(R15),1        STATUS SPECIFIED?          U004
         BNE   *+8                      NO                         U004
         OI    FLAG,F@STAT              YES - SET FLAG             U004
         CLI   STAT+1-PDL(R15),2        NOSTATUS SPECIFIED?        U010
         BNE   *+8                      NO                         U010
         NI    FLAG,255-F@STAT          YES - UNSET FLAG           U010
         CLI   HEAD+1-PDL(R15),2        NOHEADER SPECIFIED?        U010
         BNE   *+8                      NO                         U010
         OI    FLAG,F@NOHEAD            YES - SET FLAG             U010
         CLI   TOTALS+1-PDL(R15),2      NOTOTALS SPECIFIED?        U018
         BNE   *+8                      NO - SKIP                  U018
         NI    FLAG,255-F@TOTALS        YES - UNSET FLAG           U018
         LA    R1,VOL-PDL(,R15)         POINT TO VOL DESCRPT       U004
         L     R2,0(,R1)                POINT TO THING
         CLI   5(R1),3                  CORRECT LENGTH?            U005
         BNE   NOT$ALL                  NO - SKIP                  U005
         CLC   =C'SYS',0(R2)            WANT PHONY GENERIC?        U005
         BNE   NOT$SYS                  NO - SKIP                  U005
         MVC   VOLLIST(VOLLISTL),UNIT$SYS+8  SET FOR SYSTEMS PGMRS U009
         B     CONTINUE                 START                      U005
         SPACE 2
NOT$SYS  CLC   =C'ALL',0(R2)            ALL VOLUMES REQUESTED ?    U018
         BNE   NOT$ALL                  NO,
         OI    FLAG,F@ALL               INDICATE ALL VOLUMES       U007
         L     R1,CPPLPSCB              -> PSCB                    U009
         TM    PSCBATR1-PSCB(R1),PSCBACCT  SYSTEMS PROGRAMMER?     U009
         BO    CONTINUE                 YES - GO DO ALL VOLUMES    U009
         SPACE 1
         LA    R0,MSGNALL               "RESTRICTED OPERAND - ALL" U012
         BAL   R11,PUTLINE                                         U012
         B     RET                      GET OUT OF HERE            U009
         SPACE 2
NOT$ALL  LH    R14,4(,R1)               AND LENGTH                 U012
         LTR   R14,R14                  ANY OPERAND                U012
         BZ    UNIT                     NO, GO DEFAULT THE SEARCH INDEX
         LA    R0,5                     MAX # OF VOL PREFIXES      U012
         LA    R3,VOLUME1               -> FIRST SLOT              U012
         SPACE 1
SAVE$VOL BCTR  R14,0                    DECREMENT FOR EXECUTE      U012
         EX    R14,NAMVC                MOVE TO VOLUME1            U012
         CLC   =X'FF000000',8(R1)       END OF LIST?               U012
         BE    CONTINUE                 YES - START PROCESSING     U012
         L     R1,8(,R1)                -> NEXT VOLUME PDE         U012
         L     R2,0(,R1)                -> DATA                    U012
         LH    R14,4(,R1)               LENGTH                     U012
         LA    R3,L'VOLUME1(,R3)        -> NEXT VOLUME SLOT        U012
         BCT   R0,SAVE$VOL              SAVE NEXT VOLUME           U012
         B     CONTINUE                 GO SEARCH ON ENTERED INDEX U005
         TITLE 'S P A C E -- PRODUCE VOLUME LIST'
*---------------------------------------------------------------------*
*
* NAME         UNIT
*
* FUNCTION     SELECT A DEFAULT SEARCH INDEX BASED ON USER'S GENERIC
*              UNIT NAME IN PSCB.
*
*---------------------------------------------------------------------*
         SPACE 2
UNIT     L     R1,CPPLPSCB              -> PSCB
         LA    R2,UNITNAME              ADDR OF UNIT NAME TABLE
         SPACE 1
UNITSCAN CLI   0(R2),X'FF'              END OF TABLE ?
         BE    UNITNONE                 YES, UNIT NAME NOT FOUND   U012
         CLC   0(8,R2),PSCBGPNM-PSCB(R1) SEARCH TABLE FOR UNITNAME
         BE    UNITYES                  FOUND
         LA    R2,UNITNLEN(,R2)         -> NEXT TABLE ENTRY        U008
         B     UNITSCAN
         SPACE 2
UNITNONE MVC   LINE(LINEL),BLANKS       CLEAR PRINT LINE           U012
         MVC  LINE(44),=C'NO TABLE ENTRY FOUND FOR GENERIC UNIT NAME -'
         MVC   LINE+45(8),PSCBGPNM-PSCB(R1)                        U012
         LA    R0,LINERDW               -> DATA LINE               U012
         BAL   R11,PUTLINE              TELL HIM ABOUT MY PROBLEM  U012
         B     RET                      GET OUT OF HERE            U012
         SPACE 1
UNITYES  MVC   VOLLIST(UNITNLEN-8),8(R2)  FIRST VOLUME INDEX       U009
         B     CONTINUE
         SPACE 2
UNITNAME DS    0H                       TABLE OF UNITNAME MAPPINGS
* THE FIELDS ARE:  CL8'TSO DEFAULT UNIT NAME'                      U002
*                  CL6'GENERIC VOLUME SERIAL (FIRST GROUP)'        U002
*                  CL6'GENERIC VOLUME SERIAL (SECOND GROUP)'       U002
*                  ETC (CURRENTLY 5 GROUP ENTRIES ALLOWED)         U009
*    THE FIRST BLANK VOLUME SERIAL ID ENDS THE GROUP               U008
*    THE UNITNAME TABLE ENTRY LENGTH MUST BE LESS THAN OR EQUAL    U009
*       TO: (EIGHT PLUS THE LENGTH OF THE FIELD 'VOLLIST').        U009
         UNITS TSO1,(TSO,DATA)                                     U011
UNITNLEN EQU   *-UNITNAME                                          U008
         UNITS DATA,(DATA)                                         U016
         UNITS MSTS,(MSTS,MSDA)                                    U016
         UNITS MSDA,(MSDA)                                         U016
         UNITS ,(??????)                                           U009
         UNITS ********,(AAAAAA,BBBBBB,CCCCCC)  PATCH SPACE        U009
         UNITS ********,(AAAAAA,BBBBBB,CCCCCC)  PATCH SPACE        U009
         DC    X'FF'                    END OF TABLE MARKER
         SPACE 1
*  ENTRIES FOR PHONY GENERIC UNITS                                 U009
UNIT$SYS UNITS SYS,(MVS,PAGE,IPO,SPOOL)                            U016
         EJECT
CONTINUE DS    0H
         SPACE 3
*        SCAN UCB TABLE
         SPACE 1
AGAIN    L     R1,CVTPTR                GET CVT ADDRESS
         L     R6,CVTILK2-CVT(,R1)      POINT TO UCB LOOKUP TABLE
         XR    R7,R7                    CLEAR BACK PTR
         SPACE 1
         TM    FLAG,F@ALL               ALL VOLUMES?               U007
         BO    LOOP                     YES, DON'T GET LENGTH      U007
         SPACE 1
         NI    FLAG,255-F@VOLFND        NO VOLS FOUND / THIS INDEX U012
         LA    R1,VOLUME1               -> GENERIC VOLUME NAME     U007
         LA    R5,5(,R1)                -> END OF NAME             U007
LEN01    CLI   0(R5),C' '               TRAILING BLANK?            U007
         BNE   LEN02                    NO, FOUND REAL END         U007
         BCT   R5,LEN01                 DECREMENT AND LOOP         U007
         SPACE 1
LEN02    SR    R5,R1                    LENGTH -1 FOR EX           U007
         BNM   *+8                      LENGTH IS GOOD             U007
         EX    0,*                      BAD LENGTH -- LOGIC ERROR  U007
         SPACE 1
LOOP     CLC   =X'FFFF',0(R6)           CHECK FOR END OF TABLE
         BE    RETURN                   ...YES, RETURN
         SR    R8,R8                    PREPARE FOR ICM
         ICM   R8,B'0011',0(R6)         LOAD UCB OFFSET
         LTR   R8,R8
         BNZ   GOTUCB                   IF NON-ZERO GO ON
NXTUCB   LA    R6,2(,R6)                -> NEXT UCB ADDRESS
         B     LOOP                     GO ON SCANNING
         SPACE 1
*        FOR EACH UCB, CHECK FOR DIRECT ACCESS 2314 OR 2301
         SPACE 1
         USNGX UCB,R8
GOTUCB   TM    UCBTBYT3,UCB3DACC        DIRECT ACCESS?
         BZ    NXTUCB                   NO
         CR    R8,R7                    ALREADY DONE THIS ONE?
         BNH   NXTUCB                   IF SO SKIP IT NOW
         LR    R7,R8                    SAVE THIS ONE AS DONE
         TM    UCBSTAT,UCBONLI          ONLINE?
         BNO   NXTUCB                   NO, IGNORE DEVICE
         CLC   UCBTBYT3(2),=X'2005'     DATA CELL?
         BE    NXTUCB                   IF SO SKIP IT
         TM    UCBVOLI,X'FF'            ANY NAME?
         BZ    NXTUCB                   NO, IGNORE IT
         TM    FLAG,F@ALL               ALL VOLUMES?               U007
         BO    USEUCB                   YES, USE IT                U007
         EX    R5,EXCLC                 SEE IF THIS IS A MATCH     U007
         BNE   NXTUCB                   IF NOT SKIP IT
         SPACE 1
*        FORMAT UCB INFORMATION FIRST
         SPACE 1
USEUCB   MVC   LINE(LINEL),BLANKS       CLEAR THE LINE             U001
         MVC   OLVOLSER,UCBVOLI         PRINT VOLUME NAME
         MVC   OLDEVADR,UCBNAME         GET UNIT ADDRESS
         SPACE 1
         MVI   OLSTAT1+4,C'/'           PUT IN DELIMITER           U004
         MVC   OLSTAT2,=C'REMOV'        ASSUME REMOVABLE           U013
         TM    UCBSTAT,UCBRESV          RESERVED?                  U004
         BNO   *+10                     NO - SKIP                  U004
         MVC   OLSTAT2,=C'RSRVD'        YES                        U004
         TM    UCBSTAT,UCBPRES          RESIDENT?                  U004
         BNO   *+10                     NO - SKIP                  U004
         MVC   OLSTAT2,=C'RSDNT'        YES                        U004
         SPACE 1
         TM    UCBSTAB,UCBBPRV          PRIVATE?                   U004
         BNO   *+10                     NO - SKIP                  U004
         MVC   OLSTAT1,=C'PRIV'         YES                        U004
         TM    UCBSTAB,UCBBPUB          PUBLIC?                    U004
         BNO   *+10                     NO - SKIP                  U004
         MVC   OLSTAT1,=C'PUBL'         YES                        U004
         TM    UCBSTAB,UCBBSTR          STORAGE?                   U004
         BNO   *+10                     NO - SKIP                  U004
         MVC   OLSTAT1,=C'STOR'         YES                        U004
         SPACE 1
         XR    R1,R1                    CLEAR A REG                U004
         IC    R1,UCBUSER               GET USE COUNT              U004
         CVD   R1,DBLW                  CONVERT TO PRINT           U004
         OI    DBLW+7,X'0F'             FIX SIGN NIBBLE            U004
         UNPK  OLUALLOC,DBLW            PLACE IT ON THE LINE       U004
         SPACE 1
         IC    R1,UCBDMCT               GET OPEN DCB COUNT         U002
         N     R1,=XL4'7F'              REPAIR IT                  U002
         CVD   R1,DBLW                  CONVERT TO PRINT
         OI    DBLW+7,X'0F'             FIX SIGN NIBBLE            U002
         UNPK  OLUOPEN,DBLW             PLACE IT ON THE LINE
         SPACE 1
         TM    UCBFLA,UCBNRY            NOT READY?                 U004
         BNO   CHECKENQ                 NO - CONTINUE              U017
         MVC   OLMSG(9),=C'NOT READY'   SAY WHAT THE PROBLEM IS    U004
         B     PUTIT                    AND DON'T HANG UP HERE     U004
         SPACE 3
*        SCAN THE ENQ STRUCTURE TO SEE IF ANYONE IS ENQUEUED ON    U006
*        'SYSVTOC' 'VOLSER'.  IF SO, DON'T DO THE LSPACE SVC,      U006
*        SINCE IT WILL HANG UP AND CAN'T BE ATTENTIONED OUT OF.    U006
CHECKENQ L     R1,16                    -> CVT                     U017
         CLC   =F'0',640(R1)            ANY QCB'S?                 U017
         BNZ   QCBSCAN                  YES - PRE SP1.2            U017
          AIF   (&GRS).GRS01                                       U017
         B     LSPACE                   SP1.2, BUT NOT SUPPORTED   U017
          AGO   .GRS02                                             U017
.GRS01    ANOP                                                     U017
         MVC   GQLIST(GQSCANXL),GQSCANX INIT PARM BLOCK            U017
         XC    TOKEN(4),TOKEN           ZERO TOKEN                 U017
         L     R2,=A(AREAL)             GET LENGTH OF 'AREA'       U017
         SPACE 1                                                   U017
         GQSCAN  AREA=(AREA,(R2)),TOKEN=TOKEN,MF=(E,GQLIST),       U017$
               RESNAME=(=CL8'SYSVTOC',UCBVOLI,6)                   U017
         SPACE 1                                                   U017
         CH    R15,=H'16'               RC IN RANGE?               U017
         BNH   *+8(R15)                 YES - DECODE IT            U017
         EX    0,*                      NO - DIE                   U017
         B     SCAN1                    00 - SCAN COMPLETE         U017
         B     LSPACE                   04 - NOTHING FOUND         U017
         B     SCAN1                    08 - SCAN INCOMPLETE       U017
         EX    0,*                      12 - FOUND ABNORMAL COND.  U017
         EX    0,*                      16 - INVALID SYSNAME       U017
         SPACE 1                                                   U017
SCAN1    ST    R0,RIBLS                 SAVE LENGTH OF RIB & RIBE  U017
         SR    R15,R15                  CLEAR RETURN REGISTER      U017
         LA    R1,AREA                  POINT TO RIB               U017
         USNGX RIB,R1                                              U017
         AH    R1,RIBVLEN               + LEN OF VARIABLE PORTION  U017
         AH    R1,RIBL                  + RIBLENGTH RETURN BY SCAN U017
*                                       R1 -> FIRST RIBE           U017
         DROPX R1                       RIB                        U017
         USNGX RIBE,R1                                             U017
         MVC   OLMSG(39),=C'TRY LATER - VTOC IN USE BY JOB ????????' 17
         MVC   OLMSG+31(8),RIBEJBNM     PUT JOBNAME INTO MESSAGE   U017
         B     PUTIT                    AND PUT OUT MESSAGE        U017
         DROPX R1                       RIBE                       U017
         SPACE 3
*---  SEARCH OLD STYLE QCB'S IN SQA                                ---*
QCBSCAN  L     R1,16                    -> CVT                     U006
         LA    R2,640(,R1)              -> QCB ORIGIN              U006
         USNGX MAJ,R2                   MAJOR QCB                  U006
         SPACE 1
MAJLOOP  L     R2,MAJNMAJ               -> NEXT MAJOR QCB          U006
         LTR   R2,R2                    END OF CHAIN?              U006
         BZ    LSPACE                   YES - NO MAJOR QCB FOR     U006
*                                       'SYSVTOC', SO IT'S SAFE.   U006
         CLC   =C'SYSVTOC ',MAJNAME     IS THIS WHAT WE WANT?      U006
         BNE   MAJLOOP                  NO - CHECK NEXT MAJOR QCB. U006
         L     R2,MAJFMIN               -> FIRST MINOR QCB         U006
         DROPX R2                       MAJOR QCB                  U006
         USNGX MIN,R2                                              U006
         B     MINLOOP1                 SKIP AROUND FIRST TIME     U006
         SPACE 1                                                   U006
MINLOOP  L     R2,MINNMIN               -> NEXT MINOR QCB.         U006
MINLOOP1 LTR   R2,R2                    ANY?                       U006
         BZ    LSPACE                   NO - IT'S SAFE.            U006
         CLC   OLVOLSER,MINNAME         IS IT FOR OUR VOLUME?      U006
         BNE   MINLOOP                  NO - CHECK NEXT            U006
*  DRAT.  SOMEONE IS ENQUEUED ON THE VTOC OF THE VOLUME I WANT TO  U006
*  LOOK AT.  SO, SAY WE'RE SORRY, AND SKIP THE LSPACE SVC.         U006
         L     R2,MINFQEL               -> FIRST QEL               U006
         DROPX R2                       MINOR QCB                  U006
         USNGX QEL,R2                                              U006
*U017    TM    QELQFLGS,QELSHARE        SHARED?                    U006
*U017    BO    LSPACE                   YES??? - DO THE LSPACE SVC U006
         MVC   OLMSG(39),=C'TRY LATER - VTOC IN USE BY JOB ????????'  5
         LH    R15,QELASID              GET OWNER'S ASID           U006
         DROPX R2                       QEL                        U017
         LTR   R15,R15                  ANY?                       U006
         BZ    PUTIT                    NO - LEAVE MSG AS IS       U006
         L     R1,16                    -> CVT                     U006
         L     R1,CVTASVT-CVT(,R1)      -> ASVT                    U017
         SLL   R15,2                    MULTIPLY ASID BY 4         U006
         LA    R1,ASVTENTY-4-ASVT(R1,R15)  -> THIS AS'S ASVT ENT   U017
         L     R1,0(,R1)                -> ASCB                    U006
         USNGX ASCB,R1                                             U017
         ICM   R15,B'1111',ASCBJBNI     -> JOBNAME                 U006
         BNZ   GOTJOBNM                 IT'S A JOB                 U006
         ICM   R15,B'1111',ASCBJBNS     -> S/M/L JOBNAME           U006
         BZ    PUTIT                    NO POINTER HERE EITHER?    U006
         DROPX R1                       ASCB                       U017
         SPACE 1
GOTJOBNM MVC   OLMSG+31(8),0(R15)       MOVE JOBNAME TO MSG        U006
         B     PUTIT                    AND WE'RE DONE.            U006
         SPACE 2                                                   U006
*        GET MORE INFORMATION FROM LSPACE
         SPACE 1
LSPACE   LR    R0,R8                    POINT TO UCB
         LA    R1,DBLW                  WORKSPACE
         SVC   78                       GET SPACE INFO
         LTR   R15,R15                  DID IT WORK?
         BZ    SPCOK                    YES
* THE 'DOS' BIT (AT LEAST) WILL GET US HERE.
* APPARENTLY, HOWEVER, THE DIRF BIT WON'T.
VTOCERR  MVC   OLFCYL(25),=CL25'ERROR IN VTOC'
PUTIT    EQU   *
         LA    R9,HEADERL1              GET LENGTH OF SHORT TITLE  U004
         TM    FLAG,F@STAT              STATUS WANTED?             U004
         BNO   *+8                      NO                         U004
         LA    R9,HEADERL2              YES - LONGER TITLE         U004
         TM    FLAG,F@HDRS+F@NOHEAD     NEED HEADERS?              U010
         BNZ   NOTFIRST                 NO - SKIP                  U010
         LA    R1,4(,R9)                GET LENGTH(RDW+DATA)       U012
         STH   R1,XHDR1RDW              SET IN RDW                 U012
         STH   R1,XHDR2RDW              SET IN RDW                 U012
         LA    R0,XHDR1RDW              -> DATA LINE               U012
         BAL   R11,PUTLINE              PRINT THE FIRST TITLE LINE U012
         LA    R0,XHDR2RDW              -> DATA LINE               U012
         BAL   R11,PUTLINE              PRINT THE SECOND TITLE     U012
         SPACE 2
NOTFIRST LA    R1,4(,R9)                GET LENGTH(RDW+DATA)       U012
         STH   R1,LINERDW               SET IN RDW                 U012
         LA    R0,LINERDW               -> DATA LINE               U012
         BAL   R11,PUTLINE              PUT IT OUT                 U012
         OI    FLAG,F@HDRS+F@VOLFND     HEADERS HAVE BEEN DONE     U012
*                                       AND VOL HAS BEEN FOUND     U012
         LH    R15,NUMVOLS              GET # OF VOLUMES FOUND     U018
         LA    R15,1(,R15)              INCREMENT                  U018
         STH   R15,NUMVOLS              SAVE UPDATED COUNT         U018
         B     NXTUCB                   NOW GO ON
         SPACE 2                                                   U004
SPCOK    MVC   OLFCYL,DBLW+7            CYL (TOTAL)
         MVC   OLFTRK,DBLW+11           TRK (TOTAL)
         CLI   OLFTRK,C'0'              ONLY 3 DIGITS?             U001
         BNE   *+8                      NO                         U001
         MVI   OLFTRK,C' '              SUPPRESS LEADING 0         U001
         MVC   OLFNUM,DBLW+16           NUM (OF AREAS)             U015
         CLI   OLFNUM,C'0'              ONLY 3 DIGITS?             U015
         BNE   *+8                      NO                         U015
         MVI   OLFNUM,C' '              SUPPRESS LEADING 0         U015
         MVC   OLCCYL,DBLW+22           CYL (LARGEST)
         MVC   OLCTRK,DBLW+27           TRK (LARGEST)
         SPACE 1
*---  UPDATE TOTALS/MAX COUNTERS                                   ---*
         PACK  PWORK,DBLW+7(3)          GET NUMBER OF FREE CYL     U018
         AP    TOTCYL,PWORK             ACCUMULATE IT              U018
         PACK  PWORK,DBLW+11(4)         GET NUMBER OF FREE TRK     U018
         AP    TOTTRK,PWORK             ACCUMULATE IT              U018
         PACK  PWORK,DBLW+16(4)         GET NUMBER OF FREE EXTENTS U018
         AP    TOTNUM,PWORK             ACCUMULATE IT              U018
         CLC   OLCCYL,MAXCYL            THIS LARGEST CONTIG EXT?   U018
         BL    GET$FMT0                 NO - SKIP                  U018
         BE    CYLSAME                  MAYBE                      U018
         MVC   MAXCYL,OLCCYL            YES - SAVE NEW LARGEST CYL U018
         MVC   MAXTRK,OLCTRK            ..AND SAVE NEW LARGEST TRK U018
         B     GET$FMT0                 DONE HERE                  U018
         SPACE 1
CYLSAME  CLC   OLCTRK,MAXTRK            THIS LARGEST CONTIG TRK?   U018
         BNH   *+10                     NO - SKIP                  U018
         MVC   MAXTRK,OLCTRK            YES - SAVE NEW LARGEST TRK U018
         SPACE 1
*        NOW FIND THE NUMBER OF DSCBS LEFT (FMT0 COUNT)
         SPACE 1
GET$FMT0 MVC   CAML(4),=AL1(193,0,0,0)  MOVE IN DSNAME OPTIONS     U002
         MVC   CAMV(6),UCBVOLI          VOLUME NAME
         LA    R0,=44X'04'              FORMAT 4 DSCB'S DSNAME     U002
         LA    R1,CAMV                  VOLUME LIST
         LA    R2,CAMW                  WORKAREA
         STM   R0,R2,CAML+4             SET UP CAMLIST
         MVC   OLFDSCB,=C'????'         SET DEFAULT                U002
         MVC   OLIXVTOC,=C'????'        SET DEFAULT                U011
         L     R1,CPPLPSCB              -> PSCB                    U017
         TM    PSCBATR1-PSCB(R1),PSCBCTRL  OPERATOR?               U017
         BO    *+10                     YES - SKIP                 U017
         MVC   OLIXVTOC,BLANKS          NO - REMOVE '??'           U017
         OBTAIN CAML                    GET FORMAT 4 DSCB
         LTR   R15,R15                  DID IT WORK?
         BNZ   PUTIT                    THEN GIVE UP               U002
******** TM    DS4VTOCI,DS4IVTOC        INDEXED VTOC?              U016
         TM    CAMW+14,1                INDEXED VTOC?              U016
         BO    IXVTOC                   YES - GO CALL CVAF         U016
         MVC   OLIXVTOC,BLANKS          CLEAR INDEXED INDICATOR    U017
         LH    R1,CAMW+6   DS4DSREC     GET NUMBER OF FMT0S        U002
         SPACE 2                                                   U016
FMT$FMT0 CVD   R1,DBLW                  CONVERT IT
         OI    DBLW+7,X'0F'             FIX SIGN NIBBLE            U002
         UNPK  OLFDSCB,DBLW             AND MOVE TO THE LINE       U002
         A     R1,TOTDSCB               TALLY TOTAL NUMBER OF ...  U018
         ST    R1,TOTDSCB               ... FREE DSCB'S            U018
         B     PUTIT                    NOW GO ON                  U016
         SPACE 2                                                   U016
IXVTOC   L     R1,CPPLPSCB              -> PSCB                    U011
         TM    PSCBATR1-PSCB(R1),PSCBCTRL  OPERATOR?               U011
         BNO   *+10                     NO - SKIP                  U016
         MVC   OLIXVTOC,=C'IX'          YES - SHOW IT'S INDEXED    U011
         MVC   CVPL(CVPLLEN),CVPLX      COPY MODEL CVPL            U016
         SPACE 1
*---  NEED TO OPEN THE VTOC AND PASS THE DEB ADDR IF NOT APF AUTH  ---*
*        CVAFDSM  ACCESS=MAPDATA,MAP=VTOC,COUNT=YES,DEB=(R??),     U016$
               CTAREA=DBLW,MF=(E,CVPL)                             U016
*---  SO, FOR NOW, WE'LL BE APF AUTHORIZED AND USE THE UCB ADDR    ---*
         LA    R0,1                     REQUEST "SET APF"          U016
         SVC   243                      DO IT                      U016
         CVAFDSM  ACCESS=MAPDATA,MAP=VTOC,COUNT=YES,UCB=(R8),      U016$
               CTAREA=DBLW,MF=(E,CVPL),BRANCH=(YES,PGM)            U016
         SPACE 1
         LR    R2,R15                   SAVE RETURN CODE           U016
         SR    R0,R0                    REQUEST "UN-SET APF"       U016
         SVC   243                      DO IT                      U016
         LTR   R2,R2                    DID CVAFDSM WORK?          U016
         BNZ   PUTIT                    I FIGURED AS MUCH          U016
         L     R1,DBLW                  GET THE FORMAT ZERO COUNT  U016
         B     FMT$FMT0                 AND GO FORMAT IT           U016
         SPACE 1
*        GO AWAY NOW
         SPACE 1
RETURN   TM    FLAG,F@VOLFND            ANY VOLUMES FOUND?         U012
         BO    RET77                    YES - OK                   U012
         SPACE 1
         MVC   LINE(LINEL),BLANKS       CLEAR PRINT LINE           U009
         MVC   LINE(28),=C'NO VOLUMES FOUND FOR INDEX -'           U009
         MVC   LINE+29(6),VOLUME1       SHOW WHAT WE DIDN'T FIND   U009
         LA    R0,LINERDW               -> DATA LINE               U012
         BAL   R11,PUTLINE              PUT IT OUT                 U012
         SPACE 2
RET77    MVC   VOLLIST(VOLLISTL-6),VOLLIST+6   NEXT VOLUME         U009
         MVC   VOLLIST+VOLLISTL-6(6),BLANKS    BLANK LAST VOLUME   U009
         CLC   VOLUME1,BLANKS           ANY MORE VOLUMES?          U007
         BNE   AGAIN                    YES - PROCESS              U018
         TM    FLAG,F@TOTALS            WANT TOTALS?               U018
         BNO   RET                      NO - END THE PROGRAM       U018
         CLC   NUMVOLS,=H'2'            MORE THAN ONE VOL FOUND?   U018
         BL    RET                      NO - SKIP TOTALS LINE      U018
         SPACE 2
*---  FORMAT THE TOTALS LINE                                       U018
         MVC   LINE(LINEL),BLANKS       CLEAR THE DISPLAY LINE     U018
         MVC   LINE+3(14),=C'*** TOTALS ***'                       U018
         MVC   OLIXVTOC+2(11),=C'*** MAX ***'                      U018
         OI    TOTCYL+3,X'0F'           REMOVE SIGN                U018
         UNPK  OLFCYL-1(L'OLFCYL+1),TOTCYL  MAKE PRINTABLE         U018
         CLI   OLFCYL-1,C'0'            LEADING ZERO?              U018
         BNE   *+8                      NO - OK                    U018
         MVI   OLFCYL-1,C' '            YES - BLANK IT             U018
         OI    TOTTRK+3,X'0F'           REMOVE SIGN                U018
         UNPK  OLFTRK-1(L'OLFTRK+1),TOTTRK  MAKE PRINTABLE         U018
         CLI   OLFTRK-1,C'0'            LEADING ZERO?              U018
         BNE   *+8                      NO - OK                    U018
         MVI   OLFTRK-1,C' '            YES - BLANK IT             U018
         OI    TOTNUM+3,X'0F'           REMOVE SIGN                U018
         UNPK  OLFNUM-1(L'OLFNUM+1),TOTNUM  MAKE PRINTABLE         U018
         CLI   OLFNUM-1,C'0'            LEADING ZERO?              U018
         BNE   *+8                      NO - OK                    U018
         MVI   OLFNUM-1,C' '            YES - BLANK IT             U018
         MVC   OLCCYL,MAXCYL            MOVE IN LARGEST CONTIG CYL U018
         MVC   OLCTRK,MAXTRK            MOVE IN LARGEST CONTIG TRK U018
         L     R1,TOTDSCB               GET TOTAL # OF FREE DSCBS  U018
         CVD   R1,DBLW                  CONVERT IT                 U018
         OI    DBLW+7,X'0F'             FIX SIGN NIBBLE            U018
         UNPK  OLFDSCB-1(L'OLFDSCB+1),DBLW  AND MOVE TO THE LINE   U018
         CLI   OLFDSCB-1,C'0'           LEADING ZERO?              U018
         BNE   *+8                      NO - OK                    U018
         MVI   OLFDSCB-1,C' '           YES - BLANK IT             U018
         LA    R0,LINERDW               -> DATA LINE               U012
         BAL   R11,PUTLINE              PUT IT OUT                 U012
         B     RET                      END THE PROGRAM            U018
         SPACE 2
*---  AT ENTRY:  R0 -> RDW FOR PUTLINE                             U012
PUTLINE  PUTLINE  OUTPUT=((0),,,DATA),MF=(E,IOPL)                  U012
         SPACE 1
         BR    R11                      RETURN TO CALLER           U012
         SPACE 3                                                   U007
NAMVC    MVC   0(*-*,R3),0(R2)          << EXECUTED >>             U012
EXCLC    CLC   VOLUME1(*-*),UCBVOLI     << EXECUTED >>
         TITLE 'LISTVO    -- CONSTANTS'
BLANKS   DC    CL80' '                                             U007
HEADER1  LC    'VOLUME  DEV  USE COUNT  ---------- FREE ---------   CON$
               TIGUOUS  IX ',                                      U013$
               '/       /    /   /                 /                /  $
                        // '                                       U013
HEADERL1 EQU   *-HEADER1                                           U004
         LC    ' /UNIT /MOUNT'                                     U013
HEADERL2 EQU   *-HEADER1                                           U004
         SPACE 1
HEADER2  LC    ' NAME   ADR   ALC OPN   CYL   TRK   EXTENTS  DSCB     C$
               YL TRK  VTOC',                                      U018$
               ' /      /     /   /     ///   ///   /        ////     /$
               // ///  ////'                                       U018
         LC    '   /STATUS  '                                      U013
         SPACE 3                                                   U007
*---  MESSAGES USE 'WTO' MACRO ONLY FOR THE PURPOSE OF GENERATING
*---  A VARIABLE LENGTH MESSAGE.  THESE ARE *NOT* REAL WTO'S.      U012
         SPACE 1
MSGNALL  WTO   'RESTRICTED OPERAND - "ALL"',MF=L                   U014
         SPACE 2
CVPLX    CVAFDSM  ACCESS=MAPDATA,MAP=VTOC,COUNT=YES,MF=L           U016
         SPACE 1
CVPLLEN  EQU   *-CVPLX                                             U016
         SPACE 2
GQSCANX  GQSCAN  AREA=(,AREAL),REQLIM=MAX,SCOPE=ALL,REQCNT=1,MF=L  U017
         SPACE 1
GQSCANXL EQU   *-GQSCANX                                           U017
         SPACE 2
*DCBX    DCB   DDNAME=*-*,???           FOR VTOC FOR CVAF          U017
         SPACE 1
*DCBXL   EQU   *-DCBX                                              U017
         SPACE 2
         LTORG
         SPACE 2
         PRINT NOGEN
PCL      IKJPARM DSECT=PDL
VOL      IKJIDENT 'VOLUME SEARCH INDEX',LIST,OTHER=ALPHANUM,           $
               MAXLNTH=6,PROMPT='VOLUME SEARCH INDEX'              U012
STAT     IKJKEYWD
         IKJNAME  'STATUS'                                         U010
         IKJNAME  'NOSTATUS'                                       U010
HEAD     IKJKEYWD
         IKJNAME  'HEADER'                                         U010
         IKJNAME  'NOHEADER'                                       U010
TOTALS   IKJKEYWD
         IKJNAME  'TOTALS'                                         U018
         IKJNAME  'NOTOTALS'                                       U018
         IKJENDP
         PRINT GEN
         TITLE 'LISTVO  - DYNAMIC STORAGE DEFINITIONS'
WORKD    DSECT                                                     U017
         SPACE 1
PUTLNMFL PUTLINE  MF=L                                             U012
         SPACE 1
CVPL     CVAFDSM  ACCESS=MAPDATA,MAP=VTOC,COUNT=YES,MF=L           U016
         SPACE 2
VOLLIST  UNITS ,(,)                     SEARCH INDICES             U017
         ORG   VOLLIST                                             U017
VOLUME1  DS    CL6                      SEARCH INDEX
         ORG   ,                                                   U017
VOLLISTL EQU   *-VOLLIST                LENGTH OF WHOLE LIST       U009
         SPACE 2
LINERDW  DS    2H                       PUTLINE RECORD DESC WORD   U012
LINE     DS    0C
OLVOLSER DS    CL6
         DS    2C                                                  U004
OLDEVADR DS    CL3
         DS    3C                                                  U004
OLUALLOC DS    CL3                                                 U004
         DS    C                                                   U004
OLUOPEN  DS    CL3                                                 U004
         DS    3C                                                  U011
OLMSG    DS    0C                       MESSAGES GO HERE           U006
OLFCYL   DS    CL3
         DS    2C                                                  U018
OLFTRK   DS    CL4
         DS    4C                                                  U015
OLFNUM   DS    CL4                                                 U015
         DS    4C                                                  U018
OLFDSCB  DS    CL4
         DS    5C                                                  U004
OLCCYL   DS    CL3
         DS    C
OLCTRK   DS    CL3
         DS    3C                                                  U011
OLIXVTOC DS    CL2                                                 U011
OLMSGL   EQU   *-OLMSG                  MAX LENGTH AVAIL FOR MSGS  U006
         DS    2C                                                  U011
OLSTAT1  DS    CL4                                                 U004
         DS    C'/'                                                U004
OLSTAT2  DS    CL5                                                 U004
LINEL    EQU   *-LINE
         SPACE 2
FLAG     DS    X                                                   U001
F@HDRS   EQU   X'80'                    HEADERS HAVE BEEN PUT      U004
F@STAT   EQU   X'40'                    WANTS STATUS ALSO          U004
F@ALL    EQU   X'20'                    WANTS ALL VOLUMES          U007
F@NOHEAD EQU   X'10'                    DOESN'T WANT HEADER LINES  U007
F@VOLFND EQU   X'08'                    FOUND A VOL FOR THIS INDEX U012
F@TOTALS EQU   X'04'                    WANT TOTALS LINE           U018
         SPACE 2
CLEAR    EQU   *                        START OF AREA TO ZERO      U018
NUMVOLS  DS    H                        NUMBER OF VOLUMES FOUND    U018
TOTCYL   DS    PL4                      TOTAL NUMBER OF FREE CYL   U018
TOTTRK   DS    PL4                      TOTAL NUMBER OF FREE TRK   U018
TOTNUM   DS    PL4                      TOTAL NUMBER OF FREE EXT   U018
TOTDSCB  DS    F                        TOTAL                      U018
MAXCYL   DS    CL4                      MAX CONTIG CYL             U018
MAXTRK   DS    CL4                      MAX CONTIG TRK             U018
CLEARL   EQU   *-CLEAR                  LENGTH TO ZERO             U018
PWORK    DS    PL4                      WORK AREA FOR PACK         U018
DBLW     DS    4D
XHDR1RDW DS    2H                       RDW FOR PUTLINE            U012
XHDR1    DS    CL(HEADERL2)                                        U011
XHDR2RDW DS    2H                       RDW FOR PUTLINE            U012
XHDR2    DS    CL(HEADERL2)                                        U011
CAM      DS    0D
CAML     DS    4F                       OBTAIN LIST
CAMV     DS    2F                       VOL NAME
         DS    0D
CAMW     DS    140X                     CAMLST WORK AREA
TOKEN    DS    F                        TOKEN FOR GQSCAN           U017
RIBLS    DS    0F                       RIB LENGTHS...             U017
RIBL     DS    H                        LENGTH OF RIB              U017
RIBEL    DS    H                        LENGTH OF RIBE             U017
GQLIST   GQSCAN  MF=L                                              U017
*OPENMFL DS    A                                                   U017
*EXLST   DS    A                                                   U017
*JFCB    DS    XL176                                               U017
*DCB     DS    XL(DCBXL)                                           U017
AREA     DS    CL4096                   GQSCAN ANSWER AREA         U017
AREAL    EQU   *-AREA                                              U017
WORKLEN  EQU   *-WORKD                                             U017
         TITLE 'LISTVO    -- DSECTS'
CVT      DSECT
         CVT
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         IKJPSCB
         EJECT
         IKJECT
         EJECT
         IHAQCB
         EJECT
         IHAQEL
         EJECT
         ISGRIB  ,                                                 U017
         SPACE 3
         PRINT NOGEN                                               U017
         SPACE 2
         IHAASVT  ,                                                U017
         SPACE 2
         IHAASCB  ,                                                U017
         SPACE 2
         END
