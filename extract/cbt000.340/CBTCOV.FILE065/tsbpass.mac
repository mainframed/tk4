TSBPASS  TITLE '-----  CHANGE LOGON PASSWORD IN TSB  -----'
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  TSBPASS                                            *
*                                                                     *
*   AUTHOR:        LEONARD D. WOREN                                   *
*                                                                     *
*   DATE:          JULY 22, 1980                                      *
*                                                                     *
*   INSTALLATION:  GIBRALTAR SAVINGS AND LOAN                         *
*                                                                     *
*   PURPOSE:       CHANGE LOGON PASSWORD IN TSB, FOR DATASET ACCESS   *
*                  PURPOSES.                                          *
*                                                                     *
*   UPDATES:                                                          *
*      02/27/82 LDW   CHANGE ENTR TO OSENTER                          *
*                     MAKE RE-ENTRANT                                 *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
TSBPASS  OSENTER  EXIT=LEAVE,RC=0,GETMAIN=(WORKLEN,WORKD)
         SPACE 2
         L     R3,X'220'                -> ASCB
         L     R3,60(,R3)               -> TSB (IN PROTECTED CORE)
         LTR   R3,R3                    ANY?
         BZ    LEAVE                    NO - BATCH JOB, SO SKIP.
         USING TSB,R3
         SPACE 1
ASK      TPUTX PROMPT,PROMPTL,ASIS      ASK FOR NEW ONE
         SPACE 1
         LA    R1,REPLY                 -> ANSWER AREA
         LA    R0,L'REPLY               LENGTH
         ICM   R1,B'1000',=X'80'        SET TGET FLAG
         TGET  (1),(0),R                GET ANSWER
         SPACE 1
         LTR   R1,R1                    ANY ANSWER?
         BNP   ASK                      NO - ASK AGAIN
         CH    R1,=H'8'                 TOO LONG?
         BNH   LENGTHOK                 NO - CONTINUE
         SPACE 1
         TPUTX 'TOO LONG - TRY AGAIN'
         B     ASK
         SPACE 2
LENGTHOK OC    REPLY(8),=CL8' '         UPPER CASE IT
         SPACE 1
         MODESET KEY=ZERO               SET PROTECT KEY = 0
         SPACE 1
         MVC   TSBPSWD(8),REPLY         MOVE IN NEW PASSWORD
         DROP  R3
         SPACE 1
         MODESET KEY=NZERO              RESET PROTECT KEY TO NORMAL
         SPACE 1
         B     LEAVE                    ALL DONE
         SPACE 3
PROMPT   DC    C'ENTER NEW PASSWORD FOR TSB:'
         DC    X'24'                    BYPASS CHARACTER
PROMPTL  EQU   *-PROMPT                 LENGTH OF PROMPT
         SPACE 3
         LTORG ,
         SPACE 3
WORKD    DSECT                          RESUME
         SPACE 1
REPLY    DC    CL80' '
         SPACE 1
WORKLEN  EQU   *-WORKD
         EJECT
         IKJTSB  LIST=YES,EXT=YES
         SPACE 2
         END
