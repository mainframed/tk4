CLOCKX   TITLE 'SUBROUTINE WHICH OBTAINS TIME OF DAY'
*
*      THIS PROGRAM OBTAINS THE TIME OF DAY BY ISSUING THE TIME SVC.
* THE CALLING SEQUENCE FOR "CLOCKX" IS AS FOLLOWS
*           1. HOURS (4-BYTE BINARY)
*           2. MINUTES (4-BYTE BINARY)
*           3. SECONDS (4-BYTE BINARY)
*
*      THE "CLOCKX" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
CLOCKX   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKLEN,0)
         REGISTER                  REGISTER EQUATES
         USING WORKAREA,R13        ADDRESS WORK AREA DSECT
         LM    R4,R6,0(R2)         LOAD PARAMETER POINTERS
         TIME  DEC                 GET TIME AND DATE
         ST    R0,WORK1            SAVE TIME IN WORK AREA
         MVC   WORK2,=X'0000000F'  INITIALIZE PACKED DECIMAL FIELD
         MVO   WORK2+2(2),WORK1+0(1)
         ZAP   WORK0,WORK2         GET HOURS INTO PACKED DOUBLEWORD
         CVB   R7,WORK0            CONVERT HOURS INTO BINARY
         ST    R7,0(,R4)           STORE HOURS IN PARAMETER AREA
         MVO   WORK2+2(2),WORK1+1(1)
         ZAP   WORK0,WORK2         GET MINUTES INTO PACKED DOUBLEWORD
         CVB   R7,WORK0            CONVERT MINUTES INTO BINARY
         ST    R7,0(,R5)           STORE MINUTES IN PARAMETER AREA
         MVO   WORK2+2(2),WORK1+2(1)
         ZAP   WORK0,WORK2         GET SECONDS INTO PACKED DOUBLEWORD
         CVB   R7,WORK0            CONVERT SECONDS INTO BINARY
         ST    R7,0(,R6)           STORE SECONDS IN PARAMETER AREA
         LA    R15,0               CLEAR RETURN CODE
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(WORKLEN,0)
         EJECT
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
WORKSTRT EQU   *
         DS    18A                 REGISTER SAVE AREA
WORK0    DS    D
WORK1    DS    F
WORK2    DS    F
WORKEND  EQU   *
WORKLEN  EQU   WORKEND-WORKSTRT    COMPUTE LENGTH OF WORK AREA
         END
