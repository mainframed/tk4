ISFUSER  TITLE 'AUTHORIZATION EXIT FOR SDSF'
***********************************************************************
*                                                                     *
*                 SPOOL DISPLAY AND SEARCH FACILITY                   *
*                  5798-DGN COPYRIGHT IBM CORP 1981                   *
*             LICENSED MATERIAL - PROGRAM PROPERTY OF IBM             *
*        REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER G120-2083        *
*                                                                     *
*              THE AUTHORIZATION CHECK EXIT IS ENTERED KEY ONE AND    *
*        SUPERVISOR IF THE SDSF SVC IF AVAILABLE.  IF NOT AVAILABLE,  *
*        IT IS ENTERED KEY 8 PROBLEM STATE.                           *
*                                                                     *
*              THIS IS THE AUTHORIZATION CHECK USER EXIT FOR SDSF.    *
*        ON ENTRY REGISTER 0 CONTAINS THE FOLLOWING:                  *
*                                                                     *
*              0 - FOR THE INITIALIZATION CALL                        *
*              4 - FOR A COMMAND CHECK CALL                           *
*              8 - FOR A DISPLAY SYSOUT CALL                          *
*             12 - FOR THE TERMINATION CALL                           *
*                                                                     *
*              ON ENTRY REGISTER ONE CONTAINS A PARAMETER LIST        *
*        WHICH IS MAPPED BY THE USERPARM DSECT CONTAINED IN THIS      *
*        MEMBER.  A RETURN CODE OF OTHER THAN ZERO WILL CANCEL THE    *
*        FUNCTION. NOTE THAT IF THE SVC IS AVAILABLE THE PARAMETER    *
*        LIST IS IN KEY ONE STORAGE.                                  *
*               FOR AN INITIALIZATION CALL IF THE SDSFATH BYTE        *
*        IS ZERO AT THE TIME OF CALL, THE USER WILL NOT BE ALLOWED    *
*        TO USE SDSF UNLESS IT IS SET TO NON-ZERO BY EXIT.            *
*               FOR BOTH THE COMMAND CHECK AND DISPLAY SYSOUT CALL    *
*        IF THE AUTHRETC WORD IS NON-ZERO AT TIME OF CALL, THEN       *
*        THE CALL WILL BE FAILED UNLESS THE EXIT ZEROS THE WORD.      *
*               A WORD NAMED USERWORD HAS BEEN PROVIDED FOR THE       *
*        USER TO SAVE THE ADDRESS OF A WORK AREA FOR INFORMATION      *
*        NEEDED FROM ONE CALL TO THE NEXT.                            *
*               THE TERMINATION CALL IS PROVIDED FOR THE USER EXIT    *
*        TO CLEAN UP ANY WORK AREAS GOTTEN DURING PRIOR CALLS.        *
*                                                                     *
***********************************************************************
         TITLE 'MAINLINE ROUTINE'
         PRINT OFF
         COPY  ISFEQU
         PRINT ON
ISFUSER  CSECT
         ISFSAVE
         BALR  R12,0               BASE REGISTER
         USING *,R12
         LR    R11,R1              USER EXIT PARM
         USING USERPARM,R11
         LR    R10,R0              CALL TYPE
         B     CALLTYPE(R10)       GO TO PROPER CODE
CALLTYPE B     INITCK              INITIALIZATION
         B     CMDCK               COMMAND CHECK
         B     DSPCK               DISPLAY SYSOUT CHECK
         B     EXIT                EXIT PROCESSING
         TITLE 'INITIALIZATION USER CHECK SECTION'
INITCK   DS    0H                      INITIALIZATION USER CHECK
         GETMAIN R,LV=STORLNTH         GET SOME WORKING STORAGE
         LTR   R15,R15                 WAS THE GETMAIN SUCCESSFUL?
         BNZ   FAIL                    IF NOT, DISCONTINUE
         ST    R1,USERWORD             SAVE THE ADDRESS OF THE STORAGE
         L     R9,USERWORD             GET THE ADDRESS OF THE STORAGE
         USING USERSTOR,R9             MAP THE USER STORAGE
         ACFINCVT R7,NONE=NOACF2       GET THE ADDRESS OF THE ACF2 CVT
         SPKA  X'00'                   SET STORAGE PROTECT KEY '0'
         ST    R7,CVTADDR              SAVE IT FOR LATER USE
         USING ACCVT,R7                MAP THE ACF2 CVT
         MVI   ACGRSRC,X'00'           ZERO RESOURCE PARM BLOCK
         MVC   ACGRSRC(ACGRSLEN-1),ACGRSRC ZERO RESOURCE PARM BLOCK
         MVI   ACGFCN,4                SET FUNCTION CODE
         MVI   ACGSFCN,ACGSINP         SET SUB FUNCTION - INTERPRET
         LA    R1,MESSAGE              GET MESSAGE AREA ADDR
         ST    R1,ACGMSG               STORE INTO PARM BLOCK
CHKINIT  DS    0H
         MVC   ACGRTYPE,=CL4'RCMD'     RESOURCE TYPE
         MVI   ACGFLGS,ACGFNRES        DONT MAKE RULES RESIDENT
         MVI   ACGRNAME,C' '           PRE-BLANK FIELD
         MVC   ACGRNAME+1(L'ACGRNAME-1),ACGRNAME
         MVC   ACGRNAME(4),=CL4'SDSF'  MOVE IN INIT PARAMETER
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   FAIL                    NO, ERROR
         TITLE 'CHECK COMMAND LEVELS'
CMDLVL7  DS    0H
         MVC   ACGRTYPE,=CL4'RISF'     RESOURCE TYPE
         MVI   ACGRNAME,C' '           PRE-BLANK FIELD
         MVC   ACGRNAME+1(L'ACGRNAME-1),ACGRNAME
         MVC   ACGRNAME(7),=CL7'CMDLVL7' MOVE IN COMMAND LEVEL FOR TEST
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CMDLVL6                 NO, ERROR
         MVI   OPERLEV,7               SET COMMAND LEVEL
         B     FUNCHEK1                GO CHECK FOR FUNCTIONS
CMDLVL6  DS    0H
         MVC   ACGRNAME+6(1),=C'6'     MOVE IN COMMAND LEVEL FOR TEST
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CMDLVL5                 NO, ERROR
         MVI   OPERLEV,6               SET COMMAND LEVEL
         B     FUNCHEK1                GO CHECK FOR FUNCTIONS
CMDLVL5  DS    0H
         MVC   ACGRNAME+6(1),=C'5'     MOVE IN COMMAND LEVEL FOR TEST
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CMDLVL4                 NO, ERROR
         MVI   OPERLEV,5               SET COMMAND LEVEL
         B     FUNCHEK1                GO CHECK FOR FUNCTIONS
CMDLVL4  DS    0H
         MVC   ACGRNAME+6(1),=C'4'     MOVE IN COMMAND LEVEL FOR TEST
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CMDLVL3                 NO, ERROR
         MVI   OPERLEV,4               SET COMMAND LEVEL
         B     FUNCHEK1                GO CHECK FOR FUNCTIONS
CMDLVL3  DS    0H
         MVC   ACGRNAME+6(1),=C'3'     MOVE IN COMMAND LEVEL FOR TEST
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CMDLVL2                 NO, ERROR
         MVI   OPERLEV,3               SET COMMAND LEVEL
         B     FUNCHEK1                GO CHECK FOR FUNCTIONS
CMDLVL2  DS    0H
         MVC   ACGRNAME+6(1),=C'2'     MOVE IN COMMAND LEVEL FOR TEST
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CMDLVL1                 NO, ERROR
         MVI   OPERLEV,2               SET COMMAND LEVEL
         B     FUNCHEK1                GO CHECK FOR FUNCTIONS
CMDLVL1  DS    0H
         MVC   ACGRNAME+6(1),=C'1'     MOVE IN COMMAND LEVEL FOR TEST
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CMDLVL0                 NO, ERROR
         MVI   OPERLEV,1               SET COMMAND LEVEL
         B     FUNCHEK1                GO CHECK FOR FUNCTIONS
CMDLVL0  DS    0H
         MVI   OPERLEV,0               NO COMMANDS ALLOWED
         TITLE 'CHECK USER FUNCTION AUTHORIZATION'
FUNCHEK1 DS    0H
         MVI   SDSFATH,X'00'           BLANK OUT AUTHORIZATION FLAG 1
CSYSID   DS    0H
         MVC   ACGRNAME(8),=CL8'SYSID'
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CPREFIX                 NO, ERROR
         OI    SDSFATH,FSYSID          TURN ON SYSID FUNCTION FLAG
CPREFIX  DS    0H
         MVC   ACGRNAME(8),=CL8'PREFIX'
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CREMOTE                 NO, ERROR
         OI    SDSFATH,FPREFIX         TURN ON PREFIX FUNCTION FLAG
CREMOTE  DS    0H
         MVC   ACGRNAME(8),=CL8'REMOTE'
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CSYSLOG                 NO, ERROR
         OI    SDSFATH,FREMOTE         TURN ON REMOTE FUNCTION FLAG
CSYSLOG  DS    0H
         MVC   ACGRNAME(8),=CL8'SYSLOG'
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CACTIVE                 NO, ERROR
         OI    SDSFATH,FLOG            TURN ON SYSLOG FUNCTION FLAG
CACTIVE  DS    0H
         MVC   ACGRNAME(8),=CL8'ACTIVE'
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CINPUT                  NO, ERROR
         OI    SDSFATH,FDA             TURN ON ACTIVE FUNCTION FLAG
CINPUT   DS    0H
         MVC   ACGRNAME(8),=CL8'INPUT'
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   COUTPUT                 NO, ERROR
         OI    SDSFATH,FI              TURN ON INPUT FUNCTION FLAG
COUTPUT  DS    0H
         MVC   ACGRNAME(8),=CL8'OUTPUT'
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CHOLD                   NO, ERROR
         OI    SDSFATH,FO              TURN ON OUTPUT FUNCTION FLAG
CHOLD    DS    0H
         MVC   ACGRNAME(8),=CL8'HOLD'
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   FUNCHEK2                NO, ERROR
         OI    SDSFATH,FH              TURN ON HOLD FUNCTION FLAG
         TITLE 'CHECK USER FUNCTION AUTHORIZATION'
FUNCHEK2 DS    0H
         MVI   SDSFATH+1,X'00'         BLANK OUT AUTHORIZATION FLAG 2
CABENDC  DS    0H
         MVC   ACGRNAME(8),=CL8'ABEND'
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CACTION                 NO, ERROR
         OI    SDSFATH+1,FABENDC       TURN ON ABEND FUNCTION FLAG
CACTION  DS    0H
         MVC   ACGRNAME(8),=CL8'ACTION'
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CINPUTC                 NO, ERROR
         OI    SDSFATH+1,FACTION       TURN ON ACTION FUNCTION FLAG
CINPUTC  DS    0H
         MVC   ACGRNAME(8),=CL8'INPUTC'
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   CNODE                   NO, ERROR
         OI    SDSFATH+1,FINPUT        TURN ON INPUT FUNCTION FLAG
CNODE    DS    0H
         MVC   ACGRNAME(8),=CL8'NODE'
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   RETURN                  NO, ERROR
         OI    SDSFATH+1,FNODE         TURN ON NODE FUNCTION FLAG
         B     RETURN
         TITLE 'OPERATOR COMMAND CHECK SECTION'
CMDCK    DS    0H                      OPERATOR COMMAND CHECK
         L     R9,USERWORD             GET THE ADDRESS OF THE STORAGE
         USING USERSTOR,R9             MAP THE USER STORAGE
         CLC   SUSERID(4),UJOBNAME     COMPARE USERID AND JOBNAME
         BE    RETURN                  IF MATCH, ALLOW ACTION
         SPKA  X'00'                   SET STORAGE PROTECT KEY '0'
         L     R7,CVTADDR              RECOVER THE ACF2 CVT ADDRESS
         USING ACCVT,R7                MAP THE ACF2 CVT
         XC    ACGRSRC(ACGRSLEN),ACGRSRC  ZERO RESOURCE PARM BLOCK
         MVI   ACGFCN,4                SET FUNCTION CODE
         MVI   ACGSFCN,ACGSINP         SET SUB FUNCTION - INTERPRET
         LA    R1,MESSAGE              GET MESSAGE AREA ADDR
         ST    R1,ACGMSG               STORE INTO PARM BLOCK
         MVC   ACGRTYPE,=CL4'RISF'     RESOURCE TYPE
         MVI   ACGFLGS,ACGFNRES        DONT MAKE RULES RESIDENT
         MVI   ACGRNAME,C' '           PRE-BLANK FIELD
         MVC   ACGRNAME+1(L'ACGRNAME-1),ACGRNAME
         MVC   ACGRNAME(3),UJOBNAME    MOVE IN JOB PREFIX
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   FAIL                    NO, ERROR
         B     RETURN                  ELSE, END NORMALLY
         TITLE 'DISPLAY SYSOUT CHECK SECTION'
DSPCK    DS    0H                  DISPLAY SYSOUT CHECK
         L     R9,USERWORD             GET THE ADDRESS OF THE STORAGE
         USING USERSTOR,R9             MAP THE USER STORAGE
         CLC   SUSERID(4),UJOBNAME     COMPARE USERID AND JOBNAME
         BE    RETURN                  IF MATCH, ALLOW ACTION
         SPKA  X'00'                   SET STORAGE PROTECT KEY '0'
         L     R7,CVTADDR              RECOVER THE ACF2 CVT ADDRESS
         USING ACCVT,R7                MAP THE ACF2 CVT
         XC    ACGRSRC(ACGRSLEN),ACGRSRC  ZERO RESOURCE PARM BLOCK
         MVI   ACGFCN,4                SET FUNCTION CODE
         MVI   ACGSFCN,ACGSINP         SET SUB FUNCTION - INTERPRET
         LA    R1,MESSAGE              GET MESSAGE AREA ADDR
         ST    R1,ACGMSG               STORE INTO PARM BLOCK
         MVC   ACGRTYPE,=CL4'RISF'     RESOURCE TYPE
         MVI   ACGFLGS,ACGFNRES        DONT MAKE RULES RESIDENT
         MVI   ACGRNAME,C' '           PRE-BLANK FIELD
         MVC   ACGRNAME+1(L'ACGRNAME-1),ACGRNAME
         MVC   ACGRNAME(3),UJOBNAME    MOVE IN JOB PREFIX
         ACFSVC ACGRSRC,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   FAIL                    NO, ERROR
         B     RETURN                  ELSE, END NORMALLY
         TITLE 'EXIT CLEAN UP SECTION'
EXIT     DS    0H                      EXIT PROCESSING
         L     R1,USERWORD             GET THE ADDRESS OF THE STORAGE
         FREEMAIN R,LV=STORLNTH,A=(1),SP=0  FREE THE USER STORAGE
         B     RETURN
         TITLE 'ROUTINE TERMINATION'
NOACF2   DS    0H
         TPUT  NOACFMSG,80
         B     FAIL
         SPACE 5
RETURN   DS    0H
         SPKA  X'10'                   SET STORAGE PROTECT KEY '1'
         RETURN (14,12),RC=0           RETURN RETURN CODE 0
         SPACE 5
FAIL     DS    0H
         SPKA  X'10'                   SET STORAGE PROTECT KEY '1'
         RETURN (14,12),RC=4           RETURN RETURN CODE 4
         SPACE 5
         LTORG
         TITLE 'USER EXIT STORAGE MAP'
USERSTOR DSECT
CVTADDR  DS    A                       ACF2 CVT ADDRESS
         SPACE 1
MESSAGE  DS    0CL128                  MESSAGE AREA
MSGLEN   DC    H'0'                    MESSAGE LENGTH
         DC    H'0'                    HALFWORD OF ZEROS
MSGDATA  DC    CL124' '                ACF2 ERROR MESSAGE
         SPACE 1
NOACFMSG DC    CL80'ACF2 NOT ACTIVE OR IN ERROR, VALIDATION FAILED'
         SPACE 1
         ACGRSRC DSECT=NO              RESOURCE PARM BLOCK
STORLNTH EQU   *-USERSTOR
         TITLE 'MAPPING MACROS'
USERPARM DSECT                         USER EXIT PARM
         ISFUPRM
         EJECT
         PRINT NOGEN
         ACCVT
         ACUCB
         ACFASVT
         CVT   DSECT=YES
         IHAPSA
         IHAASCB
         IKJTCB DSECT=YES
         TITLE 'SOURCE CODE TERMINATION'
         END
