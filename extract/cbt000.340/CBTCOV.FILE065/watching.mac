         TITLE '***** WATCHING - WATCH THE MASTER CONSOLE BUFFER '
WATCHING CSECT
***********************************************************************
*                                                                     *
*     THIS PROGRAM IS DESIGNED TO WATCH THE MASTER CONSOLE BUFFER     *
*     FOR THE CHARACTER STRING SPECIFIED IN THE PARM FIELD OF THE     *
*     EXEC JCL STATEMENT ------>                                      *
*              ===>  PARM='TEXT-STRING'                               *
*                                                                     *
*     ONCE THE STRING HAS BEEN OBSERVED IN THE MASTER CONSOLE         *
*     BUFFER, THE FILE 'GOFORIT' IS OPENED AND THE COMMANDS IN        *
*     THE INPUT FILE ARE ISSUED.                                      *
*                                                                     *
*     I.E.;                                                           *
*     START CICSRDR,SYS=PROD                                          *
*                                                                     *
*     IF THE SPCIFICIFIED 'TEXT-STRING' IS NOT FOUND THE PROGRAM      *
*     WILL WAIT FOR .5 SECONDS AND THEN TRY AGAIN.  THIS PROCESS      *
*     IS REPEATED UNTIL THE 'TEXT-STRING' IS FOUND, CPU TIME IS       *
*     CONSUMED, OR THE JOB/TASK IS CANCELLED.                         *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        STANDARD REGISTER EQUATES                                    *
*                                                                     *
***********************************************************************
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
R00      EQU   00
R01      EQU   01
R02      EQU   02
R03      EQU   03
R04      EQU   04
R05      EQU   05
R06      EQU   06
R07      EQU   07
R08      EQU   08
R09      EQU   09
         SPACE 5
         STM   R14,R12,12(R13)     *****
         LR    R12,R15             * STANDARD
         USING WATCHING,R12        *    LINKAGE
         LA    R15,SAVEAREA        *     CONVENTIONS AND
         ST    R13,4(,R15)         *       SAVE AREA
         ST    R15,8(,R13)         *         CHAINING.
         LR    R13,R15             *****
         EJECT
***********************************************************************
*                                                                     *
*        PROGRAM INITIALLIZATION AND HOUSEKEEPING.                    *
*                                                                     *
***********************************************************************
         SPACE 2
INIT     DS    0H
         L     R10,0(,R1)     PICK UP PARAMETER ADDRESS
         CLC   0(2,R10),=H'0' IS THERE A PARAMETER?
         BE    RETURN         ...NO, THEN GET OUT
         LH    R11,0(,R10)    ...YES, PICK UP PARM LENGTH
         LA    R9,LMESSAGE    PICK UP BUFFER LENGTH
         SR    R9,R11         R9  = SCAN LENGTH
         BCTR  R11,0          R11 = ARGUMENT LENGTH -1
         LA    R10,2(,R10)    POINT TO TEXTSTRING
         SPACE 2
         OPEN  (GOFORIT,(INPUT))
         LA    R1,GOFORIT     SET ADDRESSABILITY TO DCB
         USING IHADCB,R1
         TM    DCBOFLGS,X'10' DID THE FILE OPEN?
         DROP  R1
         BNO   RETURN         ...NO, THEN GET OUT, NO MESSAGES
         SPACE 2
         MODESET KEY=ZERO,MODE=SUP
         SPACE 2
         SYSEVENT DONTSWAP
         SPACE 2
         EJECT
***********************************************************************
*                                                                     *
*        GET CROSS MEMORY SERVICES INFO TO LOOK AT 'CONSOLE' ASCB     *
*                                                                     *
***********************************************************************
         SPACE 2
         L     R2,548(R0)    --> OLD ASCB POINTER
         MVC   MONASID,36(R2) SAVE THIS ASID
         L     R1,16(R0)     --> CVT
         L     R1,556(R1)    --> ASVT
         LA    R1,524(R1)    --> PRIOR TO FIRST ASVT ENTRY
SCANASVT DS    0H
         LA    R1,4(R1)      --> NEXT ASVT ENTRY
         CLC   0(4,R1),=X'80000000'   IS THIS THE LAST ENTRY?
         BNE   SCANASVX
         ABEND 1,DUMP        OOPPPPSSSSSS!!!!!
SCANASVX DS    0H
         TM    0(R1),X'80'   IS THIS AVAILABLE?
         BO    SCANASVT      ...YES, THEN BYPASS IT
         L     R2,0(,R1)     --> ASCB ADDRESS
         ICM   R3,15,176(R2) --> LOGON JOBNAME
         BZ    SCANASVT      BYPASS IF *NONAME*
         CLC   0(8,R3),=C'CONSOLE ' IS THIS THE CONSOLE ADDRESS SPACE
         BNE   SCANASVT         ...NO, THEN BYPASS THIS ONE
         MVC   CONSASID,36(R2)  ...YES, THEN SAVE CONSOLE'S ASID
         EJECT
***********************************************************************
*                                                                     *
*        ESTABLISH CROSS MEMORY SERVICES AUTHORIZATION INDEX          *
*                                                                     *
***********************************************************************
         SPACE 2
         LA    R2,1
         AXSET AX=(R2)       SET AUTHORIZATION INDEX
         EJECT
***********************************************************************
*                                                                     *
*        GET MASTER CONSOLE  UCMID AND SET THE SECONDARY ASID.        *
*                                                                     *
***********************************************************************
         SPACE 2
MAINLINE DS    0H
         L     R2,16         --> CVT
         L     R2,100(R2)    --> UCM
         S     R2,=F'4'      --> PREFIX
         L     R3,0(R2)      --> MCS PREFIXTO UCM
         L     R3,0(R3)      --> MASTER CONS UCM ENTRY
         MVC   UCMID,26(R3)  SAVE MASTER CONSOLE ID
         L     R3,28(R3)     --> FIXED DCM
         L     R3,0(R3)      --> PAGEABLE DCM(THIS POINTS TO SCREEN)
         LA    R3,48(R3)     --> DCMASCRN
         LA    R1,4          NUMBER OF BYTES TO MOVE
         LA    R2,SCRNPTR    AREA TO CONTAIN CONSOLE'S BUFFER ADDRESS
         LA    R4,0          PROTECT KEY
         LH    R5,CONSASID   CONSOLE'S ASID (SECONDARY ADDRESS ID)
         SSAR  R5            SET SECONDARY ADDRESS SPACE
         EJECT
***********************************************************************
*                                                                     *
*        COPY MASTER CONSOLE BUFFER INTO PROGRAM BUFFER.              *
*                                                                     *
***********************************************************************
         SPACE 2
         MVCP  0(R1,R2),0(R3),R4 MOVE IN DCMASCRN
         LA    R1,LMESSAGE   LENGTH TO MOVE
         LA    R2,TARGET     TARGET AREA IN OUR BUFFER
         L     R3,SCRNPTR    AREA TO MOVE FROM
         S     R3,=F'6'      ADJUSTMENT FOR DEVICE CNTRL CHARS
         LA    R4,0          STORAGE KEY
MVCPLOOP DS    0H
         MVCP  0(R1,R2),0(R3),R4
         BZ    SCANBUFF      IF ALL IS MOVED, CONTINUE
         SL    R1,=F'256'    SINCE IT ONLY DOES 256 BYTES AT A TIME
         AL    R2,=F'256'    WE HAVE TO ADJUST ALL THE REGISTERS
         AL    R3,=F'256'    SO WE CAN RE-ISSUE THE MVCP
         B     MVCPLOOP      GOTO MOVE REMAINDER OF CONSOLE BUFFER
SCANBUFF DS    0H
         LH    R5,MONASID    SET SECONDARY ASID TO PRIMARY
         SSAR  R5
         EJECT
***********************************************************************
*                                                                     *
*        SCAN THE PROGRAM CONSOLE BUFFER FOR TEXT STRING.             *
*                                                                     *
***********************************************************************
         SPACE 2
         LA    R6,1           SCAN INCREAMENT
         LA    R7,TARGET(R9)  ---> LAST FIELD TO SCAN
         LA    R8,TARGET      ---> FIRST FIELD TO SCAN
SCANLOOP DS    0H
         EX    R11,COMPARG    IS THE 'TEXT-STRING' IN THE BUFFER
         BE    FOUNDARG       ...YES, THEN GOTOIT.
         BXLE  R8,R6,SCANLOOP ...NO, THE SCAN THROUGH BUFFER
         SPACE 2
         STIMER WAIT,DINTVL=TIMERVAL TEXT NOT IN BUFFER.  WAIT!!!!!
         B     MAINLINE       GOTO RECHECK MASTER CONSOLE BUFFER
COMPARG  CLC   0(0,R8),0(R10) COMPARE FIELD TO ARGUMENT
         SPACE 2
         EJECT
***********************************************************************
*                                                                     *
*        THE 'TEXT-STRING' WAS SUCCESSFULLY FOUND IN THE BUFFER.      *
*        READ THE 'GOFORIT' FILE AND BEGIN TO ISSUE THE COMMANDS.     *
*                                                                     *
***********************************************************************
         SPACE 2
FOUNDARG DS    0H
         GET   GOFORIT,COMMAND GET A CARD IMAGE
         LA    R1,CMD         POINT TO COMMAND
         LA    R0,0           INDICATE MASTER CONSOLE
         IC    R0,UCMID       MOVE IN CONSOLE ID
         SVC   34             ISSUE THE COMMAND
         B     FOUNDARG       GO CREATE A NEW SCREEN
         SPACE 5
CLOSEIT  DS    0H             END-OF-FILE ON GOFORIT
         CLOSE (GOFORIT)
         B     RETURN
         EJECT
RETURN   DS    0H
         MODESET KEY=NZERO,MODE=PROB
         SPACE 2
RETURNX  DS    0H
         L     R13,4(,R13)    RESTORE REGISTERS AND RETURN
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         XR    R15,R15
         BR    R14            RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*        PROGRAM CONSTANTS                                            *
*                                                                     *
***********************************************************************
         SPACE 2
         DS    0D
SAVEAREA DC    18F'0'
TIMERVAL DC    CL8'00000010'
SCRNPTR  DC    F'0'
CONSASID DC    H'0'
MONASID  DC    H'0'
         SPACE
CMD      DC    H'80',H'0'
COMMAND  DC    CL80' '
UCMID    DC    X'00'                 CONSOLE ID
         SPACE 2
         LTORG
         EJECT
GOFORIT  DCB   DSORG=PS,MACRF=(GM),EODAD=CLOSEIT,DDNAME=GOFORIT,       *
               LRECL=80
         EJECT
TARGET   DC    256XL1'00'     PLACE TO COPY MASTER CONSOLE BUFFER
         DC    256XL1'00'      512
         DC    256XL1'00'      768
         DC    256XL1'00'     1024
         DC    256XL1'00'     1280
         DC    256XL1'00'     1536
         DC    256XL1'00'     1792
         DC    256XL1'00'     2048
         DC    256XL1'00'     2304
LMESSAGE EQU   *-TARGET
         EJECT
         DCBD  DSORG=PS,DEVD=DA
         END   WATCHING
