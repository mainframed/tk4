         TITLE 'S W A P - SYSTEM WORKLOAD ANALYSIS PROGRAM - LEVEL 4.2'
***********************************************************************
*                                                                     *
*                              S W A P                                *
*                                                                     *
*                 SYSTEM  WORKLOAD  ANALYSIS  PROGRAM                 *
*                                                                     *
*                  MARTY SPRINZEN AND STEVE LANGLEY                   *
*                     SOUTHERN CALIFORNIA EDISON                      *
*                       ROSEMEAD, CALIF. 91770                        *
*                            213-572-3521                             *
*                                                                     *
*  COMMAND         DESCRIPTION                                        *
*                                                                     *
*     A     DISPLAY TSO, BATCH AND STARTED TASK JOBS                  *
*     B     DISPLAY BATCH AND STARTED TASK JOBS ONLY                  *
*     E     EXIT FROM SWAP                                            *
*     G     THIS MODE WILL DISPLAY GENERAL ADDRESS SPACE INFO.        *
*           (CPU, PROCSTEP, STEPNAME, M/S/L, TRS. COUNT, TCBS)        *
*     I     THIS MODE WILL DISPLAY ONLY THOSE MEMORIES WHICH ARE      *
*           EITHER SWAPPED IN AND RUNNING OR SWAPPED OUT BUT READY    *
*           TO RUN.                                                   *
*     L     ATTEMPT TO XCTL TO OPERATOR SPY PROGRAM                   *
*     O     THIS MODE WILL DISPLAY ALL ACTIVE MEMORIES IN THE SYSTEM, *
*           REGARDLESS OF LOCATION.                                   *
*     S     THIS MODE WILL DISPLAY SRM INFO. (MEM,WSS,DP,DQ,G,P,D,RM, *
*           WR,RAT,SRV,I/O)                                           *
*     T     DISPLAY TSO JOBS ONLY                                     *
*     DXX   SET DELAY TIME TO XX TENTHS OF A SECOND                   *
*     WXX   ENTER AUTOMATIC REFRESH MODE. SWAP WILL AUTOMATICALLY     *
*     W     REFRESH AND UPDATE THE SCREEN ONCE A SECOND FOR XX        *
*           SECONDS. IF XX IS OMITTED, A DEFAULT OF 30 SECONDS IS     *
*           ASSUMED. IF XX = 0, THE TIMER WILL COUNT UP INSTEAD OF    *
*           DOWN, AND WILL COUNT UNTIL 'INTERRUPT' IS PRESSED.        *
*     ?     DISPLAY SHORT HELP FOR SWAP COMMANDS AND OUTPUT           *
*                                                                     *
*  GLOSSARY OF TERMS FOR OUTPUT DISPLAY:                              *
*                                                                     *
*    HH:MM:SS  TRANSACTION TIME                                       *
*    R         REASON CODE FOR SWAP OUT FROM OUCBEFL.                 *
*              THE FOLLOWING ARE THE REASON CODES DISPLAYED:          *
*                   NOT SWAPPED - IN CORE                             *
*                O   SWAP CODE 1 : TERMINAL OUTPUT WAIT               *
*                I   SWAP CODE 2 : TERMINAL INPUT WAIT                *
*                W   SWAP CODE 3 : LONG WAIT                          *
*                A   SWAP CODE 4 : AUXILIARY STORAGE SHORTAGE         *
*                R   SWAP CODE 5 : REAL STORAGE SHORTAGE              *
*                V   SWAP CODE 6 : MS0 DETECTED WAIT                  *
*                S   SWAP CODE 7 : REQSWAP SYSEVENT ISSUED            *
*                E   SWAP CODE 8 : ENQHOLD EXCHG BY SWAP ANALYSIS     *
*                X   SWAP CODE 9 : EXCHG RECOMMENDED BY SWAP ANALYSIS *
*                $   SWAP CODE A : UNILATERAL SWAPOUT                 *
*                ?   NON OF ABOVE: PROBABLE ERROR OR SRM MODIFICATION *
*    L         CURRENT LOCATION OF THIS MEMORY:                       *
*                I  SWAPPED IN AND ELIGIBLE TO RUN.                   *
*                O  SWAPPED OUT BUT READY TO RUN.                     *
*                W  SWAPPED OUT AND NOT READY TO RUN.                 *
*                $  SWAPPED IN AND V=R OR NON-SWAPPABLE STATUS.       *
*                ?  TRANSITIONING BETWEEN STATES.                     *
*    MEM       CURRENT AMOUNT OF MEMORY ALLOCATED TO THIS MEMORY.     *
*    WSS       SRM'S VIEW OF THE WORKING SET SIZE FOR THIS MEMORY.    *
*    DP        DISPATCHING PRIORITY OF MEMORY (IN HEX).               *
*    DQ        RELATIVE POSITION OF MEMORY ON DISPATCHING QUEUE.      *
*    PG        PERFORMANCE GROUP                                      *
*    P         PERFORMANCE GROUP PERIOD                               *
*    D         DOMAIN                                                 *
*    RM        RESOURCE MANAGER RECOMMENDATION                        *
*    WR        WORK LOAD MANAGER RECOMMENDATION                       *
*    SU/S      SERVICE RATE (S.U.'S PER SECOND)                       *
*    S.U.      SERVICE UNITS (IN INTERVAL IF MODE=S )                 *
*    I/O       EXCP'S IN INTERVAL (MAX=65K)                           *
*    CPU       TASK CPU TIME                                          *
*    TCPU      THE TOTAL CPU TIME USED BY THIS MEMORY (TASK+SRB).     *
*    TRS       SERVICE ACCUMULATED IN THE TRANSACTION.                *
*    SC        NUMBER OF TIMES THE MEMORY HAS BEEN SWAPPED            *
*    MSL       MOUNT/START/LOGON INITIATED TASK                       *
*    TCB       NUMBER OF ACTIVE TCBS                                  *
*    PROCNAME  PROCSTEPNAME FOR THIS STEP                             *
*    STEPNAME  STEPNAME                                               *
*    TSLS      TIME SINCE LAST SWAP ACTION IN SECONDS                 *
*                                                                     *
*     SWAP REQUIRES SYS1.AMODGEN AS A MACLIB                          *
*                                                                     *
*  REGISTERS USED:                                                    *
*    R1 - BUFFER ADDRESS         R6 - PTR TO OUCB                     *
*    R2 - PTR TO ASVT ENTRY      R7 - R11  WORKING REGS               *
*    R3 - COUNT OF ASCBS         R12 - BASE REGISTER                  *
*    R4 - PTR TO OUXB                                                 *
*    R5 - PTR TO ASCB                                                 *
*                                                                     *
***********************************************************************
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         LCLC  &DATE                                        *HMD 07/81*
&DATE    SETC  '&SYSDATE'(1,3).'&SYSDATE'(7,2).')'          *HMD 07/81*
SWAP     CSECT
         B     100(R15)           BRANCH AROUND SAVE AREAS
         DC    CL9'SWAP'          IDENTIFIER
         DC    CL9'&SYSDATE'
         DC    CL6'&SYSTIME'
SAVE     DC    18F'0'             SAVE AREA
         STM   R14,R12,12(R13)    SAVE REGISTERS
         LR    R12,R15            R12 = ADDR OF ENTRY POINT
         USING SWAP,R12            ADDRESABILITY TO CSECT
         LA    R11,SAVE           R11 = ADDR OF OUR SAVE AREA
         ST    R13,SAVE+4         SAVE POINTER TO CALLERS SAVE AREA
         ST    R11,8(R13)         SAVE PTR TO OUR SAVE AREA IN CALLER'S
         LR    R13,R11            R13 = ADDR OF OUR SAVE AREA
         LA    3,STAXLIST
         STAX  ATTNEXIT,MF=(E,(3))     ATTN EXIT TRAP
         GTSIZE
         LTR   R0,R0
         BZ    HARDCOPY
         LR    R2,R0              R2 = NUMBER OF LINES PER SCREEN
         SH    R2,=H'2'           R2 = R2 - 2
         MH    R2,=H'81'          R2 = NUMBER OF BYTES IN SCREEN BUFR
         LA    R3,BUFFER          R3 = ADDR OF START OF BUFFER
         LA    R2,0(R2,R3)        R2 = ADDR OF END OF BUFFER
         ST    R2,ADBUF           SAVE R2
         STFSMODE ON,INITIAL=YES  TURN ON FULL SCREEN MODE FOR VTAM
         B     BOTHTYPE
HARDCOPY STSIZE SIZE=80           SET LINE SIZE TO 80
         MVI   CRTFLAG,X'00'      WE ARE USING A HARDCOPY
         MVC   CLR(26),BLANKS     ZAP OUT CNTRL CHARS
         MVC   CMDCTRL1(2),BLANKS
BOTHTYPE MVC   HEADINGA(80),HEADING2   LOAD GENERAL HEADING INITIALLY.
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                        LOCATE CVT AND ASVT POINTERS                 *
*                                                                     *
***********************************************************************
FINDCVT  L     R2,CVTPTR          CVT POINTER
         USING CVT,R2
         L     R2,CVTASVT         ASVT POINTER
         DROP  R2
         USING ASVT,R2
         L     R3,ASVTMAXU        MAXIMUM NUMBER OF ENTRIES
         LA    R2,ASVTFRST        FIRST ENTRY MINUS 4
         ST    R2,FRSTASVT        SAVE IT FOR LATER         *HMD 07/81*
         LA    R1,BUFFER          LOAD ADDRESS OF OUTPUT BUFFER.
         ST    R2,ASCBADDR        SAVE ACSB ADDRESS
         CLC   JOBMASK(8),BLANKS  IS THE JOBMASK BLANK?
         BNE   ASCBLOOP           NO, SO DON'T SET PAGE TO 0
         MVI   PAGE,C'0'          RESET PAGE TO PAGE 0
         EJECT
***********************************************************************
*                                                                     *
*            MAJOR LOOP (THROUGH ALL ASCB'S)                          *
*                                                                     *
***********************************************************************
ASCBLOOP L     R2,ASCBADDR        R2 = ADDR OF LAST ASCB ENTRY
         LA    R2,4(R2)           INCREMENT TO NEXT ASCB ENTRY
         ST    R2,ASCBADDR        SAVE ASCB ADDR
         ICM   R5,B'1111',0(R2)   ASCB POINTER
         USING ASCB,R5
         BM    NOGOOD             BRANCH IF BAD PTR
         TM    102(R5),X'04'      TEST FOR SWAPPED OUT
INOROUT  BC    1,NOGOOD           BRANCH IF OUT AND NOT READY TO RUN
*  NOTE: DEFAULT IS NON-TSO MEMORIES THAT ARE 'IN'          *HMD 07/81*
         L     R6,ASCBOUCB        OUCB POINTER
         USING OUCB,R6
         L     R4,ASCBOUXB        OUXB POINTER
         USING OUXB,R4
         TM    OUCBYFL,OUCBLOG    LOGON CREATED USER?
TSOORNO  BC    1,NOGOOD           BRANCH IF BATCH ONLY
BATCHORN BC    0,NOGOOD           BRANCH IF TSO ONLY
         MVI   LINE+2,C' '        CLEAR OUTPUT LINE TO BLANKS.
         MVC   LINE+3(77),LINE+2
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                              JOBNAME                                *
*                                                                     *
***********************************************************************
         MVC   JOB(8),START       INITIALIZE 'STARTING' JOBNAME
         ICM   R7,B'1111',ASCBJBNI JOBNAME POINTER
         BZ    NOTJOB             JOBNAME NOT AVAILABLE
         MVC   JOB,0(R7)          MOVE IN JOBNAME
         B     CONT1
NOTJOB   ICM   R7,B'1111',ASCBJBNS S/M/L POINTER
         BZ    CONT1              S/M/L NOT AVAILABLE
         MVC   JOB,0(R7)          MOVE IN JOBNAME FOR S/M/L
CONT1    CLC   JOBMASK(8),BLANKS  IS THE JOB MASK ALL BLANKS?
         BE    CONT1A             YES, SO GO ON
         LA    R8,JOBMASK         R8 = ADDR OF JOBMASK
         LA    R7,JOBMASK+7       R7 = ADDR OF LAST CHAR IN MASK
MASKLOOP CLI   0(R8),C' '         IS THE CHAR A BLANK?
         BE    GOTLEN             YES
         CR    R8,R7              IS R8 = R7?
         BE    GOTLEN             YES - END OF MASK
         LA    R8,1(R8)           POINT TO NEXT CHAR
         B     MASKLOOP           CHECK NEXT CHAR
GOTLEN   LA    R7,JOBMASK+1       R7 = ADDR OF FIRST CHAR IN MASK + 1
         SR    R8,R7              R8 = LENGTH OF MASK - 1
         EX    R8,VARCLC1         IS THE JOB MASK = JOBNAME?
         BNE   NOGOOD             NO, SO FLUSH THIS ASCB
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                             SWAP COUNT                              *
*                                                                     *
***********************************************************************
CONT1A   LH    R7,OUCBSWC         R7 = SWAP COUNT
         CVD   R7,WORK            CONVERT IT TO DECIMAL.
         MVO   WORK,WORK
         MVI   WORK+7,X'0C'
         MVC   $UCBSWC(2),PTRN1+5
         ED    $UCBSWC-1(3),WORK+6
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                          SWAP REASON CODE - OUCBSRC                 *
*                                                                     *
***********************************************************************
SWAPCODE TM    OUCBQFL,OUCBOUT    ARE WE SWAPPED OUT?
         BZ    FORGET             NO, SWAP CODE MEANS NOTHING
         CLI   OUCBSRC,X'0A'      CHECK FOR VALID SWAP REASON
         BH    BADCODE            NO.
         CLI   OUCBSRC,X'00'
         BE    BADCODE            NO.
         SR    R8,R8
         IC    R8,OUCBSRC         USE CODE NUMBER AS OFFSET INTO TABLE
         SH    R8,=H'1'           DECREASE BY 1
         LA    R7,CODETABL
         LA    R7,0(R8,R7)        LOAD ADDR OF CORRECT ALPHA CODE
         MVC   STATUS(1),0(R7)    MOVE CODE TO OUTPUT
         B     FORGET
BADCODE  MVI   STATUS,C'?'        ERROR IN CODE OR PROGRAM LOGIC
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                    ADDRESS SPACE QUEUE LOCATION                     *
*                                                                     *
***********************************************************************
FORGET   MVI   LOC,C'I'           ASSUME ADDRESS SPACE ON 'IN' QUEUE
         MVI   LINE+1,X'E8'       ATTR BYTE - PROTECTED, HI INTENSITY
         TM    OUCBSFL,OUCBNSW    ADDRESS SPACE - NON-SWAP
         BZ    LOGIC              NO                        *HMD 09/81*
         MVI   LOC,C'$'           MOVE IN NON-SWAP INDICATION.
         B     DONE
LOGIC    TM    OUCBQFL,OUCBLSW    LOGICALLY SWAPPED?        *HMD 09/81*
         BZ    WAITQ              NO, CHECK WAIT QUEUE      *HMD 09/81*
         MVI   LOC,C'L'           INDICATE LOGICAL SWAP     *HMD 09/81*
         MVI   LINE+1,X'60'       ATTR BYTE - LOW INTENS.   *HMD 09/81*
         B     DONE               CONTINUE                  *HMD 09/81*
WAITQ    TM    OUCBQFL,OUCBOFF    'WAIT' QUEUE
         BZ    OUTQ               NO
         MVI   LOC,C'W'           'WAIT' QUEUE INDICATION
         MVI   LINE+1,X'60'       ATTR BYTE - LOW INTENSITY
         B     DONE
OUTQ     TM    OUCBQFL,OUCBOUT    'OUT' QUEUE
         BZ    TRANS              NO
         MVI   LOC,C'O'           'OUT' QUEUE INDICATION
         MVI   LINE+1,X'60'       ATTR BYTE - LOW INTENSITY
         B     DONE
TRANS    TM    OUCBQFL,OUCBGOO+OUCBGOI+OUCBGOB    TRANSITIONING' STATUS
         BZ    DONE               NO
         MVI   LOC,C'?'           'TRANSITIONING' INDICATION
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                    TOTAL CPU TIME (TASK + SRB)                      *
*                                                                     *
***********************************************************************
DONE     LM    R8,R9,ASCBEJST     EJST (CPU TIME - TASK TYPE)
         SRDL  R8,12              CONVERT TO MICROSEC
         LM    R10,R11,ASCBSRBT   SRB TIME
         SRDL  R10,12             CONVERT TO MICROSEC
         AR    R9,R11             TOTAL (TASK + SRB) CPU TIME
         BNO   NOOVFLO            BRANCH IF NO OVERFLOW.
         A     R8,=F'1'           OTHERWISE ADD ONE TO HIGH ORDER.
NOOVFLO  AR    R8,R10             HIGH ORDER.
         D     R8,=F'100000'      FOR DIVIDE.               *HMD 07/81*
         CVD   R9,WORK            CONVERT TO DECIMAL.       *HMD 07/81*
         MVC   TCPU(6),PTRN2                                *HMD 07/81*
         ED    TCPU-1(7),WORK+5                             *HMD 07/81*
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                 FRAMES ALLOCATED TO ADDRESS SPACE                   *
*                                                                     *
***********************************************************************
         LH    R7,ASCBFMCT        ALLOCATED PAGE FRAME COUNT
         CVD   R7,WORK
         MVC   MEM(3),PTRN1+4
         ED    MEM-1(4),WORK+6
         EJECT
***********************************************************************
*                                                                     *
*     'GENERAL' OR 'SYSTEM' MODE?                                     *
*                                                                     *
***********************************************************************
         CLI   SRMSW,X'00'
         BE    GENERAL
***********************************************************************
*  SRM MODE:                                                          *
***********************************************************************
*                                                                     *
*                     WORKING SET SIZE AT SWAPIN                      *
*                                                                     *
***********************************************************************
         LH    R7,OUCBWSS         SWAPIN FRAME COUNT
         CVD   R7,WORK
         MVC   $UCBWSS(3),PTRN1+4
         ED    $UCBWSS-1(4),WORK+6
***********************************************************************
*                                                                     *
*                        DISPATCHING PRIORITY                         *
*                                                                     *
***********************************************************************
         XR    R7,R7              R7 = 0
         IC    R7,ASCBDP          R7 = DISPATCHING PRIORITY
         SRL   R7,4               CONVERT FIRST HEX CHARACTER
         IC    R8,HEX(R7)
         STC   R8,DP
         IC    R7,ASCBDP          R7 = DISPATCHING PRIORITY
         N     R7,=F'15'          CONVERT SECOND HEX CHARACTER
         IC    R8,HEX(R7)
         STC   R8,DP+1
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                   POSITION ON DISPATCHING QUEUE                     *
*                                                                     *
***********************************************************************
         CLI   LOC,C'O'           MEMORY SWAPPED OUT ?
         BE    NOPOS              IF YES, FORGET ABOUT POSITION.
         CLI   LOC,C'W'           WAIT STATUS ?
         BE    NOPOS              IF YES, FORGET ABOUT POSITION.
         LH    R7,ASCBSEQN        LOAD DP QUEUE POSITION.
         CVD   R7,WORK            PREPARE FOR CONVERSION TO EBCDIC.
         UNPK  DQ,WORK+6(2)       CONVERT TO DECIMAL.
         OI    DQ+1,X'F0'
***********************************************************************
*                                                                     *
*                  RESOURCE MANAGER RECOMMENDATION                    *
*                                                                     *
***********************************************************************
NOPOS    L     R7,OUCBCMRV        COMPOSITE WLM RECOMMENDATION VALUE
         LTR   R7,R7              HI-ORDER BIT IS ON ?
         BNM   RMROK              YES, WMR INVALID
         MVC   $UCBWMR,=C'**'     'NOT AVAIL' INDICATION
         MVI   $UCBWMS+4,C'-'     BLANK OUT THE SERVICE COUNTER.
         B     CONT3
RMROK    SRL   R7,8               DIVIDE BY 256
         CVD   R7,WORK            CONVERT TO DECIMAL .
         UNPK  $UCBRMR,WORK+6(2)
         OI    $UCBRMR+1,X'F0'
         CP    WORK+6(2),=PL2'0'  RMR GREATER THAN 99 ?
         BNE   CONT3              YES, DOUBLE ZERO FIELD.
         MVI   $UCBRMR,C' '       WMR LESS THAN 1.
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                         PERFORMANCE GROUP                           *
*                                                                     *
***********************************************************************
CONT3    XR    R7,R7              R7 = 0
         ICM   R7,B'0011',OUCBSPG R7 = PERFORMANCE GROUP    *HMD 09/81*
         CVD   R7,WORK            CONVERT TO DECIMAL.
         UNPK  $UCBPGN,WORK+6(2)                            *HMD 07/81*
         OI    $UCBPGN+1,X'F0'                              *HMD 07/81*
***********************************************************************
*                                                                     *
*                      PERFORMANCE GROUP PERIOD                       *
*                                                                     *
***********************************************************************
         XR    R7,R7         R7 = 0
         IC    R7,OUCBPGP         PERFORMANCE GROUP PERIOD OFFSET
         SH    R7,=H'12'
         SRL   R7,4                                         *HMD 07/81*
         LA    R7,1(R7)
         STC   R7,$UCBPGP
         OI    $UCBPGP,X'F0'
***********************************************************************
*                                                                     *
*                         DOMAIN                                      *
*                                                                     *
***********************************************************************
         XR    R7,R7         R7 = 0
         IC    R7,OUCBDMN         R7 = DOMAIN
         CVD   R7,WORK            CONVERT TO DECIMAL.
         UNPK  $UCBDOM,WORK+6(2)                            *HMD 07/81*
         OI    $UCBDOM+1,X'F0'                              *HMD 07/81*
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                      WLM RECOMMENDATION VALUE                       *
*                                                                     *
***********************************************************************
         L     R7,OUCBWMR         WORK LOAD MANAGER REC. VALUE
         LTR   R7,R7              HI-ORDER BIT IS ON ?
         BNM   WMROK              YES, WMR INVALID
         MVC   $UCBWMR,=C'NA'     'NOT AVAIL' INDICATION
         MVI   $UCBWMS+4,C'-'     BLANK OUT THE SERVICE COUNTER.
         B     NORECOM
WMROK    SRL   R7,8               DIVIDE BY 256
         CVD   R7,WORK            CONVERT TO DECIMAL .
         UNPK  $UCBWMR,WORK+6(2)
         OI    $UCBWMR+1,X'F0'
         CP    WORK+6(2),=PL2'0'  WMR GREATER THAN 99 ?
         BNE   NORECOM            YES, DOUBLE ZERO FIELD.
         MVI   $UCBWMR,C' '       WMR LESS THAN 1.
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                      IN-STORAGE SERVICE RATE                        *
*                                                                     *
***********************************************************************
NORECOM  L     R10,CVTPTR         R10 = ADDR OF CVT
         USING CVT,R10
         L     R10,CVTOPCTP       R10 = ADDR OF RMCT
         DROP  R10
         L     R10,X'7C'(R10)     R10 = CURRENT TOD
         S     R10,OUCBTMW        WLM INTERVAL START TIME
         BP    PLUSRATE
         MVC   SERATE(3),BLANKS
         B     NOSRV
PLUSRATE L     R8,OUCBWMS         INTERVAL SERVICE UNITS
         SRDL  R8,32              CALCULATE SEVICE RATE
         M     R8,=F'976'
         DR    R8,R10
         CVD   R9,WORK            CONVERT TO DECIMAL
         MVC   SERATE(3),PTRN1+4
         ED    SERATE-1(4),WORK+6
***********************************************************************
*                                                                     *
*                   INTERVAL SERVICE ACCUMULATION                     *
*                                                                     *
***********************************************************************
NOSRV    L     R7,OUCBWMS         SERVICE UNITS - XACTION
         CVD   R7,WORK            CONVERT TO DECIMAL.
         MVC   $UCBWMS(6),PTRN1+1
         MVO   WORK,WORK
         MVI   WORK+7,X'0C'
         ED    $UCBWMS-1(7),WORK+4
***********************************************************************
*                                                                     *
*               IO COUNT USED FOR RECOMMENDATION VALUE                *
*                                                                     *
***********************************************************************
         LH    R9,ASCBIOSM        R9 =  I/0 SERVICE MEASURE
         LH    R10,OUXBIOS        R10 = WLM BASE I/O MEASUREMENT
         SLR   R9,10              IO COUNT FOR THIS IN CORE INTERVAL
         N     R9,=X'0000FFFF'    CLEAR HIGH ORDER BYTES
         CVD   R9,WORK
         MVC   IOC(5),PTRN1+2
         ED    IOC-1(6),WORK+5
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*              TSLS - TIME SINCE LAST SWAP (SECONDS)                  *
*                                                                     *
***********************************************************************
         L     R8,CVTPTR          R8 = ADDR OF CVT
         USING CVT,R8
         L     R8,CVTOPCTP        R8 = ADDR OF RMCT
         DROP  R8
         L     R8,X'7C'(R8)       R8 = TIME-OF-DAY
         S     R8,OUCBTMS         SUBTRACT TIME OF LAST SWAP ACTION
         SRDL  R8,32              CONVERT TO H.MM.SS
         D     R8,=F'1024'        CONVERT TO SECONDS
         CVD   R9,WORK
         MVC   TSLS(5),PTRN1+2
         ED    TSLS-1(6),WORK+5
*
*
         B     LINEDONE
         EJECT
***********************************************************************
*                                                                     *
*                      ELAPSED TRANSACTION TIME                       *
*                                                                     *
***********************************************************************
GENERAL  L     R8,16              R8 = ADDR OF CVT
         USING CVT,R8
         L     R8,CVTOPCTP        R8 = ADDR OF RMCT
         DROP  R8
         L     R8,X'7C'(R8)       R8 = CURRENT TOD
         S     R8,OUCBTMO         SUBTRACT TRANSACTION START TIME
         SRDL  R8,32              CONVERT TO H.MM.SS
         D     R8,=F'1024'        CONVERT TO SECONDS
         ST    R9,TRANSSEC        STORE SECONDS FOR LATER
         SR    R8,R8              GET HOURS
         D     R8,=F'3600'
         CVD   R9,PACK
         UNPK  HH,PACK+6(R2)
         OI    HH+1,X'F0'
         MVI   HH+2,C':'
         SRDL  R8,32              GET MINUTES
         D     R8,=F'60'
         CVD   R9,PACK
         UNPK  MM(2),PACK+6(2)
         OI    MM+1,X'F0'
         MVI   MM+2,C':'          GET SECONDS
         CVD   R8,PACK
         UNPK  SS(2),PACK+6(2)
         OI    SS+1,X'F0'
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                          ADDRESS SPACE ID                           *
*                                                                     *
***********************************************************************
         L     R9,ASCBADDR        R9 = ADDR OF ENTRY IN ASVT TABLE
         S     R9,FRSTASVT        MINUS ADDR OF FIRST ENTRY
         SRL   R9,2               DIVIDE BY 4 = ASID
         LR    R7,R9              R7 = ASID
         SRL   R7,4               CONVERT FIRST HEX CHARACTER
         IC    R8,HEX(R7)
         STC   R8,ASID
         LR    R7,R9              R7 = ASID
         N     R7,=F'15'          CONVERT SECOND HEX CHARACTER
         IC    R8,HEX(R7)
         STC   R8,ASID+1
***********************************************************************
*                                                                     *
*                           TASK CPU TIME                             *
*                                                                     *
***********************************************************************
         LM    R8,R9,ASCBEJST     EJST (CPU TIME - TASK TYPE)
         SRDL  R8,12              CONVERT TO MICROSEC
         D     R8,=F'100000'      CONVERT TO SECONDS X 10-3 *HMD 07/81*
         CVD   R9,WORK            CONVERT TO DECIMAL.       *HMD 07/81*
         MVC   CPU(6),PTRN2                                 *HMD 07/81*
         ED    CPU-1(7),WORK+5                              *HMD 07/81*
***********************************************************************
*                                                                     *
*                            STEPNAME                                 *
*                                                                     *
***********************************************************************
         ICM   R7,B'1111',ASCBJBNI JOBNAME POINTER
         BZ    SML                JOBNAME NOT AVAILABLE
         MVC   PROCSTEP(8),24(R7) MOVE IN PROCSTEPNAME
         MVC   STEPNAME,56(R7)    MOVE IN STEPNAME
         B     CONT
SML      ICM   R7,B'1111',ASCBJBNS S/M/L POINTER
         BZ    CONT               S/M/L NOT AVAILABLE
         CLI   JOB,C'*'           IS THIS *MASTER* ?
         BE    TSO                YES, TREAT IT LIKE A USERID
         TM    OUCBYFL,OUCBSTT    IS THIS A STARTED TASK?
         BNO   TSO                NO, SO USE TSO STEPNAME
         B     CONT
TSO      MVC   STEPNAME,8(R7)     MOVE IN STEPNAME FOR LOGON
CONT     EQU    *
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                    M/S/L                                            *
*                                                                     *
***********************************************************************
         TM    OUCBYFL,OUCBSTT    START CREATED TASK?
         BZ    TEST2              NO
         MVI   MSL,C'S'
         B     CONT2
TEST2    TM    OUCBYFL,OUCBLOG    LOGON CREATED TASK?
         BZ    TEST3              NO
         MVI   MSL,C'L'
         B     CONT2
TEST3    TM    OUCBYFL,OUCBMNT    MOUNT CREATED TASK?
         BZ    CONT2              NO
         MVI   MSL,C'M'
***********************************************************************
*                                                                     *
*                  # TCBS ACTIVE                                      *
*                                                                     *
***********************************************************************
CONT2    L     R7,ASCBTCBS         R7 = NO. OF ACTIVE TCBS
         CVD   R7,WORK
         UNPK  TCBA,WORK+7(1)
         OI    TCBA,X'F0'
***********************************************************************
*                                                                     *
*                  TRANSACTION SERVICE ACCUMULATION                   *
*                                                                     *
***********************************************************************
         TM    OUCBQFL,OUCBOUT    ADDRESS SPACE SWAPPED IN
         BO    OUTRANS            YES
         L     R7,OUXBTRS         ACCUMULATED SERVICE
         CVD   R7,WORK            CONVERT TO DECIMAL.
         MVC   $UXBTRS(7),PTRN1
         ED    $UXBTRS-1(8),WORK+4
         B     LINEDONE
OUTRANS  MVC   $UXBTRS(7),BLANKS  OUXB NOT AVAILABLE
*
******** MOVE LINE TO BUFFER
*
LINEDONE MVC   0(81,R1),LINE      MOVE THE LINE TO THE 3270 BUFFER.
         LH    R7,SCRSIZE         INCREMENT 'TPUT' SIZE
         LA    R7,81(R7)          ADD 81 TO CURRENT BUFFER LENGTH
         STH   R7,SCRSIZE         STORE AWAY AGAIN
         LA    R1,81(R1)          R1 = ADDR OF BOTTOM OF BUFFER
         C     R1,ADBUF           HAVE WE REACHED THE BOTTOM?
         BE    PUTIT              YES, GO PUT THIS SCREEN.
NOGOOD   BCT   R3,ASCBLOOP        PROCESS NEXT ADDRESS SPACE
         DROP  R4,R5,R6
         EJECT
***********************************************************************
*                                                                     *
*              PUT THE SWAP BUFFER TO THE 3270 SCREEN                 *
*                                                                     *
***********************************************************************
PUTIT    CLC   JOBMASK(8),BLANKS  IS THE JOBMASK BLANK?
         BNE   CHKTYPE            NO, SO SKIP
         IC    R8,PAGE            LOAD CURRENT PAGE CHARACTER
         LA    R8,1(R8)           ADD ONE TO IT
         STC   R8,PAGE            STORE IT BACK
         CLC   SCRSIZE(2),=H'0'   IS THE SCREEN EMPTY?
         BE    FINDCVT            YES - GO DO THE SCREEN OVER
CHKTYPE  CLI   CRTFLAG,X'FF'      IS THIS A CRT?
         BE    CRT1               YES
         LA    R1,ERROR           R1 = ADDR OF FIRST LINE
         LA    R0,ERROREND-ERROR  R0 = LENGTH OF HEADING    *HMD 11/81*
         ICM   R1,8,EDITFLG       EDIT MODE
         CLI   ATTNFLG,X'FF'      WAS ATTENTION HIT?        *HMD 12/79*
         BE    ATTNHELP           PROCESS INTERRUPT         *HMD 12/79*
         TPUT  (1),(0),R          WRITE LINE
         LA    R1,HEADINGA+1      R1 = ADDR OF FIRST LINE
         LA    R0,79              R0 = LENGTH OF HEADING
         ICM   R1,8,EDITFLG       EDIT MODE
         CLI   ATTNFLG,X'FF'      WAS ATTENTION HIT?        *HMD 12/79*
         BE    ATTNHELP           PROCESS INTERRUPT         *HMD 12/79*
         TPUT  (1),(0),R          WRITE LINE
         LH    R7,SCRSIZE         R7 = TOTAL SCREENSIZE
         LA    R8,81              R8 = ONE LINE
         LA    R1,BUFFER+2        SET POINTER TO FIRST LINE
         ICM   R1,8,EDITFLG       EDIT MODE
         LA    R0,79              R0 LENGTH OF OUTPUT LINE
NEXTL    LR    R2,R1              SAVE R1 SINCE TPUT ZAPS IT
         CLI   ATTNFLG,X'FF'      WAS ATTENTION HIT?        *HMD 12/79*
         BE    ATTNHELP           ACCEPT INTERRUPT          *HMD 12/79*
         TPUT  (1),(0),R          PRINT ONE LINE
         LA    R8,81(R8)          ADD 81 BYTES
         CR    R7,R8              HAVE WE PRINTED LAST LINE?
         BL    CONTA              YES, CONTINUE
         LA    R1,81(R2)          NOPE, POINT TO NEXT LINE
         LA    R0,79              LOAD LENGTH
         ICM   R1,8,EDITFLG       EDIT MODE
         B     NEXTL              PRINT NEXT LINE
CRT1     LA    R1,CLEAR           R1 = ADDR OF BUFFER
         LA    R0,LENGTH          R0 = LENGTH OF BUFFER
         AH    R0,SCRSIZE         R0 = FULL LENGTH OF BUFFER
         ICM   R1,8,FULLSCR       FULL SCREEN MODE
         TPUT  (1),(0),R          PUT SCREEN
CONTA    MVC   ERROR(27),BLANKS   BLANK OUT ERROR FIELD
         CLI   WAITSW,X'FF'       ARE WE IN WAIT MODE?
         BNE   GETCHAR            NO, READ FROM TERMINAL
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                             WAIT MODE                               *
*                                                                     *
***********************************************************************
         STIMER WAIT,BINTVL=DELAY YES, WAIT 1 SECOND
         L     R10,COUNTER        LOAD COUNTER
         BCTR  R10,0              COUNTER = COUNTER - 1
         ST    R10,COUNTER        STORE BACK
         CVD   R10,WORK           CONVERT TO DECIMAL.
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN TIME
         MVC   TIME(3),SCRATCH+1  MOVE IN TIME
         LTR   R10,R10            COUNTER = 0 ?
         BNZ   CONTINUE           REFRESH IF STILL COUNTING
         XI    WAITSW,X'FF'       COUNTER UP, DO READS AGAIN
         MVC   TIME(3),BLANKS     CLEAR COUNTER FIELD
         B     CONTINUE           REFRESH ONE LAST TIME
GETCHAR  TGET  REPLY,33           'TGET' - WHAT NEXT FROM USER ?
         CLI   REPLY,X'6E'        TEST FOR RESHOW (VTAM ONLY)
         BE    CONTINUE
         CLI   REPLY,C' '         TEST FOR NO ENTRY
         BE    CONTINUE
         BAL   R14,INITSCAN       INITIALIZE IKJSCAN CALL   *HMD 07/81*
         OC    REPLY(33),BLANKS   CONVERT TO UPPER CASE
         XC    CBUF+2(2),CBUF+2   CLEAR OFFSET BYTES        *HMD 07/81*
         MVC   CSPLBLOK(24),CSPLSAVE                        *HMD 07/81*
         XC    CSOABLOK(8),CSOABLOK                         *HMD 07/81*
         EJECT                                              *HMD 07/81*
***********************************************************************
*                                                                     *
*   IKJSCAN IS BEING CALLED MERELY TO ALLOW THE 'X' FEATURE OF        *
*   PCF TO WORK.  IF SWAP IS BEING CALLED USED IN CONJUCTION WITH     *
*   SPY, THEN SWAP MUST BE APF AUTHORIZED.  SINCE 'X' WILL NOT WORK   *
*   WITH AUTHORIZED PROGRAMS, WE FLIP OFF THE AUTH BIT IN THE JSCB    *
*   TO FOOL IT. THEN WE 'SYNCH' TO IT TO SET PROBLEM KEY, STATE, ETC. *
*                                                                     *
*   NOTE: AUTHORIZATION IS ONLY NEEDED HERE SO THAT THIS PROGRAM CAN  *
*         WORK IN CONJUNCTION WITH THE 'SPY' PROGRAM (VIA THE 'L'     *
*         COMMAND) AND ALSO USE THE PCF 'X' FEATURE. REMOVE THE       *
*         CODE BELOW AND INSTALL A 'CALLTSSR' TO IKJSCAN IF YOU       *
*         RUN THIS NON-AUTHORIZED. IN THAT CASE IT WILL NOT WORK      *
*         IN CONJUNCTION WITH THE SPY PROGRAM AND WILL ABEND          *
*         WHEN 'SPY' TRIES TO XCTL TO IT.                             *
*                                                                     *
*         THIS IS A READ ONLY MODULE NORMALLY AND THE KEY ZERO        *
*         CODE IS ONLY NEEDED TO FOOL 'PCF II'.                       *
***********************************************************************
         TESTAUTH FCTN=1           ARE WE AUTHORIZED?       *HMD 01/82*
         LTR   R15,R15             WELL ARE WE?             *HMD 01/82*
         BZ    MODEXX              YES, ISSUE MODESET       *HMD 01/82*
* INSTALL USER SVC HERE TO TURN ON AUTHORITY
* IF YOU DESIRE TO RUN THIS UNDER SPF OPTION 6
* AND LINK TO SPY PROGRAM.
         MVI   AUTHFLG,X'FF'       INDICATED WE TURNED ON   *HMD 01/82*
MODEXX   MODESET KEY=ZERO          GET KEY ZERO             *HMD 07/81*
         L     R2,SAVEJSCB         R2 = ADDR OF JSCB        *HMD 07/81*
         USING IEZJSCB,R2          TELL ASSEMBLER           *HMD 07/81*
         NI    JSCBOPTS,255-JSCBAUTH                        *HMD 07/81*
         LA    R1,CSPLBLOK         POINT TO PARMS           *HMD 07/81*
         L     R15,ADDRSCAN        GET SCANS ADDRESS        *HMD 07/81*
         SYNCH (15),STATE=PROB,KEYADDR=KEYBYTE,RESTORE=YES  *HMD 07/81*
         DELETE EPLOC=IKJSCANL     GET RID OF SCAN NOW      *HMD 04/82*
         L     R2,SAVEJSCB         REGET JSCB ADDR          *HMD 08/81*
         CLC   ADDRPSCB+1(3),JSCBPSCB+1                     *HMD 08/81*
         BNE   ABENDXX             VALIDITY CHECK JSCB      *HMD 08/81*
         L     R15,JSCBCSCB        GET CSCB ADDRESS         *HMD 08/81*
         USING CSCB,R15            FOR EXTRA CHECK          *HMD 08/81*
         CLC   USERID,CHKEY        CHECK FOR MATCH          *HMD 08/81*
         BNE   ABENDXX             NON-VALID JSCB           *HMD 08/81*
         DROP  R15                                          *HMD 08/81*
         OI    JSCBOPTS,JSCBAUTH   TURN ON AUTH AGAIN       *HMD 07/81*
*                                  IGNORED FOR HARD COPY    *HMD 08/81*
         DROP  R2                                           *HMD 07/81*
         MODESET KEY=NZERO         NO KEY ZERO NOW          *HMD 07/81*
         STFSMODE ON               RESTORE FULL-SCREEN MODE *HMD 08/81*
         CLI   AUTHFLG,X'FF'       DID WE TURN AUTH ON?     *HMD 01/82*
         BNE   SCANXX              NO, THEN DONT TURN OFF   *HMD 01/82*
*
*  INSERT USER SVC HERE TO TURN OFF AUTHORIZATION
*  IF YOU PREVIOUSLY TURNED IT ON VIA USER SVC
*
SCANXX   LA    R1,CSOABLOK         GET CSOA ADDRESS         *HMD 07/81*
         USING CSOA,R1             TELL ASSEMBLER           *HMD 07/81*
         TM    CSOAFLG,CSOANOC     IS THE BUFFER EMPTY?     *HMD 07/81*
         BO    CONTINUE            YES, JUST GO REFRESH     *HMD 07/81*
         B     WAITCHK             BRANCH AROUND ABEND      *HMD 08/81*
         DROP  R1                                           *HMD 07/81*
ABENDXX  MODESET KEY=NZERO         TURN OFF KEY ZERO        *HMD 08/81*
         L     R1,ABCODE           LOAD ABEND CODE          *HMD 08/81*
         ABEND (1),DUMP            DUMP THIS TASK           *HMD 08/81*
ABCODE   DS    0F                                           *HMD 08/81*
         DC    X'00BAD172'         SYS=BAD,USER=370         *HMD 08/81*
         EJECT
***********************************************************************
*                                                                     *
*                       W  --  ENTER WAIT MODE                        *
*                                                                     *
***********************************************************************
WAITCHK  CLI   REPLY,C'W'         GO INTO WAIT MODE?
         BNE   CDELAY             NO
         XI    WAITSW,X'FF'       YES, TOGGLE WAIT SWITCH
         LA    R2,30              SET DEFAULT VALUE = 30
         LA    R15,CONVBIN        BRANCH TO CONVERSION RTN
         BALR  R14,R15            EBCDIC TO BINARY
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN TIME LEFT
         MVC   TIME(3),SCRATCH+1  MOVE TIME LEFT INTO PLACE
         ST    R2,COUNTER         STORE STARTING TIMER VALUE
         B     CONTINUE           ALL SET - GO DISPLAY NEXT PAGE
         EJECT
***********************************************************************
*                                                                     *
*            D  --  SET TIMER DELAY IN TENTHS OF A SECOND             *
*                                                                     *
***********************************************************************
CDELAY   CLI   REPLY,C'D'         ARE WE CHANGING THE TIME DELAY?
         BNE   ENDCHK             NO, SO CONTINUE
         LA    R2,10              SET DEFAULT VALUE = 10 TENTHS SECOND
         LA    R15,CONVBIN        BRANCH TO CONVERSION RTN
         BALR  R14,R15            EBCDIC TO BINARY
         MVC   SCRATCH(5),DPATTRN MOVE IN EDIT PATTERN
         ED    SCRATCH(5),WORK+6  EDIT IN DELAY TIME
         MVC   PAUSE(3),SCRATCH+2 MOVE TIME LEFT INTO PLACE
         MH    R2,=H'10'          CONVERT TO 100THS OF A SECOND
         ST    R2,DELAY           STORE WAIT DELAY VALUE
         B     CONTINUE           ALL SET - GO DISPLAY NEXT PAGE
         EJECT
***********************************************************************
*                                                                     *
*                          E  --  END SWAP                            *
*                                                                     *
***********************************************************************
ENDCHK   CLI   REPLY,C'E'         TERMINATION REQUESTED ?
         BE    STOP               ALL DONE
         SPACE 3
***********************************************************************
*                                                                     *
*                   T  --  TSO ADDRESS SPACES ONLY                    *
*                                                                     *
***********************************************************************
TSOCHK   CLI   REPLY,C'T'         TSO ONLY ?
         BNE   BATCHCHK           NO
         MVI   MODESW,X'FF'       INDICATE CHANGE OF MODES.
         MVI   BATCHORN+1,X'80'   RESET THE BRANCHES
         MVI   TSOORNO+1,X'00'    ONLY TSO MEMORIES.
         MVI   MODETBA,C'T'       SET MODE INDICATOR
         B     CONTINUE
         SPACE 3
***********************************************************************
*                                                                     *
*                  B  --  BATCH ADDRESS SPACES ONLY                   *
*                                                                     *
***********************************************************************
BATCHCHK CLI   REPLY,C'B'         BATCH ONLY ?
         BNE   BOTHCHK            NO
         MVI   MODESW,X'FF'       INDICATE CHANGE OF MODES.
         MVI   TSOORNO+1,X'10'    RESET THE BRANCHES
         MVI   BATCHORN+1,X'00'   ONLY BATCH (STC) MEMORIES.
         MVI   MODETBA,C'B'       SET MODE INDICATOR
         B     CONTINUE
         SPACE 3
***********************************************************************
*                                                                     *
*             A  --  ALL ADDRESS SPACES (BATCH AND TSO)               *
*                                                                     *
***********************************************************************
BOTHCHK  CLI   REPLY,C'A'         BATCH AND TSO?
         BNE   OUTCHK             NO
         MVI   MODESW,X'FF'       INDICATE CHANGE OF MODES.
         MVI   TSOORNO+1,X'00'    YES, SO NOP ALL THE
         MVI   BATCHORN+1,X'00'   SELECTION BRANCHES.
         MVI   MODETBA,C'A'       SET MODE INDICATOR
         B     CONTINUE
         EJECT
***********************************************************************
*                                                                     *
*        O  --  ADDRESS SPACES WHICH ARE 'OUT' AND NOT READY          *
*                                                                     *
***********************************************************************
OUTCHK   CLI   REPLY,C'O'         OUT MODE ?
         BNE   INCHK              NO
         MVI   MODESW,X'FF'       INDICATE CHANGE OF MODES.
         MVI   INOROUT+1,X'00'    NOP THE BRANCH
         MVI   MODEIO,C'O'        SET MODE INDICATOR
         B     CONTINUE
         SPACE 3
***********************************************************************
*                                                                     *
*             I  --  ADDRESS SPACES WHICH ARE SWAPPED IN              *
*                                                                     *
***********************************************************************
INCHK    CLI   REPLY,C'I'         IN OR READY MODE ?
         BNE   FINDCHK            NO
         MVI   INOROUT+1,X'10'    BRANCH
         MVI   MODESW,X'FF'       INDICATE MODE SWITCH.
         MVI   MODEIO,C'I'        SET MODE INDICATOR
         B     CONTINUE
         SPACE 3
***********************************************************************
*                                                                     *
*                F  --  DISPLAY ONLY CERTAIN JOBNAMES                 *
*                                                                     *
***********************************************************************
FINDCHK  CLI   REPLY,C'F'         SHOULD WE SET THE JOBMASK?
         BNE   SRMCHK             NO
         MVI   MODESW,X'FF'       INDICATE MODE SWITCH.
         MVC   JOBMASK(8),REPLY+1 MOVE MASK INTO PLACE
         MVC   MASKNAME(8),JOBMASK MOVE MASK INDICATOR INTO PLACE
         CLC   JOBMASK(8),BLANKS  IS JOBMASK BLANK?
         BNE   CONTINUE           NO, GO ON
         MVC   MASKNAME(8),=C'  PAGE 1'
         B     CONTINUE
         EJECT
***********************************************************************
*                                                                     *
*                      S  --  SRM DISPLAY MODE                        *
*                                                                     *
***********************************************************************
SRMCHK   CLI   REPLY,C'S'         SRM MODE ?
         BNE   GENCHK             NO
         MVI   SRMSW,X'FF'        INDICATE SRM MODE
         MVC   HEADINGA(80),HEADING1    MOVE IN SRM HEADING
         MVC   MODEGS(7),=C'S.R.M. '   SET MODE INDICATOR
         B     CONTINUE
         EJECT
***********************************************************************
*                                                                     *
*                    G  --  GENERAL DISPLAY MODE                      *
*                                                                     *
***********************************************************************
GENCHK   CLI   REPLY,C'G'         GENERAL MODE ?
         BNE   LINKSPY            NO
         MVI   SRMSW,X'00'        INDICATE GENERAL MODE
         MVC   HEADINGA(80),HEADING2    MOVE IN GENERAL HEADING
         MVC   MODEGS(7),=C'GENERAL'  SET MODE INDICATOR
         B     CONTINUE
         EJECT
***********************************************************************
*                                                                     *
*                         L  --  LINK TO SPY                          *
*                                                                     *
***********************************************************************
LINKSPY  CLI   REPLY,C'L'         XCTL TO SPY?
         BNE   NEEDHELP           NO
         L     R13,SAVE+4         CALLERS SAVE AREA POINTER.
         XCTL  (2,12),EP=SPY      XCTL TO SPY
         EJECT
***********************************************************************
*                                                                     *
*               ?  --  DISPLAY HELP FOR SWAP COMMANDS                 *
*                                                                     *
***********************************************************************
NEEDHELP CLI   REPLY,C'?'         USER NEEDS HELP PAGE DISPLAYED?
         BNE   BADCMD
         CLI   CRTFLAG,X'00'      IS THIS A CRT TERMINAL?   *HMD 12/79*
         BNE   DO3270                                       *HMD 12/79*
         L     R1,HLPAADDR        GET HELP FOR HARDCOPY     *HMD 12/79*
         LA    R0,78              LENGTH OF MESSAGE         *HMD 12/79*
         ICM   R1,8,EDITFLG       INSERT EDIT FLAG          *HMD 12/79*
         LA    R4,(ENDHELPA-HELP1A)/80 NUMBER OF MESSAGES   *HMD 12/79*
NEXTHLP  LR    R3,R1              SAVE R1 OVER TPUT         *HMD 12/79*
         TPUT  (1),(0),R          PUT OUT HEADER            *HMD 12/79*
         CLI   ATTNFLG,X'FF'      WAS ATTN HIT?             *HMD 12/79*
         BE    ATTNHELP           DO LAST HELP MSG          *HMD 12/79*
         LA    R1,80(R3)          INCREMENT REGISTER        *HMD 12/79*
         LA    R0,78              LENGTH OF MESSAGE         *HMD 12/79*
         BCT   R4,NEXTHLP         KEEP PUTTING OUT TEXT     *HMD 12/79*
         TGET  REPLY,79           GET RESPONSE FROM USER    *HMD 12/79*
         CLI   REPLY,C' '         IS REPLY BLANK?           *HMD 12/79*
         BE    NEXTHLP1           YES DISPLAY NEXT PAGE     *HMD 12/79*
         B     CONTINUE           CONTINUE WITH PROGRAM     *HMD 12/79*
NEXTHLP1 DS    0H                 DISPLAY NEXT PAGE OF HELP *HMD 12/79*
         L     R1,HLPBADDR        GET ADDRESS OF PAGE 2     *HMD 12/79*
         LA    R0,78              LENGTH OF MESSAGE         *HMD 12/79*
         ICM   R1,8,EDITFLG       EDIT MODE                 *HMD 12/79*
         LA    R4,(ENDHELPB-HELP2A)/80                      *HMD 12/79*
NEXTHLP2 LR    R3,R1              SAVE R1 OVER TPUT         *HMD 12/79*
         TPUT  (1),(0),R          PUT LINE OUT TO TERMINAL  *HMD 12/79*
         CLI   ATTNFLG,X'FF'      WAS ATTENTION HIT?        *HMD 12/79*
         BE    ATTNHELP                                     *HMD 12/79*
         LA    R1,80(R3)          INCREMENT TO NEXT LINE    *HMD 12/79*
         LA    R0,78              LENGTH OF LINE            *HMD 12/79*
         BCT   R4,NEXTHLP2        PUT OUT NEXT LINE         *HMD 12/79*
         L     R1,=A(LASTHMSG)    LOAD MESSAGE ADDRESS      *HMD 11/81*
         LA    R0,78              LOAD MESSAGE LENGTH       *HMD 11/81*
         TPUT  (1),(0),R          PUT OUT LAST LINE         *HMD 12/79*
         B     GETCHAR            GET NEXT REPLY            *HMD 12/79*
ATTNHELP L     R1,=A(LASTHMSG)    LOAD MESSAGE ADDRESS      *HMD 11/81*
         LA    R0,78              LENGTH OF MESSAGE         *HMD 11/81*
         TPUT  (1),(0),R          PUT OUT LAST MESSAGE      *HMD 12/79*
         MVI   ATTNFLG,X'00'      YES, RESET FLAG
         MVC   TIME(3),BLANKS     BLANK OUT TIMER FIELD
         MVI   WAITSW,X'00'       TURN OFF WAIT FLAG
         LA    R3,0               R3 = 0
         ST    R3,COUNTER         SET TIMER TO 0
         B     GETCHAR            GET THE NEXT CHARACTER    *HMD 12/79*
DO3270   L     R1,HLP1ADDR        R1 = ADDR OF HELP
         LA    R0,HELPLEN1        R0 = LENGTH OF HELP
         ICM   R1,8,FULLSCR       FULL SCREEN MODE
         TPUT  (1),(0),R          WRITE HELP TO SCREEN
         TGET  REPLY,33           GO GET A COMMAND
         L     R1,HLP2ADDR        R1 = ADDR OF HELP
         LA    R0,HELPLEN2        R0 = LENGTH OF HELP
         ICM   R1,8,FULLSCR       FULL SCREEN MODE
         TPUT  (1),(0),R          WRITE HELP TO SCREEN
         B     GETCHAR            GO GET A COMMAND
         EJECT
***********************************************************************
*                                                                     *
*                     BLANK OR INVALID COMMANDS                       *
*                                                                     *
***********************************************************************
BADCMD   CLI   REPLY,C' '         WAS A BLANK JUST ENTERED?
         BE    CONTINUE
         MVC   ERROR(27),ERRMSG2  MOVE IN BAD COMMAND MSG
CONTINUE MVC   SCRSIZE,=H'0'      RESET SCREEN SIZE.
         LA    R1,BUFFER          ---+
         LA    R6,BUFFER             
         ICM   R6,8,C' '              MOVE CHARACTER LONG TO FILL
         LA    R7,22*81               SCREEN BUFFER WITH BLANKS
         LA    R8,BUFFER             
         SR    R9,R9                 
         MVCL  R6,R8              ---+
         CLI   MODESW,X'FF'       CHANGE OF MODES ?
         BNE   MODEOK             BRANCH IF SAME MODE.
         MVI   MODESW,X'00'       RESET MODE CHANGE.
         B     FINDCVT            START OVER FROM BEGINNING.
MODEOK   LTR   R3,R3              MORE MEMORIES TO PROCESS ?
         BNZ   NOGOOD             YES.
         CLI   ATTNFLG,X'00'      WAS ATTN HIT?
         BE    FINDCVT            NO
         MVI   ATTNFLG,X'00'      YES, RESET FLAG
         MVC   TIME(3),BLANKS     BLANK OUT TIMER FIELD
         MVI   WAITSW,X'00'       TURN OFF WAIT FLAG
         LA    R3,0               R3 = 0
         ST    R3,COUNTER         SET TIMER TO 0
         CLI   CRTFLAG,X'FF'      IS THIS A HARDCOPY?       *HMD 12/79*
         BE    FINDCVT            NO, CONTINUE FROM TOP     *HMD 12/79*
         B     ATTNHELP           ELSE, ALLOW INTERRUPT     *HMD 12/79*
STOP     CLI   CRTFLAG,X'00'      IS THIS A CRT?
         BE    STOP1              NO; DON'T CLEAR
         LA    R1,CLR             R1 = ADDR OF CLEAR STRING
         LA    R0,CLRLEN          R0 = LENGTH OF CLEAR STRING
         ICM   R1,8,FULLSCR       FULL SCREEN MODE
         TPUT  (1),(0),R          CLEAR THE SCREEN BEFORE WE LEAVE
         STFSMODE OFF             TURN OFF FULLSCREEN MODE
STOP1    L     R13,SAVE+4
         RETURN (14,12),RC=0
VARCLC1  CLC   JOBMASK(0),JOB
         EJECT
***********************************************************************
*                                                                     *
*            CONVERT EBCDIC NUMBERS FROM USER INTO BINARY             *
*                                                                     *
***********************************************************************
CONVBIN  CVD   R2,WORK            CONVERT TO DECIMAL.
         CLI   REPLY+1,C' '       DID HE ENTER A NUMBER?
         BE    RTRN               NO, USE THE DEFAULT
         CLI   REPLY+1,C'0'       IS THE HEX CODE < 'F0' ?
         BL    BADCHAR            YES, ERROR
         CLI   REPLY+1,C'9'       IS THE HEX CODE > 'F9' ?
         BH    BADCHAR            YES, ERROR
         PACK  WORK(8),REPLY+1(1) PACK EBCDIC (ASSUME 1 DIGIT)
         CLI   REPLY+2,C' '       DID HE ENTER 2 DIGITS?
         BE    CVB                NO, DONT DO THE 2 DIGIT PACK
         CLI   REPLY+2,C'0'       IS THE HEX CODE < 'F0' ?
         BL    BADCHAR            YES, ERROR
         CLI   REPLY+2,C'9'       IS THE HEX CODE > 'F9' ?
         BH    BADCHAR            YES, ERROR
         PACK  WORK(8),REPLY+1(2) PACK AGAIN, WITH 2 DIGITS THIS TIME
CVB      CVB   R2,WORK            GET BINARY
RTRN     BR    R14                RETURN TO MAINLINE
BADCHAR  MVC   ERROR(27),ERRMSG1  NUMERIC ERROR
         B     RTRN
***********************************************************************
*                                                                     *
*          SETUP AND INITIALIZE PARAMETER LIST FOR IKJSCAN            *
*                                                                     *
***********************************************************************
INITSCAN STM   R15,R4,SAVE154     SAVE REGS
         L     R3,CVTPTR          R3 = ADDR OF CVT
         L     R3,0(R3)           R3 = ADDR OF ADDR OF TCB
         L     R3,4(R3)           R3 = ADDR OF TCB
         USING TCB,R3
         ICM   R3,7,TCBJSCBB      R3 = ADDR OF JSCB
         ST    R3,SAVEJSCB        SAVE ADDR OF JSCB
         DROP  R3
         USING IEZJSCB,R3
         L     R4,JSCBPSCB        R4 = ADDR OF PSCB
         DROP  R3
         USING PSCB,R4
         MVC   USERID(7),PSCBUSER SAVE USER IDENTIFICATION  *HMD 08/81*
         ST    R4,ADDRPSCB        SAVE PSCB ADDRESS         *HMD 08/81*
         L     R3,PSCBUPT         R3 = ADDR OF UPT
         ST    R3,SAVEUPT         SAVE ADDR OF UPT
         LA    R2,CSPLSAVE        R2 = ADDR OF CMD SCAN PARM LIST
         USING CSPL,R2
         ST    R3,CSPLUPT         SAVE UPT ADDR
         L     R3,PSCBRLGB        R3 = ADDR OF RELOGON BUFFER
         L     R3,256(R3)         R3 = ADDR OF ECT
         ST    R3,CSPLECT         SAVE ECT ADDR
         LA    R3,CPECB           R3 = ADDR OF FAKE ECB FOR THIS CP
         ST    R3,CSPLECB         SAVE ECB ADDR
         LA    R3,FLAGWORD        R3 = ADDR OF CSPL FLAG WORD
         ST    R3,CSPLFLG         SAVE FLAG ADDR
         LA    R3,CSOABLOK        R3 = ADDR OF CMD SCAN OUTPUT AREA
         ST    R3,CSPLOA          SAVE OUTPUT AREA ADDR
         LA    R3,CBUF            R3 = ADDR OF 'CMD BUFFER'
         ST    R3,CSPLCBUF        SAVE CBUF ADDR
         DROP  R2,R4
         LOAD  EPLOC=IKJSCANL     LOAD SCAN ADDRESS         *HMD 04/82*
         ST    R0,ADDRSCAN
         LM    R15,R4,SAVE154     RESTORE REGS 15 - 4
         BR    R14
         SPACE 2
         DS    0F
SAVE154  DS    6F
         SPACE 1
SAVEUPT  DS    F                   SAVE ADDR OF UPT
SAVEJSCB DS    F                   SAVE ADDR OF JSCB
CSPLBLOK DS    6F                  CMD SCAN PARAMETER LIST
CSPLSAVE DS    6F
CPECB    DC    F'0'                FAKE ECB FOR THIS CP
FLAGWORD DC    F'0'
CSOABLOK DS    2F
CBUF     DC    AL2(37),AL2(0)
REPLY    DC    CL33' '             USER'S COMMAND INPUT BUFFER
ADDRSCAN DS    A
IKJSCANL DC    CL8'IKJSCAN'        ENTRY POINT LOCATION     *HMD 04/82*
         DROP  R12
         EJECT
***********************************************************************
*                                                                     *
*                          A T T N E X I T                            *
*                                                                     *
*         TRAP USERS ATTENTION INTERRUPTS AND FLAG FOR RESET          *
*                                                                     *
***********************************************************************
ATTNEXIT SAVE  (14,12)            SAVE THE CALLERS REGISTERS
         LR    R7,R15             ESTABLISH
         USING ATTNEXIT,R7        ADDRESSABILITY.
         LA    R11,SAVEA          SET-UP
         ST    R13,SAVEA+4         SAVE
         ST    R11,8(,R13)          AREA
         LR    R13,R11               CHAINING
         MVI   ATTNFLG,X'FF'      SET ATTN FLAG
         L     R13,SAVEA+4        CALLERS SAVE AREA POINTER.
         RETURN (14,12),RC=0      AND AWAY WE GO...
SAVEA    DS    18F
         DROP  R7
         EJECT
***********************************************************************
*                                                                     *
*                             CONSTANTS                               *
*                                                                     *
***********************************************************************
STAXLIST STAX  ATTNEXIT,MF=L
PACK     DS    D
WORK     DS    D
SCRATCH  DS    D                  SCRATCH AREA
ASCBADDR DS    F                  ASCB ADDR SAVE AREA
ADDRPSCB DC    F'0'               SAVED ADDR OF PSCB        *HMD 08/81*
FRSTASVT DS    F                  FIRST ASVT ENTRY ADDR     *HMD 07/81*
COUNTER  DC    F'0'               WAIT COUNTER
TRANSSEC DS    F                  TRANSACTION TIME IN SECONDS
DELAY    DC    F'100'             2 SECOND DELAY FOR REFRESH
HLP1ADDR DC    A(HELP1)
HLP2ADDR DC    A(HELP2)
HLPAADDR DC    A(HELP1A)                                    *HMD 12/79*
HLPBADDR DC    A(HELP2A)                                    *HMD 12/79*
ADBUF    DC    A(0)
SCRSIZE  DC    H'0'
ATTNFLG  DC    X'00'              ATTENTION FLAG
WAITSW   DC    X'00'              WAIT MODE
MODESW   DC    X'00'              MODE CHANGE.
SRMSW    DC    X'00'              SRM  MODE.
AUTHFLG  DC    X'00'              SPECIAL AUTHORIZED FLAG   *HMD 01/82*
KEYBYTE  DC    X'80'              PROBLEM KEY INDICATOR     *HMD 07/81*
PTRN1    DC    X'20202020202120'  EDIT PATTERN
PTRN2    DC    X'202021204B202020'
PATTERN  DC    X'40202020'        EDIT PATTERN
DPATTRN  DC    X'4021204B20'      EDIT PATTERN
BLANKS   DC    CL40' '
USERID   DC    CL8' '              SAVE USER IDENTIFICATION *HMD 08/81*
CODETABL DC    C'O'  SWAP CODE 1 : TERMINAL OUTPUT WAIT
         DC    C'I'  SWAP CODE 2 : TERMINAL INPUT WAIT
         DC    C'W'  SWAP CODE 3 : LONG WAIT
         DC    C'A'  SWAP CODE 4 : AUXILIARY STORAGE SHORTAGE
         DC    C'R'  SWAP CODE 5 : REAL STORAGE SHORTAGE
         DC    C'V'  SWAP CODE 6 : MS0 DETECTED WAIT
         DC    C'S'  SWAP CODE 7 : REQSWAP SYSEVENT ISSUED
         DC    C'E'  SWAP CODE 8 : ENQHOLD EXCHG BY SWAP ANALYSIS
         DC    C'X'  SWAP CODE 9 : EXCHG RECOMMENDED BY SWAP ANALYSIS
         DC    C'$'  SWAP CODE A : UNILATERAL SWAPOUT
HEX      DC    C'0123456789ABCDEF'
START    DC    C'Starting'
         EJECT
***********************************************************************
*                                                                     *
*                        FORMAT DISPLAY LINE                          *
*                                                                     *
***********************************************************************
X        DC    CL80' '
         ORG   X                  BYTE
LINE     DC    X'1D60'            0            PROTECTED, LOW INTENSITY
JOB      DC    CL8' '             2
         DC    C' '               10
         ORG   X+11               FOR GENERAL LISTING
PROCSTEP DC    CL8' '             11
         DC    C' '               19
STEPNAME DC    CL8' '             20
         DC    CL3' '             28
ASID     DC    CL2' '             31                        *HMD 07/81*
         DC    CL2' '             33                        *HMD 07/81*
CPU      DC    CL6' '             35
         DC    C' '               41
         ORG   X+11               FOR SRM LISTING
$UCBPGN  DC    CL2' '             11
         DC    C' '               13
$UCBPGP  DC    C' '               14
         DC    C' '               15
$UCBDOM  DC    CL2' '             16
         DC    C' '               18
DP       DC    CL2' '             19
         DC    C' '               21
DQ       DC    CL2' '             22
         DC    C' '               24
$UCBRMR  DC    CL2' '             25
         DC    C' '               27
$UCBWMR  DC    CL2' '             28
         DC    C' '               30
SERATE   DC    CL3' '             31
         DC    C' '               34
$UCBWMS  DC    CL6' '             35
         DC    C' '               41
         ORG   X+42               FOR GENERAL AND SRM LISTINGS
TCPU     DC    CL6' '             42
         DC    C' '               48
$UCBSWC  DC    CL2' '             49
         DC    C' '               51
STATUS   DC    C' '               52
         DC    C' '               53
LOC      DC    C' '               54
         DC    C' '               55
MEM      DC    CL3' '             56
         DC    C' '               59
         ORG   X+60               FOR SRM LISTING
$UCBWSS  DC    CL3' '             60
         DC    C' '               63
IOC      DC    CL5' '             64
         DC    C' '               69
TSLS     DC    CL5' '             70
         DC    C' '               75
         ORG   X+60               FOR GENERAL LISTING
HH       DC    CL2' '             60
         DC    C':'               62
MM       DC    CL2' '             63
         DC    C':'               65
SS       DC    CL2' '             66
         DC    C' '               68
MSL      DC    C' '               69
         DC    C' '               70
$UXBTRS  DC    CL7' '             71
         DC    C' '               78
TCBA     DC    CL1' '             79
         DC    C' '               80
         ORG X+90
FULLSCR  DC    X'03'
EDITFLG  DC    X'00'
CRTFLAG  DC    X'FF'
JOBMASK  DC    CL8'        '
ERRMSG1  DC    CL27' ERROR - Non-numeric value '
ERRMSG2  DC    CL27' ERROR - Invalid command   '
         LTORG
HEADING1 DC CL80' JOBNAME  PG P DM DP DQ RM WR SU/S  S.U.  TCPU  SC R LX
                MEM WSS   I/O  TSLS       '
HEADING2 DC CL80' JOBNAME  PROCSTEP STEPNAME  ASID   CPU   TCPU  SC R LX
                MEM HH:MM:SS MSL S.U. TCB '
CLR      DC    X'C1'               WCC - CLEAR SCREEN AT END OF PGM
         DC    X'115D7E'           SBA TO ROW 24, COL 79 (FSE 5.0)
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'13'               INSERT CURSOR
CLRLEN   EQU   *-CLR
CLEAR    DC    X'C1'               WCC
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
PHEADING DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'1D40'             ATTR BYTE - UNPROTECTED, LOW INTENS.
         DC    X'13'               INSERT CURSOR
         DC    C'   '
ERROR    DC    CL30' '
CMDCTRL1 DC    X'1DE8'             ATTR BYTE - PROTECTED, HI INTENS.
         DC    CL7'Timer: '
TIME     DC    CL3'   '
SLASH    DC    CL1'/'
PAUSE    DC    CL3'1.0'
         DC    CL5'     '
         DC    CL6'Mode: '
MODETBA  DC    CL1'B'
MODEIO   DC    CL2'O '
MODEGS   DC    CL9'General  '
MASKNAME DC    CL7'  Page '
PAGE     DC    CL1'1'
ERROREND DS    0C                  END OF MSG FOR HARDCPY   *HMD 11/81*
HEADINGA DS    CL80' '
BUFFER   DC    41CL81' '
LENGTH   EQU   BUFFER-CLEAR
         EJECT
HELP1    DC    X'C1'               WCC
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
         DC    X'114040',X'1DE8',C'Command      Description'
         DC    X'11C150',X'1D60'
         DC    X'11C260',C'   A         Display Started tasks, batch '
         DC    C'and TSO'
         DC    X'11C3F0',C'   B         Display Started tasks and '
         DC    C'batch only'
         DC    X'11C540',C'   DXX       Set delay to XX tenths seconds'
         DC    X'11C650',C'   E         End SWAP'
         DC    X'11C760',C'   F         Display all Jobnames'
         DC    X'11C8F0',C'   FXXXXXXXX Display only Job ''XXXXXXXX'''
         DC    X'114A40',C'   G         General Information Will be '
         DC    C'displayed'
         DC    X'114B50',C'   I         Display only swapped-in '
         DC    C'memories'
         DC    X'114C60',C'   O         Display both in and out '
         DC    C'memories'
         DC    X'114DF0',C'   L         LINK to program SPY '
         DC    C'(if available)'
         DC    X'114F40',C'   S         SRM information displayed'
         DC    X'115050',C'   T         Display TSO jobs only'
         DC    X'11D160',C'   W         Start timer mode for 30 '
         DC    C'seconds'
         DC    X'11D2F0',C'   WXX       Start timer mode for XX '
         DC    C'seconds'
         DC    X'11D440',C'   W0        Start timer mode until '
         DC    C'interrupt'
         DC    X'11D550',C'   ?         Display this page'
         DC    X'11D660'
         DC    X'11D7F0',C'Hitting INTERRUPT will stop the wait timer'
         DC    X'11D940'
         DC    X'115A50',X'1DE8',C'*WARNING*  ATTENTION may be ignored'
         DC    C' if timer is set too fast (< .4 sec)'
         DC    X'115B60',X'1D60'
         DC    X'115CF0',C'Hit ENTER to continue'
         DC    X'115DC61D40134040401DE8'
HELPMRK1 EQU   *
HELPLEN1 EQU   HELPMRK1-HELP1
         EJECT
HELP2    DC    X'C1'               WCC
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
         DC    X'114040',X'1DE8',C'Swap Code    Description'
         DC    X'11C150',X'1D60'
         DC    X'11C260',C'   A       Auxilary storage shortage'
         DC    X'11C260',C'   E       ENQHOLD exchange'
         DC    X'11C3F0',C'   I       Terminal input wait'
         DC    X'11C540',C'   O       Terminal output wait'
         DC    X'11C650',C'   R       Real storage shortage'
         DC    X'11C760',C'   S       REQSWAP SYSEVENT issued'
         DC    X'11C8F0',C'   V       MS0 detected wait'
         DC    X'114A40',C'   W       Long wait'
         DC    X'114B50',C'   X       Exchange swap'
         DC    X'114C60',C'   $       Unilateral swapout'
         DC    X'114DF0',C'   ?       Unknown'
         DC    X'114F40'
         DC    X'115050',X'1DE8',C'Loc. Code   Description'
         DC    X'11D160',X'1D60'
         DC    X'11D2F0',C'    I      Swapped in and eligible to run'
         DC    X'11D440',C'    O      Swapped out but ready to run'
         DC    X'11D550',C'    W      Swapped out and not ready to run'
         DC    X'11D660',C'    $      Swapped in: V=R or non-swappable'
         DC    X'11D7F0',C'    ?      In transition between states'
         DC    X'11D940',C'    L      Logically Swapped   '
         DC    X'115A50'
         DC    X'115B60',X'1DE8',C'Version 4.5 (GTEDS-HMD &DATE'
         DC    X'115CF0',X'1D60',C'Hit ENTER to continue'
         DC    X'115DC61D40134040401DE8'
HELPMRK2 EQU   *
HELPLEN2 EQU   HELPMRK2-HELP2
         EJECT                                              *HMD 12/79*
HELP1A   DC    CL80'Command      Description '
         DC    CL80'   A         Display STARTED tasks, BATCH '
         DC    CL80'             and TSO'
         DC    CL80'   B         Display STARTED tASKS and '
         DC    CL80'             BATCH only'
         DC    CL80'   DXX       Set delay to XX tenths seconds'
         DC    CL80'   E         End SWAP'
         DC    CL80'   F         Display all jobnames'
         DC    CL80'   FXXXXXXXX Display only JOB ''XXXXXXXX'''
         DC    CL80'   G         General information will be '
         DC    CL80'             displayed'
         DC    CL80'   I         Display only SWAPPED-IN '
         DC    CL80'             memories'
         DC    CL80'   O         Display both IN and OUT '
         DC    CL80'             memories'
         DC    CL80'   L         Link to program SPY '
         DC    CL80'             (if availible)'
         DC    CL80'   S         SRM information displayed'
         DC    CL80'   T         Display TSO users only'
         DC    CL80'   W         Start TIMER MODE for 30 '
         DC    CL80'             seconds'
         DC    CL80'   WXX       Start TIMER MODE for XX '
         DC    CL80'             seconds'
         DC    CL80'   W0        Start TIMER MODE until '
         DC    CL80'             interrupt'
         DC    CL80'   ?         Display this page'
         DC    CL80' '
         DC    CL80'Hitting INTERRUPT will stop the wait timer'
         DC    CL80' '
         DC    CL80'*WARNING*  ATTENTION may be ignored if '
         DC    CL80'timer is set too fast (< .4 sec)'
         DC    CL80' '
LASTHMSG DC    CL80'Hit ENTER to continue'
ENDHELPA EQU   *                                            *HMD 12/79*
HELP2A   DC    CL80'Swap Code    Description'
         DC    CL80' '
         DC    CL80'   A       Auxilary storage shortage'
         DC    CL80'   E       Enqhold exchange'
         DC    CL80'   I       Terminal input wait'
         DC    CL80'   O       Terminal output wait'
         DC    CL80'   R       Real storage shortage'
         DC    CL80'   S       REQSWAP SYSEVENT issued'
         DC    CL80'   V       MS0 detected wait'
         DC    CL80'   W       Long wait'
         DC    CL80'   X       Exchange swap'
         DC    CL80'   $       Unilateral swapout'
         DC    CL80'   ?       Unknown'
         DC    CL80' '
         DC    CL80'Loc. Code   Description'
         DC    CL80' '
         DC    CL80'    I      Swapped in and eligible to run'
         DC    CL80'    O      Swapped out but ready to run'
         DC    CL80'    W      Swapped out and not ready to run'
         DC    CL80'    $      Swapped in: V=R or NON-SWAPPABLE'
         DC    CL80'    ?      In transition between states'
         DC    CL80'    L      Logically swapped '          *HMD 07/81*
         DC    CL80' '                                      *HMD 07/81*
         DC    CL80'Version 4.5 (GTEDS-HMD &DATE'           *HMD 07/81*
ENDHELPB EQU   *
         EJECT
***********************************************************************
*                                                                     *
*                SYSTEM CONTROL BLOCK MAPPING DSECTS                  *
*                                                                     *
*     ALL MACROS CAN BE FOUND IN EITHER 'SYS1.MACLIB' OR IN           *
*     'SYS1.AMODGEN'.                                                 *
*                                                                     *
***********************************************************************
         CVT   DSECT=YES          COMMUNICATIONS VECTOR TABLE
         EJECT
         IRAOUCB ,                SRM USER CONTROL BLOCK
         EJECT
         IEZJSCB ,                JOB STEP CONTROL BLOCK
         EJECT
         IHAASCB ,                ADDRESS SPACE CONTROL BLOCK
         EJECT
         IHAASVT ,                ADDRESS SPACE VECTOR TABLE
         EJECT
         IHAOUXB ,                SRM USER EXTENSION BLOCK
         EJECT
         IKJCSOA ,                COMMAND SCAN OUTPUT AREA
         EJECT
         IKJCSPL ,                COMMAND SCAN PARAMETER LIST
         EJECT
CSCB     DSECT
         IEECHAIN ,               COMMAND SCHED. CNTL BLOCK
         EJECT
         IKJPSCB ,                PROTECTED STEP CONTROL BLOCK
         EJECT
         IKJTCB  LIST=YES         TASK CONTROL BLOCK
         END   SWAP
