         PRINT NOGEN
*/********************************************************************/
*/*                                                                  */
*/*  THIS PROGRAM PERFORMS THE FOLLOWING FUNCTIONS                   */
*/*                                                                  */
*/*  A) RETURNS VOLUME LIST OF A CATALOGUED DATASET  FUNC I          */
*/*  B) UNCATALOGS A DATA SET                        FUNC U          */
*/*  C) SCRATCHES A CATALOGUED DATASET               FUNC S          */
*/*  D) SCRATCHES AND UNCATALOGS A DATASET           FUNC P          */
*/*  E) VERIFIES A DATSET IS ON A GIVEN VOLUME       FUNC L          */
*/*  F) SCRATCHES AN UNCATALOGUED DATASET            FUNC D          */
*/*  G) CATALOGS A DATASET                           FUNC C          */
*/*                                                                  */
*/*   FUNCS U,S,AND P REQUIRE THE DATASET TO BE CATALOGUED.          */
*/*                                                                  */
*/*   FUNCS L,C,AND D REQUIRE THE VOLUME ID TO BE PASSED AS THE      */
*/*   FIRST ELEMENT OF THE ARRAY VOLLIST.                            */
*/*                                                                  */
*/*  RESTRICTIONS :                                                  */
*/*                                                                  */
*/*   A) NON-VSAM DATASETS ONLY                                      */
*/*   B) NO CVOLS TO BE USED (FUNCS L AND D EXCEPTED)                */
*/*   C) DATASETS MUST BE ON DISK AND ONLY 1 VOLUME (FUNC I EXCEPTED)*/
*/*                                                                  */
*/*                                                                  */
*/*                                                                  */
*/*                                                                  */
*/*  PARM PASSED FROM PLI PROG IS A 169 BYTE CHAR FIELD              */
*/*  MAPPED AS FOLLOWS                                               */
*/*                                                                  */
*/*    DCL 1 WORKAREA,                                               */
*/*          3 FUNC        CHAR(1),      SEE ABOVE                   */
*/*          3 DSNAME      CHAR(44),                                 */
*/*          3 VOLCOUNT    FIXED BIN(31),                            */
*/*          3 VOLLIST(20) CHAR(6);                                  */
*/*                                                                  */
*/*  FOR FUNCTION L THE VOLUME ID MUST BE PRESENT IN THE FIRST       */
*/*  6 CHARS OF THE VOLLIST FIELD.                                   */
*/*                                                                  */
*/*                                                                  */
*/*  IF CATALOG MANAGEMENT / DASDM RETURNS AN ERROR CONDITION THEN   */
*/*  R15 WILL HAVE A RETURN CODE IN IT.                              */
*/*                                                                  */
*/* THIS PROGRAM CAN ALSO BE CALLED BY COBOL BUT PLEASE NOTE YOU     */
*/* MUST PASS THE PARAMETER LIST AS A CHARACTER STRING (REDEFINED IF */
*/* YOU WISH). PLI MAPS DATA STRUCTURES DIFFERENTLY TO COBOL.        */
*/*                                                                  */
*/*                                                                  */
*/*                                                                  */
*/* MIKE HAWTHORNE   TTI CITICORP                                    */
*/*                  3100 OCEAN PK BLVD                              */
*/*                  SANTA MONICA CA 90405    (213) 450-9111 X2353   */
*/*                                                                  */
*/*                                                                  */
*/*                                                                  */
*/********************************************************************/
CATMAN   CSECT                             /* CALLED BY PLI PROG     */
         STM  R14,R12,12(R13)              /* SAVE REGS              */
         BALR  R10,R0                      /* BASE                   */
         USING *,R10
         ST    R13,SAVEAREA+4              /* READY TO CHAIN SAVES   */
         LR    R2,R13                      /* PL/I DSA               */
         LA    R13,SAVEAREA                /* POINT TO OUR SAVE      */
         ST    R13,8(R2)                   /* CHAIN OUR SAVE         */
         L     R3,0(R1)                    /* PARM FROM PLI PROG     */
         LR    R5,R3                       /* KEEP IT                */
         LA    R3,1(R3)                    /* SAVES RE-WRITING CODE  */
         MVC   DSN(44),0(R3)               /* GET DATASET NAME       */
         CLI   0(R5),C'L'                  /* LOCATE DSN NEEDED      */
         BE    SETUP                       /* YES  SCAN UCB TAB      */
         CLI   0(R5),C'C'                  /* LOCATE DSN NEEDED      */
         BE    SETUP                       /* YES  SCAN UCB TAB      */
         CLI   0(R5),C'D'                  /* LOCATE DSN NEEDED      */
         BNE   NEXT                        /* NO CONTINUE            */
SETUP    MVC   VOLID(6),48(R3)             /* GET VOLUME ID          */
         XC    SCRSTAT,SCRSTAT             /* INITIALIZE SCRATCH STAT*/
*/********************************************************************/
*/*                                                                  */
*/* NOTE MVS/XA HAS UCB ENTRIES 3 BYTES LONG SO THE UCB LOOKUP       */
*/* METHOD HAS BEEN CHANGED TO USE IOSVSUCB.                         */
*/*                                                                  */
*/* THIS METHOD WILL ALSO WORK ON MVS SP 1.3.0 AND ABOVE             */
*/*                                                                  */
*/* I NEED TO GET THE UCB SINCE I HAVE TO VERIFY                     */
*/* A) THE VOLUME EXISTS AND IT'S ONLINE.                            */
*/* B) IT'S DEVICE TYPE                                              */
*/*                                                                  */
*/* THE STUPID DEVTYPE MACRO NEEDS A DDNAME AND I DON'T WANT TO      */
*/* SPEND TIME WITH THAT GARBAGE SVC 99 JUNK OF HAVING TO ALLOCATE   */
*/* AND DEALLOCATE A DATASET EVERY TIME I CALL THIS MODULE.          */
*/* DO IT THIS WAY   IT'S EASIER AND QUICKER.                        */
*/*                                                                  */
*/*                                                                  */
*/********************************************************************/
         SPACE 1
         DS    0H
         L     R4,CVTPTR                GET CVT ADDRESS         JMH
         USING CVTMAP,R4                                        JMH
         XC    WORKAREA,WORKAREA        CLEAR WORK AREA        JMH
         LA    R1,WORKAREA              WORK AREA ADDRESS      JMH
         ST    R1,PARMWA                STORE ITS ADDRESS      JMH
         LA    R1,DEVCLASS              ADDRESS OF DEVICE CLASS JMH
         ST    R1,PARMDEVT              STORE IT               JMH
         MVI   DEVCLASS,UCB3DACC        INDICATE DEV CLASS DA  JMH
         LA    R1,ADDRUCB               UCB ADDR FROM SYSTEM   JMH
         ST    R1,PARMUCB               STORE IT IN PARMLIST   JMH
         USING UCBOB,R8                                        JMH
AGAIN    EQU   *                                               JMH
         LA    R1,PARMLIST              GET PARAMETER LIST     JMH
         L     R15,CVTUCBSC             POINT TO UCB LOOKUP    JMH
         BALR  R14,R15                  CALL ROUTINE           JMH
         SPACE 1
LOOP     CL    R15,=F'0'                HAVE WE DONE ALL UCBS  JMH
         BNE   BADRET                   YES  DEVICE NOT FOUND  JMH
         DROP  R4
         SPACE 1
GOTUCB   L     R8,ADDRUCB               GET ITS ADDRESS        JMH
         TM    SRTESTAT,SRTEONLI        IS THIS DEVICE ONLINE  JMH
         BNO   AGAIN                    LETS GET NEXT DEVICE   JMH
         CLC   UCBTBYT3(2),=X'2005'     DATA CELL?               JMH
         BE    AGAIN                    IF SO SKIP IT            JMH
         TM    SRTEVOLI,X'FF'           ANY NAME?
         BZ    AGAIN                    NO, IGNORE IT            JMH
         CLC   VOLID(6),SRTEVOLI        IS THIS THE VOLID WE WANT
         BNE   AGAIN                    IF NOT SKIP IT
         MVC   DEVCODE(4),UCBTYP       GET UCBTYPE OUT
         DROP  R8
*/********************************************************************/
*/*                                                                  */
*/*                   FUNCTION L   LOCATE VIA SPECIFIC VOL ID        */
*/*                                                                  */
*/********************************************************************/
         OBTAIN FINDIT                     /* ISSUE OBTAIN MACRO     */
         LTR   R15,R15                     /* DID WE FIND IT         */
         BNZ   BADRET                      /* NOPE   SORRY FOLKS     */
         B     EOJ                         /* YEP    GOODLUCK        */
         CLI   0(R5),C'D'                  /* SCRATCH WANTED         */
         BNE   KAT
*/********************************************************************/
*/*                                                                  */
*/*                   FUNCTION D    DELETE VIA SPECIFIC VOL ID       */
*/*                                                                  */
*/********************************************************************/
         SCRATCH NOTKAT                    /* SCRATCH VIA VTOC       */
         LTR   R15,R15                     /* DID WE FIND IT         */
         BNZ   BADRET                      /* NOPE   SORRY FOLKS     */
         B     EOJ                         /* YEP    GOODLUCK        */
*/********************************************************************/
*/*                                                                  */
*/*                   FUNCTION C    CATALOG DASD DATASET (1 VOL)     */
*/*                                                                  */
*/********************************************************************/
KAT      CATALOG NOTKAT#1                  /* ISSUE CATALOG          */
         LTR   R15,R15                     /* DID WE FIND IT         */
         BNZ   BADRET                      /* NOPE   SORRY FOLKS     */
         B     EOJ                         /* YEP    GOODLUCK        */
NEXT     CLI   0(R5),C'U'                  /* UNCATALOG NEEDED       */
         BNE   K1
*/********************************************************************/
*/*                                                                  */
*/*                   FUNCTION U    UNCATALOG DATASET                */
*/*                                                                  */
*/********************************************************************/
         BAL   R9,UNCAT                    /* DO IT                  */
         LTR   15,15                       /* DID IT WORK            */
         BNZ   BADRET
         B     EOJ
K1       DS    0H
*/********************************************************************/
*/*                                                                  */
*/*                   LOCATE VIA CATALOG SEARCH  FUNCTIONS P I       */
*/*                                                                  */
*/********************************************************************/
         LOCATE USERDATA                   /* ISSUE LOCATE MACRO     */
         LTR   R15,R15                     /* HAVE WE FOUND DATASET */
         BNZ   BADRET                      /* NOT IN CATALOG        */
         CLI   0(R5),C'P'                  /* PURGE AND UNCAT       */
         BNE   K2
*/********************************************************************/
*/*                                                                  */
*/*                   FUNCTION P    UNCATALOG DATASET FIRST          */
*/*                                                                  */
*/********************************************************************/
         BAL   R9,UNCAT                    /* UNCATALOG DATASET     *
         LTR   R15,R15                     /* DID IT WORK           */
         BNZ   BADRET                      /* NO                    */
*/********************************************************************/
*/*                                                                  */
*/*                   FUNCTION P    NOW GET RID OF IT                */
*/*                                                                  */
*/********************************************************************/
         BAL   R9,KILLIT                   /* TRY GETING RID OF IT  */
         LTR   R15,R15                     /* HAVE WE FOUND DATASET */
         BNZ   BADRET                      /* NOT IN CATALOG        */
         B     EOJ
K2       DS    0H                          /* INFO WANTED           */
         CLI   0(R5),C'I'                  /* IF INVALID PARM       */
         BE    K3                          /* SET RETURN CODE       */
*/********************************************************************/
*/*                                                                  */
*/*                   FUNCTION ?  BAD FUNCTION CODE SO QUIT          */
*/*                                                                  */
*/********************************************************************/
         L     R15,=F'48'                  /* SET UNUSED COND CODE  */
         B     BADRET                      /* QUIT                  */
*/********************************************************************/
*/*                                                                  */
*/*                   FUNCTION I    RETURN CATALOG INFO              */
*/*                                                                  */
*/********************************************************************/
K3       DS    0H
         XR    R7,R7                       /* ZEROIZE COUNTER       */
         LA    R8,LOCAREA+6                /* FIRST VOL ENTRY       */
         MVC   46(2,R3),LOCAREA            /* GET VOLUME COUNT      */
         L     R7,44(R3)                   /* SAVE IT FOR INDEX     */
         LA    R3,48(R3)                   /* POINT TO USER LIST    */
LUPE     MVC   0(6,R3),0(R8)               /* MOVE VOL ENTRY        */
         LA    R8,12(R8)                   /* GET NEXT ENTRY        */
         LA    R3,6(R3)                    /* GET USER VOL ENTRY    */
         S     R7,=F'1'                    /* REDUCE VOL COUNT      */
         CL    R7,=F'0'                    /* DONE ALL VOLUME YET   */
         BH    LUPE                        /* GO ROUND AGAIN        */
EOJ      DS    0H
         XR    R15,R15            /* SET RETURN CODE FOR PLIRETV   */
BADRET   L     R13,SAVEAREA+4     /* RESTORE CALLERS DSA           */
         L     R14,12(R13)        /* RESTORE R14                   */
         LM    R0,R12,20(R13)     /* RESTORE CALLERS REGS          */
         BR    14                 /* BACK TO CALLER                */
UNCAT    DS    0H
         CATALOG UNCATLST                  /* POINT TO IT          */
         BR     R9
KILLIT   SCRATCH SCRLIST
         BR     R9
DSN      DC    CL44' '                     /* DSNAME                 */
LOCAREA  DS    0D
         DS    265C                        /* WORKAREA FOR CAMLST   */
SAVEAREA DC    20F'0'             /* PLI SAVE NEEDS 80 BYTES       */
SCRLIST  CAMLST SCRATCH,DSN,,LOCAREA,,OVRD /* LOCATE DONE IT FOR US */
USERDATA CAMLST NAME,DSN,,LOCAREA          /* FIND CATALOG ENTRY     */
UNCATLST CAMLST UNCAT,DSN
FINDIT   CAMLST SEARCH,DSN,VOLID,WORK140
NOTKAT   CAMLST SCRATCH,DSN,,VOLIST,,OVRD
NOTKAT#1 CAMLST CAT,DSN,,VOLIST
WORK140  DS    140C
         DS    0H
VOLIST   DC    H'1'             /* 1 ENTRY IN THE LIST              */
DEVCODE  DC    F'0'             /* 4 BYTES FROM UCBTYPE             */
VOLID    DC    CL6' '           /* VOLUME SERIAL                    */
SCRSTAT  DC    H'0'             /* SCRATCH STATUS                   */
*/*******************************************************************/
*/*                                                                 */
*/*  THE FOLLOWING FIELDS ARE THE REQUIRED PARAMETER FIELDS         */
*/*  FOR THE MVS/XA UCB LOOKUP ROUTINE. (ALSO WORKS ON MVS/SP).     */
*/*                                                                 */
*/*******************************************************************/
PARMLIST DS    3F                                                JMH
         ORG   PARMLIST                                          JMH
PARMWA   DS    F               ADDR OF 100 BYTE WORK AREA        JMH
PARMDEVT DS    F               ADDR OF DEVICE TYPE BYTE          JMH
PARMUCB  DS    F               ADDR OF UCB GOT FROM SYSTEM       JMH
DEVCLASS DS    CL1             DEVICE CLASS BYTE                 JMH
ADDRUCB  DS    F               UCB ADDRESS                       JMH
         DS    0D                                                JMH
WORKAREA DS    CL100           WORK AREA FOR IOSVSUCB            JMH
         REGS
         CVT   DSECT=YES
         EJECT
         PRINT NOGEN       STUPID CVT MACRO RESETS GEN OPTION
*                          LIST= DOES NOT APPEAR IN MACRO !!!!!!!
         IEFUCBOB LIST=NO
 END
