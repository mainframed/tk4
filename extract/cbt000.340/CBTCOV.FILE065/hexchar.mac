HEXCHAR  CSECT
***********************************************************************
*
*     THIS ROUTINE CONVERTS A STRING OF HEX DIGITS N BYTES LONG
*     TO A STRING OF THEIR CHARACTER EQUIVALENTS WHICH IS 2*N
*     BYTES LONG
*
*     PARAMETERS: N       - NUMBER OF BYTES (FULL WORD INTEGER)
*                 INSTRG  - N BYTE STRING OF HEX DIGITS
*                 OUTSTRG - 2*N BYTE STRING TO CONTAIN RESULT
*
*     HEXCHAR IS REENTRANT AND REUSABLE
*
***********************************************************************
*
*     REGISTER EQUATES AND USAGE
*
*
R0       EQU   0
R1       EQU   1
R2       EQU   2                       COUNT OF BYTES LEFT
R3       EQU   3                       PTR TO CURRENT BYTE IN INSTRG
R4       EQU   4                       PTR TO CURRENT BYTE IN OUTSTRG
R5       EQU   5
R6       EQU   6                       WORK
R7       EQU   7                       WORK
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                      BASE REG
R13      EQU   13                      CALLING SAVE AREA ADDR
R14      EQU   14                      RETURN ADDR
R15      EQU   15                      RETURN CODE
*
*     HOUSEKEEPING
*
         SAVE  (14,12),,*              SAVE REGISTERS
         LR    R12,R15                 SET REG 12 TO ENTRY ADDR
         USING HEXCHAR,R12             ESTABLISH ADDRESSABILITY
*
*     INITIALIZE
*
         LM    R2,R4,0(R1)             LOAD PARAMETER ADDRESSES
         L     R2,0(R2)                LOAD N
*
*     LOOP THROUGH BYTES
*
BYTELOOP SLR   R6,R6                   CLEAR R6
         IC    R6,0(R3)                R6,R7=000000XX,00000000
         SRDL  R6,4                    R6,R7=0000000X,X0000000
         SLL   R6,4                    R6,R7=000000X0,X0000000
         SLDL  R6,4                    R6,R7=00000X0X,00000000
         STCM  R6,B'0011',0(R4)        SAVE 0X0X
         TR    0(2,R4),=C'0123456789ABCDEF'   TRANSLATE TO CHARS
         LA    R3,1(R3)                MOVE OVER 1 BYTE IN INSTRG
         LA    R4,2(R4)                MOVE OVER 2 BYTES IN OUTSTRG
         BCT   R2,BYTELOOP             DECREMENT AND LOOP IF NOT DONE
*
*     TERMINATE
*
         LM    R14,R12,12(R13)         RESTORE REGISTERS
         XR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN
         END
