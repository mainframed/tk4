STATE     TITLE 'RETURN D.S. STATE INFORMATION'
***********************************************************************
*
*        THIS TSO COMMAND PROCESSOR WILL ISSUE A MESSAGE INDICATING
*        THE STATE (STATUS) A NAMED DATASET AND PASS A RETURN
*        CODE INDICATING SAME (SIMILAR TO THE LISTDS COMMAND).
*        THIS COMMAND HOWEVER, WILL PASS A REURN CODE ALLOWING
*        A CLIST TO QUERY A DATASET'S STATE (STATUS) PRIOR TO
*        ISSUING DELETE OR ALLOCATE COMMANDS.
*
*        AN OPTIONAL PARAMETER (RETCODE) ALLOWS THE USE OF THE
*        RETURN CODE FEATURE WITHOUT ANY MESSAGES BEING ISSUED.
*
*        'STATE' AS USED HERE IMPLIES 'STATUS' OF A DATASET.
*        THE STATUS (EXISTS OR DOES NOT EXIST) MAY BE PASSED TO A
*        CLIST VIA RETURN CODES AS FOLLOWS:
*
*        RETURN CODE  0 = THE DATASET IS CATALOGED AND EXISTS
*                     8 = THE DATASET IS NOT CATALOGED
*
*        EXAMPLES:
*           STATE JCL.CNTL RETCODE     PASS THE RETURN CODE ONLY
*
*           STATE JCL.CNTL             PASS RETURN CODE AND PROVIDE
*                                      A MESSAGE
*
*           STATE 'USER.JCL.CNTL'      SAME AS ABOVE
*
***********************************************************************
         EJECT
STATE    TSOENTER PL=(CPPL,IO,PARSE),REGS=YES,WORK=@WORKEND-@WORK
         EJECT
         MVC   @OBTAIN(MOVELEN),OBTAIN MOVE MODELS
         LA    R15,@DSN       ADDRESS OF DSNAME
         ST    R15,@OBTAIN+4  PROVIDE ADDRESS
         ST    R15,@LOCATE+4  PROVIDE ADDRESS
         LA    R15,@VOLSER    ADDRESS OF VOLSER
         ST    R15,@OBTAIN+8  PROVIDE ADDRESS
         LA    R15,@WORKA     ADDRESS OF WORKAREA
         ST    R15,@OBTAIN+12 PROVIDE ADDRESS
         ST    R15,@LOCATE+12 PROVIDE ADDRESS
         L     R1,IKJADCON    ADDRESS OF IKJPARM CSECT
         ST    R1,PPLPCL      AS PARSE CONTROL LIST
         MVC   PPLCBUF,CPPLCBUF   MOVE COMMAND BUF POINTER
         LA    R1,1               NUMBER OF MSG SEGMENTS
         ST    R1,OLD             SAVE IN OLD
         LA    R1,@MSG            MESSAGE ADDRESS
         ST    R1,OLD+4           SAVE FOR PUTLINE
         LA    R1,84          LENGTH OF MESSAGE AREA
         SLL   R1,16          SHIFT TO HI ORDER
         ST    R1,@MSG        SAVE IN MESSAGE AREA
         CALLTSSR EP=IKJPARS,MF=(E,PPL)
         L     R11,PPLRANS    GET POINTER TO PARSE ANSWER
         USING IKJPARMD,R11   GAIN ADDRESSABILITY TO ANSWER
         L     R1,DSNAME      GET POINTER TO DSNAME
         LH    R4,DSNAME+4    GET SIZE OF DSNAME
         BCTR  R4,R0          DECREMENT FOR EXECUTE
         EX    R4,MOVENAME    MOVE TO WORK AREA
         OC    OPT(2),OPT     WAS 'RETCODE' REQUESTED?
         BZ    NORET          NO, SKIP MESSAGE
         OI    @FLAG,@RET     SIGNAL RETCODE ONLY
NORET    MVI   @RETCODE+3,8   SET RETURN CODE DEFAULT
         LOCATE @LOCATE       TRY TO LOCATE THE NAMED DATASET
         LTR   R15,R15        FOUND?
         BNZ   RLSA           NO, EXIT WITH RETCODE 8
         MVC   @VOLSER,@WORKA+6 MOVE THE VOLSER FOR OBTAIN
         OBTAIN @OBTAIN       SEE IF THE DATASET IS ON THE VOLUME
         MVI   @RETCODE+3,4   SET RETURN CODE DEFAULT
         LTR   R15,R15        WAS DATASET FOUND?
         BNZ   RLSA           NO, EXIT WITH RETCODE 4
         MVI   @RETCODE+3,0   SET ZERO RETURN CODE
*
RLSA     IKJRLSA @ANSWER      FREE PARSE STORAGE
         DROP  R11            DROP ADDRESSABILITY
         TM    @FLAG,@RET     RETURN CODE ONLY?
         BO    EXIT           YES, RETURN
          MVC  @MSG+4,BLANKS    CLEAR MESSAGE AREA
         MVC   @MSG+4(MSG0L),MSG0     ASSUME NORMAL RETURN
         MVC @MSG+27(6),@VOLSER    MOVE IN VOLSER
         MVC @MSG+35(44),@DSN      MOVE IN DSNAME
         CLI   @RETCODE+3,0   ZERO RETURN CODE?
         BE    TPUT           YES, CONTINUE
         MVC   @MSG+4(MSG4L),MSG4      ASSUME RETCODE 4
         MVC   @MSG+28(6),@VOLSER    VOLSER TO MESSAGE
         MVC   @MSG+35(44),@DSN      MOVE IN DSNAME
         CLI   @RETCODE+3,4   ZERO RETURN CODE?
         BE    TPUT           YES, CONTINUE
         MVC   @MSG+4(MSG8L),MSG8    MUST BE RETURN CODE 8
         MVC   @MSG+35(44),@DSN    MOVE IN DSNAME
TPUT     PUTLINE PARM=PTPB,OUTPUT=(OLD,SINGLE,TERM,INFOR),             X
               MF=(E,IOPL)
EXIT     L     R10,@RETCODE   GET RETURN CODE
         TSOLEAVE ISN=LTORG,RETCODE=(R10)
         EJECT
***********************************************************************
*
*        CONSTANTS
*
***********************************************************************
MOVENAME MVC   @DSN(0),0(R1) MOVE NAME TO WORK AREA
* ------ RELOCATED CONSTANTS ----------------------------------------
         DS    0D             DOUBLEWORD ALIGNMENT
OBTAIN   CAMLST SEARCH,*,*,*  OBTAIN CAM LIST
LOCATE   CAMLST NAME,*,,*     LOCATE CAM LIST
DSN      DC    CL44' '        DSNAME FOR LOCATE/OBTAIN
VOLSER   DC    CL6' '         VOLUME SERIAL FOR OBTAIN
MOVELEN  EQU   *-OBTAIN       SIZE FOR RELOCATE MOVE
* ------ END OF RELOCATE SECTION ------------------------------------
MSG0     DS    0C
MSG0A    DC    C'HMD055I CTLD, FOUND ON VVVVVV'
MSG0L    EQU   *-MSG0
MSG4     DS    0C
MSG4A    DC    C'HMD055I CTLD BUT NOT ON VVVVVV'
MSG4L    EQU   *-MSG4
MSG8     DS    0C
MSG8A    DC    C'HMD055I NOT CTLGD ......'
MSG8L    EQU   *-MSG8
BLANKS   DC    CL80' '
IKJADCON DC    V(STATEPCL)    ADDRESS OF PARSE PDL/PCL
          EJECT
STATEPCL IKJPARM
DSNAME   IKJPOSIT DSNAME,USID,PROMPT='DATASET TO LOCATE'
OPT      IKJKEYWD
         IKJNAME 'RETCODE',ALIAS=('CODE','CONDCODE')
         IKJENDP
         LTORG
         EJECT
***********************************************************************
*
*        WORK AREA DSECT AND TSO DSECTS
*
***********************************************************************
TSODSECT DSECT                RESUME DSECT
@WORK    DS    0F             ALIGN TO FULLWORD
@SAVE    DS    18F            REG SAVE AREA
PTPB     PUTLINE MF=L         PUTLINE PARM BLOCK
OLD      DS      F            OLD FOR PUTLINE
MSGA     DS      F            MESSAGE ADDRESS
@MSGA    DS    A              MESSAGE LENGTH, INSERT
@MSG     DS    CL84           MESSAGE AREA
@OBTAIN  CAMLST SEARCH,*,*,*
@LOCATE  CAMLST NAME,*,,*
@DSN     DS    CL44           DSNAME FOR LOCATE/OBTAIN
@VOLSER  DS    CL6            VOLUME SERIAL FOR OBTAIN
@RETCODE DS    F              RETURN CODE AREA
@ECB     DS    F              PARSE/ATTACH ECB
@ANSWER  DS    F              PARSE ANSWER AREA
@PPLUPT  DS    F              PARSE PARM LIST UPT
@PPLECT  DS    F              PARSE PARM LIST ECT
@PPLECB  DS    F              PARSE PARM LIST ECB
@PPLPCL  DS    F              PARSE PARM LIST PCL
@PPLANS  DS    F              PARSE PARM LIST ANSWER ->
@PPLCBUF DS    F              PARSE PARM LIST COMMAND ->
@FLAG    DS    X              OPTION FLAG
@RET     EQU   X'01'          NO MESSAGE REQUESTED (ONLY RETCODE)
@SIZE    EQU   *-@WORK        SIZE OF WORKAREA
         DS    0D             DOUBLE WORD ALIGN
@WORKA   DS    265C           LOCATE/OBTAIN WORKAREA
@WORKEND EQU   *              END OF WORKAREA
         EJECT
         CVT   LIST=YES,DSECT=YES
         END
