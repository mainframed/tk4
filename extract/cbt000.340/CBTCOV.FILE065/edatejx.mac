EDATEJX  TITLE 'EUROPEAN TO JULIAN DATE CONVERSION SUBROUTINE'
*
*      THIS PROGRAM CONVERTS A EUROPEAN DATE TO A JULIAN DATE. UPON
* COMPLETION, REGISTER 15 CONTAINS 0 IF THE CONVERSION WAS SUCCESSFUL
* OR 12 IF THE CONVERSION FAILED (DUE TO INVALID INPUT DATE).
* THE CALLING SEQUENCE FOR "EDATEJX" IS AS FOLLOWS
*           1. THE INPUT EUROPEAN DATE IN THE FORMAT "DDMMYY"
*           2. THE OUTPUT JULIAN DATE IN THE FORMAT "YYDDD"
*
*      THE "EDATEJX" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
EDATEJX  ENTER PARMREG=2,         SAVE REGISTERS AND INITIALIZE        X
               GETMAIN=(WORKSIZE,0)
         REGISTER                 REGISTER EQUATES
         USING WORKAREA,R13       ADDRESS GETMAINED WORK AREA
         LM    R4,R5,0(R2)        POINT TO PARAMETERS
         MVC   CALLIST,CCALLST    MOVE "CALL" PARAMETER LIST TO WORK
         MVC   GDATE+0(2),2(R4)   MOVE MONTH TO GREGORIAN DATE
         MVC   GDATE+2(2),0(R4)   MOVE DAY TO GREGORIAN DATE
         MVC   GDATE+4(2),4(R4)   MOVE YEAR TO GREGORIAN DATE
         LOAD  EP=DATEJX          LOAD GREG TO JULIAN CONVERSION PROG
         LR    R15,R0             SAVE "DATEJX" ENTRY POINT ADDRESS
         CALL  (15),              CALL "DATEJX"                        X
               (GDATE,(R5)),                                           X
               MF=(E,CALLIST)
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
         EJECT
CCALLST  CALL  ,(,),MF=L          GENERATE PARAMETER LIST FOR CALL
CALLEN   EQU   *-CCALLST          COMPUTE PARAMETER LIST LENGTH
WORKAREA DSECT ,                  MAP OF GETMAINED WORK AREA
         DS    18A                REGISTER SAVE AREA
CALLIST  DS    CL(CALLEN)         PARAMETER LIST FOR CALL MACRO
GDATE    DS    CL6                GREG DATE
WORKSIZE EQU   *-WORKAREA         COMPUTE SIZE OF WORKAREA
         END
