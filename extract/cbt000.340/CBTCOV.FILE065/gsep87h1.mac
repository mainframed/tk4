GSEP87H1 TITLE 'IBM 3287 DEFAULT USER HEADER SEPARATOR ROUTINE'
GSEP87H1 CSECT
R0       EQU    0
R1       EQU    1
R2       EQU    2
R3       EQU    3
R4       EQU    4
R5       EQU    5
R6       EQU    6
R7       EQU    7
R8       EQU    8
R9       EQU    9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12),T,GSEP87H1_&SYSDATE SAVE THE CALLERS REGS
         LR    R11,R15           GET ENTRY ADDRESS FOR BASE
         USING GSEP87H1,R11      TELL ASSEMBLER 11 IS BASE
         USING $SASCOM,R12       12 POINTS TO SASCOM FROM CALLER
         LA    R15,SAVEAREA      GET ADDRESS OF MY SAVE AREA
         ST    R15,8(R13)        CHAIN MINE TO MY CALLERS
         ST    R13,4(R15)
         LR    R13,R15           GET SAVE ADDRESS INTO 13
*
*        R1 -> ADDRESS OF DCB
*              ADDRESS OF WORK AREA
*
         L     R10,0(R1)         GET DCB ADDRESS
         BAL   R14,BANNER        CALL INTERNAL ROUTINE TO PRINT BANNER
         PUT   (R10),LINE1
         BAL   R14,BANNER        CALL INTERNAL ROUTINE TO PRINT BANNER
*
*        CHAIN THRU MVS CONTROL BLOCKS TO GET ADDRESS OF TIOT
*        WHICH HAS JOBNAME AND STEPNAME (USERID IF RUNNING UNDER TSO)
*
         L     R4,16               R4--> CVT
         L     R4,0(R4)            R4--> TCB WORDS
         L     R4,4(R4)            R4--> TCB
         L     R8,12(R4)           R8--> TIOT
*
*        TEST THE TSO FLAG IN SASCOM TO SEE IF IT IS A JOB OR A TSO
*        USER MAKING THIS OUTPUT
*
         TM    $TSOSW,$TSO       ARE WE RUNNING UNDER TSO?
         BNO   BATCH             IF OFF THEN BATCH
*
*        FOR A TSO USER PRINT THE USERID= MESSAGE
*
         MVC   USERID,0(R8)
         PUT   (R10),LINE3
         B     MOVPROC           GO TO MOVE THE PROCEDURE NAME
*
*        FOR A BATCH JOB PRINT JOBNAME/STEPNAME= MESSAGE
*
BATCH    DS    0H
         MVC   JOBNAME,0(R8)
         MVC   STEPNAME,8(R8)
         PUT   (R10),LINE2
*
*        GET THE NAME OF THE GRAPHICS PROCEDURE THAT WAS INVOKED
*        TO MAKE THIS OUTPUT AND PRINT THE PROC= LINE
*
MOVPROC  DS    0H
         BAL   R14,BANNER        CALL INTERNAL ROUTINE TO PRINT BANNER
         MVC   NAME1,$PROCNAM
         MVC   NAME2,$PROCNM2
         PUT   (R10),LINE7
         BAL   R14,BANNER        CALL INTERNAL ROUTINE TO PRINT BANNER
*
*        GET THE DATE THAT APPEARS ON THE USERS LOG AND PUT IT ON
*        THE HEADER PAGE TOO
*
         MVI   DATE,C' '
         MVC   DATE+1(39),DATE
         LH    R2,$DATEL
         BCTR  R2,0
         EX    R2,MVCINST1
*MVCINST MVC   DATE(1),$DATE       EXECUTED INSTRUCTION
         PUT   (R10),LINE4
         BAL   R14,BANNER        CALL INTERNAL ROUTINE TO PRINT BANNER
         PUT   (R10),LINE5
         L     R13,4(R13)        RESTORE REG 13
         RETURN (14,12),T,RC=0   RESTORE CALLERS REGS
         TITLE 'BANNER BMOD'
BANNER   ST    R14,R14SAVE       SAVE RETURN REGISTER
         PUT   (R10),LINE6
         PUT   (R10),LINE6
         L     R14,R14SAVE       RESTORE RETURN REGISTER
         BR    R14               RETURN TO CALLER
         TITLE 'CONSTANT WORK AREA'
SAVEAREA DS    18F               SAVE AREA
R14SAVE  DS    F                 SAVE AREA FOR BANNER INTERNAL ROUTINE
MVCINST1 MVC   DATE(1),$DATE       EXECUTED INSTRUCTION
LINE1    DC    AL2(LLINE1),AL2(0)
         DC    C' '        CARRIAGE CONTROL
         DC    5C'SAS '
         DC    C'      SAS/GRAPH IBM 3287 OUTPUT '
LLINE1   EQU   *-LINE1
LINE2    DC    AL2(LLINE2),AL2(0)
         DC    C' '        CARRIAGE CONTROL
         DC    5C'SAS '
         DC    C'      FOR JOBNAME/STEPNAME= '
JOBNAME  DS    CL8
         DC    C'/'
STEPNAME DS    CL8
LLINE2   EQU   *-LINE2
LINE3    DC    AL2(LLINE3),AL2(0)
         DC    C' '        CARRIAGE CONTROL
         DC    5C'SAS '
         DC    C'      FOR TSO USERID= '
USERID   DS    CL8
LLINE3   EQU   *-LINE3
LINE4    DC    AL2(LLINE4),AL2(0)
         DC    C' '        CARRIAGE CONTROL
         DC    5C'SAS '
         DC    C'               AT '
DATE     DS    CL40
LLINE4   EQU   *-LINE4
LINE5    DC    AL2(LLINE5),AL2(0)
         DC    C'1 '
LLINE5   EQU   *-LINE5
LINE6    DC    AL2(LLINE6),AL2(0)
         DC    C' '        CARRIAGE CONTROL
         DC    5C'SAS '
LLINE6   EQU   *-LINE6
LINE7    DC    AL2(LLINE7),AL2(0)
         DC    C' '        CARRIAGE CONTROL
         DC    5C'SAS '
         DC    C'      PRODUCED BY PROC '
NAME1    DS    CL8
NAME2    DS    CL8
LLINE7   EQU   *-LINE7
         SASCOM ,          INCLUDE THE SAS COMMUNICATION TABLE
         END   GSEP87H1
