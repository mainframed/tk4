CHARHEX  CSECT
***********************************************************************
*
*     THIS ROUTINE CONVERTS A STRING OF HEX CHARACTERS N BYTES LONG
*     TO A STRING OF THEIR HEX EQUIVALENTS WHICH IS N/2
*     BYTES LONG
*
*     PARAMETERS: N       - NUMBER OF BYTES (FULL WORD INTEGER)
*                           NOTE: IF N IS ODD, IT IS ROUNDED DOWN BY 1
*                 INSTRG  - N BYTE STRING OF HEX DIGITS
*                 OUTSTRG - N/2 BYTE STRING TO CONTAIN RESULT
*
*     IF AN INVALID HEX CHARACTER IS ENCOUNTERED, R15 IS SET TO 8.
*
*     CHARHEX IS REENTRANT AND REUSABLE
*
***********************************************************************
*
*     REGISTER EQUATES AND USAGE
*
*
R0       EQU   0
R1       EQU   1
R2       EQU   2                       COUNT OF BYTES LEFT
R3       EQU   3                       PTR TO CURRENT BYTE IN INSTRG
R4       EQU   4                       PTR TO CURRENT BYTE IN OUTSTRG
R5       EQU   5                       CURRENT BYTE
R6       EQU   6                       HIGH ORDER NIBBLE
R7       EQU   7                       WORK
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                      BASE REG
R13      EQU   13                      CALLING SAVE AREA ADDR
R14      EQU   14                      RETURN ADDR
R15      EQU   15                      RETURN CODE
*
*     HOUSEKEEPING
*
         SAVE  (14,12),,*              SAVE REGISTERS
         LR    R12,R15                 SET REG 12 TO ENTRY ADDR
         USING CHARHEX,R12             ESTABLISH ADDRESSABILITY
*
*     INITIALIZE
*
         LM    R2,R4,0(R1)             LOAD PARAMETER ADDRESSES
         L     R2,0(R2)                LOAD N
         SRL   R2,1
         SLL   R2,1                    ROUND DOWN TO EVEN
*
*     LOOP THROUGH BYTES
*
BYTELOOP SLR   R5,R5                   CLEAR R5
         IC    R5,0(R3)                LOAD CHARACTER
         N     R5,=X'0000000F'         JUST WANT THE LOW ORDER NIBBLE
         CLI   0(R3),C'A'
         BL    BADCHAR
         CLI   0(R3),C'F'
         BH    NOTALPHA
         AH    R5,=H'9'                CONVERT TO HEX VALUE
         B     GOODCHAR
NOTALPHA CLI   0(R3),C'0'
         BL    BADCHAR
         CLI   0(R3),C'9'
         BH    BADCHAR
GOODCHAR LR    R7,R2                   COPY R2
         N     R7,=X'00000001'         IS THIS AN EVEN BYTE?
         BNZ   ODDBYTE                 NO
         SLL   R5,4                    R5=000000X0
         LR    R6,R5                   SAVE NIBBLE
         B     NEXT
ODDBYTE  OR    R5,R6                   OR IN THE HIGH ORDER NIBBLE
         STC   R5,0(R4)                AND RETURN IT
         LA    R4,1(R4)                MOVE OVER 1 BYTE IN OUTSTRG
NEXT     LA    R3,1(R3)                MOVE OVER 1 BYTE IN INSTRG
         BCT   R2,BYTELOOP             DECREMENT AND LOOP IF NOT DONE
         B     RETURN0
BADCHAR  LA    R15,8                   SET NASTY RETURN CODE
         B     RETURN
*
*     TERMINATE
*
RETURN0  XR    R15,R15                 SET RETURN CODE TO ZERO
RETURN   L     R14,12(R13)             RESTORE R14
         LM    R0,R12,20(R13)          RESTORE R0 THROUGH R12
         BR    R14                     RETURN
         END
