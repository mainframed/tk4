DEFILE   START 0
**********************************************************************
*
*   SUBROUTINE VERSION OF FORTRAN DEFILE STATEMENT
*   THE FOLLOWING TWO STATEMENTS ARE EQUIVALENT---
*     CALL DEFILE ( NFILE, NREC, LRECL, 'E', IVAR, BUFFER)
*     DEFINE FILE   NFILE( NREC, LRECL,  E , IVAR)
*   EXCEPT THAT NFILE, NREC, AND LRECL MUST BE INTEGER CONSTANTS
*     ( NOT VARIABLES) IN THE STANDARD FORTRAN VERSION.
*    NOTE BUFFER MUST BE AT LEAST 24 BYTES LONG AND MAY NOT BE
*          OVERLAYED WHILE FILE IS OPEN.......
*
**********************************************************************
         USING  *,15
WORK     EQU    0
ARGLIST  EQU    1
FILEPTR  EQU    2
NRECPTR  EQU    3
LRECPTR  EQU    4
FMTPTR   EQU    5
VARPTR   EQU    6
BUFPTR   EQU    7
*
         SAVE   (14,12),,*
         LR     4,13
         LA     13,SAVEAREA
         USING  SAVEAREA,13
         ST     4,SAVEAREA+4
         ST     13,8(4)
         B      START
SAVEAREA DS     18F
START    EQU    *
         LM     2,7,0(ARGLIST)
         USING  LIST,7
         ST     VARPTR,ASSOCVAR
         L      WORK,0(LRECPTR)
         ST     WORK,LRECL
         L      WORK,0(NRECPTR)
         ST     WORK,NREC
         LA     WORK,LRECL
         ST     WORK,ADRLREC
         LA     WORK,NREC
         ST     WORK,ADRNREC
         L      WORK,0(FILEPTR)
         STC    WORK,FILENO
         IC     WORK,0(FMTPTR)
         STC    WORK,FTYPE
         MVI    LASTFLAG,128
*
         LR     ARGLIST,BUFPTR
         L      15,VDIOCS
         BALR   14,15
*
         L      13,4(13)
         RETURN (14,12),T
VDIOCS   DC     V(DIOCS#)
*
LIST     DSECT
         DS     0F
FILENO   DS     0XL1
ADRNREC  DS     AL4
FTYPE    DS     0XL1
ADRLREC  DS     AL4
LASTFLAG DS     0XL1
ASSOCVAR DS     AL4
NREC     DS     F
LRECL    DS     F
         END
