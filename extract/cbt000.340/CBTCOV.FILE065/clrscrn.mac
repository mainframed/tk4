CLRSCRN  CSECT
         STM   14,12,12(13)               USUAL SAVE THE REGISTERS
         LR    10,15                      BASE REGISTER
         USING K,10                       ADDRESSABILITY
         LR    11,13                      PREPARE TO SAVE THE SAVEAREA
         LA    13,SAVEAREA                GET MY SAVE AREA
         ST    13,8(11)                   SAVE MY SAVEAREA
         ST    11,4(13)                   SAVE SYSTEM SAVEAREA
         GTSIZE ,
         CLM   0,B'0001',=X'01'           BE THIS A TTY ?
         BE    EXIT                       YEAH. LETS NOT SEND JUNK
         STFSMODE ON,INITIAL=YES          SET FULL SCREEN MODE ON
         TPUT  LINE,LINEL,FULLSCR,,HOLD   CLEAR THE SCREEN, BUT WAIT
         STFSMODE OFF                     SET FULL SCREEN MODE OFF
EXIT     L     13,4(13)                   RESTORE THE SAVEAREA
         LM    14,12,12(13)               RESTORE THE REGISTERS
         SR    15,15                      MAKE THE "RC" ZERO
         BR    14                         ZOOM BACK TO OS
SAVEAREA DS    18F
LINE     DC    X'27F5'                    ESCAPE & OP CODE
         DC    X'40'                      WCC (NO BITS)
         DC    X'115D7E'                  TCAM/SPF PREFIX
         DC    X'114040'                  CURSOR AT TOP OF SCREEN
LINEL    EQU   *-LINE
         END
