USERS    TITLE 'USERS - TSO DISPLAY LOGGED ON USERS COMMAND'
         MACRO
&NAME    TSDERASE &N
&NAME    BCR   0,0                      NOP
         MEND
         SPACE 1
         PRINT NOGEN
USERS    CSECT
         SAVE  (14,12),T,USERS_TSO_COMMAND
         USING USERS,R15
         LA    R11,SAVEAREA
         ST    R13,SAVEAREA+4
         ST    R11,8(,R13)
         LR    R13,R11
         B     START
         DROP  R15
         SPACE 2
SAVEAREA DC    18F'0'
         SPACE 2
START    DS    0H
         USING SAVEAREA,R13
         SPACE 2
         LM    R6,R9,CLEARREG
         MVCL  R6,R8                    CLEAR TABLE
         SPACE
         TSDERASE                       ERASE THE SCREEN
         L     R1,16                    FIND CVT
         L     R2,X'94'(,R1)            FIND BASEA (M.S.RES.DATA)
         LA    R3,TABLE                 NAME TABLE
         LA    R4,10                    BXLE INCREMENT
         LA    R5,TABLE+199*10          TABLE END
         SR    R6,R6                    COUNT OF USERS
CSCBLOOP ICM   R2,15,0(R2)
         BZ    CSCBEND                  WHEN DONE
         CLI   28(R2),1                 TSO USER ID?
         BNE   CSCBLOOP
         LA    R6,1(,R6)                BUMP USER COUNT
         MVC   0(8,R3),8(R2)            MOVE USERID TO TABLE
         BXLE  R3,R4,CSCBLOOP
         TPUT  MSGOV,L'MSGOV
         SPACE 2
CSCBEND  CVD   R6,DOUBLE                NUM USERS MSG
         MVC   NUMUSERS(4),=X'40202020'
         ED    NUMUSERS(4),DOUBLE+6
         TPUT  NUMUSERS,LMSG
         SPACE 2
         LR    R5,R3                    SET END OF TABLE
         LA    R2,TABLE                 SET START OF TABLE
         LR    R6,R4                    FOR SORT
         LR    R7,R5
         SH    R7,=H'20'                BACK UP ONE ENTRY
         LR    R8,R2                    SAVE FIRST ADDRESS
SORTLOOP CLC   0(8,R2),10(R2)           COMPARE TWO
         BNH   SORTBXLE
         LR    R8,R2                    LAST SWAP
         XC    0(8,R2),10(R2)
         XC    10(8,R2),0(R2)
         XC    0(8,R2),10(R2)
SORTBXLE BXLE  R2,R6,SORTLOOP
         LR    R7,R8                    SAVE LAST SWAP
         LA    R2,TABLE                 REINIT
         LR    R8,R2                    REINIT LAST SWAP
COMPARE  CR    R2,R7                    DONE?
         BL    SORTLOOP
         SPACE 2
* SKIP FOR NON BLANK NAME
         SPACE 1
BLNKLOOP CLI   0(R2),C' '
         BNE   PRNTLOOP
         BXLE  R2,R4,BLNKLOOP
         SPACE 1
* OUTPUT NAMES
         SPACE 1
PRNTLOOP LR    R3,R5                    COMPUTE LENGTH
         SR    R3,R2
         TPUT  (2),(3),R
         SPACE 1
         L     R13,SAVEAREA+4
         RETURN (14,12),RC=0
         SPACE 1
NUMUSERS DC    X'40202020',C' USERS LOGGED ON'
LMSG     EQU   *-NUMUSERS
MSGOV    DC    C'* TABLE OVERFLOW *'
DOUBLE   DC    D'0'
         SPACE 2
         REGS
         SPACE 1
         LTORG
         SPACE 1
CLEARREG DC    A(TABLE,2000),A(0),X'40000000'
TABLE    DC    200CL10' '
         END   USERS
