CMD      TITLE 'CMD - TSO COMMAND TO ISSUE OPERATOR COMMANDS'
CMD      CSECT
         BALR  12,0
         USING *,12
         L     1,16
         L     1,0(1)
         L     1,4(1)
         L     1,X'B4'(1)               JSCB
         LH    2,X'10C'(1)              TJID
         N     2,=X'00007FFF'
         O     2,=X'00008000'
NEXT     DS    0H
         MVI   INPUT,C' '
         MVC   INPUT+1(L'INPUT-1),INPUT
         TPUT  MSG1,L'MSG1
         TGET  INPUT,L'INPUT
         LTR   15,15                    SUCCESSFUL?
         BNZ   EXIT                     NO, ERROR
         LA    3,INPUT                  POINT TO INPUT BFR  *HMD 07/82*
         LA    4,1                      INCREMENT VALUE     *HMD 07/82*
         LA    5,INPUT+L'INPUT          END OF INPUT BFR    *HMD 07/82*
LOOP1    CLI   0(3),C' '                IS THIS A CHAR?     *HMD 07/82*
         BNE   GOT1ST                   YES..PROCESS        *HMD 07/82*
         BXLE  3,4,LOOP1                PROCESS BUFFER      *HMD 07/82*
         B     NEXT                     EMPTY BUFFER        *HMD 07/82*
GOT1ST   SR    5,3                      LENGTH OF COMMAND   *HMD 07/82*
         SR    5,4                      BUMP FOR EX         *HMD 07/82*
         MVI   DATA,C' '                CLEAR DATA AREA
         MVC   DATA+1(L'DATA-1),DATA
         EX    5,MVCDATA
         CLC   =C'END',DATA
         BE    EXIT
*************************************************************HMD 07/82*
** THE FOLLOWING CODE IS NECESSARY THE ENABLE THIS COMMAND  *HMD 07/82*
** TO WORK UNDER MVS/SP RELEASE 3. THIS IS DUE TO A CHANGE  *HMD 07/82*
** IN THE COMMAND SCHEDULER CSCB CREATION MODULE IEE0803D.  *HMD 07/82*
**                                                          *HMD 07/82*
** IN MVS/SP 3, IF AN ASID IS PRESENT IN THE XSA, THEN THE  *HMD 07/82*
** COMMAND SCHEDULER MOVES YOUR TSO USERID INTO THE NEWLY   *HMD 07/82*
** CREATED CSCB. WHY THIS IS DONE, WHO KNOWS. IT CAUSES,    *HMD 07/82*
** HOWEVER, THE STC INITIALIZATION ROUTINE IEEPRWI2 TO      *HMD 07/82*
** THINK THAT A 'SYSTEM' ADDRESS SPACE IS BEING CREATED     *HMD 07/82*
** DUE TO THE CHKEY FIELD OF THE CSCB CONTAINING YOUR       *HMD 07/82*
** USERID INSTEAD OF ZERO. BECAUSE THIS ADDRESS SPACE       *HMD 07/82*
** INITIALIZATION ROUTINE NAME FOR SYSTEM ADDRESS SPACES    *HMD 07/82*
** IS CONTAINED IN THE CHKEY FIELD, IEEPRWI2 ATTEMPT TO     *HMD 07/82*
** LINK TO YOUR TSO USERID TO INITIALIZE THE STARTED        *HMD 07/82*
** ADDRESS SPACE. THUS AN ABEND806-4 AND A RESULTING SVC    *HMD 07/82*
** DUMP.                                                    *HMD 07/82*
**                                                          *HMD 07/82*
** SINCE RESPONSE AT THE TERMINAL IS NOT REQUIRED FOR       *HMD 07/82*
** START AND MOUNT COMMANDS (AND THAT IS THE ONLY PLACE     *HMD 07/82*
** THIS CRAZY THING HAPPENS) WE WILL ISSUE THOSE COMMANDS   *HMD 07/82*
** WITH REGISTER ZERO EQUAL TO ZERO.                        *HMD 07/82*
*************************************************************HMD 07/82*
         CLC   =C'S ',DATA              START COMMAND?      *HMD 07/82*
         BE    SETSTART                 YES                 *HMD 07/82*
         CLC   =C'START ',DATA          START COMMAND?      *HMD 07/82*
         BE    SETSTART                 YES                 *HMD 07/82*
         CLC   =C'M ',DATA              MOUNT COMMAND?      *HMD 07/82*
         BE    SETSTART                 YES                 *HMD 07/82*
         CLC   =C'MOUNT ',DATA          MOUNT COMMAND?      *HMD 07/82*
         BNE   GETLEN                   NO                  *HMD 07/82*
SETSTART OI    FLAGS,FSTART                                 *HMD 07/82*
GETLEN   LA    15,DATA+L'DATA-1
         LA    0,1
         LA    1,DATA-1
         LNR   0,0
CLEAR    CLI   0(15),X'00' END OF COMMAND?
         BE    LOOP
         CLI   0(15),C' '
         BNE   LAST
LOOP     BXH   15,0,CLEAR
         B     NEXT
LAST     LA    15,1(15)
         MVI   0(15),C' '
         LA    1,BUFFER
         SR    15,1
         STH   15,BUFFER
         CLC   =H'5',BUFFER
         BNE   GO
         CLI   DATA,C'E'                ABBREV FOR END?
         BE    EXIT
GO       DS    0H
         MODESET KEY=ZERO
         LA    1,BUFFER
         LR    0,2
         SVC   34
         LR    3,15
         MODESET KEY=NZERO
AFTER    DS    0H
         B     NEXT
EXIT     SR    15,15
         SVC   3
MVCDATA  MVC   DATA(0),0(3)
FLAGS    DC    X'00'
FSTART   EQU   X'80'
BUFFER   DS    0F
         DC    AL2(ENDBUFF-*,0)
DATA     DC    CL120'$',C' '
ENDBUFF  DS    0H
INPUT    DC    CL120'$',C' '
MSG1     DC    C'ENTER REQUEST'
         LTORG
         END
