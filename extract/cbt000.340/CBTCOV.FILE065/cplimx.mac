CPLIMX   TITLE 'SUBROUTINE/FUNCTION TO GET JOB STEP CPU TIME LIMIT'
*
*     THIS PROGRAM RETRIEVES THE JOB STEP CPU TIME LIMIT FROM THE
* ASCB (ASCBJSTL), CONVERTS IT INTO SECONDS, AND RETURNS THE RESULT
* TO THE CALLER IN BOTH REGISTER 0 (SERVING AS A FUNCTION) AND IN THE
* USER'S PARAMETER AREA (SERVING AS A SUBROUTINE). THE CALLING SEQUENCE
* FOR "CPLIMX" IS AS FOLLOWS:
*          1. THE JOB STEP CPU TIME LIMIT IN SECONDS
*                 (FULL WORD BINARY)
*
*     "CPLIMX" IS REENTRANT AND REUSEABLE.
*
CPLIMX   ENTER PARMREG=2,         SAVE REGISTERS AND INITIALIZE        X
               GETMAIN=(72,0)
         REGISTER                 REGISTER EQUATES
         USING ASCB,R4            ADDRESS ASCB DSECT
         L     R2,0(,R2)          POINT TO CPU TIME FIELD
         L     R4,16              POINT TO CVT
         L     R4,0(,R4)          POINT TO TCB/ASCB WORD LIST
         L     R4,12(,R4)         POINT TO CURRENT ASCB
         LA    R6,0               CLEAR FIRST REGISTER OF DOUBLEWORD
         L     R7,ASCBJSTL        LOAD JOB STEP CPU TIME LIMIT
         SLA   R7,10              MULTIPLY BY 1024
         BINTIME 6                CONVERT TO HUNDREDTHS OF SECONDS
         D     R6,=F'100'         CONVERT TO SECONDS
         C     R6,=F'50'          IF REMAINDER IS LESS THAN 1/2 SECOND
         BL    ROUNDED               THEN DON'T ROUND UP
         LA    R7,1(,R7)          ROUND UP TO NEXT SECOND
ROUNDED  DS    0H
         ST    R7,0(,R2)          SAVE CPU TIME LIMIT IN USER'S FIELD
         L     R5,4(,R13)         POINT TO REGISTER SAVE AREA
         ST    R7,20(,R5)         STORE RESULT IN REGISTER 0 SAVE AREA
         LEAVE RETCODE=0,         RESTORE REGISTERS, RETURN TO CALLER  X
               GETMAIN=(72,0)
         EJECT
         IHAASCB
         END
