GETMONX  TITLE 'SUBROUTINE WHICH CONVERTS MONTH NUMBER TO MONTH NAME'
*
*      THIS PROGRAM ACCEPTS A NUMBER BETWEEN 1 AND 12, INCLUSIVE AS
* INPUT AND RETURNS THE FIRST THREE LETTERS OF THE CORRESPONDING
* MONTH. UPON COMPLETION, REGISTER 15 CONTAINS 0 IF THE CONVERSION
* WAS SUCCESSFUL OR 12 IF IT WAS UNSUCCESSFUL. THE CALLING SEQUENCE
* FOR "GETMONX" IS AS FOLLOWS
*           1. THE MONTH NUMBER (4-BYTE BINARY)
*           2. THE MONTH NAME (3-BYTE CHARACTER)
*
*      THE "GETMONX" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
GETMONX  ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(72,0)
         REGISTER                  REGISTER EQUATES
         LM    R4,R5,0(R2)         LOAD PARAMETER POINTERS
         L     R4,0(,R4)           LOAD MONTH NUMBER
         C     R4,=F'01'           IF  MONTH NUMBER IS LESS THAN 01
         BL    ERROR                  THEN FLAG FOR ERROR
         C     R4,=F'12'           IF  MONTH NUMBER IS GREATER THAN 12
         BH    ERROR                  THEN FLAG FOR ERROR
         BCTR  R4,R0               DECREMENT MONTH NUMBER BY 1
         MH    R4,=H'3'            COMPUTE OFFSET INTO MONTH NAME TABLE
         LA    R6,MNAMES(R4)       LOAD ADDRESS OF MONTH NAME ENTRY
         MVC   0(3,R5),0(R6)       MOVE MONTH NAME TO PARAMETER AREA
         LA    R15,0               INDICATE SUCCESSFUL CONVERSION
         B     EXIT                   AND LEAVE
ERROR    DS    0H
         LA    R15,12              INDICATE UNSUCCESSFUL CONVERSION
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(72,0)
         EJECT
MNAMES   DS    0F                  TABLE OF 3-CHARACTER MONTH NAMES
         DC    CL3'JAN'
         DC    CL3'FEB'
         DC    CL3'MAR'
         DC    CL3'APR'
         DC    CL3'MAY'
         DC    CL3'JUN'
         DC    CL3'JUL'
         DC    CL3'AUG'
         DC    CL3'SEP'
         DC    CL3'OCT'
         DC    CL3'NOV'
         DC    CL3'DEC'
         SPACE
         END
