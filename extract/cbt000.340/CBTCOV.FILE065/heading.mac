HEADING  TITLE 'PROGRAM WHICH PRINTS BLOCK CHARACTER BANNER LINES'
*
*     THIS PROGRAM ACCEPTS INPUT VIA THE PARM ON THE JCL "EXEC" CARD
* AND WRITES A BANNER LINE IN BLOCK CHARACTERS USING THAT INPUT. THE
* OUTPUT IS WRITTEN TO THE DDNAME "SYSPRINT". THE FORMAT OF THE PARM
* IS AS FOLLOWS:
*         CSXXXXXXXXXX
* WHERE C IS A CARRIAGE CONTROL CHARACTER TO BE WRITTEN BEFORE PRO-
* DUCING THE BANNER LINE, S IS THE SYMBOL WITH WHICH THE BLOCK CHAR-
* ACTERS ARE TO BE CONSTRUCTED, AND XXXXXXXXXX IS A 10 CHARACTER STRING
* TO BE TRANSLATED INTO BLOCK CHARACTERS AND WRITTEN TO "SYSPRINT".
* FOR EXAMPLE, TO PRINT THE CHARACTER STRING 'TESTING123' AFTER A PAGE
* EJECT WITH THE BLOCK CHARACTERS COMPOSED OF THE SYMBOL 'X', USE THE
* FOLLOWING JCL:
*         //STEP1  EXEC PGM=HEADING,PARM='1XTESTING123'
*         //SYSPRINT DD SYSOUT=A
*
*     "HEADING" IS REENTRANT AND REUSEABLE.
*
HEADING  ENTER PARMREG=2,         SAVE REGISTERS AND INITIALIZE        X
               GETMAIN=(WORKSIZE,0)
         REGISTER                 REGISTER EQUATES
         USING PARM,R2            ADDRESS PARM AREA DSECT
         USING WORKAREA,R13       ADDRESS WORK AREA DSECT
         L     R2,0(,R2)          POINT TO PARM
         LA    R2,2(,R2)          INCREMENT PAST PARM LENGTH
         MVC   CALLIST,CCALLIST   MOVE CALL PARAMETER LIST TO WORK
         MVC   OPENLIST,COPENLST  MOVE OPEN PARAMETER LIST TO WORK
         MVC   SYSPRINT,DUMMYDCB  MOVE DCB FOR "SYSPRINT" TO WORK
         LA    R4,SYSPRINT        POINT TO "SYSPRINT" DCB
         LA    R5,CC              LOAD ADDRESS OF CARRIAGE CONTROL
         OPEN  ((R4)),            OPEN THE "SYSPRINT" DATA SET         X
               MF=(E,OPENLIST)
         LOAD  EP=HEADIN          GET BLOCK CHAR TRANSLATION ROUTINE
         LR    R15,R0             SAVE "HEADINGS" ENTRY POINT ADDRESS
         CALL  (15),              CALL "HEADINGS"                      X
               (SYMBOL,STRING,BUFFER),                                 X
               MF=(E,CALLIST)
         MVC   CC,CARRIAGE        MOVE CARRIAGE CONTROL TO RECORD AREA
         MVC   RECORD,BLANKS      CLEAR REST OF RECORD
         PUT   (R4),(R5)          WRITE THE CARRIAGE CONTROL RECORD
         MVI   CC,C' '            CLEAR THE CARRIAGE CONTROL CHAR
         LA    R6,10              LOAD THE SIZE OF THE BLOCK CHAR
         LA    R7,BUFFER          POINT TO THE BLOCK CHARACTER ARRAY
LOOP     DS    0H
         MVC   RECORD,0(R7)       MOVE THE BLOCK CHAR ROW TO PRINT AREA
         PUT   (R4),(R5)          WRITE A LINE
         LA    R7,132(,R7)        INCREMENT TO NEXT ROW
         BCT   R6,LOOP               AND GO PRINT IT
         MVC   RECORD,BLANKS      CLEAR THE RECORD AREA
         PUT   (R4),(R5)             AND WRITE A FINAL RECORD
         CLOSE ((R4)),            CLOSE THE "SYSPRINT" DATA SET        X
               MF=(E,OPENLIST)
         LEAVE RETCODE=0,         RESTORE REGISTERS, RETURN TO CALLER  X
               GETMAIN=(WORKSIZE,0)
         EJECT
DUMMYDCB DCB   DDNAME=SYSPRINT,   DUMMY "SYSPRINT" DCB                 X
               RECFM=FA,BLKSIZE=133,DSORG=PS,MACRF=PM
DCBLEN   EQU   *-DUMMYDCB
         SPACE
CCALLIST CALL  ,(0,0,0),MF=L      DUMMY PARAMETER LIST FOR "HEADINGS"
CCALLEN  EQU   *-CCALLIST
         SPACE
COPENLST OPEN  (0,OUTPUT),MF=L    DUMMY PARAMETER LIST FOR OPEN/CLOSE
COPENLEN EQU   *-COPENLST
         SPACE
BLANKS   DC    CL132' '
         EJECT
PARM     DSECT ,                  MAP OF PASSED PARM
CARRIAGE DS    CL1                CARRIAGE CONTROL CHARACTER
SYMBOL   DS    CL1                SYMBOL USED IN BLOCK CHARACTERS
STRING   DS    CL10               STRING TO BE TRANSLATED TO BLOCK CHAR
         EJECT
WORKAREA DSECT ,                  MAP OF GETMAINED WORK AREA
         DS    18A                REGISTER SAVE AREA
SYSPRINT DS    CL(DCBLEN)         "SYSPRINT" DCB
CALLIST  DS    CL(CCALLEN)        PARAMETER LIST FOR "HEADINGS"
OPENLIST DS    CL(COPENLEN)       PARAMETER LIST FOR OPEN/CLOSE
CC       DS    CL1                CARRIAGE CONTROL AREA
RECORD   DS    CL132              PRINT LINE AREA
BUFFER   DS    10CL132            BLOCK CHARACTER ARRAY
WORKSIZE EQU   *-WORKAREA
         END
