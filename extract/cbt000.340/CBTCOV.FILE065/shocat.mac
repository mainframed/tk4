SHOCAT   START
***********************************************************************
*
*    ROUTINE NAME ---------> SHOCAT
*
*    ENTRY POINTS ---------> -NONE-
*
*    EXTERNAL REFERENCES --> -NONE-
*
*    ROUTINE DESCRIPTION:
*
*         THIS ROUTINE ISSUES A SHOCAT FOR THE NAME SUPPLIED.
*
*    PARMS:  NAME     - 44 CHARACTER NAME
*            REG15    - A FULLWORD INTEGER CONTAINING THE RETURN CODE
*                       FROM SHOCAT
*
*    DATE LAST MODIFIED ---> 04/08/81
*
***********************************************************************
*
*     REGISTER EQUATES AND USAGE
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12 SHOCAT BASE REG
R13      EQU   13
R14      EQU   14
R15      EQU   15 RETURN CODE
*
         B     12(R15)                 BRANCH AROUND CONSTANT
         DC    X'07'
         DC    CL7'SHOCAT '
         STM   R14,R12,12(R13)         SAVE REGISTERS
         LR    R12,R15                 COPY ENTRY ADDR REGISTER
         USING SHOCAT,R12              DEFINE BASE REG AND ADDR
         ST    R13,SAVEAREA+4          STORE SAVE AREA ADDR
         LR    R2,R13                  ALSO STASH IT TEMPORARILY
         LA    R13,SAVEAREA            LOAD ADDR OF MY SAVE AREA
         ST    R13,8(R2)               STORE IT IN CALLING SAVE AREA
         LM    R2,R3,0(R1)             LOAD ADDRESSES OF PARMS
         MVC   DSN,0(R2)               MOVE IN DSN
*
*     ISSUE SHOWCAT
*
         SHOWCAT AREA=SHOWAREA,NAME=DSN
         ST    R15,0(R3)               RETURN REGISTER 15
         XR    R15,R15                 LOAD ZERO RETURN CODE
         L     R13,SAVEAREA+4          LOAD ADDR OF CALLING SAVE AREA
         L     R14,12(R13)             RESTORE R14
         LM    R0,R12,20(R13)          RESTORE R0 THROUGH R12
         BR    R14                     RETURN
*
*     CONSTANTS
*
         LTORG   ,
SAVEAREA DS    18F
DSN      DS    CL44
SHOWAREA DC    H'64'
         DS    CL62
         END   SHOCAT
