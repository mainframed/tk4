ROUTE    TITLE 'SUBROUTINE TO DYNAMICALLY UNALLOCATE DATA SETS'
*
*      THIS SUBROUTINE DYNAMICALLY UNALLOCATES THE DDNAME SPECIFIED
* BY THE CALLER. OPTIONALLY, THE SYSOUT CLASS AND THE REMOTE USER
* DESTINATION CAN BE RESET BY THE CALLER. IF THE CALLER DOES NOT
* WISH TO CHANGE THE OPTIONAL PARAMETERS, THEIR VALUES SHOULD BE
* SET TO SPACES. THE CALLING SEQUENCE FOR "ROUTE" IS AS FOLLOWS:
*           1. THE DDNAME TO BE UNALLOCATED (8 CHARACTERS)
*           2. THE SYSOUT CLASS TO WHICH THE DDNAME IS TO BE
*                  RESET (1 CHARACTER)
*           3. THE REMOTE DESTINATION TO WHICH THE DDNAME IS
*                  TO BE RESET (7 CHARACTERS)
*
*     UPON RETURN FROM "ROUTE", THE LEFT HALFWORD IN REGISTER 15
* CONTAINS THE SVC 99 INFORMATION REASON CODE AND THE RIGHT HALFWORD
* IN REGISTER 15 CONTAINS THE SVC 99 ERROR REASON CODE.
*
*     "ROUTE" IS REENTRANT AND REUSEABLE.
*
ROUTE    ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING WORKAREA,R13        ADDRESS WORK AREA DSECT
         USING S99RB,R7            ADDRESS SVC 99 REQUEST BLOCK MAP
         USING S99TUNIT,R8         ADDRESS SVC 99 TEXT UNIT MAP
         LM    R4,R6,0(R2)         LOAD PARAMETER POINTERS
         XC    DYNDATA(DYNLEN),DYNDATA  CLEAR SVC 99 DATA AREAS
         LA    R7,DYNRB            POINT TO SVC 99 REQUEST BLOCK
         MVI   S99RBLN,20          SET LENGTH TO 20
         MVI   S99VERB,S99VRBUN    INDICATE UNALLOCATION
         LA    R9,TUPTR0           POINT TO TEXT UNIT POINTER LIST
         ST    R9,S99TXTPP         STORE TEXT UNIT POINTER LIST ADDRESS
         LA    R8,UNATU            POINT TO UNALLOC OPTION TEXT UNIT
         ST    R8,TUPTR0           SAVE ADDRESS
         MVI   S99TUKEY+1,DUNUNALC INDICATE UNALLOC TEXT UNIT KEY
         MVI   S99TUNUM+1,0        INDICATE TEXT UNIT NUMBER OF 0
         LA    R9,TUPTR1           POINT TO TEXT UNIT POINTER LIST
         LA    R8,DUNTU            POINT TO DDNAME TEXT UNIT
         ST    R8,TUPTR1           SAVE ADDRESS
         MVI   S99TUKEY+1,DUNDDNAM INDICATE DDNAME TEXT UNIT KEY
         MVI   S99TUNUM+1,1        INDICATE TEXT UNIT NUMBER OF 1
         MVI   S99TULNG+1,8        SET DDNAME LENGTH TO 8
         MVC   S99TUPAR(8),0(R4)   MOVE IN DDNAME
         OI    TUPTR1,X'80'        MARK THE FIRST AS LAST
         TM    0(R2),X'80'         IF ONLY ONE PARAMETER PASSED
         BO    SVC99                  THEN GO UNALLOCATE
PARM2    DS    0H
         CLC   0(1,R5),BLANKS      IF THE SYSOUT CLASS IS BLANK
         BE    PARM3                  THEN SKIP THIS PARAMETER
         LA    R9,4(,R9)           POINT TO NEXT TEXT UNIT POINTER
         LA    R8,SOCTU            POINT TO SYSOUT TEXT UNIT
         ST    R8,0(,R9)           SAVE ADDRESS
         MVI   TUPTR1,X'00'        MARK THE TEXT UNIT
         MVI   0(R9),X'80'            POINTERS
         MVI   S99TUKEY+1,DUNOVCLS INDICATE SYSOUT TEXT UNIT KEY
         MVI   S99TUNUM+1,1        INDICATE TEXT UNIT NUMBER OF 1
         MVI   S99TULNG+1,1        SET SYSOUT CLASS LENGTH TO 1
         MVC   S99TUPAR(1),0(R5)   MOVE IN SYSOUT CLASS
PARM3    DS    0H
         TM    4(R2),X'80'         IF ONLY TWO PARAMETERS PASSED
         BO    SVC99                  THEN GO UNALLOCATE
         CLC   0(7,R6),BLANKS      IF THE REMOTE DEST IS BLANK
         BE    SVC99                  THEN SKIP THIS PARAMETER
         LA    R9,4(,R9)           POINT TO NEXT TEXT UNIT POINTER
         LA    R8,RDSTU            POINT TO REMOTE TEXT UNIT
         ST    R8,0(,R9)           SAVE ADDRESS
         MVI   TUPTR1,X'00'        MARK THE TEXT
         MVI   TUPTR1+4,X'00'         UNIT
         MVI   0(R9),X'80'          POINTERS
         MVI   S99TUKEY+1,DUNOVSUS INDICATE REMOTE TEXT UNIT KEY
         MVI   S99TUNUM+1,1        INDICATE TEXT UNIT NUMBER OF 1
         MVI   S99TULNG+1,7        SET REMOTE DEST LENGTH TO 7
         MVC   S99TUPAR(7),0(R6)   MOVE IN REMOTE DEST
SVC99    DS    0H
         ST    R7,DYNPTR           SAVE REQUEST BLOCK ADDRESS
         OI    DYNPTR,X'80'        INDICATE END OF PARAMETER LIST
         LA    R1,DYNPTR           POINT TO SVC 99 PARAMETER LIST
         DYNALLOC                  DYNAMICALLY UNALLOCATE
         ICM   R15,B'1100',S99INFO    SAVE INFO CODE IN RIGHT HALFWORD
         ICM   R15,B'0011',S99ERROR   SAVE ERROR CODE IN LEFT HALFWORD
         LEAVE RETCODE=(15),       RESTORE REGISTERS, RETURN TO CALLER X
               GETMAIN=(WORKSIZE,0)
         SPACE 3
BLANKS   DC    CL8' '
         EJECT
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
         DS    18A                 REGISTER SAVE AREA
DYNDATA  DS    0CL1                SVC99 DATA AREAS
DYNPTR   DS    A                   POINTER TO SVC 99 REQUEST BLOCK
DYNRB    DS    XL20                SVC 99 REQUEST BLOCK
TUPTR0   DS    A                   TEXT UNIT POINTER FOR UNALLOCATE
TUPTR1   DS    A                   TEXT UNIT POINTER FOR DDNAME
TUPTR2   DS    A                   TEXT UNIT POINTER FOR SYSOUT CLASS
TUPTR3   DS    A                   TEXT UNIT POINTER FOR REMOTE DEST
UNATU    DS    CL16                UNALLOCATE OPTION TEXT UNIT
DUNTU    DS    CL16                DDNAME UNALLOCATE TEXT UNIT
SOCTU    DS    CL16                RESET SYSOUT CLASS TEXT UNIT
RDSTU    DS    CL16                RESET REMOTE DEST TEXT UNIT
DYNLEN   EQU   *-DYNDATA
WORKSIZE EQU   *-WORKAREA
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         SPACE 3
         END
