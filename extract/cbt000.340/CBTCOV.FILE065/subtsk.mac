SUBTSK   TITLE 'PROGRAM WHICH ATTACHES A SUBTASK'
*
*      THIS PROGRAM ATTACHES A SUBTASK FROM THE PROGRAM LIBRARY
* SPECIFIED WITH DDNAME "TASKLIB". THE PROGRAM THEN WAITS FOR
* COMPLETION OF THE SUBTASK, DETACHES IT, AND RETURNS TO THE CALLER.
* THE PARM PASSED TO "SUBTSK" IS THE NAME OF THE PROGRAM TO BE
* ATTACHED.
*
*      "SUBTSK" IS REENTRANT AND REUSEABLE.
*
SUBTSK   ENTER PARMREG=2,        SAVE REGISTERS AND INITIALIZE         X
               GETMAIN=(WORKSIZE,0)
         REGISTER                REGISTER EQUATES
         USING WORKAREA,R13      ADDRESS WORK AREA DSECT
         MVC   TASKLIB,DUMMYDCB  MOVE DCB TO WORK AREA
         MVC   ATTLIST,CATTLST   MOVE ATTACH PARAMETER LIST TO WORK
         MVC   OPENLIST,COPNLST  MOVE OPEN PARAMETER LIST TO WORK
         LR    R5,R2             SAVE PARM REGISTER
         L     R2,0(,R2)         POINT TO PARM
         XC    0(2,R2),0(R2)     ZERO OUT PARM LENGTH
         MVC   PGMNAME,2(R2)     SAVE NAME OF PROGRAM TO BE ATTACHED
         LA    R4,TASKLIB        POINT TO TASKLIB DCB
         OPEN  ((R4)),              AND OPEN IT                        X
               MF=(E,OPENLIST)
         LR    R1,R5             RESTORE PARM REGISTER
         XC    ECB,ECB           CLEAR THE ECB
         ATTACH EPLOC=PGMNAME,   ATTACH THE SUBTASK                    X
               DCB=(R4),             LOOK IN TASKLIB FOR PROGRAM       X
               ECB=ECB,              SPECIFY ECB TO WAIT ON            X
               TASKLIB=(R4),         USE TASKLIB FOR DYNAMIC LOADS     X
               SF=(E,ATTLIST)
         ST    R1,TCBADDR        SAVE THE TCB ADDRESS
         LA    R1,ECB            GET ECB ADDRESS
         WAIT  ECB=(1)           WAIT FOR SUBTASK TO COMPLETE
         LA    R1,TCBADDR        LOAD ADDRESS OF SUBTASK'S TCB ADDRESS
         DETACH (1)                 AND DETACH THE SUBTASK
         CLOSE ((R4)),           CLOSE THE TASKLIB DCB                 X
               MF=(E,OPENLIST)
         LEAVE RETCODE=0,        RESTORE REGISTERS, RETURN TO CALLER   X
               GETMAIN=(WORKSIZE,0)
         EJECT
COPNLST  OPEN  (0),MF=L          DUMMY OPEN PARAMETER LIST
COPNLEN  EQU   *-COPNLST
         SPACE
CATTLST  ATTACH ,SF=L            DUMMY ATTACH PARAMETER LISTX
CATTLEN  EQU   *-CATTLST
         SPACE
DUMMYDCB DCB   DDNAME=TASKLIB,   DUMMY TASKLIB DCB                     X
               DSORG=PO,MACRF=(R)
DCBLEN   EQU   *-DUMMYDCB
         EJECT
WORKAREA DSECT ,                 MAP OF GETMAINED WORK AREA
         DS    18A               REGISTER SAVE AREA
TASKLIB  DS    CL(DCBLEN)        TASKLIB DCB
ATTLIST  DS    CL(CATTLEN)       ATTACH PARAMETER LIST
OPENLIST DS    CL(COPNLEN)       OPEN PARAMETER LIST
ECB      DS    F                 ECB FOR SUBTASK SYNCHRONIZATION
TCBADDR  DS    A                 ADDRESS OF SUBTASK'S TCB
RCODE    DS    F                 SAVE AREA FOR RETURN CODE
PGMNAME  DS    CL8               NAME OF PROGRAM TO BE ATTACHED
WORKSIZE EQU   *-WORKAREA
         END
