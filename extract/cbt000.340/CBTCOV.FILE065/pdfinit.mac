PDFINIT  TITLE 'PDFINIT DYNANIC ALLOCATION COMMAND FOR ISPF/PDF'
*
* THIS PGM FROM CONN BANK TAPE FILE 361 FROM REVLON WHICH CONTAINS A
* COPY OF THEIR TSO CP TO ALLOCATE THE PDF PROFILE FOR ISPF.
*
* THIS COMMAND WILL ALLOCATE THE PDF PROFILE DATASET AT LOGON TIME
* OR MAY BE INVOKED PRIOR TO INVOKING ISPF/PDF. THIS IS CONSIDERABLY
* FASTER THAN USING A CLIST TO ALLOCATE.
*
* MODIFIED FOR MVS/XA 4/84 BY:
*
*    STEVE HAGGERTY
*    TRANSACTION TECHNOLOGY INC. (TTI)
*    3100 OCEAN PARK BLVD.
*    SANTA MONICA, CA, 90405
*    (213) 450-9111
*
*
* THE PDF DATASET IS ALLOCATED AS OLD AND IF UNABLE TO ALLOCATE,
* TRYS TO ALLOCATE AND CATALOG A NEW PROFILE DATASET.
*
* CURRENT ALLOCATION IS AS:
*   ATTR FB6160 RECFM(F B) LRECL(80) BLKSIZE(6160)
*   ALLOC F(ISPPROF) DA('USERID.ISPPROF') SP(1) TRA DIR(1) -
*       UNIT(3380)  US(FB6160)  < VOL(XXXXXX) REMOVED >
*
* THIS CODE IS SET FOR ANY LENGTH TSO USER ID.           *4/84*SRH*
*
* ALLOCATION DEFAULTS CAN BE CHANGED IN THE DC'S FOR THE APPROPRIATE
* DEFAULTS.  I.E. DSNAME,  UNIT, ETC.
*
* CODE COULD BE ADDED TO TELL THE USER WHY THE ALLOCATION FAILED -
* BE MY GUEST. WE JUST FILTER OUT THE OBVIOUS ERROR AND RESPOND BACK
* WITH THE RETURN CODE, ERROR CODE AND INFO CODE IF A REAL ERROR
* OCCURS.
*
* THIS MODULE MAY BE ASSEMBLED AND LINKED INTO A LINKLIST LIB OR
* INTO LPALIB
*
* THE INVOCATION AT LOGON TIME IS BY USING THE PARM FIELD ON THE
* EXEC STATEMENT IN THE LOGON PROC
*
         EJECT
PDFINIT  CSECT
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3          BASE REG
R4       EQU   4
R5       EQU   5          ==> RB
R6       EQU   6          WORK REG
R7       EQU   7          WORK REG
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13         ==> GOTTEN AREA
R14      EQU   14
R15      EQU   15
         SPACE 2
         SAVE  (14,12),,PDFINIT.&SYSTIME..&SYSDATE
         LR    R3,R15        ESTABLISH BASE REGISTER
         USING PDFINIT,R3    AND ADDRESSABILITY
         SPACE
         GETMAIN R,LV=AREALEN GET AREA FOR REGISTER SAVE AND WORK
         SPACE
         LR    R10,R1        SAVE POINTER TO GOTTEN AREA
         MVI   0(R10),X'00'  CLEAR GOTTEN AREA TO 0
         MVC   1(255,R10),0(R10)
         MVC   257(AREALEN-256,R10),256(R10)
         ST    R13,4(R10)    CHAIN SAVE AREA ADDRESSES
         ST    R10,8(R13)
         LR    R13,R10
         SPACE
         USING WORKAREA,R13  ESTABLISH ADDRESSABILITY TO SAVE/WORK AREA
         EJECT
*        BUILD SVC99 REQUEST BLOCKS IN GOTTEN AREA TO DEALLOCATE THE
*        PROFILE DDNAME
         SPACE
         LA    R5,RB1        BUILD S99 RB PTR
         ST    R5,APRB1
         OI    APRB1,S99RBPND
         SPACE
         USING S99RB,R5
         MVI   S99RBLN,X'14' BUILD S99 RB
         MVI   S99VERB,S99VRBUN
         LA    R6,RB1P1
         ST    R6,S99TXTPP
         DROP  R5
         SPACE 2
         LA    R5,RB1TU1
         ST    R5,RB1P1
         USING S99TUNIT,R5   BUILD RB1TU1 - DDNAME
         MVI   S99TUKEY+1,DALDDNAM
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,L'DDNAME
         MVC   S99TUPAR(8),DDNAME
         OI    RB1P1,S99RBPND               FLAG END OF CHAIN
         SPACE 2
         DROP  R5
         SPACE
         LA    R1,APRB1      LOAD PARAMETER POINTER FOR SVC99
         DYNALLOC            ISSUE SVC99
         SPACE
         LTR   R15,R15       CHECK RETURN CODE
         BZ    ALLOCOLD
         SPACE
         LA    R5,RB1
         USING S99RB,R5
         CLC   S99ERROR,=X'0438'  SEE IF DDNAME NOT FOUND
         BE    ALLOCOLD           ... OK NO ERROR AFTER ALL
         DROP  R5
         SPACE
         BAL   R12,ERRMSGPR       GO TELL USER OF ERROR
         EJECT
*        BUILD SVC99 REQUEST BLOCKS IN GOTTEN AREA TO ALLOCATE THE
*        PROFILE DATASET AS OLD
         SPACE
ALLOCOLD LA    R5,RB1        BUILD S99 RB PTR
         ST    R5,APRB1
         OI    APRB1,S99RBPND
         SPACE
         USING S99RB,R5
         MVI   S99RBLN,X'14' BUILD S99 RB
         MVI   S99VERB,S99VRBAL
         MVI   S99FLG11,S99NOCNV
         LA    R6,RB1P1
         ST    R6,S99TXTPP
         DROP  R5
         SPACE 2
         LA    R5,RB1TU1
         ST    R5,RB1P1
         USING S99TUNIT,R5   BUILD RB1TU1 - DDNAME
         MVI   S99TUKEY+1,DALDDNAM
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,L'DDNAME
         MVC   S99TUPAR(8),DDNAME
         SPACE 2
         LA    R5,RB1TU2     BUILD RB1TU2 - DSNAME
         ST    R5,RB1P2
         MVI   S99TUKEY+1,DALDSNAM
         MVI   S99TUNUM+1,X'01'
         SPACE 2                                              *4/84*SRH
         L     R7,16       CVT PTR                            *4/84*SRH
         L     R7,0(,R7)   DISP D-WORD                        *4/84*SRH
         L     R7,4(,R7)   CURRENT TCB ADDR                   *4/84*SRH
         L     R7,180(,R7) CURRENT JSCB ADDR                  *4/84*SRH
         L     R7,264(,R7) CURRENT PSCB PTR                   *4/84*SRH
         SR    R2,R2       CLEAR R2                           *4/84*SRH
         ICM   R2,B'0001',7(R7) GET USERID LEN                *4/84*SRH
         LR    R8,R2       SAVE LEN                           *4/84*SRH
         BCTR  R2,0        LEN-1                              *4/84*SRH
MOVEID   MVC   S99TUPAR(0),0(R7) ***** EXECUTED ********      *4/84*SRH
         EX    R2,MOVEID   MOVE UID                           *4/84*SRH
         LA    R2,S99TUPAR PT TO BEGINNING OF TXT UNIT        *4/84*SRH
         LA    R2,0(R8,R2) ADD DISPL TO DSNAME SUFFIX         *4/84*SRH
         MVC   0(L'DSNAMEX,R2),DSNAMEX MOVE SUFFIX            *4/84*SRH
         LA    R2,L'DSNAMEX(,R8)  GET LEN OF TXT UNIT         *4/84*SRH
         STCM  R2,B'0001',S99TULNG+1 STOR LEN                 *4/84*SRH
         SPACE 2
         LA    R5,RB1TU4     BUILD RB1TU4 - STATUS
         ST    R5,RB1P4
         MVI   S99TUKEY+1,DALSTATS
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,X'01'
         MVI   S99TUPAR,DA08OLD             SET FOR OLD
         SPACE 2
         LA    R5,RB1TU5     BUILD RB1TU5 - DISPOSITION
         ST    R5,RB1P5
         MVI   S99TUKEY+1,DALNDISP
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,X'01'
         MVI   S99TUPAR,DA08KEEP            SET FOR KEEP
         OI    RB1P5,S99RBPND               FLAG END OF CHAIN
         SPACE 2
         DROP  R5
         SPACE
         LA    R1,APRB1      LOAD PARAMETER POINTER FOR SVC99
         DYNALLOC            ISSUE SVC99
         SPACE
         LTR   R15,R15       CHECK SVC99 RETURN CODE
         BZ    RETURN        0 - WE ALLOCATED IT AS OLD - LETS GO HOME
         SPACE
         LA    R5,RB1
         USING S99RB,R5
         CLC   S99ERROR,=X'1708'  SEE IF DSNAME NOT FOUND
         BE    ALLOCNEW           ... OK NO ERROR AFTER ALL
         DROP  R5
         SPACE
         BAL   R12,ERRMSGPR       GO TELL USER OF ERROR
         EJECT
*        OK - MUST NOT HAVE ONE SO LETS BUILD A NEW PROFILE DATASET
*
*        BUILD MORE SVC99 REQUEST BLOCKS IN GOTTEN AREA TO ALLOCATE
*        THE PROFILE DATASET AS NEW, CATLG
         SPACE 2
         USING S99TUNIT,R5
         SPACE
ALLOCNEW LA    R5,RB1TU4     BUILD RB1TU4 - STATUS
         MVI   S99TUPAR,DA08NEW             SET FOR NEW
         SPACE 2
         LA    R5,RB1TU5     BUILD RB1TU5 - DISPOSITION
         ST    R5,RB1P5      RESET END OF CHAIN FLAG
         MVI   S99TUPAR,DA08CAT             SET FOR CATLG
         SPACE 2
         LA    R5,RB1TU7     BUILD RB1TU7 - TRACK ALLOCATION
         ST    R5,RB1P7
         MVI   S99TUKEY+1,DALTRK
         MVI   S99TUNUM+1,X'00'
         SPACE 2
         LA    R5,RB1TUA     BUILD RB1TUA - PRIMARY SPACE
         ST    R5,RB1PA
         MVI   S99TUKEY+1,DALPRIME
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,X'03'
         MVC   S99TUPAR+1(2),TRKS           SET FOR # TRACK
         SPACE 2
         LA    R5,RB1TUC     BUILD RB1TUC - DIRECTORY BLKS
         ST    R5,RB1PC
         MVI   S99TUKEY+1,DALDIR
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,X'03'
         MVC   S99TUPAR+1(2),DIRBLKS        SET FOR # DIR BLK
         SPACE 2
*                                                            *4/84*SRH
* NOTE: DON'T SPECIFY A VOLSER  -----SRH-------              *4/84*SRH
*                                                            *4/84*SRH
*        LA    R5,RB1TU10    BUILD RB1TU10- VOL SER          *4/84*SRH
*        ST    R5,RB1P10                                     *4/84*SRH
*        MVI   S99TUKEY+1,DALVLSER                           *4/84*SRH
*        MVI   S99TUNUM+1,X'01'                              *4/84*SRH
*        MVI   S99TULNG+1,X'06'                              *4/84*SRH
*        MVC   S99TUPAR(6),VOLSER           MOVE IN VOLSER   *4/84*SRH
*        SPACE 2                                             *4/84*SRH
         LA    R5,RB1TU15    BUILD RB1TU15- UNIT
         ST    R5,RB1P15
         MVI   S99TUKEY+1,DALUNIT
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,L'UNIT
         MVC   S99TUPAR(L'UNIT),UNIT        MOVE IN UNIT
         SPACE 2
         LA    R5,RB1TU30    BUILD RB1TU30- BLKSIZE
         ST    R5,RB1P30
         MVI   S99TUKEY+1,DALBLKSZ
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,X'2'
         MVC   S99TUPAR(2),BLKSIZE          MOVE IN BLKSIZE
         SPACE 2
         LA    R5,RB1TU3C    BUILD RB1TU3C- DSORG
         ST    R5,RB1P3C
         MVI   S99TUKEY+1,DALDSORG
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,X'2'
         MVI   S99TUPAR,X'02'               MOVE IN DSORG=PO
         SPACE 2
         LA    R5,RB1TU42    BUILD RB1TU42- LRECL
         ST    R5,RB1P42
         MVI   S99TUKEY+1,DALLRECL
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,X'02'
         MVC   S99TUPAR(2),LRECL            MOVE IN LRECL
         SPACE 2
         LA    R5,RB1TU49    BUILD RB1TU49- RECFM
         ST    R5,RB1P49
         MVI   S99TUKEY+1,DALRECFM
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,X'1'
         MVI   S99TUPAR,X'80'               MOVE IN RECFM=FB
         SPACE
         OI    RB1P49,S99RBPND              FLAG END OF CHAIN
         SPACE
         DROP  R5
         SPACE 2
         LA    R1,APRB1      LOAD PARAMETER POINTER FOR SVC99
         DYNALLOC            ISSUE SVC99
         SPACE
         LTR   R15,R15       CHECK SVC99 RETURN CODE
         BZ    RETURN0       0 - WE ALLOCATED OK
         SPACE
         BAL   R12,ERRMSGPR  GO TELL USER OF ERROR
         SPACE
         LA    R1,MSGBAD       - UNABLE TO ALLOCATE - TELL THE USER
         LA    R0,L'MSGBAD
         B     TPUT
         SPACE 2
RETURN0  LA    R1,MSGGOOD      - TELL THE USER WE CREATED A NEW ONE
         LA    R0,L'MSGGOOD
         SPACE
TPUT     TPUT  (R1),(R0)     WRITE OUT RESULTS OF NEW ALLOCATION
         SPACE
RETURN   LR    R1,R13        GET POINTER TO GOTTEN AREA FOR FREEMAIN
         LA    R0,AREALEN    AND LENGTH TO FREE
         FREEMAIN R,LV=(0),A=(1) FREE GOTTEN AREA
         SPACE
         L     R13,4(R13)
         RETURN (14,12),RC=0 RETURN FROM WHENCE WE CAME
         EJECT
ERRMSGPR LA    R5,RB1        POINT TO THE S99 REQUEST BLOCK
         USING S99RB,R5
         SPACE
         ST    R15,DW        GET RETURN CODE AND MAKE READABLE
         UNPK  W2,DW(5)
         NC    W2(8),NCSTRING
         TR    W2(8),TRTAB
         MVC   ERRMSGR1,W2
         SPACE
         LH    R15,S99ERROR GET ERROR CODE AND MAKE READABLE
         ST    R15,DW
         UNPK  W2,DW(5)
         NC    W2(8),NCSTRING
         TR    W2(8),TRTAB
         MVC   ERRMSGE1,W2+4
         SPACE
         LH    R15,S99INFO  GET INFO CODE AND MAKE READABLE
         ST    R15,DW
         UNPK  W2,DW(5)
         NC    W2(8),NCSTRING
         TR    W2(8),TRTAB
         MVC   ERRMSGI1,W2+4
         SPACE
         MVC   ERRMSGRC,=C'RC='
         MVI   ERRMSGS1,C' '
         MVC   ERRMSGEC,=C'ERROR CODE='
         MVI   ERRMSGS2,C' '
         MVC   ERRMSGIC,=C'INFO CODE='
         SPACE
         LA    R1,ERRMSG
         LA    R0,ERRMSGL
         SPACE
         TPUT  (R1),(R0)
         SPACE
         BR    R12
         SPACE
         DROP  R5
         SPACE 2
NCSTRING DC    XL8'0F0F0F0F0F0F0F0F'
TRTAB    DC    CL16'0123456789ABCDEF'
         EJECT
* CURRENT ALLOCATION IS AS:
*   ATTR FB6160 RECFM(F B) LRECL(80) BLKSIZE(6160)
*   ALLOC F(ISPPROF) DA('USERID.ISPPROF') SP(1) TRA DIR(1) - *4/84*SRH
*       UNIT(TSO)  US(FB6160)  <VOL(XXXXXX) REMOVED>         *4/84*SRH
         SPACE
DDNAME   DC    C'ISPPROF '                DDNAME - MUST BE ISPPROF
DSNAMEX  DC    C'.ISPPROF'                DSNAME SUFFIX      *4/84*SRH
TRKS     DC    H'01'                      TRKS   - # OF TRKS TO ALLOC
DIRBLKS  DC    H'01'                      DIRBLKS- # OF DIRECTORY BLKS
UNIT     DC    C'TSO'                     UNIT   - UNIT TO ALLOC   SRH
         SPACE
**** VOLSER REMOVED                                          *4/84*SRH
*VOLSER   DC    C'S80222'                 VOLSER - VOLSER TO ALLOC SRH
**** VOLSER REMOVED                                          *4/84*SRH
         SPACE
LRECL    DC    H'80'                      LRECL  - MUST BE 80 FOR ISPF
BLKSIZE  DC    H'6160'                    BLKSIZE-
         SPACE 2
MSGGOOD  DC    C'*** ISPF PROFILE DATA SET HAS BEEN CREATED ***'
MSGBAD   DC    C'*** UNABLE TO ALLOCATE ISPF PROFILE DATASET ***'
         SPACE 2
WORKAREA DSECT                   DSECT FOR GOTTEN AREA
         SPACE
DW       DS    D
W2       DS    CL9
         DS    CL3
REGAREA  DS    18F               REGISTER SAVE AREA
         SPACE
APRB1    DS    F                 POINTER TO SVC99 REQUEST BLOCKS
         SPACE
RB1      DS    5F                SVC 99 REQUEST BLOCK
         SPACE
RB1P1    DS    F                 POINTER TO SVC99 TEXT UNIT 01
RB1P2    DS    F                 POINTER TO SVC99 TEXT UNIT 02
RB1P4    DS    F                 POINTER TO SVC99 TEXT UNIT 04
RB1P5    DS    F                 POINTER TO SVC99 TEXT UNIT 05
RB1P7    DS    F                 POINTER TO SVC99 TEXT UNIT 07
RB1PA    DS    F                 POINTER TO SVC99 TEXT UNIT 0A
RB1PC    DS    F                 POINTER TO SVC99 TEXT UNIT 0C
RB1P10   DS    F                 POINTER TO SVC99 TEXT UNIT 10
RB1P15   DS    F                 POINTER TO SVC99 TEXT UNIT 15
RB1P30   DS    F                 POINTER TO SVC99 TEXT UNIT 30
RB1P3C   DS    F                 POINTER TO SVC99 TEXT UNIT 3C
RB1P42   DS    F                 POINTER TO SVC99 TEXT UNIT 42
RB1P49   DS    F                 POINTER TO SVC99 TEXT UNIT 49
         SPACE
MAXUIDL  EQU   7                 MAX LEN TSO UID                  SRH
MAXDSN   EQU   L'DSNAMEX+MAXUIDL MAX LEN DSN                      SRH
RB1TU1   DS    XL14              SVC99 TEXT UNIT 01 - DDNAME
RB1TU2   DS    XL(6+MAXDSN)      SVC99 TEXT UNIT 02 - DSNAME      SRH
RB1TU4   DS    XL7               SVC99 TEXT UNIT 04 - DS STATUS
RB1TU5   DS    XL7               SVC99 TEXT UNIT 05 - DS DISPOSITION
RB1TU7   DS    XL4               SVC99 TEXT UNIT 07 - TRACK ALLOC
RB1TUA   DS    XL9               SVC99 TEXT UNIT 0A - PRIMARY SPACE
RB1TUC   DS    XL9               SVC99 TEXT UNIT 0C - DIRECTORY BLKS
RB1TU10  DS    XL12              SVC99 TEXT UNIT 10 - VOL SER
RB1TU15  DS    XL(6+L'UNIT)      SVC99 TEXT UNIT 15 - UNIT
RB1TU30  DS    XL8               SVC99 TEXT UNIT 30 - BLKSIZE
RB1TU3C  DS    XL8               SVC99 TEXT UNIT 3C - DSORG
RB1TU42  DS    XL8               SVC99 TEXT UNIT 42 - LRECL
RB1TU49  DS    XL7               SVC99 TEXT UNIT 49 - RECFM
         SPACE
ERRMSG   DS    0C
ERRMSGRC DS    CL3
ERRMSGR1 DS    CL8
ERRMSGS1 DS    CL1
ERRMSGEC DS    CL11
ERRMSGE1 DS    CL4
ERRMSGS2 DS    CL1
ERRMSGIC DS    CL10
ERRMSGI1 DS    CL4
         SPACE
ERRMSGL  EQU   *-ERRMSG
         SPACE
AREALEN  EQU   *-WORKAREA
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         EJECT
         IKJDAP08
         SPACE
         END
