SMFDSNS  BEGIN BASE2=3
         OPEN  (SMFDATA,INPUT)
         LA    R9,20
         LA    R10,OUTPUT
BES      MVC   0(80,R10),=CL80' '
         LA    R10,80(R10)
         BCT   R9,BES
         LA    R7,20
         LA    R8,OUTPUT
         USING LINE,R8
         LA    R1,FSPARMS
         MVI   MODE,C'I'
         MVC   ROW,=H'0'
         MVC   COLUMN,=H'0'
         MVI   UPPER,C'U'
         LINK  EP=FULSCR
GETDSN   MVI   MODE,C'X'
         MVC   INATTR,LOLITE
         MVC   INPUT,=CL79' '
         MVC   DSNATTR,UNPROT
         MVC   MSGS,=CL79'ENTER THE DSN/ALL BELOW                 '
         MVC   ROW,=H'0'
         MVC   COLUMN,=H'0'
         MVI   ALARM,C'A'
         LA    R1,FSPARMS
         LINK  EP=FULSCR
         CLC   DSNPARM,=CL44' '
         BE    GETDSN
         MVC   MSGS,=CL79' '
         MVC   DSNATTR,HILITE
NEWPAGE  DS    0H
         LA    R9,20
         LA    R10,OUTPUT
CLRPAGE  MVC   1(79,R10),=CL79' '
         LA    R10,80(R10)
         BCT   R9,CLRPAGE
         LA    R7,20
         LA    R8,OUTPUT
         USING LINE,R8
READER   GET   SMFDATA,SMFREC
         LA    R2,SMFREC
         USING SMF14HDR,R2
         MVC   ATTR,LOLITE
         CLI   SMF14RTY,14
         BNE   TST2
         MVC   TYPE,INREC
         B     TYPE14
TST2     CLI   SMF14RTY,15
         BNE   TST3
         MVC   TYPE,OUTREC
         MVC   ATTR,HILITE
         B     TYPE14
TST3     CLI   SMF14RTY,17
         BNE   TST4
         MVC   TYPE,SCRREC
         MVC   ATTR,HILITE
         B     TYPE17
TST4     CLI   SMF14RTY,18
         BNE   TST5
         MVC   TYPE,RENREC
         MVC   ATTR,HILITE
         B     TYPE18
TST5     CLI   SMF14RTY,62
         BNE   TST6
         MVC   TYPE,OPNREC
         MVC   ATTR,LOLITE
         B     TYPE62
TST6     CLI   SMF14RTY,63
         BNE   TST7
         MVC   TYPE,DEFREC
         MVC   ATTR,HILITE
         B     TYPE63
TST7     CLI   SMF14RTY,67
         BNE   TST8
         MVC   TYPE,DELREC
         MVC   ATTR,HILITE
         B     TYPE67
TST8     CLI   SMF14RTY,68
         BNE   TST9
         MVC   TYPE,VRNREC
         MVC   ATTR,HILITE
         B     TYPE68
TST9     CLI   SMF14RTY,69
         BNE   READER
         MVC   TYPE,DSPREC
         B     TYPE69
TYPE14   MVC   JOB,SMF14JBN            INITIALIZE JOBNAME
         MVC   DATE,=X'202020202020202020'
         ED    DATE-1(9),SMF14RSD
         MVC   DSN,SMFJFCB1            INITIALIZE DSN
         XR    R5,R5                   7 OUT DIVIDEND
         TCONV SMF14TME,HOUR,R5        LOAD SMF TIME
         B     PUTIT                   BRANCH TO TPUT ROUTINE
TYPE17   DS    0H
         DROP  R2
         USING SMF17HDR,R2
         MVC   JOB,SMF17JBN            INITIALIZE JOBNAME
         MVC   DATE,=X'202020202020202020'
         ED    DATE-1(9),SMF17RSD
         MVC   DSN,SMF17DSN            INITIALIZE DSN
         XR    R5,R5                   7 OUT DIVIDEND
         TCONV SMF17TME,HOUR,R5        LOAD SMF TIME
         B     PUTIT                   BRANCH TO TPUT ROUTINE
TYPE18   DS    0H
         DROP  R2
         USING SMF18HDR,R2
         MVC   JOB,SMF18JBN            INITIALIZE JOBNAME
         MVC   DATE,=X'202020202020202020'
         ED    DATE-1(9),SMF18RSD
         MVC   DSN,SMF18ODS            INITIALIZE DSN
         XR    R5,R5                   7 OUT DIVIDEND
         TCONV SMF18TME,HOUR,R5        LOAD SMF TIME
         B     PUTIT                   BRANCH TO TPUT ROUTINE
TYPE62   DS    0H
         DROP  R2
         USING SMFRCD62,R2
         MVC   JOB,SMF62JBN            INITIALIZE JOBNAME
         MVC   DATE,=X'202020202020202020'
         ED    DATE-1(9),SMF62RSD
         MVC   DSN,SMF62DNM            INITIALIZE DSN
         XR    R5,R5                   7 OUT DIVIDEND
         TCONV SMF62TME,HOUR,R5        LOAD SMF TIME
         B     PUTIT                   BRANCH TO TPUT ROUTINE
TYPE63   DS    0H
         DROP  R2
         USING SMFRCD63,R2
         MVC   JOB,SMF63JBN            INITIALIZE JOBNAME
         MVC   DATE,=X'202020202020202020'
         ED    DATE-1(9),SMF63RSD
         MVC   DSN,SMF63ENM            INITIALIZE DSN
         XR    R5,R5                   7 OUT DIVIDEND
         TCONV SMF63TME,HOUR,R5        LOAD SMF TIME
         B     PUTIT                   BRANCH TO TPUT ROUTINE
TYPE67   DS    0H
         DROP  R2
         USING SMFRCD67,R2
         MVC   JOB,SMF67JBN            INITIALIZE JOBNAME
         MVC   DATE,=X'202020202020202020'
         ED    DATE-1(9),SMF67RSD
         MVC   DSN,SMF67DEN            INITIALIZE DSN
         XR    R5,R5                   7 OUT DIVIDEND
         TCONV SMF67TME,HOUR,R5        LOAD SMF TIME
         B     PUTIT                   BRANCH TO TPUT ROUTINE
TYPE68   DS    0H
         DROP  R2
         USING SMFRCD68,R2
         MVC   JOB,SMF68JBN            INITIALIZE JOBNAME
         MVC   DATE,=X'202020202020202020'
         ED    DATE-1(9),SMF68RSD
         MVC   DSN,SMF68ONM            INITIALIZE DSN
         XR    R5,R5                   7 OUT DIVIDEND
         TCONV SMF68TME,HOUR,R5        LOAD SMF TIME
         B     PUTIT                   BRANCH TO TPUT ROUTINE
TYPE69   DS    0H
         DROP  R2
         USING SMFRCD69,R2
         MVC   JOB,SMF69JBN            INITIALIZE JOBNAME
         MVC   DATE,=X'202020202020202020'
         ED    DATE-1(9),SMF69RSD
         MVC   DSN,SMF69CNM            INITIALIZE DSN
         XR    R5,R5                   7 OUT DIVIDEND
         TCONV SMF69TME,HOUR,R5        LOAD SMF TIME
PUTIT    DS    0H
         CLC   DSNPARM,ALLDSN
         BE    PUTIT2
         CLC   DSN,DSNPARM
         BE    PUTIT2
         LA    R4,DSNPARM
         LA    R5,DSN
         LA    R6,44
C1       DS    0H
         CLI   0(R4),C'-'
         BE    PUTIT2
         CLC   0(1,R5),0(R4)
         BE    C2
         CLI   0(R4),C'*'
         BNE   READER
C2       LA    R4,1(R4)
         LA    R5,1(R5)
         BCT   R6,C1
PUTIT2   MVC   INATTR,LOLITE
         MVC   INPUT,=CL79' '
         MVI   MODE,C'P'
         MVI   ALARM,C' '
         MVC   ROW,=H'0'
         MVC   COLUMN,=H'0'
         LA    R1,FSPARMS
         LINK  EP=FULSCR
         LA    R8,80(R8)
         BCT   R7,READER
GETIT    MVC   INATTR,UNPROT
         MVC   MSGS(40),=CL40'ENTER ''S'' FOR NEW DSN, ''Z'' TO END'
         MVC   MSGS+40(39),=CL39'JUST HIT ENTER TO CONTINUE'
         MVI   MODE,C'X'
         MVI   ALARM,C'A'
         MVC   ROW,=H'0'
         MVC   COLUMN,=H'0'
         LA    R1,FSPARMS
         LINK  EP=FULSCR
         CLI   INPUT,C'Z'
         BE    EXIT
         CLI   INPUT,C'S'
         BE    NEWDSN
         MVC   MSGS,=CL79' '
         B     NEWPAGE
NEWDSN   CLOSE (SMFDATA)
         OPEN  (SMFDATA,INPUT)
         B     GETDSN
EOD      DS    0H
         MVC   INATTR,UNPROT
         MVC   MSGS(79),=CL79'ENTER ''S'' FOR NEW DSN, ''Z'' TO END'
         MVC   ATTR,HILITE
         MVC   LINE+1(79),=CL79'**** END OF INPUT DATA *****'
         MVI   MODE,C'X'
         MVI   ALARM,C'A'
         MVC   ROW,=H'0'
         MVC   COLUMN,=H'0'
         LA    R1,FSPARMS
         LINK  EP=FULSCR
         CLI   INPUT,C'Z'
         BE    EXIT
         CLI   INPUT,C'S'
         BE    NEWDSN
         B     EOD
EXIT     CLOSE (SMFDATA)
         EXIT  RC=0
         LTORG
         REGISTER
*
ALLDSN   DC    CL44'ALL'
INREC    DC    CL2'SI'
OUTREC   DC    CL2'SO'
SCRREC   DC    CL2'SS'
RENREC   DC    CL2'SR'
OPNREC   DC    CL2'VO'
DEFREC   DC    CL2'VD'
DELREC   DC    CL2'VS'
VRNREC   DC    CL2'VR'
DSPREC   DC    CL2'VA'
UNPROT   SF    (UNPROT,RED)
LOLITE   SF    (PROT,BLUE)
HILITE   SF    (PROT,WHITE)
FSPARMS  DS    0D
         DC    A(MODE)
         DC    A(SCREEN)
         DC    A(LINES)
         DC    A(ROW)
         DC    A(COLUMN)
         DC    A(ALARM)
         DC    A(UPPER)
         DC    A(KEY)
MODE     DC    C'I'
LINES    DC    H'0'
ROW      DC    H'0'
COLUMN   DC    H'0'
ALARM    DC    C' '
UPPER    DC    C'U'
KEY      DC    H'0'
*
SCREEN   DS    0CL2560
         SF    (PROT,WHITE)
HEADING  DC    CL79'I/O/S   DATE   TIME      JOBNAME   DATA SET NAME'
OUTPUT   DS    CL1600
INATTR   SF    (PROT,WHITE)
INPUT    DC    CL79' '
         SF    (PROT,WHITE)
MSGS     DC    CL79' '
         SF    (PROT,BLUE)
         DC    CL34' DATA SET NAME BEING SEARCHED FOR:'
DSNATTR  SF    (PROT,WHITE)
DSNPARM  DC    CL44' '
         SF    (PROT,BLUE)
         DC    CL160' '         FOR 3279 COMPATIBILITY
         DC    CL160' '         FOR 3279 COMPATIBILITY
         DC    CL160' '         FOR 3279 COMPATIBILITY
         DC    CL160' '         FOR 3279 COMPATIBILITY
SMFDATA  DCB   DDNAME=SMFDATA,DSORG=PS,MACRF=GM,EODAD=EOD,EROPT=SKP
SMFREC   DS    CL32767
LINE     DSECT
ATTR     DS    CL01
         DC    CL02' '
TYPE     DS    CL02
         DC    CL02' '
DATE     DS    CL07
         DC    CL02' '
HOUR     DS    CL08
         DC    CL02' '
JOB      DS    CL08
         DC    CL02' '
DSN      DS    CL44
         IFASMFR (14,17,18,62,63,67,68,69)
         END
