EDATEBX  TITLE 'EUROPEAN DATE SUBROUTINE'
*
*      THIS PROGRAM OBTAINS THE EUROPEAN DATE ("DDMMYY"). IF THE DATE
* IS OBTAINED SUCCESSFULLY, 0 IS RETURNED IN REGISTER 15. IF THE DATE
* IS NOT OBTAINED SUCCESSFULLY, 12 IS RETURNED IN REGISTER 15.
* THE CALLING SEQUENCE FOR "EDATEBX" IS AS FOLLOWS
*           1. THE OUTPUT EUROPEAN DATE IN THE FORMAT "DDMMYY"
*
*      THE "EDATEBX" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
EDATEBX  ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING WORKAREA,R13        ADDRESS WORKAREA DSECT
         MVC   CALLIST,CCALLST     MOVE PARAMETER LIST TO WORK AREA
         LA    R4,JDATE            POINT TO JULIAN DATE FIELD
         LOAD  EP=JULDATX          LOAD ROUTINE TO OBTAIN JULIAN DATE
         LR    R15,R0              SAVE "JULDATX" ENTRY POINT ADDRESS
         CALL  (15),((R4)),       CALL "JULDATX"                       X
               MF=(E,CALLIST)
         LTR   R15,R15             IF THE SUBROUTINE FAILED
         BNZ   ERROR                  THEN FLAG FOR ERROR
         L     R5,0(,R2)           POINT TO EUROPEAN DATE FIELD
         LOAD  EP=EJDATEX          LOAD EUROPEAN CONVERSION ROUTINE
         LR    R15,R0              SAVE "EJDATEX" ENTRY POINT ADDRESS
         CALL  (15),((R4),(R5)),  CALL "EJDATEX"                       X
               MF=(E,CALLIST)
         LTR   R15,R15             IF THE SUBROUTINE FAILED
         BNZ   ERROR                  THEN FLAG FOR ERROR
         B     EXIT                   AND LEAVE
ERROR    DS    0H
         LA    R15,12              INDICATE ERROR IN INPUT
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
         EJECT
CCALLST  CALL  ,(,),MF=L            GENERATE PARAMETER LIST FOR CALL
CALLEN   EQU   *-CCALLST           COMPUTE PARAMETER LIST LENGTH
         SPACE
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
WORKSTRT EQU   *
         DS    18A                 REGISTER SAVE AREA
CALLIST  DS    CL(CALLEN)          PARAMETER LIST FOR CALL MACRO
JDATE    DS    CL5                 JULIAN DATE
WORKEND  EQU   *
WORKSIZE EQU   WORKEND-WORKSTRT    COMPUTE SIZE OF WORKAREA
         END
