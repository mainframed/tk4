USVR     TITLE 'ROUTINE TO ACF2 VALIDATE CATALOG ACCESS'
XACFUSVR CSECT
         ACCVT ,                  ACF2 CVT MAPPING MACRO
USVRWORK DSECT
         DS    18F                SAVE AREA
         ACDSV DSECT=NO           ACF2 DSN VALIDATION PARM LIST
USVRDSN  DS    CL44               DSN WORK AREA
USVRMSG  DS    CL128              MSG WORK AREA
USVRWLEN EQU   ((*-USVRWORK+7)/8)*8
XACFUSVR CSECT
         ACFREGS LINK=R14
         LR    R12,R15            SAVE ENTRY POINT
         USING XACFUSVR,R12       SET CSECT ADDRESSABILITY
         LR    R11,R14            SAVE RETURN ADDRESS
         LR    R2,R1              SAVE ADDRESS OF PARAMETER LIST
         GETMAIN R,LV=USVRWLEN    GET STORAGE FOR WORK AREA
         LR    R13,R1             SAVE WORK AREA ADDRESS
         USING USVRWORK,R13       SET WORK AREA ADDRESSABILITY
         XC    ACDSV(ACFSPRML),ACDSV CLEAR PARM LIST
         MVI   ACFSPREQ,ACFSPRDS  SET DSN VALIDATION ONLY
         OI    ACFSPREQ,ACFSPRFF  SET MSG RETURN BIT
         MVI   ACFSPID1,ACFSPIUR  INDICATE USER CALL
         MVI   ACFSPAC1,ACFSPAUP  INDICATE UPDATE ACCESS
         MVC   USVRDSN,0(R2)      MOVE DSN INTO WORK AREA
         LA    R1,USVRDSN         GET ADDRESS OF DSN
         ST    R1,ACFSPDSN        STORE ADDRESS OF DSN
         LA    R1,USVRMSG         GET ADDRESS OF MSG
         ST    R1,ACFSPMSG        STORE ADDRESS OF MSG
         ACFSVC ACDSV,TYPE=S,     ISSUE ACF2 VALIDATION SVC            X
               NONE=ERROR,                                             X
               CVT=FIND
         LR   R10,R15             SAVE RETURN CODE
RETURN   EQU   *
         FREEMAIN R,A=(R13),LV=USVRWLEN FREE WORK AREA
         LR    R15,R10            RESTORE RETURN CODE
         BR    R11                RETURN TO MAIN LINE
ERROR    EQU   *
         LA    R10,12             SET RETURN CODE
         B     RETURN             RETURN
         END   XACFUSVR
