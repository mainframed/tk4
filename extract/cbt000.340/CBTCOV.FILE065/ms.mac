MS       TITLE 'MS - TSO MASTER SUMMARY COMMAND'
         MACRO
&NAME    TSDERASE &N
&NAME    BCR   0,0                      NOP
         MEND
         SPACE 1
         PRINT NOGEN
MS       CSECT
         SAVE  (14,12),T,MS_TSO_COMMAND
         USING MS,R15
         LA    R11,SAVEAREA
         ST    R13,SAVEAREA+4
         ST    R11,8(,R13)
         LR    R13,R11
         B     START
         DROP  R15
         SPACE 2
SAVEAREA DC    18F'0'
         SPACE 2
START    DS    0H
         USING SAVEAREA,R13
         SPACE 2
         MVC  INPUT(80),=CL80' '        BLANK INPUT
         MVC  INPUT(2),=C'J '           SET UP DEFAULT
         MVC  OLDINPUT,INPUT            SAVE FOR REPEAT
         B    CMDJ                      GO DO 1ST TIME
         SPACE 2
MAINLOOP TSDERASE                       ERASE THE SCREEN
         L     R1,16                    FIND CVT
         L     R1,X'22C'(,R1)           FIND ASVT
         LA    R3,X'210'(,R1)           FIND FIRST ENTRY
         LA    R4,4                     BXLE INCREMENT
         L     R5,X'204'(,R1)           MAX ADDR SPEC
         SLA   R5,2                     MULT BY FOUR
         LA    R5,0(R3,R5)              POINT TO END
         BCTR  R5,0                     BACK UP ONE
         SPACE
         CLC   =C'SS',PROMPT            STATS COMMAND?
         BE    DOSS                     YES, GO DO IT
         SPACE
         TPUT  HEADER,L'HEADER
         SPACE 2
LOOP     TM    0(R3),X'FF'              CHECK BITS ON
         BNZ   LOOPBXLE                 SKIP IF ONE ON
         L     R2,0(,R3)                PICK UP POINTER TO ASCB
         MVC   LINE(80),=CL80' '        BLANK LINE
         ICM   R1,15,172(R2)            JOB NAME FOR INIT PGMS
         BZ    *+10
         MVC   LINE+15(8),0(R1)
         ICM   R1,15,176(R2)            JOBNAME FOR START MOUNT ETC
         BZ    *+10
         MVC   LINE+5(8),0(R1)
         SPACE
         ICM   R1,15,56(R2)             FIND CSCB POINTER
         BZ    TESTSYS                  NO POINTER, MUST BE SYSTASK
         TM    28(R1),B'00000011'       CHECK FOR SYSTEM TASKS
         BZ    TESTSYS                  BOTH OFF MEANS STARTED TASK
         BO    TESTINIT                 BOTH ON MEANS A JOB
         CLC   =C'INIT',LINE+5          INIT OR TSU IS LEFT
         BE    TESTINIT                 IF BIT ON, OK
         TM    28(R1),B'00000001'       CHECK FOR TSU
         BZ    TESTSYS                  NO, MUST BE STC
         SPACE
TESTTSU  CLI   PROMPT,C'T'              ASKING FOR TSU
         BNE   LOOPBXLE
         B     LIKEIT                   ELSE REJECT
         MVC   LINE+15(8),16(R1)        COPY IN PROCNAME
         SPACE
TESTINIT CLI   PROMPT,C'I'              ASKING FOR INIT?
         BE    LIKEIT
         B     LOOPBXLE                 ELSE REJECT
         SPACE
TESTSYS  CLI   PROMPT,C'S'              WANT SYS TASKS?
         BNE   LOOPBXLE                 IF NOT, WE NO LIKE
         LTR   R1,R1                    CSCB PRESENT?
         BZ    LIKEIT                   NO, SKIP THE FOLLOWING
         MVC   LINE+22(3),24(R1)        MOVE IN UNIT ID
         MVC   LINE+5(8),8(R1)          MOVE IN ID
         MVC   LINE+14(8),16(R1)        MOVE IN PROCNAME
         SPACE
LIKEIT   LH    R1,36(,R2)               PICK UP ASID
         CVD   R1,DOUBLE
         UNPK  LINE(3),DOUBLE
         OI    LINE+2,C'0'              MAKE IT PRINTABLE
         SPACE
         LH    R1,120(,R2)              ALLOC AUX SLOTS (VAM)
         SLA   R1,2                     MULT BY 4 TO GET IT IN 'K'
         CVD   R1,DOUBLE
         MVC   LINE+25(7),=X'4020206B202120'  EDIT MASK
         ED    LINE+25(7),DOUBLE+5      DO THE EDIT
         MVI   LINE+32,C'K'
         LH    R1,122(,R2)              ALLOC AUX SLOTS (NON-VAM)
         SLA   R1,2                     MULT BY 4 TO GET IT IN 'K'
         CVD   R1,DOUBLE
         MVC   LINE+35(7),=X'4020206B202120'  EDIT MASK
         ED    LINE+35(7),DOUBLE+5      DO THE EDIT
         MVI   LINE+42,C'K'
         LH    R1,152(,R2)              REAL STORAGE IN USE
         SLA   R1,2                     MULT BY 4 TO GET IT IN 'K'
         CVD   R1,DOUBLE
         MVC   LINE+45(7),=X'4020206B202120'  EDIT MASK
         ED    LINE+45(7),DOUBLE+5      DO THE EDIT
         MVI   LINE+52,C'K'
         LM    R0,R1,64(R2)             PICK UP CPU TIME
         SRDL  R0,24                    MOVE TO ODD REG
         M     R0,=A(1048576)           COMPUTE IN SECONDS
         D     R0,=A(10000*256)
         CVD   R1,DOUBLE
         MVC   LINE+55(10),=X'4020206B2021204B2020'
         ED    LINE+55(10),DOUBLE+4      DO THE EDIT
         MVI   LINE+65,C'S'
         LM    R0,R1,200(R2)            PICK UP SRB TIME
         SRDL  R0,24                    MOVE TO ODD REG
         M     R0,=A(1048576)           COMPUTE IN SECONDS
         D     R0,=A(10000*256)
         CVD   R1,DOUBLE
         MVC   LINE+66(10),=X'4020206B2021204B2020'
         ED    LINE+66(10),DOUBLE+4      DO THE EDIT
         MVI   LINE+76,C'S'
         UNPK  DOUBLE(3),43(2,R2)       CONVERT DPRTY TO HEX
         TR    DOUBLE(2),HEXTAB
         MVC   LINE+78(2),DOUBLE        MOVE FOR PRNTING
         SPACE
         TPUT  LINE,L'LINE
LOOPBXLE BXLE  R3,R4,LOOP
         SPACE 2
ASKAGAIN TPUT  PROMPT,L'PROMPT,ASIS
         TGET  INPUT,L'INPUT            GET COMMAND
         SPACE
         OC    INPUT(2),=C'  '          SHIFT TO UPPER CASE
         CLI   INPUT,C' '               NULL LINE?
         BNE   NOTNULL                  NO,SKIP THE FOLLOWING
         MVC   INPUT,OLDINPUT           SET COMMAND TO SAME AS LASTTIME
NOTNULL  MVC   OLDINPUT,INPUT           SAVE OLD INPUT
         CLC   =C'SS',INPUT             SYSTEM STATS
         BE    CMDSS
         CLC   =C'LP',INPUT             SYSTEM STATS
         BE    CMDLP
         CLC   =C'TS',INPUT             TSO    STATS
         BE    CMDTS
         CLI   INPUT,C'I'               INIT COMMAND?
         BE    CMDI
         CLI   INPUT,C'J'               JOBS COMMAND?
         BE    CMDJ
         CLI   INPUT,C'S'               TSC?
         BE    CMDS
         CLI   INPUT,C'T'               TSU
         BE    CMDT
         CLI   INPUT,C'E'               EXIT?
         BE    EXIT
         CLI   INPUT,C'?'
         BE    OPTIONS
         CLI   INPUT,C'H'
         BE    OPTIONS
         B     MAINLOOP
         SPACE
CMDI     MVC   PROMPT(2),=C'IN'
         MVC   HEADER+15(9),=CL9'JOB'
         B     MAINLOOP
         SPACE
CMDTS    MVC   PROMPT(2),=C'TS'
         MVC   HEADER+15(9),=CL80' '
         B     MAINLOOP
         SPACE
CMDT     MVC   PROMPT(2),=C'T '
         CALL  USERS                    CALL USERS COMMAND
         B     ASKAGAIN
         SPACE
CMDJ     MVC   PROMPT(2),=C'J '
         CALL  JOBS                     CALL JOBS COMMAND
         B     ASKAGAIN
         SPACE
CMDS     MVC   PROMPT(2),=C'S '
         MVC   HEADER+15(9),=CL9'PROC/UNIT'
         B     MAINLOOP
         SPACE
CMDSS    MVC   PROMPT(2),=C'SS'
         BE    MAINLOOP
         SPACE
CMDLP    MVC   PROMPT(2),=C'LP'
* LPA LISTER
         TSDERASE
         L     R2,16                    FIND CVT
LPAFIRST L     R2,X'0BC'(,R2)           FIND 1ST LPDE
         ICM   R2,15,0(R2)              SKIP DUMMY
         BZ    ASKAGAIN                 IF NULL
LPALOOP  MVC   LINE(80),=CL80' '        BLANK LINE
         LA    R3,LINE
         LA    R4,26                    BXLE SETUP
         LA    R5,LINE+80-25
LPALOOP1 MVC   7(8,R3),8(R2)            MOVE IN MODULE NAME
         ST    R2,FWORK                 CONVERT LPDE ADDR TO HEX
         UNPK  DOUBLE(7),FWORK+1(4)
         TR    DOUBLE(6),HEXTAB
         MVC   0(6,R3),DOUBLE
         UNPK  DOUBLE(7),17(4,R2)       CONVERT EPA
         TR    DOUBLE(6),HEXTAB
         MVC   17(6,R3),DOUBLE
         ICM   R2,15,0(R2)              FOLLOW PNTR
         BZ    LPATPUT
         BXLE  R3,R4,LPALOOP1
LPATPUT  TPUT  LINE,L'LINE
         LTR   R2,R2
         BNZ   LPALOOP
         B     ASKAGAIN
         SPACE
OPTIONS  TSDERASE
         MVC   PROMPT(2),=C'H '
         TPUT  OP1,L'OP1
         TPUT  OP2,OPLEN
         B     ASKAGAIN
         SPACE
DOSS     SR    R7,R7                    VIO SLOTS USED COUNTER
         SR    R8,R8                    REG SLOTS IN USE COUNTER
         SR    R9,R9                    WORKING SET ACCUM
SSLOOP   TM    0(R3),X'FF'              ENTRY IN USE
         BNZ   SSBXLE                   NO, SKIP
         L     R2,0(,R3)                PICK UP ASCB POINTER
         AH    R7,120(,R2)              PICK UP VIO PAGES
         AH    R8,122(,R2)              PICK UP AUX PAGES
         AH    R9,152(,R2)              PICK UP REAL PAGES
SSBXLE   BXLE  R3,R4,SSLOOP
         SPACE 1
         MVC   LINE(80),=CL80' '
         MVC   LINE+29(21),=C'* SYSTEM STATISTICS *'
         TPUT  LINE,L'LINE
         SPACE
         L     R2,16                    FIND CVT
         L     R3,X'C4'(,R2)            FIND SMCA
         MVC   LINE(80),=CL80' '
         MVC   LINE(7),=C'SYSTEM-'
         MVC   LINE+7(4),16(R3)         MOVE IN SYSTEM ID
         S     R2,=F'6'                 BACK UP TO CVT PREFIX
         MVC   LINE+15(8),=C'RELEASE-'
         MVC   LINE+23(2),2(R2)         FIRST HALF OF REL NO
         MVI   LINE+25,C'.'
         MVC   LINE+26(2),4(R2)         SECOND HALF OF REL NO
         UNPK  DOUBLE(5),0(3,R2)        MODEL NUM
         TR    DOUBLE+1(3),HEXTAB
         MVC   LINE+30(10),=C'CPU MODEL-'
         MVC   LINE+40(3),DOUBLE+1
         MVC   LINE+45(9),=C'CPU SIZE-'
         L     R3,312+6(,R2)
         LA    R3,1(,R3)                BUMP BY ONE
         SRL   R3,10                    CONVERT TO K
         CVD   R3,DOUBLE
         MVC   LINE+54(7),=X'4020206B202120'  EDIT MASK
         ED    LINE+54(7),DOUBLE+5      DO THE EDIT
         MVI   LINE+61,C'K'
         TPUT  LINE,L'LINE
         SPACE
         MVC   LINE(80),=CL80' '
         MVC   LINE(16),=C'VIO SLOTS IN USE'
         SLA   R7,2                     MULT BY 4 TO MAKE K
         CVD   R7,DOUBLE
         MVC   LINE+20(10),=X'40206B2020206B202120'
         ED    LINE+20(10),DOUBLE+4
         MVI   LINE+30,C'K'
         MVC   LINE+40(17),=C'A.S. SLOTS IN USE'
         SLA   R8,2                     MULT BY 4 TO MAKE K
         CVD   R8,DOUBLE
         MVC   LINE+60(10),=X'40206B2020206B202120'
         ED    LINE+60(10),DOUBLE+4
         MVI   LINE+70,C'K'
         TPUT  LINE,L'LINE
         SPACE
         MVC   LINE(80),=CL80' '
         MVC   LINE(17),=C'SLOT SPACE IN USE'
         AR    R7,R8                    SUM UP
         CVD   R7,DOUBLE
         MVC   LINE+20(10),=X'40206B2020206B202120'
         ED    LINE+20(10),DOUBLE+4
         MVI   LINE+30,C'K'
         MVC   LINE+40(17),=C'REAL SPACE IN USE'
         SLL   R9,2                     MULT BY 4 TO GET K
         CVD   R9,DOUBLE
         MVC   LINE+60(10),=X'40206B2020206B202120'
         ED    LINE+60(10),DOUBLE+4
         MVI   LINE+70,C'K'
         TPUT  LINE,L'LINE
         SPACE
         MVC   LINE(80),=CL80' '
         MVC   LINE(17),=C'AVAIL FRAME SPACE'
         L     R1,16                    FIND CVT
         L     R7,X'164'(,R1)           FIND PVT
         LH    R8,2(,R7)                LOAD AVAIL COUNT
         SLA   R8,2                     MULT BY 4 TO GET K
         CVD   R8,DOUBLE
         MVC   LINE+20(10),=X'40206B2020206B202120'
         ED    LINE+20(10),DOUBLE+4
         MVI   LINE+30,C'K'
         MVC   LINE+40(18),=C'USABLE FRAME SPACE'
         LH    R8,8(,R7)                USABLE FRAME COUNT
         SLL   R8,2                     MULT BY 4 TO GET K
         CVD   R8,DOUBLE
         MVC   LINE+60(10),=X'40206B2020206B202120'
         ED    LINE+60(10),DOUBLE+4
         MVI   LINE+70,C'K'
         TPUT  LINE,L'LINE
         SPACE
         MVC   LINE(80),=CL80' '
         MVC   LINE(19),=C'CSA/LPA FRAME SPACE'
         LH    R8,292(,R7)
         SLA   R8,2                     MULT BY 4 TO GET K
         CVD   R8,DOUBLE
         MVC   LINE+20(10),=X'40206B2020206B202120'
         ED    LINE+20(10),DOUBLE+4
         MVI   LINE+30,C'K'
         MVC   LINE+40(12),=C'NUCLEUS SIZE'
         L     R3,16                    FIND CVT
         L     R2,128(,R3)              PICK UP END OF NUC+1
         SRL   R2,10                    CONVERT TO K
         CVD   R2,DOUBLE
         MVC   LINE+60(10),=X'40206B2020206B202120'
         ED    LINE+60(10),DOUBLE+4
         MVI   LINE+70,C'K'
         TPUT  LINE,L'LINE
         SPACE
         B     ASKAGAIN
         SPACE
EXIT     TSDERASE
         L     R13,SAVEAREA+4
         RETURN (14,12),RC=0
         SPACE 2
DOUBLE   DC    D'0'
FWORK    DC    F'0'
LINE     DC    CL80' '
INPUT    DC    CL80' '
OLDINPUT DC    CL80' '
PROMPT   DC    CL2'IN'
HEADER   DC    CL80'ASID NAME                 --VIO--   -SLOTS-   WKG S*
               ET     CPU TIME   SRB TIME PR'
OP1      DC    CL40'* * OPTIONS CURRENTLY SUPPORTED * *'
OP2      DC    CL40'E  - END THIS PROCESSOR'
OP3      DC    CL40'H  - THIS DISPLAY (''?'' IS AN ALIAS)'
OP4      DC    CL40'I  - INITIATOR DISPLAY'
OP5      DC    CL40'J  - RUNNING JOBS DISPLAY'
OP6      DC    CL40'LP - DISPLAY OF MODULES IN LPA'
OP7      DC    CL40'S  - STARTED TASK DISPLAY'
OP8      DC    CL40'SS - SYSTEM PERFORMANCE STATISTICS'
OP9      DC    CL40'T  - TSO USER DISPLAY'
OP10     DC    CL40'TS - TSO USER DISPLAY W/STATISTICS'
OPLEN    EQU   *-OP2
         SPACE
         LTORG
         SPACE
HEXTAB   EQU   *-C'0'
         DC    C'0123456789ABCDEF'
         SPACE 2
         REGS
         END   MS
