*          DATA SET LINKDATE   AT LEVEL 005 AS OF 03/06/78
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*        THIS PROGRAM PROVIDES A DATE STAMP FOR THE LINKAGE
*        EDITOR AND GIVES SYSLMOD LIBRARY NAME AND TIME ON
*        THE LINKAGE EDITOR OUTPUT.  IT ALSO PROVIDES SOURCE
*        NAME AND PANVALET LEVEL FROM PASSED INFORMATION.
*
*        INSTALLED 2/24/78 BY JAN REDDING
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
LINKDATE CSECT
         COPY  REGS
         SAVE  (14,12)
         LR    R12,R13
         LR    R3,R1
         BALR  R6,0
         USING *,R6
         LA    R13,SAVEAREA
         ST    R12,4(R13)
         ST    R13,8(R12)
         TITLE 'ROUTINES TO PROVIDE LINK EDIT DATA'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*              ROUTINE TO PROVIDE DATE AND TIME                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TIME  DEC                     GET DATE AND TIME
*              PROVIDE DATE
         ST    R1,DATEAREA
GETNORML MVO   WRKYEAR,DATEAREA+1(1)   TAKE THE YEAR FROM THE DATE
         CVB   R10,DBLWRD
         STC   R10,ONECHAR             CHECK FOR MULTIPLE OF FOUR
         TM    ONECHAR,X'03'           ARE BITS 6 AND/OR 7 ON
         BNZ   NOLEAP                  YES - BRANCH (NO LEAP YEAR)
         ZAP   FEB,=P'29'              CHANGE FEB TO 29 DAYS
NOLEAP   LA    R10,TABLE
         ZAP   MNTCTR,=P'1'            SET COUNTER
         ZAP   DAYCTR,DATEAREA+2(2)
LOOP1    CP    DAYCTR,0(2,R10)         IS THIS CORRECT MONTH
         BNH   FOUND1                  YES - BRANCH
         SP    DAYCTR,0(2,R10)
         AP    MNTCTR,=P'1'
         LA    R10,2(R10)              INCREMENT SUB
         B     LOOP1
FOUND1   UNPK  MONTH,MNTCTR            FORMAT
         OI    MONTH+1,X'F0'                 DATE
         UNPK  DAY,DAYCTR
         OI    DAY+1,X'F0'
         UNPK  YEAR,WRKYEAR
*              PROVIDE TIME
         ST    R0,DATEAREA             GET TIME OF DAY
         MVI   HOURMIN+3,X'0C'
         MVO   HOURMIN(3),DATEAREA(2)
         UNPK  TIME,HOURMIN(3)
         OI    TIME+3,X'F0'
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*              ROUTINE TO PROVIDE LIBRARY AND MEMBER                *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         RDJFCB (SYSLMOD)              READ JFCB
         MVC   LIBRARY,LIBNAME         GET DSN OF LIBRARY
         MVC   MEMBER,MEMNAME          GET MEMBER NAME
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*              ROUTINE TO PROVIDE PAN NAME AND LEVEL                *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         OPEN  SYSPASS
         TM    SYSPASS+48,X'10'        TEST FOR GOOD OPEN
         BNO   SLINOPEN          BAD OPEN
         GET   SYSPASS,PASSAREA
         CLC   =C'TMP',PASSAREA        CHECK FOR TEMP UPDATE
         BNE   BYPASSI                 NO... GO MOVE LEVEL TO SETSSI
         MVC   LEVEL,=C'FF'            YES... MOVE IN INDICATOR
         B     BYCONT                  BYPASS SETSSI MOVE
BYPASSI  EQU   *
         MVC   LEVEL,PASSAREA+1        LAST 2 DIGITS OF SOURCE LEVEL
BYCONT   EQU   *                       LEVEL IN SETSSI
         MVC   SLEVEL,PASSAREA         ALL OF SOURCE LEVEL
         MVC   SNAME,PASSAREA+3        SOURCE NAME
         LA    R9,10                   CONSTANT 10 FOR BCT LOOP
         LA    R8,SNAME                START OF SOURCE NAME
LPRNLOOP EQU   *                       LOOK FOR 1ST SPACE IN SOURCE NM
         CLI   0(R8),X'40'             SPACE?
         BE    PRNINSRT                YES...INSERT LEFT PAREN
         LA    R8,1(R8)                INCR TO NEXT POSITION
         BCT   R9,LPRNLOOP             IF DONE INSERT IN POS 11
PRNINSRT EQU   *                       INSERT LEFT PAREN
         MVI   0(R8),X'5D'
         B     PANCLOSE
PASSEOF  EQU   *
         MVC   PANINFO+1(30),PANINFO   SPACE OUT PAN INFO AT EOF
PANCLOSE EQU   *
         CLOSE SYSPASS
SLINOPEN EQU   *                       TO BYPASS PANCLOSE
         TITLE 'PUT LINK EDITOR CARDS  AND LINK TO HEWL'
*******************************************************************
*              PUT THE LINKAGE EDITOR  INPUT CARDS                *
*******************************************************************
         OPEN  (SYSLIN,(OUTPUT))
         PUT   SYSLIN,CARD1
         PUT   SYSLIN,CARD2
         CLOSE (SYSLIN)
         LR    R1,R3
         LINK  EP=HEWL
         LR    R13,R12
         RETURN (14,12),RC=(15)
         TITLE 'CONSTANTS AND WORK AREAS'
*******************************************************************
*              DCB'S, CONSTANTS AND WORK AREAS                    *
*******************************************************************
         PRINT NOGEN
SAVEAREA DS    18F
SYSPASS  DCB   DDNAME=SYSPASS,DSORG=PS,RECFM=F,MACRF=GM,               X
               EODAD=PASSEOF,LRECL=21,BLKSIZE=21
SYSLIN   DCB   DSORG=PS,DDNAME=SYSLIN,MACRF=PM,LRECL=80,BLKSIZE=80,    X
               RECFM=FB
SYSLMOD  DCB   DDNAME=SYSLMOD,EXLST=GETNAME,DSORG=PO,MACRF=R
GETNAME  DC    X'87',AL3(WORKAREA)
WORKAREA DS    0CL256
LIBNAME  DS    CL44
MEMNAME  DS    CL08
HOURMIN  DS    CL03
         DS    CL201
DATEAREA DS    F
DBLWRD   DS    0D
         DC    F'0'
         DC    H'0'
WRKYEAR  DC    X'000F'
ONECHAR  DS    CL1
DAYCTR   DS    PL2
MNTCTR   DS    PL2
TABLE    DC    PL2'31'
FEB      DC    PL2'28'
         DC    X'031F030F031F030F031F031F030F031F030F031F'
*                 M   A   M   J   J   A   S   O   N   D
*                 A   P   A   U   U   U   E   C   O   E
*                 R   R   Y   N   L   G   P   T   V   C
*
PASSAREA DS    CL21
CARD1    DS    0CL80                   FIRST CARD
         DC    CL26' INCLUDE THISPGM  LIBRARY='
LIBRARY  DC    CL44' '
         DC    CL10'  00000010'        CARD NUMBER
CARD2    DS    0CL80                   SECOND CARD
         DC    CL8' SETSSI '
YEAR     DS    CL2
MONTH    DS    CL2
DAY      DS    CL2
LEVEL    DC    C'00'
         DC    CL6' TIME='
TIME     DS    CL4
         DC    CL5' MEM='
MEMBER   DS    CL8
PANINFO  DS    0CL31
         DC    CL11' PAN(LEVEL='
SLEVEL   DS    CL3
         DC    CL6' NAME='
SNAME    DS    CL10
         DC    CL11'   00000020'
         LTORG
         END
