*          DATA SET DATASETL   AT LEVEL 001 AS OF 08/12/75
*          DATA SET DATASETL   AT LEVEL 001 AS OF 07/22/75
*          DATA SET DATASETL   AT LEVEL 001 AS OF 07/22/75
*          DATA SET DATASETL   AT LEVEL 003 AS OF 07/21/75
DATASETL CSECT
         TITLE '     USER DOCUMENTATION'
         SPACE 18
*                    SUPERLIST UTILITY - 'DATASETL'
*
*
*                             HOWARD MOODY
*                        A. O. SMITH CORPORATION
*                          MILWAUKEE WISCONSIN
*                   PHONE: (414) 873-3000  EXT 26-251
*
*
         EJECT
         SPACE 10
*        DATASETL IS A UTILITY PROGRAM THAT WILL SEQUENTIALLY PROCESS
*    THE VTOC ON ANY GIVEN 3330-1, 3330-11 OR 2314 DISK PACK AND LIST
*    PARTICULAR INFORMATION ABOUT EACH DATA SET ON THE PACK. THIS
*    UTILITY ALSO REFERENCES THE CATALOG TO DETERMINE IF THE DATA SETS
*    ARE CATALOGED TO THIS PACK AND IF NOT WHERE THEY ARE CATALOGED TO.
         SPACE 10
*        ADVANTAGES OF THIS UTILITY OVER OTHER SUPERLIST PROGRAMS ARE
*    THAT IT IS MORE COMPACT, EASIER TO READ, CONTAINS CATALOG INFORMA-
*    TION AND ALLOWS THE USER TO CONTROL THE OUTPUT. THESE CONTROLS ARE
*    ESTABLISHED THROUGH A PARM LIST. A DISADVANTAGE COULD BE THAT IT
*    DOES NOT LIST ANY DCB INFORMATION FOR THE DATA SETS. WHEN USING
*    THIS UTILITY IT SHOULD BE NOTED THAT IT DOES MAKE USE THE 'RDJFCB'
*    INSTRUCTION WHICH HAS CAUSED PROBLEMS WHEN USED IN A TSO ENVIRON-
*    MENT. HOWEVER, STEPS WERE TAKEN TO RELIEVE THIS PROBLEM (AS
*    DOCUMENTED BY IBM) AND NO PROBLEMS HAVE OCCURED SINCE.
         EJECT
         SPACE 2
*        THE JCL NECESSARY TO RUN THIS PROGRAM FOLLOWS:
*    //JOB CARD
*    //STEP1      EXEC PGM=DATASETL<,PARM='PARMLIST'>      *1
*    //STEPLIB    DD DSN=LIBRARY,DISP=SHR
*    //SYSPRINT   DD SYSOUT=A
*    //SYSUDUMP   DD SYSOUT=A
*    //NAME1      DD UNIT=UNIT1,VOL=SER=VOLSER1,DISP=SHR
*    //NAME2      DD UNIT=UNIT2,VOL=SER=VOLSER2,DISP=SHR
*    //  "         "  "  "  "  "  "  "  "  "  "  "  "  "
*    //NAMEN      DD UNIT=UNITN,VOL=SER=VOLSERN,DISP=SHR   **2
         SPACE
*        WHERE UNIT(1...N) AND VOLSER(1...N) ARE THOSE OF THE PACKS TO
*    BE PROCESSED WHILE NAME(1...N) CAN BE ANY SET OF CHARACTERS
*    FOLLOWING OS STANDARDS FOR DDNAMES EXCEPT THE FOLLOWING:
*        SYSIN,SYSPRINT,SYSABEND,SYSUDUMP,SYSLIB,SYSPROC,MACLIB,
*        PLIDUMP,PL1DUMP,STEPLIB,JOBLIB,PANDD1, & PANDD2
         SPACE
*    *1  -- THE < > INDICATES THE CHARACTER STRING WITHIN IS OPTIONAL
*    **2 -- N IS BOUNDED ONLY BY OS LIMITATIONS FOR THE NUMBER OF DD
*           CARDS ALLOWED PER STEP.
         SPACE 5
*        IF NO PARM LIST IS SPECIFIED, THE DEFAULT OUTPUT CONSISTS
*    OF THE FOLLOWING:
*    0) HEADINGS  - DESCRIBING THE OUTPUT AND LABELING THE FIELDS.
*    1) VOLSER    - OF THE PACK BEING LISTED.
*    2) DSN       - DATA SET NAME.
*    3) SPACE     - DESCRIPTION, CONSISTING OF SPACE ALLOCATED, USED,
*                   AND UNUSED. IF THE USED AND UNUSED FIELDS ARE
*                   PRECEDED BY A '*', THIS IS TO INDICATE THIS FIELD
*                   FOR THIS PARTICULAR DATA SET IS NOT ACCURATE. IT
*                   WILL OCCUR WHENEVER THE DATA SET IS NOT EITHER A
*                   SEQUENTIAL OR PARTIONED DATA SET.
*    4) DATE      - CREATION DATE FOR THIS DATA SET.
*    5) STATISTICS  CONTAINS THE FOLLOWING FIELDS OF STATISTICS ABOUT
*                   THE DATA SETS:
*                 - WHETHER INITIAL REQUEST WAS IN TRK, CYL, OR BLK(S)
*                 - WHETHER INITIAL REQUEST WAS FOR CONTIGUOUS
*                   ('CONTIG') SPACE OR NOT.
*                 - WHETHER THE  DSCB TTR POINTER IN THE CATALOG IS
*                   ACCURATE (BLANK) OR NOT ('-','*', OR '0').
*                 - WHETHER DATA SET IS CATALOGED (BLANK) TO THIS PACK
*                   OR NOT ('**UNCATL**').
*    6) FORMAT5   - FREE SPACE DESCRIPTION SUMMARY FOR THE PACK.
         EJECT
         SPACE 6
*    PARMLIST = 'NONE' OR
*             = 'TPDK' OR
*             = 'DDN=' OR
*             = ANY COMBINATION OF THE FOLLOWING (FMT5,HDNG,STAT,CYLS,
*                                                      DATE,DSCB,DDN=)
         SPACE 2
*        BEFORE DEFINING THE PARMS IT SHOULD BE NOTED THAT THE EXIST-
*    ANCE OF ANY PARM ON THE EXEC CARD CAUSES THE DEFAULT OUTPUT TO BE
*    REDUCED TO VOLSER, DSN, AND SPACE (WITHOUT HEADINGS) OR 1), 2),
*    AND 3) ABOVE. THUS THE PARM LIST DESCRIBES THOSE FIELDS THE USER
*    WANTS LISTED ALONG WITH THE DEFAULT OUTPUT.
*
*    'NONE'  - ONLY THE DEFAULT OUTPUT LISTED (VOLSER,DSN, & SPACE).
*    'DDN='  - SPECIFIES THAT THE ONLY DDNAME TO BE PROCESSED FOLLOWS
*              THE '=' SIGN. THIS PARM  M U S T  EITHER BE THE ONLY
*              PARM LISTED OR THE LAST ONE IN PARMLIST!!!!! THIS PARM
*              IS MOST USEFUL IN A TSO ENVIRONMENT TO ELIMINATE THE
*              EXTRANEOUS OUTPUT CAUSED BY THE NUMEROUS DATA SETS
*              ALLOCATED AT LOGON. (DDNAME MUST FOLLOW OS STANDARDS).
*    'FMT5'  - INCLUDE FREE SPACE DESCRIPTION SUMMARY.
*    'HDNG'  - INCLUDE HEADINGS FOR THOSE FIELDS IN OUTPUT.
*    'STAT'  - INCLUDE STATISTICS AS DESCRIBED ABOVE. THIS PARM
*              AUTOMATICALLY CAUSES 'DATE' TO BE INCLUDED.
*    'CYLS'  - CAUSES SPACE DESCRIPTION TO BE GIVEN IN CYLINDERS.
*    'DATE'  - INCLUDES THE CREATION DATE IN OUTPUT.
*    'DSCB'  - CAUSES ALL MODEL DSCB'S TO BE INCLUDED IN OUTPUT. THEY
*              WILL BE MARKED WITH '**MODEL DSCB**' IN THE SPACE
*              DESCRIPTION FIELD AND THE DATE WILL BE INCLUDED IF THE
*              'DATE' OR 'STAT' PARM IS PRESENT.
*    'TPDK'  - CAUSES A SPECIAL OUTPUT FORMAT USED ONLY FOR THE A. O.
*              SMITH TAPE/DISK ACCOUNTING SYSTEM. THE OUTPUT INCLUDES
*              VOLSER, DSN, SPACE(IN CYLINDERS), AND DATE.
         EJECT
         SPACE 5
*        MISCELLANEOUS INFORMATION-----
         SPACE 2
*     -----USER ABEND 999 OCCURS IF AN OPEN IS UNSUCCESSFUL. IT IS
*          MOST LIKELY THAT THE SYSPRINT STATEMENT IS EITHER MISSING
*          OR MISSPELLED.
         SPACE
*     -----USER ABEND 990 OCCURS IF THERE IS A BAD PARMLIST. USER
*          SHOULD CHECK THE SPELLING OF THE PARMLIST.
         SPACE
*     -----USER ABEND 995 OCCURS IF PARM 'DDN=' IS NOT EITHER ALONE OR
*          AT THE END OF THE PARMLIST. THERE IS THE POSSIBILITY TOO,
*          THAT EITHER USER ABEND 990 OR NOTHING AT ALL WILL HAPPEN
*          IF THIS PARM IS IN ERROR.
         SPACE
*     -----THE SYSPRINT OUTPUT DATA SET HAS THE FOLLOWING DCB INFO:
*               LRECL=133, BLKSIZE=665, RECFM=FBA
         SPACE
*     -----IF A VOLSER FOLLOWS THE '**UNCATL**' MESSAGE, THIS INDICATES
*          THAT THE DSN IN QUESTION IS NOT CATALOGED TO THE PROCESSED
*          PACK BUT INSTEAD TO THE PACK WITH THE VOLSER LISTED. IN
*          ADDITION, IF AN INTEGER FOLLOWS THIS LISTED VOLSER, THEN
*          THIS DATA SET IS A MULTI-VOLUME DATA SET CATALOGED TO THAT
*          MANY ADDITIONAL VOLUMES.
         SPACE
*     _____THE UNLABELED FIELD TWO COLUMNS LEFT OF THE  "'TRK','CYL',
*          'BLK'"  FIELD INDICATES WHETHER OR NOT THE CATALOG DSCB
*          POINTER IS ACCURATE.THE FOLLOWING IS A TABLE OF MEANINGS:
*            ' ' - INDICATES THE TTR IS ACCURATE.
*            '0' - INDICATES THE CATALOG TTR IS ZERO.
*            '-' - INDICATES NO CATALOG INFORMATION AVAILABLE.
*            '*' - INDICATES THE CATALOG TTR POINTS TO THE WRONG DSCB.
         SPACE
*     -----SHOULD A 'MOUNT REQ. XXXXXX' MESSAGE APPEAR, THIS MEANS ONLY
*          THAT TO DETERMINE WHETHER OR NOT THIS DATA SET IS CATALOGED,
*          THE VOLUME WITH VOLSER 'XXXXXX' MUST BE MOUNTED.
         SPACE
*     -----SEVERAL ERROR AND WARNING MESSAGES EXIST WITHIN THIS PROGRAM
*          THEY ARE SELF EXPLANITORY AND SHOULD HARDLY EVER BE SEEN.
         TITLE '     DATASETL SOURCE LISTING'
*
*
*   REGISTER EQUATES
*
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
**********************************************************************
*
*   SAVE THE REGISTERS, PROCESS THE PARM LIST, OPEN OUTPUT FILE AND
*   ESTABLISH POINT OF BRANCH (TO RESTORE REGISTERS) WHEN PROCESSING
*   IS FINISHED.
*
*
         USING *,R12                REG 12 -- BASE
         STM   R14,R12,12(R13)      SAVE REGISTERS
         LR    R12,R15              SET UP REG 12
         LR    R14,R13              REG 14 -- SAVE AREA
         CNOP  0,4                  ALLIGN ON FULL WORD
         BAL   R13,NULL0001         SET UP REG 13 TO MY SAVEAREA & BR
         DC    3F'0'                SAVE--
         DS    15F                      --AREA
RETURN   L     R13,4(0,R13)         POINT OF BRANCH WHEN DONE---
*                                   RESTORE REG 13
         L     R14,12(0,R13)        RESTORE REG 14
         LM    R2,R12,28(R13)       RESTORE REMAINDER OF REGS
         BR    R14                  RETURN TO CALLING ROUTINE
NULL0001 ST    R13,8(0,R14)         SAVE ORIG. REG 13 IN CALLING RT SA
         ST    R14,4(0,R13)         SAVE ORIG. REG 14 IN CALLING RT SA
         L     R10,SECBASE          SET UP SECOND BASE REG.
         USING DATASETL+4096,R10
*
*   INITIALLY REG 1 POINTS TO THE PARM LIST. THIS LIST IS PROCESSED
*   AND SWITCHES ARE SET SO THROUGHOUT THE PROGRAM, IT IS KNOWN
*   WHAT OPTIONS THE USER SPECIFIED.
*
*
         L     R1,0(R1)             REG 1 - PTR TO PARM LIST
         LH    R3,0(R1)             REG 3 - LENGTH OF PARM LIST
         LTR   R3,R3                IS LENGTH ZERO?
         BZ    START                YES - BR TO BEGIN PROCESSING PROG.
         XC    SWITCH,SWITCH        NO - CLEAR SWITCHES & PROCESS PARM
         LA    R1,2(R1)             REG 1 POINTS TO 1ST ON PARM LIST
CHK001   MVC   PARM(4),0(R1)        GET NEXT PARM
         CLC   PARM,=C'NONE'        WAS IT 'NONE'
         BNE   PRM001               NO - CONTINUE CHECKING
         XC    SWITCH,SWITCH        TURN OFF ALL SWITCHES
         B     START                BR TO START (IGNORE OTHER PARMS)
PRM001   CLC   PARM,=C'FMT5'        WAS IT 'FMT5'
         BNE   PRM002               NO - CONTINUE
         OI    SWITCH,X'01'         TURN ON FMT5 SWITCH
         B     SETPARAM             BR TO SET LOOPING PARAMETERS
PRM002   CLC   PARM,=C'HDNG'        WAS IT 'HDNG'
         BNE   PRM003               NO - CONTINUE
         OI    SWITCH,X'02'         TURN ON HDNG SWITCH
         B     SETPARAM             BR TO LOOP
PRM003   CLC   PARM,=C'STAT'        WAS IT STAT
         BNE   PRM004               NO - CONTINUE
         OI    SWITCH,X'14'         TURN ON STAT SWITCH (& DATE)
         B     SETPARAM
PRM004   CLC   PARM,=C'CYLS'        WAS IT CYLS
         BNE   PRM005               NO - CONTINUE
         OI    SWITCH,X'08'         TURN ON CYLS SWITCH
         B     SETPARAM             BR TO LOOPING PARAMETERS
PRM005   CLC   PARM,=C'DATE'        WAS IT DATE
         BNE   PRM006               NO - CONTINUE
         OI    SWITCH,X'10'         TURN ON DATE SWITCH
         B     SETPARAM             BR TO LOOPING PARAMETERS
PRM006   CLC   PARM,=C'TPDK'        WAS IT TPDK
         BNE   PRM007               NO - CONTINUE
         XC    SWITCH,SWITCH        CLEAR SWITCH
         OI    SWITCH,X'38'         TURN ON SWITCH FOR TPDK
         B     START
PRM007   CLC   PARM,=C'DDN='        WAS IT DDN=
         BNE   PRM008               NO - CONTINUE
         OI    SWITCH,X'80'         TURN ON DDN= SWITCH
         A     R1,=F'4'             SET POINTER TO DDNAME
         S     R3,=F'5'             DECREASE COUNTER TO THE LNGH OF DDN
         EX    R3,MVCINST           EXECUTE REMOTE MOVE INST.
         B     START                BEGIN EXECUTION
PRM008   CLC   PARM,=C'DSCB'        WAS IT DSCB
         BE    GOODMTCH             YES - SET SWITCH
         ABEND 990,DUMP    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
GOODMTCH OI    SWITCH,X'40'         TURN ON DSCB SWITCH
SETPARAM S     R3,=F'5'             DECREASE COUNTER
         A     R1,=F'5'             INCREASE POINTER
         LTR   R3,R3                CHECK FOR LENGTH LE ZERO
         BC    12,START             YES - BR TO START
         B     CHK001               NO - CHECK FOR ANOTHER PARM
MVCINST  MVC   DDNAME(0),0(R1)      MOVE PARM DDNAME TO WORK AREA (EX)
START    OPEN  (PRINT,OUTPUT)
         TM    PRINT+48,X'10'       CHECK FOR SUCCESSFUL OPEN
         BNZ   GOODOPEN             GOOD? - BR AROUND ABEND
         ABEND 999,DUMP             ABEND SINCE THIS IS UNRECOVERABLE
**********************************************************************
*
*   GET ADDR. OF TASK I/O TABLE WITH THE PURPOSE OF MOVING EACH
*   DD NAME TO THE INPUT DCB. THUS ONLY ONE DCB IS REQUIRED FOR
*   INPUT WITH ANY NUMBER OF DD CARDS IN THE JOB STREAM.
*
*
GOODOPEN L     R6,16                PTR TO C.V.T.
         L     R6,0(R6)             PTR TO TCBWORDS
         L     R6,4(R6)             PTR TO OUR T.C.B.
         L     R6,12(R6)            PTR TO OUR T.I.O.T.
         LA    R6,24(R6)            FIND ADDR. OF FIRST DD NAME
         ST    R6,TIOTADR           SAVE IT
NEWDD    TM    INDICATR,X'08'       HAS PARM DDNAME BEEN PROCESSED?
         BO    ENDDD                YES - PREPARE TO END
         L     R6,TIOTADR           LOAD ADDR. OF NEXT DD NAME
         CLI   0(R6),X'00'          IS IT ZERO??
         BNE   NEXTDD               NO - CHECK NEXT DDNAME
         TM    SWITCH,X'80'         WAS A DDN= PARM FOUND?
         BZ    ENDDD                NO - END NORMALLY
         ABEND 995,DUMP    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
NEXTDD   MVC   TIOELNGH+3(1),0(R6)  SAVE LENGTH OF DD ENTRY
         LR    R7,R6
         A     R7,TIOELNGH          COMPUTE AND SAVE ADDR.---
         ST    R7,TIOTADR                OF NEXT DD NAME
*
*   THE FOLLOWING MACRO DETERMINES IF THIS DDNAME CORRESPONDS TO
*   A DISK DEVICE. IF NOT THEN THE NEXT DDNAME IE EXAMINED.
*
*
         MVC   DDNLOC(8),4(R6)      MOVE DDNAME TO WORKAREA
         TM    SWITCH,X'80'         WAS A DDN= PARM FOUND?
         BZ    DODDN                NO - CONTINUE NORMALLY
         CLC   DDNLOC(8),DDNAME     YES - IS THIS IT?
         BNE   NEWDD                NO - CHECK NEXT DDNAME
         OI    INDICATR,X'08'       YES - SET BIT SAYING ITS BEEN DONE.
DODDN  DEVTYPE DDNLOC,DEVAREA
         CLI   DEVAREA+2,X'20'      IS THIS A DISK DEVICE??
         BNE   NEWDD                NO -- GO GET NEXT DDNAME
*                                   YES -- CONTINUE
*
*   THE FOLLOWING LOOP SEARCHES THE TABLE OF PRIVELEGED DDNAMES THAT
*   CANNOT BE PROCESSED. ANY DDNAME NOT IN THIS TABLE IS OK.
*
*
         LA    R3,TABLE             LOAD ADDR. OF DDNAME TABLE
TABSRCH  SR    R4,R4                CLEAR WORK REG.
         IC    R4,0(R3)             GET LENGTH OF DDNAME
         LA    R3,1(R3)             REG 3 - PTS TO DDNAME IN TABLE
         LTR   R4,R4                R4=0 - THEN WE'RE DONE SEARCHING
         BZ    GETDDN               BR TO PROCESS THIS DDNAME
         BCTR  R4,R0                ADJUST FOR CORRECT LENGTH FOR CLC
         EX    R4,CLCINST           EXECUTE REMOTE COMPARE INST.
         LA    R4,1(R4)             RESTORE TRUE LENGTH OF DDNAME
         BE    NEWDD                FOUND IT, SO GO TRY NEXT DDNAME
         AR    R3,R4                SET PTR TO NEXT DDNAME IN TABLE
         B     TABSRCH              BR TO CONTINUE
CLCINST  CLC   4(0,R6),0(R3)        REMOTE COMPARE INSTR.  (EX)
GETDDN   MVC   A+40(8),4(R6)       MOVE DD NAME INTO THE DCB
         MVC   UCBADR+1(3),17(R6)  GET ADDR OF UCB
**********************************************************************
*
*   READ THE JFCB AND INSERT A DSN OF ALL '04'S SO WHEN THE FILE
*   IS OPENED, IT IS POINTING TO THE VTOC
*
*
        RDJFCB (A)
         OI    JFCBA+52,X'0A'       SET BITS - JFCB FIELDS NOT MERGED
         MVC   JFCBA(44),VTOCNAME
         MVC   DEVICE,JFCBA+118     MOVE DEVICE IN
         MVC   PK(6),DEVICE
         OPEN  (A,INPUT),TYPE=J
         TM    A+48,X'10'          CHECK FOR SUCCESSFUL OPEN
         BNZ   GOODOPNJ            GOOD - BR AROUND ERROR MSG.
         MVC   LINE,=CL132'-UNSUCCESSFUL OPEN FOR PACK '
         MVC   NAME+21(6),PK       INSERT DEVICE ID FOR MSG.
         PUT   PRINT,LINE          OUTPUT THE MESSAGE
         XC    LINE,LINE
         B     NEWDD               CONTINUE WITH NEXT PACK
**********************************************************************
*
*   PRINT THE HEADINGS FOR THE PACK DESCRIBED BY THIS DSN.
*
*
GOODOPNJ TM    SWITCH,X'02'         TEST HEADING BIT SET
         BZ    LOOPA                SKIP PRINTING OF HEADING
         TM    INDICATR,X'01'       HAVE HEADINGS ALREADY BEEN ADJUSTD
         BO    SET003               YES - PRINT THEM
         TM    SWITCH,X'04'         TEST FOR STATUS BIT SET
         BO    SET002
         MVC   R2TITLE1,ERASE02
         MVC   R2TITLE2,ERASE04
SET001   TM    SWITCH,X'10'         TEST IF DATE BIT SET
         BO    SET002               HEADING OK FOR DATE
         MVC   RTITLE1(8),ERASE01   ADJUST HEADINGS FOR DATE
         MVC   RTITLE2(8),ERASE03
SET002   TM    SWITCH,X'08'         TEST IF CYLS BIT SET
         BZ    SET003               NO - CONTINUE
         MVC   TYPALLOC-1(9),=C'CYLINDERS'  ADJ HEADING FOR CYLINDERS
SET003   OI    INDICATR,X'01'       SET HEADING READY BIT
         TM    A+17,X'29'           ARE THE BITS SET FOR A 3330?
         BO    UNIT3330             YES - MOVE '3330' FLAG INTO LINE
         MVC   UNIT(4),=C'2314'     NO - MOVE '2314' FLAG IN
         B     PRTHEAD              BR TO PRINT HEADING
UNIT3330 MVC   UNIT(4),=C'3330'
PRTHEAD  PUT   PRINT,HEADING
PUTITLE  PUT   PRINT,TITLE1
         PUT   PRINT,TITLE2
         SP    LNECNT(4),LNECNT(4)  CLEAR LINE COUNT
         AP    LNECNT(4),=P'3'      ADD TITLE COUNT TO LINE COUNT
         MVI   TITLE1,C'1'          MOVE IN PAGE CONTROL
**********************************************************************
*
*   START LOOP TO READ ALL FORMAT #1 RECORDS
*
*
LOOPA    XC    ECB,ECB              CLEAR THE ECB
         READ  ECB,SF,A,DSCB,'S'
         CHECK ECB
         CLI   DSCB+44,X'F4'        IS THIS THE FORMAT - 4
         BNE   CLI1                 NO - CONTINUE
         MVC   SAVEAREA(2),DSCB+64
         LH    R11,SAVEAREA         LOAD NO. OF TRKS/CYL.
         XC    SAVEAREA,SAVEAREA    CLEAR SAVEAREA
         MVC   NUMCYL+2(2),DSCB+62
         L     R9,NUMCYL            LOAD NO. OF CYL/PACK
         MR    R8,R11               REG 9 - TOTAL NO. TRKS/PACK
         ST    R9,TOTLTRK           SAVE TOTAL NO. OF TRKS
*
*   MOVE HIGH WATER MARK TO OUTPUT AREA
*
         LH    R7,DSCB+45           LOAD CC
         CVD   R7,CVDAREA           CONVERT TO DECIMAL
         MVC   CC1(4),=XL4'40202120'
         ED    CC1(4),CVDAREA+6
         LH    R7,DSCB+47          LOAD HH
         CVD   R7,CVDAREA          CONVERT TO DECIMAL
         MVC   HH1(4),=XL4'40202120'
         ED    HH1(4),CVDAREA+6
         LH    R7,DSCB+49          LOAD R
         SRA   R7,8                GET JUST R
         CVD   R7,CVDAREA          CONVERT TO DECIMAL
         MVC   R01(4),=XL4'40202120'
         ED    R01(4),CVDAREA+6
*
*   CHECK FOR VTOC ERRORS
*
*
         TM    SWITCH,X'02'         CHECK IF HEADING PARM PRESENT
         BZ    LOOPA                DO NOT PRINT ERROR MESSAGES IF NOT
         TM    DSCB+58,X'80'        CHECK 1ST OF THREE POSSIBLE ERRORS
         BZ    VTOCOK1              GOOD - GO CHECK 2ND
         PUT   PRINT,WARN01         PRINT WARNING MESSAGE
VTOCOK1  TM    DSCB+58,X'08'        CHECK 2ND ...
         BZ    VTOCOK2
         PUT   PRINT,WARN02
VTOCOK2  TM    DSCB+58,X'04'        CHECK 3RD ...
         BZ    LOOPA                BR - VTOC OK
         PUT   PRINT,WARN03
         B     LOOPA                BR SINCE THIS WASNT A FORMAT 1
CLI1     CLI   DSCB+44,X'F5'        TEST FOR FORMAT 5
         BNE   FMT1                 NO - BR TO CHECK FOR FMT 1
         CLC   FMT5ADR(5),=X'0000000000' IS FMT 5 ADR ALREADY SAVED?
         BNE   LOOPA                YES - IGNORE IT FOR NOW
         MVC   FMT5ADR(5),A+8       NO - SAVE IT
         B     LOOPA                READ NEXT DSCB
FMT1     CLI   DSCB+44,X'F1'        TEST FOR FORMAT 1
         BNE   LOOPA                NO - READ ANOTHER
*
*   IF THIS FORMAT 1 DESCRIBES A MODEL DSCB, THEN IF THE DSCB PARM WAS
*   GIVEN, ONLY THE DSNAME AND DATE (IF THE DATE BIT IS SET) WILL
*   BE LISTED.
*
*
         MVC   HWMCCHHR(5),A+8      SAVE DSCB HIGH WATER MARK
         CLI   DSCB+59,X'00'        IS THIS A MODEL DSCB
         BE    SKIP001              YES - GO TEST DSCB SWITCH SET
         MVC   NAME,DSCB            MOVE DSNAME TO OUTPUT FIELD
         B     SKIP009              CONTINUE NORMALLY
SKIP001  TM    SWITCH,X'40'         TEST BIT
         BZ    LOOPA                GO READ ANOTHER FORMAT 1
         MVC   NAME,DSCB            MOVE DSNAME TO OUTPUT FIELD.
         MVC   PATTERN(20),=C' * * MODEL DSCB * * '
         OI    INDICATR,X'04'       SET BIT FOR MODEL DSCB
*
*   DETERMINE THE CREATION DATE OF THIS DATA SET
*
*
SKIP009  TM    SWITCH,X'10'         TEST IF DATE BIT IS SET
         BZ    BY000                SKIP DATE COMPUTATION
         MVC   DATEYDD+5(3),DSCB+53 GET DATE
         MVI   PYEARTAB+4,28        ASSUME NON-LEAP YEAR
         TM    DATEYDD+5,X'03'      TEST FOR LEAP YEAR
         BNE   PAST001
         MVI   PYEARTAB+4,29        SET TABLES FOR LEAP YEAR
PAST001  XC    DATEYDD(6),DATEYDD   REMOVE YEAR
         L     R5,DATEYDD+4         LOAD DAY IN BINARY
         LA    R6,PYEARTAB-4        PREPARE TO SCAN TABLE
         SR    R7,R7
PRQDATE  SR    R5,R7                CONVERT FROM JULIAN TO GREGORIAN
         LA    R6,4(,R6)
         IC    R7,0(,R6)            × WHEN FINISHED R5 - DAYS
         CR    R5,R7                × REFERENCE MON. USING R6
         BH    PRQDATE              × YEAR IS IN DSCB
         CVD   R5,CVDAREA           PREPARE FOR OUTPUT
         UNPK  DATE+2(2),CVDAREA
         OI    DATE+3,X'F0'
         SR    R5,R5
         IC    R5,DSCB+53
         CVD   R5,CVDAREA
         UNPK  DATE+4(2),CVDAREA
         OI    DATE+5,X'F0'
         MVC   DATE(2),1(R6)
BY000    TM    INDICATR,X'04'       IS THIS DSN A MODEL DSCB
         BO    PRTLINE              YES - GO PRINT THE LINE
*
*   SAVE THE CCHHR, FOUND IN THE DCB, OF THE RECORD JUST READ.
*
*
         MVC   DCBCCHHR(5),A+8      SAVE CCHHR
*
*   DETERMINE HOW THIS SPACE WAS ALLOCATED. THAT IS, WHETHER
*   IT WAS ASKED FOR IN BLOCKS, TRACKS, OR CYLINDERS.
*
*
BY001    TM    SWITCH,X'04'         TEST IF STATUS BIT SET
         BZ    BY004                SKIP STATUS COMPUTATION
         TM    DSCB+94,X'C0'        TEST FOR CYLINDER ALLOC.
         BC    8,TRKALL             IT WAS A REQUEST FOR TRKS
         BM    UNKNWN               COULD BE BLKS OR TRKS
         MVC   HOWCATLG(3),=C'CYL'
         B     BY002                CONTINUE
UNKNWN   TM    DSCB+94,X'40'        TEST FOR BLOCK ALLOC.
         BC    8,TRKALL             ANOTHER REQUEST FOR TRKS
         MVC   HOWCATLG(3),=C'BLK'
         B     BY002                CONTINUE
TRKALL   MVC   HOWCATLG(3),=C'TRK'
*
*   DETERMINE IF THE REQUEST WAS FOR CONTIGUOUS SPACE
*
*
BY002    TM    DSCB+94,X'0C'
         BC    8,BY003              NO - DO NOT TAG THIS LINE
         MVC   CONTIG(6),=C'CONTIG'
*
*   DETERMINE IF THIS DATA SET WAS CATALOGUED. FLAG IT IF NOT.
*   ALSO CONVERT CCHHR FOUND IN THE DCB TO TTR TO MATCH THAT FOUND
*   IN THE CATALOG.
*
*
BY003    LOCATE LOCLIST
         LTR   R15,R15              IS IT CATALOGUED??
         BNZ   UNCTG                NO - CHECK FOR COND. CODE 4
         CLC   CATBLK(2),=X'0001'   IS THIS DSN ON MORE THAN ONE PACK?
         BNH   TTR001               NO -
         MVC   HOWCATLG+3(2),=C' M' YES - FLAG WITH AN 'M'
         B     TTROK
TTR001   CLC   CATBLK+252(3),=X'000000'  IS THE TTR ALL ZEROS?
         BNE   TTR002               NO -
         MVC   HOWCATLG+3(2),=C' 0' YES - FLAG WITH A ZERO
         B     TTROK
TTR002   LH    R5,DCBCCHHR          LOAD CC
         MR    R4,R11               CONVERT TO TRKS
         LH    R6,DCBCCHHR+2        LOAD HH
         AR    R5,R6                REG 5 -- ABSOLUTE TRK ADDR.
         MVC   DCBCCHHR(2),CATBLK+252  MOVE TT FROM CATALOG
         LH    R6,DCBCCHHR          LOAD TT
         CR    R5,R6
         BNE   BADTTR
         CLC   DCBCCHHR+4(1),CATBLK+254  COMPARE R'S
         BE    TTROK                IF EQUAL - THEN TTR'S ARE EQUAL
BADTTR   MVC   HOWCATLG+3(2),=C' *' SET OUTPUT IND. THAT TTR'S ARE BAD
TTROK    LH    R5,CATBLK            LOAD NO. PACKS THIS DS CATL ON
         LA    R6,CATBLK            LOAD ADDR. OF LOCATE WORKAREA
CD0      CLC   DEVICE(6),6(R6)      IS THIS SAME AS ONE OF THOSE CATL
         BE    BY004                YES - CONTINUE
         LA    R6,12(R6)            INCREMENT POINTER
         BCT   R5,CD0               BR TO CONTINUE SEARCH
         MVC   CATLG,UNCATLG        MOVE FLAG INTO PRINT LINE
         MVC   DVC(6),CATBLK+6      MOVE PACK ID WHERE DS IS CATL.
         MVC   HOWCATLG+3(2),=C' -' SET FLAG FOR TTR WHEN UNCATLOGED.
         LH    R5,CATBLK            GET NO. PLACES CATL. TO PRINT
         C     R5,=F'1'             IS IT CATL. IN ONLY ONE PLACE
         BE    BY004                YES - DO NOT PRINT NUMBER
         BCTR  R5,R0                ADJUST IT
         CVD   R5,CVDAREA           EDIT FOR OUTPUT
         MVC   CATVOL(6),=XL6'40214E202020'
         ED    CATVOL(6),CVDAREA+5
         B     BY004
UNCTG    MVC   HOWCATLG+3(2),=C' -' SET FLAG FOR TTR WHEN UNCATLOGED.
         S     R15,=F'4'
         BZ    CD4                  BR SINCE IT WAS COND. CODE 4
         MVC   CATLG,UNCATLG        FLAG PRINT LINE
         B     BY004
CD4      MVC   CATLG,MNTREQ         FLAG PRINT LINE DIFFERENTLY
         MVC   DVC,CATBLK+259       MOVE DEVICE REQ. MOUNT INTO LINE
*
*   LOOP TO SUM UP FORMAT 1 EXTENT DESCRIPTIONS
*
*
BY004    NI    INDICATR,X'FD'       TURN 'VALID SPACE' BIT OFF
         TM    DSCB+82,X'42'        TEST ORG. OF DATA SET
         BNZ   BY005                ORG. IS PART. OR SEQ.
         TM    DSCB+82,X'FF'        IS ORG. UNDEFINED??
         BZ    BY005                YES - SPACE IS VALID
         OI    INDICATR,X'02'       SET BIT FOR INVALID SPACE
BY005    XC    LASTREC,LASTREC
         MVC   LASTREC+2(2),DSCB+98 GET NO. OF TRKS USED
         USING IFMT1,R1
         LA    R1,DSCB
         MVC   SAVEAREA+3(1),DS1NOEPV
         L     R2,SAVEAREA          LOAD NO. EXTENTS INTO REG 2
         LTR   R2,R2                CHECK IF REG 2 IS ZERO
         BZ    PUTOUT               SKIP COMPUTATION, OUTPUT RESULT
         XC    SAVEAREA,SAVEAREA    CLEAR SAVEAREA
         LA    R4,3                 LOAD NO. OF EXTENTS INTO FORMAT 1
MOVE1    MVC   LOWERADR(8),DS1EXT1+2 MOVE EXTENT DESC. INTO WORK AREA
         BAL   R3,EXTSPACE          BR TO ROUT. TO COMP. LEN. OF EXT.
         BCT   R2,CONTINU1          WAS THAT THE LAST EXTENT
         B     PUTOUT               YES - OUTPUT RESULTS
CONTINU1 BCT   R4,MORE1             WAS THAT LAST EXT IN THIS FMT 1
         B     READFMT3             YES - READ FORMAT 3
MORE1    LA    R1,10(R1)            BUMP BASE REGISTER
         B     MOVE1                GET NEXT EXTENT
**********************************************************************
*
*    THIS CODE IS ENTERED ONLY WHEN THERE ARE MORE THAN THREE EXTENTS
*   OR IN OTHER WORDS WHEN A FORMAT #3 RECORD EXISTS. THIS LOOP
*   READS THE FORMAT #3 AND PROCESSES ALL EXTENTS ON IT.
*
*
READFMT3 OBTAIN CBLIST              SVC TO READ FORMAT 3
         LTR   R15,R15              TEST REG 15 FOR COMPLETION CODE
         BZ    OBTAINGD             GOOD? - BR AROUND ERROR EXIT
         B     ABNORM3              BR TO CODE TO PRINT ERROR MESSAGE
OBTAINGD CLI   DSCB+44,X'F2'        IS THIS A FORMAT 2 INSTEAD
         BE    READFMT3             YES - GO READ NEXT RECORD
         CLI   DSCB+44,X'F3'        CHECK TO BE SURE THIS IS A FMT 3
         BNE   PUTOUT               IF NOT PRINT RESULTS
         LA    R4,4                 LD NO EXT IN 1ST GRP IN FMT 3
         USING IFMT3,R1
         LA    R1,DSCB              LOAD BASE REG
MOVE3    MVC   LOWERADR(8),EXTS4TO7+2 LOOP TO MOVE EXT INTO WORK AREA
         BAL   R3,EXTSPACE          BR TO ROUT TO COMP LEN OF EXT
         BCT   R2,CONTINU3          WAS THAT LAST EXTENT
         B     PUTOUT               YES - GO TO OUTPUT
CONTINU3 BCT   R4,MORE3             WAS THAT LAST EXT IN THIS GROUP
         LA    R1,1(R1)             YES - THEN SKIP THE EXTRAN. BYTE
         LA    R4,10                MAKE THIS LARGE SO WONT CAUSE BR
MORE3    LA    R1,10(R1)            BUMP BASE REG
         B     MOVE3                LOOP BACK TO GET NEXT EXT
**********************************************************************
*
*   THIS IS WHERE THE BRANCH IS TO TO PREPARE FOR OUTPUT
*
*
PUTOUT   L     R8,RUNSUM            LOAD TOTAL NO. TRKS ALLOC.
         L     R9,USEDSUM           LOAD TOTAL NO. TRKS USED
         A     R8,SUM               ADD NO. ALLOC. FOR THIS DSN
         A     R9,LASTREC           ADD NO. USED IN THIS DSN
         ST    R8,RUNSUM            SAVE RESULTS
         ST    R9,USEDSUM
         TM    SWITCH,X'08'         TEST CYL SWITCH SET
         BZ    PRTTRK               SKIP CYL AS OUTPUT
         SR    R8,R8                CLEAR REGISTER 8
         L     R9,SUM               GET TOTAL AREA ALLOC
         AR    R9,R11               ADJUST FOR ROUNDING UP
         BCTR  R9,R0
         DR    R8,R11               COMPUTE CYLINDERS
         ST    R9,SUM               REPLACE TRKS WITH CYLS
         SR    R8,R8                CLEAR REGISTER 8
         L     R9,LASTREC           GET TOTAL AREA USED
         AR    R9,R11               ADJUST FOR ROUNDING UP
         BCTR  R9,R0
         DR    R8,R11               COMPUTE CYLINDERS
         ST    R9,LASTREC           REPLACE TRK WITH CYLS
PRTTRK   L     R9,SUM               LOAD TOTAL AREA FOR THIS DSN
         XC    SUM,SUM              CLEAR THE AREA FOR FUTURE USE
         CVD   R9,CVDAREA           CONVERT TO DECIMAL
         MVC   PATTERN,=XL6'402020202120'  MOVE PATTERN TO OUTPUT AREA
         ED    PATTERN(6),CVDAREA+5  NO. OF TRKS TO OUTPUT AREA
         S     R9,LASTREC           SUBTRACT TO FIND TOT. TRKS UNUSED
         CVD   R9,CVDAREA           EDIT AND MOVE TO OUTPUT AREA
         MVC   PATTERN2,=XL6'402020202120'
         LA    R1,PATTERN2+5
         EDMK  PATTERN2(6),CVDAREA+5
         TM    INDICATR,X'02'       IS THIS VALID INFO
         BZ    VALIDSP1
         BCTR  R1,0
         MVI   0(R1),C'*'
VALIDSP1 L     R9,LASTREC
         CVD   R9,CVDAREA           EDIT AND MOVE TO OUTPUT AREA
         MVC   PATTERN1,=XL6'402020202120'
         LA    R1,PATTERN1+5
         EDMK  PATTERN1(6),CVDAREA+5
         TM    INDICATR,X'02'
         BZ    VALIDSP2
         BCTR  R1,0
         MVI   0(R1),C'*'
VALIDSP2 TM    SWITCH,X'20'         TEST TPDK SWITCH SET
         BZ    PRTLINE              SKIP SPECIAL OUTPUT FORMAT
         MVC   PATTERN1(13),PATTERN1-1  CLEAR AN AREA OF OUTPUT LINE
         MVC   PATTERN1+3(6),DATE   SET DATE IN NEW POSITION
         MVC   DATE(50),DATE-1      CLEAR REMAINDER OF LINE
PRTLINE  PUT   PRINT,LINE           OUTPUT THE LINE
         MVC   RESTLINE,BLK
         NI    INDICATR,X'FB'       CLEAR THE MODEL DSCB BIT
         AP    LNECNT(4),=P'1'      BUMP LINE COUNT
         TM    SWITCH,X'02'         IS HEADINGS BIT SET??
         BZ    LOOPA                NO - READ NEXT DSCB
         CP    LNECNT(4),=P'54'     IS THIS PAGE FULL??
         BNH   LOOPA                NO - READ NEXT DSCB
         B     PUTITLE              YES - PUT HEADINGS THEN READ DSCB
**********************************************************************
*
*   PACK HAS BEEN ANALYSED, THE FOLLOWING PRINTS SOME STATISTICS.
*
*
EOF      CLOSE (A)
*
*   COMPUTE PERCENTAGES OF TOTAL SPACE ALLOC. AND TOTAL SPACE USED
*
*
         TM    SWITCH,X'02'         TEST HEADING BIT SET
         BZ    FMT5BEGN             SKIP BOTTOM HEADINGS
         L     R9,RUNSUM
         L     R7,USEDSUM
         M     R8,=F'100'
         M     R6,=F'100'
         CLC   RUNSUM,=X'00000000'  HAVE ANY TRKS BEEN ALLOCATED??
         BNE   OKDIV                YES - THEN CONTINUE W/COMPUTATIONS
         MVC   LINE,=CL132'-          NO DATASETS ALLOCATED'
         PUT   PRINT,LINE           NO - PRINT MESSAGE
         XC    LINE,LINE            CLEAR OUTPUT LINE
         B     FMT5BEGN             CONTINUE
OKDIV    D     R6,RUNSUM
         D     R8,TOTLTRK
         MVC   PACK(6),DEVICE       EDIT AND PREPARE FOR OUTPUT
         CVD   R9,CVDAREA
         MVC   PATT1,=XL4'40202120'
         ED    PATT1(4),CVDAREA+6
         L     R9,RUNSUM
         CVD   R9,CVDAREA
         MVC   PATT2,=XL6'402020202120'
         ED    PATT2(6),CVDAREA+5
         PUT   PRINT,CMT
         CVD   R7,CVDAREA
         MVC   PATRN1,=XL4'40202120'
         ED    PATRN1(4),CVDAREA+6
*
*   PUT ACTUAL HWM INTO OUTPUT AREA
*
         LH    R7,HWMCCHHR          LOAD CC
         CVD   R7,CVDAREA           CONVERT TO DECIMAL
         MVC   CC2(4),=XL4'40202120'
         ED    CC2(4),CVDAREA+6
         LH    R7,HWMCCHHR+2        LOAD HH
         CVD   R7,CVDAREA           CONVERT TO DECIMAL
         MVC   HH2(4),=XL4'40202120'
         ED    HH2(4),CVDAREA+6
         LH    R7,HWMCCHHR+4        LOAD R
         SRA   R7,8                 GET JUST R
         CVD   R7,CVDAREA           CONVERT TO DECIMAL
         MVC   R02(4),=XL4'40202120'
         ED    R02(4),CVDAREA+6
         PUT   PRINT,CMMT
**********************************************************************
*
*   THIS BLOCK COMPUTES THE 'FREE SPACE DESCRIPTION'. IN OTHER WORDS
*   IT PROCESSES THE FORMAT 5 RECORDS TO GIVE A LISTING OF WHERE AND
*   HOW MUCH FREE SPACE IS ON THE PACK BEING READ.
*
*
FMT5BEGN TM    SWITCH,X'01'         TEST FORMAT 5 BIT SET
         BZ    RETDD                SKIP FMT5 COMPUTATION
         CLC   FMT5ADR(5),=X'0000000000'  IS THERE ANY FREE SPACE
         BNE   PTHEAD               YES - PRINT HEADING
         PUT   PRINT,NOFREESP       NO - PRINT STATEMENT SAYING SO
         B     RETDD
PTHEAD   AP    LNECNT(4),=P'6'      BUMP LINE COUNTER
         CP    LNECNT(4),=P'50'     CHECK FOR FULL PAGE
         BNH   PRTOK                NO - PRINT HEADINGS ON CURRENT PAGE
         MVC   FMT5HED1(1),=C'1'    YES - SET CARRAGE CONTROL
         SP    LNECNT(4),LNECNT(4)  CLEAR COUNT
PRTOK    PUT   PRINT,FMT5HED1       PRINT HEADINGS
         PUT   PRINT,FMT5HED2
         MVC   FMT5HED2(1),=C'1'    SET FUTURE CARRAGE CONTROL
*
*   COMPUTE RELATIVE TRACK ADDR. FROM THE BEGINNING OF THE DISK OF
*   OF THE START AND END OF THIS FREE SPACE EXTENT. ALSO DETERMINE
*   THE SIZE IN TRACKS OF THIS EXTENT AND THE NO. OF COMPLETELY EMPTY
*   CYLINDERS.
*
*
READFMT5 OBTAIN FMT5LIST
         LTR   R15,R15              TEST FOR SUCCESSFUL OBTAIN
         BZ    OBTAINOK             YES - CONTINUE
         B     ABNORM5              BR TO CODE TO PRINT ERROR MESSAGE
OBTAINOK L     R4,=F'8'             LOAD NO. EXTENT DESC. IN 1ST GRP
         LA    R5,DSCB              SET REG. FOR LOOPING
LKAGN    MVC   SAVEAREA(4),4(R5)    MOVE NEXT DESC. INTO WORK AREA
         CLC   SAVEAREA(4),=F'0'    IS THIS AN EXTENT DESC.?
         BE    RETDD                NO - BR TO PROCESS NEXT DD CARD
         L     R2,SAVEAREA          LOAD INFO FROM SAVEAREA
         SRDL  R2,16                MOVE START ADR OF FREE SPACE
         SRL   R3,16                MOVE NO. COMPLETELY EMPTY CYL.
BYE002   LR    R7,R3
         MR    R6,R11               COMPUTES NO. UNUSED TRKS
         XC    LASTREC,LASTREC      CLEAR AREA FOR WORK SPACE
         MVC   LASTREC+3(1),8(R5)   NO. OF UNUSED TRKS NOT IN CYL
         A     R7,LASTREC           TOTAL NO. UNUSED TRKS
         CVD   R2,CVDAREA           EDIT AND OUTPUT
         MVC   PRN1,=X'402020202120'
         ED    PRN1(6),CVDAREA+5
         AR    R2,R7                COMPUTE EXTENT END ADR
         BCTR  R2,R0                ADJUST THIS ADDR.
         CVD   R2,CVDAREA
         MVC   PRN2,=X'402020202120'
         ED    PRN2(6),CVDAREA+5
         CVD   R7,CVDAREA
         MVC   PRN3,=X'402020202120'
         ED    PRN3(6),CVDAREA+5
         LH    R7,SAVEAREA+2        GET NO. EMPTY CYL FOR OUTPUT
         CVD   R7,CVDAREA
         MVC   PRN4,=X'402020202120'
         ED    PRN4(6),CVDAREA+5
         AP    LNECNT(4),=P'1'      BUMP LINE COUNT
         CP    LNECNT(4),=P'54'     CHECK FULL PAGE
         BNH   PUTOK                NO - PUT THE LINE
         SP    LNECNT(4),LNECNT(4)  CLEAR LINE COUNT
         PUT   PRINT,FMT5HED2       PRINT HEADING
PUTOK    PUT   PRINT,CMMMMT         PRINT THE LINE
*
*   SET PARAMETERS TO CONTINUE LOOPING OR TO READ ANOTHER FMT 5
*
*
         LA    R5,5(R5)             BUMP BASE REG TO CONTINUE LOOP
         BCT   R4,LKAGN             DECREASE COUNTER & CONTINUE
         S     R5,DSCBADR           IF CNT WAS 0, READ NEXT FMT 5??
         CL    R5,=F'40'
         BH    FIX                  YES BR TO RESET PARAMETERS
         LA    R5,DSCB              N0 - RESET R5 . . .
         LA    R5,41(R5)
         LA    R4,18                RESET R4 (COUNTER). . .
         B     LKAGN                BR TO CONT. PROCESSING THIS FMT5
FIX      CLC   FMT3PTR(5),=X'0000000000' IS THERE ANOTHER FMT5??
         BE    RETDD                NO - BR TO PROCESS NEXT DD CARD
         MVC   FMT5ADR(5),FMT3PTR   YES - MOVE IN ADDR.
         B     READFMT5             BR TO READ IT
*
*   STEPS NECESSARY BEFORE THE RETURN TO PROCESS NEXT DD CARD
*
*
RETDD    XC    FMT5ADR,FMT5ADR      CLEAR AREA FOR NEXT DD CARD
         XC    SAVEAREA,SAVEAREA
         XC    RUNSUM,RUNSUM
         XC    USEDSUM,USEDSUM
         MVC   FMT5HED1(1),=C'-'    RESTORE . . .
         MVC   FMT5HED2(1),=C'0'              . . .
         MVC   TITLE1(1),=C'-'                  . . . CARRAGE CNTL
         TM    SWITCH,X'01'         TEST FORMAT 5 BIT SET
         BZ    NEWDD                SKIP PRINTING SUMMARY FREE SPACE
*
*   USE SVC 78 TO COMPUTE THE TOTAL AMOUNT OF FREE SPACE AVAILABLE
*   ON THIS PACK.
*
*
         L     R0,UCBADR     LOAD ADDR OF UCB
         LA    R1,WORK              WORK AREA FOR SVC
         SVC   78
         PACK  CVDAREA(8),WORK+11(4)  CHANGE VALUES TO BINARY FOR EDIT
         MVC   PT2,=XL6'402020202120' EDIT & PREPARE FOR OUTPUT
         ED    PT2(6),CVDAREA+5
         PACK  CVDAREA(8),WORK+6(4)   CHANGE VALUES TO BINARY FOR EDIT
         MVC   PT1,=XL6'402020202120'
         ED    PT1(6),CVDAREA+5
         PUT   PRINT,CMMMT          PRINT STATS ON NO. CYL & TRK EMPTY
         B     NEWDD
**********************************************************************
*
*   CODE TO PREPARE MESSAGES DUE TO ERRORS CAUSED BY AN UNSUCCESSFUL
*   "OBTAIN" MACRO.
*
*
ABNORM3  CVD   R15,CVDAREA          EDIT AND PRINT CMPLETION CODE
         MVC   CC3,=X'40202120'
         ED    CC3(4),CVDAREA+6
         PUT   PRINT,BD1TO5
         B     NEWDD                CONTINUE WITH A NEW PACK
ABNORM5  CVD   R15,CVDAREA          EDIT AND PRINT COMPLETION CODE
         MVC   CC5,=X'40202120'
         ED    CC5(4),CVDAREA+6
         PUT   PRINT,AB1TO5
         B     NEWDD                CONTINUE WITH A NEW PACK
**********************************************************************
*
*   CODE ENTERED TO CLOSE INPUT FILE AND RESTORE REGISTERS
*
*
ENDDD    CLOSE (PRINT)
         SR    15,15
         B     RETURN
         TITLE 'CAMLISTS AND READ JFCB LISTS'
         DS    0D
DDNLOC   DC    8C' '
DEVAREA  DC    8C' '
         DS    0F
LIST     DC    X'87'
         DC    AL3(JFCBA)
JFCBA    DC    CL176' '
CBLIST   CAMLST SEEK,FMT3PTR,DEVICE,DSCB
LOCLIST  CAMLST NAME,NAME,,CATBLK
FMT5LIST CAMLST SEEK,FMT5ADR,DEVICE,DSCB
         DS    0D
DDNAME   DC    CL8' '
         TITLE '     TABLE OF PRIVELEGED DDNAMES'
TABLE    DC    X'05',C'SYSIN'
         DC    X'08',C'SYSPRINT'
         DC    X'08',C'SYSABEND'
         DC    X'08',C'SYSUDUMP'
         DC    X'06',C'PANDD1'
         DC    X'06',C'PANDD2'
         DC    X'06',C'SYSLIB'
         DC    X'07',C'SYSPROC'
         DC    X'06',C'MACLIB'
         DC    X'07',C'PLIDUMP'
         DC    X'07',C'PL1DUMP'
         DC    X'07',C'STEPLIB'
         DC    X'06',C'JOBLIB'
         DC    X'00'
         TITLE '   TABLE FOR INLINE DATE ROUTINE   '
         DS    0D
PYEARTAB DC    AL1(31),X'F0F100'
         DC    AL1(28),X'F0F200'
         DC    AL1(31),X'F0F300'
         DC    AL1(30),X'F0F400'
         DC    AL1(31),X'F0F500'
         DC    AL1(30),X'F0F600'
         DC    AL1(31),X'F0F700'
         DC    AL1(31),X'F0F800'
         DC    AL1(30),X'F0F900'
         DC    AL1(31),X'F1F000'
         DC    AL1(30),X'F1F100'
         DC    AL1(255),X'F1F200'
         DS    0D
DATEYDD  DC    D'0'
         TITLE 'PRINT LINE FORMATS AND WORK AREAS'
         DS    0D
DSCB     DS    0CL350
         DC    XL135'00'
FMT3PTR  DC    XL5'00'
         DC    XL210'00'
LINE     DS    0CL133
CTL      DC    CL1' '
DEVICE   DC    CL6' '
BLK      DC    CL1' '
RESTLINE DS    0CL125
NAME     DC    CL44' '
         DC    CL4' '
PATTERN  DS    CL6
         DC    C' '
PATTERN1 DC    CL6' '
         DC    C' '
PATTERN2 DC    CL6' '
         DC    CL4' '
DATE     DC    CL6' '
         DC    CL4' '
HOWCATLG DC    CL3' '
         DC    CL4' '
CONTIG   DC    CL6' '
         DC    CL3' '
CATLG    DC    CL12' '
         DC    CL1' '
DVC      DC    CL6' '
CATVOL   DC    CL6' '
         DC    CL2' '
UNCATLG  DC    C'** UNCATL **'
MNTREQ   DC    C' MOUNT REQ. '
LOWERADR DC    CL4' '
UPPERADR DC    CL4' '
         DS    0D
WORK     DC    30C' '
         DS    0D
SUM      DC    F'0'
SAVEAREA DC    F'0'
LASTREC  DC    F'0'
USEDSUM  DC    F'0'
RUNSUM   DC    F'0'
TIOTADR  DC    F'0'
TIOELNGH DC    F'0'
NUMCYL   DC    F'1'
TOTLTRK  DC    F'0'
UCBADR   DC    F'0'
LNECNT   DC    PL4'0'
VTOCNAME DC    44X'04'
FMT5ADR  DC    XL5'0000000000'
SWITCH   DC    BL1'00010111'
INDICATR DC    BL1'00000000'
PARM     DC    CL4' '
         DS    0D
CVDAREA  DC    D'0'
DSCBADR  DC    A(DSCB)
SECBASE  DC    A(DATASETL+4096)
         DS    0D
CATBLK   DS    0CL265
         DC    265C' '
         DS    0F
DCBCCHHR DC    CL5' '
         TITLE '     VTOC WARNING AND ERROR MESSAGES'
WARN01   DS    0CL133
         DC    C' *** WARNING ***   EITHER NO FORMAT 5 DSCB EXISTS OR'
         DC    C' THEY DO NOT REFLECT THE TRUE STATUS OF THIS VOLUME.'
         DC    CL30' '
WARN02   DS    0CL133
         DC    C' *** WARNING ***   OS/360 MAY NOT BE ABLE TO PROCESS'
         DC    C' SOME DATA SETS WHICH DOS/360 MAY HAVE CREATED ON'
         DC    C' THIS VOLUME.'
         DC    CL23' '
WARN03   DS    0CL133
         DC    C' *** WARNING ***   A DADSM FUNCTION HAS BEEN PREMATU'
         DC    C'RELY TERMINATED. POSSIBLE VTOC ERRORS EXIST.'
         DC    CL38' '
         TITLE 'HEADINGS, TITLES AND OUTPUT FORMATS'
TITLE1   DS    0CL133
         DC    C'-'
         DC    C' PACK '
         DC    C' **********     DATA SET NAME     **********'
         DC    CL12' '
TYPALLOC DC    C'TRACKS'
         DC    CL10' '
ERASE01  DC    CL1' '
RTITLE1  DS    0CL54
         DC    C'CREATE'
         DC    CL3' '
ERASE02  DC    CL1' '
R2TITLE1 DS    0CL45
         DC    C'*****  STATUS OF THIS DATA SET  *****'
TITLE2   DS    0CL133
         DC    CL57' '
         DC    C'ALLOC  USED  UNUSED'
         DC    CL4' '
ERASE03  DC    CL1' '
RTITLE2  DS    0CL45
         DC    C'DATE'
         DC    CL7' '
ERASE04  DC    CL1' '
R2TITLE2 DS    0CL45
         DC    C'(BLANK INDICATES NORMAL STATUS)'
         DC    CL18' '
HEADING  DS    0CL133
         DC    C'1'
         DC    CL35' '
         DC    C'DISK DATA SET ALLOCATION SUMMARY FOR PACK '
PK       DC    CL6' '
         DC    C' ON A '
UNIT     DC    CL4' '
         DC    CL39' UNIT.'
CMT      DS    0CL133
         DC    C'0TOTAL NUMBER OF TRACKS ALLOCATED FOR PACK '
PACK     DC    CL6' '
         DC    C' IS'
PATT2    DC    CL6' '
         DC    C' FOR A'
PATT1    DC    CL4' '
         DC    C'% UTILIZATION RATE.'
         DC    CL47' '
NOFREESP DS    0CL132
         DC    CL132'*****  NO FREE SPACE EXISTS ON THIS PACK  *****'
CMMT     DS    0CL133
         DC    C'0'
         DC    CL14' '
         DC    C'FOR THOSE ALLOCATED TRACKS, ONLY'
PATRN1   DC    CL4' '
         DC    C'% OF THEM ARE IN USE.'
         DC    CL10' '
         DC    CL5'HWM ='
CC1      DC    CL4' '
HH1      DC    CL4' '
R01      DC    CL4' '
SEP020   DC    CL3' : '
CC2      DC    CL4' '
HH2      DC    CL4' '
R02      DC    CL4' '
         DC    CL20' '
HWMCCHHR DC    CL5' '
CMMMT    DS    0CL133
         DC    C'0'
         DC    C'THE FREE SPACE ON THIS PACK CONSISTS OF'
PT1      DC    CL6' '
         DC    C' FULL CYLINDERS AND'
PT2      DC    CL6' '
         DC    C' EXTRA TRACKS.'
         DC    CL50' '
FMT5HED1 DS    0CL133
         DC    C'-***** FREE SPACE DESCRIPTION *****'
         DC    CL100' '
FMT5HED2 DS    0CL133
         DC    C'0  FROM       TO   TRACKS    FULL CYL'
         DC    CL96' '
CMMMMT   DS    0CL133
         DC    CL1' '
PRN1     DC    CL6' '
         DC    CL3' '
PRN2     DC    CL6' '
         DC    CL3' '
PRN3     DC    CL6' '
         DC    CL3' '
PRN4     DC    CL6' '
         DC    CL100' '
AB1TO5   DS    0CL133
         DC    C'---OBTAIN MACRO ERROR---'
         DC    C'      -FORMAT 5 RECORD NOT FOUND, CONDITION CODE'
CC5      DC    CL4' '
         DC    CL57' '
BD1TO5   DS    0CL133
         DC    C'---OBTAIN MACRO ERROR---'
         DC    C'      -FORMAT 3 RECORD NOT FOUND, CONDITION CODE'
CC3      DC    CL4' '
         DC    CL57' '
         TITLE '                    I/O DCBS'
PRINT    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FBA,          *
               LRECL=133,BLKSIZE=665
         DS    0F
A        DCB   DDNAME=XXXXXX,DEVD=DA,DSORG=PS,MACRF=(R),BLKSIZE=96,    *
               RECFM=F,EODAD=EOF,EXLST=LIST,KEYLEN=44
         TITLE '     ROUTINE TO CALCULATE EXTENT LENGTHS'
*
*   THIS ROUTINE DOES ALL THE CALCULATION IN COMPUTING THE NUMBER
*   OF TRACKS PER EXTENT. A RUNNING SUM IS CARRIED IN 'SUM'
*
*
EXTSPACE MVC   SAVEAREA(4),LOWERADR  LOWER EXTENT DESCRIPTION
         LH    R7,SAVEAREA           CC OF LOWER BOUND
         LH    R8,SAVEAREA+2         HH OF LOWER BOUND
         XC    SAVEAREA,SAVEAREA
         MR    R6,R11                (TRKS/CYL) * CYL
         AR    R7,R8                 TRKS + TRKS
         MVC   SAVEAREA(4),UPPERADR  UPPER EXTENT DESCRIPTION
         LH    R9,SAVEAREA           CC OF UPPER BOUND
         LH    R0,SAVEAREA+2         HH OF UPPER BOUND
         XC    SAVEAREA,SAVEAREA
         MR    R8,R11                (TRKS/CYL) * CYL
         AR    R9,R0                 TRKS + TRKS
         SR    R9,R7                 TOTAL TRKS IN THIS EXTENT
         LA    R9,1(R9)              ADJUST THE RESULT
         A     R9,SUM                CALC RUNNING SUM
         ST    R9,SUM                SAVE IT
         BR    R3                    RETURN
         TITLE '          DSECT FORMATS'
IFMT1    DSECT
DS1DSNAM DS    CL44
DS1FMTID DS    CL1
         DS    CL14
DS1NOEPV DS    CL1
         DS    CL45
DS1EXT1  DS    CL10
         SPACE 5
IFMT3    DSECT
         DS    CL4
EXTS4TO7 DS    CL40
         END
