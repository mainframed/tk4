PDSCLEAR CSECT
         EJECT
*----------------D  O  C  U  M  E  N  T  A  T  I  O  N----------------*
*                                                                     *
*  TITLE      =  PDS DIRECTORY CLEAR                                  *
*                                                                     *
*  FUNCTION   =  CLEARS THE DIRECTORY OF A PDS AND INDICATES THAT NO  *
*                MEMBERS EXIST.                                       *
*                                                                     *
*  OPERATION  =  BSAM IS USED TO READ THE DIRECTORY OF THE DATA SET   *
*                AND TO DETERMINE THE NUMBER OF DIRECTORY BLOCKS THAT *
*                NEED TO BE RESET. EXCP IS USED TO UPDATE THE DIRECT- *
*                ORY BLOCKS. THE FOLLOWING I/O COMMANDS ARE USED TO   *
*                UPDATE THE BLOCKS : SEARCH ID EQUAL, TIC AND WRITE   *
*                KEY AND DATA.                                        *
*                                                                     *
*  INPUT      =  THE USER SPECIFIES A PDS TO BE CLEARED BY GIVING IT  *
*                A DDNAME WITH THE FIRST THREE CHARACTERS OF 'DSN'.   *
*                (ACCEPTABLE DEVICES ARE 3330,3330-1 & 3350) .        *
*                                                                     *
*  OUTPUT     =  1) NO. OF DIRECTORY BLOCKS UPDATED.                  *
*                2) TOTAL NO. OF DIRECTORY BLOCKS IN THE DATA SET.    *
*                3) AMOUNT OF SPACE ALLOCATED TO THE DIRECTORY.       *
*                                                                     *
*  EXIT/NORMAL=  RETURN TO CALLER                                     *
*                                                                     *
*  EXIT/ERROR =  U100 - I/O ERROR OCCURRED                            *
*                U200 - ERROR OPENING SYSPRINT DD STATEMENT           *
*                                                                     *
*  ATTRIBUTES =  ***** AUTHORIZED *****                               *
*                                                                     *
*  LAST UPDATE=  05/13/80                                             *
*                                                                     *
*  REASON     =  TO CORRECT THE PROBLEM WHEN THE NUMBER OF DIRECTORY  *
*                BLOCKS TO BE FORMATTED IS EQUAL TO THE MAXIMUM NUMBER*
*                OF DIRECTORY BLOCKS PER TRACK.  THIS WOULD RESULT IN *
*                AN S0C1. CHANGES ARE IDENTIFIED AS 'V2'
*                                                                     *
*  AUTHOR     =  PAUL FELIX                                           *
*                TRW DSSG, REDONDO BEACH, CALIFORNIA                  *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*     REGISTER  EQUATES                                               *
*---------------------------------------------------------------------*
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
RBASE    EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
*---------------------------------------------------------------------*
*        H O U S E K E E P I N G                                      *
*---------------------------------------------------------------------*
         SPACE 1
         SAVE  (14,12)                 SAVE CALLER'S REGS
         BALR  RBASE,R0                     ESTABLISH
         USING *,RBASE                    ADDRESSABILITY
         LA    R2,SAVEAREA             ADDRESS OF MY SAVEAREA
         ST    R13,4(R2)               CHAIN THE SAVE AREAS
         ST    R2,8(R13)                      AND
         LR    R13,R2                  ESTABLISH THE POINTER TO MINE
         SPACE 2
*---------------------------------------------------------------------*
*        OPEN OUTPUT FILE THAT WILL CONTAIN                           *
*          STATISTICS AND ERROR MESSAGES                              *
*---------------------------------------------------------------------*
         SPACE 1
         OPEN  (SYSPRINT,(OUTPUT))     OPEN OUTPUT FILE
         LA    R1,SYSPRINT             LOAD ADDR IN CASE OF ERROR
         TM    SYSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO, EXIT INDICATING ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        GET TIOT ADDRESS AND CHECK FOR A VALID DD NAME (FIRST THREE  *
*        CHARACTERS OF THE DDNAME IS EQUAL TO DSN) .                  *
*---------------------------------------------------------------------*
         SPACE 1
         L     R2,CVTPTR               LOAD ADDRESS OF CVT
         L     R2,0(R2)                LOAD ADDRESS OF TCB DOUBLEWORD
         L     R2,4(R2)                LOAD ADDRESS OF MY TCB
         L     R2,12(R2)               LOAD ADDR OF MY TIOT
         LA    R2,24(R2)               POINT TO THE FIRST ENTRY
         SR    R3,R3                   R3 - LENGTH OF TIOT ENTRY
CHECKDDN ICM   R3,1,0(R2)              ?/END OF TIOT
         BZ    RETURN                  YES, ALL THROUGH -- EXIT
         CLC   4(3,R2),DDNID           ?/ACCEPTABLE DD NAME
         BE    VALIDDSN                YES, CONTINUE CHECKING
         AR    R2,R3                   NO, POINT TO NEXT ENTRY
         B     CHECKDDN                TRY AGAIN
         SPACE 2
*---------------------------------------------------------------------*
*        PREPARE FOR AND READ JFCB                                    *
*---------------------------------------------------------------------*
         SPACE 1
VALIDDSN CLC   SEQDDN(8),4(R2)         ?/DUPLICATE DD NAMES          V1
         BNE   VERDSN                  NO, CONTINUE WITH VERIFY      V1
         LA    R8,ERR9                 POINT TO ERROR MESSAGE        V1
         B     PROCERR                 PROCESS THE ERROR             V1
VERDSN   MVC   SEQDDN(8),4(R2)         SAVE THE DDNAME IN BOTH       V1
         MVC   PDSDDN(8),4(R2)               DCB'S
         STM   R2,R3,TIOTREGS          SAVE REGS USED FOR TIOT SCAN
         RDJFCB PDSSEQ                 READ JFCB
         LTR   R15,R15                 ?/ERROR OCCURRED
         BZ    VERIFYPO                NO, CONTINUE TO VERIFY
         LA    R8,ERR1                 LOAD ADDR OF ERROR MESSAGE
         B     PROCERR                 GO PROCESS THE ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        VERIFY THAT THE DATA SET IS PARTITION ORGANIZATION           *
*---------------------------------------------------------------------*
         SPACE 1
VERIFYPO MVC   DSCBNAME,JFCBAREA       GET THE DATA SET NAME
         MVC   VOLID,JFCBVOLS          GET THE VOL SER.
         OBTAIN DSCBREAD               GET THE FORMAT 1 DSCB
         LTR   R15,R15                 ?/OBTAINED WORKED
         BZ    TESTPO                  YES, CONTINUE TO VERIFY
         LA    R8,ERR2                 LOAD ADDR OF ERROR MESSAGE
         B     PROCERR                 GO PROCESS THE ERROR
TESTPO   TM    DSCBORG,X'02'           ?/IS IT PO
         BO    JFCBINFO                YES, GET JFCB INFORMATION
         LA    R8,ERR3                 LOAD ADDR OF ERROR MESSAGE
         B     PROCERR                 PROCESS THE ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        SAVE VARIOUS JFCB AND DSCB INFORMATION FOR LATER USE         *
*---------------------------------------------------------------------*
         SPACE 1
JFCBINFO MVC   DSNAME,JFCBAREA         SAVE THE DATA SET NAME
         MVC   MBBCCHHR+3(4),DSCBXTNT  SAVE BEGINNING EXTENT FOR DSN
         MVC   F1CCHHR,DSCBCCHR        SAVE CCHHR OF ASSOCIATED DSCB
         MVC   LRECLKEY,DSCBRECK       SAVE LRECL AND KEYLEN
         SPACE 2
*---------------------------------------------------------------------*
*        PREPARE TO READ THE DIRECTORY                                *
*---------------------------------------------------------------------*
         SPACE 1
         LA    R5,264                  LENGTH OF AREA
         BAL   R8,GETAREA              GO TO GETAREA ROUTINE
         USING PDSDSECT,R4             ESTABLISH ADDRESSABILITY
         LR    R4,R1                     TO PDS DSECT
         OPEN  (PDSSEQ)                OPEN PDS DS AS SEQUENTIAL
         LA    R1,PDSSEQ               LOAD ADDR IN CASE OF ERROR
         TM    SEQOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 ERROR OPENING THE DATA SET
         SR    R5,R5                   CLEAR REG & USE AS A COUNTER
         SPACE 2
*---------------------------------------------------------------------*
*        READ THE DIRECTORY (THE WHOLE DIRECTORY WILL BE READ).       *
*          DIRUCNT = NO. OF DIRECTORY BLOCKS IN USE                   *
*          DIRTCNT = NO. OF DIRECTORY BLOCKS IN THE DATA SET          *
*---------------------------------------------------------------------*
         SPACE 1
READDIR  READ  SEQDECB,SF,PDSSEQ,(R4)  READ DIRECTORY BLOCK
         CHECK SEQDECB                 WAIT FOR I/O
         LA    R5,1(R5)                UPDATE THE NO. OF BLOCKS READ
         CLC   PDSKEY,HIKEY            ?/LAST USED DIRECTORY BLOCK
         BNE   READDIR                 NO, CONTINUE READING
         ST    R5,DIRUCNT              SAVE COUNT OF USED BLOCKS
         B     READDIR                 READ THE REST OF DIRECTORY
         SPACE 1
*        POST PROCESSING AFTER THE DIRECTORY HAS BEEN READ
         SPACE 1
EOF      ST    R5,DIRTCNT              SAVE COUNT OF TOTAL BLOCKS
         L     R5,SEQDEB               GET DEB ADDRESS IN ORDER
         MVC   UCBADDR+1(3),33(R5)       TO SAVE THE UCB ADDR
         DROP  R4                      DROP ADDRESSABILITY TO PDSDSECT
         BAL   R8,FREEAREA             GO FREE THE AREA
         CLOSE PDSSEQ                  CLOSE THE DATA SET
         SPACE 2
*---------------------------------------------------------------------*
*        CALCULATE THE TTR AND LL OF THE DATASET TO BE SAVED IN THE   *
*        FORMAT 1 DSCB.                                               *
*          TTR = POINTER TO THE DATA PORTION OF THE DATA SET          *
*          LL  = NO. OF BYTES REMAINING ON THE LAST USED TRACK        *
*---------------------------------------------------------------------*
         SPACE 1
         L     R3,DIRTCNT              LOAD NO. OF DIRECTORY BLOCKS
         SR    R2,R2                   CLEAR REG
         SR    R4,R4                   CLEAR REG
         MVI   DSCBNAME,X'04'          MOVE IN VTOC NAME
         MVC   DSCBNAME+1(43),DSCBNAME
         OBTAIN DSCBREAD               READ IN FORMAT 4 DSCB
         LTR   R15,R15                 ?/EVERYTHING OK
         BZ    CONTTTR                 YES, CONTINUE
         LA    R8,ERR2                 LOAD ADDR OF ERROR MSG
         B     PROCERR                 GO PROCESS THE ERROR
CONTTTR  IC    R4,DSCBDIR              GET NUMBER OF DIR. BLOCKS PER TR
         DR    R2,R4                   CALCULATE NO. OF TRACKS REQUIRED
         STH   R3,TTRLL                SAVE THE TT VALUE
         LA    R2,1(R2)                ADD 1 TO R VALUE FOR EOF
         STC   R2,TTRLL+2              SAVE R VALUE
         LA    R4,8                    R4 - LENGTH OF KEY
         LA    R5,256                  R5 - LENGTH OF DATA
         SPACE 1
***      THIS PART CALCULATES THE LL VALUE (THE NUMBER OF BYTES LEFT
***      ON THE LAST USED TRACK
         SPACE 1
         STM   R2,R3,TTRREGS           SAVE REGS 2 & 3
         SR    R3,R3                   CLEAR R3
         IC    R3,DS4DEVI              INSERT NOT LAST KEYED BLK OVRHD
         TM    DS4DEVFG,X'08'          ?/ANY TOLERANCE FACTOR
         BZ    NOTOL                   NOPE
         LH    R3,DS4DEVI              YES, GET VALUE
NOTOL    AR    R5,R4                   ADD KEYLEN TO DATA LEN
         LH    R2,DS4DEVTL             GET TOLERANCE FACTOR
         MR    R4,R2                   MULTIPLY TOL. FACTOR BY KEY & DA
         LA    R2,512                  TOLERANCE DIVISOR
         DR    R4,R2                   DIVIDE TOLERANCE CONSTANT
         LTR   R4,R4                   ?/ANY REMAINDER
         BZ    NOREMAIN                NOPE
         LA    R5,1(R5)                YES, ROUND UP
NOREMAIN AR    R5,R3                   ADD IN OVERHEAD---NOT LAST
         LR    R4,R5                   SAVE VALUE
         LM    R2,R3,TTRREGS           RESTORE REGS 2 & 3
         LR    R3,R2                   NO. OF RECS USED ON LAST DIR. TR
         BCTR  R3,R0                   DECREMENT BECAUSE OF EOD
         MR    R2,R4                   MULT. NO. OF RECS BY AVG. REC
         LH    R4,DS4DEVTK             GET NO. OF BYTES PER TRK
         SR    R4,R3                   CALC. NO. OF UNUSED BYTES
         SR    R3,R3                   CLEAR REG
         IC    R3,DS4DEVI              INSERT OVERHEAD
         TM    DS4DEVFG,X'08'          ?/ANY TOLERANCE OVERHEAD
         BZ    NOTOL1                  NOPE
         LH    R3,DS4DEVI              GET TOLERANCE VALUE
NOTOL1   SR    R4,R3                   DEDUCT OVERHEAD
         SR    R3,R3                   CLEAR WORK REG
         IC    R3,DS4DEVK              INSERT OVERHEAD FOR NON-KEY
         AR    R4,R3                   ADJUST FOR NON-KEY
         BCTR  R4,R0                   ACCOUNT FOR EOF
         STC   R4,TTRLL+4              SAVE REMAINING BYTES
         SRL   R4,8                           IN
         STC   R4,TTRLL+3                 TTRLL
         EJECT
*---------------------------------------------------------------------*
*        PREPARE FOR I/O PROCESSING                                   *
*        - AREA FOR THE CHANNEL PROGRAM IS GETMAINED & IS CALCULATED *
*          AS FOLLOWS:                                                *
*          ((A+B+C+D) * E)                                            *
*              WHERE A = 8, CCW FOR SEARCH ID                         *
*                    B = 8, CCW FOR TIC TO SEARCH ID                  *
*                    C = 8, CCW TO WRITE KEY AND DATA                 *
*                    D = 8, CCHHR FOR SEARCH ID COMMAND               *
*                    E = NUMBER OF DIRECTORY BLOCKS PER TRACK         *
*                                                                     *
*        - REG 9  = UPDATED POINTER TO CHANNEL PROGRAM                *
*        - REG 10 = UPDATED POINTER TO CCHHR VALUE                    *
*        - REG 2  = NO. OF DIRECTORY BLOCKS TO BE FORMATTED FOR THIS  *
*                   TRACK. IF THIS IS THE FIRST AND ONLY TRACK OR THE *
*                   LAST TRACK WHEN MULTIPLE TRACKS ARE TO BE RE-     *
*                   FORMATTED, THEN THIS VALUE WILL EQUAL DIRUCNT.    *
*                   OTHERWISE THIS VALUE WILL EQUAL THE MAXIMUM NO.   *
*                   OF DIRECTORY BLOCKS PER TRACK.                    *
*        - REG 3  = NO. OF FULL TRACKS TO CONTAIN REFORMATTED DIRECT- *
*                   ORY BLOCKS.                                       *
*---------------------------------------------------------------------*
         SPACE 1
         SR    R4,R4                   CLEAR REG
         IC    R4,DSCBDIR              INSERT NO. OF DIR. BLKS PER TRK
         LR    R2,R4                   INIT. WORK REG
         SLL   R2,3                    MULTIPLY BY 8
         LR    R5,R2                   SAVE THE VALUE
         MH    R5,=H'04'               CALCULATE THE ARE FOR CP
         BAL   R8,GETAREA              GO GET AREA FOR CP
         LR    R9,R1                   ADDR OF WRITE CCW'S
         ST    R9,IOBSTART             SPECIFY ADDR OF CP IN IOB
         AR    R1,R5                   ADDR OF READ CCW'S
         SR    R1,R2
         LR    R10,R1
         STM   R9,R10,CPREGS           SAVE REGS TO BE REUSED
         L     R3,DIRUCNT              LOAD NO. OF USED DIR BLOCKS
         SR    R2,R2                   CLEAR REG
         DR    R2,R4                   CALCULATE NO. TRACKS NEEDED
         LTR   R3,R3                   ?/ANY TRACKS NEED
         BZ    BUILDCP                 NOPE
         ST    R2,EXTRADIR             SAVE VALUE OF INCOMPLETE TRKS
         LR    R2,R4                   LOAD MAX NO. OF DIR BLKS PRE TRK
         B     BUILDCP                 CONTINUE PROCESSING
         SPACE 2
*---------------------------------------------------------------------*
*        BUILD THE CHANNEL PROGRAM                                    *
*---------------------------------------------------------------------*
         SPACE 1
BUILDCP  OI    FLAG1,X'FF'             TURN FLAG ON SAYING INITIAL CCW
         OPEN  (PDSPDS,(OUTPUT))
         LA    R1,PDSPDS
         TM    PDSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO - GO INDICATE ERROR
CPLOOP   MVC   IOBSEEK,MBBCCHHR        ESTABLISH THE SEEK ADDRESS
CPLOOP1  LM    R4,R7,CCWSID            LOAD SEARCH ID AND TIC CCW
         AR    R6,R9                   ESTABLISH THE TIC ADDRESS
         STM   R4,R7,0(R9)             SAVE THE CCW'S IN CHANNEL PGM
         LA    R4,3(R10,R4)            POINT TO THE CCHHR FOR SEARCH ID
         STCM  R4,7,1(R9)              SAVE ADDRESS IN CP
         MVC   16(8,R9),CCWWRITE       MOVE WRITE CCW TO CP
         LM    R6,R7,MBBCCHHR          GET CCHHR
         AL    R7,TRKBUMP              ADD 1 TO RECORD VALUE
         STM   R6,R7,0(R10)            SAVE UPDATED CCHHR
         STM   R6,R7,MBBCCHHR                  ××
         TM    FLAG1,X'FF'             ?/FIRST CCW IN CHANNEL PGM.
         BO    FIRSTCCW                YES - TO DO SPECIAL PROCESSING
NEXTCCW  LA    R9,24(R9)               NO - UPDATE CP POINTER
         LA    R10,8(R10)              UPDATE LOCATION OF CCHHR'S
         BCT   R2,CPLOOP1              ?/ANY MORE DIR. BLKS TO FORMAT
         SH    R9,=H'08'               NO - BACK UP IN CP
         NI    4(R9),X'BF'              AND TURN OFF CHAINING BIT
         BAL   R8,IO                   GO ISSUE IO
         B     ALLOVER                 CHECK IF WE'RE FINISH
FIRSTCCW LA    R8,HIKEY                LOAD ADDRESS OF HI-KEY
         STCM  R8,7,17(R9)             POINT TO HI-KEY IN WRITE CCW
         MVI   23(R9),X'12'            CHANGE LENGTH OF KEY AND DATA
         NI    FLAG1,X'00'             INDICATE FIRST CCW PROCESS DONE
         B     NEXTCCW                 CONTINUE WITH NEXT CCW
ALLOVER  LTR   R3,R3                   ?/IO ALL FINISHED
         BZ    DSCBUPDT                YES, GO UPDATE FMT 1 DSCB
         BCTR  R3,R0                   NOPE, DECREMENT NO. OF TRACKS
         SLDL  R6,24
         SPACE 2
*---------------------------------------------------------------------*
*        CALCULATE THE NEW CCHHR                                      *
*          R6 = CCHH                                                  *
*          R7 = R                                                     *
*          DEVTAB = A TABLE CONTAINING THE DEVICE TYPE AND NO. OF     *
*                   TRACKS PER CYLINDER. DEVICES CURRENTLY SUPPORTED  *
*                   ARE 3330 AND 3350.                                *
*---------------------------------------------------------------------*
         SPACE 1
         LR    R8,R6                   LOAD CCHH INFORMATION
         AL    R8,TRKBUMP              UPDATE THE TRACK ADDRESS
         LA    R9,DVTAB                LOAD ADDR OF DEVICE TABLE
         LA    R10,DVTABEND            LOAD NO. OF ENTRIES IN TABLE
         L     R11,UCBADDR             LOAD DATASET UCB ADDRESS
         LA    R11,19(R11)             POINT TO UCB TYPE
COMPDEVT CLC   0(1,R11),0(R9)          ?/DEVICE TYPE MATCH
         BE    RIGHTDEV                YES,CONTINUE PROCESSING
         LA    R9,2(R9)                PREPARE FOR NEXT ENTRY
         BCT   R10,COMPDEVT            ?/END OF TABLE
         LA    R8,ERR7                 LOAD ADDR OF ERROR MSG
         B     PROCERR                 GO PROCESS THE ERROR
RIGHTDEV CLM   R8,1,1(R9)              ?/TRACK VALUE OK
         BL    TRACKOK                 YES
         AL    R6,CYLBUMP              NO, UPDATE CYL ADDRESS
         IC    R6,NULLKEY              ZERO THE TRACK COUNT(NULLKEY=0)
         LR    R8,R6                   COMPATIBILITY INSTRUCTION
TRACKOK  LR    R6,R8                   LOAD CORRECT CCHH INFO.
         SR    R7,R7                   SET RECORD COUNT TO 0
         SRDL  R6,24                   PREPARE TO SAVE
         STM   R6,R7,MBBCCHHR          SAVE NEW SEEK ADDRESS
         LM    R9,R10,CPREGS           RESTORE CHANNEL PGM REGS
         IC    R2,DSCBDIR              INSERT MAX DIR. BLOCKS
         LTR   R3,R3                   ?/PROCESS AT RECORD LEVEL
         BNZ   CPLOOP                  NOPE, CONTINUE AT TRACK LEVEL
         L     R2,EXTRADIR             YES, LOAD NO. OF DIR.
         LTR   R2,R2                   ?/ANY EXTRA DIR. TO PROCESS  V2
         BNZ   CPLOOP                  YES. GO DO ITV2
         SPACE 2
*---------------------------------------------------------------------*
*        THIS CODE IS RESPONSIBLE FOR UPDATING THE FORMAT 1 DSCB. THE *
*        DSCB IS FIRST READ INTO STORAGE AND THEN MODIFIED. THE DATA  *
*        THAT IS MODIFIED IS :                                        *
*          DS1NOBDB - NO. OF BYTEST USED IN LAST DIRECTORY BLOCK      *
*          DS1LSTAR - LAST USED TRACK AND BLOCK ON TRACK              *
*          DS1TRBAL - BYTES REMAINING ON LAST TRACK USED              *
*          DS1LRECL - RECORD LENGTH                                   *
*          DS1KEYL  - KEY LENGTH                                      *
*---------------------------------------------------------------------*
         SPACE 1
DSCBUPDT CLOSE (PDSPDS)                CLOSE PDS
         RDJFCB PDSPDS
         LTR   R15,R15                 ?/READJFCB OK
         BZ    SAVENAME                YES, CONTINUE
         LA    R8,ERR1                 LOAD ADDR OF ERROR MSG
         B     PROCERR                 GO PROCESS THE ERROR
SAVENAME MVC   JFCBAREA(44),DSCBNAME   MOVE IN VTOC NAME
         MVC   IOBSEEK+3(5),F1CCHHR    ESTABLISH FORMAT 1 SEEK ADDRESS
         LA    R2,DSCBAREA             LOAD TARGET FOR DSCB 1
         L     R3,GETADDR              LOAD ADDR TO CP AREA
         OPEN  (PDSPDS,(OUTPUT)),TYPE=J  OPEN THE VTOC
         LA    R1,PDSPDS               LOAD ADDR IN CASE OF ERROR
         TM    PDSOFLAG,X'10'          ?/OPEN OK
         BNO   OPENERR                 NO, PROCESS THE ERROR
DSCBCP   MVC   0(24,R3),CCW1           SET UP CP FOR READING DSCB 1
         MVI   16(R3),X'06'            INDICATE READ DATA
         MVC   20(1,R3),CCWFLAG        MANIPULATE CCW FLAG
         L     R4,8(R3)                GET FIRST PART OF TIC
         AR    R4,R3                   ESTABLIST TIC ADDRESS
         ST    R4,8(R3)                RESTORE UPDATED TIC ADDR
         L     R4,16(R3)               GET RECEIVING LOCATION OF READ
         AR    R4,R2                   ESTABLISH IT
         ST    R4,16(R3)                AND SAVE IT
         BAL   R8,IO                   GO DO IO
         TM    FLAG1,X'FF'             ?/IS IT OVER
         BNO   DSCBCP1                 KEEP ON TRUCKING
         MVI   CCWFLAG,X'00'           INDICATE NO CHAINING
         DEQ   (MAJOR,MINOR,6,SYSTEMS),UCB=UCBADDR  RELEASE VTOC
         MVI   ENQFLAG,X'00'           INDICATE IT HAS BEEN DONE
         B     STATS                   PRINT THE STATS
DSCBCP1  XC    16(1,R2),16(R2)         NOPE, UPDATE DSCB
         MVC   54(5,R2),TTRLL          CHANGE TTRLL IN DSCB
         MVC   44(3,R2),LRECLKEY       CHANGE LRECL AND KEYLEN IN DSCB
         MVC   0(24,R3),CCW1           SET CCW'S TO WRITE DSCB
         L     R4,8(R3)                ESTABLISH TIC ADDRESS
         AR    R4,R3
         ST    R4,8(R3)
         L     R4,16(R3)               ESTABLISH DATA TO BE WRITTEN
         AR    R4,R2
         ST    R4,16(R3)
         LA    R3,24(R3)               PREPARE FOR READ CCW'S
         SR    R2,R2                   CLEAR THIS REG
         OI    FLAG1,X'FF'             TURN FLAG ON
         MVI   CCWFLAG,X'10'           SUPPRESS THE TRANSFER OF DATA
         RESERVE (MAJOR,MINOR,E,6,SYSTEMS),UCB=UCBADDR  ENQ ON VTOC
         B     DSCBCP
         EJECT
*---------------------------------------------------------------------*
*        STATS PUTS OUT THE FOLLOWING STATISTICS AT THE DATA SET      *
*        LEVEL :                                                      *
*           DATA SET NAME                                             *
*           NUMBER OF DIRECTORY BLOCKS FORMATTED                      *
*           TOTAL NUMBER OF DIRECTORY BLOCKS IN THE DATA SET          *
*           SPACE ALLOCATED TO THE DATA SET(TRACKS + BLOCKS)          *
*---------------------------------------------------------------------*
         SPACE 1
STATS    MVC   OUTLINE,HDR
         MVC   OUTLINE+39(44),DSNAME
         PUT   SYSPRINT,OUTLINE
         L     R2,DIRUCNT
         CVD   R2,WORK
         UNPK  WORK+4(8),WORK+5(3)
         OI    WORK+11,X'F0'
         MVC   OUTLINE,LINE1
         MVC   OUTLINE+43(5),WORK+7
         PUT   SYSPRINT,OUTLINE
         L     R2,DIRTCNT
         CVD   R2,WORK
         UNPK  WORK+4(8),WORK+5(3)
         OI    WORK+11,X'F0'
         MVC   OUTLINE,LINE2
         MVC   OUTLINE+43(5),WORK+7
         PUT   SYSPRINT,OUTLINE
         L     R2,TTRLL
         SRL   R2,16
         CVD   R2,WORK
         UNPK  WORK+4(8),WORK+5(3)
         OI    WORK+11,X'F0'
         MVC   OUTLINE,LINE3
         MVC   OUTLINE+40(4),WORK+8
         SR    R2,R2
         IC    R2,TTRLL+2
         CVD   R2,WORK
         UNPK  WORK+4(8),WORK+5(3)
         OI    WORK+11,X'F0'
         MVC   OUTLINE+52(3),WORK+9
         PUT   SYSPRINT,OUTLINE
         SPACE 2
*---------------------------------------------------------------------*
*        PREPARE TO PROCESS THE NEXT DD STATEMENT AND START ALL OVER. *
*---------------------------------------------------------------------*
         SPACE 1
NEXTDD   CLOSE (PDSPDS)                CLOSE THE DATA SET
         BAL   R8,FREEAREA             FREE CORE FOR CHANNEL PGM
         XC    MBBCCHHR,MBBCCHHR       CLEAR THE REUSABLE FIELD
         LM    R2,R3,TIOTREGS          RESTORE THE REGS USED FOR TIOT
         AR    R2,R3                   POINT TO THE NEXT TIOT ENTRY
         B     CHECKDDN                START ALL OVER
         SPACE 2
*---------------------------------------------------------------------*
*        THIS IS THE END, CONTROL WILL BE GIVEN BACK TO THE CALLER    *
*---------------------------------------------------------------------*
         SPACE 1
RETURN   CLOSE (SYSPRINT)              CLOSE STATS & MSG FILE
         L     R13,4(R13)              RESTORE PTR TO CALLER'S SAVEAREA
         LM    R14,R12,12(R13)         RESTORE HIS REGISTERS
         SR    R15,R15                 SET RC TO 0
         BR    R14
         SPACE 2
*---------------------------------------------------------------------*
*        CONTROL IS GIVEN TO IO VIA BAL R8. IF AN IO ERROR OCCURS     *
*        IT'S ALL OVER, A MESSAGE IS WRITTEN AND THE PROGRAM SELF-    *
*        DESTRUCTS. YOU ARE ON YOUR OWN TO DETERMINE THE PROBLEM.     *
*---------------------------------------------------------------------*
         SPACE 1
IO       EXCP  PDSIOB
         WAIT  1,ECB=PDSECB
         TM    PDSECB,X'20'
         BNO   IOERROR
         XC    PDSECB,PDSECB
         MVC   IOBSTART,GETADDR
         BR    R8
IOERROR  MVC   OUTLINE(37),ERR8+1      MOVE ERROR MSG TO BUFFER
         TM    ENQFLAG,X'FF'           ?/RESERVE OUTSTANDING
         BZ    ISSUEMSG                NO - ISSUE ERROR MESSAGE
         DEQ   (MAJOR,MINOR,6,SYSTEMS),UCB=UCBADDR  YES, DEQ VTOC
         MVC   ENQFLAG,X'00'           INDICATE DEQ HAS BEEN DONE
ISSUEMSG PUT   SYSPRINT,OUTLINE        WRITE THE ERROR MSG
         ABEND 100,DUMP                ABEND - I/O ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        THIS ROUTINE IS RESPONSIBLE FOR ACQUIRING ALL AREA. CONTROL  *
*        IS PASSED TO IT VIA BAL R8.                                  *
*---------------------------------------------------------------------*
         SPACE 1
GETAREA  GETMAIN R,LV=(R5)             R5 = LENGTH OF AREA
         LTR   R15,R15                 ?/AREA ACQUIRED
         BNZ   GETERR                  NO, GO INDICATE AN ERROR
         LA    R1,0(R1)                CLEAR HI-ORDERED BYTE
         ST    R1,GETADDR              SAVE ADDR OF AREA
         ST    R5,GETLNGTH             SAVE LENGTH OF AREA
         BR    R8                      RETURN TO THE CALLER
GETERR   LA    R8,ERR4                 LOAD ADDR OF ERR MSG
         B     PROCERR                 GO PROCESS THE ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        THIS ROUTINE FREES ALL ACQUIRE AREA. CONTROL IS PASSED TO IT *
*        VIA BAL R8.                                                  *
*---------------------------------------------------------------------*
         SPACE 1
FREEAREA L     R5,GETLNGTH             GET AMOUNT TO FREE
         L     R6,GETADDR              GET BEGINNING ADDRESS
         FREEMAIN R,LV=(R5),A=(R6)     FREE THE AREA
         LTR   R15,R15                 ?/AREA FREED
         BCR   8,R8                    YES, RETURN TO THE CALLER
         LA    R8,ERR5                 LOAD ADDR OF ERROR MESSAGE
         B     PROCERR                 GO PROCESS THE ERROR
         SPACE 2
*---------------------------------------------------------------------*
*        ANY FAILURE TO OPEN A DCB CAUSES OPENERR TO RECEIVE CONTROL. *
*        IF SYSPRINT IS THE DCB THAT FAILED WE DESTRUCT  WITH A USER  *
*        200. OTHERWISE AN ERROR MESSAGE WILL BE WRITTEN AND WE WILL  *
*        TERMINATE NORMALLY.                                          *
*---------------------------------------------------------------------*
         SPACE 1
OPENERR  CLC   40(8,R1),=C'SYSPRINT'   ?/OPEN SYSPRINT FAILED
         BNE   SAVEDDN                 NO, SAVE DDNAME IN MSG
         ABEND 200,DUMP
SAVEDDN  LA    R8,ERR6                 LOAD ADDR OF ERROR MSG
         MVC   22(8,R8),40(R1)         INDICATE FILE IN ERROR
PROCERR  SR    R2,R2                   CLEAR R2
         IC    R2,0(R8)                GET ERROR MESSAGE LENGTH
         MVI   OUTLINE,X'40'           CLEAR OUTPUT BUFFER           V1
         MVC   OUTLINE+1(120),OUTLINE         ××                     V1
         EX    R2,ERRORMSG             MOVE MESSAGE TO OUTPUT LINE
         MVI   OUTLINE,X'60'           INDICATE TRIPLE SPACING
         PUT   SYSPRINT,OUTLINE        WRITE THE MESSAGE
         B     RETURN                  PREPARE FOR EXIT
ERRORMSG MVC   OUTLINE+1(0),1(R8)      MOVE MESSAGE TO BUFFER
         EJECT
*---------------------------------------------------------------------*
*        ERROR MESSAGES. THE FOLLOWING IS THE FORMAT:                 *
*                                                                     *
*     --------------------------------------------------------        *
*     × LENGTH ×              THE                            ×        *
*     × OF MSG ×            MESSAGE                          ×        *
*     --------------------------------------------------------        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
ERR1     DC    X'11',C'ERROR DOING RDJFCB'                           V1
ERR2     DC    X'11',C'ERROR DOING OBTAIN'                           V1
ERR3     DC    X'0E',C'DSORG IS NOT PO'                              V1
ERR4     DC    X'0C',C'GETMAIN ERROR'                                V1
ERR5     DC    X'0D',C'FREEMAIN ERROR'                               V1
ERR6     DC    X'1B',C'ERROR OPENING FILE',CL10' '                   V1
ERR7     DC    X'1D',C'DATA SET ON UNSUPPORTED DEVICE'               V1
ERR8     DC    X'23',C'I/O ERROR---DATA SET COULD BE RUINED'         V1
ERR9     DC    X'10',C'DUPLICATE DDNAMES'                            V1
         SPACE 2
*---------------------------------------------------------------------*
*        HEADERS FOR THE PRINT FILE                                   *
*---------------------------------------------------------------------*
         SPACE 1
OUTLINE  DC    CL121' '
HDR      DC    CL121'-*** PDSCLEAR STATISTIC FOR DATASET : '
LINE1    DC    CL121'0   NUMBER OF DIRECTORY BLOCKS FORMATTED : '
LINE2    DC    CL121'    NUMBER OF DIRECTORY BLOCKS AVAILABLE : '
LINE3    DC    CL121'    SPACE ALLOCATED TO THE DIRECTORY :       TRACKX
               S     BLOCKS'
         SPACE 2
*---------------------------------------------------------------------*
*        DVTAB INDICATES HOW MANY TRACKS PER CYLINDER FOR THE FOLLOW- *
*        ING DEVICES :                                                *
*            3330-1                                                   *
*            3330-11                                                  *
*            3350                                                     *
*        THE FORMAT OF THE TABLE IS AS FOLLOWS :                      *
*                                                                     *
*        ---------------------------------------                      *
*        ×    UCB DEVICE    ×    TRACKS PER    ×                      *
*        ×      TYPE        ×     CYLINDER     ×                      *
*        ---------------------------------------                      *
*                                                                     *
*---------------------------------------------------------------------*
DVTAB    DS    0H
         DC    XL2'0D13'           3330
         DC    XL2'0B1E'           3350
         DC    XL2'0000'           END OF TABLE
DVTABEND EQU   ((*-DVTAB)/2)
         SPACE 2
*---------------------------------------------------------------------*
*        DATA CONTROL BLOCKS                                          *
*---------------------------------------------------------------------*
         SPACE 1
PDSSEQ   DCB   DSORG=PS,MACRF=R,DDNAME=DATASET1,KEYLEN=8,EODAD=EOF,RECFX
               M=F,LRECL=256,BLKSIZE=256,EXLST=JFCBPTR
PDSPDS   DCB   DDNAME=DATASET1,MACRF=(E),EXLST=JFCBPTR
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,BLKSIZE=121,RECFM=FA
         SPACE 2
*---------------------------------------------------------------------*
*        PARAMETER LISTS                                              *
*---------------------------------------------------------------------*
         SPACE 1
JFCBPTR  DS    0F
         DC    X'87'
         DC    AL3(JFCBAREA)
JFCBAREA DS    176C
DSCBREAD CAMLST SEARCH,DSCBNAME,VOLID,DSCBAREA
DSCBNAME DC    CL44' '
VOLID    DC    CL6' '
DSCBAREA DS    0D
         DS    148C
         SPACE 2
*---------------------------------------------------------------------*
*        INPUT/OUTPUT BLOCK                                           *
*---------------------------------------------------------------------*
         SPACE 1
PDSIOB   DS    0D
IOBFLAG1 DC    X'42000000'             COMMAND CHAIN, NOT RELATED
         DC    A(PDSECB)               ADDRESS OF ECB
         DC    2F'0'
IOBSTART DC    A(0)                    CP BEGINNING
         DC    A(PDSPDS)               ADDRESS OF DCB
         DC    2F'0'
IOBSEEK  DC    D'0'                    INITIAL SEEK ADDRESS
         SPACE 2
*--------------------------------------------------------------------*
*        CONSTANTS/WORK AREAS/FLAGS/ETC.                             *
*--------------------------------------------------------------------*
         SPACE 1
         DS    0D
WORK     DS    3F
PDSECB   DC    F'0'
DIRUCNT  DC    F'0'
DIRTCNT  DC    F'0'
MBBCCHHR DC    D'0'
TIOTREGS DC    2F'0'
GETADDR  DC    F'0'
UCBADDR  DC    F'0'
GETLNGTH DC    F'0'
EXTRADIR DC    F'0'
CPREGS   DC    2F'0'
TTRREGS  DC    2F'0'
MAJOR    DC    CL8'SYSVTOC'
SAVEAREA DC    18F'0'
DDNID    DC    C'DSN'
CCWFLAG  DC    XL1'00'
RCDBUMP  DC    XL4'01000000'      *****    T H E S E    *****
TRKBUMP  DC    XL4'00000001'    ***   S T A T E M E N T S   ***
CYLBUMP  DC    XL4'00010000'    ***         N E E D         ***
TTRLL    DC    5X'00'             ***** A L I G N I N G *****
LRECLKEY DC    XL3'00'
F1CCHHR  DC    XL5'00'
FLAG1    DC    XL1'00'
ENQFLAG  DC    XL1'00'
DSNAME   DC    CL44' '
NULLKEY  DC    8X'00'
HIKEY    DC    8X'FF'
DATA     DC    XL2'000E',8X'FF'
RCNTCCW  DC    XL4'10000008'
         SPACE 1
*---------------------------------------------------------------------*
*        CCW'S                                                        *
*---------------------------------------------------------------------*
         SPACE 1
CCWSID   CCW   X'31',0,X'40',5            SEARCH ID EQUAL
CCWTIC   CCW   X'08',0,X'40',0            TIC
CCWWRITE CCW   X'0D',NULLKEY,X'60',8      WRITE KEY AND DATA
CCW1     CCW   X'31',F1CCHHR,X'40',X'05'  SEARCH ID EQUAL
CCW2     CCW   X'08',0,X'40',0            TIC
CCW3     CCW   X'05',0,X'40',X'60'        WRITE DATA
         SPACE 2
*---------------------------------------------------------------------*
*        EQUATES                                                      *
*---------------------------------------------------------------------*
         SPACE 1
MINOR    EQU   VOLID+0
CVTPTR   EQU   16
SYSOFLAG EQU   SYSPRINT+48
SEQDDN   EQU   PDSSEQ+40
JFCBVOLS EQU   JFCBAREA+118
DSCBORG  EQU   DSCBAREA+38
DSCBXTNT EQU   DSCBAREA+63
DSCBCCHR EQU   DSCBAREA+96
DSCBRECK EQU   DSCBAREA+44
SEQOFLAG EQU   PDSSEQ+48
SEQDEB   EQU   PDSSEQ+44
DSCBDIR  EQU   DSCBAREA+31
DS4DEVI  EQU   DSCBAREA+24
DS4DEVFG EQU   DSCBAREA+27
DS4DEVTL EQU   DSCBAREA+28
DS4DEVTK EQU   DSCBAREA+22
DS4DEVK  EQU   DSCBAREA+26
PDSOFLAG EQU   PDSPDS+48
PDSDDN   EQU   PDSPDS+40
         SPACE 2
*---------------------------------------------------------------------*
*        DSECTS                                                       *
*---------------------------------------------------------------------*
         SPACE 1
PDSDSECT DSECT
PDSKEY   DS    CL8
PDSBLOCK DS    CL256
         END
