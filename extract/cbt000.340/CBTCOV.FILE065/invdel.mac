 INVDEL:  PROC OPTIONS(MAIN); /* INVOKE FORGROUND DELINKER     MH */
  /* ************************************************************** */
  /*                                                                */
  /*                                                                */
  /*    A TSO FOREGROUND DELINK FACILITY UNDER ISPF                 */
  /*                                                                */
  /*    (WRITTEN IN A HIGH LEVEL LANGUAGE FOLKS: (WELL MOSTLY)      */
  /*                                                                */
  /*    THE ASSEMBLER SUBROUTINES ARE ALL INCLUDED WITH THIS CODE.  */
  /*                                                                */
  /*    THEY ARE 1) A DYNAMIC ALLOCATION INTERFACE - VERY EASY TO   */
  /*                                                 USE SEE CODE   */
  /*             2) CATALOG MANAGEMENT FOR DATASET LOCATE.          */
  /*             3) TSO COMMAND HANDLER SO WE CAN ISSUE TSO COMMANDS*/
  /*                SUCH AS FREE AND PRINTOFF WITHOUT HAVING TO     */
  /*                GO THROUGH ALL THAT TSO COMMAND PROCESSOR       */
  /*                INITIALISATION GARBAGE : PPL CPPL ECT ETC ETC.  */
  /*            4)  PDS MEMBER FINDER                               */
  /*            5)  THE DELINK PROGRAM                              */
  /*                                                                */
  /*  WISH LIST : THE USER DATASETS DO NOT HAVE TO BE CATALOGUED.   */
  /*              I CAN ADD VOLUME INFORMATION LATER. MAYBE         */
  /*              RELEASE 2 OF THIS (IF I EVER GET ROUND TO IT)     */
  /*              WILL ADD THIS FUNCTION.                           */
  /*                                                                */
  /*                                                                */
  /*  COMMENTS SUGGESTIONS IMPROVEMENTS AND ERROR INFORMATION       */
  /*  GREATLY APPRECIATED. ADDRESS BELOW.                           */
  /*                                                                */
  /*                                                                */
  /*                                                                */
  /*                                                                */
  /*    MIKE HAWTHORNE  TTI CITICORP                                */
  /*                    3100 OCEAN PK BLVD                          */
  /*                    SANTA MONICA CA 90405                       */
  /*                                                                */
  /*    (213) 450-9111 X2353                                        */
  /*                                                                */
  /*                                                                */
  /*                                                                */
  /* THIS PROGRAM DISPLAYS A PANEL TO OBTAIN THE LOAD LIBRARY AND   */
  /* THE MEMBER NAME TOGETHER WITH THE USERS TSO ID. THE            */
  /* DATASETS REQUIRED ARE DYNAMICALLY ALLOCATED BY USING THE       */
  /* DYNAM DYNAMIC ALLOCATION INTERFACE.                            */
  /* (EXISTING DATASETS ARE DELETED USING THE CATMAN CATALOG        */
  /* INTERFACE) AND THE DISASSEMBLER PROGRAM IS CALLED.             */
  /*                                                                */
  /* THE LIST DATASET IS THEN BROWSED BY USING THE BROWSE FEATURE   */
  /* OF ISPF.                                                       */
  /*                                                                */
  /* NOTE : USING PANELS AND ISPF SERVICES IS MUCH EASIER THAN      */
  /*        WRITING A TSO COMMAND PROCESSOR TO DO THIS JOB.         */
  /*        NOBODY HAS TSO THESE DAYS WITHOUT ALSO HAVING ISPF.     */
  /*        PARSING COMMANDS IS MUCH SIMPLER IN PL/I AND MOST       */
  /*        OF THE CODE CAN BE WRITTEN IN A HIGH LEVEL LANGUAGE.    */
  /*                                                                */
  /*                                                                */
  /* ************************************************************** */
  /*                                                                */
  /* DEFINE EXTERNAL REFERENCES : DYNAMIC ALLOCATION INTERFACE      */
  /*                            : CATALOG MANAGEMENT                */
  /*                            : ISPF SERVICES                     */
  /*                            : TSO COMMAND PROCESSOR.            */
  /*                            : PDS MEMBER FINDER.                */
  /*                            : DELINK PROGRAM                    */
  /*                                                                */
  /* ************************************************************** */
 DCL ISPLINK          ENTRY EXTERNAL OPTIONS(ASM,INTER,RETCODE);
 DCL (CATMAN, DYNAM)  ENTRY EXTERNAL OPTIONS(ASM,INTER,RETCODE);
 DCL INVTSO           ENTRY EXTERNAL OPTIONS(ASM,INTER,RETCODE);
 DCL INVFIND          ENTRY EXTERNAL OPTIONS(ASM,INTER,RETCODE);
 DCL DELINK           ENTRY EXTERNAL OPTIONS(ASM,INTER,RETCODE);
  /* ************************************************************** */
  /*                                                                */
  /* DEFINE PLI BUILTIN FUNCTIONS/SERVICES.                         */
  /*                                                                */
  /* ************************************************************** */
 DCL (PLIRETV, LENGTH, SUBSTR, INDEX, ADDRESS, ADDR) BUILTIN;
  /* ************************************************************** */
  /*                                                                */
  /* DEFINE NON ISPF VARIABLES .                                    */
  /*                                                                */
  /* ************************************************************** */
 DCL CONS             CHAR(80) INIT(' '),
     DISL             CHAR(80) INIT(' '),
     Q                PTR,
     I                FIXED BIN(15) INIT(0),
     J                FIXED BIN(15) INIT(0),
     K                FIXED BIN(15) INIT(0),
     MEMBER           CHAR(8) INIT(' '),
     MEML             FIXED BIN(15) INIT(0),
     ERR_FLAG         CHAR(1) INIT('N'),
     MSGID            CHAR(8) INIT(' '),
     BLANK            CHAR(8) INIT(' '),
     LEN_USERID       FIXED BIN(15) INIT(0),
     LEN_ERR          FIXED BIN(15) INIT(0),
     SAVERC           FIXED BIN(15) INIT(0),
     L_LOAD           FIXED BIN(15) INIT(0),
     L_LNAME          FIXED BIN(15) INIT(0),
     L_CSECT          FIXED BIN(15) INIT(0),
     MEM              CHAR(8) INIT(' '),
     WORK             FIXED BIN(31) INIT(0),
     TESTDSN          CHAR(44) INIT(' '),
     BROWSE_DSN       CHAR(44) INIT(' '),
     MSGFILE          FILE STREAM OUTPUT ENV(FB),
     SYSIN            FILE STREAM ENV(FB),
     FLAG             CHAR(1) INIT(' '),
     QUOTE            CHAR(1) INIT(''''), /* THIS IS A SINGLE QUOTE !*/
  /* ************************************************************** */
  /*                                                                */
  /* SET UP PARAMETER LIST FOR CATALOG MANAGEMENT INTERFACE         */
  /*                                                                */
  /* ************************************************************** */
  PARMS1              CHAR(169) BASED(Q),
  1  WORK1            UNALIGNED,
    3 FUNC            CHAR(1) INIT('P'),
    3 DSNAME          CHAR(44) INIT(' '),
    3 VOLCOUNT        FIXED BIN(31) INIT(0),
    3 VOLLIST(20)     CHAR(6),
  /* ************************************************************** */
  /*                                                                */
  /* SET UP PARAMETER LIST FOR MEMBER FINDER MODULE                 */
  /*                                                                */
  /* ************************************************************** */
  1 FORMEM            UNALIGNED,
    3  MPARML         FIXED BIN(15) INIT(8),
    3  LIBNAME        CHAR(9) INIT(' '),
  /* ************************************************************** */
  /*                                                                */
  /* SET UP PARAMETER LIST FOR TSO COMMAND PROCESSING.              */
  /*                                                                */
  /* ************************************************************** */
  TSOLEN              FIXED BIN(31) INIT(0),
  TSOCMD              CHAR(255) INIT(' '),
  /* ************************************************************** */
  /*                                                                */
  /* DEFINE ISPF VARIABLES WE ARE GOING TO USE IN THE PROGRAM.      */
  /*                                                                */
  /* ************************************************************** */
     LOAD             CHAR(44) INIT(' '),
     LNAME            CHAR(8) INIT(' '),
     CSECT            CHAR(8) INIT(' '),
     ZUSER            CHAR(7)  INIT('ZUSER'),
     USERID           CHAR(8)  INIT(' '),
     LLNAME           FIXED BIN(31) INIT(LENGTH(LNAME));
 DCL LLOAD            FIXED BIN(31) INIT(LENGTH(LOAD)),
     LCSECT           FIXED BIN(31) INIT(LENGTH(CSECT)),
     LZUSER           FIXED BIN(31) INIT(LENGTH(ZUSER));
 Q = ADDR(WORK1);
 FETCH DYNAM;
 FETCH CATMAN;
 FETCH INVTSO;
  /* ************************************************************** */
  /*                                                                */
  /* SET UP THE ISPF ENVIRONMENT:                                   */
  /*                                                                */
  /*        A) DEFINE OUR PROGRAMS VARIABLES TO ISPF SO WE          */
  /*           CAN GET DATA FROM A PANEL.                           */
  /*        B) GET THE TSO USER-ID FROM ISPF. THIS IS THE SYSTEM    */
  /*           VARIABLE ZUSER FROM ISPF OBTAINED VIA VCOPY FUNCTION */
  /*        C) SHOW THE USER THE PANEL                              */
  /*        D) GET DATA BACK FROM THE PANEL                         */
  /*                                                                */
  /*       (IF PLIRETV = 8 THEN PFK3 OR END KEY HAS BEEN PRESSED)   */
  /*        IF THIS HAS HAPPENED WE EXIT WITHOUT MORE ADO.          */
  /*                                                                */
  /* ************************************************************** */
 CALL ISPLINK('VDEFINE', '(LNAME)', LNAME, 'CHAR', LLNAME);
 CALL ISPLINK('VDEFINE', '(CSECT)', CSECT, 'CHAR', LCSECT);
 CALL ISPLINK('VDEFINE', '(LOAD)', LOAD, 'CHAR', LLOAD);
 CALL ISPLINK('VCOPY', ZUSER, LZUSER, USERID, 'MOVE');
 CALL ISPLINK('DISPLAY', 'DELINK  ', 'INV022  ');
 LEN_USERID = INDEX(USERID,' ');
 IF LEN_USERID ^= 0
 THEN LEN_USERID = LEN_USERID - 1;
 REDOO: IF PLIRETV = 8    /* COME BACK HERE ON ERRORS         */
 THEN GO TO EXIT2;        /* EXIT WITHOUT DOING ANY PROCESSING*/
 TSOLEN = 1;
 CALL DYNAM(WORK, 'INIT ');
 CALL DYNAM(WORK,'ALLOC ','DD=MSGFILE;','TE;');
 OPEN FILE(MSGFILE) LINESIZE(80);
  /* ************************************************************** */
  /*                                                                */
  /*   PARSE THE DATA FROM THE USER.                                */
  /*                                                                */
  /*        A) GET MEMBER NAME FROM THE PANEL. IF NOT ENTERED THEN  */
  /*           ISSUE A MESSAGE AND REDISPLAY PANEL'                 */
  /*        B) GET THE LOAD LIBRARY NAME FROM THE PANEL.            */
  /*           IF THE DATASET IS UNQUOTED APPEND THE TSO USER-ID.   */
  /*           IF MISSING QUOTE THEN ISSUE ERROR MESSAGE .          */
  /*           IF UNCATALOGUED THEN ISSUE A MESSAGE.                */
  /*        C) CHECK TO SEE IF THE MEMBER IS ACTUALLY IN THE LOAD   */
  /*           LIBRARY. IF IT'S NOT ISSUE A MESSAGE .               */
  /*        D) IF EVERYTHINGS OK THEN ALLOCATE DATASETS FOR THE     */
  /*           DELINK PROGRAM.                                      */
  /*                                                                */
  /* ************************************************************** */
  DSNAME = ' ';
  MEM = ' ';
 IF SUBSTR(LNAME,1,1) = ' '          /* MISSING MEMBER NAME        */
 THEN DO;
         CALL ISPLINK('DISPLAY', BLANK, 'INV023  ', 'LNAME  ');
         GO TO REDOO;
      END;
 MEM = LNAME;
  IF SUBSTR(LOAD,1,1) = ' '
  THEN DO;
         CALL ISPLINK('DISPLAY', BLANK, 'INV024  ', 'LOAD   ');
         GO TO REDOO;
       END;
  TESTDSN = LOAD;
  CALL PARSE;
  IF ERR_FLAG = 'Y'          /* PARSE ERROR */
  THEN DO;
          ERR_FLAG = 'N';
          CALL ISPLINK('DISPLAY', BLANK, MSGID, 'LOAD    ');
          GO TO REDOO;
       END;
  K = INDEX(DSNAME,' ');
  K =K - 1;
  CALL DYNAM(WORK, 'ALLOC ', 'DSN='
       ×× SUBSTR(DSNAME,1,K)  ×× ' SHR;', 'DD=WXYZ;');
  LIBNAME = ' ';
  MPARML = 8;
  LIBNAME = MEM;
  FETCH INVFIND;
  CALL INVFIND(FORMEM);
  SAVERC = PLIRETV;
  RELEASE INVFIND;
  CALL DYNAM(WORK, 'UNALLOC ', 'DSN='
       ×× SUBSTR(DSNAME,1,K) ×× ' KEEP;', 'DD=WXYZ;');
  IF SAVERC ^= 0
  THEN DO;
          CALL ISPLINK('DISPLAY', BLANK, 'INV008  ', 'LNAME   ');
          GO TO REDOO;
       END;
  I = INDEX(LIBNAME,' ');
  I = I - 1;
  DISL = ' ' ×× SUBSTR(LIBNAME,1,I) ×× ' ' ×× SUBSTR(CSECT,1,8);
  LOAD = DSNAME;
  I = INDEX(CSECT, ' ');
  IF I > 9
  THEN DO;
          CALL ISPLINK('DISPLAY', BLANK, 'INV007  ', 'CSECT   ');
          GO TO REDOO;
       END;
  L_LOAD = INDEX(LOAD,' ') - 1;
  /* ************************************************************** */
  /*                                                                */
  /*                                                                */
  /*   DELETE DATASETS USERID.DELINK.LIST USERID.DELINK.OBJ         */
  /*   UNALLOCATE SYSPUNCH,SYSLIB,SYSPRINT AND SYSIN.               */
  /*   (THESE DATASETS ARE OFTEN ALREADY ALLOCATED BY THE USER      */
  /*    ALREADY. IF NOT ALLOCATED IGNORE ANY SYSLOG MESSAGES)       */
  /*   ALLOCATE DATASETS NEEDED BY THE DELINK PROGRAM               */
  /*   AND CALL IT .                                                */
  /*                                                                */
  /* ************************************************************** */
 CONS= 'TSO INTERACTIVE DELINK FUNCTION V1.0 MIKE HAWTHORNE AUG 1985';
 CALL WRITEIT;
 FUNC = 'P';                    /* SET FOR PURGE OF DATASETS */
 DSNAME=SUBSTR(USERID,1,LEN_USERID) ×× '.DELINK.LIST';
 CALL CATMAN(PARMS1);           /* IGNORE RET CODE IF NOT 0  */
 FUNC = 'P';                    /* SET FOR PURGE OF DATASETS */
 DSNAME=SUBSTR(USERID,1,LEN_USERID) ×× '.DELINK.OBJ';
 CALL CATMAN(PARMS1);           /* IGNORE RET CODE IF NOT 0  */
 FUNC = 'P';                    /* SET FOR PURGE OF DATASETS */
 DSNAME=SUBSTR(USERID,1,LEN_USERID) ×× '.DELINK.UT1';
 CALL CATMAN(PARMS1);           /* IGNORE RET CODE IF NOT 0  */
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SYSPUNCH;');
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SYSLIB;');
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SYSIN;');
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SYSPRINT;');
 CALL DYNAM(WORK, 'ALLOC ',
 'DSN=' ×× SUBSTR(USERID,1,LEN_USERID) ×× '.DELINK.OBJ NEW CATLG;',
          'LRECL=80;','BLKSIZE=800;','FB;','CL;',
          'UNIT=SYSDA;','DD=SYSPUNCH;',
          'TRK PRIMARY=15 SECONDARY=5;');
 CALL DYNAM(WORK, 'ALLOC ',
 'DSN=' ×× SUBSTR(USERID,1,LEN_USERID) ×× '.DELINK.UT1 NEW CATLG;',
          'UNIT=SYSDA;','DD=SYSIN;',
          'LRECL=80;','BLKSIZE=80;','FB;',
          'TRK PRIMARY=1 SECONDARY=1;');
 PUT FILE(SYSIN) EDIT (DISL)(A(80));
 CLOSE FILE(SYSIN);    /* STILL LEAVE ALLOCATED HOWEVER        */
 CALL DYNAM(WORK, 'ALLOC ',
 'DSN=' ×× SUBSTR(USERID,1,LEN_USERID) ×× '.DELINK.LIST NEW CATLG;',
          'LRECL=133;','BLKSIZE=133;','FBA;',
          'UNIT=SYSDA;','DD=SYSPRINT;',
          'TRK PRIMARY=15 SECONDARY=5;');
 CALL DYNAM(WORK, 'ALLOC ',
 'DSN=' ×× SUBSTR(LOAD,1,L_LOAD) ×× ' SHR;',
          'DD=SYSLIB;');
  FETCH DELINK;
  CALL DELINK;
  SAVERC = PLIRETV;
  RELEASE DELINK;
  CONS= 'DELINK FUNCTION HAS HAS FINISHED RC = ' ×× SAVERC;
  CALL WRITEIT;
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SYSPUNCH;');
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SYSLIB;');
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SYSIN;');
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=SYSPRINT;');
 FUNC = 'P';                    /* SET FOR PURGE OF DATASETS */
 DSNAME=SUBSTR(USERID,1,LEN_USERID) ×× '.DELINK.UT1';
  CALL CATMAN(PARMS1);         /* IGNORE RET CODE IF NOT 0  */
  CONS = 'ENTERING ISPF BROWSE OF DELINK OUTPUT : PLEASE PRESS ENTER';
  CALL WRITEIT;
  CONS = 'TO EXIT BROWSE PRESS PFK3/15';
  CALL WRITEIT;
  BROWSE_DSN = 'DELINK.LIST';
  CALL ISPLINK ('BROWSE', BROWSE_DSN);
  CONS = 'INTERACTIVE DELINK FUNCTION HAS FINISHED';
  CALL WRITEIT;
  GO TO EXIT1; /* REDUNDANT STATEMENT BUT MAKES CODE EASIER TO READ */
  /* ************************************************************** */
  /*                                                                */
  /*  THIS SUBROUTINE DOES THE FOLLOWING :                          */
  /*                                                                */
  /*                  A) TOO MANY QUOTES ON DATASET NAME.           */
  /*                  B) IF WE HAVE ( OR ) IN THE DATASET           */
  /*                     IS IT VALID. I.E '(' MUST COME BEFORE ')'. */
  /*                  C) IS THE MEMBER NAME 8 CHARS OR LESS.        */
  /*                  D) CHECKS FOR QUOTED DATASET. IF QUOTED       */
  /*                     CHECKS FOR MATCHING END OF QUOTE.          */
  /*                  E) IF NOT QUOTED APPENDS THE USERS TSO ID     */
  /*                     IN FRONT OF THE DSN.                       */
  /*                  F) CHECKS WHETHER THE DATASET CATALOGUED.     */
  /*                  G) IS THE MEMBER IN THE PDS IF ITS A PDS.     */
  /*                                                                */
  /*                                                                */
  /* ERRORS WILL CAUSE A RE-DISPLAY OF THE PANEL WITH A MESSAGE.    */
  /* PF2 WILL PRESENT THE ALTERNATE LONG MESSAGE.                   */
  /*                                                                */
  /*                                                                */
  /* ************************************************************** */
 PARSE: PROC;
 MEML = 0;
 MEMBER=' ';
 DSNAME = ' ';
 K = INDEX (TESTDSN,QUOTE ×× QUOTE); /* CHECK FOR DOUBLE QUOTES */
 IF K ^= 0
 THEN DO;
         ERR_FLAG = 'Y';
         MSGID = 'INV009  '; /* TOO MANY QUOTES          */
         GO TO ENDPARS;
      END;
 IF SUBSTR(TESTDSN,1,1) = QUOTE   /* EXTRACT QUOTES IF ANY   */
  THEN DO;                        /* IF THERE APPEND USER ID */
          K = INDEX (TESTDSN,' ');     /* GET LENGTH OF SOURCE DSN  */
          K = K - 1;                   /* GET THE REAL LENGTH       */
          IF SUBSTR(TESTDSN,K,1) ^= QUOTE
          THEN DO;
                  ERR_FLAG = 'Y';
                  MSGID = 'INV004  ';
                  GO TO ENDPARS;
               END;
          ELSE DO;
                  DSNAME=SUBSTR(TESTDSN,2,K-2); /* EXTRACT QUOTES */
               END;
       END;
  ELSE DO;
          K = INDEX (TESTDSN,QUOTE);   /* CHECK FOR A SINGLE QUOTE */
          IF K = 0                             /* NOT FOUND   OKAY */
          THEN DO;
                  K = INDEX (TESTDSN,' ');
                  K = K - 1;
                  DSNAME=SUBSTR(USERID,1,LEN_USERID) ×× '.'
                  ×× SUBSTR(TESTDSN,1,K);
                END;
          ELSE DO;
                  ERR_FLAG = 'Y';
                  MSGID = 'INV004  ';
                  GO TO ENDPARS;
               END;
       END;
 K = INDEX (DSNAME,'(');  /* NOTE WE HAVE NOW EXTRACTED QUOTE */
 IF K = 0                 /* AND IF NECESSARY APPENDED TSO USER ID */
 THEN DO;
         K = INDEX (DSNAME,')');
         IF K ^=0
         THEN DO;
                 ERR_FLAG = 'Y';
                 MSGID = 'INV004  '; /* SET INVALID DATASET NAME */
                 GO TO ENDPARS;
             END;
      END;        /* THAT TAKES CARE OF UNBALANCED PARENTHESES */
 ELSE DO;         /* WE HAVE ALREADY FOUND A  '(' SO VALIDATE DS */
         I = INDEX (DSNAME,')');
         IF I = 0
         THEN DO;
                 ERR_FLAG = 'Y';    /* UNBALANCED PARENTHESES */
                 MSGID = 'INV004  ';
                 GO TO ENDPARS;
             END;
         IF SUBSTR(DSNAME,I+1,1) = QUOTE  /* QUOTED DSN FOLKS   */
         THEN DO;
                 IF SUBSTR(DSNAME,I+2,1) ^= ' ' /* GARBAGE DSN  */
                 THEN DO;
                         ERR_FLAG = 'Y';
                         MSGID = 'INV004  ';
                         GO TO ENDPARS;
                      END;
              END;
         IF SUBSTR(DSNAME,I+1,1) ^= ' ' /* GARBAGE DSN  */
         THEN DO;
                 ERR_FLAG = 'Y';
                 MSGID = 'INV004  ';
                 GO TO ENDPARS;
              END;
         ELSE DO;
                 IF I < K          /* PARENTHESES ASS ABOUT TIT  */
                 THEN DO;          /* GOT ) BEFORE (  TURKEY !   */
                         ERR_FLAG = 'Y';
                         MSGID = 'INV004  ';
                         GO TO ENDPARS;
                      END;
                 MEML = I - K;
                 IF MEML > 9      /* INVALID MEMBER NAME          */
                 THEN DO;
                         ERR_FLAG = 'Y';
                         MSGID = 'INV007  ';
                         GO TO ENDPARS;
                      END;
                 ELSE DO;
                         MEMBER = SUBSTR(DSNAME,K+1,I-1);
                      END;
             END;
       END;
  IF ERR_FLAG = 'N'          /* ON ERROR SKIP REST OF PROCESSING    */
  THEN DO;
          FUNC = 'I';        /* CHECK IF CATALOGED                  */
          CALL CATMAN(PARMS1);
          IF PLIRETV ^=0
          THEN DO;
                  MSGID = 'INV002  ';
                  ERR_FLAG = 'Y';
                  GO TO ENDPARS;
               END;
       END;
  ENDPARS: IF ERR_FLAG = 'N'
  THEN DO;
          IF MEML > 0       /* WE'VE GOT A PDS HERE                */
          THEN DO;          /* NOW CHECK FOR MEMBER OF A PDS       */
                  CALL CHEKMEM;
               END;
       END;
 END PARSE;
- /* ************************************************************** */
  /*                                                                */
  /*  THIS SUBROUTINE ISSUES ANY NON ISPF TERMINAL MESSAGES         */
  /*                                                                */
  /*  (NOTE MSGFILE IS AN ORDINARY PL/I STREAM DIRECTED I/O         */
  /*   DATASET : THE DYNAMIC ALLOCATION INTERFACE ALLOCATES IT      */
  /*   AS A TERMINAL FILE SO YOU DO NOT NEED SUBROUTINES            */
  /*   FOR TPUT/TGET)                                               */
  /*                                                                */
  /* ************************************************************** */
 WRITEIT: PROC;
          PUT FILE(MSGFILE) EDIT (CONS)(A(80));
 END WRITEIT;
 CHEKMEM: PROC;
  /* ************************************************************** */
  /*                                                                */
  /* WE WILL CHECK FOR MEMBER OF A PDS. CATALOG MANAGEMENT WILL     */
  /* TELL US IF THE WHOLE PDS IS OUT THERE BUT WILL NOT TELL US     */
  /* IF AN ACTUAL MEMBER IS IN THE PDS.                             */
  /* WE CAN GET THAT FROM DYNAMIC ALLOCATION ROUTINES AND OUR       */
  /* PDS MEMBER FINDER WHICH NEEDS A DDNAME OF WXYZ.                */
  /*                                                                */
  /* ************************************************************** */
  IF ERR_FLAG = 'N'
  THEN DO;
         K = INDEX(DSNAME,'(');
         K = K - 1;
         CALL DYNAM(WORK, 'ALLOC ', 'DSN='
         ×× SUBSTR(DSNAME,1,K)  ×× ' SHR;', 'DD=WXYZ;');
         IF PLIRETV ^= 0
         THEN DO;
                 CONS= 'DYNALLOC FAILED DSN='
                 ×× SUBSTR(DSNAME,1,K) ×× 'RC='
                 ×× PLIRETV;
                 CALL WRITEIT;
                 GO TO EXIT1;
              END;
         LIBNAME = ' ';
         MPARML = 8;
         LIBNAME = SUBSTR(MEMBER,1,MEML-1);
         FETCH INVFIND;
         CALL INVFIND(FORMEM);
         SAVERC = PLIRETV;
         RELEASE INVFIND;
         CALL DYNAM(WORK, 'UNALLOC ', 'DSN='
                    ×× SUBSTR(DSNAME,1,K) ×× ' KEEP;', 'DD=WXYZ;');
         IF SAVERC ^= 0
         THEN DO;
                 ERR_FLAG = 'Y';
                 MSGID = 'INV008';
              END;
       END;
  END CHEKMEM;
 EXIT1: CLOSE FILE(MSGFILE);
 CALL DYNAM(WORK, 'UNALLOC ', 'DD=MSGFILE;');
 EXIT2: RELEASE CATMAN;
 RELEASE DYNAM;
 RELEASE INVTSO;
 CALL ISPLINK('VDELETE', '(LNAME)');
 CALL ISPLINK('VDELETE', '(CSECT)');
 CALL ISPLINK('VDELETE', '(LOAD)');
 END INVDEL;
