CMD      TITLE 'CMD - TSO COMMAND TO ISSUE OPERATOR COMMANDS'
CMD      CSECT
         BALR  12,0
         USING *,12
         L     1,16
         L     1,0(1)
         L     1,4(1)
         L     1,X'B4'(1)               JSCB
         LH    2,X'10C'(1)              TJID
         N     2,=X'00007FFF'
         O     2,=X'00008000'
NEXT     DS    0H
         TPUT  MSG1,L'MSG1
         XC    DATA,DATA
         TGET  DATA,L'DATA
         LTR   15,15                    SUCCESSFUL?
         BNZ   EXIT                     NO, ERROR
         CLC   =C'END',DATA
         BE    EXIT
         LA    15,DATA+L'DATA-1
         LA    0,1
         LA    1,DATA-1
         LNR   0,0
CLEAR    CLI   0(15),X'00' END OF COMMAND?
         BE    LOOP
         CLI   0(15),C' '
         BNE   LAST
LOOP     BXH   15,0,CLEAR
         B     NEXT
LAST     LA    15,1(15)
         MVI   0(15),C' '
         LA    1,BUFFER
         SR    15,1
         STH   15,BUFFER
         CLC   =H'5',BUFFER
         BNE   GO
         CLI   DATA,C'E'                ABBREV FOR END?
         BE    EXIT
GO       DS    0H
         MODESET KEY=ZERO
         LA    1,BUFFER
         SR    0,0                      FOR MASTER CON CMDS
         SVC   34
         LR    3,15
         MODESET KEY=NZERO
AFTER    DS    0H
         B     NEXT
EXIT     SR    15,15
         SVC   3
BUFFER   DS    0F
         DC    AL2(ENDBUFF-*,0)
DATA     DC    CL120'$',C' '
ENDBUFF  DS    0H
MSG1     DC    C'ENTER REQUEST'
         LTORG
         END
