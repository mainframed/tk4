EJDATEX  TITLE 'JULIAN TO EUROPEAN DATE CONVERSION SUBROUTINE'
*
*      THIS PROGRAM CONVERTS A JULIAN DATE TO A EUROPEAN DATE. UPON
* COMPLETION, REGISTER 15 CONTAINS 0 IF THE CONVERSION WAS SUCCESSFUL
* OR 12 IF THE CONVERSION FAILED (DUE TO INVALID INPUT JULIAN DATE).
* THE CALLING SEQUENCE FOR "EJDATEX" IS AS FOLLOWS
*           1. THE INPUT JULIAN DATE IN THE FORMAT "YYDDD"
*           2. THE OUTPUT EUROPEAN DATE IN THE FORMAT "DDMMYY"
*
*      THE "EJDATEX" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
EJDATEX  ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING INPUT,R4            ADDRESS INPUT PARAMETER DSECT
         USING OUTPUT,R5           ADDRESS OUTPUT PARAMETER DSECT
         USING WORKAREA,R13        ADDRESS WORKAREA DSECT
         LM    R4,R5,0(R2)         LOAD PARAMETER POINTERS
         LOAD  EP=JDATEX           LOAD GREGORIAN CONVERSION ROUTINE
         LR    R1,R2               POINT TO PARAMETER LIST
         LR    R15,R0              POINT TO "JDATEX" ENTRY POINT
         BALR  R14,R15             CALL "JDATEX"
         LTR   R15,R15             IF THE CONVERSION DIDN'T SUCCEED
         BNZ   ERROR                  THEN FLAG AS ERROR
         MVC   WORK,MONTHOUT       CONVERT FROM GREGORIAN TO
         MVC   MONTHOUT,DAYOUT        EUROPEAN
         MVC   DAYOUT,WORK            FORMAT
         B     EXIT                     AND LEAVE
ERROR    DS    0H
         LA    R15,12              INDICATE ERROR IN INPUT
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
         EJECT
INPUT    DSECT ,                   MAP OF INPUT JULIAN DATE
YEARIN   DS    CL2                 INPUT JULIAN YEAR
DAYIN    DS    CL3                 INPUT JULIAN DAY
         SPACE
OUTPUT   DSECT ,                   MAP OF OUTPUT EUROPEAN DATE
DAYOUT   DS    CL2                 OUTPUT EUROPEAN DAY
MONTHOUT DS    CL2                 OUTPUT EUROPEAN MONTH
YEAROUT  DS    CL2                 OUTPUT EUROPEAN YEAR
         SPACE
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
WORKSTRT EQU   *
         DS    18A                 REGISTER SAVE AREA
WORK     DS    CL2
WORKEND  EQU   *
WORKSIZE EQU   WORKEND-WORKSTRT    COMPUTE SIZE OF WORKAREA
         END
