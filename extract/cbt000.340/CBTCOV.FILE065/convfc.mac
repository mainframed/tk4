CONVFC   TITLE 'PACK, CONV TO FLOATING PT AND CALC SYSTEM SEC'
*
*   THIS ROUTINE COMPUTES IS PASSED CPU TIME, EXCPS, AND SERVICE UNITS
*   AND RETURNS SYSTEM SECONDS.
*
*   DESCRIPTION OF PARAMETERS:
*
*        PARM1 - 7  CHARACTER CPU TIME (IN HUNDREDTHS OF SECONDS)
*        PARM2 - 9  CHARACTER EXCP COUNT
*        PARM3 - 15 CHARACTER SERVICE UNIT COUNT
*        PARM4 - 4  BYTE FLOATING POINT SYSTEM SECOND COUNT
*        PARM5 - 15 CHARACTER SYSTEM SECOND COUNT (IN HUNDREDTHS)
*
*  THE 'CONVFC' SUBROUTINE IS REENTRANT AND REUSABLE.
*
CONVFC   ENTER PARMREG=2,              INITIALIZATION                  X
               GETMAIN=(WORKSIZE,0)
         REGISTER                      REGISTER EQUATES
         USING WORKAREA,R13            ADDRESS WORK AREA DSECT
         MVC   CALLIST,CCALLST
         MVC   PARMLIST,0(R2)          SAVE PARAMETER ADDRESSES
*
*     LOAD THE GUYS WE'LL NEED
*
         LOAD  EP=COAD06
         ST    R0,COAD06EP
         LOAD  EP=COAD01
         ST    R0,COAD01EP
         LOAD  EP=MODTMX
         ST    R0,MODTMXEP
*
*     CONVERT INPUT PARMS BACK TO INTEGER, THEN TO REAL
*
         L     R4,PARM1                LOAD PTR TO CPU PARM
         PACK  DBLWORD(8),0(7,R4)
         CVB   R7,DBLWORD
         ST    R7,ICPU
         L     R15,COAD01EP
         CALL  (15),                   CONVERT INTEGER TO REAL         X
               (ICPU,FCPU),                                            X
               MF=(E,CALLIST)
         L     R5,PARM2                LOAD PTR TO EXCP PARM
         PACK  DBLWORD(8),0(9,R5)
         CVB   R7,DBLWORD
         ST    R7,IEXCP
         L     R15,COAD01EP
         CALL  (15),                   CONVERT INTEGER TO REAL         X
               (IEXCP,FEXCP),                                          X
               MF=(E,CALLIST)
         L     R6,PARM3                LOAD PTR TO SERV UNIT PARM
         PACK  DBLWORD(8),0(15,R6)
         CVB   R7,DBLWORD
         ST    R7,ISERU
         L     R15,COAD01EP
         CALL  (15),                   CONVERT INTEGER TO REAL         X
               (ISERU,FSERU),                                          X
               MF=(E,CALLIST)
*
*     NOW COMPUTE SYSTEM SECONDS
*
         L     R5,MODTMXEP             GET ADDRESSES OF COEFFICIENTS
         LE    R2,0(R5)                LOAD COEF1
         LE    R4,4(R5)                LOAD COEF2
         LE    R6,8(R5)                LOAD COEF3
         ME    R2,FCPU                 COEF1*CPU
         ME    R4,FEXCP                COEF2*EXCPS
         ME    R6,FSERU                COEF3*SERVU
         ME    R4,=E'100'              EXCPS*100 (TO MATCH CPU SEC)
         ME    R6,=E'100'              SERVU*100 (TO MATCH CPU SEC)
         AER   R4,R2                   COEF1*CPU+COEF2*EXCPS
         AER   R4,R6                   COEF1*CPU+COEF2*EXCPS+COEF3*SERV
         L     R2,PARM4                LOAD ADDR OF FLOATING SS TIME
         STE   R4,0(R2)                AND RETURN IT
         STE   R4,FSERU                ALSO SAVE IT FOR CONVERT
         L     R15,COAD06EP
         CALL  (15),                   CONVERT IT BACK TO INTEGER      X
               (FSERU,ISSTIME),                                        X
               MF=(E,CALLIST)
         L     R8,ISSTIME
         CVD   R8,DBLWORD
         L     R8,PARM5
         UNPK  0(15,R8),DBLWORD(8)
         OI    14(R8),X'F0'
*
*     CLEAN UP AND LEAVE
*
         DELETE EP=COAD06
         DELETE EP=COAD01
         DELETE EP=MODTMX
         LEAVE RETCODE=(15),       RETURN TO CALLER                    X
               GETMAIN=(WORKSIZE,0)
*
*     CONSTANTS
*
         LTORG
CCALLST  CALL  ,(,,,,,),MF=L       CONSTANT "CALL" PARAMETER LIST
CCALLEN  EQU   *-CCALLST           COMPUTED LENGTH OF "CCALLIST"
*
*     MAP OF WORK AREA
*
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
         DS    18A                 REGISTER SAVE AREA
COAD01EP DS    A                   "COAD01" ENTRY POINT ADDRESS
COAD06EP DS    A
MODTMXEP DS    A                   "MODTMX" ENTRY POINT ADDRESS
CALLIST  DS    CL(CCALLEN)         PARAMETER LIST FOR "CALL" MACRO
ISSTIME  DS    F
FCPU     DS    E
FEXCP    DS    E
FSERU    DS    E
ICPU     DS    F
IEXCP    DS    F
ISERU    DS    F
DBLWORD  DS    D
PARMLIST DS   0CL(5*4)
PARM1    DS    A
PARM2    DS    A
PARM3    DS    A
PARM4    DS    A
PARM5    DS    A
WORKSIZE EQU   *-WORKAREA          COMPUTED SIZE OF WORK AREA
         END
