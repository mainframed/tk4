ITIMERX   TITLE 'SUBROUTINE WHICH CALLS OTHER UTILITY PROGRAMS'
*
*      THIS PROGRAM CALLS OTHER UTILITY PROGRAMS BASED ON A CODE
* WHICH IT IS PASSED. THE CODES AND THE CORRESPONDING UTILITY PROGRAMS
* ARE AS FOLLOWS:
*
*        CODE     PROGRAM      FUNCTION
*
*          0      SECOND       OBTAIN JOB STEP ACCUMULATED TCB TIME
*          1      DATE         OBTAIN THE CURRENT DATE "MM/DD/YY"
*          2      CLOCK        OBTAIN THE TIME OF DAY  " HH.MM.SS."
*          3      JDATE        OBTAIN THE JULIAN DATE  "YYDDD"
*
* THE CALLING SEQUENCE FOR "ITIMERX" IS AS FOLLOWS:
*          1. THE CODE INDICATING WHICH PROGRAM TO CALL
*                (FULL WORD BINARY)
*          2. THE RESULT FIELD (FULL WORD BINARY FOR FUNCTION CODE #0,
*                 TEN CHARACTERS FOR THE OTHER FUNCTION CODES)
*
*     "ITIMERX" IS REENTRANT AND REUSEABLE.
*
ITIMERX  ENTER PARMREG=2,         SAVE REGISTERS AND INITIALIZE        X
               GETMAIN=(72,0)
         REGISTER                 REGISTER EQUATES
         LM    R4,R5,0(R2)        LOAD PARAMETER POINTERS
         L     R4,0(,R4)          GET FUNCTION CODE INTO REGISTER
         C     R4,=F'3'           HIGHEST OPTION CODE
         BH    ILLEGF             ..ERROR
         C     R4,=F'0'           LOWEST OPTION CODE
         BL    ILLEGF             ..ERROR
         SLA   R4,3               MULTIPLY BY EIGHT
         LA    R4,PGMTBL(R4)      POINT TO DESIRED PROGRAM
         LOAD  EPLOC=(R4)         LOAD THE DESIRED PROGRAM
         LR    R15,R0             SAVE PROGRAM ENTRY POINT ADDRESS
         LA    R1,4(R2)           POINT TO RESULT FIELD ADDRESS
         CALL  (15),MF=(E,(1))    CALL THE DESIRED PROGRAM
EXITT    DS    0H
         LEAVE RETCODE=0,         RESTORE REGISTERS, RETURN TO CALLER  X
               GETMAIN=(72,0)
         SPACE 2
ILLEGF   L     R15,=F'-1'         STORE ERROR FLAG IN R15   AND
         ST    R15,0(,R5)         RESULT FIELD ADDRESS.
         B     EXITT
         EJECT
PGMTBL   DS    0H                 LIST OF UTILITY PROGRAMS TO BE CALLED
         DC    CL8'SECOND'
         DC    CL8'DATE'
         DC    CL8'CLOCK'
         DC    CL8'JDATE'
         SPACE 2
         END
