IDMF     TITLE 'IDMBOZD - INTERFACE TO INFO/MVS'
         @     IDMBOZD                                                ,
***********************************************************************
*/*                                                                  */
*/*      ========================================================    */
*/*       I D M F  -  INTERACTIVE DATA MANAGEMENT FACILITY           */
*/*      ========================================================    */
*/*                                                                  */
*/********************************************************************/
            EJECT
***********************************************************************
*                                                                     *
*           IDMBOZD                                                   *
*                                                                     *
* ABSTRACT: INTERFACE TO INFO/MVS - BLGOZD                            *
*                                                                     *
* FUNCTION: IDMBOZD IS A PROGRAM TO INVOKE BLGOZD                     *
*                                                                     *
*           IDMBOZD CALLED BY IDMMAIN, WHEN USER SELECTS A ROW.       *
*           IDMBOZD GETS DSNAME (AND VOLUME) FROM ROW, SELECTED BY    *
*                    USER, ALLOCATES INFO/MVS DATABASE AND            *
*                    LINKS TO BLGOZD (INFO/MVS RETRIEVEL PGM).        *
*                                                                     *
*           AFTER FUNCTION DONE   IDMBOZD UPDATES ROW IN TABLE AND    *
*           SETS UP MESSAGES FOR USER.                                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* INPUT: A ROW VARIABLES, PASSED IN SHARED POOL.                      *
*        R1 -> ADDRESS OF EXTERNAL PARM FIELD, WHICH CONTAINS         *
*              INFO/MVS RETRIEVEL PROGRAM NAME.                       *
*                                                                     *
* PROGRAM WRITTEN USING STRUCTURED ASSEMBLER MACROS                   *
* MACROS USED: SET OF STRUCTURED MACROS                               *
*              SAVE                                                   *
*              RETURN                                                 *
*              GETMAIN                                                *
*              CALL                                                   *
*              FREEMAIN                                               *
*              IKJPSCB                                                *
* ATTRIBUTES: REENTRANT,REFRESHABLE,REUSABLE                          *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE
***********************************************************************
*                                                                     *
*  REGISTERS:   15 - ENTRY ADDRESS & RETURN CODE                      *
*               14 - RETURN ADDRESS                                   *
*               13 - SAVE AREA ADDRESS AND BASE FOR DSECT             *
*               12 - CSECT BASE                                       *
*               11 - CSECT SECOND BASE                                *
*               10 -
*                9 -
*                8 -                                                  *
*                7 -                                                  *
*                6 - WORK REG                                         *
*                5 - WORK REG                                         *
*                4 - WORK REG                                         *
*                3 - WORK REG                                         *
*                2 -                                                  *
*                1 - PARAM ADDRESS                                    *
*                0 -                                                  *
***********************************************************************
         EJECT
***********************************************************************
*               IDMBOZD CODE                                          *
***********************************************************************
         SPACE
*                            ******************************************
*                            *     DSECTS  AREAS.                     *
*                            ******************************************
         SPACE                                                        ,
         IKJPSCB             PROTECTED STEP CONTROL BLOCK
         EJECT
*                            ******************************************
*                            *   IDMBOZD CSECT INITIALIZATION.        *
*                            ******************************************
         SPACE 2
         INIT# IDMBOZD,BASEREG=(12,11),CODE=RENT,                      X
               EXEC=MAINLINE,REL=01.03.00,                             X
               EXLIST=YES
         EJECT
         CPYRIGHT IDMF,1982                                           ,
         EJECT
*                            ******************************************
*                            *  CONTROL TABLES AND FIELDS.            *
*                            ******************************************
         SPACE
BLANK    EQU   C' '                                                   ,
         SPACE
*        BYTE 1-OPTIONS:     - USE TM/OI
IDMNWAIT EQU   X'80'              NOT WAIT FOR MOUNT BIT              *
*        BYTE 2-OPTIONS:     - USE CLI/MVI
IDMDEALL EQU   C'D'               DEALLOC DATASET                     *
IDMDOALL EQU   C'A'               ALLOCATE DATASET                    *
IDMCHALL EQU   C'C'               CHANGE ATTRIBUTES                   *
IDMNUALL EQU   C'U'               MARK NOT IN USE                     *
*
OLDST    EQU   X'01'              OLD STATUS FOR SVC 99
MODST    EQU   X'02'              MOD STATUS
NEWST    EQU   X'04'              NEW STATUS
SHRST    EQU   X'08'              SHR STATUS
*
DSORGVS  DC    X'0008'            VSAM ORGANIZATION                   ,
DSORGPO  DC    X'0200'            PDS                                 ,
DSORGPS  DC    X'4000'            SEQUENTIAL                          ,
DSORGUN  DC    X'0000'            UNKNOWN DSORG                       ,
         SPACE ,                                                      *
*---------------------------------                                    ,
         DS    0F                                                     ,
OZDPARM  DC    H'3',C'D=A'        BLGOZD PARM                         ,
*---------------------------------                                    ,
DEFUNIT  DC    CL8'SYSDA'    DEFAULT UNIT NAME, IF NOTHING IN UADS    *
INFODDN  DC    CL8'VSAM'     DDNAME FOR INFO/MVS DATABASE             ,
BLANK256 DC    CL256' '                                               ,
*---------------------------------                                    ,
CONTROL  DC    C'CONTROL'                                             ,
DISPLAY  DC    C'DISPLAY'                                             ,
REFRESH  DC    C'REFRESH'                                             ,
VDEFINE  DC    C'VDEFINE'                                             ,
VGET     DC    C'VGET'                                                ,
SHARED   DC    C'SHARED'                                              ,
CHAR     DC    C'CHAR'                                                ,
DSNAMEN  DC    C'(DSNAME)'                                            ,
DSNAMEL  DC    F'44'                                                  ,
VOLN     DC    C'(VOL)'                                               ,
VOLL     DC    F'6'                                                   ,
TUSERN   DC    C'(DSNAME VOL)'                                        ,
         SPACE                                                        ,
EXTRACT  EXTRACT ,FIELDS=(PSB),MF=L                                   ,
EXTRACTE EQU   *                                                      ,
         SPACE
CALL11   CALL  ,(VDEFINE,DSNAMEN,,CHAR,DSNAMEL),VL,MF=L
CALL11E  EQU   *
         SPACE
CALL12   CALL  ,(VDEFINE,VOLN,,CHAR,VOLL),VL,MF=L
CALL12E  EQU   *
         SPACE
*---------------------------------                                    ,
         EJECT ,                                                      *
*                            ******************************************
*                            *    WORK FIELDS.                        *
*                            ******************************************
         SPACE
         DATA#
*------- PARM LIST - KEEP TOGETHER -----------------------------------*
ADSNAME  DS    F                                                      *
AVOL     DS    F                                                      *
AOPTIONS DS    F                                                      *
ADDNAME  DS    F                                                      *
AUNITN   DS    F                                                      *
ADSORG   DS    F                                                      *
AERRMSG  DS    F                                                      *
*------- KEEP PREV FIELDS TOGETHER -----------------------------------*
         SPACE ,
ISPLADDR DS    F               ISPLINK ADDRESS                        ,
ADDRPSCB DS    F               PSCB ADDRESS                           ,
INFOPGM  DS    CL8             NAME OF INFO/MVS PROGRAM               ,
DVOL     DS    CL6                                                    *
DOPTIONS DS    F                                                      *
DSDDNAME DS    CL8                                                    *
DUNITN   DS    CL8                                                    *
DDSORG   DS    CL2                                                    *
BUFFMSG  DS    CL256                                                  ,
VOL      DS    CL6                                                    ,
DSNAME   DS    CL56                                                   ,
*---------------------------------                                    ,
         DS    0F                                                     ,
LEXTRACT DS    CL(EXTRACTE-EXTRACT),0F                                ,
LCALL11  DS    CL(CALL11E-CALL11),0F                                  ,
LCALL12  DS    CL(CALL12E-CALL12),0F                                  ,
*---------------------------------
DSATRC   DS    XL1                                                    *
         ENDDATA#
         EJECT
*                            ******************************************
*                            * MAINLINE- MAIN ROUTINE OF THE IDMBOZD  *
*                            ******************************************
         SPACE 2
         ENTRE# MAINLINE
         SPACE ,                                                      *
*---------------------------------------------------------------------,
*        PROCESS INPUT PARM - GET INFO/MVS PROGRAM NAME               ,
*---------------------------------------------------------------------,
         L     R3,0(R1)           LOAD ADDR OF FIRST PARM
         LA    R4,2(R3)           R4=PARAM ADDRESS
         LH    R3,0(R3)           R3=LENGTH OF PARAM
         IFOR R3,EQ,0,LTR         IF PARAM DOES NOT EXIST
         IF R3,GT,=H'8',CH        OR PARAM LONGER THAN 8 BYTES
            TPUT   =C'INVALID OR MISSING PARAMETER',28                ,
            IEXIT# RC=12             ERROR - EXIT
         ENDIF                    ENDIF
         BCTR  R3,R0              -1 FOR EX
         MVI   INFOPGM,BLANK      BLANK OUT TARGET FIELD FOR PGM NAME,
         MVC   INFOPGM+1(L'INFOPGM-1),INFOPGM                         ,
         MVC   INFOPGM(0),0(R4)   MOVE PGM NAME TO TARGET FIELD     ,
         EX    R3,*-6
*---------------------------------------------------------------------,
*        GET INFO FROM SHARED POOL AND PSCB ADDRESS                   *
*---------------------------------------------------------------------,
         LOAD  EP=ISPLINK        LOAD SPF INTERFACE                   ,
         ST    R0,ISPLADDR       SAVE ENTRY POINT                     ,
         SPACE                                                        ,
         MVC   LCALL11,CALL11                                         ,
         MVC   LCALL12,CALL12                                         ,
         SPACE
         L     RF,ISPLADDR
         LA    R2,DSNAME                           GET DSNAME ADDR
         CALL  (15),(,,(R2),,),VL,MF=(E,LCALL11)   DEFINE DSNAME
         SPACE
         L     RF,ISPLADDR
         LA    R2,VOL                              GET VOLUME ADDR
         CALL  (15),(,,(R2),,),VL,MF=(E,LCALL12)   DEFINE VOLUME
         SPACE
         L     RF,ISPLADDR
         CALL  (15),(VGET,TUSERN,SHARED),VL   GET INFO FROM SHARED    ,
         SPACE
         MVC   LEXTRACT,EXTRACT         MOVE LIST FORM TO DYNAM AREA  ,
         LA    R3,ADDRPSCB              R3=ADDRESS OF PSCB POINTER    ,
         EXTRACT (R3),MF=(E,LEXTRACT)   GET PSCB ADDRESS IN ADDRPSCB
*---------------------------------------------------------------------,
*        ALLOCATE INFO/MVS DATABASE                                   ,
*---------------------------------------------------------------------,
         MVI   DSATRC,0                                               *
         MVC   DSDDNAME,INFODDN     WE SUPPLY DDNAME FOR INFO/MVS     ,
         MVC   DVOL,VOL                                               *
         XC    DOPTIONS,DOPTIONS                                      *
         MVI   DOPTIONS+1,IDMDOALL                                    *
         MVI   DOPTIONS+2,SHRST                                       *
         IF VOL,NE,BLANK256,CLC                                       *
            L     R4,ADDRPSCB                                         *
            USING PSCB,R4                                             *
            MVC   DUNITN,PSCBGPNM                                     *
            IF DUNITN,EQ,BLANK,CLI                                    *
               MVC   DUNITN,DEFUNIT                                   *
            ENDIF                                                     *
            DROP  R4                                                  *
         ENDIF                                                        *
         MVI   BUFFMSG,BLANK                CLEAR MSG BUFFER
         MVC   BUFFMSG+1(L'BUFFMSG-1),BUFFMSG
         SPACE ,                                                      *
         LA    R3,DSNAME                                              *
         ST    R3,ADSNAME                                             *
         LA    R3,DVOL                                                *
         ST    R3,AVOL                                                *
         LA    R3,DOPTIONS                                            *
         ST    R3,AOPTIONS                                            *
         LA    R3,DSDDNAME                                            *
         ST    R3,ADDNAME                                             *
         LA    R3,DUNITN                                              *
         ST    R3,AUNITN                                              *
         LA    R3,DDSORG                                              *
         ST    R3,ADSORG                                              *
         LA    R3,BUFFMSG                                             *
         ST    R3,AERRMSG                                             *
         SPACE ,                                                      *
         LA    R1,ADSNAME                                             *
         LINK  EP=IDMALLOC                                            *
         IF RF,NE,0,LTR                                               *
            TPUT   =C'ALLOCATION FAILED',17                           ,
            IEXIT# RC=8                                               *
         ENDIF                                                        *
***                                                                   ,
*        INVOKE INFO/MVS                                              ,
***                                                                   ,
         L     RF,ISPLADDR
         CALL  (15),(CONTROL,DISPLAY,REFRESH),VL   REFRESH SCREEN     ,
         LOAD  EPLOC=INFOPGM
         LR    RF,R0                                                  ,
         CALL  (15),(OZDPARM),VL                                      ,
         DELETE EPLOC=INFOPGM                                         ,
         SPACE                                                        ,
*---------------------------------------------------------------------*
*        FREE DATASET BY DD-NAME (PARM LIST IS READY-JUST SET OPTION) *
*---------------------------------------------------------------------*
         XC    DOPTIONS,DOPTIONS       ZERO OUT OPTIONS WORD          *
         MVI   DOPTIONS+1,IDMDEALL     SET OPTION - DEALLOCATE        *
         SPACE ,                                                      *
         LA    R1,ADSNAME              R1 POINTS TO PARM LIST         *
         LINK  EP=IDMALLOC             LINK TO DEALLOCATION MODULE    *
         SPACE
         EXIT# RC=0
         EJECT
*                            ******************************************
*                            *     LITERALS                           *
*                            ******************************************
         SPACE
         LTORG
         SPACE 3
         END#  IDMBOZD
