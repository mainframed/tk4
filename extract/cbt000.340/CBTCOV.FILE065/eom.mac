           **********************************************
           **          TSIFLP.CMDB.CNTL(EOM)           **
           **       SYSPARM IN JCL MAY BE SET TO       **
           **           LAST MONTH IN REPORT           **
           **          (FOR JULY: SYSPARM = 7)         **
           **! DO NOT CHANGE THIS PRODUCTION PROGRAM ! **
           **********************************************
           **  CHECK 'TSSMS.USAGE.CNTL(CNTL1982)'      **
           **  FOR BAD DAYS TO EXCLUDE FROM REPORTS    **
           **********************************************;
MACRO BADAYS       IF JULIAN = 82132 OR
                      JULIAN = 82139 OR
                      JULIAN = 82140
                      THEN DELETE;     %
MACRO DAYSINMO       ARRAY MO1(J) M101-M112;
                     ARRAY MO2(J) M201-M212;
                     ARRAY  YR(K) MO1-MO2;
       RETAIN  M101 21  M102 18  M103 21  M104 22
               M105 20  M106 22  M107 23  M108 21
               M109 20  M110 21  M111 19  M112 22
               M201 20  M202 19  M203 23  M204 22
               M205 17  M206 22  M207 21  M208 22
               M209 21  M210 20  M211 19  M212 21 ;   %
MACRO  MO_MACRO
    RUNDAY   = TODAY();
    RUNMONTH = MONTH(RUNDAY);
    RUNYEAR  = YEAR(RUNDAY);
    IF SYSPARM() = 0  THEN NEWMONTH = RUNMONTH - 1 ;
       ELSE NEWMONTH = SYSPARM() ;
    IF NEWMONTH <= RUNMONTH THEN DO;
           IF (MONTH>NEWMONTH)  AND (YEAR=RUNYEAR) THEN GO TO NG;
           IF (MONTH<=NEWMONTH) AND (YEAR<RUNYEAR) THEN GO TO NG;
           IF (RUNYEAR-YEAR) > 1                   THEN GO TO NG;
           END;
    IF NEWMONTH > RUNMONTH  THEN DO;
           IF (MONTH>NEWMONTH)  AND (YEAR=(RUNYEAR-1)) THEN GO TO NG;
           IF (MONTH<=NEWMONTH) AND (YEAR<(RUNYEAR-1)) THEN GO TO NG;
           IF (MONTH<NEWMONTH)  AND (YEAR=RUNYEAR)     THEN GO TO NG;
           END;
    GO TO OK;
NG: DELETE;
OK:    IF (MONTH <= NEWMONTH) THEN MONTH = MONTH + 12;
        %
PROC  FORMAT ;*       DDNAME = SASLIB;
      VALUE  MON    1 = JAN    2 = FEB    3 = MAR
                    4 = APR    5 = MAY    6 = JUN
                    7 = JUL    8 = AUG    9 = SEP
                   10 = OCT   11 = NOV   12 = DEC
                   13 = JAN   14 = FEB   15 = MAR
                   16 = APR   17 = MAY   18 = JUN
                   19 = JUL   20 = AUG   21 = SEP
                   22 = OCT   23 = NOV   24 = DEC  ;
      VALUE  $ID    0 = 2058   6 = 1658
                    7 = 1733   8 = 1833  ;
DATA  BACHDATA (KEEP = CLASS JOB_MIN MONTH YEAR)
       ALLDATA (KEEP =       JOB_MIN MONTH YEAR)
       TSODATA (KEEP = LOGONHRS MONTH XACTNS XACTRATE YEAR)
       ONLINE  (KEEP = CPU_IMS CPU_TCAM CPU_CFSR CPU_BFSR CPU_CCSP
                      CPU_CCSR MONTH TYPE CPU_SUM YEAR);
SET  INDDSMF.SMFDATA;
     MONTH = MONTH(STARTDTE);   YEAR = YEAR(STARTDTE);
   * EXECUTE MONTH MACRO: ;     MO_MACRO
     JULIAN = JULDATE(STARTDTE);
   * EXECUTE BAD DAYS MACRO;    BADAYS
     IF JOBTYPE = '8' THEN DO;   XACTNS   = NOFTXNS/100 ;
                                 LOGONHRS = ET_MIN/60 ;
                                 XACTRATE = NOFTXNS/ET_MIN ;
                                 OUTPUT TSODATA;   RETURN;
                          END;
      OUTPUT ALLDATA;
   IF (CLASS = 'Q') OR (CLASS = 'E') OR (CLASS = 'T')  OR
      (CLASS = 'A') OR (CLASS = 'D') OR (CLASS = 'P')  OR
      (CLASS = 'G')
       THEN OUTPUT BACHDATA;
  *
   ONLINE CPU SECTION  ;
   IF CPUID = '0' OR CPUID = '6' THEN CPU = CPU/5.5 ;
        IF CLASS = 'U' AND JOBNAME = 'OSMTC   '
        THEN DO;
   CPU_CFSR = CPU; CPU_SUM =CPU; TYPE = 'C_FASTER'; OUTPUT ONLINE; END;
   ELSE IF CLASS = '0' AND JOBNAME = 'OSMTB   '
        THEN DO;
   CPU_BFSR = CPU; CPU_SUM =CPU; TYPE = 'B_FASTER'; OUTPUT ONLINE; END;
   ELSE IF CLASS = 'V' AND JOBNAME = 'PHARMACY'
        THEN DO;
   CPU_CCSP = CPU; CPU_SUM =CPU; TYPE = 'PHRMCICS'; OUTPUT ONLINE; END;
   ELSE IF CLASS = '1' AND JOBNAME = 'TCAM    '
        THEN DO;
   CPU_TCAM = CPU; CPU_SUM =CPU; TYPE = 'TCAM';     OUTPUT ONLINE; END;
   ELSE IF (CLASS = 'F' & (JOBNAME='LBPAARS ' OR JOBNAME='PAARS   ')) OR
           (CLASS = 'W' & JOBNAME='PAARS   ')
        THEN DO;
   CPU_CCSR = CPU; CPU_SUM =CPU; TYPE = 'PARSCICS'; OUTPUT ONLINE; END;
   ELSE IF (CLASS = '4' & SUBSTR(JOBNAME,1,7) = 'IMSMSGP') OR
           (CLASS = '6' & JOBNAME = 'IMSCTLB ')
        THEN DO;
   CPU_IMS = CPU;  CPU_SUM =CPU; TYPE = 'IMS';      OUTPUT ONLINE; END;
DATA  CPUTRND (KEEP = MONTH CPUID CPU_FRST CPU_SECD CPU_THRD);
SET INDDRMF.RMFDATA ;
    MONTH    = MONTH(DATE) ;       YEAR = YEAR(DATE) ;
  * EXECUTE MONTH MACRO : ;        MO_MACRO
    JULIAN = JULDATE(DATE);
  * EXECUTE BAD DAYS MACRO:  ;     BADAYS
      OPTIONS   NODATE ;
       IF (SHIFT = '1')    THEN CPU_FRST = CPU_BUSY;
 ELSE  IF (SHIFT = '2')    THEN CPU_SECD = CPU_BUSY;
 ELSE  IF (SHIFT = '3')    THEN CPU_THRD = CPU_BUSY;
PROC SORT DATA = CPUTRND;                       BY MONTH CPUID ;
PROC MEANS MEAN NOPRINT DATA = CPUTRND;         BY MONTH CPUID ;
     VAR CPU_FRST CPU_SECD CPU_THRD;
     OUTPUT OUT  = CPUTREND  MEAN = CPU_FRST CPU_SECD CPU_THRD;
PROC SORT   DATA = CPUTREND ;     BY CPUID;
PROC PRINT  DATA = CPUTREND ;  FORMAT MONTH MON. CPUID $ID. ;
PROC PLOT   DATA = CPUTREND ;  FORMAT MONTH MON. CPUID $ID. ;
            BY CPUID;
            TITLE1 AVERAGE PERCENT CPU BUSY ON EACH SHIFT ;
     PLOT CPU_FRST*MONTH='1' CPU_SECD*MONTH='2' CPU_THRD*MONTH='3' /
                               OVERLAY
                               HPOS = 85     VPOS = 26
                               VAXIS = 0 TO 100 BY 20;
DATA CPUSUM (KEEP = MONTH CPU_FRST CPU_SECD CPU_THRD);
SET CPUTREND;
    IF CPUID^='6' THEN RETURN;
    CPU_FRST = CPU_FRST/5.5;
    CPU_SECD = CPU_SECD/5.5;
    CPU_THRD = CPU_THRD/5.5;
PROC SORT  DATA = CPUSUM ;                     BY MONTH ;
PROC MEANS SUM  NOPRINT DATA = CPUSUM;         BY MONTH ;
     VAR CPU_FRST CPU_SECD CPU_THRD;
     OUTPUT OUT  = ALLCPU  SUM = CPU_FRST CPU_SECD CPU_THRD;
PROC PRINT  DATA = ALLCPU ;  FORMAT MONTH MON. ;
            TITLE1; TITLE2;
PROC PLOT   DATA = ALLCPU ;  FORMAT MONTH MON. ;
            TITLE1 AVERAGE TOTAL IBM CPU USAGE ON EACH SHIFT  ;
            TITLE2 '(300 = 3-3033''S AT 100% BUSY)'  ;
     PLOT CPU_FRST*MONTH='1' CPU_SECD*MONTH='2' CPU_THRD*MONTH='3' /
                               OVERLAY
                               HPOS = 85     VPOS = 26
                               VAXIS = 0 TO 300 BY 60 ;
PROC SORT DATA = ONLINE;                 BY YEAR  MONTH ;
PROC MEANS SUM NOPRINT DATA = ONLINE;    BY YEAR  MONTH ;
     VAR CPU_SUM ;
     OUTPUT OUT = TPDATA2
      SUM = CPU_SUM ;
PROC PRINT DATA = TPDATA2;
DATA ONLNSUM (KEEP = CPU_SUM MONTH);
SET  TPDATA2;
             * MACRO : ;    DAYSINMO
     IF (MONTH = 12) THEN J = 12 ; ELSE J = MOD(MONTH,12) ;
     K = YEAR - 1980;                DAYS = YR;
     CPU_SUM = CPU_SUM/DAYS;
PROC PLOT DATA = ONLNSUM;     FORMAT MONTH MON. ;
     TITLE TOTAL CPU SECONDS PER DAY FOR ALL ONLINE SYSTEMS ;
     PLOT CPU_SUM*MONTH='*'/ HPOS=85 VPOS=26 VZERO;
PROC SORT DATA = ONLINE;                 BY TYPE YEAR MONTH ;
PROC MEANS SUM NOPRINT DATA = ONLINE;    BY TYPE YEAR MONTH ;
     VAR CPU_CFSR CPU_CCSP CPU_CCSR CPU_TCAM CPU_IMS CPU_BFSR ;
     OUTPUT OUT = TPDATA1
      SUM = C_FASTER PHRMCICS PARSCICS TCAM IMS B_FASTER ;
PROC PRINT DATA = TPDATA1; FORMAT MONTH MON.  ;
DATA ONLNDATA (KEEP = C_FASTER PHRMCICS PARSCICS TCAM IMS B_FASTER
                      TYPE MONTH);
SET  TPDATA1;
             * MACRO : ;    DAYSINMO
     IF (MONTH = 12) THEN J = 12 ; ELSE J = MOD(MONTH,12) ;
     K = YEAR - 1980;                DAYS = YR;
     C_FASTER = C_FASTER/DAYS;       PHRMCICS = PHRMCICS/DAYS;
     PARSCICS = PARSCICS/DAYS;       TCAM     = TCAM/DAYS;
     IMS      = IMS/DAYS;            B_FASTER = B_FASTER/DAYS;
PROC PLOT  DATA = ONLNDATA ;  FORMAT MONTH MON.  ;
     TITLE TOTAL ONLINE CPU SECONDS PER DAY (IMS,TCAM,B-FASTER) ;
     PLOT TCAM*MONTH='T'  IMS*MONTH='I'  B_FASTER*MONTH='B'/
          OVERLAY HPOS=85 VPOS=26 VZERO;
PROC PLOT  DATA = ONLNDATA ;  FORMAT MONTH MON.  ;
   TITLE TOTAL ONLINE CPU SECONDS PER DAY (PAARS,PHARMACY,C-FASTER);
     PLOT C_FASTER*MONTH='F'  PHRMCICS*MONTH='P'  PARSCICS*MONTH='R'/
          OVERLAY HPOS=85 VPOS=26 VZERO;
DATA PAGING (KEEP = DMNDPGRT PAGFAULT SWAPRATE MONTH CPUID) ;
SET INDDRMF.RMFDATA;
    MONTH = MONTH(DATE);   YEAR = YEAR(DATE);
    MO_MACRO
    IF (CPUID = '0') THEN DELETE;
    IF (SHIFT ^='1') THEN DELETE;
PROC SORT DATA = PAGING;     BY MONTH CPUID;
PROC MEANS MEAN NOPRINT DATA = PAGING;  BY MONTH CPUID;
     VAR DMNDPGRT PAGFAULT SWAPRATE;
     OUTPUT OUT = PAGETRND  MEAN = DEMAND P_FAULTS SWAPS;
PROC SORT DATA =PAGETRND;               BY CPUID;
PROC PLOT DATA = PAGETRND;  FORMAT MONTH MON.  CPUID $ID. ;
     BY CPUID;
     TITLE1 AVERAGE PAGING RATE TRENDS  ;
     TITLE2 D = DEMAND PAGES/SECOND  ;
     TITLE3 F = PAGE FAULTS/SECOND  ;
     TITLE4 S = SWAPS/SECOND  ;
     PLOT DEMAND*MONTH='D'  P_FAULTS*MONTH='F'  SWAPS*MONTH='S' /
     OVERLAY   HPOS = 85  VPOS = 26  VAXIS = 0 TO 75 BY 15 ;
PROC SORT DATA = ALLDATA LEAVE = 30000;        BY YEAR MONTH;
PROC SORT DATA = BACHDATA LEAVE = 30000;       BY YEAR MONTH CLASS;
PROC MEANS N MEAN MAX  DATA = ALLDATA  NOPRINT;
           VAR JOB_MIN;          BY YEAR MONTH;
           OUTPUT OUT = BDATA1 N=JOBCOUNT MEAN=AVE_MIN MAX=MAX_MIN;
DATA PRINT1 (KEEP = JOBCOUNT AVE_MIN MAX_MIN MONTH);
SET  BDATA1;
             * MACRO : ;    DAYSINMO
     IF (MONTH = 12) THEN J = 12 ; ELSE J = MOD(MONTH,12) ;
     K = YEAR - 1980;                DAYS = YR;
     JOBCOUNT = JOBCOUNT/DAYS;
PROC PRINT DATA = PRINT1;     FORMAT MONTH MON. ;
           TITLE1;  TITLE2;   TITLE3;
PROC MEANS N MEAN MAX   DATA = BACHDATA  NOPRINT;
           VAR JOB_MIN;                    BY MONTH CLASS;
           OUTPUT OUT = PRINT2  N=JOBCOUNT MEAN=AVE_MIN MAX=MAX_MIN;
PROC PRINT DATA = PRINT2;     FORMAT MONTH MON. ;
DATA ETDATA (KEEP = CLASS MONTH JOBS CLASS_P CLASS_G
                    CLASS_A CLASS_D CLASS_T CLASS_E CLASS_Q
                    JOBS_A  JOBS_D  JOBS_T  JOBS_E  JOBS_Q
                    JOBS_P  JOBS_G  );
SET BACHDATA;                        BY MONTH CLASS;
                 * MACRO --> ;   DAYSINMO
       JOBS + 1;
       TWO_HR + JOB_MIN<120;
       IF LAST.CLASS THEN DO; PERCENT = (TWO_HR/JOBS)*100;
               IF (MONTH = 12) THEN J = 12 ; ELSE J = MOD(MONTH,12) ;
                   K = YEAR - 1980;                DAYS = YR;
                     IF CLASS='Q' THEN DO; CLASS_Q=PERCENT;
                                            JOBS_Q=JOBS/DAYS; END;
               ELSE  IF CLASS='E' THEN DO; CLASS_E=PERCENT;
                                            JOBS_E=JOBS/DAYS; END;
               ELSE  IF CLASS='T' THEN DO; CLASS_T=PERCENT;
                                            JOBS_T=JOBS/DAYS; END;
               ELSE  IF CLASS='D' THEN DO; CLASS_D=PERCENT;
                                            JOBS_D=JOBS/DAYS; END;
               ELSE  IF CLASS='A' THEN DO; CLASS_A=PERCENT;
                                            JOBS_A=JOBS/DAYS; END;
               ELSE  IF CLASS='P' THEN DO; CLASS_P=PERCENT;
                                            JOBS_P=JOBS/DAYS; END;
               ELSE  IF CLASS='G' THEN DO; CLASS_G=PERCENT;
                                            JOBS_G=JOBS/DAYS; END;
                             OUTPUT ETDATA;
                             JOBS = 0;   TWO_HR = 0 ;
                        END;
PROC SORT DATA = ETDATA;               BY MONTH;
PROC PLOT DATA = ETDATA;   FORMAT MONTH MON. ;
     TITLE1 BATCH JOBS PER DAY ;
     PLOT JOBS_Q*MONTH='Q' JOBS_E*MONTH='E'   /
          OVERLAY HPOS = 85  VPOS = 26 VZERO;
     PLOT JOBS_P*MONTH='P' JOBS_G*MONTH='G'   /
          OVERLAY HPOS = 85  VPOS = 26 VZERO;
     PLOT JOBS_A*MONTH='A' JOBS_D*MONTH='D' JOBS_T*MONTH='T'  /
          OVERLAY HPOS = 85  VPOS = 26 VZERO;
PROC PLOT  DATA = PRINT1;            FORMAT MONTH MON. ;
     TITLE1 BATCH JOBS PER DAY ;
     TITLE2 ALL CLASSES ;
     PLOT JOBCOUNT*MONTH='*'/ HPOS = 85   VPOS = 26  VZERO;
PROC PLOT DATA = ETDATA;   FORMAT MONTH MON. ;
     TITLE1 PERCENTAGE OF JOBS COMPLETED WITHIN TWO HOURS ;
     PLOT CLASS_Q*MONTH='Q' CLASS_E*MONTH='E' /
                              OVERLAY
                              VAXIS=  0 TO 100 BY 20
                              HPOS = 85   VPOS = 26 ;
     PLOT CLASS_P*MONTH='P' CLASS_G*MONTH='G' /
                              OVERLAY
                              VAXIS=  0 TO 100 BY 20
                              HPOS = 85   VPOS = 26 ;
     PLOT CLASS_A*MONTH='A' CLASS_D*MONTH='D' CLASS_T*MONTH='T' /
                              OVERLAY
                              VAXIS=  0 TO 100 BY 20
                              HPOS = 85   VPOS = 26 ;
PROC SORT DATA = TSODATA LEAVE = 30000;  BY YEAR MONTH;
PROC MEANS NOPRINT SUM MEAN  DATA = TSODATA;
     VAR LOGONHRS XACTNS XACTRATE;      BY YEAR MONTH;
     OUTPUT OUT = TSODATA1  SUM = LOGONHRS  XACTS
                           MEAN = AVELOGON AVEXACTS XACTRATE;
DATA TSOTREND (KEEP = LOGONHRS XACTS AVELOGON AVEXACTS XACTRATE MONTH);
SET TSODATA1;
             * MACRO --> ;  DAYSINMO
     IF (MONTH = 12) THEN J = 12 ; ELSE J = MOD(MONTH,12) ;
     K = YEAR - 1980;                DAYS = YR;
     LOGONHRS = LOGONHRS/DAYS;       XACTS = XACTS/DAYS;
PROC PRINT DATA = TSOTREND;  FORMAT MONTH MON. ;
PROC PLOT  DATA = TSOTREND;  FORMAT MONTH MON. ;
     TITLE1 USER HOURS (X) PER DAY ;
     TITLE2 AND  ;
     TITLE3 TSO TRANSACTIONS/100 (*) PER DAY ;
     PLOT LOGONHRS*MONTH='X'  XACTS*MONTH='*' / OVERLAY
          VAXIS=0 TO 1000 BY 200   HPOS = 85   VPOS = 26 ;
PROC PLOT DATA = TSOTREND;    FORMAT MONTH MON. ;
     TITLE1 TSO PRODUCTIVITY RATE ;
     TITLE2 '(TRANSACTIONS PER USER LOGON MINUTE)' ;
     PLOT XACTRATE*MONTH='*'  /   HPOS = 85   VPOS = 26 VZERO;
