***********************************************************************
         TITLE 'DATE ROUTINE'
***********************************************************************
*
*DATE WRITTEN. JULY 16, 1987.
*
*AUTHOR.       TOM DIEN.
*
*  INPUT: R1 POINTS TO A PARMLIST CONTAINING:
*         +0 ADDR OF 32 BYTES AREA TO BE FILLED UP WITH DATE VALUES
*            (MAP BY TDATEMAP)
*
*             DAY OF THE WEEK:  DS   CL9  MONDAY-SUNDAY
*             GREGORIAN DATE:   DS   CL8  MM/DD/YY
*                               DS   CL1  BLANK
*             JULIAN DATE:      DS   CL5  YYDDD
*                               DS   CL1  BLANK
*             HOURS:            DS   CL8  HH:MM:SS
*
*                              TOTAL = 32 BYTES
*         +4 ADDR OF AREA OF DATE EITHER MM/DD/YY OR YYDDD.
*
*         +0 HIGH ORDER BIT MUST BE ON IF +4 IS NOT USED.
*
*         RETURN CODE = 4 WHEN DATE IN R0 IS BAD
*
*  SEND INQUIRIES TO: TOM DIEN
*                     LOS ANGELES TIMES
*                     TIMES MIRROR SQUARE
*                     LOS ANGELES, CA 90053
*
*  OR PHONE: (213) 237-5840
*
*
*  TDATEMAP MACRO INCLUDED IN-LINE
***********************************************************************
***********************************************************************
         MACRO
&N       BITEQU
&CNT     SETA  128
&B       SETA  0
.NEXT    ANOP
BIT&B    EQU  &CNT
         AIF  (&B EQ 7).DONE
&CNT     SETA (&CNT/2)
&B       SETA (&B+1)
         AGO  .NEXT
.DONE    MEND
***********************************************************************
         MACRO
&N       REGEQU
&CNT     SETA  0
.NEXT    ANOP
R&CNT    EQU  &CNT
         AIF  (&CNT EQ 15).DONE
&CNT     SETA (&CNT+1)
         AGO  .NEXT
.DONE    MEND
***********************************************************************
         MACRO
         TDATEMAP
*  MAPPING AREA FOR RETURN VALUES FROM DATE CONVERSION ROUTINE
CURDAY   DS    CL9              DAY OF THE WEEK
CURDATE  DS    0CL8              DATE OF THE WEEK
CURMM    DS     CL2              MONTH
CURS1    DC     CL1'/'
CURDD    DS     CL2              DAY
CURS2    DC     CL1'/'
CURGYY   DS     CL2              YEAR
BLANK1   DS    CL1
YYDDD    DS    CL5
BLANK2   DS    CL1
OHHMMSS  DS    CL8
         MEND
***********************************************************************
TDATE    CSECT
         REGEQU
         BITEQU
         SAVE  (14,12)                                         TD JUL87
         LR    R12,R15                LOAD ENTRY ADDR          TD JUL87
         USING TDATE,R12                BASE IS R12            TD JUL87
         XR    R10,R10                                         TD JUL87
         L     R11,0(R1)                                       TD JUL87
         TM    0(R1),BIT0                                      TD JUL87
         BO    NODATE                                          TD JUL87
         L     R10,4(R1)                                       TD JUL87
NODATE   GETMAIN R,LV=WORKLEN         GETMAIN FOR RE-ENTRANT   TD JUL87
         ST    R13,4(R1)              STORE CALLER'S SAVE ADDR TD JUL87
         ST    R1,8(R13)              OUR SAVE ADDR TO CALLER  TD JUL87
         LR    R13,R1                 SETUP OUR SAVE ADDRESS   TD JUL87
         USING WORKAREA,R13           BASE REG FOR WORKAREA    TD JUL87
*---------------------------------------------------------------------*
* SUBROUTINE TO OBTAIN JULIAN DATE                             TD JUL87
*---------------------------------------------------------------------*
$JULIAN  DS    0H                                              TD JUL87
         TIME  DEC                   ISSUE TIME MACRO          TD JUL87
         ST    R1,WKFULL             STORE PACKED JULIAN DATE  TD JUL87
         LR    R7,R1                                           TD JUL87
         LR    R8,R0                                           TD JUL87
         BAL   R9,CHECKD                                       TD JUL87
         UNPK  CURYYDDD(5),WKFULL+1(3) UNPACK JULIAN DATE      TD JUL87
         OI    CURYYDDD+L'CURYYDDD-1,X'F0'                     TD JUL87
         SRL   R8,4                  DROP HUNDREDTH OF SECONDS TD JUL87
         O     R8,=F'15'             FORCE SIGN AT END         TD JUL87
         ST    R8,WKFULL             SAVE TIME                 TD JUL87
         MVC   HHMMSS(9),EDHHMMSS                              TD JUL87
         ED    HHMMSS(9),WKFULL     EDIT WITH FORMAT HH:MM:SS  TD JUL87
         MVC   OHHMMSS(8),HHMMSS+1                             TD JUL87
         MVC   YYDDD(5),CURYYDDD MOVE TO HEADING               TD JUL87
*---------------------------------------------------------------------*
* SUBROUTINE TO CALCULATE GREGORIAN DATE FROM GIVEN JULIAN DATETD JUL87
*---------------------------------------------------------------------*
$DATE    DS    0H                                              TD JUL87
         LA    R2,12                 12 TIMES                  TD JUL87
         LA    R3,TABDDDMM           ADDR OF MONTH/DAY TABLE   TD JUL87
LOOP     CLC   CURDDD,0(R3)          CURRENT DAY LOWER THAN TABLE?
         BL    MONTH                 YES, BRANCH TO MONTH      TD JUL87
         BE    MONTH                                           TD JUL87
         LA    R3,5(R3)              UP BY 5                   TD JUL87
         BCT   R2,LOOP               AGAIN IF NOT 0            TD JUL87
*        B     NOWAY                 WHAT??? JULIAN CAME FROM SYSTEMS
MONTH    S     R3,=F'5'              BACK 5                    TD JUL87
         PACK  TBPDDD,0(3,R3)        PACK TABLE DATE           TD JUL87
         PACK  WKPDDD,CURDDD         PACK CURRENT JULIAN DAY   TD JUL87
         PACK  WKPYY,CURYY           PACK CURRENT YEAR         TD JUL87
         DP    WKPYY,=P'4'           DIVIDE 4 INTO YEAR        TD JUL87
         CP    WKPYY+2(1),=P'0'      LEAP YEAR?                TD JUL87
         BNE   LEAPOK                NO, GO OUT                TD JUL87
         CP    WKPDDD,=P'59'         YES, PASS FEBRUARY?       TD JUL87
         BNH   LEAPOK                NO, GO OUT                TD JUL87
         AP    WKPDDD,=P'1'          YES, ADD 1 TO DAY         TD JUL87
LEAPOK   DS    0H                                              TD JUL87
         SP    WKPDDD,TBPDDD         SUBTRACT TABLE DAYS FROM CUR. DAYS
         MVC   CURMM,3(R3)           MOVE IN MONTH FROM TABLE  TD JUL87
         UNPK  CURDD,WKPDDD          MOVE IN THE DAY OF THE MONTH
         OI    CURDD+1,X'F0'         DROP SIGN                 TD JUL87
         MVC   CURGYY,CURYY          MOVE CURRENT YEAR TO GREGORIAN
*---------------------------------------------------------------------*
* SUBROUTINE TO CALCULATE DAY OF THE WEEK FROM KNOWN JULIAN DATE
*---------------------------------------------------------------------*
*                                                              TD JUL87
* FORMULA TO OBTAIN INDEX TO DAY OF THE WEEK:                  TD JUL87
*                                                              TD JUL87
*     DAY OF WEEK = (OPER1 - OPER2 + 1)                        TD JUL87
*                                                              TD JUL87
*           OPER1 = (DDD) + ( (YY - 1) / 4 ) + (YY - 1)        TD JUL87
*           OPER2 = (OPER1 / 7) * 7                            TD JUL87
*                                                              TD JUL87
*---------------------------------------------------------------------*
$DAY     DS    0H                                              TD JUL87
         PACK  WKPYY,CURYY           PACK CURRENT YEAR         TD JUL87
         PACK  WKPDDD,CURDDD         PACK CURRENT DAY          TD JUL87
         SP    WKPYY,=P'1'           SUBTRACT 1 FROM CURR YEAR TD JUL87
         ZAP   OPER1,WKPYY           STORE TO OPER1            TD JUL87
         DP    WKPYY,=P'4'           DIVIDE 4 INTO CURR YEAR-1 TD JUL87
         AP    OPER1,WKPYY(2)        ADD RESULT TO OPER1       TD JUL87
         AP    OPER1,WKPDDD          ADD CUR DAY TO OPER1      TD JUL87
         ZAP   OPER2,OPER1           STORE TO OPER2            TD JUL87
         DP    OPER2,=P'7'           DIVIDE 7 INTO OPER2       TD JUL87
         ZAP   OPER2,OPER2(2)        FORGET ABOUT REMAINDER    TD JUL87
         MP    OPER2,=P'7'           MULTIPLY OPER2 BY 7       TD JUL87
         SP    OPER1,OPER2           SUBTRACT OPER2 FROM OPER1 TD JUL87
         AP    OPER1,=P'1'           ADD 1 TO OPER1            TD JUL87
         LA    R2,7                  MAX LOOP                  TD JUL87
         LA    R3,TABDAY             ADDR OF DAY OF WEEK TABLE TD JUL87
DAYLOOP  CP    9(1,R3),OPER1         OPER1 = VALUE IN TABLE?   TD JUL87
         BE    MOVEDAY               YES, GO MOVE DAY          TD JUL87
         LA    R3,10(R3)             UP BY ONE DAY             TD JUL87
         BCT   R2,DAYLOOP            LOOK AGAIN                TD JUL87
MOVEDAY  MVC   CURDAY,0(R3)          MOVE IN THE DAY OF THE WEEK
         XR    R8,R8                                           TD JUL87
*--------------------------------------------------------------------*
* FINISH                                                       TD JUL87
*--------------------------------------------------------------------*
FINI     DS    0H                                              TD JUL87
         MVI   BLANK1,C' '                                     TD JUL87
         MVI   BLANK2,C' '                                     TD JUL87
         MVI   CURS1,C'/'                                      TD JUL87
         MVI   CURS2,C'/'                                      TD JUL87
         MVC   0(PARML,R11),PARMDATA                           TD JUL87
         LR    R1,R13                                          TD JUL87
         L     R13,4(R13)                                      TD JUL87
         FREEMAIN R,LV=WORKLEN,A=(1)   FREE MAIN               TD JUL87
         LR    R15,R8                                          TD JUL87
         RETURN (14,12),RC=(15)        RETURN TO CALLER        TD JUL87
EDIT     DS    0H                                              TD JUL87
         CLI   2(R10),C'/'                                     TD JUL87
         BE    EDIT1                                           TD JUL87
         MVN   CURYYDDD,=X'0F0F0F0F0F'                         TD JUL87
         MVZ   CURYYDDD,0(R10)                                 TD JUL87
         CLC   CURYYDDD,=X'FFFFFFFFFF'                         TD JUL87
         BNE   ERR                                             TD JUL87
EDITDONE BR    R2                                              TD JUL87
EDIT1    CLI   5(R10),C'/'                                     TD JUL87
         BNE   ERR                                             TD JUL87
         SPACE 1                                               TD JUL87
         MVN   CURYY,=X'0F0F'                                  TD JUL87
         MVZ   CURYY,0(R10)                                    TD JUL87
         CLC   CURYY,=X'FFFF'                                  TD JUL87
         BNE   ERR                                             TD JUL87
         SPACE 1                                               TD JUL87
         MVZ   CURYY,3(R10)                                    TD JUL87
         CLC   CURYY,=X'FFFF'                                  TD JUL87
         BNE   ERR                                             TD JUL87
         SPACE 1                                               TD JUL87
         MVZ   CURYY,6(R10)                                    TD JUL87
         CLC   CURYY,=X'FFFF'                                  TD JUL87
         BNE   ERR                                             TD JUL87
         BR    R2                                              TD JUL87
CHECKD   DS    0H                                              TD JUL87
         LTR   R10,R10                                         TD JUL87
         BZ    NONE                                            TD JUL87
         BAL   R2,EDIT                                         TD JUL87
         LA    R1,TABDDDMM                                     TD JUL87
         LA    R2,12                                           TD JUL87
NXT      CLC   3(2,R1),0(R10)                                  TD JUL87
         BE    FNDD                                            TD JUL87
         LA    R1,5(R1)                                        TD JUL87
         BCT   R2,NXT                                          TD JUL87
*  ASSUME JULIAN                                               TD JUL87
         CLC   2(3,R10),=C'366'                                TD JUL87
         BL    FNJL                                            TD JUL87
*  BAD JULIAN                                                  TD JUL87
ERR      LA    R8,4                                            TD JUL87
         B     FINI                                            TD JUL87
FNDD     XR    R0,R0                                           TD JUL87
         ST    R0,WKFULL                                       TD JUL87
         PACK  WKFULL+2(2),0(3,R1)   PACK DAY (DDD) FROM TABLE TD JUL87
         PACK  TBPDDD,3(2,R10)       PACK DAY (DD) FROM PARM   TD JUL87
         AP    WKFULL,TBPDDD                                   TD JUL87
         MVC   OPER1(2),6(R10)                                 TD JUL87
         PACK  TBPDDD,OPER1(3)                                 TD JUL87
         MVC   WKFULL+1(1),TBPDDD                              TD JUL87
         BR    R9                                              TD JUL87
FNJL     PACK  OPER1,0(5,R10)                                  TD JUL87
         XR    R1,R1                                           TD JUL87
         ICM   R1,B'0111',OPER1                                TD JUL87
         ST    R1,WKFULL                                       TD JUL87
NONE     BR    R9                                              TD JUL87
*--------------------------------------------------------------------*
* FIXED WORK AREA                                              TD JUL87
*--------------------------------------------------------------------*
TABDDDMM DC    C'000010310205903090041200515106'     DAY OF YEAR TABLE
         DC    C'181072120824309273103041133412'               TD JUL87
         SPACE 1                                               TD JUL87
TABDAY   DC    CL9'MONDAY   ',X'1F'                  DAY OF WEEK TABLE
         DC    CL9'TUESDAY  ',X'2F'                            TD JUL87
         DC    CL9'WEDNESDAY',X'3F'                            TD JUL87
         DC    CL9'THURSDAY ',X'4F'                            TD JUL87
         DC    CL9'FRIDAY   ',X'5F'                            TD JUL87
         DC    CL9'SATURDAY ',X'6F'                            TD JUL87
         DC    CL9'SUNDAY   ',X'7F'                            TD JUL87
TABHEX   DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'   HEX TABLE TD JUL87
EDHHMMSS DC    XL9'2120207A20207A2020'                         TD JUL87
*--------------------------------------------------------------------*
* DYNAMIC WORK AREA FROM GETMAIN                               TD JUL87
*--------------------------------------------------------------------*
WORKAREA DSECT                                                 TD JUL87
         DS    18F              SAVE AREA                      TD JUL87
WKFULL   DS    F                                               TD JUL87
MMDDYY   DS    CL6                                             TD JUL87
OPER1    DS    CL3          FIRST OPERAND FOR DAY OF WEEK FORMULAR
OPER2    DS    CL3          SECOND OPERAND FOR DAY OF WEEK FORMULAR
PARMDATA DS    0H                                              TD JUL87
         TDATEMAP                                              TD JUL87
PARML    EQU   *-PARMDATA                                      TD JUL87
HHMMSS   DS    CL9                                             TD JUL87
WKPDDD   DS    PL2          CURRENT DAY OF YEAR                TD JUL87
WKPYY    DS    PL3          CURRENT YEAR                       TD JUL87
TBPDDD   DS    CL2              YEAR                           TD JUL87
CURYYDDD DS    0CL5                                            TD JUL87
CURYY    DS    CL2              YEAR                           TD JUL87
CURDDD   DS    CL3              DAY                            TD JUL87
WORKLEN  EQU   *-WORKAREA                                      TD JUL87
         END
