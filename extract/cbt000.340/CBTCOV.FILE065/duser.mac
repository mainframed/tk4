DUSER    CSECT
         PRINT GEN
         SAVE  (14,12),,*_&SYSDATE_&SYSTIME
         LR    R12,R15
         USING DUSER,R12
         LA    R0,GETMAINL             GET READY FOR GETMAIN'G SAV AREA
         GETMAIN R,LV=(R0)
         USING GETMAIND,R13            INITIALIZE GETMAIN AREA
         LR    R6,R13                  NORMAL
         LR    R13,R1                       SAVE
         ST    R6,SAVEAREA+4                        CHAINING
         ST    R13,8(0,R6)
         XC    ACALT(ACACFLMU-ACALT+2),ACALT
         MVI   ACAFCN,X'02'
         MVI   ACASFCN,ACASIRT         RETURN TERSE ID-UID-NAME
         LA    R6,MSG128
         ST    R6,ACAMSG                  AND MSG AREA
         LA    R6,ACASMAP
         ST    R6,ACARETB
         LA    R6,ACASLEN
         ST    R6,ACARETL
         SPACE 2
         MVI   DREC,C' '
         MVC   DREC+1(LDREC-1),DREC
         SPACE 2
CVTASVT  EQU   556
ASVTMAXU EQU   516
ASVTFRST EQU   524
ASCBOUCB EQU   144
ASCBASID EQU   36
ASCBJBNS EQU   176
OUCBYFL  EQU   18
         TPUT  HREC,HDREC              PRINT HEADINGS
         L     R10,16                  LOAD CVT ADDRESS
         L     R10,CVTASVT(,R10)       GET ASVT ADDR
         L     R9,ASVTMAXU(,R10)       LOAD MAX USERS
         LA    R10,ASVTFRST(,R10)      POINT TO FIRST
         USING ACCVT,5
         ACFINCVT 5,NONE=RETURN
         L     R6,ACCASVT
         USING ACFASVT,R6
LOOP     ICM   R8,15,0(R10)
         BNP   NEXT
         L     R11,ASCBOUCB(,R8)       GET OUCB
         TM    OUCBYFL(R11),X'20'      TSO USER ?
         BNO   NEXT
         ICM   R4,15,ASCBJBNS(R8)      LOAD USERID NAME POINTER
         BZ    NEXT
         MVC   DLID,0(R4)
         MVC   ACALID,0(R4)
         ACFSVC ACALT,TYPE=A,NONE=RETURN,CVT=HAVE
         MVI   DNAME,C' '
         MVC   DNAME+1(L'DNAME-1),DNAME
         LTR   R15,R15
         BNZ   SKIPIT
         MVC   DNAME,ACASNAME
         MVC   DTERM,ASVSRC
SKIPIT   L     R1,COUNTER      TOTAL NUMBER OF USERS    2-06-81
         LA    R1,1(R1)        TOTAL NUMBER OF USERS    2-06-81
         ST    R1,COUNTER      TOTAL NUMBER OF USERS    2-06-81
WRITEIT  TPUT  DREC,LDREC
NEXT     LA    R10,4(R10)              LOOP THRU TABLE
         LA    R6,32(R6)
         BCT   R9,LOOP
RETURN   DS    0H
         L     R1,COUNTER
         CVD   R1,DOUBLE
         MVC   CNTR,=X'40202120'
         ED    CNTR,DOUBLE+6
         TPUT  HDR1,FINLEN      PRINT TOTAL
         LR    R1,R13
         L     R13,SAVEAREA+4
         LA    R0,GETMAINL
      FREEMAIN R,LV=(0),A=(1)
       RETURN (14,12),,RC=0
*********************************************************************
         LTORG
HREC     DS    0H
HLID     DC    CL8'LOGONID'
         DC    C' '
HNAME    DC    CL20'    NAME'
         DC    C' '
HTERM    DC    CL8'TERMINAL'
HDREC    EQU   *-HREC
HDR1     EQU   *                       LENGTH OF HEADER MESSAGE
         DC    C'TOTAL NUMBER OF USERS'
CNTR     DC    CL8' '
FINLEN   EQU   *-HDR1                  AREA FOR LOCATION DESC.
DOUBLE   DS    D                       FOR TOTAL USERS
COUNTER  DS    F                       FOR TOTAL USERS
         ACCVT
         ACFASVT
GETMAIND DSECT
SAVEAREA DS    18F
         ACALT   DSECT=NO
         ACASMAP DSECT=NO
MSG128   DC    CL128' '
LOGONID  DC    CL8' '
DREC     DS    0H
DLID     DS    CL8
         DC    CL1' '
DNAME    DS    CL20
         DC    CL1' '
DTERM    DS    CL8
LDREC    EQU   *-DREC
GETMAINL EQU   *-GETMAIND
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   DUSER
