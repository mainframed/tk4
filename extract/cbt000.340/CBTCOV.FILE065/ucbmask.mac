UCBMASK  CSECT
TOP      SAVE  (14,12),T,UCBMASK
         LR    BASE,15
         USING TOP,BASE
         LA    R1,SAVEAREA
         ST    13,4(R1)                NEW CORE IS SAVE AREA
         ST    R1,8(13)
         LR    13,R1
         USING SAVEAREA,13
**************************************************************
*** FUNC: TO SET MOUNT ATTR BASED ON VOLSER                ***
***       USING AT VOLUME LIST WITH GENERIC NAMES          ***
*** DATE: 06/11/90                                         ***
***                                                        ***
*** ATTR: ACF AUTH .. AC(1)                                ***
***                                                        ***
*** SAMPLE INPUT ON DD SYSIN .. COL1 VOL, COL8 : PT PRIVATE **
***                                              ST STORAGE **
***                                              PU PUBLIC  **
***      ....+....1....                                     **
***      SYSDA* ST                                         ***
***      SYSL*  PT                                         ***
***      TSOVS* ST                                         ***
***      TSO*   ST                                         ***
***                                                        ***
***                                                        ***
*** NOTES:   ALL DASD UCBS ARE SCANNED, FIRST SYSIN        ***
***          ENTRY TO MATCH UCB VOL WILL APPLY MOUNT ATTR. ***
***          IF NO MATCH, PRIVATE WILL BE USED AS ATTR.    ***
***                                                        ***
***                                                        ***
***          THIS WORKS FOR ALL XA AND ESA SYSTEMS         ***
***          SHOULD WORK WITH MVS/370 SYSTEMS WITH         ***
***          UCBSCAN SUPPORT  1.3.0 AND UP                 ***
*************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
BASE     EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
E        EQU   8
O        EQU   9
XFF      EQU   X'FF'
X0F      EQU   X'0F'
XF0      EQU   X'F0'
OFLG     EQU   X'10'
EOF      EQU   1
NOCLOSE  EQU   2
ECHO     EQU   4
NOCLOSEI EQU   8
NOSYSIN  EQU   16
NOOUT2   EQU   32
         MVI   FLAG1,0
         MVI   FLAG2,0
         L     R4,AVOLS                 SAVE VOLS HERE ALSO
         ST    R4,LASTVOL               PRESET LAST VOL ENTRY
OPENSYSN OPEN (SYSIN,INPUT)
         TM    SYSIN+48,OFLG
         BZ    NOINPUT
GETSYSIN GET   SYSIN,OUTLINE
         MVC   0(16,R4),OUTLINE
         LA    R5,1(R4)                 SECOND CHAR IN VOL
         LA    R6,5                     LOOK AT NEXT 5 CHAR
CHKG     CLI   0(R5),C'*'               VOL IS GENERIC
         BE    GENERIC                  YES
         LA    R5,1(R5)                 TRY NEXT CHAR
         BCT   R6,CHKG                  LOOP ON COUNT
         MVI   15(R4),5                 NO GENERIC, COMP ON ALL 6 CHR
         B     NEXTIN
GENERIC  LA    R6,1(R4)                 CALCULATE
         SR    R5,R6                             LENGTH
         STC   R5,15(R4)                                FOR COMPARE
NEXTIN   LA    R4,16(R4)
         ST    R4,LASTVOL
         B     GETSYSIN
SYSINEOF DS    0H
         CLOSE (SYSIN)
         B     GETCVT
NOINPUT  WTO   'TSDXXXI UCBMASK NO INPUT, FUCTION TERMIATED ',    XXXXXX
               ROUTCDE=(2,11)
         B     EXIT
GETCVT   L     R3,16                    CVT
         L     R3,X'434'(R3)            IOSVSUCB
         SR    R2,R2                    ZERO FOR ICM
         L     R4,AVOLS                 START VOL SEARCH HERE
         XC    UCBWORK,UCBWORK
         LA    R1,UCBWORK
         ST    R1,UCBP1
         LA    R1,=X'20'                DASD TYPE
         ST    R1,UCBP2
         LA    R1,UCBADDR
         ST    R1,UCBP3
         OI    UCBP3,X'80'
         MODESET KEY=ZERO
UCBLOOP  LR    R15,R3
         LA    R1,UCBP1
         BALR  R14,R15
         LTR   R15,R15                  END OF UCBS
         BNZ   ENDUCB                   YES
         L     R2,UCBADDR               LOAD UCB ADDR
         CLI   18(R2),X'20'             DASD
         BNE   UCBLOOP                  NO
         CLI   28(R2),0                 VOL SER
         BE    UCBLOOP                  NO
         L     R4,AVOLS                 GET NEXT EMPTY SLOT
         SR    R5,R5                    CLEAR FOR IC
VOLCK    C     R4,LASTVOL               CHECK END OF LIST
         BE    NOVOL                    NOT IN LIST
         IC    R5,15(R4)                LENGTH FOR GENERIC
         EX    R5,VCOMP                 DO VOL COMPARE
         BE    VOLMATCH                 MATCH
         LA    R4,16(R4)                TRY NEXT
         B     VOLCK
VCOMP    CLC   0(0,R4),28(R2)           MATCH VOLSER
VOLMATCH DS    0H
         CLI   7(R4),C'S'               REQUEST STORAGE
         BNE   TRYPUB
         NI    34(R2),255-X'18'         TURN OF PUB AND PRI
         OI    34(R2),X'04'             TURN ON STORAGE
         B     UCBLOOP                  TRY THIS ONE
TRYPUB   CLI   7(R4),C'B'               REQUEST BYPASS THIS UCB
         BE    UCBLOOP                  YES TRY NEXT UCB
         CLI   7(R4),C'P'               REQUEST PUB OR PRI
         BNE   NOVOL                    INVALID, MAKE PRI
         CLI   8(R4),C'U'               REQUEST PUBLIC
         BNE   NOVOL                    NO, MAKE PRI
         NI    34(R2),255-X'14'         TURN OF STR AND PRI
         OI    34(R2),X'08'             PUBLIC
         B     UCBLOOP
NOVOL    DS    0H
         NI    34(R2),255-X'0C'         TURN OF PUB AND STR
         OI    34(R2),X'10'             TURN ON PRIVATE
         B     UCBLOOP                  TRY THIS ONE
ENDUCB   WTO   'TSDXXXI UCBMASK DONE',ROUTCDE=(2,11)
EXIT     L     R13,4(R13)
         RETURN (14,12),T,RC=0
SAVEAREA DS    18F
DWORD    DS    D
SAVE14   DS    F
UCBP1    DS    F
UCBP2    DS    F
UCBP3    DS    F
UCBADDR  DS    F
LASTLINE DS    F
NUMVOLS  DS    F
LASTVOL  DS    F
AVOLS    DC    A(VOLS)
         LTORG
FLAG1    DS    X
FLAG2    DS    X
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,EODAD=SYSINEOF,          X
               RECFM=FB,LRECL=80
OUTLINE  DS    CL80
         DS    0D
UCBWORK  DS    CL100
VOLS     DS    1000CL16
         END
