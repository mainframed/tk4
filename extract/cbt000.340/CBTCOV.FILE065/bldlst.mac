XXBLDLST TITLE 'PROGRAM TO LIST OUT DYNAMIC BLDL TABLES'
XXBLDLST CSECT
*
*        CODE TO PRINT DYNAM-BLDL STATISTICS
*        NOTE: THIS PROGRAM IS NOT REENTRANT
*
*   CODE LEVEL DEPENDENCIES:                                *HMD 11/82*
*
*        CODE MARKED WITH '$$$$$$' MAY HAVE TO BE           *HMD 11/82*
*        CHANGED IF THE LEVEL OF IGC018XX CHANGES           *HMD 11/82*
*                                                           *HMD 11/82*
*        SEE THE COMMENTS ON THOSE LINES FOR FURTHER INFO   *HMD 11/82*
*                                                           *HMD 11/82*
*   END OF CODE LEVEL DEPENDENCIES
*                                                           *HMD 05/80*
*   LAST MODIFIED BY:                                       *HMD 05/80*
*                                                           *HMD 05/80*
*        HOWARD M. DEAN                                     *HMD 05/80*
*        GTE DATA SERVICES                                  *HMD 05/80*
*        4750 LINCOLN BLVD.                                 *HMD 05/80*
*        MARINA DEL REY, CA                                 *HMD 05/80*
*                                                           *HMD 05/80*
*        05/30/80 - INCREASE HITS, MISSES TO FULLWORD       *HMD 05/80*
*                   ADD EDMK ROUTINE FOR PRINTING           *HMD 05/80*
*                                                           *HMD 05/80*
*        01/24/82 - REFIT IGC018XX TO DF/DS LEVEL (JDM1134) *HMD 05/80*
*                   CODE ADDED TO VERIFY PATCH IN IGC018    *HMD 05/80*
*                   BEFORE REPLACEMENT INCREASE $$$DIS04    *HMD 01/82*
*                   OFFSET BY X'10' BYTES TO X'328'         *HMD 01/82*
*                                                           *HMD 01/82*
*        11/24/82 - USE NEW DYNAMIC RELOCATION DICTIONARY   *HMD 11/82*
*                   AT BEGINNING OF IGC018XX FOR OFFSET     *HMD 11/82*
*                   DISPLACEMENTS.                          *HMD 11/82*
*                                                           *HMD 05/80*
         EJECT                                              *HMD 12/79*
*
*
**
***      EQUATE REGISTERS AND SET UP DYNAMIC SAVEAREA
**
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
RA       EQU   10
R11      EQU   11
RB       EQU   11
R12      EQU   12
RC       EQU   12
R13      EQU   13
RD       EQU   13
R14      EQU   14
RE       EQU   14
R15      EQU   15
RF       EQU   15
*                                                           *HMD 11/82*
SVCBLDL  EQU   18                                           *HMD 11/82*
$LEVEL   EQU    5            $$$$$$     'LEVEL' STAMP       *HMD 11/82*
$DYNRLD  EQU   60            $$$$$$     DYNAMIC RLD SECTION *HMD 11/82*
*                                                           *HMD 11/82*
         EJECT                                              *HMD 12/79*
         USING XXBLDLST,RF              TEMPORARY BASE      *HMD 11/82*
         B     STO1                     BRANCH AROUND ID
         DC    AL1(STO1-STO2)           LENGTH OF ID        *HMD 11/82*
STO2     DC    CL9'XXBLDLST'                                *HMD 11/82*
         DC    CL9'&SYSDATE'                                *HMD 11/82*
         DC    C'COPYRIGHT (C) HOWARD M. DEAN 1982,1983'    *HMD 11/82*
STO1     STM   RE,RC,12(RD)             SAVE REGS IN HI SAVE AREA
         LR    RC,RF                    SET UP BASE
         USING XXBLDLST,RC              OF R12
         USING SAVEAREA,RD              DYNAMIC SAVEAREA BASE + WORK
         DROP  RF                       NO NEED FOR BASE    *HMD 11/82*
         LR    R2,R1                    SAVE PARAMS
         LR    RB,RD                    SAVE CALLERS SAVE AREA ADDR
         GETMAIN R,LV=GETSIZE1          GET SOME CORE
         LR    RD,R1                    SET DSECT BASE
         ST    RB,SAVE1+4               SAVE CALLERS SAVE AREA ADDR
         ST    RD,8(RB)                 SAVE OURS IN CALLERS
         EJECT
*
**
***      CHECK OUT PARM FIELD PASSED
**
*
         L     R2,0(R2)                 PARM FIELD PTR
         LH    R3,0(R2)       PARM LENGTH
         LTR   R3,R3
         BZ    NOPARM         NO PARM TO DO
         LA    R2,1(R3,R2)    ADDR LAST BYTE OF PARM
T1       CLI   0(R2),C'S'     SUMMARY ONLY REQUESTED
         BNE   T2
         OI    CTLFLAG,X'80'  YES,FLAG AS SUMMARY ONLY
T2       DS    0H             NEXT PARM TEST......
         BCTR  R2,0           ADDR PREVIOUS CHAR
         BCT   R3,T1          PROCESS IT IF ANY..
         EJECT
*
**
***      FIND IGC018XX IN CORE AND CHECK CODE LEVEL
**
*
NOPARM   DS    0H
         L     R3,CVTPTR                   ==> CVT          *HMD 11/82*
         L     R3,CVTABEND-CVT(,R3)        ==> SCVT         *HMD 11/82*
         L     R3,SCVTSVCT-SCVTSECT(,R3)   ==> SVC TABLE    *HMD 11/82*
         LA    R3,SVCBLDL*8(,R3)           ==> IGC018       *HMD 11/82*
         L     R2,0(R3)                    ==> IGC018XX     *HMD 11/82*
$CHKBLDL CLC   $LEVEL(L'LEVEL,R2),LEVEL    CORRECT LEVEL?   *HMD 01/82*
*                                                           *HMD 01/82*
* CHECK IF DF/DS SUPPORT (JDM1134) IS INCLUDED.             *HMD 01/82*
* LEVEL IS DF/DS JDM1134 WITH PTF UZ37480 (TAPE 8107)       *HMD 01/82*
*                                                           *HMD 01/82*
* CHECK BLDL CODE (IGC018XX) AT OFFSET 4 FOR CORRECT        *HMD 01/80*
* MODULE NAME AND RELEASE DATE (CHANGE ABOVE IF NECESSARY)  *HMD 01/80*
*                                                           *HMD 01/82*
* CHANGED 01/24/82 TO SUPPORT DF/DS PP (JDM1134)            *HMD 05/80*
* AND ASSOCIATED MAINTAINCE.                                *HMD 01/82*
*                                                           *HMD 01/82*
         BNE   UNSUPORT       NO ,TELL USER & ABEND
         EJECT
*
**
***      MOVE TABLE FROM IGC018XX TO OUR PROGRAM
**
*
         LA    R3,$DYNRLD(,R2)      DYNAMIC RLD DSECT       *HMD 11/82*
         USING RLDSECT,R3           ADDRESS THE DSECT       *HMD 11/82*
         L     R4,DIS04             OFFSET OF $$$DIS04      *HMD 11/82*
         DROP  R3                                           *HMD 11/82*
         AR    R4,R2                RELOCATE                *HMD 11/82*
         L     R6,SORT-TABLE(R4)    GET SORT VALUE          *HMD 11/82*
         ST    R6,SORT                                      *HMD 11/82*
         L     R6,HITS-TABLE(R4)    GET HITS VALUE          *HMD 11/82*
         ST    R6,HITS                                      *HMD 11/82*
         L     R6,MISSES-TABLE(R4)     LOAD # MISSES        *HMD 11/82*
         ST    R6,MISSES               SAVE IN TABLE        *HMD 11/82*
*
         LM    R5,R7,TABSTAT
         L     R9,TABSTAT-TABLE(R4)    LOAD TABLE ADDRESS   *HMD 11/82*
MOVEDATA MVC   0(64,R5),0(R9)
         AR    R9,R6
         BXLE  R5,R6,MOVEDATA
         EJECT
*
**
***      MAIN PROCESSING
**
*
         OPEN  (SYSPRINT,(OUTPUT))
         TM    SYSPRINT+DCBOFLGS-IHADCB,DCBOFOPN   OPEND?   *HMD 05/80*
         BO    OPENOK                                       *HMD 05/80*
         WTO   'XXBLDLST -- SYSPRINT FAILED TO OPEN',       *HMD 11/82*X
               ROUTCDE=(1,2,10,11),DESC=(4,6)               *HMD 11/82*
         B     RET           LEAVE                          *HMD 05/80*
         USING ENTRY,R5
OPENOK   LM    R5,R7,TABSTAT
PRINTIT  MVC   OUTLINE+1(79),OUTLINE CLEAR OUT LINE
         L     R9,RECENT      Q.ANY ACTION
         LTR   R9,R9          ELSE DONT PRINT LINE
         BZ    PRINTED
         TM    CTLFLAG,X'80'  SUMMARY ONLY
         BO    PRINTED
         LH    R9,RECENT
         LTR   R9,R9
         BZ    PRECENT
         CVD   R9,DWD
         UNPK  OUTLINE+3(5),DWD+5(3)
         OI    OUTLINE+7,X'F0'
PRECENT  DS    0H
         LH    R9,TOTAL
         LTR   R9,R9
         BZ    PTOTAL
         CVD   R9,DWD
         UNPK  OUTLINE+9(5),DWD+5(3)
         OI    OUTLINE+13,X'F0'
PTOTAL   DS    0H
         MVC   OUTLINE+16(8),DATA
         SR    R9,R9
         IC    R9,DATA+59     CONCAT NO
         IC    R9,CHARTABL(R9)
         STC   R9,OUTLINE+26
         UNPK  OUTLINE+35(7),DATA+8(4)
         UNPK  OUTLINE+41(7),DATA+11(4)
         UNPK  OUTLINE+47(7),DATA+14(4)
         UNPK  OUTLINE+53(7),DATA+17(4)
         UNPK  OUTLINE+59(7),DATA+20(4)
         UNPK  OUTLINE+65(7),DATA+23(4)
         UNPK  OUTLINE+71(7),DATA+26(4)
         MVI   OUTLINE+75,C' '
         NC    OUTLINE+35(40),=40X'0F'
         TR    OUTLINE+35(40),CHARTABL
         PUT   SYSPRINT,OUTLINE
PRINTED  BXLE  R5,R6,PRINTIT
         PUT SYSPRINT,BLANKS
*                                                           *HMD 05/80*
         LH    R9,SORT                   LOAD SORT VALUE    *HMD 05/80*
         XC    DWD,DWD                   CLEAR DWD          *HMD 05/80*
         CVD   R9,DWD                    CONVERT            *HMD 05/80*
         MVC   SORTPAT,BLANKS            CLEAR FIELD        *HMD 05/80*
         LA    R1,SPATT+L'SPATT-1        IN CASE OF SIG.    *HMD 05/80*
         EDMK  SPATT,DWD+3               EDIT FIELD         *HMD 05/80*
         LA    R2,SPATT+L'SPATT          GET LENGTH OF FIELD*HMD 05/80*
         SR    R2,R1                     LENGTH TO MOVE     *HMD 05/80*
         BCTR  R2,0                      -1 FOR EX          *HMD 05/80*
         EX    R2,ISPATT                 MOVE RESULT        *HMD 05/80*
         PUT   SYSPRINT,SORTSUM          PRINT RESULT       *HMD 05/80*
*                                                           *HMD 05/80*
         L     R9,HITS                   LOAD HITS VALUE    *HMD 05/80*
         XC    DWD,DWD                   CLEAR DWD          *HMD 05/80*
         CVD   R9,DWD                    CONVERT            *HMD 05/80*
         MVC   HITPAT,BLANKS             CLEAR FIELD        *HMD 05/80*
         LA    R1,HPATT+L'HPATT-1        IN CASE OF SIG.    *HMD 05/80*
         EDMK  HPATT,DWD+3               EDIT FIELD         *HMD 05/80*
         LA    R2,HPATT+L'HPATT          GET LENGTH OF FIELD*HMD 05/80*
         SR    R2,R1                     LENGTH TO MOVE     *HMD 05/80*
         BCTR  R2,0                      -1 FOR EX          *HMD 05/80*
         EX    R2,IHPATT                 MOVE RESULT        *HMD 05/80*
         PUT   SYSPRINT,HITSUM           PRINT RESULT       *HMD 05/80*
*                                                           *HMD 05/80*
         L     R9,MISSES                 LOAD MISSES VALUE  *HMD 05/80*
         XC    DWD,DWD                   CLEAR DWD          *HMD 05/80*
         CVD   R9,DWD                    CONVERT            *HMD 05/80*
         MVC   MISSPAT,BLANKS            CLEAR FIELD        *HMD 05/80*
         LA    R1,MPATT+L'MPATT-1        IN CASE OF SIG.    *HMD 05/80*
         EDMK  MPATT,DWD+3               EDIT FIELD         *HMD 05/80*
         LA    R2,MPATT+L'MPATT          GET LENGTH OF FIELD*HMD 05/80*
         SR    R2,R1                     LENGTH TO MOVE     *HMD 05/80*
         BCTR  R2,0                      -1 FOR EX          *HMD 05/80*
         EX    R2,IMPATT                 MOVE RESULT        *HMD 05/80*
         PUT   SYSPRINT,MISSSUM          PRINT RESULT       *HMD 05/80*
*                                                           *HMD 05/80*
CLOSER   CLOSE SYSPRINT                  CLOSE SYSPRINT     *HMD 05/80*
         EJECT
*
**
***      FREE DYNAMIC SAVEAREA AND RETURN
**
*
RET      DS    0H
         LR    R1,RD                    ADDR OF AREA TO FREE
         L     RD,4(RD)                 HI SAVE AREA ADDR
         FREEMAIN R,LV=GETSIZE1,A=(1)   FREE UP CORE
         LM    RE,RC,12(RD)             RESORE CALLERS REGS
         BR    RE                       RETURN
         EJECT
*
**
***      WTO WRONG LEVEL IN IGC018XX CODE
**
*
UNSUPORT DS    0H
         WTO   'XXBLDLST - WRONG LEVEL OF IGC018XX CODE',   *HMD 11/82*X
               ROUTCDE=(1,2,10,11),DESC=(4,6)               *HMD 11/82*
         B     RET                      HOME
         LTORG
         EJECT
**                                                          *HMD 05/80*
**  CONSTANTS                                               *HMD 05/80*
**                                                          *HMD 05/80*
SPATT    DC    X'402020206B2020206B202120'                  *HMD 05/80*
HPATT    DC    X'402020206B2020206B202120'                  *HMD 05/80*
MPATT    DC    X'402020206B2020206B202120'                  *HMD 05/80*
*                                                           *HMD 05/80*
ISPATT   MVC   SORTPAT(0),0(R1)    MOVE EDITED FIELD        *HMD 05/80*
IHPATT   MVC   HITPAT(0),0(R1)     MOVE EDITED FIELD        *HMD 05/80*
IMPATT   MVC   MISSPAT(0),0(R1)    MOVE EDITED FIELD        *HMD 05/80*
*                                                           *HMD 05/80*
LEVEL    DC    C'IGC018XX11-24-82'             CURRENT LVL  *HMD 11/82*
*                                                           *HMD 11/82*
DWD      DC    D'0'
BLANKS   DC    CL80' '
SORTSUM  DS    0CL80    SORT SUMMARY                        *HMD 05/80*
         DC    C' SORTS=  '                                 *HMD 05/80*
SORTPAT  DC    CL(80-(SORTPAT-SORTSUM))' '    FILL TO 80    *HMD 05/80*
HITSUM   DS    0CL80    HIT  SUMMARY                        *HMD 05/80*
         DC    C' HITS=   '                                 *HMD 05/80*
HITPAT   DC    CL(80-(HITPAT-HITSUM))' '      FILL TO 80    *HMD 05/80*
MISSSUM  DS    0CL80                                        *HMD 05/80*
         DC    C' MISSES= '                                 *HMD 05/80*
MISSPAT  DC    CL(80-(MISSPAT-MISSSUM))' '    FILL TO 80    *HMD 05/80*
OUTLINE  DC    CL80' '
CHARTABL DC    C'0123456789ABCDEF'
CTLFLAG  DC    X'00'          CONTROL FLAG FROM PARMS
*                             X'80' - PRINT SUMMARY ONLY
         EJECT
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,LRECL=80,             X
               RECFM=FA,BLKSIZE=80
         EJECT
ENT      EQU   200      INCREASE ENTRIES TO 300      $$$$$$ *HMD 05/80*
ENTX     EQU    74      ENTRIES/2.71828 - FIXED PART $$$$$$ *HMD 05/80*
*                                                           *HMD 11/82*
*  $$$$$$ THIS TABLE MUST MATCH THE TABLE IN IGC018XX       *HMD 11/82*
*                                                           *HMD 11/82*
TABLE    DS    0D
TABSTAT  DC    A(START)
TABSIZE  DC    F'64'
TABEND   DC    A(LASTENT)
TABLAST  DC    A(LASTENT)
TABSLOT  DC    A(START+ENTX*64)
SORT     DC    H'0'
ENTRIES  DC    AL2(ENT)
HITS     DC    F'0'
MISSES   DC    F'0'
START    DC    ((ENT-1)*64)X'0'
LASTENT  DC    64X'0'
LINKEND  DS    0F
*                                                           *HMD 11/82*
         EJECT
*
**
***      TABLE ENTRY DSECT
**
*
ENTRY    DSECT
RECENT   DS    H
TOTAL    DS    H
DATA     DS    0F
         EJECT
*
**
***      DSECT FOR GETMAINED WORK AREA
**
*
SAVEAREA DSECT
SAVE1    DS    18F
GETEND1  DS    0D
GETSIZE1 EQU   GETEND1-SAVE1
         EJECT                                              *HMD 05/80*
*
**
***      IGC018XX RELOCATION DICTIONARY DSECT               *HMD 11/82*
**
*
RLDSECT  DSECT                 IGC018XX RELOCATION DSECT
IGC018AD DS    A               REAL IGC018 EP               *HMD 11/82*
LINKADDR DS    A               LINKTAB ADCON                *HMD 11/82*
LINKTAB  DS    A               LINKTAB ADDRESS              *HMD 11/82*
ENTRY1   DS    A               ENTRY1 OFFSET                *HMD 11/82*
NWB1     DS    A               NWB1 ADCON                   *HMD 11/82*
ENTRY2   DS    A               ENTRY2 ADDRESS               *HMD 11/82*
NWB2     DS    A               NWB2 ADCON                   *HMD 11/82*
TABSTAT1 DS    A               TABSTAT1 ADDRESS             *HMD 11/82*
START1   DS    A               START1   OFFSET              *HMD 11/82*
LASTENT1 DS    A               LASTENT1 OFFSET              *HMD 11/82*
TABEND1  DS    A               TABEND1  OFFSET              *HMD 11/82*
LASTSLT1 DS    A               LASTSLT1 OFFSET              *HMD 11/82*
TABSLOT1 DS    A               TABSLOT1 OFFSET              *HMD 11/82*
CALC     DS    A               CALCULATION                  *HMD 11/82*
DIS01    DS    A               $$$DIS01 OFFSET              *HMD 11/82*
DIS02    DS    A               $$$DIS02 OFFSET              *HMD 11/82*
DIS04    DS    A               $$$DIS04 OFFSET              *HMD 11/82*
         EJECT                                              *HMD 11/82*
         DCBD  DSORG=PS,DEVD=(TA,DA)                        *HMD 05/80*
         EJECT                                              *HMD 11/82*
         CVT   LIST=YES,DSECT=YES                           *HMD 11/82*
         EJECT                                              *HMD 11/82*
         IHASCVT LIST=YES,DSECT=YES                         *HMD 11/82*
         END
