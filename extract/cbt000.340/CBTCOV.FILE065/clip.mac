         PRINT NOGEN
CLIP     TITLE 'CLIP - RELABEL A DISK PACK'
***********************************************************************
*                                                                     *
*        THIS PROGRAM WILL RELABEL ANY OFFLINE DISK PACK. THE PROGRAM *
*        IS USED AS A STARTED TASK AND INTERACTION IS DONE THROUGH    *
*        WTO AND WTOR. ANY NUMBER OF PACKS CAN BE RELABELED IN ONE    *
*        EXECUTION OF THE PROGRAM.                                    *
*                                                                     *
*                                                                     *
*        LINKEDIT ATTRIBUTES: AC=1                                    *
*                                                                     *
*        JCL REQUIRED:                                                *
*                                                                     *
*        //CLIP     EXEC PGM=CLIP,DPRTY=10                            *
*        //STEPLIB  DD  <TO A AUTHORIZED LIBRARY, OPTIONAL>           *
*        //SYSPRINT DD SYSOUT=A <OUTPUT FORM IEHDASDR>                *
*        //SYSIN    DD DSN=&&CLIP,UNIT=SYSDA,SPACE=(TRK,(1)),         *
*        //            DCB=(RECFM=FB,LRECL=80,BLKSIZE=80)             *
*                                                                     *
*        WRITTEN BY                                                   *
*                                                                     *
*             KENNETH RASMUSSEN                                       *
*             ROCKWELL INTERNATIONAL                                  *
*             P O BOX 2515  041/SH28                                  *
*             SEAL BEACH, CA. 90740                                   *
*             (213) 594-2540                                          *
*                                                                     *
***********************************************************************
         EJECT
CLIP     PRIME REG=12
         REGEQU
MAINLOOP OPEN  (SYSIN,(OUTPUT))         CREATE THE CONTROL CARD
ASKAGAIN XC    CLIPECB,CLIPECB          RESET ECB WORD
         MVC   CLIPRPLY(10),BLANKS      RESET REPLY AREA
         WTOR  'CLIP - REPLY UUU,VVVVVV OR END',                       C
               CLIPRPLY,10,CLIPECB,ROUTCDE=2
         WAIT  ECB=CLIPECB
         OC    CLIPRPLY(10),BLANKS      SET REPLY TO CAPS
         CLC   CLIPRPLY(3),=CL3'END'    REPLY = END ?
         BE    ENDPGM                   IF YES, THEN BRANCH
         EJECT
***********************************************************************
*                                                                     *
*        CHECK REPLY AND VALIDATE DEVICE ADDRESS                      *
*                                                                     *
***********************************************************************
         CLI   CLIPRPLY+3,C','          CHECK MSG FORMAT
         BE    CHKRPLY2                 IF OK, THEN BRANCH
         CLI   CLIPRPLY+4,C' '          CHECK MSG FORMAT
         BNE   CHKRPLY2                 IF OK, THEN BRANCH
CHKRPLY1 LA    R1,WTO01                 INVALID MSG FORMAT
         SVC   35
         B     ASKAGAIN
CHKRPLY2 L     R2,CVTPTR                GET CVT POINTER
         L     R2,CVTILK2(,R2)          GET TABLE POINTER
         USING UCB,R4
         LA    R3,CLIPRPLY              POINT AT SEPCIFIED DEVICE
         SR    R4,R4
UCBSCAN  ICM   R4,B'0011',0(R2)         GET NEXT UCB ADDRESS
         BZ    UCBLOOP                  IF ZERO, THEN BRANCH
         C     R4,TABLEEND              END OF LIST ?
         BE    UCBERROR                 IF YES, UCB NOT FOUND, BRANCH
         CLC   UCBNAME(3),0(R3)         MATCH ON UCB ?
         BE    CHKDEV1                  BRANCH IF FOUND
UCBLOOP  LA    R2,2(,R2)                GET NEXT ENTRY IN LOOKUP TABLE
         B     UCBSCAN                  LOOP THRU TABLE
UCBERROR MVC   WTO02+11(3),0(R3)        INSERT BAD UCB ADDRESS
         LA    R1,WTO02
         SVC   35
         B     ASKAGAIN
CHKDEV1  CLI   UCBTBYT3,UCB3DACC        DIRECT ACCESS DEVICE ?
         BE    CHKDEV2                  BRANCH IF YES
         MVC   WTO03+11(3),0(R3)        INSERT UCB ADDRESS
         LA    R1,WTO03
         SVC   35
         B     ASKAGAIN
CHKDEV2  TM    UCBSTAT,UCBONLI          DEVICE OFFLINE ?
         BO    CHKDEV3                  IF ONLINE, THEN BRANCH
         TM    UCBSTAT,UCBCHGS          DEVICE CHANGING STATUS ?
         BNO   DASDLABL                 IF NOT, THEN BRANCH
CHKDEV3  TM    UCBSTAT,UCBPRES          DEVICE PERM RESIDENT ?
         BNO   CHKDEV4                  IF NOT, THEN BRANCH
         MVC   WTO04+11(3),0(R3)        INSERT UCB ADDRESS
         LA    R1,WTO04
         SVC   35
         B     ASKAGAIN
CHKDEV4  MVC   WTO05+11(3),0(R3)        INSERT UCB ADDRESS
         LA    R1,WTO05                 DEVICE NOT OFFLINE
         SVC   35
         B     ASKAGAIN
         DROP  R4
         EJECT
***********************************************************************
*                                                                     *
*        INVOKE IEHDASDR TO RELABEL VOLUME                            *
*                                                                     *
***********************************************************************
DASDLABL MVC   CLIPUNIT(3),0(R3)        SETUP FOR IEHDASDR
         MVC   CLIPVOL(6),CLIPRPLY+4     UNIT AND VOLUME
         PUT   SYSIN,INPUT
         CLOSE SYSIN
         LINK  EP=IEHDASDR,PARAM=(CLIPLIST),VL=1
         C     R15,RETCODE              CHK HIGHEST RETURN CODE
         BNH   DASDRET
         ST    R15,RETCODE              SAVE RETURN CODE
DASDRET  LTR   R5,R15                   REURN CODE = 0 ?
         BNZ   DASDRERR                 IF NOT, THEN BRANCH
         MVC   WTO06+11(3),CLIPRPLY     EDIT UNIT
         MVC   WTO06+36(6),CLIPRPLY+4    AND VOLUME
         LA    R1,WTO06
         SVC   35
         B     MAINLOOP                 LOOP AGAIN
DASDRERR MVC   WTO06+11(3),CLIPRPLY     ERROR IN IEHDASDR EXECUTION
         LA    R1,WTO07
         SVC   35
         B     MAINLOOP                 LOOP AGAIN
ENDPGM   LA    R1,WTO08
         SVC   35
         L     R15,RETCODE              PICK UP RETURN CODE
         TERME
         EJECT
***********************************************************************
*                                                                     *
*        WORKAREA AND CONSTANTS                                       *
*                                                                     *
***********************************************************************
SAVEAREA DC    18F'0'                   SAVE AREA FOR IEHDASDR
CLIPECB  DC    F'0'                     WTOR ECB
RETCODE  DC    F'0'                     IEHDASDR RETURN CODE (HIGHEST)
TABLEEND DC    X'0000FFFF'              FOR UCB TABLE END CHECK
CLIPLIST DS    0H                       IEHDASDR PARAMETERS
         DC    H'0'
CLIPRPLY DC    10C' '                   WTOR REPLY AREA
BLANKS   DC    10C' '
INPUT    DC    C' '                     SYSIN RECORD FOR IEHDASDR
         DC    C'LABEL TODD='
CLIPUNIT DC    CL3' '
         DC    C',NEWVOLID='
CLIPVOL  DC    CL6' '
         DC    (80-(*-INPUT))C' '
CVTILK2  EQU   X'28'                    ADDRESS OF UCB LOOKUP TABLE
CVTPTR   EQU   X'10'                    ADDRESS OF CVT POINTER
         EJECT
***********************************************************************
*                                                                     *
*        WRITE TO OPERATOR'S                                          *
*                                                                     *
***********************************************************************
WTO01    WTO  'CLIP - INVALID REPLY',ROUTCDE=2,MF=L
WTO02    WTO  'CLIP - XXX IS AN INVALID DEVICE ADDRESS',ROUTCDE=2,MF=L
WTO03    WTO  'CLIP - XXX IS NOT A DIRECT ACCESS DEVICE',ROUTCDE=2,MF=L
WTO04    WTO  'CLIP - XXX IS A PERM RESIDENT VOLUME',ROUTCDE=2,MF=L
WTO05    WTO  'CLIP - XXX IS NOT OFFLINE',ROUTCDE=2,MF=L
WTO06    WTO  'CLIP - XXX RELABELED TO VOL SER YYYYYY',ROUTCDE=2,MF=L
WTO07    WTO  'CLIP - XXX IEHDASDR ERROR - CHECK SYSPRINT',            C
               ROUTCDE=2,MF=L
WTO08    WTO  'CLIP - RUN ENDED',ROUTCDE=2,MF=L
         EJECT
SYSIN    DCB   DDNAME=SYSIN,BUFNO=1,DSORG=PS,MACRF=PM
         LTORG
         EJECT
TIOT     DSECT
         IEFTIOT1
         EJECT
UCB      DSECT
         IEFUCBOB
         END
