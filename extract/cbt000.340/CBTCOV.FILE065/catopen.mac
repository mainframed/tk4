         PRINT NOGEN
CATO     TITLE 'PROGRAM TO DISPLAY OPEN VSAM CATALOGS'
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*                                                                     *
*        THE PURPOSE IF THIS PROGRAM IS TO LIST OUT ANY OPEN          *
*        VSAM CATALOGES. THIS PROGRAM SHOULD BE RUN BEFORE ANY        *
*        CATALOG REORGINATION IS ATTEMPTED.                           *
*        WTO WITH ROUTE CODE 2 AND 11 ARE ISSUED.                     *
*                                                                     *
*        THE ENQ AND DEQ INSTRUCTIONS ARE TO BE USED, THIS            *
*        PROGRAM MUST BE LINK EDITED AUTHORIZED AND PLACED IN         *
*        AN AUTHORIZED LIBRARY.                                       *
*                                                                     *
*        JCL REQUIRED:                                                *
*                                                                     *
*        //CATOPEN  EXEC PGM=CATOPEN                                  *
*        //STEPLIB  DD  DSN=  (OPTIONAL)                              *
*        //SYSUDUMP DD  SYSOUT=*                                      *
*                                                                     *
*        WRITTEN BY                                                   *
*                                                                     *
*             KENNETH RASMUSSEN                                       *
*             ROCKWELL INTERNATIONAL                                  *
*             P O BOX 2515  041/SH28                                  *
*             SEAL BEACH, CA. 90740                                   *
*             (213) 594-2540                                          *
*                                                                     *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         EJECT
CATOPEN  CSECT
         PRIME
         REGEQU
         L     R2,16               LOAD CVT
         LA    R3,256              OFFESET TO AMCBS PTR
         L     R2,0(R2,R3)         POINT TO AMCBS
*        ENQ   (MAJNAME,MINNAME,S,4,SYSTEM) ENQ CAXWA CHAIN
*        LTR   R15,R15             RETURN CODE ZERO?
*        BNZ   ABEND               NO, ABEND
         L     R2,20(,R2)          POINT TO CAXWA CHAIN
         LTR   R2,R2               CAXWA CHAIN PTR ZERO?
         BZ    NOCATS              YES, BRANCH
CAXWA    EQU   *
         MVC   WTO+36(44),52(R2)   SAVE CAT NAME IN MSG
         SR    R5,R5               CLEAR R5
         IC    R5,11(R2)           LOAD ACTIVITY COUNT
         CVD   R5,WORK             CONVERT TO DECIMAL
         MVC   CNT,PTRN
         ED    CNT,WORK+6
         MVC   WTO+27(3),CNT+1 EDIT ACTIVE COUNT
WTO      WTO   'OPEN CATALOG - CNT=CCC DSN= XXXXXXXXXXXXXXXXXXXXXXXXXXXX
               XXXXXXXXXXXXXXX      ',ROUTCDE=(2,11)
         L     R2,4(R2)            GET NEXT CAXWA
         LTR   R2,R2               MORE ENTRIES?
         BNZ   CAXWA               YES, BRANCH
         B     DEQ
NOCATS   EQU   *
*        THIS CODE SHOULD NEVER BE EXECUTED SINCE THE MASTER
*        IS ALWAYS OPEN
         WTO   'NO OPEN CATALOGS',ROUTCDE=(2,11)
DEQ      EQU   *
*        DEQ   (MAJNAME,MINNAME,4,SYSTEM) DEQ CAWXA CHAIN
         TERME
ABEND    EQU   *
         ABEND 1,DUMP
MAJNAME  DC    CL8'SYSZCAXW'
MINNAME  DC    CL4'CAXW'
WORK     DC    D'0'
CNT      DC    CL4' '
PTRN     DC    X'40202120'
         END
