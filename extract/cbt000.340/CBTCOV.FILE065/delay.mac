DELAY    TITLE '-----  WAIT FOR A SPECIFIED TIME PERIOD  -----'
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  DELAY                                              *
*                                                                     *
*   PURPOSE:       TO ALLOW A CLIST OR A BATCH JOB TO ENTER A WAIT    *
*                  STATE FOR A USER SPECIFIED LENGTH OF TIME          *
*                                                                     *
*   AUTHOR:        LEONARD D. WOREN                                   *
*                                                                     *
*   DATE:          LONG, LONG AGO (AT SDC)                            *
*                                                                     *
*   UPDATES:                                                          *
*      03/26/84 LDW  ALLOW FRACTIONAL SECONDS IN OPERAND              *
*      01/07/82 LDW  CHANGE STIMER FROM WAIT TO REAL (TO ALLOW ATTN)  *
*                                                                     *
*   INPUT:         THE NUMBER OF SECONDS TO WAIT.  AS A TSO COMMAND   *
*                  PROCESSOR, THE INPUT IS THE OPERAND.  AS A BATCH   *
*                  JOB, THE NUMBER OF SECONDS IS THE PARM FIELD.      *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
DELAY    OSENTER  ENV=(CALL,SIM),EXIT=LEAVE,RC=0,                      $
               PARMREG=R4,ADDRREG=R2,LENREG=R3
         SR    R9,R9                    CLEAR ACCUMULATOR
         LA    R7,100                   GET SCALE FACTOR
         SPACE 2
LOOP     LTR   R3,R3                    ANYTHING LEFT?
         BNP   DONE
         CLI   0(R2),C'.'               DECIMAL POINT?
         BE    DECPOINT                 YES - GO HANDLE FRACTIONS
         MH    R9,=H'10'                SHIFT PREVIOUS
         IC    R1,0(,R2)                GET A CHAR
         N     R1,=F'15'                STRIP IT
         AR    R9,R1                    ADD NEW
         LA    R2,1(,R2)                MOVE SCAN PTR
         BCTR  R3,0                     DECR LENGTH LEFT
         CLI   FLAG,0                   DECIMAL POINT HIT?
         BE    LOOP                     NO
         SR    R6,R6                    CLEAR FOR DIVIDE
         D     R7-1,=F'10'              ADJUST SCALE FACTOR
         LTR   R7,R7                    ANYTHING LEFT?
         BNP   DONE                     NO - WHY BOTHER
         B     LOOP
         SPACE 3
DECPOINT BCTR  R3,0                     ADJUST FOR DECIMAL POINT
         LA    R2,1(,R2)                ...
         MVI   FLAG,X'FF'               REMEMBER HIT DECIMAL POINT
         CH    R3,=H'2'                 MORE THAN .TH?
         BNH   LOOP                     NO - USE WHAT'S LEFT
         LH    R3,=H'2'                 YES - USE JUST .TH
         B     LOOP
         SPACE 3
DONE     MR    R9-1,R7                  CONVERT SECONDS TO HUNDREDTHS
         LTR   R9,R9                    ANY DELAY GIVEN?
         BNP   *+8                      NO - USE DEFAULT
         ST    R9,TIME
         SPACE 2
         STIMER  REAL,RING,BINTVL=TIME
         SPACE 1
         WAIT  ECB=ECB
         SPACE 1
         B     LEAVE
         SPACE 3
         DROPX ,
         SPACE 1
         USING *,R15
RING     POST  ECB,1
         SPACE 1
         BR    R14
         SPACE 1
         DROP  R15
         SPACE 3
TIME     DC    A(24*60*100-1)           LONG TIME
ECB      DC    F'0'
FLAG     DC    X'00'
         LTORG
         END
