./ ADD NAME=$$DOC                                  12JUN82 12.33.18
CONTENTS OF THIS DATASET:
------------------------
$$DOC      -   THIS MEMBER, YOU ARE READING IT

AMDSYS0A   -   ASSEMBLER SOURCE MODULE FOR CCWTRACE

CCWASM     -   JCL TO ASSEMBLE CCWTRACE AND PUT OBJECT IN YOUR DATASET

CCWDOC     -   ORIGINAL IBM DOCUMENTATION FOR CCWTRACE (READ IT)

CCWMODS    -   JCLIN/UCLIN/SMP STATEMENTS ALONG WITH PRE-SMP CLEANUP
               EVERYTHING IS HERE THAT IS REQUIRED TO INSTALL
               CCWTRACE. ONLY THE NAMES OF DATASETS NEED BE CHANGED.

GTFCCW     -   ASSEMBLER SOURCE FOR CCWTRACE

GTFCCWPR   -   ASSEMBLER SOURCE FOR CCWTRACE - USED BY PRINT PROGRAM

GTFPRINT   -   ASSEMBLER SOURCE FOR CCWTRACE - PRINT PROGRAM

GTFSIO     -   ASSEMBLER SOURCE FOR CCWTRACE
./ ADD NAME=AMDSYS0A                               12JUN82 12.33.18
         TITLE 'AMDSYS0A    W.BAUR  VERSION 3.0  FILE09   -- IBM INTERNA
               AL USE ONLY  -- '
AMDSYS0A CSECT
         SPACE 1
*        COMMON EQUATES
         SPACE 1
@00      EQU   0
@01      EQU   1
@02      EQU   2
@03      EQU   3
@04      EQU   4
@05      EQU   5
@06      EQU   6
@07      EQU   7
@08      EQU   8
@09      EQU   9
@10      EQU   10
@11      EQU   11
@12      EQU   12
@13      EQU   13
@14      EQU   14
@15      EQU   15
         EJECT
         SPACE 3
*******************************************************F.BIEDERMANN****
*                                                                     *
*        THIS LOAD IS THE INTERFACE MODULE TO EITHER AMDSYS0S,        *
*        OR GTFCCWPR. AT ENTRY A CHECK IS MADE                       *
*        WETHER THIS IS FIRST ENTRY OR NOT. IF THIS IS THE FIRST      *
*        ENTRY, THE LLS CHAIN IS CHECKED, WETHER GTFCCWPR IS          *
*        AVAILABLE OR NOT. IF AVAILABLE ITS ENTRYPOINT ADDRESS IS     *
*        PLACED IN THE INTERFACE ADDRESS FIELD. OTHERWISE AMDSYS0S    *
*        IS LOADED.                                                   *
*                                                                     *
*        FOR THE NEXT ENTRIES, THIS LOAD WORKS AS INTERFACE MODULE    *
*        AND GIVE THE CONTROL TO THE REQUIRED ENTRYPOINT.             *
*                                                                     *
*******************************************************F.BIEDERMANN****
         EJECT
         SPACE 3
AMDSYS0A CSECT
         SPACE 1
         USING *,@15
         L     @15,ENTRY          GET BRANCH ADDRESS
         LTR   @15,@15            ADDRESS ALREADY IN ?
         BCR   2,@15              ADDRESS ALREADY IN
         B     SKIP               SKIP ID
         SPACE 1
MODID    DC    C'3.0'             USED TO COMPARE THE LEVEL BY GTFPRINT
         DS    0F
         DC    C'AMDSYS0A W.BAUR VERSION 3.0'
         SPACE 1
SKIP     DS    0H
         STM   @14,@12,SAVE+12    SAVE REGISTERS
         L     @04,16             CVT
         L     @03,0(,@04)        TCBWORDS
         L     @03,4(,@03)        TCB
         L     @05,36(,@03)       LLS
LOOP     DS    0H
         LTR   @05,@05            LAST
         BZ    LOAD               YES NORMAL START
         L     @03,4(,@05)        CDE
         CLC   8(L'GTFCCWPR,@03),GTFCCWPR
         BE    FOUND              GTFPRINT FOUND ?
         L     @05,0(,@05)        POINT TO NEXT
         B     LOOP               RETRY SCAN
         SPACE 1
FOUND    DS    0H
         L     @00,16(,@03)       GET ITS ENTRYPOINT
         B     EXIT               LEAVE
         SPACE 1
LOAD     DS    0H
         LR    @05,@15            SAVE BASE ADDRESS
         LOAD  EPLOC=AMDSYS0S
         LR    @15,@05            RESTORE BASE ADDRESS
EXIT     DS    0H
         N     0,MASK             ENABLE BRANCH
         ST    @00,ENTRY          PLACE ADDRESS IN
         LM    @14,@12,SAVE+12    GET MY REGISTERS
         BR    @15                TRY ENTRYPOINT AGAIN
         SPACE 2
         DS    0F
MASK     DC    X'00FFFFFF'        TO ENABLE BRANCH
GTFCCWPR DC    CL8'GTFCCWPR'      NAME OF GTF PRINT EDITOR
ENTRY    DS    0F
         DC    X'FF'              INDICATE - MEANS NO BRANCH
         DC    AL3(AMDSYS0A)      BRANCH ADDRESS
AMDSYS0S DC    CL8'AMDSYS0S'      MAY BE CHANGED TO GTFCCWPR
SAVE     DC    18F'0'             TEMP SAVE AEREA
         END
./ ADD NAME=CCWASM                                 12JUN82 12.33.18
//CCWASM    PROC     MAC1='SYS1.MACLIB',       **  SYSTEM MACRO LIB **
//  MAC2='SYS1.AMODGEN',                       **  ADDT'L MACRO LIB **
//  OBJ='SSU.OBJ',                             **  OBJECT DATASET   **
//  RENT=',RENT',                              **  SPECIFY RENT     **
//  MOD=GTFSIO                                 **  DEFAULT MOD NAME **
//********************************************************************
//**  A S S E M B L E    G T F C C W    T R A C E    M O D U L E S  **
//**                                                                **
//**  THIS PROCEDURE IS FOR THE ASSEMBLY OF MODULES NEEDED TO       **
//**  INSTALL GTFCCW TRACE VERSION 3.0 (LZ90030).                   **
//********************************************************************
//ASM       EXEC PGM=IFOX00,REGION=1024K,
//  PARM='DECK,NOOBJ,TERM,STMT,NUM&RENT'
//SYSPRINT  DD   SYSOUT=*
//SYSTERM   DD   SYSOUT=*
//SYSLIB    DD   DSN=&MAC1,DISP=SHR
//          DD   DSN=&MAC2,DISP=SHR
//SYSUT1    DD   UNIT=VIO,SPACE=(1024,(1000,2000)),DSN=&&ASM-UT1
//SYSUT2    DD   UNIT=VIO,SPACE=(1024,(1000,2000)),DSN=&&ASM-UT1
//SYSUT3    DD   UNIT=VIO,SPACE=(1024,(1000,2000)),DSN=&&ASM-UT1
//SYSPUNCH  DD   DSN=&OBJ(&MOD),DISP=SHR
//SYSIN     DD   DSN=SSU.ASM(&MOD),DISP=SHR
//   PEND
//*
//AMDSYS0A  EXEC CCWASM,MOD=AMDSYS0A,RENT=',BUF(MAX)'
//GTFCCW    EXEC CCWASM,MOD=GTFCCW,RENT=',BUF(MAX)'
//GTFSIO    EXEC CCWASM,MOD=GTFSIO,RENT=',BUF(MAX)'
//GTFCCWPR  EXEC CCWASM,MOD=GTFCCWPR,RENT=',BUF(MAX)'
//GTFPRINT  EXEC CCWASM,MOD=GTFPRINT,RENT=',RENT,BUF(MAX)'
./ ADD NAME=CCWDOC                                 12JUN82 12.33.18
1
  *******************************************************************
  *                                                                 *
  *   FOR VERSION 3.0 FIXES, REFER TO RETAIN 370 WITH FOLLOWING     *
  *   SELECTION.   N;4/V1/F MSC      P;CCWTRACE  R030               *
  *                                                                 *
  *******************************************************************

  *******************************************************************
  *                                                                 *
  *       CCCC  CCCC  W   W  TTTTT  RRRR      A     CCCC  EEEE      *
  *      C     C      W   W    T    R   R    A A   C      E         *
  *      C     C      W W W    T    RRRR    A   A  C      EEEE      *
  *      C     C      W W W    T    R  R    AAAAA  C      E         *
  *       CCCC  CCCC   W W     T    R   R   A   A   CCCC  EEEE      *
  *                                                                 *
  *                                                                 *
  *       THESE UNOFFICIAL MODIFICATIONS PROVIDE THE IBM            *
  *       REPRESENTATIVE WITH THE POSSIBILITY TO TRACE CCW'S AND    *
  *       THEIR ASSOCIATED DATA IN ADDITION TO THE STANDARD GTF     *
  *       INFORMATION.  IT IS INTENDED AS A DEBUGGING TOOL FOR      *
  *       USE BY IBM REPRESENTATIVES.                               *
  *                                                                 *
  *         - UP TO 512 CCW'S FOR EACH EVENT                        *
  *         - UP TO 32767 BYTES OF DATA FOR EACH CCW                *
  *         - TRACE OF THE IOSB ON REQUEST OR IN CASE OF AN ERROR   *
  *         - TRACE OF THE EWA ON REQUEST OR IN CASE OF AN ERROR    *
  *                                                                 *
  *******************************************************************

        THIS TAPE CONTAINS -
           VERSION 3.0  (MVS ONLY)       DATE IS 79-11-28

       MODIFICATIONS-
       -------------
          1). VERSION 3.0 FIXES ERRORS KNOWN IN PREVIOUS VERSIONS.
          2). HAS BECOME RELEASE SU AND FEATURE INDEPENDENT, BY USING
              SYSTEM EDIT AND TRACE MODULES AMDSYS00 (AMDSYS0S) AND
              AHLTSIO.
          3). SUPPORTS MVS RELEASE (Z020,Z030,Z037,Z038).
          4). GTFSIO USES ITS OWN FRR AND RECOVERY ROUTINE.
          5). MP PROCESSING PARM IS NEW FOR GTFPRINT.
          6). TRACE OF IOSB AND EWA IS INCLUDED FOR SIO PROCESSING
              EITHER IF EWA IS PRESENT, ANY ERROR OCCURED, OR BY
              REQUEST.
          7). WILL RETRY IN CASE OF ERROR. (WORK AREA CONTAINING PSW
              AND REGS WILL BE WRITTEN TO THE TRACE DATASET AND TRACE
              IS AVAILABLE FOR NEXT ENTRY.)
          8). SOME ROUTINES HAVE BEEN ADDED TO GTFPRINT AND GTFCCW
              TO DETECT MOST OF THE INSTALLATION MISTAKES MADE WITH
              PREVIOUS VERSIONS. MESSAGES ARE PRINTED BY THESE MODULES
              IF DIFFERENT MODULE LEVELS ARE FOUND DURING EXECUTION.
          9). A SET OF SAMPLE JCL IS AVAILABLE ON FILE12 TO ASSIST
              YOU IN INSTALLATION OF GTFCCW.
1                                                               PAGE 01
-
-
      THE ORIGINAL CONCEPT AND SOME PREVIOUS VERSIONS OF CCWTRACE
      WERE PRODUCED BY MR W. BAUR OF IBM SWITZERLAND.
      VERSIONS 2.0 AND 3.0 ARE REWORKED BY MESSRS:

       F. BIEDERMANN, BO BONN, AND D. HUGHES, APSG - GERMANY.

      THIS MODIFICATION IS INOFFICIAL AND IS IN NO WAY SUPPORTED BY IBM.
      IF PROBLEMS ARE ENCOUNTERED, REFER TO THE MAINTENANCE SECTION OF
      THIS DESCRIPTION.

      THE ADDRESS PROVIDED BELOW IS TO BE USED TO REPORT PROBLEMS
      WITH AND/OR SUGGESTIONS FOR CCWTRACE AFTER OPENING AN INCIDENT.
      READ ALSO THE MAINTENANCE HINTS WITHIN THE DESCRIPTION OF
      CCWTRACE.

                    IBM GERMANY
                    GODESBERGER ALLEE 115
                    D 5300  BONN 2       DEPT. TA SOFTWARE
                    WEST GERMANY         ATTN. F.BIEDERMANN
                                         BLDG. 5300-63


      USE THE FOLLOWING JOB TO GET THE CCWTRACE DOCUMENTATION:



      //IBM001K  JOB (0,0),IBMTA,REGION=240K,TIME=1,MSGLEVEL=(1,1),
      //         NOTIFY=IBM001
      //STEP1   EXEC PGM=IEBPTPCH
      //SYSPRINT  DD    SYSOUT=A
      //SYSUT1    DD    UNIT=TAPE,LABEL=(1,NL),VOL=SER=CCWTRC,
      //          DCB=(DEN=3,LRECL=80,BLKSIZE=400,RECFM=FB),
      //          DISP=OLD,DSN=CCWTRACE
      //SYSUT2    DD    SYSOUT=A
      //SYSIN     DD    *
         PRINT PREFORM=A
      /*
1                                                               PAGE 02
  INDEX
  -----

 MODIFICATIONS TO THIS VERSION  .........................  00
 HOW TO PRINT THIS DOCUMENTATION  .......................  01
 INDEX  .................................................  02
 HOW TO INSTALL  ........................................  04
 REGRESSION PROBLEMS AFTER SU INSTALLATION OR SYSGEN ....  08
 HOW TO TRACE
   FORMAT OF THE START COMMAND  .........................  09
   KEYWORDS
       CCW  .............................................  10
       DATA  ............................................  10
       IO  ..............................................  10
       IOSB .............................................  10
       NO  ..............................................  10
       SIO  .............................................  10
       SMOD  ............................................  10
       SORT  ............................................  10
       TEST  ............................................  11
       JOB  .............................................  11
   TRACE EXAMPLE  .......................................  11
 HOW TO EDIT  ...........................................  12
   KEYWORDS
       EXIT  ............................................  13
       CPU  .............................................  13
       TEST  ............................................  13
   SAMPLE EDIT JCL  .....................................  14
   UNDERSTANDING THE OUTPUT OF CCWTRACE .................  14
 HOW TO WRITE A TRANSLATION EXIT  .......................  15
   FORMAT OF THE PARAMETER TABLE  .......................  16
   EXAMPLE OF A TRANSLATION EXIT  .......................  17
 JCL TO PRINT OR PUNCH A FILE
   PRINT THE INSTRUCTIONS  ..............................  20
   PRINT A FILE  ........................................  20
   PUNCH A FILE  ........................................  20
 MESSAGE DIRECTORY
   GTFCCW MESSAGES  .....................................  21
   GTFSIO MESSAGES  .....................................  24
   GTFPRINT MESSAGES  ...................................  24
   GTFCCWPR MESSAGES  ...................................  25
 MAINTENANCE HINTS  .....................................  26
 FORMAT AND CONTENTS OF THE DISTRIBUTION TAPE  ..........  27
-
1                                                               PAGE 03
-
         MODIFICATIONS:
         --------------
        FIVE NEW MODULES HAVE BEEN WRITTEN TO PERFORM THESE FUNCTIONS-



        GTF MODIFICATIONS ARE IN TWO MODULES -

        GTFCCW     THIS IS THE TRACE INITIALIZATION MODULE.  IT
                   SEPARATES THE CCWTRACE PARAMETERS IN THE PARM= INPUT
                   STRING FROM THE PARMS TO BE PASSED TO NORMAL GTF
                   STARTUP. WHEN PARAMETER PROCESSING IS COMPLETE,
                   CONTROL PASSES TO AHLGTF TO PERFORM THE STANDARD
                   INITIALIZATION.

        GTFSIO     THIS MODULE IS LINKED TOGETHER WITH THE STANDARD
                   MODULE AHLTSIO. BY USE OF THE LINKAGE EDITOR CHANGE
                   CONTROL STATEMENT, THE ENTRY POINTS AHLTIO AND
                   AHLTSIO1 ARE ROUTED TO THE CORRESPONDING GTFSIO
                   ENTRY POINTS. FROM THESE, GTFSIO WILL BALR TO THE
                   AHLTSIO FUNCTIONS. ON RETURN, GTFSIO PERFORMS CCW
                   AND DATA TRACING, AS FAR AS THE FUNCTIONS ARE
                   SELECTED.
                   THIS MODULE HAS ALIASES OF AHLTIO, AHLTUIO, AHLTSIO1,
                   AND AHLTSIO AND AN ENTRY STATEMENT FOR GTFSIO.



        PRINT-DUMP MODIFICATIONS ARE IN THREE MODULES

        AMDSYS00   THIS IS AN INTERFACE MODULE.  IT CHECKS TO SEE IF
                   MODULE GTFCCWPR IS IN THE LLE CHAIN.  IF SO,
                   IT PASSES CONTROL TO GTFCCWPR TO PERFORM THE EDIT
                   FUNCTION.  IF NOT, CONTROL PASSES TO AMDSYS0S,
                   THE STANDARD GTF FORMAT ROUTINE.

        GTFPRINT   THIS MODULE LOADS THE MODULES AMDSYS0S, GTFCCWPR,
                   AND THE TRANSLATION ROUTINE IF SPECIFIED. AFTER
                   PROVIDING AMDPRDMP PARMS, IT LINKS TO AMDPRDMP.

        GTFCCWPR   THIS MODULE COMPLEMENTS THE NORMAL IO AND SIO PRINT
                   MODULE AMDSYS00, THAT IS RENAMED TO AMDSYS0S.  IT
                   FORMATS THE CCWTRACE RECORDS . FOR NORMAL RECORDS IT
                   STILL USES AMDSYS0S (AMDSYS00).
1                                                               PAGE 04




          HOW TO INSTALL
          --------------
 INSTALLATION CONSISTS OF THE FOLLOWING STEPS, SOME OF WHICH ARE
 DEPENDENT ON WHETHER OR NOT YOU HAVE ONE OF THE PREVIOUS VERSIONS
 OF GTF CCWTRACE INSTALLED OR NOT. YOU MAY USE A SET OF SAMPLE JCL
 CONTAINED IN FILE12 OF THIS VOLUME. PUNCH IT OUT AND MODIFY IT
 ACCORDIG TO YOUR REQUIREMENTS. MAKE SURE, THAT PREVIOUS VERSIONS
 OF GTFCCW ARE ACCEPTED, BEFORE STARTING TO INSTALL A NEW VERSION.
 ALSO ACCEPT ANY SU, THAT MODIFIES EITHER AMDSYS00 OR AHLTSIO.

   1)  ADD A NEW PROCEDURE TO SYS1.PROCLIB
   2)  ADD GTFCCW TO THE PROGRAM PROPERTIES TABLE (PPT)
   3)  PREPARE THE LPALIB AND LINKLIB
   4)  RUN SMP TO INSTALL MODIFICATIONS
   5)  RE-IPL THE SYSTEM WITH CLPA





   1)  ADD THE NEW PROCEDURE TO SYS1.PROCLIB
       -------------------------------------

       A SAMPLE PROCEDURE IS ON FILE 5 OF THE TAPE.  THE FOLLOWING
       JOB CAN BE USED TO ADD IT TO YOUR PROCLIB.

     //ST9999TU JOB (0,1100,10,9),'W.BAUR',MSGCLASS=A
     //STEP1   EXEC PGM=IEBGENER
     //*
     //*****************************************************
     //*                                                   *
     //*   UPDATE GTFCCW PROCEDURE                         *
     //*                                                   *
     //*****************************************************
     //*
     //SYSPRINT DD SYSOUT=A
     //SYSIN    DD DUMMY
     //SYSUT2   DD DSN=SYS1.PROCLIB(GTFCCW),DISP=SHR
     //SYSUT1   DD UNIT=TAPE,LABEL=(5,NL),VOL=SER=CCWTRC,
     //         DCB=(RECFM=FB,BLKSIZE=400,LRECL=80,DEN=3),DISP=OLD

          TOGETHER WITH PROCLIB UPDATE, CHECK THE SU=XXXX PARM IN THE
          GIVEN PROCEDURE. CORRECT IT, AS EXPLAINED TOGETHER WITH THE
          PARAMETER DESCRIPTION OF GTFCCW. UNPREDICTABLE RESULTS MAY
          OCCUR, IF YOU START GTFCCW WITH WRONG SU PARM. SU=0000 IS
          CODED IN THE GIVEN PROCEDURE.

          PLEASE NOTE, THAT IT IS A RESTRICTION OF GTF, THAT IT HAS TO
          BE STARTED FROM THE CONSOLE. TIME=1440 MUST ALSO BE CODED,
          AS IT IS IN THE SAMPLE PROCEDURE.


1                                                               PAGE 05
-

   2)  ADD GTFCCW TO THE PPT
       ---------------------

       NOTE:  IF YOU ALREADY HAVE GTF CCWTRACE VERSION 2.0 OR HIGHER
              IN YOUR SYSTEM, THIS STEP CAN BE IGNORED.


       THE PPT IS IN LPALIB AS PART OF MODULE IEFSD060.  THE CSECT
       NAME IS IEFSDPPT.  THE SOURCE IS LOCATED IN SYS1.AMODGEN AS
       MEMBER SGIEF0PT.

       IT IS NECESSARY TO MAKE AN ENTRY FOR MODULE 'GTFCCW' IN
       THE PPT.  IT SHOULD HAVE THE SAME ATTRIBUTES AS AHLGTF,
       I.E., X'E800FFFF'.  IF THE PPT FOR SOME REASON CANNOT BE
       EXPANDED, THE ENTRY FOR HHLGTF OR IHLGTF ALREADY HAS THE
       CORRECT ATTRIBUTES AND CAN BE OVERWRITTEN AS GTFCCW.

       ALTERNATIVELY, MEMBER SGIEF0PT CAN BE UPDATED VIA SMP BY
       USING THE FOLLOWING SKELETON SMP STATEMENTS WITH YOUR
       OWN PROCEDURE:

          //SMPPTFIN  DD  *
          ++ PTF (LZ90000) .
          ++ UPDTE (SGIEF0PT) .
          ./ CHANGE NAME=SGIEF0PT
          ****   ASSEMBLER LANGUAGE UPDATES TO ADD GTFCCW
          ****   USE AHLGTF AS AN EXAMPLE (IT IS ALREADY THERE)
          ./ ENDUP
          //SMPCNTL  DD  *
            RECEIVE.
            APPLY S (LZ90000) .
          /*


        NOTE: THE PPT FORMAT IS DIFFERENT AS OF PTF UZ13109.
1                                                               PAGE 06
-
    3)  PREPARE THE LPALIB AND LINKLIB
        ------------------------------

        A) RENAME SOME OF THE MODULES IN THE LPALIB OR LINKLIB. EXPECT
           RETURN CODE OF IEHPROGM, IF EITHER ANY PREVIOUS VERSION WAS
           ALREADY INSTALLED OR VERSION 1.4 WAS NOT INSTALLED.

           //SCRCCW    JOB    ....  ETC  ....
           //STEP1  EXEC  PGM=IEHPROGM
           //* **************************************************
           //*
           //*  RENAME AMDSYS00 TO AMDSYS0S (IN LPALIB)
           //*  RENAME BHLGTF1 TO AHLGTF (IN LINKLIB)
           //*
           //* **************************************************
           //SYSPRINT  DD  SYSOUT=A
           //LPALIB    DD  VOL=REF=SYS1.LPALIB,DISP=OLD
           //LINKLIB   DD  VOL=REF=SYS1.LINKLIB,DISP=SHR
           //SYSIN     DD  *
            RENAME  DSNAME=SYS1.LPALIB,VOL=3350=SYSRES,          ******
                          MEMBER=AMDSYS00,NEWNAME=AMDSYS0S
            RENAME DSNAME=SYS1.LINKLIB,VOL=3350=SYSRES,          ******
                          MEMBER=BHLGTF1,NEWNAME=AHLGTF

        B) LINK AHLTSIO AS GTFSIO TO THE LPALIB, TO HAVE IT AVAILABLE
           FOR SMP.

           //STEP2  EXEC  LINKS,PARM='NCAL,LIST,XREF,AC=1,RENT,REUS',
           //       UNIT=3330,SER=SYSRES,NAME=LPALIB,P1=' ',
           //       N=SYS1,MOD=,P2=' ',OBJ=OBJPDS01,CLASS=A
           //AOS11  DD  DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.AOS11
           //SYSLIN DD  *
            CHANGE  AHLTIO(BHLTIO)
            CHANGE  AHLTSIO1(BHLTSIO1)
            INCLUDE AOS11(AHLTSIO)
            NAME GTFSIO(R)
           /*

        C) DELETE AHLTSIO FROM SYS1.PARMLIB(IEAPAKXX) (IF PRESENT) OR
           YOU WILL GET MESSAGE IEA357I AT CLPA.
1                                                               PAGE 07
-
       4)  LINK OF NEW MODULES
           -------------------

 //GRZ024   JOB (0,2),SYSPTF,TIME=9,MSGLEVEL=(1,1)
 //*  INSTALL GTFCCW WITH SMP
 //STEP1     EXEC  PGM=IEBGENER
 //SYSIN     DD  DUMMY
 //SYSPRINT  DD  SYSOUT=A
 //SYSUT1    DD  UNIT=2400-3,LABEL=(2,NL),VOL=SER=CCWTRC,
 //          DCB=(LRECL=80,BLKSIZE=400,RECFM=FB),DISP=(OLD,PASS)
 //SYSUT2    DD  UNIT=SYSDA,DSN=&&CNTLIN,DISP=(NEW,PASS),
 //          SPACE=(TRK,(5,1)),DCB=(LRECL=80,BLKSIZE=400,RECFM=FB)
 //STEP2     EXEC  PGM=IEBGENER
 //SYSIN     DD  DUMMY
 //SYSPRINT  DD  SYSOUT=A
 //SYSUT1    DD  UNIT=2400-3,LABEL=(3,NL),VOL=SER=CCWTRC,
 //          DCB=(LRECL=80,BLKSIZE=400,RECFM=FB),DISP=(OLD,PASS)
 //SYSUT2    DD  UNIT=SYSDA,DSN=&&JCLIN,DISP=(NEW,PASS),
 //          SPACE=(TRK,(5,1)),DCB=(LRECL=80,BLKSIZE=400,RECFM=FB)
 //SMP  EXEC SMP,PARM.SMP='LKED=IEWL,SIZE=(800K,120K),DATE=U'
 //SMP.SMPCNTL DD DSN=&&CNTLIN,DISP=(OLD,DELETE)
 //SMP.SMPJCLIN DD DSN=&&JCLIN,DISP=(OLD,DELETE)
 //SMP.SMPPTFIN DD UNIT=2400-3,LABEL=(4,NL),VOL=SER=CCWTRC,
 //           DCB=(LRECL=80,BLKSIZE=400,RECFM=FB)


       SMP WILL OPEN MORE THAN ONE DATASET AT A TIME, SO IT IS
       NECESSARY TO COPY THE CNTLIN AND JCLIN DATASETS FROM TAPE TO
       DISK BEFORE DOING AN SMP UPDATE.
       (STEPS 1 AND 2 IN THE ABOVE EXAMPLE.)
-
-
   6)  RE-IPL THE SYSTEM WITH CLPA
       ---------------------------

       GTFCCW IS READY FOR USE.


1                                                               PAGE 08
-
  SOME REGRESSION PROBLEMS. -
  ---------------------------

        SOME OR ALL OF THE MODULES INVOLVED MAY BE REGRESSED AFTER
        A NEW SYSGEN OR BY A SU INSTALLATION. MODULES AFFECTED
        ARE EITHER AMDSYS00 FOR AMDPRDMP OR AHLTSIO FOR AHLTGTF.
        REGRESSION MAY ALSO BE FOR THE PPT BY A NEW SYSGEN.
        ALSO THE SMPCDS MAY BE REGRESSED BY A JCLIN RUN WITH
        ANY SU OR PTF.
         A PTF INSTALLATION THAT REPLACES ANY OF THE INVOLVED
        MODULES (AMDSYS00 OR AHLTSIO) IS NOT A REGRESSION,
        EXCEPT THEY REQUIRE A JCLIN. FOR ALL THE FOLLOWING ACTIONS,
        IT IS RECOMMENDED, THAT GTFCCW HAS BEEN ACCEPTED BEFORE.

 1.) THE SMPCDS IS REGRESSED AFTER A JCLIN FOR A PTF OR A SU.
        GET THE CNTLIN DATASET USED IN THE INSTALLATION STEPS.
        REMOVE THE APPLY STATEMENT.
        RUN A UCLIN AND A JCLIN AS IN THE INSTALLATION PROCEDURE.
        THIS MAY BE DONE AT ANY TIME WITHOUT PROBLEMS.

 2.) ANY OF THE MODULES ARE REGRESSED (AHLTSIO, AMDSYS00).
        THIS MAY BE AFTER A NEW SYSGEN OR A SU INSTALLATION .

        GET THE JCLIN DATASET USED IN THE INSTALLATION STEPS.
        MODIFY IT TO THE REQUIREMENTS OF YOUR INSTALLATION.
        RUN IT TO RELINK ALL MODULES AFFECTED.
        THIS MAY BE DONE AT ANY TIME WITHOUT PROBLEMS.

        AFTER THE LINK STEP, CHECK, IF MODULE AMDSYS00 IS PRESENT
        IN THE LPALIB. IF YES, SCRATCH IT. YOU HAVE JUST LINKED IT
        AS AMDSYS0S BY THE LINK STEP ABOVE.

 3.) CHECK IF THE PPT HAS BEEN REGRESSED. THAT MAY BE POSSIBLE
        AFTER A SYSGEN IF THE PPT WAS ZAPPED INSTEAD OF UPDATED.
        THE QUICKEST WAY, TO CHECK THIS, IS TO START GTFCCW.
        IF THE PPT ENTRY IS NOT AVAILABLE, YOU WILL GET MESSAGE T15.
        IF REQUIRED, REPEAT THE CORRESPONDING INSTALLATION STEP ABOVE.

 4.) CHECK, WETHER THE SU=XXXX PARAMETER IS STILL VALID IN YOUR
        START PROCEDURE. INVALID SU SPECIFICATION MAY GIVE
        UNPREDICTABLE RESULTS. REFER TO THE TEXT FOR PARAMETER
        'SUDATA' FOR GTFCCW IN THIS DESCRIPTION.

 5.) DO A CLPA BEFORE USING GTFCCW.
1                                                               PAGE 09
-
         HOW TO TRACE
         ------------
              THE GTFCCW TRACE IS STARTED IN THE SAME WAY AS THE
              STANDARD GTF (SEE GC28-0674).
              HOWEVER YOU MAY SELECT SOME ADDITIONAL OPTIONS WHICH
              BELONG ONLY TO THE CCW TRACE.


         GENERAL FORMAT OF THE START COMMAND
         -----------------------------------
-
                  ×- -× ×-    -× ×-    -× ×-  -× ×-            -×
        S PROCNAME×.ID×,×DEVADR×,×VOLSER×,×PARM×,×KEYWORD=OPTION×,...
                  ×- -× ×-    -× ×-    -× ×-  -× ×-            -×
-
             FOR PROCNAME, ID, DEVADDR, VOLSER AND THE KEYWORDS TIME,
             DEBUG, AND MODE SEE STANDARD GTF (GC28-0674).
-
1                                                               PAGE 10

        THE FOLLOWING ARE THE KEYWORDS WHICH BELONG TO CCWTRACE ONLY
        ------------------------------------------------------------

        CCW=NNN                    DEFAULT=50
           =NO
                    NNN= INTEGER BETWEEN 1 AND 512
                         THIS IS THE MAXIMUM NUMBER OF CCW'S TO BE
                         TRACED FOR ONE EVENT.

        DATA=NNNNN                 DEFAULT=24
            =NO
                  NNNNN= INTEGER BETWEEN 1 AND 32767
                         IF THE COUNT IN THE CCW IS GREATER THAN NNNNN,
                         THE FIRST HALF OF THE TRACED DATA IS FROM THE
                         START OF THE BUFFER AND THE SECOND HALF IS FROM
                         THE END OF THE BUFFER.

        IO=YES                     DEFAULT=YES
          =NO
        IO               IF 'YES' IS SPECIFIED OR DEFAULTED TO, I/O
                         INTERRUPTS WILL BE TRACED.
                         IF YOU CODE IO=NO, NO TRACING WILL TAKE PLACE
                         FOR I/O INTERRUPTS.

        IOSB=YES                   DEFAULT=NO
            =NO
        IOSB             THIS PARAMETER RESULTS IN TRACING OF IOSB
                         AND EWA, AS FAR AS AVAILABLE. HOWEVER, THIS
                         IS ONLY PERFORMED, IF THE SIO FUNCTION IS
                         ALSO ACTIVE. IF THIS PARAMETER IS OMITTED OR
                         IOSB=NO IS CODED, IOSB AND EWA TRACING IS
                         ONLY PERFORMED AS A RESULT OF AN GTFSIO ERROR.

        NO               THIS IS A DUMMY PARAMETER, THAT IS IGNORED BY
                         GTFCCW. IT MAY BE USEFUL IF YOU WORK WITH THE
                         SHORT FORM OF THE PARMS, AND YOU WANT TO
                         OVERWRITE THE KEYWORDS IN A PROCEDURE. NOTE
                         THAT PARM SIZE IS LIMITED TO 100.

        SIO=YES                    DEFAULT=NO
           =NO
        SIO              IF SIO=YES IS SPECIFIED, CCW (AND DATA)
                         TRACING WILL BE PERFORMED FOR SIO EVENTS.
                         IF YOU CODE SIO=NO, NO CCW TRACING WILL
                         TAKE PLACE FOR SIO'S.

        SMOD=YES                   DEFAULT=NO
            =NO
        SMOD             THE SMOD PARAMETER SPECIFIES WHAT ACTION IS
                         TO BE TAKEN WHEN STATUS MODIFIER IS PRESENT.
                         IF SMOD=NO IS SPECIFIED, THE TRACE ROUTINE
                         WILL IGNORE THE POSSIBILITY OF AN EXTENDED
                         CCW CHAIN.  IF SMOD=YES, THEN ALL TIC'S WILL
                         BE CHECKED FOR THE PRESENCE OF ANOTHER
                         CCW CHAIN AT THE TIC ADDRESS +8.  USE OF
                         SMOD=YES LEADS TO THE POSSIBILITY THAT NOT
                         ALL CCW'S SHOWN IN THE TRACE OUTPUT HAVE
                         ACTUALLY BEEN EXECUTED.
1                                                              PAGE 11
-
-
        SORT=YES                   DEFAULT=NO
            =NO
        SORT             IF YOU CODE SORT=YES THE CCW'S ARE SORTED
                         IN ASCENDING ADDRESS SEQUENCE.
                         IF YOU CODE SORT=NO, THE CCW'S ARE PRINTED IN
                         EXECUTION SEQUENCE.

        SUDATA=0000                DEFAULT=0000
        SU=
                         THIS PARAMETER IS USED TO CONTROL THE INTERNAL
                         FLOW OF GTFSIO. IF CODED WRONG, THE RESULT MAY
                         BE UNPREDICTABLE. CODING DEPENDS ON THE SU OR
                         FEATURE LEVEL OF YOUR INSTALLATION.
                         IF SU50, SU65 (MVS-SE), OR ONE OF THE
                         CORRESPONDING FEATURES IS INSTALLED, SU=8000
                         MUST BE CODED. OTHERWISE OMIT THE OPERAND
                         OR CODE SU=0000.
                         IF CODED, CODE ALWAYS 4 HEX DIGITS OR YOU
                         GET A PARM ERROR.

                         SPECIFY SU=8000 FOR SYSTEMS HAVING    @LZ90330
                         MVS/SP1.1 INSTALLED                   @LZ90330
                         SPECIFY SU=8080 FOR SYSTEMS HAVING    @LZ90330
                         MVS/SP1.2 INSTALLED                   @LZ90330
-
       TEST=YES                    DEFAULT=NO
           =NO
       TEST              IF YOU CODE TEST=YES, GTFCCW WILL TERMINATE
                         WITH MESSAGE T09 FOLLOWED BY AN ABEND U122,
                         INSTEAD OF NORMAL TERMINATION OF GTFCCW.
                         SO YOU HAVE THE POSSIBILITY TO GET GTFCCW AND
                         GTFSIO DUMPED TO HELP WITH THE ANALYSIS OF
                         SOME PROBLEMS.

       JOBNAME=NNNNNNNN            DEFAULT=NO
              =NO
       JOB=              IF YOU CODE JOB=JOBNAME ONLY THOSE JOBS HAVING
                         THE SPECIFIED NAME WILL HAVE THEIR CCW'S
                         TRACED. THE JOB PARAMETER HAS NO EFFECT ON
                         STANDARD GTF TRACING. IF JOB=NO, ALL ACTIVITY
                         ON THE SELECTED UNITS WILL HAVE CCW TRACING
                         PERFORMED.
1                                                              PAGE 12



           TRACE EXAMPLE
          ---------------

       S GTFCCW.TRACE,280,GTFTRC,,LABEL=(,NL),CCW=20,DATA=60,SORT=YES

          CCWTRACE I00.  CCWTRACE IS STARTING.

         00 AHL100A SPECIFY TRACE OPTIONS
        R 00,TRACE=IOP,SIOP,PCI

         01 AHL101A SPECIFY TRACE EVENT KEYWORDS-IO=
        R 01,IO=(150,151,152),END
        ***  OR    R 01,IO=SIO=(150,151,152),END

          AHL103I TRACE OPTIONS SELECTED -- IO
          AHL103I IO=(150,151,152)

          02 AHL125A RESPECIFY TRACE OPTIONS OR REPLY U
        R 02,U
1                                                               PAGE 13
-
      HOW TO EDIT-       ( SEE JCL EXAMPLE BELOW )
      ------------
           THE EDIT FUNCTION IS PERFORMED IN A WAY SIMILAR TO
           THE STANDARD GTF PROCEDURE.  AN ADDITIONAL PARAMETER
           MAY BE SPECIFIED WHILE USING THE CCWTRACE EDIT MODULE.

      NOTE -
      ----      1. TAPES PRODUCED UNDER PREVIOUS VERSIONS OF CCWTRACE
                   CANNOT BE EDITED WITH THIS VERSION.
                2. THE JCL IN THE EXAMPLE CAN BE USED FOR EDITING DUMPS
                   AS WELL. IF ANY GTF CCWTRACE RECORDS ARE PRESENT,
                   THEY WILL BE FORMATTED PROPERLY, IF SELECTED.

     EDIT PARAMETERS FOR CCWTRACE
     ----------------------------

     IN ADDITION TO THE STANDARD AMDPRDMP PARAMETERS, THE FOLLOWING
     PARAMETER MAY ALSO BE SPECIFIED.

      EXIT=NAME            THIS IS THE NAME OF A TRANSLATION EXIT.
                           THE EXIT RECEIVES CONTROL JUST BEFORE THE
                           DATA ON A PRINT-LINE ARE TRANSLATED TO
                           EBCDIC. THIS ALLOWS FOR THE USE OF A
                           DIFFERENT TRANSLATION TABLE OR FOR PRINT
                           LINE FILTERING.
                           FOR DETAILS SEE-
                              ' HOW TO WRITE A TRANSLATION EXIT '

      CPU=NUMBER                    DEFAULT=0

                           THIS PARM IS USED FOR PROCESSING
                           MP OUTPUT. IF NOT SPECIFIED, SELECTED
                           OUTPUT OF CPU 0 WILL BE PRINTED.

                           IF YOU NEED THE TRACE OUTPUT OF ALL CPUS,
                           YOU HAVE TO RUN GTFPRINT FOR EACH CPU YOU
                           NEED.

                           NOTE: GTF, ONCE STARTED ON AN MP, IS ACTIVE
                           FOR ALL CPUS.

      TEST                 IF THIS PARM IS SPECIFIED, GTFCCWPR OR
                           GTFPRINT WILL ABEND WITH U122 IF ANY
                           UNEXPECTED SITUATION OCCURS.
                           IT MAY BE USED TO GET A DUMP IN CASE OF
                           UNEXPECTED INPUT FROM THE TRACE DATASET
                           OR TO DEBUG GTFSIO, GTFCCWPR, OR GTFPRINT.
1                                                               PAGE 14
-
           SAMPLE EDIT JCL
           ---------------
-
 //ST9999TL JOB (0,1100,9,9),'W.BAUR',MSGLEVEL=(1,1),
 //             MSGCLASS=X
 //EDIT EXEC  PGM=GTFPRINT,PARM='CPU=0'
 //SYSPRINT DD SYSOUT=A
 //PRINTER DD SYSOUT=A
 //TAPE DD UNIT=TAPE,LABEL=(1,NL),VOL=SER=CCWTRC,DISP=OLD,DSN=GTFCCW
 //SYSUT1 DD UNIT=3330,SPACE=(2052,(50,10))
 //SYSIN DD *
  EDIT DDNAME=TAPE,SIO,IO
    END
-
           UNDERSTANDING THE OUTPUT OF CCWTRACE
           ------------------------------------
0
  THIS ITEM WILL PROVIDE THE FIRST TIME USER OF CCWTRACE WITH
  THE ABILITY TO UNDERSTAND THE OUTPUT DEVELOPED BY CCWTRACE.

  THE FIRST TWO LINES OF A SIO OR I/O INTERRUPT TRACE WILL BE EXACTLY
  AS DOCUMENTED IN MVS DEBUGGING HANDBOOK VOLUME 1, GC28-0708.

  THE CCWTRACE RECORDS ARE CONSTRUCTED AS AN ADD-ON TO THE
  NORMAL GTF RECORDS AND WILL APPEAR AS FOLLOWS:

   CCWADDRESS(1) ACTUAL CCWS(2) DATA(3)

    (1) THE ADDRESS IS THE VIRTUAL LOCATION OF THE 'REAL' CCW.
    (2) THE ACTUAL CCW WILL CONTAIN THE 'REAL' DATA ADDRESS.
    (3) THE DATA DISPLAYED WILL BE THE DATA ADDRESSED BY THE
        CCW. IF THE DATA LENGTH REFERENCED BY THE CCW IS
        GTREATER THAN THE LENGTH REQUESTED BY THE TRACE (DEFAULT
        IS DECIMAL 24 BYTES), THEN THE FIRST AND LAST PORTIONS
        OF THE DATA ARE TRACED. IE: IF THE CCW COUNT IS HEX 18 (24)
        OR LESS, THE ENTIRE DATA WILL BE DISPLAYED. IF THE DATA
        EXCEEEDS HEX 18, THEN THE FIRST AND LAST HEX C BYTES WILL
        BE DISPLAYED IF THE DEFAULT LENGTH IS USED.

     'READ' COMMANDS WILL NOT HAVE DATA TRACED ON AN SIO TRACE.
     ALL DATA TRANSFER COMMANDS WILL HAVE DATA TRACED ON
     I/O INTERRUPT TRACES (IF THE DATA ADDRESS PASSES THE
     VIRTUAL/REAL CONVERSION.
1                                                               PAGE 15
-
     HOW TO WRITE A TRANSLATION EXIT
     -------------------------------
-
          GTF-CCW-TRACE TRANSLATES THE DATA ACCORDING TO THE
          DEVICE-TYPE.  IT IS POSSIBLE THAT THE TRANSLATION IS NOT
          CORRECT FOR SOME DEVICES.  THIS OPTION ALLOWS FOR
          DATA TRANSLATION USING A DIFFERENT TRANSLATE TABLE.
-
          REGISTER-CONTENTS AT ENTRY TO THE EXIT
            REG 1 POINTS TO A PARAMETER-TABLE
            REG 15 IS THE ENTRY POINT ADDRESS OF THE EXIT
            THE PARAMETER-TABLE CONTAINS THE ADDRESS OF THE WRITE
            ROUTINE.
-
          THE FOLLOWING ARE THE VALID PRINT CONTROL CHARACTERS -
            1     =      SKIP TO CHANNEL 1 AND PRINT
            -     =      SPACE 3 AND PRINT
            0     =      SPACE 2 AND PRINT
            ANY OTHER =  SPACE 1 AND PRINT
-
          WHEN CALLING THE WRITE ROUTINE -
            REG 1  POINTS TO THE OUTPUT BUFFER.  LENGTH IS 121 BYTES.
                   THE FIRST BYTE CONTAINS THE PRINT CONTROL CHARACTER.
            REG 15 MUST BE THE ENTRY POINT ADDRESS OF THE WRITE ROUTINE.
-                  ---
          UPON RETURN -
            REG 14  MUST CONTAIN THE RETURN ADDRESS
            REG 15  MUST CONTAIN A RETURN-CODE BETWEEN 0 AND 8
-
          RETURNCODE MEANING -
             0   =    CONTINUE WITH NORMAL PROCESSING
             4   =    PRINT THE LINE WITHOUT TRANSLATING IT
             8   =    DO NOT PRINT THIS LINE
-
1                                                               PAGE 16
-
          FORMAT OF THE PARAMETER-TABLE
          -----------------------------
-
       0   *************************************************
           *                                               *
           *     ADDR OF THE WRITE ROUTINE             L=4 *
           *                                               *
       4   *************************************************
           *                     *            *            *
           * DEVICE ADDRESS  L=2 * DEVTYP L=1 * BLANK  L=1 *
           *                     * NOTE-1     *            *
           *************************************************
           ******   OUTPUT BUFFER STARTS HERE **************
       8   *************************************************
           *               BLANK                       L=9 *
      17   *************************************************
           * OUTPUT AEREA FOR CCW ADDRESS L=6 * BLANK  L=2 *
      25   *************************************************
           * FIRST PART OF EDITED CCW     L=8 * BLANK  L=1 *
      34   *************************************************
           * SECOND PART OF EDITED CCW    L=8 * BLANK  L=2 *
      44   *************************************************
           *                                               *
           *     DATA OR BLANK                        L=58 *
           *                                               *
           *                                    ************
           *                                    *          *
           *                                    * C'*' L=1 *
           *                                    *          *
     103   *************************************************
           *                                               *
           *     UNTRANSLATED DATA                    L=24 *
           *                                               *
           *                                    ************
           *                                    *          *
           *                                    * C'*' L=1 *
           *                                    *          *
           *************************************************
0
     NOTE 1
     ------

           THE DEVICE-TYPE FOR TP-DEVICES IS TRACED TO ALLOW
           A CORRECT TRANSLATION.
           THIS BYTE IS A COMBINATION OF
           BITS 4-7 OF UCBTYP-FIELD BYTE 1    AND
           BITS 0-3 OF UCBTYP-FIELD BYTE 4
           THE FORMAT IS   XY
             X   =     BITS 4-7 OF UCBTYP-FIELD BYTE 1
             Y   =     BITS 0-3 OF UCBTYP-FIELD BYTE 4
             FOR NON-TP-DEVICES THIS FIELD CONTAINS X'FF'


1                                                               PAGE 17
     EXAMPLE OF A TRANSLATION EXIT
     -----------------------------
-
 //ST9999TA JOB (0,1000,2,2),'W.BAUR',MSGLEVEL=(1,1)
 //ASM EXEC ASMFCL
 //SYSIN DD  *
 GTFEXIT  CSECT
          LR    10,15             BASE
          LR    11,1              PARMBASE
          USING GTFEXIT,10
          USING PARM,11
          ST    14,RETSAVE        SAVE REGISTER 14
-
 *
 *       TRANSLATE DATA FOR DEVICE 062 WITH A SPECIAL TRANSLATE-TABLE
 *
-
          CLC    DEVADR,=X'0062'  IS THIS THE DEV WITH SPECIAL NEEDS
          BNE    TEST1            NO SEE FOR OTHER SPECIAL ACTIONS
          TR     EBCDIC,TRTAB
          LA     15,4             IND NO TRANSLATE NEEDED
          B      RETURN
-
 *
 *        TRANSLATE CONTROL-CHARACTERS OF A 2740 TO PRINTABLE CHARACTERS
 *        AND PRINT THEM JUST ABOVE THE EBCDIC LINE IN TRACE
 *
-
 TEST1    EQU    *
          CLI    DEVTYPE,X'41'    IS IT A 2740
          BNE    TEST2        NO
          MVC    OUTPUT,EBCDIC    MOVE DATA
          TR     OUTPUT,TAB2740   TR CONTROL CHARACTERS TO PRINTABLE
          LA     1,OUTPUTL        POINT TO BUFFER
          MVI    OUTPUTL,C'0'     SET SPACE=2
          L      15,WRITADR       ADDRESS OF WRITE ROUTINE
          BALR   14,15            PRINT THIS LINE
          SR     15,15            RETURN (TRANSLATE AND PRINT)
          B      RETURN
-
1                                                               PAGE 18
-
 *
 *        EDIT ONLY SENSE-CCW'S FOR DEVICE 060 (NOTE THAT ONLY THESE
 *        SENSE CCW'S ARE TRACED FOR WHICH A INTERRUPT OCCURS
 *        I.E FOR TP DEVICES)
 *
 TEST2    EQU     *
          SR     15,15            SET RETURNCODE TO 0
          CLC    DEVADR,=X'0060'  LINE 60
          BNE    RETURN           RETURN TRANSLATE AND PRINT
          CLC    CCW(2),=C'04'    IS IT A SENSE CCW
          BE     RETURN           YES PRINT IT
          LA     15,8             SET NO PRINT RETURNCODE
          B      RETURN
-
 RETURN   EQU     *
          L      14,RETSAVE       RESTORE REGISTER 14
          BR     14
          EJECT
-
 RETSAVE  DS    1F         SAVE AREA FOR RETURNREGISTER (RETURN MUST BE
 *                         MADE WITH REGISTER 14.
 TRTAB    DS    0C         TABLE TO TRANSLATE DATA OF 062 TO EBCDIC
 TAB2740  DS    0C         TABLE WHICH TRANSLATS 2740 CONTROL-CHARACTER
 *                         TO PRINTABLE CHARACTERS.
 *                         I.E.           E=EOB
 *                                        N=NEWLINE
 *                                        C=CIRCLE-C
 *                                        A.S.O.
 OUTPUTL  DS    0CL121    PRINTBUFFER
          DC    C' '       FIRST POSITION IS THE CARRIAGE-CONTROL-CHAR.
 *                           C'-'      =    SPACE 3 AND WRITE
 *                           C'0'      =    SPACE 2 AND WRITE
 *                           C'1'      =    SKIP TO CHANNEL 1 AND WRITE
 *                           ANY OTHER =    SPACE 1 AND WRITE
 *
 OUTPUTD  DC    96C' '    UNUSED OUTPUT DATA AREA
 OUTPUT   DC    24C' '    OUTPUT AREA FOR TRANSLATED CONTROL CHARACTERS
 *                        THIS LOCATION CORRESPONDS TO POSITION OF THE
 *                        EBCDIC DATA OF THE TRACE
-
1                                                               PAGE 19
-
 PARM     DSECT
 *
 *
 *          DSECT FOR THE PARAMETERS PASSED FROM TRACE-PROGRAM
 *          REGISTER 1 POINTS TO THIS DSECT
 *
 *
 WRITADR  DS    F        ADDRESS OF THE PRINT ROUTINE
 DEVADR   DS    H        ADDRESS OF THE DEVICE BEING EDITED
 DEVTYPE  DS    C        TRACED DEVICETYPE FOR TP-DEVICES. IT IS A
 *                       COMBINATION OF BITS 4-7 OF UCBTYP-FIELD BYTE 1
 *                       AND OF THE BITS 0-3 OF THE UCBTYP-FIELD BYTE4
 *                       THE FORMAT IS    XY
 *                          X  =  BITS 4-7 OF UCBTYP-FIELD BYTE 1
 *                          Y  =  BITS 0-3 OF UCBTYP-FIELD BYTE 4
          DS    C' '     BLANK TO CLEAR CCWBUFF
 CCWBUFF  DS    0CL120   TRACE PRINT BUFFER. IT CONTAINS A COMPLETE
 *                       EDITED LINE
          DS    CL10     FILLER
 CCWADDR  DS    CL6      EDITED ADDRESS OF CCW (OR BLANK)
          DS    CL2      FILLER
 CCW      DS    CL16     EDITED CCW (OR BLANK)
          DS    CL2      FILLER
 DATA     DS    CL57     CCW-DATA (VARIABLE)
          DS    CL1      FILLER
          DS    CL1      C'*'
 EBCDIC   DS    CL24     DATA IN FORM AS THEY WERE IN THE BUFFER
*                        (UNTRANSLATED)
          DS    CL1       C'*'
    END
 /*
 //LKED.SYSLMOD DD DSN=ST9999T.PROG.LOAD(GTFEXIT),DISP=SHR
 //
-
1                                                               PAGE 20
-
     JCL EXAMPLES TO PRINT OR PUNCH FILES FROM THE DISTRIBUTION-TAPE
     ---------------------------------------------------------------

     TO PRINT THE TRACE INSTRUCTIONS

 //SJ9999TH JOB (0,1100,10,10,1100),'W.BAUR',MSGLEVEL=(1,1)
 //PRINT EXEC PGM=IEBPTPCH
 //SYSPRINT DD SYSOUT=A
 //SYSUT1 DD UNIT=TAPE,LABEL=(1,NL),VOL=SER=CCWTRC,
 //  DCB=(RECFM=FB,BLKSIZE=400,LRECL=80,DEN=3),DISP=OLD
 //SYSUT2 DD SYSOUT=A
 //SYSIN DD *
    PRINT PREFORM=A
 /*
-
     TO PRINT ANY OTHER FILE

 //SJ9999TH JOB (0,1100,10,10,1100),'W.BAUR',MSGLEVEL=(1,1)
 //PRINT EXEC PGM=IEBPTPCH
 //SYSPRINT DD SYSOUT=A
 //SYSUT1 DD UNIT=TAPE,LABEL=(X,NL),VOL=SER=CCWTRC,
 //  DCB=(RECFM=FB,BLKSIZE=400,LRECL=80,DEN=3),DISP=OLD
 //SYSUT2 DD SYSOUT=A
 //SYSIN DD *
     PRINT MAXFLDS=1
     RECORD FIELD=(80)
 /*
-
     TO PUNCH A FILE

 //SJ9999TH JOB (0,1100,10,10,1100),'W.BAUR',MSGLEVEL=(1,1)
 //PRINT EXEC PGM=IEBPTPCH
 //SYSPRINT DD SYSOUT=A
 //SYSUT1 DD UNIT=TAPE,LABEL=(X,NL),VOL=SER=CCWTRC,
 //  DCB=(RECFM=FB,BLKSIZE=400,LRECL=80,DEN=3),DISP=OLD
 //SYSUT2 DD SYSOUT=B
 //SYSIN DD *
     PUNCH MAXFLDS=1
     RECORD FIELD=(80)
 /*
-
1                                                               PAGE 21
   MESSAGE DIRECTORY
   -----------------

     GTFCCW MESSAGES
     ---------------


   THE FOLLOWING MESSAGES ARE CONTAINED IN AND ISSUED BY MODULE
   GTFCCW, WHICH IS THE INTERFACE TO GTF FOR THE CCWTRACE FUNCTION.

      GENERAL FORMAT
      ------- ------
   CCWTRACE MXX.  ' TEXT '

      WHERE -
      M = T   MEANS THAT CCWTRACE WILL TERMINATE AFTER
              ISSUING THE MESSAGE.
      M = I   MEANS THAT THE MESSAGE IS INFORMATIONAL AND CCWTRACE
              PROCESSING WILL CONTINUE.
      XX      IS A DECIMAL MESSAGE NUMBER.

      DIAGNOSTIC BYTES
      ----------------
   SOME MESSAGE TEXTS WILL ALSO CONTAIN 'DIAGNOSTIC BYTES'. THESE BYTES
   ARE FOR PROBLEM DETERMINATION AND THE BITS WITHIN IT INDICATE
   WHICH PARAMETER CAUSED THE ERROR BEING REPORTED.  BIT ASSIGNMENTS
   ARE AS FOLLOWS -

     8000   IO   PARAMETER
     4000   CCW  PARAMETER
     2000   DATA PARAMETER
     1000   JOB  PARAMETER
     0800   SIO  PARAMETER
     0400   SMOD PARAMETER
     0200   SORT PARAMETER
     0100   TEST PARAMETER
     0080   IOSB PARAMETER
     0040   NON  PARAMETER RELATED ERRORS.
            EG.',,' CODED, OR PARM STARTS OR ENDS WITH ','.
     0001   SU   PARAMETER
1                                                               PAGE 22



     GTFCCW MESSAGES  (CONTINUED)
     ----------------------------


   I00.  CCWTRACE IS STARTING.

   T01.  CCWTRACE IS ALREADY ACTIVE.
         GTFCCW WAS STARTED PREVIOUSLY AND HAS NOT COMPLETED
         TRACING.  THIS IS CHECKED VIA AN EXCLUSIVE ENQ FOR
         QNAME = CCWTRACE  AND  RNAME = INITIALIZATION.

   I02.  PARM OUT OF RANGE.  MAX ASSUMED.  DIAGNOSTIC BYTES = XXXX.
         THIS MESSAGE IS ISSUED WHEN THE VALUE SPECIFIED FOR 'CCW' WAS
         GREATER THAN 255 OR WHEN THE 'DATA' VALUE SPECIFIED WAS GREATER
         THAN 32767.  BITS IN THE DIAGNOSTIC BYTES ARE AS DESCRIBED
         ABOVE.  THE VALUES ASSUMED FOR THE INCORRECT PARAMETER ARE
         AS FOLLOWS -   CCW = 255    AND/OR   DATA = 32767.

   I03.  MULTIPLE PARM SPECIFICATION. LAST USED. DIAGNOSTIC BYTES = XXXX
         A PARAMETER WAS SPECIFIED MORE THAN ONCE IN THE PARM= INPUT.
         THE VALUE GIVEN IN THE LAST SPECIFICATION IS THE ONE IN EFFECT.
         THE BITS IN THE DIAGNOSTIC BYTES ARE DESCRIBED ABOVE.

   T04.  PARAMETER ERROR(S).  DIAGNOSTIC BYTES = XXXX.
         THE PARM= INPUT STRING CONTAINED A CCWTRACE PARAMETER WHOSE
         SPECIFIED VALUE IS INCORRECT.  THIS CAN BE DUE TO MIS-
         SPELLINGS, MISSING COMMAS, INCORRECT VALUES, ETC.  IF PROBLEMS
         IN THIS AREA PERSIST, SPECIFY TEST=YES AS THE FIRST KEYWORD
         AND A DUMP WILL BE TAKEN AFTER ISSUING THE MESSAGE.  BIT
         MEANINGS FOR THE DIAGNOSTIC BYTES ARE AS ABOVE.

   T05.  TRACE PROGRAM COULD NOT BE FIXED.  TYPE = X.
         A RETURN CODE OF 4 OR GREATER WAS RETURNED FROM AN ATTEMPT TO
         FIX A PAGE.  IF TYPE = S, IT WAS A SHORT TERM FIX OF THE
         PARAMETER LIST TO BE USED FOR THE LONG TERM FIX REQUEST WHICH
         FAILED.  IF TYPE = L, IT WAS THE LONG TERM FIX OF THE TRACE
         ROUTINE ITSELF WHICH COULD NOT BE SATISFIED.  IF TYPE = F, THE
         FREE OF THE SHORT TERM FIX FOR THE PARAMETER LIST FAILED.
         IF THE PROBLEM PERSISTS, SPECIFY TEST=YES TO GET A DUMP.  CHECK
         THE RANGE BEING SPECIFIED.  A FAILURE TO SATISFY A LONG TERM
         FIX REQUEST MAY INDICATE THE NEED TO RE-IPL WITH CLPA.

   T06.  TRACE PROGRAM COULD NOT BE FREED.  TYPE = X.
         IF TYPE = F, AN ATTEMPT WAS MADE TO FIX THE PAGEFREE PARAMETER
         LIST BUT COULD NOT BE SATISFIED.  IF TYPE = L, THE TRACE
         ROUTINE COULD NOT BE FREED FROM LONG-TERM-FIX STATUS.  IF
         TYPE = S, THE RELEASE OF THE PARAMETER LIST WAS UNSUCCESSFUL.
         IF THE PROBLEM PERSISTS, SPECIFY TEST=YES TO GET A DUMP.  CHECK
         THE PARAMETERS FOR THE RELEASE REQUEST.  PROBLEMS IN THIS AREA
         ARE PROBABLY SYSTEM-WIDE AND WILL NOT BE READILY SOLVABLE WITH
         THE DUMP PROVIDED.

1                                                               PAGE 23

     GTFCCW MESSAGES  (CONTINUED)
     ----------------------------
-
   I07.  TRACING STOPPED.
         THIS MESSAGE INDICATES THAT CONTROL HAS BEEN RETURNED FROM THE
         GTF MONITOR AND THAT CCWTRACE CLOSEDOWN IS IN PROGRESS.

   T08.  TRACE RETURN CODE IS XXXX.  DUMP FOLLOWS.
         IF THE GTF MONITOR RETURNS TO GTFCCW WITH A RETURN CODE OF 8 OR
         MORE, A DUMP IS AUTOMATICALLY TAKEN.  THIS MESSAGE IS ISSUED
         TO CLARIFY THE REASON FOR THE DUMP SINCE IT IS ALSO POSSIBLE
         FOR A DUMP TO BE MADE BASED ON THE TEST OPTION -- SEE
         MESSAGE T09.  IF AN ABEND SITUATION OCCURS WHILE TRACING,
         GTF'S FRR WILL -
            1. MAKE A LOGREC ENTRY
            2. WRITE A DUMP TO THE SYS1.DUMPXX DATASET
            3. TERMINATE THE TRACING FUNCTION AND RETURN WITH A CODE
               OF 8 IN REGISTER 15.
         THIS INFORMATION WILL BE NEEDED ALONG WITH THE DUMP BEING TAKEN
         BY GTFCCW.

   T09.  DUMPING BECAUSE TEST = YES.
         IF TEST=YES IS SPECIFIED, A DUMP IS TAKEN UPON TERMINATION OF
         TRACING ACTIVITY WITHOUT REGARD TO THE RETURN CODE.

   T12.  PARAMETER LIST TOO LONG.
         THIS MESSAGE IS ISSUED WHEN THE LENGTH OF THE PARAMETER LIST
         PASSED TO GTFCCW IS LONGER THAN 110 BYTES. A DUMP IS TAKEN TO
         HELP DETERMINE WHERE THE INCORRECT LENGTH OCCURRED.

   T13.  MODID OF AHLTSIO DOES NOT MATCH MODID OF GTFCCW.
         THIS MESSAGE IS ISSUED WHEN CONFLICTING MODULE LEVELS ARE
         DETECTED. IT IS FOLLOWED BY A U0122 DUMP.  THE USUAL REASON
         FOR THIS MESSAGE IS THAT CLPA WAS NOT SPECIFIED DURING IPL
         OR AN INCORRECT LEVEL OF AHLTSIO IS IN AN ACTIVE MLPA LIBRARY.
         IT MAY ALSO BE POSSIBLE, THAT GTFSIO WAS NOT CORECTLY LINKED
         TOGETHER WITH AHLTSIO.

   T14.  INITIALIZATION ERROR FOR MODULE GTFSIO.
         THIS MESSAGE IS GIVEN, IF GTFSIO FAILS TO INITIALIZE ITSELF.
         CHECK YOUR LKED RUN IN THIS CASE.

   T15.  GTFCCW IS NOT AUTORIZED FOR PK ZERO.
         IF THIS MESSAGE IS GIVEN, CHECK YOUR PPT ACCORDING TO STEP 2
         OF THE INSTALLATION PROCEDURE. ONE OF THE MOST COMMON REASONS
         IS, THAT YOU FORGOT CLPA DURING IPL, OR THE CUSTOMER WORKS
         WITH MLPA DURING IPL AND WORKS WITH ITS OWN INITIATOR.
1                                                               PAGE 24
-
    GTFSIO MESSAGES (TRACE ROUTINE)
    -------------------------------
-
    NOTE - THESE MESSAGES ARE WRITTEN TO THE GTF OUTPUT FILE BY THE
           TRACE ROUTINE.  THEY ARE PRINTED BY THE EDIT ROUTINE ONLY
           WHEN PRINTING CCW TRACE OUTPUT.
-
    GTFSIO02  NO VALID CCW'S LOCATED DURING CCW SCAN.
       THIS MESSAGE IS WRITTEN WHEN ALL POSSIBLE CCW ADDRESSES ARE
       FOUND NOT TO CONTAIN VALID CCW'S.

       IF THIS MESSAGE IS GIVEN VERY FREQUENTLY, MAKE SURE     @LZ90330
       YOUR SUDATA PARAMETER IS CORRECTLY SPECIFIED.           @LZ90330
-
    GTFPRINT MESSAGES (EDIT ROUTINE)
    --------------------------------
-
    GTF001  PARAMETER ERROR, PGM TERMINATED.
       THIS MESSAGE IS GIVEN VIA A WTL WTOR. TOGETHER WITH THIS
       MESSAGE, THE PGM IS TERMINATED AND THE RETURNCODE
       IS SET TO 16. CHECK YOUR PARM DEFINITION FOR THE FOLLOWING
       REASON AND RERUN THE JOB.

       1. THE PARM FIELD PASSED IS TOO LONG.
       2. THE PARM FIELD PASSED IS NOT THE ONLY ONE.
       3. ONE OF THE GTFPRINT PARMS IS INVALID OR CONTAINS
          AN ERROR.
       4. ONE OF THE GTFPRINT PARMS IS DOUBLE SPECIFIED.

     GTF002  MODID OF GTFPRINT DOES NOT MATCH THE MODID OF GTFCCWPR.
        WHEN RUNNING GTFPRINT, IT IS REQUIRED, THAT ALL LOADS NEEDED
        HAVE THE SAME LEVEL. IF GTFCCWPR HAS AN OTHER LEVEL, THIS
        MESSAGE IS PRINTED AND THE PGM TERMINATE WITH A USER ABEND122.

     GTF003  MODID OF GTFPRINT DOES NOT MATCH THE MODID OF AMDSYS00.
        WHEN RUNNING GTFPRINT, IT IS REQUIRED, THAT ALL LOADS NEEDED
        HAVE THE SAME LEVEL. IF AMDSYS00 HAS AN OTHER LEVEL, THIS
        MESSAGE IS PRINTED AND THE PGM TERMINATE WITH A USER ABEND122.
          IN CASE OF THIS MESSAGE, PLEASE CHECK THE INSTALLATION
        PROCEDURE FOR CORRECT RENAME OF AMDSYS00 TO AMDSYS0S IN THE
        LPALIB. YOU SCHOULD ALSO NOTE, THAT STEPLIB PROCESSING IS
        NOT POSSIBLE FOR THOSE LOADS, THAT MUST RESIDE IN THE LPALIB.
1                                                              PAGE 25
-
       GTFCCWPR MESSAGES (EDIT ROUTINE)
       --------------------------------
-
    GTFPR01  ERROR IN RECORD COUNT, MAY BE LOST EVENTS.
       ALL TRACE RECORDS ARE SEQUENCED.  THIS MESSAGE IS WRITTEN WHEN
       AN OUT-OF-SEQUENCE RECORD IS DETECTED.  THERE ARE SEVERAL
       POSSIBLE CAUSES, AMONG THEM, THAT GTF COULD NOT WRITE ITS
       BUFFERS FAST ENOUGH OR THAT AN I/O ERROR OCCURRED ON THE
       GTF OUTPUT DEVICE.

    GTFPR02  CHAIN NOT COMPLETE.
       THERE WERE MORE CCW'S THAN SPECIFIED ON THE CCW = PARAMETER
       OR ELSE THERE WERE MORE THAN 512 CCW'S IN THE CHAIN.

    GTFPR03  END OF NORMAL CHAIN.
       END OF CHAIN IS DETECTED, BUT EDIT OF CCW'S CONTINUES.
         A) IN CASE OF SIO TYPE THE CURRENT CCW DOES NEITHER HAVE
            COMMAND CHAINING, NOR DATA CHAINING.
         B) IN CASE OF IO TYPE THE CURRRENT CCW IS POINTED BY THE CSW-8.

    GTFPR04  CHAIN LOST.
       THE ADDRESS OF THE NEXT CCW IS INVALID:
         A) NOT IN REAL STORAGE
         B) NOT A MULTIPLE OF 8
         C) TIC-TO ADDRESS IS INVALID
       IN THE CASE OF AN I/O INTERRUPT, THE ADDRESS AT CSW - 8 WILL
       BE ATTEMPTED IN ORDER TO AT LEAST BRING THE LAST CCW EXECUTED.

    GTFPR05  CHAIN STARTS AT 00XXXXXX
       WHEN SORT = YES, IT IS POSSIBLE THAT THE 1ST CCW LISTED IS NOT
       THE LOGICAL START OF THE CHAIN.  THE ADDRESS GIVEN IN THIS
       MESSAGE IS THE LOGICAL START OF THE CHAIN.  THIS MESSAGE ONLY
       OCCURS WHEN SORT WAS SPECIFIED FOR THE TRACE.

    GTFPR06  ERROR IN INPUT RECORD.
       EITHER 1) THE LAST RECORD WAS NOT FOUND IN A CHAIN
                 (E.G., GTF BUFFERS ALL FULL, DATA LOST),
           OR 2) THE RECORD COUNT IS NOT THE EXPECTED VALUE.

    GTFPR07  DATA COULD NOT BE TRACED.
       EITHER 1) INVALID DATA ADDRESS,
           OR 2) DATA BUFFER NOT IN REAL STORAGE.

    GTFPR08  DATA TRACING NOT REQUESTED.
       DATA=NO WAS SPECIFIED WHEN THE GTFCCW PROCEDURE WAS STARTED.

    GTFPR09  EXPECTED CONTINUATION OF PREVIOUS RECORD NOT RECEIVED.
       GTFCCWPR IS PROCESSING A RECORD, WANTS TO READ ITS CONTINUATION,
       BUT THE RECORD TYPE RECEIVED IS NOT THAT AS EXPECTED.
       THE NEXT LINE FOLLOWING THIS MESSAGE MAY EXPLAIN THE REASON.
       THE MESSAGE MAY ALSO BE FOLLOWED BY A DUMP RECORD, TITLED 'DUMP'.
       IN THIS CASE, A RECOVERABLE PROGRAM CHECK OCCURRED IN GTFSIO.
1                                                               PAGE 26
-


 MAINTENANCE HINTS.
 -----------------

 1. FOR GTFCCW
 -------------

 IN CASE OF A PGM CHECK, SEND A SYSUDUMP WHICH CONTAINS THE LPA MODULE
 AHLTSIO. USE THE CHNGDUMP COMMAND TO ALTER THE SYSUDUMP PARMS IF YOUR
 DUMP DOES NOT CONTAIN LPA MODULES.  EXAMPLE:
    CD SET,SYSUDUMP,SDATA=(SQA,LSQA,CB,TRT),PDATA=(ALLPDATA)

 IN MOST CASES SYS1.LOGREC WILL HAVE PERTINENT INFORMATION IN IT.  BE
 SURE TO PRINT IT AND SEND IT ALONG WITH THE DUMP.

 USE GTFPRINT TO PRINT THE TRACE DATASET AT TIME OF ERROR.  SEND IT
 ALONG WITH THE DUMP. A BLOCK TITLED 'DUMP' WILL HAVE THE MOST PERTINENT
 INFORMATION IN IT.

 WARNING: AFTER ANY ABNORMAL TERMINATION OF GTFCCW OR NORMAL GTF WITH
          OR WITHOUT THIS MODIFICATION, DO NOT START AGAIN PRIOR IPL.
          OTHERWISE YOU MAY GET ABEND S0F6.

          NOTE ALSO, THAT IT IS A RESTRICTION OF GTF, THAT IT HAS TO
          BE STARTED FROM THE CONSOLE. TIME=1440 MUST ALSO BE CODED IN
          THE EXEC STATEMENT OF THE PROCEDURE.


 2.) FOR GTFPRINT
 ----------------

 IN CASE OF A PROGRAM CHECK, SEND A SYSUDUMP AND A MACHINE READABLE
 COPY OF THE FAILING TRACE DATASET.
 IN CASE OF INCORRECT OUTPUT, IT MAY BE USEFUL TO RUN GTFPRINT WITH
 PARAMETER 'TEST'. IN CASE OF AN UNEXPECTED PARAMETER OR RECORD,
 EITHER GTFPRINT OR GTFCCWPR WILL ABEND WITH A USER CODE OF U122.
 SEND THE DUMP AND/OR THE FAILING OUTPUT ALONG WITH YOUR ERROR
 DESCRIPTION.


 3.) GENERAL
 -----------

 FIXES FOR KNOWN PROBLEMS WILL BE IN RETAIN IN THE SEARCH FILE.
 THE FOLLOWING SEARCH ARGUMENT SHOULD GET YOU TO THE INFORMATION:
   N;4/F ALL/P;CCWTRACE R030
 HAVE A LOOK AT THE GTF COMPONENT IN SSF, TOO.

 IT MAY ALSO BE USEFUL TO LOOK IN THE INCIDENT FILE FOR PROBLEMS.  IF
 YOU WANT TO DEBUG A PROBLEM YOURSELF, YOU CAN GET AN ASSEMBLER LISTING
 OF YOUR FAILING MODULE. SOURCE IS WITHIN THIS VOLUME;  REFER TO:
 'FORMAT AND CONTENTS OF THE DISTRIBUTION TAPE'.

 IN ALL CASES, AN INCIDENT IN RETAIN IS HELPFUL TO CLEAR THE
 SITUATION. OPEN THE INCIDENT WITH THE ADDITIONAL KEYWORD 'CCWTRACE' TO:

      QUEUE GT3 COMPONENT 5752SC111
1                                                               PAGE 27
-
    FORMAT AND CONTENTS OF THE DISTRIBUTION TAPE
    --------------------------------------------

                                      RECFM=FB
                                      LRECL=80
                                      BLKSIZE=400
                                      DEN=3
-
       CONTENTS OF THE DISTRIBUTION TAPE
       ---------------------------------

  FILE NO  1    GTF CCWTRACE DOCUMENTATION
  FILE NO  2    SMP CNTLIN DATASET
  FILE NO  3    SMP JCLIN DATASET
  FILE NO  4    SMP PTFIN DATASET
  FILE NO  5    GTF PROCEDURE     -  IEBUPDTE INPUT
  FILE NO  6    TRANS MACRO       -  TRANSLATION EXIT SUPPORT
  FILE NO  7    GTFCCW SOURCE     -  GTF PARAMETER CHECKER
  FILE NO  8    GTFSIO SOURCE     -  GTF TRACE ROUTINE
  FILE NO  9    AMDSYS00 SOURCE   -  EDIT CONTROL ROUTER
  FILE NO 10    GTFPRINT SOURCE   -  EDIT PARAMETER PROCESSOR
  FILE NO 11    GTFCCWPR SOURCE   -  CCWTRACE DATA EDITOR
  FILE NO 12    SET OF SAMPLE JCL TO ASSIST YOU IN INSTALLATION OF
                GTFCCW.
./ ADD NAME=CCWMODS                                12JUN82 12.33.18
//CCWTRACE JOB MSGLEVEL=1
//***
//***    SYS PROG: HOWARD DEAN
//***    DATE:     05/05/81
//***    LOCATION: SSU.MVSMODS(CCWTRACE)
//***    DESC:     INSTALLATION OF GTFCCW. PREPARE
//***              THE WAY FOR SMP BY DELETING
//***              OLD MODULES AND RUNNING JCLIN AND
//***              UCLIN. THIS SHOULD BE RUN AFTER A
//***              REGRESSION OF ANY GTFCCW MODULES.
//***
//******************************************************************
//*             C C W T R A C E    I N S T A L L                   *
//*    PREPARE LPALIB AND LINKLIB ACCORDING TO STEP3 OF THE        *
//*    INSTALLATION PROCEDURE.                                     *
//*    EXPECT A RETURNCODE OTHER THAN ZERO FROM IEHPROGM, IF       *
//*    EITHER A PREVIOUS VERSION OF GTFCCW WAS INSTALLED, OR       *
//*    VERSION 1.4 OF GTFCCW WAS NOT INSTALLED.                    *
//*                                                                *
//******************************************************************
//STEP3     EXEC  PGM=IBMPROGM
//SYSPRINT  DD   SYSOUT=*
//LPALIB    DD   DISP=SHR,VOL=REF=SYST.LPALIB    ** TARGET LPALIB  **
//LINKLIB   DD   DISP=SHR,VOL=REF=SYST.LINKLIB   ** TARGET LINKLIB **
//SYSIN     DD   *
  RENAME  DSNAME=SYST.LPALIB,VOL=3350=ITST2B,        *******************
               MEMBER=AMDSYS00,NEWNAME=AMDSYS0S
  RENAME  DSNAME=SYST.LINKLIB,VOL=3350=ITST2B,       *******************
               MEMBER=BHLGTF1,NEWNAME=AHLGTF
//******************************************************************
//*                                                                *
//*    LINK GTFSIO TO LPALIB, TO HAVE IT AVAILABLE FOR SMP.        *
//*    YOU MAY USE THE FOLLOWING IEWL STEP OR THE LINKS PROCEDURE  *
//*    DESCRIBED IN STEP3B OF THE INSTALLATION PROCEDURE.          *
//*                                                                *
//******************************************************************
//STEP3A    EXEC  PGM=IEWL,PARM='NCAL,LIST,LET,XREF,AC=1,RENT,REUS'
//SYSPRINT  DD   SYSOUT=*
//SYSLMOD   DD   DSN=SYST.LPALIB,DISP=SHR
//AOS11     DD   DSN=SYS1.AOS11,DISP=SHR,UNIT=DISK,VOL=SER=ITSTC2
//SYSUT1    DD   UNIT=SYSDA,SPACE=(1700,(300,50))
//SYSLIN    DD   *
 CHANGE  AHLTIO(BHLTIO)
 CHANGE  AHLTSIO1(BHLTSIO1)
 INCLUDE AOS11(AHLTSIO)
 NAME    GTFSIO(R)
//STEP3B      EXEC PUTSMP,OPT=200
//SMPCNTL     DD   *
UCLIN CDS DIS(WRITE)  /* CCWTRACE INSTALLATION - VERSION 3.0 */.
 DEL MAC  (TRANS   ) .
 DEL MAC  (GTFCCWP ) .
 DEL MOD  (GTFCCW  ) .
 DEL MOD  (GTFPRINT) .
 DEL MOD  (GTFCCWPR) .
 DEL MOD  (AMDSYS00) .
 DEL MOD  (AMDSYS0A) .
 DEL MOD  (AHLTSIO ) .
 DEL MOD  (GTFSIO  ) .
 DEL LMOD (GTFCCW  ) .
 DEL LMOD (GTFPRINT) .
 DEL LMOD (GTFCCWPR) .
 DEL LMOD (AMDSYS00) .
 DEL LMOD (AMDSYS0S) .
 DEL LMOD (AHLTSIO)  .
ENDUCL  .
 RESETRC.
JCLIN DIS(WRITE).
/*
//SMPJCLIN   DD  DATA,DLM=$$
//CCWTRC  0JOB (0,0),SYSPTF,REGION=1024K,TIME=9,MSGLEVEL=(1,1),
//         NOTIFY=IBM001
//SG02 EXEC LINKS,PARM='NCAL,LIST,XREF,RENT,REUS',
//    UNIT='3330',SER=SYSRES,NAME=LPALIB,P1=' ',                       X
//             N=SYS1,                                                 X
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=A
//AOS12 DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.AOS12
//SYSLIN DD *
  INCLUDE AOS12(AMDSYS00)
 NAME AMDSYS0S(R)
//SG02 EXEC LINKS,PARM='NCAL,LIST,XREF,AC=1,RENT,REUS',
//    UNIT='3330',SER=SYSRES,NAME=LPALIB,P1=' ',                       X
//             N=SYS1,                                                 X
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=A
//AOS11 DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.AOS11
//SYSLIN DD *
   ORDER AHLTSIO(P),GTFSIO
   CHANGE AHLTIO(BHLTIO)
   CHANGE AHLTSIO1(BHLTSIO1)
   INCLUDE AOS11(AHLTSIO)
   INCLUDE AOS11(GTFSIO)
   ALIAS AHLTIO
   ALIAS AHLTUIO
   ALIAS AHLTSIO1
   ALIAS AHLTSIO
   ENTRY GTFSIO
   NAME GTFSIO(R)
//SG03 EXEC LINKS,PARM='NCAL,LIST,XREF',
//    UNIT='3330',SER=SYSRES,NAME=LINKLIB,P1=' ',
//             N=SYS1,                                                 X
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=A
//AOS12 DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.AOS12
//SYSLIN DD *
  INCLUDE AOS12(AMDSYS0A)
 NAME AMDSYS00(R)
  INCLUDE AOS12(GTFCCWPR)
 NAME GTFCCWPR(R)
//SG04 EXEC LINKS,PARM='NCAL,LIST,XREF,AC=1',
//    UNIT='3330',SER=SYSRES,NAME=LINKLIB,P1=' ',
//             N=SYS1,                                                 X
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=A
//AOS11 DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.AOS11
//SYSLIN DD *
  INCLUDE AOS11(GTFCCW)
 NAME GTFCCW(R)
//SG05 EXEC LINKS,PARM='NCAL,LIST,RENT,REUS,REFR,XREF',
//    UNIT='3330',SER=SYSRES,NAME=LINKLIB,P1=' ',
//             N=SYS1,                                                 X
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=A
//AOS12 DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.AOS12
//SYSLIN DD *
  INCLUDE AOS12(GTFPRINT)
 NAME GTFPRINT(R)
/*
$$
//STEP4A   EXEC PUTSMP,OPT=200
//SMPCNTL  DD *
   RECEIVE S(LA00106).
   APPLY   S(LA00106) DIS(WRITE).
//SMPPTFIN DD DATA,DLM=$$
++ USERMOD (LA00106)    /* CCWTRACE VERSION 3.0
     *****************************************************************
     *                                                               *
     *       GTEDS: MARINA DEL REY, CALIFORNIA                       *
     *                                                               *
     *       DATE:  05/05/81  BY: H.M. DEAN                          *
     *                                                               *
     *       RELEASE         - OS/MVS 3.8  MVS/SP1.0                 *
     *       ENVIRONMENT     - 8MB IBM 3033, 12MB AMDAHL V8          *
     *                       - 8MB IBM 3033N                         *
     *                                                               *
     *       PTF LEVEL       - 8101                                  *
     *                                                               *
     *       USERMOD         - LA00106                               *
     *       JOBNAME         - SSUD106C                              *
     *       TYPE            - MOD/MAC/ZAP                           *
     *                                                               *
     *       PRODUCT         - GENERALIZED TRACE FACILITY            *
     *       LMOD            - AMDSYS0A,AMDSYS0S,GTFSIO,GTFCCW,      *
     *                       - GTFCCWPR,GTFPRINT                     *
     *                                                               *
     *       MOD (CSECT)     - GTFCCW,GTFSIO,AMDSYS0A,GTFPRINT,      *
     *                         GTFCCWPR                              *
     *                                                               *
     *       MAC             - TRANS   - TRANSLATE EXIT MACRO        *
     *                       - GTFCCWP - GTFCCW TRACE PROCEDURE      *
     *                                                               *
     *       PROBLEM         - GENERALIZED TRACE FACLIITY DOES       *
     *                         NOT PROVIDE FOR TRACING OF CHANNEL    *
     *                         COMMANDS AND ASSOCIATED DATA.         *
     *                                                               *
     *       PURPOSE/        - INSTALL INFORMALLY SUPPORTED IBM      *
     *       ENHANCEMENT       WRITTEN CCWTRACE MODIFICATIONS TO     *
     *                         GTF.                                  *
     *                                                               *
     *****************************************************************
     */.
++ VER (Z038) FMID(JBB1126)           /* MVS SYSTEM PRODUCT REL 1  */.
++ JCLIN.
//CCWTRC  0JOB (0,0),SYSPTF,REGION=1024K,TIME=9,MSGLEVEL=(1,1),
//         NOTIFY=IBM001
//SG02 EXEC LINKS,PARM='NCAL,LIST,XREF,RENT,REUS',
//    UNIT='3330',SER=SYSRES,NAME=LPALIB,P1=' ',                       X
//             N=SYS1,                                                 X
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=A
//AOS12 DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.AOS12
//SYSLIN DD *
  INCLUDE AOS12(AMDSYS00)
 NAME AMDSYS0S(R)
//SG02 EXEC LINKS,PARM='NCAL,LIST,XREF,AC=1,RENT,REUS',
//    UNIT='3330',SER=SYSRES,NAME=LPALIB,P1=' ',                       X
//             N=SYS1,                                                 X
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=A
//AOS11 DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.AOS11
//SYSLIN DD *
  ORDER AHLTSIO(P),GTFSIO
    CHANGE AHLTIO(BHLTIO)
    CHANGE AHLTSIO1(BHLTSIO1)
  INCLUDE AOS11(AHLTSIO)
  INCLUDE AOS11(GTFSIO)
    ALIAS AHLTIO
    ALIAS AHLTUIO
    ALIAS AHLTSIO1
    ALIAS AHLTSIO
  ENTRY GTFSIO
    NAME  GTFSIO(R)
//SG03 EXEC LINKS,PARM='NCAL,LIST,XREF',
//    UNIT='3330',SER=SYSRES,NAME=LINKLIB,P1=' ',
//             N=SYS1,                                                 X
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=A
//AOS12 DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.AOS12
//SYSLIN DD *
  INCLUDE AOS12(AMDSYS0A)
 NAME AMDSYS00(R)
  INCLUDE AOS12(GTFCCWPR)
 NAME GTFCCWPR(R)
//SG04 EXEC LINKS,PARM='NCAL,LIST,XREF,AC=1',
//    UNIT='3330',SER=SYSRES,NAME=LINKLIB,P1=' ',
//             N=SYS1,                                                 X
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=A
//AOS11 DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.AOS11
//SYSLIN DD *
  INCLUDE AOS11(GTFCCW)
 NAME GTFCCW(R)
//SG05 EXEC LINKS,PARM='NCAL,LIST,RENT,REUS,REFR,XREF',
//    UNIT='3330',SER=SYSRES,NAME=LINKLIB,P1=' ',
//             N=SYS1,                                                 X
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=A
//AOS12 DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.AOS12
//SYSLIN DD *
  INCLUDE AOS12(GTFPRINT)
 NAME GTFPRINT(R)
++ MOD (GTFCCW  ) DISTLIB (AOS11) LEPARM (AC=1) TXLIB(LOCLOBJ).
++ MOD (GTFSIO  ) DISTLIB (AOS11) LEPARM (RENT,REUS,AC=1)
                  TXLIB(LOCLOBJ).
++ MOD (AMDSYS0A) DISTLIB (AOS12) LEPARM (STD) TXLIB(LOCLOBJ).
++ MOD (GTFPRINT) DISTLIB (AOS12) LEPARM (RENT,REUS,REFR)
                  TXLIB(LOCLOBJ).
++ MOD (GTFCCWPR) DISTLIB (AOS12) LEPARM (STD) TXLIB(LOCLOBJ).
++ MAC (TRANS   ) DISTLIB (AMACLIB)  SYSLIB(MACLIB).
         MACRO
&NAME    TRANS &DEV=,&ADDR=,&TABLE=,&UTABLE=
         GBLB  &SW
         LCLB &LSW
         LCLA    &C
         AIF   ('&DEV'  NE '' OR '&ADDR' NE '').L1
         MNOTE 4,'NO DEVICETYP AND NO ADDR SPECIFIED'
&LSW     SETB  1
.L1      ANOP
         AIF   ('&TABLE'  NE '' OR '&UTABLE' NE '').L2
         MNOTE  4,'NO TRANSLATE-TABLE SPECIFIED'
&LSW     SETB  1
.L2      ANOP
         AIF   (&LSW).EXIT
         AIF   (&SW).TABLER
&NAME    CSECT
         USING  *,10
         LR     10,15
         LR    11,1
         USING  PARM,11
         L     2,=A(DEVADRT)    POINT TO ADDR TABLE
ADRLOOP  EQU   *
         CLI   0(2),255        END OF TABLE
         BE    TYPR             YES NEXT ROUTINE
         CLC   DEVADDR(2),0(2) ADDR IN TABLE
         BE     FOUND          YES
         LA    2,4(2)           POINT TO NEXT
         B     ADRLOOP
TYPR     EQU   *
         SR    15,15             RETURN CODE
         L      2,=A(DEVTYPT)     DEVICE TYP TABLE
TYPLOOP  EQU   *
         CLI    0(2),255        END OF TABLE
         BCR    8,14             YES RETURN
         CLC     DEVTYPE,0(2)   SELECTED TYP
         BE     FOUND
         LA     2,4(2)
         B      TYPLOOP
FOUND    EQU    *
         LH     2,2(2)
         A      2,=A(TABBASE)
         TR     EBCDIC,0(2)
         LA     15,4
         BR     14
PARM     DSECT
         DS     1F
DEVADDR  DS     H
DEVTYPE  DS     C
         DS    CL96
EBCDIC   DS    CL24
DEVADRT  CSECT
         DC    A(-1)
DEVTYPT  CSECT
         DC    A(-1)
&SW      SETB  1
.TABLER  ANOP
         AIF   ('&DEV' EQ '').L10
DEVTYPT  CSECT
         ORG   *-4
&C       SETA  0
.L5      ANOP
&C       SETA  &C+1
         DC    X'&DEV(&C)'
         DC    X'00'
         AIF   ('&TABLE' EQ '').L6
         DC    AL2(IECT&TABLE-TABBASE)
         AGO   .L7
.L6      ANOP
         DC    AL2(&UTABLE-TABBASE)
.L7      ANOP
         AIF  (&C LT N'&DEV).L5
         DC    A(-1)
.L10     ANOP
         AIF   ('&ADDR' EQ '').L30
DEVADRT  CSECT
         ORG   *-4
&C       SETA   0
.L25     ANOP
&C       SETA    &C+1
         DC      XL2'&ADDR(&C)'
         AIF    ('&TABLE' EQ '').L26
         DC     AL2(IECT&TABLE-TABBASE)
         AGO    .L27
.L26     ANOP
         DC     AL2(&UTABLE-TABBASE)
.L27     ANOP
         AIF    (&C LT N'&ADDR).L25
         DC     A(-1)
.L30     ANOP
         AIF   ('&TABLE' EQ   '').EXIT
TABBASE  CSECT
         ASMTRTAB &TABLE
.EXIT    ANOP
         MEND
++ MAC (GTFCCWP ) DISTLIB (APROCLIB) SYSLIB(PROCLIB).
//GTFCCWP PROC REG=1400,CCW=50,SIO=NO,IOSB=NO,IO=YES,SORT=NO,DATA=24,
//             TEST=NO,JOB=NO,SMOD=NO
//***************************************************************
//*                                                             *
//*  THIS PROCEDURE CORRESPONDS TO VERSION 3.0 OF CCWTRACE      *
//*                                                             *
//***************************************************************
//IEFPROC EXEC PGM=GTFCCW,TIME=1440,REGION=&REG.K,DPRTY=(15,15),
// PARM=('TIME=YES,MODE=EXT,SU=8000,CCW=&CCW,SIO=&SIO',
// 'IOSB=&IOSB,IO=&IO,SORT=&SORT,DATA=&DATA,TEST=&TEST,JOB=&JOB',
// 'SMOD=&SMOD')
//IEFRDER   DD DSN=SYS1.TRACE,UNIT=2400-3,VOL=SER=CCWTRC,LABEL=(1,NL),
//             DISP=OLD
//SYSPRINT  DD SYSOUT=A
//SYSUDUMP  DD SYSOUT=A
$$
//*** THE NEXT STEP FIXES BUGS IN CCWTRACE VERSION 3.0
//*** THESE FIXES ARE FOR MVS/SYSTEM PRODUCT 1
//***
//***    SYS PROG: HOWARD DEAN
//***    DATE:     05/06/81
//***    LOCATION: SSU.MVSMODS(CCWTRACE)
//***    DESC:     MVS SP/1 FIXES FOR VERSION 3.0 OF CCWTRACE
//***              USERMOD LA00106 INSTALLES CCWTRACE, BUT THIS
//***              USERMOD IS NEEDED BECAUSE ZAPS AND MODS
//***              CANNOT BE INCLUDED IN THE SAME SYSMOD.
//***
//STEP5A   EXEC PUTSMP,OPT=200
//SMPCNTL  DD *
   RECEIVE S(LA00107).
   APPLY   S(LA00107) DIS(WRITE).
//SMPPTFIN DD DATA,DLM=$$
++ USERMOD (LA00107)    /* MVS SP1.1 FIXES FOR CCWTRACE REL 3.0
     *****************************************************************
     *                                                               *
     *       GTEDS: MARINA DEL REY, CALIFORNIA                       *
     *                                                               *
     *       DATE:  05/06/81  BY: H. M. DEAN                         *
     *                                                               *
     *       RELEASE         - OS/MVS 3.8 - SP REL 1.0               *
     *       ENVIRONMENT     - 8MB IBM 3033, 12MB AMDAHL V8          *
     *                       - 8MB IBM 3033N                         *
     *                                                               *
     *       PTF LEVEL       - 8101                                  *
     *                                                               *
     *       USERMOD         - LA00107                               *
     *       JOBNAME         - SSUD107A                              *
     *       TYPE            - ZAP                                   *
     *                                                               *
     *       PRODUCT         - GTF CCWTRACE                          *
     *       ZAP (CSECT)     - GTFSIO,GTFCCWPR,GTFPRINT              *
     *                                                               *
     *       PROBLEM         - GTF CCWTRACE DOES NOT FULLY SUPPORT   *
     *                         MVS SYSTEM PRODUCT RELEASE 1.0        *
     *                                                               *
     *       PURPOSE/        - INSTALL FIXES SUPPLIED BY IBM         *
     *       ENHANCEMENT       TO SUPPORT THIS PRODUCT.              *
     *                                                               *
     *****************************************************************
     */.
++ VER (Z038) FMID(JBB1126) PRE(LA00106) /* PREREQ IS CCWTRACE 3.0 */.
++ ZAP (GTFSIO) DISTLIB(AOS11).
NAME GTFSIO
************************************************************
* 1. GTFSIO FAILS TO TRACE DATA WITH BIT 35 ON IN A WRITE
*    TYPE CCW.  FIXED R032.
* 2. BRING UP THE RECOVERY PROCESSING, THAT IT IS NO LONGER
*    AN IPL CASE, IF YOU SPECIFY AN INVALID SUDATA PARM FOR
*    A DIFFERENT SYSTEM.
* 3. GET NO REIPL CASE IF INSTALLED IN THE WRONG SYSTEM.
*    (WITHOUT THIS ZAP CCWTRACE WILL NOT RUN IN THE MVS/SP
*    ENVIRONMENT.)
* 4. GET AN UPDATE TO RUN WITH MVS/SP1.2
* 5. CORRECT THE USING OF SIO ERROR RETRY COUNT.
* 6. IN CASE OF AN ERROR, THE FRR MAY LEAVE THE MODULE WITHOUT
*    RELOADING THE CALLER'S CNTLREG1.
* 7. BEFORE LOADING CNTLREG1, MAKE SURE YOU'VE FOUNDED AT
*    LEAST A VALID ASCB BY CHECKING IT'S ACRONYM.
*****************************************************************
*    THIS ZAP REQUIRES SOME DOCUMENTATION CHANGE
*    PAGE 11  ADD BEHIND SUDATA:
*    SPECIFY SU=8000 FOR SYSTEMS HAVING MVS/SP1.1 INSTALLED
*    SPECIFY SU=8080 FOR SYSTEMS HAVING MVS/SP1.2 INSTALLED
*    PAGE 24  ADD TO MESSAGE GTFSIO02:
*    IF THIS MESSAGE IS GIVEN VERY FREQUENTLY,MAKE SURE
*    YOUR SUDATA PARAMETER IS CORRECTLY SPECIFIED.
*
*    SOME ADDITIONAL NOTES TO THIS FIX.
*    THIS FIX SOLVES A BASIC RECOVERY PROBLEM AND SHOULD BE
*    INSTALLED TO EVERY ACCOUNT HAVING R030 INSTALLED. IF
*    YOUR FMID DOES NOT MATCH, ADD WHAT EVER YOU WANT , BUT
*    PREREQUISITE OF LZ90030 IS REQUIRED IN ANY CASE.
***********************************************************
***********************************************************
************************************************************
VER 008C 43E060DB         RETRY COUNT IS FROM WRONG OFFSET
REP 008C 43E060DA         NOW IT IS OKAY
VER 0306 920010FC
VER 032A C7E3C6E2C9D64040  PREVIOUS CSECT ID
VER 051A 961060D5,B711F02C
VER 0600 91104004,4710B6AE,950160D9,4770B618,91014000
VER 0614 47E0B6AE
VER 0CF0 0000,0000,0000,0000,0000,0000,0000,0000
VER 0D00 0000,0000,0000,0000,0000,0000,0000,0000
VER 0D10 0000,0000,0000,0000,0000,0000,0000,0000
VER 0D20 0000,0000,0000,0000,0000,0000,0000,0000
VER 0D30 0000,0000,0000,0000,0000,0000,0000,0000
VER 0D40 0000,0000,0000,0000,0000,0000,0000,0000
VER 0D50 0000,0000,0000,0000
************************************************************
REP 0306 47F0BD40                 GO TO PATCH
REP 032A D3E9F9F0F3F3F040  SHOW LEVEL LZ90330 IN RTM2WA
REP 051A 47F0BCFC,00000000
REP 0600 91014000,4770B618,950160D9,4780B6AE,91104004
REP 0614 4770B6AE
REP 0CF0 D3E9F9F0F3F3F040,C1E2C3C2  SET ZAPID AND ASCB
REP 0CFC D503F000BCF8     IS THIS AN ASCB?
REP 0D02 4770BD36         NO,GIVE ERROR MESSAGE
REP 0D06 9180B001         IS MVS-SP1.2 SPECIFIED ?
REP 0D0A 4710BD22         YES, HANDLE SP1.2
REP 0D0E BF1FF02C         GET CNTLREG1 FROM ASCB
REP 0D12 4780BD36         IF ZERO IT IS WRONG
REP 0D16 961060D5         SET CTL1REG SAVED
REP 0D1A B711F02C         GET IT IN CNTLREG1
REP 0D1E 47F0B522         RETURN, CTLREG1 SET
REP 0D22 BF1FF0F8           ASCBASTE PRESENT
REP 0D26 4780BD36           NO GIVE ERROR MESSAGE
REP 0D2A 961060D5           SET CNTLREG1 SAVED
REP 0D2E B7111008           GET CNTLREG1 FROM ASCBASTE
REP 0D32 47F0B522           RETURN
REP 0D36 968060D5           DO NOT TRY TO DUMP IOSB
REP 0D3A 47F0BA34           GO, GIVE ERROR MESSAGE
REP 0D40 920010FC,1266,4780B30A   WORKAREA PRESENT? NO EXIT
REP 0D4A 911060D5                 CNTLREG1 TO BE RELOADED ?
REP 0D4E 4780B30A                 NO, RETURN
REP 0D52 B711600C                 RELOAD CNTLREG1
REP 0D56 47F0B30A                 RETURN
IDRDATA LA00107
* END OF ZAP LZ90330
* END OF ZAP
++ ZAP (GTFPRINT) /* EXIT PARAMETER IGNORED BY GTFCCWPR */ .
NAME GTFPRINT
VER 02B4 5000A004
REP 02B4 5000A008
IDRDATA LA00107
* END OF ZAP
++ ZAP (GTFCCWPR) /* GTFCCWPR IGNORES THE DEVICETYPE
                     INFORMATION PASSED FROM GTFSIO AND USES
                     ALWAYS THE SAME TRANSLATE TABLE FOR
                     EDIT. */ .
NAME GTFCCWPR
VER 07A8 94F0,ABC4
VER 07B8 9503,ABC4
REP 07A8 940F,ABC4
REP 07B8 0700,0700
IDRDATA LA00107
* END OF ZAP
$$
./ ADD NAME=GTFCCW                                 12JUN82 12.33.18
         TITLE 'GTFCCW  PARAMETER CHECKER FOR CCWTRACE VERSION 3.0  FILE
               E07       -- IBM INTERNAL USE ONLY -- '
GTFCCW   CSECT
*        COMMON EQUATES                                      BIEDERMANN
         SPACE 1                                             BIEDERMANN
@00      EQU   0                      WORK REGISTER          BIEDERMANN
@01      EQU   1                      WORK REGISTER          BIEDERMANN
@02      EQU   2                      RESERVED TO KEEP THE EP OF GTFSIO
@03      EQU   3                      R03                    BIEDERMANN
@04      EQU   4                      R04                    BIEDERMANN
@05      EQU   5                      R05                    BIEDERMANN
@06      EQU   6                      R06                    BIEDERMANN
@07      EQU   07                     R07                    BIEDERMANN
@08      EQU   08                     R08                    BIEDERMANN
@09      EQU   09                     R09                    BIEDERMANN
@10      EQU   10                     R10                    BIEDERMANN
@11      EQU   11                     R11                    BIEDERMANN
@12      EQU   12                     BASE REGISTER          BIEDERMANN
@13      EQU   13                     COMMON OS USAGE        BIEDERMANN
@14      EQU   14                     WORK REGISTER          BIEDERMANN
@15      EQU   15                     WORK REGISTER          BIEDERMANN
         SPACE 1
CVTPTR   EQU   16                     POINTER TO CVT         BIEDERMANN
CVT      EQU   0
CVTTCBP  EQU   CVT+0                  POINTER TO TCB         BIEDERMANN
         SPACE 1
TCBOLD   EQU   4                      TCB DISPATCH           BIEDERMANN
TCB      EQU   0                      TCB                    BIEDERMANN
TCBRB    EQU   TCB+0                  TCB RB POINTER         BIEDERMANN
         SPACE 1
RB       EQU   0                      RB                     BIEDERMANN
RBOPSW   EQU   RB+16                  PSW IN RB              BIEDERMANN
         TITLE 'GTFCCW  PARAMETER CHECKER FOR CCWTRACE VERSION 3.0  FILE
               E07       -- IBM INTERNAL USE ONLY -- '
GTFCCW   CSECT
         SPACE 1
         USING PARMTABL,@08
         USING GTFCCW,@12
         SPACE 1
         STM   @14,@12,12(@13)
         LR    @12,@15
         B     JUMPID
         DC    C'GTFCCW W.BAUR LEVEL 3.0'
         ORG   *-3
GTFCCWID DS    CL3                    TO COMPARE GTFSIO      BIEDERMANN
         SPACE 2
***********************************************************************
*                                                                     *
* THIS MODULE IS THE INPUT PARAMETER CHECKER FOR  THE MVS VERSION OF  *
* THE EXTENSION TO GTF TO PERFORM CCW AND/OR DATA TRACING.  THIS      *
* MODULE WILL SCAN THE PARM= FIELD SPECIFIED ON THE EXEC STATEMENT    *
* AND WILL PROCESS ANY OF THE PARAMETERS THAT APPLY TO THE CCWTRACE   *
* EXTENSION.  PARAMETERS THAT DO NOT APPLY TO THE CCW FUNCTIONAL      *
* EXTENSION WILL BE PASSED TO THE NORMAL, SYSTEM SUPPLIED GTF         *
* INITIALIZATION MODULE.                                              *
*                                                                     *
***********************************************************************
         SPACE 2
JUMPID   DS    0H                                            BIEDERMANN
         LA    @15,SAVEAREA           GET MY SAVEAREA        BIEDERMANN
         ST    @15,8(,@13)            SAVE ADDRESS IN PREV.  BIEDERMANN
         ST    @13,SAVEAREA+4         PREVIOUS SAVE IN MINE  BIEDERMANN
         SLR   @15,@15                SET RETURN CODE        BIEDERMANN
         ST    @15,16(,@13)           KEEP IT IN SAVE        BIEDERMANN
         LA    @13,SAVEAREA           GET MY SAVE AEREA      BIEDERMANN
         LR    @05,@01                KEEP PARM ADDRESS
         SPACE 1
***********************************************************************
* ENQUEUE TO MAKE SURE THAT THE GTF CCWTRACE IS NOT ALREADY ACTIVE.   *
* IF CCWTRACE IS ALREADY ACTIVE, ISSUE A MSG AND EXIT. (RC = 16)      *
***********************************************************************
         SPACE 1
         ENQ   (QNAME,RNAME,E,RNMLEN,SYSTEM),RET=USE
         LTR   @15,@15
         BZ    OKAY
         WTO   'CCWTRACE T01. CCWTRACE IS ALREADY ACTIVE.',            *
               ROUTCDE=(2,11)
EXITRC16 L     @13,SAVEAREA+4         GET PREVIOUS SAVE
         LM    @14,@12,12(@13)        GET PREVIOUS REGISTERS
         LA    @15,16                 SET RETURN CODE
         BR    @14                    RETURN TO CALLER
         SPACE 1                                             BIEDERMANN
SAVEAREA DC    18F'-1'
QNAME    DC    C'CCWTRACE'
RNAME    DC    C'INITIALIZATION'
RNMLEN   EQU   14                     LENGTH OF RNAME
         DS    0H
OKAY     WTO   'CCWTRACE I00. CCWTRACE IS STARTING.',                  *
               ROUTCDE=(2,11)
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
*        FIRST, CHECK AUTHORITY TO MAKE SHURE, WE WORK WITH CORRECT   *
*        AUTHORITY.                                                   *
*                                                                     *
***********************************************************************
         SPACE 1
TESTAUTH DS    0H
         SPACE 1
         L     @01,CVTPTR             GET CVT ADDRESS
         L     @01,CVTTCBP(,@01)      GET TCB POINTERS
         L     @01,TCBOLD(,@01)       GET MY TCB
         L     @01,TCBRB(,@01)        GET TCB RB POINTER
         TM    RBOPSW+1(@01),X'F0'    CHECK PK=0
         BZ    SCANIT                 YES, GO TO NEXT STAGE
         WTO   'CCWTRACE T15. GTFCCW IS NOT AUTORIZED FOR PK ZERO.'    *
               ROUTCDE=(2,11)
         SPACE 1
         L     @01,SAVEAREA+4         GET PREVIOUS SAVE
         LA    @15,16                 SET RETURN CODE
         ST    @15,16(,@01)           PLACE IT IN SAVE AEREA
         B     MODEXIT                LEAVE
         EJECT
***********************************************************************
*                                                                     *
* THIS SECTION SCANS THE PARM= STRING SPECIFIED ON THE EXEC STATEMENT *
* LOOKING FOR ANY OF THE FOLLOWING KEYWORDS:                          *
*                                                                     *
*     CCW    JOB   SORT  SMOD  IOSB                                   *
*     DATA   SIO   TEST  SUDATA                                       *
*                                                                     *
* IF ANY OF THE ABOVE KEYWORDS ARE FOUND, CONTROL IS PASSED TO A SUB- *
* ROUTINE TO READ AND PROCESS THE VALUE SPECIFIED FOR THE KEYWORD.    *
* PARAMETERS OTHER THAN THOSE IN THE ABOVE LIST ARE COPIED AND WILL   *
* BE PASSED TO THE 'NORMAL' SYSTEM GTF INITIALIZATION MODULE.         *
*                                                                     *
***********************************************************************
         SPACE 1
SCANIT   DS    0H
         L     @09,0(,@05)   GET FIRST PARM FIELD ADDRESS
         LH    @03,0(,@09)   GET LENGTH OF THE PARM STRING
         LTR   @05,@03       CHECK FOR ZERO PARM LENGTH         LZ90224
         BZ    LOAD          IF NONE GIVEN, GO TO NORMAL GTF    LZ90224
         CH    @03,=Y(L'TOPASS)  IS LENGTH GREATER THAN ALLOWED ?
         BH    MSG12         IF SO, ISSUE MSG AND GET OUT.
         LA    @04,TOPASS    AREA FOR PARMS TO BE PASSED TO NORMAL GTF
         SLR   @05,@05       LENGTH OF PARMS TO BE PASSED ON
         B     STRTSCAN      BEGIN PARAMETER PROCESSING
         SPACE 2
******************     DATA USED IN THIS SECTION     *****************
         SPACE 1
         DS    0F
PARMPTR  DC    XL1'80',AL3(PASSLEN)
PASSLEN  DC    H'0'
TOPASS   DC    CL110' '
         EJECT
****************************************************  F.BIEDERMANN  ***
*                                                                     *
*        KEYWORD STRING SCAN                                          *
*                                                                     *
****************************************************  F.BIEDERMANN  ***
         SPACE 3                                             BIEDERMANN
STRTSCAN DS    0H
         LA    @06,2(,@09)   SCAN POINTER INITIALIZATION
         LA    @03,0(@03,@06) SET TO END OF STRING TO BE SCANNED
         BCTR  @03,0         LAST VALID BYTE TO BE SCANNED
         OI    SCANFLAG,FIRSTKWD INDICATE LOOKING FOR 1ST NON-CCW KWD
PARMCHK  DS    0H                                            BIEDERMANN
         LA    @08,PARMTAB   START OF KEYWORD TABLE
         SLR   @10,@10       CLEAR FOR LATER USE
NEXTKWD  DS    0H                                            BIEDERMANN
         CLI   PKEYWRD,X'FF' SCAN END ?
         BE    NOTKNOWN      YES, LEAVE, BECAUSE ITS NOT KNOWN
         IC    @10,PALGTH    NO. OF BYTES TO COMPARE
         LR    @01,@03       GET SCAN END
         SR    @01,@06       GET SIZE REMAINING
         SR    @01,@10       SUBTRACT SIZE OF KEYWORD
         BM    NOTCOMP       SIZE IS TOO SMALL
         EX    @10,COMPARE   IS THIS A CCWTRACE KEYWORD?
         BE    MATCH
NOTCOMP  DS    0H
         LA    @08,PEND-PARMID+4(@08,@10) SEL SCAN TO NEXT CCWTRACE KWD
         SRL   @08,2         ROUND TO ADDRESS ALIGMENT
         SLL   @08,2         ROUND TO ADDRESS ALIGMENT
         B     NEXTKWD       CHECK NEXT CCWTRACE KEYWORD
         SPACE 1
NOTKNOWN DS    0H            KEYWORD IS NOT FOR CCWTRACE
         LA    @08,ZERRID    GET ERROR ENTRY TAB
         LR    @11,@06       START OF KEYWORD
NEXTBYTE DS    0H
         LA    @06,1(,@06)   START LOOKING FOR A COMMA
         CR    @06,@03       OR THE END OF THE STRING
         BH    STOPSCAN      END OF STRING?
         CLI   0(@06),C','   FOUND A COMMA?
         BNE   NEXTBYTE      IF NOT, GO CHECK NEXT CHARACTER
         CR    @06,@03       WAS ',' LAST ?
         BE    SCANERR       YES, ERROR
STOPSCAN DS    0H
         LR    @14,@06       LAST BYTE SCANNED
         SR    @14,@11       COMPUTE LENGTH TO BE MOVED
         BCTR  @14,0         SUBTRACT 1 FOR THE MVC
         LA    @07,0(@04,@05)
         TM    SCANFLAG,FIRSTKWD IS A COMMA NEEDED TO
         BO    FSTKEYWD      SEPARATE THE PARAMETERS?
         MVI   0(@07),C','   INSERT A COMMA
         LA    @07,1(,@07)
         LA    @05,1(,@05)   INCREMENT THE NUMBER OF BYTES TO PASS
COPYPARM DS    0H
         EX    @14,COPYPRM   COPY THE PARM TO BE PASSED
         AR    @05,@14       UPDATE @05 TO POINT TO THE
         LA    @05,1(,@05)   NEXT UNUSED BYTE
         LA    @06,1(,@06)   BUMP PAST THE COMMA
         SPACE 1                                            BIEDERMANN
***********************************************************************
*                                                                     *
* AT THIS POINT, R6 SHOULD POINT TO THE 1ST BYTE AFTER A COMMA        *
* SEPARATING TWO PARMS OR ELSE BEYOND THE END OF THE PARM STRING.     *
*                                                                     *
***********************************************************************
         SPACE 1                                            BIEDERMANN
         CR    @06,@03       MORE TO SCAN?
         BH    SCANDONE
         B     PARMCHK
         SPACE 1
FSTKEYWD DS    0H                                           BIEDERMANN
         SPACE 1
         NI    SCANFLAG,NOTFIRST   FROM NOW ON, NEED A COMMA
         B     COPYPARM
         SPACE 2
MATCH    DS    0H
         LA    @06,1(@06,@10) SET TO 1ST BYTE AFTER THE = SIGN
         L     @14,PARMID    SET THE HEX PROCEDURE ID
         L     @15,DEFINED   GET FLAG BYTES
         O     @14,DEFINED   SET FLAG BYTES
         CR    @14,@15       ALL OK ?
         BNE   MATCH01       YES, CONTINUE
         L     @14,PARMID    GET PARMID AGAIN
         O     @14,MULTIPLE  OR PARMID
         ST    @14,MULTIPLE  WRITE IT BACK
         LA    @08,ZENDID    SET TO END
         B     SCANERR       GO TO ERROR HANDLER
         SPACE 1
MATCH01  DS    0H
         ST    @14,DEFINED   WRITE IT BACK
         L     @15,PROCADDR  GET EXIT ADDRESS
         LTR   @15,@15       IS THERE ONE ?
         BCR   7,@15         YES, USE IT
         L     @14,PARMID    GET PARMID
         O     @14,TSUFLG    OR WITH FLAGS
         ST    @14,TSUFLG    STORE FLAGS BACK
         B     PROCEXIT      CONTINUE WITH NEXT PARM
         SPACE 1
COMPARE  CLC   0(0,@06),PKEYWRD
COPYPRM  MVC   0(0,@07),0(@11)
         SPACE 3
***************    DATA USED IN THE SCAN SECTION   ********************
         SPACE 1
SCANFLAG DC    X'00'    STATUS OF SCAN PROCESS
FIRSTKWD EQU   X'80'    FIRST NON-CCWTRACE KEYWORD STILL TO COME
NOTFIRST EQU   X'7F'
         SPACE 1
*        FOR DETAILED BIT INFORMATION, REFER TO THE PARMTABL
*        PARMID DEFINITION.
         SPACE 1
DEFINED  DC    F'0'     A BIT IS TURNED ON FOR EACH PARM PROCESSED
ERRDEF   DC    F'0'     A BIT IS TURNED ON FOR EACH PARM IN ERROR
MULTIPLE DC    F'0'     A BIT IF PARM GIVEN MORE THAN ONCE
TOOBIG   DC    F'0'     A PARAMETER VALUE IS NOT WITHIN RANGE
         SPACE 1
         SPACE 1
*        AREA TO BE MOVED TO AHLTSIO.
*        THIS FIELDS MUST BE KEPT ONE BEHIND THE OTHER, DO NOT CHANGE.
         SPACE 1
TSUFLG   DS    0F       PARM FOR GTFSIO
TSU1     DS    C        SUFLAG1
TSU2     DS    C        SUFLAG2
PSWITCH  DC    X'80'    DEFAULT = IO
IOFUNC   EQU   X'80'    IO ACTIVE
PSWITCH2 DC    X'00'    SECOND SWITCH BYTE
         SPACE 1
TRACEPRM DS    0F
CCWCNT   DC    F'50'    CCW  = 50 IS THE DEFAULT
DATACNT  DC    F'24'    DATA = 24 IS THE DEFAULT
         SPACE 1
*        FOR DETAILED BIT INFORMATION, REFER TO THE PARMTABL
*        PARMID DEFINITION.
         SPACE 1
JOBNAME  DC    CL8'        '
PRMEND   EQU   *        END LABEL
         EJECT
         SPACE 3
         SPACE
***********************************************************************
*                                                                     *
* ON ENTRY TO THE KEYWORD PROCESSING ROUTINES, REGISTER 6 POINTS TO   *
* THE 1ST BYTE AFTER THE = SIGN. REGISTER 10 CONTAINS THE LENGTH OF   *
* THE KEYWORD NOT INCLUDING THE = SIGN.  REGISTER 14 CONTAINS THE HEX *
* IDENTIFIER OF THE PARAMETER PROCESSING ROUTINE.  REGISTERS AVAIL-   *
* ABLE FOR USE WITHOUT RESTORING ARE 1, 10, 11, AND 15.  RETURN IS TO *
* THE LABEL 'CONTINUE'.  AT THE TIME OF RETURN, REGISTER 6 IS EXPEC-  *
* TED TO POINT TO THE BYTE BEYOND THE END OF THE PARAMETER VALUE AND  *
* ANY COMMA SEPARATING PARAMETERS.                                    *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* POSSIBLE VALUES FOR THE CCW= PARAMETER ARE EITHER 'NO' OR A DECIMAL *
* NUMBER BETWEEN 1 AND 512.  ANY OTHER VALUES WILL CAUSE A PARAMETER  *
* SPECIFICATION ERROR FLAG TO BE RAISED WHICH WILL EVENTUALLY CAUSE   *
* A MESSAGE TO BE WRITTEN DESCRIBING THE ERROR AND POSSIBLY EVEN      *
* TERMINATION OF THE ATTEMPT TO START CCWTRACE.                       *
*                                                                     *
***********************************************************************
         SPACE 2
CCW      DS    0H                                            BIEDERMANN
         LR    @15,@06                GET SCAN START         BIEDERMANN
         SR    @15,@03                SPACE LEFT ?           BIEDERMANN
         BNM   CCW001                 NO, DO NOT CHECK       BIEDERMANN
         CLC   0(2,@06),=C'NO'        IS IT 'NO' ?           BIEDERMANN
         BE    NOCCW                  GO SET CCW TO ZERO
CCW001   DS    0H                     NO SPACE LEFT ENTRY    BIEDERMANN
         SLR   @15,@15                CLEAR FOR LATER USE
         IC    @15,PKLGTH             GET MAXIMUM NUMBER OF DEC DIGITS
         BAL   @01,NUMBER             GO CONVERT IT TO HEX
         LTR   @15,@15                WAS A NUMBER RETURNED ?
         BM    SCANERR                IF MINUS, SCAN ERROR
         C     @15,MAXCCW             NUMBER EXCEEDS MAX VALUE ?
         BNH   CCWPOKAY               NO, WITHIN RANGE
         L     @14,PARMID             GET PARMID
         O     @14,TOOBIG             INDICATE PARM WAS TOO BIG
         ST    @14,TOOBIG             SAVE VALUE
         L     @15,MAXCCW             GET MAX CCW
CCWPOKAY DS    0H                                            BIEDERMANN
         ST    @15,CCWCNT             PUT THE VALUE IN THE TABLE
         L     @14,PARMID             GET PARM STRING
         O     @14,TSUFLG             COMBINE
         ST    @14,TSUFLG             STORE IT BACK
         B     PROCEXIT               CONTINUE WITH NEXT
         SPACE 1                                             BIEDERMANN
NOCCW    DS    0H                                            BIEDERMANN
         LA    @06,2(,@06)            SKIP TO AFTER 'NO'
         SLR   @15,@15                SET NUMBER OF CCWS TO ZERO
         ST    @15,CCWCNT             AND PUT IT IN THE TABLE
         L     @14,PARMID             REFRESH PARMID         BIEDERMANN
         LCR   @14,@14                GET COMPLEMENT
         N     @14,TSUFLG             SET FLAGS
         ST    @14,TSUFLG             STORE IT BACK
         B     PROCEXIT               CONTINUE WITH NEXT
         SPACE 2
MAXCCW   DC    F'512'
         EJECT
***********************************************************************
*                                                                     *
* ALLOWABLE VALUES FOR THE DATA= PARAMETER ARE EITHER 'NO' OR A       *
* DECIMAL NUMBER BETWEEN 1 AND 32,767.                                *
* VALUES OTHER THAN THOSE DESCRIBED WILL RAISE A PARAMETER SPECI-     *
* FICATION ERROR AND MAY TERMINATE THE TRACE ATTEMPT.                 *
*                                                                     *
***********************************************************************
         SPACE 2
DATA     DS    0H                                            BIEDERMANN
         LR    @15,@06                GET SCAN START         BIEDERMANN
         SR    @15,@03                SPACE LEFT ?           BIEDERMANN
         BNM   DATA01                 NO, DO NOT CHECK       BIEDERMANN
         CLC   0(2,@06),=C'NO'        IS DATE ='NO'
         BE    NODATA
DATA01   DS    0H                                            BIEDERMANN
         SLR   @15,@15                CLEAR FOR LATER USE    BIEDERMANN
         IC    @15,PKLGTH             MAXIMUM DIGITS
         BAL   @01,NUMBER             CONVERT IT TO HEX
         LTR   @15,@15                WAS A NUMBER RETURNED ?
         BM    SCANERR                NOT A VALID NUMBER
         C     @15,MAXDATA            MAKE SHURE, IT IS NOT TOO BIG
         BNH   DATAOKAY               IF OKEY, GO STORE IT
         L     @14,PARMID             GET PARMID
         O     @14,TOOBIG             SET VALUE WAS TOO BIG
         ST    @14,TOOBIG             SAVE VALUE
         L     @15,MAXDATA            SET IT TO MAXIMUM
DATAOKAY DS    0H                                            BIEDERMANN
         ST    @15,DATACNT            PUT IT TO THE PARM LIST
         B     PROCEXIT               GO TO NEXT KEYWORD
         SPACE 1                                             BIEDERMANN
NODATA   DS    0H
         LA    @06,2(,@06)            SKIP TO AFTER 'NO'
         SLR   @15,@15                SET DATA TO ZERO
         ST    @15,DATACNT            AND PUT IT IN THE PARM LIST
         B     PROCEXIT
         SPACE 2
MAXDATA  DC    F'32767'               MAXIMUM AMOUNT OF DATA TO TRACE
         EJECT
***********************************************************************
*                                                                     *
* ALLOWABLE VALUES FOR THE SIO=, SORT=, TEST, AND SMOD=               *
* PARAMETERS ARE YES AND NO.  ANYTHING ELSE WILL CAUSE A PARAMETER    *
* ERROR AND POSSIBLY TERMINATON OF THE TRACE ATTEMPT.                 *
*                                                                     *
***********************************************************************
         SPACE 3
SSTCOM   DS    0H                                            BIEDERMANN
         L     @14,PARMID             GET FUNCTION FLAGS
         LR    @15,@06                GET PARM START         BIEDERMANN
         SR    @15,@03                SPACE LEFT ?           BIEDERMANN
         BNM   SCANERR                NO, ERROR              BIEDERMANN
         CLC   0(2,@06),=C'NO'        IS PARM = 'NO' ?
         BE    COMNO                  PARM IS NO, BRANCH
         AH    @15,=Y(1)              CHECK LENGTH FOR 'YES' BIEDERMANN
         BNM   SCANERR                SPACE LEFT IN PARM ?   BIEDERMANN
         CLC   0(3,@06),=C'YES'       IS PARM = YES ?
         BNE   SCANERR                NEITHER YES NOR NO => BAD
COMYES   DS    0H                                            BIEDERMANN
         O     @14,TSUFLG             INDICATE FUNCTION TO BE ACTIVE
         ST    @14,TSUFLG             STORE IT BACK
         LA    @06,3(,@06)            SKIP TO AFTER 'YES'
         B     PROCEXIT               CONTINUE WITH NEXT FIELD
COMNO    DS    0H                                            BIEDERMANN
         L     @15,=F'-1'             SET FOR LATER USE      BIEDERMANN
         XR    @15,@14                GET A COMPLEMENT       BIEDERMANN
         N     @15,TSUFLG             TURN OFF THE FUNCTION FLAG BIT
         ST    @15,TSUFLG             STORE THE OFF VALUE BACK
         LA    @06,2(,@06)            BUMP SCAN POINTER TO AFTER 'NO'
         B     PROCEXIT               CONTINUE WITH NEXT FIELD
         EJECT
         SPACE 2
***********************************************************************
*                                                                     *
* NUMBER IS ENTERED WITH REGISTER 6 POINTING TO WHAT SHOULD BE A DECI-*
* MAL NUMBER STILL IN EBCDIC.  THE MAXIMUM NUMBER OF DIGITS ALLOWED   *
* IS SPECIFIED IN REGISTER 15.  THIS ROUTINE CAN HANDLE A MAXIMUM OF  *
* 6 DECIMAL DIGITS; NO CHECKS ARE MADE FOR INVALID COUNTS IN REG15.   *
* UPON RETURN FROM THIS ROUTINE, @15 WILL EITHER BE NEGATIVE,         *
* INDICATING AN ERROR IN THE PROCESS, OR THE HEXADECIMAL VALUE CORRES-*
* PONDING TO THE DECIMAL PARAMETER SPECIFIED.  REGISTER 6 ON RETURN   *
* SHOULD POINT TO THE NEXT BYTE FOLLOWING THE PARAMETER.  RETURN IS   *
* TO THE ADDRESS IN REGISTER 1.  REGISTERS AVAILABLE WITHOUT          *
* RESTORING ARE 10 AND 11.                                            *
*                                                                     *
***********************************************************************
         SPACE 2
NUMBER   DS    0H                                            BIEDERMANN
         LR    @11,@06                COPY START OF SCAN     BIEDERMANN
         SR    @11,@03                SPACE LEFT ?           BIEDERMANN
         BP    BADNUMBR               NO, LEAVE              BIEDERMANN
         SLR   @11,@11                NUMBER OF DIGITS PROCESSED
         MVI   DECNUMBR,C'0'          INITIALIZE THE FIELD TO ZERO
         MVC   DECNUMBR+1(5),DECNUMBR EBCDIC ZEROS
NXTDIGIT DS    0H                                            BIEDERMANN
         CLI   0(@06),C'0'   * BE SURE THAT
         BL    BADNUMBR      * THE NUMBER IS
         CLI   0(@06),C'9'   * BETWEEN ZERO
         BH    BADNUMBR      * AND NINE.
         LA    @10,DECNUMBR(@11)      OFFSET INTO FIELD
         MVC   0(1,@10),0(@06)        COPY THE DIGIT
         LA    @11,1(,@11)            INCREMENT THE COUNT READ
         LA    @06,1(,@06)            GET THE NEXT DIGIT
         CLI   0(@06),C','   * IF AT THE END OF THE NUMBER, AS
         BE    PACKIT        * INDICATED BY A COMMA OR BEING BEYOND
         CR    @06,@03       * THE END OF THE INPUT STRING,
         BH    PACKIT        * THEN GO PACK IT FOR CONVERSION.
         BCT   @15,NXTDIGIT           NOT MORE THAN SPECIFIED
         SPACE 2
PACKIT   DS    0H                                            BIEDERMANN
         BCTR  @11,0                  BACK UP ONE
         LA    @10,DECNUMBR(@11)      POINT TO LAST DIGIT
         NI    0(@10),X'CF'  SET A VALID ZONE,
         EX    @11,PACKINST           PACK THE NUMBER,
         CVB   @15,PKNUMBR            AND CONVERT TO HEX.
         BR    @01                    RETURN TO THE CALLER
         SPACE 2
BADNUMBR DS    0H                                            BIEDERMANN
         L     @15,=F'-1'             INDICATE BAD DECIMAL INPUT
         BR    @01                    RETURN TO CALLER
         SPACE 2
**********  DATA USED IN THIS SECTION  ******************************
         SPACE 1
PACKINST PACK  PKNUMBR(8),DECNUMBR(0)
DECNUMBR DC    C'000000'
PKNUMBR  DC    D'0'
         EJECT                                               BIEDERMANN
****************************************************  F.BIEDERMANN  ***
*                                                                     *
*        THE FOLLOWING SECTION WILL SCAN FOR JOB=NO OR JOB=JOBNAME.   *
*        IF A JOBNAME IS FOUND, THE JOB FUNCTION IS SWITCHED ON AND   *
*        THE NAME IS MOVED TO THE JOBNAME FIELD.  THE ONLY VALIDITY   *
*        CHECKS ARE THAT A NAME IS SPECIFIED AND IS NOT LONGER THAN   *
*        4 BYTES.  OS NAMING CONVENTIONS ARE NOT CHECKED.             *
*        EXIT IS TO THE COMMON EXIT PROCEDURE.                        *
*                                                                     *
****************************************************  F.BIEDERMANN  ***
         SPACE 1                                             BIEDERMANN
JOB      DS    0H                                            BIEDERMANN
         MVC   JOBNAME,BLANKS     MOVE BLANKS                BIEDERMANN
         LR    @15,@06            GET SCAN START             BIEDERMANN
         SR    @15,@03            SPACE LEFT ?               BIEDERMANN
         BP    PROCEXIT           NO, NOTHING SPECIFIED      BIEDERMANN
         SLR   @15,@15            GET SCAN END               BIEDERMANN
         IC    @15,PKLGTH         GET SCAN END               BIEDERMANN
         LA    @15,0(@06,@15)     GET SCAN END               BIEDERMANN
         LR    @10,@06            GET SCAN START             BIEDERMANN
JOBLOOP  DS    0H                                            BIEDERMANN
         CLI   0(@10),C','        END FOUND ?                BIEDERMANN
         BE    JOBFOUND           YES, LEAVE                 BIEDERMANN
         CR    @15,@10            EXCEEDS JOBNAME ?          BIEDERMANN
         BNH   SCANERR            OK, LEAVE                  BIEDERMANN
         CR    @10,@03            PARM END ?                 BIEDERMANN
         LA    @10,1(,@10)        INCREASE SCAN ADDRESS      BIEDERMANN
         BL    JOBLOOP            NO, RETRY                  BIEDERMANN
JOBFOUND DS    0H                                            BIEDERMANN
         SR    @10,@06            FIND TRUE LENGTH           BIEDERMANN
         BNP   PROCEXIT           NOTHING SPECIFIED          BIEDERMANN
         L     @14,PARMID         REFRESH PARMID             BIEDERMANN
         BCTR  @10,0              DECREASE ONE FOR EX        BIEDERMANN
         CH    @10,=Y(1)          SPACE LEFT FOR 'NO' ?      BIEDERMANN
         BNE   JOBF1              NO, SKIP NEXT TEST         BIEDERMANN
         CLC   0(2,@06),=C'NO'    IS IT NO ?                 BIEDERMANN
         BE    COMNO              ITS NO, GO TO PROCESS      BIEDERMANN
JOBF1    DS    0H                                            BIEDERMANN
         EX    @10,JOBMOVE        MOVE JOBNAME WITH EX       BIEDERMANN
         O     @14,TSUFLG         COMBINE IT                 BIEDERMANN
JOBF2    DS    0H                                            BIEDERMANN
         ST    @14,TSUFLG         STORE IT BACK              BIEDERMANN
         LA    @06,1(@06,@10)     ADJUST SCAN POINTER        BIEDERMANN
         B     PROCEXIT           USE COMMON ROUTINES        BIEDERMANN
         SPACE 1
JOBMOVE  MVC   JOBNAME(0),0(@06)  MOVE JOBNAME               BIEDERMANN
BLANKS   DC    CL8' '
         EJECT                                               BIEDERMANN
****************************************************  F.BIEDERMANN  ***
*                                                                     *
*        THE FOLLOWING SECTION WILL SCAN FOR SUDATA=NO OR SUDATA=XXXX *
*        IF A VALUE IS FOUND, THE SU FUNCTION IS SWITCHED ON AND      *
*        THE VALUES ARE MOVED TO THE TSU1 AND/OR THE TSU2 FIELD. THE  *
*        ONLY CHECKS ARE, THAT THE TEXT IS NOT LONGER THAN 4 BYTES    *
*        AND THE VALUES ARE HEX DATA.                                 *
*                                                                     *
*        EXIT IS TO THE COMMON EXIT PROCEDURE.                        *
*                                                                     *
****************************************************  F.BIEDERMANN  ***
         SPACE 1                                             BIEDERMANN
SUDATA   DS    0H                                            BIEDERMANN
         LR    @15,@06            GET SCAN START             BIEDERMANN
         SR    @15,@03            CHECK FOR SPACE LEFT       BIEDERMANN
         BP    PROCEXIT           NO, NOTHING SPECIFIED      BIEDERMANN
         SLR   @15,@15            GET SCAN END               BIEDERMANN
         IC    @15,PKLGTH         GET SCAN END               BIEDERMANN
         LA    @15,0(@06,@15)     GET SCAN END               BIEDERMANN
         LR    @10,@06            GET SCAN START             BIEDERMANN
SULOOP   DS    0H                                            BIEDERMANN
         CLI   0(@10),C','        END FOUND ?                BIEDERMANN
         BE    SU001              YES, LEAVE                 BIEDERMANN
         CR    @15,@10            EXCEEDS SUFLAG SIZE ?      BIEDERMANN
         BNH   SCANERR            OK, LEAVE                  BIEDERMANN
         CR    @10,@03            PARM END ?                 BIEDERMANN
         LA    @10,1(,@10)        INCREASE SCAN ADDRESS      BIEDERMANN
         BL    SULOOP             NO, RETRY                  BIEDERMANN
SU001    DS    0H                                            BIEDERMANN
         SR    @10,@06            FIND TRUE LENGTH           BIEDERMANN
         BNP   PROCEXIT           NOTHING SPECIFIED          BIEDERMANN
         L     @14,PARMID         REFRESH PARMID             BIEDERMANN
         CH    @10,=Y(2)          LENGTH OF 'NO' ?           BIEDERMANN
         BNE   SU002              NO, SKIP NEXT TEST         BIEDERMANN
         CLC   0(2,@06),=C'NO'    IS IT NO ?                 BIEDERMANN
         BNE   SCANERR            NO, PROCESS SU STRING      BIEDERMANN
         B     COMNO              USE COMMON ROUTINES        BIEDERMANN
         SPACE 1
SU002    DS    0H                                            BIEDERMANN
         SLR   @01,@01            CLEAR FOR LATER USE        BIEDERMANN
         IC    @01,PKLGTH         GET LENGTH                 BIEDERMANN
         CR    @10,@01            SCHOULD BE SAME            BIEDERMANN
         BNE   SCANERR            IS NOT, ELSE ERROR         BIEDERMANN
         BAL   @01,TOHEX          CONVERT HEX VALUE          BIEDERMANN
         LTR   @15,@15            VALUE VALID ?              BIEDERMANN
         BM    SCANERR            NO, ERROR                  BIEDERMANN
*        L     @14,PARMID         REFRESH PARMID             BIEDERMANN
         SLL   @15,16             GET IT TO HIGH ORDER       BIEDERMANN
         OR    @14,@15            SET IT IN @14              BIEDERMANN
         O     @14,TSUFLG         COMBINE IT                 BIEDERMANN
         ST    @14,TSUFLG         STORE IT BACK              BIEDERMANN
         LA    @06,0(@06,@10)     ADJUST SCAN POINTER        BIEDERMANN
*        B     PROCEXIT           USE COMMON ROUTINES        BIEDERMANN
         SPACE 1
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
* PROCEXIT IS ENTERED WITH REG6 POINTING TO THE BYTE AFTER THE LAST   *
* CHARACTER OF THE PARAMETER VALUE.  REG14 CONTAINS THE HEX ID OF THE *
* ROUTINE WHICH BRANCHED TO PROCEXIT.  THE BYTE POINTED TO BY REG6    *
* SHOULD BE EITHER A COMMA OR ELSE BEYOND THE END OF THE INPUT STRING.*
* IF AN ERROR SITUATION IS FOUND, THE PROCEDURE ID SUPPLIED IN REG14  *
* WILL BE OR'ED INTO THE ERRDEF BYTES TO AID IN PROBLEM DETERMIN-     *
* ATION.  CONTROL RETURNS IN ANY CASE TO THE LABEL 'CONTINUE'.        *
*                                                                     *
***********************************************************************
         SPACE 3
PROCEXIT DS    0H
         LA    @08,ZERRID    SET TO UNRELATED ERROR
         CR    @06,@03       IS IT BEYOND THE END OF THE INPUT ?
         BH    SCANDONE      IF SO, LEAVE
         CLI   0(@06),C','   IS IT A COMMA ?
         BNE   SCANERR       ELSE SCAN ERROR                 BIEDERMANN
         LA    @06,1(,@06)   BUMP PAST THE COMMA
         CR    @06,@03       WAS A COMMA THE LAST
         BH    BADEXIT       CHARACTER IN THE INPUT STRING ?
         B     PROCEX01
         SPACE 2
*  IF HERE, THERE'S SOMETHING WRONG WITH THE PARAMETER.
         SPACE 1
SCANERR  DS    0H            ERROR ENTRY POINT               BIEDERMANN
         CLI   0(@06),C','   PARAMETER END ?                 BIEDERMANN
         LA    @06,1(,@06)   SKIP                            BIEDERMANN
         BE    BADEXIT       YES, LEAVE                      BIEDERMANN
         CR    @06,@03       FIELD END ?                     BIEDERMANN
         BNH   SCANERR       NO, RETRY                       BIEDERMANN
BADEXIT  DS    0H                                            BIEDERMANN
         L     @14,PARMID    REFRESH PARM ID
         O     @14,ERRDEF    SET ON ERROR FLAG
         ST    @14,ERRDEF    WRITE IT BACK
PROCEX01 DS    0H            AND RETURN TO THE MAINLINE
         CR    @06,@03       MORE TO SCAN?
         BNH   PARMCHK
*        B     SCANDONE      THIS IS AT NEXT PAGE            BIEDERMANN
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
* REGISTER USAGE AT THIS POINT IS AS FOLLOWS:                         *
* @00,@01                    VOLATILE - - USE WITH CAUTION            *
* @02                        RESERVED TO KEEP THE EP OF GTFSIO        *
* @03,@04                    FREE, AVAILABLE FOR USE                  *
* @05    LENGTH OF DATA TO PASS TO NORMAL GTF INITIALIZATION MODULE   *
* @06,@07,@08,@09,@10,@11    FREE, AVAILABLE FOR USE                  *
* @12                        BASE REGISTER                            *
* @13                        SAVE AREA POINTER                        *
* @14,@15                    VOLATILE - - USE WITH CAUTION            *
*                                                                     *
***********************************************************************
*                                                                     *
* AT THIS POINT, ALL OF THE CCWTRACE KEYWORDS IN THE PARAMETER STRING *
* HAVE BEEN PROCESSED.  CHECKS ARE MADE FOR PARAMETER CORRECTNESS,    *
* LIMITS EXCEEDED, AND MULTIPLE SPECIFICATIONS.  IF ANY ERROR FLAGS   *
* ARE SET, A MESSAGE DESCRIBING THE ERROR IS ISSUED.  IF TRACING IS   *
* TO BE ACTIVE, MODULES NEED TO BE FIXED AND PARAMETER INFO PASSED ON *
* TO THESE MODULES.  IN EITHER CASE, THE LIST OF NON-CCWTRACE PARMS   *
* IS PASSED ON TO NORMAL GTF INITIALIZATION.                          *
*                                                                     *
***********************************************************************
         SPACE 2
SCANDONE DS    0H                                            BIEDERMANN
         L     @15,TOOBIG    ANY LIMITS EXCEEDED ?
         LTR   @15,@15       ANY LIMITS EXCEEDED ?
         BNZ   MSG02         IF SO, PRINT A MESSAGE
CHKMULT  DS    0H                                            BIEDERMANN
         L     @15,MULTIPLE  ANY PARMS TWICE ?
         LTR   @15,@15       IS IT ?
         BNZ   MSG03         YES, PRINT A MESSAGE
CHKFATAL DS    0H                                            BIEDERMANN
         L     @15,ERRDEF    GET ERROR FLAGS
         LTR   @15,@15       ANY ONES ?
         BNZ   MSG04         IF NOT ZERO, WRITE MESSAGE
         L     @15,CCWCNT    GET CCWCOUNT
         LTR   @15,@15       ANY CCWS ?
         BNZ   LOAD          YES, GO TO LOAD
         NC    TSUFLG,TESTID TURN OFF ALL FUNCTIONS BUT TEST
*        B     LOAD          GO TO LOAD
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
* THE MODULE TO PERFORM THE TRACE RESIDES IN LPA.  A LOAD IS DONE TO  *
* OBTAIN ITS ADDRESS AND LENGTH.  THIS IS USED TO BUILD THE FIX LIST. *
* AFTER THAT, THE MODULE IS DELETED AND LOADED AGAIN, BY ITS          *
* ENTRYPOINT. BECAUSE, IT REQUIRES SOME DYNAMIC MODIFICATION. THE     *
* LOAD IS FIXED.                                                      *
*                                                                     *
***********************************************************************
         SPACE 3
         USING GTFSIO,@02                                    BIEDERMANN
         SPACE 1                                             BIEDERMANN
LOAD     DS    0H                                            BIEDERMANN
         LOAD   EPLOC=AHLTSIO
         LR    @02,@00          STARTING ADDRESS
         LA    @02,0(,@02)      CLEAR FLAGS                  BIEDERMANN
         SLL   @01,3            ADJUST LENGTH IN DOUBLE WORDS
         LA    @03,0(@01,@02)   END OF MODULE
         BCTR  @03,0            END OF MODULE
         STM   @02,@03,PAGEFIX  ADDRESS RANGE OF MODULE
         DELETE EPLOC=AHLTSIO
         LOAD   EPLOC=CCWTRACE
         LR    @02,@00          STARTING ADDRESS
         LA    @02,0(,@02)      CLEAR FOR COMPARE            BIEDERMANN
         CLC   GTFCCWID,AHLID   GTFSIO SAME LEVEL ?          BIEDERMANN
         BNE   IDERROR          NO, ERROR                    BIEDERMANN
         C     @02,PAGEFIX      WITHIN RANGE ?               BIEDERMANN
         BL    IDERROR          NO, ERROR                    BIEDERMANN
         C     @02,PAGEFIX+4    WITHIN RANGE ?               BIEDERMANN
         BH    IDERROR          NO, ERROR                    BIEDERMANN
         B     FIX                                           BIEDERMANN
         SPACE 2
         DS    0F                                            BIEDERMANN
CCWTRACE DC    CL8'GTFSIO'      LIST TO GET THE EP           BIEDERMANN
AHLTSIO  DC    CL8'AHLTSIO'     LIST TO BUILD THE FIX LIST   BIEDERMANN
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
* IN ORDER TO FIX THE TRACE MODULE, THE PARAMETER LIST DESCRIBING THE *
* BOUNDS OF THE MODULE MUST FIRST BE FIXED.  THAT IS DONE WITH A      *
* SHORT-TERM FIX.  IF THE PARAMETER LIST IS FIXED OKAY, A LONG-TERM   *
* FIX IS DONE FOR THE TRACING MODULE IN LPA.  IF THAT IS OKAY, THE    *
* SHORT TERM FIX FOR THE PARAMETER LIST IS RELEASED.  IF EITHER OF    *
* PAGE FIX ATTEMPTS FAIL, A MESSAGE IS ISSUED AND THE ATTEMPT TO      *
* START THE TRACE IS TERMINATED.                                      *
*                                                                     *
***********************************************************************
         SPACE 3
FIX      DS    0H
         OI    PAGEFIX+4,X'80'  SET LAST INDICATOR IN RANGE
         XC    ECB,ECB          CLEAR ECB                    BIEDERMANN
         PGFIX R,A=PAGEFIX,EA=FIXEND,ECB=ECB,LONG=N
         ST    @15,FIXRET       SAVE RETURN CODE             BIEDERMANN
         CH    @15,=Y(8)        AWAITING COMPLETION ?
         BNE   PAGEFIX1         NO, SKIP WAIT
         WAIT  ECB=ECB          WAIT FOR COMPLETION
PAGEFIX1 DS    0H
         TM    ECB+3,X'FF'      PARAMETER LIST FIXED ?
         BNZ   FIXERROR         IF NOT, ISSUE ERROR MESSAGE
         EJECT
LONGFIX  XC    ECB,ECB          CLEAR THE ECB FOR THE NEXT REQUEST
         PGFIX L,LA=PAGEFIX,ECB=ECB,LONG=Y
         ST    @15,FIXRET       SAVE RETURN CODE             BIEDERMANN
         CH    @15,=Y(8)        AWAITING COMPLETION ?
         BNE   PAGEFIX2         NO, SKIP WAIT
         WAIT  ECB=ECB
PAGEFIX2 DS    0H
         TM    ECB+3,X'FF'      PAGE FIXED ?
         BNZ   LFIXERR          IF NOT, ISSUE ERROR MESSAGE
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
* SO FAR, SO GOOD.  NOW RELEASE THE PAGEFIX PARAMETER LIST AND SETUP  *
* TO PASS CONTROL TO THE SYSTEM-SUPPLIED GTF INITIALIZATION MODULE.   *
*                                                                     *
***********************************************************************
         SPACE 3
         XC    ECB,ECB
         PGFREE R,A=PAGEFIX,EA=FIXEND,ECB=ECB
         ST    @15,FIXRET       SAVE RETURN CODE             BIEDERMANN
         LTR   @15,@15          OK  ?
         BNZ   FREERROR         IF NOT, ISSUE ERROR MESSAGE
         B     TOGTF            THIS IS ON NEXT PAGE
         SPACE 2
****************     DATA USED IN FIX PROCESS    **************
         SPACE 1
         DS    0D                                            BIEDERMANN
PAGEFIX  DC    2F'0'
FIXEND   EQU   *                                             BIEDERMANN
ECB      DC    F'0'
FIXRET   DC    F'0'
         EJECT
***********************************************************************
*                                                                     *
* AT THIS POINT, ALL CCWTRACE PARAMETERS HAVE BEEN HANDLED.  CONTROL  *
* IS PASSED TO THE NORMAL GTF INITIALIZATION MODULE ALONG WITH ANY    *
* NON-CCWTRACE PARAMETERS.                                            *
*                                                                     *
* TO PATCH GTFSIO IF REQUIRED, WITHOUT CLPA, THE FOLLOWING REG WILL   *
* BE SET :                                                            *
*          @01 POINTS TO THE PATCH AEREA OF GTFCCW                    *
*          @02 POINTS TO THE EP OF GTFSIO                             *
*          @03 POINTS TO THE EP OF GTFSIO + 4096                      *
*                                                                     *
***********************************************************************
         SPACE 3
TOGTF    DS    0H                                            BIEDERMANN
         LA    @01,TSUFLG        SET PARM TO START TRACE     BIEDERMANN
         L     @15,GTFINIT       GET INIT ROUTINE            BIEDERMANN
         BALR  @14,@15           GO, INIT CCWTRACE           BIEDERMANN
         LTR   @15,@15           DOES IT WORK ?              BIEDERMANN
         BNZ   WTO14             NO, PRINT MESSAGE           BIEDERMANN
         STH   @05,PASSLEN       LENGTH OF GTF PARAMETERS
         LA    @01,PATCH         GET MY PATCH AEREA
         LR    @03,@02           SET UP FOR EP + 4096
         A     @03,=A(4096)      ADD TO SECOND GTFSIO AEREA
         DC    10XL4'07000700'   TO REP GTFSIO PATCHES
         LA    @01,PARMPTR       SET UP PARM REGISTER
         SPACE 1
         LINK  EP=AHLGTF         NORMAL GTF START UP
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
* CONTROL HAS BEEN RETURNED FROM THE GTF TRACE PROCESS.  INDICATE     *
* THAT FACT VIA A WTO.  CHECK RETURN CODES AND THE TEST BIT SETTING   *
* TO SEE IF A DUMP IS TO BE TAKEN.                                    *
*                                                                     *
***********************************************************************
         SPACE 3
         STM   @00,@15,ABNDSAVE SAVE IN CASE A DUMP IS TAKEN
         L     @15,GTFTERM       GET TERMINATION ROUTINE     BIEDERMANN
         BALR  @14,@15           GO TO TERMINATE CCWTRACE    BIEDERMANN
WTO7     WTO   'CCWTRACE I07.  TRACING STOPPED.',ROUTCDE=(2,11)
         L     @15,ABNDSAVE+L'ABNDSAVE*@15       RETURN CODE
         L     @01,SAVEAREA+4   GET PREVIOUS SAVE            BIEDERMANN
         ST    @15,16(,@01)     SAVE RETURN CODE             BIEDERMANN
         CH    @15,=Y(8)        WAS RC MORE THAN 8 ?
         BNL   MSG8             IF SO, GO TO GET A DUMP
         L     @01,TESTID       LOAD THE TEST FUNCTION ID
         N     @01,TSUFLG       IS IT TEST ?                 BIEDERMANN
         BNZ   MSG9             IF TEST, ISSUE MSG AND TAKE DUMP
*        B     NORMEND          IF NOT, PERFORM NORMAL EXIT
         EJECT
***********************************************************************
*                                                                     *
* NORMAL END PROCESSING INVOLVES:                                     *
*    1. RELEASING ANY PAGES THAT WERE FIXED                           *
*    2. DELETING THE TRACE MODULE IF IT WAS LOADED                    *
*    3. DEQUEUEING FROM THE CCWTRACE RESOURCE                         *
*                                                                     *
* TO FREE THE FIXED PAGES, THE PARAMETER LIST MUST AGAIN BE FIXED.    *
* FAILURE IN THIS ATTEMPT OR IN THE FREE ATTEMPT WILL CAUSE A MESSAGE *
* TO BE ISSUED AND, IF TEST = YES WAS SPECIFIED, WILL ALSO CAUSE A    *
* DUMP TO BE TAKEN.                                                   *
*                                                                     *
***********************************************************************
         SPACE 3
NORMEND  DS    0H
         XC    ECB,ECB
         PGFIX R,A=PAGEFIX,EA=FIXEND,ECB=ECB,LONG=N
         ST    @15,FIXRET       SAVE RETURN CODE             BIEDERMANN
         CH    @15,=Y(8)        AWAITING COMPLETION ?
         BNE   PAGEFIX4         NO, SKIP WAIT
         WAIT  ECB=ECB                                       BIEDERMANN
PAGEFIX4 DS    0H                                            BIEDERMANN
         TM    ECB+3,X'FF'
         BNZ   TYPEF
         XC    ECB,ECB                                       BIEDERMANN
TRCDONE  DS    0H                                            BIEDERMANN
         PGFREE R,A=PAGEFIX,ECB=ECB
         ST    @15,FIXRET       SAVE RETURN CODE             BIEDERMANN
         LTR   @15,@15          OK ?                         BIEDERMANN
         BNZ   TYPEL            N0, LEAVE                    BIEDERMANN
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
* THE ONLY THING LEFT NOW IS TO RELEASE THE PAGEFIX PARAMETER LIST,   *
* DELETE THE TRACE MODULE IF IT WAS PREVIOUSLY LOADED,                *
* DEQUEUE FROM THE CCWTRACE RESOURCE, SUPPLY THE GTF PROVIDED RETURN  *
* CODE, AND GET OUT.                                                  *
*                                                                     *
***********************************************************************
         SPACE 3
         XC    ECB,ECB
         PGFREE R,A=PAGEFIX,EA=FIXEND,ECB=ECB
         ST    @15,FIXRET       SAVE RETURN CODE             BIEDERMANN
         LTR   @15,@15          OK ?                         BIEDERMANN
         BNZ   FREEFAIL         NO, LEAVE                    BIEDERMANN
         DELETE EPLOC=CCWTRACE
         SPACE 3
         DROP  @02                                           BIEDERMANN
         SPACE 1                                             BIEDERMANN
MODEXIT  DS    0H                                            BIEDERMANN
         DEQ   (QNAME,RNAME,RNMLEN,SYSTEM),RET=HAVE
         L     @13,SAVEAREA+4   GET ADDRESS OF PREVIOUS SAVE
         LM    @14,@12,12(@13)                               BIEDERMANN
         BR    @14
         EJECT
***********************************************************************
*                                                                     *
* CCWTRACE I02. PARM OUT OF RANGE.  MAX ASSUMED. DIAGNOSTIC BYTE = XX.*
* AFTER WRITING THIS MESSAGE, CONTROL RETURNS TO THE MAINLINE SINCE   *
* THIS IS NOT A TERMINATING ERROR.                                    *
*                                                                     *
***********************************************************************
         SPACE 2
MSG02    DS    0H                                            BIEDERMANN
         BAL   @14,TOEBCDIC     CONVERT HEX TO EBCDIC
         STCM  @15,15,TEXT02+75 PUT IN THE DIAGNOSTIC BYTE
TEXT02   WTO   'CCWTRACE I02.  PARM OUT OF RANGE.  MAX ASSUMED.  DIAGNO*
               STIC BYTE = XXXX.',ROUTCDE=(2,11)
         B     CHKMULT          RETURN TO MAINLINE
         EJECT                                               BIEDERMANN
         SPACE 3
***********************************************************************
*                                                                     *
* CCWTRACE I03.  MULTIPLE PARM SPECIFICATION.  LAST USED.  DIAGNOSTIC *
* BYTE = XX.  AFTER WRITING THIS MESSAGE, CONTROL RETURNS TO THE      *
* MAINLINE SINCE THIS IS NOT A TERMINATING ERROR.                     *
*                                                                     *
***********************************************************************
         SPACE 2
MSG03    DS    0H                                            BIEDERMANN
         BAL   @14,TOEBCDIC     CONVERT HEX TO EBCDIC
         STCM  @15,15,TEXT03+83 PUT IN THE DIAGNOSTIC BYTE
TEXT03   WTO   'CCWTRACE I03.  MULTIPLE PARM SPECIFICATION.  LAST USED.*
                 DIAGNOSTIC BYTE = XXXX.',ROUTCDE=(2,11)
         B     CHKFATAL         RETURN TO MAINLINE
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
* CCWTRACE T04. PARAMETER ERROR(S).  DIAGNOSTIC BYTE = XX.  AFTER     *
* WRITING THIS MESSAGE, CONTROL PASSES TO THE MODULE EXIT.            *
*                                                                     *
***********************************************************************
         SPACE 3
MSG04    DS    0H                                            BIEDERMANN
         BAL   @14,TOEBCDIC     CONVERT HEX TO EBCDIC
         STM   @00,@15,ABNDSAVE SAVE REGS IN CASE A DUMP IS TAKEN
         STCM  @15,15,TEXT04+62 PUT IN THE DIAGNOSTIC BYTE
TEXT04   WTO   'CCWTRACE T04.  PARAMETER ERROR(S).  DIAGNOSTIC BYTE = X*
               XXX.',ROUTCDE=(2,11)
         L     @01,TESTID       IS TEST OPTION SET ?
         N     @01,TSUFLG       IS TEST OPTION SET ?
         BNZ   ABEND            IF SO, GET A DUMP
         LA    @15,12           IF NOT, SET A RETURN CODE OF 12
         L     @01,SAVEAREA+4   GET PREVIOUS SAVE AREA       BIEDERMANN
         ST    @15,16(,@01)     AND SET IT FOR EXIT
         B     MODEXIT          STOP CCWTRACE INITIALIZATION
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
* CCWTRACE T05.  TRACE PROGRAM COULD NOT BE FIXED.  A FATAL PROBLEM.  *
*                                                                     *
***********************************************************************
         SPACE 3
FREERROR DS    0H                                           BIEDERMANN
         MVI   WTO5+65,C'F'     INDICATE FREE OF PARM LIST FAILED
         B     FIXERROR         ISSUE THE MESSAGE
         SPACE 1
LFIXERR  DS    0H                                           BIEDERMANN
         MVI   WTO5+65,C'L'     INDICATE LONG TERM FIX FAILED
FIXERROR DS    0H                                           BIEDERMANN
         STM   @00,@15,ABNDSAVE IN CASE A DUMP IS TAKEN
WTO5     WTO   'CCWTRACE T05.  TRACE PROGRAM COULD NOT BE FIXED.  TYPE *
               = S.',ROUTCDE=(2,11)
         L     @01,TESTID       IS TEST OPTION SET ?
         N     @01,TSUFLG       IS TEST OPTION SET ?
         BNZ   ABEND            IF SO, TAKE A DUMP
         LA    @15,16           IF NOT, GIVE A RETURN CODE OF 16
         L     @01,SAVEAREA+4   GET PREVIOUS SAVE AREA       BIEDERMANN
         ST    @15,16(,@01)     AND SET IT FOR EXIT
         B     MODEXIT
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
* CCWTRACE T06.  TRACE PROGRAM COULD NOT BE FREED.  A FATAL PROBLEM.  *
*                                                                     *
***********************************************************************
         SPACE 2
TYPEL    DS    0H                                            BIEDERMANN
         MVI   WTO6+65,C'L'     SHOW ERROR IN FREE OF LONG TERM FIX
         B     FREEFAIL         GO ISSUE THE MESSAGE
         SPACE 1                                             BIEDERMANN
TYPEF    DS    0H                                            BIEDERMANN
         MVI   WTO6+65,C'F'     INDICATE FIX FAILURE
FREEFAIL DS    0H                                            BIEDERMANN
         STM   @00,@15,ABNDSAVE SAVE IN CASE DUMP IS TAKEN
WTO6     WTO   'CCWTRACE T06.  TRACE PROGRAM COULD NOT BE FREED.  TYPE *
               = S.',ROUTCDE=(2,11)
         L     @01,TESTID       IS TEST OPTION SET ?
         N     @01,TSUFLG       IS TEST OPTION SET ?
         BNZ   ABEND            IF SO, GO TAKE A DUMP
         LA    @15,16           IF NOT, GIVE A RETURN CODE OF 16
         L     @01,SAVEAREA+4   GET PREVIOUS SAVE AREA       BIEDERMANN
         ST    @15,16(,@01)     AND SET IT FOR EXIT
         B     MODEXIT
         EJECT
***********************************************************************
*                                                                     *
* CCWTRACE T08.  TRACE RETURN CODE IS XX.  DUMP FOLLOWS. THIS MESSAGE *
* IS ISSUED WHEN THE RETURN CODE FROM THE SYSTEM TRACE MODULES IS     *
* EIGHT OR GREATER.  IT IS NOT DEPENDENT ON THE TEST PARAMETER.       *
*                                                                     *
***********************************************************************
         SPACE 3
MSG8     DS    0H
         BAL   @14,TOEBCDIC     CONVERT RETURN CODE TO PRINTABLE CHARS
         STCM  @15,15,TEXT8+44  PUT RETURN CODE INTO MESSAGE TEXT
TEXT8    WTO   'CCWTRACE T08.  TRACE RETURN CODE IS XXXX. DUMP FOLLOWS.C
               ',ROUTCDE=(2,11)
         B     ABEND
         EJECT
***********************************************************************
*                                                                     *
* CCWTRACE T09.  DUMPING BECAUSE TEST = YES.       THIS MESSAGE IS    *
* ISSUED WHEN THE RETURN CODE FROM THE TRACE ROUTINES IS BETWEEN ZERO *
* AND EIGHT AND TRACE = YES WAS SPECIFIED IN THE INPUT PARAMETERS.    *
*                                                                     *
***********************************************************************
         SPACE 3
MSG9     DS    0H
WTO9     WTO   'CCWTRACE T09.  DUMPING BECAUSE TEST = YES.',           *
               ROUTCDE=(2,11)
         B     ABEND
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
*  CCWTRACE T12.  PARAMETER LIST TOO LONG.      THIS MESSAGE IS       *
*  ISSUED WHEN THE LENGTH OF THE PARAMETER LIST PASSED TO GTFCCW IS   *
*  LONGER THAN 110 BYTES.  A DUMP IS TAKEN TO HELP DETERMINE WHERE    *
*  THE INCORRECT LENGTH OCCURRED.                                     *
*                                                                     *
***********************************************************************
         SPACE 3
MSG12    DS    0H
         STM   @00,@15,ABNDSAVE   SAVE REGISTERS FOR ABEND
WTO12    WTO   'CCWTRACE T12.  PARAMETER LIST TOO LONG.',              *
               ROUTCDE=(2,11)
         B     ABEND
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
*  CCWTRACE T13.  MODID OF GTFCCW DOES NOT MATCH MODID OF GTFCCW.     *
*  THIS MSG IS ISSUED AFTER LOAD OF AHLTSIO, IF A COMPARE BEETWEEN    *
*  THE MOD ID OF GTFCCW AND AHLTSIO IS DIFFERENT. A DUMP IS TAKEN     *
*  TO GET THE MODULES LISTED.                                         *
*                                                                     *
***********************************************************************
         SPACE 3                                             BIEDERMANN
IDERROR  DS    0H                     MSG13                  BIEDERMANN
         SPACE 1                                             BIEDERMANN
         STM   @00,@15,ABNDSAVE     SAVE REGISTERS FOR ABEND BIEDERMANN
         SPACE 1                                             BIEDERMANN
MSG13    WTO   'CCWTRACE T13.  MODID OF AHLTSIO DOES NOT MATCH MODID OFX
                GTFCCW.',ROUTCDE=(2,11)                      BIEDERMANN
         B     ABEND                GO TO GET A DUMP         BIEDERMANN
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        A PROBLEM OCCURED, WILL TRYING TO INIT GTFSIO. AT END OF     *
*        MESSAGE, PROGRAM WILL GO TO ABEND.                           *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
MSG14    DS    0H
WTO14    WTO   'CCWTRACE T14.  INITIALIZATION ERROR FOR MODULE GTFSIO',*
               ROUTCDE=(2,11)
         B     ABEND
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
* A DUMP IS TAKEN UNDER THE FOLLOWING CONDITIONS:                     *
*                                                                     *
*   1.) A PARAMETER SPECIFICATION ERROR WAS MADE WHICH PREVENTS       *
*       SUCCESSFUL INITIALIZATION OF THE CCWTRACE FUNCTION --         *
*       PROVIDING THE TEST PARAMETER WAS SPECIFIED AND WAS CORRECTLY  *
*       PROCESSED.                                                    *
*   2.) THE TRACE ROUTINE RETURNS A CODE OF 8 OR GREATER IN REG15.    *
*       THE TEST PARAMETER IS NOT CHECKED IN THIS CASE.               *
*   3.) TEST = YES WAS SPECIFIED.                                     *
*                                                                     *
*  AT THE TIME THE DECISION IS MADE TO TAKE A DUMP, THE REGISTERS     *
*  HAVE BEEN SAVED IN 'ABNDSAVE' SO THAT THEY ARE NOT LOST DUE TO     *
*  WTO'S OR OTHER PROCESSING BEFORE THE DUMP CAN BE TAKEN.            *
*                                                                     *
***********************************************************************
         SPACE 3
ABEND    DS    0H                                            BIEDERMANN
         LM    @00,@15,ABNDSAVE RECOVER REGS FROM TIME OF 'ERROR'
         SPACE 1
         ABEND 122,DUMP
         SPACE 1
         DS    0D
ABNDSAVE DC    16F'-1'
         EJECT
         SPACE 2
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING ROUTINE IS PROVIDED TO CONVERT A EBCDIC STRING *
*        IN A HALFWORD FORMAT. IT IS ENTERED WITH RETURN ADDRESS IN   *
*        @01, LENGTH OF DATA IN @10, AND DATATEXT IN @06.             *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 2
TOHEX    DS    0H
         LTR   @10,@10                IS THERE A LENGTH ?
         BNP   HEXERR                 NO, ERROR
         CH    @10,=Y(4)              WILL IT FIT ?
         BH    HEXERR                 NO, ERROR
         SLR   @15,@15                CLEAR FOR LATER USE
         SLR   @00,@00                CLEAR WORK REGISTER
HEXLOOP  DS    0H
         SLL   @15,4                  MAKE ROOM FOR ADDRESS OUT
         IC    @00,0(,@06)            GET STRING
         CLI   0(@06),C'0'            THIS ZERO ?
         BL    TOHEX01                NO, BELOW
         CLI   0(@06),C'9'            WITHIN VALID RANGE ?
         BNH   HEXOK                  YES, OK
         B     HEXERR                 NO, ERROR
         SPACE 1
TOHEX01  DS    0H
         CLI   0(@06),C'A'            THIS VALID ?
         BL    HEXERR                 NO, ERROR
         CLI   0(@06),C'F'            THIS VALID ?
         BH    HEXERR                 NO, ERROR
         AH    @00,=Y(9)              ROUND TO HEX VALUE
HEXOK    DS    0H
         N     @00,=F'15'             CLEAR FLAGS
         OR    @15,@00                KEEP IT
         LA    @06,1(,@06)            INCREASE ADDRESS
         BCT   @10,HEXLOOP            RETRY
         BR    @01                    RETURN
         SPACE 1
HEXERR   DS    0H                                            BIEDERMANN
         L     @15,=F'-1'             INDICATE BAD INPUT
         BR    @01                    RETURN TO CALLER
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
* TOEBCDIC CONVERTS THE HEX VALUE OF THE TWO LOW ORDER BYTES OF @15   *
* TO (PRINTABLE) EBCDIC AND RETURNS THE PRINTABLE CHARACTERS IN @15.  *
* CONTROL IS RETURNED TO THE ADDRESS PASSED IN @14.                   *
*                                                                     *
*         REGISTERS MODIFIED:  @15                                    *
*                                                                     *
***********************************************************************
         SPACE 2
TOEBCDIC DS    0H                                            BIEDERMANN
         SLL   @15,4                  MAKE ROOM FOR SIGN     BIEDERMANN
         O     @15,=X'FFF0000F'       SET A SIGN             BIEDERMANN
         ST    @15,PFIELD+4           KEEP FOR DEC. INSTRUCTIONS
         UNPK  PFIELD(8),PFIELD+5(3)  CORRECT IT             BIEDERMANN
         TR    PFIELD+4(4),EBCDIC     TRANSLATE IT           BIEDERMANN
         L     @15,PFIELD+4           RESTORE IT TO @15      BIEDERMANN
         BR    @14                    RETURN TO CALLER       BIEDERMANN
         SPACE 2
************      DATA USED IN THIS SECTION      *********************
         SPACE 1                                             BIEDERMANN
         ORG   *-C'0'                 SET TO START OF TABLE  BIEDERMANN
EBCDIC   DS    0C                                            BIEDERMANN
         ORG   *+C'0'                                        BIEDERMANN
         DC    C'0123456789ABCDEF'
         DS    0D                                            BIEDERMANN
PFIELD   DC    XL8'0'                 USED FOR TRANSLATION   BIEDERMANN
         EJECT
         LTORG
         SPACE 1
         DS    0D                                            BIEDERMANN
         DC    CL8'PATCH'                                    BIEDERMANN
PATCH    DC    50F'0'                 PATCH AEREA            BIEDERMANN
         EJECT
****************************************************  F.BIEDERMANN  ***
*                                                                     *
*        THE FOLLOWING IS THE PARAMETER SCAN TABLE.                   *
*        NOTE, THAT THE KEYWORDS MUST BE IN ASCENDING ORDER.          *
*                                                                     *
****************************************************  F.BIEDERMANN  ***
         SPACE 1
PARMTAB  DS    0F
         SPACE 1
ZERRID   DC    X'00000040'     PARM FLAG FOR NON GTFCCW PARM ERRORS
         DC    A(SCANERR)      EXIT ADDRESS OF SUB ROUTINE
         DC    X'00'           LENGTH - ONE
         DC    X'00'           MAX DATA LENGTH ALLOWED
         DC    C','            KEYWORD TO COMPARE
         SPACE 1
         DS    0F
         DC    X'00004000'     PARM FLAG TO SET
         DC    A(CCW)          EXIT ADDRESS OF SUB ROUTINE
         DC    X'03'           LENGTH - ONE
         DC    X'03'           MAX DATA LENGTH ALLOWED
         DC    C'CCW='         KEYWORD TO COMPARE
         SPACE 1
         DS    0F
         DC    X'00002000'
         DC    A(DATA)
         DC    X'04'
         DC    X'05'
         DC    C'DATA='
         SPACE 1
         DS    0F
IOFLAGS  DC    X'00008000'
         DC    A(SSTCOM)
         DC    X'02'
         DC    X'03'
         DC    C'IO='
         SPACE 1
         DS    0F
         DC    X'00000080'
         DC    A(SSTCOM)
         DC    X'04'
         DC    X'03'
         DC    C'IOSB='
         SPACE 1
         DS    0F
         DC    X'00000080'
         DC    A(0)
         DC    X'03'
         DC    X'00'
         DC    C'IOSB'
         SPACE 1
         DS    0F
         DC    X'00008000'
         DC    A(0)
         DC    X'01'
         DC    X'00'
         DC    C'IO'
         SPACE 1
         DS    0F
         DC    X'00001000'
         DC    A(JOB)
         DC    X'03'
         DC    X'08'
         DC    C'JOB='
         SPACE 1
         DS    0F
         DC    X'00001000'
         DC    A(JOB)
         DC    X'07'
         DC    X'08'
         DC    C'JOBNAME='
         SPACE 1
         DS    0F
         DC    X'00000000'     THIS ENTRY IS USED BY THE ERROR HANDLER
         DC    A(0)            BUT DOES NOT SET ANY ERROR FLAG.
         DC    X'01'           IT MAY BE USED TO OVERWRITE AN UNUSED
         DC    X'00'           PARM.
         DC    C'NO'
         SPACE 1
         DS    0F
         DC    X'00000800'
         DC    A(SSTCOM)
         DC    X'03'
         DC    X'03'
         DC    C'SIO='
         SPACE 1
         DS    0F
         DC    X'00000800'
         DC    A(0)
         DC    X'02'
         DC    X'00'
         DC    C'SIO'
         SPACE 1
         DS    0F
         DC    X'00000400'
         DC    A(SSTCOM)
         DC    X'04'
         DC    X'03'
         DC    C'SMOD='
         SPACE 1
         DS    0F
         DC    X'00000400'
         DC    A(0)
         DC    X'03'
         DC    X'00'
         DC    C'SMOD'
         SPACE 1
         DS    0F
         DC    X'00000200'
         DC    A(SSTCOM)
         DC    X'04'
         DC    X'03'
         DC    C'SORT='
         SPACE 1
         DS    0F
         DC    X'00000200'
         DC    A(0)
         DC    X'03'
         DC    X'00'
         DC    C'SORT'
         SPACE 1
         DS    0F
         DC    X'00000001'
         DC    A(SUDATA)
         DC    X'06'
         DC    X'04'
         DC    C'SUDATA='
         SPACE 1
         DS    0F
         DC    X'00000001'
         DC    A(SUDATA)
         DC    X'02'
         DC    X'04'
         DC    C'SU='
         SPACE 1
         DS    0F
TESTID   DC    X'00000100'
         DC    A(SSTCOM)
         DC    X'04'
         DC    X'03'
         DC    C'TEST='
         SPACE 1
         DS    0F
         DC    X'00000100'
         DC    A(0)
         DC    X'03'
         DC    X'00'
         DC    C'TEST'
         SPACE 1
         DS    0F
ZENDID   DC    X'00000000'     DUMMY OR LAST ENTRY
         DC    A(SCANERR)
         DC    X'00'
         DC    X'00'
         DC    X'FF'
         EJECT
         SPACE 3
****************************************************  F.BIEDERMANN  ***
*                                                                     *
*        THE FOLLOWING DSECT IS USED TO ADDRESS THE PARMTAB AEREA     *
*        USED BY THE SCANNER.                                         *
*                                                                     *
****************************************************  F.BIEDERMANN  ***
PARMTABL DSECT
         SPACE 1
PARMID   DC    X'00004000'     PARM FLAG TO SET
PROCADDR DC    A(CCW)          EXIT ADDRESS OF SUB ROUTINE
PALGTH   DC    X'03'           LENGTH - ONE
PKLGTH   DC    X'03'           MAX DATA LENGTH ALLOWED
PEND     EQU   *               END OF KEYWORD TABLE - TEXT
PKEYWRD  DC    C'CCW='         KEYWORD TO COMPARE
         EJECT                                               BIEDERMANN
         SPACE 3
****************************************************  F.BIEDERMANN  ***
*                                                                     *
*        THE FOLLOWING DSECT IS USED TO ADDRESS THE GTFSIO WORK       *
*        AEREAS IN FRONT OF GTFCCW.                                   *
*                                                                     *
****************************************************  F.BIEDERMANN  ***
         SPACE 3                                             BIEDERMANN
GTFSIO   DSECT                                               BIEDERMANN
SUSWITCH DS    0F                                            BIEDERMANN
SUFLAG1  DC    X'00'                TO KEEP SU INFORMATIONS  BIEDERMANN
SU50     EQU   X'80'                SU 50 INSTALLED          BIEDERMANN
SU64     EQU   X'40'                SU 64 INSTALLED          BIEDERMANN
SU65     EQU   X'20'                SU 65 INSTALLED          BIEDERMANN
SUFLAG2  DC    X'00'                TO KEEP SU INFORMATIONS  BIEDERMANN
AHLPSWCH DC    X'00'                FUNCTION SWITCHES        BIEDERMANN
OSWITCH  DC    X'00'                TO KEEP INFORMATIONS     BIEDERMANN
OIOSB    EQU   X'80'                IOSB AND EWA TRACE REQUESTED
AHLID    DS    CL3                  VERSION NUMBER           BIEDERMANN
         DS    0F                                            BIEDERMANN
GTFINIT  DC    A(0)                 INIT EP TO GTFSIO        BIEDERMANN
GTFTERM  DC    A(0)                 TERMINATION ENTRYPOINT   BIEDERMANN
WORKPNT  DC    A(0)                 POINTER TO WORK AEREA    BIEDERMANN
AHLTPARM DS    0CL16                PARM AEREA IN GTFSIO     BIEDERMANN
AHLRCMAX DC    F'50'                NUMBER OF CCWS           BIEDERMANN
AHLMAX   DC    F'24'                NUMBER OF DATA BYTES     BIEDERMANN
AHLNAME  DS    CL8                  JOBNAME TO BE TRACED     BIEDERMANN
         SPACE 1                                             BIEDERMANN
         END
./ ADD NAME=GTFCCWPR                               12JUN82 12.33.18
         TITLE 'GTFCCWPR - EDIT FORMATTING ROUTINE VERSION 3.0  FILE11 X
                --  IBM INTERNAL USE ONLY -- '
         SPACE 3
GTFCCWPR CSECT                                                  01S0001
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        SINCE VERSION 3.0 GTFCCWPR HAS BEEN MODIFIED, TO USE         *
*        AMDSYS0S (AMDSYS00) MODULE OF MVS TO PRINT THE IO AND SIO    *
*        HEADER LINE. BECAUSE, SOME OF THE INFORMATIONS NEEDED BY     *
*        THIS LOAD, ARE PASSED BY @13, THIS LOAD DOES NOT FOLLOW THE  *
*        OS SAVING CONVENTIONS, BUT USES @13, WITHOUT PROVIDING A NEW *
*        SAVEAEREA. AT EXIT TO AMDSYS0S (AMDSYS00) THE REGISTERS AT   *
*        ENTRYPOINT ARE RELOADED, EXCEPT @14, TO GET RETURN TO        *
*        GTFCCWPR.                                                    *
*                                                                     *
*        THE FOLLOWING RETURN CODES TO AMDPRDMP ARE USED:             *
*          @15=00  PRINT THE PRINTBUFFER AND RETURN                   *
*          @15=04  PRINT THE PRINTBUFFER AND RETURN WITH NEW BUFFER   *
*          @15=08  IGNORE THE PRINTBUFFER AND RETURN WITH A NEW REC.  *
*          @15=0C  DUMP THE CURRENT RECORD IN HEX                     *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING EQUS ARE USED IN THIS PGM.                     *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
@00      EQU   00                 EQUATES FOR REGISTERS 0-15
@01      EQU   01
@02      EQU   02                 RESERVED                   BIEDERMANN
@03      EQU   03
@04      EQU   04                 KEEPS BUFFER ADDRESS       BIEDERMANN
@05      EQU   05
@06      EQU   06
@07      EQU   07
@08      EQU   08
@09      EQU   09
@10      EQU   10                 USED AS FIRST BASE
@11      EQU   11                 USED AS SECOND BASE
@12      EQU   12                 HOLDS 200 BYTES WORK SPACE
@13      EQU   13                 POINTS TO PREVIOUS SAVEAEREA
@14      EQU   14
@15      EQU   15
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        FIRST OF ALL, SET UP THE REQUIRED REGISTERS. AFTER THAT LOOK *
*        IF ONE REQUESTS A SPECIAL EXIT. AFTER THE EXIT IS DONE, A    *
*        CHECK IS MADE, WETHER GTFCCWPR WILL PROCESS THE CURRENT      *
*        RECORD, OR A STANDART RECORD IS TO BE PROCESSED. IN CASE OF  *
*        A STANDART RECORD, A BALR TO AMDSYS0S (AMDSYS00) IS DONE.    *
*                                                                     *
*        BEFORE LEAVING THIS LOAD ON RETURN, THE RETURNCODE IS        *
*        CHECKED FOR LESS THAN FOR, AND THE NOTRACE SWITCH IS SET     *
*        ACCORDING TO THE RESULT.                                     *
*                                                                     *
*        NOTE, AT THE BEGINNING OF GTFCCWPR, THERE IS A FIXED         *
*        WORKAEREA, THAT MUST NOT BE MODIFIED, WITHOUT LOOKING TO     *
*        GTFPRINT BEFORE. THIS AEREA IS USED TO PASS INFORMATIONS     *
*        FROM GTFPRINT TO GTFCCWPR.                                   *
*                                                                     *
***********************************************************************
         SPACE 3
GTFCCWPR CSECT                                                  01S0001
         USING *,@15
         B     PROLOG
         SPACE 1                                             BIEDERMANN
EXITA    DC    A(0)               EXIT ADDRESS FOR AMDSYS0S
EXITB    DC    A(0)               EXIT ADDRESS FOR GTFCCWPR OR ZERO
CPU#     DC    Y(0)               CPUID TO BE PROCESSED      BIEDERMANN
         DS    0F
MODID    DC    CL4'3.0 '          MODID OF THIS LOAD         BIEDERMANN
PRFLAGS  DS    0CL4               CONTAINS THE GTFPRINT PARM FLAGS
FLAG01   DS    CL1                RESERVED                   BIEDERMANN
FLAG02   DS    CL1                RESERVED                   BIEDERMANN
FLAG03   DS    CL1                RESERVED                   BIEDERMANN
FLAG04   DS    CL1                MIXED FUNCTIONS            BIEDERMANN
FCPU     EQU   X'04'              CPU PARM WAS USED          BIEDERMANN
FEXIT    EQU   X'02'              THE EXIT PARM WAS USED     BIEDERMANN
FTEST    EQU   X'01'              TEST SPECIFIED             BIEDERMANN
MODNAME  DC    C'GTFCCWPR'        MODID FOR DUMP             BIEDERMANN
BASE     DC    A(GTFCCWPR),A(GTFCCWPR+X'1000')               BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 2                                             BIEDERMANN
PROLOG   DS    0H
         STM   @14,@12,12(@13)    SAVE REGISTERS
         LM    @10,@11,BASE       GET BASE REGISTERS
         SPACE 1
         DROP  @15
         USING RECORD,@06         FOR DATA BUFFER            BIEDERMANN
         USING GTFCCWPR,@10
         USING GTFCCWPR+4096,@11
         SPACE 1
         ST    @14,RETURN         KEEP RETURN ADDRESS        BIEDERMANN
         LR    @03,@01            MOVE PARM ADDRESS TO @03
         L     @12,X'18'(,@03)    @12 =WKAREADD= 200 BYTES WORK AEREA
         L     @14,X'1C'(,@03)    POINT TO LOCALBUFF               BAUR
         L     @04,X'08'(,@14)    THATS THE BUFFER                 BAUR
         L     @15,0(,@03)        POINT TO INPUT                   BAUR
         LA    @15,0(,@15)        CLEAR FLAGS                BIEDERMANN
         LH    @07,0(,@15)        LENGTH OF RECORD                 BAUR
         L     @06,16(,@03)       POINT TO DATA PORTION            BAUR
         LR    @08,@06            @06=@08                          BAUR
         SR    @08,@15            @08=LINGTH OF HEADER             BAUR
         SR    @07,@08            REAL DATA LENGTH                 BAUR
         LA    @15,8              SET RETURN CODE            BIEDERMANN
         LH    @14,CPU#           GET CPUID                  BIEDERMANN
         LTR   @14,@14            IS THERE ONE ?             BIEDERMANN
         BM    PROLOG01           NO, SKIP TEST              BIEDERMANN
         TM    SW,SMYRECID        CCW RECORD ?                     BAUR
         BNO   PROLOG00           NO, SKIP                         BAUR
         CLC   CPU#+L'CPU#-L'CPUID(L'CPUID),CPUID            BIEDERMANN
         BNE   EXIT10             IGNORE THIS RECORD         BIEDERMANN
         B     PROLOG01           CONTINUE NORMAL            BIEDERMANN
         SPACE 1                                             BIEDERMANN
PROLOG00 DS    0H                                            BIEDERMANN
         CLC   CPU#+L'CPU#-L'PRECPUID(L'PRECPUID),PRECPUID-RECORD(@06)
         BNE   EXIT10             LEAVE                      BIEDERMANN
PROLOG01 DS    0H                                            BIEDERMANN
         L     @15,L1RET          GET EXIT, WHERE TO CONTINUE
         LTR   @15,@15            IS THERE ONE ?             BIEDERMANN
         MVI   L1RET,X'FF'        SET ENTRY INACTIVE         BIEDERMANN
         BCR   2,@15              YES, USE IT                BIEDERMANN
         TM    SW,SMYRECID        THIS MY RECORD ?           BIEDERMANN
         BO    START00            YES, CONTINUE              BIEDERMANN
         MVI   SW1,NOTMYREC       SET SW1 TO FOREIGN RECORD        BAUR
         L     @15,EXITA          GET EXIT ADDRESS                 BAUR
         LM    @00,@12,20(@13)    RESTORE REGISTERS          BIEDERMANN
         BALR  @14,@15            GO TO AMDSYS0S             BIEDERMANN
         SPACE 1                                             BIEDERMANN
         STM   @00,@12,20(@13)    SAVE REGISTERS             BIEDERMANN
         BALR  @14,0              GET ME A BASE              BIEDERMANN
         LA    @10,*-GTFCCWPR     GET ME A BASE              BIEDERMANN
         LCR   @10,@10            GET ME A BASE              BIEDERMANN
         LA    @14,0(,@14)        GET ME A BASE              BIEDERMANN
         AR    @10,@14            GET ME A BASE              BIEDERMANN
         LM    @10,@11,BASE       SET MY BASE                BIEDERMANN
EXIT     DS    0H                                            BIEDERMANN
         CH    @15,=Y(8)          IS IT BELOW 8 ?                  BAUR
         BL    EXIT10             NOT RETURNCODE 8                 BAUR
         OI    SW1,NOEDIT         NO EDITING FOR NEXT RECS         BAUR
EXIT10   DS    0H                                                  BAUR
         L     @14,RETURN         GET RETURN ADDRESS         BIEDERMANN
         LM    @00,@12,20(@13)    RESTORE REGISTERS          BIEDERMANN
         BR    @14                RETURN TO THE CALLER       BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 2                                             BIEDERMANN
***********************************************************************
*                                                                     *
*        START SECTION FOR EVERY SPECIAL RECORD                       *
*                                                                     *
***********************************************************************
         SPACE 3
START00  DS    0H                                                  BAUR
         LA    @15,8              SET RETURN CODE            BIEDERMANN
         TM    SW1,NOEDIT         SHOULD WE EDIT                   BAUR
         BO    EXIT10             NO, LEAVE                        BAUR
START01  DS    0H                                            BIEDERMANN
         MVC   RECHDR(L'SW+L'CPUID+L'RECNO),SW               BIEDERMANN
         LA    @08,L'SW+L'CPUID+L'RECNO  SET SCAN POINTER    BIEDERMANN
         CLI   RECNO,1            THIS RECORD ONE ?          BIEDERMANN
         BNE   START10            NO, DEV TYPE AND COUNT IN        BAUR
         TM    SW1,NOTMYREC       ANY HEADER RECORD BEFORE ?
         BO    START02            YES, USE THIS RECORD       BIEDERMANN
         OI    SW1,NOEDIT         PREVENT EDIT               BIEDERMANN
         LA    @15,8              SKIP ALL RECORDS OF THIS BLOCK
         B     EXIT10             LEAVE                      BIEDERMANN
         SPACE 1
START02  DS    0H                                            BIEDERMANN
         NI    SW1,255-NOTMYREC   RESET NOT MY RECORD        BIEDERMANN
         XC    RECOUNT,RECOUNT    CLEAR                            BAUR
         TM    SW,SMSG            THIS A MESSAGE ?           BIEDERMANN
         BO    START10            YES, SKIP NEXT CLEANUP     BIEDERMANN
         LA    @14,PCCW10A        ENTRYPOINT FOR FIRST RECORD      BAUR
         STM   @09,@05,ALLSAVE    RETURN ADDRESS                   BAUR
         MVC   RECHDR+L'SW+L'CPUID+L'RECNO(L'RECHDR-L'SW-L'CPUID-L'RECNC
               O),RECORD+L'SW+L'CPUID+L'RECNO                      BAUR
         LA    @08,STARTA+1-RECORD SET SCAN POINTER                BAUR
START10  DS    0H                                                  BAUR
         L     @14,RECOUNT        GET PREV COUNT                   BAUR
         LA    @14,1(,@14)        INCR BY 1                        BAUR
         SLL   @14,24             CLEAR FLAGS                BIEDERMANN
         SRL   @14,24             ONLY FIRST RECORD IS NUMBER ONE
         LTR   @14,@14            WAS THERE A CARRY ?        BIEDERMANN
         BNZ   START20            NO, NO RESET REQUIRED      BIEDERMANN
         LA    @14,2              COUNT OF 2 EXPECTED        BIEDERMANN
START20  DS    0H                                            BIEDERMANN
         SLR   @15,@15            CLEAR FOR LATER USE        BIEDERMANN
         IC    @15,RECNO          GET RECORD COUNT           BIEDERMANN
         CR    @15,@14            COUNT VALID                      BAUR
         BNE   START40            NO, CHECK FOR REC1 OR EXIT       BAUR
         ST    @14,RECOUNT        SAVE COUNT                       BAUR
         OI    SW2,SW2FIRST       INDICATE ANY VALID RECORD        BAUR
         TM    SW,SMSG            THIS A MESSAGE ?           BIEDERMANN
         BO    MESSAGE            YES, GO TO MSG PROCESSOR   BIEDERMANN
         SR    @07,@08            UPDATE SIZE                      BAUR
         AR    @08,@06            ADD RECORD BASE                  BAUR
         LM    @09,@05,ALLSAVE    RETURN ADDRESS                   BAUR
         BR    @14                CONTINUE                         BAUR
         SPACE 1                                                   BAUR
START40  DS    0H                                                  BAUR
         MVI   SW1,0              RESET SWITCH                     BAUR
         SLR   @08,@08            CLEAR REG FOR LATER USE          BAUR
         ST    @08,RECOUNT        SET PREV TO ZERO                 BAUR
         TM    SW2,SW2FIRST       WAS ANY RECORD VALID ?           BAUR
         BNZ   ERROR1             YES, ELSE ERROR
         CLI   RECNO,1            CHECK RECORD COUNT         BIEDERMANN
         BE    START02            GO START AGAIN                   BAUR
         LA    @15,8              SET RETURN CODE            BIEDERMANN
         MVI   SW1,NOEDIT         INDICATE NOEDIT            BIEDERMANN
         B     EXIT10             NOT RECORD ONE, IGNORE     BIEDERMANN
         SPACE 1                                             BIEDERMANN
ERROR1   DS    0H                                                  BAUR
         MVI   0(@04),C' '                                   BIEDERMANN
         MVC   1(L'CCWBUF-1,@04),0(@04)                            BAUR
         MVC   CCWOUT1-CCWBUF(L'MSG01,@04),MSG01                   BAUR
         BALR  @15,0              SET IN CASE OF ABEND       BIEDERMANN
         TM    FLAG04,FTEST       THIS A TEST RUN ?          BIEDERMANN
         BO    ABEND              YES, ELSE ABEND            BIEDERMANN
         SLR   @15,@15            SET REURN CODE                   BAUR
         CLI   RECNO,1            THIS RECORD ONE ?                BAUR
         BE    EXIT10             SKIP THIS RECORD           BIEDERMANN
         LA    @15,4              SET RETURN CODE            BIEDERMANN
         OI    SW1,NOEDIT         PREVENT EDIT               BIEDERMANN
         B     EXIT10             LEAVE                      BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING CODING IS RESPONSABLE TO HANDLE MESSAGES AND   *
*        CONTROL BLOCKS TO BE DUMPED. A MESSAGE IS EITHER IN EBCDIC   *
*        WITH BLKADDR = ZERO, OR IT IS A CNTL BLOCK TO BE DUMPED.     *
*        IF WRITTEN IN EBCDIC, CONTROL IS GIVEN TO THE EBCDIC         *
*        PROCESSOR TO WRITE THE MESSAGE.                              *
*                                                                     *
*        REGISTER USAGE IN THIS SECTION:                              *
*          @05 CONTAINS THE REMINING TOTOL LENGTH TO BE EDITED.       *
*          @07 LENGTH OF THE COMPLETE RECORD WITHOUT HEADER.          *
*          @09 IS USED AS A POINTER TO THE OUTPUT LOCATION.           *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
MESSAGE  DS    0H                                            BIEDERMANN
         SLR   @14,@14            CLEAR FOR LATER USE        BIEDERMANN
         ICM   @14,7,BLKADDR      GET MESSAGE ADDRESS        BIEDERMANN
         BZ    MSGEBCD            IF ZERO, REAL MESSAGE      BIEDERMANN
         MVC   CCWOUT1(L'BLKID),BLKID  MOVE ID               BIEDERMANN
         SH    @07,=Y(L'SW+L'CPUID+L'RECNO+L'BLKID+L'BLKLGTH)
         BNP   MSGHERR            NOTHING TO PRINT           BIEDERMANN
         LA    @03,L'BLKADDR      GET SIZE TO BE PROCESSED   BIEDERMANN
         LA    @08,BLKADDR        GET ADDRESS TO EDIT        BIEDERMANN
         LA    @09,CCWOUT2        GET OUT ADDRESS            BIEDERMANN
         MVI   SW1,0              RESET SWITCH               BIEDERMANN
         BAL   @14,EDIT           EDIT ADDRESS               BIEDERMANN
         SLR   @05,@05            GET AEREA LENGTH           BIEDERMANN
         ICM   @05,3,BLKLGTH      GET PROCESSOR LENGTH       BIEDERMANN
         LA    @08,BLKTRC         GET AEREA TO DUMP          BIEDERMANN
         LA    @03,32             GET BUFFER LENGTH          BIEDERMANN
         CR    @03,@05            MORE THAN ONE LINE ?       BIEDERMANN
         BNH   MSGH02             YES, PROCESS NORMAL        BIEDERMANN
         LR    @03,@05            USE IT                     BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        AT THIS POINT, EDIT OF A NEW LINE STARTS.
         SPACE 1                                             BIEDERMANN
MSGH02   DS    0H                                            BIEDERMANN
         LA    @09,DATAOUT        SET TO OUTPUT AEREA        BIEDERMANN
         MVI   CSTAR1,C' '        CLEAR PRINT AEREA          BIEDERMANN
         MVI   CSTAR2,C' '        CLEAR PRINT AEREA          BIEDERMANN
MSGH03   DS    0H                                            BIEDERMANN
         SR    @05,@03            SET SIZE REMAINING         BIEDERMANN
         BM    MSGHERR            ANYTHING WRONG             BIEDERMANN
         MVI   SW1,0              RESET SWITCH               BIEDERMANN
         BAL   @14,MSGHED         GO AND EDIT                BIEDERMANN
MSGH03A  DS    0H                                            BIEDERMANN
         BAL   @14,WRITE1         GO TO WRITE                BIEDERMANN
         LTR   @05,@05            STILL SOME WORK TO DO ?    BIEDERMANN
         BZ    MSGH07             NO, CHECK FOR ERRORS.      BIEDERMANN
         BM    MSGHERR            ERROR                      BIEDERMANN
         LA    @03,32             SET UP FOR A NEW LINE      BIEDERMANN
         CR    @05,@03            THIS A FULL LINE ?         BIEDERMANN
         BNL   MSGH04             YES, CONTINUE              BIEDERMANN
         LR    @03,@05            SET FOR PARTITIAL LINE     BIEDERMANN
MSGH04   DS    0H                                            BIEDERMANN
         CR    @07,@03            STILL ENOUGH DATA ?        BIEDERMANN
         BNL   MSGH02             YES, PRINT                 BIEDERMANN
         LA    @09,DATAOUT        GET OUTPUT AEREA           BIEDERMANN
         LR    @02,@07            KEEP SIZE USED             BIEDERMANN
         LTR   @03,@07            GET SIZE TO EDIT           BIEDERMANN
         BZ    MSGH05             THERE IS NO                BIEDERMANN
         BM    MSGHERR            ANYTHING WRONG             BIEDERMANN
         SR    @05,@03            DECREASE RECORD COUNT      BIEDERMANN
         BM    MSGHERR            SOMTHING WRONG             BIEDERMANN
         MVI   CSTAR1,C' '        CLEAR PRINT AEREA          BIEDERMANN
         MVI   CSTAR2,C' '        CLEAR PRINT AEREA          BIEDERMANN
         BAL   @14,MSGHED         EDIT THE DATA              BIEDERMANN
         LTR   @05,@05            WAS IT LAST DATA ?         BIEDERMANN
         BZ    MSGH03A            YES, GO TO PRINT           BIEDERMANN
         LR    @14,@02            GET REGISTER               BIEDERMANN
         SLL   @14,30             CLEAR FLAGS                BIEDERMANN
         SRL   @14,30             CLEAR FLAGS                BIEDERMANN
         LTR   @14,@14            ANY ODD VALUES ?           BIEDERMANN
         BNZ   MSGH05             YES, SKIP NEXT INSTRUCTION BIEDERMANN
         LA    @09,1(,@09)        INCREASE ONE               BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        ENTRY TO GET A NEW RECORD.                          BIEDERMANN
         SPACE 1                                             BIEDERMANN
MSGH05   DS    0H                                            BIEDERMANN
         TM    SW,SEND            CURRENT RECORD LAST ?      BIEDERMANN
         BO    MSGHERR            SHOULD NOT BE              BIEDERMANN
         STM   @00,@15,ALLSAVE    KEEP REGISTERS             BIEDERMANN
         LA    @14,MSGH06         GET NEXT RECORD EXIT ADDR. BIEDERMANN
         ST    @14,L1RET          SET RETURN ADDRESS         BIEDERMANN
         LA    @15,8              SET RETURN CODE            BIEDERMANN
         B     EXIT10             GET A NEW RECORD           BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        THIS ROUTINE IS ENTERED, AFTER AN EXIT, TO GET A NEW
*        RECORD FOR MSG PROCESSING.
         SPACE 1
MSGH06   DS    0H                 THIS ENTRY IS TO RETURN    BIEDERMANN
         TM    SW,SMYRECID+SMSG   IS THIS MY RECORD ?        BIEDERMANN
         BNO   MSGHERR            NO, ERROR                  BIEDERMANN
         BAL   @14,COUNTUP        ADJUST COUNT               BIEDERMANN
         LTR   @15,@15            CHECK RETURN CODE          BIEDERMANN
         BNZ   MSGHERR            ERROR IN RECORD COUNT      BIEDERMANN
         L     @02,ALLSAVE+L'ALLSAVE*@02                     BIEDERMANN
         L     @05,ALLSAVE+L'ALLSAVE*@05                     BIEDERMANN
         SH    @07,=Y(BLKID-SW)   SKIP UNUSED PART           BIEDERMANN
         LA    @08,BLKID          GET START OF DATA          BIEDERMANN
         L     @09,ALLSAVE+L'ALLSAVE*@09                     BIEDERMANN
         LR    @14,@02            SET TO VALUE               BIEDERMANN
         SLL   @14,30             CLEAR PRINTED SIZE         BIEDERMANN
         SRL   @14,30             ADJUST IT                  BIEDERMANN
         LTR   @14,@14            ZERO ?                     BIEDERMANN
         BZ    MSGH06B            YES, SKIP IT               BIEDERMANN
         LA    @03,4              GET BLOCK ADDRESS          BIEDERMANN
         SR    @03,@14            GET SIZE TO BE PROCESSED   BIEDERMANN
         CR    @07,@03            END OF RECORD ?            BIEDERMANN
         BNL   MSGH06A            SKIP UPDATE                BIEDERMANN
         LR    @03,@07            GET SIZE TO EDIT           BIEDERMANN
MSGH06A  DS    0H                                            BIEDERMANN
         SR    @05,@03            ADJUST COUNT               BIEDERMANN
         BM    MSGHERR            SHOULD NOT OCCUR           BIEDERMANN
         BAL   @14,EDIT           EDIT IT                    BIEDERMANN
         LA    @09,1(,@09)        CALCULATE GAP              BIEDERMANN
MSGH06B  DS    0H                                            BIEDERMANN
         LA    @03,32             GET LINE SIZE              BIEDERMANN
         LA    @02,3(,@02)        ROUND                      BIEDERMANN
         SRL   @02,2              ROUND                      BIEDERMANN
         SLL   @02,2              ROUND                      BIEDERMANN
         SR    @03,@02            CALCULATE SIZE REMAINING   BIEDERMANN
         BM    MSGHERR            SHOULD NOT OCCUR           BIEDERMANN
         BP    MSGH06C            CONTINUE                   BIEDERMANN
         BAL   @14,WRITE1         LINE IS NOW FULL           BIEDERMANN
         LA    @03,32             GET FULL LINE SIZE         BIEDERMANN
         B     MSGH04             TRY IT AGAIN               BIEDERMANN
         SPACE 1
MSGH06C  DS    0H
         CR    @05,@03            MORE THAN SCHOULD BE       BIEDERMANN
         BNL   MSGH06D            YES                        BIEDERMANN
         LR    @03,@05            EDIT REST OF DATA          BIEDERMANN
MSGH06D  DS    0H                                            BIEDERMANN
         CR    @07,@03            CHECK CURRENT BUFFER       BIEDERMANN
         BNL   MSGH03             ENOUGH DATA IN             BIEDERMANN
         B     MSGHERR            THIS WAS A NEW  RECORD     BIEDERMANN
         SPACE 1                                             BIEDERMANN
MSGH07   DS    0H                                            BIEDERMANN
         LTR   @07,@07            RECORD COUNT ZERO ?        BIEDERMANN
         BNZ   MSGHERR            ELSE ERROR                 BIEDERMANN
         LA    @15,8              SET RETURN CODE            BIEDERMANN
         TM    SW,SEND            THIS LAST RECORD ?         BIEDERMANN
         BO    EXIT10             YES, ALL OK                BIEDERMANN
         B     MSGHERR            ELSE ERROR                 BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        THIS SUBROUTNIE IS USED, TO EDIT IN 4 BYTES BLOCKS
*        UNTIL THE LINE IS FULL.
         SPACE 1                                             BIEDERMANN
MSGHED   DS    0H                                            BIEDERMANN
         STM   @00,@15,ALLSAVE    SAVE MY REGISTERS          BIEDERMANN
MSGHED1  DS    0H                                            BIEDERMANN
         CH    @03,=Y(4)          THIS ONE FIELD ?           BIEDERMANN
         BNH   EDIT               YES, GO                    BIEDERMANN
         LA    @03,4              GET ONE FIELD              BIEDERMANN
         BAL   @14,EDIT           EDIT THIS ONE              BIEDERMANN
         LA    @09,1(,@09)        MAKE A FREE SLOT           BIEDERMANN
         L     @14,ALLSAVE+L'ALLSAVE*@14                     BIEDERMANN
         L     @03,ALLSAVE+L'ALLSAVE*@03                     BIEDERMANN
         SH    @03,=Y(4)          SUBTRACT                   BIEDERMANN
         ST    @03,ALLSAVE+L'ALLSAVE*@03                     BIEDERMANN
         BCR   8,@14              RETURN                     BIEDERMANN
         BM    MSGHERR            SHOULD NOT OCCUR           BIEDERMANN
         B     MSGHED1                                       BIEDERMANN
         SPACE 2                                             BIEDERMANN
*        THIS IS THE ERROR ROUTINE. IT IS ENTERED, WHENEVER
*        SOMTHING IS WRONG, WITH THE COUNTS OR THE RECORD.
         SPACE 1                                             BIEDERMANN
MSGHERR  DS    0H                                            BIEDERMANN
         BALR  @15,0              INDICATE REASON IN A DUMP  BIEDERMANN
         TM    FLAG04,FTEST       THIS A TEST RUN ?          BIEDERMANN
         BO    ABEND              YES, ABEND                 BIEDERMANN
         MVI   0(@04),C' '        CLEAR BUFFER               BIEDERMANN
         MVC   0(L'CCWBUF-1,@04),1(@04) CLEAR BUFFER         BIEDERMANN
         MVC   CCWOUT1-CCWBUF(L'MSG06,@04),MSG06             BIEDERMANN
         SLR   @15,@15            SET RETURN CODE TO ZERO    BIEDERMANN
         OI    SW1,NOEDIT         PREVENT EDIT               BIEDERMANN
         B     EXIT10             LEAVE                      BIEDERMANN
         EJECT                                                     BAUR
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING IS THE MESSAGE PROCESSOR TO WRITE MESSAGES     *
*        IN PRINTABLE FORMAT, PASSED BY THE TRACE MODULE.             *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
MSGEBCD  DS    0H                                            BIEDERMANN
         SH    @07,=Y(L'SW+L'CPUID+L'RECNO+L'BLKID+L'BLKLGTH+L'BLKADDR)
         BNP   MSGBERR            NOTHING TO PRINT           BIEDERMANN
         MVI   0(@04),C' '        CLEAR BUFFER               BIEDERMANN
         MVC   1(L'CCWBUF-1,@04),0(@04)                      BIEDERMANN
         MVC   CCWOUT1-CCWBUF(L'BLKID,@04),BLKID             BIEDERMANN
         ICM   @14,3,BLKLGTH      GET LENGTH OF MESSAGE      BIEDERMANN
         CR    @14,@07            DOES THIS MATCH ?          BIEDERMANN
         BNE   MSGBERR            NO, ERROR                  BIEDERMANN
         SH    @14,=Y(1)          SUBTRACT ONE FOR EX        BIEDERMANN
         BM    MSGBERR            NOTHING TO PRINT           BIEDERMANN
         CH    @14,=Y(L'DATAOUT-1) WILL RECORD FIT ?         BIEDERMANN
         BH    MSGBERR            THIS RECORD IS IN ERROR    BIEDERMANN
         TM    SW,SEND            THIS LAST RECORD ?         BIEDERMANN
         BNO   MSGBERR            NO, ERROR                  BIEDERMANN
MSGEBCD1 DS    0H                                            BIEDERMANN
         EX    @14,MSGMVC         MOVE MESSAGE               BIEDERMANN
         LA    @15,4              SET RETURN CODE            BIEDERMANN
         B     EXIT10             LEAVE                      BIEDERMANN
         SPACE 1                                             BIEDERMANN
MSGBERR  DS    0H                                            BIEDERMANN
         BALR  @15,0              INDICATE REASON IN A DUMP  BIEDERMANN
         TM    FLAG04,FTEST       THIS A TEST RUN ?          BIEDERMANN
         BO    ABEND              YES, ABEND                 BIEDERMANN
         MVI   0(@04),C' '        CLEAR BUFFER               BIEDERMANN
         MVC   0(L'CCWBUF-1,@04),0(@04)                      BIEDERMANN
         MVC   CCWOUT1-CCWBUF(L'MSG06,@04),MSG06             BIEDERMANN
         OI    SW1,NOEDIT         PREVENT EDIT               BIEDERMANN
         LA    @15,4              SET RETURN CODE            BIEDERMANN
         B     EXIT10             LEAVE                      BIEDERMANN
         SPACE 1                                             BIEDERMANN
MSGMVC   MVC   DATAOUT-CCWBUF(0,@04),BLKTRC  MOVE WITH EX    BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING ROUTINE IS RESPONSABLE, TO TAKE CARE ABOUT     *
*        THE CURRENT RECORD COUNT. IT MAY BE USED, WHENEVER A L1RET   *
*        ROUTINE, DOES NOT WANT TO USE A START00 ENTRY. IN CASE OF    *
*        BAD RETURN CODE, REGISTERS MAY BE NO LONGER VALID.           *
*                                                                     *
*        REGISTER USAGE:                                              *
*          @00     USED AS WORK REGISTER                              *
*          @06     MUST POINT TO THE CURRENT RECORD                   *
*          @14     RETURN REGISTER                                    *
*          @15     RETURN CODE  (NOT ZERO = COUNT INVALID)            *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
COUNTUP  DS    0H                                            BIEDERMANN
         SLR   @00,@00            CLEAR FOR LATER USE        BIEDERMANN
         IC    @00,RECNO          GET RECORD COUNT           BIEDERMANN
         L     @15,RECOUNT        GET RECORD COUNT           BIEDERMANN
         LA    @15,1(,@15)        INCREASE BY ONE            BIEDERMANN
         SLL   @15,24             CLEAR FLAGS                BIEDERMANN
         SRL   @15,24             ADJUST                     BIEDERMANN
         LTR   @15,@15            LOOK FOR CARRY             BIEDERMANN
         BNZ   COUNTUP1           ALREADY VALID              BIEDERMANN
         LA    @15,2              SET IN CASE OF CARRY       BIEDERMANN
COUNTUP1 DS    0H                                            BIEDERMANN
         CR    @00,@15            COUNT VALID ?              BIEDERMANN
         BNE   COUNTUP2           NO, ERROR                  BIEDERMANN
         ST    @15,RECOUNT        SAVE NEW COUNT             BIEDERMANN
         SLR   @15,@15            SET RETURN CODE            BIEDERMANN
         BR    @14                RETURN                     BIEDERMANN
         SPACE 1                                             BIEDERMANN
COUNTUP2 DS    0H                                            BIEDERMANN
         LA    @14,0(,@14)        SET RETURN ADDRESS         BIEDERMANN
         ST    @14,L1RET          SET RETURN ADDRESS         BIEDERMANN
         MVI   0(@04),C' '        PREPARE PRINT BUFFER       BIEDERMANN
         MVC   0(L'CCWBUF-1,@04),0(@04)    CLEAR             BIEDERMANN
         MVC   CCWOUT1-CCWBUF(L'MSG01,@04),MSG01             BIEDERMANN
         BALR  @15,0              SET FOR ABEND              BIEDERMANN
         TM    FLAG04,FTEST       THIS A TEST RUN ?          BIEDERMANN
         BO    ABEND              YES, ABEND                 BIEDERMANN
         SLR   @15,@15            SET RETURN CODE            BIEDERMANN
         B     EXIT10             PRINT AND RETURN TO CALLER BIEDERMANN
         EJECT
RETURN8  DS    0H                                                  BAUR
         NI    SW1,NOEDIT                                          BAUR
         SLR   @14,@14            SET NO RETURN              BIEDERMANN
         STM   @09,@05,ALLSAVE    SAVE RETURNADDR                  BAUR
         B     RETURN8A           CONTINUE                   BIEDERMANN
         SPACE 1                                             BIEDERMANN
READNEXT DS    0H                                                  BAUR
         STM   @09,@05,ALLSAVE    SAVE RETURNADDR                  BAUR
         LA    @14,RETURNB        GET RETURN ADDRESS         BIEDERMANN
RETURN8A DS    0H                                            BIEDERMANN
         TM    RECHDR+SW-RECORD,SEND LAST RECORD OF EVENT ?        BAUR
         BNO   RETURN82           NO, GET NEXT INPUT REC           BAUR
         TM    RECHDR+SW-RECORD,SLOST WAS CHAIN COMPLETE ?         BAUR
         BZ    RETURNA            YES                              BAUR
         MVI   CSTAR1,C' '        WIPE OUT C'*'              BIEDERMANN
         MVI   CSTAR2,C' '        WIPE OUT C'*'              BIEDERMANN
         MVC   CCWOUT1(L'MSG02),MSG02                              BAUR
         BAL   @14,WRITE1                                          BAUR
RETURNA  DS    0H                                                  BAUR
         NI    SW1,NOEDIT         RESET LEAVE NOEDIT               BAUR
         SLR   @14,@14            CLEAR                            BAUR
RETURN82 DS    0H                 THIS IS RETURN FOR A NEW RECORD  BAUR
         ST    @14,L1RET          SET FOR RETURN             BIEDERMANN
         LA    @15,8              SET RETURN CODE TO GET NEXT      BAUR
*                                 INPUT RECORD                     BAUR
         B     EXIT10             LEAVE APPENDAGE               POEPPEL
         SPACE 1
RETURNB  DS    0H                                            BIEDERMANN
         L     @06,16(,@03)       POINT TO DATA PORTION      BIEDERMANN
         TM    SW-RECORD(@06),SMYRECID  THIS MY RECORD ?     BIEDERMANN
         BO    RETURNE            YES, NORMAL CONTINUATION   BIEDERMANN
         SLR   @15,@15            CLEAR FOR LATER USE        BIEDERMANN
         ST    @15,RECOUNT        RESET COUNT                BIEDERMANN
RETURNC  DS    0H                                            BIEDERMANN
         CLC   CCWBUF(94),CCWBLANK SOMTHING IN ?             BIEDERMANN
         BE    RETURND            NO, DO NOT EMPTY           BIEDERMANN
         BAL   @14,WRITE1         EMPTY BUFFER               BIEDERMANN
RETURND  DS    0H                                            BIEDERMANN
         MVI   0(@04),C' '        CLEAR PRINT BUFFER         BIEDERMANN
         MVC   1(L'CCWBUF-1,@04),0(@04)                      BIEDERMANN
         MVC   CCWOUT1-CCWBUF(L'MSG09,@04),MSG09             BIEDERMANN
         SLR   @15,@15            SET RETURN CODE            BIEDERMANN
         B     EXIT10             WRITE MESSAGE AND RETURN   BIEDERMANN
         SPACE 1                                             BIEDERMANN
RETURNE  DS    0H                 CHECK FOR MESSAGE          BIEDERMANN
         TM    SW-RECORD(@06),SMSG THIS A MESSAGE ?          BIEDERMANN
         BO    RETURNC            YES, ERROR                 BIEDERMANN
         LM    @14,@12,12(@13)    NORMAL CONTINUATION        BIEDERMANN
         BR    @15                WITH A RESTART             BIEDERMANN
         EJECT
         SPACE 3                                                   BAUR
********************************************************* POEPPEL *****
*                                                                     *
*        EDIT CCW AND UPDATE VALUES FOR DATA TRACE                    *
*                                                                     *
********************************************************* POEPPEL *****
         SPACE 3                                                   BAUR
PCCW10A  DS    0H                                                  BAUR
         CH    @07,=Y(L'CCWADDR+L'CCW+L'PADDR)                     BAUR
         BL    ERROR              SPACE NOT REMAINING        BIEDERMANN
         LA    @03,L'PADDR        GET SIZE TO SKIP                 BAUR
         TM    RECHDR+PFLAGS-RECORD,PSORT CHAIN SORTED ?           BAUR
         BZ    PCCW1AAA           NO, SKIP START MESSAGE           BAUR
         MVC   ADDROUT+10(L'MSG05),MSG05                           BAUR
         LA    @09,ADDROUT+10+L'MSG05 OUTPUT LOCATION              BAUR
         BAL   @14,EDIT           EDIT                             BAUR
         BAL   @14,WRITE1         WRITE LINE                       BAUR
         B     PCCW10             CONTINUE                         BAUR
         SPACE 1                                             BIEDERMANN
PCCW1AAA DS    0H                                            BIEDERMANN
         AR    @08,@03            UPDATE SCAN POINTER        BIEDERMANN
         SR    @07,@03            SET REMAINING SIZE         BIEDERMANN
PCCW10   DS    0H                                                  BAUR
         LA    @14,PCCW10C        GET READ RETURN ADDRESS          BAUR
         CH    @07,=Y(L'CCWADDR+L'CCW)      MORE DATA ?            BAUR
         BNL   PCCW10C            YES, CONTINUE                    BAUR
         LTR   @07,@07            SOMTHING REMAINING ?       BIEDERMANN
         BZ    READNEXT           NO, ALL OK                 BIEDERMANN
         B     ERROR              SOMTHING IS WRONG          BIEDERMANN
         SPACE 1
*        IT IS EXPECTED, THAT CCWADDRESS AND CCW IS NOT SPLITTED.
         SPACE 1
*        EDIT CCW ADDRESS
         SPACE 1
PCCW10C  DS    0H                                                  BAUR
         NI    SW1,255-DATA       IND DATA FOR EDIT                BAUR
         MVC   RECHDR+CCWADDR-RECORD(L'CCWFLAGS+L'CCWA+L'CCW),0(@08)
         LA    @14,L'CCWFLAGS     SKIP CCWFLAGS                    BAUR
         AR    @08,@14            SET CURRENT POINTER              BAUR
         SR    @07,@14            DECREMENT COUNT                  BAUR
         LA    @03,L'CCWA         LENGTH                           BAUR
         LA    @09,ADDROUT        OUTPUTADDR                       BAUR
         BAL   @14,EDIT           EDIT                             BAUR
         SPACE 3                                                   BAUR
*        EDIT CCW                                                  BAUR
         SPACE 3                                                   BAUR
         LA    @09,CCWOUT1        OUTPUT LOCATION                  BAUR
         LA    @03,4              HALF CCW LENGTH                  BAUR
         BAL   @14,EDIT           EDIT                             BAUR
         LA    @09,CCWOUT2        OUTPUT LOCATION                  BAUR
         LA    @03,4              HALF CCW LENGTH                  BAUR
         BAL   @14,EDIT           EDIT                             BAUR
         TM    RECHDR+CCWFLAGS-RECORD,CDATERR                      BAUR
         BNO   PCCW10E            NO DATA ERROR              BIEDERMANN
         MVC   DATAOUT(L'MSG07),MSG07    WRITE MESSAGE       BIEDERMANN
         B     PCCW1C             GO TO WRITE ROUT.          BIEDERMANN
         SPACE 1
PCCW10E  DS    0H                                            BIEDERMANN
         TM    RECHDR+CCWFLAGS-RECORD,CZERO                  BIEDERMANN
         BZ    PCCW10F            ZERO DATA REQUESTED ?      BIEDERMANN
         MVC   DATAOUT(L'MSG08),MSG08   SET UP MSG08         BIEDERMANN
         B     PCCW1C             GO TO WRITE                BIEDERMANN
         SPACE 1
PCCW10F  DS    0H                                            BIEDERMANN
         TM    RECHDR+CCWFLAGS-RECORD,CTIC+CNODAT                  BAUR
         BZ    PCCW30             GO TO TRACE DATA                 BAUR
PCCW1C   DS    0H                                                  BAUR
         BAL   @14,WRITE1         PRINT LINE                       BAUR
         B     PCCW50             EDIT NEXT                        BAUR
         SPACE 3
*        EDIT  DATA
         SPACE 3
PCCW30   DS    0H
         LA    @14,PCCW30A        GET EXIT ADDRESS                 BAUR
         CH    @07,=Y(L'CCWLNGT)  MORE DATA IN RECORD              BAUR
         BNL   PCCW30A            YES                              BAUR
         LTR   @07,@07            IS COUNT ZERO ?            BIEDERMANN
         BZ    READNEXT           YES, ALL OK                BIEDERMANN
         B     ERROR              NO, ERROR                  BIEDERMANN
PCCW30A  DS    0H                                                  BAUR
         SLR   @03,@03            CLEAR COUNTS                     BAUR
         ST    @03,EDITCNT2       CLEAR COUNTS                     BAUR
         ICM   @03,3,0(@08)       GET LENGTH                       BAUR
         ST    @03,EDITCNT1       SAVE IT AS LENGTH 1              BAUR
         LA    @14,L'CCWLNGT      GET LENGTH TO SKIP         BIEDERMANN
         AR    @08,@14            SET LOCATION POINTER       BIEDERMANN
         SR    @07,@14            COUNT UPDATE               BIEDERMANN
         OI    SW1,DATA           IND DATA                         BAUR
         TM    RECHDR+CCWFLAGS-RECORD,CSPLIT  DATA SPLITTED ?      BAUR
         BNO   PCCW301            NO, COUNT 1 ONLY                 BAUR
         LR    @15,@03            GET COUNT                  BIEDERMANN
         SLDL  @14,31             DIVIDE BY 2                      BAUR
         SRL   @15,31             DIVIDE BY 2                      BAUR
         ST    @14,EDITCNT2       SAVE CNT 2                       BAUR
         LA    @03,0(@14,@15)     GET  CNT 1                       BAUR
         ST    @03,EDITCNT1       SAVE CNT 1                       BAUR
PCCW301  DS    0H                                                  BAUR
         LA    @09,DATAOUT        OUTPUT                           BAUR
         LA    @05,L'EBCDIC       LINE SIZE                        BAUR
PCCW301A DS    0H                                                  BAUR
         NI    SW1,255-LINEP      INDICATE NO LINE PRINTED         BAUR
         L     @03,EDITCNT1       COUNT                            BAUR
         LR    @00,@03            SAVE                             BAUR
         CR    @05,@03            MORE THAN 1 LINE                 BAUR
         BNL   PCCW302            NO                               BAUR
         LR    @03,@05            USE LINE SIZE                    BAUR
PCCW302  DS    0H                                                  BAUR
         SLR   @00,@03            REMAINING COUNT                  BAUR
         ST    @00,EDITCNT1       SAVE REMAINDER                   BAUR
         BAL   @14,EDIT           EDIT                             BAUR
         SR    @05,@03            OUT SIZE                         BAUR
         BP    PCCW303            STILL SOME SPACE IN THIS LINE    BAUR
         BAL   @14,WRITE          PRINT A LINE                     BAUR
         OI    SW1,LINEP          IN LINE  PRINTED                 BAUR
         LA    @05,L'EBCDIC       LINE SIZE                        BAUR
         LA    @09,DATAOUT        OUTPUT LOC                       BAUR
PCCW303  DS    0H                                                  BAUR
         SR    @00,@00                                             BAUR
         C     @00,EDITCNT1       MORE DATA IN THIS PART           BAUR
         BNE   PCCW301A           YES EDIT                         BAUR
         C     @00,EDITCNT2       SECOND PART                      BAUR
         BE    PCCW40             NO                               BAUR
         MVC   EDITCNT1(4),EDITCNT2    MAKE 2 TO 1                 BAUR
         ST    @00,EDITCNT2       CLEAR 2                          BAUR
         MVC   0(9,@09),=C'--- '  BREAK                            BAUR
         LA    @09,4(,@09)        UPDATE POINTER                   BAUR
         B     PCCW301A           EDIT                             BAUR
         SPACE 1
PCCW40   DS    0H                                                  BAUR
         TM    SW1,LINEP          IS LINE  PRINTED                 BAUR
         BO    PCCW50             YES                              BAUR
         BAL   @14,WRITE          WRITE IT                         BAUR
PCCW50   DS    0H                                            BIEDERMANN
         TM    RECHDR+CCWFLAGS-RECORD,CLAST LAST OF CHAIN ?  BIEDERMANN
         BNO   PCCW60             SKIP IF NO ENDCHAIN MESSAGE
         TM    RECHDR+SW-RECORD,SEND  IS THIS LAST ?         BIEDERMANN
         BNO   PCCW50A            NO, PRINT MESSAGE          BIEDERMANN
         LTR   @07,@07            MORE DATA ?                BIEDERMANN
         BZ    PCCW60             NO, NO MESSAGE             BIEDERMANN
PCCW50A  DS    0H                                            BIEDERMANN
         MVI   CSTAR1,C' '        WIPE OUT C'*'              BIEDERMANN
         MVI   CSTAR2,C' '        WIPE OUT C'*'              BIEDERMANN
         MVC   CCWOUT1(L'MSG03),MSG03                              BAUR
         BAL   @14,WRITE1                                          BAUR
PCCW60   DS    0H                                                  BAUR
         TM    RECHDR+CCWFLAGS-RECORD,CLOST PREVIOUS LOST ?        BAUR
         BNO   PCCW10             NO                               BAUR
         MVI   CSTAR1,C' '        WIPE OUT C'*'              BIEDERMANN
         MVI   CSTAR2,C' '        WIPE OUT C'*'              BIEDERMANN
         MVC   CCWOUT1(L'MSG04),MSG04                              BAUR
         BAL   @14,WRITE1         PRINT LINE                       BAUR
         B     PCCW10                                              BAUR
         EJECT
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING ROUTINE IS USED TO MODIFY THE HEX CODE TO      *
*        READABLE EBCDIC CODE. THE RESULT OF THE TRANSLATION IS PUT   *
*        OUT, TO THE ADDRESS PASSED IN @09.                           *
*                                                                     *
*        REGISTERS AT ENTRY:                                          *
*         @03 LENGTH TO BE EDITED                                     *
*         @05 SIZE REMAINING IN CURRENT OUT RECORD                    *
*         @07 SIZE REMAINING IN CURRENT RECORD                        *
*         @09 ADDRESS OF OUTPUT AEREA                                 *
*         @14 HOLDS THE RETURN ADDRESS                                *
*                                                                     *
*        REGISTERS THAT MAY BE DAMAGED:                               *
*         @00,@01,@15                                                 *
*                                                                     *
*        REGISTERS UPDATED AT RETURN:                                 *
*         @03,@07,@09                                                 *
*                                                                     *
*        SAVEAEREAS USED:                                             *
*         EDITSAVE                                                    *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
EDIT     DS    0H                                                  BAUR
         ST    @14,EDITSAVE       SAVE EXIT ADDRESS          BIEDERMANN
         LTR   @03,@03            ZERO LENGTH                      BAUR
         BCR   8,@14                                               BAUR
         ST    @03,SIZES          SAVE SIZE                        BAUR
         LA    @01,WORK           POINT TO WORKAREA                BAUR
         CR    @07,@03            ALL IN THIS RECORD               BAUR
         BNL   EDIT1              YES                              BAUR
         LTR   @07,@07            DO WE HAVE DATA                  BAUR
         BNP   EDIT1A             NO GET A NEW RECORD              BAUR
         TM    SW1,DATA           SPLIT POSSIBLE                   BAUR
         BZ    ERROR              WHAT HAPPENS ?                   BAUR
         SR    @03,@07            CALC REMAINING                   BAUR
         BCTR  @07,0              -1                               BAUR
         EX    @07,MOVE           MOVE FIRST PART TO WORK          BAUR
         LA    @01,1(@01,@07)     POINT TO NEXT                    BAUR
EDIT1A   DS    0H                                                  BAUR
         BAL   @14,READNEXT       GET A NEW RECORD                 BAUR
EDIT1    DS    0H                                                  BAUR
         SR    @07,@03            CALC REMAINING                   BAUR
         LR    @00,@03            SAVE REMAINDER                   BAUR
         BCTR  @03,0              -1                               BAUR
         EX    @03,MOVE           MOVE DATA                        BAUR
         L     @03,SIZES          RESTORE INITIAL SIZE
         TM    SW1,DATA           DATA BEING EDITED
         BZ    EDIT2              NO                               BAUR
         LA    @01,EBCDIC+L'EBCDIC    END OF OUT TRANSLATION       BAUR
         SR    @01,@05            CALC ACTUAL LOC                  BAUR
         BCTR  @03,0              -1                               BAUR
         EX    @03,TRMOVE         MOVE DATA TO TRANSLATE           BAUR
         LA    @03,1(,@03)        READJUST COUNT                   BAUR
         LA    @01,WORK           POINT TO BEGIN                   BAUR
EDIT2    DS    0H                                                  BAUR
         AR    @08,@00            STEP IN BUFFER                   BAUR
         TM    SW1,DATA           DATA TO EDIT                     BAUR
         BNZ   EDITDATA           YES                              BAUR
EDIT2A   DS    0H                                                  BAUR
         UNPK  0(3,@09),0(2,@01)  UNPK                             BAUR
         MVI   2(@09),X'40'       CLEAR                            BAUR
         TR    0(2,@09),TR        TRANSLATE                        BAUR
         LA    @01,1(,@01)        UPDATE
         LA    @09,2(,@09)        UPDATE
         BCT   @03,EDIT2A         LOOP
         TM    SW1,DATA           DATA EDITED                      BAUR
         BO    EDITDA2            YES                              BAUR
EDIT3    DS    0H                                                  BAUR
         L     @03,SIZES          RESTORE
         L     @14,EDITSAVE       GET EXIT ADDRESS           BIEDERMANN
         BR    @14                RETURN
EDITDATA DS    0H                                                  BAUR
         LR    @00,@03            TOTAL COUNT IN @00               BAUR
EDITDL   DS    0H                                                  BAUR
         LA    @03,4              COUNT FOR FULLWORD               BAUR
         CR    @03,@00            REMAINDER LESS 4                 BAUR
         BNH   EDITDA1            NO                               BAUR
         LR    @03,@00            USE REMAINDER                    BAUR
EDITDA1  DS    0H
         SR    @00,@03            CALC REMAINDER                   BAUR
         ST    @00,EDCNTSAV       SAVE IT                          BAUR
         B     EDIT2A             EDIT A FULLWORD                  BAUR
EDITDA2  DS    0H                                                  BAUR
         LA    @09,1(,@09)        UPDATE OUTPUT POINTER            BAUR
         L     @00,EDCNTSAV       RESTORE COUNT                    BAUR
         LTR   @00,@00            MORE TO EDIT                     BAUR
         BZ    EDIT3              NO                               BAUR
         B     EDITDL                                              BAUR
         SPACE 1
MOVE     MVC   0(0,@01),0(@08)
TRMOVE   MVC   0(0,@01),WORK
         SPACE 1
         ORG   *-240
TR       DS    0C
         ORG   *+240
         DC    C'0123456789ABCDEF'
         EJECT                                                     V1.2
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        WRITE ROUTINE   MOVE REC TO OUT ,WRITE AND CLEAR             *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                                   V1.2
WRITE    DS    0H                                                  V1.2
         L     @15,EXITB          POINT TO EXIT ROUTINE            V1.2
         LTR   @15,@15            IS AN EXIT PRESENT               V1.2
         BZ    WRITEB             NO                               V1.2
         MVC   PDEVTYP,RECHDR+DEVTYP-RECORD                  BIEDERMANN
         STM   @00,@14,EXITSAVE   SAVE REGISTERS                   V1.2
         LA    @01,PWRITEXA       POINT TO PARMS                   V1.2
         BALR  @14,@15            GO TO EXIT                       V1.2
         DROP  @10                                                 V1.2
         DROP  @11                                                 V1.2
         BALR  @14,0              GET ME A BASE
         USING *,@14                                               V1.2
         LM    @00,@14,EXITSAVE   RESTORE REGISTERS                V1.2
         DROP  @14                                                 V1.2
         USING GTFCCWPR,@10                                        V1.2
         USING GTFCCWPR+4096,@11                                   V1.2
         N     @15,=A(12)         VALIDATE RETURNCODE              V1.2
         B     WRITEA(@15)        BRANCH                           V1.2
         SPACE 1
WRITEA   DS    0H                                                  V1.2
         B     WRITEB             RETURNCODE=0                     V1.2
         B     WRITE1             RETURNCODE=4                     V1.2
         B     WRITE2A            RETURNCODE=8                     V1.2
         SPACE 1
WRITEB   DS    0H                                                  V1.2
         MVC   DEVTN,RECHDR+DEVTYP-RECORD RECORD TYP TO WORK       V1.2
         NI    DEVTN,X'F0'        CLEAR ZONE                       V1.2
         LA    @15,RF40           POINT TO TRTAB                   V1.2
         CLI   DEVTN,1            IBM ADAPTER TYPE 1               V1.2
         BE    TRANSL             YES                              V1.2
         CLI   DEVTN,3            IBM TTY ADAPTER                  V1.2
         LA    @15,TELE360        POINT TO TRTAB                   V1.2
         CLI   DEVTN,9            IS IT BSCA                       V1.2
         BE    TRANSL             YES                              V1.2
         LA    @15,RSCI           POINT TO TRTAB                   V1.2
         CLI   DEVTN,8            2260                             V1.2
         BE    TRANSL             TR                               V1.2
         LA    @15,RCTW           WTTA  (RCT3?)                    V1.2
         CLI   DEVTN,6            WTTA                             V1.2
         BE    TRANSL             YES                              V1.2
         LA    @15,RCT1           (115A OR 83B3)                   V1.2
         CLI   DEVTN,4            IS IT TTY TYPE 1                 V1.2
         BE    TRANSL
         LA    @15,RCT2           TWX                              V1.2
         CLI   DEVTN,5            TWX                              V1.2
         BE    TRANSL
         LA    @15,RC30           GET 1030 CODE                    V1.2
         CLI   DEVTN,2            1030 DEVICE ?                    V1.2
         BE    TRANSL             YES                              V1.2
         LA    @15,TELE360                                         BAUR
TRANSL   DS    0H
         TR    EBCDIC,0(@15)      TRANSLATE                     POEPPEL
WRITE1   DS    0H
         ST    @14,WSAVE          SAVE RETURN                      BAUR
         MVC   0(L'CCWBUF,@04),CCWBUF  MOVE TO OUTPUT BUFFER       BAUR
WRITE1A  DS    0H
         LA    @14,WRITE2         RETURN ADDR
         ST    @14,L1RET          SET LEVEL1 RETURN          BIEDERMANN
         STM   @00,@15,PRINTSAV   SAVE
         SR    @15,@15
         B     EXIT10             LEAVE APPENDAGE
         SPACE 1
WRITE2   DS    0H
         LM    @00,@15,PRINTSAV   GET MY REGISTER SET        BIEDERMANN
WRITE2A  DS    0H                                            BIEDERMANN
         MVC   CCWBUF,CCWBLANK    CLEAR WORK AEREA                 BAUR
         MVI   CSTAR1,C'*'        SET STARS AGAIN            BIEDERMANN
         MVI   CSTAR2,C'*'        SET STARS AGAIN            BIEDERMANN
         L     @14,WSAVE          RESTORE RETURN                   BAUR
         BR    @14                RETURN TO CALLER                 BAUR
         SPACE 1
ERROR    DS    0H
         BALR  @15,0              IN CASE OF A DUMP          BIEDERMANN
         TM    FLAG04,FTEST       THIS TEST ?                BIEDERMANN
         BO    ABEND              YES, ABEND                 BIEDERMANN
         MVI   CSTAR1,C' '        WIPE OUT C'*'              BIEDERMANN
         MVI   CSTAR2,C' '        WIPE OUT C'*'              BIEDERMANN
         MVC   CCWOUT1(L'MSG06),MSG06
         BAL   @14,WRITE1                                    BIEDERMANN
         OI    SW1,NOEDIT
         B     RETURN8
         SPACE 2
EXITWRT  DS    0H                                                  V1.2
         USING *,@15                                               V1.2
         STM   @03,@15,EXWRSAVE   SAVE REGS                        V1.2
         LM    @03,@13,EXITSAVE+@03*4 RESTORE BASE REGS            V1.2
         DROP  @15                                                 V1.2
         USING GTFCCWPR,@10                                        V1.2
         USING GTFCCWPR+4096,@11                                   V1.2
         MVC   0(L'CCWBUF,@04),1(@01)   MOVE DATA                  V1.2
         BAL   @14,WRITE1A         LOAD PARAMETER                  V1.2
         LM    @03,@15,EXWRSAVE    RESTORE REGS                    V1.2
         BALR  @15,0               SET FOR ABEND             BIEDERMANN
         TM    FLAG04,FTEST        IS THIS TEST ?            BIEDERMANN
         BNZ   ABEND               GET A DUMP                BIEDERMANN
         BR    @14
         EJECT
         SPACE 3
****************************************************** F.BIEDERMANN ***
*                                                                     *
*        THE FOLLOWING SECTION IS ENTERED, IF TEST IS ACTIV, TO GET   *
*        A DUMP. AT ENTRYPOINT @15 POINTS TO THE LOCATION, THAT       *
*        REQUESTS THE DUMP.                                           *
*                                                                     *
****************************************************** F.BIEDERMANN ***
         SPACE 3                                             BIEDERMANN
ABEND    DS    0H                                            BIEDERMANN
         ABEND 122,DUMP                                      BIEDERMANN
         EJECT
         SPACE 3
****************************************************** F.BIEDERMANN ***
*                                                                     *
*        THE FOLLOWIND ARE THE MESSAGES USED IN THIS PGM.             *
*                                                                     *
****************************************************** F.BIEDERMANN ***
         SPACE 3
MSG01    DC    C'GTFPR01  ERROR IN RECORD COUNT, MAY BE LOST EVENTS.'
MSG02    DC    C'GTFPR02  CHAIN NOT COMPLETE.'
MSG03    DC    C'GTFPR03  END OF NORMAL CHAIN (FOR PCI OR IO INT. TAKENX
                FROM THE CURRENT CSW - 8)'
MSG04    DC    C'GTFPR04  CHAIN LOST.'
MSG05    DC    C'GTFPR05  CHAIN STARTS AT 00'
MSG06    DC    C'GTFPR06  ERROR IN INPUT RECORD.'
MSG07    DC    C'GTFPR07  DATA COULD NOT BE TRACED.'
MSG08    DC    C'GTFPR08  DATA TRACING NOT REQUESTED.'       BIEDERMANN
MSG09    DC    C'GTFPR09  EXPECTED CONTINUATION OF PREVIOUS RECORD NOT X
               RECEIVED.'                                    BIEDERMANN
         EJECT
         DC  CL8'PATCH'
PATCH    DC  20F'0'               PATCH AEREA
         SPACE 1
         LTORG
         EJECT
         SPACE 3
EXWRSAVE DS    5F
         SPACE 2
ALLSAVE  DS    16F
EXITSAVE DS    15F
PRINTSAV DS    16F
RETURN   DS    1F                  HOLD THE EXIT ADDRESS     BIEDERMANN
EDITSAVE DS    1F
L1RET    DS    1F                 IF NOT MINUS OR ZERO - EXIT ON RET.
         SPACE 2
         DC    C'HDR '            TO INDICATE IT IN A DUMP
RECHDR   DS    CL18
WORK     DS    CL50
         DS    0F
         DC    C'WORK'            TO INDICATE IT IN A DUMP
SIZES    DS    1F
EDITCNT1 DS    1F
EDITCNT2 DS    1F
EDCNTSAV DS    1F                                                  BAUR
WSAVE    DS    1F
RECOUNT  DC    F'0'                                                BAUR
DEVTN    DC    X'0'
SW1      DC    X'01'              INDICATION SWITCH, SET TO NOEDIT BAUR
DATA     EQU   X'80'
LINEP    EQU   X'10'
NOTMYREC EQU   X'02'              INDICATES, THAT HEADER WAS FOUND.
NOEDIT   EQU   X'01'              INDICATES NO EDIT UNTIL          BAUR
*                                 SW IS RESETED WITH A VALID       BAUR
*                                 RECORD                           BAUR
SW2      DC    X'0'               INDICATION SWITCH          BIEDERMANN
SW2FIRST EQU   X'80'              FIRST VALID RECORD RECEIVED
SW3      DC    X'0'               INDICATION SWITCH          BIEDERMANN
SW4      DC    X'0'               INDICATION SWITCH          BIEDERMANN
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING IS THE OUTPUT BUFFER. IT IS ALSO THE EXIT LIST *
*        PASSED TO THE USER EXIT.                                     *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
         DS    0D                                            BIEDERMANN
         DS    CL8'EXITPARM'                                 BIEDERMANN
PWRITEXA DC    A(EXITWRT)
PDEVADRS DC    H'0'
PDEVTYP  DC    X'00'                                         BIEDERMANN
CCWBLANK DC    C' '                                                BAUR
CCWBUF   DS    0CL120             CCW BUFFER FOR EDIT              BAUR
         DC    CL9' '             FILLER
ADDROUT  DC    CL6' '             OUTPUTAREA FOR CCWADDR           BAUR
         DC    CL2' '                                              BAUR
CCWOUT1  DC    CL8' '             OUTPUTAREA FOR CCW               BAUR
         DC    CL1' '             FILLER                     BIEDERMANN
CCWOUT2  DC    CL8' '             OUTPUT AEREA OF SECOND HALF CCW
         DC    CL2' '             FILLER
DATAOUT  DC    CL58' '            OUTPUTAREA FOR DATA              BAUR
CSTAR1   DC    CL1'*'
EBCDIC   DC    CL24' '            FOR OUTPUT TRANSLATION
CSTAR2   DC    CL1'*'
         EJECT
         SPACE 3                                             BIEDERMANN
****************************************************** F.BIEDERMANN ***
*                                                                     *
*        THE FOLLOWIND ARE THE TRANSLATE TABLES USED IN THIS PGM,     *
*        ACCORDING TO THE DEVICETYP FIELD PASSED IN THE TRACE RECORD. *
*                                                                     *
****************************************************** F.BIEDERMANN ***
         SPACE 3                                             BIEDERMANN
RC30     EQU   *
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'4B40F14BF24B4BF3F44B4BF54BF6F74B'  0
         DC    X'F84B4BF94B4B7B4B4B4B4B4B4B4B4B4B'  1
         DC    X'F04B4B614BE2E34B4BE4E54BE64B4BE7'  2
         DC    X'4BE8E94B4B4B4B6B4B4B4B4B4B4B4B4B'  3
         DC    X'604B4BD14BD2D34B4BD4D54BD64B4BD7'  4
         DC    X'4BD8D94B4B4B4B5B4B4B4B4B4B4B4B4B'  5
         DC    X'4B50C14BC24B4BC3C44B4BC54BC6C74B'  6
         DC    X'C84B4BC94B4B4B4B4B4B4B4B4B4B4B4B'  7
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  8
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  9
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  A
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  B
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  C
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  D
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  E
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  F
RF40     EQU   *
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'4B40F14BF24B4BF3F44B4BF54BF6F74B'  0
         DC    X'F84B4BF94BF07B4B4B4B4B4B4B4B4B4B'  1
         DC    X'7C4B4B614BE2E34B4BE4E54BE64B4BE7'  2
         DC    X'4BE8E94B4B4B4B6B4B4B4B4B4B4B4B4B'  3
         DC    X'604B4BD14BD2D34B4BD4D54BD64B4BD7'  4
         DC    X'4BD8D94B4B4B4B5B4B4B4B4B4B4B4B4B'  5
         DC    X'4B50C14BC24B4BC3C44B4BC54BC6C74B'  6
         DC    X'C84B4BC94B4B4B4B4B4B4B4B4B4B4B4B'  7
         DC    X'4B407E4B4C4B4B5E7A4B4B6C4B7D6E4B'  8
         DC    X'5C4B4B4D4B5D7F4B4B4B4B4B4B4B4B4B'  9
         DC    X'4A4B4B6F4BE2E34B4BE4E54BE64B4BE7'  A
         DC    X'4BE8E94B4B4B4B4F4B4B4B4B4B4B4B4B'  B
         DC    X'6D4B4BD14BD2D34B4BD4D54BD64B4BD7'  C
         DC    X'4BD8D94B4B4B4B5A4B4B4B4B4B4B4B4B'  D
         DC    X'4B4EC14BC24B4BC3C44B4BC54BC6C74B'  E
         DC    X'C84B4BC94B4B5F4B4B4B4B4B4B4B4B4B'  F
         EJECT
         SPACE 3
RSCI     EQU   *
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  0
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  1
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  2
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  3
         DC    X'406A1A7B5B6C507D4D5D5C4E6B604B61'  4
         DC    X'F0F1F2F4B4F5F6F7F8F97A5E4C7E6E6F'  5
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  6
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  7
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  8
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  9
         DC    X'4BC1C2C3C4C5C6C7C8C9D1D2D3D4D5D6'  A
         DC    X'D7D8D9E2E3E4E5E6E7E8E94B4B0A4B6D'  B            A28624
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  C            A28624
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  D
         DC    X'7C4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  E
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B5F4B4F4B'  F
RCT1     EQU   *
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'4BE34BD640C8D5D44BD3D9C7C9D7C3E5'  0
         DC    X'C5E9C4C2E2E8C6E7C1E6D14BE4D8D24B'  1
         DC    X'4BF54BF9404B6B4B4B5DF450F8F07A5E'  2
         DC    X'F37F5B6F7DF65A6160F24B4BF7F14D4B'  3
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  4
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  5
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  6
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  7
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  8
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  9
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  A
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  B
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  C
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  D
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  E
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  F
         EJECT
         SPACE 3
RCT2     EQU   *
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'4B4B4B7C4B404B4B4B4BD7D7F0F04B4B'  0
         DC    X'4B4BC8C84D4D4B4B4B4B4BE74BF84B4B'  1
         DC    X'4B37C4C45B5B4B4B4B4B4BE34BF44B4B'  2
         DC    X'4B4B4BD34B6B4B4B4B4BE1E14C4C4B4B'  3
         DC    X'4B4BC2C27F7F4B4B344B4BD94BF24B4B'  4
         DC    X'4B4B4BD14B5C4B4B4B4BE9E97A7A4B4B'  5
         DC    X'4B4B4BC64B504B4B4B4BE5E5F6F64B4B'  6
         DC    X'4B4BD5D54B4B4B4B4B4B4B4F4B6E4B4B'  7
         DC    X'4B4BC1C15A5A4B4B4B4B4BD84BF14B4B'  8
         DC    X'4B4B4BC94B5D4B4B4B4BE8E8F9F94B4B'  9
         DC    X'4B4B4BC54B6C4B4B4B4BE4E4F5F54B4B'  A
         DC    X'4B4BD4D460604B4B4B4B4B494B7E4B4B'  B
         DC    X'4B4B4BC34B7B4B4B4B3CE2E2F4B34B4B'  C
         DC    X'4B4BD2D24E4E4B4B4B4B4B794B5E4B4B'  D
         DC    X'4B4BC7C77D7D4B4B4B4B4BE64BF74B4B'  E
         DC    X'4B4B4BD64B614B4B4B4B6D6D6F6F4B4B'  F
RCTW     EQU   *                  ITA2 CODE TO EBCDICL 180300
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F                  000G
         DC    X'4BE34BD640C8D5D44BD3D9C7C9D7C3E5' 0               000G
         DC    X'C5E9C4C2E2E8C6E7C1E6D14BE4D8D24B' 1               000G
         DC    X'4BF54BF9404B6B4B4B5DF44BF8F07A7E' 2               000G
         DC    X'F34E4B6F7DF64B6160F24B4BF7F14D4B' 3               000G
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 4               000G
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 5               000G
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 6               000G
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 7               000G
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 8               000G
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' 9               000G
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' A               000G
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' B               000G
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' C               000G
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' D               000G
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' E               000G
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B' F               000G
         EJECT
         SPACE 3
TELE360  EQU   *     EBCDIC LOWER TO UPPER CASE
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  0
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  1
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  2
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  3
         DC    X'404B4B4B4B4B4B4B4B4B4A4B4C4D4E4F'  4
         DC    X'504B4B4B4B4B4B4B4B4B4B5B5C5D5E5F'  5
         DC    X'60614B4B4B4B4B4B4B4B4B6B6C6D6E6F'  6
         DC    X'4B4B4B4B4B4B4B4B4B4B7A7B7C7D7E7F'  7
         DC    X'4BC1C2C3C4C5C6C7C8C94B4B4B4B4B4B'  8
         DC    X'4BD1D2D3D4D5D6D7D8D94B9B9C9D9E9F'  9
         DC    X'4B4BE2E3E4E5E6E7E8E94B4B4B4B4B4B'  A
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  B
         DC    X'C0C1C2C3C4C5C6C7C8C94B4B4B4B4B4B'  C
         DC    X'D0D1D2D3D4D5D6D7D8D94B4B4B4B4B4B'  D
         DC    X'E04BE2E3E4E5E6E7E8E94B4B4B4B4B4B'  E
         DC    X'F0F1F2F3F4F5F6F7F8F94B4B4B4B4B4B'  F
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*                                                                     *
*        DESCRIPTION OF THE TRACE RECORD                              *
*        THIS LAYOUT IS AN EXTENSION OF THE NORMAL I/O RECORD         *
*                                                                     *
* OFFSET 0    1   2   3    4          6    7              10          *
*       ****************************************************          *
*       *     * C * C *    *          *    *               *          *
*       * SW  * P * N *DEV-*  NUMBER  *PFLG*  CCWSTART     *          *
*       *     * U * T *TYPE*  OF DATA *    *               *          *
*       ****************************************************          *
*      10   11         14                                             *
*       ************************************************************  *
*       *    *   CCW    *                                          *  *
*       *CFLG*  START   *  DATA CONTAINING CCWS  AND DATA VARIABLE *  *
*       *    *  ADDR.   *                                          *  *
*       ************************************************************  *
*                                                                     *
*                                                                     *
***************************************************** F.BIEDERMANN ****
         SPACE 3                                             BIEDERMANN
RECORD   DSECT                                               BIEDERMANN
         SPACE 1                                             BIEDERMANN
SW       DS    CL1                SWITCHES FOR VARIOUS USAGE BIEDERMANN
SMYRECID EQU   X'F8'              INDICATE MY RECORD         BIEDERMANN
SEND     EQU   X'04'              THIS RECORD IS LAST        BIEDERMANM
SMSG     EQU   X'02'              THIS RECORD IS A MESSAGE   BIEDERMANN
SLOST    EQU   X'01'              END OF CHAIN NOT REACHED   BIEDERMANN
CPUID    DS    CL1                CPU RCORD BELONGS TO       BIEDERMANN
RECNO    DS    CL1                CURRENT RECORD NUMBER      BIEDERMANN
DEVTYP   DS    CL1                DEVICETYP, SHORTED         BIEDERMANN
DATAL    DS    CL2                LENGTH OF MAXDATA AS SPECIFIED
STARTA   DS    0C                 START OF CCW CHAIN - MAY BE SORTED
PFLAGS   DC    X'00'              PSWITCH FLAGS AS SET BY GTFCCW
PIO      EQU   X'80'              IO TRACE WAS ACTIV         BIEDERMANN
PJOBFLAG EQU   X'01'              TRACE SELECTED BY JOBNAME  BIEDERMANN
PSIO     EQU   X'08'              SIO REQUESETED             BIEDERMANN
PSTATMOD EQU   X'04'              STATUS MODIFIER REQUESTED  BIEDERMANN
PSORT    EQU   X'02'              SORT REQUESTED             BIEDERMANN
PTEST    EQU   X'01'              TEST WAS REQUESTED         BIEDERMANN
PADDR    DC    AL3(0)             START OF CCW CHAIN         BIEDERMANN
CCWADDR  DS    0AL4               CCW ADDRESS
CCWFLAGS DC    X'00'              SEVERAL CCW DEPENDENT FLAGS
CTICOK   EQU   X'80'              A TIC IS SUCCESFUL RESOLVED
CTIC     EQU   X'40'              COMMAND IS A TIC           BIEDERMANN
CNODAT   EQU   X'20'              NO DATA FOLLOWING THAT CCW BIEDERMANN
CDATERR  EQU   X'10'              NO DATA, DATA IN ERROR     BIEDERMANN
CSPLIT   EQU   X'08'              DATA IS SPLITTED           BIEDERMANN
CLAST    EQU   X'04'              LAST OF NORMAL CHAIN       BIEDERMANN
CZERO    EQU   X'02'              DATA OF ZERO REQUESTED     BIEDERMANN
CLOST    EQU   X'01'              CCW CHAIN LOST WITH THIS CCW
CCWA     DC    AL3(0)             CCW ADDRESS                BIEDERMANN
CCW      DS    CL8                CCW                        BIEDERMANN
CCWLNGT  DC    AL2(0)             DATALENGTH FOLLOWING IF APLICABLE
DATATEXT DC    C'TEXT'            VAREABLE IN SIZE           BIEDERMANN
         SPACE 1                                             BIEDERMANN
         ORG   RECORD+5           THE FOLLOWING IS FOR NORMAL GTF
PRECPUID DS    CL1                FOR NORMAL GTF RECORD ONLY BIEDERMANN
         SPACE 1                                             BIEDERMANN
         ORG   RECORD+L'SW+L'CPUID+L'RECNO  SET FOR MSG      BIEDERMANN
BLKID    DS    CL4                IF WORK, THIS FIELD CONTAINS 'WORK'
*                                 IF IOSB, THIS FIELD CONTAINS 'IOSB'
*                                 IF EWA,  THIS FIELD CONTAINS 'EWA '
*                                 IF MSG,  THIS FIELD CONTAINS 'MSG '
BLKADDR  DS    CL3                ADDRESS OF BLK OR ZERO     BIEDERMANN
BLKLGTH  DS    CL2                LENGTH OF BLOCK            BIEDERMANN
BLKTRC   DS    0C                 START OF DATA              BIEDERMANN
         SPACE 3
         END
./ ADD NAME=GTFPRINT                               12JUN82 12.33.18
         TITLE 'GTFPRINT - PRINT MONITOR VERSION 3.0  FILE10   --  IBM I
               INTERNAL USE ONLY -- '
GTFPRINT CSECT
         SPACE 1
*        REGISTER EQUATES
         SPACE 1
@00      EQU   0                      USED AS WORK REGISTER
@01      EQU   1                      USED AS WORK REGISTER
@02      EQU   2                      RESERVED
@03      EQU   3                      CALLERS PARM ADDRESS
@04      EQU   4                      RESERVED
@05      EQU   5                      USED AS WORK REGISTER
@06      EQU   6                      USED AS WORK REGISTER
@07      EQU   7                      USED AS WORK REGISTER
@08      EQU   8                      USED AS WORK REGISTER
@09      EQU   9                      USED AS WORK REGISTER
@10      EQU   10                     USED AS WORK REGISTER
@11      EQU   11                     RESERVED
@12      EQU   12                     BASE REGISTER
@13      EQU   13                     SAVE AEREA ADDRESS
@14      EQU   14                     USED AS WORK REGISTER
@15      EQU   15                     USED AS WORK REGISTER
         SPACE 2
*        THE FOLLOWING EQU ARE USED TO ADDRESS THE PARM FIELDS IN
*        GTFCCWPR AND HAS TO BE CHANGED, IF GTFCCWPR IS CHANGED AT
*        THE CORRESPONDING VALUES.
         SPACE 1
GTFCCWPR EQU   0                      WORKAEREA IN GTFCCWPR
EXITA    EQU   GTFCCWPR+X'04'         ADDRESS OF AMDSYS0S
EXITB    EQU   GTFCCWPR+X'08'         ADDRESS OF USER EXIT
CPU#     EQU   GTFCCWPR+X'0C'         OFFSET OF CPUID IN GTFCCWPR
MODID    EQU   GTFCCWPR+X'10'         OFFSET OF MODULE LEVEL
PRFLAGS  EQU   GTFCCWPR+X'14'         OFFSET OF OPTION FLAGS
MODNAME  EQU   GTFCCWPR+X'18'         MODULE NAME = GTFCCWPR
         SPACE 1
AMDSYS00 EQU   0                      WORKAEREA IN AMDSYS0A (AMDSYS00)
AMMODID  EQU   AMDSYS00+X'0C'         CHECK HERE FOR MODID
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THIS LOAD IS PROVIDED TO LOAD AND DELETE GTFCCWPR .          *
*        IT IS ALSO RESPONSABLE TO CHECK THE CALLERS PARM FIELD FOR   *
*        THOSE KEYWORDS, THAT ARE RESERVED FOR GTFPRINT. IF EXIT IS   *
*        SPECIFIED, THE EXIT IS LOADED AND ITS ADDRESS IS PLACED AT   *
*        X'30' IN THE GTFCCWPR LOAD.                                  *
*                                                                     *
*        IT IS ALSO RESPONSABLE TO SEPERATE THE PARM FIELD PASSED     *
*        AND TO PROVIDE A NEW PARM AEREA IF SPLITTING IS REQUIRED.    *
*                                                                     *
*        THIS LOAD IS COMPLETELY REWRITTEN FOR MVS VERSION 2.0        *
*        IT WAS REWORKED WITH VERSION 3.0 TO IMPLEMENT CPU=           *
*        AND TEST PROCESSING.                                         *
*                                                                     *
*        PARM.LKED: RENT,REUS,REFR - DO NOT TAKE NON REENTRANT MOD.   *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
         SPACE 1
         STM   @14,@12,12(@13)        SAVE
         LR    @12,@15                BASE
         L     @03,0(,@01)            GET PARM ADDRESS
         SPACE 1
         USING GTFPRINT,@12
         USING SAVE,@13
         SPACE 1
         B     SKID
         DC    C'GTFPRINT F.BIEDERMANN VERSION 3.0'
         ORG   *-3
MYMODID  DS    CL3
         SPACE 1
SKID     DS    0H
         LA    @00,SIZE               GET SAVE AEREA
         O     @00,SUBPOOL            GET CORRECT SUBPOOL
         BAL   @01,*+4                INDICATE GETMAIN
         SVC   10                     GETMAIN
         SPACE 1
         ST    @13,4(,@01)            SAVE BACKWARD POINTER
         ST    @01,8(,@13)            SAVE FORWARD POINTER
         LA    @15,16                 GET RETURN CODE
         ST    @15,16(,@13)           SET IT, IN CASE OF PARM ERROR
         LR    @13,@01                GET MY SAVE
         B     COMMON                 GO TO CONTINUE
         SPACE 1
SUBPOOL  DC    A(40*X'1000000')       TO FIND IT IN A DUMP
         EJECT                        GO TO NEXT PAGE
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        NOW DO SOME COMMON BUISINESS.                                *
*                                                                     *
*          1. PREPARE THE NEW GOTTEN WORKAEREA                        *
*          2. LOAD GTFCCWPR AND KEEP ITS ADDRESS IN @10               *
*          3. LOAD AMDSYS00 (AMDSYS0A) AND CHECK, IF IT IS MY LOAD.   *
*          4. COPY THE CALLERS PARM AEREA IN MY OWN, IF REQUIRED      *
*          5. SET REGISTERS FOR SCAN PROCESSOR.                       *
*                                                                     *
*          REGISTERS AT ENTRY:                                        *
*                                                                     *
*            @03 PARM ADDRESS                                         *
*            @10 ENTRYPOINT OF GTFCCWPR                               *
*            @12 BASE REGISTER                                        *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 2
COMMON   DS    0H
         MVC   EXITNAME,=C'        '  CLEAR EXIT NAME
         SLR   @01,@01                CLEAR FOR LATER USE
         ST    @01,BITS               SET SWITCHES ALL ZERO
         SPACE 1
         LOAD  EPLOC=GTFCCWP          GET PRINT PROCESSOR
         SPACE 1
         LR    @10,@00                KEEP GTFCCWPR ENTRYPOINT
         LA    @10,0(,@10)            BE SURE TO HAVE FLAGS CLEARED
         CLC   MYMODID,MODID(@10)     COMPARE MODID
         BNE   MSG02                  IF WRONG, GIVE MESSAGE
         SPACE 1
         LOAD  EPLOC=AMDLOAD
         LR    @01,@00                KEEP THE ENTRYPOINT
         CLC   MYMODID,AMMODID(@01)   SAME LEVEL ?
         BNE   MSG03                  IF WRONG, GIVE MESSAGE
         SPACE 1
         DELETE EPLOC=AMDLOAD         NOW, REMOVE IT
         SPACE 1
         LOAD  EPLOC=AMDSYS0S         GET IO,SIO PRINT PROCESSOR
         SPACE 1
         ST    @00,EXITA(,@10)        SAVE ITS ADDRESS
         SLR   @00,@00                CLEAR TO RESET PARM POINTERS
         ST    @00,PARMADDR           RESET PARM ADDRESS
         STH   @00,PARM               RESET PARM LENGTH
         LTR   @03,@03                LAST PARM ?
         BNM   MSG01                  NO, ERROR
         LA    @03,0(,@03)            CLEAR FLAGS
         LTR   @03,@03                ANY PARM ADDRESS
         BZ    LINK                   NO, LEAVE
         LH    @05,0(,@03)            GET SIZE OF PARM
         CH    @05,=Y(L'PARMTEXT)     EXCEEDS PARM SIZE RESERVED ?
         BH    MSG01                  PARM ERROR
         STH   @05,PARM               SAVE PARM LENGTH
         LTR   @05,@05                ZERO PARM ?
         BNP   LINK                   YES, LEAVE
         BCTR  @05,0                  DECREASE ONE FOR EX
         EX    @05,PARMMVC            COPY PARM
         LA    @05,1(,@05)            ADJUST
         LA    @03,PARM               GET ADDRESS OF PARM FIELD
         ST    @03,PARMADDR           SAVE PARM ADDRESS
         MVI   PARMADDR,X'80'         INDICATE LAST
         LA    @03,PARMTEXT           GET ADDRESS OF PARM TEXT
         LA    @07,PARMTEXT(@05)      SET SCAN END ADDRESS
         B     SCAN                   GO TO SCAN
         SPACE 1
PARMMVC  MVC   PARMTEXT(0),2(@03)     MOVE PARM WITH EX
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        NOW SCAN THE COPIED PARM FIELD FOR KEYWORDS, THAT ARE OWNED  *
*        BY GTFPRINT. REGISTER USAGE IS AS FOLLOW:                    *
*                                                                     *
*          @03 ADDRESS OF THE CURRENT PARM POINTER                    *
*          @05 PARM LENGTH REMAINING                                  *
*          @07 USED TO KEEP SCAN END ADDRESS (WORK REGISTER)          *
*          @08 CURRENT OFFSET IN SCAN TAB                             *
*          @10 ENTRYPOINT OF GTFCCWPR                                 *
*          @12 BASE REGISTER.                                         *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
SCAN     DS    0H
         SR    @09,@09                CLEAR FOR LATER USE
         LA    @08,SCANTAB            GET ADDRESS OF SCAN TABLE
SCNLOOP  DS    0H
         IC    @09,SCNLNGTH-SCANTAB(,@08) LENGTH TO COMPARE
         LTR   @09,@09                LAST VALID ADDRESS ?
         BE    SCANEND                YES, LEAVE
         CR    @05,@09                EXEEDS REMAINING SPACE ?
         BL    SCNNEXT                YES, GO TO NEXT KEYWORD
         BCTR  @09,0                  SUBTRACT ONE FOR COMPARE
         EX    @09,EXCOMP             IS THERE A MATCH ?
         LA    @09,1(,@09)            CORRECT @09
         BE    SCNFOUND               YES, FOUND
SCNNEXT  DS    0H
         LA    @08,SCNEND-SCANTAB+3(@08,@09) TO NEXT ADDRESS
         SRL   @08,2                  ROUND TO FULL WORD
         SLL   @08,2                  ROUND TO FULL WORD
         B     SCNLOOP                TRY NEXT KEYWORD
         SPACE 1
EXCOMP   CLC   0(0,@03),SCNWORD-SCANTAB(@08) COMPARE KEYWORD WITH EX
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        AT SCANEND, SCAN FOUND OUT, THAT THE CURRENT KEYWORD DOES    *
*        NOT BELONG TO GTFPRINT, ELSE THE PARM FIELD REMAINS          *
*        UNTOUCHED, UNTIL THE NEXT ',' IS FOUND.                      *
*        REGISTER USAGE IS AS FOLLOW:                                 *
*                                                                     *
*          @03 ADDRESS OF THE CURRENT SCAN POINTER                    *
*          @05 PARM SIZE REMAINING                                    *
*          @07 PARM END ADDRESS                                       *
*          @10 ENTRYPOINT OF GTFCCWPR                                 *
*          @12 BASE REGISTER                                          *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
SCANEND  DS    0H
         SPACE 1
         CLI   0(@03),C','            NEW KEYWORD ?
         LA    @03,1(,@03)            INCREASE SCAN POINTER
         BE    SCANE01                FOUND, LEAVE
         BCT   @05,SCANEND            RETRY SCAN
         B     LINK                   LEAVE
         SPACE 1
SCANE01  DS    0H
         SPACE 1
         BCT   @05,SCAN              GO TO SCAN
         B     MSG01                 LAST MUST NOT BE ','
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        AT THAT POINT A KEYWORD IS FOUND, THAT BELONGS TO GTFPRINT.  *
*        REGISTER USAGE IS AS FOLLOW:                                 *
*                                                                     *
*          @03 ADDRESS OF THE MATCHED PARM FIELD.                     *
*          @05 PARM LENGTH REMAINING.                                 *
*          @06 START OF KEYTEXT.                                      *
*          @07 USED TO KEEP SCAN END ADDRESS.                         *
*          @08 CURRENT OFFSET OF SCAN END ADDRESS.                    *
*          @10 ENTRYPOINT OF F 'GTFCCWPR'                             *
*          @12 RESERVED AS BASE REGISTER.                             *
*          @14 SIZE OF THE KEYWORD TEXT.                              *
*          @15 ADDRESS OF SUBROUTINE TO BE ENTERED OR ZERO.           *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
SCNFOUND DS    0H
         SPACE 1
         L     @01,SCNFLAGS-SCANTAB(,@08) GET FLAGS
         L     @00,BITS               GET A COPY
         O     @01,BITS               SAVE BIT STRING
         CR    @00,@01                ANYTHING CHANGED ?
         BE    MSG01                  NO, PREVIOUSLY DEFINED
         ST    @01,BITS               SAVE IT
         ST    @01,BITS               STORE IT BACK
         LA    @06,0(@03,@09)         SKIP KEYWORD
         SLR   @15,@15                CLEAR FOR LATER USE
         ICM   @15,7,SCNADDR-SCANTAB(@08) BRANCH ADDRESS
         LR    @14,@06                GET A COPY OF THE SCAN POINTER
SCNF01   DS    0H
         CR    @07,@14                AT SCAN END ?
         BNH   SCNF02                 YES, LEAVE
         CLI   0(@14),C','            SCAN END ?
         BE    SCNF02                 YES, LEAVE
         LA    @14,1(,@14)            INCREASE SCAN
         B     SCNF01                 LOOP
          SPACE 1
SCNF02   DS    0H
         SR    @14,@06                GET EXIT NAME LENGTH
         LTR   @15,@15                IS THERE AN EXIT ?
         BCR   2,@15                  YES, USE IT
         LTR   @14,@14                ELSE KEYWORD SCHOULD BE ZERO
         BNZ   MSG01                  ERROR
         B     COMPRESS               ELSE COMPRESS PARM
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        SPLIT THE PARM FIELD FOR AMDPRDMP, BY REMOVING THE GTFPRINT  *
*        PARM FROM THE STRING. REGISTER SETTING IS AS FOLLOW:         *
*                                                                     *
*          @05 CONTAINS PARM LENGTH REMAINING                         *
*          @06 CONTAINS POINTER TO PARM TEXT                          *
*          @07 USED TO KEEP SCAN END ADDRESS                          *
*          @08 ADDRESS OF CURRENT SCAN TAB                            *
*          @09 CONTAINS LENGTH OF KEYWORD                             *
*          @10 ENTRYPOINT OF GTFCCWPR                                 *
*          @12 RESERVED AS BASE REGISTER                              *
*          @14 CONTAINS LENGTH OF TEXT                                *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
COMPRESS DS    0H
         SPACE 1
         LA    @01,0(@09,@14)         GET LENGTH OF KEYWORD
         LH    @00,PARM               GET PREVIOUS PARM SIZE
         SR    @00,@01                GET NEW PARM SIZE
         BM    MSG01                  ANY ERROR ?
         STH   @00,PARM               SAVE, IN CASE OF LAST
         BZ    LINK                   IT WAS LAST PARM
         BCTR  @00,0                  DECREASE ONE TO SKIP SEPERATOR
         STH   @00,PARM               SAVE NEW PARM SIZE
         LA    @01,1(,@01)            THIS IS TO SKIP THE SEPERATOR
         SR    @05,@01                GET NEW LENGTH
         BM    LINK                   THIS WAS LAST PARM
         SPACE 1
         LR    @15,@07                GET COPY OF END ADDRESS
         BCTR  @15,0                  DECREASE ONE FOR SEPERATOR
         SR    @15,@06                SUBTRACT CURRENT TXT POINTER
         SR    @15,@14                SUBTRACT SIZE OF TEXT
         BNP   MSG01                  IF NEGATIVE, ANY ERROR
         BCTR  @15,0                  DECREASE ONE FOR EX
         SR    @07,@01                ADJUST PARM END ADDRESS
         AR    @01,@03                GET OFFSET OF NEW PARMFIELD
         EX    @15,PARMCOMP           COMPRESS PARM
         B     SCAN                   LOOK FOR NEXT KEYWORD
         SPACE 1
PARMCOMP MVC   0(0,@03),0(@01)        COMPRESS REMAINING PARM
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING ROUTINE IS RESPONSABLE TO HANDLE THE EXIT      *
*        PARAMETER.                                                   *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
BEXIT    DS    0H
         LTR   @14,@14                IS THERE AN EXIT ?
         BNP   MSG01                  NO,ERROR
         CH    @14,=Y(L'EXITNAME)     LENGTH VALID ?
         BH    MSG01                  NO,ERROR
         BCTR  @14,0                  DECREASE ONE FOR EX
         EX    @14,EXMVC              MOVE NAME TO FIELD
         LA    @14,1(,@14)            ADJUST R14
         B     COMPRESS               GO TO REMOVE PARM
         SPACE 1
EXMVC    MVC   EXITNAME(0),0(@06)     MOVE NAME WITH EX
         EJECT
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING ROUTINE IS ENTERED TO HANDLE THE CPU=          *
*        PARAMETER.                                                   *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
BCPU     DS    0H
         SPACE 1
         CH    @14,=Y(3)              MAY BE ALL ?
         BNE   BCPU1                  NO,LEAVE
         CLC   0(3,@06),=C'ALL'       IS IT ALL ?
         BNE   BCPU1                  NO, LEAVE
         L     @00,=F'-1'             SET TO ALL
         STH   @00,CPU#(,@10)         SET IT IN GTFCCWPR
         B     COMPRESS               GO TO COMPRESS
         SPACE 1
BCPU1    DS    0H
         STM   @14,@12,12(@13)        SAVE REGISTERS
         BAL   @01,TOHEX              CONVERT NUMBER
         LM    @00,@12,20(@13)        RESTORE REGISTERS
         LTR   @15,@15                ALL OK ?
         BM    MSG01                  NO,ERROR
         STH   @15,CPU#(,@10)         SAVE IT IN GTFCCWPR
         LM    @14,@15,12(@13)        RESTORE REGISTERS
         B     COMPRESS
         EJECT
         SPACE 2
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING ROUTINE IS PROVIDED TO CONVERT A EBCDIC STRING *
*        IN A HALFWORD FORMAT. IT IS ENTERED WITH RETURN ADDRESS IN   *
*        @01, LENGTH OF DATA IN @14, AND DATATEXT IN @06.             *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 2
TOHEX    DS    0H
         LTR   @14,@14                IS THERE A LENGTH ?
         BNP   HEXERR                 NO, ERROR
         CH    @14,=Y(4)              WILL IT FIT ?
         BH    HEXERR                 NO, ERROR
         SLR   @15,@15                CLEAR FOR LATER USE
         SLR   @00,@00                CLEAR WORK REGISTER
HEXLOOP  DS    0H
         SLL   @15,4                  MAKE ROOM FOR ADDRESS OUT
         IC    @00,0(,@06)            GET STRING
         CLI   0(@06),C'0'            THIS ZERO ?
         BL    TOHEX01                NO, BELOW
         CLI   0(@06),C'9'            WITHIN VALID RANGE ?
         BNH   HEXOK                  YES, OK
         B     HEXERR                 NO, ERROR
         SPACE 1
TOHEX01  DS    0H
         CLI   0(@06),C'A'            THIS VALID ?
         BL    HEXERR                 NO, ERROR
         CLI   0(@06),C'F'            THIS VALID ?
         BH    HEXERR                 NO, ERROR
         AH    @00,=Y(9)              ROUND TO HEX VALUE
HEXOK    DS    0H
         N     @00,=F'15'             CLEAR FLAGS
         OR    @15,@00                KEEP IT
         LA    @06,1(,@06)            INCREASE ADDRESS
         BCT   @14,HEXLOOP            RETRY
         BR    @01                    RETURN
         SPACE 1
HEXERR   DS    0H                                            BIEDERMANN
         L     @15,=F'-1'             INDICATE BAD INPUT
         BR    @01                    RETURN TO CALLER
         EJECT
         SPACE 1
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        AT THIS POINT, PARAMETER PROCESSING IS FINISHED. IF FREE     *
*        OF ERRORS, THE REQIRED LOADS WILL BE LOADED AND A LINK       *
*        WILL BE MADE TO AMDPRDMP.                                    *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
LINK     DS    0H
         ICM   @00,15,BITS            GET BIT STRING
         STCM  @00,15,PRFLAGS(@10)    SAVE IT IN GTFCCWPR
         CLC   EXITNAME,=C'        '  IS THERE AN EXIT NAME ?
         BE    LINK1                  NO, SKIP LOAD
         LOAD  EPLOC=EXITNAME         GET EXIT
         ST    @00,EXITA(,@10)        SAVE IT IN GTFCCWPR
         ST    @15,LOADRC             SAVE FOR DEBUGGING
LINK1    DS    0H
         L     @01,PARMADDR           GET PARM ADDRESS
         LTR   @01,@01                IS THERE ONE ?
         BZ    LINK2                  NO, LEAVE
         LA    @01,PARMADDR           GET PARM ADDRESS
         B     LINK3                  LEAVE
LINK2    DS    0H
         L     @01,4(,@13)            GET PREVIOUS SAVE
         L     @01,24(,@01)           RESTORE @01
LINK3    DS    0H
         LINK  EPLOC=AMDPRDMP         GO TO AMDSYS00
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        AFTER RETURN FROM LINK, SOME CLEANUP BUISINESS IS REQUIRED.  *
*                                                                     *
*        1. DELETE THE MODULES LOADED                                 *
*        2. FREEMAIN THE SIZE ALLOCATED                               *
*        3. GET CALLERS GENERAL PURPOSE REGISTERS                     *
*        4. RETURN TO THE CALLER                                      *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
RETURN   DS    0H
         SPACE 1
         L     @01,4(,@13)            GET PREVIOUS SAVE
         ST    @15,16(,@01)           SAVE RETURN CODE
         DELETE EPLOC=GTFCCWP         DELETE LOAD
         SPACE 1
         DELETE EPLOC=AMDSYS0S        DELETE AMDSYS0S
         SPACE 1
         CLC   EXITNAME,=C'        '  ANY NAME PRESENT ?
         BE    EXIT                   NO, LEAVE
         DELETE EPLOC=EXITNAME        DELETE IT
EXIT     DS    0H
         LA    @00,SIZE               GET SIZE
         O     @00,SUBPOOL            FREE CORRECT SUBPOOL
         LA    @01,0(,@13)            GET ADDRESS
         L     @13,4(,@13)            GET PREVIOUS SAVE
         SVC   10                     FREE
         SPACE 1
         LM    @14,@12,12(@13)        RESTORE REGISTERS
         BR    @14                    RETURN
         EJECT
         SPACE 3
*************************************************** F.BIEDERMANN ******
*                                                                     *
*        THIS MESSAGE IS GIVEN FOR ONE OF THE FOLLOWING REASONS:      *
*                                                                     *
*          1. THE PARAMETER FIELD PASSED TO GTFPRINT IS TOO LONG.     *
*          2. THE PARAMETER FIELD PASSED TO GTFPRINT IS NOT THE ONLY  *
*             ONE.                                                    *
*          3. ONE OF THE GTFPRINT KEYWORDS CONTAINS AN ERROR.         *
*          4. ONE OF THE GTFPRINT KEYWORDS IS DOUBLE SPECIFIED.       *
*                                                                     *
*        TOGETHER WITH THIS MESSAGE, THE RETURNCODE IS SET TO 16      *
*        AND GTFPRINT IS TERMINATED.                                  *
*                                                                     *
*************************************************** F.BIEDERMANN ******
         SPACE 3
MSG01    WTO   'GTF001  PARAMETER ERROR, PGM TERMINATED',ROUTCDE=(11)
         SPACE 1
         L     @01,4(,@13)            GET PREVIOS SAVE
         LA    @15,16                 GET RETURN CODE
         ST    @15,16(,@01)           SAVE IT IN SAVEAEREA
         TM    FLAG04,FTEST           IS IT TEST ?
         BNO   EXIT                   NO, EXIT
         SPACE 1
         ABEND 122,DUMP               THIS IS FOR TEST ONLY
         EJECT
         SPACE 3
*************************************************** F.BIEDERMANN *****
*                                                                    *
*        THIS MESSAGE IS GIVEN FOR THE FOLLOWING REASON :            *
*        THE MODID OF MY LOAD (GTFPRINT), DOES NOT MATCH THE MODID   *
*        OF THE PRINT PROCESSOR (GTFCCWPR)                           *
*        AFTER THAT, THE PGM WILL IMMEDIATELY ABEND122 WITH A DUMP.  *
*                                                                    *
*************************************************** F.BIEDERMANN *****
         SPACE 3
MSG02    WTO   'GTF002  MODID OF GTFPRINT DOES NOT MATCH THE MODID OF GX
               TFCCWPR.',ROUTCDE=(11)
         SPACE 1
         ABEND 122,DUMP
         EJECT
         SPACE 3
*************************************************** F.BIEDERMANN *****
*                                                                    *
*        THIS MESSAGE IS GIVEN FOR THE FOLLOWING REASON :            *
*        THE MODID OF MY LOAD (GTFPRINT), DOES NOT MATCH THE MODID   *
*        OF THE PRINT PROCESSOR (AMDSYS00/AMDSYS0A).                 *
*        AFTER THAT, THE PGM WILL IMMIDIATELY ABEND122 WITH A DUMP   *
*                                                                    *
*************************************************** F.BIEDERMANN *****
         SPACE 3
MSG03    WTO   'GTF003  MODID OF GTFPRINT DOES NOT MATCH THE MODID OF AX
               MDSYS00.',ROUTCDE=(11)
         SPACE 1
         ABEND 122,DUMP
         EJECT
         SPACE 3
*************************************************** F.BIEDERMANN *****
*                                                                    *
*        THE FOLLOWING TABLE IS USED TO CONTAIN KEYWORDS, RESEVED    *
*        FOR GTFPRINT.                                               *
*                                                                    *
*************************************************** F.BIEDERMANN *****
         SPACE 3
SCANTAB  DS    0F
SCNFLAGS DC    AL4(FTEST)             MASK TO FIND DUPLICATE ENTRIES
SCNLNGTH DC    AL1(4)                 LENGTH OF KEYWORD
SCNADDR  DC    AL3(0)                 ADDRESS TO BRANCH TO
SCNEND   EQU   *                      TO CALCULATE TAB LENGTH
SCNWORD  DC    CL4'TEST'              KEYWORD TO COMPARE
         SPACE 1
         DS    0F                     ALIGNMENT
         DC    AL4(FEXIT)
         DC    AL1(5)
         DC    AL3(BEXIT)             CHECK FOR EXIT=
         DC    CL5'EXIT='
         SPACE 1
         DS    0F
         DC    AL4(FCPU)
         DC    AL1(4)
         DC    AL3(BCPU)              CHECK FOR CPU=
         DC    CL4'CPU='
         SPACE 1
         DS    0F
         DC    XL4'00000000'
         DC    AL1(0)                 DUMMY ENTRY OR LAST
         EJECT
         SPACE 1
         DS    0D
GTFCCWP  DC    CL8'GTFCCWPR'          LINK MODULE NAME
AMDLOAD  DC    CL8'AMDSYS00'          LOAD MODULE NAME
AMDSYS0S DC    CL8'AMDSYS0S'          LINK MODULE NAME
AMDPRDMP DC    CL8'AMDPRDMP'          LINK MODULE NAME
         SPACE 1
         LTORG
         SPACE 2
SAVE     DSECT                        TO KEEP REENTRANT
         DS    18F                    SAVE AEREA
BITS     DS    0F                     USED TO KEEP FUNCTION FLAGS
FLAG01   DS    CL1                    RESERVED
FLAG02   DS    CL1                    RESERVED
FLAG03   DS    CL1                    RESERVED
FLAG04   DS    CL1                    COMMON FUNCTION FLAGS
FTEST    EQU   X'01'                  TEST HAS BEEN USED
FEXIT    EQU   X'02'                  EXIT HAS BEEN USED
FCPU     EQU   X'04'                  CPU HAS BEEN USED
         SPACE 1
EXITNAME DS    CL8                    USER EXIT NAME
LOADRC   DS    1F                     LOAD RETURN CODE FOR FUTURE USE
PKNUMBR  DS    1D                     USED TO CONVERT DEC IN HEX
DECNUMBR DS    CL6                    USED TO CONVERT DEC TO HEX
PARMADDR DS    F                      ADDRESS OF SPLITTED PARM OR ZERO
PARM     DS    1H                     PARM LENGTH
PARMTEXT DS    CL256                  PARM TEXT
SAVEEND  DS    0F                     END OF DSECT
SIZE     EQU   SAVEEND-SAVE           LENGTH OF AEREA
         END
./ ADD NAME=GTFSIO                                 12JUN82 12.33.18
         TITLE 'IO,SIO CCW TRACE - BAUR MODIFICATION VERSION 3.0  FILE08
               8  -- IBM INTERNAL USE ONLY -- '
         SPACE 1
GTFSIO   CSECT
         ENTRY AHLTIO
         ENTRY AHLTSIO1
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING SECTION CONTAINS THE EQUATES USED IN THIS PGM. *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
@00      EQU   00                      EQUATES FOR REGISTERS 0-15
@01      EQU   01
@02      EQU   02
@03      EQU   03
@04      EQU   04
@05      EQU   05
@06      EQU   06
@07      EQU   07
@08      EQU   08
@09      EQU   09
@10      EQU   10
@11      EQU   11
@12      EQU   12
@13      EQU   13
@14      EQU   14
@15      EQU   15
         SPACE 1
PSA      EQU   0
FLCPOPSW EQU   PSA+40
PSALCCAV EQU   PSA+528
PSACPUSA EQU   PSA+794
PSARSVT  EQU   PSA+896
         SPACE 1
CVTPTR   EQU   16
CVTMAP   EQU   0
CVTPGSIA EQU   CVTMAP+348
CVTASVT  EQU   CVTMAP+556
         SPACE 1
ASVT     EQU   0
ASVTENTY EQU   ASVT+528
         SPACE 1
IOSB     EQU   0
IOSASID  EQU   IOSB+6
IOSCC    EQU   IOSB+20
IOSERP   EQU   IOSB+52
IOSRST   EQU   IOSB+72
         SPACE 1
LCCA     EQU   0
LCCAPGR2 EQU   LCCA+72
LCCACPUS EQU   LCCA+536
         SPACE 1
ASCB     EQU   0
ASCBJBNI EQU   ASCB+172
ASCBJBNS EQU   ASCB+176
         SPACE 1
WSAC     EQU   0
WSACGTF  EQU   WSAC+4
         SPACE 1
UCB      EQU   0
UCBOB    EQU   0
UCBSFLS  EQU   UCBOB+6
UCBFLA   EQU   UCBSFLS
UCBACTV  EQU   X'02'                                            LZ90124
UCBTYP   EQU   UCBOB+16
UCBDEV   EQU   UCBOB+24
         SPACE 1
MCQE     EQU   0
MCQEAT   EQU   MCQE+32
         SPACE 1
FRRS     EQU   0
FRRSHEAD EQU   FRRS
FRRSCURR EQU   FRRSHEAD+12
FRRSENTR EQU   0
         SPACE 1
SDWA     EQU   0
SDWAPARM EQU   SDWA
SDWAABCC EQU   SDWA+4
SDWAEC1  EQU   SDWA+104
SDWANXT1 EQU   SDWAEC1+4
SDWAAEC2 EQU   SDWA+128
SDWATRN2 EQU   SDWA+132
SDWASRSV EQU   SDWA+136
SDWAMCH  EQU   SDWA+204
SDWAMCHI EQU   SDWAMCH+8
SDWAMCHD EQU   SDWAMCHI+1
SDWAREGU EQU   SDWAMCHD
SDWAINSF EQU   SDWAMCHD
SDWAFLGS EQU   SDWA+232
SDWAERRA EQU   SDWAFLGS
SDWAMCHK EQU   SDWAERRA
SDWAERRC EQU   SDWAFLGS+2
SDWAPERC EQU   SDWAERRC
SDWAEAS  EQU   SDWAERRC
SDWAERRD EQU   SDWAFLGS+3
SDWACLUP EQU   SDWAERRD
SDWARTYA EQU   SDWA+240
SDWAPARQ EQU   SDWA+252
SDWARCDE EQU   SDWAPARQ
SDWARECP EQU   SDWA+292
SDWASNPA EQU   SDWA+320
         SPACE 1
GTFPCT   EQU   0
GTFSTAT  EQU   GTFPCT+8
GTFDEBUG EQU   GTFSTAT
GTFOPTS  EQU   GTFPCT+12
GTFOPTS4 EQU   GTFOPTS+3
OPTTIME  EQU   GTFOPTS4
PCTCATF  EQU   GTFPCT+16
SBLOKRTN EQU   GTFPCT+188
PCTDMPMD EQU   GTFPCT+308
PCTWTOMD EQU   GTFPCT+312
         SPACE 1
MCAWSA   EQU   0
MCATRAIL EQU   MCAWSA+256
MCATRL4  EQU   MCATRAIL+3
MCAMASCB EQU   MCATRL4
MCAMCR   EQU   MCAWSA+264
MCARECPR EQU   MCAWSA+268
MCAREC   EQU   MCAWSA+284
MCAWORK  EQU   MCAWSA+584
MCRWSA   EQU   0
MCROUTSA EQU   MCRWSA+64
MCRMCA   EQU   MCRWSA+128
MCREID   EQU   MCRWSA+134
MCIREGS  EQU   MCRWSA+136                                   BIEDERMANN
JOBNAME  EQU   0
JBNMTEST EQU   JOBNAME
FRRPARM  EQU   0
SVR14NRM EQU   FRRPARM+8
SVR14FRR EQU   FRRPARM+12
         SPACE 1
AHLGREC  EQU   0
AHLGLGTH EQU   AHLGREC
AHLGRES  EQU   AHLGREC+2
AHLGAID  EQU   AHLGREC+4
AHLGFID  EQU   AHLGREC+5
AHLGTST  EQU   AHLGREC+6
AHLGEIDS EQU   AHLGTST
AHLGEIDL EQU   AHLGREC+14
EVREGS   EQU   LCCAPGR2
EVREG2   EQU   EVREGS+8
EVREG7   EQU   EVREGS+28
LCCAPTR  EQU   PSALCCAV
PSARSVTE EQU   PSARSVT
PSACSTK  EQU   PSARSVTE
UCBOBS06 EQU   UCBDEV
WORKAREA EQU   MCAWORK
MCQESAV  EQU   WORKAREA
MCAWTO   EQU   WORKAREA+8
WTOLIST  EQU   MCAWTO
WTOFUNC  EQU   WTOLIST+4
WTOUN    EQU   WTOLIST+14
IOSCPA   EQU   IOSRST
MCRREGS  EQU   MCROUTSA
MCRREG0  EQU   MCRREGS
IOREC    EQU   MCAREC
IOCPA    EQU   IOREC+34
IODSID   EQU   IOREC+46
IOFLGS   EQU   IOREC+50
SIOREC   EQU   MCAREC
*                                      START UNREFERENCED COMPONENTS
FRRSPARM EQU   FRRSENTR+8
FRRSFRRA EQU   FRRSENTR
FRRSELEN EQU   FRRSHEAD+8
FRRSLAST EQU   FRRSHEAD+4
FRRSEMP  EQU   FRRSHEAD
IOSUCB   EQU   IOSB+16
PSALSFCC EQU   PSA+1012
CSW      EQU   64
CAW      EQU   72
         EJECT
GTFSIO   CSECT
         SPACE 1
         USING GTFSIO,@11
         USING GTFSIO+4096,@12
         USING RECORD,@03              USING FOR WORKAREA
         USING WORK01,@06              WORK AEREA ADDRES.    BIEDERMANN
         SPACE 1
SUSWITCH DS    0F
SUFLAG1  DC    X'00'                   SU SWITCH 1           BIEDERMANN
SU50     EQU   X'80'                   SU 50 INSTALLED       BIEDERMANN
SU64     EQU   X'40'                   SU 64 INSTALLED       BIEDERMANN
SU65     EQU   X'40'                   SU 65 INSTALLED       BIEDERMANN
SUFLAG2  DC    X'00'                   SU SWITCH 2           BIEDERMANN
PSWITCH  DC    X'00'                   OPTION SWITCH               BAUR
PIO      EQU   X'80'                   IO REQUESTED          BIEDERMANN
PCCW     EQU   X'40'                   THE CCW= PARM WAS USED.
PDATA    EQU   X'20'                   THE DATA= PARM WAS USED
PJOBFLAG EQU   X'10'                   JOBNAME TO BE TRACED  BIEDERMANN
PSIO     EQU   X'08'                   SIO REQUESTED         BIEDERMANN
PSTATMOD EQU   X'04'                   STATUS MODIFIER       BIEDERMANN
PSORT    EQU   X'02'                   SORT CCWS             BIEDERMANN
PTEST    EQU   X'01'                   TEST REQUESTED        BIEDERMANN
OSWITCH  DC    X'00'                   SUB FUNCTION FLAGS    BIEDERMANN
OIOSB    EQU   X'80'                   DUMP OF IOSB REQUESTED
OANYERR  EQU   X'40'                   ANY NON GTFCCW PARM ERROR
OSUDATA  EQU   X'10'                   THE SUDATA= PARM WAS USED
AHLID    DC    C'3.0'                  ID FOR GTFCCW         BIEDERMANN
         DC    A(CCWINIT)              ADDRESS OF INIT ROUTINE
         DC    A(CCWTERM)              ADDRESS OF TERMINATION ROUTIN
WORKPNT  DC    A(WORKTAB)              POINTER TO WORK AEREA TABLE
         DS    0F
         DS    CL16                    RESERVED                 LZ90130
AHLTPARM DS    0CL16                   PARM AEREA FOR CCWTRACE  LZ90130
RCMAX    DC    F'50'                   CCWS TO BE TRACED           BAUR
MAX      DC    F'24'                   DATA BYTES TO BE TRACED     BAUR
PJOBNAME DC    CL8'        '           JOBNAME TO BE TRACED  BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        WARNING, NEVER PUT ANY STATEMENT BEFORE THAT POINT,
*        OTHERWISE YOU HAVE TO CHANGE GTFCCW TOO.            BIEDERMANN
         SPACE 1
         DC    C'GTFSIO VERSION 3.0'               BIEDERMANN
         EJECT
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        GATHER DATA AND BUILD IO TRACE RECORD                        *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
AHLTIO   DS    0H                                            BIEDERMANN
         USING *,@15                                         BIEDERMANN
         LM    @11,@12,BASEADR         GET MY BASE ADDRESS   BIEDERMANN
         DROP  @15                                           BIEDERMANN
         SPACE 1                                             BIEDERMANN
         LR    @10,@14                 SAVE RETURN ADDRESS   BIEDERMANN
         TM    PSWITCH,PIO             IS IO TRACE ACTIV ?   BIEDERMANN
         BNO   AHLTIO1                 NO,LEAVE              BIEDERMANN
         BAL   @14,GETWORK             SET WORKAEREA ADDRESS BIEDERMANN
         LTR   @06,@06                 CHECK WORK REGISTER   BIEDERMANN
         BZ    AHLTIO1                 LEAVE                 BIEDERMANN
         ST    @10,RETURN              SAVE RETURN ADDRESS   BIEDERMANN
         L     @15,IOENTRY             GET EXIT ADDRESS      BIEDERMANN
         BALR  @14,@15                 GO, BUILD THE IO RECORD
         BALR  @14,0                   GET ME A BASE         BIEDERMANN
         USING *,@14                   TO FETCH MY BASE      BIEDERMANN
         LM    @11,@12,BASEADR         GET MY BASE           BIEDERMANN
         DROP  @14                     NO LONGER VALID       BIEDERMANN
         LR    @01,@15                 SAVE RETURN CODE      BIEDERMANN
         BAL   @14,GETWORK             GET MY WORK           BIEDERMANN
         L     @14,RETURN              GET EXIT ADDRESS      BIEDERMANN
         LTR   @15,@01                 CHECK RETURN CODE     BIEDERMANN
         BCR   7,@14                   RETURN, IF NOT ZERO   BIEDERMANN
         IC    @14,SW3                 GET RETRY SWITCH      BIEDERMANN
         SLL   @14,1                   SET TO PREVIOUS       BIEDERMANN
         ST    @15,SWITCHES-SW1+SW1-SW2+SW2-SW3+SW3-SW4+SW4  BIEDERMANN
         STC   @14,SW3                 SAVE SWITCHES         BIEDERMANN
         IC    @14,SW6                 GET IO SWITCH         BIEDERMANN
         SLL   @14,1                   CALCULATE             BIEDERMANN
         STC   @14,SW5                 STORE IT BACK         BIEDERMANN
         ST    @15,LAST                SET LAST TO ZERO      BIEDERMANN
         STH   @15,RECCOUNT            SET COUNT TO ZERO     BIEDERMANN
         B     START                   GO TO CCW HANDLER     BIEDERMANN
         SPACE 1                                             BIEDERMANN
AHLTIO1  DS    0H                                            BIEDERMANN
         LR    @14,@10                 RESTORE EXIT ADDRESS  BIEDERMANN
         L     @15,IOENTRY             GET EXIT ADDRESS      BIEDERMANN
         BR    @15                     XCTL TO IO TRACE      BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        GATHER DATA AND BUILD SIO TRACE RECORD                       *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
AHLTSIO1 DS    0H                                            BIEDERMANN
         USING *,@15                                         BIEDERMANN
         LM    @11,@12,BASEADR         GET MY BASE           BIEDERMANN
         DROP  @15                                           BIEDERMANN
         SPACE 1                                             BIEDERMANN
         LR    @10,@14                 SAVE RETURN ADDRESS   BIEDERMANN
         TM    PSWITCH,PSIO            IS SIO ACTIV          BIEDERMANN
         BZ    AHLTSIO2                NO, LEAVE             BIEDERMANN
         BAL   @14,GETWORK             SET WORK POINTER ADDRESS
         LTR   @06,@06                 IS THERE ONE ?        BIEDERMANN
         BZ    AHLTSIO2                NO, EXIT              BIEDERMANN
         ST    @10,RETURN              SAVE EXIT ADDRESS     BIEDERMANN
         L     @15,SIOENTRY            ADDRESS OF SIO TRACE  BIEDERMANN
         BALR  @14,@15                 GO TO SIO TRACE       BIEDERMANN
         BALR  @14,0                   GET ME A BASE         BIEDERMANN
         USING *,@14                   TO ADDRESS MY BASE    BIEDERMANN
         LM    @11,@12,BASEADR         GET ME MY BASE        BIEDERMANN
         DROP  @14
         LR    @01,@15                 SAVE RETURN CODE      BIEDERMANN
         BAL   @14,GETWORK             SET UP @06            BIEDERMANN
         L     @14,RETURN              GET EXIT ADDRESS      BIEDERMANN
         LTR   @15,@01                 SET UP RETURN CODE    BIEDERMANN
         BCR   7,@14                   LEAVE, IF NOT ZERO    BIEDERMANN
         IC    @14,SW3                 GET SW3 FOR RETRY     BIEDERMANN
         SLL   @14,1                   SET TO RETRY          BIEDERMANN
         ST    @15,SWITCHES-SW1+SW1-SW2+SW2-SW3+SW3-SW4+SW4  BIEDERMANN
         STC   @14,SW3                 KEEP RETRY SWITCHES   BIEDERMANN
         IC    @14,SW6                 GET SIO SWITCH        BIEDERMANN
         SLL   @14,1                   CALCULATE             BIEDERMANN
         STC   @14,SW6                 SAVE SIO SWITCH       BIEDERMANN
         ST    @15,LAST                SET LAST TO ZERO      BIEDERMANN
         STH   @15,RECCOUNT            SET COUNT TO ZERO     BIEDERMANN
         B     START                   GO TO CCWTRACE        BIEDERMANN
         SPACE 1                                             BIEDERMANN
AHLTSIO2 DS    0H                                            BIEDERMANN
         LR    @14,@10                 GET RETURN ADDRESS    BIEDERMANN
         L     @15,SIOENTRY            GET ADDRESS OF SIO TRACE
         BR    @15                     XCTL TO IO TRACE      BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THIS ROUTINE ACTIVATES THE CCWTRACE BY ZAPPING AHLTSIO.      *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
CCWINIT  DS    0H                                            BIEDERMANN
         USING *,@15                                         BIEDERMANN
         STM   @14,@12,12(@13)         SAVE REGISTERS        BIEDERMANN
         LM    @11,@12,BASEADR         GET BASE ADDRESS      BIEDERMANN
         DROP  @15                                           BIEDERMANN
         LA    @15,WORKLAST-WORKTAB     CALCULATE NR OF TABS BIEDERMANN
         SRL   @15,2                   DEVIDE BY FOUR        BIEDERMANN
         L     @08,WORKPNT             GET WORK POINTERS     BIEDERMANN
CCWINIT1 DS    0H
         BCT   @15,ICLEAR              CLEAR WORK AS REQUIRED
         MVC   AHLTPARM,4(@01)         MOVE PARM             BIEDERMANN
         MVC   SUSWITCH,0(@01)         MOVE SWITCHE          BIEDERMANN
         SR    @15,@15                 SET RETURN CODE       BIEDERMANN
         LM    @00,@12,20(@13)         SAVE REGISTERS        BIEDERMANN
         BR    @14                     RETURN                BIEDERMANN
         SPACE 1                                             BIEDERMANN
ICLEAR   DS    0H                      CLEAR WORK AEREA      BIEDERMANN
         L     @06,0(,@08)             GET WORK AEREA        BIEDERMANN
         SLR   @07,@07                 CLEAR FOR LATER USE   BIEDERMANN
         ST    @07,SWITCHES-SW1+SW1-SW2+SW2-SW3+SW3-SW4+SW4  BIEDERMANN
         STC   @07,SW5                 CLEAR SW5             BIEDERMANN
         STC   @07,SW6                 CLEAR SW6             BIEDERMANN
         LA    @08,L'WORKTAB(,@08)     GO TO NEXT ENTRY      BIEDERMANN
         B     CCWINIT1                RETRY                 BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING SUBROUTINE IS CALLED BY GTFCCW TO TERMINATE    *
*        CCWTRACE.                                                    *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
CCWTERM  DS    0H                                            BIEDERMANN
         USING *,15                                          BIEDERMANN
         STM   @14,@12,12(@13)         SAVE REGISTERS        BIEDERMANN
         LM    @11,@12,BASEADR         GET BASE ADDRESS      BIEDERMANN
         DROP  @15                                           BIEDERMANN
         XC    SUSWITCH,SUSWITCH       DISABLE CCWTRACE      BIEDERMANN
         LM    @00,@12,20(@13)         SAVE REGISTERS        BIEDERMANN
         SLR   @15,@15                 CLEAR FOR LATER USE   BIEDERMANN
         BR    @14                     RETURN                BIEDERMANN
         SPACE 1                                             BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING SUB ROUTINE IS USED TO BUILD AN FRR.           *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 2                                             BIEDERMANN
AHLPRECM DS    0H
         SPACE 1
*              SETFRR A,FRRAD=(RTCAREG),WRKREGS=(@07,@09),PARMAD=(@09)
         SPACE 1
         L     @07,PSACSTK-PSA
         L     @09,FRRSCURR-FRRS(,@07)
         C     @09,FRRSLAST-FRRS(,@07)
         BE    PSALSFCC-PSA
         A     @09,FRRSELEN-FRRS(,@07)
         ST    @09,FRRSCURR-FRRS(,@07)
         ST    @04,FRRSFRRA-FRRSENTR(,@09)
         XC    FRRSPARM-FRRSENTR(24,@09),FRRSPARM-FRRSENTR(@09)
         ST    @10,FRRSPARM-FRRSENTR+SVR14NRM(,@09)
         TM    FLCPOPSW+1,X'81'        TEST FOR SUPER STATE
         BNZ   AHLPSCM2                EXIT
         BR    @14
         EJECT
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING SUB ROUTINE IS RESPONSABLE TO LOAD EITHER      *
*        THE LCCA IN REG @15 OR GET THE POINTERS TO MCIREGS. THIS     *
*        DEPENDS, WETHER EITHER SU50 OR SU65 IS INSTALLED OR NOT.     *
*        THIS IS DONE BY CHECKING THE SUFLAGS PASSED BY GTFCCW.       *
*                                                                     *
*        ENTRY AT LABEL AHLLCCA WITH RETURN ADDRESS IN @14            *
*        REGISTERS USED : @14 AS RETURN ADDRESS                       *
*                         @15 CONTAINS THE OUTPUT ADDRESS             *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 2                                             BIEDERMANN
AHLLCCA  DS    0H                                            BIEDERMANN
         TM    SUFLAG1,SU50+SU65       IS SU50 OR SU65 ON ?  BIEDERMANN
         BNZ   AHLLCCA1                YES, BRANCH           BIEDERMANN
         L     @01,LCCAPTR             NO, GET LCCA          BIEDERMANN
         BR    @14                     RETURN                BIEDERMANN
         SPACE 1                                             BIEDERMANN
AHLLCCA1 DS    0H                                            BIEDERMANN
         L     @01,MCAMCR(,@03)        GET POINTER TO REG AEREA
         LA    @01,MCIREGS-EVREGS(,@01) BACK UP              BIEDERMANN
         BR    @14                     RETURN                BIEDERMANN
         EJECT
         SPACE 2
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE BUILDS THE GTF RECORD PREFIX AND THEN     *
*        CALLS AHLSBLOK TO STACK THE RECORD.                          *
*                                                                     *
***********************************************************************
         SPACE 2
AHLPSCM1 DS    0H
         LA    @01,MCARECPR(,@03)      GET ADDRESS OF OUTBUFF
         L     @12,MCAMCR(,@03)        GET EID AND PUT IT IN THE PREFIX
         ICM   @12,3,MCREID(@12)       GET EID
         STCM  @12,3,AHLGEIDL(@01)     SAVE EID IN RECORD
         TM    OPTTIME(@02),X'01'      CHECK FOR TIME STAMP REQUESTED
         BNO   AHLPSC01                NO, NO TIMESTAMP NEEDED
         LA    @05,16(,@05)            SET UP RECORD LENGTH
         B     AHLPSC02                USE ROUTINE WITHOUT TIMESTAMP
         SPACE 1
AHLPSC01 DS    0H                      ENTRY FOR SHORT PREFIX
         LA    @05,8(,@05)             CALCULATE TOTAL SIZE
         LA    @01,8(,@01)             CALCULATE OFFSET IN RECORD
AHLPSC02 DS    0H
         STH   @05,AHLGLGTH(,@01)      PUT IT IN THE REFIX.
         SLR   @12,@12                 ZERO OUT FOR QSAM COMPATIBILITY
         STH   @12,AHLGRES(,@01)
         MVI   AHLGAID(@01),X'FF'      PUT *** AID *** INTO PREFIX
         STC   @12,AHLGFID(,@01)       PUT *** FID *** INTO PREFIX
         LA    @15,4                   SET DEFAULT RETURN CODE
         L     @14,SBLOKRTN(,@02)      GET A STABLE COPY OF AHLSBLOK
         LTR   @14,@14                 CHECK IF AHLSBLOK IS AVAILABLE
         BCR   8,@10                   NO, LEAVE
         LR    @15,@14                 GET ADDRESS IN @15
         BALR  @14,@15                 GO TO AHLSBLOK
         BR    @10                     RETURN                BIEDERMANN
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING ROUTINE IS USED TO RELEASE AN FRR AND TO LEAVE *
*        THE MODULE.                                                  *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
AHLPSCM2 DS    0H
         SPACE 1
*   GEN(SETFRR D,WRKREGS=(7,9));
*        SETFRR D,WRKREGS=(7,9)
         SPACE 1
         DS    0H
         L     @07,PSACSTK-PSA
         L     @09,FRRSCURR-FRRS(,@07)
         L     @10,SVR14NRM+FRRSPARM-FRRSENTR(,@09)          BIEDERMANN
         C     @09,FRRSEMP-FRRS(,@07)
         BE    *+12
         S     @09,FRRSELEN-FRRS(,@07)
         ST    @09,FRRSCURR-FRRS(,@07)
         BR    @10                     RETURN
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING ROUTINE IS USED TO ALLOCATE A WORKAEREA        *
*        ACCORDING TO THE CPUID IN THE PSA. IF THE CPUID IS BELOW 0   *
*        OR LARGER, THAN TABLES ARE ALLOCATED, EXIT IS MADE WITH ZERO *
*        IN @06, INSTEAD OF THE TABLE ADDRESS.                        *
*                                                                     *
*        ENTRY IS MADE WITH RETURN ADDRESS IN @14. REGISTERS USED:    *
*        @06,@14,@15                                                  *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
GETWORK  DS    0H                                            BIEDERMANN
         SPACE 1                                             BIEDERMANN
         SLR   @06,@06                SET IN CASE OF ERROR   BIEDERMANN
         LH    @15,PSACPUSA           GET CPUID              BIEDERMANN
         LTR   @15,@15                IS THERE ONE ?         BIEDERMANN
         BCR   4,@14                  IF NOT, RETURN         BIEDERMANN
         SLL   @15,2                  MULT BY 4              BIEDERMANN
         CH    @15,=Y(WORKLAST-WORKTAB) STILL VALID ?        BIEDERMANN
         BCR   10,@14                 NO, LEAVE              BIEDERMANN
         L     @06,WORKPNT            GET ADDRESS OF TABLE   BIEDERMANN
         L     @06,0(@06,@15)         GET WORK AEREA         BIEDERMANN
         BR    @14                    RETURN                 BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING ROUTINE IS TO HANDLE ERRORS. FIRST IT TRIES    *
*        TO FIND OUT, WETHER THIS IS A MCH CHECK OR A PGM CHECK.      *
*          IN CASE OF A PGM CHECK, LOOK FOR RECOVERABLE ERROR AND     *
*        SET UP FOR RETRY, IF POSSIBLE. IF RECURSION, OR NOT          *
*        RECOVERABLE, SET UP EXIT FOR PRCULATION. NOTE, THAT ONLY ONE *
*        FAILURE IS ALLOWED PER TWO ENTRIES TO THIS MODULE.           *
*          IN CASE OF PGM CHECK, LOOK IF THE ERROR IS RECOVERABLE.    *
*        IF NOT, SET UP ENTRY FOR PERCULATION, OTHERWISE SET RETRY    *
*        ADDRESS IN SDWA AND SET UP FOR EXIT. NOTE, THAT ONLY ONE     *
*        PGM CHECK IS ALLOWED IN TWO CASES OF THE SAME ENTRY.         *
*                                                                     *
*        EACH ENTRY TO THIS ROUTINE WILL WRITE A LOGEC ENTRY.         *
*        ERRORS WILL BE KEEPT IN RELATION TO THE FAILING CPU.         *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
FRRENTRY DS    0H                                            BIEDERMANN
         SPACE 1                                             BIEDERMANN
         USING *,@15                   TO RELOAD MY BASE     BIEDERMANN
         LM    @11,@12,BASEADR         GET MY BASE           BIEDERMANN
         DROP  @15                                           BIEDERMANN
         SPACE 1                                             BIEDERMANN
         L     @09,SDWAPARM(,@01)      GET POINTER TO USER PARM
         ST    @14,SVR14FRR(,@09)      SAVE RETURN ADDRESS TO RTM
         SPACE 1                                             BIEDERMANN
         BAL   @14,GETWORK             TRY TO SET UP @06     BIEDERMANN
         LTR   @06,@06                 IS THERE ONE ?        BIEDERMANN
         BZ    FRREXIT                 NO, DO NOT RETRY      BIEDERMANN
         MVC   WORKPSW,SDWAEC1(@01)    MOVE PSW              BIEDERMANN
         MVC   WORKCOMP,SDWAABCC(@01)  MOVE COMP CODE
         MVC   WORKVIRT,SDWATRN2(@01)  LAST TRANSL. EXEPTION
         MVC   WORKSAVE(L'WORKSAVE*16),SDWASRSV(@01)         BIEDERMANN
         L     @12,LCCAPTR             GET ADDRESS OF MCAWSA BIEDERMANN
         L     @12,LCCACPUS(,@12)      FROM MCRWSA           BIEDERMANN
         L     @12,WSACGTF(,@12)                             BIEDERMANN
         L     @03,MCRMCA(,@12)                              BIEDERMANN
         SPACE 1                                             BIEDERMANN
         L     @12,MCAMCR(,@03)        MCQE AT TIME OF ERROR BIEDERMANN
         L     @00,MCRREG0(,@12)                             BIEDERMANN
         SPACE 1                                             BIEDERMANN
         LR    @12,@00                 PCT AT TIME OF ERROR  BIEDERMANN
         L     @02,MCQEAT(,@12)                              BIEDERMANN
         STM   @00,@01,MCQESAV(@03)    IN CASE REGS ARE USED
         MVC   SDWARECP(L'RECID,@01),RECID  COPY RECORD PARAMETERS
         SPACE 1                                             BIEDERMANN
*        IF A MACHINE CHECK WITH INSRUCTION FAILURE OCCORED,
*        AND PSW AND REISTERS ARE VALID, DO A RETRY.
         SPACE 1                                             BIEDERMANN
         TM    SDWAMCHK(@01),X'80'     A MACHINE CHECK OCCURED ?
         BNO   FRR001                  NO, SKIP              BIEDERMANN
         CLI   SW3,X'00'               PREVIOUS ONE ?        BIEDERMANN
         BNE   FRREXIT                 YES, EXIT             BIEDERMANN
         OI    SW3,SW3MCH              SET RECURSION         BIEDERMANN
         TM    SDWAINSF(@01),X'04'     INSTRUCTION FAILURE ? BIEDERMANN
         BNO   FRREXIT                 NO, NO RETRY          BIEDERMANN
         TM    SDWAREGU(@01),X'60'     REGISTERS IN SDWA VALID ?
         BNZ   FRREXIT                 NO, NO RETRY          BIEDERMANN
         MVI   SDWARCDE(@01),X'04'     SET RETURN CODE FOR RETRY
         L     @12,SDWANXT1(,@01)      SAVE RETY ADDRESS     BIEDERMANN
         ST    @12,SDWARTYA(,@01)      SAVE RETRY ADDRESS    BIEDERMANN
         L     @14,SVR14FRR(,@09)      SET UP RETURN TO RTM  BIEDERMANN
         BR    @14                     RETURN                BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        CHECK FOR PERCOLATION. IF YES, LOOK IF PREVIOUS WAS SUCCESSFUL
*        AND EXIT IF IT WAS.
         SPACE 1                                             BIEDERMANN
FRR001   DS    0H                      ENTRY IS FOR SIO      BIEDERMANN
         TM    ENTRYTYP+1,TSIO         SIO FAILING ?         BIEDERMANN
         BNO   FRR002                  NO, ELSE IO           BIEDERMANN
         CLI   SW6,X'00'               PREVIOUS ERROR ?      BIEDERMANN
         BNE   FRREXIT                 YES, NO RETRY         BIEDERMANN
         OI    SW6,SW6SIO              SET TO SIO ERROR      BIEDERMANN
         B     FRR003                  CONTINUE              BIEDERMANN
         SPACE 1                                             BIEDERMANN
FRR002   DS    0H                      ENTRY IS FOR IO       BIEDERMANN
         CLI   SW5,X'00'               PREVIOUS ERROR ?      BIEDERMANN
         BNE   FRREXIT                 YES, NO RETRY         BIEDERMANN
         OI    SW5,SW5IO               SET IO ERROR          BIEDERMANN
FRR003   DS    0H                      ENTRY IS COMMON FOR IO AND SIO
         TM    SDWAPERC(@01),X'10'     CHECK IF ROUTINE WAS PERCOLATED
         BNZ   FRREXIT                 YES, EXIT             BIEDERMANN
         TM    PSWITCH,PTEST           LOOK FOR TEST OPTION  BIEDERMANN
         BO    FRREXIT                 TEST, SKIP            BIEDERMANN
         TM    SDWACLUP(@01),X'80'     EXIT IS ONLY FOR CLEAN UP ?
         BNO   FRRRETRY                NO, RETRY             BIEDERMANN
FRREXIT  DS    0H                      EXIT WITHOUT RETRY    BIEDERMANN
         MVI   SDWARCDE(@01),X'00'     SET RETURN CODE       BIEDERMANN
         L     @14,SVR14FRR(,@09)      GET EXIT ADDRESS      BIEDERMANN
         BR    @14                     LEAVE WITHOUT RETRY   BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        THIS ENTRY IS USED, TO SET UP RECOVERY, BY USING THE
*        THE RETRY OPTION IN THE SDWA.                       BIEDERMANN
         SPACE 1
FRRRETRY DS    0H                      PGM CHECK RETRY       BIEDERMANN
         MVI   SDWARCDE(@01),X'04'     SET RETURNCODE IN SDWA
         LA    @14,FRRCEXIT            SET UP RETRY ADDRESS  BIEDERMANN
         ST    @14,SDWARTYA(,@01)      SET RETRY ADDRESS     BIEDERMANN
         L     @14,SVR14FRR(,@09)      GET EXIT ADDRESS      BIEDERMANN
         BR    @14                     RETURN                BIEDERMANN
         SPACE 3                                             BIEDERMANN
RECID    DC    CL24'GTFSIO  GTFSIO  GTFSIO  '                BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*      COMMON FRR EXIT ROUTINE FOR NONRECOVERABLE FRRS                *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
FRRCEXIT DS    0H                                            BIEDERMANN
         SPACE 1                                             BIEDERMANN
         TM    SW2,ANY                 ANY DATA TRACED ?     BIEDERMANN
         BZ    FRREX01                 NO, SKIP NEXT PART    BIEDERMANN
         BAL   @15,TRC                 WRITE OUT CURRENT RECORD
         NI    SW2,255-ANY             RESET ANY             BIEDERMANN
         SPACE 1                                             BIEDERMANN
FRREX01  DS    0H                                            BIEDERMANN
         TM    SW2,SW2DUMP             DID WE TRY TO DUMP ?  BIEDERMANN
         BO    IOSTRACE                YES, DO NOT RERTRY    BIEDERMANN
         OI    SW2,SW2DUMP             SET DUMP IN CONTROL   BIEDERMANN
         MVI   SW1,SMSG                INDICATE MESSAGE      BIEDERMANN
         LA    @15,L'SW+L'CPUID+L'RECNO+L'BLKID+L'BLKADDR+L'BLKLGTH
         LCR   @15,@15                 GET COMPLEMENT        BIEDERMANN
         A     @15,TWTSIZE             ADD SIZE              BIEDERMANN
         ST    @15,SIZE                SAVE IT               BIEDERMANN
         MVC   BLKID,=C'DUMP'          MOVE ID               BIEDERMANN
         LA    @05,WORK01              GET AEREA TO DUMP     BIEDERMANN
         STCM  @05,7,BLKADDR           SAVE WORK ADDRESS     BIEDERMANN
         LA    @10,WORKEND-WORK01      GET SIZE OF WORK AEREA
         STCM  @10,3,BLKLGTH           SAVE AEREA SIZE       BIEDERMANN
         BAL   @14,TRACE               TRACE IT              BIEDERMANN
         MVI   SW1,SMSG+END            SET SW1 AS REQUIRED   BIEDERMANN
         BAL   @15,TRC                 WRITE ALL OUT         BIEDERMANN
         NI    SW2,255-ANY             RESET ANY             BIEDERMANN
         MVI   SW1,0                   RESET SW1             BIEDERMANN
         B     IOSTRACE                TRY TO TRACE IOSB AND EWA
         SPACE 1                                             BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING CODE CONTAINS REENTRENT DATA ONLY              *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
BASEADR  DC    A(GTFSIO),A(GTFSIO+4096)
IOENTRY  DC    V(BHLTIO)               ADDRESS OF REMOUTE IO MODULE
SIOENTRY DC    V(BHLTSIO1)             ADDRESS OF RMOUTE SIO MODULE
TWTSIZE  DC    A(256)                  LENGTH OF WA                BAUR
         SPACE 1                                             BIEDERMANN
*        THE FOLLOWING VALUES ARE USED BY GTF TO IDENTIFY THE RECORD
*        TYPE IN PROCESS. THE CCWTRACE MODIFICATIONS WILL USE ITS
*        OWN TYPES FOR THE FOLLOWING REASONS.
*
*        1. THE RECORD TYP MUST BE KEPT IN THE WORK AEREA FOR DEBUGGING
*        2. THE RECORD ID IS TO BE ACCESS WITHIN ONE BYTE.
*        3. THE RECORD TYP IS TO BE ACCESSED WITH ONE INSTRUCTION.
         SPACE 1                                             BIEDERMANN
SIOID    DC    XL2'5100'               TYPE ID FOR SIO       BIEDERMANN
EOSID    DC    XL2'5101'               TYPE ID FOR EOS       BIEDERMANN
IOID     DC    XL2'5200'               TYPE ID FOR IO        BIEDERMANN
PCIID    DC    XL2'2100'               TYPE ID FOR PCI       BIEDERMANN
UIOID    DC    XL2'5201'               TYPE ID FOR UIO       BIEDERMANN
         SPACE 1                                             BIEDERMANN
         SPACE 1                                             BIEDERMANN
MSG02S   DC    C'MSG '                 ID FOR MESSAGE        BIEDERMANN
         DC    XL3'000000'             ADDRESS OF BLK        BIEDERMANN
         DC    AL2(L'MSG02)            LENGTH OF MESSAGE     BIEDERMANN
MSG02    DC    C'GTFSIO02 NO VALID CCWS LOCATED DURING CCW SCAN.'
         EJECT
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING SECTION IS RESPONSABLE TO FIND OUT, WETHER     *
*        CCW TRACE IS REQUIRED OR NOT. IF NOT, EXIT IS IMMEDIATELY.   *
*        THIS SECTION MUST BE REENTRANT.                              *
*                                                                     *
*         REGISTERS: @02,@03 RESERVED FOR AHLTSIO                     *
*                    @0B,@0C RESERVED AS BASE REGISTERS               *
*                    @10     USED AS RETURN REISTER FROM AHLPSCM1     *
*                    @15     IS TO SET UP WITH THE ASCB ADDRESS       *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
START    DS    0H                                                  BAUR
         BAL   @14,GETWORK             GET A WORKAEREA       BIEDERMANN
         LA    @04,FRRENTRY            GET FRR EXIT ADDRESS  BIEDERMANN
         L     @10,RETURN              GET RETURN ADDRESS    BIEDERMANN
         BAL   @14,AHLPRECM            SET A FRR             BIEDERMANN
         L     @15,MCAMCR(,@03)        GET MCAPTR            BIEDERMANN
         LA    @04,TSIO                SET UP FOR ENTRY TYPE BIEDERMANN
         CLC   MCREID(L'SIOID,@15),SIOID THIS SIO ?          BIEDERMANN
         BE    START00                 YES, CONTINUE         BIEDERMANN
         LA    @04,TIO                 SET UP FOR ENTRY TYPE BIEDERMANN
         CLC   MCREID(L'IOID,@15),IOID THIS IO ?             BIEDERMANN
         BE    START00                 YES, CONTINUE         BIEDERMANN
         LA    @04,TEOS                SET UP FOR ENTRY TYPE BIEDERMANN
         CLC   MCREID(L'EOSID,@15),EOSID THIS EOS ?          BIEDERMANN
         BE    START00                 YES, CONTINUE         BIEDERMANN
         LA    @04,TPCI                SET UP FOR ENTRY TYPE BIEDERMANN
         CLC   MCREID(L'PCIID,@15),PCIID THIS PCI ?          BIEDERMANN
         BNE   AHLPSCM2                NO, EXIT              BIEDERMANN
START00  DS    0H                                            BIEDERMANN
         STH   @04,ENTRYTYP            SAVE ENTRYTYP         BIEDERMANN
         BAL   @14,AHLLCCA             GET LCCA POINTER      BIEDERMANN
         L     @08,EVREG2(,@01)        GET IOSBREG           BIEDERMANN
         LA    @08,0(,@08)             CLEAR              RON PATTERSON
         LTR   @08,@08                 ZERO IOSB ?        RON PATTERSON
         BZ    AHLPSCM2                YES, NO TRACE NEEDED   PATTERSON
         L     @15,CVTPTR              GET CVT ADDRESS       BIEDERMANN
         L     @15,CVTASVT(,@15)       FIND ASCB             BIEDERMANN
         SLR   @14,@14                 POINT TO ASID         BIEDERMANN
         ICM   @14,3,IOSASID(@08)      POINT TO ASID         BIEDERMANN
         SLA   @14,2                   ADJUST                BIEDERMANN
         L     @15,ASVTENTY-4(@14,@15) CONTAINS ASCB NOW     BIEDERMANN
         TM    PSWITCH,PJOBFLAG        TRACE BY JOBNAME ?    BIEDERMANN
         BNO   STARTB0                 NO                    BIEDERMANN
         L     @14,ASCBJBNI(,@15)      GET JOBNAME POINTER   BIEDERMANN
         LTR   @14,@14                 IS THERE ONE ?        BIEDERMANN
         BNZ   STARTA0                 YES, LEAVE            BIEDERMANN
         L     @14,ASCBJBNS(,@15)      GET JOBNAME POINTER   BIEDERMANN
         LTR   @14,@14                 IS THERE ONE ?        BIEDERMANN
         BZ    AHLPSCM2                NO POINTER AVAILABLE  BIEDERMANN
STARTA0  L     @01,JBNMTEST(,@14)      GET JOBNAME           BIEDERMANN
         LTR   @01,@01                 IS THERE ONE ?        BIEDERMANN
         BZ    AHLPSCM2                NOT AT ALL            BIEDERMANN
         CLC   JOBNAME(L'PJOBNAME,@14),PJOBNAME              BIEDERMANN
         BNE   AHLPSCM2                NO, DO NOT TRACE      BIEDERMANN
STARTB0  DS    0H                                            BIEDERMANN
         TM    ENTRYTYP+1,TSIO         SIO INTERRUPT ?       BIEDERMANN
         BNO   STARTC0                 NO, GO TO IO                BAUR
         SPACE 1                                             BIEDERMANN
*        GET SIO UCB ADDRESS                                 BIEDERMANN
         SPACE 1                                             BIEDERMANN
         TM    IOSCC(@08),X'30'        IS SIO COND EQU 0 ?   BIEDERMANN
         BNZ   AHLPSCM2                NO, LEAVE             BIEDERMANN
         L     @09,IOSUCB(,@08)        GET UCB ADDRESS       BIEDERMANN
         B     STARTE0                 CONTINUE COMMON       BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        ENTRY IS FOR IO INTERRUPT, EOS AND PCI              BIEDERMANN
         SPACE 1                                             BIEDERMANN
STARTC0  DS    0H
         TM    ENTRYTYP+1,TPCI         THIS PCI ?            BIEDERMANN
         BO    STARTD0                 YES, CONTINUE         BIEDERMANN
         TM    ENTRYTYP+1,TIO          THIS IO ?             BIEDERMANN
         BNO   STARTD0                 NO                    BIEDERMANN
         TM    CSW,X'03'               DEFF. SIO ?        RON PATTERSON
         BNZ   AHLPSCM2                YES, DO NOT TRACE  RON PATTERSON
         SPACE 1                                             BIEDERMANN
*        GET UCB ADDRESS                                     BIEDERMANN
         SPACE 1                                             BIEDERMANN
STARTD0  DS    0H                                            BIEDERMANN
         BAL   @14,AHLLCCA             GET LCCA POINTER      BIEDERMANN
         LA    @09,512                 GET TO ADD            BIEDERMANN
         AL    @09,EVREG7(,@01)        GET UCB ADDRESS       BIEDERMANN
         TM    UCBFLA(@09),UCBACTV     CHANNEL PGM ACTIV ?      LZ90124
         BNO   AHLPSCM2                NO, RETURN               LZ90124
*        B     STARTE0                 THIS IS AT NEXT PAGE  BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        1.) GET UCB DATA AS REQUIRED. IF NOT AVAILABLE, DO NOT       *
*            CCWTRACE AND RETURN. IF AVAILABLE, SAVE IT IN TRACE      *
*            RECORD.                                                  *
*                                                                     *
*        @02 RESERVED BY AHLTSIO                                      *
*        @03 RESERVED BY AHLTSIO                                      *
*        @08 CONTAINS IOSB ADDRESS                                    *
*        @09 CONTAINS UCB  ADDRESS                                    *
*        @11 FIRST BASE REGISTER                                      *
*        @12 SECOND BASE REGISTER                                     *
*        @15 CONTAINS ASCB ADDRESS                                    *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
STARTE0  DS    0H                                            BIEDERMANN
         SPACE 1                                             BIEDERMANN
         LA    @01,255                 360 DEV TYPE IND            BAUR
         CLI   18(@09),X'40'           COMMUNICATION DEVICE        BAUR
         BNE   STARTF0                 NO USE 360 TRANS IN EDIT    BAUR
         IC    @01,16(,@09)            GET DEVICE CLASS            BAUR
         SLL   @01,8                   MAKE ROOM FOR TYPE          BAUR
         IC    @01,19(,@09)            GET DEVICE TYPE             BAUR
         SRL   @01,4                   COMBINE BOTH                BAUR
STARTF0  DS    0H                                                  BAUR
         STC   @01,DEVTYP              TRACE DEV TYPE              BAUR
*        B     STARTH0                 THIS IS AT NEXT PAGE
         SPACE 2                                             BIEDERMANN
         EJECT                                               BIEDERMANN
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THIS PART IS COMMON TO BOTH, IO AND SIO                      *
*        FUNCTION IS AS FOLLOW :                                      *
*                                                                     *
*         1. SET OR RESET SWITCHES AND PREPARE WORK AEREA             *
*         2. SWITCH ADDRESSABILITY TO THAT, THE CALLER BELONGS TO     *
*                                                                     *
*        @02 RESERVED BY AHLTSIO                                      *
*        @03 RESERVED BY AHLTSIO                                      *
*        @06 CONTAINS THE WORKAEREA ADDRESS                           *
*        @08 CONTAINS IOSB ADDRESS                                    *
*        @09 CONTAINS UCB  ADDRESS                                    *
*        @15 CONTAINS ASCB ADDRESS                                    *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
STARTH0  DS    0H                                            BIEDERMANN
         LH    @14,4(,@09)             FOR DEBUGGING ONLY    BIEDERMANN
         STH   @14,UCBADDR             FOR DEBUGGING ONLY    BIEDERMANN
         LA    @14,CCWADDR-SW          USED SPACE
         L     @01,TWTSIZE             GET MAX SIZE                BAUR
         SR    @01,@14                 DECR SIZE                   BAUR
         ST    @01,SIZE                SAVE SIZE                   BAUR
         MVC   DATAL,MAX+2             TRACE MX DATA               BAUR
         SPACE 1
*        SWITCH THE ADDRESSABILITY TO ASCB OF CURRENT REQUEST.
*        SET WAIT BIT IN PSA IF MVS - SE IS NOT INSTALLED AND TEST
*        IS REQUESTED BY THE CALLER.
         SPACE 1                                             BIEDERMANN
         STCTL 1,1,CTL1SAVE            SAVE CURRENT SEG TABLE POINTER
         OI    SW2,SW2CTL1             CTL1SAVE IS NOW VALID
         LCTL  1,1,44(@15)             SWITCH ADDR SPACE
*        B     STARTI0                 THIS IS AT NEXT PAGE  BIEDERMANN
         EJECT                                               BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        FIND START OF CHANNEL PGM EITHER BY CAW IN CASE OF A SIO     *
*        OR BY IOSCPA POINTER IN CASE OF AN IO OR PCI INTERRUPT.      *
*        IN CASE OF EOS CSW ADDRESS - 8 WILL BE TAKEN, IF THIS        *
*        POINTS TO A SENSE CCW, OTHERWISE EOS ENTRY WILL BE IGNORED.  *
*                                                                     *
*        @02 RESERVED BY AHLTSIO                                      *
*        @03 RESERVED BY AHLTSIO                                      *
*        @06 CONTAINS THE WORKAEREA ADDRESS                           *
*        @08 CONTAINS THE IOSB ADDRESS                                *
*                                                                     *
*        AT EXIT, THE START ADDRESS IS TRACED IN THE HDR RECORD AND   *
*        @04 POINTS TO START CHANNEL PGM.                             *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
STARTI0  DS    0H                                         RON PATTERSON
         CLI   ENTRYTYP+1,TSIO         THIS A SIO ?       RON PATTERSON
         BNE   STARTM0                 NO, LEAVE          RON PATTERSON
         SPACE 1                                          RON PATTERSON
*        PROCESS SIO ENTRIES HERE                         RON PATTERSON
         SPACE 1                                          RON PATTERSON
         OI    SW4,CTICOK              TRACE SMOD FOR EXCPCCW ONLY
         ICM   @01,7,CAW+1             GET ADDRESS FROM CAW   PATTERSON
         BAL   @14,REALVIRT            CONVERT TO VIRT.   RON PATTERSON
         BNZ   STARTK0                 NO, USE IOSB START    BIEDERMANN
         CLC   IOSCPA+1(3,@08),CAW+1   THIS SAME ?           BIEDERMANN
         BNE   STARTL0                 NO, USE IT            BIEDERMANN
         NI    SW4,255-CTICOK          RESET TIC OK          BIEDERMANN
         B     STARTL0                 USE CAW START         BIEDERMANN
STARTK0  DS    0H                                            BIEDERMANN
         NI    SW4,255-CTICOK          RESET TIC OK          BIEDERMANN
         ICM   @01,7,IOSCPA+1(@08)     GET CCW START         BIEDERMANN
         BAL   @14,REALVIRT            CONVERT TO VIRT.      BIEDERMANN
         BNZ   NOTRACE                 NO VALID START FOUND  BIEDERMANN
STARTL0  DS    0H                                            BIEDERMANN
         LR    @04,@01                 GET VIRT TO @04    RON PATTERSON
         B     STARTP0                 CONTINUE           RON PATTERSON
         SPACE 1                                             BIEDERMANN
*        PROCESS  IO TYPES HERE                           RON PATTERSON
         SPACE 1                                             BIEDERMANN
STARTM0  DS    0H                                         RON PATTERSON
         CLI   ENTRYTYP+1,TEOS         THIS EOS              BIEDERMANN
         BNE   STARTN0                 NO, NORMAL IO      RON PATTERSON
         SPACE 1                                          RON PATTERSON
*        PROCESS EOS HERE                                 RON PATTERSON
         SPACE 1                                          RON PATTERSON
         ICM   1,7,CSW+1               GET CSW            RON PATTERSON
         SH    1,=Y(8)                 GO TO LAST CCW     RON PATTERSON
         BAL   @14,REALVIRT            GO TO CONVERT      RON PATTERSON
         BNZ   NOTRACE                 FAILED, LEAVE      RON PATTERSON
         CLI   0(@01),X'04'            THIS A SENSE CCW ? RON PATTERSON
         BNE   STARTM1                 NO, IGNORE         RON PATTERSON
         ICM   @04,7,1(@01)            GET SENSE AEREA    RON PATTERSON
         BNZ   STARTO0                 CONTINUE NORMAL    RON PATTERSON
STARTM1  DS    0H                      SET RETCDE AND RETURN  PATTERSON
         SLR   @15,@15                 SET RETURN CODE       BIEDERMANN
         B     EXIT10                  IN CASE OF SILLY SENSE
         SPACE 1                                             BIEDERMANN
*        PROCESS IO AND PCI HERE                             BIEDERMANN
         SPACE 1
STARTN0  DS    0H                                         RON PATTERSON
         L     @01,IOSCPA(,@08)        GET REAL START        BIEDERMANN
         BAL   @14,REALVIRT            CONVERT TO MAKE ADDRESSABILITY
         BNZ   NOTRACE                 SOMETHING IS WRONG    BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        THIS PART IS COMMON FOR IO, EOS AND PCI
         SPACE 1                                             BIEDERMANN
STARTO0  DS    0H                                         RON PATTERSON
         LR    @04,@01                 VIRTUAL ADDRESS             BAUR
         L     @01,CSW                 CSW CCW ADDR IN @01         BAUR
         LA    @15,8                                               BAUR
         SR    @01,@15                 POINT TO LAST CCW           BAUR
         BAL   @14,REALVIRT            TRANSLATE                   BAUR
         BNZ   NOTRACE                 SOMTHING IS WRONG     BIEDERMANN
         ST    @01,LAST                SAVE IT                     BAUR
         SPACE 1                                             BIEDERMANN
*        THIS ENTRY IS COMMON FOR ALL TYPES                  BIEDERMANN
         SPACE 1                                             BIEDERMANN
STARTP0  DS    0H                                                  V1.2
         IC    @14,PSWITCH             GET PSWITCH
         STC   @14,STARTA              SAVE IT IN TRACE RECORD
         STCM  @04,7,STARTA+1          ADDR TO TRACE RECORD        BAUR
*        B     SCANCCW                 THIS IS AT NEXT PAGE
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        SCANCCW - THIS ROUTINE WILL SAVE THE ADDRESSES OF CCWS TO    *
*        BE TRACED IN CCWTAB.                                         *
*        THE FOLLOWING CCWFLAGS WILL ALSO SET IN THE CCWTAB AS        *
*        REQUIRED.                                                    *
*                                                                     *
*        1. WETHER THIS IS A TIC OR NOT                               *
*        2. WETHER DATA IS PRESENT WITH THIS CCW OR NOT               *
*        3. WETHER DATA SHOULD BE TRACED OR NOT                       *
*        4. WETHER DATA IS TO BE SPLITTED OR NOT                      *
*        5. WETHER ZERO DATA OR DATA=NO IS SPECIFIED OR NOT           *
*        6. WETHER THIS CCW IS LAST OF LOGICAL CHAIN OR NOT           *
*                                                                     *
*        @02  RESERVED BY AHLTSIO                                     *
*        @03  RESERVED BY AHLTSIO                                     *
*        @04  CONTAINS CURRENT CCW IN PROCESS                         *
*        @06  CONTAINS THE WORK AEREA ADDRESS                         *
*        @07  COMTAINS THE CURRENT ENTRY IN THE CCW TAB               *
*        @08  IS SET TO 4 UNTIL THIS REQUEST TERMINATE                *
*        @09  LAST VALID ADDRESS IN THE CCW TAB                       *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
SCANCCW  DS    0H
         LA    @07,CCWTAB              START OF CCWTAB             BAUR
         L     @09,RCMAX               MAX NUMBER OF CCWS          BAUR
         BCTR  @09,0                   -1                          BAUR
         SLL   @09,2                   CCW COUNT*4                 BAUR
         AR    @09,@07                 VIRTUAL END IN @09          BAUR
         LA    @08,4                   INCREMENT                   BAUR
SCNCCWA0 DS    0H                                                  BAUR
         ST    @08,0(,@07)             CLAER ENTRY                 BAUR
         BAL   @14,CHECKCCW            SEE IF ADDR VALID OR ALREADY
*                                      TRACED                      BAUR
         B     SCNSECC0                ALREADY TRACED              BAUR
         B     SCNFAIL                 INDICATE ERROR              BAUR
*        B     *+4                     OK, STORE IT                BAUR
         SPACE 1                                                   BAUR
         ST    @04,0(,@07)             STORE ADDR IN CCWTAB        BAUR
         TM    0(@04),7                POSSIBLE TIC                BAUR
         BNZ   SCNCCWB0                NO                          BAUR
         TM    0(@04),8                IS IT TIC                   BAUR
         BO    SCANTIC                 YES                         BAUR
SCNCCWB0 DS    0H                                                  BAUR
         CLI   0(@04),X'03'            THIS A NOP ?          BIEDERMANN
         BE    SCNNODAT                YES, SET NO DATA      BIEDERMANN
         TM    4(@04),X'10'            SUPRESS DATA ?        BIEDERMANN
         BO    SCNNODAT                YES, SET NO DATA      BIEDERMANN
         CLI   ENTRYTYP+1,TSIO         SIO TRACE ?           BIEDERMANN
         BNE   SCNCCWC0                NO, TRACE ALL DATA    BIEDERMANN
         TM    0(@04),1                WRITE TYP COMMAND ?   BIEDERMANN
         BNO   SCNNODAT                NO, DO NOT TRACE      BIEDERMANN
SCNCCWC0 DS    0H                                            BIEDERMANN
         L     @14,MAX                 GET MAX DATA          BIEDERMANN
         LTR   @14,@14                 IS THERE ONE ?        BIEDERMANN
         BNP   SCNZERO                 ZERO LENGTH           BIEDERMANN
         SLR   @01,@01                 CLEAR FOR LATER USE   BIEDERMANN
         ICM   @01,3,6(@04)            GET CCW DATA LENGTH
         BZ    SCNNODAT                NO VALID DATA         BIEDERMANN
         CLI   ENTRYTYP+1,TPCI         THIS PCI ?            BIEDERMANN
         BNE   SCNCCWC1                NO, LEAVE             BIEDERMANN
         C     @04,LAST                IS THIS LAST ?
         BE    SCNCCWD0                YES, SKIP             BIEDERMANN
         TM    0(@04),1                WRITE TYPE CCW ?      BIEDERMANN
         BO    SCNCCWD0                YES, TRACE IT         BIEDERMANN
         TM    SW1,LASTCCW             LAST CCW FOUND ?      BIEDERMANN
         BO    SCNNODAT                YES, DATA NOT YET VALID
         B     SCNCCWD0                NO, CONTINUE NORMAL   BIEDERMANN
         SPACE 1                                             BIEDERMANN
SCNCCWC1 DS    0H
         C     @04,LAST                THIS LAST ?           BIEDERMANN
         BNE   SCNCCWD0                NO, LEAVE             BIEDERMANN
         SLR   @15,@15                 CLEAR FOR LATER USE   BIEDERMANN
         ICM   15,3,CSW+6              GET RESIDUAL COUNT    BIEDERMANN
         SR    @01,@15                 GET TRUE LENGTH       BIEDERMANN
SCNCCWD0 DS    0H                                            BIEDERMANN
         CR    @01,@14                 MORE THAN MAX ?       BIEDERMANN
         BNH   SCNCCWE0                NO, CONTINUE          BIEDERMANN
         OI    0(@07),CSPLIT           INDICATE SPLITTED     BIEDERMANN
SCNCCWE0 DS    0H                                            BIEDERMANN
         C     @04,LAST                IS THIS LAST ?        BIEDERMANN
         BE    SCANLAST                YES, LEAVE            BIEDERMANN
SCNCCWF0 DS    0H                                                  BAUR
         TM    4(@04),X'C0'            CD OR CC                    BAUR
         BZ    SCNBRK                  NO CHAIN BROKEN             BAUR
SCNCCWG0 DS    0H                                                  BAUR
         LA    @04,8(,@04)             POINT TO NEXT CCW           BAUR
SCNCCWH0 DS    0H                                                  BAUR
         BXLE  @07,@08,SCNCCWA0        LOOP                        BAUR
         SPACE 1                                             BIEDERMANN
*        THIS ENTRY IS MADE, IF MORE CCWS ARE FOUND THAN SPECIFIED
*        OR IF THE CCWTAB ENDS.                              BIEDERMANN
         SPACE 1                                             BIEDERMANN
SCNLOST  DS    0H
         SR    @07,@08                 AJUST END                   BAUR
         OI    SW1,LOST                IND LOST                    BAUR
         ST    @07,CCWTABE             SAVE LAST VALID TAB ADDRESS BAUR
         B     SORT                    GO TO SORT                  BAUR
         SPACE 1                                             BIEDERMANN
*        THIS ENTRY IS MADE, IF THE NEXT LOGICAL CCW FAILS WITH
*        VALIDITY CHECK                                      BIEDERMANN
         SPACE 1                                             BIEDERMANN
SCNFAIL  DS    0H                                            BIEDERMANN
         SR    @07,@08                 GET PREVIOS           BIEDERMANN
SCFAIL10 DS    0H                                            BIEDERMANN
         OI    0(@07),CLOST            INDICATE LOST         BIEDERMANN
         TM    SW1,LASTCCW             LAST ALREADY SET ?    BIEDERMANN
         BO    SCNSECA0                YES, CONTINUE         BIEDERMANN
         CLI   ENTRYTYP+1,TPCI         THIS PCI ?            BIEDERMANN
         BNE   SCNSECA0                NO, SECOND SCAN       BIEDERMANN
         L     @04,LAST                GET LAST              BIEDERMANN
         B     SCANCCW                 START AGAIN           BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        THIS ENTRY IS MADE, IF FOR ANY REASON, DATA SHOULD NOT
*        BE TRACED.                                          BIEDERMANN
         SPACE 1                                             BIEDERMANN
SCNNODAT DS    0H                                            BIEDERMANN
         OI    0(@07),CNODAT           NO DATA PRESENT       BIEDERMANN
         B     SCNCCWE0                RETURN TO MAIN FLOW   BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        THIS ENTRY IS MADE, IF DATA SHOULD BE TRACED, HOWEVER
*        DATA=NO OR DATA=0 IS SPECIFIED.                     BIEDERMANN
         SPACE 1                                             BIEDERMANN
SCNZERO  DS    0H                                            BIEDERMANN
         OI    0(@07),CNODAT+CZERO     NO DATA PRESENT       BIEDERMANN
         B     SCNCCWE0                RETURN TO MAIN FLOW   BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        THE FOLLOWING ENTRY IS TAKEN, IF THE CCWCHAIN IS FOUND
*        TO BE BROKEN, BECAUSE NEITHER DATA CHAINING NOR COMMAND
*        CHAINING IS FOUND IN THE PREVIOUS CCW.              BIEDERMANN
         SPACE 1
SCNBRK   DS    0H                                                  BAUR
         CLI   ENTRYTYP+1,TSIO         THIS SIO ?            BIEDERMANN
         BNE   SCNBRK10                NO, LEAVE             BIEDERMANN
         TM    SW1,LASTCCW             LAST FOUND FOR SIO ?  BIEDERMANN
         BO    SCNBRK20                YES, DO NOT SET LAST  BIEDERMANN
         OI    SW1,LASTCCW             SET LAST              BIEDERMANN
         OI    0(@07),CLAST            INDICATE CCW AS LAST  BIEDERMANN
         B     SCNBRK20                SKIP                  BIEDERMANN
         SPACE 1                                             BIEDERMANN
SCNBRK10 DS    0H                                            BIEDERMANN
         TM    SW1,LASTCCW             LAST FOUND                  BAUR
         BO    SCNSECA0                YES TRY SECAOND PASS        BAUR
SCNBRK20 DS    0H                                            BIEDERMANN
         CLI   0(@04),X'03'            POSSIBLE AUTOPOLL           BAUR
         BNE   SCNBRK30                NO TRY SECAOND PASS         BAUR
         LR    @10,@04                 ADDR OF CCW                 BAUR
         SH    @10,=Y(8)               POINT TO LAST CCW           BAUR
         LRA   @01,0(,@10)             GET REAL ADDRESS      BIEDERMANN
         BNZ   SCNBRK30                NOT VALID, GO OUT     BIEDERMANN
         CLI   0(@10),X'03'            POSSIBLE AUTOPOLL           BAUR
         BE    SCNCCWG0                YES TRY                     BAUR
         CLI   0(@10),X'09'            AUTOPOLL                    BAUR
         BE    SCNCCWG0                YES, TRY AUTOPOLL           BAUR
SCNBRK30 DS    0H                                            BIEDERMANN
         TM    SW1,LASTCCW             LAST ALREADY SET ?    BIEDERMANN
         BO    SCNSECA0                YES, SECOND PASS      BIEDERMANN
         B     SCNSECA1                FORCE SECOND PASS     BIEDERMANN
         SPACE 1
*        THIS ENTRY IS TO HANDLE TIC COMMANDS
         SPACE 1
SCANTIC  DS    0H                                                  BAUR
         OI    0(@07),CTIC+CNODAT      IND TIC IN TAB              BAUR
         OC    0(L'CCWFLAGS,@07),SW4   SET RESOLVED IF REQUIRED
         C     @04,LAST                THIS LAST ?           BIEDERMANN
         BE    SCANLAST                YES, LEAVE            BIEDERMANN
         TM    3(@04),7                DW                          BAUR
         BNZ   SCFAIL10                NO                          BAUR
         LR    @00,@04                 SAVE CURRENT CCW ADDR       BAUR
         SLR   @01,@01                 CLEAR FOR LATER USE         BAUR
         ICM   @01,7,1(@04)            GET TIC TO ADDRESS          BAUR
         BAL   @14,REALVIRT            TRANSLATE TO VIRT           BAUR
         BNZ   SCFAIL10                INVALID ADDR                BAUR
         LR    @04,@01                 LOAD VIRTUAL ADDR           BAUR
         BAL   @14,CHECKCCW            SEE IF CCW ALREADY TRACED   BAUR
         B     SCANTIC2                YES ALREADY TRACED          BAUR
         B     SCFAIL10                INDICATE ERROR              BAUR
*        B     *+4                     OK USE TIC-TO CCW AS NEXT   BAUR
         SPACE 1                                             BIEDERMANN
         TM    SW4,CTICOK              OR SWITCH OFF ?       BIEDERMANN
         BZ    SCNCCWH0                YES, OK               BIEDERMANN
         BAL   @14,AHLLCCA             GET LCCA              BIEDERMANN
         L     @14,EVREG2(,@01)        GET IOSBREG           BIEDERMANN
         LR    @15,@00                 GET PREVIOUS CCW ADDRESS
         CLC   IOSCPA+1(3,@14),1(@15)  THIS IOSB START ?     BIEDERMANN
         BNE   SCNCCWH0                NO, LEAVE             BIEDERMANN
         NI    SW4,255-CTICOK          RESET TIC OK          BIEDERMANN
         B     SCNCCWH0                CONTINUE              BIEDERMANN
         SPACE 1                                             BIEDERMANN
SCANTIC2 DS    0H                                                  BAUR
         OI    0(@07),CTICOK           IND TIC+8 TAKEN             BAUR
         LR    @04,@00                 RESTORE CCW ADDR            BAUR
         BAL   @14,VALID               VALIDATE NEX CCW            BAUR
         B     SCNCCWG0                CCW OK                      BAUR
*        B     *+4                     NO CCW TRY SECOND PASS      BAUR
         CLI   ENTRYTYP+1,TSIO         IS THIS A SIO ?       BIEDERMANN
         BE    SCNSECA0                YES, TRY SECOND       BIEDERMANN
         L     @04,LAST                TRY LAST              BIEDERMANN
         OI    0(@07),CLOST            INDICATE LOST FOR EDIT
         B     SCNCCWH0                RETRY WITH LAST       BIEDERMANN
         SPACE 1
*        THIS ENTRY IS MADE, IF THE CURRENT CCW IS LAST POINTED BY
*        CSW - 8
         SPACE 1
SCANLAST DS    0H
         OI    SW1,LASTCCW             IND LAST FOUND              BAUR
         OI    0(@07),CLAST            INDICATE LAST         BIEDERMANN
         CLI   ENTRYTYP+1,TPCI         THIS PCI ?            BIEDERMANN
         BE    SCNCCWF0                YES, RETURN TO SIO    BIEDERMANN
         TM    0(@07),CTIC+CTICOK      TIC SET ?             BIEDERMANN
         BNM   SCNSECA0                NOT, OR NOT VALID     BIEDERMANN
         OI    0(@07),CTICOK           SET TIC RESOLVED      BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        THIS ENTRYPOINT IS USED BY MOST EXITS TO CHECK, WETHER A
*        SECOND PASS IS REQUIRED OR NOT.
         SPACE 1                                             BIEDERMANN
SCNSECA0 DS    0H                                                  BAUR
         ST    @07,CCWTABE             IN CASE OF SMOD = NO  BIEDERMANN
         TM    PSWITCH,PSTATMOD        STATMOD REQUESTED ?   BIEDERMANN
         BNO   SORT                    NO, LEAVE             BIEDERMANN
         TM    SW1,LASTCCW             LASTCCW FOUND            LZ90324
         BNZ   SORT                    IN CASE OF SMOD AND LAST LZ90324
SCNSECA1 DS    0H                                                  BAUR
         BXLE  @07,@08,SCNSECB0        POINT TO NEW ENTRY          BAUR
         B     SCNLOST                 NO MORE ENTRIES             BAUR
SCNSECB0 DS    0H                                                  BAUR
         ST    @08,0(,@07)             CLEAR ENTRY                 BAUR
SCNSECC0 DS    0H                                                  BAUR
         STM   @07,@09,SCANSAVE        SAVE REGISTERS              BAUR
         LR    @09,@07                 MAKE CURRENT TO END         BAUR
         LA    @07,CCWTAB              START OF TAB                BAUR
SCNSECD0 DS    0H                                                  BAUR
         TM    0(@07),CTICOK+CTIC      TIC RESOLVED                BAUR
         BM    SCNSECG0                NO TRY TO RESOLVE           BAUR
SCNSECE0 DS    0H                                                  BAUR
         BXLE  @07,@08,SCNSECD0        LOOP                        BAUR
SCANENDA DS    0H                                                  BAUR
         SR    @09,@08                 END OF TAB                  BAUR
         ST    @09,CCWTABE             SAVE TAB END                BAUR
         B     SORT                                                BAUR
         SPACE 3                                                   BAUR
SCNSECG0 DS    0H                                                  BAUR
         L     @04,0(,@07)             POINT TO TIC                BAUR
         LA    @04,0(,@04)             CLEAR COMAND                BAUR
         CR    @04,@07                 END OF TAB                  BAUR
         BE    SCANENDA                YES                         BAUR
         OI    0(@07),CTICOK           IND TIC RESOLVED            BAUR
         TM    3(@04),7                DW                          BAUR
         BNZ   SCNSECE0                NOT VALID                   BAUR
         LM    @07,@09,SCANSAVE        RESTORE REGS                BAUR
         SR    @07,@08                 POINT TO LAST USED ENTRY    BAUR
         BAL   @14,VALID               CHECK CCW                   BAUR
         B     SCNCCWG0                CCW OK                      BAUR
         B     SCNSECA1                NOT OK, FORCE SECOND PASS   BAUR
         EJECT                                                     BAUR
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING SUB ROUTINE IS ENTERED WITH RETURN ADDRESS IN  *
*        @14. IT IS USED TO VALIDATE THE NEXT LOGICAL CCW IF A TIC    *
*        POINTS TO AN ADDRESS ALREADY TRACED, OR IN CASE OF SMOD=YES  *
*        TO CHECK, IF A TIC+8 MAY BE A VALID CCW.                     *
*                                                                     *
*        RETURN IS TO ADDRESS GIVEN IN @14 IF THE AFFECTED STRING     *
*        IS TO BE ASSUMED A CCW. IF IT IS ASSUMED NOT TO BE A CCW     *
*        @14 ADDRESS +4 IS TAKEN TO RETURN.                           *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
VALID    DS    0H                                                  BAUR
         CLI   8(@04),X'00'            VALID ?               BIEDERMANN
         BE    4(0,@14)                NOT VALID             BIEDERMANN
         TM    8(@04),X'07'            IS IT A COMAND              BAUR
         BNZ   VALID10                 MAY BE                      BAUR
         TM    8(@04),X'08'            IS IT A TIC                 BAUR
         BO    VALID30                 YES
         B     4(0,@14)                NOT VALID                   BAUR
VALID10  DS    0H
         TM    12(@04),X'03'           FLAGS                       BAUR
         BNZ   4(0,@14)                NOT VALID
         TM    12(@04),X'10'           SKIP BIT ON
         BCR   1,@14                   YES ADDR MAY BE 0
         CLI   8(@04),X'03'            IS THIS A NOP ?       BIEDERMANN
         BCR   8,@14                   YES, OK RETURN        BIEDERMANN
VALID30  DS    0H
         CLC   9(3,@04),=X'000000'     ADDR =0                     BAUR
         BE    4(0,@14)                NOT VALID                   BAUR
         L     @01,8(,@04)             IN CORE                     BAUR
         STM   @00,@15,REGSAVE         SAVE ALL REGS               BAUR
         L     @15,CVTPTR              GET CVT                     BAUR
         L     @15,X'0120'(,@15)       ADDR OF TRANSLATE ROUTINE   BAUR
         LA    @01,0(,@01)             ADDR TO BE TRANSLATED       BAUR
         BALR  @14,@15                 TRANSLATE                   BAUR
         SPACE 1
         USING *,@14                   ADDRESSABILITY
         SPACE 1
         LTR   @15,@15                 TRANSLATION OK ?            BAUR
         LM    @00,@15,REGSAVE         RESTORE @02-@15             BAUR
         SPACE 1
         DROP  @14
         SPACE 1                                             BIEDERMANN
         BNZ   4(,@14)                 RETURN WITH ERROR     BIEDERMANN
         BR    @14                     OK
         EJECT                                                     BAUR
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        SORT CCWS IN ASCENDING ORDER IF REQUESTED.             BAUR  *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
SORT     DS    0H                                                  BAUR
         TM    PSWITCH,PSORT           IS SORT REQUESTED ?   BIEDERMANN
         BZ    TRCCW                   NO, LEAVE             BIEDERMANN
         LA    @07,CCWTAB              START                       BAUR
         L     @09,CCWTABE             END OF TAB                  BAUR
         LA    @09,0(,@09)             CLEAR FLAGS           BIEDERMANN
SORT10   DS    0H                                            BIEDERMANN
         LA    @14,4(,@07)             CLEAR FLAGS           BIEDERMANN
         CR    @07,@09                 END OF SORT ?         BIEDERMANN
         BNL   TRCCW                   YES, LEAVE            BIEDERMANN
SORT20   DS    0H                                            BIEDERMANN
         CLC   1(3,@07),1(@14)         IN SEQUENCE ?         BIEDERMANN
         BNH   SORT30                  YES, LEAVE            BIEDERMANN
         XC    0(4,@07),0(@14)         MERGE IT              BIEDERMANN
         XC    0(4,@14),0(@07)         MERGE IT              BIEDERMANN
         XC    0(4,@07),0(@14)         MERGE IT              BIEDERMANN
SORT30   DS    0H                                            BIEDERMANN
         LA    @14,4(,@14)             GO TO NEXT ENTRY      BIEDERMANN
         CR    @14,@09                 END OF CURRENT LEVEL  BIEDERMANN
         BNH   SORT20                  NO, CONTINUE          BIEDERMANN
         LA    @07,4(,@07)             INCREASE              BIEDERMANN
         B     SORT10                  CONTINUE              BIEDERMANN
         EJECT
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        CHECK IF ANYTHING IS TO BE TRACED. GIVE ERROR MESSAGE IF NOT *
*        TRACE THE CCW ADDRESS AND THE CCW IN THAT SECTION.           *
*                                                                     *
*        @02  RESERVED FOR AHLTSIO                                    *
*        @03  RESERVED FOR AHLTSIO                                    *
*        @06  CONTAINS THE WORK AEREA ADDRESS                         *
*        @08  CONTAINS A FIXED VALUE OF 4                             *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
TRCCW    DS    0H                                                  BAUR
         LA    @07,CCWTAB              START                       BAUR
         L     @09,CCWTABE             END                         BAUR
         C     @08,0(,@07)             ANY CCW TRACED              BAUR
         BE    NOTRACE                 NO SAY IT TO EDIT PROGRAM   BAUR
         SPACE 1
*
*        TRACE CCW ADDR
*
         SPACE 1
TRCCW10  DS    0H                                                  BAUR
         LA    @14,12                  GET CCWADDRESS AND CCW LENGTH
         C     @14,SIZE                WILL THAT FIT ?       BIEDERMANN
         BNH   TRCCW20                 YES, OK               BIEDERMANN
         BAL   @15,TRC                 NO, WRITE THAT RECORD BIEDERMANN
TRCCW20  DS    0H                                            BIEDERMANN
         LR    @05,@07                 ADDR OF CCWADDR             BAUR
         LA    @10,L'CCWADDR           LENGTH  OF  4               BAUR
         BAL   @14,TRACE               TRACE IT                    BAUR
         SPACE 1
*
*        TRACE CCW
*
         SPACE 1
         L     @05,0(,@07)             ADDR OF CCW                 BAUR
         LA    @10,8                   GET CCW LENGTH = 8          BAUR
         BAL   @14,TRACE               TRACE IT                    BAUR
*        B     TRDAT                   GO TO TRACE DATA      BIEDERMANN
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        CHECK IF DATA ARE TO BE TRACED, IF NOT GO TO NEXT CCW,       *
*        OTHERWISE CHECK ALL DATA FOR VALIDITY. IF VALID, WRITE       *
*        DATA LENGTH AND DATA AS REQUIRED. IF DATA IS INVALID, GO     *
*        BACK TO THE JUST WRITTEN CCW AND MARK DATA INVALID IN THE    *
*        CCWFLAG. FOR THAT POSSIBILITY, DATA MUST NOT BE SPLITTED TO  *
*        AN OTHER BUFFER.                                             *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3
TRDAT   DS    0H                                                  BAUR
         SPACE 1
         TM    0(@07),CTIC+CNODAT      ANY DATA ?            BIEDERMANN
         BNZ   TRDATI0                 NO, GET NEXT          BIEDERMANN
         L     @05,0(,@07)             CCW ADDR
         SLR   @10,@10                 CLEAR FOR LATER USE   BIEDERMANN
         ICM   @10,3,6(@05)            GET CCW LENGTH        BIEDERMANN
         TM    ENTRYTYP+1,TSIO+TPCI    THIS A SIO OR PCI ?      LZ90524
         BNZ   TRDATA0                 YES, LEAVE            BIEDERMANN
         TM    0(@07),CLAST            LAST CCW                    BAUR
         BNO   TRDATA0                 NO                    BIEDERMANN
         SLR   @15,@15                 CLEAR IN CASE OF HIGH ORDER
         ICM   @15,3,CSW+6             GET RESIDUAL COUNT          BAUR
         SR    @10,@15                 GET REAL COUNT              BAUR
TRDATA0  DS    0H                                                  BAUR
         L     @01,0(,@05)             DATA ADDR                   BAUR
         TM    4(@05),X'04'            IAL ON
         BZ    TRDATB0                 NO IAL SKIP                 BAUR
         TM    3(@05),X'03'            FULL WORD ?           BIEDERMANN
         BNZ   TRDATK0                 NO, ERROR             BIEDERMANN
         BAL   @14,REALVIRT            TRANSLATE
         BNZ   TRDATK0                 TRANLATION BAD
         L     @01,0(,@01)             DATA ADDR
TRDATB0  DS    0H
         BAL   @14,REALVIRT            TRANSLATE                   BAUR
         BNZ   TRDATK0                 WRONG ADDR DONT TRACE
         TM    0(@05),X'03'            MIGHT IT BE READ BACKWARD?
         BNZ   TRDATC0                 NO
         TM    0(@05),X'0C'            IS IT READ BACKWARD?
         BNO   TRDATC0                 NO
         SPACE 1
*        READ BACKWARD PROCESSING STARTS HERE.
         SPACE 1
         SR    @01,@10                 SUBTRACT LENGTH             BAUR
         LA    @01,1(,@01)             POINT TO 1ST BYTE OF DATA
         TM    ENTRYTYP+1,TSIO+TPCI    THIS A SIO OR PCI ?      LZ90524
         BNZ   TRDATC0                 YES, LEAVE            BIEDERMANN
         TM    0(@07),CLAST            THIS LAST ?           BIEDERMANN
         BNO   TRDATC0                 NO, SKIP LAST         BIEDERMANN
         SLR   @14,@14                 CLEAR FOR LATER USE   BIEDERMANN
         ICM   @14,3,CSW+6             SUBTRACT RESCOUNT     BIEDERMANN
         AR    @01,@14                 POINT TO START OF DATA
TRDATC0  DS    0H
         ST    @01,DATSTART            SAVE FOR LATER USE    BIEDERMANN
         LA    @15,0(@01,@10)          CALCULATE END ADDRESS BIEDERMANN
         ST    @15,DATEND              SAVE FOR LATER USE          BAUR
         LA    @14,0(,@01)             GET START OF DATA IN @14
         SRL   @14,11                  ROUND TO NEXT LOWER 2 K BLOCK
         SLL   @14,11                  ROUND                 BIEDERMANN
         BCTR  @15,0                   DECREASE ONE          BIEDERMANN
TRDATD0  DS    0H                                            BIEDERMANN
         LRA   @01,0(,@14)             BUFFER VALID ?        BIEDERMANN
         BNZ   TRDATK0                 NO, ERROR EXIT        BIEDERMANN
         LA    @14,X'800'(,@14)        INCREASE TO NEXT BLOCK
         CR    @14,@15                 CHECK END ?           BIEDERMANN
         BNH   TRDATD0                 NO, RETRY             BIEDERMANN
         TM    0(@07),CSPLIT           ALL DATA TO TRACE           BAUR
         BNO   TRDATF0                 YES, USE CCWCOUNT           BAUR
         LA    @05,MAX+2               GET SIZE TO BE TRACED BIEDERMANN
         LA    @10,2                   GET LENGTH OF LENGTH FIELD
         C     @10,SIZE                WILL THAT FIT         BIEDERMANN
         BNH   TRDATE0                 YES, ALL OK           BIEDERMANN
         BAL   @15,TRC                 NO, WRITE CURRENT RECORD
TRDATE0  DS    0H                                            BIEDERMANN
         BAL   @14,TRACE               TRACE IT              BIEDERMANN
         L     @15,MAX                 GET MAX LENGTH        BIEDERMANN
         SLDL  @14,31                  ADJUST                BIEDERMANN
         SRL   @15,31                  CALCULATE             BIEDERMANN
         LA    @10,0(@14,@15)          HERE IS THE RESULT    BIEDERMANN
         L     @05,DATSTART            RESTORE DATA ADDRESS  BIEDERMANN
         BAL   @14,TRACE               TRACE                       BAUR
         L     @05,DATEND              END OF DATA                 BAUR
         L     @10,MAX                 GET LENGTH                  BAUR
         SRL   @10,1                   HALF LENGTH                 BAUR
         SR    @05,@10                 START OF DATA TO TRACE      BAUR
         B     TRDATH0                 TRACE SECOND HALF     BIEDERMANN
         SPACE 1                       TRACE SECOND HALF     BIEDERMANN
TRDATF0  DS    0H                                                  BAUR
         ST    @10,DATSIZE             TRACE LENGTH NOW      BIEDERMANN
         LA    @10,2                   GET LENGTH TO BE TRACED
         C     @10,SIZE                WILL THAT FIT ?       BIEDERMANN
         BNH   TRDATG0                 YES, CONTINUE         BIEDERMANN
         BAL   @15,TRC                 NO, WRITE THIS RECORD BIEDERMANN
TRDATG0  DS    0H                                            BIEDERMANN
         LA    @05,DATSIZE+2           GET ADDRESS TO BE TRACED
         BAL   @14,TRACE               TRACE                 BIEDERMANN
         L     @10,DATSIZE             RESTORE DATA SIZE     BIEDERMANN
         L     @05,DATSTART            RESTORE AFTER TRACE   BIEDERMANN
TRDATH0  DS    0H                                                  BAUR
         BAL   @14,TRACE               TRACE                       BAUR
TRDATI0  DS    0H                                                  BAUR
         BXLE  @07,@08,TRCCW10         LOOP                        BAUR
         B     EXIT                    LEAVE                       BAUR
         EJECT                                               BIEDERMANN
         SPACE 3
TRDATK0  DS    0H
         L     @01,SIZE                /* BACK UP TO FLAGS */
         LA    @15,SW-12               - CCW ADDR AND CCW    BIEDERMANN
         A     @15,TWTSIZE             CALCULATE IT          BIEDERMANN
         SR    @15,@01                 THIS IS THE RESULT    BIEDERMANN
         OI    0(@15),CDATERR+CNODAT   INDICATE DATA ERROR   BIEDERMANN
         B     TRDATI0                 CONTINUE WITH NEXT CCW
         EJECT                                               BIEDERMANN
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING ROUTINES WILL BE ENTERED TO EXIT TO THE GTF    *
*        MONITOR.                                                     *
*                                                                     *
*        AT LABEL NOTRACE , MSG02 WILL BE WRITTEN.                    *
*        AT LABEL EXIT, NORMAL RETURN AFTER CCW TRACING.              *
*        AT LABEL EXIT10, NORMAL RETURN WITHOUT END SWITCH SETTING    *
*        AND @15 SET EITHER TO 4 OR ZERO.                             *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 1                                             BIEDERMANN
NOTRACE  DS    0H
         MVI   SW,SMYRECID+SMSG+SEND   SET FLAGS             BIEDERMANN
         LH    @15,PSACPUSA            GET CPUID             BIEDERMANN
         STC   @15,CPUID               SAVE IT IN RECORD     BIEDERMANN
         LA    @15,1                   GET RECORD COUNT      BIEDERMANN
         STC   @15,RECNO               SET RECORD COUNT      BIEDERMANN
         STH   @15,RECCOUNT            SET RECORD COUNT      BIEDERMANN
         MVC   BLKID(L'BLKID+L'BLKADDR+L'BLKLGTH+L'MSG02),MSG02S
         LA    @05,L'SW+L'CPUID+L'RECNO+L'BLKID+L'BLKADDR+L'BLKLGTH+L'MX
               SG02                    GET MESSAGE LENGTH    BIEDERMANN
         STM   @00,@15,REGSAVE         SAVE REGS                LZ90624
         BAL   @10,AHLPSCM1            WRITE THE RECORD      BIEDERMANN
         LM    @04,@14,REGSAVE+L'REGSAVE*@04   RESTORE          LZ90624
         LTR   @15,@15                 ZERO RETURN CODE ?    BIEDERMANN
         BNZ   EXIT10                  NO, LEAVE             BIEDERMANN
         B     IOSTRACE                TRACE IOSB AND EWA    BIEDERMANN
         SPACE 1                                             BIEDERMANN
EXIT     DS    0H                                                  BAUR
         NI    SW1,255-LASTCCW-END     CLEAR LASTIND
         TM    SW2,ANY                 ANY CCWS TRACED             BAUR
         BZ    IOSTRACE                TRACE IOSB AND EWA        MERONI
         OI    SW1,END                 IND END FOR EDIT PGM        BAUR
         BAL   @15,TRC                 WRITE PARTIAL RECORD        BAUR
         NI    SW2,255-ANY             RESET ANY SWITCH      BIEDERMANN
         TM    OSWITCH,OIOSB           TRACE OF IOSB REQUESTED ? MERONI
         BO    IOSTRACE                YES, GO TO TRACE          MERONI
         SLR   @15,@15                 SET RETURN CODE       BIEDERMANN
         TM    ENTRYTYP+1,TSIO         THIS A SIO REQUEST ?  BIEDERMANN
         BNO   EXIT10                  NO, LEAVE                 MERONI
         BAL   @14,AHLLCCA             GET REG BASE          BIEDERMANN
         ICM   @05,7,EVREG2+1(@01)     GET IOSB REG              MERONI
         BZ    EXIT10                  NOT VALID                 MERONI
         ICM   @05,7,IOSERP+1(@05)     GET EWA ADDRESS           MERONI
         BNZ   IOSTRACE                IF PRESENT, TRACE IT      MERONI
EXIT10   DS    0H                                                  BAUR
         ST    @15,RETURN              SAVE RETURN CODE FOR DEBUG
         TM    SW2,SW2CTL1             CTL1SAVE VALID ?      BIEDERMANN
         BNO   AHLPSCM2                NO, DO NOT CHANGE     BIEDERMANN
         LCTL  1,1,CTL1SAVE            RESTORE ORIGINAL ADDR SPACE
         B     AHLPSCM2                RETURN                BIEDERMANN
         EJECT                                                   MERONI
         SPACE 1                                                 MERONI
******************************************************* MERONI ********
*                                                                     *
*        THE FOLLOWING ROUTINES WILL BE ENTERED, IF EITHER NO VALID   *
*        CCWS ARE FOUND OR IOSB TRACE IS REQUESTED BY CALLER.         *
*        REASON IS TO TRACE IOSW AND EWA AS FAR AS AVAILABLE.         *
*        AT ENTRY, IT IS ALWAYS EXPECTED, THAT THE TRACE BUFFER IS    *
*        COMPLETELY FREE .                                            *
*                                                                     *
******************************************************* MERONI ********
         SPACE 1                                                 MERONI
IOSTRACE DS    0H                                                MERONI
         SLR   @15,@15                 SET RETURN CODE           MERONI
         TM    SW2,SW2IOSB             ALREADY TRACED ?      BIEDERMANN
         BO    EXIT10                  YES, DO NOT RETRY     BIEDERMANN
         OI    SW2,SW2IOSB             SET TRAIL SWITCH      BIEDERMANN
         TM    PSWITCH,PSIO            IS SIO TRACE ACTIV ?      MERONI
         BNO   EXIT10                  NO, LEAVE                 MERONI
         TM    ENTRYTYP+1,TSIO         THIS A SIO ENTRY ?        MERONI
         BZ    EXIT10                  NO, EXIT                  MERONI
         BAL   @14,AHLLCCA             FIND IOSB ADDRESS         MERONI
         ICM   @05,7,EVREG2+1(@01)     GET IOSB (REG2)           MERONI
         BZ    EXIT10                  IF ZERO, LEAVE            MERONI
         SPACE 1
*        TRACE IOSB
         SPACE 1
         LRA   @01,0(,@05)             TEST FOR VALD ADDRESS     MERONI
         BNZ   EXIT10                  INVALID, LEAVE            MERONI
         LRA   @01,IOSBLGTH-1(,@05)    FULL SIZE OK ?            MERONI
         BNZ   EXIT10                  NO, LEAVE                 MERONI
         MVI   SW1,SMSG+END            SET MESSAGE AND END       MERONI
         MVC   BLKID,=C'IOSB'          INDICATE IOSB             MERONI
         STCM  @05,7,BLKADDR           SAVE IOSB ADDRESS         MERONI
         LA    @10,IOSBLGTH            GET LENGTH OF IOSB        MERONI
         STCM  @10,3,BLKLGTH           SAVE LENGTH OF IOSB       MERONI
         MVC   BLKTRC(IOSBLGTH),0(@05) MOVE IOSB                 MERONI
         LA    @10,L'SW+L'CPUID+L'RECNO+L'BLKID+L'BLKADDR+L'BLKLGTH+IOSX
               BLGTH                   GET LENGTH OF IOSB + HDR LENGTH
         L     @01,TWTSIZE             GET SIZE REMAINING        MERONI
         SR    @01,@10                 GET SIZE REMAINING        MERONI
         ST    @01,SIZE                SAVE SIZE REMAINING       MERONI
         BAL   @15,TRC                 WRITE THE RECORD          MERONI
         SPACE 1                                                 MERONI
*        TRACE EWA
         SPACE 1                                                 MERONI
         SLR   @15,@15                 SET RETURN CODE           MERONI
         ICM   @05,7,IOSERP+1(@05)     GET EWA ADDRESS           MERONI
         BZ    EXIT10                  NO, LEAVE                 MERONI
         LRA   @01,0(,@05)             TEST FOR VALD ADDRESS     MERONI
         BNZ   EXIT10                  INVALID, LEAVE            MERONI
         LRA   @01,EWALGTH-1(,@05)     FULL SIZE OK ?            MERONI
         BNZ   EXIT10                  INVALID, LEAVE            MERONI
         MVI   SW1,SMSG+END            SET MESSAGE AND END       MERONI
         MVC   BLKID,=C'EWA '          INDICATE EWA              MERONI
         STCM  @05,7,BLKADDR           SAVE EWA ADDRESS          MERONI
         LA    @10,EWALGTH             GET LENGTH OF EWA         MERONI
         STCM  @10,3,BLKLGTH           SAVE LENGTH OF EWA        MERONI
         MVC   BLKTRC(EWALGTH),0(@05)  MOVE EWA                  MERONI
         LA    @10,L'SW+L'CPUID+L'RECNO+L'BLKID+L'BLKADDR+L'BLKLGTH+EWAX
               LGTH                    GET LENGTH OF IOSB + HDR LENGTH
         L     @01,TWTSIZE             GET SIZE REMAINING        MERONI
         SR    @01,@10                 GET SIZE REMAINING        MERONI
         ST    @01,SIZE                SAVE SIZE REMAINING       MERONI
         BAL   @15,TRC                 WRITE THE RECORD          MERONI
         SLR   @15,@15                 SET RETURN CODE           MERONI
         B     EXIT10                  LEAVE                     MERONI
         EJECT
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING SUB ROUTINE IS ENTERED WITH RETURN ADDRESS IN  *
*        @14. IT IS USED TO GATHER TRACE DATA IN THE TRACE BUFFER.    *
*        IF THE BUFFER IS FOUND TO BE FILLED, AN EXIT IS MADE TO TRC  *
*        TO PASS IT TO GTF.                                           *
*                                                                     *
*        REGISTERS DAMAGED: @01,@05,@10,@15                           *
*        USED AEREAS:       REGSAVE IN CASE OF AN EXIT TO TRC         *
*                                                                     *
*        USED REGISTERS:  Ö05 TO HOLD THE DATA ADDRESS                *
*                         Ö10 TO HOLD THE LENGTH TO BE TRACED         *
*                         Ö14 TO HOLD THE RETURN ADDRESS              *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
TRACE    DS    0H                                                  BAUR
         LTR   @10,@10                 LENGTH=0                    BAUR
         BCR   12,@14                  YES RETURN                  BAUR
         OI    SW2,ANY                 INDICATE ANY RECORD TRACED  BAUR
         LA    @15,SW                  START OF BUFFER             BAUR
         L     @01,SIZE                REMAINING SPACE             BAUR
         LTR   @01,@01                 WAS SIZE=0                  BAUR
         BNP   TRACE10                 YES                         BAUR
         A     @15,TWTSIZE             END OF BUFFER               BAUR
         SR    @15,@01                 ACTUAL LOCATION             BAUR
         CR    @10,@01                 ENOUGH SPACE ?              BAUR
         BNH   TRACE20                 YES                         BAUR
         SR    @10,@01                 @10=REMAINING AMOUNT        BAUR
         BCTR  @01,0                   -1                          BAUR
         EX    @01,TRMOVE              MOVE DATA                   BAUR
         LA    @05,1(@05,@01)          POINT TO DATA NOT TRACED    BAUR
         SR    @01,@01                                             BAUR
         ST    @01,SIZE                SIZE=0                      BAUR
         SPACE 1
TRACE10  DS    0H                                                  BAUR
         BAL   @15,TRC                 TRACE                       BAUR
         B     TRACE                   TRACE REMAINING             BAUR
         SPACE 1
TRACE20  DS    0H                                                  BAUR
         SR    @01,@10                 CALC NEW SIZE               BAUR
         ST    @01,SIZE                SAVE NEW SIZE               BAUR
         BCTR  @10,0                   -1                          BAUR
         EX    @10,TRMOVE              MOVE                        BAUR
         BR    @14                     RETURN                      BAUR
         SPACE 1
TRMOVE   MVC   0(0,@15),0(@05)         MOVE INSTR.                 BAUR
         EJECT
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING SUB ROUTINE IS ENTERED WITH RETURN ADDRESS IN  *
*        @15. IT IS USED COMMUNICATE WITH GTF TO WRITE OUT A          *
*        FILLED BUFFER.                                               *
*                                                                     *
*        REGISTERS DAMAGED: NONE                                      *
*        USED AEREAS:       REGSAVE                                   *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
TRC      DS    0H
         STM   @00,@15,REGSAVE         SAVE REGS                   BAUR
         MVC   SW,SW1                  MOVE IND SWITCH ABOUT STAT. BAUR
         OI    SW,SMYRECID             IND MYRECORD FOR EDIT       BAUR
         LH    @15,PSACPUSA            GET CPUID             BIEDERMANN
         STC   @15,CPUID               KEEP IT IN RECORD     BIEDERMANN
         LH    @15,RECCOUNT            GET REC COUNTER             BAUR
         LA    @15,1(,@15)             INCR 1                      BAUR
         SLL   @15,24                  CLEAR FLAGS           BIEDERMANN
         SRL   @15,24                  ADJUST                BIEDERMANN
         LTR   @15,@15                 CARRY ?               BIEDERMANN
         BNZ   TRC10                   NO                    BIEDERMANN
         LA    @15,2                   SET NEW COUNT         BIEDERMANN
TRC10    DS    0H                                            BIEDERMANN
         STH   @15,RECCOUNT            SAVE REC COUNTER            BAUR
         STC   @15,RECNO               TRACE RECORD NUMBER         BAUR
         L     @05,TWTSIZE             MAX SIZE                    BAUR
         S     @05,SIZE                REAL SIZE                   BAUR
         BM    TRCABEND                SOMTHING IS WRONG     BIEDERMANN
         BAL   @10,AHLPSCM1            WRITE RECORD                BAUR
         LM    @00,@01,REGSAVE         RESTORE REGISTERS     BIEDERMANN
         LM    @04,@14,REGSAVE+L'REGSAVE*@04   RESTORE       BIEDERMANN
         LTR   @15,@15                 TEST RETURN CODE      BIEDERMANN
         BNZ   EXIT10                  LEAVE                 BIEDERMANN
         LA    @15,L'SW+L'CPUID+L'RECNO GET USED SIZE        BIEDERMANN
         LCR   @15,@15                 GET USED SIZE         BIEDERMANN
         A     @15,TWTSIZE             GET USED SIZE         BIEDERMANN
         ST    @15,SIZE                SAVE SIZE                   BAUR
         L     @15,REGSAVE+@15*L'REGSAVE RESTORE R15         BIEDERMANN
         BR    @15                                                 BAUR
         SPACE 1                                             BIEDERMANN
TRCABEND EX    0,TRCABEND              GET AN ABEND0C3       BIEDERMANN
         EJECT
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING SUB ROUTINE IS ENTERED WITH RETURN ADDRESS IN  *
*        @14. IT IS DESIGNET TO TAKE CHECKS TO FIND OUT, WETHER THE   *
*        ADDRESS PASSED IN @04 POINTS TO A VALID CCW OR NOT.          *
*        IF IT POINTS, IT WILL BE CHECKED, WTEHER ITS ADDRESS IS      *
*        ALREADY IN THE CCWTAB OR NOT.                                *
*                                                                     *
*        EXIT IS AS FOLLOW :                                          *
*        @14 + 00    CCW ALREADY IN THE CCWTAB                        *
*        @14 + 04    ADDRESS OR CCW INVALID                           *
*        @14 + 08    NOT YET TRACED, OK                               *
*                                                                     *
*        REGISTERS DAMAGED: @10                                       *
*        WORKAEREAS USED:   CHCCWSAV                                  *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                             BIEDERMANN
CHECKCCW DS    0H
         STM   @07,@09,CHCCWSAV        SAVE WORK REGS              BAUR
         LR    @09,@07                 MAKE ACTUAL TO END          BAUR
         LA    @07,CCWTAB              POINT TO BEGIN              BAUR
         LA    @04,0(,@04)             CLEAR HIGH ORDER            BAUR
         LRA   @10,0(,@04)             THIS VALID ?                BAUR
         BNZ   CHCCWD0                 NO, LEAVE                   BAUR
         LA    @10,7                                               BAUR
         NR    @10,@04                 CHECK IF DW BOUNDRY         BAUR
         BNZ   CHCCWD0                 NO CHAIN LOST               BAUR
         TM    0(@04),X'07'            TIC POSS
         BNZ   CHCCWA0                 NO, LEAVE             BIEDERMANN
         TM    0(@04),X'08'            IS IT TIC
         BO    CHCCWB0                 YES
CHCCWA0  DS    0H
         TM    4(@04),X'03'            VALID ?               BIEDERMANN
         BNZ   CHCCWD0                 BRANCH IF NOT               BAUR
CHCCWB0  DS    0H                                                  BAUR
         L     @10,0(,@07)             CCWADRR                     BAUR
         LA    @10,0(,@10)             CLEAR HIGH                  BAUR
         CR    @04,@10                 ALREADY IN TAB              BAUR
         BE    CHCCWC0                 YES RETURN +0               BAUR
         BXLE  @07,@08,CHCCWB0         LOOP                        BAUR
         LM    @07,@09,CHCCWSAV        RESTORE                     BAUR
         B     8(0,@14)                RETURN, NOT YET TRACED      BAUR
CHCCWC0  DS    0H                                                  BAUR
         LM    @07,@09,CHCCWSAV        RESTORE                     BAUR
         BR    @14                     RETURN                      BAUR
CHCCWD0  DS    0H                                                  BAUR
         LM    @07,@09,CHCCWSAV        RESTORE                     BAUR
         B     4(0,@14)                RETURN, IND LOST            BAUR
         EJECT
         SPACE 3                                             BIEDERMANN
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        THE FOLLOWING SUB ROUTINE IS ENTERED WITH RETURN ADDRESS IN  *
*        @14. IT IS TO CONVERT A REAL ADDRESS TO A VIRTUELL ADDRESS.  *
*        TO BE SHURE THE CORRECT ASID IS FOUND, A LRA FOLLOWS THE     *
*        THE CONVERT. IF CONVERSION FAILS, A RETURNCODE OTHER THAN    *
*        ZERO IS RETURNED.                                            *
*                                                                     *
*        INPUT:  @01 CONTAINS THE REAL ADDRESS TO BE CONVERTED.       *
*        OUTPUT: @01 CONTAINS THE VIRTUELL ADDRESS.                   *
*                                                                     *
*        REGISTERS DAMAGED: NONE                                      *
*        USED AEREAS:       REGSAVE                                   *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 3                                            BIEDERMANN
REALVIRT DS    0H
         STM   @00,@15,REGSAVE         SAVE ALL REGS               BAUR
         LA    @14,REALV1              SET RETURN BASE       BIEDERMANN
         LA    @15,4                   SET RETURN CODE       BIEDERMANN
         LA    @01,0(,@01)             ADDR TO BE TRANSLATED       BAUR
         LTR   @01,@01                 THIS ZERO ADDRESS ?   BIEDERMANN
         BZ    REALV2                  YES, ERROR            BIEDERMANN
         L     @15,CVTPTR              GET CVT                     BAUR
         L     @15,X'0120'(,@15)       ADDR OF TRANSLATE ROUTINE   BAUR
         BALR  @14,@15                 TRANSLATE                   BAUR
         USING REALV1,@14              ADDRESSABILITY
REALV1   LA    @01,0(,@01)             CLEAR FLAGS
REALV2   DS    0H                                            BIEDERMANN
         LTR   @15,@15                 TRANSLATION OK ?            BAUR
         BNZ   REALV3                  NO, LEAVE             BIEDERMANN
         LRA   @15,0(,@01)             IS IT VALID ?         BIEDERMANN
REALV3   DS    0H                                            BIEDERMANN
         L     @00,REGSAVE             RESTORE                     BAUR
         LM    @02,@15,REGSAVE+@02*L'REGSAVE   RESTORE @02-@15     BAUR
         BR    @14                     RETURN WITH COND CODE BIEDERMANN
         SPACE 1
         DROP  @14
         EJECT
         SPACE 1
         LTORG
         SPACE 1                                             BIEDERMANN
         DS    0D                                            BIEDERMANN
         DC    CL8'PATCH'                                    BIEDERMANN
PATCH    DC    50F'0'                                        BIEDERMANN
         SPACE 3
         EJECT
         SPACE 3
**************************************************** F.BIEDERMANN *****
*                                                                     *
*        NON REENTANT WORK AEREA DEFINITION .                         *
*                                                                     *
**************************************************** F.BIEDERMANN *****
         SPACE 2
         DS    0D
WORKTAB  DC    A(WORK01)               FIRST WORK AEREA      BIEDERMANN
         DC    A(WORK02)               NEXT WORK AEREA       BIEDERMANN
WORKLAST DS    0C                      END OF ADDRESS TABLE  BIEDERMANN
         SPACE 1                                             BIEDERMANN
WORK01   DS    0D                                            BIEDERMANN
         DC    CL8'WORKCPU0'                                 BIEDERMANN
RETURN   DS    1F                      TO SAVE THE EXIT ADDRESS
CTL1SAVE DS    1F
REGSAVE  DS    16F
         DS    0D                                            BIEDERMANN
WORKPSW  DS    CL8                                           BIEDERMANN
WORKCOMP DS    CL4                                           BIEDERMANN
WORKVIRT DS    CL4                                           BIEDERMANN
WORKSAVE DS    16F                                           BIEDERMANN
SCANSAVE DS    3F                                                  BAUR
CHCCWSAV DS    3F
CCWTABE  DS    1F                                                  BAUR
DATSTART DS    1F                      USED TO SAVE DATA START
DATEND   DS    1F                                                  BAUR
DATSIZE  DS    1F                      SIZE OF DATA TO BE TRACED
SIZE     DS    1F                      SIZE REMAINING IN BUFFER    BAUR
LAST     DC    A(0)                    END CCW
RECCOUNT DC    Y(0)                    RECORD COUNTER              BAUR
         SPACE 1                                             BIEDERMANN
SWITCHES DS    0F                      BIT STRING SWITCHES   BIEDERMANN
         SPACE 1                                             BIEDERMANN
SW1      DC    X'0'                    INDICATOR SWITCH FOR CCW    BAUR
END      EQU   X'04'                   LAST RECORD                 BAUR
LASTCCW  EQU   X'08'                   LAST CCW REACHED            BAUR
LOST     EQU   X'01'                   END OF CHAIN NOT REACHED    BAUR
         SPACE 1                                             BIEDERMANN
SW2      DC    X'00'                   COMMON SWITCHES
SW2IOSB  EQU   X'80'                   IOSB HAS TRIED TO DUMP
SW2DUMP  EQU   X'40'                   DUMP OF WORK HAS TRIED
ANY      EQU   X'20'                                               BAUR
SW2CTL1  EQU   X'10'                   CTL1SAVE NOW VALID.         BAUR
*        THE FOLLOWING SWITCH MUST NOT BE USED FOR ANY OTHERS
*        BECAUSE ITS CONTENS MAY BE SHIFTED.
SW3      DC    X'00'                   USED FOR MCH CHECKS   BIEDERMANN
SW3MCH   EQU   X'20'                   A MCH OCCURED         BIEDERMANN
         SPACE 1                                             BIEDERMANN
SW4      DC    X'00'                   TIC OR SWITCH         BIEDERMANN
ENTRYTYP DC    Y(0)                    ENTRY TYP             BIEDERMANN
TSIO     EQU   X'01'                   TYPE IS SIO           BIEDERMANN
TIO      EQU   X'02'                   TYPE IS IO            BIEDERMANN
TEOS     EQU   X'04'                   TYPE IS EOS           BIEDERMANN
TPCI     EQU   X'08'                   TYPE IS PCI           BIEDERMANN
TUIO     EQU   X'10'                   TYPE IS UIO           BIEDERMANN
*        THE FOOLOWING TWO SWITCHES MUST NOT BE USED FOR ANY OTHERS
*        BECAUSE ITS CONTENS MAY BE SHIFTED.
SW5      DC    X'00'                   USED FOR IO PGM CHECKS
SW5IO    EQU   X'20'                   A PGM CHECK DURING IO
SW6      DC    X'00'                   USED FOR SIO PGM CHECKS
SW6SIO   EQU   X'20'                   A PGM CHECK DURING SIO
UCBADDR  DC    Y(0)                    UCB ADDRESS FOR DEBUGGING
         SPACE 1                                             BIEDERMANN
CCWTAB   DS    512F
WORKEND  DS    0D                      WORKAEREA END IND.    BIEDERMANN
         SPACE 1                                             BIEDERMANN
WORK02   DS    0D                      SECOND WORK AEREA     BIEDERMANN
         DC    CL8'WORKCPU1'                                 BIEDERMANN
         ORG   WORK02+WORKEND-WORK01                         BIEDERMANN
GTFSEND  EQU   *                       END OF MODULE FOR DUMP
         EJECT
RECORD   DSECT
         DS    CL256                                               BAUR
TSW      DS    C
         DS    CL7
MCA      DS    1F
         DS    CL16
SW       DS    CL1                                                 BAUR
SMYRECID EQU   X'F8'                   INDICATE MY RECORD    BIEDERMANN
SEND     EQU   X'04'                   THIS RECORD IS LAST   BIEDERMANN
SMSG     EQU   X'02'                   THIS RECORD IS A MSG  BIEDERMANN
SLOST    EQU   X'01'                   END OF CHAIN NOT REACHED
         SPACE 1
*        FOR MORE FUNCTIONS REFEER TO SW1                    BIEDERMANN
         SPACE 1
CPUID    DS    CL1                                           BIEDERMANN
RECNO    DS    CL1                                                 BAUR
DEVTYP   DS    CL1                                                 BAUR
DATAL    DS    CL2                                                 BAUR
         SPACE 1
*        THE FOLLOWING IS FOR INFORMATION ONLY AND SHOWS     BIEDERMANN
*        THE RELATIONSHIP OF CCWADDR., CCW, LENGTH AND DATA. BIEDERMANN
         SPACE 1                                             BIEDERMANN
STARTA   DS    0C                      START OF CCW CHAIN    BIEDERMANN
         DC    X'00'                   PSWITCH FLAGS         BIEDERMANN
         DC    AL3(0)                  START OF CCW CHAIN    BIEDERMANN
CCWADDR  DS    0CL4                    CCW ADDRESS           BIEDERMANN
CCWFLAGS DC    X'0'                    SEVERAL FLAGS         BIEDERMANN
CTICOK   EQU   X'80'                   TIC RESOLVED IND.     BIEDERMANN
CTIC     EQU   X'40'                   COMMAND IS A TIC      BIEDERMANN
CNODAT   EQU   X'20'                   NO DATA FOR THIS CCW  BIEDERMANN
CDATERR  EQU   X'10'                   DATA ERRORR THIS CCW  BIEDERMANN
CSPLIT   EQU   X'08'                   DATA IS SPLITTED      BIEDERMANN
CLAST    EQU   X'04'                   THIS IS LAST CCW      BIEDERMANN
CZERO    EQU   X'02'                   NO DATA REQUESTED     BIEDERMANN
CLOST    EQU   X'01'                   CCW CHAIN LOST        BIEDERMANN
CCWA     DC    AL3(0)                  ADDRESS PORTION       BIEDERMANN
CCW      DC    XL8'0'                  CCW                   BIEDERMANN
DATALNGT DC    AL2(0)                  LENGTH OF DATA        BIEDERMANN
DATATEXT DC    C'TEXT'                 VAREABLE DATA         BIEDERMANN
         SPACE 1                                             BIEDERMANN
*        THE FOLLOWING REDEFINITION IS FOR MESSAGES AND CNTL BLOCK
*        TRACE.
         SPACE 1
         ORG   RECNO+L'RECNO           SET UP FOR BLK TRACE
BLKID    DS    CL4                     IF IOSB THIS FIELD CONTAINS IOSB
*                                      IF EWA  THIS FIELD CONTAINS EWA
*                                      IF WORK THIS FIELD CONTAINS WORK
*                                      IF MSG  THIS FIELD CONTAINS MSG
BLKADDR  DS    CL3                     ADDRESS OF BLK OR ZERO
BLKLGTH  DS    CL2                     LENGTH OF BLOCK
BLKTRC   DS    0C                      START OF BLK DATA     BIEDERMANN
         SPACE 1
IOSBLGTH EQU   108                     LENGTH OF IOSB        BIEDERMANN
EWALGTH  EQU   160                     LENGTH OF EWA         BIEDERMANN
         SPACE 2                                             BIEDERMANN
         END
