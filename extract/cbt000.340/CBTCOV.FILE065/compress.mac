*          DATA SET COMPRESS   AT LEVEL 003 AS OF 05/19/78
COMPRESS TITLE 'TSO PDS COMPRESS ROUTINE'
*
* 'COMPRESS'                     PDS COMPRESSION COMMAND PROCESSOR
*
*
*                         THIS COMMAND WILL DYNAMICALLY ALLOCATE THE
*                         REQUESTED DATASET, ALLOCATE A SYSPRINT
*                         DATASET TO VIO, AND A SYSIN DATASET TO DUMMY.
*                         IT WILL THEN LINK TO THE IEBCOPY UTILITY
*                         WITH THE COMPRESS PARAMETER TO COMPRESS THE
*                         DATASET.  AFTER COMPLETION OF THE COMPRESS
*                         THE SYSPRINT DATASET WILL BE OPTIONALLY
*                         PRINTED BASED ON PARAMETERS, OR PRINTED IF
*                         THERE WAS A NON ZERO COMPLETION CODE FROM
*                         IEBCOPY.
*
*                         NOTE:  THIS COMMAND MUST BE LINKED AS
*                                AC(1), MUST BE INVOKED FROM AN
*                                AUTHORIZED LIBRARY, AND MUST BE
*                                KNOWN TO THE TMP AS AN AUTHORIZED
*                                COMMAND (IN CSECT IKJEFTE2).
*
*                         THIS COMMAND WAS RECEIVED FROM DEPT DENKR,
*                         COMPUTER SERVICES, UNITED AIR LINES, DENVER
*                         IN EARLY 1975, AND HAS BEEN EXTENSIVELY
*                         MODIFIED SINCE THEN.
*
*                         REVISED     05/19/78  JOHN A.NORDTVEDT
*                                               DEPT EXOKF
*                                               UNITED AIR LINES
*                                               P.O. BOX 66100
*                                               CHICAGO, ILL. 60666
*                                               (312) 952-5410
*
*
         MACRO
&LAB     PUTIT &MESS
         LCLC  &L
&L       SETC  'L'''
&LAB     TPUT  M&SYSNDX,&L.M&SYSNDX
         B     RETURN
M&SYSNDX DC    C&MESS
         MEND
*
         EJECT
         IKJCPPL
*
***********************************************************************
*
COMPRESS   CSECT
*
***********************************************************************
*
PARMLIST IKJPARM
DSNAM    IKJPOSIT DSNAME,PROMPT='PDS DATA SET NAME'
LISTPDE  IKJKEYWD DEFAULT='NOLIST'
         IKJNAME  'NOLIST'
         IKJNAME  'LIST'
         IKJNAME  'FULLLIST'
         IKJENDP
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
LINK2    EQU   9
R10      EQU   10
LINK     EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
BASE     EQU   R12
START    DS    0H
         STM 14,12,12(13)
         LR    BASE,15
         USING  COMPRESS,BASE
         L     15,=A(SAVEAREA)
         ST    13,4(15)
         ST    15,8(13)
         LR    13,15
         B     SAVEAREA+72
*
SAVEAREA DC    18A(0)
*
         LR    R10,R1                  SAVE PARM LIST
         USING CPPL,R10                ADDRESS IT
         EJECT
*
***********************************************************************
*
*        SET UP PARM FOR PARSE
*
***********************************************************************
*
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         MVC   PPLCBUF,CPPLCBUF
         LA    R1,PPLUPT               POINT TO IT
         LINK  EP=IKJPARS
         B     *+4(R15)                BRANCH ON RETURNED CODE
         B     PARSOK
         B     ERROR2
         B     RETURN                  USER ATTNED OUT
         B     ABEND1                  PROGRAM BUG
         B     ERROR3                  SPACE NOT AVAILABLE IN REGION
         B     ERROR4                  INVALID PARM
*
PARSOK   L     R8,PPLANS               CHECK ANSWER ADDRESS
         LTR   R8,R8
         BZ    ABEND1                  PARSE ERROR
         USING IKJPARMD,R8
         SPACE 5
*
***********************************************************************
*
*        SET UP DAIR BLOCK TO ALLOCATE FILE
*
***********************************************************************
*
         TM    DSNAM+6,X'80'           IS DSNAME THERE
         BZ    ERROR2                     *  NO
         TM    DSNAM+6,X'40'           CHECK FOR QUOTES
         BO    SKIPUID  YES, DO NOT TELL DAIR TO PUT IN USERID
         OI    DA08FLG1,FLG1UID  SET FLAGS IN ALLOCATE BLOCKS
*        OI    DA18FLG,FLG1UID  SET FLAGS IN DEALLOCATE BLOCKS
*        OI    DA18FLG2,FLG1UID
SKIPUID  LH    R1,DSNAM+4              SET UP DSNAME
         STH   R1,DDSNAML              SAVE LENGTH FOR LATER
         BCTR  R1,0                    GET 360 LENGTH
         L     R2,DSNAM                GET ADDRESS
         EX    R1,MOVED                MOVE IT TO BLOCK
         TM    DSNAM+14,X'80'          SET UP FOR MEMBER NAME ALSO
         BO    NOMEMBER
         TM    DSNAM+22,X'80'          TEST FOR PASSWORD
         BZ    NOPASS
         LH    R1,DSNAM+20             LENGTH
         L     R2,DSNAM+16             ADDRESS
         BCTR  R1,0
         EX    R1,MOVEP               MOVE PASSWORD
NOPASS   EQU   *
*
**********************************************************************
*
*              USE DAIR TO FREE DATASET
*
**********************************************************************
*
         MVC   DAPLUPT,CPPLUPT
        MVC    DAPLECT,CPPLECT
         MVC   DAPLPSCB,CPPLPSCB
         LA    1,DA18CD
         ST    1,DAPLDAPB
         LA    1,DAPLUPT
         LINK  EP=IKJDAIR
         LA    1,DAIRBLK
         ST    1,DAPLDAPB
*
***********************************************************************
*
*              USE DAIR TO ALLOCATE SYSUT1 AND SYSUT2 TO DATASET
*
***********************************************************************
*
         BAL   LINK,DAIR
         MVC   DDNMLUT1,DA08DDNM      SAVE NEW DDNAME FOR SYSUT1
         MVC   DA08DDNM(8),=CL8'        '
         BAL   LINK,DAIR
         MVC   DDNMLUT2,DA08DDNM      SAVE NEW DDNAME FOR SYSUT2
         MVC   DA08DDNM(8),=CL8'        '
*
*
***********************************************************************
*
*              USE DAIR TO ALLOCATE ATTRIBUTE NAMES
*
***********************************************************************
*
         MVC   DA34INAM+5(3),DDNMLUT1+5       CREATE ATTR NAMES
         MVC   DA34ONAM+5(3),DDNMLUT2+5          *
         BAL   LINK2,FREEATTR         FREE ATTRIB LIST NAMES
         LA    R1,DA34IN              SET UP SYSIN ATTR LIST
         MVI   DA34IN+6,X'40'            *
         ST    R1,DAPLDAPB               *
         BAL   LINK,DAIR                 *
         LA    R1,DA34OUT             SET UP SYSPRINT ATTR LIST
         MVI   DA34OUT+6,X'40'           *
         ST    R1,DAPLDAPB               *
         BAL   LINK,DAIR                 *
*
***********************************************************************
*
*              USE DAIR TO ALLOCATE SYSIN TO DUMMY
*
***********************************************************************
*
         LA    R1,DAIRBLK
         ST    R1,DAPLDAPB
         MVC   DDSNAM(8),=C'        '      NULLFILE
         MVC   DA08PSWD+8(3),=X'040404'
         MVI   DA08FLG1,X'06'
         MVI   DA08FLG2,X'00'
         SR    R4,R4                  ZERO DSNAME POINTER
         ST    R4,DA08DSN               *
         MVC   DDSNAML(2),=H'0'
         MVC   DA08ALN(8),DA34INAM
         BAL   LINK,DAIR
         MVC   DDNMLIN,DA08DDNM       SAVE NEW DDNAME FOR SYSIN
*
***********************************************************************
*
*              USE DAIR TO ALLOCATE SYSPRINT
*
***********************************************************************
*
         MVC   DA08ALN(8),DA34ONAM
         MVC   DA08DDNM(8),=C'        '
         MVI   DA08FLG1,X'82'        SET FLAGS
         MVI   DA08FLG2,X'40'          *
         MVC   DA08PQTY,=F'10'       SET PRIMARY SPACE ALLOCATION
         MVC   DA08SQTY,=F'10'       SET SECONDARY SPACE ALLOCATION
         MVC   DA08UNIT,=CL8'VIO '   SET UNIT TO VIO
         BAL   LINK,DAIR
         MVC   DDNMLOUT,DA08DDNM      SAVE NEW DDNAME FOR SYSPRINT
*
***********************************************************************
*
*              LINK TO IEBCOPY UTILITY PROGRAM
*
***********************************************************************
*
         STAX  DEFER=YES               DEFER ALL ATTENTIONS
*
         LA    1,APARM
         LINK  EP=IEBCOPY
         ST    R15,RETC               SAVE THE RETURN CODE FROM IEBCOPY
         LA    R1,DA18CD  DEALLOCATE THE FILE
         ST    R1,DAPLDAPB            POINT BLOCK TO DIFFERENT BLOCK
         MVC   DA18DDN,DDNMLIN
         BAL   LINK,FREE
         MVC   DA18DDN(8),DDNMLUT1
         BAL   LINK,FREE
         MVC   DA18DDN(8),DDNMLUT2
         BAL   LINK,FREE
*
         STAX  DEFER=NO
*
         L     R15,RETC              LOAD RETURN CODE FORM IEBCOPY
         LTR   R15,R15               AND TEST FOR ZERO
         BNZ   WRITECC                  * NOT ZERO, GO PRINT
         CLI   LISTPDE+1,1           WAS NOLIST SPECIFIED (DEFAULT)
         BE    COMPOK                   *  YES, DON'T PRINT
         B     OPENOUT                  *  NO, PREPARE TO PRINT MSG
WRITECC  CVD   R15,PACKED            CONVERT RETURN CODE
         ED    EMSG12(4),PACKED+6       *  AND FORMAT MESSAGE
         TPUT  EMSG11,80                *   AND PRINT IT
OPENOUT  MVC   PDCB+40(8),DDNMLOUT   MOVE SYSPRINT DDNAME TO DCB
         OPEN  (PDCB,(INPUT))           *  AND OPEN FILE
WLOOP    GET   PDCB                  READ EACH SYSPRINT RECORD
         LA    R1,1(R1)                 *
         CLC   24(8,R1),=CL8' '       OMIT THIS MESSAGE
         BE    WLOOP                    *  YES
         CLI   RETC+3,0           IS THE RETURN CODE ZERO
         BNE   PUTOUT                   *  NO, PRINT EVERYTHING
         CLI   LISTPDE+1,3        WAS FULLLIST SPECIFIED
         BE    PUTOUT                   *  YES, PRINT EVERYTNING
         CLC   0(7,R1),=C'IEB152I'    OMIT THIS MESSAGE
         BE    WLOOP                    *  YES
         CLC   0(7,R1),=C'IEB154I'    OMIT THIS MESSAGE
         BE    WLOOP                    *  YES
         CLC   0(7,R1),=C'IEB167I'    OMIT THIS MESSAGE
         BE    WLOOP                    *  YES
PUTOUT   TPUT  (1),120                  * AND PRINT IT AT THE TERMINAL
         B     WLOOP
EOFPRINT CLOSE (PDCB)                CLOSE THE PRINT FILE
COMPOK   MVC   DA18DDN,DDNMLOUT
         BAL   LINK,FREE
         BAL   LINK2,FREEATTR          FREE ATTRIBUTE BLOCKS
*
RETURN   L     R13,4(R13)
         L     R15,RETC               LOAD RETURN CODE
         RETURN (14,12),RC=(15)
         EJECT
*
*
***********************************************************************
*
*              SUBROUTINE TO LINK TO DAIR FOR ALLOCATE
*
***********************************************************************
*
DAIR     LA    1,DAPLUPT
         LINK  EP=IKJDAIR             CALL DAIR
          LTR    15,15
          BCR    8,LINK
         ST    15,RETC                STORE RET CODE
         LA    1,DAIRFAIL             PARM ADDR
         LINK  EP=IKJEFF18            ERROR MESSAGE ROUTINE
         B     RETURN
*
***********************************************************************
*
*              SUBROUTINE TO LINK TO DAIR FOR DEALLOCATE
*
***********************************************************************
*
*
FREE      EQU   *
         LA    R1,DAPLUPT POINT TO DAIR PARM LIST
         LINK  EP=IKJDAIR
         BR    LINK
*
**********************************************************************
*
*              SUBROUTINE TO FREE ATTRIBUTE BLOCKS
*
**********************************************************************
*
FREEATTR  MVI   DA34IN+6,X'80'        FREE SYSIN ATTR BLOCK
         LA    R1,DA34IN                *
         ST    R1,DAPLDAPB              *
         BAL   LINK,FREE                 *
         TM    DA34IN+2,X'80'            *    WAS ATTR NAME FOUND
         BZ    FREEATT2                  *         NO
         MVI   DA34IN+6,X'20'            *         YES
         BAL   LINK,FREE                 *
FREEATT2 MVI   DA34OUT+6,X'80'        FREE SYSPRINT ATTR BLOCK
         LA    R1,DA34OUT               *
         ST    R1,DAPLDAPB              *
         BAL   LINK,FREE                 *
         TM    DA34OUT+2,X'80'           *     WAS ATTR NAME FOUNT
         BCR   8,LINK2                   *         NO
         MVI   DA34OUT+6,X'20'           *         YES
         BAL   LINK,FREE                 *
         BR    LINK2
*
         EJECT
*
***********************************************************************
*
*        PARSE PARAMETER LIST
*
***********************************************************************
*
PPLUPT   DC    A(0)
PPLECT   DC    A(0)
PPLECB   DC    A(PPLECBS)
PPLPCL   DC    A(PARMLIST)
         DC    A(PPLANS)
PPLCBUF  DC    A(0)
PPLUWA   DC    A(0)
PPLECBS  DC    A(0)
PPLANS   DC    A(0)  ANSWER PLACE
*
***********************************************************************
*
*        DAIR ALLOCATE BY DSNAME BLOCK
*
***********************************************************************
*
DAIRBLK  DS    0F
DA08CD   DC    X'0008'
DA08FLG  DC    H'0'
DA08DARC DC    H'0'
DA08CTRC DC    H'0'
DA08DSN  DC    A(DDSNAML)
DA08DDNM DC    CL8' '  LET DAIR FILL IN DDNAME
DA08UNIT DC    CL8' '  UNIT NAME
         DC    CL8' '
         DC    F'0'
DA08PQTY DC    F'0'               PRIMARY SPACE ALLOCATION
DA08SQTY DC    F'0'               SECONDARY SPACE ALLOCATION
         DC    F'0'
DA08MNAM DC    CL8' '
DA08PSWD DC    CL8' '
         DC    X'010808' OLD,KEEP,KEEP
DA08FLG1 DC    X'00'
FLG1UID  EQU   X'20'  OR IN TO CONCAT USERID
         DC    3X'00'
DA08FLG2 DC    XL1'00'
DA08ALN  DC    CL8'        '
*
***********************************************************************
*
*              DAIR BLOCKS FOR ATTRIBUTE
*
***********************************************************************
*
DDSNAML  DC    H'0'
DDSNAM   DC    CL44' '
DA34IN   DS    0D                     ATTR CONTROL BLOCK FOR SYSIN
         DC    XL2'0034'
         DC    2XL2'00'
         DC    XL2'2000'
DA34INAM DC    CL8'SATTR000'
         DC    AL4(DAIRACBI)
*
DA34OUT  DS    0D                     ATTR CONTROL BLOCK FOR SYSPRINT
         DC    XL2'0034'
         DC    2XL2'00'
         DC    XL2'2000'
DA34ONAM DC    CL8'SATTR000'
         DC    AL4(DAIRACBP)
*
*
DAIRACBI DS    0D                     ATTR LIST BLOCK FOR SYSIN
         DC    36X'0'
         DC    X'8000'
         DC    2H'80'
         DC    5X'0'
DAIRACBP DS    0D                     ATTR LIST BLOCK FOR SYSPRINT
         DC    36X'0'
         DC    X'9400'
         DC    H'12100'               BLOCK SIZE
         DC    H'121'                 LRECL
         DC    5X'0'
*
***********************************************************************
*
*              DAIRFAIL
*
***********************************************************************
*
DAIRFAIL  DC    A(DAPLUPT)
          DC    A(RETC)
          DC    A(ZERO)
          DC    A(SET1)
ZERO      DC    F'0'
RETC      DS    F
          DS    0D
PACKED    DS    PL8
SET1      DC    X'0001'
*
***********************************************************************
*
*        SOME EXECUTED INSTRUCTIONS
*
***********************************************************************
*
MOVED   MVC   DDSNAM(0),0(2)
MOVEP   MVC   DA08PSWD(0),0(2)
*
***********************************************************************
*
*              PARAMETER BLOCKS FOR IEBCOPY
*
***********************************************************************
*
         CNOP  0,4
APARM    DC    X'00',AL3(PARM)
         DC    X'80',AL3(DDNMLIST)
         CNOP  6,8
PARM     DC    H'8'                   PARM
         DC    C'COMPRESS'
         CNOP  6,8
DDNMLIST DC    H'88'                  DDNAME LIST
         DC    4XL8'00'
DDNMLIN  DC    CL8' '                 DDNAME FOR SYSIN
DDNMLOUT DC    CL8' '                 DDNAME FOR SYSPRINT
         DC    XL8'00'
DDNMLUT1 DC    CL8' '                 DDNAME FOR SYSUT1
DDNMLUT2 DC    CL8' '                 DDNAME FOR SYSUT2
         DC    2XL8'00'
*
***********************************************************************
*
*              DAIR PARAMETER LIST
*
***********************************************************************
*
DAPLUPT  DC    A(0)
DAPLECT  DC    A(0)
DAPLECB  DC    A(DAPLECBS)
DAPLPSCB DC    A(0)
DAPLDAPB DC    A(DAIRBLK)
DAPLECBS DC    A(0)
*
***********************************************************************
*
*        DAIR DEALLOCATE BY DSNAME BLOCK
*
***********************************************************************
*
         DS    0F
DA18CD   DC    X'0018'
DA18FLG  DC    H'0'
DA18DARC DC    H'0'
DA18CTRC DC    H'0'
*A18DSN  DC    A(DDSNAML)
DA18DSN  DC    F'0'
DA18DDN  DC    CL8'        '
DA18MEM  DC    CL8' '
         DC    CL2' '
         DC    X'08'
DA18FLG2 DC    X'10'
         DC    CL8' '
*
PDCB     DCB    DDNAME=SYSPRINT,MACRF=GL,EODAD=EOFPRINT,DSORG=PS
*
*
***********************************************************************
*
*              ERROR MESSAGES
*
***********************************************************************
*
         PRINT NOGEN
ERROR2   PUTIT 'INCOMPLETE PARAMETERS'
NOMEMBER PUTIT 'MEMBER NAME NOT ALLOWED'
*
ERROR3   PUTIT 'NOT ENOUGH SPACE IN REGION'
*
ERROR4   PUTIT 'INVALID PARAMETERS'
*
ABEND1   ABEND 1004,DUMP
*
*              ERROR MESSAGES
*
EMSG11   DC    C' COMPRESS HAS COMPLETED WITH A COMPLETION CODE OF'
EMSG12   DC    X'40202120'
EMSG13   DC    CL(80-L'EMSG11-L'EMSG12)', PRINT FOLLOWS '
*
         LTORG
         END   START
