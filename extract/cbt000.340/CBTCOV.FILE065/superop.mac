SUPEROP  ENTER
         REGISTER
* INITIALIZATION
INIT     DS    0H
         OPEN  DATASET       OPEN DATASET WITH OPERATOR COMMANDS
         LOAD  EP=AMIINT     LOAD "AMIINT" TO GET JOB CALLER'S ORIGIN
         LR    R15,R0        SETUP ENTRY PT ADDRESS FOR "CALL" MACRO
         CALL  (15),(JOBSTAT)
         CLC   JOBSTAT,ONE   CALLER: 0=BATCH,1=STARTED TSK,2=LOGON
         BE    VALIDCAL      STARTED TASK VALID CALLER OF THIS MODULE
         LOAD  EP=AMIOPER    LOAD "AMIOPER" TO TEST FOR "OPER" AUTH
         LR    R15,R0        SETUP ENTRY PT ADDRESS FOR "CALL" MACRO
         CALL  (15)          CALL "AMIOPER"
         LTR   R15,R15       IF "OPER" AUTHORITY
         BZ    VALIDCAL         THEN CALLER IS OK
         ABEND 913              ELSE ABEND
VALIDCAL DS    0H
         MODESET KEY=ZERO,MODE=SUP
         L     R2,16         -->CVT
         L     R2,100(R2)    -->UCM
         SH    R2,=H'4'      -->PREFIX
         L     R2,0(R2)      -->MCS PREFIXTO UCM
         L     R2,0(R2)      -->MASTER CONS UCM ENTRY
         MVC   UCMID,26(R2)    SAVE MASTER CONSOLE ID
         L     R2,28(R2)     -->FIXED DCM
         L     R2,0(R2)      -->PAGEABLE DCM(THIS POINTS TO SCREEN)
         EJECT
* THIS IS THE MAIN PROGRAM LOOP
LOOP     DS    0H
         LA    R4,CMDLINE          GET ADDR OF COMMAND LINE
GET      GET   DATASET,COMMAND     GET A COMMAND
         OC    COMMAND,BLANKS      MAKE THE COMMAND UPPER CASE
         MVC   0(71,R4),COMMAND    MOVE IN NEW COMMAND
         CLI   COMMAND+71,C' '     IS THERE MORE?
         BE    ISSUE               NOPE, -->
         CLI   LINEFLAG,C' '       IS IT MORE THAN I CAN HANDLE?
         BNE   ISSUE               YEP, -->
         MVI   LINEFLAG,C'X'       SET THE SECOND LINE FLAG
         LA    R4,71(R4)           BUMP PAST FIRST PART
         B     GET                 GET THE REST OF IT
ISSUE    LA    R1,CMD             POINT TO COMMAND
         LA    R0,0               INDICATE MASTER CONSOLE
         IC    R0,UCMID           MOVE IN CONSOLE ID
         SVC   34                 SEND THE COMMAND
         MVC   CMDLINE,BLANKS     CLEAR THE COMMAND LINE
         MVI   LINEFLAG,C' '      ZERO LINE FLAG
         B     LOOP               GO READ ANOTHER COMMAND
         EJECT
*  LEAVE
RETURN   DS    0H
         MODESET KEY=NZERO,MODE=PROB
         CLOSE DATASET        CLOSE DATASET WITH OPERATOR COMMANDS
         LEAVE                RESTORE REGISTERS AND RETURN
* CONSTANTS
CMD      DC    H'104',H'0'
CMDLINE  DC    CL142' '
COMMAND  DC    CL80' '
BLANKS   DC    CL142' '
LINEFLAG DC    C' '
JOBSTAT  DC    F'0'           "AMIINT" REPLY WORD
ONE      DC    F'1'
UCMID    DC    X'00'                 CONSOLE ID
ERRMSG1  DC    C'*** CAN''T FIND THE ACF2 CVT'
ERRMSG2  DC    C'*** CAN''T FIND THE ACF2 ACUCB'
* DCB
DATASET  DCB   DDNAME=DATASET,DSORG=PS,LRECL=80,EODAD=RETURN,          X
               MACRF=GM
         END
