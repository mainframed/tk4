GETCOR   TITLE 'GETMAIN / FREEMAIN PROGRAM'
**********************************************************************
*                                                                    *
*   THIS PROGRAM OBTAINS AND RELEASES STORAGE VIA GETMAIN AND        *
*   FREEMAIN MACROS. THE CALLIN SEQUENCE IS AS FOLLOWS:              *
*                                                                    *
*   ADDR    - FULLWORD BINARY THAT CONTAINS ADDRESS FROM GETMAIN     *
*             AND IS REQUIRED FOR FREEMAIN.                          *
*   SIZE    - FULLWORD BINARY THAT CONTAINS SIZE FOR GETMAIN         *
*             AND IS REQUIRED FOR FREEMAIN.                          *
*   TYPE    - ONE BYTE CHARACTER THAT TELLS GETCOR THE TYPE OF       *
*             OPERATION REQUIRED.                                    *
*             'G'  - FOR GETMAIN                                     *
*             'F'  - FOR FREEMAIN                                    *
*   RETC    - FULLWORD BINARY THAT CONTAINS RETURN CODE FROM THE     *
*             REQUEST.                                               *
*             0 - SUCCESSFUL COMPLETION                              *
*             4 - NO STORAGE OBTAINED OR RELEASED                    *
*             8 - INVALID TYPE SPECIFIED                             *
*                                                                    *
*                                                                    *
*             "GETCOR" IS REENTRANT AND REUSABLE                     *
*                                                                    *
*                                                                    *
**********************************************************************
GETCOR   ENTER PARMREG=2,REGEQUS=YES,    GET READY FOR SOME WORK       X
               GETMAIN=(WORKLEN,0)
START    DS    0H
         LM    R6,R9,0(R2)               GET THE PARMS
         L     R7,0(R7)                  FIND HOW MUCH HE NEEDS
         CLI   0(R8),C'G'                DOES HE WANT SOME CORE?
         BE    GETMAIN                   YES, -->OK
         CLI   0(R8),C'F'                DOES HE WANT TO GIVE IT BACK?
         BE    FREEMAIN                  YES, -->OK
         MVC   0(4,R9),=F'8'             NO, MOVE IN ERROR
         B     EXIT                      AND GO
GETMAIN  DS    0H
         GETMAIN RC,LV=(R7)              GET SOME CORE
         ST    R1,0(R6)                  TELL HIM WHERE IT IS
         B     RETURN                    AND LEAVE
FREEMAIN DS    0H
         L     R6,0(R6)                  FIND THE ADDRESS
         FREEMAIN RC,A=(R6),LV=(R7)      GIVE THE CORE BACK
RETURN   DS    0H
         ST    R15,0(R9)                 TELL HIM THE RETURN CODE
EXIT     LEAVE GETMAIN=(WORKLEN,0)       BYE BYE........
         LTORG
WORKAREA DSECT
         DS    18A
WORKLEN  EQU   *-WORKAREA
         END   GETCOR
