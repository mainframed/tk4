***********************************************************************
*                                                              TD APR91
MODMVS   TITLE 'PROGRAM TO LOCATE A MEMBER/MODULE'
***********************************************************************
*    CHANGE DATE: APRIL 1991 XA (2.2) AND ESA (3.1.3) .. TD ..
*
*     ABSTRACT
*
*     MODMVS PROVIDES THE ABILITY TO LOCATE A MEMBER OF PDS'S THAT
*     ARE ALLOCATED TO YOUR TSO SESSION AND LPA/LNK LIST AREA.
*     WHEN INVOKE WITH JUST THE MEMBER NAME, MODMVS LOOKS FOR THE
*     MEMBER FIRST IN YOUR STEPLIB CONCATENATION, THEN ISPLLIB
*     CONCATENATION, THEN LPALIST AREA, THEN LNKLST AREA.
*
*     METHOD OF OPERATION:
*
*     TIOT AREA IS SEARCH FOR A MATCHED DDNAME, THEN ALL DATASETS
*     WITHIN THE SELECTED DDNAME ARE DYNAMICALLY ALLOCATED AND A
*     BLDL IS ISSUED TO LOOK FOR THE MEMBER.
*
*     LPALST DATASETS ARE OBTAINED FROM ABOVE THE 16M AREA, ALL LPALST
*     DSNS ARE ALLOCATED DYNAMICALLY, AND A BLDL IS ISSUED TO LOOK FOR
*     THE MEMBER.
*
*     FINALLY, LNKLST DATASETS ARE PICKED UP, ALLOCATED AND BLDL IS
*     DONE TO REPORT ANY FOUND MEMBER.
*
*     MODMVS SUPPORTS THE FOLLOWING RELEASES:
*
*     MVS/XA 2.2.0 - ESA 3.1.3
*
*     INQUIRIES:
*
*     SEND INQUIRIES TO:
*
*              TOM D. DIEN, INFORMATION SYSTEMS 3RD FLOOR
*              LOS ANGELES TIMES
*              TIMES MIRROR SQUARE
*              LOS ANGELES, CA 90053
*
*     OR PHONE: (213) 237-5840
*
*     INSTALLATION:
*
*     MODMVS IS A SINGLE MODULE AND CAN BE LINK EDITED INTO ANY LOAD
*     LIBRARY.  SUGGEST THAT MODMVS BE LNKLSTED SO EVERYONE CAN
*     ACCESS THIS MODULE.  AUTHORIZATION IS NOT REQUIRED.
*
*     THREE IN-LINE MACROS INCLUDED:
*
*         AMODE24, AMODE31, REGEQU
*
*     ATTRIBUTES: NONE
*
*     INVOKE AS A TSO COMMAND:
*
*         MODMVS MEMNAME <DDNAME>
*
*     EXAMPLE:
*
*     MODMVS ISR@PRIM ISPPLIB
*            FIND WHERE ISR@PRIM RESIDE IN THE CONCATENATION LIST OF
*            ISPPLIB
*
*     MODMVS IEFBR14
*            FIND OUT WHERE IEFBR14 IS LOCATED
*
*     MODMVS
*            DISPLAY SYNTAX FOR MODMVS
*
*     NOTE:
*
*     THERE USED TO BE A MODFIND THAT LOOKS ONLY IN THE LINKLST AREA
*     AND REPORTS THE MEMBERNAME AND LOADLIB NAME.  THIS MODFIND WORKS
*     ONLY ON XA.  SINCE WE WENT TO ESA 3.1.3, AND HENCE MODMVS WAS
*     WRITTEN.  WE ONLY HAVE THE MODFIND MODULE, NO SOURCE.
*     I WOULD APPRECIATE ANY SUGGESTION OR ADVICE ON HOW TO QUICKLY
*     LOCATE A MEMBER IN A MORE EFFICIENT WAY.  I AM USING BLDL, IF
*     YOU ADD YOUR LNKLST, LPALST, STEPLIB/ISPLLIB CONCATENATION, YOU
*     COULD HAVE 40/50 DATASETS THAT NEED TO BE OPEN/BLDL/CLOSE.
*
**********************************************************************
***********************************************************************
*
* MACRO TO SWITCH TO AMODE 24 - 4/11/91 - TOM DIEN
*
         MACRO
&N       AMODE24
&LABEL   SETC  'A24A&SYSNDX'                                   TD APR91
&BRANCH  SETC  'A24B&SYSNDX'                                   TD APR91
&LABEL  DS     0H                                              TD APR91
         LA    R1,&BRANCH                                      TD APR91
         BSM   0,R1                                            TD APR91
&BRANCH  DS    0H                                              TD APR91
         BR    R14
         MEND
***********************************************************************
*
* MACRO TO SWITCH TO AMODE 31 - 4/11/91 - TOM DIEN
*
         MACRO
&N       AMODE31
&LABEL   SETC  'A31A&SYSNDX'                                   TD APR91
&BRANCH  SETC  'A31B&SYSNDX'                                   TD APR91
&LABEL   DS    0H                                              TD APR91
         LA    R1,&BRANCH                                      TD APR91
         O     R1,=X'80000000'                                 TD APR91
         BSM   0,R1                                            TD APR91
&BRANCH  DS    0H                                              TD APR91
         BR    R14                                             TD APR91
         MEND
***********************************************************************
*
         MACRO
&N       REGEQU
&CNT     SETA  0                                               TD APR91
.NEXT    ANOP                                                  TD APR91
R&CNT    EQU  &CNT                                             TD APR91
         AIF  (&CNT EQ 15).DONE                                TD APR91
&CNT     SETA (&CNT+1)                                         TD APR91
         AGO  .NEXT                                            TD APR91
.DONE    MEND
***********************************************************************
MODMVS   CSECT
         REGEQU
MODMVS   AMODE 24
MODMVS   RMODE 24
         SAVE  (14,12)                  *                      TD APR91
         LR    R12,R15                  *                      TD APR91
         USING MODMVS,R12               *                      TD APR91
         LR    R2,R1                    *                      TD APR91
         GETMAIN R,LV=WORKLEN,SP=1      *                      TD APR91
         ST    R13,4(R1)                *                      TD APR91
         ST    R1,8(R13)                *                      TD APR91
         LR    R13,R1                   *                      TD APR91
         USING CPPL,R2                                         TD APR91
         L     R3,CPPLUPT                                      TD APR91
         L     R4,CPPLECT                                      TD APR91
         LA    R5,ECBADS                                       TD APR91
         STM   R3,R5,IOPLADS                                   TD APR91
         DROP  R2                                              TD APR91
         L     R2,0(R2)                 COMMAND BUFFER         TD APR91
         LA    R3,0                     *                      TD APR91
         LA    R4,0                     *                      TD APR91
         ICM   R3,B'0011',0(R2)         LENGTH OF TOTAL COMMAND BUFFER
         ICM   R4,B'0011',2(R2)         LENGTH OF COMMAND PLUS A BLANK
         AR    R2,R4                    POINT R2 TO BEGINNING OF PARM
         SR    R3,R4                    CMD BUF - ACTUAL COMMAND
         S     R3,=F'4'                 MINUS LENGTH WORD + EX LENGTH
         CH    R3,=H'20'                *                      TD APR91
         BH    LENBAD                   *                      TD APR91
         LTR   R3,R3                    *                      TD APR91
         BZ    LENBAD                   *                      TD APR91
         BCTR  R3,0                     *                      TD APR91
         MVC   MEMBER,H40               *                      TD APR91
         MVC   MEMBER(0),4(R2)          *                      TD APR91
         EX    R3,*-6                   *                      TD APR91
         OC    MEMBER(0),H40            *                      TD APR91
         EX    R3,*-6                   *                      TD APR91
*                                       *                      TD APR91
         OI    FLAG,NFOUND              *                      TD APR91
*                                       *                      TD APR91
         BAS   R14,CHKDDNAM             *                      TD APR91
*                                       *                      TD APR91
         BAS   R14,GETTIOT              *                      TD APR91
*                                       *                      TD APR91
         CLC   STEPLIB,ISPLLIB          *                      TD APR91
         BE    FINI                     *                      TD APR91
*                                       *                      TD APR91
         BAS   R14,GETLPA               *                      TD APR91
*                                       *                      TD APR91
         BAS   R14,GETLNK               *                      TD APR91
*                                       *                      TD APR91
FINI     DS    0H                       *                      TD APR91
         TM    FLAG,NFOUND              *                      TD APR91
         BZ    FNDSOME                  *                      TD APR91
         TPUT  MSGNFND,MSGNFNDL         *                      TD APR91
FNDSOME  L     R13,4(R13)               *                      TD APR91
         FREEMAIN R,SP=1                *                      TD APR91
         RETURN (14,12),RC=0       RETURN TO CALLER            TD APR91
***********************************************************************
* PARSING THE HARD WAY                                         TD APR91
***********************************************************************
CHKDDNAM DS    0H                       *                      TD APR91
         LA    R2,MEMBER                *                      TD APR91
         LA    R3,MEMLISTX              *                      TD APR91
         LA    R4,16                    *                      TD APR91
CHK1     CLI   0(R2),C' '               *                      TD APR91
         BE    CHK2                     *                      TD APR91
         MVC   0(1,R3),0(R2)            *                      TD APR91
         LA    R2,1(R2)                 *                      TD APR91
         LA    R3,1(R3)                 *                      TD APR91
         BCT   R4,CHK1                  *                      TD APR91
         B     DDNDONE                  *                      TD APR91
CHK2     DS    0H                       *                      TD APR91
         MVC   MSGGOODM,MEMLISTX        *                      TD APR91
         CLC   1(R2),C' '               *                      TD APR91
         BE    DDNDONE                  *                      TD APR91
         LA    R2,1(R2)                 *                      TD APR91
         LA    R3,USERLIB               *                      TD APR91
         LA    R4,8                     *                      TD APR91
MVCDDNAM DS    0H                       *                      TD APR91
         CLI   0(R2),C' '               *                      TD APR91
         BE    DDNDONE                  *                      TD APR91
         MVC   0(1,R3),0(R2)            *                      TD APR91
         LA    R2,1(R2)                 *                      TD APR91
         LA    R3,1(R3)                 *                      TD APR91
         BCT   R4,MVCDDNAM              *                      TD APR91
DDNDONE  DS    0H                       *                      TD APR91
         CLI   USERLIB+1,C'*'                                  TD APR91
         BER   R14                                             TD APR91
         MVC   STEPLIB,=C'NONELIB'                             TD APR91
         MVC   ISPLLIB,STEPLIB                                 TD APR91
         BR    R14                      *                      TD APR91
***********************************************************************
* TIOT PROCESSING                                              TD APR91
***********************************************************************
GETTIOT  DS    0H                       *                      TD APR91
         ST    R14,SAVE14                                      TD APR91
         TPUT  MSGTIOT,L'MSGTIOT                               TD APR91
         L     R2,PSATNEW-PSA           CURR TCB               TD APR91
         USING TCB,R2                                          TD APR91
         L     R2,TCBTIO                *                      TD APR91
         DROP  R2                                              TD APR91
         LA    R2,X'18'(R2)             *                      TD APR91
TIOTDD   DS    0H                       *                      TD APR91
         CLC   =F'0',0(R2)              *                      TD APR91
         BE    TIOTDONE                 *                      TD APR91
         CLC   USERLIB,4(R2)            *                      TD APR91
         BE    TIOTPROC                 *                      TD APR91
         CLC   STEPLIB,4(R2)            *                      TD APR91
         BE    TIOTPROC                 *                      TD APR91
         CLC   ISPLLIB,4(R2)            *                      TD APR91
         BE    TIOTPROC                 *                      TD APR91
         LA    R2,20(R2)                *                      TD APR91
         B     TIOTDD                   *                      TD APR91
TIOTDONE DS    0H                                              TD APR91
         L     R14,SAVE14                                      TD APR91
         BR    R14                      *                      TD APR91
TIOTPROC DS    0H                       *                      TD APR91
         LA    R3,0                     *                      TD APR91
         ICM   R3,B'0111',12(R2)        JFCB                   TD APR91
         MVC   X99DSN(44),16(R3)        DSNAME                 TD APR91
         BAS   R10,DYNAMIC              *                      TD APR91
         LA    R2,20(R2)                *                      TD APR91
         CLC   4(8,R2),=CL8' '          *                      TD APR91
         BE    TIOTPROC                 *                      TD APR91
         B     TIOTDD                   *                      TD APR91
***********************************************************************
* LPA CONCATENATED DATASETS ARE KEPT IN EXTENDED CSA           TD APR91
***********************************************************************
GETLPA   DS    0H                       *                      TD APR91
         ST    R14,SAVE14                                      TD APR91
         TPUT  MSGLPA,L'MSGLPA                                 TD APR91
         BAS   R14,AMODE31              *                      TD APR91
         USING CVTMAP,R14                                      TD APR91
         L     R14,CVTPTR                                      TD APR91
         USING CVTVSTGX,R1                                     TD APR91
         L     R1,CVTSMEXT                                     TD APR91
         L     R2,CVTEPLPS              *                      TD APR91
         DROP  R1,R14                                          TD APR91
         L     R3,4(R2)                 *                      TD APR91
         LA    R2,8(R2)                 *                      TD APR91
LPA31    MVC   X99DSN(44),1(R2)         *                      TD APR91
         BAS   R14,AMODE24              *                      TD APR91
         BAS   R10,DYNAMIC              *                      TD APR91
         BAS   R14,AMODE31              *                      TD APR91
         LA    R2,45(R2)                *                      TD APR91
         BCT   R3,LPA31                 *                      TD APR91
         BAS   R14,AMODE24              *                      TD APR91
         L     R14,SAVE14                                      TD APR91
         BR    R14                      *                      TD APR91
***********************************************************************
* LNKLST CONNCATENATED DATASETS ARE KEPT IN BELOW CSA          TD APR91
***********************************************************************
GETLNK   DS    0H                       *                      TD APR91
         ST    R14,SAVE14                                      TD APR91
         TPUT  MSGLNK,L'MSGLNK                                 TD APR91
         USING CVTMAP,R1                                       TD APR91
         L     R1,CVTPTR                *                      TD APR91
         L     R2,CVTLLTA               *                      TD APR91
         DROP  R1                                              TD APR91
*                                       *                      TD APR91
         L     R3,4(R2)                 *                      TD APR91
         LA    R2,8(R2)                 *                      TD APR91
NEXTENT  MVC   X99DSN(44),1(R2)         *                      TD APR91
         BAS   R10,DYNAMIC              *                      TD APR91
         LA    R2,45(R2)                *                      TD APR91
         BCT   R3,NEXTENT               *                      TD APR91
         L     R14,SAVE14                                      TD APR91
         L     R14,SAVE14                                      TD APR91
         BR    R14                      *                      TD APR91
*                                       *                      TD APR91
***********************************************************************
* ALLOCATE THE LIBRARY WITH DISP=SHR                           TD APR91
***********************************************************************
DYNAMIC  DS    0H                       *                      TD APR91
         MVC   X99TULNG,=X'0008'        *                      TD APR91
         LA    R1,REQPTR                *                      TD APR91
         DYNALLOC                       *                      TD APR91
         LTR   R15,R15                  *                      TD APR91
         BZ    ALLOCOK                  *                      TD APR91
         B     ALLOCERR                 *                      TD APR91
ALLOCOK  DS    0H                       *                      TD APR91
         LA    R1,XIN                   *                      TD APR91
         USING IHADCB,R1                *                      TD APR91
         MVC   DCBDDNAM(8),X99DDNAM     *                      TD APR91
         DROP  R1                       *                      TD APR91
         OPEN  (XIN,INPUT)              *                      TD APR91
         BLDL  XIN,MEMLIST              *                      TD APR91
         LTR   R15,R15                  *                      TD APR91
         BZ    FINDOK                   *                      TD APR91
         MVC   MSGGOOD+8(44),X99DSN                            TD APR91
         TPUT  MSGGOOD,MSGGOODL-8                              TD APR91
*        TPUT  X99DSN,44                *                      TD APR91
         B     FREEBYDD                 *                      TD APR91
ALLOCERR DS    0H                       *                      TD APR91
         MVC   MSGALCR(44),X99DSN       *                      TD APR91
         TPUT  MSGALCR,MSGALCRL         *                      TD APR91
         B     FREEOK                   *                      TD APR91
FINDOK   DS    0H                       *                      TD APR91
         MVC   MSGGOOD+8(44),X99DSN                            TD APR91
         TPUT  MSGGOOD,MSGGOODL         *                      TD APR91
         NI    FLAG,255-NFOUND          *                      TD APR91
FREEBYDD DS    0H                       *                      TD APR91
         CLOSE (XIN)                    *                      TD APR91
         MVC   FREEDDN,X99DDNAM         *                      TD APR91
         LA    R1,FREEPT                *                      TD APR91
         DYNALLOC                       *                      TD APR91
*        LTR   R15,R15                  *                      TD APR91
*        BZ    FREEOK                   *                      TD APR91
*        B     FREEBAD                  *                      TD APR91
FREEOK   DS    0H                       *                      TD APR91
         BR    R10                      *                      TD APR91
FREEBAD  MVC   MSGFREE(44),X99DSN       *                      TD APR91
         TPUT  MSGFREE,MSGFREEL         *                      TD APR91
         B     FINI                     *                      TD APR91
AMODE31  DS    0H                       *                      TD APR91
         AMODE31                        *                      TD APR91
AMODE24  DS    0H                       *                      TD APR91
         AMODE24                        *                      TD APR91
         LTORG                          *                      TD APR91
***********************************************************************
* ISSUE HELP MESSAGES                                          TD APR91
***********************************************************************
LENBAD   DS    0H                       *                      TD APR91
         PUTLINE PARM=PUTBLOK,OUTPUT=(MSGHELP1-8,MULTLIN,DATA),        X
               MF=(E,IOPLADS)
         B     FINI                     *                      TD APR91
ECBADS   DC    F'0'                                            TD APR91
IOPLADS  DC    4F'0'                                           TD APR91
PUTBLOK  PUTLINE MF=L                                          TD APR91
DOUBLE   DS    D                        *                      TD APR91
HEXCONV  DS    0H                      CONVERT 4 BYTES TO PRINTABLE HEX
         LA    R1,DOUBLE                *                      TD APR91
         UNPK  EBCDICEQ(9),0(5,R1)     UNPACK 4 BYTES + 1      TD APR91
         TR    EBCDICEQ,HEXTAB-240     TRANSLATE WITH HEX TABLETD APR91
         BR    R14                     DONE                    TD APR91
SAVE14   DS    F                                               TD APR91
STEPLIB  DC    CL8'STEPLIB'             *                      TD APR91
ISPLLIB  DC    CL8'ISPLLIB'             *                      TD APR91
USERLIB  DC    CL8'**'                  *                      TD APR91
ADDRTIOT DS    F                                               TD APR91
EBCDICEQ DC    CL8' ',C' '          HEX TRANSLATED AREA        TD APR91
HEXTAB   DC    C'0123456789ABCDEF'          HEX CONVERSION TABLE
*                                                              TD APR91
MEMLIST  DC    H'1',H'12'                                      TD APR91
MEMLISTX DC    CL8' '                                          TD APR91
*                                                              TD APR91
MSGTIOT  DC    CL85'MODMVS -- LOOKING IN YOUR TSO TASK I/O'    TD APR91
MSGLPA   DC    CL85'MODMVS -- LOOKING IN LPA LIST'             TD APR91
MSGLNK   DC    CL85'MODMVS -- LOOKING IN LINK LIST'            TD APR91
         DC    A(MSGHELP2-8),AL2(L'MSGHELP1+4),AL2(0)          TD APR91
MSGHELP1 DC    C'SYNTAX: MODMVS MEMBER <DDNAME>'               TD APR91
         DC    A(MSGHELP3-8),AL2(L'MSGHELP2+4),AL2(0)          TD APR91
MSGHELP2 DC C'   DDNAME=ANY VALID DDNAME ALLOCATED TO YOUR SESSION.'
         DC    A(MSGHELP4-8),AL2(L'MSGHELP3+4),AL2(0)          TD APR91
MSGHELP3 DC    C' '                                            TD APR91
         DC    A(MSGHELP5-8),AL2(L'MSGHELP4+4),AL2(0)          TD APR91
MSGHELP4 DC    C'  LOCATE A MEMBER AND FIND ANY DUPLICATION WITHIN'
         DC    A(MSGHELP6-8),AL2(L'MSGHELP5+4),AL2(0)          TD APR91
MSGHELP5 DC    C'  CONCATENATED DATASETS.'                     TD APR91
         DC    A(MSGHELP7-8),AL2(L'MSGHELP6+4),AL2(0)          TD APR91
MSGHELP6 DC    C' '                                            TD APR91
         DC    A(MSGHELP8-8),AL2(L'MSGHELP7+4),AL2(0)          TD APR91
MSGHELP7 DC    C'  WHEN ISSUE WITHOUT THE OPTIONAL DDNAME, MODMVS'
         DC    A(0),AL2(L'MSGHELP8+4),AL2(0)                   TD APR91
MSGHELP8 DC    C'  SEARCHES: STEPLIB/ISPLLIB/LPALST, THEN LNKLST.'
         DC    A(MSGHELP3),AL2(L'MSGHELP2+4),AL2(0)            TD APR91
MSGALCR  DC    CL44' ',C'DYNAMIC ALLOCATION FAILED'            TD APR91
MSGALCRL EQU   *-MSGALCR                                       TD APR91
MSGFIND  DC    CL44' ',C'FIND MACRO FAILED'                    TD APR91
MSGFINDL EQU   *-MSGFIND                                       TD APR91
MSGFREE  DC    CL44' ',C'DYNAMIC FREE OF DATASET FAILED'       TD APR91
MSGFREEL EQU   *-MSGFREE                                       TD APR91
MSGGOOD  DS    CL8,CL44                                        TD APR91
         DC    C'---> '                                        TD APR91
MSGGOODM DC    CL8' '                                          TD APR91
MSGGOODL EQU   *-MSGGOOD                                       TD APR91
MSGNFND  DC    C'-- NOT FOUND --'                              TD APR91
MSGNFNDL EQU   *-MSGNFND                                       TD APR91
MSGINFO  DC    C'MODMVS: SEQUENCE OF SEARCH FOR MODULE:'       TD APR91
         DC    C' STEPLIB / ISPLLIB / LPALIST / LNKLST'        TD APR91
MSGINFOL EQU   *-MSGINFO                                       TD APR91
FLAG     DC    X'00'                                           TD APR91
NFOUND   EQU   X'01'                                           TD APR91
MEMBER   DC    CL16' '                                         TD APR91
H40      DC    CL16' '                                         TD APR91
REQPTR   DS    0F                                              TD APR91
         DC    X'80',AL3(REQBLK)                               TD APR91
REQBLK   DS    0F                                              TD APR91
X99RBLN  DC    AL1(20) LENGTH ALWAYS 20                        TD APR91
X99VERB  DC    X'01'      DSNAME ALLOC                         TD APR91
X99FLAG1 DC    XL2'00'                                         TD APR91
X99ERROR DC    XL2'00'                                         TD APR91
X99INFO  DC    XL2'00'                                         TD APR91
X99TXTPP DC    A(X99TUPTR)                                     TD APR91
X99RESVD DC    F'0'                                            TD APR91
X99FLAG2 DC    A(0)                                            TD APR91
X99TUPTR DC    X'00',AL3(ALLOCTU1)                             TD APR91
         DC    X'00',AL3(REQTXTU2)                             TD APR91
         DC    X'80',AL3(REQTXTU3)                             TD APR91
ALLOCTU1 DS    0F                                              TD APR91
X99KEY   DC    X'0055'                                         TD APR91
X99TUNUM DC    X'0001'                                         TD APR91
X99TULNG DC    X'0008'                                         TD APR91
X99DDNAM DS    CL8                                             TD APR91
REQTXTU2 DS    0F                                              TD APR91
         DC    X'0002'                                         TD APR91
         DC    X'0001'                                         TD APR91
X99DSLEN DC    X'002C'                                         TD APR91
X99DSN   DS    CL44                                            TD APR91
REQTXTU3 DS    0F                                              TD APR91
         DC    X'0004'                                         TD APR91
         DC    X'0001'                                         TD APR91
X99DISPL DC    X'0001'                                         TD APR91
X99KEEP  DC    X'08'                                           TD APR91
         DS    0F                                              TD APR91
FREEPT   DC    X'80',AL3(FREERB)                               TD APR91
FREERB   DC    AL1(20) LENGTH ALWAYS 20                        TD APR91
FREEVB   DC    X'02'                                           TD APR91
FREEFG1  DC    XL2'00'                                         TD APR91
FREEERR  DC    XL2'00'                                         TD APR91
FREEINFO DC    XL2'00'                                         TD APR91
FREETP   DC    AL4(FREETU)                                     TD APR91
FREERSVD DC    AL4(0)                                          TD APR91
FREEFG2  DC    AL4(0)                                          TD APR91
FREETU   DC    X'80',AL3(FREETU1)                              TD APR91
FREETU1  DS    0F                                              TD APR91
         DC    X'0001'                                         TD APR91
         DC    X'0001'                                         TD APR91
         DC    X'0008'                                         TD APR91
FREEDDN  DS    CL8                                             TD APR91
XIN      DCB   DDNAME=A,MACRF=R,DSORG=PO,EODAD=FINI            TD APR91
XINL     EQU   *-XIN                                           TD APR91
WORKAREA DSECT                                                 TD APR91
         DS   18F                                              TD APR91
WORKLEN  EQU  *-WORKAREA                                       TD APR91
         IKJCPPL                                               TD APR91
         IEFZB4D0                                              TD APR91
         IEFZB4D2                                              TD APR91
         DCBD DSORG=PO                                         TD APR91
         CVT DSECT=YES                                         TD APR91
         IKJTCB DSECT=YES                                      TD APR91
         IHAPSA DSECT=YES                                      TD APR91
         DSECT                                                 TD APR91
         IEFTIOT1                                              TD APR91
         END
