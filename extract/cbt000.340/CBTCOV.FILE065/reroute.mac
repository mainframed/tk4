REROUTE  CSECT                         DEFINE CSECT
***********************************************************************
*
*    THIS ROUTINE REROUTES TO LOCAL ALL OF THE SYSOUT FOR THE EXECUTING
*    JOB.
*    IT ALSO SENDS NOTICE OF THIS ACTION TO THE ORIGINAL DESTINATION
*    VIA SYSOUT CLASS A.
*
*    REROUTE MAY OPTIONALLY BE PASSED A PARM WHICH CONTAINS A
*    DESTINATION OTHER THAN LOCAL.  THE PARM IS IGNORED IF IT IS
*    MORE THAN 8 CHARACTERS IN LENGTH.
*
***********************************************************************
*
*     REGISTER EQUATES AND USAGE
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5                       PTR TO ACCVT
R6       EQU   6                       PTR TO ACUCB
R7       EQU   7                       PTR TO PARM
R8       EQU   8                       PARM LENGTH
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                      REROUTE BASE REG
R13      EQU   13                      PTR TO SAVE AREA
R14      EQU   14                      RETURN ADDR
R15      EQU   15
*
*     HOUSEKEEPING
*
         SAVE  (14,12),,*              SAVE REGISTERS
         LR    R12,R15                 SET REG 12 TO ENTRY ADDR
         USING REROUTE,R12             DEFINE BASE REG AND ADDR
         ST    R13,SAVEAREA+4          STORE SAVE AREA ADDR
         LR    R2,R13                  ALSO STASH IT TEMPORARILY
         LA    R13,SAVEAREA            LOAD ADDR OF MY SAVE AREA
         ST    R13,8(R2)               STORE IT IN CALLING SAVE AREA
         L     R7,0(R1)                LOAD PTR TO 1ST PARM
*
*     LOCATE ACF2 UCB AND GET MY JES NUMBER
*
         USING PSA,R0                  ADDRESSABILITY FOR PSA
         ACFINCVT R5,NONE=NOACF2       GET THE ADDR OF THE ACF2 CVT
         USING ACCVT,R5                ADDRESSABILITY FOR ACCVT
         ACFGUCB R6,NONE=NOACF2        GET THE ADDR OF THE ACF2 UCB
         USING ACUCB,R6                ADDRESSABILITY FOR ACUCB
         B     HAVEJES#
NOACF2   EQU   *
         MVC   WTOMSG,MSG1             MOVE IN MESSAGE
         LA    R1,WTOSTUFF             LOAD ADDR OF WTO PARAMETER LIST
         SVC   35                      ISSUE THE WTO SVC
         B     RETURN                  AND QUIT
HAVEJES# MVC   JESNO,ACUJESID+4        MOVE IN MY JES NUMBER
         LA    R3,JESNO                FIRST BYTE OF JES NUMBER
         LA    R4,4                    THIS MANY BYTES TO CHECK
JESLOOP  CLI   0(R3),C' '              IS IT BLANK?
         BNE   NOTBLANK                NO, LEAVE IT ALONE
         MVI   0(R3),C'0'              NEEDS A LEADING ZERO
NOTBLANK LA    R3,1(R3)                POINT TO NEXT BYTE
         BCT   R4,JESLOOP              AND KEEP GOING
         MVC   COMMAND+10(4),JESNO     MOVE IN MY JES NUMBER
*
*     NOW ISSUE JES COMMAND TO REROUTE ALL OUTPUT TO DEST=LOCAL
*
         CLC   0(2,R7),=H'0'           IS PARM LENGTH ZERO?
         BE    NOPARM                  YEP, NOTHING TO DO
         CLC   0(2,R7),=H'8'           IS PARM LENGTH>8?
         BH    NOPARM                  YEP, NOTHING TO DO
         MVC   COMMAND+17(8),BLANKS    BLANK OUT DESTINATION
         MVC   RMTMSG+59(8),BLANKS     BLANK OUT DESTINATION
         MVC   MSG2+48(8),BLANKS       BLANK OUT DESTINATION
         LH    R8,0(R7)                LOAD PARM LENGTH
         BCTR  R8,0                    DECREMENT LENGTH FOR EX
         EX    R8,MOVE3                MOVE DEST TO COMMAND
         EX    R8,MOVE4                MOVE DEST TO MESSAGE
         EX    R8,MOVE5                MOVE DEST TO MESSAGE
NOPARM   EQU   *
         L     R2,16                   POINT TO CVT
         L     R2,100(R2)              POINT TO UCM BASE
         LR    R1,R2                   COPY UCM BASE PTR
         S     R1,=F'4'                BACK UP 4
         L     R1,0(R1)                LOAD PTR TO UCM PREFIX
         L     R3,0(R1)                LOAD PTR TO MASTER CONSOLE UCME
         MVC   UCMID,26(R3)            SAVE THE CONSOLE ID
         MODESET KEY=ZERO,MODE=SUP     GET AUTHORIZED
         LA    R1,CMD                  POINT TO COMMAND
         XR    R0,R0                   CLEAR R0
         IC    R0,UCMID                MOVE IN CONSOLE ID
         SVC   34                      SEND THE COMMAND
         MODESET KEY=NZERO,MODE=PROB   RELINQUISH AUTHORIZATION
         MVC   WTOMSG,MSG2             MOVE IN MESSAGE
         LA    R1,WTOSTUFF             LOAD ADDR OF WTO PARAMETER LIST
         SVC   35                      ISSUE THE WTO SVC
*
*     NOW FIGURE OUT WHERE WE CAME FROM
*
         CLI   ACUTIDNT,C'R'           DOES SOURCE BEGIN WITH R?
         BNE   ITSLOCAL                NO, MAKE IT LOCAL
         LA    R2,ACUTIDNT+2           POINT TO THIRD BYE OF SOURCE
         LA    R3,2                    COUNT OF BYTES IN REMOTE NAME
RJELOOP  CLC   0(3,R2),=C'.RD'         DOES IT LOOK LIKE RNNN.RDN ?
         BE    ITSREMOT                YEP, IT CAME FROM A REMOTE
         LA    R2,1(R2)                POINT TO NEXT BYTE
         LA    R3,1(R3)                BUMP COUNT OF BYTES
         CH    R3,=H'4'                MORE THAN 4?
         BNH   RJELOOP                 NO, KEEP LOOKING
ITSLOCAL EQU   *
         MVC   MSG4+48(8),ACUTIDNT     MOVE IN SOURCE
         MVC   WTOMSG,MSG4             MOVE IN MESSAGE
         LA    R1,WTOSTUFF             LOAD ADDR OF WTO PARAMETER LIST
         SVC   35                      ISSUE THE WTO SVC
         B     RETURN                  AND QUIT
ITSREMOT EQU   *
         STH   R3,DESTLEN              SET LENGTH OF TEXT UNIT
         BCTR  R3,0                    DECREMENT LENGTH FOR MVC
         EX    R3,MOVE1
         EX    R3,MOVE2
*
*     ALLOCATE DDNAME NOTIFY TO SYSOUT=A
*
         LA    R1,DYNAPTR              POINT TO SVC 99 RB PTR
         DYNALLOC ,                    INVOKE DYNAMIC ALLOCATION
         LTR   R15,R15                 TEST RETURN CODE
         BZ    ALOCATED
         MVC   WTOMSG,MSG5             MOVE IN MESSAGE
         LA    R1,WTOSTUFF             LOAD ADDR OF WTO PARAMETER LIST
         SVC   35                      ISSUE THE WTO SVC
         B     RETURN                  AND QUIT
ALOCATED EQU   *
*
*     SEND NOTIFICATION TO ORIGINAL DESTINATION
*
         OPEN  (OUTDCB,OUTPUT)
         MVI   RECORD,C'*'             PLANT AN ASTERISK
         MVC   RECORD+1(119),RECORD    AND PROPAGATE IT
         LA    R2,3                    WE'LL DO THIS 3 TIMES
LOOP1    PUT   OUTDCB,RECORD
         BCT   R2,LOOP1
         MVI   RECORD+3,C' '           PLANT A BLANK
         MVC   RECORD+4(113),RECORD+3  AND PROPAGATE IT
         LA    R2,5                    WE'LL DO THIS 5 TIMES
LOOP2    PUT   OUTDCB,RECORD
         BCT   R2,LOOP2
         MVC   RECORD+23(L'RMTMSG),RMTMSG
         PUT   OUTDCB,RECORD
         MVI   RECORD+3,C' '           PLANT A BLANK
         MVC   RECORD+4(113),RECORD+3  AND PROPAGATE IT
         LA    R2,5                    WE'LL DO THIS 5 TIMES
LOOP3    PUT   OUTDCB,RECORD
         BCT   R2,LOOP3
         MVI   RECORD,C'*'             PLANT AN ASTERISK
         MVC   RECORD+1(119),RECORD    AND PROPAGATE IT
         LA    R2,3                    WE'LL DO THIS 3 TIMES
LOOP4    PUT   OUTDCB,RECORD
         BCT   R2,LOOP4
         CLOSE (OUTDCB)
         MVC   WTOMSG,MSG3             MOVE IN MESSAGE
         LA    R1,WTOSTUFF             LOAD ADDR OF WTO PARAMETER LIST
         SVC   35                      ISSUE THE WTO SVC
*
*     TERMINATE
*
RETURN   L     R13,SAVEAREA+4          LOAD ADDR OF CALLING SAVE AREA
         LM    R14,R12,12(R13)         RESTORE REGISTERS
         SR    R15,R15                 SET RETURN CODE TO ZERO
         BR    R14                     RETURN
*
*     CONSTANTS AND STORAGE RESERVATIONS
*
SAVEAREA DS    18F
RECORD   DS    CL120
BLANKS   DC    CL8' '
MSG1     DC C'COULD NOT LOCATE ACF2 UCB IN REROUTE ROUTINE            '
MSG2     DC C'ALL OUTPUT FOR THIS JOB HAS BEEN ROUTED TO DEST=LOCAL   '
MSG3     DC C'NOTIFICATION OF THIS ACTION WAS SENT TO DEST=RX         '
MSG4     DC C'NO NOTIFICATION OF THIS ACTION WAS SENT, SOURCE=XXXXXXXX'
MSG5     DC C'SYSOUT DYNAMIC ALLOCATION FAILED IN REROUTE ROUTINE     '
WTOSTUFF DS    0F                      FULL WORD ALIGN
         DC    AL2(MSGLEN+4)           TEXT LENGTH+4
         DC    B'1000000000000000'     MCS FLAGS
MSGSTART EQU   *
         DC    C'>>---> '
WTOMSG   DC    CL56' '
         DC    C' <---<<'
MSGLEN   EQU   *-MSGSTART
         DC    B'0000000000000000'     DESCRIPTOR CODES
         DC    B'0000000000100000'     ROUTING FLAGS (=11)
DYNAPTR  DS    0F                  POINTER TO SVC 99 REQUEST BLOCK
         DC    AL1(128)            INDICATE LAST POINTER IN LIST
         DC    AL3(DYNARB)         ADDRESS OF SVC 99 REQUEST BLOCK
DYNARB   DS    0F
         DC    X'14'               SVC 99 REQUEST BLOCK LENGTH
         DC    X'01'               DSNAME ALLOCATION VERB CODE
         DC    X'2000'             FLAGS
ERROR    DC    H'0'                ERROR CODE
INFO     DC    H'0'                INFO CODE
         DC    A(DDNTUPTR)         POINTER TO FIRST TEXT UNIT POINTER
         DC    F'0'                RESERVED
         DC    F'0'                FLAGS2
DDNTUPTR DC    A(DDNTU)            PTR TO DDN TEXT UNIT
         DC    A(SYSOUTTU)         PTR TO SYSOUT TEXT UNIT
         DC    AL1(128)            INDICATE LAST POINTER IN LIST
         DC    AL3(DESTTU)         PTR TO DEST TEXT UNIT
DDNTU    DS    0F                  DDNAME TEXT UNIT
         DC    X'0001'             DDN TEXT UNIT KEY
         DC    H'1'                DDN NUMBER OF TEXT UNITS
         DC    H'8'                DDN TEXT UNIT TEXT LENGTH
         DC    CL8'NOTIFY  '       DDN TEXT UNIT (DDNAME)
SYSOUTTU DS    0F                  SYSOUT TEXT UNIT
         DC    X'0018'             TEXT UNIT KEY
         DC    H'1'                # OF TEXT UNITS
         DC    H'1'                LEN OF TEXT UNIT
         DC    C'A'                SYSOUT CLASS
DESTTU   DS    0F                  DEST TEXT UNIT
         DC    X'0058'             TEXT UNIT KEY
         DC    H'1'                # OF TEXT UNITS
DESTLEN  DC    H'0'                LEN OF TEXT UNIT
DEST     DC    C'CCCCC'            SYSOUT CLASS
JESNO    DS    CL4
UCMID    DS    XL1                     CURRENT CONSOLE ID
CMD      DC    H'29',H'0'          COMMAND LENGTH+4
COMMAND  DC    C'$R ALL,J=JXXXX,D=LOCAL   '
RMTMSG   DC    C'>>-----> ALL OUTPUT FOR THIS JOB HAS BEEN REROUTED TO X
               DEST=LOCAL    <-----<<'
MOVE1    MVC   DEST(0),ACUTIDNT        MOVE DEST INTO TEXT UNIT
MOVE2    MVC   MSG3+45(0),ACUTIDNT     AND INTO MESSAGE
MOVE3    MVC   COMMAND+17(0),2(R7) MOVE IN DESTINATION
MOVE4    MVC   RMTMSG+59(0),2(R7)  MOVE IN DESTINATION
MOVE5    MVC   MSG2+48(0),2(R7)    MOVE IN DESTINATION
OUTDCB   DCB   DDNAME=NOTIFY,DSORG=PS,LRECL=120,BLKSIZE=3600,RECFM=FB, X
               MACRF=PM
*
*     CONTROL BLOCK MAPS
*
         ACCVT   ,                 MAP THE ACF2 CVT
         ACFASVT ,                 MAP THE ACF2 ASVT
         ACUCB   ,                 MAP THE ACF2 UCB
         IHAPSA  ,                 MAP THE PSA
         IHAASCB ,                 MAP THE ASCB
         END   REROUTE
