UNPBYT   TITLE 'SUBROUTINE WHICH UNPACKS BYTES INTO WORDS'
*
*      THIS PROGRAM UNPACKS A NUMBER OF BYTES INTO THE SAME NUMBER
* OF WORDS. THE WORDS MAY BE LEFT-JUSTIFIED WITH TRAILING BYTES OR
* RIGHT-JUSTIFIED WITH LEADING BINARY ZEROES. UPON COMPLETION, REGISTER
* 15 CONTAINS 0 IF THE UNPACK OPERATION SUCCEEDED OR 12 IF THE UNPACK
* WAS UNSUCCESSFUL. THE CALLING SEQUENCE FOR "UNPBYT" IS AS FOLLOWS:
*           1. THE SOURCE FIELD (TO BE UNPACKED FROM)
*           2. THE TARGET FIELD (TO BE UNPACKED INTO)
*           3. THE NUMBER OF BYTES TO BE UNPACKED (4-BYTE BINARY)
*           4. THE MODE OF JUSTIFICATION (1-BYTE CHARACTER) WHERE
*                  "L" INDICATES LEFT-JUSTIFIED AND
*                  "R" INDICATES RIGHT-JUSTIFIED
*
*      THE "UNPBYT" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
UNPBYT   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(72,0)
         REGISTER                  REGISTER EQUATES
         LM    R4,R7,0(R2)         LOAD PARAMETER POINTERS
         L     R6,0(,R6)           LOAD NUMBER BYTES TO UNPACK
         LTR   R6,R6               IF NUMBER OF BYTES IS INVALID
         BNP   ERROR                  THEN FLAG FOR ERROR
         CLI   0(R7),C'R'          IF RIGHT JUSTIFICATION INDICATED
         BE    RIGHTJST               THEN PREPARE FOR IT
         CLI   0(R7),C'L'          IF NOT LEFT JUSTIFICATION  EITHER
         BNE   ERROR                  THEN FLAG FOR ERROR
         LA    R2,=CL4'    '       POINT TO A WORD OF BLANKS
         LA    R8,0                LOAD OFFSET OF A BYTE IN TARGET
         B     PARMOK              GO UNPACK THE DATA
RIGHTJST DS    0H
         LA    R2,=XL4'00000000'   POINT TO A WORD OF BINARY ZEROES
         LA    R8,3                LOAD OFFSET OF A BYTE IN TARGET
PARMOK   DS    0H
         MVC   0(4,R5),0(R2)       INITIALIZE TARGET WORD
         LA    R9,0(R8,R5)         COMPUTE TARGET BYTE ADDRESS
         MVC   0(1,R9),0(R4)       MOVE THE BYTE INTO THE TARGET
         LA    R5,4(,R5)           INCREMENT TARGET ADDRESS
         LA    R4,1(,R4)           INCREMENT SOURCE ADDRESS
         BCT   R6,PARMOK           CONTINUE IF NOT DONE
         LA    R15,0               INDICATE SUCCESSFUL OPERATION
         B     EXIT                   AND LEAVE
ERROR    DS    0H
         LA    R15,12              INDICATE UNSUCCESSFUL OPERATION
EXIT     DS    0H
         LEAVE RETCODE=(15),       RETURN TO CALLER           XXXXXXXXXX
               GETMAIN=(72,0)
         END
