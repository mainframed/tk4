AMIINT   TITLE 'SUBROUTINE WHICH OBTAINS BATCH/LOGON/STC STATUS'
*
*      THIS PROGRAM DETERMINES WHETHER THE CURRENT JOB IS A BATCH
* JOB, A STARTED TASK, OR A LOGON. ONE OF THE FOLLOWING VALUES IS
* RETURNED TO THE CALLER:
*                 0 IF BATCH JOB
*                 1 IF STARTED TASK
*                 2 IF LOGON
* "AMIINT" IS CALLED USING ONE ARGUMENT - A FULLWORD BINARY FIELD
* INTO WHICH ONE OF THE ABOVE VALUES IS STORED.
*
*      THE "AMIINT" SUBROUTINE IS REENTRANT AND REUSEABLE.
*
AMIINT   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(72,0)
         REGISTER
         USING CVT,R4              ADDRESS CVT DSECT
         USING ASCB,R5             ADDRESS ASCB DSECT
         USING TCB,R7              ADDRESS TCB DSECT
         L     R4,16               POINT TO THE CVT
         L     R5,CVTTCBP          POINT TO TCB/ASCB WORDS
         L     R5,12(,R5)          POINT TO THE CURRENT ASCB
         L     R6,ASCBJBNI         LOAD POINTER TO BATCH JOB JOBNAME
         LTR   R6,R6               IF THIS IS A BATCH JOB
         BNZ   BATCH                  THEN SET UP REGISTER 0
         L     R7,CVTTCBP          POINT TO TCB/ASCB WORDS
         L     R7,0(,R7)           POINT TO CURRENT TCB
         L     R8,TCBJSCB          POINT TO JSCB
         L     R9,264(,R8)         POINT TO PSCB
         LTR   R9,R9               IF PSCB ADDRESS IS ZERO
         BZ    STC                    THEN THIS IS A STARTED TASK
LOGON    DS    0H
         LA    R0,2                INDICATE LOGON
         B     EXIT                   AND LEAVE
STC      DS    0H
         LA    R0,1                INDICATE STARTED TASK
         B     EXIT                   AND LEAVE
BATCH    DS    0H
         LA    R0,0                INDICATE BATCH JOB
EXIT     DS    0H
         L     R2,0(,R2)           POINT TO USER'S PARAMETER
         ST    R0,0(,R2)           SAVE FLAG VALUE
         LEAVE RETCODE=0,          RETURN TO CALLER                    X
               GETMAIN=(72,0)
         EJECT
         IHAASCB
         EJECT
         IKJTCB
         EJECT
         CVT   DSECT=YES
         END
