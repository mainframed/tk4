./       ADD   NAME=PLAYBAC2
PLAYBAC2 CSECT
         SPACE
*   COPYRIGHT (C) 1980 BY SAS INSTITUTE INC., CARY NC
*
*   NAME:      PLAYBAC2
*   SUPPORT:   CRAIGE HALES
*   PRODUCT:   GRAPH
*   TYPE:      PROC
*   ATTRIB:    NORENT
*   LANGUAGE:  ASM
*   PURPOSE:   PLAYBACK A FT14F001 FILE
*   USAGE:
*
*   HISTORY:
*   FUTURE:
*   MACROS:
*   AREAS:
*   EXTREF:
*   REF:
*   NOTES: FT14F001 WAS WRITTEN WITH THE OUTPUT FOR A
*          BLOCK MODE DEVICE
*   KEYWORDS:
*   END
         SPACE
         PRINT NOGEN
         STM   14,12,12(13)        HANDSHAKE VERSION
         BALR  12,0
         USING *,12
         ST    13,SAVE+4
         LA    13,SAVE
         OPEN  (FT14F001,INPUT)    OPEN INPUT FILE
LOOP     GET   FT14F001            READ A RECORD
         LH    0,0(1)              R0 IS LENGTH FIELD
         S     0,=F'4'             MINUS VB WORD AT START
         LA    1,4(1)              R1 POINTS TO START OF DATA
         O     1,CNTL              SET HIGH BYTE FOR CONTROL TPUT
         SVC   93                  TPUT SVC
         LA    1,BUFFER            R1 POINTS TO PLACE TO PUT DATA
         LA    0,16                R0 IS LENGTH OF BUFFER FOR DATA
         O     1,TGET              SET HIGH BYTE FOR EDIT TGET
         SVC   93                  TGET SVC
         B     LOOP
EOD      CLOSE (FT14F001)          END OF FILE EXIT
         FREEPOOL FT14F001         CLEAN UP
         L     13,SAVE+4
         LM    14,12,12(13)
         SR    15,15
         BR    14
FT14F001 DCB   DSORG=PS,RECFM=VB,LRECL=256,MACRF=GL,DDNAME=FT14F001,   +
               EODAD=EOD
TGET     DC    X'80000000'
CNTL     DC    X'02000000'
SAVE     DS    18F
BUFFER   DC    CL16'****************'
         END   PLAYBAC2
