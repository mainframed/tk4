INTRDR   TITLE '-----  DISPLAY STATUS OF JES2 INTERNAL READERS  -----'
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  INTRDR                                             *
*                                                                     *
*   AUTHOR:        LEONARD D. WOREN                                   *
*                                                                     *
*   DATE:          SEPTEMBER 30, 1980                                 *
*                                                                     *
*   INSTALLATION:  HARTFIELD-ZODY'S INFORMATION RESOURCE MGT. DIV.    *
*                                                                     *
*   PURPOSE:       DISPLAY STATUS OF JES2 INTERNAL READERS            *
*                                                                     *
*   UPDATES:                                                          *
*      06/22/81 LDW1 ADD JOBNAME/USERID TO DISPLAY                    *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
INTRDR   OSENTER  EXIT=LEAVE,RC=0
         L     R1,16                    -> CVT
         L     R1,CVTJESCT-CVT(,R1)     -> JESCT
         L     R1,JESSSCT-JESCT(,R1)    -> SSCT
         L     R2,SSCTSSVT-SSCT(,R1)    -> SSVT
         USING SSVT,R2
         L     R3,$SVIRDRS              -> FIRST INTRDR DCT
         USING DCTDSECT,R3
         TPUTX TITLE1,L'TITLE1
         TPUTX TITLE2,L'TITLE2
LOOP     MVC   LINE,BLANKS
         MVC   L@NAME,DCTDEVN           DEVICE NAME
         MVC   L@JOBID,RIDJOBID         JOB ID
         TM    RIDFLAGS,RIDALLOC        ALLOCATED?
         BNO   NOTALLOC                 NO - SKIP
         MVC   L@REMARK(13),=C'* ALLOCATED *'
         L     R1,RIDASCBP              -> ASCB
         USING ASCB,R1
         HEX   L@ASID,ASCBASID,LEN=2    FORMAT ASID
         ICM   R15,B'1111',ASCBJBNI     GET PTR TO JNAME FOR INIT PGMS
         BNZ   *+8                      SKIP IF GOT IT
         L     R15,ASCBJBNS             GET PTR TO JNAME FOR S/M/L
         MVC   L@JNAME(8),0(R15)
         DROP  R1
NOTALLOC TPUTX LINE,80
         ICM   R3,B'1111',DCTCHAIN      -> NEXT DCT
         BNZ   LOOP                     CONTINUE
         B     LEAVE                    ALL DONE
BLANKS   DC    CL80' '
TITLE1   DC    C'DEVNAME   LAST JOB                 ASID  JOBNAME '
TITLE2   DC    C'--------  --------                 ----  --------'
LINE     DC    CL80' '
         ORG   LINE
L@NAME   DS    CL8
         DS    CL2
L@JOBID  DS    CL8
         DS    CL2
L@REMARK DS    C'* ALLOCATED *'
         DS    CL2
L@ASID   DS    CL4
         DS    CL2
L@JNAME  DS    CL8
         ORG   ,
HEXTAB   HEXTAB  DUAL=NO
         LTORG ,
         PRINT NOGEN
         $CVT
         $JESCT
         $SSCT
         $SVT
         $ASCB
         PRINT GEN
         $DCT
         END
