HEADIN   TITLE 'BLOCK CHARACTER TRANSLATION SUBROUTINE'
*
*      THIS PROGRAM ACCEPTS AS INPUT A CHARACTER STRING 10 BYTES IN
* LENGTH AND RETURNS AN ARRAY OF BLOCK CHARACTERS WHICH HAS 10 ROWS
* AND 132 COLUMNS (EACH BLOCK CHARACTER IS 10X10 AND IS FOLLOWED
* BY THREE COLUMNS OF BLANKS). THE CALLER CAN SPECIFY THE SYMBOL TO
* BE USED TO CONSTRUCT THE BLOCK CHARACTERS. THE CALLING SEQUENCE FOR
* "HEADIN" IS AS FOLLOWS:
*          1. THE CHARACTER TO BE USED IN THE CONSTRUCTION OF THE
*                 BLOCK CHARACTERS (1 CHARACTER)
*          2. THE INPUT CHARACTER STRING (10 CHARACTERS)
*          3. THE OUTPUT BLOCK CHARACTER ARRAY (1320 CHARACTERS)
*
*     THE FOLLOWING FORTRAN PROGRAM IS AN EXAMPLE OF HOW TO USE
* "HEADIN". IT READS A CHARACTER STRING FROM LOGICAL FILE 5, CALLS
* "HEADIN" TO CONVERT THE CHARACTER STRING INTO THE BLOCK CHARACTER
* ARRAY "ARRAY", AND PRINTS THE TEN LINES OF "ARRAY" TO LOGICAL FILE 6
* WITH AN IMPLIED DO-LOOP.
*
*              DIMENSION MSG (3)
*              DIMENSION ARRAY (330)
*              DATA C /1H1/
*              DATA L /1HX/
*              READ (5,5) MSG
*        5     FORMAT (3A4)
*              CALL HEADIN (L,MSG,ARRAY)
*              WRITE (6,10) C
*        10    FORMAT (A1)
*              WRITE (6,20) ARRAY
*        20    FORMAT (1H ,33A4)
*              RETURN
*              END
*
*      THE "HEADIN" PROGRAM IS REENTRANT AND REUSEABLE.
*
         EJECT
HEADIN   ENTER PARMREG=2,          SAVE REGISTERS AND INITIALIZE       X
               GETMAIN=(WORKSIZE,0)
         REGISTER                  REGISTER EQUATES
         USING WORKAREA,R13        ADDRESS WORK AREA DSECT
         LM    R4,R6,0(R2)         LOAD PARAMETER POINTERS
         MVC   WORKSTRG,0(R5)      MOVE INPUT STRING TO WORK FIELD
         TR    WORKSTRG,TRTABLE    TRANSLATE INPUT STRING INTO INDEXES
         MVC   (0*132)(132,R6),BLANKS   CLEAR OUTPUT AREA
         MVC   (1*132)(132,R6),BLANKS     "     "     "
         MVC   (2*132)(132,R6),BLANKS     "     "     "
         MVC   (3*132)(132,R6),BLANKS     "     "     "
         MVC   (4*132)(132,R6),BLANKS     "     "     "
         MVC   (5*132)(132,R6),BLANKS     "     "     "
         MVC   (6*132)(132,R6),BLANKS     "     "     "
         MVC   (7*132)(132,R6),BLANKS     "     "     "
         MVC   (8*132)(132,R6),BLANKS     "     "     "
         MVC   (9*132)(132,R6),BLANKS     "     "     "
         LA    R2,10               LOAD NUMBER OF INPUT CHARACTERS
         LR    R7,R6               SAVE ADDRESS OF OUTPUT AREA
         LA    R8,WORKSTRG         SAVE ADDRESS OF TRANSLATED INPUT
LOOP1    DS    0H
         LA    R9,0                CLEAR WORK REGISTER
         IC    R9,0(,R8)           INSERT INDEX INTO BLOCK CHAR TABLE
         MH    R9,=H'100'          COMPUTE OFFSET IN BLOCK CHAR TABLE
         LA    R10,10              LOAD NUMBER OF ROWS IN BLOCK CHAR
         LR    R11,R7              SAVE ADDRESS OF OUTPUT AREA
LOOP2    DS    0H
         LA    R12,BLOKCHAR        POINT TO START OF BLOCK CHAR TABLE
         AR    R12,R9              COMPUTE START OF DESIRED BLOCK CHAR
         MVC   0(10,R11),0(R12)    MOVE IN ONE ROW OF BLOCK CHAR
         LA    R11,132(,R11)       INCREMENT TO NEXT OUTPUT ROW
         LA    R9,10(,R9)          INCREMENT TO NEXT ROW IN TABLE
         BCT   R10,LOOP2           GO MOVE THE NEXT ROW
LOOP2END DS    0H
         LA    R8,1(,R8)           POINT TO NEXT INDEX INTO TABLE
         LA    R7,13(,R7)          POINT TO NEXT OUTPUT POSITION
         BCT   R2,LOOP1            GO DO ANOTHER BLOCK CHARACTER
LOOP1END DS    0H
         CLI   0(R4),C'*'          IF THE USER SYMBOL IS THE DEFAULT
         BE    EXIT                   THEN LEAVE
         LOAD  EP=REPBYT           GET ROUTINE TO REPLACE DEFAULT CHAR
         LR    R15,R0              SAVE "REPBYT" ENTRY POINT ADDRESS
         MVC   CALLIST,CCALLST     INITIALIZE CALL PARAMETER LIST
         CALL  (15),               CALL "REPBYT", CHANGING EVERY       X
               ((R6),                   ASTERISK IN THE OUTPUT AREA    X
               =F'1320',                INTO THE CHARACTER SPECIFIED   X
               =C'*',                   BY THE CALLER                  X
               (R4)),                                                  X
               MF=(E,CALLIST)
EXIT     DS    0H
         LEAVE RETCODE=(15),       RESTORE REGISTERS, RETURN TO CALLER X
               GETMAIN=(WORKSIZE,0)
         EJECT
TRTABLE  DS    0CL256              TRANSLATE TABLE WHICH CONTAINS THE
*                                    INDEXES INTO THE BLOCK CHARACTER
*                                    TABLE
         SPACE 1 ×.0.1.2.3.4.5.6.7.8.9.A.B.C.D.E.F×
         DC     X'00000000000000000000000000000000'     X'00'-X'0F'
         DC     X'00000000000000000000000000000000'     X'10'-X'1F'
         DC     X'00000000000000000000000000000000'     X'20'-X'2F'
         DC     X'00000000000000000000000000000000'     X'30'-X'3F'
         DC     X'00000000000000000000010203040506'     X'40'-X'4F'
         DC     X'0700000000000000000008090A0B0C0D'     X'50'-X'5F'
         DC     X'0E0F0000000000000000101112131415'     X'60'-X'6F'
         DC     X'00000000000000000000161718191A1B'     X'70'-X'7F'
         DC     X'00000000000000000000000000000000'     X'80'-X'8F'
         DC     X'00000000000000000000000000000000'     X'90'-X'9F'
         DC     X'00000000000000000000000000000000'     X'A0'-X'AF'
         DC     X'00000000000000000000000000000000'     X'B0'-X'BF'
         DC     X'001C1D1E1F2021222324000000000000'     X'C0'-X'CF'
         DC     X'0025262728292A2B2C2D000000000000'     X'D0'-X'DF'
         DC     X'00002E2F303132333435000000000000'     X'E0'-X'EF'
         DC     X'363738393A3B3C3D3E3F000000000000'     X'F0'-X'FF'
         SPACE 2
BLANKS   DC    CL132' '
         SPACE
CCALLST  CALL  ,(0,0,0,0),MF=L  MODEL PARAMETER LIST FOR CALL MACRO
CCALLEN  EQU   *-CCALLST        COMPUTED CALL PARAMETER LIST LENGTH
         SPACE
         LTORG
         EJECT
BLOKCHAR DS    0C          BEGINNING OF BLOCK CHARACTER TABLE
         SPACE 2
X40      DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         SPACE
X4A      DC    C'    **    '
         DC    C' ******** '
         DC    C'**********'
         DC    C'**  **   *'
         DC    C'**  **    '
         DC    C'**  **    '
         DC    C'**  **   *'
         DC    C'**********'
         DC    C' ******** '
         DC    C'    **    '
         SPACE
X4B      DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'   ****   '
         DC    C'  ******  '
         DC    C'   ****   '
         SPACE
X4C      DC    C'        **'
         DC    C'      **  '
         DC    C'    **    '
         DC    C'  **      '
         DC    C'**        '
         DC    C'**        '
         DC    C'  **      '
         DC    C'    **    '
         DC    C'      **  '
         DC    C'        **'
         EJECT
X4D      DC    C'      *   '
         DC    C'    **    '
         DC    C'   **     '
         DC    C'  **      '
         DC    C'  **      '
         DC    C'  **      '
         DC    C'  **      '
         DC    C'   **     '
         DC    C'    **    '
         DC    C'      *   '
         SPACE
X4E      DC    C'          '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C' ******** '
         DC    C' ******** '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'          '
         SPACE
X4F      DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         SPACE
X50      DC    C'    **    '
         DC    C'   ****   '
         DC    C'   ****   '
         DC    C'    **    '
         DC    C'  ** **   '
         DC    C'**   ** **'
         DC    C'**    *** '
         DC    C'**    *** '
         DC    C' ****** **'
         DC    C'  ****   *'
         EJECT
X5A      DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'          '
         DC    C'    **    '
         DC    C'    **    '
         SPACE
X5B      DC    C'    **    '
         DC    C' ******** '
         DC    C'**********'
         DC    C'**  **    '
         DC    C'********* '
         DC    C' *********'
         DC    C'    **  **'
         DC    C'**********'
         DC    C' ******** '
         DC    C'    **    '
         SPACE
X5C      DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'  *    *  '
         DC    C'   *  *   '
         DC    C'* ****** *'
         DC    C'   *  *   '
         DC    C'  *    *  '
         DC    C'          '
         DC    C'          '
         SPACE
X5D      DC    C'   *      '
         DC    C'    **    '
         DC    C'     **   '
         DC    C'      **  '
         DC    C'      **  '
         DC    C'      **  '
         DC    C'      **  '
         DC    C'     **   '
         DC    C'    **    '
         DC    C'   *      '
         EJECT
X5E      DC    C'          '
         DC    C'          '
         DC    C'    **    '
         DC    C'   ****   '
         DC    C'    **    '
         DC    C'          '
         DC    C'    **    '
         DC    C'   ****   '
         DC    C'    **    '
         DC    C'   *      '
         SPACE
X5F      DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'**********'
         DC    C'**********'
         DC    C'        **'
         DC    C'        **'
         DC    C'          '
         DC    C'          '
         SPACE
X60      DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C' *******  '
         DC    C' *******  '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         SPACE
X61      DC    C'         *'
         DC    C'        **'
         DC    C'       ** '
         DC    C'      **  '
         DC    C'     **   '
         DC    C'    **    '
         DC    C'   **     '
         DC    C'  **      '
         DC    C' **       '
         DC    C'*         '
         EJECT
X6A      DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'          '
         DC    C'          '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         SPACE
X6B      DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'    **    '
         DC    C'   ****   '
         DC    C'    **    '
         DC    C'   *      '
         SPACE
X6C      DC    C' **      *'
         DC    C'****    **'
         DC    C' **    ** '
         DC    C'      **  '
         DC    C'     **   '
         DC    C'    **    '
         DC    C'   **     '
         DC    C'  **   ** '
         DC    C' **   ****'
         DC    C'*      ** '
         SPACE
X6D      DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'**********'
         DC    C'**********'
         EJECT
X6E      DC    C'**        '
         DC    C'  **      '
         DC    C'    **    '
         DC    C'      **  '
         DC    C'        **'
         DC    C'        **'
         DC    C'      **  '
         DC    C'    **    '
         DC    C'  **      '
         DC    C'**        '
         SPACE
X6F      DC    C'   ****   '
         DC    C' ******** '
         DC    C'**      **'
         DC    C'*      ** '
         DC    C'     **   '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'          '
         DC    C'    **    '
         DC    C'    **    '
         SPACE
X7A      DC    C'          '
         DC    C'          '
         DC    C'    **    '
         DC    C'   ****   '
         DC    C'    **    '
         DC    C'          '
         DC    C'    **    '
         DC    C'   ****   '
         DC    C'    **    '
         DC    C'          '
         SPACE
X7B      DC    C'          '
         DC    C'   *  *   '
         DC    C'   *  *   '
         DC    C' ******** '
         DC    C'   *  *   '
         DC    C'   *  *   '
         DC    C' ******** '
         DC    C'   *  *   '
         DC    C'   *  *   '
         DC    C'          '
         EJECT
X7C      DC    C'  ******* '
         DC    C' *********'
         DC    C'**      **'
         DC    C'**  ******'
         DC    C'**  **  **'
         DC    C'**  **  **'
         DC    C'**  ******'
         DC    C'**        '
         DC    C' *********'
         DC    C'  ******* '
         SPACE
X7D      DC    C'   *****  '
         DC    C'   *****  '
         DC    C'   *****  '
         DC    C'    ***   '
         DC    C'    ***   '
         DC    C'     *    '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         SPACE
X7E      DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'**********'
         DC    C'**********'
         DC    C'          '
         DC    C'**********'
         DC    C'**********'
         DC    C'          '
         DC    C'          '
         SPACE
X7F      DC    C'  **  **  '
         DC    C'  **  **  '
         DC    C'  **  **  '
         DC    C'  **  **  '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         DC    C'          '
         EJECT
XC1      DC    C' ******** '
         DC    C'**********'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**********'
         DC    C'**********'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         SPACE
XC2      DC    C'********* '
         DC    C'**********'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'********* '
         DC    C'********* '
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**********'
         DC    C'********* '
         SPACE
XC3      DC    C' ******** '
         DC    C'**********'
         DC    C'**       *'
         DC    C'**       *'
         DC    C'**        '
         DC    C'**        '
         DC    C'**       *'
         DC    C'**       *'
         DC    C'**********'
         DC    C' ******** '
         SPACE
XC4      DC    C'********* '
         DC    C'**********'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**********'
         DC    C'********* '
         EJECT
XC5      DC    C'**********'
         DC    C'**********'
         DC    C'**        '
         DC    C'**        '
         DC    C'*******   '
         DC    C'*******   '
         DC    C'**        '
         DC    C'**        '
         DC    C'**********'
         DC    C'**********'
         SPACE
XC6      DC    C'**********'
         DC    C'**********'
         DC    C'**        '
         DC    C'**        '
         DC    C'*******   '
         DC    C'*******   '
         DC    C'**        '
         DC    C'**        '
         DC    C'**        '
         DC    C'**        '
         SPACE
XC7      DC    C' ******** '
         DC    C'**********'
         DC    C'**        '
         DC    C'**        '
         DC    C'**        '
         DC    C'**    ****'
         DC    C'**    ****'
         DC    C'**      **'
         DC    C'**********'
         DC    C' *********'
         SPACE
XC8      DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**********'
         DC    C'**********'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         EJECT
XC9      DC    C'**********'
         DC    C'**********'
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'**********'
         DC    C'**********'
         SPACE
XD1      DC    C'  ********'
         DC    C'  ********'
         DC    C'     **   '
         DC    C'     **   '
         DC    C'     **   '
         DC    C'     **   '
         DC    C'**   **   '
         DC    C'**   **   '
         DC    C'*******   '
         DC    C' *****    '
         SPACE
XD2      DC    C'**      **'
         DC    C'**     ** '
         DC    C'**   **   '
         DC    C'** **     '
         DC    C'*****     '
         DC    C'**  **    '
         DC    C'**   **   '
         DC    C'**    **  '
         DC    C'**     ** '
         DC    C'**      **'
         SPACE
XD3      DC    C'**        '
         DC    C'**        '
         DC    C'**        '
         DC    C'**        '
         DC    C'**        '
         DC    C'**        '
         DC    C'**        '
         DC    C'**        '
         DC    C'**********'
         DC    C'**********'
         EJECT
XD4      DC    C'**      **'
         DC    C'***    ***'
         DC    C'****  ****'
         DC    C'** **** **'
         DC    C'**  **  **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         SPACE
XD5      DC    C'**      **'
         DC    C'***     **'
         DC    C'****    **'
         DC    C'** **   **'
         DC    C'**  **  **'
         DC    C'**  **  **'
         DC    C'**   ** **'
         DC    C'**    ****'
         DC    C'**     ***'
         DC    C'**      **'
         SPACE
XD6      DC    C' ******** '
         DC    C'**********'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**********'
         DC    C' ******** '
         SPACE
XD7      DC    C'********* '
         DC    C'**********'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**********'
         DC    C'********* '
         DC    C'**        '
         DC    C'**        '
         DC    C'**        '
         DC    C'**        '
         EJECT
XD8      DC    C' ******** '
         DC    C'**********'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**  *** **'
         DC    C'**    ****'
         DC    C'********* '
         DC    C' ****** **'
         SPACE
XD9      DC    C'********* '
         DC    C'**********'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**********'
         DC    C'********* '
         DC    C'**   **   '
         DC    C'**    **  '
         DC    C'**     ** '
         DC    C'**      **'
         SPACE
XE2      DC    C' ******** '
         DC    C'**********'
         DC    C'**       *'
         DC    C'**        '
         DC    C'********* '
         DC    C' *********'
         DC    C'        **'
         DC    C'*       **'
         DC    C'**********'
         DC    C' ******** '
         SPACE
XE3      DC    C'**********'
         DC    C'**********'
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         EJECT
XE4      DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**********'
         DC    C' ******** '
         SPACE
XE5      DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C' **    ** '
         DC    C'  **  **  '
         DC    C'   ****   '
         DC    C'    **    '
         SPACE
XE6      DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**  **  **'
         DC    C'**  **  **'
         DC    C'** **** **'
         DC    C'***    ***'
         DC    C' **    ** '
         SPACE
XE7      DC    C'**      **'
         DC    C' **    ** '
         DC    C'  **  **  '
         DC    C'   ****   '
         DC    C'    **    '
         DC    C'   ****   '
         DC    C'  **  **  '
         DC    C' **    ** '
         DC    C'**      **'
         DC    C'**      **'
         EJECT
XE8      DC    C'**      **'
         DC    C' **    ** '
         DC    C'  **  **  '
         DC    C'   ****   '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         SPACE
XE9      DC    C'**********'
         DC    C'**********'
         DC    C'       ** '
         DC    C'      **  '
         DC    C'     **   '
         DC    C'   **     '
         DC    C'  **      '
         DC    C' **       '
         DC    C'**********'
         DC    C'**********'
         SPACE
XF0      DC    C'   ****   '
         DC    C'  ******  '
         DC    C' **    ** '
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**      **'
         DC    C' **    ** '
         DC    C'  ******  '
         DC    C'   ****   '
         SPACE
XF1      DC    C'   ***    '
         DC    C'  ****    '
         DC    C' ** **    '
         DC    C'*   **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'    **    '
         DC    C'**********'
         DC    C'**********'
         EJECT
XF2      DC    C' ******** '
         DC    C'**********'
         DC    C'*       **'
         DC    C'        **'
         DC    C'       ** '
         DC    C'     **   '
         DC    C'   **     '
         DC    C' **       '
         DC    C'**********'
         DC    C'**********'
         SPACE
XF3      DC    C'**********'
         DC    C'********* '
         DC    C'      **  '
         DC    C'    **    '
         DC    C'   ***    '
         DC    C'     **   '
         DC    C'       ** '
         DC    C'*       **'
         DC    C'********* '
         DC    C' *******  '
         SPACE
XF4      DC    C'     ***  '
         DC    C'    ****  '
         DC    C'   ** **  '
         DC    C'  **  **  '
         DC    C' **   **  '
         DC    C'**********'
         DC    C'**********'
         DC    C'      **  '
         DC    C'      **  '
         DC    C'      **  '
         SPACE
XF5      DC    C'**********'
         DC    C'**********'
         DC    C'**        '
         DC    C'**        '
         DC    C'********* '
         DC    C'**********'
         DC    C'        **'
         DC    C'*       **'
         DC    C'**********'
         DC    C' ******** '
         EJECT
XF6      DC    C' ******** '
         DC    C'**********'
         DC    C'**      **'
         DC    C'**        '
         DC    C'********* '
         DC    C'**********'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**********'
         DC    C' ******** '
         SPACE
XF7      DC    C'**********'
         DC    C'**********'
         DC    C'        **'
         DC    C'       ** '
         DC    C'      **  '
         DC    C'    **    '
         DC    C'   **     '
         DC    C'  **      '
         DC    C' **       '
         DC    C'**        '
         SPACE
XF8      DC    C' ******** '
         DC    C'**********'
         DC    C'**      **'
         DC    C'**      **'
         DC    C' ******** '
         DC    C' ******** '
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**********'
         DC    C' ******** '
         SPACE
XF9      DC    C' ******** '
         DC    C'**********'
         DC    C'**      **'
         DC    C'**      **'
         DC    C'**********'
         DC    C' *********'
         DC    C'        **'
         DC    C'*       **'
         DC    C'**********'
         DC    C' ******** '
         EJECT
WORKAREA DSECT ,                   MAP OF GETMAINED WORK AREA
         DS    18A                 REGISTER SAVE AREA
CALLIST  DS    CL(CCALLEN)         PARAMETER LIST FOR CALL
WORKSTRG DS    CL10                INPUT STRING WORK AREA
WORKSIZE EQU   *-WORKAREA          COMPUTED SIZE OF WORK AREA
         END
