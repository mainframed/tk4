         MACRO
         IGGCP17  &OPTCD=
.*          RELEASE 23 DELETIONS/CHANGES
.*          RELEASE 22 DELETIONS/CHANGES
.*          RELEASE 21 DELETIONS/CHANGES
* CP17   CHANGES THE KEY IN A RECORD OR OVERFLOW TRACK INDEX ENTRY OR
*        IN A HIGHER LEVEL INDEX ENTRY
CK1      CCW   SIDEQ,0,CC,5              SEARCH FOR LAST INDEX ENTRY
CK2      CCW   TIC,CK1-CB1,0,0
CK3      CCW   RDATA,CK8-CB1,CC,10          READ DATA OF LAST ENTRY
CK30     CCW   NOP,0,CC+SILI,1
CK4      CCW   SIDEQ,0,CC,5              SEARCH FOR ENTRY AGAIN
CK5      CCW   TIC,CK4-CB1,0,0
CK6      CCW   WKD,0,DC,0               WRITE NEW HIGH KEY AND REWRITE
         AIF   ('&OPTCD' NE 'W' AND '&OPTCD' NE 'VW').SWC
CK7      CCW   X'00',CK8-CB1,CC,10      DATA 0F ENTRY
         AGO   .TWC
.SWC     ANOP
CK7      CCW   X'00',CK8-CB1,0,10       DATA OF ENTRY
.TWC     ANOP
SZ17     EQU   (CK7+8-CK1)+16            L'(CK1 - CK7) + L'(CK8 - CK9)
         AIF   ('&OPTCD' NE 'W' AND '&OPTCD' NE 'VW').CKNOP
* 17W    WRITE VALIDITY CHECK
CK70     CCW   NOP,0,CC+SILI,1
CK7A     CCW   SIDEQ,0,CC,5            SEARCH FOR UPDATED ENTRY
CK7B     CCW   TIC,CK7A-CB1,0,0
CK7C     CCW   RKD,0,SKIP,0            READ IT BACK
SZ17W    EQU   CK7C+8-CK70+SZ17     L'(CK70 - CK7C) + SZ17
.CKNOP   ANOP
CK8      DC    2F'00'                   M B B C C H H R
CK9      DC    2F'00'                   F P _ _ _ _ _ _
         MEND
