* /*                                                                 */
* /* MACCOMP Y-2 SC1DE/SJD61                                         */
* /* MACSTAT Y-2 73103/Y02113                                        */
* /*                                                                 */
         MACRO
&NAME    IDAPFMT  &RPL=,&TYPE=
         AIF   ('&RPL' NE '').OKY
         IHBERMAC 24
         MEXIT
.OKY     ANOP
&NAME    DS    0H
         AIF   ('&RPL'(1,1) EQ '(').RPLR
         LA    1,&RPL                   LOAD RPL ADDRESS
         AGO   .RPLSET
.RPLR    AIF   ('&RPL' EQ '(1)').RPLSET
         LR    1,&RPL(1)                LOAD RPL ADDRESS INTO REG. 1
.RPLSET  ANOP
         AIF   ('&TYPE' EQ 'INDEX').INDEX
         LA    0,8                      INDICATE A DATA PREFORMAT
         AGO   .TYPESET
.INDEX   ANOP
         LA    0,9                      INDICATE AN INDEX PREFORMAT
.TYPESET L     15,24(1)                 LOAD ACB ADDRESS FROM RPL
         L     15,8(15)                 LOAD VSAM INTERFACE RTN ADDR
         BALR  14,15                    LINK TO INTERFACE ROUTINE
         MEND
