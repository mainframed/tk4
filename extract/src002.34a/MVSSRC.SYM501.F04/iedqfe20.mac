 TITLE 'IEDQFE20 LINE I/O TRACE DUMP'
IEDQFE20 CSECT
*A292000,320000                                                  S21903
*C360000,376000,392000,404000,416000,472000,508000,552000,580000 S21903
*C676000,680000,688000,696000,744000,816000                      S21903
*D332000-340000,972000                                           S21903
*A292000                                                        SA68701
*C804000-812000                                                 SA68701
*C864000                                                        SA68667
*A314000,925000-926000                                           X02004
*A234000,238000,242000,246000,816000-832000,861000-863000        Y02027
*A924300-924600,977000-978000                                    Y02027
*C476000,612000,708000                                           Y02027
*D280000,540000-600000,816000-836000                             Y02027
*C376000,404000                                                @SA69974
*A408000                                                       @SA69974
*A636000,660000,856000                                         @SA69981
*D664000-674300,532000,764000-768000                           @SA69984
*D788000-792000,800000-810000,524000-528000                    @SA69985
*C512000                                                       @SA69983
*D572000,680000-692000,772000,868000-872000                    @ZA02069
*C688000                                                       @ZA02063
*A638500                                                       @SA73359
*C884000                                                       @SA73359
*A468000                                                       @ZA01160
*D660600-663000                                                @ZA02609
*D858000                                                       @ZA02609
*A268000,380000                                                @XA09811
*D636500-639500                                                @XA09811
*D368000-380000,404000-412000                                  @OZ18768
*C136000,464000                                                @OZ30604
***********************************************************************
*                                                                     *
*MODULE NAME--IEDQFE20 LINE I/O TRACE DUMP                            *
*                                                                     *
*STATUS CHANGE LEVEL 0                                                *
*                                                                     *
*FUNCTION                                                             *
*   IEDQFE20 IS USED TO WRITE THE TCAM LINE I/O TRACE TABLE ONTO A    *
*   SEQUENTIAL DATA SET USING THE COMWRITE ROUTINE.                   *
*   UPON ENTRY FROM IEDQC6 THIS ROUTINE CHECKS TO SEE IF THIS IS A    *
*   LOAD OR DELETE FUNCTION.IF A LOAD IS REQUESTED THE FLAGS AT       *
*   AVTAFE20 ARE TESTED. IF THEY ARE OFF,THIS ROUTINE STORES ITS ENTRY*
*   POINT ADDRESS INTO AVTAFE20 AND SETS THE FLAGS ON,INDICATING THAT *
*   THE ROUTINE IS ACTIVE.THE ROUTINE THEN RETURNS TO IEDQC6 WITH A   *
*   RETURN CODE OF ZERO. IF THE FLAGS ARE ALREADY ON RETURN IS TO     *
*   IEDQC6 WITH A RETURN CODE OF FOUR.IF A DELETE FUNCTION IS         *
*   REQUESTED THE FLAGS IN AVTAFE20 ARE SET OFF INDICATING THAT THE   *
*   ROUTINE IS NOT ACTIVE.                                            *
*   MODULE IGG019Q0 TESTS THE FLAGS AT AVTAFE20 EACH TIME THE I/O     *
*   TRACE TABLE IS HALF FULL AND FULL.IF THE FLAGS ARE ON IGG019Q0    *
*   BRANCHS TO THIS ROUTINE.                                          *
*   UPON ENTRY FROM IGG019Q0 REGISTERS ARE SAVED.A BLOCK COUNT IS     *
*   UPDATED AND STORED INTO BYTES THREE AND FOUR OF THE FIRST ENTRY   *
*   IN THE I/O TRACE TABLE. THE COMWRITE FLAGS ARE TESTED TO SEE IF   *
*   COMWRITE HAS WRITTEN THE TABLE FROM ANY PREVIOUS REQUESTS. THE ID *
*   CHARACTERS I AND O ARE PUT INTO BYTES TWO AND TEN RESPECTIVELY OF *
*   THE FIRST ENTRY. THE PARMLIST IS SET TO POINT TO THE PROPER HALF  *
*   OF THE TABLE AND THE COMWRITE ECB IS POSTED VIA SVC 102 TO HAVE   *
*   THE TABLE WRITTEN TO THE COMWRITE DATASET.                        *
*                                                                     *
*ENTRY POINT IEDQFE20
*                                                                     *
*EXTERNAL ROUTINES-SUPERVISOR BRANCH ENTRY TO POST FOR COMWRITE ECB.  *
*                                                                     *
*INPUT I/O TRACE TABLE                                         @OZ30604
*      REGISTER 1 CONTAINS COUNT OF HALF-TABLES THAT WILL BE   @OZ30604
*      MISSING IN THE COMWRITE OUTPUT.                         @OZ30604
*                                                                     *
*OUTPUT ADDRESS OF A PARM LIST STORED IN AVT AT SYMBOL AVTCWPM1       *
*       OR AVTCWPM2. THIS PARM LIST IS USED BY COMWRITE TO WRITE OUT  *
*       THE I/O TRACE TABLE.                                          *
*                                                                     *
*EXITS  NORMAL BR 14 TO IEDQC6 WITH R15=0.                            *
*       ERROR  BR 14 TO IEDQC6 WITH R15= RETURN CODE.                 *
*                                                                     *
*TABLES AND WORK AREAS - I/O TRACE TABLE.                             *
*                                                                     *
*ATTRIBUTES SERIALLY REUSABLE                                         *
*                                                                     *
***********************************************************************
 EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
RCCODE   EQU   10                       POST COMPLETION CODE     Y02027
R11      EQU   11
RECB     EQU   11                       ECB ADDR FOR XMPOST RTN  Y02027
R12      EQU   12
RERRET   EQU   12                       ADDR OF POST ERR RTN     Y02027
R13      EQU   13
RASCB    EQU   13                       ASCB ADDR FOR XMPOST     Y02027
R14      EQU   14
R15      EQU   15
RTCTL    EQU   6                   ADDRESS OF I/O TRACE CONTROL WORDS
RAVT     EQU   11                  ADDRESS OF AVT
RWK1     EQU   2
RWK2     EQU   3
ONE      EQU   1                        ONE                    @XA09811
THREE    EQU   3                        THREE                  @XA09811
DISPADDR EQU   240                 DISPLACMENT OF DISPATCHER IN CVT
ATRACE   EQU   372                 DISP OF I/O TRACE TABLE IN AVT
ERRRET   EQU   X'04'               ERROR RETURN CODE             S21903
ENTRYSIZ EQU   X'10'                    TRACE ENTRY SIZE         S21903
POSTED   EQU   X'40'               CHECK FOR ECB POSTED          S21903
I        EQU   X'C9'               CHARACTER I                  SA68701
O        EQU   X'D6'               CHARACTER O                  SA68701
         EJECT
         USING IEDQAVTD,RAVT
         USING IEDQIOTC,RTCTL
         DC    A(ENDFE20-IEDQFE20)      LENGTH OF MODULE         X02004
         USING *,R15
START    EQU   *
IEDQFE20 IEDHJN ID                 MODULE IDENTIFIER             S21903
         STM   R14,R12,12(R13)     SAVE REGISTERS
         ST    R13,SAVE+4          SAVE ADDR OF SAVE AREA
         USING START,R12
         DROP  R15
         LR    R12,R15
         L     RAVT,16                 GET ADDR OF CVT
         L     RAVT,AVTCVTPT(,RAVT) DISPATCHER LOAD LIST         S21903
         L     RAVT,0(RAVT)            GET ADDRESS OF AVT
         NC    AVTRACE+ONE(THREE),AVTRACE+ONE TABLE SPECIFIED  @XA09811
         BNZ   TBLOK                    BRANCH ON YES          @XA09811
         LA    R15,NOTBLMSG             ERROR MESSAGE ADDRESS  @XA09811
         LCR   R15,R15                  INDICATE USER MSG      @XA09811
         B     RETURN                   RETURN                 @XA09811
TBLOK    EQU   *                                               @XA09811
         LA    R7,SET              ADDR OF ENTRY POINT
         ST    R7,AVTAFE20         SET ENTRY POINTER IN AVT
         MVI   AVTAFE20,AVTEFF     SET LOAD FLAG                 S21903
         SR    R15,R15             SET RETURN CODE
         B     RETURN
ERROR    LA    R15,ERRRET          SET BAD RETURN CODE           S21903
RETURN   L     R13,SAVE+4          GET SAVE AREA ADDR
         L     R14,12(,R13)        RESTORE USERS REGISTERS
         LM    R0,R12,20(R13)
         BR    14                  RETURN TO ROUTER
         DROP  R12
         USING SET,R9
*                                                                     *
*NOTE-                                                                *
*     THE FOLLOWING INSTRUCTION IS CHANGED TO A NOP AFTER FIRST TIME. *
*                                                                     *
SET      B     SETUP               SET UP FIRST TIME THROUGH
         STM   R0,R15,SAVE
SET2     L     RAVT,AVTSAVE
         L     RTCTL,AVTRACE       GET TRACE TABLE ADDRESS       S21903
         L     RWK1,COUNT          TRACE NUMBER
         LA    RWK1,1(R1,RWK1)          OF TIMES TABLE         @OZ30604
         ST    RWK1,COUNT          HAS BEEN USED.
         LA    R14,RETURNIO             SET SECOND BASE        @ZA01160
         TM    PARMFLAG,POSTED     HAS COMWRITE WRITTEN TABLE    S21903
         BZ    RETURNIO                 NO, RETURN TO CALLER     Y02027
         TS    AVTCWTS1            PARM POINTER AVAILABLE
         BC    4,CKTS2             NO BRANCH
         LA    RWK1,PARMLIST       LOAD ADDRESS OF COMWRITE PARMS
         ST    RWK1,AVTCWPM1       STORE ADDRESS IN AVT PARM POINTER
         LA    RWK1,AVTCWEC1       GET COMWRITE ECB ADDRESS
         B     POST                GO TO SET UP TO WRITE
WRTEND   EQU   *
         NI    IOTCFLAG,AVTEFF-IOTCFEND RESET BIT                S21903
*                                  FIRST HALF OF THE TABLE.
         L     RWK2,IOTCHALF       BEGIN ADDRESS OF TABLE TO BE WRITEN
         B     POSTECB
SETUP    EQU   *
         STM   R0,R15,SAVE
         LA    RTCTL,CVTPTR             PUT ADDRESS OF           Y02027
         L     RTCTL,0(RTCTL)      CVT IN REG RCTCL
         L     RTCTL,DISPADDR(RTCTL) GET ADDRESS OF DISPATCHER
         L     RTCTL,0(RTCTL)      GET AVT ADDRESS
         LR    RAVT,RTCTL          INITIALIZE RAVT
         ST    RAVT,AVTSAVE
         L     RTCTL,AVTRACE       GET TRACE TABLE ADDRESS       S21903
         L     RWK1,IOTCSTRT       START OF TABLE
         LA    RWK1,0(RWK1)        CLEAR HIGH ORDER BYTE
         L     RWK2,IOTCHALF       MID POINT OF TABLE
         LA    RWK2,0(RWK2)        CLEAR HIGH ORDER BYTE
         SR    RWK2,RWK1           FIND LENGTH
         STH   RWK2,PARMLEN             STORE LENGTH INTO PARAM LIST
 SPACE
         NI    PARMFLAG,AVTEFF-NOPREFIX INDICATE PREFIX          S21903
NOP      MVI   SET+1,AVTEZERO      INDICATE FIRST ENTRY DONE     S21903
         B     SET2
CKTS2    TS    AVTCWTS2            PARM POINTER AVAILABLE
         BNZ   RETURNIO                 NO, RETURN TO CALLER     Y02027
         LA    RWK1,PARMLIST       LOAD ADDRESS OF COMWRITE PARMS
         ST    RWK1,AVTCWPM2       STORE ADDRESS IN AVT PARM POINTER
         LA    RWK1,AVTCWEC2       GET COMWRITE ECB ADDRESS
POST     EQU   *
         TM    IOTCFLAG,IOTCFMID   IS THIS THE MID POINT OF TRACE
         BNO   WRTEND              NO BRANCH TO WRITE LAST HALF
         NI    IOTCFLAG,AVTEFF-IOTCFMID RESET BIT                S21903
*                                  TRACE TABLE POINTERS DO NOT NEED TO
*                                  BE UPDATED. LAST HALF WILL BE USED
*                                  NEXT.
         L     RWK2,IOTCSTRT       BEGINNING ADDRESS TO BE WRITTEN
POSTECB  EQU   *
         LA    RWK2,0(,RWK2)       CLEAR HIGH ORDER BYTE
         ST    RWK2,PARMAREA       MOVE IN START ADDRESS TO BE WRITTEN
         MVC   12(4,RWK2),COUNT         MOVE IN BLOCK COUNT
SEQEND   L     R15,CVTPTR               PICKUP CVT ADDRESS       Y02027
         USING CVT,R15                                           Y02027
         LR    RECB,RWK1                PICKUP ECB ADDRESS       Y02027
         SR    R14,R14                  CLEAR FOR OR INSTRUCTION Y02027
         ICM   R14,HI,XMPOSTMK          PICKUP MASK              Y02027
         OR    RECB,R14                 INDICATE CROSS-MEMORY    Y02027
*                                       POST REQUEST             Y02027
         LA    RERRET,CVTBRET           SETUP ERROR RETURN ADDR  Y02027
*                                       FOR XMPOST TO BR14 INST  Y02027
         OR    RERRET,R14               NOT MEMORY RELATED       Y02027
         L     RASCB,CVTAQAVT           PICKUP ADDR OF TCX       Y02027
         USING IEDQTCX,RASCB                                     Y02027
         L     RASCB,TCXASCB            PICKUP TCAM'S ASCB ADDR  Y02027
         DROP  RASCB                                             Y02027
         LA    RCCODE,CODE7F            PASS CONDITION CODE      Y02027
         SLL   RCCODE,TWNTY4            SHIFT TO HI ORDER BYTE   Y02027
         L     R15,CVT0PT01             PICKUP POST RTN ADDR     Y02027
         BALR  R14,R15             GO POST ECB
         DROP  R9
         USING *,R14
RETURNIO LM    R0,R15,SAVE
         BR    R14
NOTBLMSG DC    CL29'IED120I NO LINE I/O TR TABLE'              @SA69981
NOPREFIX EQU   X'08'                    INDICATOR FOR LACK OF PREFIX
TWNTY4   EQU   24                       CONSTANT                 Y02027
CODE7F   EQU   X'7F'                    COMPLETION CODE          Y02027
HI       EQU   8                        MASK FOR HI ORDER ICM    Y02027
MINPREFX DC    H'80'               MIN VALUE FOR PREFIX HEADER  SA68667
COUNT    DC    F'0'                BLOCK COUNT
AVTSAVE  DC    F'0'
SAVE     DC    15F'0'                                          @SA73359
SAVER15  DC    F'0'                                            @SA73359
PARMLIST DS    0F
PARMAREA DC    F'0'                POINTER TO DATA AREA
PARMTS   DC    XL1'00'             USER TEST & SET BYTE
PARMFLAG DC    X'40'               PARMLIST COMPLETE SET ON
PARMLEN  DC    H'0'                LENGTH OF DATA AREA
PARMCT   DC    F'0'                RECORD COUNT IF OPTION SPECIFIED
PARMID   DC    C'IOTR'             PARMLIST I D
XMPOSTMK DC    X'80'                    MASK TO INDICATE CROSS   Y02027
*                                       MEMORY POST              Y02027
         CNOP  0,8
ENDFE20  EQU   *                        END OF MODULE            X02004
IEDQIOTC DSECT
IOTCCURR DS    AL4                 ADDRESS OF CURRENT I/O TRACE ENTRY
IOTCSTRT DS    AL4                 ADDRESS OF START OF I/O TRACE TBL
IOTCLAST DS    AL4                 ADDRESS OF END OF I/O TRACE TBL
IOTCFLAG DS    0XL1                I/O TRACE ROUTINE FLAGS
*   ***********I/O TRACE TABLE FLAGS ***********
IOTCFMID EQU   X'20'               MIDPOINT OF TRACE HAS BEEN REACHED
IOTCFEND EQU   X'10'               END OF TRACE HAS BEEN REACHED
IOTCHALF DS    AL4                 ADDR OF MIDPOINT OF TRACE TABLE
         TAVTD
         TTCXD
         CVT DSECT=YES
         END
