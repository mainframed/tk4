BD01     TITLE '''IEDQBD'' - BUFFER DISPOSITION SUBTASK'
         SPACE 2
IEDQBD   CSECT
*A-000000-999999                                               @Z37X9MG
         SPACE 3
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A317900                                                         A51786
*C317200-317320                                                  A51786
*A175500,444500,492500                                           X01004
*C097600,131000,140800,175000,207000,215000,242940,259800,259940,X01004
*C376400,387000,440000,444000,448000,454000,459000,462000-464000,X01004
*C470000                                                         X01004
*D098300,132000-133000,176600,204000-205000,216000-217000,       X01004
*D259970-259980,376600-376800,441000,442000,449000-451000        X01004
*C452000                                                         X02004
*D259990                                                         X02004
*A360000,369200,371000,491000                                    S99228
*A105000,242000,259000,379000                                    S21101
*D381000                                                         S21101
*C098300,140800,383000                                           S21101
*C036000,100000,141000,225300,383000,488000-490000               S22025
*D384000-385000                                                  S22025
*A258000,380000,386000,437000,441000                             S22025
*A177600,209000                                                  A42363
*C0983000                                                        A42363
*A003500,176250-176260,226607-226691,233800-237000               S22026
*A372000                                                        SA52971
*C137000-137600,226250-226295                                   SA52971
*D260000-262000                                                 SA52971
*C098300                                                          M0891
*D259970-259980                                                   M0891
*C301000                                                         S21903
*A378000                                                        SA57687
*C181000,192000,194000,195000                                   SA59170
*A196000                                                        SA59170
*A364900,503100                                                 SA59976
*A379600                                                        SA61764
*C379500                                                        SA61764
*C141600                                                        SA66621
*A364905-364915,503050                                           S22024
*C097600-098300                                                  S22024
*A317900,319000                                                 SA67078
*A380100-380500                                                  Y06327
*A321100                                                        SA66605
* A388400                                                       OY00539
*A 365000                                                       OY03908
*A107200,308000,388600                                           X03039
*C008000,388480-388490                                           X03039
*A190000,388750,489000,503260                                  @X50X9IL
*A365100                                                       @SA62375
*C356000                                                       @SA73549
*A141400                                                       @OX11952
*A365100,365112,503260                                         @OS76699
*C141320                                                       @OS76997
*C380600                                                       @YA10449
*A034000,057000,059000,064000,127000,190600,226900,242000      @Y17XAMG
*A317000                                                       @Y17XAMG
*C388500,008000,388500                                         @Y17XAMG
*D388650-388710                                                @Y17XAMG
*C225800-225900                                                @OX13506
*A137600,D226270-226280                                        @OX12557
*A354000                                                       @YM04622
*C141340,317400,365380                                         @YM04622
*D141320,141360,356000-359000,365360,365400,388000-388520      @YM04622
*D388750-388800                                                @YM04622
*C107560,107620                                                @YM07396
*D380100-380500                                                @OY08751
*C365006-365012                                                @OS78326
*A141460,141520                                                @OZ25938
* DUMMY APAR                                                   @OZ27328
         SPACE 3
         ENTRY IEDQBD01
         ENTRY IEDQBD02                                          S22026
***********************************************************************
*TITLE -- 'IEDQBD' BUFFER DISPOSITION SUBTASK                         *
*                                                                     *
*  MODULE NAME = IEDQBD
*
*  DESCRIPTIVE NAME = BUFFER DISPOSITION
*
*  COPYRIGHT = NONE
*                                                                     *
*STATUS -- CHANGE LEVEL 10                                     @Y17XAMG
*                                                                     *
*FUNCTION -- THIS SUBTASK GAINS CONTROL WHEN THE LAST SEGMENT OF A    *
*  MESSAGE HAS BEEN RECEIVED OR SENT AND PROCESSED BY THE MESSAGE     *
*  HANDLER UP TO BUT NOT INCLUDING THE INMSG/OUTMSG SUBGROUP.  IT     *
*  ALSO RECEIVES CONTROL WHEN THE END OF A POLLING LIST HAS BEEN      *
*  REACHED, AND WHEN THE LAST BUFFER OF A BLOCK HAS BEEN RECEIVED     *
*  OR SENT TO A BUFFERRED TERMINAL.  UNUSED BUFFERS ARE RETURNED TO   *
*  THE BUFFER RETURN QUEUE AND THE INMSG/OUTMSG MACROS ARE EXECUTED.  *
*  THIS SUBTASK CHECKS THE PARAMETER LIST FOR EACH MACRO IN THE       *
*  INMSG/OUTMSG SUBGROUP CHECKING THE ERROR WORD AGAINST THE MASK     *
*  SPECIFIED , IF EXECUTION OF THE FUNCTION IS CONDITIONAL.  IF THE   *
*  ROUTINE REPRESENTED BY THE PARAMETER LIST REQUIRES A RECALLED      *
*  HEADER, THE ERB IS TPOSTED TO THE DISK I/O QCB TO PERFORM A RECALL *
*  THIS RECALLED HEADER IS RETURNED TO IEDQBD VIA A TPOST AND PASSED  *
*  TO THE ROUTINE.  EACH ROUTINE THAT RECEIVES CONTROL, PERFORMS ITS  *
*  FUNCTION AND EXITS BY TPOSTING THE RECALLED HEADER (IF ONE WAS     *
*  PASSED TO IT) OR THE ERB BACK TO IEDQBD TO CONTINUE EXECUTION      *
*  OF THE NACRO INSTRUCTIONS.  IN THE CASE OF RECALLED HEADERS, AFTER *
*  THE RECALLED HEADER IS TPOSTED BY THE MACRO ROUTINE, IT IS         *
*  RETURNED TO IEDQBD BY IEDQFA.  WHEN THE INEND/OUTEND EXPANSION IS  *
*  DETECTED, A CHECK IS MADE FOR DISTRIBUTION LIST, MULTIPLE          *
*  ROUTING AN CHECKPOINT REQUEST.  IF ANY OF THESE FUNCTIONS HAVE     *
*  BEEN REQUESTED,  THE APPROPRIATE SUBTASK RECEIVES CONTROL VIA A    *
*  TPOST.  FOR OUTPUT MESSAGES, THE MESSAGE JUST SENT IS THEN MARKED  *
*  SERVICED.  FOR BOTH SEND AND RECEIVE, THE LCB IS THEN TPOSTED TO   *
*  ITSELF.                                                            *
*                                                                     *
*  FOR MESSAGES INPUT FROM A SNA LU(OUTBOARD) BUFFER DISPOS-   @Y17XAMG
*  ITION LINKS TO IEDQRS TO GENERATE A DEFAULT RESPONSE IF     @Y17XAMG
*  A RESPONSE WAS NOT SENT DURING USER (SSCP/TOTE) MH          @Y17XAMG
*  PROCESSING. THIS IS DONE IF LCBRESP IS SET INDICATING A     @Y17XAMG
*  RESPONSE IS REQUIRED. ALSO, IF A RESONSE TO A CANCEL RU     @Y17XAMG
*  IS REQUIRED (LCBCANXT^=0) LINKAGE IS MADE TO IEDQRS TO      @Y17XAMG
*  RESPOND.                                                    @Y17XAMG
*                                                                     *
*ENTRY POINTS -- IEDQBD01,IEDQBD02                              S22025
*                                                                     *
*   CALLING SEQUENCE -                                                *
*                  L     R1,ELMAD                                     *
*                  L     R15,QCBSTCHN-1                               *
*                  LA    R15,4(R14)                                   *
*                  BR    R15                                          *
*                                                                     *
*INPUT -- ENTRY IS ALWAYS FROM THE TCAM DISPATCHER                    *
*         PARAMETERS ARE PASSED IMPLICITLY IN REGISTERS AS FOLLOWS:   *
*                  R1    ADDRESS OF THE PASSED ELEMENT -              *
*                        BUFFER,  ERB,  OR LCB                        *
*                  R11   BASE FOR TCAM DISPATCHER                     *
*                  R13   ADDRESS OF AVTSAVE2                          *
*                  R15   BASE FOR THIS ROUTINE                        *
*                                                                     *
*OUTPUT --  THE LCB IS TPOSTED TO ITSELF, FOR SEND OPERATIONS,        *
*  THE MESSAGE IS MARKED SERVICED,  FOR RECEIVE - THE END OF MESSAGE  *
*  SEGMENT IS TPOSTED TO THE DESTINATION QUEUE,  AND THE INMSG/OUTMSG *
*  SUBGROUP IS EXECUTED.                                              *
*                                                                     *
*EXTERNAL ROUTINES -- IEDQTNT(TNT INDEX CONVERSION ROUTINE            *
*                     IEDQRS(RESPONSE GENERATOR)               @Y17XAMG
*                                                                     *
*EXITS - NORMAL -- THE TCAM DISPATCHER AT ENTRY POINT DSPCHAIN        *
*                  MH FUNCTION ROUTINES VIA A BRANCH           @Y17XAMG
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORKAREAS --                                                  *
*    DSECTS USED - TAVTD, TDISPD, TSCBD, TLCBD, TPRFD, TQCBD   @Y17XAMG
*                  TPRIOR, TTRMD, DCBD                         @Y17XAMG
*                                                                     *
*ATTRIBUTES -- REUSEABLE, REFRESHABLE, ENABLED, RESIDENT, PROBLEM     *
*              PROGRAM MODE.                                          *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET  *
*                                                                     *
***********************************************************************
*
*            REGISTERS
*
R0       EQU   0                        REG 0                    S22024
R1       EQU   1                        PASSED ELEMENT           S22024
R2       EQU   2                        REG 2                    S22024
RSCB     EQU   3                        REG 3                    S22024
RLCB     EQU   4                        REG 4                    S22024
R5       EQU   5                        REG 5                    S22024
RPREFIX  EQU   6                        REG 6                    S22024
RQCB     EQU   7                        LAST DISPATCHED QCB      S22024
R8       EQU   8                        REG 8                    S22024
R9       EQU   9                        REG 9                    S22024
R10      EQU   10                       REG 10                   S22024
R11      EQU   11                       ADDRESS OF DISPATCHER    S22024
R12      EQU   12                       REG 12                   S22024
R13      EQU   13                       DISPR DAVE AREA (AVTSAVE2S22024
R14      EQU   14                       REG 14                   S22024
R15      EQU   15                       SUBTASK ENTRY POINT
         USING IEDQSCB,RSCB
         USING IEDQLCB,RLCB
         USING IEDQDISP,R11
         USING AVTSAVE2,R13
         USING IEDQPRF,RPREFIX
         EJECT
IEDQBD   IEDHJN ,                       SET NAME AND DATE        S22024
         DC    AL1(DSPMCPL4)            SUBTASK ENTRY CODE (VTO)
         DC    AL3(*-1)                 STCB ADDRESS             S22025
         EJECT
IEDQBD01 EQU   *
         USING *,R15
         SR    R0,R0                    CLEAR FOR ZEROING FIELDS
         LA    RQCB,0(RQCB)             INSURE POSITIVE REGISTER
         MVC   AVTDOUBL(1),LCBPRI-IEDQLCB(R1) SAVE POST PRIORITY S21101
         CLI   LCBPRI-IEDQLCB(R1),PRIFSPCI-1 LCB FOR CLEANUP
         BNE   CKBFR                    BRANCH NO CHECK ERB
*
*                                       VTAM PLCB                X03039
         CLI   LCBFLAG1-IEDQLCB(R1),PLCB                         X03039
         BNE   NOTAPLCB                 BR NO                  @Y17XAMG
*                                                                X03039
*                                       INDICATE NO BFRS TO FREE X03039
         TM    LCBSTAT1-IEDQLCB(R1),LCBRCLLN                   @YM07396
         BO    BD02BR                   GO CLEAN UP PLCB       @YM07396
*                                                                X03039
NOTAPLCB EQU   *                                               @Y17XAMG
         L     R1,LCBLSPCI-IEDQLCB-1(R1)  PICK UP FIRST BUFFER @
CKBFR    EQU   *
         LR    RPREFIX,R1               ASSUME ELEMENT IS A BUFFER
         LR    R1,R0                    INDICATE END OF POST LIST
         LR    R9,R0                    CLEAR FOR IC
*
         TM    PRFTIC,X08               PASSED ELEMENT A BUFFER
         BZ    CKERB                      BRANCH NO
*
         L     RLCB,PRFLCB-1            SOURCE/DESTINATION LCB
         L     RSCB,LCBSCBA-1           SCB ADDRESS
         L     R8,SCBMACR-1             FIRST/NEXT  INMSG/OUTMSG MACRO
         TM    LCBSTAT1,LCBRCLLN        LCB SET TO RECALL
         BO    CONTIN                     BRANCH YES, EXECUTE MACRO
*
         LR    R5,RPREFIX               SET TO POST BUFFER
         IC    R9,PRFNBUNT              NUMBER OF UNITS/BUFFER
NUNIT    EQU   *
         L     RPREFIX,PRFTIC           NEXT BUFFER(UNIT) ADDRESS
         BCT   R9,NUNIT                 BRANCH UNTIL BUFFER FOUND
*
         TM    LCBSTAT1,LCBSENDN        SEND OPERATION
         BZ    RECEIVE                    BRANCH NO
*
         STCM  R5,AD,LCBLSPCI           SAVE LAST BUFFER ADDRESS X01004
         MVC   PRFRCB-IEDQPRF(3,R5),SCBDESTQ  SAVE FOR FA
*** THE PRIORITY LEVEL MUST BE MAINTAINED ACCROSS RECALLS FOR   SA52971
*   MARKING SERVICED                                            SA52971
         MVC   PRFTQBCK-IEDQPRF(1,R5),SCBPRI SET THE PRI LEVEL  SA52971
         MVC   PRFTQBCK+1-IEDQPRF(2,R5),SCBOSEQ SET SEQUENCE   @OX12557
*                                         OUT FOR TERMINALS    @OX12557
*                                         (NON-CONC)           @OX12557
         LA    R12,RSTMDL               SET EXIT ADDRESS
         TM    LCBERBST,LCBERROR        TEXT ERROR ENCOUNTERED
         BZ    NOERR                   BRANCH NO
*
         NI    SCBQTYPE,X'FF'-SCBBFMM  RESET MIDDLE OF MESSAGE
         B     ERBWAIT                 GO SEE IF SHOULD WAIT ON ERB
         SPACE 1
NOERR    EQU   *
         TM    PRFSTAT1-IEDQPRF(R5),PRFITCPN  LOGICAL END OF MSG X01004
         BZ    RSTMDL                   BRANCH-NO                S22025
*
         OI    LCBERBST,LCBERROR       SIMULATE ERROR
         STCM  RQCB,7,LCBRCQCB+1        QCB ADDRESS            @YM04622
         LR    RPREFIX,R5               RESET BUFFER ADDRESS
         SR    R12,R12                  CLEAR FOR STORE        @OX11952
         TM    SCBQTYPE,SCBBBFTM+SCBBFMM MIDDLE OF MSG ?       @OZ25938
         BO    NOCLR                    DO NOT CLEAR IF SENDING
*                                       AND IN MIDDLE OF MSG   @OZ25938
         ST    R12,SCBMRFSD             CLEAR MULTIROUTE FIELDS@OX11952
NOCLR    EQU   *                        BYPASS CLEARING        @OZ25938
         LA    R12,BBNOFRE              SET EXIT ADDRESS        SA66621
ERBWAIT  EQU   *
*
         BAL   R10,BUFPST               FREE ANY BIFFERS AFTER
*                                       ERROR EOM BUFFER - IF ANY
*
         TM    LCBERBST,LCBDLNKN+LCBEOMSG IS ERB AVAILABLE
         BNZ   ADDLABEL                 WAIT FOR ERB           @Y17XAIX
         BAL   R14,DSPCHAIN                                    @Y17XAIX
*
ADDLABEL BR    R12                       EXIT                  @Y17XAIX
POSTERB  EQU   *
         LPR   R2,RQCB                  QCB ADDRESS
         LA    R5,LCBERB                ERB ADDRESS
         LA    R9,PRIEDISP              PRIORITY
         B     EXIT                     ADD TO POST LIST AND EXIT
*                                       TO DISPATCHER
*
CONTIN   EQU   *
         NI    LCBSTAT1,X'FF'-LCBRCLLN  RECALL FLAG OFF
         L     RPREFIX,LCBERBCH-1       ADDRESS OF RECALLED BUFFER
         B     REMBUF                   FREE THE RECALLED UNIT(S)
*
CKERB    EQU   *
*                                       ACCESS LCB BY SUBTRACTING
         LR    RLCB,RPREFIX             TO LCB REGISTER
         SH    RLCB,ERBLCB                SIZE OF LCB PRIOR TO ECB
         L     RSCB,LCBSCBA-1           SCB ADDRESS
         L     R8,SCBMACR-1             NEXT/LAST MACRO EXECUTED
         SR    RPREFIX,RPREFIX                                   X01004
         ICM   RPREFIX,AD,LCBERBCH      RECALLED-HEADER ADDR     X01004
         TM    LCBSTAT1,LCBRCLLN        LCBSTATE SET TO RECALL
         BO    CONTINUE                 BRANCH YES
*
         TM    SCBQTYPE,SCBBBFTM+SCBBFMM  BFRD MIDDLE OF MSG
         BNO   RSTMDL                   BRANCH NO
*
         TM    SCBQTYPE,SCBCONC         CONC                     S22026
         BO    BDCLUP                   BRANCH IF YES            S22026
         B     LOGEOM                   BRANCH SEND LOGICAL END OF
*                                       MESSAGE FOR BUFFERRED TERMS
*
CONTINUE EQU   *
*
         LTR   RPREFIX,RPREFIX          WAS RECALL SUCCESSFUL
         BNZ   NORCALL                  BRANCH YES
*
*                    IGNORE MACRO - CHECK NEXT
         OI    SCBERR3,SCBLOSTN         SET MESSAGE LOST
         OI    SCBHBFNO,SCBNORCL         THE PREVIOUS RECALL DID A42363
*                                          NOT GET A BUFFER      A42363
*
RSTMDL   EQU   *
         NI    SCBQTYPE,X'FF'-SCBBFMM   RESET MIDDLE OF MESSAGE BIT
FNDMAC   EQU   *
         IC    R9,1(R8)                 LENGTH OF PARAMETER LIST
         CLI   1(R8),X02                ANY LOGICAL BITS ON     SA59170
         BE    ADDLGTH                  NO, DONT CLEAR          SA59170
         N     R9,CLEARL2               CLEAR LOW ORDER BITS    SA59170
ADDLGTH  EQU   *                                                SA59170
         AR    R8,R9                    ADD LENGTH TO ACCESS NEXT LIST
         TM    0(R8),X01                EXECUTION CONDITIONAL
         BO    CKRCALL                    BRANCH NO
*
         OC    SCBERR2,AVTSYSER         SET SYSTEM ERROR BITS
         L     R10,SCBERRST             ERROR WORD
         ST    R10,LCBCSWSV-1           STORE FOR COMPARE
         MVC   LCBCSWSV+3(1),LCBSENS0   MOVE SENSE BYTE
*                                                              @X50X9IL
*              CHECK WHETHER MESSAGE ERROR RECORD DATA NEEDS   @X50X9IL
*              TO BE MODIFIED BEFORE BEING TESTED (APPLICABLE  @X50X9IL
*              FOR SNA LOGICAL UNITS ONLY).  THE EFFECT OF     @X50X9IL
*              THIS MODIFICATION IS AS FOLLOWS:                @X50X9IL
*                                                              @X50X9IL
*              IF THE SNA MINOR SENSE BYTE IN THE ERROR MASK   @X50X9IL
*              IS CODED AS '00'X THE SNA MAJOR SENSE BITS ARE  @X50X9IL
*              INTERPRETED IN A GENERAL WAY (TEST FOR ALL TYPES@X50X9IL
*              OF ERRORS WITHIN A GENERAL CLASS, IGNORING THE  @X50X9IL
*              SNA MINOR SENSE VALUE).  NO MODIFICATION IS     @X50X9IL
*              DONE IN THIS CASE.                              @X50X9IL
*                                                              @X50X9IL
*              IF THE SNA MINOR SENSE BYTE IN THE ERROR MASK   @X50X9IL
*              IS CODED AS A NON-ZERO VALUE THEN THE SNA       @X50X9IL
*              SENSE BITS ARE INTERPRETED IN A SPECIFIC WAY    @X50X9IL
*              (TEST FOR ONLY THE ERROR WITH THIS EXACT SNA    @X50X9IL
*              MAJOR AND MINOR SENSE).  IF THE ACTUAL SNA      @X50X9IL
*              ERROR DATA MATCHES THE SNA ERRORS SPECIFIED ON  @X50X9IL
*              THE ERROR MASK, NO MODIFICATION TAKES PLACE.    @X50X9IL
*              IF THEY DO NOT MATCH, BOTH THE SNA MAJOR AND    @X50X9IL
*              MINOR SENSE DATA IN THE MESSAGE ERROR RECORD    @X50X9IL
*              ARE CLEARED TO ZERO BEFORE TESTING AGAINST      @X50X9IL
*              THE FULL ERROR MASK.                            @X50X9IL
*                                                              @X50X9IL
         USING MASKD,R8                 SET ERROR MASK BASE    @X50X9IL
         CLI   LCBFLAG1,PLCB            IS THIS A PLCB?        @X50X9IL
         BNE   TESTMASK                 BRANCH IF NO           @X50X9IL
         TM    LCBSTAT5,LCBLUNIT        SNA LOGICAL UNIT?      @X50X9IL
         BZ    TESTMASK                 BRANCH IF NO           @X50X9IL
         MVC   LCBCSWSV+3(1),LCBLUSNS+1 MOVE SNA MINOR SENSE   @Y17XAMG
*                                       INTO 5TH ERROR BYTE    @Y17XAMG
*                                       POSITION.              @Y17XAMG
         CLI   MASKLAST,X00             LAST BYTE OF MASK = 0? @X50X9IL
         BE    TESTMASK                 BRANCH IF YES          @X50X9IL
*              R10 CONTAINS THE 1ST 4 MSG ERROR RECORD BYTES   @X50X9IL
         N     R10,SNAMAJOR             ANY SNA MAJOR SENSE?   @X50X9IL
         BZ    ZEROLAST                 BRANCH IF NO           @X50X9IL
         MVC   AVTSAVE3(4),MASK4        MOVE 1ST 4 BYTES OF    @X50X9IL
*                                       ERROR MASK TO FULLWORD @X50X9IL
         N     R10,AVTSAVE3             IS THE SAME SNA MAJOR  @X50X9IL
*                                       SENSE BIT IN ERR MASK? @X50X9IL
         BZ    ZEROLAST                 BRANCH IF NO           @X50X9IL
         CLC   LCBCSWSV+3(1),MASKLAST   ARE LAST BYTES EQUAL?  @X50X9IL
         BE    TESTMASK                 BRANCH IF YES          @X50X9IL
         X     R10,LCBCSWSV-1           TURN OFF SNA MAJOR SNS @X50X9IL
         ST    R10,LCBCSWSV-1           ...IN MSG ERROR RCD    @X50X9IL
*                                                              @X50X9IL
ZEROLAST EQU   *                                               @X50X9IL
         MVI   LCBCSWSV+3,X00           ZERO SNA MINOR SENSE   @X50X9IL
*                                       IN MSG ERROR RECORD    @X50X9IL
TESTMASK EQU   *                                               @X50X9IL
         NC    LCBCSWSV-1(5),3(R8)      CHECK MASK AGAINST ERROR BYTES
         BNZ   CHECKAND                 GO CHECK AND FNCTION    SA59170
         TM    1(R8),X02                IS NAND FUNCTION REQSTD SA59170
         BO    CKRCALL                  IF YES, BRANCH          SA59170
         B     FNDMAC                   NO,ACCESS NEXT MACRO    SA59170
*
CHECKAND EQU   *                                                SA59170
         TM    1(R8),1                  IS AND FUNCTION REQUESTED
         BO    COMPARE                  BRANCH IF YES           SA59170
         TM    1(R8),X02                IS NAND FUNCTION REQSTD SA59170
         BO    FNDMAC                   IF YES, BYPASS MACRO    SA59170
         B     CKRCALL                  NO, OR IS REQUESTED     SA59170
*
COMPARE  EQU   *                                                SA59170
         CLC   LCBCSWSV-1(5),3(R8)      WAS AND SUCCESSFUL
         BNE   FNDMAC                   BRANCH NO, ACCESS NEXT MACRO
*
CKRCALL  EQU   *
         TM    0(R8),X02                RECALLED HEADER NEEDED
         BZ    SETNXT                     BRANCH NO
*
         LA    R2,AVTBFRTB              ADDRESS OF BUFFER RETURNN
         CLM   R2,AD,SCBDESTQ           IS DESTINATION BUFR RTN  X01004
         BE    FNDMAC                   BRANCH YES - BYPASS THIS MACRO
*
         TM    SCBHBFNO,SCBNORCL         HAS A PREVIOUS RECALL   A42363
*                                          FAILED TO GET A BFR   A42363
         BO    FNDMAC                    BR YES TO CONTINUE PROC.A42363
         LTR   R2,RQCB                  CAN RECALL BE DONE
         BM    POSTERB                  BRANCH NO - WAIT UNTIL HEADER
*                                       HAS BEEN POSTED TO DESTINATION
*
SETNXT   EQU   *
         STCM  R8,AD,SCBMACR            SAVE LIST ADDRESS        X01004
         BNZ   BBRCALL                  RECALL HEADER
         XC    LCBERBCH,LCBERBCH        CLEAR ERBCH FOR NEXT   @YM07367
*                                       INMSG/OUTMSG MACRO     @YM07367
NORCALL  EQU   *
         L     R12,AVTMSGS-1            ACCESS MH MACRO ROUTINE ADDRS.
         IC    R9,0(R8)                 INDEX TO ROUTINE VCON
         N     R9,CLEARL2               CLEAR LOW ORDER 2 BITS
         L     R12,0(R9,R12)            SETUP BASE FOR MH ROUTINE
         BCR   7,R12                      BRANCH TO ROUTINE
*                                       IF ZERO, THIS IS IN/OUTEND
*
         TM    LCBSTAT1,LCBSENDN        SEND STATE
         BZ    BDCLUP                   BRANCH NO - CHECK FOR    S22025
*                                       IEDQNX                   S22025
*
*                  SET MESSAGE SERVICED
         L     R5,LCBLSPCI-1            ADDRESS OF EOM SEGMENT
         N     R5,CLEARL2               WAS HOLD EXECUTED
         BNZ   MARKSRV                  NO, GO MARK MSG SRVCD  @OX13506
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN LOCK MODE            @OX13506
         BZ    BDCLUP                   NO,GO CLEAN UP         @OX13506
         XI    LCBSTAT1,LCBSENDN+LCBRECVN SET TO RETRANSMIT    @OX13506
         XC    SCBMRFSD(3),SCBMRFSD     CLEAR MULTIPLE ROUTE   @OX13506
         B     BDCLUP                   GO CLEAN UP            @OX13506
MARKSRV  EQU   *                                               @OX13506
*
         MVC   SCBDESTQ,PRFRCB-IEDQPRF(R5)  RESTORE DESTQ FOR FA
         BAL   R9,SVQTYP                SAVE Q TYPE
*** THIS CODE WILL SET THE SEQUENCE OUT AND FEFO FOR TERMINALS  SA52971
*   SO THAT IEDQFA WILL NOT HAVE TO DISTINGUISH BETWEEN         SA52971
*   APPLICATION PROGRAMS AND NON-CONCENTRATOR TERMINALS TO FIND SA52971
*   THE FEFO POINTER TO WRITE WHEN MADKING SERVICED             SA52971
         MVC   PRFSRCE-IEDQPRF(3,R5),SCBFEFO SET NEW FEFO       SA52971
*                                         POINTER FOR TERMINALS SA52971
*                                         (NON-CONC)            SA52971
*
         NI    PRFSTAT1-IEDQPRF(R5),PRFNLSTF  INSURE LAST SEGMENT
         LA    R2,AVTDSIOB              DISK I/O QUEUE ADDRESS
         LA    R9,PRIDKSRV              PRIORITY
         BAL   R14,POSTSUB              INSERT IN LIST
*
BDCLUP   EQU   *
         CLI   LCBFLAG1,PLCB            IS THIS A PLCB         @YM06129
         BNE   NORESP                   NO                     @YM06129
         TM    LCBSTAT5,LCBLUNIT        IS THIS AN LU?         @Y17XAMG
         BZ    NORESP                   BRANCH IF NOT          @Y17XAMG
         TM    LCBSTAT2,LCBRESP         DEFAULT RESPONSE       @Y17XAMG
*                                       REQUIRED?              @Y17XAMG
         BO    RESPOND                  BRANCH IF SO           @Y17XAMG
         TM    LCBCANFG,LCBCANXT        CANCEL RESP REQUIRED?  @Y17XAMG
         BZ    NORESP                   BRANCH IF NOT          @Y17XAMG
RESPOND  EQU   *                                               @Y17XAMG
         SLR   R0,R0                    INDICATE IEDQBD ENTRY  @Y17XAMG
*                                       TO RESPONSE FUNCTION   @Y17XAMG
         SLR   R9,R9                    INDICATE NON-MACRO CALL@Y17XAMG
         L     R12,AVTSAVTP             SAVT ADDRESS           @Y17XAMG
         L     R12,SAVTQRS-IEDNSVTD(R12) IEDQRS ENTRY ADDRESS  @Y17XAMG
         BALR  R14,R12                  GENERATE DEFAULT OR    @Y17XAMG
*                                       CANCEL RESPONSE        @Y17XAMG
NORESP   EQU   *                                               @Y17XAMG
         OI    LCBSTAT1,LCBRCLLN        SET FLAG FOR CLEANUP TO
*                                       INDICATE BUFFERS FRED
         LA    R2,IEDQBD02              ADDRESS OF CLEANUP SUBTASK
         LR    R5,RLCB                  SET TO POST LCB TO CLEANUP
         LA    R9,PRIDESTQ-1            PRIORITY FOR POST - MUST BE
*                                       LOWER THAN THAT USED TO POST
*                                       EOM SEGMENT TO DESTINATION
         TM    SCBQTYPE,SCBCONC         CONC                     S22026
         BNO   NOTCONC                  BRANCH IF NO             S22026
         L     R2,LCBQCBA-1             SET POST REG             S22026
         LA    R9,PRIRLCB               CONC POST PRI            S22026
NOTCONC  EQU   *                                                 S22026
         B     EXIT                     ADD TO POST LIST AND EXIT
         SPACE
RECEIVE  EQU   *                        PROCESS RECEIVE EOM    @YM07396
         LA    R9,PRIDESTQ              SET UP PRIORITY        @YM07396
         CLI   AVTDOUBL,PRIFSPCI-1      LCB/PLCB E7 POST       @YM07396
         BE    BUFRET                   BRANCH YES CLEAN UP BFR@YM07396
         SPACE
         CLI   LCBFLAG1,PLCB            IS THIS PLCB           @YM07396
         BNE   NOTLUNIT                 BRANCH NO              @YM07396
         TM    LCBSTAT5,LCBLUNIT        IS THIS A SNA LU       @YM07396
         BZ    CKSSCP                   BRANCH NO              @YM07396
         TM    LCBSTAT2,LCBRESP         RSP ALREADY SENT       @YM07705
         BNO   CKSSCP                   BRANCH YES             @YM07705
         TM    LCBRSPFG,LCBRSRH         RSP ALREADY GENERATED  @YM07705
         BO    CKSSCP                   BRANCH YES             @YM07396
         LA    R10,PRF1LEN              LENGTH OF NEG. PREFIX  @YM07396
         LNR   R10,R10                  SET UP FOR ADD         @YM07396
         AR    R10,R5                   R5 NOW POINTS TO PREFIX@YM07396
         USING IEDPF1,R10                                      @YM07396
         MVC   LCBRHSV,PRF1RH           SAVE RH IN PLCB        @YM07396
         MVC   LCBTHSQ,PRF1SQID         SAVE TH SEQ. NO.       @YM07396
         TM    PRF1FLG1,PRFIEXPI        EXPEDITED RU           @YM07396
         BZ    CKSSCP                   BRANCH NO              @YM07396
         OI    LCBRSPFG,LCBEXPI         INDICATE EXPEDITED     @YM07396
         DROP  R10                                             @YM07396
CKSSCP   EQU   *                                               @YM07396
         TM    SCBDESTQ+2,1             DEST= SSCP             @YM07396
         BZ    NOTLUNIT                 BRANCH NO              @YM07396
         NI    SCBDESTQ+2,X'FE'         RESET SSCP INDICATOR   @YM07396
         LA    R9,PRISSBUF              SSCP PRIORITY          @YM07396
NOTLUNIT EQU   *                                               @YM07396
         L     R2,SCBDESTQ-1            DESTINATION QCB ADDR   @YM07702
         TM    PRFSTAT1-IEDQPRF(R5),PRFITCPN  LOGICAL END OF MSG X01004
         BZ    NOLOGEOM                 BRANCH IF NO             S21101
         SPACE 1                                                 S21101
*        THE FOLLOWING CODE CHECKS FOR A ZERO LENGTH BUFFER      S21101
* POSTED TO A QUEUE.    BUFFER ORIGINALLY HAD ONLY EOT AND WAS   S21101
* REMOVED BY LINEEND.  PRFSIZE REFLECTS IDLES+ PREFIX SIZE.      S21101
* IEDQAA TEST FOR THIS CONDITION AND CLEARS PRFSIZE.             S21101
         SR    R10,R10                  CLEAR WORK REGISTER      S21101
         CH    R10,PRFSIZE-IEDQPRF(R5)  NO DATA IN BUFFER        S21101
         BNE   NOLOGEOM                 BRANCH IF NO             S21101
         SPACE 1                                                 S21101
BUFRET   EQU   *                                                 S21101
         LA    R2,AVTBFRTB              POST BUFFER TO BUFF RETRNS21101
NOLOGEOM EQU   *                                                 S21101
         BAL   R14,POSTSUB              ADD TO POST LIST
*
         LA    R10,LCBERB               ERB ADDRESS
         TM    LCBERBST,LCBDLNKN        IS DLINK SWITCH SET
         BO    CKHDR                    BRANCH NOT IN CHAIN
*
         LA    R9,AVTBFRTB-4            BUFFER RETURN QCB
*
NEXT     EQU   *
         LR    R12,R9                   SET TO PICK UP NEXT ELEMENT
         L     R9,4(0,R12)              FIRST/NEXT ELEMENT IN CHAIN
         LA    R9,0(0,R9)               CLEAR HIGH ORDER
         CLR   R9,R10                   IS THIS ELEMENT THE ERB
         BNE   NEXT                     BRANCH NO
*
         MVC   5(3,R12),5(R9)           REMOVE ERB FROM CHAIN
         OI    LCBERBST,LCBDLNKN        TURN ON DELINK SWITCH    S22025
CKHDR    EQU   *
         LA    R10,BDCLUP               ASSUME MIDDLE OF MSG     S21101
         CLI   AVTDOUBL,PRIFSPCI-1      CLEANUP OPERATION ONLY   S21101
         BE    BUFPST                   BRANCH YES               S21101
         TM    PRFSTAT1-IEDQPRF(R5),PRFITCPN  LOGICAL END OF MSG X01004
         BZ    EOM                      BRANCH IF NO             S21101
         SPACE 1                                                 S21101
         NI    PRFSTAT1-IEDQPRF(R5),X'FF'-PRFITCPN RESET LOG EOM X01004
         B     BUFPST                   POST OTHER BUFFERS       S21101
EOM      EQU   *                                                 S21101
         NI    SCBQTYPE,X'FF'-SCBBFMM   SET TO REAL EOM          S21101
         LNR   RQCB,RQCB                PREVENT RECALL UNTILL POST
*
REMBUF   EQU   *
         LA    R10,FNDMAC               SET RETURN ADDRESS
*
BUFPST   EQU   *
         LR    R5,RPREFIX               SAVE @ OF FIRST UNIT
FREEBFR  EQU   *
         SR    R9,R9                    CLEAR FOR BCT
BYPUNT   EQU   *
         C     R9,MAXFREE               COUNT REG = MAXIMUM
         BE    CKEND                    BRANCH YES
*
         EX    RPREFIX,BBTM             END OF BUFFER CHAIN
         BNZ   CKEND                    BRANCH YES
*
         L     RPREFIX,PRFTIC           NEXT BUFFER/UNIT
         BCT   R9,BYPUNT                COUNT UNITS
*
CKEND    EQU   *
         LPR   R9,R9                    MAKE COUNT POSITIVE
         BCR   8,R10                    EXIT IF ZERO UNITS
*
         STC   R9,PRFNBUNT-IEDQPRF(R5)  NUMBER OF UNITS TO BE FREED
         LA    R2,AVTBFRTB              ADDRESS OF BUFFER RETURN QCB
         LA    R9,PRIBFRTB              PRIORITY OF RETURNED BUFFER>
         BAL   R14,POSTSUB              ADD BUFFER TO POST LIST
         LR    R5,RPREFIX              SET NEXT BUFFER ADDRESS
         CLI   PRFNBUNT-IEDQPRF(R1),XFF  LAST BUFFER HAVE 255 UNITS
         BE    FREEBFR                  BRANCH YES - CONTINUE
*
         BR    R10                      RETURN TO CALLER
*
         EJECT
         DS    0F
         ORG   *-8                      SET UP FOR QCB           S21903
         USING *,RQCB
IEDQBD02 EQU   *
         ORG
         DC    AL1(DSPMCPL4)            SUBTASK ENTRY CODE (VTO)
         DC    AL3(*-1)                 STCB ADDRESS
         L     R15,BASE                 SET ADDRESSABILITY FOR CSECT
         DROP  RQCB
BD02BR   EQU   *                        BR ENTRY TO BD02         X03039
         SR    R0,R0                    CLEAR FOR ZEROING FIELDS
         LA    R9,X01                   PREVENT 0 REG FOR LOCK TEST
         LR    RLCB,R1                  ELEMENT IS AN LCB
         USING IEDQLCB,RLCB
         L     RSCB,LCBSCBA-1           SCB ADDRESS            @YM07350
         USING IEDQSCB,RSCB
         CLI   LCBFLAG1,PLCB            IS THIS A PLCB         @YM06129
         BNE   NOTLU                    NO                     @YM06129
         TM    LCBSTAT5,LCBLUNIT        LU?                    @Y17XAMG
         BZ    NOTLU                    BRANCH IF NOT          @Y17XAMG
         XC    LCBRHSV(LCBRSPFG-LCBRHSV+1),LCBRHSV CLEAR RSP   @Y17XAMG
*                                       AREA IN PLCB           @Y17XAMG
NOTLU    EQU   *                                               @Y17XAMG
         TM    LCBCHAIN,LCBERMSG        ERP MSG WAITING        @YM04622
         BZ    NOPAWR                   BRANCH NO              @YM04622
         ICM   R12,AD,AVTNX+X01         ADDRESS OF OPERATOR    @YM04622
*                                       AWARENESS ROUTINE      @YM04622
         BCR   7,R12                    BRANCH YES - EXIT TO IEDQNX
*
NOPAWR   EQU   *                                                 A51786
         SR    R1,R1                    END OF POST LIST        SA67078
         TM    SCBQTYPE,SCBBBFTM+SCBBFMM  MIDDLE OF MESSAGE ?
         BO    TSTFRE                   YES, NO MULTIPLE ROUTE  SA67078
         TM    LCBSTAT1,LCBRECVN        IS LCB RECEIVING        SA67078
         BO    CKMLT                    YES, CHECK MULTI-ROUTE  SA67078
         ST    R1,SCBMRFSD              CLEAR MULTI-ROUTE FIELD SA67078
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN   IF MESSAGE LOCK     SA66605
         BNO   TSTFRE                        TURN OFF LOCK      SA66605
         NI    SCBSTATE,X'FF'-(SCBLCK1N+SCBMSGLN)  AFTER SEND   SA66605
         B     TSTFRE                   CHECK IF BUFFERS FREED
*
CKMLT    EQU   *
*                                        AND DLIST FLAGS
         L     R1,SCBMRFSD              MULTIPLE ROUTE AND DLIST
*                                       INDICATOR
         LTR   R2,R1                    IS MULTIPLE ROUTING SET
         BNZ   BBMLT                      BRANCH YES
TSTFRE   EQU   *
         TM    LCBSTAT1,LCBRCLLN        HAVE BUFFERS BEEN FREED YET
         BO    BBNOFRE                    BRANCH YES
*
         L     RPREFIX,LCBLSPCI-1       FIRST BUFFER TO BE FREED
         BAL   R10,BUFPST               FREE UNUSED BUFFERS
*
BBNOFRE  EQU   *
         TM    SCBQTYPE,SCBBBFTM+SCBBFMM  MIDDLE OF MESSAGE FOR A
*                                       BUFFERRED TERMINAL
         BO    BFMDL                    BRANCH YES
*
         TM    SCBSTATE,SCBCKPT         CHECKPOINT REQUESTED
         BNZ   BBCKPT                   BRANCH YES
*
         ST    R0,SCBERRST              CLEAR ERROR WORD
         MVI   LCBSENS0,AVTEZERO        RESET 5TH ERROR BYTE-IOS
         CLI   LCBFLAG1,LCBPLCB         PLCB                   @YM09045
         BE    PUTLCB                   BR YES                 @YM09045
         CLI   LCBRSKEY,APLCB           APP PROG PUT LCB?        S99228
         BE    PUTLCB                   BYPASS GEN POLL        @YM04622
         SR    R5,R5                    CLEAR INDEX REGISTER     S99228
         L     R2,LCBDCBPT              LOAD DCB ADDR            S99228
         IC    R5,DCBEIOBX-IHADCB(R0,R2)   INSERT LCB LENGTH     S99228
         SH    R5,AVTHA4                REDUCE LENGTH BY 8       S99228
         SH    R5,AVTHA4                BYTES TO POINT TO LCB EXTS99228
         LA    R5,X00(R5,RLCB)          SET ADDR LCB EXTENSION   S99228
         USING IEDQLCBX,R5                                       S99228
         TM    LCBSTAT1,LCBSENDN        SENDING                 SA59976
         BO    NOMSGLMT                 YES, NOTEST FOR MSGLMT  SA59976
         L     R10,LCBSTCBA-1           FIRST STCB IN CHAIN     SA59976
         CLI   0(R10),DSPMCPL8          THIS LMD STCB           SA59976
         BE    NOMSGLMT                 YES, NOMSGLINIT         SA59976
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN LOCK MODE OR EXTENDED
*                                       LOCK MODE              @OS78326
         BM    NOMSGLMT                 BRANCH, IF LOCK EXTEND @OS78326
         TM    SCBBSCFM,SCBMLMTN        MSGLIMIT REACHED        SA59976
         BNO   NOMSGLMT                 BR NO                   SA59976
         NI    SCBBSCFM,SCBMLMTF        RESET MSGLMT FLAG       SA59976
         TM    LCBXFLAG,LCBGPCTV        GEN.POLL IN PROGRESS    SA59976
         BO    NOMSGLMT                 YES, NO INVL CHANGE     SA59976
         TM    LCBDCT1,C3270            3270 TERMINAL?         @OS76699
         BNO   NOT3270R                 BRANCH NO              @OS76699
         TM    LCBDCT2,CLOCAL           IS IT A LOCAL?         @OS76699
         BO    NOT3270R                 BRANCH YES             @OS76699
         CLC   LCBINVPT,LCBERADR        WAS POLL FOR SOH MSG   @SA62375
         BNE   NOMSGLMT                 YES, DON'T UPDATE      @SA62375
NOT3270R EQU   *                                               @OS76699
         SR    R10,R10                  CLEAR                   SA59976
         IC    R10,LCBUCBX              REL. LINE NO.           SA59976
         SLL   R10,TWO                  SHIFT FOR OFFSET        SA59976
         L     R2,DCBINVLI-IHADCB(R10,R2) INVLIST PTR           SA59976
         SR    R10,R10                  CLEAR                   SA59976
         IC    R10,WIDTH(,R2)           WIDTH OF ENTRY          SA59976
         L     R2,LCBINVPT-1            CURRENT ENTRY PTR       SA59976
         CLI   0(R2),FE                 END OF LIST             SA59976
         BE    NOMSGLMT                 YES, NO UPDATE          SA59976
         CLI   ONE(R2),FE               END OF BSC LIST         SA59976
         BE    NOMSGLMT                 YES, NO UPDATE          SA59976
         LA    R10,0(R10,R2)            BUMP TO NEXT ENTRY      SA59976
         STCM  R10,7,LCBINVPT           SET NEW ENTRY          @YM04622
NOMSGLMT EQU   *                                                SA59976
*                                                                S99228
*        ASSUME GENERAL POLL IN PROGRESS                         S99228
*                                                                S99228
         NI    LCBXFLAG,LCBSRSTP        RESET SRC DETERM INDIC   S99228
PUTLCB   EQU   *                                                 S99228
         TM    SCBSTATE,SCBLCK1N        LOCK REQUESTED
         BZ    NOTLOCK                  BRANCH NOT LOCK MODE
         CLI   LCBFLAG1,LCBPLCB         PLCB                   @YM09045
         BE    PUTSLCB                  BR YES                 @YM09045
         CLI   LCBRSKEY,APLCB           APP PROG PUT LCB?        S99228
         BE    PUTSLCB                  BRANCH YES               S99228
*                                                                S99228
         NI    LCBXFLAG,LCBGPSTP        STOP GENERAL POLL        S99228
PUTSLCB  EQU   *                                                 S99228
*
         NI    LCBINSRC+2,X'FF'-X80     CLEAR POST FLAG
         BZ    NOTLOCK                  BRANCH IF LCB CAN BE POSTED
*                                       RESPONSE HAS BEEN PUT
*
         LNR   R9,R9                    PREVENT LCB POST
NOTLOCK  EQU   *
         DROP  R5                                                S99228
         MVI   SCBHBFNO,X00             CLEAR RECALL INDICATOR
         XC    SCBSCHDR(3),SCBSCHDR     CLEAR HDR PTR AS HM03   SA52971
*                                         FLAG                  SA52971
         NI    SCBSTATE,SCBLCK1N+SCBMSGLN  RESET SCBSTATE
         LA    R2,AVTBFRTB              BUFFER RETURN QUEUE
         STCM  R2,AD,SCBDESTQ           SET DESTINATION          X01004
         ST    R0,SCBDEOB               CLEAR DEOB
         STH   R0,SCBEOB                CLEAR
         MVI   SCBPRI,AVTEZERO          CLEAR PRIORITY BYTE     SA57687
         NI    SCBQTYPE,X0F             CLEAR QTYPE FLAGS
         NI    SCBBSCFM,AVTEFF-SCBTRNSP-SCBNONTR-SCBDATEN       SA61764
*                                       RESET BSC BITS          SA61764
BFMDL    EQU   *
         NI    LCBTSOB,X'FF'-LCBTSBUF-LCBINHBN-LCBWRBRK-LCBPREP
*                                       SET TSO BITS OFF       @YA10449
LOGEOM   EQU   *                                                 S22025
         NI    LCBSTAT2,LCBMSGNF        RESET MSGEN BIT
         NI    LCBCHAIN,X'FF'-LCBBFRSZ-LCBABRTN-LCBEXCP-LCBNORTY-LCBERMS
               SG                         RESET CHAIN FLAGS      S22025
         ST    R0,LCBERBCH-1            CLEAR ERB STATE AND CHAIN
         OI    LCBERBST,LCBDLNKN        TURN ON DELINK SWITCH    S22025
         STCM  R0,AD,LCBLSPCI           CLEAR LAST SERVICED PCI  X01004
         STCM  R0,AD,LCBSTART           CLEAR LCBSTART WHICH   @YM07035
*                                       IS THE SAME FIELD AS   @YM07035
*                                       LCBPAKCH               @YM07035
         NI    LCBSTAT1,X'FF'-LCBRCLLN  RESET RECALL BIT
         NI    LCBCSW+3,AVTEFF-UNEX     RESET UNIT EXCEPTION     S22024
         LTR   R9,R9                    CAN LCB BE POSTED
         BNM   ADDLABL        BRANCH NO - LCB WILL BE POSTED   @Y17XAIX
*                                       WHEN PUT RESPONSE HAS BEEN
*                                       QUEUED
         BAL   R14,DSPCHAIN                                    @Y17XAIX
ADDLABL  LA    R9,PRILNFRE       PRIORITY FOR FREEING A LINE   @Y17XAIX
         LR    R2,RLCB                  POST LCB
         LR    R5,RLCB                    TO ITSELF
*
EXIT     EQU   *
         LA    R14,DSPCHAIN             ADDRESS OF DISPATCHER
*              FALL THRU TO ADD ELEMENT TO POST LIST
*              AND EXIT TO THE DISPATCHER
         SPACE 2
POSTSUB  EQU   *
         ST    R2,0(R5)                 QCB ADDRESS TO RECB
         ST    R1,4(R5)                 LINK ELEMENTS
         STC   R9,4(R5)                 STORE PRIORITY
         LR    R1,R5                    NEW LAST ELEMENT
         BALR  R14,R14                  RETURN TO CALLER       @Y17XAIX
*
BBCKPT   EQU   *
         LA    R5,LCBERB                POST ERB TO CHECKPOINT
         LA    R9,PRICKPLN              PRIORITY
         LA    R2,AVTCKPTB              CHECKPOINT QCB
         B     EXIT                     CHAIN ELEMENTS
*
BBMLT    EQU   *
         SRA   R2,X16                   IS MULTIPLE ROUTING REQUESTED
         BZ    BBDLST                   BRANCH IF ONLY A DLIST
         L     R8,SCBMRFPL-1            FORWARD PARAMETER LIST
         IC    R9,3(R8)                 OFFSET TO MULTIPLE ROUTE
         L     R2,AVTMSGS-1             ADDRESS OF VCON LIST
         L     R2,X00(R2,R9)            ADDRESS OF MULTI-ROUTE QCB
         ST    R2,SCBBKFCT-1            SET TO RETURN TO MULTIPLE
*                                       ROUTE SUBTASK
BBDLST   EQU   *
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF WORD
         BZ    BBRCALL                  RECALL BUFFER
*                                       WHICH WILL BE PASSED TO
*                                       MULTIPLE ROUTE SUBTASK
         L     R15,AVTRNMPT             ADDRESS OF TNT CODE
         BALR  R14,R15                  LINK TO TNT
*
         USING *,R14
         L     R15,BASE                 RESET ADDRESSABILITY
         DROP  R14
         ST    R1,LCBLINK-1             ENTRY ADDRESS
         LA    R9,2                     SET DLPTR TO PICK UP
         STH   R9,SCBDLPTR                SECOND ENTRY IN LIST
         L     R2,TRMDESTQ-IEDQTRM-1(R1)  ADDRESS OF IEDQBC
         SR    R1,R1                    INDICATE END OF POST LIST
BBRCALL  EQU   *
          MVC   LCBCPA+4(3),SCBTRANS     SAVE SCB TRANS ADDR     S22025
*                                        ACROSS RECALL RESTORED  S22025
*                                       IN IEDQAS                S22025
         BAL   R9,SVQTYP                SAVE Q TYPE
*
         MVI   SCBPRI,X00               CLEAR PRIOITY QCB POINTER
         STCM  R2,AD,LCBRCQCB+1         SET RETURN FOR RECALL    X01004
         MVC   LCBCPA(4),SCBDEOB        SAVE DEOB ACROSS RECALL  S22025
*                                       FOR HOLD WITH MIDBATCH   S22025
         OI    LCBCHAIN,LCBBFRSZ        RECALL ORIGINAL BUFFER SIZE
         SR    R2,R2                                             X01004
         ICM   R2,AD,SCBCCHDR           ASSUME CORE RECALL       X01004
         TM    SCBHBFNO,SCBNREUS+SCBREUS  IS THERE DISK BACKUP
         BZ    CORE                     BRANCH NO - USE CORE ADDRESS
*
         ICM   R2,AD,SCBTRANS           ORIG RECALLED HDR ADDR   X01004
         BNZ   OKCORE                   BRANCH IF NOT 1ST RECALL X02004
*
         ICM   R2,AD,SCBDCHDR           CURRENT RECORD ADDRESS   X01004
CORE     EQU   *
         TM    LCBSTAT1,LCBSENDN        IS LINE SENDING
         BZ    OKCORE                   BRANCH NO
*
         ICM   R2,AD,SCBSCHDR           SEND SIDE RE"ALL ADDRESS X01004
OKCORE   EQU   *
         ST    R2,SCBDEOB               SET RECALL ADDRESS
         SR    R2,R2                                             X01004
         STCM  R2,AD,SCBXTRA            CLEAR FOR FA             X01004
         STCM  R2,AD,SCBNTXT            CLEAR FOR FA             X01004
         NI    SCBQTYPE,X0F             CLEAR QTYPE FOR EACH RECALL
         OI    LCBSTAT1,LCBRCLLN        SET LCB TO RECALL STATE
         MVI   LCBERBST,X00             CLEAR ERB STATUS
         LA    R0,D256                  RECALL 1 BUFFER
         STH   R0,LCBERBCT              SET ERB COUNT = 1
         STCM  R2,AD,LCBERBCH           CLEAR FOR RECALL         X01004
         LA    R2,AVTDSIOB              DISK I/O QCB
         LA    R5,LCBERB                ELEMENT TO BE POSTED
         LA    R9,PRIRECAL              RECALLED BUFFER PRIORITY
         B     EXIT                     ADD TO POST LIST AND EXIT
*
SVQTYP   EQU   *
         TM    SCBHBFNO,XF0             HAS QTYPE BEEN SAVED
         BCR   7,R9                     RETURN
*
         XC    SCBTRANS,SCBTRANS        CLEAR FOR FA
         MVI   SCBHBFNO,X00             CLEAR RESIDUAL COUTER
         MVZ   SCBHBFNO(1),SCBQTYPE     SET UP FOR POSSIBLE RECALL
         NI    SCBQTYPE,X0F             CLEAR HIGH ORDER HALH
         BR    R9                       RETURN
         EJECT
BBTM     TM    BBTHREE,0                EXECUTED TM
BASE     DC    A(IEDQBD01)              FOR CSECT ADDRESSABILITY
CLEARL2  DC    X'00FFFFFC'              MASK FOR 'ANDING'        S22025
MAXFREE  DC    F'-255'                  MAXIMUM UNIT COUNT       S22025
*                                                              @X50X9IL
*              THE FOLLOWING FIELD IS A MASK USED TO CHECK THE @X50X9IL
*              MESSAGE ERROR RECORD FOR THE PRESENCE OF ANY OF @X50X9IL
*              THE SNA MAJOR SENSE BITS                        @X50X9IL
*                   BIT 13 - REQUEST REJECT                    @X50X9IL
*                   BIT 21 - FUNCTION INTERPRETER ERROR        @X50X9IL
*                   BIT 23 - PATH ERROR                        @X50X9IL
*                   BIT 29 - CPM ERROR                         @X50X9IL
*                   BIT 30 - STATE ERROR                       @X50X9IL
         DS    0F                                              @X50X9IL
SNAMAJOR DC    X'00040506'                                     @X50X9IL
*                                                              @X50X9IL
ERBLCB   DC    AL2(LCBERB-IEDQLCB)      OFFSET OF ERB IN THE LCB S22025
BBTHREE  DC    AL1(3)                   INITIALIZE TO 3 FOR TM   S22025
ONE      EQU   1                        CONSTANT EQUATE         SA59976
TWO      EQU   2                        TWO                      S22024
X00      EQU   0                        ZERO                     S22024
AD       EQU   7                        MASK FOR ICM AND STCM    X01004
X01      EQU   X'01'                    HEX 01                   S22024
X02      EQU   X'02'                    HEX 02                   S22024
SNS      EQU   X'04'                    HEX 04                   S22024
X08      EQU   X'08'                    HEX 08                   S22024
X0F      EQU   X'0F'                    HEX 0F                   S22024
XFF      EQU   X'FF'                    HEX FF                   S22024
SKPFL    EQU   X'10'                    HEX 10                   S22024
X16      EQU   16                       SIXTEEN                  S22024
X80      EQU   X'80'                    HEX 80                   S22024
D256     EQU   256                      TWO56                    S22024
XF0      EQU   X'F0'                    HEX F0                   S22024
UNEX     EQU   X'01'                    UNIT EXCEPTION           S22024
PLCB     EQU   X'00'                    3705 PLCB                S22024
APLCB    EQU   X'12'                    APP PROG PUT LCB         S99228
WIDTH    EQU   2                        OFFSET IN INVITATION    SA59976
*                                        LIST CONTROL WORD TO   SA59976
*                                        WIDTH OF EACH ENTRY    SA59976
FE       EQU   X'FE'                    END OF LIST INDICATOR   SA59976
C3270    EQU   X'04'                    3270 TERMINAL          @OS76699
CLOCAL   EQU   X'02'                    LOCAL TERMINAL         @OS76699
         SPACE 2                                               @X50X9IL
MASKD    DSECT                          DSECT FOR ERROR MASK   @X50X9IL
         DS    CL3                      PARAMETER LIST FIELDS  @X50X9IL
MASK     DS    0CL5                     ERROR MASK             @X50X9IL
MASK4    DS    CL4                      1ST 4 BYTES OF MASK    @X50X9IL
MASKLAST DS    CL1                      LAST BYTE OF MASK      @X50X9IL
         EJECT
         SPACE 2
         DCBD  DSORG=TX                                          S99228
         TTRMD
         TPRIOR
         TQCBD
         TPRFD
         TLCBD
         TSCBD
         TDISPD
         TAVTD
         END
