AV01     TITLE '''IEDQAV'' - LOOK UP DESTINATION ROUTINE'
IEDQAV   CSECT
*A-000000-999999                                               @X02X5M0
*A244000,628000,632000                                         @Y17XAMG
*C708600,709200                                                @Y17XAMG
***********************************************************************
*                                                                     *
*STATUS -- CHANGE LEVEL 10                                       S22024
*                                                                     *
*FUNCTION -- PLACES THE DESTINATION KEY IN THE PREFIX AND THE QCB     *
*   ADDRESS IN THE SCB.                                               *
*                                                                     *
*   THE KEY (ORDINAL INDEX) FOR THE DESTINATION IS ACCESSED FROM      *
*   THE PARAMETER LIST AND STORED IN THE BUFFER PREFIX DESTINATION    *
*   INDEX FIELD (PRFDEST).                                            *
*                                                                     *
*   THE ROUTINE LINKS TO THE TERMINAL NAME TABLE CODE (AVTRNMPT),     *
*   PASSING THE DESTINATION KEY,  TO GET THE ADDRESS OF THE           *
*   TERMINAL TABLE ENTRY FOR THE DESTINATION.                         *
*                                                                     *
*   THE ADDRESS OF THE DESTINATION QCB IS ACCESSED FROM THE           *
*   TERMINAL TABLE ENTRY (TRMDESTQ) AND STORED IN THE SCB             *
*   DESTINATION QCB FIELD (SCBDESTQ).                                 *
*                                                                     *
*   THE ADDRESS OF THE SCB IS PLACED IN REGISTER 0 AND RETURN IS      *
*   MADE TO RETURN INTERFACE.                                         *
*                                                                     *
*ENTRY POINTS --                                                      *
*       'IEDQAV' TO PLACE THE DESTINATION KEY IN THE PREFIX AND THE   *
*   QCB ADDRESS IN THE SCB.  CALLING SEQUENCE FROM USER INTERFACE     *
*   IS:                                                               *
*        L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE        *
*        IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR     *
*        L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDRESS           *
*        BR    R12                      EXIT TO ROUTINE               *
*    ENTRY AT 'IEDQAV' MAY BE FROM IEDQA5 OR IEDQAZ.           @Y17XAMG
*                                                                     *
*INPUT --                                                             *
*   REGISTER 1 - THE ADDRESS OF A MACRO-GENERATED PARAMETER LIST.     *
*   PARAMETER LIST FORMAT IS:                                         *
*                                                                     *
*        *********************************                            *
*        *       * PARAM *               *                            *
*        * INDEX * LIST  *  DESTINATION  *                            *
*        *       * LNGTH *      KEY      *                            *
*        *********************************                            *
*                                                                     *
*                                                                     *
*   REGISTER 3 - THE ADDRESS OF THE SCB.                              *
*                                                                     *
*   REGISTER 6 - THE ADDRESS OF THE BUFFER.                           *
*                                                                     *
*   REGISTER 9 - THE ADDRESS OF THE AVT.                              *
*                                                                     *
*   REGISTER 12 - THE ENTRY POINT ADDRESS AND BASE REGISTER FOR       *
*   THIS ROUTINE.                                                     *
*                                                                     *
*OUTPUT --                                                            *
*   REGISTER 0 - THE ADDRESS OF THE SCB.                              *
*                                                                     *
*   BUFFER PREFIX DESTINATION INDEX FIELD (PRFDESTQ) - THE KEY        *
*   (ORDINAL INDEX) FOR THE DESTINATION.                              *
*                                                                     *
*   SCB DESTINATION QCB FIELD (SCBDESTQ) - THE ADDRESS OF THE         *
*   DESTINATION QCB.                                                  *
*                                                              @Y17XAMG
*   AVTPARM - THE DESTINATION TERMINAL TABLE ENTRY ADDRESS.    @Y17XAMG
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*                                                                     *
*   TERMINAL NAME TABLE CODE (AVTRNMPT) TO GET THE ADDRESS OF THE     *
*   TERMINAL TABLE ENTRY.                                             *
*                                                                     *
*   'IEDQLM' - RETURN INTERFACE ROUTINE FOR EXIT.                     *
*                                                                     *
*   AVT - ADDRESS VECTOR TABLE.                                       *
*                                                                     *
*   BUFFER CURRENTLY BEING PROCESSED.                                 *
*                                                                     *
*   SCB - STATION CONTROL BLOCK FOR CURRENT STATION.                  *
*                                                                     *
*   TERMINAL TABLE.                                                   *
*                                                                     *
*EXITS,  NORMAL -- THE BUFFER PREFIX DESTINATION INDEX FIELD          *
*   (PRFDESTQ) CONTAINS THE KEY (ORDINAL INDEX) FOR THE               *
*   DESTINATION.  THE SCB DESTINATION QCB FIELD (SCBDESTQ)            *
*   CONTAINS THE ADDRESS OF THE DESTINATION QCB.  REGISTER 0          *
*   CONTAINS THE ADDRESS OF THE SCB.                                  *
*                                                                     *
*EXITS,  ERROR -- N/A.                                                *
*                                                                     *
*TABLES/WORK AREAS -- N/A.                                            *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     *
*   PROBLEM PROGRAM MODE.                                             *
*                                                                     *
*   NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN     *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
*                                                                     *
***********************************************************************
         EJECT
********* REGISTER EQUATES *********
*
R0       EQU   0                        WORK REGISTER
         SPACE
R1       EQU   1                        PARAMETER REGISTER
*
R13      EQU   13                       ADDRESS OF SAVE AREA
R14      EQU   14                       RETURN ADDRESS REGISTER
R15      EQU   15                       ENTRY POINT AND RETURN CODE
         SPACE
********* OTHER EQUATES *********
         SPACE
ZERO     EQU   8                        CONDITION CODE: ZERO
EQUAL    EQU   X'08'                    CONDITION CODE: EQUAL
         SPACE
PUTPROCQ EQU   X'02'                    PUT PROCESS QUEUE FLAG
         EJECT
         USING AVTSAVE3,R13
         USING IEDQAV01,R15
         SPACE
IEDQAV01 EQU   *
IEDQAV   IEDHJN LOOKUP                                           S22024
         ST    R14,AVTDOUBL             SAVE RETURN ADDRESS      S22024
         L     R14,AVTADBUF             GET BUFFER ADDRESS FROM AVT
         USING IEDQPRF,R14
         LTR   R1,R1                    IS DESTINATION KEY PASSED
         BZ    LOADPFX                  NO, MUST GET IT FROM PREFIX
         SPACE
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER BUFFER
         BNO   SETPFX                   YES, SET PRFDEST FIELD
         SPACE
         STH   R1,AVTPARM3              NO, RETURN KEY IN AVT
         B     LINKTNT                  GO GET TERM ENTRY ADDR
         SPACE
SETPFX   EQU   *
         STH   R1,PRFDEST               SET DEST KEY IN PREFIX
         B     LINKTNT                  GO GET TERM ENTRY
         SPACE
LOADPFX  EQU   *
         LH    R1,PRFDEST               GET DEST FROM PREFIX
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALFWORD
         BNZ   LINKTNT                  NOT ZERO, PROCEED
         SPACE
         LH    R1,AVTDLQX               ZERO, GET DEAD-LTR Q KEY
         N     R1,AVTCLRHI              IS IT ZEROES TOO
         STH   R1,PRFDEST               SET IT AS DEST
         BNZ   LINKTNT                  NO, PROCEED
         SPACE
         LA    R15,AVTECD4              YES, RETURN ERROR RTN CODE
         L     R14,AVTDOUBL             RESTORE RETURN ADDRESS   S22024
         BR    R14                      RETURN TO CALLER
         SPACE
LINKTNT  EQU   *
         L     R15,AVTRNMPT             GET ADDRESS OF TERM ENTRY
         BALR  R14,R15                  BRANCH AND LINK          S22024
         SPACE
         L     R14,AVTDOUBL             RESTORE RETURN ADDRESS   S22024
         USING IEDQTRM,R1
         ST    R1,AVTPARM               RETURN TERM ENTRY ADDR
         LA    R15,TRMPRFSZ             TTE PREFIX SIZE        @Y17XAMG
         SR    R1,R15                   R1 = TRM PREFIX ADDR   @Y17XAMG
         LA    R15,AVTECD4              ASSUME BAD RETURN CODE @Y17XAMG
         USING IEDNTRM,R1               PREFIX BASE            @Y17XANG
         CLI   TRMDSORG,TRMLGB          IS THIS AN LGB?        @Y17XAMG
         BCR   EQUAL,R14                RETURN ON R14 IF SO    @Y17XAMG
         BALR  R15,0                    RESET PROGRAM BASE     @Y17XAMG
         USING *,R15                                           @Y17XAMG
         TM    TRMSTATE,TRMSNGLE        SINGLE RESOURCE?       @Y17XAMG
         BNZ   LDQCB                    BRANCH IF NOT          @Y17XAMG
         TM    TRMSTATE,TRMPREF         NON-270X RESOURCE?     @Y17XAMG
         BZ    LDQCB                    BRANCH 270X            @Y17XAMG
         LA    R15,AVTECD4              ASSUME BAD RETURN CODE @Y17XAMG
         CLI   TRMTYPE,TRMSSCP          SSCP TTE?              @Y17XAMG
         BCR   EQUAL,R14                BRANCH IF SO           @Y17XAMG
         CLI   TRMTYPE,TRMLNCP          LOCAL NCP TTE?         @Y17XAMG
         BCR   EQUAL,R14                BRANCH IF SO           @Y17XAMG
         CLI   TRMTYPE,TRMPUNT          PU TTE?                @Y17XAMG
         BCR   EQUAL,R14                BRANCH IF SO           @Y17XAMG
LDQCB    EQU   *                                               @Y17XAMG
         LA    R15,AVTECD4              ASSUME BAD RETURN CODE @Y17XAMG
         DROP  R15                                             @Y17XAMG
         L     R1,TRMDESTQ-1            GET QCB ADDRESS
         DROP  R1                                              @Y17XAMG
         SPACE
         USING IEDQQCB,R1                                           TSO
         TM    QCBDSFLG,QCBDISK+QCBCORE ANY QUEUING FOR THIS TERM?  TSO
         BCR   ZERO,R14                 BRANCH ON NO                TSO
         SPACE
*(A DISTRIBUTION OR CASCADE LIST ENTRY WOULD ALSO HAVE A NONZERO
* CONDITION HERE BECAUSE THEY HAVE A LETTER IN THE HIGH ORDER BYTE
* OF THEIR RESPECTIVE DUMMY QCBS.)
         SPACE
         SPACE
         LR    R0,R1                    SAVE QCB ADDRESS
         L     R1,AVTADBUF              GET BUFFER ADDRESS FROM AVT
         DROP  R14
         USING IEDQPRF,R1
         L     R1,PRFLCB-1              GET LCB ADDRESS FROM BUFFER
         USING IEDQLCB,R1
         L     R14,AVTPARM              ADDRESS OF TERMINAL ENTRYS22024
         USING IEDQTRM,R14                                       S22024
         TM    TRMSTATE,TRMPREF         IS DEVICE ON 3705      @Y17XAMG
         BALR  R15,0                    SET BASE FOR BRANCH      S22024
         USING *,R15                                             S22024
         BZ    CHKQCB                   BRANCH IF NOT 3705     @Y17XAMG
         L     R15,AVTADBUF             ADDRESS OF BUFFER        S22024
         USING IEDQPRF,R15                                       S22024
         MVC   LCBTTBIN(2),PRFDEST      SAVE DESTINATION IN LCB  S22024
CHKQCB   EQU   *                                                 S22024
         LR    R14,R0                   ADDRESS OF QCB           S22024
         USING IEDQQCB,R14                                       S22024
         CLI   QCBDSFLG,PUTPROCQ        IS DEST PUT PROC QCB     S22024
         LA    R15,AVTECD4              ASSUME BAD RETURN CODE   S22024
         L     R14,AVTDOUBL             RESTORE RETURN ADDRESS   S22024
         BCR   EQUAL,R14                YES, ERROR, RETURN       S22024
         L     R1,LCBSCBA-1             GET SCB ADDRESS FROM LCB
         USING IEDQSCB,R1
         IC    R15,SCBDESTQ-1
         ST    R0,SCBDESTQ-1            SET DEST QCB ADDRESS IN SCB
         STC   R15,SCBDESTQ-1
         SR    R15,R15                  0 EQUALS GOOD RETURN        TSO
         BR    R14                      RETURN
         EJECT
********* DSECTS *********
         SPACE 2
         TAVTD
         EJECT
         TLCBD
         EJECT
         TPRFD
         EJECT
         TQCBD
         EJECT
         TSCBD
         EJECT
         TTRMD
         DCBD  DSORG=TX                                          S22024
         END
