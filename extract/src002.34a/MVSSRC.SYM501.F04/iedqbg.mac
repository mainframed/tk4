BG01     TITLE '''IEDQBG'' - CASCADE LIST SUBTASK'
IEDQBG   CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS;
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A424000,570000,588000,882000                                    A47149
*C312000,324000-348000,360000-372000,384000,396000,433000,434000 S21903
*A885000,900000                                                  S22024
*C258000,433000,805000                                           S22024
*C 574000                                                      @SA70004
*C574000,594000-612000 MOVED AFTER 570000                      @OZ18779
*A096000,870000,885100                                         @OZ25426
*C216000,270000-276000,810000-828000                           @OZ25426
* DUMMY APAR                                                   @OZ27328
         SPACE 3
***********************************************************************
*TITLE -- 'IEDQBG' CASCADE LIST SUBTASK                               *
*                                                                     *
*  MODULE NAME = IEDQBG                                               *
*                                                                     *
*  DESCRIPTIVE NAME = CASCADE LIST SUBTASK                            *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
*                                                                     *
*FUNCTION -- THIS SUBTASK GAINS CONTROL WHEN A MESSAGE HAS BEEN       *
*   SENT TO A CASCADE LIST ENTRY IN THE TERMINAL TABLE. EACH          *
*   TERMINAL IN THE LIST IS EXAMINED IN ORDER, SEARCHING FOR THE      *
*   FIRST TERMINAL THAT CAN ACCEPT, IS NOT HELD, H AS AN OPEN DCB AND *
*   HAS THE FEWEST MESSAGES QUEUED FOR IT. THE MESSAGE IS THEN        *
*   TPOSTED TO THE DESTINATION QCB FOR THAT TERMINAL. IF A TERMINAL   *
*   THAT MEETS ALL THESE CONDITIONS CAN NOT BE FOUND, THE MESSAGE     *
*   IS TPOSTED TO THE QCB FOR THE FIRST ENTRY IN THE LIST.            *
*   IF THE CHOSEN TERMINAL IS A TPROCESS ENTRY WITH DATE=YES  @OZ25426*
*   SPECIFIED, THEN YOU BALR TO IEDQB3 TO INSERT THE DATE     @OZ25426*
*   AND TIME                                                  @OZ25426*
*                                                                     *
*ENTRY POINT -- IEDQBG                                                *
*                                                                     *
*   CALLING SEQUENCE -                                                *
*                  L     R1,ELMAD                                     *
*                  L     R15,QCBSTCHN-1                               *
*                  LA    R15,4(R14)                                   *
*                  BR    R15                                          *
*                                                                     *
*INPUT -- ENTRY IS ALWAYS FROM THE TCAM DISPATCHER                    *
*         PARAMETERS ARE PASSED IMPLICITLY IN REGISTERS AS FOLLOWS:   *
*                  R1    ADDRESS OF THE BUFFER                        *
*                  R11   BASE FOR TCAM DISPATCHER                     *
*                  R13   ADDRESS OF AVTSAVE2                          *
*                  R15   BASE FOR THIS ROUTINE                        *
*                                                                     *
*OUTPUT -- THE MESSAGE IS TPOSTED TO A DESTINATION QCB. THE SCB AND   *
*   PREFIX DESTINATION FIELDS ARE SET FOR THE NEW DESTINATION.        *
*                                                                     *
*EXTERNAL ROUTINES -- IEDQTNT,IEDQB3                          @OZ25426*
*                                                                     *
*EXITS - NORMAL -- THE TCAM DISPATCHER AT ENTRY POINT DSPPOST         *
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORKAREAS --                                                  *
*   DSECTS USED - LCB,SCB,AVT,DIS,TERM,PRF,DCB,QCB,LGB          S22024*
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED,       @OZ25426*
*              RESIDENT, PROBLEM PROGRAM MODE.                @OZ25426*
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET  *
*                                                                     *
***********************************************************************
R0       EQU   0                        REGISTER EQUATE          S21903
R1       EQU   1                        PASSED ELEMENT (BUFFER)
R2       EQU   2                        REGISTER EQUATE          S21903
R3       EQU   3                        REGISTER EQUATE          S21903
R4       EQU   4                        REGISTER EQUATE          S21903
R5       EQU   5                        REGISTER EQUATE          S21903
RPREFIX  EQU   6                        REGISTER EQUATE          S21903
RQCB     EQU   7                        LAST DISPATCHER QCB
R8       EQU   8                        REGISTER EQUATE          S21903
R9       EQU   9                        REGISTER EQUATE          S21903
R10      EQU   10                       REGISTER EQUATE          S21903
R11      EQU   11                       ADDRESS OF DISPATCHER
R12      EQU   12                       REGISTER EQUATE          S21903
R13      EQU   13                       DISPATCHER SAVE AREA (AVTSAVE2)
R14      EQU   14                       REGISTER EQUATE          S21903
R15      EQU   15                       SUBTASK ENTRY POINT
         EJECT
         USING *,RQCB
         DC    CL8'IEDQBG01'            QCB/STCB
*                                       (IEDQBA EXAMINES 6TH BYTE --
*                                           C INDICATES DISTRIBUTION
*                                             LIST
*                                           G INDICATES CASCADE LIST)
         DC    AL1(DSPMCPL4)            SUBTASK ENTRY CODE
         DC    AL3(*-1)                 STCB ADDRESS
IEDQBG   IEDHJN ID,HJN                                           S22024
         EJECT
         USING AVTSAVE2,R13
         USING IEDQDISP,R11
         L     R15,AVTRNMPT             ADDRESS OF TERMNAME TABLE
         LR    RPREFIX,R1               BUFFER ADDRESS
         USING IEDQPRF,RPREFIX
         LH    R1,PRFDEST               CASCADE LIST LOCATION IN THE
*                                         TERMINAL NAME TABLE
         BAL   R14,CLFNDTT              GET TERMINAL TABLE ADDRESS
         USING IEDQTRM,R1
         LR    R5,R1                    SAVE CASCADE LIST ENTRY ADDRESS
         SR    R8,R8                    CLEAR QCB REGISTER
         LR    R2,R5                    SAVE LIST ADDRESS
         LH    R3,TLISTCNT              COUNT OF LIST ENTRIES
CLSTP    EQU   *
         LA    R2,X2(,R2)               FIRST/NEXT ENTRY
         LH    R1,TLISTEN-IEDQTRM-2(R2)     FIRST/NEXT ENTRY
         LR    R10,R1                   S AVE OFFSETT
         BAL   R14,CLFNDTT              GET TERMINAL TABLE ADDRESS
*
         TM    TRMSTATE,TRMACPTN        CAN TERMINAL ACCEPT ?
         BZ    CLNEXT                     BRANCH NO
*
         L     R9,TRMDESTQ-1            DESTINATION QCB
         USING IEDQQCB,R9
         L     R14,QCBDCBAD-1           DCB ADDRESS
         USING IHADCB,R14
         TM    TRMSTATE,X20             IS THIS PROCESS ENTRY    A47149
         BO    NOTLGB                   YES,BYPASS             @OZ18779
         TM    TRMSTATE,TRMHELDN        IS TERMINAL HELD ?
         BO    CLNEXT                     BRANCH YES
NOTLGB   EQU   *
         TM    DCBOFLGS,X10             IS DCB OPEN
         BZ    CLNEXT                     BRANCH NO
*
         LTR   R8,R8                    ANY DESTINATION FOUND YET
         BZ    CLSWAP                     BRANCH NO, USE NEW
*
         CLC   QCBMSGCT,QCBMSGCT-IEDQQCB(R8)
*                                       COMPARE COUNT OF MESSAGES IN
*                                         QUQUE WITH COUNT IN
*                                         PREVIOUSLY FOUND QCB,
*                                         SEARCHING FOR LOWEST
*
         BNL   CLNEXT                   CHECK NEXT ENTRY
*
CLSWAP   EQU   *
         LR    R8,R9                    USE NEW QCB
         LR    R4,R10                   USE NEW OFFSETT
*
CLNEXT   EQU   *
         BCT   R3,CLSTP                 CONTINUE IF NOT END OF LIST
         LTR   R8,R8                    WAS AN AVAILABLE DESTINATION
*                                         FOUND
         BNZ   CLPOST                   BRANCH YES
*
         LH    R1,TLISTEN-IEDQTRM(R5)   USE FIRST ENTRY
         LR    R4,R1                    SET TO PICK UP FIRST
         BAL   R14,CLFNDTT              GET TERMINAL TABLE ADDRESS
*
         L     R8,TRMDESTQ-1            DESTINATION QCB
CLPOST   EQU   *
         STH   R4,PRFDEST               SET DESTINATION
         L     R4,PRFLCB-1              LCB ADDRESS
         L     R5,LCBDCBPT-IEDQLCB(R4)  ADDRESS OF DCB/LGB       S22024
         TM    LGBDSORG-IEDNLGB(R5),LG  IS THIS AN LGB ?         S22024
         BZ    BGDCB                    BRANCH ON NO             S22024
         MVC   LCBTTBIN-IEDQLCB(2,R4),PRFDEST  PLCB DESTINATION  S22024
BGDCB    EQU   *                                                 S22024
         L     R3,LCBSCBA-IEDQLCB-1(R4) SCB ADDRESS            @OZ25426
         IC    R2,SCBDESTQ-IEDQSCB-1(R3)  SAVE HIGH ORDER BYTE @OZ25426
         ST    R8,SCBDESTQ-IEDQSCB-1(R3)  STORE DESTINATION QCB@OZ25426
         STC   R2,SCBDESTQ-IEDQSCB-1(R3)  RESTORE SAVED BYTE   @OZ25426
         SR    R2,R2                    CLEAR REG              @OZ25426
         IC    R2,TRMSTATE              GET STATUS FLAG        @OZ25426
         SRL   R2,OPTOUT                SHIFT OUT UNWANTED OPTS@OZ25426
         CH    R2,AVTHA3                PROCESS ENTRY WITH DATE@OZ25426
         BNE   POSTBUF                  NO, BRANCH             @OZ25426
         L     R2,QCBDCBAD-IEDQQCB-1(R8)  ADDRESS OF PCB       @OZ25426
         L     R15,PCBDTRTN-IEDQPCB-1(R2)  ADDRESS OF DATE MOD @OZ25426
         LA    R15,0(R15)               CLEAR HIGH BYTE        @OZ25426
         LTR   R15,R15                  WAS ADDRESS THERE      @OZ25426
         BZ    POSTBUF                  NO, BRANCH             @OZ25426
         LR    R9,R13                   ADDRESS OF AVTSAVE2    @OZ25426
         SH    R9,BEGINAVT              BACK UP TO START OF AVT@OZ25426
         LA    R13,SAVE                 GET ADDR OF SAVEAREA   @OZ25426
         BALR  R14,R15                  LINK TO DATE ROUTINE   @OZ25426
         LR    R13,R9                   ADDRESS OF AVT         @OZ25426
         AH    R13,BEGINAVT             RESTORE AVTSAVE2 ADDR  @OZ25426
POSTBUF  EQU   *                                               @OZ25426
         LR    R1,RPREFIX               ELEMENT TO POST IS BUFFER
         ST    R8,PRFQCBA-1             QCB ADDRESS TO RECB
         MVI   PRFPRI,PRIDESTQ          PRIORITY OF ELEMENT
         BAL   R14,DSPPOST        EXIT TO DISPATCHER - POST    @Y17XAMX
*                                         BUFFER TO DESTINATION
CLFNDTT  N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF WORD
         BR    R15                        LINK TO TNT CODE
SAVE     DS    18F                      SAVEAREA FOR IEDQB3    @OZ25426
BEGINAVT DC    H'72'                    LENGTH OF SAVEAREA     @OZ25426
OPTOUT   EQU   4                        SHIFT LENGTH           @OZ25426
X2       EQU   2                        TWO                      S22024
X10      EQU   X'10'                    HEX 10                   S22024
X20      EQU   X'20'                    HEX 20                   S22024
LG       EQU   X'80'                    LINE GROUP BLOCK         S22024
         EJECT                                                 @OZ25426
         TPCBD                                                 @OZ25426
         EJECT                                                 @OZ25426
         TPRIOR
         TDISPD
         TTRMD
         EJECT
         TLGBD                                                   S22024
         EJECT
         TPRFD
         TSCBD
         TLCBD
         DCBD  DSORG=TX
         TQCBD
         TAVTD
         END
