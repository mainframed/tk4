AM01     TITLE '''IEDQAM'' - ORIGIN ROUTINE'
IEDQAM   CSECT
*A000000-000000                                                @Z37XAMF
*A504000,627000                                                  S22024
*C021000                                                         S22024
*C513000-705000                                                  S22025
*A504000,681000,771000                                          SA69092
*C594000,663180,663450                                          SA69092
*A633000,735000                                                @ZA00209
*A075000,484200,485200,615000,627000,660000,6635000,705000     @Y17XAMF
*C594000                                                       @OS77960
*A593200                                                       @OZ26896
*C621320,663069                                                @OZ26896
*D627600-629400                                                @OZ26896
***********************************************************************
*                                                                     *
*TITLE 'IEDQAM' ORIGIN ROUTINE                                        *
*                                                                     *
*                                                                     *
*STSTUS -- CHANGE LEVEL 5                                        S22024
*                                                                     *
*                                                                     *
*FUNCTION -- VERIFIES THE ORIGIN OF THE MESSAGE WHEN IT IS SPECIFIED  *
*   OR INITIALIZES IT WHEN IT IS NOT.                                 *
*                                                                     *
*   THE RETURN CODE FROM BINARY SEARCH IS TESTED, IF ZERO THE NAME    *
*   FROM THE BUFFER WAS NOT FOUND IN THE TERMINAL NAME TABLE.  THE    *
*   'INVALID ORIGIN' BIT IS SET IN THE SCB STATUS FIELD (SCBSTAT1)    *
*   AND EXIT IS MADE TO RETURN INTERFACE WITH A RETURN CODE OF        *
*   X'04' IN REGISTER 15.                                             *
*                                                                     *
*   IF THE RETURN CODE IS NOT ZERO,  IT IS THE KEY (ORDINAL INDEX)    *
*   TO THE TERMINAL TABLE ENTRY FOR THE NAME FOUND.  THIS KEY IS      *
*   COMPARED WITH THE KEY IN THE BUFFER PREFIX SOURCE FIELD           *
*   (PRFSRCE).  IF THEY ARE EQUAL,  THE KEY IN THE PREFIX IS          *
*   CORRECT.  EXIT IS MADE TO RETURN INTERFACE WITH A RETURN CODE     *
*   OF X'00' IN REGISTER 15.                                          *
*                                                                     *
*   IF THE KEYS ARE EQUAL, A VALIDITY CHECK IS THIS MODULES    @PI06026
*   ONLY FUNCTION.  IF THE KEYS ARE NOT EQUAL AND THE TERMINAL @PI06026
*   IS NOT A DIAL RESOURCE, AN ERROR IS RETURN TO USER.        @PI06026
*   IF THE KEYS ARE NOT EQUAL AND THE TERMINAL IS A DIAL       @Y17XAMF
*   RESOURCE,  THE FOLLOWING LOGIC THEN OCCURS:                @Y17XAMF
*                                                              @Y17XAMF
*   A NON-ZERO PRFSRCE IS VALIEDATED AS FOLLOWS.   IF AN EP    @Y17XAMF
*   RESOURCE, PRFSRCE MUST REFLECT A UTERM. IF A NCP RESOURCE  @Y17XAMF
*   PRFSRCE MUST REFLECT A CTERM.                              @Y17XAMF
*                                                              @Y17XAMF
*   THE ORIGIN TTE IS THEN VALIDATED BY CHECKING THAT IF IT    @Y17XAMF
*   IS AN EP RESOURCE IT MUST BE A SINGLE, NON-LGB ENTRY.      @Y17XAMF
*   IF IT IS A NCP RESOURCE, IT MUST BE A PRE-SNA DEVICE.      @Y17XAMF
*                                                              @Y17XAMF
*   AFTER VALIDATING THE TTE TYPES, THE MODULE INSURES THAT    @Y17XAMF
*   BOTH RESOURCES HAVE THE SAME DCB(LGB) ADDRESS.             @Y17XAMF
*                                                              @Y17XAMF
*   IF THE ORIGIN NAME IS AN EP RESOURCE THE PRFSRCE AND       @Y17XAMF
*   LCBTTCIN IS UPDATED WITH THE ORIGINS TTCIN AND 0 RTN CODE  @Y17XAMF
*   IS SET.   IF IT IS AN NCP RESOURCE, IEDDVAL IS CALLED IN   @Y17XAMF
*   ORDER TO CHANGE THE CONTROL BLOCKS SO THAT ALL COMMUNIC-   @Y17XAMF
*   ATION WILL OCCUR WITH THE DIAL ENTRY.  LCBTTCIN AND        @Y17XAMF
*   PRFSRCE ARE THEN UPDATED WITH THE DIAL TTE'S TTCIN, AND    @Y17XAMF
*   A 0 RETURN CODE IS SET.                                    @Y17XAMF
*                                                              @Y17XAMF
*                                                              @Y17XAMF
*                                                              @Y17XAMF
*                                                              @Y17XAMF
*                                                              @Y17XAMF
*                                                              @Y17XAMF
*                                                                     *
*ENTRY POINTS --                                                      *
*   'IEDQAM01' TO VERIFY OR INITIALIZE THE ORIGIN. CALLING SEQUENCE   *
*   IS:                                                               *
*                                                                     *
*        L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE        *
*        IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR     *
*        L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDRESS           *
*        BR    R12                      EXIT TO ROUTINE               *
*                                                                     *
*INPUT -- REGISTER 1 - ADDRESS OF A MACRO-GENERATED PARAMETER LIST.   *
*                                                                     *
*   REGISTER 3 - ADDRESS OF THE SCB.                                  *
*                                                                     *
*   REGISTER 4 - ADDRESS OF THE LCB.                                  *
*                                                                     *
*   REGISTER 6 - ADDRESS OF THE BUFFER.                               *
*                                                                     *
*   REGISTER 9 - ADDRESS OF THE AVT.                                  *
*                                                                     *
*   REGISTER 12 - ENTRY POINT ADDRESS AND BASE REGISTER.              *
*                                                              @Y17XAMF
*   REGISTER 13 - REGISTER SAVE AREA ADDR PASSED BY IEDQUI.    @Y17XAMF
*                                                                     *
*   REGISTER 14 - RETURN REGISTER.                                    *
*                                                                     *
*   REGISTER 15 - LINK REGISTER AND RETURN CODE REGISTER.             *
*                                                                     *
*OUTPUT -- REGISTER 15 - ON SUCCESSFUL COMPLETION,  A RETURN CODE     *
*   OF X'00'.  IF THE ORIGIN SPECIFIED BY THE NAME IN THE BUFFER IS   *
*   INVALID,  A RETURN CODE OF X'04'.                                 *
*                                                                     *
*   BUFFER PREFIX SOURCE FIELD (PRFSRCE) - IF ZERO ON ENTRY,  SET     *
*   TO THE KEY TO THE CORRECT TERMINAL ON SUCCESSFUL COMPLETION.      *
*                                                                     *
*   LCB TERMINAL-CURRENTLY-CONNECTED FIELD (LCBTTCIN) - IF PRFSRCE    *
*   IS ZERO ON ENTRY,  SET TO THE KEY TO THE CORRECT TERMINAL ON      *
*   SUCCESSFUL COMPLETION.                                            *
*                                                                     *
*   SCB STATUS FIELD (SCBSTAT1) - IF RETURN FROM BINARY SEARCH        *
*   ROUTINE IS ZERO,  IF PRFSRCE CONTAINS A KEY DIFFERENT FROM THE    *
*   KEY RETURNED BY BINARY SEARCH,  OR IF THE TERMINAL INDICATED BY   *
*   THE KEY RETURNED BY BINARY SEARCH IS NOT IN THE DCB INDICATED     *
*   BY THE LCB FROM THE BUFFER PREFIX,  THE 'INVALID ORIGIN' BIT IS   *
*   SET ON.                                                           *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*   'IEDQA1' - BINARY SEARCH ROUTINE                             S22025
*   'IEDDVAS'- DEVICE ASSOCIATION ROUTINE(DIAL SUPPORT)        @Y17XAMF
*                                                                     *
*   TERMINAL NAME TABLE CODE (AVTRNMPT)  TO GET THE ADDRESS OF THE    *
*   TERMINAL TABLE ENTRY.                                             *
*                                                                     *
*   AVT - ADDRESS VECTOR TABLE.                                       *
*                                                                     *
*   LCB - LINE CONTROL BLOCK.                                         *
*                                                                     *
*   SCB - STATION CONTROL BLOCK.                                      *
*                                                                     *
*   QCB - QUEUE CONTROL BLOCK.                                        *
*                                                                     *
*   TERMINAL NAME TABLE.                                              *
*                                                                     *
*   TERMINAL TABLE.                                                   *
*                                                                     *
*   BUFFER CURRENTLY BEING PROCESSED.                                 *
*                                                                     *
*EXITS,  NORMAL --                                                    *
*   THE KEY IN THE BUFFER PREFIX SOURCE FIELD (PRFSRCE) MATCHES       *
*   THE KEY FOR THE NAME FOUND IN THE BUFFER.  REGISTER 15 CONTAINS   *
*   A RETURN CODE OF X'00'.                                           *
*                                                                     *
*   PRFSRCE,  ZEROES ON ENTRY,  IS INITIALIZED TO THE KEY FOR THE     *
*   NAME FOUND IN THE BUFFER.  THE LCB TERMINAL-CURRENTLY-CONNECTED   *
*   FIELD (LCBTTCIN) IS INITIALIZED TO THE SAME KEY.  REGISTER 15     *
*   CONTAINS A RETURN CODE OF X'00'.                                  *
*                                                                     *
*EXITS,  ERROR --                                                     *
*   THE NAME FOUND IN THE BUFFER IS NOT FOUND IN THE TERMINAL NAME    *
*   TABLE (AVTRNMPT).  THE 'INVALID ORIGIN' BIT IN THE SCB STATUS     *
*   FIELD (SCBSTAT1) IS SET ON.  REGISTER 15 CONTAINS A RETURN CODE   *
*   OF X'04'.                                                         *
*                                                                     *
*   THE KEY FOR THE NAME FOUND IN THE BUFFER DOES NOT MATCH THE KEY   *
*   IN PRFSRCE.  THE 'INVALID ORIGIN' BIT IN SCBSTAT1 IS SET ON.      *
*   REGISTER 15 CONTAINS A RETURN CODE OF X'04'.                      *
*                                                                     *
*   THE TERMINAL FOR THE NAME FOUND IN THE BUFFER IS NOT ASSOCIATED   *
*   WITH THE SAME DCB AS THE LCB WHOSE ADDRESS IS IN THE BUFFER       *
*   (PRFLCB).  THE 'INVALID ORIGIN' BIT IN SCBSTAT1 IS SET ON.        *
*   REGISTER 15 CONTAINS A RETURN CODE OF X'04'.                      *
*                                                                     *
*TABLES/WORK AREAS -- N/A.                                            *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     *
*   PROBLEM PROGRAM MODE.                                             *
*                                                                     *
*NOTE -- THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS             *
*   EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS      *
*   BEEN ARRANGED SO THAT REDEFINITION OF ''CHARACTER'' CONSTANTS,    *
*   BY REASSEMBLY, WILL RESULT IN A CORRECT MODULE FOR THE NEW        *
*   DEFINITIONS.                                                      *
*                                                                     *
***********************************************************************
         EJECT
********* REGISTER EQUATES *********
         SPACE
R0       EQU   0                        WORK REGISTER
         SPACE
R1       EQU   1                        PARAMETER LIST ADDRESS
RWORK2   EQU   2                        WORK REGISTER            S22025
RSCB     EQU   3                        SCB ADDRESS              S22025
R3       EQU   3                        WORK REG               @Y17XAMF
RLCB     EQU   4                        LCB ADDRESS              S22025
RLEN     EQU   5                        LENGTH OF PASSED FIELD   S22025
RQCB2    EQU   5                        SECOND QCB BASE          X03039
RPREFIX  EQU   6                        BUFFER ADDRESS           S22025
RQCB     EQU   7                        QCB ADDRESS              S22025
RSAVT    EQU   8                        SAVT ADDRESS           @Y17XAMF
RAVT     EQU   9                        AVT ADDRESS              S22025
RPARLEN  EQU   10                       LENG PARM FROM SCAN ROUT S22025
RIOB     EQU   11                       IOB ADDRESS              S22025
RTERM2   EQU   11                       SECOND TERMINAL ENTRY PTRX03039
RBASE    EQU   12                       BASE REGISTER            S22025
         SPACE
R13      EQU   13                       SAVE AREA ADDRESS      @Y17XAMF
R14      EQU   14                       RETURN ADDRESS
R15      EQU   15                       LINK ADDRESS AND
*                                         RETURN CODE REGISTER
TWO      EQU   2                        OFFSET FOR CHAR LENGTH   S22025
FOUR     EQU   4                        OFFSET FOR BRANCH        S22025
EIGHT    EQU   8                        OFFSET FOR A1 ADDR       S22025
ADLEN    EQU   3                        LENGTH OF A 3-BYTE ADDRESS
LCBBKUP  EQU   X'20'                                            SA69092
         EJECT
         USING IEDQPRF,RPREFIX                                   S22025
         USING IEDQLCB,RLCB                                      S22025
         USING IEDQSCB,RSCB                                      S22025
         USING IEDQAVTD,RAVT                                     S22025
         USING IEDQAM01,RBASE                                    S22025
IEDQAM01 EQU   *                                                 S22025
IEDQAM   IEDHJN SKIP                    MODULE ID                S22025
         LA    R1,2(R1)                 INDEX TO SCANS PARM LIST S22025
         L     R15,AVTUI                GET RET INTERFACE ROUT   S22025
         BALR  R14,R15                  BRANCH TO SCAN ROUTINE   S22025
         SPACE 1
         LTR   R15,R15                  CHECK FOR RUNOUT         S22025
         BM    RETURN                   YES, RETURN TO CALLER    S22025
         SPACE 1
         SPACE 1                                                 S22025
         LA    R1,AVTDOUBL              ADDR OF CAHR STRING      S22025
         SR    R0,R0                    CLEAR REGISTER           S22025
         IC    R0,AVTPARM+1             CHAR STRING LENGTH       S22025
         L     R15,AVTMSGS-1            GET VCON TABLE ADDR      S22025
         L     R15,EIGHT(R15)           LOAD ADDR OF A1 ROUTINE  S22025
         BAL   R14,FOUR(R15)            LINK TO BINARY SRCH ROUT S22025
         SPACE 1                                                 S22025
PROCESS  EQU   *                                                 S22025
         LR    R1,R15                   LOAD TERMNAME INDEX      S22025
         LR    RWORK2,R1                SAVE KEY                 S22025
         LTR   R1,R1                    IS KEY ZERO              S22025
         BZ    SETBIT3                  YES, ERROR, BRANCH       S22025
         SPACE 1                                                 S22025
         CH    RWORK2,PRFSRCE           IS PREFIX KEY EQ TO KEY  S22025
         BNE   CKSOURCE                 NO, CHECK SOURCE       @OS77960
         TM    LCBSTAT2,LCBDIAL         DIAL TERMINAL ?        @OS77960
         BZ    GOODRTN                  NO, GO SET GOOD CODE   @OS77960
*                                        AND RETURN            @OS77960
         CLI   LCBFLAG1,LCBPLCB         NCP TERMINAL           @OZ26896
         BZ    GOODRTN                  SET GOOD RETURN        @OZ26896
         B     PASSKEY                  YES, VERIFY TERM NOT   @OS77960
*                                        ACTIVE ON MORE THAN   @OS77960
*                                        ONE LCB               @OS77960
CKSOURCE EQU   *                                               @OS77960
         SPACE 1                                                 S22025
         LH    R1,PRFSRCE               LOAD KEY FROM PREFIX     S22025
         LTR   R1,R1                    IS IT ZERO               S22025
         BZ    PASSKEY                  YES, GET ENTRY FOR KEY   S22025
         SPACE 1                                                 S22025
         L     R15,AVTRNMPT             NO, GET ADDR OF TNT CODE S22025
         BALR  R14,R15                  GET TERM ENTRY ADDR      S22025
         SH    R1,TPREFIX               GET NEG PREFIX ADDRESS @Y17XAMF
         SPACE 1                                                 S22025
         USING IEDNTRM,R1                                        S22025
         LR    RTERM2,R1                SAVE ORIGINAL TERMENTRY  X03039
         TM    TRMSTATE,TRMPREF         ATTACHED TO 3705       @Y17XAMF
         BO    PASSKEY                  YES, AVOID LINE CHECK  @OZ26896
         TM    TRMSTATE,TRMLINE         IS THIS A LINE           X03039
         BNO   SETBIT3                  NO, ERROR, BRANCH        S22025
         SPACE 1                                                 S22025
PASSKEY  EQU   *                                                 S22025
         CLI   LCBRSKEY,DSPPUTSC        IS IT APPL PROG        @ZA00209
         BE    SETTCIN                  BR YES                 @ZA00209
         LR    R1,RWORK2                PASS KEY FOR SPEC NAME   S22025
         L     R15,AVTRNMPT             GET ADDR OF TNT CODE     S22025
         BALR  R14,R15                  GET TERM ENTRY ADDR      S22025
         SH    R1,TPREFIX               GET NEG PREFIX ADDRESS @Y17XAMF
         SPACE 1                                                 S22025
         L     RQCB,TRMDESTQ-1          QCB ADDR FROM TERM ENTRY S22025
         USING IEDQQCB,RQCB                                      S22025
         CLC   LCBDCBPT+1(ADLEN),QCBDCBAD  DO LCB & QCB POINT    S22025
*                                          TO THE SAME DCB       S22025
         BNE   SETBIT3                  NO, ERROR, BRANCH        S22025
         SPACE 1                                                 S22025
         TM    TRMSTATE,TRMPREF         ATTACHED TO 3705       @Y17XAMF
         BO    IS3705                   BRANCH IF YES          @Y17XAMF
         TM    TRMSTATE,TRMSNGLE        IF 270X, ORIGIN MUST   @Y17XAMF
         BNZ   SETBIT3                   BE A SINGLE ENTRY     @Y17XAMF
         TM    TRMDSORG,TRMLGB          AND MUST NOT BE A LGB  @Y17XAMF
         BO    SETBIT3                                         @Y17XAMF
         B     CKLCBS                                          @Y17XAMF
IS3705   DS    0H                                              @Y17XAMF
         NC    TRMCOHRT(L'TRMCOHRT),TRMCOHRT IF FIRST ORIGIN   @Y17XAMF
         BZ    FSTORG                    DO NOT GET LINE TTE   @Y17XAMF
         ST    R1,SAVREG                SAVE R1                @Y17XAMF
         LH    R1,TRMCOHRT              GO GET COHORT TTE ADDR @Y17XAMF
         N     R1,AVTCLRHI              MAKE POSTIVE           @Y17XAMF
         L     R15,AVTRNMPT             LOAD CONV ROUTINE ADDR @Y17XAMF
         BALR  R14,R15                  GO GET TTE ADDRESS     @Y17XAMF
         SH    R1,TPREFIX               GET PREFIX ADDR        @Y17XAMF
         CH    RWORK2,TRMSUBST          IF SAME TTCIN AS ORIGIN@Y17XAMF
         BE    SETBIT3                   BRANCH                @Y17XAMF
         L     R1,SAVREG                RELOAD R1 WITH ORIGIN  @Y17XAMF
*                                        TTE ADDR              @Y17XAMF
FSTORG   DS    0H                                              @Y17XAMF
         CLI   TRMTYPE,TRMPSNA          ORIGIN MUST BE PRESNA  @Y17XAMF
         BE    TSTCONT                  YES, CHECK CONTACT     @OZ26896
         CLI   TRMTYPE,TRMCTERM         IS IT A CTERM          @OZ26896
         BNE   SETBIT3                  IF NOT, ERROR          @OZ26896
TSTCONT  EQU   *                                               @OZ26896
         TM    TRMPRE1,TRMCONT          IS CONTACT OUTSTANDING @Y17XAMF
         BO    SETBIT3                  IF YES, BRANCH         @Y17XAMF
         LR    R1,RWORK2                GET NEW ORIGIN INDEX   @YM09050
         L     R15,AVTSAVTP             SET SAVT BASE          @YM09050
         USING IEDNSVTD,R15                                    @YM09050
         L     R15,SAVTTNTX             ADDR TNT INDEX TO NET  @YM09050
*                                       ADDR CONVERSION        @YM09050
         BALR  R14,R15                  GET NETWORK ADDRESS OF @YM09050
*                                       NEW ORIGIN             @YM09050
         LTR   R15,R15                  INVALID NETWORK ADDR   @YM09050
         BZ    SETBIT3                  BR YES                 @YM09050
         LH    R0,PRFSRCE               R0 = CTERM'S TTCIN     @Y17XAMF
         N     R0,AVTCLRHI              MAKE R0 POSTIVE        @Y17XAMF
         LR    R1,RWORK2                R1 = ORIGIN TTCIN      @Y17XAMF
         L     R15,AVTSAVTP             GET SAVT ADDRESS       @YM09050
         L     R15,SAVTDVAS             R15 = ADDR OF IEDDVAS  @Y17XAMF
         BALR  R14,R15                  GO TO IEDDVAS          @Y17XAMF
         DROP  R15                                             @YM09050
SETTCIN  EQU   *                                                 S22024
         STH   RWORK2,LCBTTCIN          INITIALIZE LCB WITH KEY  S22025
         STH   RWORK2,PRFSRCE           SET KEY IN BUFFER PREFIX S22025
         SPACE 1                                                 S22025
GOODRTN  EQU   *                                                 S22025
         SR    R15,R15                  SET GOOD RETURN CODE     S22025
         B     RETURN                   RETURN TO CALLER         S22025
         SPACE
CKLCBS   EQU   *                                                SA69092
         SR    RLEN,RLEN                ZERO WORK REGISTER      SA69092
         L     R15,QCBDCBAD-1           DCB ADDRESS             SA69092
         L     R14,DCBIOBAD-IHADCB(,15) DUMMY DCB ADDRESS       SA69092
         LA    R14,0(,R14)              CLEAR HIGH BYTE         SA69092
         LA    R1,LCBBKUP               IOB OFFSET              SA69092
         SR    R14,R1                   BACK UP DUMMY ADDRESS   SA69092
         SR    R1,R1                    CLEAR WORK REGISTER     SA69092
         IC    R1,DCBEIOBX-IHADCB(,R15) LCB LENGTH              SA69092
         L     R15,DCBDEBAD-IHADCB(,R15)   DCB ADDRESS          SA69092
         IC    RLEN,DEBNMEXT-DEBNMSUB(,15)  NO. OF LINES        SA69092
RLNLOOP  EQU   *                                                SA69092
         AR    R14,R1                   LCB ADDRESS             SA69092
         TM    LCBSTAT1-IEDQLCB(R14),LCBSENDN+LCBRECVN  LCBS ACTSA69092
         BNM   NEXTLCB                  BRANCH IF NO            SA69092
         LA    RLCB,0(,RLCB)            CURRENT LCB             SA69092
         CR    RLCB,R14                 SAME AS CURRENT         SA69092
         BE    NEXTLCB                  BRANCH IF YES           SA69092
         CH    RWORK2,LCBTTCIN-IEDQLCB(,R14) SAME SOURCE        SA69092
*                                            ALREADY ATTACHED   SA69092
         BE    SETBIT3                  BRANCH IF YES           SA69092
NEXTLCB  EQU   *                                                SA69092
         BCT   RLEN,RLNLOOP             CHECK NEXT LCB          SA69092
         B     SETTCIN                  COMPLETE PROCESSING     SA69092
         SPACE 1                                                 S22025
SETBIT3  EQU   *                                                 S22025
         OI    SCBERR1,SCBORIGN         SET 'INVALID ORIGIN' IN SS22025
         LA    R15,AVTECD4              SET ERROR RETURN CODE    S22025
         SPACE 1                                                 S22025
RETURN   EQU   *                                                 S22025
         L     RBASE,AVTUI              GET RET INTERFACE ADDR   S22025
         B     FOUR(RBASE)              RETURN TO CALLER         S22025
SAVREG   DS    A                        TTE ADDR SAVE AREA     @Y17XAMF
TPREFIX  DS    0H                                              @Y17XAMF
         DC    AL2(TRMPRFSZ)            TTE PREFIX SIZE        @Y17XAMF
         SPACE 1                                                 S22025
         EJECT
********* DSECTS *********
         TDISPD                                                @ZA00209
         EJECT                                                 @ZA00209
         SPACE
         TAVTD
         EJECT
         TLCBD
         EJECT
         TPRFD
         EJECT
         TQCBD
         EJECT
         TSCBD
         EJECT
         TTRMD
         EJECT
         DCBD  DSORG=TX                                         SA69092
         EJECT
         TDEBD                                                  SA69092
         END
