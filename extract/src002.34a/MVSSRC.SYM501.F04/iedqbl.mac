BL01     TITLE '''IEDQBL01''  -  MSGGEN ROUTINE'
IEDQBL   CSECT
         SPACE 3
***********************************************************************
*                                                                     *
* MODULE NAME = IEDQBL (TCAM, MH)                              @Y17XAMS
*                                                                     *
* DESCRIPTIVE NAME = MESSAGE GENERATOR                                *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = VERSION 10.0                                        @Y17XAMS
*                                                                     *
* FUNCTION = ALLOW MESSAGE HANDLER TO SCHEDULE DELIVERY OF A   @Y17XAMS
* USER SPECIFIED MESSAGE TO THE SOURCE/DESTINATION TERMINAL IN @Y17XAMS
* AN IMMEDIATE FASHION, IE, WITHOUT BEING QUEUED.              @Y17XAMS
*                                                                     *
* NOTES = SEE BELOW                                            @Y17XAMS
*                                                                     *
*    DEPENDENCES = NONE                                        @Y17XAMS
*                                                                     *
*    RESTRICTIONS = NONE                                       @Y17XAMS
*                                                                     *
*    REGISTER CONVENTIONS = SEE REGISTER EQUATES               @Y17XAMS
*                                                                     *
*    PATCH LABEL = NONE                                        @Y17XAMS
*                                                                     *
* MODULE TYPE = PROCEDURE                                      @Y17XAMS
*                                                                     *
*    PROCESSOR = ASSEMBLER                                     @Y17XAMS
*                                                                     *
*    MODULE SIZE = 700 DECIMAL BYTES                           @Y17XAMS
*                                                                     *
*    ATTRIBUTES = REUSABLE, ENABLED, PROBLEM PROGRAM MODE      @Y17XAMS
*                                                                     *
* ENTRY POINT = 1. FIRST EXECUTABLE INSTRUCTION                @Y17XAMS
*               2. AT LABEL ERBENTRY                           @Y17XAMS
*                                                                     *
*    PURPOSE =  1. SEE FUNCTION                                @Y17XAMS
*               2. RETURN POST OF ERB AFTER ERB POST TO        @Y17XAMS
*                  REQUEST BUFFER                              @Y17XAMS
*
*    LINKAGE =  1. FROM IEDQBD                                 @Y17XAMS
*               2. FROM TCAM DISPATCHER                        @Y17XAMS
*                                                                X03039
* INPUT = FROM IEDQBD                                            X03039
*         R1    CHAIN OF ELEMENTS TO BE POSTED OR ZERO (0)            *
*         R3    ADDRESS OF STATION CONTROL BLOCK (SCB)                *
*         R4    ADDRESS OF LINE CONTROL BLOCK (LCB)                   *
*         R7    ADDRESS OF IEDQBD QCB                                 *
*         R8    ADDRESS OF MSGGEN PARAMETER LIST                      *
*         R11   BASE FOR TCAM DISPATCHER                              *
*         R12   BASE FOR THIS ROUTINE                                 *
*         R13   ADDRESS OF AVTSAVE2                                   *
*                                                                     *
*       = FROM TCAM DISPATCHER                                 @Y17XAMS
*         R1    ERB ADDRESS                                    @Y17XAMS
*         R3    STCB ADDRESS (GENSTCB)                         @Y17XAMS
*         R7    QCB ADDRESS (GENQCB)                           @Y17XAMS
*         R11   TCAM DISPATCHER BASE ADDRESS                   @Y17XAMS
*         R13   AVTSAVE2 ADDRESS                               @Y17XAMS
*                                                                     *
* OUTPUT = BUFFERS RETURNED TO BUFFER POOL                     @Y17XAMS
*          ERB POSTED TO IEDQBD                                @Y17XAMS
*          ERB POSTED TO IOGENER                               @Y17XAMS
*          ERB POSTED TO IEDNGD                                @Y17XAMS
*                                                                X03039
* EXITS - NORMAL = TCAM DISPATCHER AT DSPCHAIN                 @Y17XAMS
*                                                                     *
* EXITS-ERROR =  NONE                                                 *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                              @Y17XAMS
*                                                                     *
*    ROUTINES = IEDQTNT (TRM LOOKUP ROUTINE)                   @Y17XAMS
*               IEDQGA02 (BUFFER STEAL ROUTINE)                @Y17XAMS
*               IEDQAE (OPTION FIELD LOOKUP ROUTINE)           @Y17XAMS
*               IEDMVBFR (MOVE BUFFER ROUTINE)                 @Y17XAMS
*                                                                     *
*    DATA AREAS = MSGGEN PARAMETER LIST                        @Y17XAMS
*                 PRIORITY DSECT                               @Y17XAMS
*                 IEDMVBFR PARAMETER LIST (TPLMVD)             @Y17XAMS
*                                                                     *
* CONTROL BLOCKS = AVT                                         @Y17XAMS
*                  BFR                                         @Y17XAMS
*                  CCW                                         @Y17XAMS
*                  DCB                                         @Y17XAMS
*                  ERB                                         @Y17XAMS
*                  LCB                                         @Y17XAMS
*                  SCB                                         @Y17XAMS
*                  STCB                                        @Y17XAMS
*                  TRM                                         @Y17XAMS
*                                                                     *
* TABLES = DEVICE CHARACTERISTICS TABLE                        @Y17XAMS
*                                                                     *
* MACROS = IEDHJN                                              @Y17XAMS
*                                                                     *
* CHANGE ACTIVITY =                                            @Y17XAMI
*A-000000-999999                                               @X31X8M0
*A400000,407000,790200                                           S22025
*A510200-513200,514100,832000                                    S22026
*C793500,794000                                                  S21903
*A408800                                                        SA57322
*A556000-557000,700090-704770,764300-764800,781000-782000,       S22024
*A790210-790400                                                  S22024
*A702970                                                        OY03454
*A110000,165000,185000,385000,408890,700000,775000,790410,       X03039
*C035000,045000,055000,075000,115000,170000,190000,200000,230000,X03039
*C408500-408800,557000,700090,700180-704940,764320,764440,764500,X03039
*C764530                                                         X03039
*D050000,060000,065000,405000,781000,783000,790060,790100,       X03039
*D790202-790250,790290-790330,831000                             X03039
*A408707                                                       @XA07544
*A512400,790410                                                @SA72461
*A512400                                                       @SA72461
*C408606,408635,512520,702380                                  @Y17XAMG
*A055000,165000,215000,267000,330000,407900,408500,408990      @Y17XAMS
*A460000,512400,512480,512540,512580,616200,645000,700000      @Y17XAMS
*A790420                                                       @Y17XAMS
*C025800,035000,045000-055000,075000-077000,170000-189200      @Y17XAMS
*C200000,220000-230000,325000,408200,408527-408536,410000      @Y17XAMS
*C480000,500000,512000,512420,512480,512560,570000,616200      @Y17XAMS
*C655000-675000,764320                                         @Y17XAMS
*D020000,085000-113600,120000,240000-260000,408545-408779      @Y17XAMS
*D408863-408866,435000,511000,764620,764830-764860             @Y17XAMS
*D790270-790350,790410-790416                                  @Y17XAMS
*A702740                                                       @ZM46617
*C700260-700280                                                @OY19400
* DUMMY APAR                                                   @OZ27328
*
***********************************************************************
         EJECT
*
*              REGISTERS
*
R0       EQU   0                        REG 0                    S22024
R1       EQU   1                        CHAIN OF ELEMENTS
R2       EQU   2                        WORK REGISTER
RSCB     EQU   3                        SCB ADDRESS
RLCB     EQU   4                        LCB ADDRESS
R5       EQU   5                        WORK REGISTER
RPRF     EQU   6                        BASE FOR BUFFER DSECT  @Y17XAMS
R7       EQU   7                        WORK REGISTER          @Y17XAMS
R8       EQU   8                        ADDRESS OF MSGGEN PARAMETERS
RTRM     EQU   8                        BASE FOR TRM           @Y17XAMS
R9       EQU   9                        REG 9                    S22024
R10      EQU   10                       REG 10                   S22024
R11      EQU   11                       BASE FOR DISPATCHER
R12      EQU   12                       BASE FOR THIS ROUTINE
R13      EQU   13                       DISPATCHER SAVE AREA (AVTSAVE2)
R14      EQU   14                       WORK REGISTER
R15      EQU   15                       WORK REGISTER
         EJECT
         USING *,R12
         USING IEDQSCB,RSCB
         USING IEDQLCB,RLCB
         USING IEDQPRF,RPRF             ADDRESSABILITY FOR PRF @Y17XAMS
         USING IEDQDISP,R11
         USING AVTSAVE2,R13
         EJECT
IEDQBL   IEDHJN START                                            S22025
         ST    R1,LCBERBLK-1            LINK ELEMENTS
         SPACE 2
         L     R9,LCBSTCBA-1            GET THE ADDRESS OF THE   S22025
*                                         FIRST STCB             S22025
         USING IEDQSTCB,R9              STCB ADDRESSABILITY    @Y17XAMS
         CLI   STCBVTO,DSPMCPL8         IS THE LMD STCB IN THE   S22025
*                                         STCB CHAIN             S22025
         DROP  R9
         BE    EXIT                     YES, RETURN              S22025
*                                                                X03039
         CLI   LCBFLAG1,LCBPLCB         3705 RESOURCE          @Y17XAMS
         BE    SNAMSGEN                 YES, TO SNA ROUTINE    @Y17XAMS
         SPACE 2
*        IF A DIAL LINE AND NOT CURRENTLY CONNECTED--DUE TO ERRORS,
*        NO MSGGEN SHOULD BE DONE                               SA57322
         SPACE 2
         TM    LCBSTAT2,LCBDIAL         DIAL LINE               SA57322
         BNO   NOTDIAL                  BR IF NOT DIAL          SA57322
         CLI   LCBTSTSW,AVTEFF          CONNECTION STILL ESTAB  SA57322
         BE    EXIT                     BR IF NOT CONNECTED     SA57322
NOTDIAL  EQU   *                                                SA57322
         LR    R14,R8                   SAVE PARAMETER LIST ADDR X03039
         SPACE 2
         USING IEDGENDS,R8              PARAMETER LIST         @Y17XAMS
         TM    GENFLAGS,GENOMSG         IS MSG DATA SPECIFIED  @YM06037
         BO    EXIT                       BRANCH NO - DISALLOW @YM06037
         TM    GENINDEX,GENMSKSP        ERROR MASK SPECIFIED
         BO    BLNOMSK                  BRANCH NO
*
         LA    R14,4(R14)               BUMP PAST ERROR WORD
BLNOMSK  EQU   *
         LM    R14,R15,4(R14)           MESSAGE TEXT ADDRESS TO R14
*                                       ASSUME TRANSLATE TABLE
*                                       SPECIFIED IN MACRO
*                                       AND PLACE IN R15
         L     R2,LCBDCBPT              DCB ADDRESS
         USING IHADCB,R2                DCB ADDRESSABILITY     @Y17XAMS
         TM    GENFLAGS,GENUSDCB        TRANSLATE TABLE CODED  @Y17XAMS
         BZ    BLSPECD                  BRANCH IF TRANSLATE TABLE
*                                       SPECIFIED
*
         L     R15,DCBTRANS-1           TRANSLATE TABLE IN DCB @Y17XAMS
         TM    0(R15),TRANLIST          IS THERE A LIST OF TRAN- S22029
*                                       SLATE TABLES             S22029
         BZ    BLSPECD                  NO, TRANSLATE TABLE WAS  S22029
*                                       FOUND IN DCB             S22029
         ST    R14,AVTPARM              SAVE MSG DATA POINTER    S22029
         MVC   AVTPARM3(4),AELIST       SETUP INTERFACE TO       S22029
         MVC   AVTPARM3+2(1),1(R15)     LOCOPT ROUTINE TO SEARCH S22029
         LA    R1,AVTPARM3              FOR TRANSLATE TABLE IN   S22029
         STM   R2,R12,28(R13)           OPTION FIELD DEFINED FOR S22029
         LA    R14,LOCRTN               THIS TERMINAL.           S22029
         ST    R14,12(R13)              SAVE RETURN ADDRESS      S22029
         L     R9,CVTPTR                PICKUP ADDR OF LOCOPT    S22029
         L     R9,AVTCVTPT(R9)          ROUTINE (IEDQAE) FROM    S22029
         L     R9,0(R9)                 6TH WORD OF IEDQMSGS     S22029
         LR    R12,R13                  SAVE CALLERS ADDR        S22029
         LA    R13,72(R13)              LOAD AE SAVE ADDRESS     S22029
         ST    R12,4(R13)               STORE BACK CHAIN         S22029
         ST    R13,8(R12)               STORE FORWARD CHAIN      S22029
         L     R12,AVTMSGS-1-AVTSVSIZ   LOAD AVTMSGS             S22029
         L     R12,20(R12)              LOAD ADDR OF AE          S22029
         BR    R12                      GO TO LOCOPT RTN         S22029
LOCRTN   EQU   *                                                 S22029
         L     R14,AVTPARM              RESTORE DATA MSG PTR     S22029
         LTR   R15,R15                  WAS OPTION FIELD FOUND   S22029
         BZ    EXIT                     NO, RETURN               S22029
         L     R15,0(R15)               IS THERE A TRANSLATE     S22029
         LTR   R15,R15                  IN THE OPTION FIELD      S22029
         BZ    EXIT                     NO, RETURN               S22029
*
BLSPECD  EQU   *
         LA    R15,0(,R15)              CLEAR HIGH ORDER
         CLC   DCBIOBAD+1(3),AVTFZERO   IS SOURCE/DESTINATION  @Y17XAMS
*                                       AN APPLICATION PROGRAM
         BE    EXIT                     BRANCH YES - IGNORE MSGGEN
         LR    R9,R1                    SAVE R1                  S22026
         LR    R10,R14                  SAVE R14                 S22026
         LR    R5,R15                   SAVE R15                 S22026
         LH    R1,LCBTTCIN              GET TERM INDEX           S22026
         LTR   R1,R1                    TERM CONNECTED           S22026
         BZ    NOTERM                   BRANCH IF NO             S22026
         L     R15,AVTRNMPT             TERM ROUTINE             S22026
         BALR  R14,R15                  LINK TO ROUTINE          S22026
         USING IEDQTRM,R1               TRM ADDRESSABILITY     @Y17XAMS
         TM    TRMDEVFL+1,TRMCONC       CONCENTRATOR           @Y17XAMS
         BO    EXIT                     BRANCH IF YES IGNORE     S22026
         TM    TRMSTATE,TRMPROC+TRMLIST DIST/CASCADE LIST?     @Y17XAMS
         BNZ   NOTERM                   BR YES, NO CHAR INDEX  @SA72461
         SR    R14,R14                  CLEAR WORK REGISTER    @SA72461
         IC    R14,TRMCHCIN             CHARACTERISTICS INDEX  @Y17XAMS
         DROP  R1                                              @Y17XAMS
         BCTR  R14,0                    DECREMENT FOR N-1TH ENT@SA72461
         MH    R14,AVTDCTLN             COMPUTE OFFSET         @Y17XAMG
         A     R14,AVTCSTCS             POINT TO CHARACTERISTIC@SA72461
         USING IEDDCT,R14               DCT ADDRESSABILITY     @Y17XAMS
         TM    DCTBYTE1,DCTUMASK        GENERAL POLL DEVICE?   @Y17XAMS
         BO    EXIT                     BR YES, IGNORE MSGGEN  @SA72461
         DROP  R14                                             @Y17XAMS
NOTERM   EQU   *                                                 S22026
         LR    R1,R9                    RESTORE REG              S22026
         LR    R14,R10                  RESTORE REG              S22026
         LR    R15,R5                   RESTORE REG              S22026
         SR    R9,R9                    ZERO REG                 S22026
*
         IC    R9,0(R14)                LENGTH OF MESSAGE
         BCTR  R9,0                     DECREMENT FOR MVC
         LA    R10,SCBUNTCT-SCBSRCE-1   MAXIMUM MSGGEN SIZE
         CLR   R9,R10                   CHECK LENGTH OF MESSAGE PASSED
         BNH   OK                       BRANCH LENGTH OK
*
         LR    R9,R10                   ERROR - USE MAXIMUM LENGTH
OK       EQU   *
         EX    R9,BLMVC                 MOVE TEXT TO SCB
*
         LA    R5,NOTR                  SET RETURN REGISTER
         TM    GENFLAGS,GENOCODE        WAS CODE =NO SPECIFIED @Y17XAMS
         BCR   1,R5                     BRANCH YES - NO TRANSLATE
*
         BCT   R15,CKTRAN               DEFAULT IN DCB IS '1'
*                                       FALL THRU IF NO TABLE
*
*
NOTR     EQU   *
         TM    LCBSTAT1,LCBSENDN        SEND STATE?              S99228
         BO    USUAL                    YES, OMIT GEN POLL CHECK S99228
         SR    R5,R5                    CLEAR INVLIST INDEX REG  S99228
         IC    R5,DCBEIOBX              INSERT LCB LENGTH      @Y17XAMS
         DROP  R2                                              @Y17XAMS
         SH    R5,AVTHA4                REDUCE LCB LENGTH BY     S99228
         SH    R5,AVTHA4                8 BYTES FOR ADDR LCB EXT S99228
         USING IEDQLCBX,R5                                       S99228
         LA    R5,ZERO(R5,RLCB)         LOAD LCB EXTENSION ADDR  S99228
*                                                                S99228
*        ASSUME GENERAL POLL IN PROGRESS                         S99228
*                                                                S99228
         NI    LCBXFLAG,LCBGPSTP        STOP GENERAL POLL        S99228
         DROP  R5                                                S99228
USUAL    EQU   *                                                 S99228
         OI    LCBSTAT2,LCBMSGNN        SET MSGGEN BIT FOR I/O GEN
         LH    R5,LCBTTCIN              CURRENTLY CONNECTED
         STH   R5,LCBTTBIN              TO BE CONNECTED
         SR    R5,R5
         STH   R5,LCBTTCIN              CLEAR CURRENTLY CONNECTED
         STH   R5,LCBTPCD               SET TP OP CODES OF ZERO
         LA    R1,LCBCPA                CHANNEL PROGRAM AREA   @Y17XAMS
         USING IEDQCCW,R1               CCW ADDRESSABILITY     @Y17XAMS
         LA    R5,SCBSRCE               DATA ADDRESS
         ST    R5,CCWADDR-1             TO LCB FOR IOGENER     @Y17XAMS
         MVI   CCWOPCDE,CCWWRITE        SET WRITE OP CODE      @Y17XAMS
         LA    R9,1(,R9)                ADD FOR COUNT
         ST    R9,CCWCOUNT-2            CLEAR FLAGS, SET COUNT @Y17XAMS
         DROP  R1                                              @Y17XAMS
*
*        SET TO POST ERB TO I/O GENERATOR
*
         LA    R9,PRIACTIV              PRIORITY FOR TPOST
         LA    R5,AVTACTIB              I/O GENERATOR QCB
         B     EXIT1                    EXIT TO DISPATCHER
         EJECT
SNAMSGEN EQU   *                        SNA MSGGEN PROCESS     @Y17XAMS
         USING IEDNSVTD,R9              SAVT ADDRESSABILITY    @YM08501
         USING IEDGENDS,R2              MSGGEN PARAMETER LIST  @Y17XAMS
         USING IEDNTRM,RTRM                                    @Y17XAMS
         L     R9,AVTSAVTP              SAVT BASE              @YM08501
         LR    R2,R8                    LOAD PARM LIST ADDR    @Y17XAMS
         LH    R1,LCBTTCIN              TNT INDEX                X03039
         L     R15,AVTRNMPT             TERMNAME ROUTINE         X03039
         BALR  R14,R15                  GET TERMINAL ADDRESS     X03039
         LR    RTRM,R1                  SET TRM BASE REG       @Y17XAMS
         SH    RTRM,TRMBCKUP            GET NEGATIVE PREFIX    @Y17XAMS
         TM    TRMBYTE0,TRMSNA          SNA RESOURCE           @Y17XAMS
         BO    LUTEST                   BRANCH IF YES          @Y17XAMS
         TM    GENFLAGS,GENOMSG        IS MSG DATA SPECIFIED   @YM06037
         BO    EXIT                      BRANCH NO- DISALLOW   @YM06037
         CLI   TRMTYPE,TRMPSNA          PRE-SNA TERMINAL       @OY19400
         BE    CKDIAL                   IF YES, CHECK IF DIAL  @OY19400
         CLI   TRMTYPE,TRMCTERM         CTERM TYPE ENTRY?      @OY19400
         BNE   EXIT                     NO, IGNORE MSGGEN      @OY19400
CKDIAL   EQU   *                                               @OY19400
         TM    TRMBYTE0,TRMDIAL         DIAL                   @Y17XAMS
         BNO   NONDIAL                  NO - CONTINUE          @YA09726
         TM    TRMPRE1,TRMSESSN         IN SESSION             @Y17XAMS
         BNO   EXIT                     NO - EXIT              @Y17XAMS
NONDIAL  EQU   *                                               @YA09726
         TM    TRMPRE1,TRMSPACT+TRMSTPND LINE UP?              @YA07123
         BZ    EXIT                     NO GET OUT             @YA07123
         B     INPROG                   YES - CONTINUE         @Y17XAMS
LUTEST   EQU   *                                               @Y17XAMS
         CLI   TRMTYPE,TRMLUNT          LU                     @Y17XAMS
         BNE   EXIT                     NO - EXIT              @Y17XAMS
         SR    R1,R1                    CLEAR                  @Y109726
         ICM   R1,AD,TRMSIBPT           GET SIB POINTER        @YA09726
         LTR   R1,R1                    SIB POINTER?           @YA09726
         BZ    EXIT                     NO GET OUT             @YA09726
         TM    SIBSESSN-IEDSIBD(R1),SIBDAPND+SIBDRPND DATA TRAFFIC
*                                       ACTIVE?                @YA09726
         BNO   EXIT                     NO GET OUT             @YA09726
INPROG   EQU   *                                               @Y17XAMS
         OI    LCBSTAT2,LCBMSGNN        SET MSGGEN IN PROGRESS @Y17XAMS
         LR    R5,R2                    PARM LIST ADDRESS      @Y17XAMS
         TM    GENINDEX,GENMSKSP        ERROR MASK SPECIFIED   @Y17XAMS
         BO    SETMADR                  NO - DONT MODIFY       @Y17XAMS
         LA    R5,4(R5)                 YES - BUMP PAST ERROR  @Y17XAMS
*                                       MASK AND POINT TO      @Y17XAMS
*                                       MSGGEN DATA ADDRESS    @Y17XAMS
SETMADR  EQU   *                                               @Y17XAMS
         L     R5,4(R5)                 GET MSGGEN DATA AREA   @Y17XAMS
         USING MSGNAREA,R5              MSGGEN DATA AREA       @Y17XAMS
         SR    RPRF,RPRF                CLEAR REGISTER         @Y17XAMS
         ICM   RPRF,AD,LCBERBCH         BUFFER ADDRESSABILITY  @Y17XAMS
         BNZ   SETPARM                  IF BFR AVAIL, USE IT   @Y17XAMS
         SR    R15,R15                  CLEAR REG              @Y17XAMS
         TM    GENFLAGS,GENOMSG        IS MSG DATA SPECIFIED   @YM06037
         BO    REMAIN                    BRANCH NO- GET 1 BUFFER
*                                                              @YM06037
         IC    R15,MSGNCT               PLACE MSGGEN DATA LEN  @Y17XAMS
*                                       IN EVEN/ODD PAIR       @Y17XAMS
         LA    R15,PRFSHDR-PRFSUNIT(R15) TOTAL BYTES           @Y17XAMS
         SR    R14,R14                  ZERO EVEN REG          @Y17XAMS
         D     R14,AVTKEYLE             COMPUTE UNITS REQUIRED @Y17XAMS
         LTR   R14,R14                  REMAINDER              @Y17XAMS
         BZ    NOREMAN                  NO                     @Y17XAMS
REMAIN   EQU   *                                               @YM06037
         LA    R15,1(R15)               YES, INCREASE UNIT     @Y17XAMS
*                                       COUNT FOR PARTIAL      @Y17XAMS
NOREMAN  EQU   *                                               @Y17XAMS
         LR    R0,R15                   UNITS REQUIRED         @Y17XAMS
         LA    R1,1                     REQUEST ONE BUFFER     @Y17XAMS
         L     R15,AVTSTEAL             IEDQGA02 ADDRESS       @Y17XAMS
         BALR  R14,R15                                         @Y17XAMS
         STCM  R15,AD,LCBERBCH          SAVE BUFFER ADDRESS    @Y17XAMS
         LTR   RPRF,R15                 BFR STEAL SUCCESSFUL   @Y17XAMS
         BNZ   SETPARM                  YES                    @Y17XAMS
*
*        BUFFER STEAL UNSUCCESSFUL, SO POST ERB TO REQUEST BUFFERS
*
         STC   R1,LCBRBCT1              REQUEST 1 BUFFER       @Y17XAMS
         STC   R0,LCBRBCT2              UNITS REQUIRED         @Y17XAMS
         OI    LCBERBST,LCBPRCPG        REQUEST ERB POST       @Y17XAMS
         LA    R14,GENQCB               QCB ADDRESS            @Y17XAMS
         ST    R14,LCBRCQCB             QCB TO POST AFTER BFR  @Y17XAMS
*                                       ALLOCATION             @Y17XAMS
         LA    R9,PRIINTRQ              ERB PRIORITY           @Y17XAMS
         LA    R5,AVTBFREB              AVAILABLE BUFFER QCB   @Y17XAMS
         B     EXIT1                                           @Y17XAMS
SETPARM  EQU   *                                               @Y17XAMS
         USING IEDPLMV,R1                                      @Y17XAMS
         XC    PRFSIZE(2),PRFSIZE      INITIALIZE TO ZERO      @YM07314
         TM    GENFLAGS,GENOMSG        IS MSG DATA SPECIFIED   @YM06037
         BO    CLEARPF1                 BR NO                  @YM08501
         LA    R1,AVTSAVE4              PARMLIST ADDRESS       @Y17XAMS
         XC    IEDPLMV(PLMVLEN),IEDPLMV CLEAR PARAMETER LIST   @ZM46617
         LA    R7,PRFSHDR               TARGET  UNIT ADDRESS   @Y17XAMS
         STM   RPRF,R7,PLMVTU           TARGET DATA & UNIT     @Y17XAMS
         LA    R14,MSGNDATA             POINT TO MSGGEN DATA   @Y17XAMS
         ST    R14,PLMVSU               SOURCE UNIT ADDRESS    @Y17XAMS
         ST    R14,PLMVSD               SOURCE DATA ADDRESS    @Y17XAMS
         SR    R14,R14                  CLEAR REG              @Y17XAMS
         IC    R14,MSGNCT               LENGTH OF ...          @Y17XAMS
         STH   R14,PLMVLNTH             ... MSGGEN DATA        @Y17XAMS
         OI    PLMVFLG1,PLMVSCTG        SET FLAG               @Y17XAMS
         L     R15,SAVTMVBF             IEDMVBFR ADDRESS       @Y17XAMS
*                                                              @Y17XAMS
*        IEDMVBFR WILL USE AVTSAVE2 AS SAVE AREA               @Y17XAMS
*                                                              @Y17XAMS
         BALR  R14,R15                                         @Y17XAMS
         SR    R14,R14                  CLEAR FOR LOAD         @YM07314
         IC    R14,MSGNCT               GET MESSAGE SIZE       @YM07314
         STH   R14,PRFSIZE              SAVE IN BUF            @YM07314
         DROP  R1                                              @Y17XAMS
CLEARPF1 EQU   *                                               @YM06995
         USING IEDPF1,R1                                       @Y17XAMS
         LR    R1,RPRF                  START OF BUFF PREFIX ..@Y17XAMS
         SH    R1,PRF1BKUP              ... NEG EXTENSION      @YM06511
         XC    IEDPF1(PRF1LEN),IEDPF1   CLEAR NEG EXT          @Y17XAMS
         TM    TRMBYTE0,TRMSNA          SNA                    @Y17XAMS
         BNO   BFPRFX                   NO                     @Y17XAMS
         TM    GENFLAGS,GENRHINC        RH DATA SPECIFIED      @Y17XAMS
         BNO   BFPRFX                   NO                     @Y17XAMS
RHBLD    EQU   *                                               @YM06037
         LA    R14,252                  HEX FC FOR AND TO TURN @Y17XAMS
*                                       OFF TWO LOW ORDER BITS @Y17XAMS
*                                       OF LENGTH              @Y17XAMS
         IC    R15,GENENGTH             PARM LIST LENGTH       @Y17XAMS
         NR    R15,R14                  TURN OFF LOW ORDER BIT @Y17XAMS
         SH    R15,AVTHA4               POINT TO RH DATA ADDR  @Y17XAMS
         L     R15,IEDGENDS(R15)        LOAD RH DATA ADDRESS   @Y17XAMS
         MVC   PRF1RH,0(R15)            MOVE RH DATA INTO BFR  @Y17XAMS
*                                       PREFIX                 @Y17XAMS
BFPRFX   EQU   *                                               @Y17XAMS
         LH    R14,PRFSIZE              GET MSG SIZE OR ZERO   @YM07314
         LA    R14,PRFSHDR-PRFSUNIT(R14) ADD HEADER SIZE       @YM07314
         STH   R14,PRFSIZE              SET UP BUFF SIZE       @YM07314
         STCM  RLCB,AD,PRFLCB           INIT LCB ADDRESS       @Y17XAMS
         MVC   PRFDEST,LCBTTCIN         SET DESTINATION        @YM07708
         NI    PRFSTAT1,PRFNHDRF        MARK AS HEADER         @YM06960
         NI    PRFSTAT1,PRFNLSTF        MARK AS LAST           @YM06960
         MVI   LCBISZE,AVTEZERO         INIT RESERVE COUNT     @Y17XAMS
         L     R5,SAVTDNIR              OUTDH QCB ADDRESS      @YM06902
         LA    R9,PRIOUTBD              PRIORITY               @YM06902
         B     EXIT1                                           @Y17XAMS
*
EXIT     EQU   *
         L     R5,AVTMSGS-1             ADDRESS OF MSGS CSECT
         L     R5,0(,R5)                ADDRESS OF IEDQBD
         LA    R9,PRIEDISP              SET LOW PRIORITY         S22025
EXIT1    EQU   *
         STC   R9,LCBERBPY              SET PRIORITY
         ST    R5,LCBERBQB-1            SET ERB QCB ADDRESS
         LA    R1,LCBERB                ERB ADDRESS
*
         BAL   R14,DSPCHAIN             EXIT TO DISPATCHER     @Y17XAMX
*
CKTRAN   EQU   *
********************************************************************
*
*        BYPASS ASSEMBLER PROBLEM
         USING DUMMY+3,R15
         L     R15,OUTRANS
*
*********************************************************************
         LTR   R15,R15                  IS ONE SPECIFIED
         BCR   8,R5                     RETURN IF NOT
*
         EX    R9,BLTRN                 TRANSLATE TO LINE CODE
         BR    R5                       RETURN
         EJECT
*****************************************************************S22024
*        THE FOLLOWING QCB IS USED TO REENTER THIS ROUTINE       S22024
*        FROM IEDQGA WHEN RETURN POSTING THE ERB AFTER         @Y17XAMS
*        ALLOCATING BUFFERS                                      S22024
*****************************************************************S22024
         SPACE 1                                                 S22024
GENQCB   DC    A(IEDQBL)                ADDR FOR RESTORING BASE@Y17XAMS
         DC    A(0)                     LINK FIELD               S22024
GENSTCB  DC    AL1(DSPMCPL4)            STCB KEY               @Y17XAMS
         DC    AL3(GENSTCB)             STCB CHAIN POINTER     @Y17XAMS
         SPACE 1                                                 S22024
         L     R12,ZERO(R7)             SET UP BASE REGISTER     S22024
         SH    R1,ERBOFFST              BACK UP TO PLCB          S22024
         LR    RLCB,R1                  SET UP LCB BASE REGISTER S22024
         L     RSCB,LCBSCBA-ONE         SET UP SCB BASE          S22024
         SR    R1,R1                    ZERO REG FOR NULL ELEMENTS22024
*                                       CHAIN                    S22024
         L     R8,SCBMACR-ONE           SET UP PARM LIST ADDRESS S22024
         B     START                    BRANCH BACK TO START     S22024
         EJECT
BLMVC    MVC   SCBSRCE(1),1(R14)        EXECUTED MOVE TO PLACE MSGGEN
*                                         TEXT IN SCB
*                                       EX MVC TO PUT TEXT IN    X03039
*                                       BFR FOR A VTAM TERMINAL  X03039
BLTRN    TR    SCBSRCE(1),0(R15)        EXECUTED TRANSLATE TO CONVERT
*                                         MSGGEN TEXT TO LINE CODE
ERBOFFST DC    AL2(LCBERBKY-IEDQLCB)    OFFSET TO ERB            S22024
TRMBCKUP DC    AL2(TRMPRFSZ)            NEGATIVE PREFIX SIZE   @Y17XAMS
PRF1BKUP DC    AL2(PRF1LEN)             BUFFER NEGATIVE PREFIX @Y17XAMS
AELIST   DC    XL4'14040010'            PARM LIST TO LOCOPT
ZERO     EQU   0                        ONE                      S22024
ONE      EQU   1                        ONE                      S22024
AD       EQU   7                        MASK FOR ADDR ICM OR STCMX03039
CVTPTR   EQU   16                       OFFSET TO CVT ADDR       S22029
TRANLIST EQU   X'80'                    FLAG INDICATING LIST     S22029
*                                       OF TRANSLATE TABLES      S22029
         EJECT
IEDGENDS DSECT                          MSGGEN DSECT PARM LIST @Y17XAMS
GENINDEX DS    X                        AVTMSGS INDEX TO IEDQBL@Y17XAMS
GENMSKSP EQU   X'01'                    MASK SPECIFIED         @Y17XAMS
GENENGTH DS    X                        PARM LIST LENGTH       @Y17XAMS
GENFLAGS DS    X                        FLAGS                  @Y17XAMS
GENUSDCB EQU   X'80'                    USE TABLE IN DCB       @Y17XAMS
GENOMSG  EQU   X'20'                   MSG DATA OMITTED        @YM06037
GENRHINC EQU   X'40'                    RH DATA INCLUDED       @Y17XAMS
GENOCODE EQU   X'10'                    CODE=NO SPECIFIED      @Y17XAMS
GENMASK  DS    XL5                      MSGGEN MASK            @Y17XAMS
         SPACE 3
MSGNAREA DSECT                          MSGGEN AREA IN BFR UNIT  X03039
MSGNCT   DS    X                        LENGTH OF MSGGEN DATA    X03039
MSGNDATA DS    0XL24                    MSGGEN TEXT              X03039
         SPACE 3                                               @Y17XAMS
***********************************************************************
*        THE FOLLOWING DSECT IS REQUIRED TO ENABLE THE ASSEMBLER      *
*        TO PROPERLY HANDLE SOMETHING OTHER THAN ORDINARY             *
*        INEFFECIENT CODING PRACTICES.                                *
***********************************************************************
DUMMY    DSECT
         DS    A                        DSECT FOR                S21903
OUTRANS  DS    A                        ASSEMBLER PROBLEM        S21903
         EJECT                                                 @Y17XAMS
         TAVTD
         EJECT
         TCCWD                                                 @Y17XAMS
         EJECT                                                 @Y17XAMS
         DCBD  DSORG=TX
         EJECT
         TDCTD                                                 @Y17XAMS
         EJECT                                                 @Y17XAMS
         TDISPD
         EJECT                                                 @Y17XAMS
         TLCBD
         EJECT                                                 @Y17XAMS
         TPLMVD                                                @Y17XAMS
         EJECT                                                 @Y17XAMS
         TPRFD ,                                                 S22024
         EJECT                                                 @Y17XAMS
         TPRIOR
         EJECT                                                 @Y17XAMS
         TSCBD
         EJECT
         TSIBD
         EJECT
         TSTCBD                                                @Y17XAMS
         EJECT                                                 @Y17XAMS
         TTRMD                                                   S22026
         END
