DQBZ TITLE  '''IEDQBZ''  LOG SCHEDULER'
         SPACE 3
IEDQBZ03 CSECT
         SPACE 3
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A402000,582000,585000,593000,711000                             S21101
*C384000,717000                                                  S21101
*D543000-579000,868000                                           S21101
*A402000,414000,537000,597000,696000,720000,858000,867000,927000 A41038
*C384000,513000-516000,594000-597000,768000                      A41038
*D372000,624000                                                  A41038
*A447000                                                        SA54935
*C384000                                                        SA54935
*C603000,378000-390000                                          SA52969
*C009000,437000,465000,708000,726800,840000,882000,885000,900000,S21903
*C903000,906000,912000                                           S21903
*A289000,462000,519000,522000,666000,692000,726000,744000       SA52971
*C288000,378000-390000,435000-437500,657000,675000-678000       SA52971
*C720000,765000-768000,852000-870000,888000-891000              SA52971
*D294000,468000-474000,495000-507000,531000,540000-582000       SA52971
*D633000,639000,697000,756000,774000-777000,804000-822000,828000SA52971
***********************************************************************
         SPACE 3
         DC    AL2(STCBMOVE-IEDQBZ+2)
         ENTRY IEDQBZ
         ENTRY IEDQBZ01
IEDQBZ   EQU   *
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE 'IEDQBZ'  LOG SCHEDULER                                        *
*                                                                     *
*STATUS -- CHANGE LEVEL /                                             *
*                                                                     *
*FUNCTION -- TO SCHEDULE THE LOGGING OF NESSAGES.  THE ROUTINE MAY    *
*   BR ACTIVATED UNDER SEVERAL DIFFERENT CIRCUMSTANCES.               *
*   1.  FROM THE DESTINATION QCB WITH A BUFFER                        *
*        ACTION TAKEN -                                               *
*        A.  MOVE THE STCB TO THE 'LCB'                               *
*        B.  POST THE 'LCB' TO ITSELF                                 *
*        C.  BYPASS FUNCTION PASSING THE BUFFER TO SECTINATION SCHED. *
*   2.  FROM THE 'LCB' WITH THE 'LCB' AS AN ELEMENT                   *
*        THE ERB WILL BE POSTED TO SIDK I/O TO RECALL ONE BUFFER      *
*   3.  FROM THE 'LCB' WITH THE ERB                                   *
*        ANY OUTSTANDING WRITES WILL BE CHECKED AND THE BUFFERS FREED *
*        ANY BUFFER UNITS TO BE WRITTEN WILL HAVE WRITES ISSUED       *
*   IF EOM WAS WRITTEN, THE ERB WILL BE POSEED TO THE 'LCB' AGAIN     *
*        TO HANDLE THE LAST BUFFER CHECKS                             *
*   IF THERE ARE NO MORE MESSAGES TO LOG FOR THIS QCB THE STCB        *
*        WILL BE MOVED BACK TO THE DESTINATION QCB.                   *
*                                                                     *
*INPUT -- IEDQBZ IS ALWAYS ACTIVATED BY THE DISPATCHER                *
*   1. FROM THE QCB                                                   *
*   R1 HAS A BUFFER ADDRESS                                           *
*   2.  FROM THE 'LCB' WITH THE 'LCB'                                 *
*        R1 HAS THE LCB ADDRESS                                       *
*   3.  FROM THE LCB WITH THE ERB                                     *
*   R1 HAS THE ERB ASDRESS                                            *
*        LCBERBCH WILL HAVE THE ADDRESS OF THE BUFFER                 *
*   AT ASSEMBLY TIME THE LOGTYPE MACRO GENEREATES AN SCB,LCB,         *
*   AND QCB.  THE ADDRESS NEEDED ARE SET AS FOLLOWS                   *
*   QCBDCBA IS THE ADDRESS OF THE LOG 'LCB'                           *
*   LCBSCBA IS THE ADDRESS OF THE LOG 'SCB'                           *
*   LCBDCBPT IS THE ADDRESS OF THE LOG DCB                            *
*   AT ASSEMBLY TIME LCBTTCIN IS SET TO THE OFFSET OF THE LOGTYPE     *
*   ENTRY IN THE TERM NAME TABLE ENTRY - AFTER THE SORT, THIS OFFSET  *
*   IS CHANGED TO THE NEW OFFSET.                                     *
*                                                                     *
*OUTPUT -- WRITE MACROS WILL HAVE BEEN ISSUED FOR THE BUFFER UNITS    *
*   THE ERB WILL BE POSTED TO DISKI/O OF THE LOG 'LCB'                *
*                                                                     *
*EXTERNAL ROUTINES -- IGG019RB - THE DISPATCHER FOR POST, UNAVAIL,    *
*   AND BYPASS FUNCTIONS                                              *
*                                                                     *
*EXITS-NORMAL -- EXIT IS THE THE DISPATCHER                           *
*                                                                     *
*EXITS-ERROR -- THE CHECK MACRO MAY EXIT TO THE SYNAD SPECIFIED IN    *
*   THE DCB IF ERRORS ARE ENDOUNTERED.  SYNAD IS A USED WRITTEN       *
*   ROUTINE THAT MUST RETURN TO THE CHECK MACRO.                      *
*                                                                     *
*TABLES/WORK AREAS -- LCB, SCB, DCB, AVT, QCB                         *
*                                                                     *
*ATTRIBUTES -- REUSABLE,REFRESHABLE, RESIDENT, ENABLED                *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MADULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
***********************************************************************
*                                                                     *
*NOTES -- THE DCB OPENED MUST BE A BSAM DCB.                          *
*   OPERANDS ARE --                                                   *
*   DSORG=PS                                                          *
*   DEVD=  (USER OPTION)                                              *
*   BFALN=  OMITTED                                                   *
*   BLKSIZE= (KEYLENGTH)   ALL UNITS WILL BE WRITTEN WITHOUT THE      *
*        12 BYTE UNIT MANAGEMENT AREA AND ALL ARE THE SAME SIZE       *
*   BUFCB=  OMITTED                                                   *
*   BUFL=  OMITTED                                                    *
*   DDNAME=  (USED OPTION)                                            *
*   NCP=  SHOULD BE THE MAXIMUM NUMBER OF UNITS THAT CAN APPEAR IN    *
*        A BUFFER.  THIS IS A USER OPTION - IT CAN BE ANYTHING.       *
*   RECFM=F                                                           *
*   MACRF=W                                                           *
*   SYNAD=  (USER SPECIFIED - SHOULD CONFORM TO STANDARDS FOR         *
*        SYNAD EXITS - ERRORS THAT MAY HAVE BEEN ENCOUNTERED WILL     *
*        IGNORED BY LOG.)                                             *
*                                                                     *
*   A GET MAIN WILL PROVIDE AS MANY CONTROL BLOCKS AS SPECIFIED IN    *
*   THE NCP OPERAND OF THE DCB.  THEY WILL BE IN A CONTIGUOUS         *
*   AREA.  THERE WILL BE A CONTROL AREA FOR EACH DCB USED FOR LOG.    *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 1
*                  REGISTERS                                          *
*                                                                     *
R0       EQU   0
R1       EQU   1
RDECB    EQU   R1                       DECB ADDRESS             CLUP21
RUNIT    EQU   2                        ADDR OF BFR UNIT TO WRITECLUP21
RSCB     EQU   3                        SCB ADDR
RLCB     EQU   4                        LCB ADDR
RNB      EQU   5                        NO UNITS TO WRITE
*                                       FOR THIS BUFFER
RPREFIX  EQU   6                        BUFFER ADDRESS
RQCB     EQU   7                        QCB ADDR
RNCP     EQU   8                        NO OF WRITES THAT CAN BE ISSUED
R9       EQU   9
RDCB     EQU   10                       DCB ADDR
R11      EQU   11                       DISPATCHER ADDR
RBASE    EQU   12                       ENTRY POINT ADDR
RAVT     EQU   13                       AVT SAVE 2 ADDR
R14      EQU   14
R15      EQU   15
*                                                                     *
*                  USING STATEMENTS                                   *
*                                                                     *
         USING IEDQSCB,RSCB
         USING IEDQDISP,R11
         USING IEDQAVTD,RAVT
         USING IEDQPRF,RPREFIX
         USING IHADCB,RDCB
         USING IEDQLCB,RLCB
         USING IEDQQCB,RQCB
         USING  *,RBASE
*                                                                     *
         LR    RBASE,R15
IEDQBZ   IEDHJN AROUND                  MODULE ID AND DATE       CLUP21
         SR    RNCP,RNCP
         CLI   QCBSTVTO,ELOGSCH         IS THIS THE QCB
         BNE   NOTQCB                   BR NO
         L     RLCB,QCBDCBAD-1          LCB ADDR
*        RESET FUNCTION  IN FA WILL SET SCBDESTQ TO AVAIL BUFFER S21101
* QUEUE AND ERRONEOUS READ FROM THIS QUEUE WILL RESULT IN 045-3  S21101
* ABEND.  THEREFORE, LOG QCB ADDRESS WILL BE SAVED IN LCB        S21101
* AND SET BACK IN SCB WHEN READ REQUEST IS INITIATED.            S21101
         ST    RQCB,LCBEOLTD            SAVE QCB ADDRESS         S21101
         NI    LCBSTAT1,LCBRCLLF        NO RECALLS               A41038
         L     RDCB,LCBDCBPT            DCB ADDR
         ST    R1,AVTDOUBL              KEEP BFR ADDR
         NC    LCBDECB+1(2),LCBDECB+1   HAS THE AREA FOR DECB BEEN BOTT
         BNZ   BYPASS                   GOTTEN - BR YES
*        LOG QCB DCB ADDR IS SET TO LOG SCB- FAKE THE DCB PTRS   A41038
         LA    R9,LCBFLAG1               LCB IOB ADDRESS         A41038
         ST    R9,LCBISZE               SET DUMMY DCB IOBAD      A41038
         MVC   LCBSCBDA(3),LCBSCBA      SET SCB ADDRESS AGAIN    A41038
         IC    RNCP,DCBNCP              MAX NO. OF WIRTES ALLOWED
         LA    R9,DECBSZ                SIZE OF A DECB
         MR    RNCP,RNCP
         LR    R0,R9                    SIZE FOR GETMAIN
         GETMAIN  R,LV=(0)
         ST    R1,LCBDECB               SAVE DECB ADDR
BYPASS   EQU   *
         L     RSCB,QCBSLINK-1          PASS STCB
*                                       DESTINTATION SCHEDULAR
         L     R1,AVTDOUBL              GET BFR ADDR
         B     DSPBYPAS                 BR TO BYPASS DISPATCHER  S21903
         SPACE 3                                                 CLUP21
STCBMOVE EQU   *
         USING *,R15
         L     RLCB,QCBDCBAD-1          LCB ADDR
         L     RDCB,LCBDCBPT            ADDRESS OF DCB
         ST    RQCB,LCBEOLTD            SAVE QCB ADDRESS        SA54931
         TM    DCBOFLGS,OPEN
         BCR   14,R14                   BR NO
         LR    R9,R14                   SAVE RTN
         CLI   LCBPRI,AVTEZERO          IS THE LCB POSTED ALREADY
         BNE   RTN                      BR IF POSTED
         LR    RNCP,RQCB                SAVE QCB ADDR
         BAL   R14,LCBPOST              BR TO POST LCB           S21903
         LR    RQCB,RNCP                RESTORE
RTN      EQU   *
         LR    R14,R9                   RESTORE
         LR    RSCB,RLCB                PASS QCB ADDR IN 3
         B     DSPUNAVR                 MOVE THE STCB
         DROP  R15
         EJECT ,                                                 CLUP21
NOTQCB   EQU   *
         LA    RLCB,0(RQCB)                                      A41038
         LA    R1,0(R1)                 POSTED TO THE LCB
         L     RDCB,LCBDCBPT            DCB ADDR
         SR    RNB,RNB
         L     RSCB,LCBSCBA-1           SCB ADDR
         CLR   R1,RLCB
         BNE   ERB                      BR IF ERB
         TM    DCBOFLGS,OPEN            ISTHIS DCB OPEN
         BNO   GOBACK                   BR NO
GETSCB   EQU   *                                                 A41038
         MVC   SCBDESTQ(3),LCBEOLTD+1   SET SCBDESTQ             S21101
         L     RQCB,SCBDESTQ-1          DEST Q ADDR
         TM    QCBDSFLG,QCBHELD         IS QUEUE BEING SERVICED  S21101
*                                         BY RE-USABILITY        S21101
         BO    GOBACK                   BRANCH IF YES            S21101
         NC    QCBFFEFO(3),QCBFFEFO     IS THERE A MSG TO LOG
         BZ    GOBACK                   BR NO
         MVC   SCBSCHDR(3),QCBFFEFO     SET TO READ HDR          A41038
         MVC   SCBSCSEG(3),QCBFFEFO                              A41038
         XC    SCBFEFO(3),SCBFEFO       CLEAR FEFO               S21101
         OI    QCBFLAG,QCBSDFFO                                  A41038
         NI    SCBQTYPE,AVTEZERO        SET UP TO GET HEADER     A41038
POSTERB  EQU   *
         XC    LCBERBST(L'LCBERBST+L'LCBERBCH),LCBERBST         SA52969
*                                         CLEAR ERBST & ERBCH   SA5296
         LA    R1,AVTDSIOB              POST THE ERB FOR RCLL TO
POSTERB1 EQU   *                        DISK I/O QCB WITH
*              RECALL SET AND RCQCB SET TO THE DUMMY LCB
         ST    R1,LCBERBQB-1
         LA    R1,LCBERB
         MVI   LCBERBCT,E01             SET COUNT TO 1 BFR
         MVI   LCBERBPY,PRIAPERB                                 A41038
         BAL   R14,DSPPOST              EXIT TO POST           @Y17XAMX
ERB      EQU   *
         L     RQCB,SCBDESTQ-1          QCB ADDR
         IC    RNCP,LCBNOWTR            NO OF OUTSTANDING WRTS
         LTR   RNCP,RNCP                ANY THERE
         BZ    WRITES1                  BR NO
         L     RDECB,LCBDECB            ADDR FIRST WRITE DECB
CHECK    EQU   *
         CHECK (1)                      CHECK THIS WRITE         CLUP21
         LA    RDECB,DECBSZ(RDECB)      ADDR NEXT DECB
         BCT   RNCP,CHECK               HAVE ALL BEEN CHECKED
         STC    RNCP,LCBNOWTR           ALL CHECKED
         L     RPREFIX,LCBCSW-1         ADDR OF BFR
         L     RUNIT,LCBCSW+3           ADDR NEXT UNIT
         IC    RNB,LCBNOUNT             ANY MORE UNITS TO
         LTR   RNB,RNB                  WRITE
         BNZ   WRITE1                   YES, GO WRITE THEM       CLUP21
         LA    R0,AVTBFRTB              ERTURN THE BFR TO THE AVAIL
         MVI   PRFPRI,PRIBFRTB          SET PRTY FOR RETURN
         TM    PRFSTAT1,PRFNLSTN        LAST SEG OF MSG
         BO    POST                     BR NOT LAST TO RETURN
         LA    R0,AVTDSIOB              IF LAST SEG POST TO HAVE
         MVI   PRFPRI,PRIDKSRV          FLAGGED SERVICED
         MVC   PRFSRCE(3),SCBFEFO       SET FEFO POINTER,       SA52971
         MVC   PRFTQBCK(1),SCBPRI         PRIORITY INDEX,       SA52971
         MVC   PRFTQBCK+1(2),SCBOSEQ      SEQ OUT               SA52971
POST     EQU   *
         LR    R1,RPREFIX               FOR POST
         ST    R0,PRFQCBA-1
         BAL   R14,DSPPOSTR             BRANCH TO POST           S21903
         L     RSCB,LCBSCBA-1           RESTORE SCB ADDR
         LR    R1,RLCB                  SET REG 1                S21101
         TM    PRFSTAT1,PRFNLSTN        WAS IT A LAST SEG
         BNO   POSTERB1                 YES BR                   S21101
WRITES1  EQU   *
         NC    LCBERBCH(3),LCBERBCH     CANCELLED MSG READ       A41038
         BZ    GETSCB                   BRANCH IF YES            A41038
         L     RPREFIX,LCBERBCH-1       ADDR OF NEW BFR
         MVC   LCBERBCH(3),PRFLINK      REMOVE THE BFR
         TM    PRFSTAT1,PRFNHDRN
         BO    WRITES                   BRANCH TO CHECK WRITE    S21903
         MVC   QCBPFEFO(3),QCBPREVF     SHUFFLE FEFO POINTERS   SA52971
         MVC   QCBPREVF(3),QCBFFEFO       FOR REUS AND RESTART  SA52971
         MVC   QCBFFEFO(3),SCBFEFO      UPDATE FEFO CHAIN        CLUP21
WRITES   EQU   *                                                 CLUP21
         IC    RNB,PRFNBUNT             NO UNITS
         ST    RPREFIX,LCBCSW-1         KEEP BFR ADDR
         LR    RUNIT,RPREFIX            UNIT ADDR
WRITE1   EQU   *
         L     RDECB,LCBDECB            ADDR FIRST WRITE DECB
WRITE    EQU   *
         EX    RNCP,CLI                 ALL DECB WRITTEN
         BE    NOTALL                   YES, GO SET UP TO CHECK  CLUP21
         LA    RNCP,1(RNCP)             ADDR 1
         LA    R9,AVTUMALN(RUNIT)       SET ADDR FOR WRITE
         L     RUNIT,PRFTIC-IEDQPRF(RUNIT)              NEXT UNIT
         WRITE   (1),SF,(10),(9),MF=E
         LA    RDECB,DECBSZ(RDECB)      NEXT DECB
         STC   RNCP,LCBNOWTR            SAVE NO WRITTEN
         BCT   RNB,WRITE                ALL UNITS WRITTNE - BR NO
         MVI   LCBNOUNT,AVTEZERO        NONE TO BE WRITTEN
         NC    LCBERBCH(3),LCBERBCH     ANY MORE BFRS
         BNZ   POSTERB2                 BR YES
         TM    PRFSTAT1,PRFNLSTN        WAS LAST SEG WRITTEN
         BO    POSTERB                  BR NO TO GET NEXT BFR
POSTERB2 EQU   *
         LR    R1,RLCB                  POST ERB TO LCB TO
         B     POSTERB1                 REGAIN CONTROL FOR CHECKS
NOTALL   EQU   *
         STC   RNB,LCBNOUNT             KEEP NO TO WRITE
         ST    RUNIT,LCBCSW+3           ADDR FOR NEXT WRITE
         MVI    LCBERBCH-1,AVTEZERO
         B     POSTERB2                 BRANCH TO POST ERROR     S21903
LCBPOST  EQU   *
         ST    RLCB,LCBQCBA-1           SET QCB ADDR FOR POST
         MVI   LCBPRI,PRILOGLB          SET TO PRTY
         LR    R1,RLCB                  FOR POST
         B     DSPPOSTR                 GO POST LCB AND RETURN   CLUP21
*                                         TO CALLER              CLUP21
GOBACK   EQU   *
         LR    RQCB,RLCB                SET FOR DISPATCHER
         L     RSCB,SCBDESTQ-1          TARGET QCB
         BAL   R14,DSPUNAVR             BRANCH TO DISPATCHER     S21903
         BAL   R14,LCBPOST              BRANCH TO POST LCB       S21903
         B     DSPDISP                  EXIT TO DISPATCHER       CLUP21
         DS    0F
IEDQBZ01 EQU   *
         DC    AL1(DSPMCPL6)            QCB                      S21903
         DC    AL3(0)                    SETUP                   S21903
         DC    X'0400'                    CONSTANT               S21903
         MVI   LCBPRI-IEDQLCB(R1),AVTEZERO
         B     DSPDISP                  BRANCH TO DISPATCHER     S21903
DECBSZ   EQU   20
OPEN     EQU   X'10'
ELOGSCH  EQU   X'16'
E01      EQU   X'01'
CLI      CLI   DCBNCP,X'00'
         TPRIOR                                                  A41038
         TSCBD
         TQCBD
         TLCBD
STCBOFF  EQU  QCBSTVTO-IEDQQCB
LCBNOUNT EQU   LCBFLAG2
LCBNOWTR EQU   LCBFLAG1
LCBDECB  EQU   LCBLSPCI-1
         TPRFD
         DCBD DSORG=BS
         TDISPD
         TAVTD 2
         END
