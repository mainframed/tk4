         TITLE 'IEDQB4  -  SLOWPOLL ROUTINE'
IEDQB4   CSECT
*C660000                                                       @G36XRAW
***********************************************************************
*                                                                     *
* STATUS - CHANGE LEVEL 0                                             *
*                                                                     *
* FUNCTION -                                                          *
*    TO INTERRUPT THE POLLING CYCLE IN PROGRESS ON A LINE. AFTER      *
*    A SPECIFIED LENGTH OF TIME, THE POLLING RESUMES IN REGULAR       *
*    ORDER. THIS FUNCTION IS HELPFUL WHEN A LINE PROBLEM IS DETECTED  *
*    BECAUSE IT AVOIDS MUCH USELESS POLLING AND SUBSEQUENT ISSUING    *
*    OF OPERATOR MESSAGES. THIS FUNCTION ALSO REDUCES OPERATOR        *
*    INTERVENTION - WHEN THE LINE IS ULTIMATELY IN WORKING ORDER      *
*    AGAIN, IT IS POLLED AUTOMATICALLY WITHOUT ANY ACTION BY THE      *
*    OPERATOR.                                                        *
*                                                                     *
*    THE INTERRUPTION OF THE POLLING CYCLE IS ACCOMPLISHED BY         *
*    PLACING THE LCB INTO THE TIME DELAY QUEUE FOR A SPECIFIED        *
*    LENGTH OF TIME.                                                  *
*                                                                     *
* ENTRY POINTS -                                                      *
*         FIRST EXECUTABLE INSTRUCTION - CALLED BY THE BUFFER         *
*         DISPOSITION SUBTASK (IEDQBD) IN AN INMESSAGE SUBGROUP       *
*         OF AN MH.                                                   *
*                                                                     *
* INPUT -                                                             *
*    R1 - LIST OF ELEMENTS TO BE POSTED                               *
*    R4 - LCB ADDRESS                                                 *
*    R8 - ADDRESS OF MACRO GENERATED PARAMETER LIST                   *
*                                                                     *
*    THE PARAMETER LIST HAS THE FOLLOWING FORMAT -                    *
*                                                                     *
*    *********************************                                *
*    *+0 B4  * LIST  *       *       *                                *
*    * INDEX * LNGTH *       *       *                                *
*    * + BIT * + LOG *       *       *                                *
*    *************************       *                                *
*    *+4                             *                                *
*    *                MASK           *                                *
*    *                               *                                *
*    *********************************                                *
*    *+4 OR +8       *               *                                *
*    *  TIME DELAY   *               *                                *
*    *  IN SECONDS   *               *                                *
*    *********************************                                *
*                                                                     *
*    BIT - MEANS THAT IF BIT 7 IS ON, THE ROUTINE IS TO BE CALLED     *
*    UNCONDITIONALLY. IN THIS CASE, THE MASK ENTRY IS OMITTED AND     *
*    THE PARAMETER LIST IS ONLY 8 BYTES LONG.                         *
*                                                                     *
*    LOG - MEANS THAT IF BIT 7 IS ON, THE ROUTINE IS TO BE CALLED     *
*    ONLY WHEN ALL OF THE BITS SPECIFIED IN THE MASK-FIELD ARE ON.    *
*    BIT 7 IS OFF IF THE ROUTINE IS TO BE CALLED WHEN ANY OF THE      *
*    BITS SPECIFIED IN THE MASK-FIELD ARE ON.                         *
*                                                                     *
*    R11 - ADDRESS OF THE DISPATCHER                                  *
*                                                                     *
*    R13 - ADDRESS OF AVTSAVE2                                        *
*                                                                     *
* OUTPUT -                                                            *
*                                                                     *
*    TO THE TIME DELAY SUBTASK -                                      *
*    R1 - ADDRESS OF THE LCB                                          *
*                                                                     *
*    TO THE DISPATCHER -                                              *
*    R1 - ADDRESS OF LCB ERB                                          *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         AVTHG01 - ADDRESS OF THE TIME DELAY ROUTINE                 *
*                                                                     *
*         DSPCHAIN - FOR RETURN                                       *
*                                                                     *
* EXITS,  NORMAL -                                                    *
*         DSPCHAIN                                                    *
*                                                                     *
* EXITS,  ERROR -                                                     *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*                                                                     *
*    AVT                                                              *
*    DISPATCHER                                                       *
*    LCB                                                              *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, ENABLED, RESIDENT, PROBLEN PROGRAM       *
*    MODE.                                                            *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN INTERNAL    *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET.                    *
*                                                                     *
* CHANGE ACTIVITY AS FOLLOWS:
*A404000,448000,836000                                         @SA72789
*                                                                     *
***********************************************************************
         EJECT
         USING IEDQSTCB,RA              STCB ADDR
         USING IEDQLCB,RLCB             LCB ADDR
         USING IEDQDISP,R11             DISPATCHER ADDR
         USING IEDQB4,RBASE             ROUTINE BASE ADDR
         USING AVTSAVE2,R13             AVT 2ND SAVE AREA ADDR
         USING IEDQSCB,R3               SCB ADDRESSABILITY     @SA72789
         SPACE 1
IEDQB4   IEDHJN IEDQB401
         SPACE 1
* FIND RECEIVE SCHEDULER STCB AND REMOVE IT FROM STCB CHAIN
* *********************************************************
         SPACE 1
         LA    R6,0(,R1)
         L     RA,LCBSTCBA-1            GET 1ST STCB ADDR
         LA    RA,0(,RA)                CLEAR TOP 8 BITS
         CLI   LCBFLAG1,AVTEZERO        CHECK FOR 3705 PLCB      S22024
         BE    B4700                    IF YES - NO SLOWPOLL     S22024
         CLI   STCBPRI,DSPCWAIT         TEST IF ANY 1ST STCB
         BE    B4700                    IF NO - NO SLOWPOLL
         L     R3,LCBSCBA-1             SCB ADDRESS            @SA72789
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN  LOCK MODE           @SA72789
         BNZ   B4700                    BRANCH IF YES,         @SA72789
*                                        NO SLOWPOLL           @SA72789
         CLI   STCBVTO,DSPRCVSC         TEST IF RECV SCHED
         BNE   B4240                    IF NO
         MVC   LCBSTCBA(THREE),STCBLINK REMOVE STCB FR STCB CHAIN
         B     B4270                    GO PREPARE LCB
         SPACE 5
B4240    LR    RB,RA                    SAVE STCB ADDR
         L     RA,STCBLINK-1            GET NEXT STCB ADDR
         LA    RA,0(,RA)                CLEAR TOP 8 BITS
         CLI   STCBPRI,DSPCWAIT         TEST IF ANY NEXT STCB
         BE    B4700                    IF NO - NO SLOWPOLL
         CLI   STCBVTO,DSPRCVSC         TEST IF RECV SCHED
         BNE   B4240                    IF NO
         MVC   STCBLINK-IEDQSTCB(THREE,RB),STCBLINK  REMOVE STCB
*                                       .. FROM STCB CHAIN
         SPACE 1
* PREPARE LCB TO BE USED AS TIME DELAY REQUEST ELEMENT
* ****************************************************
         SPACE 1
B4270    IC    RTIME,PARMLH(,RPARM)     GET PARAM LIST LH
         N     RTIME,LHMASK             CLEAR BITS 0-23 AND 31
         AR    RTIME,RPARM              GET NEXT PARAM LIST ADDR
         SH    RTIME,FOUR               GET THIS PARAM LIST LAST WORD
*                                       .. ADDR
         MVC   LCBEOLTD(TWO),0(RTIME)   INS TIME DELAY INTO LCB
         MVI   LCBTDL,LCBINSRC-1-IEDQLCB INS LCB-TYPE TIME-DELAY Q
*                                       .. OFFS
         SPACE 1
* INSERT LCB INTO TIME DELAY QUEUE
* ********************************
         SPACE 1
         MVC   LCBQCBA(THREE),AVTDELYB+1  INS TIME DELAY QCB ADDR
*                                       .. INTO LCB
         LR    R1,RLCB                  SET LCB ADDR AS ELEM-PTR
         IC    R7,LCBRSKEY              SAVE RECEIVE SCHEDULER KEY
         MVI   LCBRSKEY,DSPBUFSC        MAKE LCB LOOK AS IF FOR
*                                       .. BUFFERED LINE IN ORDER TO
*                                       .. SKIP POSTING OF BUFFER LCB
*                                       .. IN THE TIME DELAY SUBTASK
         L     R15,AVTHG01              GET TIME DELAY SUBTASK ADDR
         BALR  RETURN,R15               INS LCB INTO TIME DELAY Q
         STC   R7,LCBRSKEY              RESTORE RECEIVE SCHEDULER KEY
         SPACE 1
* EXIT FROM THIS ROUTINE
* **********************
         SPACE 1
B4700    L     R8,AVTMSGS-1             GET OPTIONAL-VCON-LIST ADDR
         L     R8,0(,R8)                GET BUFF-DISP-SUBTASK ADDR
         ST    R8,LCBERBQB-1            INS IT AS ERB QCB INTO LCB
         ST    R6,LCBERBLK-1
         MVI   LCBERBPY,PRIRCQCB        SET PRIORITY
         NI    LCBSTAT1,XFF-LCBRCLLN    TURN OFF RECALL BIT
         LA    R1,LCBERB                SET LCB ERB ADDR
         BAL   RETURN,DSPCHAIN          RETURN TO DISPATCHER   @G36XRAW
         SPACE 5
* REFERENCE SECTION
* *****************
         SPACE 1
* REGISTERS
* .........
         SPACE 1
R1       EQU   1                        ELEMENT REG
RA       EQU   1                        STCB REG
RTIME    EQU   2                        TIME DELAY
RB       EQU   2                        WORK REG
R3       EQU   3                                               @SA72789
RLCB     EQU   4                        ADDRESS OF LCB
R6       EQU   6                        SAVE REG FOR LIST FROM BD
R7       EQU   7                        SAVE REG FOR RCV SCH KEY
R8       EQU   8                        WORK REG
RPARM    EQU   8                        ADDR OF SLOWPOLL PARM LIST
R11      EQU   11                       ADDR OF DISPATCHER
RBASE    EQU   12                       ROUTINE BASE REG
R13      EQU   13                       ADDR OF AVTSAVE2
RETURN   EQU   14                       LINK REG
R15      EQU   15                       FOR BRANCH TO TIME DELAY
         SPACE 1
* PARAMETER LIST OFFSETS
* ......................
         SPACE 1
PARMLH   EQU   1                        OFFSET TO PARM LIST LNGTH
         SPACE 1
* MISCELLANEOUS
* .............
         SPACE 1
         DS    0F
LHMASK   DC    X'000000FE'              TO CLEAR PARM LNGTH REG
FOUR     DC    H'4'                     TO COMPUTE ADDR OF TIME PARM
XFF      EQU   X'FF'                    USED IN TURNING OFF BITS
TWO      EQU   2                        LENGTH OF 2 BYTES
THREE    EQU   3                        LENGTH OF 3 BYTES
         SPACE 5
         TAVTD
         SPACE 5
         TDISPD
         SPACE 5
         TLCBD
         SPACE 5
         TPRIOR
         SPACE 5
         TSTCBD
         EJECT
         TSCBD                                                 @SA72789
         SPACE 5
         DROP  RA,RLCB,R11,RBASE,R13
         SPACE 1
         END
