IEDQAD   CSECT
*A525000,650000,745000                                           A41039
*C602400,755000                                                  A41039
*D650000-710000,830000-855000                                    A41039
*A522000                                                         S22025
*C095000,12000,600800,602400-604000,790000-800000                S22025
*D601600-604000,605000                                           S22025
AD01     TITLE '''IEDQAD'' -- OUTPUT SEQUENCE INSERTION ROUTINE'
         SPACE
***********************************************************************
*                                                                     *
*STATUS -- CHANGE LEVEL 0                                             *
*                                                                     *
*FUNCTION --                                                          *
*   INSERTS THE OUTPUT SEQUENCE NUMBER IN THE BUFFER.                 *
*                                                                     *
*   THE ROUTINE ACCESSES THE OUTPUT SEQUENCE NUMBER FROM THE SCB      *
*   (SCBOSEQ) AND CONVERTS IT INTO EBCDIC, SUPPRESSING LEADING        *
*   ZEROES.  THE ROUTINE THEN FIGURES THE LENGTH OF THE NUMBER,       *
*   ADDS ONE FOR A LEADING BLANK,  AND LINKS TO THE EXPAND BUFFER     *
*   ROUTINE (IEDQAF) TO SHIFT DATA IN THE BUFFER LEFTWARDS THE        *
*   REQUIRED AMOUNT.  IF RETURN FROM IEDQAF INDICATES INSUFFICIENT    *
*   RESERVE CHARACTERS,  A X'04' IS SET IN REGISTER 15 AND RETURN     *
*   IS MADE TO THE CALLER VIA THE RETURN INTERFACE ROUTINE            *
*   (IEDQUI+4).                                                  S22025
*                                                                     *
*   IF EXPANSION WAS SUCCESSFUL,  THE ROUTINE LINKS AGAIN TO IEDQAF   *
*   TO INSERT THE OUTPUT SEQUENCE NUMBER,  INCLUDING A LEADING        *
*   BLANK,  INTO THE BUFFER.  A X'00' RETURN CODE IS SET IN REGIS-    *
*   TER 15 AND RETURN IS MADE TO THE CALLER VIA (IEDQUI+4).      S22025
*                                                                     *
*ENTRY POINTS --                                                      *
*       'IEDQAD01' TO INSERT THE OUTPUT SEQUENCE NUMBER IN THE        *
*   BUFFER.  CALLING SEQUENCE FROM USER INTERFACE IS:                 *
*                                                                     *
*        L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE        *
*        IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR     *
*        L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDRESS           *
*        BR    R12                      EXIT TO ROUTINE               *
*                                                                     *
*INPUT --                                                             *
*   REGISTER 1 - THE ADDRESS OF A MACRO-GENERATED PARAMETER LIST.     *
*   PARAMETER LIST FORMAT IS:                                         *
*                                                                     *
*        *****************                                            *
*        * INDEX * INDEX *                                            *
*        *  TO   *  TO   *                                            *
*        *   AD  *   AF  *                                            *
*        *****************                                            *
*                                                                     *
*   REGISTER 3 - THE ADDRESS OF THE SCB.                              *
*                                                                     *
*   REGISTER 6 - THE ADDRESS OF THE CURRENT BUFFER.                   *
*                                                                     *
*   REGISTER 9 - THE ADDRESS OF THE AVT.                              *
*                                                                     *
*   REGISTER 12 - THE ENTRY POINT ADDRESS AND BASE REGISTER FOR       *
*   THIS ROUTINE.                                                     *
*                                                                     *
*OUTPUT --                                                            *
*   REGISTER 15 - RETURN CODE.  CONTAINS X'00' ON SUCCESSFUL          *
*   COMPLETION OR X'04' IF INSUFFICIENT RESERVE CHARACTERS ARE        *
*   SPECIFIED.                                                        *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*   'IEDQAF' - EXPAND BUFFER ROUTINE.                                 *
*                                                                     *
*                                                                     *
*   AVT - ADDRESS VECTOR TABLE.                                       *
*                                                                     *
*   BUFFER CURRENTLY BEING PROCESSED.                                 *
*                                                                     *
*   SCB - STATION CONTROL BLOCK.                                      *
*                                                                     *
*EXITS,  NORMAL -- SUCCESSFUL COMPLETION-  REGISTER 15 CONTAINS A     *
*   RETURN CODE OF X'00'.  THE OUTPUT SEQUENCE NUMBER IS PLACED IN    *
*   THE BUFFER.                                                       *
*                                                                     *
*EXITS,  ERROR -- INSUFFICIENT RESERVE CHARACTERS ARE SPECIFIED.      *
*   REGISTER 15 CONTAINS A RETURN CODE OF X'04'.                      *
*                                                                     *
*TABLES/WORK AREAS    -- TRANSLATE-AND-TEST TABLE USED TO SUPPRESS    *
*   LEADING ZEROES.                                                   *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE,  REFRESHABLE,  ENABLED,             *
*   RESIDENT,  PROBLEM PROGRAM MODE.                                  *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN        *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
*                                                                     *
***********************************************************************
********* REGISTER EQUATES *********
         SPACE
R1       EQU   1                         PARAMETER LIST ADDRESS
RWORK2   EQU   2                         WORK REGISTER
RSCB3    EQU   3                         SCB ADDRESS
RPREFIX  EQU   6                         BUFFER ADDRESS
RTRAN7   EQU   7                         TRANSLATE TABLE ADDRESS
RSAVE8   EQU   8                        SAVE REGISTER
RAVT9    EQU   9                         AVT ADDRESS
RRET10   EQU   10                        LOCAL RETURN REGISTER
RBASE    EQU   12                        ENTRY POINT & BASE REGISTER
R13      EQU   13                        SAVE AREA ADDRESS
R14      EQU   14                        RETURN REGISTER
R15      EQU   15                        LINK REGISTER
         SPACE
********* OTHER EQUATES *********
         SPACE
ULEN     EQU   4                         LENGTH OF UNPACKED SEQ NO.
FOUR     EQU   4                        OFFSET USED ON RETURN    S22025
PLEN     EQU   3                         LENGTH OF PACKED SEQ NO.
ILEN     EQU   5                        INSERT LNG               A41039
         SPACE
CHARZERO EQU   C'0'                      EBCDIC ZERO
         SPACE
PARMAF   EQU   1                        IEDQAF INDEX
         SPACE
EXBFFLAG EQU   X'03'                     EXPAND REQUEST FLAG
         SPACE
ONE      EQU   1                         OFFSET OF ONE BYTE
         EJECT
         USING IEDQAVTD,RAVT9
         USING IEDQPRF,RPREFIX
         USING IEDQSCB,RSCB3
         USING IEDQAD,RBASE
         SPACE
IEDQAD01 EQU   *
IEDQAD   IEDHJN SEQOUT                  MODULE ID                S22025
         IC    RWORK2,PARMAF(,R1)        PICK UP OFFSET TO IEDQAF
         STC   RWORK2,AVTPARM            SET IN PARM LIST
         SPACE
         LH    RWORK2,SCBOSEQ            LOAD SEQUENCE OUT NUMBER
         CVD   RWORK2,AVTDOUBL           CONVERT IT TO DECIMAL
         UNPK  AVTDOUBL+1(ULEN),AVTDOUBL+5(PLEN) UNPACK IT
         OI    AVTDOUBL+ULEN,CHARZERO    MAKE SIGN BYTE NORMAL ZONE
         SPACE
         MVI   AVTDOUBL,AVTEBLNK        PUT IN BLANK             A41039
         MVI   AVTPARM+1,ILEN           SET EXPAND LNG           A41039
         OI    AVTPARM,EXBFFLAG          REQUEST EXPAND FUNCTION
         BAL   R14,LINKAF                LINK TO EXPAND BUFFER
         SPACE
         LTR   R15,R15                   TEST FOR GOOD RETURN
         LA    R15,AVTECD4               ANTICIPATE ERROR RETURN CODE
         BNZ   EXIT                      RETURN TO MH IF ERROR
         SPACE
         LA    RSAVE8,AVTDOUBL          GET INSERT DATA ADDR     A41039
         ST    RSAVE8,AVTPARM3           STORE INSERT DATA ADDRESS
         MVI   AVTPARM3,ILEN            SET INSERT LNG           A41039
         MVI   AVTPARM+1,AVTEZERO        SET DATA TYPE = CHARS
         NI    AVTPARM,AVTEFF-ONE        REQUEST INSERT AND RETURN
         BAL   R14,LINKAF               LINK TO INSERT DATA
         SPACE
         SR    R15,R15                   SET GOOD RETURN CODE
         SPACE
EXIT     EQU   *
         L     RBASE,AVTUI              GET RET INTERFACE ADDR   S22025
         B     FOUR(RBASE)              BRANCH TO RETURN ROUTINE S22025
         SPACE
LINKAF   EQU   *
         LA    R1,AVTPARM                POINT TO IEDQAF'S PARM LIST
         L     R15,AVTUI                 GET USER INTERFACE ADDRESS
         BR    R15                       LINK TO IEDQAF
         EJECT
********* DSECTS *********
         SPACE
         TAVTD
         EJECT
         TPRFD
         EJECT
         TSCBD
         SPACE
         END
