IEDQBS   CSECT
         TITLE '''IEDQBS'' - SHOW CONTROL BLOCK CONTENTS MODULE'
***********************************************************************
*                                                                     *
* MODULE NAME = IEDQBS                                                *
*                                                                     *
* DESCRIPTIVE NAME = DISPLAY CONTROL BLOCK CONTENTS                   *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = VERSION 10.0                                               *
*                                                                     *
* FUNCTION = PROVIDES CONTENTS OF SELECTED TCAM CONTROL BLOCKS TO A   *
*            USER WHEN HE ISSUED AN IEDSHOW MACRO.                    *
*                                                                     *
* NOTES = ALL OF THE INCLUDED MEMBERS OF THE SYSLIB EXTERNAL LIBRARY  *
* THAT CONTAIN DSECTS (DUMMY CONTROL SECTIONS) ARE INCLUDED           *
* ALPHABETICALLY BY THE PREFIX LETTERS USED FOR THE MEMBER VARIABLES. *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = ALL OF THE REGISTER SYMBOLS USED BY THIS  *
*    MODULE ARE DEFINED IN NUMERICAL ORDER BY REGISTER NUMBER.        *
*                                                                     *
*    PATCH LABEL = NONE                                               *
*                                                                     *
* MODULE TYPE = PROCEDURE                                             *
*                                                                     *
*    PROCESSOR = ASSEMBLER                                            *
*                                                                     *
*    MODULE SIZE = (SEE LISTING)                                      *
*                                                                     *
*    ATTRIBUTES = THIS MODULE IS SERIALLY REUSABLE                    *
*                                                                     *
* ENTRY POINT = IEDQBS AND IEDQBS+4                                   *
*                                                                     *
* INPUT = ENTRY TO IEDQBS (FROM IEDQUI)                               *
*              R1 - ADDRESS OF PARAMETER LIST (SEE PARMLIST DSECT)    *
*              R3 - SCB ADDRESS                                       *
*              R4 - LCB ADDRESS                                       *
*              R6 - BUFFER ADDRESS                                    *
*              R9 - AVT ADDRESS                                       *
*              R11 - DISPATCHER BASE                                  *
*              R12 - ENTRY POINT                                      *
*              R13 - SAVE AREA ADDRESS                                *
*                                                                     *
*         ENTRY TO IEDQBS+4 (FROM OUTSIDE MH)                         *
*              R1 - ADDRESS OF PARAMETER LIST (SEE PARMLIST DSECT)    *
*              R13 - SAVE AREA ADDRESS                                *
*              R14 - RETURN ADDRESS                                   *
*              R15 - ADDRESS OF IEDQBS                                *
*                                                                     *
* OUTPUT = REQUESTED CONTROL BLOCK INFORMATION IN USER PROVIDED       *
*          RESULT FIELD AND REGISTER 15=0; OR                         *
*          R15 = 4  IF FUNCTION NOT SUPPORTED; OR                     *
*          R15 = 8  IF STATION NOT FOUND                              *
*                                                                     *
*                                                                     *
* EXIT-NORMAL = IEDQUI+4 (IF FROM MH),  BR 14 (IF NOT FROM MH)        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = THE EXTERNAL REFERENCES FOR THIS MODULE ARE   *
* DESCRIBED UNDER THE FOLLOWING SUBORDINATE TOPICAL HEADINGS.         *
*                                                                     *
*    ROUTINES = THE FOLLOWING IS A LIST OF ALL OF THE EXTERNAL        *
*    ROUTINES THAT ARE REFERENCED BY THIS MODULE:                     *
*                  IEDQA1                                             *
*                  IEDQTNT                                            *
*                  IEDQUI                                             *
*                                                                     *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCKS = AVT,CVT,TCX,LCB,BUFFER,SCB,TTE,QCB,SIB,         *
*                     PARMLIST DEFINED BY PARMLIST DSECT.             *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = THE MACROS USED BY THIS MODULE IS  IEDHJN                  *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
* A000000-999999                                              @Y17XANQ*
*                                                                     *
***********************************************************************
         EJECT
R0       EQU   0
RPARM    EQU   1                        INPUT PARM LIST
R2       EQU   2                        WORK REGISTER
RSCB     EQU   3                        SCB ADDRESS
RLCB     EQU   4                        LCB ADDRESS
RFIND    EQU   5                        WORK REGISTER
RBUF     EQU   6                        BUFFER ADDRESS
R7       EQU   7                        WORK REGISTER          @YM08177
RAVT     EQU   9                        AVT ADDRESS
RTERM    EQU   10                       TERMINAL ENTRY ADDRESS
RBASE    EQU   12                       BASE
RSAVE    EQU   13                       SAVE AREA ADDRESS
RETURN   EQU   14                       RETURN
R15      EQU   15                       ENTRY POINTREG IF FROM OUTSIDE
*                                       MH
CVTADD   EQU   16                       CVT ADDRESS
TWELVE   EQU   12                       CONSTANT OF 12
FOUR     EQU   4                        CONSTANT OF 4
EIGHT    EQU   8                        CONSTANT OF 8
ONE      EQU   1                        CONSTANT OF 1
TWO      EQU   2                        CONSTANT OF 2
THREE    EQU   3                        CONSTANT OF 3
SIX      EQU   6                        CONSTANT OF 6
*        TSTATUS +3 RESULT BYTE
TS01     EQU   X'01'                    TRMLULOG
TS02     EQU   X'02'                    DCT 3270
TS10     EQU   X'10'                    TRMSPACT
TS40     EQU   X'40'                    TRMTDIFF
TS80     EQU   X'80'                    TRMRSACT
         EJECT
         USING PARMLIST,R2
         USING IEDQAVTD,RAVT
         USING IEDQPRF,RBUF
         USING IEDQLCB,RLCB
         USING IEDQBS,RBASE
         B     INMH                     ENTRY FROM IEDQUI
         B     NOTMH-IEDQBS(,R15)       ENTRY FROM OUTSIDE MH
IEDQBS   IEDHJN
NOTMH    DS    0H
         STM   R0,RETURN,TWELVE(RSAVE)  SAVE CALLERS REGISTERS
         L     RAVT,CVTADD              CVT ADDRESS
         L     RAVT,AVTCVTPT(,RAVT)     TCX ADDRESS
         L     RAVT,0(,RAVT)            AVT ADDRESS
         L     RBUF,AVTADBUF            BUFFER ADDRESS
         L     RLCB,PRFLCB-1            LCB ADDRESS
         L     RSCB,LCBSCBA-1           SCB ADDRESS
         LR    RBASE,R15                SET UP BASE
         SPACE 2
INMH     EQU   *
         LR    R2,RPARM                 SAVE PARAMETER LIST
         SR    RFIND,RFIND              CLEAR FOUR IC
         IC    RFIND,BSCBI              CONTROL BLOCK ID
         BCT   RFIND,NOTBFR             BRANCH IF NOT BUFFER
         LR    RPARM,RBUF               BUFFER IS CONTROL BLOCK
         LA    R15,PRFSIZE-IEDQPRF      OFFSET TO PRFSIZE
         CLM   R15,1,BSCBO              IS DATASIZE REQUESTED
         BNE   CKFORM                   BRANCH NO
         SPACE 1
         IC    RFIND,LCBISZE            COUNT OF RESERVES
         LA    RFIND,PRFSTXT-PRFNBUNT(,RFIND) TEXT PREFIX SIZE @XM05737
         TM    PRFSTAT1,PRFNHDRN        IS THIS A TEXT BUFFER
         BO    NOTHDR                   BRANCH YES
         SPACE 1
         LA    RFIND,PRFSHDR-PRFSTXT(,RFIND) ADD HEADER
*                                       INCREMENT
NOTHDR   EQU   *
         LH    RPARM,PRFSIZE            PICK UP PRFSIZE
         N     RPARM,AVTCLRHI           CLEAR HI ORDER
         BZ    STOREIT                  BRANCH IF ZERO
         SPACE 1
         SLR   RPARM,RFIND              SUBTRACT PREFIX SIZE
STOREIT  EQU   *
         L     R15,BSRSLT               ADDRESS OF RESULT AREA
         STH   RPARM,0(R15)             PUT IN DATA SIZE
         B     EXIT2                    EXIT
         SPACE 2
CKFORM   EQU   *
         SPACE
         LA    R15,PRFDEST-IEDQPRF      PRFDEST OFFSET
         CLM   R15,1,BSCBO              REQUEST FOR PRFDEST
         BNE   NOTDEST                    BRANCH NO
         SPACE 1
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER
         BO    NOTFOUND                   BRANCH NO - NO PRFDEST
         SPACE 1
NOTDEST  EQU   *
         TM    BSFMT,BSFMNAM            NAME FORMAT REQUESTED
         BZ    MOVEIT                     BRANCH NO
         SPACE 1
         IC    RFIND,BSCBO              OFFSET TO SOURCE/DESTINATION
         LH    RPARM,0(RFIND,RPARM)     PICK UP TTCIN
         N     RPARM,AVTCLRHI           CLEAR HIGH ORDER
         BZ    NOTFOUND                   BRANCH IF ZERO
         L     R15,AVTRNMPT             ADDRESS OF TNT
         USING IEDQTNTD,R15
         IC    RFIND,TNTENLEN           LENGTH OF TERMINAL NAMES
         BCTR  RPARM,0                  DECREMENT TTCIN
         LA    RBUF,THREE(,RFIND)       ADD THREE
         MR    R0,RBUF                  MULTIPLY BY LENGTH
         LA    RPARM,TNTFIRST(RPARM)    ADDRESS OF TERM NAME
         BCTR  RFIND,0                  DECREMENT LENGTH
         B     MOVE2                    MOVE NAME TO USER AREA
NOTBFR   EQU   *
         BCT   RFIND,NOTLCB             BRANCH IF NOT LCB
         LR    RPARM,RLCB               CONTROL BLOCK IS LCB
         LA    R15,LCBTTCIN-IEDQLCB     TTCIN OFFSET
         CLM   R15,1,BSCBO              IS THIS LCATTEN
         BE    NOTDEST                  BRANCH YES
         B     MOVEIT                   MOVE DATA
         SPACE 2
NOTLCB   EQU   *
         BCT   RFIND,NOTSCB             BRANCH IF NOT SCB
         LR    RPARM,RSCB               SCB IS CONTROL BLOCK
         B     MOVEIT                   MOVE DATA
         SPACE 2
NOTSCB   EQU   *
         LH    RPARM,LCBTTCIN           USE CURRENTLY CONNECTED
         TM    BSFLGS,BSSTNM            WAS STATION= SPECIFIED
         BZ    USETTC                     BRANCH NO - USE CURRENT
         SPACE
         LR    RLCB,RSAVE               PRESERVE USER SAVE AREA
         SR    R0,R0                    CLEAR
         L     R15,AVTRNMPT             TNT ADDRESS
         IC    R0,TNTENLEN              MAXIMUM STATION NAME
         L     RPARM,BSSTAT             POINT TO STATION NAME
         L     R15,AVTMSGS-1            VCON TABLE
         L     R15,EIGHT(,R15)          ADDRESS OF IEDQA1
         LA    RSAVE,INTSAVE            GIVE A1 A SAVE AREA
         BAL   RETURN,FOUR(,R15)        LINK TO A1
         SPACE
         LR    RSAVE,RLCB               RESTORE SAVE AREA
         LTR   RPARM,R15                DID BINARY SEARCH FIND NAME
         BZ    NOTFOUND                   BRANCH NO
         SPACE
USETTC   EQU   *
         N     RPARM,AVTCLRHI           ZERO HI
         BZ    NOTFOUND                 NO TTE
         L     R15,AVTRNMPT             TNT ADDRESS
         BALR  RETURN,R15                 FIND TTE ADDRESS
         SPACE 1
         BCT   RFIND,NOTTRM             BRANCH IF NOT TTE
         SPACE 1
         CLI   BSCBO,X'FF'              IS TSTATUS REQUESTED
         BNE   MOVEIT                     BRANCH NO - MOVE DATA
         LR    RTERM,RPARM              TTE ADDRESS
         LA    RPARM,TRMPRFSZ           PREFIX SIZE
         SLR   RTERM,RPARM              BACK UP TO PREFIX
         USING IEDNTRM,RTERM
         L     R15,BSRSLT               RESULT AREA
         XC    0(SIX,R15),0(R15)        CLEAR RESULT
         MVC   ONE(ONE,R15),TRMSTATE    MOVE TRMSTATE
         IEDDCT REG=RFIND,FLD=FLDDCT,LEN=6                     @YM08479
         USING IEDDCT,RFIND
         TM    DCTBYTE1,DCT3270         IS THIS A 3270         @YM08479
         BZ    NOT3270                  BRANCH NO
         OI    THREE(R15),TS02          INDICATE 3270
NOT3270  EQU   *
         DROP  RFIND
         TM    TRMSTATE,TRMPREF         PREFIX EXIST
         BZ    EXIT2                    BRANCH NO - EXIT
         SPACE 1
         MVC   0(ONE,R15),TRMTYPE       SET UP TRMTYPE
         MVC   TWO(ONE,R15),TRMBYTE0    SET UP STATUS
         NI    TWO(R15),X'FF'-X'90'     RESET IRRELEVANT BITS
         TM    TRMLU,TRMLULOG           LU CAPABLE
         BZ    NOTLU                    BRANCH NO
         OI    THREE(R15),TS01          SET FLAG
NOTLU    EQU   *
         TM    TRMBYTE2,TRMRSACT        RESOURCE ACTIVE
         BZ    NOTRSA                   BRANCH NO
         SPACE 1
         OI    THREE(R15),TS80          INDICATE ACTIVE
NOTRSA   EQU   *
         TM    TRMPRE1,TRMSPACT         LINE ACTIVE
         BZ    NOTSPA                   BRANCH NO
         SPACE 1
         OI    THREE(R15),TS10          INDICATE LINE ACTIVE
NOTSPA   EQU   *
         TM    TRMLU,TRMTDIFF           TOTE TRM IND
         BZ    NOTOTE                   BRANCH NO
         OI    THREE(R15),TS40          INDICATE TOTE
NOTOTE   EQU   *
         TM    TRMBYTE0,TRMSNA          SNA DEVICE
         BZ    EXIT2                    BRANCH NO - DONE
         SPACE 1
         TM    TRMSTATE,TRMLINE         SNA LINE
         BO    EXIT2                    BRANCH YES - DONE
         SPACE 1
         L     RPARM,TRMSIBPT-1         ACCESS SIB
         LA    RPARM,0(,RPARM)          CHECK IF
         LTR   RPARM,RPARM              SIB IS ALLOCATED
         BZ    EXIT2                    BRANCH NO
         SPACE 1
         L     R7,AVTSAVTP              SAVT ADDRESS           @YM08177
         LH    RPARM,SIBINDEX-IEDSIBD(RPARM) SESSION PARTNER
         CH    RPARM,SAVTSCPT-IEDNSVTD(R7)  IS THIS SSCP SIB   @YM08177
         BE    EXIT2                    BRANCH YES - DONE
         SPACE 1
         STH   RPARM,FOUR(R15)          INDICATE SESSION
*                                       PARTNER
         B     EXIT2                    EXIT
         SPACE 2
NOTTRM   EQU   *
         BCT   RFIND,BADCODE            BRANCH IF NOT QCB
         L     RPARM,TRMDESTQ-IEDQTRM-1(,RPARM)  QCB ADDRESS
         B     MOVEIT                     MOVE DATA
         SPACE 1
BADCODE  EQU   *
         LA    R15,FOUR                 INDICATE FUNCTION NOT SUPPORTED
         B     EXIT                     EXIT
         SPACE 1
NOTFOUND EQU   *
         LA    R15,EIGHT                STATION NOT FOUND
         B     EXIT                     EXIT
         SPACE 2
MOVEIT   EQU   *
         IC    RFIND,BSCBO              CONTROL BLOCK OFFSET
         ALR   RPARM,RFIND              ADD TO CONTROL BLOCK ORIGIN
         IC    RFIND,BSFLN              LENGTH OF FIELD-1
MOVE2    EQU   *
         L     R15,BSRSLT               ADDRESS OF RESULT FIELD
         EX    RFIND,MOVEFLD            MOVE FIELD
EXIT2    EQU   *
         SLR   R15,R15                  SET GOOD RETURN CODE
EXIT     EQU   *
         CLI   BSINDEX,X'FF'            WAS ENTRY FROM IEDQUI
         BE    OUTEXIT                    BRANCH NO
         SPACE 1
         L     RBASE,AVTUI              RETURN INTERFACE ADDRESS
         B     FOUR(,RBASE)             RETURN TO CALLER
         SPACE 2
OUTEXIT  EQU   *
         LM    R0,RETURN,TWELVE(RSAVE)  RESTORE USER REGISTERS
         BR    RETURN                   RETURN
         SPACE 1
INTSAVE  DS    18F
FLDDCT   DS    CL6                      DCT WORKAREA           @YM08479
MOVEFLD  MVC   0(,R15),0(RPARM)         EXECUTED MOVE
         EJECT
         TAVTD
         EJECT
PARMLIST DSECT
BSINDEX  DS    XL1                      VCON TABLE INDEX
BSLEN    DS    XL1                      PARM LIST LENGTH
BSIND2   DS    XL1                      INDEX TO IEDQBS
BSFLGS   DS    XL1                      FLAGS
BSSTNM   EQU   1                        STATION= WAS SPECIFIED
BSFMT    DS    XL1                      FORMAT/SPECIAL REQUESTS
BSFMNAM  EQU   1                        PROVIDE STATION NAME-NOT TTCIN
BSCBI    DS    XL1                      CONTROL BLOCK IDENTIFIER
BSBFR    EQU   1                          CB= BUFFER
BSLCB    EQU   2                          CB= LCB
BSSCB    EQU   3                          CB= SCB
BSTRM    EQU   4                          CB= TERMINAL ENTRY
BSQCB    EQU   5                          CB= QCB
BSCBO    DS    XL1                      CONTROL BLOCK OFFSET
BSFLN    DS    XL1                      LENGTH OF FIELD-1
BSRSLT   DS    A                        RESULT FIELD LOCATION
BSSTAT   DS    A                        ADDRESS OF STATION NAME IF
*                                       SPECIFIED
         EJECT
         TDCTD
         EJECT
         TLCBD
         TPRFD
         TSCBD
         TSIBD
         TTNTD
         TTRMD
         END
