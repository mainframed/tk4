E4       TITLE '''IEDQE4'' - TCAM ICOPY SERVICE ROUTINE'
IEDQE4   CSECT
         SPACE 3                                                 S21903
*  CHANGE ACTIVITY AS FOLLOWS
*********************MICROFICHE FLAGS*************************SUPT CODE
*C THIS MODULE HAS BEEN COMPLETELY REWRITTEN FOR TCAM LEVEL 6    Y02027
*    CLEAN UP                                                    S21903
*        THIS MODULE HAS BEEN REWRITTEN FOR FETCH PROTECT        Y01004
* CHANGE ACTIVITY AS FOLLOWS:
*A865000                                                         Y05330
*A827000,880000                                                 SA61775
*C444000                                                       @OZ09941
         SPACE 4
***********************************************************************
*  TITLE:  'IEDQE4'-- ICOPY SERVICE ROUTINE                           *
*                                                                     *
*  MODULE NAME = IEDQE4                                               *
*                                                                     *
*  DESCRIPTIVE NAME = ICOPY SERVICE ROUTINE                           *
*                                                                     *
*  COPYRIGHT = NONE                                                   *
*                                                                     *
*  STATUS = CHANGE LEVEL 6                                       Y02027
*                                                                     *
* FUNCTION: THIS MODULE BUILDS AND INITIALIZES A PARAMETER LIST FOR   *
*   SVC 102.  THE ACTUAL PROCESSING FOR ICOPY IS DONE BY IGC0A10D IN  *
*   THE MCP.  IGC0A10D GETS CONTROL VIA SVC 102'S TPOSTING THE OP     *
*   CONTROL QCB.                                                      *
* ENTRY POINT:  IEDQE4                                                *
* INPUT:                                                              *
*         R0  - RELATIVE LINE NUMBER IN HIGH ORDER BYTE AND DDNAME    *
*               ADDRESS IN LOW ORDER THREE BYTES                      *
*         R1    ADDRESS OF USER'S WORK AREA                           *
*         R13 - ADDRESS OF USER SAVEAREA                              *
*         R14   RETURN  ADDRESS                                       *
*         R15   ENTRY   ADDRESS                                       *
* OUTPUT:                                                             *
*         R15 - COMPLETION CODE                                       *
* EXTERNAL ROUTINES:                                                  *
*         SVC 102 (IEDQEB)                                            *
* EXIT:    COMPLETION CODE                                            *
*         0   - NORMAL                                                *
*         1   - INVALID RELATIVE LINE NUMBER                          *
*        12   - CALLER NOT A VALID TCAM APPLICATION PROG              *
*        18   - AIB TOO SMALL TO ACCOMODATE DATA                      *
*        20   - INVALID DDNAME                                        *
* TABLES/WORKAREAS:                                                   *
*         NONE                                                        *
* ATTRIBUTES: SERIALLY REUSABLE, PROBLEM PROGRAM MODE                 *
* NOTES:  THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL       *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
***********************************************************************
         EJECT                                                   S21903
RZERO    EQU   0                        PARAMETER REGISTER
RPARM    EQU   1                        PARAMETER LIST ADDRESS   S22024
RWORK    EQU   2                        WORK REGISTER            Y02027
RBASE    EQU   12                       CSECT BASE REGISTER
RSAVE    EQU   13                       SAVEAREA POINTER
RETURN   EQU   14                       RETURN ADDRESS
RENTRY   EQU   15                       ENTRY POINT/RETURN CODE
         SPACE 2
         SPACE 3                                                 S21903
         SAVE  (14,12)                  SAVE USERS REGS          Y01004
         BALR  RBASE,RZERO              ESTABLISH                Y01004
         USING *,RBASE                  ADDRESSABILITY           Y01004
IEDQE4   IEDHJN ENTER                                            Y01004
         EJECT
         ST    RSAVE,SAVEAREA+FOUR      POINTER TO USER SAVEAREA Y01004
         LA    RSAVE,SAVEAREA           POINTER TO SAVE AREA     Y01004
         SPACE 2                                                 Y02027
**************************************************************** Y02027
*              BUILD PARAMETER LIST INPUT TO SVC 102           * Y02027
**************************************************************** Y02027
         SPACE 1
         ST    RPARM,APUSWKA            STORE USER WORKAREA PTR  Y02027
         ST    RZERO,APNAME             SET RLN AND DDNAME IN    Y02027
*                                       PARM LIST                Y02027
         OI    APCOMCD,ICOPYCD          TURN ON ICOPY FLAG     @OZ09941
         LA    RZERO,TWO                SET FUNCTION CODE        Y02027
         LA    RPARM,APPARM             SET PARM REG FOR SVC 102 Y02027
         AQCTL                          ISSUE SVC                Y02027
         SPACE 2
         LA    RWORK,RC80               CHECK FOR ERROR RET CD   Y02027
         CR    RENTRY,RWORK             IS TCAM ACTIVE           Y02027
         BNE   EXIT                     BR IF YES                Y02027
         LA    RENTRY,NOTCAM            MPP EXPECTS RET CD = 08  Y02027
         SPACE 1
EXIT     EQU   *                                                 Y02027
         L     RSAVE,SAVEAREA+FOUR      RESTORE CALLER'S SAVE    Y02027
*                                       AREA POINTER             Y02027
         RETURN (14,12),T,RC=(15)       RETURN TO USER
         SPACE 2
         EJECT
**************************************************************** Y02027
*        C O N S T A N T S   A N D   E Q U A T E S             * Y02027
**************************************************************** Y02027
         SPACE 2
SAVEAREA DC    18F'0'                   MY SAVE AREA             Y02027
NOTCAM   EQU   8                        TCAM NOT ACTIVE CODE     Y02027
NOPEN    EQU   12                       NO OPEN DCB CODE         Y02027
ID       EQU   X'C0'                    TCAM DEB ID              Y02027
ZERO     EQU   0                        OFFSET                   Y02027
TWO      EQU   2                        FUNCTION CDOE            Y02027
FOUR     EQU   4                        OFFSET & MOVE LENGTH     Y02027
ICOPYCD  EQU   X'50'                    ICOPY FLAG               Y02027
ADD      EQU   7                        ADDRESS MASK             Y02027
ONE      EQU   1                        OFFSET                   Y02027
THREE    EQU   3                        MOVE LENGTH              Y02027
FIVE     EQU   5                        OFFSET                   Y02027
EIGHT    EQU   8                        OFFSET                   Y02027
RC80     EQU   X'80'                    CODE RETURNED BY SVC 102 Y02027
*                                       IF TCAM NOT ACTIVE       Y02027
TWLV     EQU   12                       OFFSET
         SPACE 2
**************************************************************** Y02027
*              MAP OF INPUT DATA PROVIDED TO SVC 102           * Y02027
**************************************************************** Y02027
APCIBMAP DS    0F                                                Y02027
APPASWD  DC    AL4(0)                   POINTER TO PASSWORD      Y02027
APECB    DC    F'0'                     ECB FOR WAIT IN SVC 102  Y02027
*                                       AND SUBSEQUENT XMPOST    Y02027
APPCBAD  DC    AL4(0)                   PCB POINTER              Y02027
APCOMCD  DS    0XL1                     COMMAND VERB CODE        Y02027
APUSWKA  DC    AL4(0)                   PTR TO USER WORK AREA    Y02027
APNAME   DC    AL4(0)                   PTR TO TERMINAL NAME     Y02027
APPARM   DS    0F                       PARAMETER FOR SVC 102    Y02027
         DC    X'84'                    EOL X'80' + FUNCTION     Y02027
*                                       CODE X'04'               Y02027
         DC    AL3(APCIBMAP)            PTR TO INPUT DATA        Y02027
         SPACE 1
**************************************************************** Y02027
         EJECT
      END
