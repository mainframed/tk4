BC01     TITLE '''IEDQBC'' - DISTRIBUTION LIST SUBTASK'
IEDQBC   CSECT
         SPACE 3
******************** MICROFICHE FLAGS *********************** SUPT CODE
*CHANGE ACTIVITY=AS FOLLOWS:
*C672000-677400                                                 SA61724
*A933000                                                        SA61724
*D674400                                                       @YA05454
*C675600                                                       @YA05454
*D798000                                                      OY11956
*A486000,528000,666000,792000,834000,933000                    @OS77848
*D546000,558000-570000                                         @OS77848
*A768000,948000                                                @OY16445
***********************************************************************
*
* MODULE NAME=IEDQBC                                                  *
*                                                                     *
* DESCRIPTIVE NAME=DISTRIBUTION LIST SUBTASK                          *
*                                                                     *
* COPYRIGHT='NONE'                                                    *
*                                                                     *
* STATUS -- CHANGE LEVEL 5                                            *
*                                                                     *
*FUNCTION -- THIS SUBTASK GAINS CONTROL WHEN A MESSAGE HAS BEEN       *
*   SENT TO A DESTINATION LIST ENTRY IN THE TERMINAL TABLE.  THE      *
*   MESSAGE IS TPOSTED TO THE FIRST/NEXT ENTRY IN THE LIST. SINCE     *
*   THE DUPLICATE HEADER BIT IS ON IN THE PREFIX, IEDQHM TPOSTS THE   *
*   BUFFER TO THE QCB INDICATED IN LCBRCQCB (IN THIS CASE IEDQBC)     *
*   AFTER THE MESSAGE HAS BEEN QUEUED.  WHEN THE BUFFER HAS BEEN      *
*   RETURNED FOR THE LAST ENTRY IN THE LIST, THE BUFFER IS TPOSTED TO *
*   THE BUFFER RETURN QCB AND THE ERB IS TPOSTED TO IEDQBD, UNLESS    *
*   MULTIPLE ROUTING IS ACTIVE FOR THIS MESSAGE.  IN THIS CASE,       *
*   LCBRCQCB IS SET TO THE ADDRESS OF THE MULTIPLE ROUTE QCB AND      *
*   THE BUFFER IS TPOSTED TO IEDQBA - THE MULTIPLE ROUTE SUBTASK.     *
*                                                                     *
*ENTRY POINT -- IEDQBC                                                *
*                                                                     *
*   CALLING SEQUENCE -                                                *
*                  L     R1,ELMAD                                     *
*                  L     R15,QCBSTCHN-1                               *
*                  LA    R15,4(R14)                                   *
*                  BR    R15                                          *
*                                                                     *
*INPUT -- ENTRY IS ALWAYS FROM THE TCAM DISPATCHER                    *
*         PARAMETERS ARE PASSED IMPLICITLY IN REGISTERS AS FOLLOWS:   *
*                  R1    ADDRESS OF THE BUFFER OR ERB                 *
*                  R11   BASE FOR TCAM DISPATCHER                     *
*                  R13   ADDRESS OF AVTSAVE2                          *
*                  R15   BASE FOR THIS ROUTINE                        *
*                                                                     *
*OUTPUT -- THE MESSAGE IS TPOSTED TO A DESTINATION QCB. THE SCB AND   *
*   PREFIX DESTINATION FIELDS ARE SET FOR THE NEW DESTINATION.        *
*                                                                     *
*EXTERNAL ROUTINES -- IEDQTNT                                         *
*                                                                     *
*EXITS - NORMAL -- THE TCAM DISPATCHER AT ENTRY POINT DSPCHAIN        *
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORKAREAS --                                                  *
*   DSECTS USED - LCB,SCB,AVT,DIS,TERM,PRF                            *
*                                                                     *
*ATTRIBUTES -- REENTRANT, REFRESHABLE, ENABLED, RESIDENT, PROBLEM     *
*              PROGRAM MODE.                                          *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET  *
*                                                                     *
***********************************************************************
*
*              REGISTERS
*
R0       EQU   0                        REG 0                    S22024
R1       EQU   1                        TERMINAL TABLE ENTRY
R2       EQU   2                        REG 2                    S22024
RSCB     EQU   3                        SCB ADDRESS
RLCB     EQU   4                        LCB ADDRESS
R5       EQU   5                        REG 5                    S22024
RPREFIX  EQU   6                        BUFFER ADDRESS
RQCB     EQU   7                        LAST DISPATCHED QCB (BASE)
R8       EQU   8                        REG 8                    S22024
R9       EQU   9                        REG 9                    S22024
R10      EQU   10                       REG 10                   S22024
R11      EQU   11                       ADDRESS OF DISPATCHER
R12      EQU   12                       REG 12                   S22024
R13      EQU   13                       AVTSAVE2
R14      EQU   14                       REG 14                   S22024
R15      EQU   15                       SUBTASK ENTRY POINT
         USING AVTSAVE2,R13
         USING IEDQDISP,R11
         USING *,RQCB
         DC    CL8'IEDQBC01'            QCB/STCB
*                                       (IEDQBA EXAMINES 6TH BYTE --
*                                           C INDICATES DISTRIBUTION
*                                             LIST
*                                           G INDICATES CASCADE LIST)
         DC    AL1(DSPMCPL4)            SUBTASK ENTRY CODE
         DC    AL3(*-1)                 STCB ADDRESS
         EJECT
         SR    R2,R2                    CLEAR REGISTER
         L     R15,AVTRNMPT             ADDR OF TERNAME TABLE  @OS77848
*                                       AND CONVERT ROTUINE    @OS77848
         LR    RPREFIX,R1               ASSUME ELEMENT IS A BUFFER
         USING IEDQPRF,RPREFIX
         TM    PRFTIC,X08               IS ELEMENT A BUFFER
         BO    BCBFR                      BRANCH YES
*
         L     RPREFIX,LCBERBCH-LCBERB-1(R1) ELEMENT IS ERB WITH
*                                       A RECALLED HEADER
         LA    RPREFIX,AVTEZERO(RPREFIX)  CLEAR HI ORDER BYTE  @OS77484
         LTR   RPREFIX,RPREFIX          IS THERE A BUFFER      @OS77848
         BNZ   BCBFR                    YES, CONTINUE          @OS77848
         LR    RLCB,R1                  NO, LOGICAL READ ERROR @OS77848
         SH    RLCB,ERBLCB              BACK UP TO LCB ADDRESS @OS77848
         USING IEDQLCB,RLCB
         L     RSCB,LCBSCBA-1           SCB ADDRESS            @OS77848
         USING IEDQSCB,RSCB
         STH   R2,SCBMRFSD              CLEAR MULTIPLE ROUTE   @OS77848
         LR    R10,R2                   INDICATE END OF LIST   @OS77848
         B     PERMERR                  SET UP FOR CLEAN UP    @OS77848
BCBFR    EQU   *
         L     RLCB,PRFLCB-1            LCB ADDRESS
         L     RSCB,LCBSCBA-1           SCB ADDRESS
         LH    R9,SCBDLPTR              DISTRIBUTION LIST COUNTER
         LTR   R10,R9                   IS THIS FIRST TIME - FROM MH
         BZ    BCFIR                      BRANCH YES
*
         L     R1,LCBLINK-1             SAVED TLIST ENTRY ADDRESS
         USING IEDQTRM,R1
         CH    R10,TLISTCNT             HAS LAST ENTRY BEEN PROCESSED
         LA    R10,1(R9)                ADD ONE TO COUNT
         BNH   BCONT                      BRANCH NO
*
         LR    R10,R2                   INDICATE LIST SERVICED
         L     R1,SCBBKFCT-1            ADDRESS OF MULTIPLE ROUTE
*                                       ROUTINE IF ACTIVE
         CH    R2,SCBMRFSD              WAS MULTIPLE ROUTE REQUESTED
         BNE   BCMLTR                   BRANCH YES
*
PERMERR  EQU   *                                               @OS77848
         TM    SCBSTATE,SCBPRER         IS OPTION FIELD FIRST   SA61724
         BZ    NORESET                  NO                      SA61724
         LH    R1,LCBTTCIN              TNT OFFSET              SA61724
         N     R1,AVTCLRHI              SOURCE                  SA61724
         BZ    NORESET                  NO                      SA61724
         BAL   R14,BCFNDTT              GET TERMINAL TABLE ENTRY ADDR
         SR    R8,R8                    CLEAR REGISTER          SA61724
         IC    R8,TRMOPT(R8)            OFFSET                  SA61724
         LH    R9,TRMOPTBL              GET ENTRY OFFSET       @YA05454
         N     R9,AVTCLRHI              CLEAR HI HALF          @YA05454
         A     R9,AVTOPTPT              ADD OPT TBL ADDR       @YA05454
         AR    R9,R8                    ADDR OF OPTION FIELD    SA61724
         MVC   AVTEZERO(X03,R9),AVTFZERO   CLEAR OPTION FIELD   SA61724
         NI    SCBSTATE,AVTEFF-SCBPRER  TURN OFF FLAG           SA61724
NORESET  EQU   *                                                SA61724
         LA    R1,AVTBFRTB              ADDR OF BUFFER RETURN QCB
         ST    R2,LCBLINK-1             END OF LIST
         LR    R2,RLCB                  SET TO POST LCB
         MVI   LCBPRI,PRILNCL           SET PRIORITY
BCMLTR   EQU   *
         ST    R1,LCBRCQCB              RETURN IS BUFFER RETURN
*                                       OR MULTIPE ROUTE SUBTASK
         B     BCRET                    RETURN TO MULTIPLE ROUT OR
*                                       BUFFER DISPOSITION
*
BCONT    EQU   *
         AR    R9,R9                    DOUBLE
         LH    R1,TLISTEN-2(R9)         PICK UP FIRST/NEXT ENTRY
         STH   R1,PRFDEST               SET DESTINATION KEY
         L     R5,LCBDCBPT              ADDRESS OF DCB/LGB       S22024
         TM    26(R5),X'80'             IS THIS AN LGB           S22024
         BZ    BCDCB                    BRANCH ON NO             S22024
         STH   R1,LCBTTBIN              SET DESTINATION IN PLCB  S22024
BCDCB    EQU   *                                                 S22024
         BAL   R14,BCFNDTT              GET TERMINAL TABLE ENTRY
*                                       ADDRESS
         L     R1,TRMDESTQ-1            DESTINATION QCB ADDRESS
         TM    QCBDSFLG-IEDQQCB(R1),QCBDISK+QCBCORE            @OY16445
*                                       ANY QUEUES SPECIFIED   @OY16445
         BNZ   HASQUE                   YES, CONTINUE          @OY16445
         SPACE
*        AT THIS TIME WE HAVE DETERMINED THAT THE DISTRIBUTION @OY16445
*        LIST CONTAINS AN ENTRY THAT HAS NO QUEUES. WE WILL    @OY16445
*        IGNORE THIS DESTINATION AND MOVE ON TO CHECK THE      @OY16445
*        NEXT. IF WE DO NOT, IEDQHM WILL ABEND TRYING TO QUEUE @OY16445
*        THE MESSAGE.                                          @OY16445
         SPACE
         STH   R10,SCBDLPTR             UPDATE THE COUNTER     @OY16445
         B     BCBFR                    MOVE ON TO NEXT        @OY16445
HASQUE   EQU   *                                               @OY16445
         IC    R0,SCBDESTQ-1            SAVE HIGH ORDER BYTE
         ST    R1,SCBDESTQ-1            SET DESTINATION
         STC   R0,SCBDESTQ-1            RESTORE BYTE
BCRET    EQU   *
         LTR   RPREFIX,RPREFIX          LOGICAL READ ERROR     @OS77848
         BNZ   BFRPOST                  NO, CONTINUE           @OS77848
         LR    R1,RLCB                  SET UP TO              @OS77848
         B     POSTLCB                    POST LCB             @OS77848
BFRPOST  EQU   *                                               @OS77848
         ST    R1,PRFQCBA-1             SET QCB ADDRESS
         ST    R2,PRFLINK-1             ZERO LINK FIELD OR ADD LCB
*                                       TO POST LIST
         MVI   PRFPRI,PRIDESTQ          SET PRIORITY FOR POST
         LR    R1,RPREFIX               BUFFER ADDRESS TO PARAMETER
*                                       REGISTER
POSTLCB  EQU   *                                               @OS77848
         STH   R10,SCBDLPTR             STORE UPDATED COUNT
         BAL   R14,DSPCHAIN             EXIT TO DISPATCHER     @Y17XAMX
*                                       BUFFER TO DESTINATION
BCFIR    EQU   *
         LH    R10,PRFDEST              SET TO SAVE DLIST POINTER
         LA    R9,1                     SET TO PICK UP FIRST
         LR    R1,R10                   TO PARAMETER REGISTER
         LA    R14,BCONT                GET TERMINAL TABLE ENTRY
*                                       ADDRESS FOR DLIST AND RETURN
*                                       TO MAIN LINE
         TM    PRFSTAT1,PRFCNCLN        WAS CANCEL MESSAGE EXECUTED
         BZ    BCFNDTT                  BRANCH NO - CONTINUE
*
         SR    R10,R10                  SET TO ABORT DISTRIBUTION LIST
*
BCFNDTT  EQU   *
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF WORD
         BR    R15                        LINK TO TNT CODE
         EJECT
X08      EQU   8                        EIGHT                    S22024
X03      EQU   3                        DISPLACEMENT            SA61724
ERBLCB   DC    AL2(LCBERB-IEDQLCB)      OFFSET OF THE ERB      @OS77848
         EJECT
         TAVTD
         TLCBD
         TQCBD                                                 @OY16445
         TSCBD
         TTRMD
         TPRIOR
         TDISPD
         TPRFD
         END
