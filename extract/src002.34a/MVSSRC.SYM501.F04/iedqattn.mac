ATTN     TITLE 'IEDQATTN - TCAM IOS ATTENTION ROUTINE'
IEDQATTN CSECT
*C000000-999999                                                  Y02027
*                                                                     *
***********************************************************************
*                                                                     *
*NAME --                                                         Y02027
*   MODULE         IEDQATTN                                      Y02027
*   COMMON         TCAM IOS ATTENTION ROUTINE                    Y02027
*   LOAD           IEDQATTN                                      Y02027
*   ALIASES        NONE                                          Y02027
*   CSECT          IEDQATTN
*                                                                     *
*FUNCTION -- TO FIELD ATTENTION INTERRUPTS FOR TCAM DEVICES AND TO    *
*   PASS CONTROL TO A SERVICING MODULE IF A LOCAL DEVICE IS IN USE    *
*                                                                     *
*ENTRY POINT --
*   (IEDQATTN) FROM THE I/O SUPERVISOR WHEN AN ATTENTION         Y02027
*   INTERRUPT OCCURS ON A DEVICE ALLOCATED TO TCAM.              Y02027
*                                                                     *
*OPERATION -- (CHANGES FOR MVM)                                  Y02027
*   (1) RESCHEDULE ATTENTION ROUTINE INTO TCAM'S MEMORY,WHEN     Y02027
*   NECESSARY.                                                   Y02027
*                                                                     *
*INPUT --                                                        Y02027
*   REGISTER 1 CONTAINS THE ADDRESS OF THE IOSB                  Y02027
*   REGISTER 13 CONTAINS THE ADDRESS OF A 16-WORD SAVE AREA      Y02027
*   REGISTER 14 CONTAINS THE RETURN ADDRESS IN IOS               Y02027
*   REGISTER 15 CONTAINS THE ENTRY POINT ADDRESS OF IEDQATTN     Y02027
*   REGISTERS 6-10 MAY BE DESTROYED                              Y02027
*   REGISTERS 2-5,13 MUST NOT BE DESTROYED                       Y02027
*                                                                     *
*OUTPUT --                                                       Y02027
*   WHEN RESCHEDULING THE ATTENTION ROUTINE INTO TCAM'S MEMORY   Y02027
*   THE IOSB (IOSBASID) WILL CONTIAN ADDRESS SPACE ID.           Y02027
*                                                                     *
*EXTERNAL ROUTINES --                                            Y02027
*   (IGG019R5) TO SERVICE THE ATTENTION INTERRUPT FOR TCAM.      Y02027
*                                                                     *
*EXITS-NORMAL --                                                      *
*   (1) RETURN TO THE I/O SUPERVISOR VIA THE ADDRESS IN REGISTER Y02027
*   14 TO INDICAGE ATTENTION PROCESSING COMPLETE.                Y02027
*   (2) RETURN TO THE I/O SUPERVISOR VIA THE ADDRESS IN REGISTER Y02027
*   14 PLUS 4 TO RESCHEDULE THE ATTENTION RTN FROM THE MASTER    Y02027
*   SCHEDULER'S EMEORY INTO THE TCAM MEMORY.                     Y02027
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*                                                                Y02027
*MAPPING MACROS USED -                                           Y02027
*   IECDIOSB                                                     Y02027
*   TAVTD                                                        Y02027
*   TTCXD                                                        Y02027
*                                                                Y02027
*ATTRIBUTES -- SUPERVISOR MODE,SUPERVISOR KEY,ENABLED,SERIALLY   Y02027
*   REUSEABLE,REFRESHABLE,HOLDS LOCAL LOCK,LINK-EDITED INTO      Y02027
*   SYS1.NUCLEUS BY SGIED300.                                    Y02027
*
*DATA SETS -- NONE                                               Y02027
*
*MESSAGES -- NONE                                                Y02027
*
*EXECUTABLE MACROS -- NONE                                       Y02027
*
*RETURN CODES -- NONE                                            Y02027
*
*ABEND CODES -- NONE                                             Y02027
*
*
*ENQUEUE RESOURCES -- NONE                                       Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET. *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*              REGISTERS                                              *
***********************************************************************
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RCVT     EQU   6                        ADDR OF COMM VECTOR TBL  Y02027
RTCX     EQU   7                        ADDR OF TCAM'S CVT       Y02027
*                                       EXTENSION                Y02027
RRETURN  EQU   14                       RETURN ADDRESS IN IOS    Y02027
RBASE    EQU   15                       BASE ADDRESS             Y02027
***********************************************************************
*              MISCELLANEOUS EQUATES                                  *
***********************************************************************
TWO      EQU   2                        EQUATE OF 2              Y02027
FOUR     EQU   4                        EQUATE OF 4              Y02027
EIGHT    EQU   8                        EQUATE OF 8              Y02027
ALL      EQU   15                       MASK FOR ICM 4 BYTES     Y02027
         EJECT
         USING *,RBASE                                           Y02027
         USING IOSB,RIOSB                                        Y02027
         USING IEDQTCX,RTCX                                      Y02027
         USING CVT,RCVT                                          Y02027
IEDQATTN IEDHJN ATTSTART                                         Y02027
         L     RCVT,CVTPTR              PICKUP ADDR OF CVT       Y02027
         L     RTCX,CVTAQAVT            PICKUP ADDR OF TCAM'S    Y02027
*                                       CVT EXTENSION            Y02027
         CLC   TCXASID(TWO),IOSASID     WAS ATTN RTN SCHEDULED   Y02027
*                                       INTO TCAM'S MEMORY       Y02027
         MVC   IOSASID(TWO),TCXASID     UPDATE IOSB WITH TCAM'S  Y02027
*                                       ADDRESS SPACE ID         Y02027
         BNE   FOUR(RRETURN)            NO, RETURN TO IOS'S POST Y02027
*                                       STATUS ROUTINE TO RESCH- Y02027
*                                       EDULE ATTENTION ROUTINE  Y02027
*                                       INTO TCAM'S MEMORY       Y02027
         ICM   RBASE,ALL,TCXATTEN       ADDRESS OF ATTENTION     Y02027
*                                       HANDLER IF LOADED        Y02027
         BZR   RRETURN                  RETURN IF NOT LOADED TO  Y02027
*                                       STATUS ROUTINE           Y02027
         BR    RBASE                    YES, SCHEULE RECEIVE     Y02027
*                                       OPERATION FOR LINE       Y02027
         EJECT
         CVT   DSECT=YES
         EJECT                                                   Y02027
         IECDIOSB
         EJECT
         TTCXD
         EJECT
         PRINT NOGEN
         TAVTD
         END
