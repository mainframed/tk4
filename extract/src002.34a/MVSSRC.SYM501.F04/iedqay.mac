AY01     TITLE '''IEDQAY'' -- SCREEN ROUTINE'
IEDQAY   CSECT
         SPACE 3
*CHANGE ACTIVITY = AS FOLLOWS:
*************** MICROFICHE FLAGS **************************** SUPT CODE
*A255000,301000                                                  S22025
*C468000,474000                                                  S22025
*D301000-305000                                                  S22025
*A336000                                                         A49230
*C302000-303000                                                  A49230
*D342000                                                         A49230
*A262000,396000,446000                                           A42385
*C303000                                                         A42385
*A612000                                                         A50185
*C303000,642000                                                  A50185
*D615000                                                        SA57324
*A642100,654100                                                 SA57324
*A301000                                                        SA59983
*A636000                                                        SA61048
*A301000                                                        SA66920
*D180700                                                       @OY14057
*A886000                                                       @OY14057
*A309100,306120,307060,809000                                  @Y17XAMG
*C306120,306180,306240,307040                                  @Y17XAMG
*A306080,                                                      @Y17XAMO
*C306100,310480                                                @Y17XAMO
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE: IEDQAY - SCREEN ROUTINE                                       *
*                                                                     *
*MODULE NAME = IEDQAY                                                 *
*                                                                     *
*DESCRIPTIVE NAME = SCREEN ROUTINE                                    *
*                                                                     *
*COPYRIGHT = 'NONE'                                                   *
*                                                                     *
*STATUS -- CHANGE LEVEL 10                                       X03039
*                                                                     *
*FUNCTION -- INITIALIZES THE SYSTEM FOR PERFORMANCE OF A SCREEN       *
*   COMMAND MODIFICATION ON THIS DESTINATION.                         *
*                                                                     *
*   ON ENTRY,  THE ROUTINE ACCESSES THE UCB AND DETERMINES IF THE     *
*   DESTINATION IS AN IBM 2260 LOCAL OR REMOTE.  IF LOCAL,  THE       *
*   ROUTINE USES AN INDEX BYTE PASSED IN THE PARAMETER LIST TO        *
*   SELECT THE FUNCTION BYTE REQUESTED FROM AN INTERNALLY-DEFINED     *
*   TABLE.  THIS FUNCTION BYTE IS SET IN THE KEY FIELD OF THE         *
*   BUFFER AND THE SCREEN REQUEST BIT IN THE LCB (LCBSCRNN) IS SET.   *
*   RETURN IS MADE TO THE CALLER WITH THE NEW FUNCTION BYTE IN        *
*   REGISTER 15.                                                      *
*                                                                     *
*   IF REMOTE,  THE ROUTINE VERIFIES THAT THE DESTINATION IS A        *
*   SCREEN DEVICE.  IF IT IS NOT,  RETURN IS MADE TO THE CALLER       *
*   WITH ZEROES IN REGISTER 15.  IF IT IS,  A LINK IS MADE TO THE     *
*   TNT CODE (AVTRNMPT) TO GET THE ADDRESS OF THE DESTINATION'S       *
*   TERMINAL TABLE ENTRY.  THE DEVICE DEPENDENT AREA IS FOUND IN      *
*   THE ENTRY.  THE CURRENT SETTING OF THE FUNCTION BYTE IS SET IN    *
*   REGISTER 15.  IF A CHANGE OF FUNCTION IS REQUESTED,  THE NEW      *
*   FUNCTION BYTE IS SELECTED AND REPLACES THE CURRENT FUNCTION       *
*   BYTE IN THE TERMINAL TABLE ENTRY.  RETURN IS MADE TO THE          *
*   CALLER.                                                           *
*                                                                     *
*ENTRY POINT --                                                       *
*       'IEDQAY01' TO INITIALIZE FOR MODIFYING A SCREEN COMMAND.      *
*   CALLING SEQUENCE FROM USER INTERFACE IS:                          *
*                                                                     *
*                                                                     *
*        L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE        *
*        IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR     *
*        L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDRESS           *
*        BR    R12                      EXIT TO ROUTINE               *
*                                                                     *
*INPUT --                                                             *
*   REGISTER 1 - THE ADDRESS OF A MACRO-GENERATED PARAMETER LIST.     *
*   PARAMETER LIST FORMAT IS:                                         *
*                                                                     *
*        *****************    REQUEST CODES:                          *
*        * INDEX *REQUEST*      X'00' - WRITE AT DISPLAY CURSOR       *
*        *  TO   * CODE  *      X'01' - WRITE AT LINE ADDRESS         *
*        *   AY  *       *      X'02' - WRITE ERASE                   *
*        *****************                                            *
*                                                                     *
*        AY INDEX BYTE: X'01' ON - COMMAND CHANGE REQUESTED           *
*                       X'01' OFF - RETURN CURRENT SETTING ONLY       *
*                                                                     *
*   REGISTER 4 - ADDRESS OF THE LCB                                   *
*                                                                     *
*   REGISTER 6 - ADDRESS OF CURRENT BUFFER                            *
*                                                                     *
*   REGISTER 9 - ADDRESS OF THE AVT                                   *
*                                                                     *
*   REGISTER 12 - ENTRY POINT ADDRESS AND BASE FOR THIS ROUTINE.      *
*                                                                     *
*OUTPUT --                                                            *
*   REGISTER 15 - CURRENT FUNCTION BYTE IF THE FUNCTION IS PER-       *
*   FORMED.  ZEROES IF THE FUNCTION IS NOT PERFORMED.                 *
*                                                                     *
*   PREFIX KEY FIELD (PRFKEY) - FOR LOCAL TERMINALS WHEN A COMMAND    *
*   CHANGE IS REQUESTED,  CONTAINS THE FUNCTION BYTE FOR THE          *
*   COMMAND REQUESTED.                                                *
*                                                                     *
*   TERMINAL TABLE ENTRY - FOR REMOTE TERMINALS WHEN A COMMAND        *
*   CHANGE IS REQUESTED,  THE FUNCTION BYTE IS CHANGED AS SPECIFIED   *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*   AVTRNMPT - TERMINAL NAME TABLE CODE                               *
*                                                                     *
*   AVT - ADDRESS VECTOR TABLE                                        *
*                                                                     *
*   BUFFER CURRENTLY BEING PROCESSED                                  *
*                                                                     *
*   LCB - LINE CONTROL BLOCK                                          *
*                                                                     *
*   DCB - DATA CONTROL BLOCK                                          *
*                                                                     *
*   DEB - DATA EVENT BLOCK                                            *
*                                                                     *
*   UCB - UNIT CONTROL BLOCK                                          *
*                                                                     *
*   TERMINAL TABLE ENTRY FOR THE DESTINATION                          *
*                                                                     *
*EXITS,  NORMAL --                                                    *
*   THE REQUESTED COMMAND CHANGE IS INITIALIZED.  REGISTER 15 CON-    *
*   TAINS THE CURRENT SETTING OF THE COMMAND.                         *
*                                                                     *
*   NO COMMAND CHANGE IS REQUESTED.  REGISTER 15 CONTAINS THE         *
*   CURRENT SETTING OF THE COMMAND.                                   *
*                                                                     *
*EXITS,  ERROR --                                                     *
*   THE DESTINATION TERMINAL IS NOT A SCREEN DEVICE.  REGISTER 15     *
*   CONTAINS ZERO.                                                    *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   A TABLE OF COMMAND CODES FOR LOCAL SCREEN DEVICE                  *
*   A TABLE OF COMMAND CODES FOR REMOTE SCREEN DEVICE                 *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE,  REFRESHABLE,  ENABLED,             *
*   RESIDENT,  PROBLEM PROGRAM MODE.                                  *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN        *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
*                                                                     *
***********************************************************************
         EJECT
********* REGISTER EQUATES *********
         SPACE
R0       EQU   0                        WORK REGISTER
         SPACE
R1       EQU   1                        PARAM REG & WORK REG
         SPACE
RWORK2   EQU   2                        WORK REGISTER
         SPACE
RLCB4    EQU   4                        LCB ADDRESS
         SPACE
RWORK5   EQU   5                        WORK REGISTER
         SPACE
RPREFIX  EQU   6                        BUFFER ADDRESS
         SPACE
RUCB7    EQU   7                        UCB ADDRESS
RWORK7   EQU   7                                                 S99228
         SPACE
RPARM8   EQU   8                        PARAMETER LIST ADDRESS
         SPACE
RAVT9    EQU   9                        ADDRESS OF AVT
         SPACE
RBASE    EQU   12                       BASE REGISTER
         SPACE
R13      EQU   13                       AVT BASE
R14      EQU   14                       RETURN REG & WORK REG
R15      EQU   15                       LINK REG & WORK REG
         SPACE
********* OTHER EQUATES *********
         SPACE
LEAUCMD  EQU   X'0F'                    LOCAL EAU COMMAND        S99228
LWRECMD  EQU   X'05'                    LOCAL WRE COMMAND        S99228
ZERO     EQU   0                                                 S99228
EIGHT    EQU   8                                                 S99228
TWELVE   EQU   12                                                S99228
D16      EQU   16                                                S99228
D20      EQU   20                                                S99228
X20      EQU   X'20'                                             S99228
TWO      EQU   2                                                 S99228
HDRSZE   EQU   X'1E'                    SIZE OF TCAM HEADER      S99228
PREFSZE  EQU   X'0C'                    SIZE OF PREFIX           S99228
EQUAL    EQU   8                                                 S99228
ESC      EQU   X'27'                    ESCAPE CHARACTER         S99228
ENDCHAR  EQU   X'FF'                    END OF TBLE CHAR         S99228
ADAPMASK EQU   X'F0'                    LOW-ORDER NIBBLE CLEAR MASK
IBMAIII  EQU   X'80'                    IBM ADAPTER TYPE III
MODLMASK EQU   X'0F'                    HI-ORDER NIBBLE CLEAR MASK
MODEL1   EQU   X'01'                    MODEL 1 FLAG
         SPACE
GRAPHICS EQU   X'10'                    DEVICE CLASS, UCB
         SPACE
PARMSET  EQU   1                        REQUEST CODE IN PARM LIST
DOITFLAG EQU   X'01'                    FUNCTION REQUEST FLAG
         SPACE
ONE      EQU   1                        INCREMENT VALUE OF ONE
THREE    EQU   3                        LENGTH OF ADDRESSING CHARS
FOUR     EQU   4                        OFFSET USED ON RETURN    S22025
SEVEN    EQU   7                        SHIFT FACTOR OF SEVEN BITS
LINEADD  EQU   X'01'                    WRITE LINE ADDRESS MASK IN
*                                         PARAMETER LIST
CMDERASE EQU   X'07'                    COMMAND CODE FOR ERASE
CMDLNADD EQU   X'05'                    COMMAND CODE LINE ADDRESS
NRESTORE EQU   X'10'                    WRITE W/O KEYBOARD RSTR  A42385
         EJECT
         USING IEDQAVTD,RAVT9
         USING IEDQLCB,RLCB4
         USING IEDQPRF,RPREFIX
         USING IEDQAY01,RBASE
         SPACE
IEDQAY01 EQU   *
IEDQAY   IEDHJN SCREEN                  MODULE ID                S22025
         SR    RPARM8,RPARM8            CLEAR WORK REGISTER     SA66920
         CH    RPARM8,PRFSIZE           ZERO LENGTH BUFFER ?    SA66920
         BE    EXIT                     BRANCH YES- LEAVE       SA66920
         TM    PRFSTAT1,PRFNHDRN        HEADER BUFFER           SA59983
         BO    EXIT                     BRANCH IF NO            SA59983
         LR    RPARM8,R1                SAVE PARAMETER LIST ADDRESS
         LH    R1,LCBTTCIN              CURRENT TERMINAL INDEX   S99228
         N     R1,AVTCLRHI              CLEAR HI-ORDER 2 BYTES   S99228
         L     R15,AVTRNMPT             ADDRESS TERMINAL TABLE   S99228
         BALR  R14,R15                  GET TERM ENTRY ADDRESS   S99228
         LA    R14,TRMPRFSZ             GET PREFIX SIZE        @Y17XAMO
         SR    R1,R14                   BACK UP TO PREFIX      @Y17XAMO
         USING IEDNTRM,R1               SET UP ADDRESSABILITY  @Y17XAMO
         IEDDCT REG=RWORK7,FLD=DCTWKARA FETCH DCT ENTRY        @Y17XAMG
         USING IEDDCT,RWORK7                                   @Y17XAMG
         SR    R15,R15                  INIT RETURN CODE REG     S99228
         TM    DCTBYTE1,DCT3270         3270 DEVICE            @Y17XAMG
         BO    CKBADOP                  YES-BRANCH               S99228
         USING SCRNPARM,RPARM8                                   S99228
         CLI   SCRNREQ,EAU              EAU REQUEST FOR 2260     S99228
         BNE   CONT2260                 NO-CONTINUE 2260 PROCESS S99228
BADREQ   DS    0H                                                S99228
         LA    R15,FOUR                 EAU INVALID EXCEPT FOR   S99228
*                                       3270                     S99228
         B     EXIT                     EXIT                     S99228
CKBADOP  DS    0H                       CHECK FOR VALID 3270     S99228
*                                       OPERATION                S99228
         LA    R1,SCRNREQ               ADDRESS OF SCREEN        S99228
*                                       REQUEST CODE             S99228
         SR    RWORK2,RWORK2            INDICATE SEARCH FOR      S99228
*                                       REQUEST CODE             S99228
         BAL   R14,TBLSRCH              GO SEARCH FOR REQUEST    S99228
*                                       CODE IN COMMAND TABLE    S99228
         LTR   RWORK2,RWORK2            REQUEST CODE FOUND       S99228
         BZ    BADONE                   NO-BRANCH                S99228
         SR    R0,R0                                             S99228
         IC    R0,TWO(RWORK2)           GET LOCAL COMMAND CODE   S99228
*                                       FROM TABLE- TO BE USED   S99228
*                                       LATER                    S99228
*    CHECK FOR NO OPERANDS AT ALL- THIS IS INVALID FOR 3270      S99228
         TM    SCRNNDX,REQFLAG          ANY REQUEST SPECIFIED    S99228
         BO    GO3270                   YES-BRANCH               S99228
         TM    SCRNFLGS,RETRIEVE        RETRIEVE=YES SPECIFIED   S99228
         BO    GO3270                   YES-BRANCH               S99228
BADONE   DS    0H                                                S99228
         LA    R15,FOUR                 INVALID COMMAND FOR 3270 S99228
         B     EXIT                     EXIT                     S99228
*****************************************************************S99228
*                                                                S99228
*                                                                S99228
*        3270 SCREEN PROCESSING                                  S99228
*                                                                S99228
*                                                                S99228
*****************************************************************S99228
GO3270   DS    0H                       PROCESS 3270 SCREEN      S99228
*                                       REQUESTS                 S99228
         TM    DCTBYTE2,DCTLOCAL        LOCAL 3270             @Y17XAMG
         BO    LOC3270                  YES-BRANCH               S99228
         DROP  RWORK7                                          @Y17XAMG
*****************************************************************S99228
*                                                                S99228
*        REMOTE 3270 PROCESSING                                  S99228
*                                                                S99228
*****************************************************************S99228
         TM    SCRNFLGS,RETRIEVE        RETRIEVE=YES SPECIFIED   S99228
         BNO   ESCPSRCH                 NO-BRANCH                S99228
*****************************************************************S99228
*        RETRIEVE=YES                                            S99228
*****************************************************************S99228
         TM    SCRNNDX,REQFLAG          REQUEST SPECIFIED        S99228
         BO    ESCPSRCH                 YES-BRANCH               S99228
         LA    R15,FOUR                 CANNOT HAVE ONLY         S99228
*                                       RETRIEVE=YES             S99228
         B     EXIT                     EXIT                     S99228
*****************************************************************S99228
*        RETRIEVE=NO (OR NOT SPECIFIED)                          S99228
*****************************************************************S99228
ESCPSRCH DS    0H                                                S99228
         BAL   R14,ESCFIND              FIND ESCAPE CHAR         S99228
         LTR   R1,R1                    ESCAPE CHAR FOUND        S99228
         BZ    NOESC                    NO-BRANCH                S99228
         LA    RWORK7,ONE(R1)           ADDRESS OF WHERE COMMAND S99228
*                                       CODE WILL BE PUT(RIGHT   S99228
*                                       AFTER ESCAPE CHAR IN     S99228
*                                       BUFFER UNIT)             S99228
         LA    R1,SCRNREQ               ADDRESS OF REQUEST CODE  S99228
         SR    RWORK2,RWORK2            INDICATE SEARCH FOR REQ  S99228
         BAL   R14,TBLSRCH              SEARCH COMMAND TABLE     S99228
         MVC   ZERO(ONE,RWORK7),ONE(RWORK2) PUT COMMAND CODE     S99228
*                                       INTO BUFFER UNIT         S99228
         B     EXIT                     EXIT                     S99228
NOESC    DS    0H                                                S99228
         LA    R15,EIGHT                ESCAPE CHAR NOT FOUND    S99228
         B     EXIT                     EXIT                     S99228
*****************************************************************S99228
*                                                                S99228
*        LOCAL 3270                                              S99228
*                                                                S99228
*****************************************************************S99228
LOC3270  DS    0H                                                S99228
         TM    SCRNFLGS,RETRIEVE        RETRIEVE=YES             S99228
         BO    GETCMD                   YES-BRANCH               S99228
*****************************************************************S99228
*        RETRIEVE=NO (OR NOT SPECIFIED)                          S99228
*****************************************************************S99228
         LR    R1,R0                    SCREEN REQUEST(CONVERTED S99228
*                                       TO LOCAL COMMAND CODE)   S99228
         BAL   R14,CCWSETUP             GO SET UP CCW            S99228
         B     EXIT                     EXIT                     S99228
*****************************************************************S99228
*        RETRIEVE=YES                                            S99228
*****************************************************************S99228
GETCMD   DS    0H                                                S99228
         BAL   R14,ESCFIND              LOCATE ESCAPE CHAR       S99228
         LTR   R1,R1                    ESCAPE CHAR FOUND        S99228
         BZ    BADCMD                   NO-BRANCH                S99228
         LA    R1,ONE(R1)               1ST CHAR AFTER ESCAPE IS S99228
*                                       REMOTE COMMAND CODE      S99228
         LA    RWORK2,ONE               INDICATE SEARCH FOR      S99228
*                                       REMOTE COMMAND CODE      S99228
         BAL   R14,TBLSRCH              SEARCH COMMAND TABLE     S99228
         LTR   RWORK2,RWORK2            COMMAND CODE FOUND       S99228
         BZ    BADCMD                   NO-BRANCH                S99228
*                                                                S99228
*        VALID COMMAND IN DATA STREAM                            S99228
*                                                                S99228
         SR    R1,R1                                             S99228
         IC    R1,ONE(RWORK2)           GET LOCAL COMMAND CODE   S99228
*                                       FROM TABLE               S99228
         BAL   R14,CCWSETUP             GO SET UP CHANNEL PROGRAMS99228
         B     EXIT                     EXIT                     S99228
*                                                                S99228
*        INVALID COMMAND OR COMMAND NOT FOUND IN DATA STREAM     S99228
*                                                                S99228
BADCMD   DS    0H                                                S99228
         TM    SCRNNDX,REQFLAG          ANY REQUESTS SPECIFIED   S99228
         BO    DEFAULT                  YES-BRANCH               S99228
         LA    R15,EIGHT                INVALID DATA STREAM      S99228
*                                       CONTENT                  S99228
         B     EXIT                     EXIT                     S99228
DEFAULT  DS    0H                                                S99228
         LR    R1,R0                    USE REQUEST SPECIFIED    S99228
*                                       ON SCREEN MACRO          S99228
*                                       (CONVERTED TO LOCAL      S99228
*                                       COMMAND CODE)            S99228
         BAL   R14,CCWSETUP             SET UP CHANNEL PROGRAM   S99228
         LA    R15,TWELVE               DEFAULT USED RETURN CODE S99228
         B     EXIT                     EXIT                     S99228
*****************************************************************S99228
*                                                                S99228
*        ESCFIND- SEARCHES FOR AN ESCAPE CHARACTER IN THE BUFFER S99228
*        INPUT- NONE REQUIRED                                    S99228
*        OUTPUT- R1 CONTAINS ADDRESS OF ESCAPE CHARACTER         S99228
*                OR 0 IF ESCAPE CHAR NOT FOUND                   S99228
*                                                                S99228
*****************************************************************S99228
ESCFIND  DS    0H                                                S99228
         SR    RWORK2,RWORK2                                     S99228
         IC    RWORK2,LCBISZE           NUMBER OF IDLES          S99228
         LA    R1,PREFSZE+HDRSZE(RWORK2,RPREFIX)   ADDRESS OF    S99228
*                                       BEGINNING OF DATA IN     S99228
*                                       1ST UNIT OF BUFFER       S99228
         CLI   LCBFLAG1,0               PLCB?                  @Y17XAMG
         BNE   NOTPLCB                  BRANCH IF NOT          @Y17XAMG
         TM    LCBSTAT5,LCBLUNIT        LU?                    @Y17XAMG
         BZ    NOTPLCB                  BRANCH IF NOT          @Y17XAMG
*                                       ITS AN LU, THEREFORE   @Y17XAMG
*                                       NO EXCAPE CHARACTER    @Y17XAMG
         BCTR  R1,0                     SET UP TO USER FIRST   @Y17XAMG
*                                       DATA CHAR AS COMMAND   @Y17XAMG
         BR    R14                      RETURN TO CALLER       @Y17XAMG
NOTPLCB  EQU   *                                               @Y17XAMG
         LH    RWORK5,PRFSIZE           SIZE OF DATA             S99228
         LA    RWORK2,HDRSZE(RWORK5)    ADD TCAM HDR SIZE TO     S99228
*                                       SIZE OF DATA IN ENTIRE   S99228
*                                       BUFFER(INCLUDES IDLES)   S99228
         CH    RWORK2,AVTKEYLE          DOES DATA FILL 1ST UNIT  S99228
         BL    NOTFULL                  NO-BRANCH                S99228
         LH    RWORK2,AVTKEYLE          KEY LENGTH               S99228
         LA    RWORK5,PREFSZE(RPREFIX,RWORK2)   ADDRESS OF END   S99228
*                                       OF 1ST UNIT              S99228
         B     SUBONE                   BEGIN SEARCH FOR ESCAPE  S99228
*                                       CHARACTER                S99228
NOTFULL  DS    0H                                                S99228
         LA    RWORK5,PREFSZE+HDRSZE(RPREFIX,RWORK5)  ADDRESS    S99228
*                                       OF END OF DATA IN        S99228
*                                       1ST UNIT                 S99228
SUBONE   DS    0H                                                S99228
         BCTR  RWORK5,R0                SUBTRACT ONE BECAUSE     S99228
*                                       ESCAPE IS INVALID AS     S99228
*                                       LAST CHAR IN DATA        S99228
ESCLOOP  DS    0H                                                S99228
         CR    R1,RWORK5                ARE WE AT END OF THE     S99228
*                                       DATA(OR END OF THE 1ST   S99228
*                                       UNIT) MINIS ONE          S99228
         BL    ESCCOMP                  YES-BRANCH               S99228
         SR    R1,R1                    ESCAPE NOT FOUND OR NOT  S99228
*                                       ENOUGH ROOM FOR NEW      S99228
*                                       OPERATION CODE           S99228
         BR    R14                      RETURN                   S99228
ESCCOMP  DS    0H                                                S99228
         CLI   ZERO(R1),ESC             ESCAPE CHAR              S99228
         BCR   EQUAL,R14                YES-RETURN               S99228
         LA    R1,ONE(R1)               INCREMENT SCANNER        S99228
         B     ESCLOOP                  CONTINUE LOOP            S99228
*****************************************************************S99228
*                                                                S99228
*        CCWSETUP- THIS SUBROUTINE SETS UP OR BUILDS CHANNEL     S99228
*          PROGRAMS. THE FOLLOWING REPRESENTS WHAT IS DONE FOR   S99228
*          EACH COMMAND CODE(OP CODE).                           S99228
*                                                                S99228
*            EAU-                                                X03039
*             NON-VTAM - EAU CHANNEL PROGRAM IS BUILT IN THE LCB X03039
*                                                                X03039
*             VTAM - THE EAU OP CODE IS SAVED IN PRFOPCDE AND    X03039
*             LCBSCRNN IS TURNED ON. IEDIAF WILL USE THIS TO     X03039
*             BUILD THE LOGICAL CHANNEL PROGRAM.                 X03039
*                                                                X03039
*            WRE-THE WRE OP CODE IS SAVED IN PRFOPCDE AND        X03039
*            LCBSCRNN IS TURNED ON. EITHER IEDQGA (NON-VTAM) OR  X03039
*            IEDIAF (VTAM) USES THIS TO BUILD THE WRE CHANNEL    X03039
*            PROGRAM.                                            X03039
*                                                                S99228
*            WDC-NOTHING IS DONE FOR THE WDC COMMAND BECAUSE THE S99228
*            CHANNEL PROGRAM WILL DEFAULT TO WDC IN IEDQGA       X03039
*            (NON-VTAM) OR IEDIAF (VTAM).                        X03039
*                                                                S99228
*        INPUT- R1 CONTAINS THE LOCAL COMMAND CODE               S99228
*                                                                S99228
*****************************************************************S99228
CCWSETUP DS    0H                                                S99228
         LA    RWORK5,LEAUCMD                                    S99228
         CR    RWORK5,R1                EAU REQUEST              S99228
         BNE   CKLOCWRE                 NO-BRANCH                S99228
         CLI   LCBFLAG1,AVTEZERO        VTAM PLCB                X03039
         BE    SETCODE                  BR YES                   X03039
*                                                                X03039
         STC   R1,LCBCPA+D16            PUT EAU COMMAND INTO     S99228
*                                       INITIAL CHANNEL PRGRAM   S99228
*                                       (WILL OVERLAY SELECT CMD)S99228
         LA    R0,X20                                            S99228
         STC   R0,LCBCPA+D20            DO NOT CHAIN EAU(3270    S99228
*                                       HARDWARE RESTRICTION)    S99228
         MVI   LCBTPCD+TWO,TPEAU        PUT TP OP CODE IN LCB    S99228
         B     ENDCCWSU                 ALL DONE                 S99228
CKLOCWRE DS    0H                                                S99228
         LA    RWORK5,LWRECMD                                    S99228
         CR    RWORK5,R1                WRE REQUEST              S99228
         BNE   ENDCCWSU                 NO-BRANCH-  DEFAULT TO   S99228
*                                       WDC(ALREADY BUILT IN     S99228
*                                       CHANNEL PROGRAM)         S99228
SETCODE  EQU   *                                                 X03039
         STC   R1,PRFOPCDE              PUT OP CODE IN BUFFER  @Y17XAMO
         OI    LCBCHAIN,LCBSCRNN        TURN ON BIT TO TELL      S99228
*                                       IEDQGD THAT THE OP CODE  S99228
*                                       IS IN THE BUFFER         S99228
ENDCCWSU DS    0H                                                S99228
         BR    R14                      RETURN TO CALLER         S99228
*****************************************************************S99228
*                                                                S99228
*        TBLSRCH- THIS SUBROUTINE SEARCHES FOR A SPECIFIED CHAR  S99228
*              IN THE COMMAND TABLE                              S99228
*                                                                S99228
*        CMDTBL - COMMAND TABLE                                  S99228
*              EACH ENTRY CONSIST OF 3 BYTES                     S99228
*                 1ST - SCREEN PARM LIST REQUEST CODE            S99228
*                 2ND - REMOTE COMMAND CODE                      S99228
*                 3RD - LOCAL COMMAND CODE                       S99228
*        A X'FF'  IN THE BYTE FOLLOWING THE LAST ENTRY INDICATES S99228
*        THE END OF THE TABLE                                    S99228
*                                                                S99228
*        INPUT                                                   S99228
*              R1- ADDRESS OF SEARCH ARGUMENT                    S99228
*              RWORK2- 0  SEARCH ARG IS A SCREEN PARMLIST        S99228
*                         REQUEST CODE                           S99228
*                      1  SEARCH ARG IS A REMOTE COMMAND CODE    S99228
*                                                                S99228
*        OUTPUT                                                  S99228
*              R1- ADDRESS OF SEARCH ARGUMENT CHARACTER          S99228
*              RWORK2- ADDRESS OF FOUND CHARACTER OR             S99228
*                      ZERO- IF NOT FOUND                        S99228
*                                                                S99228
*****************************************************************S99228
TBLSRCH  DS    0H                                                S99228
         LA    RWORK5,CMDTBL            ADDRESS OF COMMAND TBL   S99228
LOOP     DS    0H                                                S99228
         CLI   ZERO(RWORK5),ENDCHAR     END OF TABLE             S99228
         BE    NOTFOUND                 YES- BRANCH              S99228
         LTR   RWORK2,RWORK2            SEARCH FOR REQUEST CODE? S99228
         BZ    SRCHREQ                  YES-BRANCH               S99228
*        SEARCH FOR REMOTE COMMAND CODE                          S99228
         CLC   ZERO(ONE,R1),ONE(RWORK5) FOUND                    S99228
         BNE   CONTINUE                 NO- BRANCH               S99228
         LA    RWORK2,ONE(RWORK5)       ADDRESS OF FOUND CHAR    S99228
         BR    R14                      RETURN                   S99228
*        SEARCH FOR REQUEST CODE                                 S99228
SRCHREQ  DS    0H                                                S99228
         CLC   ZERO(ONE,R1),ZERO(RWORK5)  FOUND                  S99228
         BNE   CONTINUE                 NO- BRANCH               S99228
         LR    RWORK2,RWORK5            ADDRESS OF FOUND CHAR    S99228
         BR    R14                      RETURN                   S99228
CONTINUE DS    0H                                                S99228
         LA    RWORK5,THREE(RWORK5)     INCREMENT TO NEXT        S99228
         B     LOOP                     CONTINUE SEARCH          S99228
NOTFOUND DS    0H                                                S99228
         SR    RWORK2,RWORK2            INDICATE CHAR NOT FOUND  S99228
         BR    R14                      RETURN                   S99228
*****************************************************************S99228
*                                                                S99228
*                                                                S99228
*        SCREEN 2260 PROCESSING                                  S99228
*                                                                S99228
*                                                                S99228
*****************************************************************S99228
CONT2260 DS    0H                                                S99228
         SPACE
         L     RUCB7,LCBDCBPT           GET DCB ADDRESS FROM LCB
         USING IHADCB,RUCB7
         L     RUCB7,DCBDEBAD           GET DEB ADDRESS FROM DCB
         USING DEBNMSUB,RUCB7
         SR    R14,R14                  CLEAR INDEX REG          A49230
         SPACE 1                                                 A49230
TRYNEXT  EQU   *                                                 A49230
         L     R15,DEBUCBAD(R14)        GET UCB ADDRESS          A49230
         LA    R15,0(R15)               CLEAR HI-ORDER BYTE      A49230
         LTR   R15,R15                  DD DUMMY                 A49230
         BNZ   GOTUCB                   BRANCH IF NOT            A49230
         LA    R14,4(R14)               ELSE INCREMENT INDEX     A49230
         B     TRYNEXT                  AND CONTINUE SEARCH      A49230
         SPACE 1                                                 A49230
GOTUCB   EQU   *                                                 A49230
         LR    RUCB7,R15                COPY VALID UCB ADDRESS   A49230
         USING IEDQUCB,RUCB7
         SPACE
         CLI   UCBTBYT3,GRAPHICS        IS IT GRAPHICS (2260 LOCAL)
         BNE   REMOTE                   NO, REMOTE, GO PROCESS
         SPACE
LOCAL    EQU   *
         LA    R15,CCWWRITE             ASSUME NO CHANGE
         TM    AVTEZERO(RPARM8),DOITFLAG IS CHANGE REQUESTED
         BNO   EXIT                     NO, RETURN NOW
         IC    RWORK2,PARMSET(RPARM8)   SAVE OPERATION BYTE      A42385
         NI    PARMSET(RPARM8),AVTEFF-NRESTORE CLEAR MOD 22 FLAG A42385
         SPACE
         CLI   PARMSET(RPARM8),LINEADD  SELECT REQUESTED WRITE
         BL    SETTIC                   IT IS WDC, BRANCH
         BE    WLA                      IT IS WRITE AT LINE
*                                         ADDRESS, BRANCH
         SPACE
*        ***** NEITHER, IT IS WRITE ERASE *****
         SPACE
         STC   R15,PRFOPCDE             MAKE BFR OP CODE A WRITE
         LA    R15,CMDERASE             GET ERASE OP CODE IN REG
         STC   R15,LCBCPA+16            SET LCB OP CODE TO ERASE
         B     SETBIT                   SET BIT & TIC & RETURN
         SPACE
WLA      EQU   *
         LA    R15,CMDLNADD             GET WLA OP CODE IN REG
         STC   R15,PRFOPCDE             SET IT IN BUFFER
         SPACE
SETBIT   EQU   *
         OI    LCBCHAIN,LCBSCRNN        TELL GD OP CODE IN BUFFER
         SPACE
SETTIC   EQU   *
         ST    RPREFIX,LCBCPA+24        SET TIC ADDRESS
         MVI   LCBCPA+24,CCWTIC           AND OP CODE IN LCB
         STC   RWORK2,PARMSET(RPARM8)   RESTORE OPERATION BYTE   A42385
         TM    PARMSET(RPARM8),NRESTORE WRITE W/O KEYBOARD RSTR  A42385
         BZ    EXIT                     BRANCH IF NO             A42385
         OI    PRFOPCDE,NRESTORE        SET NO KEYBOARD RESTORE  A42385
         SPACE
EXIT     EQU   *
         L     RBASE,AVTUI              GET RET INTERFACE ADDR   S22025
         B     FOUR(RBASE)              BRANCH TO RETURN ROUTINE S22025
         EJECT
REMOTE   EQU   *
         SR    R15,R15                  SET ZERO RETURN CODE
         IC    R0,UCBTBYT4              PICK UP UNIT TYPE
         STC   R0,AVTDOUBL              STORE IN AVT
         NI    AVTDOUBL,ADAPMASK        CLEAR LOW-ORDER NIBBLE
         CLI   AVTDOUBL,IBMAIII         IS IT IBM ADAPTER III
         BNE   EXIT                     NO, NOT SCREEN DEVICE,
*                                       EXIT WITH ZERO RETURN CODE
         SPACE
         IC    R0,UCBTBYT1              PICK UP IOS/MODEL BYTE
         STC   R0,AVTDOUBL              STORE IN AVT
         NI    AVTDOUBL,MODLMASK        CLEAR HIGH-ORDER NIBBLE
         CLI   AVTDOUBL,MODEL1          IS IT MODEL 1
         BNE   EXIT                     NO, NOT SCREEN DEVICE,
*                                         EXIT WITH ZERO RETURN CODE
         SPACE
         LH    R1,PRFDEST               GET DESTINATION KEY
         N     R1,AVTCLRHI
         L     R15,AVTRNMPT             GET ADDRESS OF TNT CODE
         BALR  R14,R15                  LINK TO GET TERM ENTRY ADDR
         SPACE
         USING IEDQTRM,R1
         SR    R0,R0
         LR    RWORK2,R0
         IC    R0,TRMDEVFL              PICK UP DEV DEP FLAGS
         TM    TRMSTATE,TRMOPTFN        TEST IF ANY OPTION FIELDS
         BZ    NOOPT                    NO, BRANCH
         SPACE
         IC    RWORK2,TRMOPNO           YES, GET NUMBER OF THEM
         LA    R1,TRMOPT                BUMP FIXED ENTRY LENGTH SA61048
         AR    R1,RWORK2                BUMP PAST OPTION OFFSETS A50185
         B     CKBUFSZ                  SEE IF BUFSIZE SPCEIFIEDSA57324
         SPACE
NOOPT    EQU   *
         LA    R1,TRMOPNO               BUMP TO POSS BUFSIZE    A57324
CKBUFSZ  EQU   *                                                A57324
         SRA   R0,SEVEN                 IS BUFSIZE SPECIFIED
         BZ    NOBFSIZE                 NO, BRANCH
         SPACE
         IC    RWORK2,AVTEZERO(,R1)     YES, GET LENGTH OF FIELD
         LA    R1,ONE(R1,RWORK2)        BUMP PAST BUFSIZE FIELD
         SPACE
NOBFSIZE EQU   *
         LR    R15,RWORK2               (CLEAR FOR INSERT)
         IC    R15,THREE(,R1)           PICK UP CURRENT SETTING
         SPACE
         TM    AVTEZERO(RPARM8),DOITFLAG IS CHANGE REQUESTED
         BZ    EXIT                     NO, RETURN NOW
         SPACE
         IC    RWORK2,PARMSET(,RPARM8)  PICK UP SELECT BYTE
         IC    RWORK2,REMOTSND(RWORK2)  PICK UP FUNCTION BYTE
         STC   RWORK2,THREE(,R1)        CHANGE TERMINAL TABLE ENTRY
         B     EXIT                     RETURN
         SPACE
********* FUNCTION BYTE TABLES *********
         SPACE
LOCALSND DC    X'010507'                LOCAL OP CODES
REMOTSND DC    X'A0B0E0'                REMOTE OP CODES
*                 × × ×
*               WDC × WRE
*                  WLA
*****************************************************************S99228
*                                                                S99228
*        COMMAND TABLE                                           S99228
*              EACH ENTRY CONSIST OF 3 BYTES                     S99228
*                 1ST - SCREEN PARM LIST REQUEST CODE            S99228
*                 2ND - REMOTE COMMAND CODE                      S99228
*                 3RD - LOCAL COMMAND CODE                       S99228
*        A X'FF'  IN THE BYTE FOLLOWING THE LAST ENTRY INDICATES S99228
*        THE END OF THE TABLE                                    S99228
*                                                                S99228
*****************************************************************S99228
CMDTBL   DS    0H                                                S99228
PARMWDC  DC    X'00'                    WDC REQUEST CODE         S99228
REMWDC   DC    X'F1'                    REMOTER WDC COMMAND CODE S99228
LOCWDC   DC    X'01'                    LOCAL WDC COMMAND CODE   S99228
*                                                                S99228
PARMWRE  DC    X'02'                    WRE REQUEST CODE         S99228
REMWRE   DC    X'F5'                    REMOTE WRE COMMAND CODE  S99228
LOCWRE   DC    X'05'                    LOCAL WRE COMMAND CODE   S99228
*                                                                S99228
PARMEAU  DC    X'03'                    EAU REQUEST CODE         S99228
REMEAU   DC    X'6F'                    REMOTE EAU COMMAND CODE  S99228
LOCEAU   DC    X'0F'                    LOCAL EAU COMMAND CODE   S99228
*                                                                S99228
ENDTBL   DC    X'FF'                    END OF TBL INDICATOR     S99228
DCTWKARA DS    CL4                      DCT WORK AREA          @Y17XAMG
         EJECT
********* DSECTS *********
         SPACE
*        SCREEN PARAMETER LIST                                   S99228
SCRNPARM DSECT                                                   S99228
SCRNNDX  DS    XL1                      INDEX BYTE               S99228
REQFLAG  EQU   X'01'                    ON-MEANS REQUEST IS      S99228
*                                       SPECIFIED,OFF - MEANS    S99228
*                                       REQUEST NOT SPECIFIED    S99228
SCRNREQ  DS    XL1                      REQUEST                  S99228
WDC      EQU   X'00'                    WRITE DISPLAY CURSOR     S99228
*                                       OR NO OPERANDS SPECIFIED S99228
WRE      EQU   X'02'                    ERASE/WRITE              S99228
EAU      EQU   X'03'                    ERASE ALL UNPROTECTED    S99228
SCRNFLGS DS    XL1                      FLAG BYTE                S99228
RETRIEVE EQU   X'80'                    ON-RETRIEVE=YES SPECIFIEDS99228
*        END SCREEN PARM LIST                                    S99228
         EJECT
         TAVTD
         EJECT
         TCCWD
         EJECT
         DCBD  DSORG=TQ
         EJECT
         TDCTD
         EJECT
         TDEBD
         EJECT
         TLCBD
         EJECT
         TPRFD
         EJECT
         TSIBD
         EJECT
         TTPD                                                  @OY14057
         EJECT
         TTRMD
         EJECT
IEDQUCB  DSECT
         IEFUCBOB
         SPACE
         END
