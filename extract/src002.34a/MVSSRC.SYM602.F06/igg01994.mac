         TITLE 'IGG01994 - SAM STAGE 3 OPEN EXECUTOR'
IGG01994 CSECT
*MODULE NAME - IGG01994                                          Y02072
*
*DESCRIPTIVE NAME - LOAD ACCESS METHOD ROUTINES                  Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*        VS2 RELEASE 02 DELETIONS                                Y02072
*000600,003000,007200,034100,0343000-034360,066600,070600,       Y02072
*103200-103400,092800,1022006200,74800-75000,75600,76600-79800   Y02072
*                                                                YM1140
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*          RELEASE 21 DELETIONS                                       *
*
*STATUS CHANGE LEVEL 002
*
*FUNCTION/OPERATION - THIS MODULE IS A CONTINUATION OF MODULE
*    IGG01992 AND LOADS END-OF-BLOCK AND CONTROL ROUTINES.
*    THE CHOICE OF ROUTINES IS
*    DETERMINED BY PREVIOUS OPEN EXECUTORS.  THE ADDRESSES OF THE
*    LOADED MODULES ARE PLACED IN THE DCB AND THE ID IN THE
*    SUBROUTINE ID SECTION OF THE DEB.
*
*ENTRY POINTS - THE MODULE IS ENTERED BY MEANS OF AN XCTL FROM
*    IGG01992.
*
*INPUT - SEE DESCRIPTION OF REGISTER CONTENTS.
*
*OUTPUT - SUBROUTINES LOADED, ADDRESSES IN DCB, ID'S IN DEB
*
*EXTERNAL ROUTINES - NONE
*EXITS  - NORMAL / XCTL TO IGG01911                              Y02072
*      - ERROR - NONE
*
*TABLES/WORKAREAS - DESCRIBED BY DSECTS AT END OF MODULE LISTING
*
*ATTRIBUTES - REENTRANT, REUSABLE, RUNS IN DATA MANAGEMENT KEY   Y02072
*             AND SUPERVISOR STATE                               Y02072
*
*MACROS-MAPPING:IECDSECS(MAIN,WTG), DCBD, CVT                    Y02072
*
* REGISTER CONVENTIONS
*
*
RE       EQU   0                        WORK REG5
RF       EQU   1                        WORK REG6
R1       EQU   RF                       PARM REG                 YM1140
RDCB     EQU   2                        DCB
RBASE    EQU   3                        BASE
RCORE    EQU   4                        WORE AREA
RPAR     EQU   5                        TOP OF PARAMETER
RWTG     EQU   6                        TOP OF WTG TABLE
RPARC    EQU   7                        CURRENT PARAMETER
RWTGC    EQU   8                        CURRENT TRANS LOAD
RTIOT    EQU   9                        TIOT
RUCB     EQU   10                       UCB
RDEB     EQU   11                       DEB
RB       EQU   12                       WORK REG1
RC       EQU   13                       WORK REG2
RD       EQU   14                       WORK REG3
RJ       EQU   15                       WORK REG4
T3       EQU   3
T25      EQU   25                       25
PARAR    EQU   RPARC
* MASK'S TO TEST PARAMETER LIST WITH
INP      EQU   X'0F'                    MASK
OUTP     EQU   X'0F'                    MASK
INOUT    EQU   X'03'                    MASK
OUTIN    EQU   X'07'                    MASK
RDBACK   EQU   X'01'                    MASK
INPRDBK  EQU   X'0E'               MASK TO CK INPUT OR RDBACK      UT18
* OFFSET FOR PARAMETER TESTS
FRTYATE  EQU   48                       MASK
ONE      EQU   X'01'
DISPSN   EQU   X'0'
* OFFSETS FOR DEB REFERENCE
BIN      EQU   36
BEXT     EQU   38
* MASK'S USED TO TEST OPTION FIELDS IN DCB
DOUBLE   EQU   X'01'
STOWB    EQU   X'80'
WRTCK    EQU   X'80'
OVRFLO   EQU   X'20'
CONCAT   EQU   X'FF'
MINKEY   EQU   X'01'
PCI      EQU   X'20'
UTFMASK  EQU   X'02'               MASK FOR TOTALING FACILITY      UT18
* MASKS FOR ACCESS METHODS IN SAM
QSAMB    EQU   X'01'                    MASK
BSAMBIT  EQU   X'00'                    MASK
PAM      EQU   X'02'                    MASK
* MASKS FOR MACRO'S TO BE USED
GETB     EQU   X'40'                    MASK
PUTB     EQU   X'40'                    MASK
READB    EQU   X'20'                    MASK
WRITEB   EQU   X'20'                    MASK
POINTB   EQU   X'04'                    MASK
CNTRLB   EQU   X'02'
* MASKS FOR STATUS BITS TO BE TESTED
EXCPN    EQU   X'30'                    MASK
COMPLETE EQU   X'80'
FIRSTIOB EQU   X'01'                    MASK
* MASKS FOR BUFFER TECHNIQUE'S
EXCHANGB EQU   X'10'
SIMPLEB  EQU   X'40'
* MODES  OPERATION FOR QSAM
LOCATEB  EQU   X'08'
MOVEB    EQU   X'10'
MOVEXCHB EQU X'14'
SUBSTUTB EQU   X'04'
* MASKS FOR OPEN OVERRIDE OPTIONS
OVRDMSK  EQU   X'C0'                    INDICATES OPEN OVERRIDE
CTDISP   EQU   X'10'                    DISPLACEMENT OF CT IN TBL
* COMMAN YTES FOR THE 2841
SRCHE    EQU   X'31'
WRTCKD   EQU   X'1D'
RDC      EQU   X'92'
RDKD     EQU   X'8E'
RDD      EQU   X'86'
TIC      EQU   X'08'
* MASKS   RECORD FORMATS
STANDRDF EQU   X'08'
FORMATU  EQU   X'C0'
FORMATF  EQU   X'80'
FORMATV  EQU   X'40'
SPANNED  EQU   X'08'
BLOCKED  EQU   X'10'
FIXEDBLK EQU   X'90'
*   CHAN  CONTROL WORD FLAGS
SKIP     EQU   X'10'
SLI      EQU   X'20'
DATACH   EQU   X'A0'
CC       EQU   X'40'
SLICC    EQU   X'60'
SLICCSK  EQU   X'70'
KEYS     EQU   X'01'
QSAM     EQU   X'01'
ASA      EQU   X'04'
BKREAD   EQU   X'01'
URBITS   EQU   X'40'
TAPEB    EQU   X'80'
IDLEN    EQU   2                        LENGTH OF A MODULE ID    Y02072
         BALR  RBASE,RE                 SET BASE
         USING SLDA,RBASE
         USING IHADCB,RDCB
         USING BLDEB,RDEB
         USING FORCORE,RCORE
SLDA     EQU   *
         B     BEGIN                    BR AROUND CONSTANTS    @Z30TSCF
         DC    C'IGG01994'              MODULE NAME            @Z30TSCF
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'10/24/74'              LAST SHIP DATE         @Z30TSCF
BEGIN    DS    0H
         L     RDCB,0(RPARC)            GET DCB ADDRESS
         LA    RDCB,0(RDCB)             CLEAR HI BYTE
         L     RCORE,4(RWTGC)           GET WORK AREA
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
*
*   THIS SECTION OF CODE UP TO THE LABEL ENDOVR WILL ENSURE THAT
*   THE EOB ROUTINE IGG019CT GETS LOADED FOR ANY OPEN OVERRIDE.
         TM    JFCBMASK+6,OVRDMSK       WAS AN OVERRIDE USED
         BNO   ENDOVR                   BR IF NO                 A32160
         TM    0(RPARC),X'0F'           WAS IT LABEL=(,,,IN)     A32160
         BZ    INCHNG                   BR IF YES                A32160
         MVC   DCBCNTRL+2(1),DCBCNTRL+3 SAVE WRITE EOB DISP
         MVI   DCBCNTRL+3,CTDISP        SET READ EOB ROUTINE FOR
*                                       THE ERROR EOB ROUTINE
         B     ENDOVR                   CONTINUE
INCHNG   MVI   DCBCNTRL+2,CTDISP        SET WRITE EOB ROUTINE FOR
*                                       THE ERROR EOB ROUTINE
ENDOVR   EQU   *
         MVI   22(RWTG),X'CB'          CHANGE DE ATTR.TO ONLY LOADABLE
         L     RTIOT,DEBAPPAD           GET APPENDAGE ADDRESS
         SR    RUCB,RUCB                CLEAR REG
         SR    RF,RF                    CLEAR RER
         IC    RUCB,DEBNMEXT            GET NUMBER OF EXTENTS
         IC    RF,DEBEXSCL              GET SCALING FACTOR
         SLL   RUCB,0(RF)               SHIFT ACCORDING TO EXTENT SIZE
         IC    RF,DEBAMLNG              GET ACCESS METHOD LENGTH
         LA    RUCB,0(RF,RUCB)          ADD TO POINT TO SUBRT ID FLD
         LA    RUCB,32(RUCB,RDEB)       ADD BASIC DEB SZ TO DEB AD
*
*  RUCB NOW POINTS TO SUBROUTINE ID SECTION OF THE DEB,            UT18
*  BUT MUST BE INCREMENTED TO POINT BEYOND THE LAST ENTRY          UT18
*
SLDAA    EQU   *                                                   UT18
         CLI   0(RUCB),X'00'            IS RUCB BEYOND LAST ENTRY  UT18
         BE    SLDAB                    BR IF YES                  UT18
         LA    RUCB,2(RUCB)             INCREMENT TO NEXT ID FIELD UT18
         B     SLDAA                    TEST NEXT FIELD            UT18
*
*
*  RUCB NOW POINTS TO NEXT AVAILABLE SUBROUTINE ID FIELD IN THE    UT18
*  DEB.                                                            UT18
*
*  IF USER TOTALING HAS BEEN SPECIFIED, EOB ROUTINES CONTAINING    UT18
*  LINKAGE TO THE TOTALING SAVE ROUTINES MUST BE LOADED INSTEAD OF UT18
*  THE NORMAL EOB ROUTINES.  THIS IS ACCOMPLISHED BY DOING A       UT18
*  TRANSLATE OF THE DCBCNTRL FIELD TO CONVERT NORMAL EOB DISPLACE- UT18
*  MENTS INTO DISPLACEMENTS FOR THE UTF EOB ROUTINES.              UT18
*                                                                  UT18
SLDAB    EQU   *                                                   UT18
         TM    DCBOPTCD,UTFMASK         TEST FOR TOTALING OPTION   UT18
         BZ    SLDAC                    BR IF NOT SPECIFIED        UT18
         TM    0(RPARC),INPRDBK         IS OPEN FOR INPUT/RDBACK   UT18
         BZ    SLDAC                    IF YES NO TOTALING REQ'RD  UT18
         TR    DCBCNTRL(4),UTFTBL       CHANGE TO UTF DISPLACEMENT UT18
SLDAC    EQU   *                                                   UT18
*                                                                  UT18
*  LOAD NECESSARY EOB ROUTINES                                     UT18
*                                                                  UT18
*
         BAL   RF,EOBROUT               GET EOB RT ID
         BAL   RC,SLDROUT               LOAD ROUTINE
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,SOP9            NEXT EXECUTOR          @Z30TSCF
         TM    DCBCIND2,QSAMB           QSAM BEING USED
         BO    SLDEOB9                 YES,BRANCH
SLDEOB2  EQU   *
         MVC   DCBEOBRZ(T3),T25(RWTG)    INSERT EOB ADDR.
SLDEOB20 EQU   *
         MVC   DCBEOBWZ(T3),T25(RWTG)   LOAD EOB WRITE ADDRESS.
SLDEOB4  EQU   *
         BAL   RF,EOBROUT               GET POSSIBLE WRT ROUT (IN/OUT)
         LTR   RC,RC                    DCB FOR IN/OUT
         BC    8,SLDEOB6                NO BRANCH
         BAL   RC,SLDROUT               LOAD WRT EOB RT.
         MVC   DCBEOBWZ(T3),T25(RWTG)    LOAD EOB WRITE ADDRESS
SLDEOB6  EQU   *
         BAL   RF,EOBROUT               IS NOTE/POINT USED
         LTR   RC,RC                    TEST FOR ITS ID
         BC    8,SLDEOBC                NO BRANCH
SLDEOB8  EQU   *
         BAL   RC,SLDROUT               LOAD NOTE/POINT ROUTINE
         MVC   DCBCNTRZ(T3),T25(RWTG)
         B     SLDEOBE                  BRANCH
SLDEOB9  EQU   *
         MVC   DCBEOBZ(T3),T25(RWTG)
SLDEOBC  EQU   *
         XC    DCBCNTRL(4),DCBCNTRL     CLEAR PARA LIST
         OI    DCBCNTZZ,QSAM            GET VECTOR TO ONE
SLDEOBE  EQU   *                        END.
         MVC   DCBEROPT(RF),DXCCW6      QSAM ERROR OPTION
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURR WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCR CURRENT DCB ENTRY PRTR
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   RWTGC,RBASE              RETURN
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BC    7,RELOOP                 NO,CHECK NEXT ENTRY
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      REINITIALIZE WTG LIST PTR
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN                IF NOT TRANSFER CONTROL
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHEK                    TO ZERO CHECK
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
*
*
SLDROUT  EQU   *
         USING WTGMODNM,RWTG                                     Y02072
         MVC   WTGMODID,0(RB)           ID TO MODULE NAME        Y02072
         DROP  RWTG                                              Y02072
         L     R1,CVTPTR                LOAD CVT ADDRESS         YM1140
         USING CVT,R1                   CVT ADDRESSABILITY       YM1140
         L     R1,CVTLINK               GET LPALIB DCB ADDRESS   YM1140
         DROP  R1                       DROP CVT BASE            YM1140
*
         LOAD  EPLOC=(RWTG),DCB=(1)                              YM1140
         SR    RD,RD
         IC    RD,DEBNMSUB              GET NO OF SUBROUTINES LOADED
         LA    RD,1(RD)                 ADD ONE TO TOTAL
         STC   RD,DEBNMSUB              STORE NEW TOTAL OF SUBRT LOADED
         ST    RE,24(RWTG)              TEMP STORE OF ROUTINE ADDR,
SLDROUT1 MVC   0(2,RUCB),0(RB)          STORE ROUTINES ID IN DEB
         LA    RUCB,2(RUCB)             UPDATE SUBRT PNTR
         BR    RC                      RETURN
EOBROUT  EQU   *
         SR    RC,RC
         LA    RE,EOBTABL               GET TABLE OF NAMES
         LA    RB,IDLEN                 LENGTH OF A MODULE ID    Y02072
         IC    RC,DCBCNTZZ              DEVICE DEP.
         L     RJ,DCBCNTRL              SHIFT IDS RIGHT BY ONE BYTE
         SRL   RJ,8
         ST    RJ,DCBCNTRL
         MR    RB,RB                    CALCULATE WHERE ID IS
         LR    RB,RC                    PREPARE TO LOAD  MODULE
         AR    RB,RE                    POINT TO CORRECT ID
         BR    RF                       RETURN                     UT18
         EJECT
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES
OPIDCNST DC    C'0S'                     ID
AMIDCNST DC    C'92'                    ID OF THIS MODULE          UT18
*
*******************************************************************UT18
*
*  THE FOLLOWING TABLE IS USED WITH A TRANSLATE INSTRUCTION TO     UT18
*  CONVERT DISPLACEMENTS FOR THE EOB TABLE INTO THOSE FOR THE      UT18
*  ENTRIES FOR THE CORRESPONDING UTF ROUTINES.                     UT18
*
UTFTBL   DC    X'00'                    NO CHANGE                  UT18
         DC    X'0D'                    IGG019CV BECOMES IGG019TV  UT18
         DC    X'0E'                    IGG019CW BECOMES IGG019TW  UT18
         DC    X'03'                    NO CHANGE                  UT18
         DC    X'04'                    NO CHANGE                  UT18
         DC    X'05'                    NO CHANGE                  UT18
         DC    X'06'                    NO CHANGE                  UT18
         DC    X'07'                    NO CHANGE                  UT18
         DC    X'0C'                    IGG019CC BECOMES IGG019TC  UT18
         DC    X'0F'                    IGG019C2 BECOMES IGG019T2  UT18
         DC    X'0A'                    NO CHANGE                  UT18
         DC    X'0B'                    NO CHANGE
         DC    X'0C'                    NO CHANGE
         DC    X'0D'                    NO CHANGE
         DC    X'0E'                    NO CHANGE
         DC    X'0F'                    NO CHANGE
         DC    X'10'                    NO CHANGE
*
*  NOTE, THERE MUST BE AN ENTRY IN THIS TRANSLATE TABLE FOR EACH
*        ENTRY IN THE EOB TABLE.
*                                                                  UT18
*******************************************************************UT18
*                                                                  UT18
SOP9     DC    C'11',VL3(IGG01911)     IGGO1911 OR IECOS90X    @Z30TSCF
EOBTABL  EQU   *
         DC    C'  '                    BLANKS
EPD2     DC    C'CV'                    ID
EGP2     DC    C'CW'                    ID
DDA2     DC    C'CX'                    ID
DDB2     DC    C'CY'                    ID
NPD2     DC    C'BK'                    NOTE-POINT  DA  WITH PCI
NPT2     DC    C'BL'                    NOTEPOINT  TAPE  WITH PCI
NPD3     DC    C'BC'                    NOTE/POINT DISK
EGP3     DC    C'CC'                    EOB RT. DISK-TP-RDR INP/OUTP
EOB2     DC    C'C2'                    EOB USED BY TRACK OVERFLOW
DUMY     DC    C'AV'                    ID
UTF1     DC    C'TD'                    USER TOTALING EOB ROUTINE  UT18
UTF2     DC    C'TC'                    USER TOTALING EOB ROUTINE  UT18
UTF3     DC    C'TV'                    USER TOTALING EOB ROUTINE  UT18
UTF4     DC    C'TW'                    USER TOTALING EOB ROUTINE  UT18
UTF5     DC    C'T2'                    USER TOTALING EOB ROUTINE  UT18
OVRD     DC    C'CT'                    OPEN OVERRIDE ERROR RTN
         SPACE
PATCH    DC    0H'0',XL50'00'           PATCH AREA             @Z30TSCF
END      EQU   *                        END OF MODULE            Y02072
         IECDSECS  MAIN,WTG,PREFX,EXPAND=YES                   @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MOD ID              Y02072
CVT      DSECT
         CVT
BLDEB    DSECT
DEB      DS    0F                       ALIGN
DEBNMSUB DS    0CL1                     NO. OF SUB.
DEBTCBAD DS    CL4                      TCB ADDR.
DEBAMLNG DS    0CL1                     ACCESS METHOD LEN.
DEBDEBAD DS    CL4                      DEB ADDR.
DEBOGLGS DS    0CL1                     OPTION
DEBIRBAD DS    CL4                      IRB ADDRESS
DEBOPATB DS    0CL1                     OPTION
DEBSYSPG DS    CL4                      SYS ABEND
DEBNMEXT DS    0CL1                     NO. OF EXTENT
DEBUSRPG DS    CL4                      USER PROG.
DEBPRIOR DS    0CL1                     PRIORITY
DEBECBAD DS    CL4                      ECB ADDRESS.
DEBPROTG DS    0CL1                     PROTECTION KEY
DEBDEBID DS    0CL1                     DEB ID
DEBDCBAD DS    CL4                      DCB ADDRESS
DEBEXSCL DS    0CL1                     EXCEPTION
DEBAPPAD DS    CL4                      APPENDAGE ADDRESS
DEBDVMOD DS    0CL1                     DEVICE
DEBUCBAD DS    CL4                      UCB ADDRESS
DEBBINUM DS    CL2                      BIN NUMBER
DEBSTRCC DS    CL2                      START CYC.
DEBSTRHH DS    CL2                      START TRACK
DEBENDCC DS    CL2                      END CYCLINDER
DEBENDHH DS    CL2                      END TRACK
DEBNMTRK DS    CL2                      NO. OF TRACKS
DEBIRBAZ EQU   DEBIRBAD+1
*
           DCBD  DSORG=PS
DCBEOBZ  EQU   DCBEOB+1
DCBCNTZZ EQU   DCBCNTRL+3
DCBEOBWZ EQU   DCBEOBW+1
DCBCNTRZ EQU   DCBCNTRL+1
DCBEOBRZ EQU   DCBEOBR+1
DCBSYZ   EQU   DCBSYNAD+2
DCBSYX   EQU   DCBSYNAD+3
DCBGETZ  EQU   DCBGET+1
DCBMACRZ EQU   DCBMACRF+1
DCBPERZ  EQU   DCBPERR+1
DCBCHECZ EQU   DCBCHECK+1
           END
