         TITLE 'IGG0191Z OPEN EXECUTOR  D/A UPDATE RECORD READY CHANNEL*
                PROGRAMS'
IGG0191Z CSECT
*MODULE NAME - IGG0191Z                                          Y02072
*
*DESCRIPTIVE NAME - BUILD IOBS                                   Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          VS 2 RELEASE 2 DELETIONS                              Y02072
*000947,010800,034000,034400,039800-040800,053060-053100,        Y02072
*058600,060420-060440,070950,109500,112500-112540,122000-129170  Y02072
*109000,116800,118000,120800,15400,83600-83800,84200-84400       Y02072
*                                                                YM4640
*C24200,70880,81800                                              YM5362
*                                                                YM7595
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*          RELEASE 20 DELETIONS
*                                                                S20201
*          RELEASE 21 DELETIONS
*0335120500-121600                                               A37507
*0335112040-112200                                               A40538
*0335029129,088060,088860,098060                                 M1760
*0335019449-019480,019720,019920,070100-070500,070800,080400,    A31983
*0335080600,087600-087800                                        A31983
*0335088120,088920,098120                                        M1779
*                                                               SA53268
*                                                                XM6306
* STATUS LEVEL = 002
*
*    THIS MODULE SPLIT INTO IGG01923 FOR RELEASE 21
*
*FUNCTION/OPERATION - INITIALIZE AND LINK IOB'S
*        BEGINS CONSTRUCTION OF CHANNEL PROGRAMS FOR RECORD READY
*        DEVICES WITH OPTIONS OF WRITE CHECK AND TRACK OVERFLOW
*        IF SPECIFIED
*        CHANNEL PROGRAM FORMAT--
*              - WRITE SECTION
*              - WRITE CHECK SECTION
*              - READ SECTION
*
*ENTRY POINTS- ENTERED FROM IGG0191P BY USE OF THE XCTL MACRO.
*
*INPUT- SEE DESCRIPTION OF REGISTERS, DCB(USERS).
*
*OUTPUT & IOB(USER)- SEE DESCRIPTION OF REGISTERS, DCB(USERS).
*
*EXTERNAL ROUTINES- NONE.
*
*EXITS-NORMAL-XCTL TO IGG01923 TO CONTINUE BUILDING CHAN PGMS
*
*EXITS-ERROR- NONE.
*
*TABLES/WORKAREAS- WHERE TO GO TABLE (WTG)
*
*      BYTE  0-7  NAME
*      BYTE  8-10 RELATIVE DISK ADDRESS(TTR) OF FIRST RECORD
*      BYTE 11    CONCATENATION NUMBER
*      BYTE 12    ZERO
*      BYTE 13    ALIAS INDICATOR AND USER DATA FIELD DESCRIPTOR.
*                        ALIAS INDICATOR 1 BIT
*                        NUMBER OF TTRS IN USERS DATA FIELD 2 BITS
*      BYTE 14-16 TTR OF FIRST TEXT RECORD
*      BYTE 17    ZERO
*      BYTE 18-19 TTR OF NOTE LIST OR SCATTER LIST
*      BYTE 20    TRANSLATION TABLE
*      BYTE 21    ZERO OR NUMBER OF ENTRIES IN THE NOTE LIST
*      BYTE 22-23 ATTRIBUTES
*      BYTE 24-26 TOTAL CONTIGUOUS MAIN STORAGE REQUIRED FOR MODULE
*      BYTE 27-28 LENGTH OF FIRST TEXT RECORD
*      BYTE 29    LENGTH OF WTG TABLE (IN DOUBLE WORDS)
*      BYTE 30-31 PATH THROUGH LOADS OF ROUTINES
*      BYTE 32-36 IDTTR OF EXECUTOR FOR FIRST DCB
*      BYTE 37-39 WORKAREA ADDRESS FOR FIRST DCB
*      BYTE 40-   TABLE OF IDTTR'S
*      BYTE 32+N(8) IDTTR OF EXECUTOR FOR N TH DCB   (5 BYTES)
*                   WORKAREA ADDRESS FOR N TH DCB    (3 BYTES)
*                   IDTTR OF LAST ROUTINE LOAD       (5 BYTES)
*                   NOT USED                         (3 BYTES)
*
*      SEE DEVICETB DSECT
*
*MACROS - ACTION : MODESET, XCTL, XCTLTABL                       Y02072
*
*MACROS - MAPPING : IEZDEB, IHACCW, IECDSECS(MAIN, IOB, WTG),    YM5362
*                   DCBD, IGGBCB                                 YM5362
*
*ATTRIBUTES- REENTRANT, REUSABLE, RUNS IN DATA MANAGEMENT KEY    Y02072
*            UNLESS OTHERWISE SPECIFIED, SUPER STATE             Y02072
*
*NOTE - SECTOR VALUES PASSED BY IGG0191P IN OPEN WORK AREA
*     - DCBREAD AND DCBCHECK FIELDS OF DCB USED TO PASS PARAMETERS
*       TO MODULE IGG01923
*     - IOB CSW FIELD USED TO PASS BUFFER ADDRESSES TO 1923      YM7595
*
***********************************************************************
*
*  REGISTER CONVENTIONS USED THROUGH OUT ALL OPEN PASSES
*
***********************************************************************
*
RDCB     EQU   2         DCB REGISTER
RBASE    EQU   3         BASE REGISTER
RCORE    EQU   4         WORK AREA ADDRESS
RPAR     EQU   5         TOP OF PARAMETER LIST
RWTG     EQU   6         TOP OF WTG TABLE
RPARC    EQU   7         CURRENT PARAMETER
RWTGC    EQU   8         CURRENT TRANS LOAD
RBUFADR  EQU   9         ADDR OF BUFFER                          YM7595
RUCB     EQU   10        COUNTER IN IOB GENERATION
RDEB     EQU   11        DEB ADDRESS
RB       EQU   12        WORK REG1  **
RWK2     EQU   RB                       WORK REGISTER            Y02072
RC       EQU   13        WORK REG2  **  USED IN IOB GENERATION
RWK3     EQU   RC                       WORK REGISTER            Y02072
RD       EQU   14        WORK REG3  **
RBRNCH   EQU   15                       SUBROUTINE BR REGISTER   YM5362
RWK1     EQU   RBRNCH     WORK REGISTER                          YM7595
RWK5     EQU   0         WORK REG5                               Y02072
RF       EQU   1         WORK REG6
RWK4     EQU   RF                       WORK REGISTER            Y02072
*
* MASKS FOR ACCESS METHODS IN SAM
QSAMB    EQU   X'01'
* DCB OFFSET CONSTANTS
WCPO     EQU   48
WCWCPL   EQU  8                         WRITE CP LEN WITH WRTCK  A31983
WCTOWCPL EQU  9                         WRITE CP LEN WITH WRTCK  A31983
*                                          AND TRK OVFLOW        A31983
THREECCW EQU  24                        LEN OF 3 CCW'S           A31983
WCPL     EQU   4                        NORMAL WRITE CP LENGTH
*        THE READ CCW OFFSET IN THE DCBOFFSR FIELD IS THE OFFSET
*        OF THE READ DATA CCW FROM THE BEGINNING OF THE SAM IOB
*        PREFIX
OFFSR    EQU   X'80'                    NORMAL RD CCW OFFSET     XM6306
OFFSW    EQU   X'48'                    WRITE CCW OFFSET
WCOSRQ   EQU  X'A0'                     READ CCW OFFSET FOR WRT  A31983
*                                          CHK WITH QSAM         A31983
TOWCOSRQ EQU  X'A8'                     READ CCW OFFSET FOR WRT  A31983
*                                          CHK + TRK OV FOR QSAM A31983
WCWTOCCW EQU   46                       OFFSET FOR COUNT FIELD
WCCCW    EQU   38                       WRT CHK - NO TRK OV
EIGHT    EQU   8                        EIGHT
FIVE     EQU   5                        FIVE
SIX      EQU   6                        CONSTANT OF SIX
FOUR     EQU   X'4'                     FOUR
SETSECTR EQU   X'23'                    COMMAND CODE
ZERO     EQU   0                        ZERO
TWO      EQU   2
THREE    EQU   3                        THREE
SEVEN    EQU   7                        SEVEN
D7       EQU   SEVEN                    CONST OF SEVEN
TWELVE   EQU   12                       CONSTANT OF 12
SIXTY5   EQU   65                       CONSTANT OF 65
C20      EQU   20                       CONSTANT OF 20
C23      EQU   23                       CONSTANT OF 23
* MASKS USED TO TEST OPTION FIELDS IN DCB
WRTCK    EQU   X'80'
TRKOVFLB EQU   X'20'                    TRACK OVERFLOW OPTION
* COMMAND BYTES FOR THE 2841 CONTROL UNIT
SEEK     EQU   X'1B'                    SEEK HEAD OP CODE        YM5362
NOP      EQU   X'03'                    NO-OPERATION OP CODE     YM5362
SRCHE      EQU   X'31'
RDD        EQU   X'86'
RDDX     EQU   X'06'
TIC        EQU   X'08'
WRTD       EQU   X'05'
WRTKD    EQU   X'0D'
RDKD     EQU   X'8E'
*   CHANNEL CONTROL WORD FLAGS
SLI        EQU   X'20'
CC         EQU   X'40'
SLICCSK  EQU   X'70'
* MASKS FOR RECORD FORMATS
FORMATF  EQU   X'40'                    NON FORMAT F MASK
* OFFSET FOR PARAMETER TESTS
ONE      EQU   1
NOT      EQU   X'FF'
* MASKS FOR STATUS BITS TO BE TESTED
SEEKCYHD EQU   X'18'                    IF BOTH ON IN FILE MASK, YM5362
*                                         INHIBIT ALL SEEKS      YM5362
READONLY EQU   X'20'                    IOB READ ONLY INDICATOR
UNRELFLG EQU   X'02'                   IOB UNRELATED FLAG        A37507
NULL     EQU   0                        ZERO DISPLACEMENT        S19015
IDLEN    EQU   5                        LENGTH OF IDTTR          S19015
*
***********************************************************************
*
         BALR  RBASE,0                  REG 3 IS BASE
         USING SOPB,RBASE
         USING IHADCB,RDCB              REG 2 IS DCB
         USING FORCORE,RCORE            REG 4 IS WORK AREA ADDRESS
         USING DEBBASIC,RDEB                                     YM5362
         SPACE 3
SOPB     EQU   *
         B     BEGIN                    BRANCH AROUND MODULE ID  YM4640
         DC    C'IGG0191Z'              MODULE NAME              YM4640
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'10/22/74'              LAST SHIP DATE         @Z30TSCF
BEGIN    DS    0H                       END OF MODULE ID         YM4640
         L     RDCB,0(RPARC)            GET DCB ADDRESS
         LA    RDCB,0(RDCB)             CLEAR HI BYTE
         L     RDEB,DCBDEBAD            GET DEB ADDRESS          YM5362
         L     RCORE,4(RWTGC)           GET WORK AREA ADDRESS
         L     RB,DCBIOBA               GET ADDR OF GETMAIN AREA
         LR    RD,RB                    GET ADDR OF FIRST IOB
         MODESET  KEYADDR=DXUKEY,WORKREG=1  GET INTO USER KEY    Y02072
         USING IOBQSAMN,RB                                       Y02072
         L     RWK3,IOBNECB             GET SECTOR ADDR          Y02072
         DROP  RB                                                Y02072
         L     RUCB,ZERO(RB)            GET IOB COUNT
         MODESET EXTKEY=DATAMGT         BACK TO DATA MANAG KEY   Y02072
         STC   RUCB,DCBREAD+ONE         SAVE NO OF BUFFERS FOR IGG01923
         ST    RWK3,SECTORAD            SAVE SECTOR ADDR         Y02072
         MODESET  KEYADDR=DXUKEY,WORKREG=1  GET INTO USER KEY    Y02072
         XC    ZERO(TWELVE,RB),ZERO(RB)  ZERO OUT FIRST 12 BYTES
*                                       WHICH WERE USED TO PASS
*                                       INFORMATION FROM IGG0191P
         L     RWK5,SECTORAD            INIT REG TO FIRST SECTOR Y02072
*                                         ADDRESS                Y02072
         CH    RUCB,ONEIOB             ONE IOB?                  A37507
         BNE   NTONEIOB                NO - BRANCH               A37507
         USING IOBQSAMN,RB             ADDRESSABILITY FOR DSECT  Y02072
         OI    IOBFLAG1,IOBUNREL       YES - SET UNRELATED FLAG  Y02072
         DROP  RB                                                Y02072
         B     NTONEIOB                 BYPASS NEXT IOB FUNCTION
NEXTIOB  EQU   *
         SR    RF,RF                    ZERO REG
         IC    RF,DCBIOBL               IOB LENGTH IN DOUBLE WORDS
         SLL   RF,THREE                 IOB LENGTH IN BYTES
         LA    RB,ZERO(RF,RD)           ADDR OF NEXT IOB
*
***********************************************************************
*
*    REGISTER USEAGE FOR CHANNEL PROGRAM CONSTRUCTION
*       RB - RD/WRT SEQUENCE POINTER
*       RC - FDA POINTER
*       RD - CURRENT IOB POINTER
*       RJ - LINK REG TO INTERNAL SUBROUTINE
*       RF - PARAREG FOR INTERNAL SUBROUTINE
*       RE - INTERNAL POINTER IN READ SEQUENCE
*
***********************************************************************
*
NTONEIOB EQU   *
         AH    RWK5,NXTSECSV            INCRE TO NEXT SECTOR     Y02072
         ST    RB,0(RD)                 LINK NEW IOB TO LAST ONE BUILT
         OI    0(RD),READONLY           IOB FOR READ ONLY         22484
         LR    RD,RB                    UPDATE CURRENT IOB POINTER
         OI    8(RD),CC                 INDICATE COMMAND CHAINING 21282
         LA    RB,4(RB)                 POINT TO ECB SLOT
         OI    0(RB),X'7F'              SET ECB COMPLETE WITHOUT ERROR
         ST    RB,12(RD)                STORE ECB ADDR IN IOB
         L     RWK4,DXUDCBAD            GET USERS DCB ADDRESS    Y02072
         USING IOBQSAMN,RD                                       Y02072
         ST    RWK4,IOBDCBPT            DCB ADDRESS TO IOB       Y02072
         DROP  RD                                                Y02072
*
***********************************************************************
*
*     CONSTRUCTION OF CHANNEL PROGRAM FOLLOWS
*
***********************************************************************
*
         LA    RB,48(RD)                UPDATE TO WRITE SEQUENCE
         USING CCW,RWK2                                          Y02072
         ST    RWK5,CCWADDRA            SECT ADDR TO CCW         Y02072
         DROP  RWK2                                              Y02072
         MVI   ZERO(RB),SETSECTR        MOVE IN COMMAND CODE
         OI    FOUR(RB),CC              TURN ON COMMAND CHAIN
         MVI   D7(RB),ONE               STORE LENGTH
         LA    RB,EIGHT(RB)             BUMP COUNTER BY EIGHT
*                                         SET SECTOR CCW GENERATED
         LA    RC,43(RD)                POINT TO CCHHR FIELD IN IOB
         ST    RC,0(RB)                 STORE ADDR IN SRCH CCW
         MVI   0(RB),SRCHE              STORE CMD BYTE
         MVI   7(RB),X'05'              STORE COUNT OF 5 IN CCW
         OI    4(RB),CC                 SET CMD CHAIN FLAG ON
         ST    RB,8(RB)                 STORE IN TIC CCW
         MVI   8(RB),TIC                STORE TIC CMD BYTE
*
***********************************************************************
*     SRCH AND TIC CCWS HAVE BEEN GENERATED AT THIS POINT
*     CONSTRUCTION OF WRITE CHANNEL PROGRAMS FOLLOW
***********************************************************************
*
         MODESET EXTKEY=DATAMGT         BACK TO DATA MANAG KEY   Y02072
         MVI   DCBWCPO,WCPO             WRITE CHANNEL PROGRAM OFFSET
         MVI   DCBWCPL,WCPL             STORE NORMAL WRT CP LEN.
         MVI   DCBOFFSW,OFFSW           WRITE CCW OFFSET
         MODESET  KEYADDR=DXUKEY,WORKREG=1  GET INTO USER KEY    Y02072
         TM    DCBRECFM,FORMATF         FORMAT F USED
         BNO   SOPB50                   BRANCH YES
         OI    20(RB),SLI               SET SILI FLAG ON
SOPB50   EQU   *
         OI    20(RB),CC                SET COMMAND CHAIN FLAG ON
         MVI   16(RB),WRTD              STORE WRITE DATA CMD OP
         TM    DCBCIND2,QSAMB           IS QSAM USED
         BO    SOPB505                  YES, BRANCH
         CLI   DCBKEYLE,ONE             IS KEY LENGTH SPECIFIED
         BC    4,SOPB52                 NO, BRANCH
         MVI   16(RB),WRTKD             STORE WRT KEY & DATA CMD BYTE
         B     SOPB52
SOPB505  EQU   *
         LA    RF,16(RB)                POINT TO ACTUAL WRT CCW
*
*        THE FOLLOWING CODE SETS UP THE CCW WITH LENGTH AND BUF ADDR
*
         TM    DCBOPTCD,WRTCK           WRITE CHECK USED?
         BNO   SOPB51                   NO, BRANCH
         TM    DCBRECFM,TRKOVFLB        TRK OV?
         BZ    NOTRKOV                  NO, BR
         MVC   WCWTOCCW(TWO,RF),DCBBLKSI  COUNT OTHER THAN ZERO
         B     SOPB51                   BR AROUND
NOTRKOV  EQU   *
         MVC   WCCCW(TWO,RF),DCBBLKSI   COUNT OTHER THAN ZERO
SOPB51   EQU   *
         L     RWK1,DCBBUFCB            ADDR BUF CNTRL BLOCK     YM7595
         USING BCBLK,RWK1
         L     RBUFADR,BCBBUFPT         ADDR OF NEXT AVAIL BUF   YM7595
         USING BUFFER,RBUFADR
         MVC   BCBBUFAD,BUFNXPTB        UPDATE WITH NEXT AVAIL   YM7595
         USING CCW,RF
         STCM  RBUFADR,B'0111',CCWADDRB  BUFFER ADDR TO CCW      YM7595
         MVC   CCWBYTE,DCBBLKSI         BYTE COUNT TO CCW        YM7595
         USING IOBQSAMN,RD
         ST    RBUFADR,IOBFLAG3         SAVE BUFF ADDR FOR 1923  YM7595
         DROP  RD,RF,RWK1,RBUFADR
SOPB52   EQU   *
         TM    DCBOPTCD,WRTCK           IS WRITE VALIDITY CHECK SPEC
         BC    14,SOPB53               NO. BRANCH
         LA   RB,THREECCW(RB)           ADD FOR WRT CHK CCW'S    A31983
         MODESET EXTKEY=DATAMGT         BACK TO DATA MANAG KEY   Y02072
         MVI  DCBWCPL,WCWCPL            WRITE CP LEN, WRT CHK    A31983
         TM   DCBRECFM,TRKOVFLB         TRK OVFLOW SPECIFIED?    A31983
         BNO  NOTRKOV1                  NO, DON'T UPDATE CP LEN  A31983
         MVI  DCBWCPL,WCTOWCPL          WRITE CP LEN, WRT CHK    A31983
*                                          + TRK OVFLOW          A31983
         MODESET  KEYADDR=DXUKEY,WORKREG=1  GET INTO USER KEY    Y02072
         LA    RF,41(RD)                POINT TO BBCCHHR IN       18445
*                                       IOBSEEK FIELD             18445
         ST    RF,0(RB)                 STORE SEEK ADDRESS        18445
         BAL   RBRNCH,SOPB700           GO GET PROPER OP CODE    YM5362
         MVI   4(RB),CC                 SET COMMAND CHAIN FLAG ON 18445
         MVI   7(RB),X'06'              STORE COUNT OF 6 IN CCW   18445
         LA    RB,8(RB)                 UPDATE RB TO NEXT CCW     18445
         B     SOPB52A                  BR AROUND MODESET        Y02072
NOTRKOV1 EQU  *                                                  A31983
         MODESET  KEYADDR=DXUKEY,WORKREG=1  GET INTO USER KEY    Y02072
         USING CCW,RB                                            Y02072
SOPB52A  ST    RWK5,CCWADDRA            SECTOR ADDR TO CHAN PGM  Y02072
         DROP  RB                                                Y02072
         MVI   ZERO(RB),SETSECTR        MOVE IN COMMAND CODE
         MVI   FOUR(RB),CC              MOVE IN LENGTH
         MVI   SEVEN(RB),ONE            INCREASE POINTER BY CCW LEN
         LA    RB,EIGHT(RB)
         OI    C20(RB),SLICCSK          SET SILI AND CC FLAGS ON
         ST    RC,0(RB)                 SAVE ADDRESS IN SRCH CCW
         MVI   0(RB),SRCHE              STORE SRCHE CMD BYTE
         OI    4(RB),CC                 SET CMD CHAIN FLAG ON
         MVI   7(RB),X'05'              STORE COUNT OF 5 IN CCW
         ST    RB,8(RB)                 SRCH ADDR IN TIC CCW
         MVI   8(RB),TIC                STORE TIC CMD BYTE
         MVI   16(RB),RDD               STORE READ DATA CMD OP
         MVI   C23(RB),ONE              SET READ VERIFY LENGTH ONE
         TM    DCBCIND2,QSAMB           QSAM SPECIFIED
         BO    SOPB55                   YES, BRANCH
         CLI   DCBKEYLE,ONE             IS KEY LENGTH SPECIFIED
         BC    4,SOPB53                 NO, BRANCH
         MVI   16(RB),RDKD              STORE RD KEY & DATA CMD BYTE
SOPB53   EQU   *
         TM    DCBCIND2,QSAMB           QSAM USED
         BC    1,SOPB55                 YES, BRANCH
         NI    20(RB),NOT-CC            CLEAR COMMAND CHAIN FOR BSAM
         LA    RB,16(RB)                POINT TO BSAM OFFSET
         B     SOPB56
SOPB55   EQU   *
         LA    RB,24(RB)                POINT TO READ SEQ.
*
***********************************************************************
*    THIS SECTION GENERATES READ CHANNEL PROGRAMS
***********************************************************************
*
SOPB56   EQU   *
         LA    RC,8(RB)                 POINT TO SRCH CCW OF RD SEQ
         ST    RC,24(RD)                STORE CP STARTAD IN IOB
         TM    DCBCIND2,QSAMB           QSAM USED?
         BNO   SOPB60                   NO, BR
         MODESET EXTKEY=DATAMGT         BACK TO DATA MANAG KEY   Y02072
         MVI   DCBOFFSR,OFFSR           STORE NORMAL RD CCW OFFSET
         TM    DCBOPTCD,WRTCK           IS WRITE VALIDITY CHECK SPEC
         BC    14,SOPB60                NO, BRANCH
         MVI  DCBOFFSR,WCOSRQ           READ CCW OFFSET FOR QSAM A31983
*                                          WITH WRT CHK          A31983
         TM   DCBRECFM,TRKOVFLB         TRK OVFLOW SPECIFIED?    A31983
         BNO  SOPB60                    NO, DON'T UPDATE OFFSET  A31983
         MVI  DCBOFFSR,TOWCOSRQ         YES, READ CCW OFFSET FOR A31983
*                                          QSAM WITH WRTCK + TKOVA31983
SOPB60   EQU   *
         MODESET  KEYADDR=DXUKEY,WORKREG=1  GET INTO USER KEY    Y02072
         LA    RC,SIXTY5(RB)            POINT TO BBCCHH FIELD
         TM    DCBCIND2,QSAMB           QSAM SPECIFIED
         BZ    SOPB601                  NO. BRANCH
         ST    RC,0(RB)                 STORE ADDR IN SEEK CCW
         BAL   RBRNCH,SOPB700           GO GET PROPER OP CODE    YM5362
         MVI   7(RB),X'06'              STORE COUNT OF 6 IN CCW
         OI    4(RB),CC                 SET COMMAND CHAIN FLAG ON
SOPB601  EQU   *
         LA    RB,EIGHT(RB)             BUMP COUNTER BY CCW LENGTH
         MVC   ONE(THREE,RB),DXCCW11+ONE  MOVE IN ADDR OF SECTOR
         MVI   ZERO(RB),SETSECTR        MOVE IN COMMAND CODE
         OI    FOUR(RB),CC              TURN ON COMMAND CHAIN
         MVI   SEVEN(RB),ONE            MOVE IN LENGTH
         LA    RC,2(RC)                 POINT TO CCHH FIELD
         ST    RC,8(RB)                 STORE ADDR IN SRCH CCW
         MVI   8(RB),SRCHE              STORE SEARCH COMMAND BYTE
         MVI   15(RB),X'05'             STORE COUNT OF 5 IN CCW
         OI    12(RB),CC                SET COMMAND CHAIN FLAG ON
         LA    RWK4,L'CCW(RB)           GET ADDR OF SEARCH CCW   Y02072
         ST    RWK4,2*L'CCW(RB)         STORE IN TIC CCW         Y02072
         MVI   16(RB),TIC               STORE TIC COMMAND BYTE
         LA    RWK4,4*L'CCW(RB)         GET ADDR OF RD COUNT CCW Y02072
         ST    RWK4,3*L'CCW(RB)         ST IN TIC CCW            Y02072
         MVI   24(RB),TIC               STORE TIC COMMAND BYTE
         TM    DCBRECFM,TRKOVFLB        TRACK OVERFLOW SPECIFIED
         BZ    SOPB605                  NO, BRANCH
*                                       YES, CHANGE TIC CCW TO RD DATA
         MVI   24(RB),RDDX              READ DATA CMD OP FOR T/O
         MVI   28(RB),SLICCSK           SKIP, SILI, CC FLAGS
         MVI   31(RB),50                NON-ZERO LENGTH
SOPB605  EQU   *
         BCT   RUCB,NEXTIOB             GO BUILD NEXT IOB
         L     RWK4,DCBIOBA             GET FIRST IOB ADDR       Y02072
         USING IOBQSAMN,RD                                       Y02072
         ST    RWK4,IOBNIOBA            LINK FIRST TO LAST       Y02072
         OI    IOBNFLG1,IOBREAD         READ ONLY BIT            Y02072
         DROP  RD                                                Y02072
         MODESET EXTKEY=DATAMGT         BACK TO DATA MANAG KEY   Y02072
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,IGG01923        ID TO WTG TABLE        @Z30TSCF
         MVC   DCBCHCKA,SECTORA         SAVE SECTOR ADDRESS      Y02072
         SR    RB,RD                    GET OFFSET INTO IOB FOR
*                                          IGG01923
         STC   RB,DCBREAD+TWO           SAVE OFFSET
         SR    RC,RD                    OFFSET FOR IGG01923
         STC   RC,DCBREAD+THREE         SAVE OFFSET
RELOOP   EQU   *
         LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURR WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCR CURRENT DCB ENTRY PRTR
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   8,RBASE
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BC    7,RELOOP                 NO,CHECK NEXT ENTRY
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      REINITIALIZE WTG LIST PTR
ZCHEK    EQU   *
         CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN                IF NOT TRANSFER CONTROL
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         BC    15,ZCHEK
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
         EJECT
*
*  THE FOLLOWING WILL SET UP THE NEXT CCW AS A SEEK HEAD OR A    YM5362
*  NOP DEPENDING ON THE DEB FILE MAS FOR PERFORMANCE REASONS.    YM5362
*  IF THE FILE MASK INDICATES ALLOCATION IN CYLINDERS, SET CCW   YM5362
*  OP CODE TO A SEEK HEAD. IF ALLOCATION IS BY TRACKS, SET TO A  YM5362
*  NOP. THIS WILL PREVENT FILE MASK VIOLATIONS DURING I/O        YM5362
*  OPERATIONS AND THUS LESS ERROR PROCESSING BY APPENDAGES,      YM5362
*  EXCP, AND THE ERP.                                            YM5362
*
SOPB700  EQU   *                                                 YM5362
         LA    RWK4,DEBBASND            ADDR OF DEB FILE MASK    YM5362
         USING DEBDASD,RWK4                                      YM5362
         USING CCW,RB                                            YM5362
         MVI   CCWCOMCD,SEEK            ASSUME ALLOC IN CYLS     YM5362
         TM    DEBDVMOD,SEEKCYHD        IS ALLOCATION IN CYLS    YM5362
         BNOR  RBRNCH                   YES, SEEK HEAD OP CD OK  YM5362
         MVI   CCWCOMCD,NOP             NO, MAKE OP CD A NOP     YM5362
         BR    RBRNCH                   RETURN MAINLINE          YM5362
         DROP  RB,RWK4                                           YM5362
         SPACE 6
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES
OPIDCNST DC    C'0S'
AMIDCNST DC    C'1Z'                    THIS MODULES ID
ONEIOB   DC    H'1'                    FOR NO OF IOB'S TEST      A37507
NXTSECSV EQU   ONEIOB                   LEN OF A SECTOR SAVE AR  Y02072
IGG01923 DC    C'23',VL3(IGG01923)      NEXT MOD               @Z30TSCF
PATCH    DC    0H'0',50X'00'            PATCH AREA               Y02072
END      EQU   *                        END OF MODULE            Y02072
         EJECT
CVT      DSECT                                                 @Z30TSCF
         CVT                                                   @Z30TSCF
         EJECT
         IGGBCB  TYPE=SAM
         EJECT
         IEZDEB
         EJECT
         DCBD  DSORG=PS
         TITLE 'IGG0191Z - FORMAT OF GOTTEN CORE'
*
         IECDSECS  (MAIN,(IOB,NO)),IOB,WTG,PREFX,EXPAND=YES    @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MODS ID             Y02072
FORCORE  DSECT                                                   Y02072
         ORG   DXCCW11                                           Y02072
SECTORAD DS    0F                       SAVE AREA FOR SECTOR AD  Y02072
         DS    X                        FILLER                   Y02072
SECTORA  DS    AL3                                               Y02072
         EJECT
         IHACCW  DSECT=YES                                       Y02072
           END
