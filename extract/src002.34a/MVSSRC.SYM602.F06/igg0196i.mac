196I     TITLE  'IGG0196I  --  DEB CONSTRUCTION'
IGG0196I CSECT
*
*MODULE NAME - IGG0196I                                          Y02072
*
*DESCRIPTIVE NAME - BEGIN DEB CONSTRUCTION                       Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*        VS1 RELEASE 02 DELETIONS
*D391300-391600                                                  OX1819
*
*        VS 2 RELEASE 02 DELETIONS
*012500,211000-222000,397000,428000,499000,455000,478000,482000, Y02072
*589000,590000                                                   Y02072
*                                                                YM4640
*C404000,421000                                                  YM7574
*                                                                YM7829
*
*        VS 2 RELEASE 03 DELETIONS
*                                                              @Z30TSCF
*
*        VS2 RELEASE 03 CHANGES/DELETIONS
*A135500-136000,430500-430600                                   ZA01540
*C210600                                                        ZA00631
*   OZ14711 IS SYSROUTE FROM OY15702                           @ZA14711
*A346051,346052                                                @ZA14711
*
*          RELEASE 21 DELETIONS
*0000496000-502000                                               M0091
*0000                                                            M0053
*1394246130,246672-246676                                        A42897
*0000444000-445000                                               A46645
*                                                               SA53194
*
*  THIS MODULE WAS CREATED IN REL 21 BY SPLITTING IGG0191A
*     ( NEW FLOW - IGG0191A - IGG0196I - IGG0196A )
*
*
*FUNCTION  THIS EXECUTOR IS ENTERED FOR USERS OF SAM AND EXCP
*          DURING OPEN FOR THE CONSTRUCTION OF DEBS.
*          THE AMOUNT OF CORE NEEDED TO BUILD THE DEB IS
*          CALCULATED AND A GETMAIN IS ISSUED.
*          CORE IS CLEARED AFTER GETMAIN
*          POINTERS TO TCB & DCB PUT INTO DEB.
*          DEBS ARE BUILT FOR ALL DEVICES SUPPORTED BY THE SEQUENTIAL
*          ACCESS METHODS OR PARTITIONED ACCESS METHOD.
*
*ENTRY POINT--IGG0196I
*
*          ENTERED FROM IGG0191A BY XCTL
*
*INPUT--  DCB ADDRESS
*          WORK AREA ADDRESS
*          WHERE TO GO TABLE
*          PARAMETER LIST
*          CURRENT ENTRY IN WHERE TO GO TABLE
*          CURRENT PARAMETER (DCB)
*
*OUTPUT-   AS OUTLINED BELOW :
*
*        BASIC DEB LAYOUT FOR ALL I/O DEVICES SUPPORTED
*
*        **********************************
*   -16  *I/OS* I/O SUPPORT DCB ADDRESS   *
*   -12  *    * RESERVED                  *
*   -08  *    * DCB MODIF MASK            *
*   -04  *DEBL*                           *
*   +00  *SUBL* TCB ADDRESS               *
*   +04  *AML * DEB LINK FIELD            *
*   +08  *DPAT* IRB ADDRESS               *
*   +12  *FLTP* SYSTEM PUTGE CHAIN        *
*   +16  *NEXT* USER PURGE CHAIN          *
*   +20  *PRTY* ECB ADDRESS               *
*   +24  *KEYD* DCB ADDRESS               *
*   +28  *SCAL* APPENDAGE ADDRESS         *
*        **********************************
*
*        DEVICE DEPENDENT SECTION OF DEB
*
* TAPE/URE
*
*        **********************************
*   +32  *DMOD*  UCB ADDRESS              *
*   +36  *VOL SEQ NO     *NO. OF VOLS.    *
*        **********************************
*
* REMAINDER OF THE DEB IS BUILT IN IGG0191N.
*
* D.A.SAM DEB.
*        **********************************
*   +32  *FMSK*  UCB ADDRESS              *
*   +36  *EXTENT FIELD BBCCHHCCHH         *
*   +40  *                                *
*   +44  *               *NO.TRACKS ALL.  *
*   +48  *VOL SEQ NO     *NO. OF VOLS     *
*        **********************************
*
* NOTE.+32 - +44 IS REPEATED PER EXTENT
*
* D.A.TO MEMBER FOR OUTPUT
*        **********************************
*   +E2  *MEMBER NAME THAT IS OPENING     *
*   +56  *MEMBER NAME THAT IS OPENING     *
*        **********************************
*
* DA BPAM CONCATENATION (INPUT)
*        **********************************
*   +48  *DSNO DSNO  DSNO DSNO            *
*        *EXTU EXT2  EXT3 EXT4            *
*        **********************************
*
*NOTE. BPAM DEB SAME AS SAM DEB WITH EXCLUSSION OF FIELD AT +48
*
*        USER LABEL EXTENT IS NOT INCLUDED IN F1 EXTENT COUNT,
*        AND IS NOT INCLUDED IN DEB EXTENTS.
*
*        ROUTINE ALSO MERGES CLASS BYTE AND DEVICE BYTE OF UCB/SRT
*        INTO DCB FIELD (DEVT)
*
* EXITS - NORMAL XCTL TO IGG0196A TO CONTINUE PROCESSING
*
*TABLES/WORKAREA - SEE DSECTS AT END OF LISTING.
*
*ATTRIBUTES- RE-ENTRANT- RE-USABLE- RUNS IN DATA MANAGEMENT KEY, Y02072
*            RUNS IN SUPERVISOR STATE                            Y02072
*
*******************************************************************
*
* MASK'S TO TEST PARAMETER LIST WITH
OPPATRUB EQU   0
DEBBSLNG EQU   80
COMMUTAB EQU   16
NEXTCORE EQU   140
SUBRNTID EQU   20                       SUBROUTINE ID LENGHT.
DEBXNTP  EQU   28                       DISP IN DEB PREFIX      ZA01540
*                                       TO DEB EXTENSION PTR    ZA01540
ZERO     EQU   0                        PARAMETRIC ZERO            UT18
INFLAG   EQU   X'0E'                    FLAG FOR INPUT TEST        UT18
BFTEKR   EQU   X'20'                    DCBBFTEK=R               S19015
VSBITS   EQU   X'48'                    VS BITS IN DCBRECFM      S19015
RECFMB   EQU   X'10'                    BLOCKED RECORD FORMAT    S19015
QSAMB    EQU   X'01'                    QSAM BIT                 S19015
EXCP     EQU   X'80'               EXCP                          I19119
TIOTTERM EQU   X'20'                    DEVICE IS TERMINAL (TSO) S20016
SULEXNT  EQU  X'40'                     TEST IF USER LABEL EXTENTA46645
SPLTCYL  EQU  X'80'                     TEST IF SPLT CYL ALLOC   A46645
* * * * * * * * * * * *   3525    CODE      * * * * * * * * * * * * * *
RP       EQU   X'60'                   READ/PUNCH MASK           M0053
RW       EQU   X'50'                   READ/PRINT MASK           M0053
PW       EQU   X'30'                   PUNCH/PRINT MASK          M0053
D3525    EQU   X'0C'                   3525 UCB UNIT TYPE CODE   M0053
ASSOCDS  EQU   X'04'                   ASSOC. DATASET--3525     SA53194
FUNCOFF  EQU   3                       DCBFUNC OFFSET            M0053
DEBCORE  EQU   230                      SUBPOOL NUMBER FOR DEB   Y02072
FMTFOUR  EQU   C'4'                     DSCB FORMAT 4 IDENTIFIER Y02072
*
*REGISTERS ARE REFERENCED SYMBOLICALLY WHERE APPROPRIATE
*
RDCB     EQU   2         DCB REGISTER
RBASE    EQU   3         BASE REGISTER
RCORE    EQU   4         WORK AREA ADDRESS
RPAR     EQU   5         TOP OF PARAMETER LIST
RWTG     EQU   6         TOP OF WTG TABLE
RPARC    EQU   7         CURRENT PARAMETER
RWTGC    EQU   8         CURRENT TRANS LOAD
RTIOT    EQU   9         USED HERE AS WRK REG AND  COMM VECTOR ADDR.
RUCB     EQU   10        USED HERE AS A COUNTER IN IOB GENERATION
RDEB     EQU   11        DEB ADDRESS
RB       EQU   12        WORK REG1  **
RC       EQU   13        WORK REG2  **  USED IN IOB GENERATION
RD       EQU   14        WORK REG3  **
RJ       EQU   15        WORK REG4
RE       EQU   0         WORK REG5
RWK1     EQU   RE                       WORK REGISTER            Y02072
RF       EQU   1         WORK REG6
*
***********************************************************************
*
         BALR  RBASE,0
         USING BEGIN,RBASE
         USING IHADCB,RDCB
         USING FORCORE,RCORE
         USING DEB,RDEB
         USING   TCB,RD
         USING TIOT,RTIOT
         USING UCBOB,RUCB
*
BEGIN    EQU   *
         B     START                    BRANCH AROUND MODULE ID  YM4640
         DC    C'IGG0196I'              MODULE NAME              YM4640
         DC    C' ZA14711'              LAST SHIP CODE         @ZA14711
         DC    C'02/16/77'              LAST SHIP DATE         @ZA14711
START    DS    0H                       END OF MODULE ID         YM4640
*
         L     RDCB,0(RPARC)            GET DCB ADDRESS
         L     RCORE,4(RWTGC)           GET WORK AREA
         L     RD,COMMUTAB         CALCULATE TCB ADDRESS
         L     RD,0(0,RD)
         L     RD,4(0,RD)               TCB  ADDR
         L     RTIOT,TCBTIO
         AH    RTIOT,DCBTIOT       RESET TIOT POINTER
         TM    TIOELINK,TIOTTERM        IS THIS TSO?             S20016
         BO    OBPAM074                                         ZA00631
         L     RUCB,DXUCBADR            GET UCB PTR              Y02072
         USING UCBOB,RUCB
         TM    UCBTBYT3,UCB3DACC        OPENING A D A DEVICE
         BC    14,OBPAM070              NO BRANCH
         SR    RB,RB               SET NUMBER OF DS COUNTER TO ZERO
         SR    RE,RE
         SR    RC,RC
         IC    RC,DSCNOEXT              GET NO OF EXT OF FIRST DS
         LTR   RC,RC                    NO OF EXTENTS = 0
         BC    7,OBPAM010               NO BRANCH
         LA    RC,1                     SUPPLY  ONE DUMMY EXTENT
         CLI   DSCFMTID,FMTFOUR         FORMAT 4 DSCB            Y02072
         BE    OBPAM010                 YES, DON'T SET VOL FULL  Y02072
         TM    DCBMACRF,X'80'           EXCP BIT ON
         BC    1,OBPAM010               BRANCH IF EXCP
         OI    DCBCIND1,X'20'           SET VOL FULL IND DUMMY EXT
OBPAM010 EQU   *
         L     RF,DSCCORE               GET NEXT DSCB ADDR
OBPAM020 EQU   *
         LTR   RF,RF                    CK FOR MORE DSCB
         BC    8,OBPAM080          BRANCH IF NONE LEFT
         CLI   44(RF),X'F1'             CK FOR FORMAT 1
         BC    7,OBPAM040               BR IF NO
*
*        THERE WILL BE FORMAT 1 ONLY IF CONCATENATION IS SPECIFIED
*
         IC    RE,59(0,RF)              GET NO OF EXTENT OF NEXT DS
         LTR   RE,RE                    NO OF EXTENTS = 0
         BC    7,OBPAM030               NO BRANCH
         LA    RE,1                     SUPPLY ONE DUMMY EXTENT
OBPAM030 EQU   *
         AR    RC,RE                    FORM TOTAL NO OF EXTENT
         LA    RB,1(0,RB)               ADD ONE TO NO OF DS
OBPAM040 EQU   *
         L     RF,NEXTCORE(0,RF)        GET NEXT DSCB
         BC    15,OBPAM020
OBPAM060 EQU   *
         CLI   JFCBELNM,X'40'           CK FOR MEMBER NAME
         BC    8,OBPAM100               BR IF NO NAME
         LA    RDEB,8(0,RDEB)           ADD 8 BYTES FOR MEMBER NAME
         BC    15,OBPAM100
OBPAM070 EQU   *
* * * * * * * * * * * *   3525    CODE      * * * * * * * * * * * * * *
         TM    UCBTBYT3,UCB3UREC       UNIT RECORD DEVICE?     @ZA14711
         BNO   OBPAM074                BRANCH IF NO            @ZA14711
         CLI   UCBTBYT4,D3525          IS THIS A 3525            M0053
         BNE   OBPAM074                BRANCH IF NOT A 3525      M0053
         TM    DCBMODE+FUNCOFF,RP      READ/PUNCH ASSOCIATED     M0053
*                                       DATA SET                 M0053
         BO    OBPAM072                BRAMCH IF YES             M0053
         TM    DCBMODE+FUNCOFF,RW      REAR/PRINT ASSOCIATED     M0053
*                                       DATA SET                 M0053
         BO    OBPAM072                BRANCH IF YES             M0053
         TM    DCBMODE+FUNCOFF,PW      PUNCH/PRINT ASSOCIATED    M0053
*                                       DATA SET                 M0053
         BNO   OBPAM074                BRANCH IF NOT PUNCH/PRINT M0053
OBPAM072 EQU   *                                                 M0053
         OI    DCBQSWS,ASSOCDS         TURN ON BIT              SA53194
         LA    RDEB,16                 16 BYTE EXTENT FOR AN     M0053.
*                                       ASSOC D,S                M0053
         B     OBPAM075                BRANCH TO LOAD AM SECTION M0053
*                                       SIZE                     M0053
OBPAM074 EQU   *                                                 M0053
* * * * * * * * * * *   END OF 3525 CODE   * * * * * * * * * * * * * *
         LA    RDEB,4                   ONE FOUR BYTE EXTENT FOR TAPE
OBPAM075 EQU   *                                                 A25961
         LA    RDEB,16(RDEB)            16 BYTE AM SECTION       A25961
         B     OBPAM100
OBPAM080 EQU   *
         LR    RDEB,RC                  TOTAL NO OF EXTENTS
         SLL   RDEB,4                   MULT BY 16 EXT SIZE
         TM    DSCFILTY,X'02'           DSORG PO                 A25961
         BNO   OBPAM075                 BR IF NO                 A25961
         TM    OPPATRUB(RPARC),X'0F'    CK FOR OUTPUT
         BC    1,OBPAM060               BR IF YES
         LA    RDEB,0(RB,RDEB)          ADD NO OF DS FOR INPUT
OBPAM100 EQU   *
         LA    RDEB,DEBBSLNG+SUBRNTID(RDEB)  ADD BASIC AND SUBRNT ID.
         TM    TIOELINK,TIOTTERM        IS THIS TSO?             S20016
         BO    OBPAM101                 BRANCH IF YES            S20016
         TM    DCBMACRF,EXCP            BR IF EXCP,  DCBOPTCD   OX01819
         BO    OBPAM115                   DOES NOT EXIST        OX01819
         TM    UCBTBYT3,UCB3DACC        IS DEVICA DA.
         BC    14,OBPAM101              BRANCH IF NOT.
         TM    DSCFILTY,X'02'           DCB REFERENCING A PDS
         BZ    OBPAM101                 BRANCH, IF NOT           A25961
         NI    DCBOPTCD,X'FD'           IF PO,IGNORE INVALID     A27334
OBPAM101 EQU   *                                                 S19015
*     THE NEXT SECTION OF CODE VALIDATES THE 'R' BIT IN THE DCBBFTEK
*     FIELD, IF IT IS ON.
         TM    DCBCIND2,QSAMB           QSAM BEING USED          S19015
         BO    OBPAM115                 YES--CONTINUE            S19015
         TM    DCBBFTEK,BFTEKR          VS SUPPORT NEEDED        S19015
         BZ    OBPAM115                 N0--CONTINUE             S19015
         TM    DCBRECFM,VSBITS          TEST FOR VS              S19015
         BNO   OBPAM112                 NOT VS--GO TURN OFF 'R'  S19015
*                                       BIT                      S19015
         TM    DCBRECFM,RECFMB          BLOCKED RECORDS          S19015
         BO    OBPAM112                 BLOCKED RECORDS NOT      S19015
*                                       SUPPORTED                S19015
         TM    ZERO(RPARC),INFLAG       INPUT                    S19015
         BC    7,OBPAM115               NO--CONTINUE             S19015
         XC    DCBOPTCD(L'DCBOPTCD),DCBOPTCD  CLEAR DCBOPTDC FIELD
         CLI   DCBKEYLE,ZERO            ARE THERE KEYS           S19015
         BNE   OBPAM115                 YES--R BIT WILL BE LEFT  S19015
*                                       ON                       S19015
OBPAM112 EQU   *                                                 S19015
         NI    DCBBFTEK,X'FF'-BFTEKR    TURN OFF BFTEKR BIT      S19015
OBPAM115 EQU   *                                                 S19015
         LA    RE,DEBCORE               GET CORE FROM SP 230     Y02072
         SLL   RE,24
         OR    RE,RDEB                  SET UP FOR GETMAIN
         ST    RE,REGSAVE0              SAVE LEN ACROSS GETMAIN  YM7574
         GETMAIN R,LV=(0)               GET CORE FOR DEB
*
*        CONSTRUCT BASIC SECTION
*
         ST    RF,REGSAVE1              SAVE ADDR OF CORE        YM7574
         LA    RJ,256                   GET CONSTANT OF 256        UT18
OBPAM140 EQU   *                                                   UT18
         CR    RDEB,RJ                  IS LENGTH GRTR THAN 256    UT18
         BNH   OBPAM145                 BR IF NOT GRTR THAN 256    UT18
         BCTR  RJ,0                     DECR LNGTH TO 255 FOR MOVE UT18
         EX    RJ,CLEARDEB              EXECUTE MOVE OF 256 BYTES  UT18
         LA    RJ,1(0,RJ)               RESTORE TO 256             UT18
         SR    RDEB,RJ                  SUBTRACT 256 FROM LENGTH   UT18
         AR    RF,RJ                    INCR AREA ADDR BY 256      UT18
         B     OBPAM140                 BR TO MOVE MORE BYTES      UT18
OBPAM145 EQU   *                                                   UT18
         BCTR  RDEB,0                   DECR LNGTH FOR MOVE INSTR  UT18
         EX    RDEB,CLEARDEB            MOVE LESS THAN 256 BYTES   UT18
         LM    RE,RF,REGSAVE0           RESTORE LENGTH,ADDRESS   YM7574
         LR    RJ,RE                    CLEAR HIGH ORDER BYTE      UT18
         LA    RJ,0(RJ)                 OF LENGTH REGISTER         UT18
         LA    RDEB,36(0,RF)            DEB ADDR PT TO BASIC SECTION
         ST    RDEB,DCBDEBAD            DEB ADDR INTO DCB
         MVC   DCBIFLGS(1),DCBIFLG     RESTORE IFLAGS
         NI    DCBDEBAD,X'3F'           TURN OFF IFLAGS
         ST    RDCB,DEBDCBAD            DCB ADDR TO DEB          Y02072
         ST    RD,DEBTCBAD              TCB ADDR INTO DEB
         ST    RF,DEBAPPAD              APP ADDR
         MVC   DEBXNTP(4,RF),DXDEBXAD   CONNECT DEB EXTENSION   ZA01540
*                                       TO DEB PREFIX           ZA01540
         STC   RB,DEBAMLNG              NO. OF DATA SETS - 1 IF  A25961
*                                       PARTITIONED. 0 IF NOT
         LA    RJ,7(RJ)                 ROUND OFF TO DW
         SRL   RJ,3                     LENGTH OF DEB IN DW
         STC   RJ,32(RF)
         MVI   DEBOFLGS,X'C0'
         NC    DEBOFLGS,JFCBIND2        OPEN NEW OLD MOD INDICATOR
         TM    TIOELINK,TIOTTERM        IS THIS TSO?             S20016
         BO    FINISH                   BRANCH IF YES            S20016
         TM    UCBTBYT3,UCB3DACC        OPENING A D.A DEVICE      18987
         BC    14,OBPAM120              NO BYPASS TEST FOR LAST   18987
*                                       VOLUME INDICATOR          18987
         TM    DSCDSIND,X'80'           TEST LAST VOL IND IN DSCB 18987
         BC    8,OBPAM120               NO DONT SET EOF IND       18987
         OI    DEBOFLGS,X'20'           SET EOF IN DEBOFLGS       18987
*
*    APAR 46645 RESULTED FROM A USER SPECIFYING BOTH STANDARD    A46645
*    USER LABELS AND SPLIT CYLINDER ALLOCATION. SPLIT CYLINDER   A46645
*    ALLOCATION WOULD NOT BE GIVEN TO THE USER UNDER THESE       A46645
*    CONDITIONS.                                                 A46645
OBPAM120 EQU  *                                                  A46645
         CLI  DSCEXTYP,SULEXNT          USER LABEL EXTENT?       A46645
         BNE  AROUND                    NO, DON'T TEST 2ND EXENT A46645
         CLI  DSCEXT1,SPLTCYL           SPLIT CYLINDER           A46645
*                                          ALLOCATION?           A46645
         BNE  OBPAM160                  NO, DON'T SET SPLT CYL   A46645
*                                          BIT                   A46645
         B    SPCYL                     YES, SET SPLT CYL BIT    A46645
AROUND   EQU  *                                                  A46645
         CLI  DSCEXTYP,SPLTCYL          SPLIT CYLINDER           A46645
*                                          ALLOCATION?           A46645
         BNE  OBPAM160                  NO, DON'T SET BIT        A46645
SPCYL    EQU  *                                                  A46645
         OI    DEBOFLGS,X'04'           SET INDICATOR IN DEB
OBPAM160 EQU   *
         STC   RC,DEBNMEXT              STORE NO. OF EXTENTS     M4878
FINISH   EQU   *
         LA    RF,SOP6A                 SET UP FOR XCTL TO       S19033
*                                       IGG0196A                 S19033
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,0(RF)           ID OF NEXT MODULE      @Z30TSCF
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     UPDATE WHERE TO GO ENTRY
         LA    RPARC,PLOFF(0,RPARC)     UPDATE PARA LIST ENTRY
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   8,RBASE                  YES BR BACK TO BEGINNING
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BC    7,RELOOP                 NO BRANCH
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      RE INIT  WTG LIST
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN                NO XCTL
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         BC    15,ZCHEK
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG
WAOFF    EQU   32                  OFFSET OF FIRST ENTRY IN WTG TABLE
PLOFF    EQU   4                   OFFSET OF DCB ENTRIES
WGOFF    EQU   8                   OFFSET OF WTG ENTRIES
OPIDCNST DC    C'0S'
AMIDCNST DC    C'6I'
SOP6A    DC    C'6A',VL3(IGG0196A)      IGG0196A - NEXT MODULE @Z30TSCF
CLEARDEB XC    ZERO(ZERO,RF),ZERO(RF)   CLEAR CORE FOR DEB         UT18
         SPACE
PATCH    DC    50X'0'                   PATCH AREA               YM4640
END      EQU   *                        END OF MODULE            Y02072
*********************************************************************
SRT      DSECT
         IEFUCBOB
*
         EJECT
           DCBD  DSORG=PS
CVT      DSECT
         CVT
*   TASK CONTROL BLOCK
         CNOP  0,4
TCB      DSECT
TCBRBP   DS    F                       PO9NTER TO EXECUTING RB
TCBPIE   DS    F                       POINTER TO PROG. INTERRUPT EL.
TCBDEB   DS    F                       POINTER TO DEB QUEUE
TCBTIO   DS    F                       POINTER TO TIOT
TCBCMP   DS    F                       TASK COMPLETION CODE
TCBTRN   DS    F                       TESTRAN FIELD
TCBMSS   DS    F                       DATA BLOCK QUEUE ELEMENT POINTER
TCBPKF   DS    CL1                     RESERVED FOR FUTURE USE
TCBFLGS  DS    FL5                     FLAG BYTES
TCBLMP   DS    CL1                     RESERVED FOR FUTURE USE
TCBDSP   DS    CL1                     RESERVED FOR FUTURE USE
TCBLLS   DS    F                       POINTER TO LOAD LIST
TCBJLB   DS    F                       POINTER TO JOBLIB DCB
TCBJSE   DS    F                       LIST OF INACTIVE PROGRAMS ADDR.
TCBGRS   DS    CL64                    GENERAL REGISTER SAVE AREA
TCBIDF   DS    CL1                     TCB IDENTIFIER FIELD
TCBFSA   DS    CL3                     POINTER TO 1ST PROB.PROG.SAVEAR
TCBTCB   DS    F                       RESERVED FOR FUTURE USE
TCBTME   DS    F                       POINTER TO TIMER ELEMENT
         DS    CL24
TIOT     DSECT
TIOELNGH DS    CL1                           LENGTH OF THIS ENTRY
TIOESTTA DS    CL1                           STATUS -A-
TIOERLOC DS    CL2                           REL. LOC OF POOL
TIOEDDNM DS    2F                            DDNAME
TIOEJFCB DS    1F                            JFCB DISK ADDR
TIOESTTB DS    0CL1
TIOEFSRT DS    CL4
         DS    CL1
*        TIOT POOL ENTRY
TIOPNSLT DS    XL1                           NR.OF SLOTS FOR POOL
         DS    CL1
TIOPNSRT DS    XL1                           NR.OF DEVICES (FILLED
*                                             SLOTS)
TIOPPOOL DS    CL8                           POOL NAME
TIOPSTTB DS    XL1                           STATUS OF SLOT
TIOPSLOT DS    XL3                           SRT ADDRESS OR EMPTY SLOT
TIOTFEND DS    1F                            FINAL END OF AN ENTRY
TIOELINK EQU   TIOERLOC+1                                        S20016
         SPACE 1
*   DATA EXTENT BLOCK  (UNTIL PROTIOS CHANGES)
DEB      DSECT
DEBNMSUB DS    0CL1
DEBTCBAD DS    CL4
DEBAMLNG DS    0CL1
DEBDEBAD DS    CL4
DEBOFLGS DS    0CL1
DEBIRBAD DS    CL4
DEBOPATB DS    0CL1
DEBSYSPG DS    CL4
DEBNMEXT DS    0CL1
DEBUSRPG DS    CL4
DEBPRIOR DS    0CL1
DEBECBAD DS    CL4
DEBPROTG DS    0CL1
DEBDEBID DS    0CL1
DEBDCBAD DS    CL4
DEBEXSCL DS    0CL1
DEBAPPAD DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    CL4
DEBTVLSQ DS    0CL2                VOL SEQ FOR TAPE
DEBBINUM DS    CL2
DEBTVLNM DS    0CL2                NO OF VOL FOR TAPE
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
*  FOR ADDITIONAL EXTENT ON DIRECT ACCESS THE FOLLOWING FIELDS
*  WILL HAVE TO BE OFFSET ANOTHER 16 BYTES
DEBVOLSQ DS    H
DEBVOLNM DS    H
*  FOR EACH SUBROUTINE LOADED BUMP THIS OFFSET BY 2
DEBSUBID DS    H
         TITLE 'IGG0196I - FORMAT OF GOTTEN CORE'
         IECDSECS  (MAIN,(IOB,NO)),WTG,PREFX,EXPAND=YES        @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                                               Y02072
CCWOFSET EQU   DXCCW12-FORCORE     CCW RELOCATABLE OFF SET FOR DXCCW12
FORCORE  DSECT                                                   YM7829
REGSAVE0 EQU   DXCCW11                  SAVE AREA FOR REG 0      YM7829
REGSAVE1 EQU   DXCCW11+4                SAVE AREA FOR REG 1      YM7829
           END
