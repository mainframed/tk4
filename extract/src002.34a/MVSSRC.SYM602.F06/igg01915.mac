 TITLE 'IGG01915-SAM STAGE 3 OPEN EXEC FOR VARIABLE LNGTH/SPANNED RECS'
IGG01915 CSECT
*
*MODULE NAME - IGG01915                                          Y02072
*
*DESCRIPTIVE NAME - LOAD ACCESS METHOD ROUTINES                  Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          VS 2 RELEASE 2 DELETIONS                              Y02072
*001100,011500,014800,190500,191100,192500-193500,207799-207803, Y02072
*506050-508150,207840,458000-459000,463000,485000-497000,        Y02072
*508320,508400-508750                                            Y02072
*                                                                YM1140
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
* D281200-281700,D307200-307620,A281500,A307500                @ZA01530
*APAR @ZA08271 UPDATES APAR @ZA01530                           @ZA08271
*C265000,267000                                                @ZA08271
*A281500,281560,281620-283500,307500-307740                    @ZA08271
*          RELEASE 20 DELETIONS                                       *
*0911207822,207830,613000-614000                                 M5150
*0911279300-279600,305300-305600                                 A36867
*0911213000                                                      A31594
*0911                                                            M2687
*0911533000,625600-626200                                        S20202
*0911613000-614000                                               M5701
*          RELEASE 21 DELETIONS                                       *
*1093014400,264000-273000,460000,529000-627000,629000-630000     S21042
*1093533000,625500-626500                                        S21040
*578000                                                          M1816
*          RELEASE 22 DELETIONS                                       *
*          RELEASE 23 DELETIONS                                       *
*          RELEASE 24 DELETIONS                                       *
*          RELEASE 25 DELETIONS                                       *
*          RELEASE 26 DELETIONS                                       *
*STATUS CHANGE LEVEL 007
*
*FUNCTION/OPERATION - THIS MODULE LOADS ACCESS METHOD SUBROUTINES FOR
*    PROCESSING VARIABLE OR SPANNED RECORDS.  IT DETERMINES WHICH
*    MODULES SHOULD BE LOADED, LOADS THEM AND ENTERS THE ADDRESS OF
*    THE LOADED MODULES IN THE APPROPRIATE DCB FIELDS AND THE ID'S IN
*    THE DEB.  IT LOADS GET/PUT OR READ/WRITE ROUTINES AND ERROR CHECK
*    ROUTINES.
*
*    THIS MODULE WAS SPLIT FOR RELEASE 18.  THE RESULTING NEW MODULE
*    IS IGG01991.
*
*ENTRY POINTS - THE MODULE MAY BE ENTERED FROM STAGE 2 EXECUTORS
*    IGG0191J, IGG0191G, IGG0191D, OR IGG0191P.
*
*INPUT - SEE DESCRIPTION OF REGISTER CONTENTS
*
*OUTPUT - ADDRESSES OF SUBROUTINES LOADED ARE PLACED IN DCB, ID'S
*    ARE PLACED IN DEB SUBROUTINE ID SECTION.
*
*EXTERNAL ROUTINES - NONE
*
*MACROS - ACTION - MODESET, LOAD, XCTL, CIRB, DMABCOND, XCTLTABL Y02072
*
*EXITS - NORMAL - XCTL TO IGG01991 TO COMPLETE LOADING OF ACCESS
*                 METHOD SUBROUTINES.
*       - ERROR - XCTL TO PROBLEM DETERMINATION FOR WTP AND 013 ABEND
*
*TABLES/WORKAREAS - DESCRIBED BY DSECTS AT END OF MODULE LISTING.
*
*ATTRIBUTES - REENTRANT, REUSABLE, SUPERVISOR STATE,             Y02072
*             RUNS IN DATA MANAGEMENT KEY UNLESS OTHEWISE        Y02072
*             SPECIFIED.                                         Y02072
*
*
* REGISTER CONVENTIONS
RE       EQU   0                        WORK REG5
RF       EQU   1                        WORK REG6
R1       EQU   RF                       PARM REG                 YM1140
RDCB     EQU   2                        DCB
RBASE    EQU   3                        BASE
RCORE    EQU   4                        WORE AREA
RPAR     EQU   5                        TOP OF PARAMETER
RWTG     EQU   6                        TOP OF WTG TABLE
RPARC    EQU   7                        CURRENT PARAMETER
RWTGC    EQU   8                        CURRENT TRANS LOAD
RTIOT    EQU   9                        TIOT
RUCB     EQU   10                       UCB
RDEB     EQU   11                       DEB
RB       EQU   12                       WORK REG1
RC       EQU   13                       WORK REG2
RD       EQU   14                       WORK REG3
RJ       EQU   15                       WORK REG4
*
*
PARAR    EQU   RPARC
THREE    EQU   X'03'                    CONSTANT=3               S20202
K24      EQU   X'18'                    CONSTANT=24              S20202
K25      EQU   X'19'                    CONSTANT=25              S20202
*
***********************************************************************
*
***********************************************************************
*
* MASK'S TO TEST PARAMETER LIST WITH
*
*********************************************************************
INP      EQU   X'0F'                    MASK
OUTP     EQU   X'0F'                    MASK
UPDATE   EQU   X'04'                    MASK
INOUT    EQU   X'03'                    MASK
OUTIN    EQU   X'07'                    MASK
RDBACK   EQU   X'01'                    MASK
INPRDBK  EQU   X'0E'               MASK TO CK INPUT AND RDBACK     UT18
*
*
*********************************************************************
*
*
* OFFSET FOR PARAMETER TESTS
*
FRTYATE  EQU   48                       MASK
DISPSN   EQU   X'0'                     MASK
*
*
*********************************************************************
*
* OFFSETS FOR DEB REFERENCE
*
*
ABEAPPOF EQU   17                       OFFSET TO AB END APP     S20202
*                                       ADDR                     S20202
FOUR     EQU   X'04'                                             M5150
BIN      EQU   36
BEXT     EQU   38
*
*
*
*********************************************************************
*
* MASK'S USED TO TEST OPTION FIELDS IN DCB
*
*
*
DOUBLE   EQU   X'01'                    MASK
*
*
STOWB    EQU   X'80'                    MASK
WRTCK    EQU   X'80'                    MASK
OVRFLO   EQU   X'20'                    MASK
PCI      EQU   X'20'                    MASK
MINKEY   EQU   X'01'                    MASK
CONCAT   EQU   X'FF'                    MASK
UTFMASK  EQU   X'02'               MASK FOR TOTALING FACILITY      UT18
BFTEKR   EQU   X'20'                    MASK FOR OFFSET READ     S19015
*
*********************************************************************
*
* MASKS USED TO TEST OPTION FIELDS IN DEB
*
INOROUT  EQU   X'0F'                    INPUT OR OUTPUT          S19015
*
******************************************************************
*
* MASKS FOR ACCESS METHODS IN SAM
*
QSAMB    EQU   X'01'                    MASK
BSAMBIT  EQU   X'00'                    MASK
PAM      EQU   X'02'                    MASK
*
*
* MASKS FOR MACRO'S TO BE USED
*
GETB     EQU   X'40'                    MASK
PUTB     EQU   X'40'                    MASK
READB    EQU   X'20'                    MASK
WRITEB   EQU   X'20'                    MASK
POINTB   EQU   X'04'                    MASK
CNTRLB   EQU   X'02'                    MASK
*
*
*********************************************************************
* MASKS FOR DEVICES TO BE SUPPORTED
*
*
*********************************************************************
*
*
PTR3211  EQU   X'49'                    MASK FOR 3211 PRINTER    S20202
*
*
*********************************************************************
*
* MASKS FOR STATUS BITS TO BE TESTED
*
*
EXCPN    EQU   X'30'                    MASK
COMPLETE EQU   X'80'                    MASK
FIRSTIOB EQU   X'01'                    MASK
*
*
*
*********************************************************************
*
* MASKS FOR BUFFER TECHNIQUE'S
*
CYCLICB  EQU   X'20'
EXCHANGB EQU   X'10'
SIMPLEB  EQU   X'40'
EXCHCYCL EQU   X'30'
*
*    MASKS FOR OPEN OVERRIDE OPTIONS
*
OVRDMSK  EQU   X'C0'                    INDICATES OPEN OVERRIDE  S21040
OVRDIN   EQU   X'0F'                    TEST FOR LABEL=(,,,IN)   S21040
CTDISP   EQU   X'10'                    CT DISPLACEMENT IN TABLE S21040
*
* MODES  OPERATION FOR QSAM
LOCATEB  EQU   X'08'
MOVEB    EQU   X'10'
MOVEXCHB EQU   X'14'
SUBSTUTB EQU   X'04'
DATAMODE EQU   X'01'
* COMMAN YTES FOR THE 2841
SRCHE    EQU   X'31'
WRTCKD   EQU   X'1D'
RDC      EQU   X'92'
RDKD     EQU   X'8E'
RDD      EQU   X'86'
TIC      EQU   X'08'
* MASKS   RECORD FORMATS
STANDRDF EQU   X'08'
FORMATU  EQU   X'C0'
FORMATF  EQU   X'80'
FORMATV  EQU   X'40'
SPANNED  EQU   X'08'
BLOCKED  EQU   X'10'
FIXEDBLK EQU   X'90'
*   CHAN  CONTROL WORD FLAGS
SKIP     EQU   X'10'
SLI      EQU   X'20'
CC       EQU   X'40'
DATACH   EQU   X'A0'
SLICC    EQU   X'60'
SLICCSK  EQU   X'70'
KEYS     EQU   X'01'
QSAM     EQU   X'01'
GETMAIN  EQU   4
XCTL     EQU   7
LOAD     EQU   8
ASA      EQU   X'04'
BKREAD   EQU   X'01'
URBITS   EQU   X'40'
TAPEB    EQU   X'80'
DABIT    EQU   X'20'
D2       EQU   2                        DISPLACEMENT OF 2        S21040
D3       EQU   3                        DISPLACEMENT OF 3        S21040
D6       EQU   6                        DISPLACEMENT OF 6        S21040
L1       EQU   1                        LENGTH OF 1              S21040
L4       EQU   4                        LENGTH OF 4              S21040
R25      EQU   25                       CONSTANT
R3       EQU   3                        CONSTANT
R5       EQU   5                        CONSTANT
R2       EQU   2                        CONSTANT                 S19015
ATBOFST  EQU   22                       OFFSET TO ATTRIBUTE      S19015
LENOFST  EQU   27                       OFFSET TO LENGTH OF TEXT S19015
ONLYLOAD EQU   X'CB'                    ONLY LOADABLE            S19015
MULTTEXT EQU   X'CA'                    MULTIPLE TEXT            S19015
UPDATEB  EQU   X'80'                    OPEN FOR UPDATE          S19015
FLGOFFST EQU   4                        OFFSET TO FLAG IN BCB    S19015
RAFLAG   EQU   X'80'                    RECORD AREA INDICATOR    S19015
OABD076  EQU   76                                                S21042
*
*
         BALR  RBASE,RE                 SET BASE
         USING SLDA,RBASE
         USING IHADCB,RDCB
         USING FORCORE,RCORE
*
*
SLDA     EQU   *
         B     BEGIN                    BR AROUND CONSTANTS    @Z30TSCF
         DC    C'IGG01915'              MODULE ID              @Z30TSCF
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'11/18/74'              LAST SHIP DATE         @Z30TSCF
BEGIN    DS    0H
*
         L     RDCB,0(RPARC)            GET DCB ADDRESS
         LA    RDCB,0(RDCB)             CLEAR HI BYTE
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
         USING DEBBASIC,RDEB
         L     RCORE,4(RWTGC)           GET WORK AREA
*
         L     RTIOT,DEBAPPAD           GET APPENDAGE ADDRESS
         USING DEBAVT,RTIOT
         SR    RUCB,RUCB                CLEAR REG
         SR    RF,RF                    CLEAR RER
         SR    RD,RD
         IC    RUCB,DEBNMEXT            GET NUMBER OF EXTENTS
         IC    RF,DEBEXSCL              GET SCALING FACTOR
         SLL   RUCB,0(RF)               SHIFT ACCORDING TO EXTENT SIZE
         IC    RF,DEBAMLNG              GET ACCESS METHOD LENGTH
         LA    RUCB,0(RF,RUCB)          ADD TO POINT TO SUBRT ID FLD
         LA    RUCB,32(RUCB,RDEB)       ADD BASIC DEB SZ TO DEB AD
*
*  RUCB NOW POINTS TO THE SUBROUTINE ID FIELD IN THE DEB
*
         TM    DCBOPTCD,UTFMASK         TEST FOR TOTALING          UT18
         BZ    SLDAA                    BR IF NOT SPECIFIED        UT18
         TM    0(RPARC),INPRDBK         IS OPEN FOR INPUT/RDBACK   UT18
         BZ    SLDAA                    IF YES,NO TOTALING REQ'RD  UT18
*
*  SINCE UTF WAS SPECIFIED, THE TOTALING SAVE ROUTINE (IGG019AX)
*  WAS LOADED BY IGG0191Y IN STAGE 1 - SO IT IS NECESSARY TO       UT18
*  UPDATE THE SUBROUTINE ID POINTER (RUCB) TO POINT TO THE         UT18
*  SECOND ID SUBFIELD.                                             UT18
*                                                                  UT18
         LA    RUCB,2(RUCB)             INCREMENT POINTER BY 2     UT18
         IC    RD,DEBNMSUB              NO. OF SUBRTNS LOADED SO A29249
*                                       FAR                      A29249
*                                                                  UT18
SLDAA    EQU   *                                                   UT18
         CLI   DCBDEVT,PTR3211          TEST FOR 3211 PRINTER    S20202
         BNE   SLDAB                    NO BRANCH AROUND         S20202
*
*   FOR A 3211 PRINTER LOAD AN ABNORMAL END APPENDAGE AND AN ERROR
*   RETRY MODULE.
*
         LA    RB,ABEAPP11              GET ADDR ABNORMAL END    S20202
*                                      APP ID                    S20202
         BAL   RC,SLDROUT              GO LOAD APP AND RETURN    S20202
         MVC   DEBXCEAD,25(RWTG)       MOVE APP ADDR INTO AVT    S20202
         LA    RB,ERRMOD                GET ADDR OF ERROR RETRY  S20202
*                                       MOD                      S20202
         BAL   RC,SLDROUT               GO LOAD ERROR RETRY MOD  S20202
*                                       3211                     S20202
*
*   WHEN RETURN FROM 'SLDROUT'- R0 CONTAINS THE ADDR OF THE
*   ASYNCHRONOUS ERROR ROUTINE. NOW BUILD AN IRB TO BE USED  IN
*   SCHEDULING THIS ROUTINE
         CIRB  EP=(0),KEY=PP,MODE=PP,STAB=(RE)
         ST    RF,K24(RWTG)             TEMP SAVE FOR IRB ADDR   S20202
         MVC   DEBIRBB,25(RWTG)   MOVE IRB ADDR INTO DEB         S20202
SLDAB    EQU   *                                                 M5150
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,NEXTMOD         ID OF NEXT MOD         @Z30TSCF
         TM    DCBCIND2,QSAMB           DCB FOR QSAM
         BC    1,SLD100                YES
*
         LA    RB,CHECK                 GET ADDRESS OF CHECK ID
         TM    DCBOFLGS,X'20'          CONCATENATING UNLIKE      A31594
*                                       ATTRIBUTES               A31594
         BC    8,SLDA0                  NO BRANCH
         BAL   RC,SLDROUT1              STORE THE ID OF CHECK IN DEB
         B     SLDA1                    DO NOT LOAD CHECK ROUTINE
*
SLDA0    BAL   RC,SLDROUT               LOAD CHECK ROUTINE
         MVC   DCBECC(R3),R25(RWTG)     LOAD CHECK ADDR INTO DCB
*
SLDA1    EQU   *
         TM    DCBBFTEK,BFTEKR          VS SUPPORT NEEDED        S19015
         BZ    SLDA3                    NO--BRANCH               S19015
         LA    RB,OFFSETRD              OFFSET READ ID           S19015
         B     SLDA4                    GO TO LOAD ROUTINE       S19015
*
*
SLDA3    EQU   *                                                 S19015
         LA    RB,WRH2                  GET RD/WRT ID ADDRESS
SLDA4    EQU   *                                                 S19015
         BAL   RC,SLDROUT               TO LOAD FUNCTION
         MVC   DCBREAD+1(R3),R25(RWTG)  LOAD R/W ADDR.
         B     SLDEOB                   GET SPECIAL ROUTINES
SLD100   EQU   *
         TM    DCBCIND2,UPDATEB         IS THIS UPDATE           S19015
         BO    SLD106                   YES, LOAD UPDATE ERR RTN S19015
         TM    DISPSN(PARAR),OUTP       DCB FOR OUTPUT
         BC    1,SLD200                 YES, BRANCH
*
         LA    RB,GERR                  GET ADDRESS OF GERR ID
         B     SLD107                   BRANCH TO TEST ATTRIBUTE S19015
*
SLD106   EQU   *                                                 S19015
*
         LA    RB,UERR                  GET ADDRESS OF UERR ID   S19015
*
SLD107   EQU   *                                                 S19015
*
         TM    DCBOFLGS,X'20'           CONTATENTION UNLIKE ATTRIBUTES/
         BC    8,SLD100A                NO, BRANCH
*
         BAL   RC,SLDROUT1
         B     SLD101                   TO CHECK NEXT PARAMETER
*
*
SLD150   EQU   *
         BAL   RC,SLDROUT               TO LOADING FUNCTION
*
SLD151   EQU   *                                                 S19015
         MVC   DCBGET+1(3),25(RWTG)     INSERT ADDRESS OF THE MODULE
         B     SLDEOB                   JUST LOADED INTO DCB FIELD
*
SLD100A  EQU   *
         BAL   RC,SLDROUT               TO OUTPUT
         MVC   DCBGERR+1(3),25(RWTG)    LOAD GERR ADDR. IN DCB
*
*
SLD101   EQU   *
         CLI   DCBLRECL,X'80'          LRECL=X
         BE    CHECKLOC                YES
SLD172   EQU   *
*
         TM    DCBRECFM,SPANNED
         BO    SLD173                   NOT SPANNED
         TM    DCBMACRF,MOVEXCHB        MOVE OR SUBST?         @Z30TSMI
         BNZ   SLD171                   YES                    @Z30TSMI
         LA    RB,GL                   IGG019AB
         B     SLD150                   DONE
CHECKLOC TM    DCBMACRF,LOCATEB        IS IT LOCATE MODE
         BO    SLD172                  YES
ABEND013 DMABCOND OABD076,PDLOAD,RETURN=NONE,RES=NO,           @ZA08271*
               REGSAVE=YES              CALL PROBLEM DETERM    @ZA08271
         B     TCTLRTN                                           S21042
SLD171   EQU   *
         LA    RB,GM                   IGG019AD
         B     SLD150                   DONE
SLD173   EQU   *
         TM    DCBCIND2,UPDATEB         IS THIS UPDATE           S19015
         BO    SLD170                   YES, BRANCH              S19015
         TM    DCBMACRF,DATAMODE        DCB USING DATA MODE
         BO    SLD176                   NOT DATA MODE
         TM    DCBMACRF,MOVEXCHB        MOVE OR SUBST?         @Z30TSMI
         BNZ   SLD177                   YES                    @Z30TSMI
         L     RC,DCBBUFCB              GET BUFFCB ADDRESS     @ZA08271
         LA    RC,DISPSN(RC)            CLEAR HI ORDER BYTE    @ZA08271
         MODESET KEYADDR=DXUKEY,WORKREG=15 GET USER KEY        @ZA08271
         TM    FLGOFFST(RC),RAFLAG      RECORD AREA PRESENT?   @ZA08271
         MODESET EXTKEY=DATAMGT                                @ZA08271
         BO    SLD169                   YES, BRANCH              S19015
         LA    RB,GLS                  IGG019FB
         B     SLD150                   ALL DONE
*
SLD169   EQU   *                                                 S19015
         TM    DCBLRECL,X'80'           LRECL=X                @ZA08271
         BO    ABEND013                 YES-GO ABEND           @ZA08271
         LA    RB,GLSLR                 GET SPECIAL GET MODULE   S19015
         B     SLD150                   ALL DONE                 S19015
SLD170   EQU   *                                                 S19015
         LA    RB,UGET                  GET UPDATE GET MODULE    S19015
         B     SLD150                   LOAD                     S19015
SLD176   EQU   *
         LA    RB,GDS                  IGG019FF
         B     SLD150                   ALL DONE
SLD177   EQU   *
         LA    RB,GMS                  IGG019FD
         B     SLD150                   ALL DNOE
SLD200   LA    RB,PERR                  GET THE ADDR. OF PERR ID INTO
         BAL   RC,SLDROUT               LOAD SUBROUTINE
         MVC   DCBPERR+1(3),25(RWTG)    INSERT PERR ADDR. INTO DCB
*
         TM    DCBRECFM,SPANNED         SPANNED
         BO    SLD175                   NOT SPANNED
         TM    DCBMACRF+1,MOVEXCHB      MOVE OR SUBST?         @Z30TSMI
         BNZ   SLD174                   YES, BRANCH            @Z30TSMI
         LA    RB,PL                   IGG019AJ
         B     SLD150                   GO TO NEXT CHECKING
SLD174   EQU   *
         LA    RB,PM                   IGG019AL
         B     SLD150                   GO TO NEXT CHECKING
SLD175   EQU   *
         TM    DCBMACRF+1,DATAMODE
         BO    SLD178                  SKIP
         TM    DCBMACRF+1,MOVEXCHB      MOVE OR SUBST?         @Z30TSMI
         BNZ   SLD179                   YES                    @Z30TSMI
         L     RB,DCBBUFCB             GET BUFFCB ADDRESS      @ZA08271
         LA    RB,DISPSN(RB)           CLEAR HI ORDER BYTE     @ZA08271
         MODESET KEYADDR=DXUKEY,WORKREG=15  GET USER KEY       @ZA08271
         TM    FLGOFFST(RB),RAFLAG     LRI PRESENT?            @ZA08271
         MODESET EXTKEY=DATAMGT                                @ZA08271
         BO    SLD180                   YES, BRANCH LD BP        S19015
         LA    RB,PLS                  IGG019FJ
         B     SLD150                   GO TO NEXT CHECKING
SLD180   EQU   *                                                 S19015
         LA    RB,PLSLR                 GET SPECIAL PUT MODULE   S19015
         B     SLD150                   TO NEXT ONE              S19015
SLD179   EQU   *
         LA    RB,PMS                  IGG019FL
         B     SLD150                   TO NEXT ONE
SLD178   EQU   *
         LA    RB,PDS                  IGG019FG
         B     SLD150                   TO NEXT ONE
SLDEOB   EQU   *
         STC   RD,DEBNMSUB              STORE NBR OF MODULES       UT18
*
*    THIS SECTION OF CODE UP TO THE LABEL ENDOVER WILL ENSURE THAT
*    THE EOB ROUTINE IGG019CT GETS LOADED FOR ANY OPEN OVERRIDE.
*    ROUTINE LOADED BY IGG01991.
*
         TM    JFCBMASK+D6,OVRDMSK      WAS AN OVERRIDE USED     S21040
         BNO   ENDOVR                   BR IF NO                 S21040
         TM    0(RPARC),OVRDIN          WAS IT LABEL=(,,,IN)     S21040
         BZ    INCHNG                   BRANCH IF YES            S21040
         MVC   DCBCNTRL+D2(L1),DCBCNTRL+D3 SAVE WRITE EOB DISP   S21040
         MVI   DCBCNTRL+D3,CTDISP       SET READ EOB ROUTINE FOR S21040
*                                       THE ERROR EOB ROUTINE
         B     ENDOVR                   CONTINUE                 S21040
INCHNG   EQU   *                                                 S21040
         MVI   DCBCNTRL+D2,CTDISP       SET WRITE EOB ROUTINE    S21040
*                                        FOR ERROR EOB ROUTINE
ENDOVR   EQU   *                                                 S21040
*
*    IF USER TOTALING WAS SPECIFIED, EOB ROUTINES CONTAINING
*    LINKAGE TO THE TOTALING SAVE ROUTINES MUST BE LOADED INSTEAD
*    OF NORMAL EOB ROUTINES.  THIS IS ACCOMPLISHED BY DOING A
*    TRANSLATE OF THE DCBCNTRL FIELD TO CONVERT NORMAL EOB
*    DISPLACEMENTS INTO DISPLACEMENTS FOR THE UTF EOB ROUTINES.
*    EOB ROUTINES ARE LOADED BY IGG01991.
*
         TM    DCBOPTCD,UTFMASK         TEST FOR TOTALING OPTION S21040
         BZ    SLD250                   BR IF NOT SPECIFIED      S21040
         TM    0(RPARC),INPRDBK         IS OPEN FOR INPUT/RDBACK S21040
         BZ    SLD250                   BR YES NO TOTALING REQ'D S21040
         TR    DCBCNTRL(L4),UTFTBL      CHANGE TO UTF DISP.      S21040
SLD250   EQU   *                                                 S21040
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURR WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCR CURRENT DCB ENTRY PRTR
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   8,RBASE                  BACK TO OPEN CONTROL
*
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BC    7,RELOOP                 NO,CHECK NEXT ENTRY
*
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      REINITIALIZE WTG LIST PTR
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN                IF NOT TRANSFER CONTROL
*
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHEK                    COMPLETE
*
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
*
*
         DS    0H                       HALF WORD BOUNDARY
SLDROUT  EQU   *
         USING WTGMODNM,RWTG                                     Y02072
         MVC   WTGMODID,0(RB)           ID TO MOD NAME           Y02072
         DROP  RWTG                                              Y02072
         L     R1,CVTPTR                LOAD CVT ADDRESS         YM1140
         USING CVT,R1                   CVT ADDRESSABILITY       YM1140
         L     R1,CVTLINK               GET LPALIB DCB ADDRESS   YM1140
         DROP  R1                       DROP CVT BASE            YM1140
*
         LOAD  EPLOC=(RWTG),DCB=(1)                              YM1140
*
*
*
         ST    RE,24(RWTG)             TEMP STORE OF ROUTINE ADDR,
SLDROUT1 MVC   0(2,RUCB),0(RB)          STORE ROUTINES ID IN DEB
         LA    RUCB,2(RUCB)             UPDATE SUBRT PNTR
         LA    RD,1(RD)                 ONE MORE SUB RT LOADED
         BR    RC                      RETURN
*
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES
OPIDCNST DC    C'0S'                    ID
AMIDCNST DC    C'15'                    MODULE ID
*
*
*    THE FOLLOWING TABLE IS USED WITH A TRANSLATE INSTRUCTION TO
*    CONVERT DISPLACEMENTS FOR THE EOB TABLE INTO THOSE FOR THE
*    ENTRIES FOR THE CORRESPONDING UTF ROUTINES.
*
UTFTBL   EQU   *                                                 S21040
         DC    X'00'                    NO CHANGE                S21040
         DC    X'0B'                    IGG019CD TO IGG019TD     S21040
         DC    X'0C'                    IFG019CC TO IGG019TC     S21040
         DC    X'03'                    NO CHANGE                S21040
         DC    X'04'                    NO CHANGE                S21040
         DC    X'05'                    NO CHANGE                S21040
         DC    X'06'                    NO CHANGE                S21040
         DC    X'07'                    NO CHANGE                S21040
         DC    X'08'                    NO CHANGE                S21040
         DC    X'09'                    NO CHANGE                S21040
         DC    X'0A'                    NO CHANGE                S21040
         DC    X'0B'                    NO CHANGE                S21040
         DC    X'0C'                    NO CHANGE                S21040
         DC    X'0D'                    NO CHANGE                S21040
         DC    X'0E'                    NO CHANGE                S21040
         DC    X'0F'                    NO CHANGE                S21040
         DC    X'10'                    NO CHANGE                S21040
*
*    NOTE, THERE MUST BE AN ENTRY IN THIS TRANSLATE TABLE FOR
*    EACH ENTRY IN THE EOB TABLE.
*
*
*
***********************************************************************
*
*         MODULE IDS LOADED BY THIS MODULE
*
NEXTMOD  DC    C'91',VL3(IGG01991)      ID OF MODULE IGG01991  @Z30TSCF
PERR     DC    C'AR'                    ID OF MODULE IGG019AR    Y02072
GERR     DC    C'AQ'                    ID OF MODULE IGG019AQ    Y02072
UERR     DC    C'BQ'                    ID OF MODULE IGG019BQ    Y02072
UGET     DC    C'BN'                    ID OF MODULE IGG019BN    Y02072
GL       DC    C'AB'                    ID OF MODULE IGG019AB    Y02072
GM       DC    C'AD'                    ID OF MODULE IGG019AD    Y02072
PL       DC    C'AJ'                    ID OF MODULE IGG019AJ    Y02072
PM       DC    C'AL'                    ID OF MODULE IGG019AL    Y02072
GLSLR    DC    C'BO'                    ID OF MODULE IGG019BO    Y02072
GLS      DC    C'FB'                    ID OF MODULE IGG019FB    Y02072
GMS      DC    C'FD'                    ID OF MODULE IGG019FD    Y02072
GDS      DC    C'FF'                    ID OF MODULE IGG019FF    Y02072
PLSLR    DC    C'BP'                    ID OF MODULE IGG019BP    Y02072
PLS      DC    C'FJ'                    ID OF MODULE IGG019FJ    Y02072
PMS      DC    C'FL'                    ID OF MODULE IGG019FL    Y02072
PDS      DC    C'FG'                    ID OF MODULE IGG019FG    Y02072
WRH2     DC    C'BA'                    ID OF MODULE IGG019BA    Y02072
OFFSETRD DC    C'BU'                    ID OF MODULE IGG019BU    Y02072
CHECK    DC    C'BB'                    ID OF MODULE IGG019BB    Y02072
ABEAPP11 DC    C'FR'                    ID OF MODULE IGG019FR    Y02072
ERRMOD   DC    C'FS'                    ID OF MODULE IGG019FS    Y02072
PDLOAD   DC    C'6M',VL3(IGG0196M)      ID OF MODULE IGG0196M  @Z30TSCF
*
PATCH    DC    0H'0',C'IGG01915',XL42'00'   PATCH AREA           Y02072
 TITLE 'IGG01915 - FORMAT OF GOTTEN CORE'
         IECDSECS MAIN,WTG,PREFX,EXPAND=YES                    @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MOD ID              Y02072
*
*
CVT      DSECT
         CVT
*
*
         IEZDEB
*
           DCBD  DSORG=PS
DCBECC   EQU   DCBCHECK+1               DCB FIELD
         END
