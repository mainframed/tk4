  TITLE  'IGG0196L - OPEN EXECUTOR - STAGE 2 - FINISH IOB''S FROM 191J'
IGG0196L CSECT
*MODULE NAME - IGG0196L                                          Y02072
*
*DESCRIPTIVE NAME - BUILD IOBS                                   Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          VS2 RELEASE 2 DELETIONS                               Y02072
*050430,050490,050550,050820-050940,078880,058600,059000,062800, Y02072
*063800                                                          Y02072
*D51150-51364                                                    YA3136
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*          RELEASE 21 DELETIONS                                       *
*0000058400                                                      M0466
*0000084800-085800                                               M0091
*
*   STATUS CHANGE LEVEL 001
*
*  THIS MODULE CREATED IN RELEASE 21 BY SPLIT OF IGG0191J
*
*FUNCTION/OPERATION: THIS ROUTINE CONTINUES CONSTRUCTING THE IOB'S
*      'GETMAINED' IN IGG0191J.
*
*ENTRY POINTS: ENTERED FROM IGG0191J BY USE OF XCTL MACRO.
*
*INPUT: SEE DESCRIPTION OF REGISTERS, DCB(USERS).
*
*OUTPUT: SEE DESCRIPTION OF REGISTERS, DCB(USERS), IOB(USERS).
*
*EXTERNAL ROUTINES : NONE
*
*EXITS-NORMAL: XCTL TO IGG01910 OR IGG01915 TO LOAD ROUTINES
*
*EXITS-ERROR: NONE
*
*MACROS : ACTION - MODESET, XCTL, XCTLTABL                       Y02072
*
*TABLES/WORKAREAS: DUPE WTG TABLE**************************************
*      SEE DEVICETB DSECT.
*
*ATTRIBUTES: REENTRANT, REUSABLE, RUNS IN USER KEY UNLESS        Y02072
*            OTHERWISE SPECIFIED, SUPER STATE                    Y02072
*
* NOTES- THIS EXECUTOR DEALS WITH DIRECT ACCESS FOR IN/OUT - OUT/IN.
*
         SPACE 1
*
*                                                                     *
*    2841  OP  CODES                                                  *
*                                                                     *
WRCNKEDA EQU   X'1D'                   WRITE COUNT KEY AND DATA
TIC      EQU   X'08'          TIC OP CODE
WRTCK    EQU   X'80'
SRCHE    EQU   X'31'          SEARCH OP CODE
READKEDA EQU   X'8E'                    READ KEY AND DATA OP CODE
RDCOUNT  EQU   X'92'                    READ COUNT CMND CODE     Y02072
SETSC    EQU   X'23'                    SET SECTOR COMMAND       S20201
RDSC     EQU   X'22'                    READ SECTOR COMMAND      S20201
*                                                                     *
***********************************************************************
*                                                                     *
*    CCW  CONSTRUCTION  EQUATES                                       *
*                                                                     *
DATASILI EQU   X'A0'                   DATA CHAIN AND SILI FLAG
SILI     EQU   X'20'                   SILI FLAG
COMSILI  EQU   X'60'         COMMAND AND SILI
COMCHAIN EQU   X'40'          COMMAND CHAIN BIT
DATACHAN EQU   X'80'          DATA  CHAIN  BIT
SKIPSILI EQU   X'30'                   SKIP AND SILI
SECNDTIC EQU   0                        OFFSET TO SECOND TIC CCW Y02072
ONE      EQU   1
FIVE     EQU   5
RDKDCCW  EQU   8                        OFFSET TO READ DATA CCW  Y02072
ATE8     EQU   8
RDCNTCCW EQU   16                       OFFSET TO READ COUNT CCW Y02072
CNTFLD2  EQU   32                       OFFSET TO SECOND COUNT   Y02072
*                                       FIELD FOR SEARCH DIRECT  Y02072
CC       EQU   X'40'                    COMMAND CHAIN            S20201
***********************************************************************
*                                                                     *
*    MISC  EQUATES                                                    *
*                                                                     *
FORMATV  EQU   X'40'                    FIXED RECORD FORMAT        DM0I
FIRSTIOB EQU   1
NONCARN  EQU   X'20'                    NO DISCONNECT USED       S20201
SDOPT    EQU   X'08'                    SEARCH DIRECT OPTION     S20201
*                                       BYTE                     S20201
SDTIC    EQU   X'40'                    SEARCH DIR TIC PREFIX    S20201
RPSOFSET EQU   6                       OFFSET IN JFCB FOR RPS BIT
RPSTIC   EQU   8                       TO BE ADDED TO IOB POINTER FOR
*                                         RPS DEVICES BECAUSE OF X'23'
*                                         OP CODE
NTRPSTIC EQU   X'58'                   OFF SET TO NON-RPS TIC
UNRELFLG EQU   X'02'
***********************************************************************
*
* DISPLACEMENTS AS EQUATES
*
D0       EQU   0                        DISPLACEMENT OF ZERO     S20201
D1       EQU   1                        DISPLACEMENT OF ONE      S20201
D4       EQU   4                        DISPLACEMENT OF FULLWORD S20201
D5       EQU   5                        DISPLACEMENT OF FIVE     S20201
D6       EQU   6                        DISPLACEMENT OF SIX      S20201
D7       EQU   7                        DISPLACEMENT OF SEVEN    S20201
D8       EQU   8                        DISPLACEMENT OF DW       S20201
D12      EQU   12                       DISPLACEMENT OF THREE    S20201
*                                       WORDS                    S20201
D16      EQU   16                       DISPLACEMENT OF 16       S20201
D23      EQU   23                       DISPLACEMENT OF 23       S20201
D24      EQU   24                       DISPLACEMENT OF SIX      S20201
*                                       WORDS                    S20201
D43      EQU   43                       DISPLACEMENT OF CCHHR    S20201
*                                       IOB                      S20201
FLAGOFF  EQU   4                        CCW FLAG OFFSET          S20201
CNTOFF   EQU   7                        CCW COUNT OFFSET         S20201
***********************************************************************
***********************************************************************
*
*
* REGISTER CONVENTIONS USED THROUGHOUT ALL OPEN PASSES
*
*
***********************************************************************
*
RE       EQU   0
RF       EQU   1                        WORK REG ONE             S20201
RDCB     EQU   2                        DCB REGISTER             S20201
RBASE    EQU   3                        BASE REGISTER            S20201
RCORE    EQU   4                        WORK AREA ADDR           S20201
RWTG     EQU   6
RPARC    EQU   7                        CURRENT PARAMETER        S20201
*
RWTGC    EQU   8
RUCB     EQU   10                       ADDRESS OF UCB           S20201
RDEB     EQU   11                       ADDRESS OF DEB           S20201
RB       EQU   12 2
RC       EQU   13 3
RD       EQU   14 4
RJ       EQU   15 5
*
*
***********************************************************************
*
***********************************************************************
*
*
         USING SOP810,RBASE                                      S20201
         USING IHADCB,RDCB              REG 2 IS DCB             S20201
         USING FORCORE,RCORE            REG 4 IS WORK AREA ADDR  S20201
         BALR  RBASE,RE                 REG 3 IS BASE   RE=0     S20201
*
SOP810   EQU   *                                                 S20201
*
         B     BEGIN                                             Y02072
         DC    C'IGG0196L'              MOD NAME                 Y02072
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'10/22/74'              LAST DATE MODIFIED     @Z30TSCF
BEGIN    DS    0H
*
         MODESET  KEYADDR=DXUKEY,WORKREG=9 GET INTO USER KEY     Y02072
         L     RDCB,D0(RPARC)           GET DCB ADDR             S20201
         LA    RDCB,D0(RDCB)            CLEAR HI BYTE            S20201
         L     RCORE,D4(RWTGC)          GET WORK AREA ADDR       S20201
*
         SR    RUCB,RUCB                CLEAR REG                S20201
         IC    RUCB,DCBNCP              GET NO. OF CHPS          S20201
         LTR   RUCB,RUCB                BEEN SPECIFIED           S20201
         BNZ   SOP817                   BRANCH IF SPECIFIED      S20201
         LA    RUCB,ONE                 FURNISH ONE IF NOT       S20201
SOP817   EQU   *                                                 S20201
         SR    RE,RE                   GET IOB LENGTH  --  STORED BY
         IC    RE,DCBIOBL                  IGG0191J
         SLL   RE,3(0)                 IOB LNG IN BYTES
         LA    RDEB,ATE8               CONSTANT OF EIGHT
*   THE ADDRESS OF THE FIRST IOB IS IN THE DCBIOBA FIELD.  IT WAS
*   STORED THERE AFTER GETMAIN IN IGG0191J.
         L     RB,DCBIOBA              ADDR OF FIRST IOB
SOP829   EQU   *
         LR    RF,RB                   SAVE IOB ADDR
*    MUST ADD TO FIRST IOB ADDRESS TO POSITION AT FIRST TIC CCW --
*    THIS IS WHERE WE WERE IN IGG0191J --
*       X'60' FOR RPS DEVICES  (3330,  2305)  --
*       X'58' FOR NON RPS DEVICES --
         LA    RB,NTRPSTIC(RB)         GET TO FIRST TIC
         TM    JFCBMASK+RPSOFSET,NONCARN   DEVICE NOT RPS?
         BO    NONRPS                  YES, NOT RPS DEVICE
         LA    RB,RPSTIC(RB)            NO, RPS DEVICE USE X'60'
NONRPS   EQU   *
         TM    JFCBMASK+D6,SDOPT        SEARCH DIRECT SPECIFIED  S20201
         BZ    SOP833A                  BRANCH IF NO             S20201
*                                       SD                       S20201
         SH    RB,CON32                 BACK UP TO FIRST TIC     S20201
         OI    D0(RB),SDTIC             INDICATE A SCH DIR C. P. S20201
         AR    RB,RDEB                  INCREMENT                S20201
         LA    RC,RDKDCCW(,RB)          POINT TO READ DATA CCW   Y02072
         ST    RC,SECNDTIC(,RB)         SET UP TIC ADDRESS       Y02072
         MVI   SECNDTIC(RB),TIC         MOVE IN TIC COMMAND      Y02072
         LA    RC,CNTFLD2(,RB)          PT TO SECOND COUNT FIELD Y02072
         TM    JFCBMASK+6,JFCNRPS       IF RPS IS NOT SPECIFIED  Y02072
         BO    NOTRPS                   THEN DON'T INCREMENT     Y02072
         AR    RC,RDEB                  ELSE INCR BEYOND THE RD  Y02072
*                                       SECTOR CCW               Y02072
NOTRPS   EQU   *                                                 Y02072
         C     RF,DCBIOBA               IS THIS THE FIRST IOB    Y02072
         BNE   NOTFIRST                 BRANCH IF NOT            Y02072
         ST    RC,SECNDTIC(,RB)         SET UP RD CNT DATA ADDR  Y02072
         MVI   SECNDTIC(RB),RDCOUNT     MOVE IN READ COUNT CMND  Y02072
*
NOTFIRST EQU   *                                                 Y02072
         MVI   D4(RB),COMSILI           SILI AND CMND CHAIN      S20201
         MVI   D7(RB),ATE8              BYTE CNT EQS EIGHT       S20201
*                                       SD                       S20201
         MVC   D8(ATE8,RB),D16(RB)      MOVE UP RD DATA CCW      S20201
*                                       SD                       S20201
         OI    D12(RB),COMSILI          SET COMMAND CHAINING     S20201
*                                       &SLI                     S20201
         SR    RC,RDEB                  PT TO FIRST COUNT FIELD  Y02072
         ST    RC,D16(RB)               PUT IN CCW ADDR          S20201
*                                       SD                       S20201
         MVI   RDCNTCCW(RB),RDCOUNT     PUT IN MT RD CNT CMND    Y02072
         MVI   D23(RB),ATE8             SET BYTE CNT TO EIGHT    S20201
*                                       SD                       S20201
         AH    RB,CON24                 RESTORE POINTER          S20201
SOP833A  EQU   *                                                 S20201
*                                       SD                       S20201
*
* UPON RETURN CHECK FOR DISCONNECT FEATURE
*
         TM    JFCBMASK+D6,NONCARN      ANY DEVICES W/O          S20201
*                                       DISCONNECT               S20201
         BO    SOP835                   BRANCH IF YES            S20201
         BAL   RD,SOP8RTA               OTHERWISE GO BUILD RD    S20201
*                                       THETA                    S20201
         AR    RB,RDEB                  INCREMENT POINTER        S20201
SOP835   EQU   *                                                 S20201
*
* SET UP FOR WRITE CHP
*
         TM    JFCBMASK+D6,SDOPT        FOR SEARCH DIRECT, IT IS S20201
         BNO   SOP835A                  IS NECESSARY TO HAVE TWO S20201
         AR    RB,RDEB                  DOUBLEWORDS BETWEEN THE  S20201
         AR    RB,RDEB                  INPUT C.P. AND THE       S20201
*                                       OUTPUT                   S20201
*                                        CHANNEL PROGRAM
SOP835A  EQU   *                                                 S20201
         SR    RB,RF                    GET WRITE CHP OFFSET     S20201
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         STC   RB,DCBWCPO               PUT IN DCB               S20201
         MODESET  KEYADDR=DXUKEY,WORKREG=9 GET INTO USER KEY     Y02072
         AR    RB,RF                    RESTORE PTR              S20201
         TM    JFCBMASK+D6,NONCARN      ANY DEVICES W/O RCD      S20201
*                                       READY                    S20201
         BO    SOP836                   BRANCH IF YES            S20201
         BAL   RD,SOP8STA               GO BUILD SET SECTOR CCW  S20201
SOP836   EQU   *                                                 S20201
         LA    RC,D43(RF)               PT TO CCHHR FIELD        S20201
         BAL   RD,SOP8SRTC              GO BUILD SEARCH AND TIC  S20201
*
* UPON RETURN INCREMENT PTR, OBTAIN WRITE CCW OFFSET
*
         AR    RB,RDEB                  INCREMENT PTR            S20201
         AR    RB,RDEB                                           S20201
*
* CONSTRUCT REMAINING WRITE CHANNEL PROGRAM
*
         LR    RC,RF                    GET IOB PTR              S20201
         AR    RC,RE                    INCREMENT TO NEXT IOB    S20201
*                                       DM                       S20201
         SR    RC,RDEB                  BACK UP ONE DW           S20201
         TM    JFCBMASK+D6,NONCARN      ANY DEVICES W/O          S20201
*                                       DISCONNECT               S20201
         BO    SOP837                   BRANCH IF YES            S20201
         SR    RC,RDEB                  BACK UP POINTER ONE MORE S20201
*                                       DW                       S20201
SOP837   EQU   *                                                 S20201
         MVI   CNTOFF(RB),ATE8          SET BYTE CNT TO 8        S20201
         OI    FLAGOFF(RB),DATASILI     DATA CHAIN AND SILI      S20201
         ST    RC,D0(RB)                PUT IN ADDR              S20201
         MVI   D0(RB),WRCNKEDA          WRITE CNT, KEY, DATA     S20201
*                                       CMND                     S20201
         AR    RB,RDEB                  INCREMENT POINTER        S20201
         SR    RB,RF                    OBTAIN WRITE CCW OFF SET S20201
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         STC   RB,DCBOFFSW              PUT IN DCB               S20201
         MODESET  KEYADDR=DXUKEY,WORKREG=9 GET INTO USER KEY     Y02072
         AR    RB,RF                    RESTORE PTR              S20201
         OI    FLAGOFF(RB),SILI         SET SILI FLAG            S20201
         AR    RB,RDEB                  INCREMENT POINTER        S20201
*
* CHECK FOR WRITE CHK SPECIFIED AND BUILD COMMAND
*
         TM    DCBOPTCD,WRTCK           WRITE CHK USED           S20201
         BNO   SOP874                   IF NOT BRANCH            S20201
         SR    RB,RDEB                  BACK UP PTR TO PRE CCW   S20201
         OI    FLAGOFF(RB),CC           CMND CHAIN               S20201
         AR    RB,RDEB                  RESTORE PTR              S20201
         TM    JFCBMASK+D6,NONCARN      ANY DEVICES W/O          S20201
*                                       DISCONNECT               S20201
         BO    SOP851                   BRANCH IF YES            S20201
         BAL   RD,SOP8RTA               GO BUILD READ SECTOR     S20201
         OI    FLAGOFF(RB),CC           COMMAND CHAIN CCW        S20201
         AR    RB,RDEB                  INCR POINTER             S20201
         BAL   RD,SOP8STA               OTHERWISE GO BUILD SET   S20201
*                                       THET                     S20201
*
* MUST OBTAIN SEARCH ADDR FOR SEARCH SUBROUTINE
*
         LR    RC,RF                    GET ICB POINTER          S20201
         AR    RC,RE                    POINT TO NEXT IOB        S20201
         SR    RC,RDEB                  BACKUP A DW              S20201
         SR    RC,RDEB                  SINCE RCD READY ONE MORE S20201
*                                       DW                       S20201
SOP851   EQU   *                                                 S20201
*
* GO BUILD  SEARCH AND TIC COMMAND FOR WRT CHK
*
         BAL   RD,SOP8SRTC              CONSTRUCT SEARCH AND TIC
         MVI   16(RB),READKEDA          READ KEY AND DATA CCW
         MVI   20(RB),SKIPSILI          SKIP AND SILI
         LA    RB,24(0,RB)              POINT TO NEXT CCW
*******
         B     SOP877                   GO FINISH IO             S20201
SOP874   EQU   *                                                 S20201
*
         TM    JFCBMASK+D6,NONCARN      ANY DEVICES W/O RCD      S20201
*                                       READY                    S20201
         BO    SOP877                   BRANCH IF YES            S20201
         BAL   RD,SOP8RTA               GO BUILD READ SECTOR     S20201
         AR    RB,RDEB                  INCR PTR                 S20201
*
SOP877   EQU   *                                                 S20201
         MVC   D5(D1,RB),DCBKEYL        MOVE KEY LEN TO          S20201
*                                       CCHHRDDLL                S20201
*
*   HAVE ALL IOBS BEEN CONSTRUCTED
*
         AR    RB,RDEB                  INCR PTR FOR SRCH ARGU   S20201
         TM    JFCBMASK+D6,NONCARN      ANY DEVICES W/O RCD      S20201
*                                       READY                    S20201
         BO    SOP888                   BRANCH IF YES            S20201
         AR    RB,RDEB                  INCR PTR FOR ADD SRCH    S20201
*                                       ARGU                     S20201
SOP888   EQU   *                                                 S20201
         BCT   RUCB,SOP829              IF ALL IOBS NOT FINISHED S20201
*                                       BR                       S20201
         L     RC,DCBIOBA               IF FINISHED GET PTR TO   S20201
*                                       FRST                     S20201
         ST    RC,D0(RF)                LINK LAST TO FIRST       S20201
*
         L     RB,DCBIOBA               GET IOB ADDRESS
         OI    0(RB),FIRSTIOB           TURN ON FIRST IOB BIT
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         TM    DCBRECFM,FORMATV         FIXED RECORD FORMAT        DM0I
         BC    8,SOP8ENDD               BRANCH, IF FIXED FORMAT    DM0I
         TM    DCBRECFM,DATACHAN        UNDEFINED RECORD FORMAT    DM0I
         BO    SOP8ENDD                 BRANCH IF UNDEFINED      M0466
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,SOPSLDVT        VAR, GET 1915          @Z30TSCF
         B     NEXT                     BACK TO LOADING            DM0I
SOP8ENDD MVC   WTGIDTTR,SOPSLDA         GET SLDA               @Z30TSCF
NEXT     EQU   *                        VARIABLE FORMAT, COME TO HEDM0I
         LR    RSTART,RBASE             RESTORE BASE REG         S20201
*
RELOOP   LA    RCWG,WGOFF(0,RCWG)      INCREMENT CURR WTG ENTRY
         LA    RCPL,PLOFF(0,RCPL)      INCR CURRENT DCB ENTRY POINTER
         CLC   0(2,RCWG),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   EGHT,RSTART              BACK TO OPEN AGAIN         DM0I
         CLC   0(2,RCWG),OPIDCNST      END OF WTG TABLE
         BC    7,RELOOP                NO,CHECK NEXT ENTRY
         LR    RCPL,RPL
         LA    RCWG,WAOFF(0,RWG)       REINITIALIZE WTG LIST PTR
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN               IF NOT TRANSFER CONTROL
         LA    RCWG,WGOFF(0,RCWG)      GET NEXT ENTRY
         LA    RCPL,PLOFF(0,RCPL)
         B     ZCHEK                    ZERO CHECK                 DM0I
**
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
**
RPL      EQU   5
RWG      EQU   6
RCPL     EQU   7
RCWG     EQU   8
RSTART   EQU   11
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES      DM0I
WGOFF    EQU   8                       OFFSET OF WTG ENTRIES
*
*  MOD ID'S
*
OPIDCNST DC    C'0S'                    ID                         DM0I
AMIDCNST DC    C'6L'                   ID THIS MODULE
SOPSLDA  DC    C'10',VL3(IGG01910)      FIXED, UNDEFINED       @Z30TSCF
SOPSLDVT DC    C'15',VL3(IGG01915)      VAR RECS               @Z30TSCF
*
*
***********************************************************************
*
*    THIS SECTION OF CODE BUILDS A SEARCH AND TIC CCW                 *
*
*
*    RC POINTS TO SEARCH ADDRESS-- RB POINTS TO NEXT CCW ON ENTRY     *
*   RB IS NOT CHANGED IN ROUTINE.-- RD  IS  RETURN--                  *
*                                                                     *
***********************************************************************
SOP8SRTC ST    RC,0(0,RB)              SEARCH ADDRESS
         MVI   0(RB),SRCHE             SEARCH OP CODE
         MVI   7(RB),FIVE              COUNT
         MVI   4(RB),COMCHAIN          COMMAND CHAIN
*
*    TIC
*
         LA    RC,5(0,RC)              SEARCH ADDRESS
         ST    RB,8(0,RB)              TIC TO SEARCH
         MVI   8(RB),TIC               TIC OP CODE
         BR    RD                       EXIT                       DM0I
***********************************************************************
*
* THIS RTN BUILDS SET THETA COMMANDS, RB POINTS TO CCW
*
SOP8STA  EQU   *                                                 S20201
         L     RC,DXCCW11               GET SET THETA ADDR       S20201
         ST    RC,D0(RB)                PUT IN CCW               S20201
         OI    FLAGOFF(RB),CC           TURN ON COMMAND CHAIN    S20201
*                                       FLAG                     S20201
         MVI   CNTOFF(RB),ONE           SET COUNT TO ONE         S20201
         MVI   D0(RB),SETSC             PUT IN COMMAND           S20201
         AR    RB,RDEB                  INCREMENT PTR            S20201
         BR    RD                       RETURN                   S20201
***********************************************************************
*
* THIS RTN BUILDS READ THETA COMMANDS, RB POINTS TO CCW
SOP8RTA  EQU   *                                                 S20201
         SR    RB,RDEB                  BACK UP CCW PTR          S20201
         OI    FLAGOFF(RB),COMSILI      SET CC AND SIL IN PREV   S20201
*                                       CCW                      S20201
         AR    RB,RDEB                  RESTORE PTR              S20201
         L     RC,DXCCW11               GET RD THETA ADDR        Y02072
         ST    RC,D0(RB)                PUT IN CCW               S20201
         MVI   CNTOFF(RB),ONE           SET BYTE COUNT TO ONE    S20201
         MVI   D0(RB),RDSC              PUT IN COMMAND           S20201
         BR    RD                       RETURN                   S20201
*
*******************************************************************
***
CON24    DC    H'24'                    24 BYTE DISPLACEMENT     S20201
CON32    DC    H'32'                    32 BYTE DISPLACEMENT     S20201
EGHT     EQU   8                                                   DM0I
PATCH    DC    0H'0',50X'00'            PATCH AREA               Y02072
***
***********************************************************************
*****
         DCBD  DSORG=PS
         IECDSECS  MAIN,WTG,PREFX,EXPAND=YES                   @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MODS ID             Y02072
         EJECT
CVT      DSECT                                                 @Z30TSCF
         CVT                                                   @Z30TSCF
DCBKEYL  EQU   DCBKEYLE
*
*
*
*    DEB
*
DEBDEF   DSECT
         DS    9F                       SPACE RESERVED             DM0I
DEBSTRBB DS    H                        START BIN NO.              DM0I
DEBSTRCC DS    CL4                      START CYCL.                DM0I
*
PARMDEF  DSECT
PARMDEF1 DS    3F                       PARAMETER                  DM0I
*
*
         SPACE  2
         SPACE 3
DEVICETB DSECT
DEVCONST DC    X'04000000'              DEVICE CONSTANT            DM0I
DEVCYLST DC    X'00'                    LIST                       DM0I
DEVCYLEN DC    X'CA'                    END CYLINDER
DEVTRKST DC    X'00'                    START TRACK
DEVTRKEN DC    X'09'                    TRACK END
DEVTKLEN DC    FL2'3649'                TRACK LENGTH - R0
DEVOVHI  DC    FL1'82'                  NORMAL OVERHEAD
DEVOVHL  DC    FL1'55'                  LAST RECORD OVERHEAD
DEVOVHK  DC    FL1'20'                 KEY ID OVERHEAD
DEVFLAG  DC    FL1'1'                   FLAG
DEVTOLR  DC    FL2'269'                 TOLERANCE
         SPACE 3
         END
