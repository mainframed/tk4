     TITLE 'IGG0201Y - SAM/PAM CLOSE EXECUTOR, DIRECT ACCESS - LOAD 2'
IGG0201Y CSECT
*
*MODULE NAME - IGG0201Y
*
*DESCRIPTIVE NAME - RESTORE RESOURCES AND CLEAN UP FOR DA
*
*STATUS CHANGE LEVEL 006
*                                                                     *
*FUNCTION - THE FOLLOWING FUNCTIONS WILL BE PERFORMED BY THIS  CLOSE
*           EXECUTOR FOR BSAM, QSAM AND BPAM DATA SETS-DIR. ACC. DEV.
*           * FREES SEGMENT WORK AREA FOR DCB'S WHICH SPECIFY BFTEK=R,
*             RECFM=VS,MACRF=WL
*           * IT WILL RETURN ALL BUFFERS TO THE BUFFER POOL IF GOTTEN
*             BY OPEN AND SIMPLE BUFFERING
*           * IF WILL FREEMAIN ALL BUFFERS FOR DATA SETS WITH CONCAT-
*             ENATION OF UNLIKE ATTRIBUTES
*           * IT WILL FREE THE RECORD AREA IF OPEN HAD ACQUIRED IT
*           * CLEAR FIELDS IN THE DCB SO THAT IT CAN'T BE USED UNLESS
*             IT IS OPENED AGAIN
*           * FREEMAIN THE AREA GOTTEN FOR THE CONSTRUCTION OF IOB'S
*           * SETS APPROPRIATE AUDIT TRAIL BITS IN THE O/C/E     Y02072
*             WORKAREA IN CASE THE USERS STAE ROUTINE SHOULD GET Y02072
*             CONTROL DURING A FORCE CLOSE SITUATION. (THESE     Y02072
*             BITS, ALTHOUGH SET DURING NORMAL CLOSE, HAVE NO    Y02072
*             MEANING).                                          Y02072
*
*ENTRY POINT - FROM IGG0201Z OR IGG0201B
*
*INPUT - SEE DESCRIPTION OF REGISTERS, DCB(USERS), IOB(USERS).
*
*OUTPUT - NONE
*
*EXTERNAL ROUTINES - NONE
*
*MACROS : MAPPING - IECDSECT, IECDSECS, DCBD, IGGBCB, IHACCW, IHAFCAUD,
*                   IEZIOB, IGGPARML, IGGSWA, IEZDEB, IGGSCW
*
*MACROS : ACTION - FREEMAIN, IECRES, MODESET
*
*EXITS-NORMAL - TO IGG0200B, IGG0200G
*
*EXITS-ABNORMAL - BR 14 TO RETURN TO THE FORCE CLOSE EXECUTOR    Y02072
*
*TABLES/WORKAREAS- WHERE TO GO TABLE (WTG)
*
*      BYTE  0-7  NAME
*      BYTE  8-10 RELATIVE DISK ADDRESS(TTR) OF FIRST RECORD
*      BYTE 11    CONCATENATION NUMBER
*      BYTE 12    ZERO
*      BYTE 13    ALIAS INDICATOR AND USER DATA FIELD DESCRIPTOR.
*                        ALIAS INDICATOR 1 BIT
*                        NUMBER OF TTRS IN USERS DATA FIELD 2 BITS
*      BYTE 14-16 TTR OF FIRST TEXT RECORD
*      BYTE 17    ZERO
*      BYTE 18-19 TTR OF NOTE LIST OR SCATTER LIST
*      BYTE 20    TRANSLATION TABLE
*      BYTE 21    ZERO OR NUMBER OF ENTRIES IN THE NOTE LIST
*      BYTE 22-23 ATTRIBUTES
*      BYTE 24-26 TOTAL CONTIGUOUS MAIN STORAGE REQUIRED FOR MODULE
*      BYTE 27-28 LENGTH OF FIRST TEXT RECORD
*      BYTE 29    LENGTH OF WTG TABLE (IN DOUBLE WORDS)
*      BYTE 30-31 PATH THROUGH LOADS OF ROUTINES
*      BYTE 32-36 IDTTR OF EXECUTOR FOR FIRST DCB
*      BYTE 37-39 WORKAREA ADDRESS FOR FIRST DCB
*      BYTE 40-   TABLE OF IDTTR'S
*      BYTE 32+N(8) IDTTR OF EXECUTOR FOR N TH DCB   (5 BYTES)
*                   WORKAREA ADDRESS FOR N TH DCB    (3 BYTES)
*                   IDTTR OF LAST ROUTINE LOAD       (5 BYTES)
*                   NOT USED                         (3 BYTES)
*
*      CLOSE WORK AREA
*
*ATTRIBUTES- REENTRANT, REUSABLE, RUNS IN USER KEY UNLESS        Y02072
*            OTHERWISE SPECIFIED, SUPERVISOR STATE               Y02072
*
*NOTES - ALL REGISTER EQUATES BEGIN WITH THE CHARACTER R.
*      - THIS MODULE CREATED AT RELEASE 19 BY SPLIT OF IGG0201Z
*      - THE DEB ADDRESS IS GOTTEN FROM THE PROTECTED DCB FOR    Y02072
*        INTEGRITY REASONS.                                      Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:
*                                                                     *
*          VS2 RELEASE 01 DELETIONS
*D173000-174000                                                YM0928
*
*          VS2 RELEASE 02 DELETIONS
*001100,001800,001984,011600,037800,071000,146500-147020,147620, Y02072
*148500-149600,190000,210000,226500-228600,311000,343500,343700, Y02072
*452000-453000,389500-393100,71500,430000-432000,444000,87000,   Y02072
*188000,208800,222080,222240,248000-259500,309810                Y02072
*436000-450000,123900,291300-291600,305160-305240,               Y02072
*                                                                YM4640
*                                                                YM2852
*                                                                YM5782
*                                                                YM6889
*
*          VS2 RELEASE 03 DELETIONS
*                                                              @Z30TSMI
*000039150002                                                  @ZA09146
*          VS1 RELEASE 01 DELETIONS
*
*          RELEASE 20 DELETIONS                                       *
*3575222200                                                      A36765
*3575292000-293000,306000                                        S20201
*3575282000                                                      M2526
*          RELEASE 21 DELETIONS                                       *
*0932                                                            A48163
*0932282200                                                      A42903
*0932                                                            A45280
* 387000                                                         A40495
*D281400,282200-282600                                          SA52447
*                                                               SA51596
*                                                               SA53272
         EJECT
*
* REGISTER USAGE
*
RWK1     EQU   0                        WORK REGISTER
R1       EQU   1                        PARAMETER REGISTER
RWK2     EQU   R1                       WORK REGISTER
RDCB     EQU   2                        ADDR OF USERS DCB
RBASE    EQU   3                        BASE REGISTER
RCORE    EQU   4                        ADDR OF CLOSE WORK AREA
RPAR     EQU   5                        PARAMETER LIST
RWTG     EQU   6                        START OF WTG
RPARC    EQU   7                        CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                        CURRENT ENTRY IN WTG TABLE
RWK3     EQU   9                        WORK REGISTER
RWK8     EQU   10                       WORK REGISTER            Y02072
RDEB     EQU   11                       DEB ADDRESS
RIOB     EQU   12                       IOB REGISTER
RWK4     EQU   13                       WORK REGISTER
RWK5     EQU   14                       WORK REGISTER
RBRNCH   EQU   RWK5                     RETURN REG TO CLOSES TRR Y02072
R15      EQU   15                       PARAMETER REGISTER
RWK6     EQU   R15                      WORK REGISTER
         EJECT
*
* EQUATES FOR CONSTANTS
*
RDBACKB  EQU   X'03'                    USED TO TEST OPEN ATTRIBUTES
UPDATE   EQU   X'04'                    OPEN ATTRIBUTE = UPDAT
OUTINOUT EQU   X'07'                    OPEN ATTR, OUT/OUTIN
SETSC    EQU   X'23'                    SET SECTOR CCW CMND      S20201
DA       EQU   X'20'                    DIRECT ACCESS MASK       S20201
MIOBLEN  EQU   48                       LENGTH OF IOB
CLOSEFIN EQU   X'00'                    CLOSE EXEC PROCESSING COMPLETED
DEFBUFNO EQU   1                        BUFNO NOT SPECIFIED, DEFAULT 1
ROUNDCNT EQU   8                        FOR ROUNDING             Y02072
INVALADD EQU   1                        USED TO INVALIDATE       Y02072
*                                         ADDRESS VECTORS        Y02072
CPOFSETN EQU   X'30'                    OFFSET TO CP IN NORMAL   Y02072
*                                         SCHEDULING IOB         Y02072
CPOFSETC EQU   X'38'                    OFFSET TO CP IN CHAINED  Y02072
*                                         SCHEDULING IOB         Y02072
         EJECT
*
* ADDRESSABILITY
*
         USING WTG,RWTG
         USING WTGENTRY,RWTGC
         BALR  RBASE,0                  ESTABLISH BASE
         USING AMCL,RBASE               ESTABLISH ADDRESSIBILITY
*
AMCL     EQU   *
         B     BEGIN                    BRANCH AROUND MODULE ID  YM4640
         DC    C'IGG0201Y'              MODULE NAME              YM4640
         DC    C'@Z30TSMI'              LAST SHIP CODE         @Z30TSMI
         DC    C'10/15/74'              LAST DATE MODIFIED     @Z30TSMI
BEGIN    DS    0H                       END OF MODULE ID         YM4640
*
         L     RCORE,WTGDCBWA           GET DCB'S WORK AREA      Y02072
         USING FORCORE,RCORE            ESTABLISH ADDRESSIBILITY Y02072
         L     RDCB,DXPDCBAD            GET PROTECTED DCB ADDR   Y02072
         USING IHADCB,RDCB                                       Y02072
         L     RDEB,DCBDEBAD            GET VALID DEB ADDR       Y02072
         USING DEBBASIC,RDEB
         MODESET  KEYADDR=DXUKEY,WORKREG=10                      Y02072
         SPACE
         L     RDCB,DXUDCBAD            GET DCB BEING CLOSED     Y02072
         L     RIOB,DCBIOBA             GET CURR IOB/ICB ADDRESS
         LA    RIOB,0(RIOB)             CLEAR HIGH ORDER BYTE
         SR    RWK3,RWK3                CLEAR WORK REG
         IC    RWK3,DCBOFFSW            GET WRITE CCW OFFSET
         TM    DEBOPATB,OUTINOUT        TEST IF OUTPUT/OUTIN
         BO    SKIPGO                   YES, RWK3 HAS WRITE OFFSET
         TM    DEBOPATB,UPDATE          TEST IF UPDATE
         BO    SKIPGO                   YES, RWK3 HAS WRITE OFFSET
         IC    RWK3,DCBOFFSR            GET READ  CCW OFFSET
SKIPGO   EQU   *
         TM    DCBCIND2,DCBCNQSM        DCB USING QSAM
         BZ    AMCL3000                 NO BRANCH
         TM    DCBCIND1,DCBCNEXB        EXCHANGE BUFFERING USED
         BO    AMCL3000                 YES BRANCH
         L     RWK5,DCBBUFCB            GET BUFFER POOL CNTRL BLK
*
* IF A RECORD AREA WAS ACQUIRED BY OPEN, IT SHOULD BE RETURNED TO
* THE SUPERVISOR.
*
         LA    RWK5,0(RWK5)             CLEAR HIGH ORDER BYTE
         USING BCBLK,RWK5
         TM    BCBFLGS,BCBLRI           RECORD AREA INDICATOR ON
         BZ    AMCL2010                 BRANCH NO TO CHECK OFLGS
         TM    DCBCIND2,DCBCNBFP        DID OPEN ACQUIRE POOL
         BZ    AMCL2010                 BRANCH NO TO CHECK OFLGS
         L     R1,BCBLRIAR              GET RECORD AREA ADDRESS
         USING LRILOC,R1
         L     RWK8,LRILGTH             GET LEN OF RECORD AREA   Y02072
         LA    RWK8,0(RWK8)             CLR HI BYT FOR OR INST   YM6889
         DROP  R1
         SPACE 2
         FREEMAIN R,LV=(RWK8),A=(1),SP=0  FREE RECORD AREA       Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        AUDIT TRAIL IN KEY 5     Y02072
         SPACE 2
         OI    FCACLOS1,FCACRECA        INDIC REC AREA FREED     Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=10  BACK TO USER KEY    Y02072
         SPACE 2
         NI    BCBFLGS,X'FF'-BCBLRI     TURN OFF RECORD AREA FLAG
         XC    BCBLRIAR,BCBLRIAR        CLEAR RECORD AREA  ADDRESS
AMCL2010 EQU   *
         TM    DCBOFLGS,DCBOFEOV        CONCATENATION UNLIKE ATTRIBUTES
         BZ    AMCL2040                 NO BRANCH TO FREE BUFFERS
         TM    DCBCIND2,DCBCNBFP        DID OPEN ACQUIRE POOL
         BZ    AMCL2040                 NO BRANCH AROUND FREEMAIN
*
* FREE BUFFER POOL  OPEN WILL ACQUIRE A NEW ONE LATER
*
         SR    RWK6,RWK6                CLEAR REGISTER           YM0928
         IC    RWK6,DCBBUFNO            GET NUMBER OF BUFFERS    YM0928
*                                        TO BE FREED
         LA    R1,BCBLK                 GET CNTRL BLK ADDRESS TO FREE
         LH    RWK5,BCBBUFSZ            GET BUFFER SIZE
         DROP  RWK5
         USING BCBLK,RWK2
         MR    RWK5,RWK5                TOTAL AMT REQUIRED FOR BUFFERS
         LA    RWK8,BCBNLN(RWK6)        ADD EIGHT FOR CNTRL BLK  Y02072
         TM    BCBFLGS,BCBEXTND         BUFCB 16 BYTES           A48163
         BZ    FREEPOOL                 NO,BUFCB IS 8 BYTES      A48163
         LA    RWK8,BCBEXLN(RWK8)       ADD EIGHT FOR EXTENDED   Y02072
*                                         CONTROL BLOCK
         DROP  RWK2
FREEPOOL EQU   *
         SPACE 2
         FREEMAIN R,LV=(RWK8),A=(1),SP=0    FREE BUFFER POOL     Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        AUDIT TRAIL IN KEY 5     Y02072
         SPACE 2
         OI    FCACLOS1,FCACBUFC        INDIC BUF POOL FREED     Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=10  BACK TO USER KEY    Y02072
         SPACE 2
         XC    DCBBUFCA,DCBBUFCA        CLEAR ADDRESS IN DCB
         OI    DCBBUFCB+3,1             SET INVALID ADDRESS
         B     AMCL3000                 BRANCH TO FREE IOB
*
* SIMULATE THE FREE BUFFER MACRO TO RETURN BUFFERS TO POOL
*
AMCL2040 EQU   *
         CLI   DCBBUFNO,0               TEST IF BUFNO PRESENT    A36765
         BE    AMCL3000                 BRANCH IF NO BUFFERS     A36765
         SR    RWK6,RWK6                CLEAR REG
         IC    RWK6,DCBBUFNO            GET NUMBER OF BUFFERS TO FREE
         L     RWK5,DCBBUFCB            GET BUFFER POOL CNTRL BLK
         USING BCBLK,RWK5
AMCL2050 EQU   *
         LA    RWK4,0(RWK3,RIOB)        GET TO RD/WRT CCW ADDR
         USING CCW,RWK4
         L     RWK4,CCWADDRA            GET BUFFER ADDRESS
         LA    RWK4,0(RWK4)             HIGH BYTE CLEARED
         TM    DEBOPATB,RDBACKB         IS DCB OPENED FOR READ BACKWARD
         BNM   AMCL2070                 IF NOT, BRANCH.
         LH    RWK1,DCBBLKSI            GET SIZE OF ONE BUFFER AND
         BCTR  RWK1,0                   DECREMENT BY ONE.
         SR    RWK4,RWK1                POINT TO BEGINNING OF BUFFER.
AMCL2070 EQU   *
         USING BUFFER,RWK4
         MVC   BUFNXPTB,BCBBUFAD        UPDATE LAST BUF WITH NEW ONE
         ST    RWK4,BCBBUFPT            UPDATE BUFFER POOL CNRL BLK
         USING IOBQSAMN,RIOB
         L     RIOB,IOBNIOBA            GET NEXT IOB/ICB IN CHAIN
         BCT   RWK6,AMCL2050            IF MORE IOBS/ICBS, LINK BUFFERS
         SPACE 2
         MODESET  EXTKEY=DATAMGT        AUDIT TRAIL IN KEY 5     Y02072
         SPACE 2
         OI    FCACLOS1,FCACBUFP        INDIC BUF RET TO POOL    Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=10  BACK TO USER KEY    Y02072
         SPACE 2
*
* PREPARE TO FREE SPACE REQUIRED FOR IOB'S/ICB'S
*
AMCL3000 EQU   *
         SR    RWK5,RWK5                CLEAR REG
         SR    RWK1,RWK1                CLEAR REG
         SR    RWK2,RWK2                CLEAR REG
         IC    RWK1,DCBIOBL             GET LEN OF IOB/ICB (DBL WRDS)
         SLL   RWK1,3                   GET LEN IN BYTES
         IC    RWK2,DCBNCP              GET NUMBER OF BSAM CP.S
         TM    DCBCIND2,DCBCNQSM        IS QSAM BEING USED
         BZ    AMCL3010                 NO BRANCH
         IC    RWK2,DCBBUFNO            GET NUMBER OF QSAM CP.S
AMCL3010 EQU   *
         TM    DCBCIND2,DCBCNCHS        WAS DCB USING PCI SCHEDULING
         BO    AMCL3050                 YES BRANCH
AMCL3020 EQU   *
         USING IOBQSAMN,RIOB
         TM    IOBNFLG1,IOBFIRST        THIS THE FIRST IOB IN CHAIN
         BO    AMCL3060                 YES BRANCH
         L     RIOB,IOBNIOBA            GET NEXT IOB             M2526
         B     AMCL3020                 CONTINUE TO LOOP
         DROP  RIOB
AMCL3050 EQU   *
         L     RIOB,DCBIOBAD            GET MAIN IOB ADDRESS
         LA    RIOB,0(RIOB)             CLEAR HIGH BYTE
         LA    RWK5,MIOBLEN             CNST OF 48 FOR MIOB
         USING CCW-CPOFSETC,RIOB        LOC OF 1ST CCW IN CP     Y02072
         CLI   CCWCOMCD,SETSC           RCD READY USED           S20201
         DROP  RIOB                                              Y02072
         BNE   AMCL3055                 BRANCH IF NO             S20201
         LA    RWK5,2*L'CCW(RWK5)       INCREMENT BY SIXTEEN FOR Y02072
*                                         SET SECTOR IN MIOB
AMCL3055 EQU   *                                                 S20201
         LA    RWK5,3*L'CCW+L'IOBSEEK(RWK5) TOTAL LENGTH OF MIOB FOR DA
         NI    DCBIFLGS,X'FF'-DCBIBIOE  TURN OFF 'NO RETRY'       14140
AMCL3060 EQU   *
         LTR   RWK2,RWK2                WERE NO. OF CP SPECIFIED
         BP    AMCL3065                 YES, BRANCH
         LA    RWK2,DEFBUFNO            NO, PROVIDE ONE
AMCL3065 EQU   *
         LR    RWK6,RWK2                SAVE NO. OF IOBS/ICBS    S20201
         TM    DCBMACF2,DCBMRWRT+DCBMRLDM  TEST BDAM CREATE     SA51596
         BNO   AMCL3067                 NO, BR AROUND TEST      SA51596
         USING IOBQSAMN,RIOB
         L     RWK3,IOBSTART            GET BEGIN OF CHAN PGM    YM5782
         DROP  RIOB
         USING CCW,RWK3                                          YM5782
         CLI   CCWCOMCD,SETSC           IS RPS DEVICE USED       YM5782
         DROP  RWK3
         BNE   AMCL3080                 NO, BRANCH OUT          SA51596
         B     AMCL3068                 ELSE ADD 8 TO IOB CORE  SA51596
AMCL3067 EQU   *                                                SA51596
         USING CCW-CPOFSETN,RIOB
         CLI   CCWCOMCD,SETSC           RCD READY USED           S20201
         DROP  RIOB
         BNE   AMCL3080                 BRANCH NO                S20201
AMCL3068 EQU   *                                                SA51596
         TM    DEBOPATB,DEBACCS-UPDATE  ANY BITS OTHER THAN      S20201
*                                         UPDATE                 S20201
         BNZ   AMCL3070                 BRANCH IF YES            S20201
         TM    DEBOPATB,UPDATE          UPDATE BIT ON            S20201
         BNO   AMCL3070                 BRANCH IF NO             S20201
         LA    RWK6,ROUNDCNT(RWK6)      FOR UPDATE, ROUND UP     S20201
         SRL   RWK6,3                     NUMBER OF CHAN PGMS+1  Y02072
         SLL   RWK6,3                     TO GET NUM OF SECTORS  Y02072
         B     AMCL3075                 GO AROUND NEXT           S20201
AMCL3070 EQU   *                                                 S20201
         LA    RWK6,L'CCW               ONE DW                   S20201
AMCL3075 EQU   *                                                 S20201
         AR    RWK5,RWK6                INCREMENT CLEAR CORE     S20201
*                                         FOR SECTOR AREAS IN IOBS
AMCL3080 EQU   *                                                 S20201
         MR    RWK1,RWK1                NUMBER OF CP'S X IOB/ICB SIZE
         LA    RWK8,0(RWK2,RWK5)        ADD ANY CNSTS REQUIRED   Y02072
         TM    DCBMACF2,DCBMRWRT+DCBMRLDM TEST BDAM CREATE      SA53272
         BNO   AMCL3090                 NO, BRANCH OUT          SA53272
         TM    DCBRECFM,DCBRECTO        IS RECFM OVERFLOW       SA53272
         BNO   AMCL3090                 NO BRANCH OUT           SA53272
         LA    RWK8,10*L'CCW(,RWK8)     INCREMENT CORE TO        Y02072
*                                         INCLUDE ERASE CCWS    SA53272
         USING IOBQSAMN,RIOB
         L     RWK3,IOBSTART            GET BEGIN OF CHAN PGM    YM5782
         DROP  RIOB
         USING CCW,RWK3
         CLI   CCWCOMCD,SETSC           IS RPS DEVICE USED       YM5782
         DROP  RWK3
         BNE   AMCL3090                 NO, BRANCH OUT           YM5782
         LA    RWK8,3*L'CCW(,RWK8)     ADD FOR RPS OVERFLOW      Y02072
AMCL3090 EQU   *                                                SA53272
         SPACE 2
         FREEMAIN R,LV=(RWK8),A=(RIOB),SP=0  FREE IOB/ICB SPACE  Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        AUDIT TRAIL IN KEY 5     Y02072
         SPACE 2
         OI    FCACLOS1,FCACIOB         INDIC IOBS FREED         Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=10  BACK TO USER KEY    Y02072
         SPACE 2
*
* THIS ROUTINE FREES SEGMENT WORK AREA FOR THE DCB WHICH SPECIFIES
* BFTEK=R,RECFM=VS,MACRF=WL
*
         TM    DCBMACF2,DCBMRWRT+DCBMRLDM  TEST IF MACRF=WL
         BNO   SKIPVRE                  NO, SKIP VRE ROUTINE
         TM    DCBBFTEK,DCBBFTR         TEST IF BFTEK=R IS SPECIFIED
         BNO   SKIPVRE                  NO,SKIP VRE ROUTINE
         TM    DCBRECFM,DCBRECF         TEST IF RECFM=U OR F
         BO    SKIPVRE                  YES,SKIP VRE ROUTINE
         TM    DCBRECFM,DCBRECSB        TEST IF RECFM=VS
         BNO   SKIPVRE                  NO,SKIP VRE ROUTINE
         TM    DCBMACF2,DCBMRSWA        TEST IF COBOL HAS BILT SWA
         BO    SKIPVRE                  YES,SKIP VRE PROCESSING
*
* THE POINTER TO THE SEGMENT WORK AREA IS IN DCBEOBW.
*
         L     R1,DCBEOBW               LOAD ADDR OF SEG WORK AREA C.B.
         XC    DCBEOBW,DCBEOBW          CLEAR SWA PTR IN DCB
         USING SWACB,RWK2               ESTABLISH BASE
         LH    RWK6,SWACBNO             LOAD NO. OF SEG. WORK AREAS
         MH    RWK6,SWACBLNG            MULTIPLY NO. * LENGTH
         LA    RWK8,SWALNG(RWK6)        ADD CONTROL BLOCK LENGTH Y02072
         DROP  RWK2
         SPACE 2
         FREEMAIN R,LV=(RWK8),A=(1),SP=0  FREE SEGMENT WORK AREA Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        AUDIT TRAIL IN KEY 5     Y02072
         SPACE 2
         OI    FCACLOS1,FCACSWA         INDIC SWA FREED          Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=10  BACK TO USER KEY    Y02072
         SPACE 2
*
* THIS SECTION OF CODE CLEARS BSAM/QSAM/BPAM VECTORS IN DCB
*
SKIPVRE  EQU   *
         TM    DCBDSORG,DCBDSGPO        IS THIS BPAM            SA59359
         BO    AMCL5001                 YES, DEB LRECL FIELD    SA59359
*                                         DOES NOT EXIST        SA59359
         SR    RWK6,RWK6               CLEAR REGISTER            A45280
         SR    RWK4,RWK4               CLEAR REGISTER            A45280
         IC    RWK4,DEBNMEXT           NUMBER OF EXTENTS         A45280
         IC    RWK6,DEBEXSCL           OBTAIN SCALING FACTOR     A45280
         SLL   RWK4,0(RWK6)            LENGTH OF DEB EXTENT AREA A45280
         LA    RWK4,DEBBASND(RWK4)     ACC METHOD SECT           A45280
         DROP  RDEB
         USING DEBACSMD,RWK4
         MVC   DCBLRECL,DEBLRECL        RESTORE LRECL            A45280
         DROP  RWK4
AMCL5001 SR    RWK6,RWK6                CLEAR REG
         ST    RWK6,DCBWCPO             CLEAR OFFSET FIELDS
         LA    RWK6,INVALADD            TO SET AN INVALID ADDR   Y02072
         ST    RWK6,DCBIOBA             IOB VECTOR CLEARED
         ST    RWK6,DCBIOBAD            IOB FOR PCI CLEARED    @Z30TSMI
         ST    RWK6,DCBEOBW             CLEAR WRITE EOB VECTOR   Y02072
         TM    DCBCIND2,X'01'           TEST FOR QSAM            Z02260
         BNO   CLRFLDS                  BR IF NOT QSAM           Z02260
         STCM  RWK6,B'1100',DCBPRECL    CLEAR DCBPRECL           Z02260
CLRFLDS  STCM  RWK6,B'0111',DCBCNTRL+1  CLEAR CNTRL MOD ADDR     Y02072
         STCM  RWK6,B'0111',DCBEOBRA    CLEAR READ EOB VECTOR    Y02072
         STCM  RWK6,B'0111',DCBREAD+1   CLEAR READ RTN VECTOR    Y02072
         TM    DCBOFLGS,DCBOFEOV        CONCATENATION UNLIKE ATTRIBUTES
         BO    AMCL5005                 YES BRANCH
         STCM  RWK6,B'0111',DCBPERRA    CLEAR ERR ROUTINE VECTOR Y02072
AMCL5005 EQU   *
         TM    DCBCIND2,DCBCNQSM        WAS QSAM USED
         BZ    AMCL5009                 NO BRANCH
         ST    RWK6,DCBEOB              CLEAR VECTOR
AMCL5009 EQU   *
         XC    DCBCIND1,DCBCIND1        CLEAR SAM INDICATOR      A40495
         NI    DCBCIND2,DCBCNFEO        CLEAR ALL BUT FEOV INDIC A40495
         MODESET  EXTKEY=DATAMGT        DATA MANAG KEY FOR STORE Y02072
         SPACE 2
         TM    FCACLOS1,FCACFORC        FORCE CLOSE IN CONTROL   Y02072
         BNO   XCTL                     NO, CONTINUE NORMAL PATH Y02072
         SR    R15,R15                  RET CD FOR FC EXEC TO    Y02072
*                                         PASS TO CLOSES TRR     Y02072
         L     RBRNCH,24(RPAR)          RET ADDR TO FC EXEC    @ZA09146
         BR    RBRNCH                   RETURN TO FORCE CL EXEC  Y02072
XCTL     EQU   *                        NORMAL PATH              Y02072
         MVI   WTGIDTTR,0               CLEAR WTG ENTRY          Y02072
RELOOP   EQU   *
         LA    RWTGC,L'WTGENTRY(,RWTGC) STEP TO NEXT ENTRY
         LA    RPARC,L'PARDCBAD(,RPARC) STEP TO NEXT ENTRY
         CLC   WTGIDNX,AMIDCNST         IS THIS MODULE NEEDED
         BCR   8,RBASE                  YES, RECURSE THRU MODULE
         CLC   WTGIDNX,CLLDB            IS THIS THE END OF THE TABLE
         BL    RELOOP                   NO, BRANCH
         CLC   WTGIDNX,CLLDG            IS THIS THE END OF THE TABLE
         BH    RELOOP                   NO, BRANCH
         LR    RPARC,RPAR               INITIALIZATION
         DROP  RWTGC
         LA    RWTGC,WTGENTRY           POINT TO NEXT WTG ENTRY
         USING WTGENTRY,RWTGC
AMCL5011 EQU   *
         CLI   WTGIDTTR,CLOSEFIN        IS THIS ENTRY = ZERO
         BNE   AMCL5019                 NO BRANCH
         LA    RWTGC,L'WTGENTRY(,RWTGC)  POINT TO NEXT WTG ENTRY
         LA    RPARC,L'PARDCBAD(,RPARC)  POINT TO NEXT PARM ENTRY
         B     AMCL5011                 BRANCH TO TEST AGAIN
AMCL5019 EQU   *
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSMI*
               MODID=WTGENTRY           GO TO NEXT ROUTINE     @Z30TSMI
*
         EJECT
*
* CONSTANTS
*
AMIDCNST DC    C'1Y'                    CURRENT MODULE ID        Y02072
CLLDB    DC    C'0B'                    ID                       Y02072
CLLDG    DC    C'0G'                    ID                       Y02072
PATCH    DC    0H'0',50X'00'            PATCH AREA               Y02072
END      EQU   *                        END OF MODULE            Y02072
         TITLE 'IGG0201Y - IECDSECS DSECT'
         IECDSECS  (MAIN,(IOB,NO)),EXPAND=YES                    Y02072
         EJECT
FORCORE  DSECT                                                   Y02072
         IGGSCW
         TITLE 'IGG0201Y - DSECT FOR AUDIT TRAIL'
         IHAFCAUD  ORG=YES                                       Y02072
         TITLE 'IGG0201Y - WTG TABLE DSECT'
         IECDSECS WTG,PREFX,EXPAND=YES
         ORG   WTG+8                                             Y02072
WRKRETRN DS    A                        RETURN ADDR TO FORCE     Y02072
*                                         CLOSE EXECUTOR         Y02072
         ORG   WTGMODID+2
WTGTTRA  DS    CL3                      TTR
WTGKZ    DS    0CL2                     KZ
WTGK     DS    C                        K
WTGZ     DS    C                        Z
WTGC     DS    C                        C
         ORG   WTGMODNM
WTGMOD   DS    CL6                      FIRST 6 CHARS OF MOD NAMEY02072
         ORG   WTGIDTTR
WTGIDNX  DS    CL2                      ID NEXT MODULE
WTGTTRNX DS    CL3                      TTR NEXT MODULE
         ORG   WTGIDTTR+4
WTGDCBWA DS    AL4                      ADDR OF WORK AREA FOR DCB
         TITLE 'IGG0201Y - CCW DSECT'
         IHACCW  DSECT=YES
         TITLE 'IGG0201Y - CVT DSECT'
CVT      DSECT
         CVT
         TITLE 'IGG0201Y - BUFFER CONTROL BLOCK DSECT'
         IGGBCB  TYPE=SAM
         TITLE 'IGG0201Y - DEB DSECT'
         IEZDEB
         TITLE 'IGG0201Y - IOB DSECT'
         IEZIOB
         TITLE 'IGG0201Y - CLOSE PARAMETER LIST DSECT'
         IGGPARML
         TITLE 'IGG0201Y - SEGMENT WORK AREA DSECT'
         IGGSWA
         TITLE 'IGG0201Y - DCB DSECT'
         DCBD  DSORG=PS
         END
