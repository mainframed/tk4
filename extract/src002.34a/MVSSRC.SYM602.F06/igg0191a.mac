191A TITLE  'IGG0191A -- VALIDITY CHECKING RTN FOR ACCESS METHODS'
IGG0191A CSECT
*
*MODULE NAME - IGG0191A                                          Y02072
*
*DESCRIPTIVE NAME - FIRST LOAD OF THE OPEN EXECUTORS FOR SAM,    Y02072
*                   PAM, AND EXCP USERS                          Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          VS1 RELEASE 2 DELETIONS
*D156000,156004-156148,234500-235000                            SA61537
*
*          VS2 RELEASE 2 DELETIONS
*012000,023000-023500,109000,110000,112000,113000-114000,115000, Y02072
*188000,189000,210600,211000,212000,213000-222000,234600,234800, Y02072
*234900,330100-339000,339500,489600-494000,499000390000,478000,  Y02072
*482000,505000,589000,261000-289000                              Y02072
*                                                                YM4640
*                                                                YM6578
*
*          VS2 RELEASE 3 DELETIONS
*          VS2 RELEASE 03 CHANGES/DELETIONS
*                                                              @Z30TSMI
*A234984-234988                                                 ZA00631
*C246466,249046                                                 ZA00631
*A156886-156926,235728-235799,235933,235991-235995,237500       ZA00732
*C246540                                                        ZA00737
*A23599513,23599523                                            @ZA08893
*A23599113                                                     @ZA11099
*C235998                                                       @ZA16001
*A300575,300585                                                @ZA16001
*C23564020                                                     @ZA18297
*C300765                                                       @ZA20474
*A246726-246732                                                @ZAXXXXX
*D247344-247349                                                @ZAXXXXX
*          RELEASE 21 DELETIONS
*1394                                                            M1803
*1394                                                            A46863
*1394                                                            A44114
*1394075000,210600,215000,222000,230000-231000,233000,246050,    S21042
*1394247740,295000-297000,480000,499000,589000-590000            S21042
*D300000,330020                                                 SA53194
*1394                                                            M0062
*1394001000,010995,019000-022000,036000-105000,123000-127000,    A37507
*1394131000-140000,143000,146000,179000,246260,249046,266000,    A37507
*1394299000-454000,489000,497000                                 A37507
*1394246130,246672-246676                                        A42897
*1394348000-455000,496000-502000                                 M0091
*          RELEASE 20 DELETIONS
*    IGG0191A RESEQUENCED IN RELEASE 20
*0970246140                                                      M6478
*                                                                A38122
*0970298300-298600                                               M0693
*0970246340-246540                                               A34919
*0970                                                            A33682
*0970                                                            A34428
*0970                                                            S20038
*0970                                                            S20016
*0970105000                                                      A31590
*0970247000-248000,251000-254000                                 A31304
*0970233200-233600,246210-246910                                 M0639
*0970                                                            M0654
*0970249400                                                      M0678
*0970224000                                                      S20201
*0970118000-122000,124000-126000,129000-130000,132000-134000,    2670
*0970136000,138000-139000,141000,150500-151000,246340            2670
*0970249400                                                      M1506
*0970                                                            M2699
*                                                                M2882
*                                                                M3296
*0970                                                            M1565
*
*
*  CHANGES IDENTIFIED BY 'DM0D' IN COLUMNS 68-71 INSERTED          DM0D
*  IN SUPPORT OF CHECKPOINT/RESTART - RELEASE 17                   DM0D
*                                                                  DM0D
*  THIS MODULE WAS SPLIT INTO IGG0196A FOR RELEASE 19
*  THIS MODULE WAS SPLIT AGAIN IN REL 21 INTO IGG0196I
*  MOD FLOW  IGG0191A - IGG0196I - IGG0196A
*
*FUNCTION  THIS EXECUTOR IS ENTERED FOR USERS OF SAM AND EXCP
*          DURING OPEN FOR VALIDITY CHECKING OF USER SPECIFIED
*          PARAMETERS WHEN CODING THE DCB.
*          CONFLICTING DCB PARAMETERS:
*          - DCBMACRF OPPOSITE OF OPEN OPTION
*          - IF USER SPECIFIED DCBBUFL & IT IS LESS THAN DCBBLKSI
*          - DCBBLKSI NOT AT LEAST 4 BYTES LARGER THAN DCBLRECL FOR
*            VARIABLE LENGTH RECORDS
*          - DCBBLKSI NOT EQUAL TO DCBLRECL FOR FIXED LEN DATA SETS
*            WHEN BOTH ARE SPECIFIED
*          - DCBBLKSI NOT SPECIFIED FOR FIXED LEN DATA SETS      A44114
*          - FOR QSAM FIXED BLOCKED DATA SETS:
*            - DCBLRECL NOT SPECIFIED
*            - DCBBLKSI NOT MULT OF DCBLRECL                     Y02072
*            - DCBBLKSI LESS THAN LRECL FOR UNSPANNED RECORDS
*
*          TESTING IS DONE TO SEE IF MERLIN/ZEUS CAN BE SUPPORTED WHEN
*          SPECIFIED.
*
*ENTRY POINT--IGG0191A
*
*          ENTEREDFROM  IGG0190M   VIA XCTL
*                        IGG0190P
*
*INPUT--  DCB ADDRESS
*          WORK AREA ADDRESS
*          WHERE TO GO TABLE
*          PARAMETER LIST
*          CURRENT ENTRY IN WHERE TO GO TABLE
*          CURRENT PARAMETER (DCB)
*
*EXITS - NORMAL XCTL TO IGG0196I TO CONTINUE PROCESSING
*        ABNORMAL - XCTL TO PROB DET (IGG0196M) FOR WTP AND 013 ABEND
*
*TABLES/WORKAREA - SEE DSECTS AT END OF LISTING.
*
*ATTRIBUTES- RE-ENTRANT- RE-USABLE- RUNS IN DATA MANAGEMENT KEY  Y02072
*            UNLESS OTHERWISE SPECIFIED, RUNS IN SUPER STATE     Y02072
*
*MACROS : MAPPING - IFEFUBOB, DCBD, CVT, IECDSECS                Y02072
*       : ACTION - DMABCOND, XCTL, MODESET                       Y02072
         EJECT
*
* MASK'S TO TEST PARAMETER LIST WITH
COMMUTAB EQU   16
BFTEKR   EQU   X'20'                    DCBBFTEK=R               S19015
RECFMB   EQU   X'10'                    BLOCKED RECORD FORMAT    S19015
*                                       BIT                      S19015
PS       EQU   X'40'               PHYSICAL SEQUENTIAL           I19119
EXCP     EQU   X'80'               EXCP                          I19119
BSAM     EQU   X'20'               BSAM                          I19119
QSAMBIT  EQU   X'01'               QSAM INDICATOR BIT            I19119
SYS      EQU   X'20'               SYSIN/SYSOUT                  I19119
OUTPUT   EQU   X'0F'               OUTPUT FLAG                   I19119
BLKSZMRG EQU   X'10'               BLOCKSIZE MERGE BIT IN        I19119
*                                       JFCBMASK+2               I19119
TIOTTERM EQU   X'20'                    DEVICE IS TERMINAL (TSO) S20016
UORVREC  EQU   X'40'                   RECFM UNDEFIN OR VARIABLE M0639
UREC     EQU   X'C0'               FORMAT U RECS                 2670
VREC     EQU   X'40'               FORMAT V RECS                 2670
MICR3890 EQU   X'16'                    3890 DEVICE TYPE        XL03123
SHIFTCON EQU   4                        SHIFT CONSTANT          XL03123
LRECLMIN EQU   12                       LRECL CONSTANT          XL03123
LRECLMAX EQU   48                       LRECL CONSTANT          XL03123
D0       EQU   0                        DISPLACEMENT OF ZERO     S20201
D6       EQU   6                        SIX BYTE DISPLACEMENT    S20201
SDOPT    EQU   X'04'                    SEARCH DIR MASK IN DCB   S20201
INOUTIN  EQU   X'03'                    MASK FOR OUTIN OR INOUT  S20201
NOTEPT   EQU   X'04'                    NOTE - POINT MASK        S20201
SAMPAM   EQU   X'42'                    SAM & PAM MASK           S20201
QSAM     EQU   X'40'                    QSAM GET BIT             S20201
SDFLAG   EQU   X'08'                    S.D. FLAG IN JFCB        S20201
PCI      EQU   X'20'                    PCI TEST MASK            S20201
TRKOVFLO EQU   X'20'                    TRK OVERFLO MASK         S20201
UTRKOVFL EQU   X'E0'                    U - TRK OVFLO MASK       S20201
FBTRKOV  EQU   X'B0'                    FB - TRK OVFLO MASK      S20201
FSTDREC  EQU   X'08'                    F STANDARD MASK          S20201
RDBACK   EQU   X'01'                    OPEN OPTION - READ BACK SA49404
OUTIN    EQU   X'07'                    OPEN OPTION - OUTPUT    SA48921
*                                         AND THEN INPUT        SA48921
UCBOCR85 EQU   X'1A'                    DEVICE ID FOR 1285 OCR   YM6578
UCBOCR88 EQU   X'1C'                    DEVICE ID FOR 1288 OCR   YM6578
SDOFF    EQU   X'F7'                    MASK USED TO TURN OFF   ZA00732
*                                       THE SEARCH DIRECT BIT   ZA00732
*                                       IN THE JFCB             ZA00732
*
*                                                                     *
******************************           ******************************
*
*
*
*
*
*REGISTERS ARE REFERENCED SYMBOLICALLY WHERE APPROPRIATE
*
RDCB     EQU   2         DCB REGISTER
RBASE    EQU   3         BASE REGISTER
RCORE    EQU   4         WORK AREA ADDRESS
RPAR     EQU   5         TOP OF PARAMETER LIST
RWTG     EQU   6         TOP OF WTG TABLE
RPARC    EQU   7         CURRENT PARAMETER
RWTGC    EQU   8         CURRENT TRANS LOAD
RTIOT    EQU   9         USED HERE AS WRK REG AND  COMM VECTOR ADDR.
RWK1     EQU   RTIOT                    WORK REGISTER            Y02072
RUCB     EQU   10        USED HERE AS A COUNTER IN IOB GENERATION
RDEB     EQU   11        DEB ADDRESS
RB       EQU   12        WORK REG1  **
RC       EQU   13        WORK REG2  **  USED IN IOB GENERATION
RD       EQU   14        WORK REG3  **
RJ       EQU   15                       WORK REG4                M1803
RE       EQU   0         WORK REG5
R0       EQU   RE                       USED TO PASS INTERNAL   SA51107
*                                         CODE TO PROB DETER    SA51107
RF       EQU   1         WORK REG6
*
*
ASCII    EQU   X'08'                    ASCII TAPE               S20038
*
***********************************************************************
*
*
OABD056  EQU   56                                                S21042
OABD057  EQU   57                                                S21042
OABD058  EQU   58                                                S21042
OABD072  EQU   72                                                S21042
OABD083  EQU   83                                                S21042
OABD114  EQU   114                      INTERNAL CODE 114       XL03123
OABD115  EQU   115                      INTERNAL CODE 115       XL03123
OABD117  EQU   117                      INTERNAL CODE 117       XL03123
VSRDBK   EQU   221                      ERROR CODE, VAR SPAN    SA49404
*                                         AND READ BACK         SA49404
OUTINOUT EQU   222                      ERROR CODE, OUTIN/INOUT SA51107
         EJECT
         BALR  RBASE,0
         USING PO,RBASE
         USING IHADCB,RDCB
         USING FORCORE,RCORE
         USING DEB,RDEB
         USING   TCB,RD
         USING TIOT,RTIOT
         USING UCBOB,RUCB
*
*
*
*
PO       EQU   *
         B     BEGIN                    BRANCH AROUND MODULE ID  YM4640
         DC    C'IGG0191A'              MODULE NAME              YM4640
         DC    C'@ZA20474'              LAST SHIP CODE         @Z30TSMI
         DC    C'&SYSDATE'              LAST SHIP DATE         @Z30TSMI
BEGIN    DS    0H                       END OF MODULE ID         YM4640
*
         L     RDCB,0(RPARC)            GET DCB ADDRESS
*   NEXT INSTRUCTION IS USED AS AN INLINE CONSTANT OF FOUR
FOUR     EQU   *+2                                               A37507
         L     RCORE,4(RWTGC)           GET WORK AREA
*
*SET A BIT IN COMMON OPENS AUDIT TRAIL INDICATING TO FORCE       Y02072
*CLOSE AND PROBLEM DETERMINATION NOT TO REFRESH THE USERS DCB    Y02072
*FROM THE KEY 5 DCB BEFORE TAKING ANY USER EXITS.                Y02072
*
         OI    DXATCOM1,DXATREFR        DO NOT REFRESH DCB       Y02072
         SR    RWK1,RWK1                PREPARE TO ZERO FIELD    Y02072
         ST    RWK1,DXMSGADR            INDICATE MSG CSECT NOT   Y02072
*                                         LOADED                 Y02072
         L     RD,COMMUTAB         CALCULATE TCB ADDRESS
         L     RD,0(0,RD)
         L     RD,4(0,RD)               TCB  ADDR
         L     RTIOT,TCBTIO
         AH    RTIOT,DCBTIOT       RESET TIOT POINTER
         TM    TIOELINK,TIOTTERM        IS THIS TSO?             S20016
         BO    GETUCBAD                 YES, BR AROUND LOADING   Y02072
*                                         THE UCB ADDRESS        Y02072
         L     RUCB,DXUCBADR            GET UCB ADDRESS          Y02072
GETUCBAD EQU   *                        TEST FOR INVALID PARMS   Y02072
         TM    DCBMACRF,EXCP            EXCP A. M.               A26010
         BO    NOSDIR                   YES                      S20201
         TM    0(RPARC),X'07'           DCB OPENED FOR INPUT,    A26010
*                                       RDBACK, INOUT, OR UPDAT
         BO    TSTWP                    NO, MUST BE OUTPUT OR    A26010
*                                       OUTIN                    A26010
         TM    DCBMACRF,X'60'           GET OR READ SPECIFIED    A26010
         BNZ   A26010A                  YES, CONTINUE            S21042
         LA    RE,OABD057               PUT INTERNAL CODE IN REG S21042
*                                       0                        S21042
         B     CALLPD                   CALL PROB DET            S21042
TSTWP    TM    DCBMACRF+1,X'60'         WRITE OR PUT SPECIFIED   A26010
         BNZ   TSTOCR                   YES, CONTINUE            YM5516
         LA    RE,OABD058               PUT INTERNAL CODE IN REG S21042
*                                       0                        S21042
         B     CALLPD                                            S21042
A26010A  EQU   *                                                 A26010
         TM    0(RPARC),RDBACK          BIT ON FOR READ BACK    SA49404
         BZ    TSTOCR                   NO, BYPASS VAR SPAN TST SA49404
         TM    0(RPARC),X'0F'-RDBACK    BITS OFF FOR READ BACK   YM5516
         BNZ   TSTOCR                   IF OFF, TEST FOR OCR     YM5516
         TM    DCBRECFM,DCBRECV+DCBRECSB  VAR SPANNED RECS      SA49404
         BNO   TSTOCR                   NO, BYPASS DMABCOND     SA49404
         LA    R0,VSRDBK                SET INTERNAL CODE       SA49404
         B     CALLPD                   GO TO PROB LDET RTN     SA49404
TSTOCR   EQU   *                                                SA61537
         TM    TIOELINK,TIOTTERM        IS THIS TSO             ZA00631
         BO    NOSDIR                   YES, BRANCH             ZA00631
         CLI   UCBTBYT4,UCBOCR85        IF LESS THAN X'1A', NOT  YM6578
*                                         AN OCR DEVICE         SA61537
         BL    TSTDSORG                 NOT AN OCR DEVICE       SA61537
         CLI   UCBTBYT4,UCBOCR88        IF HIGHER THAN X'1C',    YM6578
*                                         NOT AN OCR DEVICE     SA61537
         BNH   OCRQSAM                  YES, AN OCR DEVICE      SA61537
TSTDSORG EQU   *                                                SA61537
         TM    DCBDSORG,SAMPAM          SAM OR PAM               S20201
         BZ    NOSDIR                   SCH DIR NOT ALLOWED      S20201
         TM    DCBOPTCD,SDOPT           IF THE SCH DIR OPTION IS S20201
         BNO   NOSDIR                    SPECIFIED IN THE DCB    S20201
         TM    UCBTBYT3,UCB3DACC         TEST TO SEE IF IT'S     S20201
         BNO   NOSDIR                    SUPPORTABLE             S20201
         TM    D0(RPARC),OUTPUT         OUTPUT ONLY IS NOT       S20201
         BO    NOSDIR                   SUPPORTED                S20201
         BZ    CHKMORE                  FOR INPUT ONLY BR        S20201
         TM    D0(RPARC),INOUTIN        IF ITS NOT INOUT OR      S20201
         BNO   NOSDIR                   OUTIN, DONT SET THE BIT  S20201
CHKMORE  EQU   *                                                 S20201
         TM    DCBRECFM,FSTDREC         IF THE FORMAT IS F STD   S20201
         BO    TESTMORE                 THEN TEST FURTHER        S20201
         TM    DCBRECFM,UTRKOVFL        ELSE TEST FOR U          S20201
*                                       FORMAT WITH TRKOVFLO
         BO    NOSDIR                   IN WHICH CASE DONT SET   S20201
*                                       THE S.D. BIT.
         TM    DCBRECFM,FBTRKOV         FB - TRK OVFLO IS ALSO   S20201
         BO    NOSDIR                   NOT SUPPORTED            S20201
         B     OK                       BRANCH TO CONTINUE       S20201
*                                       TESTING                  S20201
TESTMORE EQU   *                                                 S20201
         TM    DCBRECFM,TRKOVFLO        NOW UNLESS IT'S FIXED    S20201
*                                       STD                      S20201
         BZ    NOSDIR                   WITH TRKOVFLO, DONT      S20201
*                                       PROVIDE SR.DIR.
OK       EQU   *                                                 S20201
         TM    DCBOPTCD,PCI               NOR IS PCI             S20201
         BO    NOSDIR                   SO BR AROUND FOR THEM    S20201
         TM    DCBMACRF,QSAM            QSAM USED                S20201
         BO    PDSTST                   BRANCH IF YES          @ZA18297
         TM    DCBMACRF,NOTEPT          NOTE - POINT IS NOT      S20201
         BO    NOSDIR                     SUPPORTED ---          S20201
         TM    DCBBFTEK,BFTEKR          OFFSET RD                S20201
         BO    NOSDIR                   BRANCH IF YES            S20201
****************************************************************ZA00732
* SEARCH DIRECT CHANNEL PROGRAMS ARE NOT DESIGNED TO SUPPORT    ZA00732
* PROCESSING THAT BEGINS AT OTHER THAN R0 OF A TRACK. THEREFORE ZA00732
* WHEN A USER REQUESTS THAT THE NOTE/POINT ROUTINES BE LOADED   ZA00732
* OR OPENS TO A MEMBER (BY SPECIFYING THE MEMBER NAME IN THE    ZA00732
* JCL), A SEARCH DIRECT CHANNEL PROGRAM WILL NOT BE BUILT.      ZA00732
****************************************************************ZA00732
PDSTST   TM    JFCBIND1,JFCPDS          IS DATA SET A PDS MEMBERZA18297
         BNO   SOP235X                  IF NOT, BRANCH          ZA00732
         CLI   JFCBELNM,C' '            MEMBER NAME IN JCL      ZA00732
         BNE   NOSDIR                   IF SO, BRANCH, THE 1ST  ZA00732
*                                       CHARACTER OF THE MEMBER ZA00732
*                                       WILL BE OTHER THAN A    ZA00732
*                                       BLANK                   ZA00732
*
SOP235X  EQU   *                                                 S20201
*
*
         OI    JFCBMASK+D6,SDFLAG       SET S.D. FLAG IN JFCB    S20201
         B     BLKTEST                                          ZA00732
*
NOSDIR   EQU   *                                                 S20201
         NI    JFCBFLG2,SDOFF           TURN OFF SEARCH DIRECT  ZA00732
         TM    TIOELINK,TIOTTERM        IS THIS TSO?           @ZA11099
         BO    BLKTEST                  YES, BRANCH            @ZA11099
         CLI   UCBTBYT4,MICR3890        DEVICE A 3890 ?        @ZA08893
         BE    OCRQSAM                  YES-BRANCH             @ZA16001
*
* BSAM - QSAM BLOCKSIZE TEST
BLKTEST  EQU  *                                                 ZA00732
         TM    DCBDSORG,PS         PHYSICAL SEQUENTIAL SPECIFIED I19119
         BNO   CONTINUE            BRANCH IF NOT PS              I19119
         TM    DCBMACRF,EXCP       TEST FOR EXCP                 I19119
         BO    CONTINUE            BRANCH IF EXCP                I19119
         TM    DCBMACRF,BSAM       TEST FOR BSAM INPUT           I19119
         BO    TSTBLKSZ            BRANCH IF BSAM INPUT          I19119
         TM    DCBMACRF+1,BSAM     TEST FOR BSAM OUTPUT          I19119
         BO    TSTBLKSZ            BRANCH IF BSAM OUTPUT         I19119
         OI    DCBCIND2,QSAMBIT    SET QSAM INDICATOR            I19119
*
*TEST FOR INVALID QSAM OPEN OPTIONS.                            SA51107
*
         TM    0(RPARC),OUTPUT          PREPARE TO TEST FOR     SA51107
*                                         INOUT/OUTIN           SA51107
         BO    AROUND                   ALL ON, CAN'T BE EITHER SA51107
         TM    0(RPARC),INOUTIN         BITS ON FOR INOUT/OUTIN SA51107
         BNO   AROUND                   NOT ON, VALID           SA51107
         LA    R0,OUTINOUT              INTERNAL ABEND CODE     SA51107
         B     CALLPD                   GO ISSUE DMABCOND       SA51107
AROUND   CLC   DCBBUFL(2),ZEROO        DID USER SPECIFY BUF LEN? M0639
         BE    TEST                    NO,  BR AROUND TEST       M0639
         CLC   DCBBLKSI(2),DCBBUFL     TEST TO SEE IF BLKSIZE IS M0639
*                                         GREATER THAN BUF LEN
         LA    RE,OABD072               LOAD INTERNAL ABEND CODE S21042
         BH    CALLPD                   CALL PROB DET            S21042
TEST     EQU   *                                                 M0639
         TM    DCBOPTCD,ASCII      ASCII TAPE?       S20038      A31304
         BO    CONTINUE                YES - BRANCH              A42897
         TM    TIOELINK,TIOTTERM        IS THIS TSO              M0693
         BO    MOVEID                  BRANCH IF YES            ZA00631
         TM    DCBRECFM,UREC       ARE RECS FORMAT U?            2670
         BNM   CONTINUE             YES, BRANCH                 ZA00737
         TM    DCBRECFM,VREC       ARE RECS FORMAT V?            2670
         BC    8,FIXREC            NO, BR AROUND TEST FOR FORMAT 2670 S
*                                       V REC                    2670
         TM    DCBRECFM,X'08'          ARE SPANNED RECS BEING    M2699
*                                       USED?                    M2699
         BC    1,CONTINUE              YES, LRECL CAN BE LARGER  M2699
*                                        THAN BLKSIZE-BR AROUND TEST
*
*   FOR VAR LEN RECORDS, BLKSIZE MUST BE AT LEAST 4 BYTES GREATER THAN
*   LRECL.
*
         LH    RE,DCBLRECL         GET LRECL FOR TEST            2670
         AH    RE,FOUR             ADD 4 BYTES FOR BDW LENGTH    2670
         CH    RE,DCBBLKSI         LRECL EQUAL OR LESS THAN      2670
*                                       BLKSIZE?                 2670
         BNH   CONTINUE            YES, CONTINUE PROCESSING      2670
         B     DCBERROR            NO, ABEND                     2670
FIXREC   EQU   *                                                 2670
         CLI   UCBTBYT4,MICR3890        DEVICE A 3890 ?         XL03123
         BE    MICRDEV                  YES,BYPASS RECFM TEST   XL03123
         CLC   DCBBLKSI,ZEROO           IS BLKSIZE EQ ZERO?     WJXXXXX
         BE    DCBERROR                 YES - ABEND             WJXXXXX
         TM    DCBRECFM,RECFMB         RECFM BLOCKED?            M0639
         BC    1,TSTBLKSZ              YES,  TEST LRECL MULT     M0639
*                                       BLKSI                    M0639
*                  RECFM IS FIXED UNBLOCKED
* USER DOES NOT HAVE TO SPECIFY LRECL FOR RECFM=F. BLOCKSIZE IS ASSUMED
* THEREFORE WE SHOULD TEST TO SEE IF USER SPECIFIED LRECL BEFORE MAKING
* FOLLOWING TEST.
         CLC   DCBLRECL(2),ZEROO       DID USER SPECIFY LRECL?   M0654
         BE    CONTINUE                NO,  DO NOT TEST EQ TO    M0654
*                                       BLKSIZE                  M0654
         CLC   DCBLRECL(2),DCBBLKSI    LRECL EQ BLKSIZE?         M0639
         LA    RE,OABD083               LOAD INTERNAL ABEND CODE S21042
         BNE   CALLPD                   CALL PROB DET            S21042
         B     CONTINUE                LRECL O.K.                M0639
TSTBLKSZ EQU   * TEST BLOCKSIZE AS A MULTIPLE OF LRECL           I19119
*  THE FOLLOWING TEST ALSO FIXES APAR NO. 34919
*  THE FOLLOWING TEST FOR V RECS IS MADE TO KEEP BLKSIZE FROM BEING
*  ADJUSTED TO A MULTIPLE OF LRECL FOR VARIABLE & UNDEFINED RECS
*
         TM    TIOELINK,TIOTTERM        IS THIS TSO              M1565
         BO    MOVEID                  BRANCH IF YES            ZA00631
         TM    DCBRECFM,UORVREC        ARE RECS U OR V?          M0654
         BC    1,CONTINUE              YES,  BYPASS LRECL TESTS  M0654
         LH    RC,DCBBLKSI             LOAD BLKSIZE FOR DIVIDE   M0639
         LH    RE,DCBLRECL             LOAD LRECL FOR DIVIDE     M0639
         LTR   RE,RE                   TEST FOR LRECL SPECIFIED  M0639
         BZ    QSAMTEST                BRANCH IF LRECL NOT
*                                       SPECIFIED                M0639
         SR    RB,RB               CLEAR REGISTER FOR DIVIDE     I19119
         DR    RB,RE               DIVIDE BLOCKSIZE BY LRECL     I19119
         LTR   RB,RB               TEST BLOCKSIZE MULTIPLE OF    I19119
*                                       LRECL                    I19119
         BZ    CONTINUE            BRANCH IF EVEN MULTIPLE       I19119
*
*** OCIP 294 *** THIS CODE REMOVED BECAUSE HASP HANDLES SYSOUT   Y02072
*
QSAMTEST EQU   * ABEND IF QSAM - CONTINUE IF BSAM                I19119
         TM    DCBCIND2,QSAMBIT    TEST FOR QSAM OR BSAM         I19119
         BNO   CONTINUE            BRANCH IF BSAM                I19119
*
DCBERROR EQU   *                                                 S21042
         LA    RE,OABD056               PUT INTERNAL CODE IN R0  S21042
CALLPD   EQU   *                                                 S21042
         DMABCOND (0),PDLOAD,RETURN=NONE,RES=NO,REGSAVE=YES      S21042X
                                        GO TO PROB DET FOR 013 ABEND
         B     TCTLRTN                                           S21042
CONTINUE EQU   *
         CLI   UCBTBYT4,MICR3890        DEVICE A 3890 ?         XL03123
         BNE   MOVEID                   NO-BRANCH               XL03123
MICRDEV  EQU   *
         TM    DCBRECFM,UORVREC         RECFM=F ?               XL03123
         BO    RECFMERR                 NO-BRANCH               XL03123
         OI    DCBRECFM,RECFMB          FORCE RECFM=FB          XL03123
         SR    RB,RB                    CLEAR RB REGISTER       XL03123
         LA    RB,LRECLMIN              LOAD LRECL CONSTANT     XL03123
         CH    RB,DCBLRECL              TEST LRECL LENGTH       XL03123
         BH    LRECLERR                 LRECL TOO LOW-BRANCH    XL03123
         LA    RB,LRECLMAX              LOAD LRECL CONSTANT     XL03123
         CH    RB,DCBLRECL              TEST LRECL LENGTH       XL03123
         BL    LRECLERR                 LRECL TOO HIGH-BRANCH   XL03123
         LH    RB,DCBLRECL              LOAD LRECL TO RB REG    XL03123
         SLL   RB,SHIFTCON              MULTIPLY LRECL BY 16    XL03123
         LH    RC,DCBBUFL               LOAD BUFL INTO RC       XL03123
         LTR   RC,RC                    DCBBUFL SPECIFIED ?     XL03123
         BZ    TSTBLKSI                 NO-BRANCH               XL03123
         CR    RB,RC                    TEST DCBBUFL            XL03123
         BH    BUFLERR                  BUFL TOO SMALL-BRANCH   XL03123
TSTBLKSI EQU   *                                                XL03123
         LH    RC,DCBBLKSI              LOAD BLKSIZE INTO RC    XL03123
         LTR   RC,RC                    BLKSIZE SPECIFIED ?     XL03123
         BZ    SUPBLKSI                 NO-BRANCH               XL03123
         CR    RB,RC                    TEST BLKSIZE            XL03123
         BH    BLKSIERR                 TOO SMALL-BRANCH        XL03123
         B     MOVEID                   XCTL OUT                XL03123
SUPBLKSI EQU   *                                                XL03123
         STH   RB,DCBBLKSI              STORE GENERATED BLKSIZE XL03123
         B     MOVEID                   XCTL OUT                XL03123
RECFMERR EQU   *                                                XL03123
         LA    RE,OABD115               LOAD ABEND CODE         XL03123
         B     CALLPD                   CALL PROBLEM DETECT     XL03123
LRECLERR EQU   *                                                XL03123
         LA    RE,OABD117               LOAD ABEND CODE         XL03123
         B     CALLPD                   CALL PROBLEM DETECT     XL03123
BUFLERR  EQU   *                                                XL03123
BLKSIERR EQU   *                                                XL03123
         LA    RE,OABD114               LOAD ABEND CODE         XL03123
         B     CALLPD                   CALL PROBLEM DETECT     XL03123
*
*  SET QSAM INDICATOR FOR OCR/QSAM DATA SETS                    SA55577
*
OCRQSAM  TM    DCBMACF1,DCBMRRD         BSAM INPUT BEING USED   SA55577
         BO    CONTINUE                 YES, DON'T SET QSAM BIT SA55577
         TM    DCBMACF2,DCBMRRD         BSAM OUTPUT BEING USED  SA55577
         BO    CONTINUE                 YES, DON'T SET QSAM BIT SA55577
         TM    DCBMACRF,EXCP            EXCP USED              @ZA16001
         BO    MOVEID                   YES, BRANCH            @ZA20474
         OI    DCBCIND2,DCBCNQSM        INDICATE QSAM DATA SET  SA55577
MOVEID   EQU   *
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,MOD196I         MOD 196I NEXT          @Z30TSMI
*
*********************************************************************
*
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     UPDATE WHERE TO GO ENTRY
         LA    RPARC,PLOFF(0,RPARC)     UPDATE PARA LIST ENTRY
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   8,RBASE                  YES BR BACK TO BEGINNING
*
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BC    7,RELOOP                 NO BRANCH
*
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      RE INIT  WTG LIST
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN                NO XCTL
*
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         BC    15,ZCHEK
*
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSMI*
               MODID=WTGENTRY                                  @Z30TSMI
        DROP  RWTGC,RWTG
*
WAOFF    EQU   32                  OFFSET OF FIRST ENTRY IN WTG TABLE
PLOFF    EQU   4                   OFFSET OF DCB ENTRIES
WGOFF    EQU   8                   OFFSET OF WTG ENTRIES
ZEROO    DC    X'0000'                 CONSTANT FOR BUFL TEST    M0639
OPIDCNST DC    C'0S'
AMIDCNST DC    C'1A'
MOD196I  DC    C'6I',VL3(IGG0196I)      NEXT EXECUTOR            Y02072
PDLOAD   DC    C'6M',VL3(IGG0196M)      PROBLEM DETERMINATION    Y02072
         SPACE
PATCH    DC    25H'0'                   PATCH AREA               YM4640
END      EQU   *                        END OF MOUDULE           Y02072
         EJECT
SRT      DSECT
         IEFUCBOB  PREFIX=YES                                    Y02072
*
         EJECT
           DCBD  DSORG=PS
CVT      DSECT
         CVT
*   TASK CONTROL BLOCK
         CNOP  0,4
TCB      DSECT
TCBRBP   DS    F                       PO9NTER TO EXECUTING RB
TCBPIE   DS    F                       POINTER TO PROG. INTERRUPT EL.
TCBDEB   DS    F                       POINTER TO DEB QUEUE
TCBTIO   DS    F                       POINTER TO TIOT
TCBCMP   DS    F                       TASK COMPLETION CODE
TCBTRN   DS    F                       TESTRAN FIELD
TCBMSS   DS    F                       DATA BLOCK QUEUE ELEMENT POINTER
TCBPKF   DS    CL1                     RESERVED FOR FUTURE USE
TCBFLGS  DS    FL5                     FLAG BYTES
TCBLMP   DS    CL1                     RESERVED FOR FUTURE USE
TCBDSP   DS    CL1                     RESERVED FOR FUTURE USE
TCBLLS   DS    F                       POINTER TO LOAD LIST
TCBJLB   DS    F                       POINTER TO JOBLIB DCB
TCBJSE   DS    F                       LIST OF INACTIVE PROGRAMS ADDR.
TCBGRS   DS    CL64                    GENERAL REGISTER SAVE AREA
TCBIDF   DS    CL1                     TCB IDENTIFIER FIELD
TCBFSA   DS    CL3                     POINTER TO 1ST PROB.PROG.SAVEAR
TCBTCB   DS    F                       RESERVED FOR FUTURE USE
TCBTME   DS    F                       POINTER TO TIMER ELEMENT
         DS    CL24
TIOT     DSECT
TIOELNGH DS    CL1                           LENGTH OF THIS ENTRY
TIOESTTA DS    CL1                           STATUS -A-
TIOERLOC DS    CL2                           REL. LOC OF POOL
TIOEDDNM DS    2F                            DDNAME
TIOEJFCB DS    1F                            JFCB DISK ADDR
TIOESTTB DS    0CL1
TIOEFSRT DS    CL4
         DS    CL1
*        TIOT POOL ENTRY
TIOPNSLT DS    XL1                           NR.OF SLOTS FOR POOL
         DS    CL1
TIOPNSRT DS    XL1                           NR.OF DEVICES (FILLED
*                                             SLOTS)
TIOPPOOL DS    CL8                           POOL NAME
TIOPSTTB DS    XL1                           STATUS OF SLOT
TIOPSLOT DS    XL3                           SRT ADDRESS OR EMPTY SLOT
TIOTFEND DS    1F                            FINAL END OF AN ENTRY
TIOELINK EQU   TIOERLOC+1                                        S20016
         SPACE 1
*   DATA EXTENT BLOCK  (UNTIL PROTIOS CHANGES)
DEB      DSECT
DEBNMSUB DS    0CL1
DEBTCBAD DS    CL4
DEBAMLNG DS    0CL1
DEBDEBAD DS    CL4
DEBOFLGS DS    0CL1
DEBIRBAD DS    CL4
DEBOPATB DS    0CL1
DEBSYSPG DS    CL4
DEBNMEXT DS    0CL1
DEBUSRPG DS    CL4
DEBPRIOR DS    0CL1
DEBECBAD DS    CL4
DEBPROTG DS    0CL1
DEBDEBID DS    0CL1
DEBDCBAD DS    CL4
DEBEXSCL DS    0CL1
DEBAPPAD DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    CL4
DEBTVLSQ DS    0CL2                VOL SEQ FOR TAPE
DEBBINUM DS    CL2
DEBTVLNM DS    0CL2                NO OF VOL FOR TAPE
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
*  FOR ADDITIONAL EXTENT ON DIRECT ACCESS THE FOLLOWING FIELDS
*  WILL HAVE TO BE OFFSET ANOTHER 16 BYTES
DEBVOLSQ DS    H
DEBVOLNM DS    H
*  FOR EACH SUBROUTINE LOADED BUMP THIS OFFSET BY 2
DEBSUBID DS    H
         TITLE 'IGG0191A - FORMAT OF GOTTEN CORE'
         IECDSECS (MAIN,(IOB,NO)),WTG,PREFX,EXPAND=YES
CCWOFSET EQU   DXCCW12-FORCORE     CCW RELOCATABLE OFF SET FOR DXCCW12
DCBOFSET EQU   DCBDEBAD-IHADCB     DEB RLCATBL OFFSET IN DCB
           END
