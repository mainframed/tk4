         TITLE 'IGG01993 - 2ND HALF OF IGG01991'
IGG01993 CSECT
*MODULE NAME - IGG01993                                          Y02072
*
*DESCRIPTIVE NAME - LOAD ACCESS METHOD ROUTINES                  Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          VS 2 RELEASE 2 DELETIONS                              Y02072
*001500,011500,013600,014100,190600,191500-194000,383500,413500, Y02072
*629000-630000,624600,458000-459000,463000,485000-479000,        Y02072
*528160-528300                                                   Y02072
*                                                                YM1140
*                                                                YM4640
*                                                                YM6834
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*          RELEASE 20 DELETIONS                                       *
*1262                                                            S20038
*1262134600,192300-192420                                        A32160
*0000528360-531000,537000-603800,624300,625600-626200            M4028
*          RELEASE 21 DELETIONS                                       *
*
*STATUS CHANGE LEVEL 005
*
*FUNCTION/OPERATION - THIS MODULE IS A CONTINUATION OF MODULE
*    IGG01991 AND LOADS  THE END-OF BLOCK RTNS AND PLACES THEIR
*    ADDRESSES IN THE DCB.
*
*    THIS MODULE WAS SPLIT FROM IGG01991 IN RLSE 20.
*
*ENTRY POINTS - THE MODULE IS ENTERED BY MEANS OF AN XCTL FROM
*    IGG01991.
*
*INPUT - SEE DESCRIPTION OF REGISTER CONTENTS.
*
*OUTPUT - SUBROUTINES LOADED, ADDRESSES IN DCB, ID'S IN DEB
*
*EXTERNAL ROUTINES - NONE
*EXITS - NORMAL - XCTL TO IGG01911                               Y02072
*      - ERROR - NONE
*
*MACROS - ACTION - XCTL, LOAD, IECDSECS(MAIN,WTG), DCBD, CVT     Y02072
*
*TABLES/WORKAREAS - DESCRIBED BY DSECTS AT END OF MODULE LISTING
*
*ATTRIBUTES - REENTRANT, REUSABLE, SUPERVISOR STATE,             Y02072
*             RUNS IN DATA MANAGEMENT KEY.                       Y02072
*
*
*
* REGISTER CONVENTIONS
*
RE       EQU   0                        WORK REG5
RF       EQU   1                        WORK REG6
R1       EQU   RF                       PARM REG                 YM1140
RDCB     EQU   2                        DCB
RBASE    EQU   3                        BASE
RCORE    EQU   4                        WORE AREA
RPAR     EQU   5                        TOP OF PARAMETER
RWTG     EQU   6                        TOP OF WTG TABLE
RPARC    EQU   7                        CURRENT PARAMETER
RWTGC    EQU   8                        CURRENT TRANS LOAD
RTIOT    EQU   9                        TIOT
RUCB     EQU   10                       UCB
RDEB     EQU   11                       DEB
RB       EQU   12                       WORK REG1
RC       EQU   13                       WORK REG2
RD       EQU   14                       WORK REG3
RJ       EQU   15                       WORK REG4
*
*
PARAR    EQU   RPARC
*
***********************************************************************
*
***********************************************************************
*
* MASK'S TO TEST PARAMETER LIST WITH
*
*********************************************************************
INP      EQU   X'0F'                    MASK
OUTP     EQU   X'0F'                    MASK
UPDATE   EQU   X'04'                    MASK
INOUT    EQU   X'03'                    MASK
OUTIN    EQU   X'07'                    MASK
RDBACK   EQU   X'01'                    MASK
INPRDBK  EQU   X'0E'               MASK TO CK INPUT OR RDBACK      UT18
*
*
*********************************************************************
*
*
* OFFSET FOR PARAMETER TESTS
*
FRTYATE  EQU   48                       MASK
DISPSN   EQU   X'0'                     MASK
*
*
*********************************************************************
*
* OFFSETS FOR DEB REFERENCE
*
*
BIN      EQU   36
BEXT     EQU   38
*
*
*
*********************************************************************
*
* MASK'S USED TO TEST OPTION FIELDS IN DCB
*
*
*
DOUBLE   EQU   X'01'                    MASK
*
*
STOWB    EQU   X'80'                    MASK
WRTCK    EQU   X'80'                    MASK
OVRFLO   EQU   X'20'                    MASK
PCI      EQU   X'20'                    MASK
MINKEY   EQU   X'01'                    MASK
CONCAT   EQU   X'FF'                    MASK
UTFMASK  EQU   X'02'               MASK FOR TOTALING FACILITY      UT18
UPDATEB  EQU   X'80'                    MASK                     S19015
*
*********************************************************************
*
* MASKS FOR ACCESS METHODS IN SAM
*
QSAMB    EQU   X'01'                    MASK
BSAMBIT  EQU   X'00'                    MASK
PAM      EQU   X'02'                    MASK
*
******************************************************************
*
*  EQUATES FOR OFFSET READ FUNCTION
*
AVTCEAPP EQU   13                       OFFSET TO CEA PTR IN AVT S19015
AVTABCEA EQU   17                       OFFSET TO ABNORMAL CEA   S19015
*                                       PTR                      S19015
CEAOFFST EQU   25                       OFFSET TO CEA PTR        S19015
ABOFFSET EQU   5                        OFFSET TO ABNORMAL CEA   S19015
*                                       PTR                      S19015
ADLEN    EQU   3                        LENGTH OF AN ADDR        S19015
TWOBYTES EQU   16                       NO OF BITS IN 2 BYTES    S19015
NOID     EQU   X'00'                    INDICATE NO MORE MODULES S19015
BFTEKR   EQU   X'20'                    BDAM VS                  S19015
*
*
* MASKS FOR MACRO'S TO BE USED
*
GETB     EQU   X'40'                    MASK
PUTB     EQU   X'40'                    MASK
READB    EQU   X'20'                    MASK
WRITEB   EQU   X'20'                    MASK
POINTB   EQU   X'04'                    MASK
CNTRLB   EQU   X'02'                    MASK
*
*
*********************************************************************
* MASKS FOR DEVICES TO BE SUPPORTED
*
*
*********************************************************************
*
*
*
*
*********************************************************************
*
* MASKS FOR STATUS BITS TO BE TESTED
*
*
EXCPN    EQU   X'30'                    MASK
COMPLETE EQU   X'80'                    MASK
FIRSTIOB EQU   X'01'                    MASK
*
*
*
*********************************************************************
*
* MASKS FOR BUFFER TECHNIQUE'S
*
CYCLICB  EQU   X'20'
EXCHANGB EQU   X'10'
SIMPLEB  EQU   X'40'
EXCHCYCL EQU   X'30'
*
* MASKS FOR OPEN OVERRIDE OPTIONS
OVRDMSK  EQU   X'C0'                    INDICATES OPEN OVERRIDE
CTDISP   EQU   X'10'                    DISPLACEMENT OF CT IN TBL
*
* MODES  OPERATION FOR QSAM
LOCATEB  EQU   X'08'
MOVEB    EQU   X'10'
SUBSTUTB EQU   X'04'
DATAMODE EQU   X'01'
* COMMAN YTES FOR THE 2841
SRCHE    EQU   X'31'
WRTCKD   EQU   X'1D'
RDC      EQU   X'92'
RDKD     EQU   X'8E'
RDD      EQU   X'86'
TIC      EQU   X'08'
* MASKS   RECORD FORMATS
STANDRDF EQU   X'08'
FORMATU  EQU   X'C0'
FORMATF  EQU   X'80'
FORMATV  EQU   X'40'
FORMATD  EQU   X'20'                    D FORMAT RECORDS         S20038
SPANNED  EQU   X'08'
BLOCKED  EQU   X'10'
FIXEDBLK EQU   X'90'
*   CHAN  CONTROL WORD FLAGS
SKIP     EQU   X'10'
SLI      EQU   X'20'
CC       EQU   X'40'
DATACH   EQU   X'A0'
SLICC    EQU   X'60'
SLICCSK  EQU   X'70'
KEYS     EQU   X'01'
QSAM     EQU   X'01'
GETMAIN  EQU   4
XCTL     EQU   7
LOAD     EQU   8
ASA      EQU   X'04'
BKREAD   EQU   X'01'
URBITS   EQU   X'40'
TAPEB    EQU   X'80'
DABIT    EQU   X'20'
R25      EQU   25                       CONSTANT
R3       EQU   3                        CONSTANT
R5       EQU   5                        CONSTANT
OFF      EQU   X'7F'                    MASK TO TURN OFF UPDATE  S19015
*                                           BIT
BYTE     EQU   1                        ONE CONSTANT             S19015
ABENDISP EQU   17                       ABEND DISPLACEMENT       S19015
IRBDISP  EQU   24                       IRB DISPLACEMENT         S19015
ADDISP   EQU   25                       ADDRESS DISPLACEMENT     S19015
IDLEN    EQU   2                        LENGTH OF A MODULE ID    Y02072
*
*
         BALR  RBASE,RE                 SET BASE
         USING SLDA,RBASE
         USING IHADCB,RDCB
         USING BLDEB,RDEB
         USING FORCORE,RCORE
*
*
SLDA     EQU   *
*
         B     BEGIN                    BRANCH AROUND MODULE ID  YM4640
         DC    C'IGG01993'              MODULE NAME              YM4640
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'10/22/74'              LAST SHIP DATE         @Z30TSCF
BEGIN    DS    0H                       END OF MODULE ID         YM4640
*
         L     RDCB,0(RPARC)            GET DCB ADDRESS
         LA    RDCB,0(RDCB)             CLEAR HI BYTE
         L     RCORE,4(RWTGC)           GET WORK AREA
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
         L     RTIOT,DEBAPPAD           GET APPENDAGE ADDRESS
         SR    RUCB,RUCB                CLEAR REG
         SR    RF,RF                    CLEAR RER
         SR    RD,RD
         IC    RD,DEBNMSUB              GET NBR OF MODULES LOADED
         IC    RUCB,DEBNMEXT            GET NUMBER OF EXTENTS
         IC    RF,DEBEXSCL              GET SCALING FACTOR
         SLL   RUCB,0(RF)               SHIFT ACCORDING TO EXTENT SIZE
         IC    RF,DEBAMLNG              GET ACCESS METHOD LENGTH
         LA    RUCB,0(RF,RUCB)          ADD TO POINT TO SUBRT ID FLD
         LA    RUCB,32(RUCB,RDEB)       ADD BASIC DEB SZ TO DEB AD
*
*  RUCB NOW POINTS TO THE SUBROUTINE ID FIELD IN THE DEB
*  BUT MUST BE INCREMENTED TO POINT BEYOND THE LAST ENTRY          UT18
*
SLDAA    EQU   *                                                   UT18
         CLI   0(RUCB),X'00'            IS RUCB BEYOND LAST ENTRY  UT18
         BE    SLDAB                    BR IF YES                  UT18
         LA    RUCB,2(RUCB)             INCREMENT TO NEXT ID FIELD UT18
         B     SLDAA                    TEST NEXT FIELD            UT18
*
SLDAB    EQU   *
         USING WTGENTRY,RWTGC                                    YM6834
         MVC   WTGIDTTR,SOP9            ID OF NEXT MODULE      @Z30TSCF
         TM    DCBCIND2,QSAMB          QSAM BEING USED           M4028
         BO    SLDAPP19                YES,BRANCH                M4028
         TM    DCBBFTEK,BFTEKR          VS BDAM SUPPORT NEEDED
         BO    SLDEOB6                  YES, BRANCH
*
*
SLDAPP19 EQU   *
*
*
         BAL   RF,EOBROUT               GET EOB RT ID
*
         BAL   RC,SLDROUT               LOAD ROUTINE
*
         TM    DCBCIND2,QSAMB           QSAM BEING USED
         BC    8,SLDEOB2                NO BRANCH
*
         MVC   DCBEOB+1(3),25(RWTG)    STORE EOB ROUTINE
         MVC   DCBCNTRL+3(1),DCBCNTRL+1  SETUP FOR QSAM CNTRL TEST
         TM    DCBDEVT,X'80'           DCB FOR TAPE
         BZ    SLDEOB8                 NO BRANCH
         B     SLDEOBC                 YES BRANCH
*
*
SLDEOB2  EQU   *
*
         MVC   DCBEOBR+1(3),25(RWTG)
*
SLDEOB20 EQU   *
*
         MVC   DCBEOBW+1(3),25(RWTG)
*
*
SLDEOB4  EQU   *
*
         BAL   RF,EOBROUT               GET POSSIBLE WRT ROUT (IN/OUT)
*
*
         LTR   RC,RC                    DCB FOR IN/OUT
         BC    8,SLDEOB6                NO BRANCH
*
         BAL   RC,SLDROUT               LOAD WRT EOB RT.
*
         MVC   DCBEOBW+1(3),25(RWTG)
*
*
SLDEOB6  EQU   *
*
         BAL   RF,EOBROUT               IS NOTE/POINT USED
*
         LTR   RC,RC                    TEST FOR ITS ID
         BP    SLDEOB9                  YES BRANCH
*
SLDEOB8  EQU   *
*
         BAL   RF,EOBROUT               GET POSSIBLE CNTRL RT ID
         LTR   RC,RC                    IS THERE A CNTRL ID
         BC    8,SLDEOBC                NO BRANCH
*
SLDEOB9  BAL   RC,SLDROUT               LOAD NOTE POINT ROUTINE
         MVC   DCBCNTRL+1(3),25(RWTG)
         B     SLDEOBE                  END
*
SLDEOBC  EQU   *
         XC    DCBCNTRL(4),DCBCNTRL     CLEAR PARA LIST
         OI    DCBCNTRL+3,X'01'         SET VECTOR  TO  ONE
*
SLDEOBE  EQU   *                        END.
*
         STC   RD,DEBNMSUB              STORE NO. SUB RTS LOADED
         MVC   DCBEROPT(1),DXCCW6       RESTORE EROPT FIELD       24346
*
***********************************************************************
*
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURR WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCR CURRENT DCB ENTRY PRTR
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   8,RBASE                  BACK TO OPEN CONTROL
*
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BC    7,RELOOP                 NO,CHECK NEXT ENTRY
*
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      REINITIALIZE WTG LIST PTR
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN                IF NOT TRANSFER CONTROL
*
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHEK                    COMPLETE
*
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
*
*
SLDROUT  EQU   *
         USING WTGMODNM,RWTG                                     Y02072
         MVC   WTGMODID,0(RB)           MOVE 2 BYTE ID TO NAME   Y02072
         DROP  RWTG                                              Y02072
         L     R1,CVTPTR                LOAD CVT ADDRESS         YM1140
         USING CVT,R1                   CVT ADDRESSABILITY       YM1140
         L     R1,CVTLINK               GET LPALIB DCB ADDRESS   YM1140
         DROP  R1                       DROP CVT BASE            YM1140
*
         LOAD  EPLOC=(RWTG),DCB=(1)                              YM1140
*
*
*
         ST    RE,24(RWTG)             TEMP STORE OF ROUTINE ADDR,
SLDROUT1 MVC   0(2,RUCB),0(RB)          STORE ROUTINES ID IN DEB
         LA    RUCB,2(RUCB)             UPDATE SUBRT PNTR
         LA    RD,1(RD)                 ONE MORE SUB RT LOADED
         BR    RC                      RETURN
*
*
***********************************************************************
*
*
***********************************************************************
*
EOBROUT  EQU   *
*
         SR    RC,RC
         LA    RE,EOBTABL               GET TABLE OF NAMES
         LA    RB,IDLEN                 LEN OF MODULE ID         Y02072
         IC    RC,DCBCNTRL+3            GET ID NO. DEVICE DEP.
         L     RJ,DCBCNTRL              SHIFT IDS RIGHT BY ONE BYTE
         SRL   RJ,8
         ST    RJ,DCBCNTRL
         MR    RB,RB                    CALCULATE WHERE ID IS
         LR    RB,RC                    PREPARE TO LOAD  MODULE
         AR    RB,RE                    POINT TO CORRECT ID
         BR    RF                       RETURN
*
***********************************************************************
*
*
*
*
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES
FOUR     DC    H'4'                     4                        M4567
OPIDCNST DC    C'0S'                    ID
AMIDCNST DC    C'93'                    THIS MODULE ID
SOP9     DC    C'11',VL3(IGG01911)      IGG01911 OR IECOS90X   @Z30TSCF
*
*
*        DEVICE DEPENDENT ROUTINES SUPPORTED
*
EOBTABL  EQU   *
         DC    C'  '                    BLANKS
EPD2     DC    C'CD'                    EOB RT DISK-OUTP/STANDRF INP
EGP2     DC    C'CC'                    EOB RT. DISK-TP-RDR. INP/OUTP.
DDA2     DC    C'CE'                    EOB RT. PRNTR/PUNCH NORMAL
DDB2     DC    C'CF'                    EOB RT. PRNTR/PUNCH ASA
NPD2     DC    C'BC'                    NOTE-POINT DISK
NPT2     DC    C'BD'                    NOTE-POINT TAPE
CNT1     DC    C'CA'                    CNTRL RT READER
CNT2     DC    C'CB'                    CNTRL RT. PRINTER
CNT3     DC    C'BE'                    CNTRL RT. TAPE
DUMY     DC    C'AV'                    DUMMY PUT/NOP ROUTINE
UTF1     DC    C'TD'                    USER TOTALING EOB ROUTINE  UT18
UTF2     DC    C'TC'                    USER TOTALING EOB ROUTINE  UT18
UTF3     DC    C'TV'                    USER TOTALING EOB ROUTINE  UT18
UTF4     DC    C'TW'                    USER TOTALING EOB ROUTINE  UT18
UTF5     DC    C'T2'                    USER TOTALING EOB ROUTINE  UT18
OVRD     DC    C'CT'                    OPEN OVERRIDE ERROR RTN
         SPACE
PATCH    DC    50X'0'                   PATCH AREA               YM4640
END      EQU   *                        END OF MODULE            Y02072
         TITLE 'IGG01993 - FORMAT OF GOTTEN CORE'
*
         IECDSECS  MAIN,WTG,PREFX,EXPAND=YES                   @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MODS ID             Y02072
*
CVT      DSECT
         CVT
*
*
BLDEB    DSECT
DEB      DS    0F                       ALIGN
DEBNMSUB DS    0CL1                     NO. OF SUB.
DEBTCBAD DS    CL4                      TCB ADDR.
DEBAMLNG DS    0CL1                     ACCESS METHOD LEN.
DEBDEBAD DS    CL4                      DEB ADDR.
DEBOGLGS DS    0CL1                     OPTION
DEBIRBAD DS    CL4                      IRB ADDRESS
DEBOPATB DS    0CL1                     OPTION
DEBSYSPG DS    CL4                      SYS ABEND
DEBNMEXT DS    0CL1                     NO. OF EXTENT
DEBUSRPG DS    CL4                      USER PROG.
DEBPRIOR DS    0CL1                     PRIORITY
DEBECBAD DS    CL4                      ECB ADDRESS.
DEBPROTG DS    0CL1                     PROTECTION KEY
DEBDEBID DS    0CL1                     DEB ID
DEBDCBAD DS    CL4                      DCB ADDRESS
DEBEXSCL DS    0CL1                     EXCEPTION
DEBAPPAD DS    CL4                      APPENDAGE ADDRESS
DEBDVMOD DS    0CL1                     DEVICE
DEBUCBAD DS    CL4                      UCB ADDRESS
DEBBINUM DS    CL2                      BIN NUMBER
DEBSTRCC DS    CL2                      START CYC.
DEBSTRHH DS    CL2                      START TRACK
DEBENDCC DS    CL2                      END CYCLINDER
DEBENDHH DS    CL2                      END TRACK
DEBNMTRK DS    CL2                      NO. OF TRACKS
*
           DCBD  DSORG=PS
DCBECC   EQU   DCBCHECK+1               DCB FIELD
         END
