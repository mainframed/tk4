1919  TITLE 'IGG01919 - SAM OPEN EXECUTOR (SECOND HALF OF IGG01913)'
***********************************************************************
*
IGG01919 CSECT
*
*MODULE NAME - IGG01919                                          Y02072
*
*DESCRIPTIVE NAME - LOAD ACCESS METHOD ROUTINES                  Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*
*        VS2 RELEASE 02 DELETIONS                                Y02072
*001010-001020,012000,061000,274500-286000,301500,302500,        Y02027
*303500-304500,367180-367261,367720-367780,367950-368070,425000, Y02072
*425500,541080-541840,583010-585000,585890,585910-585930,        Y02072
*586500,587000,517000,519000-533000,538000,50600-507000,511000,  Y02072
*516000,547000,573000,576000-583000,676000-677000                Y02072
*                                                                YM1141
*                                                                YM1140
*                                                                YM2507
*C367660                                                         YM5922
*                                                                YM1002
*        VS2 RELEASE 03 DELETIONS                               ZA02510
*511580                                                        @Z30TSCF
*
*          RELEASE 20 DELETIONS
*2370                                                            S20038
*0930308000-310000                                               A32160
*0930277000,363000,369000-370000,606000,672000-673000            S20201
*          RELEASE 21 DELETIONS
*D606000,673100                                                 XM5707
*0090015000-021000,030000,304000-317000,342000-359000,415000,    M1753
*0090421000-422000,431000-432000,434000-484000,554000-572000,    M1753
*0090584000-605000,606000,608000-609000,646000-668000,669000,    M1753
*0090671700-672400                                               M1753
*
*STATUS CHANGE LEVEL 006
*
*FUNCTION/OPERATION - THIS MODULE IS A CONTINUATION OF MODULE      UT18
*    IGG01913 AND LOADS I/O APPENDAGES AND AN ASYNCHRONOUS ROUTINE,
*    AS APPROPRIATE.  THE ENTRY POINTS OF THE LOADED APPENDAGES ARE
*    PLACED IN THE APPENDAGE VECTOR TABLE.  FOR TRACK OVERFLOW, A
*    CIRB MACRO IS ISSUED, AND THE ENTRY POINT OF THE ASYNCHRONOUS
*    ROUTINE IS PLACED IN THE IRB.  ID'S FOR ALL LOADED MODULES ARE
*    PLACED IN THE SUBROUTINE ID SECTION OF THE DEB.
*
*ENTRY POINTS - ENTERED ONLY FROM IGG01913 BY USE OF XCTL MACRO    UT18
*
*INPUT: SEE DESCRIPTION OF REGISTERS, DCB(USERS)
*
*OUTPUT: DCB(USERS),IOB(USERS).
*
*EXTERNAL ROUTINES: NONE.
*EXITS - NORMAL: XCTL TO IGG01926
*
*EXITS-ERROR: NONE.
*
*TABLES/WORKAREAS- WHERE TO GO TABLE (WTG)
*
*      BYTE  0-7  NAME
*      BYTE  8-10 RELATIVE DISK ADDRESS(TTR) OF FIRST RECORD
*      BYTE 11    CONCATENATION NUMBER
*      BYTE 12    ZERO
*      BYTE 13    ALIAS INDICATOR AND USER DATA FIELD DESCRIPTOR.
*                        ALIAS INDICATOR 1 BIT
*                        NUMBER OF TTRS IN USERS DATA FIELD 2 BITS
*      BYTE 14-16 TTR OF FIRST TEXT RECORD
*      BYTE 17    ZERO
*      BYTE 18-19 TTR OF NOTE LIST OR SCATTER LIST
*      BYTE 20    TRANSLATION TABLE
*      BYTE 21    ZERO OR NUMBER OF ENTRIES IN THE NOTE LIST
*      BYTE 22-23 ATTRIBUTES
*      BYTE 24-26 TOTAL CONTIGUOUS MAIN STORAGE REQUIRED FOR MODULE
*      BYTE 27-28 LENGTH OF FIRST TEXT RECORD
*      BYTE 29    LENGTH OF WTG TABLE (IN DOUBLE WORDS)
*      BYTE 30-31 PATH THROUGH LOADS OF ROUTINES
*      BYTE 32-36 IDTTR OF EXECUTOR FOR FIRST DCB
*      BYTE 37-39 WORKAREA ADDRESS FOR FIRST DCB
*      BYTE 40-   TABLE OF IDTTR'S
*      BYTE 32+N(8) IDTTR OF EXECUTOR FOR N TH DCB   (5 BYTES)
*                   WORKAREA ADDRESS FOR N TH DCB    (3 BYTES)
*                   IDTTR OF LAST ROUTINE LOAD       (5 BYTES)
*                   NOT USED                         (3 BYTES)
*
*ATTRIBUTES: REENTRANT, REUSABLE, RUNS IN DATA MANAGEMENT KEY,   Y02072
*            RUNS IN SUPERVISOR STATE                            Y02072
*
*NOTES: THIS ROUTINE HAS A LIST OF ALL BSAM, QSAM MODULES SUPPORTED.
*
***********************************************************************
         EJECT
***********************************************************************
*  REGISTER CONVENTIONS USED THROUGH OUT ALL OPEN PASSES
***********************************************************************
*
RE       EQU   0         WORK REG5
RF       EQU   1         WORK REG6
R1       EQU   RF                       PARM REGISTER            Y02072
RDCB     EQU   2         DCB REGISTER
RBASE    EQU   3         BASE REGISTER
RCORE    EQU   4         WORK AREA ADDRESS
RPAR     EQU   5         TOP OF PARAMETER LIST
RWTG     EQU   6         TOP OF WTG TABLE
RPARC    EQU   7         CURRENT PARAMETER
RWTGC    EQU   8         CURRENT TRANS LOAD
RTIOT    EQU   9         USED HERE AS WRK REG AND  COMM VECTOR ADDR.
RUCB     EQU   10        USED HERE AS A COUNTER IN IOB GENERATION
RDEB     EQU   11        DEB ADDRESS
RB       EQU   12        WORK REG1  **
RWK2     EQU   RB                       WORK REGISTER            Y02072
RC       EQU   13        WORK REG2  **  USED IN IOB GENERATION
RBRNCH   EQU   RC                       WORK REGISTER            Y02072
RD       EQU   14        WORK REG3  **
RJ       EQU   15        WORK REG4
RWK1     EQU   RJ                       WORK REGISTER            Y02072
         SPACE 2
OUTP     EQU   X'0F'
TAPEB    EQU   X'80'
SDINDIC  EQU   X'08'                    SEARCH DIRECT INDICATOR  S20201
NRECREDY EQU   X'20'
D6       EQU   6                        DISPLACEMENT OF SIX
SIX      EQU   6
ASISVLN  EQU   9                        DW FOR IRB WKA           YM1141
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES
         EJECT
         BALR  RBASE,0
         USING *,RBASE
         B     BEGIN                    BR AROUND CONSTANTS      Y02072
         DC    C'IGG01919'              MOD NAME                 Y02072
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'10/24/74'              LAST DATE MODIFIED     @Z30TSCF
BEGIN    DS    0H                                                Y02072
         L     RDCB,0(RPARC)            GET DCB ADDRESS
         LA    RDCB,0(RDCB)             CLEAR HI BYTE
         USING IHADCB,RDCB
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
         USING BLDEB,RDEB
         L     RCORE,4(RWTGC)           GET WORK AREA
         USING FORCORE,RCORE
         MVI   22(RWTG),X'CB'          CHANGE DE ATTR.TO ONLY LOADABLE
         L     RTIOT,DEBAPPAD           GET APPENDAGE ADDRESS
         USING AVT,RTIOT
         SR    RUCB,RUCB                CLEAR REG
         SR    RF,RF                    CLEAR RER
         IC    RUCB,DEBNMEXT            GET NUMBER OF EXTENTS
         IC    RF,DEBEXSCL              GET SCALING FACTOR
         SLL   RUCB,0(RF)               SHIFT ACCORDING TO EXTENT SIZE
         IC    RF,DEBAMLNG              GET ACCESS METHOD LENGTH
         LA    RUCB,0(RF,RUCB)          ADD TO POINT TO SUBRT ID FLD
         LA    RUCB,32(RUCB,RDEB)       ADD BASIC DEB SZ TO DEB AD
*
*  RUCB NOW POINTS TO THE SUBROUTINE ID FIELD IN THE DEB AND MUST  UT18
*  NOW BE INCREMENTED TO POINT BEYOND THE LAST ENTRY.              UT18
*                                                                  UT18
SLDAA    CLI   0(RUCB),X'00'            IS RUCB BEYOND LAST ENTRY  UT18
         BE    SLDAB                    BR IF YES                  UT18
         LA    RUCB,2(RUCB)             INCREMENT TO NEXT ID FIELD UT18
         B     SLDAA                    TEST NEXT FIELD            UT18
*                                                                  UT18
*  RUCB NOW POINTS TO THE NEXT AVAILABLE SUBROUTINE ID FIELD IN    UT18
*  THE DEB.                                                        UT18
*
SLDAB    EQU   *
         TM    DCBDEVT,TAPEB            IS THIS A TAPE DEVICE    S20038
         BO    SLDAPP14                 YES, BRANCH-IT'S PCI     S20038
         TM    0(RPARC),OUTP            DCB FOR OUTPUT ONLY
         BO    LAB001                   YES,BRANCH               S20201
         TM    DCBRECFM,DCBRECTO        DCB FOR TRACK OVERFLOW
         BZ    SLDAPP04                 NO,MUST BE PCI
         TM    JFCBMASK+D6,SDINDIC      SEARCH DIRECT USED       S20201
         BNO   NOSDOPT                  BRANCH NO                S20201
         LA    RB,CEAFP                 GET SEARCH DIRECT CE     S20201
         BAL   RC,SLDROUT               LOAD ROUTINE             S20201
         ST    RE,AVTCE                 APPENDAGE TABLE ENTRY FOR CE
         LA    RB,AEOESF                GET END OF EXTENT APPEN  S20201
         BAL   RC,SLDROUT               LOAD IT IN AND PUT       S20201
         ST    RE,AVTEOE                APPENDAGE TABLE ENTRY FOR EOE
         B     SLDAPP19                 AROUND NEXT              S20201
LAB001   EQU   *                                                 S20201
         TM    JFCBMASK+SIX,NRECREDY    IS IT RECORD READY       S20201
         BO    SLDAPP06                 NO, BRANCH TO PROCESS    S20201
*                                       OUTPUT
         TM    DCBCIND2,DCBCNCHS        IS IT CHAINED SCHEDULING YM5922
         BO    SLDAPP06                 YES, BRANCH AROUND       S20201
*
*                       DETERMINE CHANNEL END APPENDAGE
*
         LA    RB,ACEU                  IGG019C0 FOR FORMAT U
         TM    DCBRECFM,DCBRECU         IS IT FORMAT U?
         BC    9,SLDAPP03               YES, LOAD APPENDAGE
         LA    RB,ACEF                  IGG019CI FOR FIXED
         TM    DCBRECFM,DCBRECF         TEST FOR FIXED FORMAT
         BO    SLDAPP03                 BRANCH IF SO
         LA    RB,ACEV                  ELSE MUST BE VARIABLE,
*                                       USE IGG019CJ
SLDAPP03 BAL   RC,SLDROUT               GO LOAD MODULE
         ST    RE,AVTCE                 PUT ADDRESS IN
*                                       APPENDAGE TABLE.
         B     SLDAPP06
NOSDOPT  EQU   *
         LA    RB,TOABNE           GET ID OF TO ABNE APPENDAGE    M3320
         BAL   RC,SLDROUT               LOAD ROUTINE              M3320
         ST    RE,AVTABNE               APPENDAGE TABLE ENTRY FOR
*                                       ABNORMAL END APPENDAGE
         TM    DCBRECFM,DCBRECU         FORMAT U RCDS            S20201
         BO    M670                     BRANCH IF YES            S20201
         LA    RB,ACEF                  GET F LN CHECK APPEND
         TM    DCBRECFM,DCBRECF+DCBRECBR  FIXED BLOCK RECORDS
         BO    SLDAPP02                YES,GO LOAD ROUTINE
         TM    DCBRECFM,DCBRECV        VARIABLE RECORDS
         BZ    M670                    NO, BRANCH                 M3320
         LA    RB,ACEV                 GET V LN CHECK APPENDAGE
SLDAPP02 EQU   *
         BAL   RC,SLDROUT              LOAD THE ROUTINE
         ST    RE,AVTCE                 APPENDAGE TABLE ENTRY FOR CE
         B     ASY                                                M3320
M670     EQU   *                                                  M3320
*                                       ABNORMAL END ENTRY POINT
         AH    RE,FOUR                  PLUS 4 IS CE ENTRY POINT
         ST    RE,AVTCE                 CHANNEL END ENTRY, AVT
ASY      LA    RB,ASYNCH           GET ASYNCH ID                  M3320
         BAL   RC,SLDROUT              LOAD ROUTINE
* WHEN RETURNING FROM 'SLDROUT',REGISTER 0 CONTAINS THE ADDRESS
*  OF THE ASYNCHRONOUS ERROR ROUTINE
*
*        BUILD IRB FOR IGG019C1 - WORKAREA IS USED IN 19C3
*
         CIRB  EP=(0),KEY=PP,MODE=PP,STAB=(RE),WKAREA=ASISVLN    YM1141
*
         ST    RF,24(RWTG)             STORE ADDRESS OF IRB TEMPORARY
         MVC   DEBIRBAD+1(3),25(RWTG)   PUT IRB ADDRESS IN DEB
         LA    RB,AEOET                GET T. O. EOE ID
         B     SLDAPP05
SLDAPP04 EQU   *
*
*  END OF EXTENT APPENDAGE NEEDED FOR DISK INPUT
*
         TM    DCBDEVT,X'20'           D.A. DEVICE
         BZ    SLDAPP14                NO,BRANCH
         LA    RB,AEOEP                GET END OF EXTENT ID FOR PCI
SLDAPP05 EQU   *
         BAL   RC,SLDROUT              LOAD ROUTINE
         ST    RE,AVTEOE                APPENDAGE TABLE ENTRY FOR EOE
SLDAPP06 EQU   *
         TM    DCBRECFM,DCBRECTO        DCB FOR TRACK OVERFLOW
         BO    SLDAPP19                BRANCH IF YES; NO MORE    M1753
*                                      APPENDAGES
*
SLDAPP14 EQU   *
*
* BEFORE LOADING CHAINED SCHEDULING APPENDAGE, TEST FOR A REAL   Y02072
* ADDRESS ENVIRONMENT.  IF V=R, LOAD IGG019V6 FROM SVCLIB        Y02072
* INSTEAD OF IGG019CU FROM LPALIB AND STORE PCI ENTRY POINT.     Y02072
* IF VIRTUAL, LOAD IGG019CU AND DO NOT STORE PCI ENTRY POINT     Y02072
* IN THE AVT.                                                    Y02072
*
         LA    RB,ACEO                  GET ID OF PCI/CE & ABNE APPEND
         LA    RBRNCH,SLDAPP17          RETURN ADDRESS           Y02072
         L     RWK1,DEBTCBAD            GET TCB ADDRESS          Y02072
         USING TCB,RWK1                                          Y02072
         TM    TCBFLGS6,TCBRV           IS TASK VIRT=REAL        Y02072
         DROP  RWK1                                              Y02072
         BZ    SLDROUT                  N0, BRANCH               Y02072
         LA    RWK2,VRPCI               GET V=R PCI APPENDAGE    Y02072
         BAL   RBRNCH,SLDROUT1          GO GET PROPER APPENDAGE  Y02072
         ST    RE,AVTPCI                PCI ENTRY IN AVT
SLDAPP17 AH    RE,SECENTRY              EPA FOR CE OR ABNE       Y02072
         ST    RE,AVTCE                 CHANNEL END ENTRY,AVT
         ST    RE,AVTABNE               ABNORMAL END ENTRY,AVT
SLDAPP19 EQU   *
         MVC   0(5,RWTGC),NEXTMOD      PUT NEXT EXECUTOR'S ID &  M1753
*                                      TTR IN WTG TABLE.
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURR WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCR CURRENT DCB ENTRY PRTR
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   8,RBASE
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BNE   RELOOP                   NO,CHECK NEXT ENTRY
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      REINITIALIZE WTG LIST PTR
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BNE   TCTLRTN                  IF NOT TRANSFER CONTROL
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHEK
TCTLRTN  EQU   *
         USING WTG,RWTG
         USING WTGENTRY,RWTGC
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTG,RWTGC                                      @Z30TSCF
*
*  COME HERE TO 'LOAD' ROUTINES
*
SLDROUT  EQU   *                        DCB ADDR ZERO FOR LOADS  Y02072
         L     RF,CVTPTR                COMM VECTOR PTR         ZA02510
         USING CVT,RF                   CVT ADDRESSABILITY      ZA02510
         L     R1,CVTLINK               GET LINKLIB DCB ADDR    ZA02510
         B     SLDROUT2                 GO LOAD ROUTINE          Y02072
*
*  COME HERE TO LOAD IGG019V6 FROM SVC LIB
*
SLDROUT1 EQU   *
         L     RF,CVTPTR(0,0)           GET COMM VECTOR
         USING CVT,RF                   CVT ADDRESSABILITY       Y02072
         L     RF,CVTSVDCB              GET SVCLIB DCB ADDR      YM1002
         USING WTG,RWTG                                          Y02072
SLDROUT2 MVC   WTGMODID,0(RB)           ID TO MODULE NAME        Y02072
         DROP  RWTG,RF                                           YM1140
*
         LOAD  EPLOC=(RWTG),DCB=(1)     LOAD ACCESS METHOD RTN   Y02072
*
         ST    RE,24(RWTG)              TEMP STORE OF RTN ADDR
         SR    RD,RD
         IC    RD,DEBNMSUB              GET NO OF SUBROUTINES LOADED
         LA    RD,1(RD)                 ADD ONE TO TOTAL
         STC   RD,DEBNMSUB              STORE NEW TOTAL OF SUBRT LOADED
         MVC   0(2,RUCB),0(RB)          STORE ROUTINES ID IN DEB Y02072
         LA    RUCB,2(RUCB)             UPDATE SUBRT PNTR
         BR    RC                      RETURN
         EJECT
FOUR     DC    H'4'                                              M4573
SECENTRY EQU   FOUR                     SECONDARY ENTRY POINT    Y02072
OPIDCNST DC    C'0S'
AMIDCNST DC    C'19'                    ID OF THIS MODULE          UT18
*
*        FOLLOWING LIST DEFINES FUNCTION OF MODS IN XCTL TABLE  XM5707
*
*        AEOEP,CZ                       EOE APPENDAGE FOR PCI   XM5707
*        AEOET,CH                       EOE APPEN NON-SF-INPUT  XM5707
*        ACEO,CU                        CE, PCI, XCE APP - V=V  Y02072
*        VRPCI,V6                       CE, PCI, XCE APP - V=R  Y02072
*        ACEF,CI                        CE APPEN LEN CHK F REC  XM5707
*        ACEV,CJ                        CE APPEN LEN CK V REC   XM5707
*        TOABNE,C3                      TRK OVFL XCE APPEN      XM5707
*        ASYNCH,C1                      TRK OVFL ASY ERROR RTN  XM5707
*        ACEU,C0                        CE FOR FORMAT U REC     XM5707
*        CEAFP,FP                       SCH DIRECT CE APPEN     XM5707
*        AEOESF,C4                      SCH DIR EOE APPEN       XM5707
*
*
*  MODULE ID'S
*
NEXTMOD  DC    C'26',VL3(IGG01926)                             @Z30TSCF
AEOEP    DC    C'CZ'                                             Y02072
AEOET    DC    C'CH'                                             Y02072
ACEO     DC    C'CU'                                             Y02072
ACEF     DC    C'CI'                                             Y02072
ACEV     DC    C'CJ'                                             Y02072
TOABNE   DC    C'C3'                                             Y02072
ASYNCH   DC    C'C1'                                             Y02072
ACEU     DC    C'C0'                                             Y02072
CEAFP    DC    C'FP'                                             Y02072
         DC    C'  '                    FILLER FOR IGG019FN      YM2507
AEOESF   DC    C'C4'                                             Y02072
VRPCI    DC    C'V6'                                             Y02072
         SPACE
PATCH    DC    25H'0'                   PATCH AREA               Y02072
END      EQU   *                        END OF MODULE            Y02072
         EJECT
         IECDSECS  MAIN,WTG,PREFX,EXPAND=YES                   @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MODS ID             Y02072
         EJECT
AVT      DSECT                          APPENDAGE VECTOR TABLE
AVTEOEPG DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTEOE   DS    A                        ADDR OF END OF EXTENT
*                                       APPENDAGE
AVTSIOPG DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTSIO   DS    A                        ADDR OF START I/O
*                                       APPENDAGE
AVTPCIPG DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTPCI   DS    A                        ADDR OF PCI APPENDAGE
AVTCEPG  DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTCE    DS    A                        ADDR OF CHANNEL END
*                                       APPENDAGE
AVTABNPG DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTABNE  DS    A                        ADDR OF ABNORMAL END
*                                       APPENDAGE
         EJECT
CVT      DSECT
         CVT
         EJECT
BLDEB    DSECT
DEB      DS    0F
DEBNMSUB DS    0CL1
DEBTCBAD DS    CL4
DEBAMLNG DS    0CL1
DEBDEBAD DS    CL4
DEBOGLGS DS    0CL1
DEBIRBAD DS    CL4
DEBOPATB DS    0CL1
DEBSYSPG DS    CL4
DEBNMEXT DS    0CL1
DEBUSRPG DS    CL4
DEBPRIOR DS    0CL1
DEBECBAD DS    CL4
DEBPROTG DS    0CL1
DEBDEBID DS    0CL1
DEBDCBAD DS    CL4
DEBEXSCL DS    0CL1
DEBAPPAD DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    CL4
DEBBINUM DS    CL2
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
         EJECT
           DCBD  DSORG=PS
         EJECT
         IKJTCB
           END
