 TITLE 'IGG01917 - SAM OPEN EXECUTOR (SECOND HALF OF IGG01910)'    UT18
IGG01917 CSECT
*MODULE NAME - IGG01917                                          Y02072
*
*DESCRIPTIVE NAME - LOADS ACCESS METHOD ROUTINES                 Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          VS 2 RELEASE 2 DELETIONS                              Y02072
*001100,011000,020000-021000,031000,038000,256200-256600,283500, Y02072
*284500,285500-286500,342480-342704,343280-343760,353530-353950, Y02072
*360300-367150,390000-394000,399000-402000,413000,505050-505900, Y02072
*598040-598096,623300,624200,624240-624400,335500-337500,342000  Y02072
*381010-381100,594200-594800,624100-624500,403000,479000-480000, Y02072
*484000,489000-501000,537000-544000                              Y02072
*                                                                YM1140
*                                                                YM2507
*                                                                YM4640
*D381600-381800                                                  YM5922
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*C371000-371100                                                @OS73907
*A371420-371424                                                @OS73907
*          RELEASE 20 DELETIONS
*0872                                                            S20038
*0872290000-292000                                               A32160
*0872259000,343000,345000,352000,568000,626000                   S20201
*          RELEASE 21 DELETIONS
*0090                                                            A49371
*0090                                                             M0062
*D139000-151000,261000-266000,325000-335000,337000              SA53194
*D381020-381040,381050                                          SA53194
*0090286000-299000,326000-338000,548000-564000,568000,625600     S21040
*0090                                                            A42906
*          VS2.037 CHANGES
*C280030,280040                                               @ZA28034
*A368000-369000                                               @ZA28034
*STATUS CHANGE LEVEL 005
*
*FUNCTION/OPERATION - THIS MODULE IS A CONTINUATION OF MODULE      UT18
*      IGG01910 AND LOADS THE REMAINING I/O MODULES.  WHAT IS TO   UT18
*      BE LOADED IS DETERMINED FROM THE CODES IN THE DCBCNTRL      UT18
*      FIELD WHICH WAS SET BY PREVIOUS LOADS OF THE OPEN EXECUTORS.UT18
*      IT WILL PLACE THE ADDRESS OF THE LOADED MODULES IN THE
*      APPROPRIATE DCB FIELDS, AND THE ID'S IN THE DEB.
*      IT WILL BRING IN THE APPROPRIATE END-OF-BLOCK(EOB) ROUTINE
*      IT WILL BRING IN A SIO APPENDAGE FOR PRINTERS THAT        Y02072
*      REQUIRE IT.                                               Y02072
*
*ENTRY POINTS - ENTERED ONLY FROM IGG01910 BY USE OF XCTL MACRO    UT18
*
*INPUT: SEE DESCRIPTION OF REGISTERS, DCB(USERS)
*
*OUTPUT: DCB(USERS),IOB(USERS).
*
*EXTERNAL ROUTINES: NONE.
*
*EXITS-NORMAL: XCTL TO IGG01911(IECOSOP9)                        Y02072
*
*EXITS-ERROR: NONE.
*
*TABLES/WORKAREAS               ***************************************
***********************************************************************
*
*ATTRIBUTES: REENTRANT, REUSABLE, SUPERVISOR STATE,              Y02072
*            RUNS IN DATA MANAGEMENT KEY                         Y02072
*
*NOTES: THIS ROUTINE HAS A LIST OF ALL BSAM, QSAM MODULES SUPPORTED.
*
***********************************************************************
*
*
*  REGISTER CONVENTIONS USED THROUGH OUT ALL OPEN PASSES
*
***********************************************************************
*
RE       EQU   0         WORK REG5
RF       EQU   1         WORK REG6
R1       EQU   RF        PARM REG                                YM1140
RDCB     EQU   2         DCB REGISTER
RBASE    EQU   3         BASE REGISTER
RCORE    EQU   4         WORK AREA ADDRESS
RPAR     EQU   5         TOP OF PARAMETER LIST
RWTG     EQU   6         TOP OF WTG TABLE
RPARC    EQU   7         CURRENT PARAMETER
RWTGC    EQU   8         CURRENT TRANS LOAD
RTIOT    EQU   9         USED HERE AS WRK REG AND  COMM VECTOR ADDR.
RUCB     EQU   10        USED HERE AS A COUNTER IN IOB GENERATION
RDEB     EQU   11        DEB ADDRESS
RB       EQU   12        WORK REG1  **
RC       EQU   13        WORK REG2  **  USED IN IOB GENERATION
RD       EQU   14        WORK REG3  **
RJ       EQU   15        WORK REG4
*
*
PARAR    EQU   RPARC
*
***********************************************************************
*
***********************************************************************
*
* MASK'S TO TEST PARAMETER LIST WITH
*
*********************************************************************
INP      EQU   X'0F'
OUTP       EQU   X'0F'
UPDATE     EQU   X'04'
INOUT      EQU   X'03'
OUTIN      EQU   X'07'
RDBACK     EQU   X'01'
INPRDBK  EQU   X'0E'               MASK TO CK INPUT AND RDBACK     UT18
*
*
*********************************************************************
*
*
* OFFSET FOR PARAMETER TESTS
*
ONE        EQU   X'01'
FRTYATE    EQU   48
DISPSN     EQU   X'0'
*
*
*********************************************************************
*
* OFFSETS FOR DEB REFERENCE
*
*
BIN      EQU   36
BEXT     EQU   38
*
*
*
*********************************************************************
*
* MASK'S USED TO TEST OPTION FIELDS IN DCB
*
*
*
DOUBLE     EQU   X'01'
*
*
STOWB      EQU   X'80'
WRTCK      EQU   X'80'
OVRFLO     EQU   X'20'
PCI        EQU   X'20'
MINKEY     EQU   X'01'
CONCAT     EQU   X'FF'
UTFMASK  EQU   X'02'                    MASK FOR TOTALING FACILITY UT18
BYPASS   EQU   X'10'                    BYPASS DOS EMBED CHKPT   S21040
*                                       RECORDS MASK, TAPE FILE
*
*********************************************************************
*
* MASKS FOR ACCESS METHODS IN SAM
*
QSAMB      EQU   X'01'
BSAMBIT    EQU   X'00'
PAM        EQU   X'02'
*
*
* MASKS FOR MACRO'S TO BE USED
*
GETB       EQU   X'40'
PUTB       EQU   X'40'
READB      EQU   X'20'
WRITEB     EQU   X'20'
POINTB     EQU   X'04'
CNTRLB     EQU   X'02'
*********************************************************************
*
* MASKS FOR STATUS BITS TO BE TESTED
*
*
EXCPN      EQU   X'30'
COMPLETE   EQU   X'80'
FIRSTIOB   EQU   X'01'
*
*
*
*********************************************************************
*
* MASKS FOR BUFFER TECHNIQUE'S
*
CYCLICB    EQU   X'20'
EXCHANGB   EQU   X'10'
SIMPLEB    EQU   X'40'
EXCHCYCL   EQU   X'30'
*
*********************************************************************
*
*
* MODES OF OPERATION FOR QSAM
*
LOCATEB    EQU   X'08'
MOVEB      EQU   X'10'
MOVEXCHB EQU   X'14'
SUBSTUTB   EQU   X'04'
*
*
*********************************************************************
*
*
* COMMAND BYTES FOR THE 2841
*
*       CONTROL UNIT
*
*
*
SRCHE      EQU   X'31'
WRTCKD     EQU   X'1D'
RDC        EQU   X'92'
RDKD       EQU   X'8E'
RDD        EQU   X'86'
TIC        EQU   X'08'
*
*
*********************************************************************
*
*
* MASKS FOR RECORD FORMATS
*
*
STANDRDF   EQU   X'08'
FORMATU    EQU   X'C0'
FORMATF    EQU   X'80'
FORMATV    EQU   X'40'
FORMATD  EQU   X'20'                    D FORMAT RECORDS         S20038
BLOCKED    EQU   X'10'
FIXEDBLK   EQU   X'90'
*
*
*********************************************************************
*
*
*   CHANNEL CONTROL WORD FLAGS
*
SKIP       EQU   X'10'
SLI        EQU   X'20'
CC         EQU   X'40'
DATACH     EQU   X'A0'
SLICC      EQU   X'60'
SLICCSK    EQU   X'70'
*
*
*********************************************************************
*
*
KEYS       EQU   X'01'
QSAM       EQU   X'01'
*
*
*********************************************************************
*
GETMAIN    EQU   4
XCTL       EQU   7
LOAD       EQU   8
*
*********************************************************************
*
*
**********************************************************************
ASA        EQU   X'04'
BKREAD     EQU   X'01'
URBITS   EQU   X'40'
TAPEB    EQU   X'80'
DABIT    EQU   X'20'
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES
*
**********************************************************************
D3525    EQU   X'4C'               3525 DEVICE                  SA53194
RP       EQU   X'60'               PRT/PCH ASSOC D.S.           SA53194
RW       EQU   X'50'               RD/PCH ASSOC D.S             SA53194
PW       EQU   X'30'               RD/PRT ASSOC D.S.            SA53194
ASSOCDS  EQU   X'04'               3525 ASSOCIATED D.S          SA53194
SIX      EQU   6                                                 S20201
NRECREDY EQU   X'20'                                             S20201
THREE    EQU   3                        CONSTANT OF 3            S20201
FIVE     EQU   5                        CONSTANT OF 5            S20201
TWENTY5  EQU   25                       CONSTANT OF 25           S20201
ABENDISP EQU   17                       OFFSET IN AVT ABE ADDR   S20201
ADLEN    EQU   3                        ADDR LENGTH              S20201
ADDISP   EQU   25                       DISP OF LOADS RTRN ADDR  S20201
STANDF   EQU   X'08'                                             S20201
SDINDIC  EQU   X'08'                    SEARCH DIRECT INDICATOR  S20201
TRKOVFL  EQU   X'20'                    TRACK OVERFLOW MASK      S20201
ZERO     EQU   0                        DISPLACEMENT OF 0        S20201
D1       EQU   1                        DISPLACEMENT OF 1        S20201
D5       EQU   5                        DISPLACEMENT OF 5        S20201
D6       EQU   6                        DISPLACEMENT OF 6        S20201
D13      EQU   13                       DISPLACEMENT OF 13       S20201
D17      EQU   17                       DISPLACEMENT OF 17       S20201
D25      EQU   25                       DISPLACEMENT OF 25       S20201
TEMPSAVE EQU   24                       OFFSET TO TEMP SAVE ADDR  M5467
*                                         OF LOADED ROUTINES      M5467
IDLEN    EQU   2                        LENGTH OF A MODULE ID    Y02072
         EJECT
         BALR  RBASE,0
         USING SLDA,RBASE
         USING IHADCB,RDCB
         USING BLDEB,RDEB
         USING FORCORE,RCORE
*
*
SLDA     EQU   *
*
         B     BEGIN                    BRANCH AROUND MODULE ID  YM4640
         DC    C'IGG01917'              MODULE NAME              YM4640
         DC    C'@ZA28034'              LAST SHIP CODE         @ZA28034
         DC    C'02/02/78'              LAST SHIP DATE         @ZA28034
BEGIN    DS    0H                       END OF MODULE ID         YM4640
*
         L     RDCB,0(RPARC)            GET DCB ADDRESS
         LA    RDCB,0(RDCB)             CLEAR HI BYTE
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
         L     RCORE,4(RWTGC)           GET WORK AREA
*
         MVI   22(RWTG),X'CB'          CHANGE DE ATTR.TO ONLY LOADABLE
*
         L     RTIOT,DEBAPPAD           GET APPENDAGE ADDRESS
         USING AVT,RTIOT
         SR    RUCB,RUCB                CLEAR REG
         SR    RF,RF                    CLEAR RER
         IC    RUCB,DEBNMEXT            GET NUMBER OF EXTENTS
         IC    RF,DEBEXSCL              GET SCALING FACTOR
         SLL   RUCB,0(RF)               SHIFT ACCORDING TO EXTENT SIZE
         IC    RF,DEBAMLNG              GET ACCESS METHOD LENGTH
         LA    RUCB,0(RF,RUCB)          ADD TO POINT TO SUBRT ID FLD
         LA    RUCB,32(RUCB,RDEB)       ADD BASIC DEB SZ TO DEB AD
*
*  RUCB NOW POINTS TO THE SUBROUTINE ID FIELD IN THE DEB AND MUST  UT18
*  NOW BE INCREMENTED TO POINT BEYOND THE LAST ENTRY.              UT18
*                                                                  UT18
SLDAA    CLI   0(RUCB),X'00'            IS RUCB BEYOND LAST ENTRY  UT18
         BE    SLDAB                    BR IF YES                  UT18
         LA    RUCB,2(RUCB)             INCREMENT TO NEXT ID FIELD UT18
         B     SLDAA                    TEST NEXT FIELD            UT18
*                                                                  UT18
*                                                                  UT18
*                                                                  UT18
*  RUCB NOW POINTS TO THE NEXT AVAILABLE SUBROUTINE ID FIELD IN    UT18
*  THE DEB.                                                        UT18
SLDAB    EQU   *
***********************************************************************
*      GET THE EOB MODULES NEEDED FOR THIS FILE
*
         TM    DCBDEVT,URBITS           UNIT RECORD DEVICE       A42906
         BO    SLDAPP01                 YES,BRANCH               A42906
         TM    JFCBMASK+D6,SDINDIC      SEARCH DIRECT USED       S20201
         BNO   NOSDOPT                  BRANCH NO                S20201
         LA    RB,CEAFP                 GET SEARCH DIRECT CE     S20201
         BAL   RC,SLDROUT               LOAD ROUTINE             S20201
         ST    RE,AVTCE                 APPENDAGE TABLE ENTRY FOR CE
         LA    RB,AEOESF                GET END OF EXTENT APPEN  S20201
         BAL   RC,SLDROUT               LOAD IT IN AND PUT       S20201
         ST    RE,AVTEOE                APPENDAGE TABLE ENTRY FOR EOE
         B     SLDAPP17                 AROUND NEXT              Y02072
*
NOSDOPT  EQU   *                                                 S20201
*
         TM    0(RPARC),OUTP            DCB FOR OUTPUT ONLY
         BO    SLDAPP10                 AROUND NEXT              S20201
*
*  END OF EXTENT  APPENDAGE NEEDED FOR DISK INPUT
*
         TM    DCBDEVT,X'20'           D.A. DEVICE
         BZ    SLDAPP01                NO BRANCH
         TM    DCBCIND1,X'40'          SEARCH DIRECT INPUT
         BNO   LAB04                   NO LOAD CH                S20201
         TM    JFCBMASK+SIX,NRECREDY    IS IT NON RPS            S20201
         BO    SLDAPP01                 YES BRANCH AROUND        S20201
         LA    RB,AEOESF                GET END OF EXTENT        S20201
*                                       APPENDAGE                S20201
*                                       FOR STANDARD FIXED
         B     LAB05                    GO AROUND NEXT           S20201
*
LAB04    EQU   *                                                 S20201
         LA    RB,AEOE                 GET END OF EXTENT ID
LAB05    EQU   *                                                 S20201
         BAL   RC,SLDROUT              LOAD ROUTINE
         ST    RE,AVTEOE                APPENDAGE TABLE ENTRY FOR EOE
*
*        LENGTH CHECKING IS REQUIRED FOR DCB
*
SLDAPP01 EQU   *
         TM    DCBDEVT,DCBDVTAP         MAG TAPE BEING USED     SA57129
         BZ    SLDAPP10                 NO, BYPASS DOS/CHKP     SA57129
         TM    DCBOPTCD,DCBBCKPT        BYPASS DOS CHKPT RCD     Y02072
         BZ    AROUND                   NO, BRANCH              SA57129
         LA    RB,DACEV                 GET BYPASS DOS CHKPT RCD S21040
*                                       V LENGTH APPENDAGE ID
         TM    DCBRECFM,FORMATF         FORMAT F OR U RCDS       S21040
         BZ    SLDAPP12                 BRANCH IF NO             S21040
         LA    RB,DACEUF                BYPASS DOS CHKPT RCD     S21040
*                                APPEND ID FOR FORMATS U AND F
         B     SLDAPP12                                          S21040
*
SLDAPP10 EQU   *
         TM    0(RPARC),OUTP            OUTPUT DCB            @ZA28034
         BO    SLDAPP14                 YES-NO LENGTH CHECK   @ZA28034
*                                       APPEN NEEDED.         @ZA28034
*
         TM    JFCBMASK+SIX,NRECREDY    IS IT RECORD READY       S20201
         BO    AROUND                   NO BRANCH AROUND         S20201
LAB002   EQU   *                                                 S20201
         TM    DCBRECFM,FORMATU         FORMAT U RECORDS         S20201
         BM    LAB03                    NO BRANCH AROUND         S20201
         TM    DCBRECFM,TRKOVFL         IS IT TRACK OVERFLOW     S20201
         BO    SLDAPP14                 YES BRANCH TO SLDAPP14   S20201
         LA    RB,ACEU                  NO GET U FORMAT CHAN END S20201
         B     SLDAPP12                 LOAD U FORMAT CHAN END   S20201
LAB03    EQU   *                                                 S20201
         TM    DCBRECFM,FORMATF         FIXED LENGTH RECS        S20201
         BO    SLDAPP11                 YES LOAD CI              S20201
         B     SALADS                   BRANCH FOR V FORMAT      A49371
AROUND   EQU   *                                                 S20201
         TM    0(RPARC),OUTP            OPEN FOR OUTPUT ONLY     A49371
         BO    SLDAPP14                 BRANCH YES -- NO LENGTH- A49371
*                                       CHECKING APPENDAGE NEEDED
         TM    DCBRECFM,FORMATU         U RECORDS
         BO    SLDAPP14                 YES, BRANCH            @OS73907
         BM    SALAD                    NO, F OR V RCDS        @OS73907
         TM    DCBRECFM,FORMATD         D OR T RCDS?           @OS73907
         BZ    SLDAPP14                 NO, EXIT               @OS73907
         TM    DCBDEVT,TAPEB            IS IT TAPE             @OS73907
         BZ    SLDAPP14                 NO, EXIT               @OS73907
         B     SALADS                   YES, BRANCH            @OS73907
SALAD    EQU   *                                                 S20038
         TM    DCBRECFM,FIXEDBLK        FIXED BLKD RECORDS
         BC    1,SLDAPP11               YES BRANCH
         TM    DCBRECFM,FORMATV         VARIABLE RECORDS
         BC    8,SLDAPP14               NO BRANCH
SALADS   EQU   *                                                 S20038
         LA    RB,ACEV                  GET V LN CKNG APPN.
         B     SLDAPP12
SLDAPP11 EQU   *
         LA    RB,ACEF                  GET F LN CKNG APPN
SLDAPP12 BAL   RC,SLDROUT               LOAD ROUTINE
         ST    RE,AVTCE                 APPENDAGE TABLE ENTRY FOR CE
         TM    DCBDEVT,DABIT            IS THIS DA               YM5922
         BZ    SALAD13                  NO, TEST FOR TAPE        YM5922
         AH    RE,SECENTRY              OFFSET TO ABNOR ENTRY    YM5922
         B     SLDAPP13                 GO STORE ABNOR ENTRY AD  YM5922
SALAD13  DS    0H                                                YM5922
         TM    DCBDEVT,DCBDVTAP         MAG TAPE BEING USED     SA57129
         BZ    SLDAPP14                 NO, BYPASS DOS/CHKPT    SA57129
         TM    DCBOPTCD,BYPASS          BYPASS DOS CHKPT RCD     S21040
         BZ    SLDAPP14                 BRANCH IF NO             S21040
SLDAPP13 DS    0H                                                YM5922
         ST    RE,AVTABNE               ABNOR END APP ADDR       YM5922
SLDAPP14 EQU   *
* * * * * * *        3525   3505   SUPORT   * * * * * * * * * *
         CLI   DCBDEVT,X'4C'           IS THIS A 3525            M0062
         BE    SLDAPP17                YES, BRANCH               M0062
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TM    DCBDEVT,X'48'            PRINTER USED
         BC    14,SLDAPP17              NO BRANCH
         LA    RB,PSIO                  GET SIO APPNDAGE
         BAL   RC,SLDROUT               LOAD ROUTINE
         ST    RE,AVTSIO                APPENDAGE TABLE ENTRY FOR SIO
*
SLDAPP17 EQU   *
         BAL   RF,EOBROUT               GET EOB RT ID
*
         BAL   RC,SLDROUT               LOAD ROUTINE
*
*
*
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,SOP9            ID OF NEXT MOD         @Z30TSCF
         TM    DCBCIND2,DCBCNQSM       QSAM BEING USED           Y02072
         BZ    SLDEOB2                 NO BRANCH                 Y02072
         MVC   DCBEOB+1(3),25(RWTG)    STORE EOB ROUTINE
         MVC   DCBCNTRL+3(1),DCBCNTRL+1  SETUP FOR QSAM CNTRL TEST
         TM    DCBDEVT,X'80'           DCB FOR TAPE
         BZ    SLDEOB8                 NO BRANCH
         B     SLDEOBC                 YES BRANCH
*
*
SLDEOB2  EQU   *
*
         MVC   DCBEOBR+1(3),25(RWTG)  LOAD EOB ADDR IN DCB
*
SLDEOB20 EQU   *
*
         MVC   DCBEOBW+1(3),25(RWTG)  LOAD EOB WRITE ADDR IN DCB
*
*
SLDEOB4  EQU   *
*
         BAL   RF,EOBROUT               GET POSSIBLE WRT ROUT (IN/OUT)
*
*
         LTR   RC,RC                    DCB FOR IN/OUT
         BC    8,SLDEOB6                NO BRANCH
*
         BAL   RC,SLDROUT               LOAD WRT EOB RT.
*
         MVC   DCBEOBW+1(3),25(RWTG)  LOAD EOB WRITE ADDR.IN DCB
*
*
SLDEOB6  EQU   *
*
         BAL   RF,EOBROUT               IS NOTE/POINT USED
*
         LTR   RC,RC                    TEST FOR ITS ID
         BP    SLDEOB9                 YES BRANCH
*
SLDEOB8  EQU   *
*
         BAL   RF,EOBROUT               GET POSSIBLE CNTRL RT ID
         LTR   RC,RC                    IS THERE A CNTRL ID
         BC    8,SLDEOBC                NO BRANCH
*
SLDEOB9  BAL   RC,SLDROUT              LOAD NOTE/POINT OR CONTROL
         CLC   DUMY(2),0(RB)            DUMMY CONTROL MODULE      M5467
         BNE   SLDEOB10                 NO, BR AROUND             M5467
         AH    RE,CNTOFSET              GET ADDR OF CNTRL ENT PT  M5467
         ST    RE,TEMPSAVE(RWTG)        TEMP SAVE ADDR            M5467
SLDEOB10 EQU   *                        NOT DUMMY                 M5467
         MVC   DCBCNTRL+1(3),25(RWTG)  STORE N/P OR CNTRL ADDR. IN DCB
         B     SLDEOBE
*
SLDEOBC  EQU   *
         XC    DCBCNTRL(4),DCBCNTRL     CLEAR PARA LIST
         OI    DCBCNTRL+3,X'01'         SET VECTOR  TO  ONE
*
SLDEOBE  EQU   *                        END.
         MVC    DCBEROPT(1),DXCCW6      REPLACE QSAM ERROR OPTIONS
*
*
***********************************************************************
*
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURR WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCR CURRENT DCB ENTRY PRTR
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   8,RBASE
*
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BC    7,RELOOP                 NO,CHECK NEXT ENTRY
*
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      REINITIALIZE WTG LIST PTR
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN                IF NOT TRANSFER CONTROL
*
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         BC    15,ZCHEK
*
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
*
*
SLDROUT  EQU   *
         USING WTGMODNM,RWTG                                     Y02072
         MVC   WTGMODID,0(RB)           ID TO MOD NAME           Y02072
         DROP  RWTG                                              Y02072
         L     R1,CVTPTR                LOAD CVT ADDRESS         YM1140
         USING CVT,R1                   CVT ADDRESSABILITY       YM1140
         L     R1,CVTLINK               GET LPALIB DCB ADDRESS   YM1140
         DROP  R1                       DROP CVT BASE            YM1140
*
         LOAD  EPLOC=(RWTG),DCB=(1)                              YM1140
*
*
*
         ST    RE,24(RWTG)             TEMP STORE OF ROUTINE ADDR,
SLDROUT1 MVC   0(2,RUCB),0(RB)          STORE ROUTINES ID IN DEB
         LA    RUCB,2(RUCB)             UPDATE SUBRT PNTR
         SR    RD,RD                    CLEAR REG
         IC    RD,DEBNMSUB              GET NO.OF SUBRTS LOADED SO FAR
         LA    RD,1(RD)                 ONE MORE SUB RT LOADED
         STC   RD,DEBNMSUB              STORE NEW TOTAL SUBRTS LOADED
         BR    RC                      RETURN
*
*
***********************************************************************
*
*
***********************************************************************
*
EOBROUT  EQU   *
*
         SR    RC,RC
         LA    RE,EOBTABL               GET TABLE OF NAMES
         LA    RB,IDLEN                 LEN OF MODULE ID         Y02072
         IC    RC,DCBCNTRL+3            GET ID NO. DEVICE DEP.
         L     RJ,DCBCNTRL              SHIFT IDS RIGHT BY ONE BYTE
         SRL   RJ,8
         ST    RJ,DCBCNTRL
         MR    RB,RB                    CALCULATE WHERE ID IS
         LR    RB,RC                    PREPARE TO LOAD  MODULE
         AR    RB,RE                    POINT TO CORRECT ID
         BR    RF                       RETURN
*
***********************************************************************
*
CNTOFSET DC    H'24'                    OFFSET OF CONTROL ROUTINE M5467
*                                         IN DUMMY MODULE         M5467
SECENTRY DC    H'4'                     OFFSET TO ABNORMAL END   YM5922
*                                         ENTRY IN APPENDAGE     YM5922
OPIDCNST DC    C'0S'
AMIDCNST DC    C'17'                    ID OF THIS MODULE          UT18
SOP9     DC    C'11',VL3(IGG01911)  IGGO1911 OR IECOS90X -QSAM @Z30TSCF
*
*        BELOW ARE THE SHARABLE ROUTINES USED BY BSAM / QSAM
*
*
*        APPENDAGES USED BY BSAM AND QSAM   ---------------------
*
*
AEOE     DC    C'CH'                    END OF EXTENT APPNDG NON-SF-INP
ACEF     DC    C'CI'                    CHANNEL END APPNDG LEN CHK FREC
ACEV     DC    C'CJ'                    CHANNEL END APPNDG LEN CHK VREC
PSIO     DC    C'CL'                    APPENDAGE FOR PRINTERS SIO
DACEUF   DC    C'EI'                    CHANNEL/ABNORMAL END     S21040
*                                       APPNDG TO                S21040
*                                       BYPASS DOS CHKPT RCDS. U,F RCD
DACEV    DC    C'EJ'                    CHANNEL/ABNORMAL END     S21040
*                                       APPNDG TO                S21040
*                                       BYPASS DOS CHKPT RCDS, V RCDS
*
*
***********************************************************************
*
*        DEVICE DEPENDENT ROUTINES SUPPORTED
*
EOBTABL  EQU   *
         DC    C'  '
EPD2     DC    C'CD'                    EOB RT DISK-OUTP/STANDRF INP
EGP2     DC    C'CC'                    EOB RT. DISK-TP-RDR. INP/OUTP.
DDA2     DC    C'CE'                    EOB RT. PRNTR/PUNCH NORMAL
DDB2     DC    C'CF'                    EOB RT. PRNTR/PUNCH ASA
NPD2     DC    C'BC'                    NOTE-POINT DISK
NPT2     DC    C'BD'                    NOTE-POINT TAPE
CNT1     DC    C'CA'                    CNTRL RT READER
CNT2     DC    C'CB'                    CNTRL RT. PRINTER
CNT3     DC    C'BE'                    CNTRL RT. TAPE
DUMY     DC    C'AV'                   DUMMY PUT/NOP ROUTINE
OVRD     DC    C'CT'                    OPEN OVERRIDE ERROR RTN   OC034
UTF1     DC    C'TD'                    USER TOTALLING EOB ROUTINE UT18
UTF2     DC    C'TC'                    USER TOTALLING EOB ROUTINE UT18
UTF3     DC    C'TV'                    USER TOTALLING EOB ROUTINE UT18
UTF4     DC    C'TW'                    USER TOTALLING EOB ROUTINE UT18
UTF5     DC    C'T2'                    USER TOTALLING EOB ROUTINE UT18
ACEU     DC    C'C0'                    CHANNEL END FOR FORMAT U S20201
AEOESF   DC    C'C4'                    SF INPUT END OF EXT      S20201
         DC    C'  '                    FILLER FOR IGG019FN      YM2507
CEAFP    DC    C'FP'                    SEARCH DIRECT CHANNEL    S20201
DDC2     DC    C'FK'                    EOB RT. DATA PROTECTION   M0062
DDD2     DC    C'FQ'                    EOB RT. PRT FOR 3525 DEV  M0062
DDE2     DC    C'FU'                    EOB RT. 3525 INTRPRT FILE M0062
CNT4     DC    C'FA'                    CNTRL RT. 3525 DEV        M0062
OCRCNTRL DC    C'LT'                    CNTRL RTNE FOR 3886 OCR XL03127
         SPACE
PATCH    DC    25H'0'                   PATCH AREA               YM4640
END      EQU   *                        END OF MODULE            Y02072
         TITLE 'IGG01917 - FORMAT OF GOTTEN CORE'
         IECDSECS  MAIN,WTG,PREFX,EXPAND=YES                   @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MOD ID              Y02072
*
*
CVT      DSECT
         CVT
*
AVT      DSECT                          APPENDAGE VECTOR TABLE
AVTEOEPG DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTEOE   DS    A                        ADDR OF END OF EXTENT
*                                       APPENDAGE
AVTSIOPG DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTSIO   DS    A                        ADDR OF START I/O
*                                       APPENDAGE
AVTPCIPG DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTPCI   DS    A                        ADDR OF PCI APPENDAGE
AVTCEPG  DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTCE    DS    A                        ADDR OF CHANNEL END
*                                       APPENDAGE
AVTABNPG DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTABNE  DS    A                        ADDR OF ABNORMAL END
*                                       APPENDAGE
*
BLDEB    DSECT
DEB      DS    0F
DEBNMSUB DS    0CL1
DEBTCBAD DS    CL4
DEBAMLNG DS    0CL1
DEBDEBAD DS    CL4
DEBOGLGS DS    0CL1
DEBIRBAD DS    CL4
DEBOPATB DS    0CL1
DEBSYSPG DS    CL4
DEBNMEXT DS    0CL1
DEBUSRPG DS    CL4
DEBPRIOR DS    0CL1
DEBECBAD DS    CL4
DEBPROTG DS    0CL1
DEBDEBID DS    0CL1
DEBDCBAD DS    CL4
DEBEXSCL DS    0CL1
DEBAPPAD DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    CL4
DEBBINUM DS    CL2
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
*
           DCBD  DSORG=PS
DCBDVTAP EQU   X'80'                    DEVICE CLASS FOR MG TAP SA57129
         IKJTCB
           END
