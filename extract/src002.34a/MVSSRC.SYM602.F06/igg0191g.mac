  TITLE  'IGG0191G - OPEN EXECUTOR - STAGE 2 - UR, MAG AND PAPER TAPE'
IGG0191G CSECT
*MODULE NAME - IGG0191G                                          Y02072
*
*DESCRIPTIVE NAME - BUILD IOBS                                   Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          RELEASE 20 DELETIONS                                       *
*2380121680                                                      S20038
*          RELEASE 21 DELETIONS                                       *
*2590065960                                                      M0129
*2590000020,001200,002000-002200,002400-002600,003000,004600,    A37507
*2590011800,015600,019800-020000,021600-021800,022200,024200-    A37507
*2590024400,025200-027000,027800-028000,028400,029600-029800,    A37507
*2590030200-030600,032200,034400-035400,036800,037800-038400,    A37507
*2590039200-043800,045600-047000,065600,073000-073600,074800,    A37507
*2590088800-117600,118400-118600,121000-121600,132800            A37507
*2590065800-066000,066200                                        A37507
*2590                                                            M0063
*2590132700-134200                                               M0091
*          VS2 RELEASE 2 DELETIONS
*000400,011400,060800,065415,065420,065425,065600,067800,068600, Y02072
*069000,085800,135200-135490,135500,135510-135570,133800,118800  Y02072
*121620,121720,123800,129000,129800                              Y02072
*                                                                YM4697
*C80200,C80400                                                 @ZA27760
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSMI
*          VS2 RELEASE 4 DELETIONS
*                                                              @Z40MSMI
*C08840000                                                     @ZM40309
*A06686000,06692000,09840000                                   @ZM40309
*
*STATUS CHANGE LEVEL 007
*
*FUNCTION/OPERATION: THIS ROUTINE WILL GET CORE(GETMAIN) TO SET UP THE
*      NECESSARY IOB'S. IT WILL CLEAR THE CORE AND LINK THE IOB'S
*      TOGETHER FOR TAPE & UNIT RECORD. (FOR UNIT RECORD, THEN GO
*      TO IGG0196K. FOR TAPE CONTINUE PROCESSING).
*      IT WILL SET UP THE NECESSARY BUFFERS IF NOT AVAILABLE.
*      IT WILL CONNECT THE DCB AND IOB.
*      IT WILL DETERMINE WHAT ROUTINES ARE NEEDED FOR PROPER EXECUTION
*      AND SET A CODE IN THE DCBCNTRL FIELD OF THE DCB.
*      IT WILL SET UP CCW'S FOR TAPE.
*      IT SETS AN AUDIT TRAIL BIT IN THE OPEN WORKAREA           Y02072
*      INDICATING TO FORCE CLOSE THAT THE CORE GETMAINED FOR THE Y02072
*      IOB'S CAN BE FREEMAINED.                                  Y02072
*
*      THIS MODULE WAS SPLIT INTO IGG0196K FOR REL 21.
*
*ENTRY POINTS: ENTERED FROM IGG0196B, IGG0193I, IGG0191T, IGG0191U,
*              IGG0191V, IGG0196Q  (NORMAL CONDITIONS)
*              ENTERED FROM IGG0191F, IGG0191Q, IGG0191R -
*              (ABNORMAL CONDITIONS - CHAINED SCHEDULING UNSUPPORTABLE)
*
*INPUT: SEE DESCRIPTION OF REGISTERS, DCB(USERS).
*
*OUTPUT: SEE DESCRIPTION OF REGISTERS, IOB(USERS), DCB(USERS).
*
*EXTERNAL ROUTINES: IFG019RA.
*
*EXITS- NORMAL: IGG0196K FOR UNIT RECORD AFTER GETMAIN
*             : IGG01910, IGG01912, IGG01915 TO LOAD
*               ACCESS METHOD ROUTINES
*EXITS-ERROR: NONE.
*
*MACROS - ACTION : MODESET, GETMAIN, IECRES
*
*TABLES/WORKAREAS-  SEE DSECTS AT THE END OF THIS LISTING
*
*
*
*ATTRIBUTES: REENTRANT, REUSABLE, RUNS IN DATA MANAGEMENT KEY
*            UNLESS OTHERWISE SPECIFIED, SUPERVISOR STATE
*
*NOTES: THIS ROUTINE GETS CORE FOR MAG & PAPER TAPE AND UNIT RECORD.
*       GOES TO IGG0196K TO CONSTRUCT IOB'S FOR UNIT RECORD.
*       THIS MODULE SAVES THE NUMBER OF IOB'S TO BE BUILT IN THE
*       HIGH ORDER BYTE OF DCBIOBA FOR MODULE IGG0196K.
*       FOR THE 3886 OCR DEVICE, ADDITIONAL CORE IS GOTTEN FOR
*       A 152 BYTE BUFFER ALONG WITH THE IOB.
*
*       FOR A 3800 PRINTER(@Z40MSMI) WHEN OPTCD=J (DYNAMIC TRANSLATE
*       TABLE SELECTION) HAS BEEN REQUESTED, THE CHANNEL PROGRAM
*       AREA MUST BE INCREASED TO ACCOMODATE THE EXTRA SELECT
*       TRANSLATE TABLE CCW. THE CHANNEL PROGRAM WILL BE:
*              NOP/ASA CNTRL
*              NOP/SEL TRANS TBL
*              WRITE
*       THE LEADING NOP IS STILL REQUIRED AND CANNOT BE USED FOR
*       THE SELECT BECAUSE OF PRINT OVERFLOW SUPPORT WHICH CHANGES
*       THE NOP TO A SKIP-TO-ONE WHEN REQUESTED.
*
*
***********************************************************************
*
*  REGISTER CONVENTIONS USED THROUGH OUT ALL OPEN PASSES
*
***********************************************************************
*
RDCB     EQU   2         DCB REGISTER
RBASE    EQU   3         BASE REGISTER
RCORE    EQU   4         WORK AREA ADDRESS
RPAR     EQU   5         TOP OF PARAMETER LIST
RWTG     EQU   6         TOP OF WTG TABLE
RPARC    EQU   7         CURRENT PARAMETER
RWTGC    EQU   8         CURRENT TRANS LOAD
RTIOT    EQU   9         USED HERE AS WRK REG AND  COMM VECTOR ADDR.
RUCB     EQU   10        USED HERE AS A COUNTER IN IOB GENERATION
RWK1     EQU   11                       WORK REGISTER            Y02072
RB       EQU   12        WORK REG1  **
RC       EQU   13        WORK REG2  **  USED IN IOB GENERATION
RWK2     EQU   RC                       WORK REGISTER            Y02072
RD       EQU   14        WORK REG3  **
RJ       EQU   15        WORK REG4
RE       EQU   0         WORK REG5
RF       EQU   1         WORK REG6
*
*
PARAR    EQU   RPARC                                         @ZA27760
DISPSN   EQU   X'0'                                          @ZA27760
***********************************************************************
*
*
*
* MASKS FOR DEVICES TO BE SUPPORTED
*
*
*
TAPEB    EQU   X'80'
URBITS   EQU   X'40'
*
*
*********************************************************************
*
*          OPERATIONS
*
WRITE      EQU   X'01'
READ       EQU   X'02'
READBACK   EQU   X'0C'
EOR        EQU   X'04'                  END OF RECORD              DM0I
*
*********************************************************************
* GENERAL EQUATES
*
ECBNORM  EQU   X'7F'                    NORMAL COMPLETION CODE
MULT8    EQU   3                        SHIFT VALUE TO DIV OR MULT BY 8
OCRIBSZ  EQU   216                      SIZE FOR 3886 OCR       XL03127
*                                       IOB&BUFR (40+24+152)    XL03127
*********************************************************************
         EJECT
         BALR  RBASE,0                  SET BASE                   DM0I
         USING SOP5,RBASE
         USING WTG,RWTG
         USING WTGENTRY,RWTGC
         USING PARML,RPARC
*
*
SOP5     EQU   *
*
         B     BEGIN                    BR AROUND CONSTANTS      Y02072
         DC    C'IGG0191G'              MODULE NAME              Y02072
         DC    C'@ZA27760'              LAST SHIP CODE         @Z40MSMI
         DC    C'11/08/77'              LAST DATE MODIFIED     @Z40MSMI
BEGIN    DS    0H                                                YM4697
         SR    RE,RE                    INITIALIZE
         L     RDCB,PARDCBAD            GET DCB ADDRESS
         USING IHADCB,RDCB
         LA    RDCB,IHADCB              CLEAR HI BYTE
         L     RCORE,WTGCORE-1          GET WRK AREA ADDRESS
         USING FORCORE,RCORE
         LA    RF,IOBEXTEN-IOBQSAMN+L'CCW  SIZE OF IOB + CCW
*
         TM    DCBDEVT,TAPEB            DCB USING TAPE
         BO    SOP52                    YES BRANCH
*                                       NO MUST BE UNIT RECORD
*
*   IF THE DEVICE IS THE 3886 OCR, ESTABLISH SIZE FOR THE IOB   XL03127
*   AND SPECIAL CONTIGUOUS BUFFER. 40(BASIC)+24(CP)+152(BUFR)   XL03127
*
         CLI   DCBDEVT,DCBDVOR9         IS DEVICE A 3886        XL03127
         BNE   SOP50                    NO,BRANCH AROUND        XL03127
         LA    RF,OCRIBSZ               YES,SET REQ'D SIZE      XL03127
         B     SOP55                    GO DO GETMAIN           XL03127
*
*
*    IF THE DEVICE IS A 3800 PRINTER AND OPTCD=J (DYNAMIC SELECTION
*    OF TRANSLATE TABLES) HAS BEEN REQUESTED, THEN AN ADDITIONAL 8
*    BYTES MUST BE GETMAINED IN THE CHANNEL PROGRAM AREA
*    FOR THE SELECT TRANSLATE TABLE CCW.
*
SOP50    EQU   *                        NOT 3886                XL03127
         CLI   DCBDEVT,DCBDVPR5         IS THIS A 3800         @Z40MSMI
         BNE   SOP51                    NO - CONTINUE          @Z40MSMI
         TM    DCBOPTCD,DCBOPTJ         OPTCD=J SPECIFIED      @Z40MSMI
         BZ    SOP54                    NO - ADD 1 CCW         @Z40MSMI
         B     SOP53                    YES - ADD 2 CCW'S      @Z40MSMI
SOP51    EQU   *                        NOT 3800               @Z40MSMI
         TM    PAROPT,PAROUTPT          TEST OPEN OPTIONS
         BO    SOP54                    OUTPUT ADD 1 CCW
         BM    SOP53                    IN/OUT OUT/IN ADD 2 CCW'S
*
         TM    DCBMACF1,DCBMRCRL        IS THE CNTRL MACRO USED
         BO    SOP54                    YES ADD ONE CCW
         B     SOP55                    ADD 0 CCW'S
*
*
***********************************************************************
*
*                 TAPE
*
SOP52    TM    PAROPT,PAROUTPT-PARRDBCK DCB FOR IN/OUT
         BM    SOP54                    IN/OUT OUT/IN ADD 1 CCW
         B     SOP55                    OUTPUT-INPUT-RDBACK ADD 0 CCW'S
*
SOP53    LA    RF,L'CCW(RF)             ADD 8 TO IOB SIZE
SOP54    LA    RF,L'CCW(RF)             ADD 8 TO IOB SIZE
SOP55    EQU   *
         SRL   RF,MULT8                 DIVIDE BY 8
         STC   RF,DCBIOBL               STORE LENGTH
         SLL   RF,MULT8                 RESET
*
SOP56    TM    DCBCIND2,DCBCNQSM        DCB USING QSAM
         BO    SOP57                    YES BRANCH
*
         IC    RE,DCBNCP                GET NO. BUFFER TO GEN
         LTR   RE,RE                    HAS NO. BEEN SPEC.
         BNZ   SOP58                    YES BRANCH
*
         LA    RE,1                     SUPPLY 1 IOB
         B     SOP58                    SKIP TO NEXT               DM0I
*
SOP57    IC    RE,DCBBUFNO              GET NO OF IOBS TO BUILD
*
*
SOP58    LR    RUCB,RE                  SAVE NO AS COUNTER
         MR    RE,RE                    COMPUTE SPACE REQUIRED
         LR    RB,RF                    SAVE FOR FUTURE USE
*
*  THE FOLLOWING SAVES THE AMOUNT OF CORE GETMAINED FOR THE IOBS YM4697
*  IN THE FORCE CLOSE AUDIT TRAIL FOR THE FORCE CLOSE EXECUTOR.  YM4697
*
         ST    RF,DXATEXC2              SAVE LENGTH              YM4697
***********************************************************************
         MODESET  KEYADDR=DXUKEY,WORKREG=11 GET INTO USER KEY    Y02072
*
         GETMAIN R,LV=(RF),SP=0                                  Y02072
*
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
***********************************************************************
*
*
         ST    RF,DCBIOBA               SAVE FIRST IOB ADDR
         OI    FCAOPEN,FCAOIOB          INDIC IOB'S CAN BE FREED Y02072
         LR    RC,RF                   SAVE IOB ADDRESS          A37507
         LR    RD,RF                    SAVE IOB ADDRESS         M0129
         MODESET  KEYADDR=DXUKEY,WORKREG=11 GET INTO USER KEY    Y02072
***********************************************************************
*
*                   CLEAR GETMAINED AREA
*
         SR    RJ,RJ                    SET FROM LENGTH TO ZERO
         LR    RE,RF                    SET ADDRESS TO AREA OBTAINED
         LR    RF,RB                    SET LENGTH
         MVCL  RE,RD                    CLEAR AREA
*
*
***********************************************************************
         CH    RUCB,ONE                 ONE IOB?                 A37507
         BNE   NTONEIOB                 NO - BRANCH              A37507
         USING IOBQSAMN,RWK2            ADDRESSABILITY FOR DSECT
         OI    IOBFLAG1,IOBUNREL        YES - SET UNRELATED FLG  Y02072
         DROP  RWK2
NTONEIOB EQU   *                                                 A37507
         CLI   DCBDEVT,DCBDVOR9         DCB FOR 3886 OCR?       XL03127
         BE    SOP500                   YES,GOTO UR EXEC        XL03127
         TM    DCBDEVT,DCBDVPTP         DCB FOR PAPER TAPE?      A37507
         BO    SOP505                   YES, CONTINUE            A37507
         TM    DCBDEVT,URBITS           DCB FOR UNIT RECORD?     A37507
         BZ    SOP505                   NO, CONTINUE             A37507
SOP500   EQU   *                                                 Y02072
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         MVC   WTGIDTTR,SOPUNREC        YES, LOAD ID OF UNIT   @Z30TSMI
*                                       RECORD MODULE IGG0196K   A37507
         STC   RUCB,DCBIOBA             SAVE NO. OF IOB'S FOR    M0063
*                                       MOD IGG0196K             M0063
         B     RELOOP                                            A37507
SOP505   EQU   *                                                 A37507
         LR    RB,RD                    SAVE IOB ADDRESS         M0129
         B     SOP512                   MUST BE AT LEAST ONE     A37507
*                                       IOB, THEREFORE BYPASS    A37507
*                                       FOLLOWING TEST           A37507
SOP510   EQU   *                        BEGINNING OF IOB BUILD LOOP
*
         LTR   RUCB,RUCB                TEST FOR LAST IOB      @ZM40309
         BC    7,SOP512                 BRANCH IF NOT LAST IOB @ZM40309
         L     RB,DCBIOBA               GET FIRST IOB ADDR
         USING IOBQSAMN,RD
         ST    RB,IOBNIOBA              LINK FIRST IOB TO LAST
         DROP  RD
         USING IOBQSAMN,RB
         OI    IOBNFLG1,IOBFIRST        SET INDICATOR
         DROP  RB
         USING IOBQSAMN,RD
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         TM    JFCBTSDM,JFCSDS          SYSIN OR SYSOUT
         BZ    SOP5180                  NO BRANCH
         CLI   DCBRTN2,RTN6             NOTE/POINT SPECIFIED
         BE    SOP511                   YES BRANCH
         CLI   DCBRTN1,RTN9             CONTROL SPECIFIED
         BNE   SOP5180                  NO BRANCH
*
SOP511   EQU   *
         MVI   DCBRTN2,RTN10            STORE DUMMY ID
         B     SOP5180                  CHECK FOR QSAM             DM0I
*
*
SOP512   EQU   *
*
         ST    RB,IOBNIOBA              LINK THIS IOB TO LAST
         LR    RD,RB                    UPDATE WORK REG
         LA    RB,IOBNECB               POINT TO ECB
         OI    IOBNECB,ECBNORM          SET ECB COMPLETE WITHOUT ERROR
         ST    RB,IOBECBPT              STORE ECB IN POINTER
         LA    RB,IOBEXTEN              POINT TO CHANNEL PROGRAM (CP)
         ST    RB,IOBSTART              STORE CP ADDRESS IN IOB
         L     RWK1,DXUDCBAD            GET USERS DCB ADDRESS    Y02072
         ST    RWK1,IOBDCBPT            DCB ADDR TO IOB          Y02072
         MVI   IOBINCAM+1,1             STORE INCREMENT AMOUNT
*
*
         LR    RC,RB                    UPDATE REG
         USING CCW,RB
*
***********************************************************************
*
*      THIS SECTION DEALS WITH MAG AND PAPER TAPE CHANNEL PROGRAMS
*
***********************************************************************
*
*
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         MVI   DCBRTN4,RTN2             EGP2 ID NUMBER
         MVI   DCBWCPO,IOBEXTEN-IOBQSAMN STORE FIRST OFFSET
         MVC   DCBWCPL(3),DCBWCPO       STORE REMAINING TREE
         MVI   DCBWCPL,1                CHANNEL PROG.IN DOUBLE WORDS
         MODESET  KEYADDR=DXUKEY,WORKREG=11 GET INTO USER KEY    Y02072
         TM    PAROPT,PAROUTPT          DCB FOR OUTPUT ONLY
         BO    SOP523                   YES BRANCH
*
         TM    DCBRECFM,DCBRECU         U RECORDS USED
         BNM   SOP520E                  YES,BRANCH                 DM0I
         TM    DCBDEVT,DCBDVPTP         TEST FOR DEVICE CODE       DM0I
         BNO   SOP5201                  NO ,BRANCH                 DMOI
         TM    DCBCIND2,DCBCNQSM        TEST                       DM0I
         BZ    SOP5201                  NO,BRANCH                  DM0I
         TM    DCBCODE,X'FE'-DCBPTCDN   TEST FOR TRANSLATE REQUEST DMOI
         BZ    SOP5201                  NO                         DM0I
         B     SOP520M                  YES                        DM0I
*                                       REMARKS                    DM0I
SOP520E  EQU   *                        ZERO                       DM0I
         TM    DCBDEVT,DCBDVPTP         PAPER TAPE                 DM0I
         BNO   SOP520M                  NO                         DM0I
         OI    CCWCOMCD,EOR             SET BIT IN READ CCW        DM0I
SOP520M  EQU   *                        HERE AGAIN                 DM0I
         OI    CCWFLGS,CCWSLI           SET SLI BIT FOR TYPE 4 REC.DM0I
*                                       PAPER TAPE FIXED TRANSLATE.
SOP5201  EQU   *
         TM    DISPSN(PARAR),X'03'     BACKWARDS INPUT       @ZA27760
         BNM   SOP521                  BRANCH NOT MIXED      @ZA27760
*
         MVI   CCWCOMCD,READBACK        STORE RD BACK CMD BYTE
*
         B     SOP521AA
*
SOP521   EQU   *
         OI    CCWCOMCD,READ            SET READ OP CODE
SOP521AA EQU   *
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         TM    DCBMACF1,DCBMRPT1        NOTE-POINT NEEDED
         BZ    SOP521A                  NO  BRANCH TO TEST CONTROL
         TM    DCBDEVT,DCBDVPTP         DCB FOR PAPER TAPE
         BO    SOP522                   YES,  LEAVE DUMMY ID
*                                       IN DCBCNTRL+1
         MVI   DCBRTN2,RTN6             TAPE NOTE/POINT ID
         B     SOP522                   GO ON                      DM0I
SOP521A  EQU   *
         TM    DCBMACF1,DCBMRCRL        CNTRL FOR TAPE NEEDED
         BZ    SOP522                   NO BRANCH
         MVI   DCBRTN1,RTN9             CNTRL TAPE ID NUMBER
*
SOP522   LA    RB,L'CCW(RB)             UPDATE CP.ADDRESS
         TM    PAROPT,PAROUTPT-PARRDBCK DCB FOR IN/OUT
         BZ    SOP524                   NO BRANCH
*
         MVI   DCBRTN3,RTN2             SECOND EOB RT (IN-OUT)
         MVI   DCBWCPO,IOBEXTEN-IOBQSAMN+L'CCW UPDATE OFFSETS
         MVI   DCBOFFSW,IOBEXTEN-IOBQSAMN+L'CCW
         MODESET  KEYADDR=DXUKEY,WORKREG=11 GET INTO USER KEY    Y02072
SOP523   EQU   *                                                 Y02072
         MVI   CCWCOMCD,WRITE           STORE WRT CMD BYTE
         OI    CCWFLGS,CCWSLI           SET SLI FLAG IN WRT CCW
         LA    RB,L'CCW(RB)             UPDATE TO NEXT IOB
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         TM    DCBMACF2,DCBMRPT2        NOTE-POINT NEEDED
         BZ    SOP523A                  NO BR TO TEST FOR CNTRL
         MVI   DCBRTN2,RTN6             NPT2 ID NUMBER
         B     SOP524                   GO ON                      DM0I
SOP523A  TM    DCBMACF2,DCBMRCTL        CNTRL MACRO USED
         BZ    SOP524                   NO BRANCH
         MVI   DCBRTN1,RTN9             CNTRL TAPE ID NUMBER
*
SOP524   EQU   *
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11 GET INTO USER KEY    Y02072
         BCTR  RUCB,0                   DECREMENT IOB COUNTER  @ZM40309
         B     SOP510                   GO BUILD NEXT IOB      @ZM40309
*
SOP5180  EQU   *
*
         MODESET EXTKEY=DATAMGT
         MVC   WTGIDTTR,SOPSLDFT        XCTL TO LOAD 1910      @Z30TSMI
         TM    DCBRECFM,DCBRECF         FIX FORMAT                 DM0I
         BO    SOP5STOP                 YES, BRANCH                DM0I
         TM    DCBRECFM,DCBRECV+DCBRECD V OR D FORMAT RECORDS    S20038
         BZ    SOP5STOP                 NO, BRANCH                 DM0I
         MVC   WTGIDTTR,SOPSLDVT        XCTL TO LOAD 1915      @Z30TSMI
SOP5STOP EQU   *
*
         TM    DCBDEVT,DCBDVPTP         PAPER TAPE DEVICE
         BNO   RELOOP                   BRANCH ON NO
         MVC   WTGIDTTR,SOPPTLD         YES, USE ALTER LOAD ID @Z30TSMI
         MVI   DCBPTFLG,0               INITIALIZE PAPER TAPE FLAGS
*
*
***********************************************************************
*
*
RELOOP   LA    RWTGC,WTGEND             INCREMENT CURR WTG ENTRY
         LA    RPARC,L'PARDCBAD(0,RPARC) INCR CURRENT DCB ENTRY PRTR
         CLC   WTGID,AMIDCNST           THIS RT NEEDED AGAIN
         BER   RBASE                    BACK TO OPEN CONTROL ROUTINDM0I
*
         CLC   WTGID,OPIDCNST           END OF TABLE
         BNE   RELOOP                   NO,CHECK NEXT ENTRY
*
         LR    RPARC,RPAR               REINITIALIZA PARM LIST PTR
         DROP  RWTGC
         LA    RWTGC,WTGENTRY           REINITIALIZE WTG LIST PTR
         USING WTGENTRY,RWTGC
ZCHEK    CLI   WTGENTRY,0               ENTRY COMPLETE             DM0I
         BNE   TCTLRTN                  IF NOT TRANSFER CONTROL
*
         LA    RWTGC,WTGEND             GET NEXT ENTRY
         LA    RPARC,L'PARDCBAD(0,RPARC) GET NEXT PARM ENTRY
         B     ZCHEK                    ZERO CHECK                 DM0I
*
TCTLRTN  EQU   *
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSMI*
               MODID=WTGENTRY           GO TO NEXT ROUTINE     @Z30TSMI
**
*
*********************************************************************
*
OPIDCNST DC    C'0S'                    ID                         DM0I
AMIDCNST DC    C'1G'                    ID                         DM0I
SOPUNREC DC    C'6K',VL3(IGG0196K)      UNIT REC EXECUTOR      @Z30TSMI
SOPSLDVT DC    C'15',VL3(IGG01915)      VAR LEN RECORDS        @Z30TSMI
SOPSLDFT DC    C'10',VL3(IGG01910)      FIXED LEN RECORDS      @Z30TSMI
SOPPTLD  DC    C'12',VL3(IGG01912)      PAPER TAPE             @Z30TSMI
ONE      DC    H'1'                     USED FOR ONE IOB TEST    A37507
PATCH    DC    25H'0'                   PATCH AREA               Y02072
END      EQU   *
         EJECT
         IHACCW
         EJECT
CVT      DSECT
         CVT
         EJECT
         DCBD  DSORG=PS
         ORG   DCBCNTRL
DCBRTN1  DS    X
DCBRTN2  DS    X
DCBRTN3  DS    X
DCBRTN4  DS    X
RTN1     EQU   1                        ROUTINE 1 ID
RTN2     EQU   2                        ROUTINE 2 ID
RTN3     EQU   3                        ROUTINE 3 ID
RTN4     EQU   4                        ROUTINE 4 ID
RTN5     EQU   5                        ROUTINE 5 ID
RTN6     EQU   6                        ROUTINE 6 ID
RTN7     EQU   7                        ROUTINE 7 ID
RTN8     EQU   8                        ROUTINE 8 ID
RTN9     EQU   9                        ROUTINE 9 ID
RTN10    EQU   10                       ROTUINE 10 ID
         EJECT
         IECDSECS (MAIN,(IOB,NO)),IOB,WTG,PREFX,EXPAND=YES
         ORG   WTGIDTTR
WTGID    DS    CL2
         EJECT
FORCORE  DSECT
         IHAFCAUD ORG=YES
         EJECT
         IGGPARML
         END
