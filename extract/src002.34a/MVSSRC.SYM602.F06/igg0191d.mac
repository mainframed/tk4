 TITLE 'IGG0191D - OPEN EXECUTOR FOR OUTPUT AND DA DEVICES'
IGG0191D CSECT
*MODULE NAME - IGG0191D                                          Y02072
*
*DESCRIPTIVE NAME = BUILD IOBS                                   Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*           VS2 RELEASE 2 DELETIONS                              Y02072
*012400,015000,053400,068400,068800,069200,070840,070880-070920, Y02072
*073480-073500,073502-073600,079400,137200,139900,139920-140240  Y02072
*200,124900,125800,129800,131200                                 Y02072
*                                                                YM4697
*                                                                YM7595
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*          RELEASE 20 DELETIONS                                       *
*0973032600-032800,058400-058600,061600,070800,073520-073540,    S20201
*0973081600-083000,108200-110600,111600-112200,113400-119800,    S20201
*0973136200-137600,139000,139400,140000                          S20201
*          RELEASE 21 DELETIONS                                       *
*0091074000,118800,120300                                        M0131
*0091004700,130800,134680-134760,139000-140000,140200,147400-    S21042
*0091147600                                                      S21042
*0091073502-073506,074600-076400,120000-121000                   A37507
*
*STATUS CHANGE LEVEL 008
*
*FUNCTION/OPERATION: THIS ROUTINE WILL SET UP A CODE IN THE DCBCNTRL
*      FIELD OF THE DCB FOR IGG01910. IGG01910 WILL FROM THIS CODE
*      DETERMINE WHAT INPUT/OUTPUT ROUTINES WILL BE BROUGHT INTO CORE
*      AT OPEN TIME. (BASIC)
*      IT WILL DETERMINE HOW MANY IOB'S ARE TO BE CREATED, USE'GETMAIN'
*      TO GET CORE FOR THE IOB'S, AND CLEAR THE CORE.
*      IF THE DCB IS BEING OPENED FOR OUTPUT
*      IT WILL LINK THE IOB'S AND INITIALIZE THEM.
*      IT WILL CONSTRUCT CHANNEL PROGRAMS NEEDED FOR PROPER EXECUTION.
*      IT WILL SET UP THE NUMBER OF EXTENTS NEEDED AND PLACE THE
*      ADDRESSES IN THE DCB AND IOB'S.
*      IT SETS AN AUDIT TRAIL BIT IN THE OPEN WORKAREA           Y02072
*      INDICATING TO FORCE CLOSE THAT THE CORE GETMAINED FOR THE Y02072
*      IOB'S CAN BE FREEMAINED.                                  Y02072
*
*      THIS MODULE WAS SPLIT FOR RELEASE 19.  THE RESULTING MODULE
*      IS IGG0191O.
*
*ENTRY POINTS: ENTERED FROM IGG0191A BY USE OF THE XCTL MACRO.
*
*INPUT: SEE DESCRIPTION OF REGISTERS, DCB(USERS).
*OUTPUT: IOB(USER), SEE DESCRIPTION OF THE REGISTERS, DCB(USERS)
*
*
*EXTERNAL ROUTINES: NONE.
*
*EXITS-NORMAL: XCTL TO IGG01910(IECOSLDA)    TO LOAD ROUTINES
*
*EXITS-ERROR-CALL PROB DET (IGG0196M) FOR WTP AND 013 ABEND,
*            INSUFFICIENT BUFFERS AVAILABLE IN
*            BUFFER POOL TO SATISFY DCB BUFFER REQUIREMENTS       M1552
*
*MACROS : ACTION - MODESET, GETMAIN, XCTL, XCTLTABL              Y02072
*
*MACROS : MAPPING - IECDSECS(DCB,WTG), IHAFCAUD, IHACCW, IEZIOB, IGGBCB
*
*TABLES/WORKAREAS-  WHERE TO GO TABLE  (WTG)
*
*   BYTE 0-7   NAME
*   BYTE 8-10  RELATIVE DISK ADDRESS (TTR) OF FIRST RECORD
*   BYTE 11    CONCATENATION NUMBER
*   BYTE 12    ZERO
*   BYTE 13    ALIAS INDICATOR AND USER DATA FIELD DESCRIPTOR:
*              ALIAS INDICATOR 1 BIT
*              NUMBER OF TTRS IN USERS DATA FIELD  2 BITS
*   BYTE 14-16 TTR OF FIRST TEXT RECORD
*   BYTE 17    ZERO
*   BYTE 18-19 TTR OF NOTE LIST OR SCATTER LIST
*   BYTE 20    TRANSLATION TABLE
*   BYTE 21    ZERO OR NUMBER OF ENTRIES IN THE NOTE LIST
*   BYTE 22-23 ATTRIBUTES
*   BYTE 24-26 TOTAL CONTIGUOUS MAIN STORAGE REQUIRED FOR MODULE
*   BYTE 27-28 LENGTH OF FIRST TEXT RECORD
*   BYTE 29    LENGTH OF WTG TABLE  (IN DOUBLE WORDS)
*   BYTE 30-31 PATH THROUGH LOADS OF ROUTINES
*   BYTE 32-34 IDTTR OF EXECUTOR FOR 1ST DCB
*   BYTE 35    WORKAREA ADDRESS FOR 1ST DCB
*   BYTE 36-39 TABLE OF IDTTR'S
*
*        32+N(8)   IDTTR OF EXECUTOR FOR NTH DCB      (3 BYTES)
*                  WORKAREA ADDRESS FOR NTH DCB       (1 BYTE)
*                  IDTTR OF LAST ROUTINE  LOAD        (3 BYTES)
*                  NOT USED                           (1 BYTE)
*
*
***********************************************************************
*ATTRIBUTES: REENTRANT, REUSABLE, SUPERVISOR STATE,              Y02072
*            RUNS IN USER KEY UNLESS OTHERWISE SPECIFIED.        Y02072
*
*
***********************************************************************
*
*
*  REGISTER CONVENTIONS USED
*
***********************************************************************
*
RDCB     EQU   2         DCB REGISTER
RBASE    EQU   3         BASE REGISTER
RCORE    EQU   4         WORK AREA ADDRESS
RPAR     EQU   5         TOP OF PARAMETER LIST
RWTG     EQU   6         TOP OF WTG TABLE
RPARC    EQU   7         CURRENT PARAMETER
RWTGC    EQU   8         CURRENT TRANS LOAD
RBUFADR  EQU   9                        MAINTAINS ADDR OF NEXT   Y02072
*                                         AVAILABLE BUFFER       Y02072
RUCB     EQU   10        USED HERE AS A COUNTER IN IOB GENERATION
RWK1     EQU   11        WORK REGISTER                           Y02072
RB       EQU   12        WORK REG1  **
RWK3     EQU   RB                       WORK REGISTER            Y02072
RC       EQU   13        WORK REG2  **  USED IN IOB GENERATION
RWK2     EQU   RC                       WORK REGISTER            Y02072
RD       EQU   14        WORK REG3  **
RJ       EQU   15        WORK REG4
RWK4     EQU   RJ        WORK REG                                YM7595
RE       EQU   0         WORK REG5
RF       EQU   1         WORK REG6
*
*
PARAR    EQU   RPARC
*
***********************************************************************
*
*
*
*********************************************************************
* MASK'S TO TEST PARAMETER LIST WITH
INP      EQU   X'0F'
OUTP       EQU   X'0F'
UPDATE     EQU   X'04'
INOUT      EQU   X'03'
OUTIN      EQU   X'07'
RDBACK     EQU   X'01'
*
*
*********************************************************************
*
*
* OFFSET FOR PARAMETER TESTS
*
ONE        EQU   X'01'
FRTYATE    EQU   48
DISPSN     EQU   X'0'
*
*
*********************************************************************
*
* OFFSETS FOR DEB REFERENCE
*
*
BIN        EQU   36                     BIN NOS.                   DM0I
BEXT       EQU   38                     BIN EXTENT                 DM0I
*
*
*
*********************************************************************
*
* MASK'S USED TO TEST OPTION FIELDS IN DCB
*
*
*
DOUBLE     EQU   X'01'
*
*
STOWB      EQU   X'80'
WRTCK      EQU   X'80'                  WRITE CHECK                DM0I
OVRFLO     EQU   X'20'
PCI        EQU   X'20'
MINKEY     EQU   X'01'
CONCAT     EQU   X'FF'
*
*********************************************************************
*
* MASKS FOR ACCESS METHODS IN SAM
*
QSAMB      EQU   X'01'
BSAMBIT    EQU   X'00'
PAM        EQU   X'02'
*
*
* MASKS FOR MACRO'S TO BE USED
*
GETB       EQU   X'40'
PUTB       EQU   X'40'
READB      EQU   X'20'
WRITEB     EQU   X'20'
POINTB     EQU   X'04'
CNTRLB     EQU   X'02'
*
*
*********************************************************************
*
* MASKS FOR DEVICES TO BE SUPPORTED
*
*
*
*********************************************************************
*
*
* DISPLACEMENTS AND BYTE COUNTS AS EQUATES
*
D0       EQU   0                        DISPLACEMENTS OF ZERO    S20201
D5       EQU   5                        DISPLACEMENTS OF FIVE    S20201
D6       EQU   6                        DISPLACEMENTS OF SIX     S20201
B1       EQU   1                        BYTE COUNT OF ONE        S20201
B8       EQU   8                        BYTE COUNT OF EIGHT      S20201
B16      EQU   16                       BYTE COUNT OF SIXTEEN    S20201
B24      EQU   24                       BYTE COUNT OF TWENTYFOUR S20201
*
FIVE     EQU   5                        BYTE COUNT OF FIVE       S20201
SIX      EQU   6                        LNGH CHP WITH RCD READY  S20201
*                                       DW                       S20201
TEN      EQU   X'0A'                    LNGH CHP WRT CHK + RCD   S20201
*                                       RDY                      S20201
MAX      EQU   255                      MAX COUNT FOR EXEC INST  S20201
INPBIOB  EQU   80                       BASIC INPUT IOB SIZE     S20201
*
*
*
FLAGOFF  EQU   4                        OFFSET IN CCW TO FLAGS   S20201
CNTOFF   EQU   7                        OFFSET IN CCW TO CNT FLD S20201
SDFLG    EQU   X'08'                    SEARCH DIRECT INDICATOR  S20201
SDOPT    EQU   X'01'                    SEARCH DIRECT MASK       S20201
*
*
*********************************************************************
*
* MASKS FOR STATUS BITS TO BE TESTED
*
*
EXCPN      EQU   X'30'
COMPLETE   EQU   X'60'                  COMPLETE MASK              DM0I
FIRSTIOB   EQU   X'01'
*
*
*
*********************************************************************
*
* MASKS FOR BUFFER TECHNIQUE'S
*
CYCLICB    EQU   X'20'
EXCHANGB   EQU   X'10'
SIMPLEB    EQU   X'40'
EXCHCYCL   EQU   X'30'
*
*********************************************************************
*
*
* MODES OF OPERATION FOR QSAM
*
LOCATEB    EQU   X'08'
MOVEB      EQU   X'10'
SUBSTUTB   EQU   X'04'
*
*
*********************************************************************
*
*
* COMMAND BYTES FOR THE 2841
*
*       CONTROL UNIT
*
*
*
SRCHE      EQU   X'31'
WRTCKD     EQU   X'1D'
RDC        EQU   X'92'
RDKD       EQU   X'8E'
RDD        EQU   X'86'
TIC        EQU   X'08'
SETSC    EQU   X'23'                    SET SECTOR CCW CMND      S20201
RDSC     EQU   X'22'                    READ SECTOR CCW CMND     S20201
*
*
*
*********************************************************************
*
*
* MASKS FOR RECORD FORMATS
*
*
STANDRDF   EQU   X'88'                  STANDARD FORMORT           DM0I
FORMATU    EQU   X'C0'
FORMATF    EQU   X'80'
FORMATV    EQU   X'40'
BLOCKED    EQU   X'10'
*
*
*********************************************************************
*
*
*   CHANNEL CONTROL WORD FLAGS
*
SKIP       EQU   X'10'
SLI        EQU   X'20'
CC         EQU   X'40'
DATACH     EQU   X'A0'
SLICC      EQU   X'60'
SLICCSK    EQU   X'70'
*
*
*********************************************************************
*
*
KEYS       EQU   X'01'
QSAM       EQU   X'01'
NORCDRY  EQU   X'20'                    NO RECORD READY          S20201
*                                       INDICATOR                S20201
*                                       MASK
*
*
*********************************************************************
*
GETMAIN    EQU   4
QSAMZ      EQU   X'05'                  MASK                       DM0I
COMMTABL   EQU   16                     MASK                       DM0I
IDTTR      EQU   5                      LENGTH OF IDTTR FOR MVC  S19015
CPINCR     EQU   24                     CP INCREMENT--OFFSET     S19015
*                                       READ                     S19015
NOROUT     EQU   0                      TO INDICATE NO EOB       S19015
*                                       NEEDED                   S19015
BFTEKR     EQU   X'20'                  BDAM VS SUPPORT          S19015
VSBITS     EQU   X'48'                  VS BITS IN DCBRECFM      S19015
UNRELFLG EQU   X'02'                   IOB UNRELATED FLAG        A37507
*********************************************************************
OABD060  EQU   60                                                S21042
*
*
*
*
*
*
*
*
         BALR  RBASE,0                  REG 3 IS BASE
         USING SOP2,RBASE
         USING IHADCB,RDCB              REG 2 IS DCB
         USING FORCORE,RCORE            REG 4 IS WORK AREA ADDR
*
*
*
*
SOP2     EQU   *
*
         B     BEGIN                    BR AROUND CONSTANTS      Y02072
         DC    C'IGG0191D'              MODULE NAME              Y02072
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'10/18/74'              LAST DATE MODIFIED     @Z30TSCF
BEGIN    DS    0H                                                Y02072
         L     RDCB,0(RPARC)            GET DCB ADDRESS
         LA    RDCB,0(RDCB)             CLEAR HI BYTE
         L     RCORE,4(RWTGC)           GET WORK AREA ADDRESS
*
         SR    RC,RC                    READY A WORK REG
         TM    DISPSN(RPARC),INP        FILE FOR INPUT
         BC    7,SOP230                 NO BRANCH
*
         TM    DCBMACRF,POINTB         POINT MACRO USED
         BC    1,SOP220A               YES BRANCH TO STANDARD F RECS
*
SOP220   TM    DCBRECFM,STANDRDF        STANDARD F RECORDS USED
         BC    1,SOP221                 YES BRANCH
*
*
SOP220A  EQU   *
         MVI   DCBCNTRL+3,2             EGP2 ID NUMBER
         LA    RC,8                     INCREMENT FOR NON SF RECS 20185
         TM    DCBCIND2,QSAMB           DATA SET FOR QSAM        S19015
         BO    SOP221                   YES--BRANCH              S19015
         TM    DCBBFTEK,BFTEKR          OFFSET READ              S19015
         BZ    SOP221                   NO--BRANCH               S19015
         LA    RC,CPINCR(RC)            INCREMENT FOR OFFSET     S19015
*                                       READ                     S19015
         MVI   DCBCNTRX,NOROUT          NO EOB ROUT NEEDED       S19015
         TM    JFCBMASK+D6,NORCDRY      ANY DEVICES W/O RCD      S20201
*                                       READY                    S20201
         BO    SOP225                   BRANCH IF YES, EXTRA     S20201
*                                       SPACE                    S20201
*                                       ALREADY ACQUIRED BY OFFSET
*                                       REA
SOP221   EQU   *                                                 S20201
         TM    JFCBMASK+D6,NORCDRY      ANY DEVICES W/O RCD      S20201
*                                       READY                    S20201
         BO    SOP225                   BRANCH IF YES            S20201
         LA    RC,B24(RC)               INCR FOR RCD READY       S20201
SOP225   EQU   *                                                 S20201
         LA    RC,INPBIOB(RC)           INCR FOR READ IOB        S20201
         B     SOP235                   GO TO GET CORE           S20201
*
*
SOP230   LA    RC,88                    AMT CORE NEEDED FOR OUTPUT IOB
         MVI   DCBCNTRX,QSAM            MOVE ID                    DM0I
         TM    JFCBMASK+D6,NORCDRY      ANY DEVICES W/O RCD      S20201
*                                       READY                    S20201
         BO    SOP233                   BRANCH IF YES            S20201
         LA    RC,B16(RC)               INCR FOR RCD READY       S20201
SOP233   EQU   *                                                 S20201
         TM    DCBOPTCD,WRTCK           WRITE VALIDITY CHECK USED
         BC    14,SOP235                NO BRANCH
*
         TM    JFCBMASK+D6,NORCDRY      ANY DEVICES W/O RCD      S20201
*                                       READY                    S20201
         BO    SOP233A                  BRANCH IF YES            S20201
         LA    RC,B8(RC)                INCR FOR RCD READY       S20201
SOP233A  EQU   *                                                 S20201
*
         LA    RC,24(RC)                AMT SPACE NEEDED FOR WRT CK CP.
*
*
*     AT THIS POINT  RC=REG.13 HAS THE SIZE OF AN IOB IN IT
*
*
SOP235   EQU   *
         TM    JFCBMASK+D6,SDFLG        SEARCH DIRECT            S20201
         BNO   NOSD1                    BRANCH NO                S20201
         LA    RC,B16(RC)               INCR IOB SIZE            S20201
*
NOSD1    EQU   *                                                 S20201
*
         SRL   RC,3                     IOB IN DOUBLE WORDS
         STC   RC,DCBIOBL               STORE IN DCB
         SLL   RC,3                     RESET IOB SIZE
*
         SR    RF,RF
         TM    DCBCIND2,QSAMB           DATA SET FOR QSAM
         BC    14,SOP237                NO BRANCH
*
*
         IC    RF,DCBBUFNO              GET NUMBER OF IOBS TO BUILD
         B     SOP239                   SKIP TO NEXT               DM0I
*
*
SOP237   EQU   *
*
         IC    RF,DCBNCP                GET NUMBER OF IOBS TO BUILD
         LTR   RF,RF                    HAS THIS BEEN SPECIFIED
         BC    7,SOP239                 YES BRANCH
*
         LA    RF,1                     SUPPLY 1 FOR USER
         STC   RF,DCBNCP                SAVE FOR NOW
*
SOP239   EQU   *
*
         LR    RUCB,RF
*
*
***********************************************************************
*
         MR    RE,RC                    COMPUTE TOTAL AMT SPACE NEEDED
*
         TM    JFCBMASK+D6,NORCDRY      ANY DEVICES W/O RCD      S20201
*                                       READY                    S20201
         BO    SOP239A                  BRANCH IF YES            S20201
         LA    RF,B8(RF)                INCR FOR THETA VALUES    S20201
SOP239A  EQU   *                                                 S20201
         LR    RD,RF
         LR    RB,RF                    SAVE FOR FUTURE USE
*
*  THE FOLLOWING SAVES THE AMOUNT OF CORE GETMAINED FOR THE IOBS YM4697
*  IN THE FORCE CLOSE AUDIT TRAIL FOR THE FORCE CLOSE EXECUTOR.  YM4697
*
         ST    RD,DXATEXC2              SAVE LENGTH              YM4697
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11 GET INTO USER KEY    Y02072
*
         GETMAIN R,LV=(RD),SP=0                                  Y02072
*
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
**
*    THIS SECTION CLEARS THE SPACE RECIEVED FROM GETMAIN
**
*
         LR    RJ,RB                    RESTORE LENGTH TO CLEAR
         LA    RE,B8                    CONSTANT OF EIGHT        S20201
         ST    RF,DCBIOBA               SAVE FIRST IOB ADDRESS
         OI    FCAOPEN,FCAOIOB          OK TO FREE IOB CORE      Y02072
         BCTR  RJ,0                     REDUCE AMT BT ONE
*
* OBTAIN  THETA ADDRESSES MAY BE USED LATER
*
         AR    RB,RF                    GET ADDR TOTAL AREA + 1  S20201
         SR    RB,RE                    BACK UP A DW             S20201
         ST    RB,DXCCW11               TEMP STORE SECT ADDR     Y02072
         LA    RB,MAX                   GET MAX EXECUTE VALVE    S20201
         MODESET  KEYADDR=DXUKEY,WORKREG=11 GET INTO USER KEY    Y02072
COMPARE  CR    RJ,RB                    IS AMT TO CLEAR GREATER 255
         BC    12,ONECLEAR              NO BRANCH
*
         EX    RB,CLEAR                 CLEAR 256 BYTES
         SR    RJ,RB                    SUBT 255 FROM AMT TO BE CLEARED
         BCTR  RJ,0                     SUBT ONE MORE BYTE
         LA    RF,256(RF)               UPDATE AREA ADDR BY 256
         B     COMPARE                  CHECK AGAIN                DM0I
*
CLEAR    XC    0(1,RF),0(RF)            ACTUAL CLEAR INSTRUCTION
*
ONECLEAR EX    RJ,CLEAR                 CLEARS LESS THAN 256
*
         L     RB,DCBIOBA              GET IOB ADDR              A37507
         CH    RUCB,ONEIOB             ONE IOB?                  A37507
         BNE   NTONEIOB                NO, BRANCH                A37507
         USING IOBQSAMN,RWK3            ESTABLISH ADDR FOR IOB
*                                       DSECT                    A37507
         OI    IOBFLAG1,IOBUNREL       YES, SET UNRELATED FLAG   Y02072
         DROP  RWK3
NTONEIOB EQU   *                                                 A37507
         TM    DISPSN(RPARC),INP        FILE FOR INPUT           S19015
         BO    SOP240A                  NO--CP WILL BE BUILT     S19015
*                                       HERE                     S19015
         MODESET  EXTKEY=DATAMGT                                 Y02072
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,SOPSLDIN       CP TO BE BUILT IN 1910  @Z30TSCF
         TM    JFCBMASK+D6,SDFLG        SEARCH DIRECT USED       S20201
         BNO   NOSD5                    BRANCH NO                S20201
         MVC   WTGIDTTR,SOPSD         MOVE IN SEARCH DIRECT ID @Z30TSCF
NOSD5    EQU   *                                                 S20201
         B     RELOOP                   GO PROCESS DCBS          S20201
*
*
SOP240A  EQU   *                                                 S19015
         LR    RC,RB                    SET UP WORK REG
         LR    RD,RB                    SET UP WORK REG
         SPACE 2
*
SOP242   EQU   *
*
         ST    RB,0(RD)                 LINK NEW IOB TO LAST ONE BUILT
         LR    RD,RB                    UPDATE CURRENT IOB POINTER
         LA    RB,4(RB)                 POINT TO ECB SLOT
         OI    0(RB),X'7F'              SET ECB COMPLETE WITHOUT ERROR
         ST    RB,12(RD)                STORE ECB ADDR IN IOB
         LA    RB,20(RB)                POINT TO CP POINTER SLOT
         LR    RC,RB                    SAVE ADDR
         LA    RB,24(RB)                POINT TO CP START ADDR
SOP243   EQU   *
         ST    RB,0(RC)                 STORE CP STARTAD IN IOB
         L     RWK1,DXUDCBAD            GET ADDR OF USERS DCB    Y02072
         USING IOBSIOCC,RWK2                                     Y02072
         ST    RWK1,IOBDCBPT            USERS DCB ADDR TO IOB    Y02072
         DROP  RWK2                                              Y02072
         MVC   40(8,RD),DCBFDAD        INITIALIZE IOBFDAD.
*
***********************************************************************
*
*     CONSTRUCTION OF CHANNEL PROGRAM FOLLOWS
*
***********************************************************************
*
SOP255   EQU   *
*
         LA    RC,19(RC)                POINT TO CCHHR FIELD IN IOB
*
* CHECK FOR RCD READY FEATURE ON ALL DEVICES FOR DATA SET AND IF ON
* ALL DEVICES BUILD RCD READY CHPS, BIT 2 OF JFCBMASK BEING A ONE
* SAYS ALL DEVICES DO NOT HAVE RCD READY FEATURE
*
         TM    JFCBMASK+D6,NORCDRY      ANY DEVICES W/O RCD      S20201
*                                       READY                    S20201
         BO    SOP257                   BRANCH IF YES            S20201
         BAL   RJ,SOP2STA               GO BUILD SET SECTOR CMND S20201
SOP257   EQU   *                                                 S20201
*
         BAL   RJ,SOP2SCH               GO BUILD SRCH AND TIC    S20201
*
         SR    RC,RC                    CLEAR REG                S20201
         IC    RC,DCBKEYLE              GET KL                   S20201
         AH    RC,DCBBLKSI              ADD DL                   S20201
         SR    RB,RE                    BACK UP CCW PTR          S20201
         STH   RC,D6(RB)                KL+DL IN RH OF TIC FOR   S20201
*                                       EOB                      S20201
*                                       ROUTINE
         AR    RB,RE                    RESTORE CCW PTR          S20201
*
*     SRCH AND TIC CCWS HAVE BEEN GENERATED AT THIS POINT
*
***********************************************************************
*
*
***********************************************************************
*
*     CONSTRUCTION OF WRITE CHANNEL PROGRAMS FOLLOW
*
***********************************************************************
*
*
SOP260   EQU   *
         LR    RC,RB                    TEMP HOLD ADDR OF WCKD   S20201
         OI    FLAGOFF(RB),DATACH       SET FLAGS                S20201
         MVI   CNTOFF(RB),B8            SET BYTE COUNT TO EIGHT  S20201
         AR    RB,RE                    INCR CCW PTR             S20201
*
         LR    RF,RB                    GET ADDR OF WRT CCW      S20201
         SR    RB,RD                    GET WRT CCW OFFSET       S20201
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         STC   RB,DCBOFFSW              PUT IN DCB               S20201
         TM    DCBMACRF+1,POINTB        NOTE-POINT NEEDED
         BZ    AA                       BRANCH IF ZEROS            DM0I
         MVI   DCBCNTRZ,QSAMZ           MOVE ID                    DM0I
AA       EQU   *                        COME HERE                  DM0I
         MVI   DCBWCPO,48               OFFSET FOR START OF CP.
         MVI   DCBWCPL,X'04'            WRT C.P. LEN IN DBLE WRDS
         MODESET  KEYADDR=DXUKEY,WORKREG=11 GET INTO USER KEY    Y02072
         AR    RB,RD                    RESTORE PTR              S20201
*
         TM    DCBCIND2,QSAMB           IS QSAM USED
         BC    14,SOP262                NO BRANCH
*
* FILL IN BUFFER ADDRESS AND BYTE COUNT
*
         L     RWK4,DCBBUFCB            BUFFER CNTRL BLK ADDRESS YM7595
         USING BCBLK,RWK4                                        YM7595
         L     RBUFADR,BCBBUFPT         NEXT AVAIL BUFFER        YM7595
         LTR   RBUFADR,RBUFADR          IS BUFFER ADDR ZERO      YM7595
         BZ    BUFERR                   YES, ISSUE DMABCOND      YM7595
         USING CCW,RF                                            YM7595
         STCM  RBUFADR,B'0111',CCWADDRB  BUFFER ADDR TO CCW      YM7595
         MVC   CCWBYTE,DCBBLKSI         BYTE COUNT TO CCW        YM7595
         USING BUFFER,RBUFADR                                    YM7595
         MVC   BCBBUFAD,BUFNXPTB        UPDATE TO NEXT AVAIL BUF YM7595
         DROP  RWK4,RBUFADR,RF                                   YM7595
SOP262   EQU   *                                                 S20201
         AR    RB,RE                    INCR PTR                 S20201
*
         TM    JFCBMASK+D6,NORCDRY      ANY DEVICES W/O RCD      S20201
*                                       READY                    S20201
         BO    SOP265                   BRANCH IF YES            S20201
         BAL   RJ,SOP2RTA               GO BUILD RD SECTOR CMND  S20201
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         MVI   DCBWCPL,SIX              SET CHP LENGTH TO        S20201
*                                       INCLUDE                  S20201
*                                       RCD RDY
         MODESET  KEYADDR=DXUKEY,WORKREG=11 GET INTO USER KEY    Y02072
SOP265   EQU   *                                                 S20201
         TM    DCBOPTCD,WRTCK           IS WRITE VALIDITY CHECK SPEC
         BC    14,SOP263               NO. BRANCH
*
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         MVI   DCBWCPL,X'07'            WRITE CHAN PORG LENGTH IF
*                                       WRITE CHECK IS SPECIFIED
         MODESET  KEYADDR=DXUKEY,WORKREG=11 GET INTO USER KEY    Y02072
         SR    RB,RE                    BACK UP CCW PTR          S20201
         OI    FLAGOFF(RB),CC           SET CMND CHAIN IN        S20201
*                                       PREVIOUS                 S20201
*                                       CCW
         AR    RB,RE                    RESTORE PTR              S20201
         TM    JFCBMASK+D6,NORCDRY      ANY DEVICES W/O RCD      S20201
*                                       READY                    S20201
         BO    SOP262A                  BRANCH IF YES            S20201
         BAL   RJ,SOP2RTA               GO TO RD SECT BUILD RTN  S20201
         SR    RB,RE                    BACKUP CCW PTR           S20201
         OI    FLAGOFF(RB),CC           TURN ON CMND CHAIN       S20201
         MVI   D0(RB),SETSC             CHANGE CMND TO SET SECT  S20201
         AR    RB,RE                    RESTORE PTR              S20201
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         MVI   DCBWCPL,TEN              SET CHP SIZE TO INCLD.   S20201
*                                       RCD                      S20201
         MODESET  KEYADDR=DXUKEY,WORKREG=11 GET INTO USER KEY    Y02072
SOP262A  EQU   *                                                 S20201
         LA    RF,B24(RB)               PT RF TO CNT FIELD AT    S20201
*                                       END                      S20201
*                                       CHANNEL PROG
*
         ST    RF,D0(RC)                PUT ADD IN WRT CKD CCW   S20201
         MVI   D0(RC),WRTCKD            PUT IN CCW CMND          S20201
*
         LR    RC,RF                    PUT ADDR IN REG FOR SRCH S20201
*                                       AND TIC RTN
         BAL   RJ,SOP2SCH               GO BLD SCH CCW           S20201
*
         MVI   D0(RB),RDKD              SET READ CMND FOR WRT    S20201
*                                       CHK                      S20201
         OI    FLAGOFF(RB),SKIP         TURN ON SKIP FLAG        S20201
         AR    RB,RE                    INCR PTR                 S20201
*
         B     SOP266                   GO TO FIN IOB            S20201
SOP263   EQU   *                                                 S20201
         ST    RB,D0(RC)                PUT ADDR IN WRT CKD      S20201
         MVI   D0(RC),WRTCKD            PUT IN WRT CKD CMND      S20201
*
*
SOP266   EQU   *                                                 S20201
         MVC   D5(B1,RB),DCBKEYLE       INIT KEY LENGTH          S20201
         AR    RB,RE                    INCR PTR                 S20201
         BCT   RUCB,SOP242             NOT LAST IOB - BRANCH     A37507
         MODESET  EXTKEY=DATAMGT        KEY OF AUDIT TRAIL
         SPACE
         OI    FCAOPEN2,FCAOIOBC        IOBS COMPLETED           YM7595
         MODESET  KEYADDR=DXUKEY,WORKREG=12
         SPACE
         L     RB,DCBIOBA              LAST IOB - LINK IOB'S     A37507
         USING IOBQSAMN,RD              ESTABLISH ADDR FOR IOB
*                                       DSECT                    A37507
         ST    RB,IOBNIOBA                FIRST TO LAST          A37507
         DROP  RD
*
***********************************************************************
*
SOP2END  EQU   *
*
*
***********************************************************************
         TM    DCBDSORG,X'02'          DCB FOR PO
         BC    8,SOP2ENDA              NO, BRANCH
*
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         MVI   DCBCNTRZ,QSAMZ                                      DM0I
         MODESET  KEYADDR=DXUKEY,WORKREG=11 GET INTO USER KEY    Y02072
*
SOP2ENDA EQU   *
*
         L     RB,DCBIOBA               GET FIRST IOB
         OI    0(RB),FIRSTIOB           SET INDICATOR ON FOR CLOSE
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         TM    DCBRECFM,FORMATV         VARIABLE FORMAT ?          DM0I
         BZ    SOP2STOP                 BRANCH NOT V FORMAT        DM0I
         TM    DCBRECFM,FORMATF         FIXED FORMAT               DM0I
         BC    1,SOP2STOP               YES, BRANCH                DM0I
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,SOPSLDVT        VARIABLE, GET 1915     @Z30TSCF
         B     RELOOP                   BACK TO MAIN LINE          DM0I
SOP2STOP EQU   *
         MVC   WTGIDTTR,SOPSLDFT        NON-VARIABLE, GET 1910 @Z30TSCF
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURR WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCR CURRENT DCB ENTRY PRTR
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   RWTGC,RBASE              BACK TO OPEN CONTROL ROUTINDM0I
*
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BC    7,RELOOP                 NO,CHECK NEXT ENTRY
*
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      REINITIALIZE WTG LIST PTR
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN                IF NOT TRANSFER CONTROL
*
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHEK                    TO ZERO CHECK              DM0I
*
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
**********************************************************************
*                                                                    *
BUFERR   EQU   *                                                  M1552
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         DMABCOND OABD060,PDLOAD,RETURN=NONE,RES=NO,REGSAVE=YES  S21042X
                                        CALL PROBLEM DETERMINATION
         B     TCTLRTN                                           S21042
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES
OPIDCNST DC    C'0S'                    ID                         DM0I
AMIDCNST DC    C'1D'                    ID                         DM0I
ONEIOB   DC    H'1'                    TEST FOR ONE IOB          A37507
*
***********************************************************************
*
* SEARCH AND TIC BUILDING RTN
*
SOP2SCH  EQU   *                                                 S20201
         ST    RC,D0(RB)                STORE SRCH ADDR IN CCW   S20201
         MVI   CNTOFF(RB),FIVE          SET CNT TO FIVE          S20201
         OI    FLAGOFF(RB),CC           SET CMND CHAIN FLAG      S20201
         MVI   D0(RB),SRCHE             PUT IN SRCH CMND         S20201
         ST    RB,B8(RB)                PUT SRCH ADDR IN NEXT    S20201
*                                       CCW                      S20201
         AR    RB,RE                    INCR PTR FOR SEARCH      S20201
         MVI   D0(RB),TIC               PUT IN TIC CMND          S20201
         AR    RB,RE                    INCR PTR FOR TIC         S20201
         BR    RJ                       RETURN                   S20201
*
* SET SECTOR BUILD RTN
*
SOP2STA  EQU   *                                                 S20201
         L     RF,DXCCW11               GET THETA ADDR           S20201
         ST    RF,D0(RB)                PUT IN CCW               S20201
         OI    FLAGOFF(RB),CC           TURN ON CMND CHAIN       S20201
         MVI   D0(RB),SETSC             PUT IN CMND              S20201
         B     SOP2FIN                  GO TO FINISH CCW         S20201
*
*
*
* READ SECTOR BUILD RTN
*
SOP2RTA  EQU   *                                                 S20201
         L     RF,DXCCW11               GET THETA ADDR           Y02072
         ST    RF,D0(RB)                PUT IN CCW               S20201
         SR    RB,RE                    BACK UP PTR              S20201
         OI    FLAGOFF(RB),CC           TURN CMND CHAIN ON IN    S20201
         AR    RB,RE                    RESTORE PTR              S20201
         MVI   D0(RB),RDSC              PUT IN CCW CMND          S20201
SOP2FIN  EQU   *                                                 S20201
         MVI   CNTOFF(RB),B1            SET BYTE CNT TO ONE      S20201
         AR    RB,RE                    INCR PTR                 S20201
         BR    RJ                       RETURN                   S20201
*
* MODULE IDS
*
SOPSLDFT DC    C'10',VL3(IGG01910)      IGG01910               @Z30TSCF
SOPSLDVT DC    C'15',VL3(IGG01915)      IGG01915               @Z30TSCF
SOPSLDIN DC    C'1O',VL3(IGG0191O)      IGG0191O               @Z30TSCF
PDLOAD   DC    C'6M',VL3(IGG0196M)      IGG0196M               @Z30TSCF
SOPSD    DC    C'9O',VL3(IGG0199O)      IGG0199O               @Z30TSCF
*
* PATCH AREA
*
PATCH    DC    0H'0',50X'00'            PATCH AREA               Y02072
END      EQU   *                        END OF MODULE            Y02072
         EJECT
CVT      DSECT                                                 @Z30TSCF
         CVT                                                   @Z30TSCF
         IECDSECS DCB,WTG,(MAIN,(IOB,NO)),PREFX,EXPAND=YES     @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      ID NEXT MOD              Y02072
DCBCNTRZ EQU   DCBCNTRL+1                                          DM0I
DCBCNTRX EQU   DCBCNTRL+3                                          DM0I
         EJECT
FORCORE  DSECT                                                   Y02072
         IHAFCAUD  ORG=YES                                       Y02072
         EJECT
         IHACCW  DSECT=YES                                       Y02072
         EJECT
         IEZIOB                                                  Y02072
         EJECT
         IGGBCB  TYPE=SAM
           END
