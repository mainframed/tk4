196A     TITLE 'IGG0196A DEB CONSTRUCTION LOAD 2'
IGG0196A CSECT
*
*MODULE NAME - IGG0196A                                          Y02072
*
*DESCRIPTIVE NAME - FINISH DEB CONSTRUCTION                      Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*        VS2 RELEASE 040 CHANGES
* 101670-101972,115380                                         @ZA06110
*A103718-103722,103927-103933,115452                           @Z40MSRZ
*
*        VS2 RELEASE 03 DELETIONS
*A017720,023571,101619-101649,114500           XA06882/SA72925 @ZA01371
*A024198-024266,094559-094799,095500,096300,099793-099848,      ZA00742
*A099870,100100-100188,100300                                   ZA00742
*D059064,093650                                                 ZA00631
*A058947-059058,023594-023599                                   ZA00631
*D059064                                                       @ZM00636
*A058931                                                       @ZM00636
*D101703                                                        Z30AAWW
*A101707-101708,115364-115368                                   Z30AAWW
*                                                              @Z30TSCF
*        VS2 RELEASE 02 DELETIONS                                Y02072
*000120,000680,018400,035820-036020,058400-058820,058863,058927, Y02072
*115300-115380,056800,058859,103700,109100,113600,114400,        Y02072
*115700-117400                                                   Y02072
*                                                                YM3943
*D96400-96600,96800-98600                                        YM7814
*          RELEASE 21 DELETIONS
*0420098120                                                      A43728
*0420004800,017610-017700,035812,035900,036040,114000,114500-    S21042
*0420114560,116870-117800,135400-135600                          S21042
*0420                                                            S21092
*0420                                                            S21088
*          RELEASE 20 DELETIONS
*1471                                                            S20016
*1471                                                            A33605
* 058000                                                          M4878
*0000017600,101665-101755                                        M3591
*0000093800-094000                                               A25961
*1471                                                            S20202
*
*STATUS CHANGE LEVEL 003
* THIS MODULE IS A SPLIT OF IGG0191A FOR RELEASE 19
*
*
*  CHANGES IDENTIFIED BY 'DM0D' IN COLUMNS 68-71 INSERTED          DM0D
*  IN SUPPORT OF CHECKPOINT/RESTART - RELEASE 17                   DM0D
*                                                                  DM0D
*
*FUNCTION  THIS EXECUTOR IS ENTERED FOR USERS OF SAM AND EXCP
*          DURING OPEN FOR THE CONSTRUCTION OF DEBS.
*          DEBS ARE BUILT FOR ALL DEVICES SUPPORTED BY THE SEQUENTIAL
*          ACCESS METHODS OR PARTITIONED ACCESS METHOD.
*
*ENTRY POINT - IGG0196A
*              ENTERED FROM IGG0191A VIA XCTL
*
*INPUT--  DCB ADDRESS
*          WORK AREA ADDRESS
*          WHERE TO GO TABLE
*          PARAMETER LIST
*          CURRENT ENTRY IN WHERE TO GO TABLE
*          CURRENT PARAMETER (DCB)
*
*OUTPUT-   AS OUTLINED BELOW :
*
*MACROS-ACTION:MODESET, DEBCHK, DMABCOND, XCTL, XCTLTABL         Y02072
*
*EXITS-ERROR: XCTL TO PROBLEM DETERMINATION FOR WTP AND 013 ABEND
*        BASIC DEB LAYOUT FOR ALL I/O DEVICES SUPPORTED
*
*        **********************************
*   -16  *I/OS* I/O SUPPORT DCB ADDRESS   *
*   -12  *    * RESERVED                  *
*   -08  *    * DCB MODIF MASK            *
*   -04  *DEBL* AM TYPE * DEBTBLOF        *
*   +00  *SUBL* TCB ADDRESS               *
*   +04  *AML * DEB LINK FIELD            *
*   +08  *DPAT* IRB ADDRESS               *
*   +12  *FLTP* SYSTEM PUTGE CHAIN        *
*   +16  *NEXT* USER PURGE CHAIN          *
*   +20  *PRTY* ECB ADDRESS               *
*   +24  *KEYD* DCB ADDRESS               *
*   +28  *SCAL* APPENDAGE ADDRESS         *
*        **********************************
*
*
*        DEVICE DEPENDENT SECTION OF DEB
*
* TAPE/URE
*
*
*        **********************************
*   +32  *DMOD*  UCB ADDRESS              *
*   +36  *VOL SEQ NO     *NO. OF VOLS.    *
*        **********************************
*
* REMAINDER OF THE DEB IS BUILT IN IGG0191N.
*
* D.A.SAM DEB.
*        **********************************
*   +32  *FMSK*  UCB ADDRESS              *
*   +36  *EXTENT FIELD BBCCHHCCHH         *
*   +40  *                                *
*   +44  *               *NO.TRACKS ALL.  *
*   +48  *VOL SEQ NO     *NO. OF VOLS     *
*        **********************************
*
*
* NOTE.+32 - +44 IS REPEATED PER EXTENT
*
*
* D.A.TO MEMBER FOR OUTPUT
*        **********************************
*   +E2  *MEMBER NAME THAT IS OPENING     *
*   +56  *MEMBER NAME THAT IS OPENING     *
*        **********************************
*
*
* DA BPAM CONCATENATION (INPUT)
*        **********************************
*   +48  *DSNO DSNO  DSNO DSNO            *
*        *EXTU EXT2  EXT3 EXT4            *
*        **********************************
*
*
*NOTE. BPAM DEB SAME AS SAM DEB WITH EXCLUSSION OF FIELD AT +48
*
*        USER LABEL EXTENT IS NOT INCLUDED IN F1 EXTENT COUNT,
*        AND IS NOT INCLUDED IN DEB EXTENTS.
*
*        ROUTINE ALSO MERGES CLASS BYTE AND DEVICE BYTE OF UCB/SRT
*        INTO DCB FIELD (DEVT)
*
*EXITS  - NORMAL                                                   UT18
*       1 IF DEVICE TYPE = DA  XCTL'S TO IGG0191N                  UT18
*       2 IF DCB IS EXCP AND:                                  @Z40MSRZ
*            - DEVICE IS A 3800 PRINTER        IGG0196Q        @Z40MSRZ
*            - DEVICE IS A UCS/FCB PRINTER     IGG0191T        @Z40MSRZ
*            - DEVICE IS OTHER UR OR TAPE      IGG01911        @Z40MSRZ
*       3 IF DEVICE TYPE NOT = DA AND USER TOTALING FACILITY NOT   UT18
*              SPECIFIED (OPTCD NOT EQUAL TO X'02') XCTL'S TO      UT18
*              IGG0191B                                            UT18
*       4 IF DEVICE TYPE NOT = DA AND USER TOT. F.  IS SPECIFIED   UT18
*              XCTL'S TO IGG0191Y                                  UT18
*                                                                  UT18
OABD074  EQU   74                                                S21042
OABD213  EQU   419                                             @ZA01371
*
*TABLES/WORKAREA - SEE DSECTS AT END OF LISTING.
*
*ATTRIBUTES- RE-ENTRANT- RE-USABLE- RUNS IN DATA MANAGEMENT KEY  Y02072
*            UNLESS OTHERWISE SPECIFIED, SUPER MODE              Y02072
*
*
*
*
*******************************************************************
*********************************************************************
*
* MASK'S TO TEST PARAMETER LIST WITH
DESZEM1    EQU   143
DESZE    EQU   144
OFFSET   EQU   4
AB013    EQU   X'013'                   ABEND CODE               M3591
OPIECAPP EQU   20
OPMDEBID EQU   X'0F'
OPPATRUB EQU   0
OPMEXSCL EQU   4
OPMINPUT EQU   X'7F'
OPMDEVT  EQU   X'40'
DEBBSLNG EQU   80
COMMUTAB EQU   16
UCBDTYPE EQU   5
SRTOUCBA EQU   0
NEXTCORE EQU   140
DUALDEN  EQU   X'20'
READB    EQU   X'20'
WRITEB   EQU   X'20'
SUBRNTID EQU   20                       SUBROUTINE ID LENGHT.
TOTFLAG  EQU   X'02'                    USER TOTALING FLAG         UT18
ZERO     EQU   0                        PARAMETRIC ZERO            UT18
FOUR     EQU   4                        PARAMETRIC 4               UT18
CON8     EQU   8                        PARAMETRIC 8               UT18
INFLAG   EQU   X'0E'                    FLAG FOR INPUT TEST        UT18
ULEXTYP  EQU   X'40'  EXTENT TYPE INDICATOR FOR USER LABEL TRACK   UL17
D1275    EQU   X'1F'                    UCBDEVT CODE FOR 1275    S19033
D1419    EQU   X'1D'                    UCBDEVT CODE FOR 1419    S19033
D1419SCU EQU   X'1E'                    UCBDEVT FOR 1419 SCU   @ZA01371
PRT3211  EQU   X'09'                   DEVICE TYPE FOR 3211      S20202
*                                       PRINTER                  S20202
TIOTTERM EQU   X'20'                    DEVICE IS TERMINAL (TSO) S20016
EXCP     EQU   X'80'                    DCBMACRF EXCP MASK
DSORGPS  EQU   X'40'                    DCBDSORG PS MASK
PRT2245  EQU   X'0B'                    MASK FOR 2245 PRINTER    S21092
TSOAMLNG EQU   X'10'                    TSO HAS ACCESS METHOD   ZA00631
*                                       LENGTH OF X'10'         ZA00631
TSONMEXT EQU   X'01'                    TSO HAS ONE EXTENT      ZA00631
*
******************************           ******************************
*
D3525    EQU   X'0C'                   3525 UCB UNIT TYPE CODE   S21088
RP       EQU   X'60'                   READ/PUNCH MASK           S21088
RW       EQU   X'50'                   READ/PRINT MASK           S21088
PW       EQU   X'30'                   PUNCH/PRINT MASK          S21088
FUNCOFF  EQU   3                       DCBFUNC OFFSET            S21088
DEB6250  EQU   X'D3'                    6250 BPI SET MODE         99223
TST6250  EQU   X'D0'                    MASK FOR 6250 BPI         99223
DEB1600  EQU   X'C3'                    1600 BPI SET MODE         99223
EXCPDCB  EQU   X'80'                    EXCP BIT IN DCBMACRF    ZA00742
FOUNDSEG EQU   X'40'                    FOUNDATION BIT          ZA00742
TRTCHTST EQU   X'38'                    TAPE RECORDING TECH     ZA00742
*
*
*
*
*REGISTERS ARE REFERENCED SYMBOLICALLY WHERE APPROPRIATE
*
RDCB     EQU   2         DCB REGISTER
RBASE    EQU   3         BASE REGISTER
RCORE    EQU   4         WORK AREA ADDRESS
RPAR     EQU   5         TOP OF PARAMETER LIST
RWTG     EQU   6         TOP OF WTG TABLE
RPARC    EQU   7         CURRENT PARAMETER
RWTGC    EQU   8         CURRENT TRANS LOAD
RTIOT    EQU   9         USED HERE AS WRK REG AND  COMM VECTOR ADDR.
RUCB     EQU   10        USED HERE AS A COUNTER IN IOB GENERATION
RDEB     EQU   11        DEB ADDRESS
RB       EQU   12        WORK REG1  **
R1       EQU   RB                       PARM REGISTER            Y02072
RC       EQU   13        WORK REG2  **  USED IN IOB GENERATION
RD       EQU   14        WORK REG3  **
RJ       EQU   15        WORK REG4
RE       EQU   0         WORK REG5
RF       EQU   1         WORK REG6
RWK1     EQU   RF                       WORK REGISTER            Y02072
CVTREG   EQU   9                   ADDR OF CVT                   M4879
*
*
*
***********************************************************************
*
*
*
*
*
*31440554-0560                                                     5798
*41260808,0808                                                     6831
         BALR  RBASE,0
         USING PO,RBASE
         USING IHADCB,RDCB
         USING FORCORE,RCORE
         USING DEB,RDEB
         USING   TCB,RD
         USING TIOT,RTIOT
         USING UCBOB,RUCB
*
*
*
*
PO       EQU   *
         B     BEGIN                    BRANCH AROUND MODULE ID  YM4640
         DC    C'IGG0196A'              MODULE NAME              YM4640
         DC    C'@Z40MSRZ'              LAST SHIP CODE         @Z40MSRZ
         DC    C'09/15/75'              LAST SHIP DATE         @Z40MSRZ
BEGIN    DS    0H                       END OF MODULE ID         YM4640
*
         L     RDCB,0(RPARC)            GET DCB ADDRESS
         L     RCORE,4(RWTGC)           GET WORK AREA
         L     RD,COMMUTAB         CALCULATE TCB ADDRESS
         L     RD,0(0,RD)
         L     RD,4(0,RD)               TCB  ADDR
         L     RTIOT,TCBTIO
         AH    RTIOT,DCBTIOT       RESET TIOT POINTER
         TM    TIOELINK,TIOTTERM        IS THIS TSO?             S20016
         BO    MAINUNIT                 BRANCH IF YES            S21042
         L     RUCB,DXUCBADR            GET UCB PTR.             Y02072
MAINUNIT EQU   *                                                 S21042
         USING UCBOB,RUCB
         L     RDEB,DCBDEBAD            POINT TO DEB BASIC SECTION
         LA    RDEB,0(RDEB)            ZERO HIGH ORDER BYTE      A33605
         LM    RE,RF,DXCCW11            RESTORE LENGTH,ADDRESS    -UT18
OBPAM160 EQU   *
*
         L     RJ,COMMUTAB
         L     RJ,OPIECAPP(0,RJ)        IOS APP ADDR
         MVC   0(20,RF),0(RJ)           FILL IN IOS APP
         MVC   DEBPROTG(1),DXUKEY       PROTECTION TAG           Y02072
         OI    DEBDEBID,X'0F'           ID
         MVC   DEBPRIOR(1),TCBDSP       PRIORITY
         MVC   DEBOPATB(1),0(RPARC)     OPEN ATTB
         NI    DEBOPATB,X'7F'           CLEAR HI ORDER BIT
         MVI   DEBEXSCL,X'04'           SCALE FACTOR
*
         L     RWK1,TCBDEB              GET FIRST DEB ON CHAIN   Y02072
         MODESET  EXTKEY=SUPR           GET INTO SUPER KEY       Y02072
OBPAM165 EQU   *                                                 Y02072
         STCM  RWK1,B'0111',DEBDEBAB    UPDATE DEB CHAIN POINTER YM3943
         CS    RWK1,RDEB,TCBDEB         ATTEMPT TO LINK NEW DEB  Y02072
         BNE   OBPAM165                 BR TO TRY AGAIN IF UNSUC Y02072
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         LA    RF,DBCKEXCP              AMTYPE FOR EXCP DEB      Y01021
         TM    DCBMACRF,EXCP            IS DCB EXCP              Y01021
         BO    DEBCHK                   BRANCH IF YES            Y01021
         LA    RF,DBCKSAM               AMTYPE FOR SAM           Y01021
         TM    DCBDSORG,DSORGPS         IS DCBDSORG PS           Y01021
         BO    DEBCHK                   BRANCH IF YES            Y01021
         LA    RF,DBCKBPAM              AMTYPE FOR BPAM DEB      Y01021
DEBCHK   DEBCHK (RDCB),TYPE=ADD,AM=(0(RF)) ADD DEB TO DEB TABLE  Y02072
         LR    RDEB,RF                  LOAD VERIFIED DEB ADDR   Y01021
         LM    RE,RF,DXCCW11            RELOAD  ADDRESS, LENGTH  Y01021
         TM    TIOELINK,TIOTTERM        IS THIS TSO?           @ZM00636
         BNO   NOTTSO                   IF NOT TSO, BRANCH AROUNZA00631
         MVI   DEBAMLNG,TSOAMLNG        PUT ACCESS METHOD LENGTHZA00631
*                                       OF X'10' INTO TSO DEB   ZA00631
         MVI   DEBNMEXT,TSONMEXT        PUT X'01' INTO NUMBER   ZA00631
*                                       OF EXTENT FIELD OF TSO  ZA00631
*                                       DEB                     ZA00631
         B     RP350015                 CONTINUE TSO PROCESSING ZA00631
NOTTSO   EQU   *                                                ZA00631
         TM    UCBTBYT3,UCB3DACC        OPENING FOR D A DEVICE
         BC    14,OBPAMTAP              NO BRANCH
         MVC   23(5,RF),DXCCW7          CCHHR
OBPAM170 EQU   *
         LA    RF,SOP1N                 SOP1N REQ'D FOR ALL D.A. DEVICE
         B     COMSTOP                 GO TO COMMON PATH FOR TAPE/UR/DA
OBPAMTAP EQU   *
*
         ST    RUCB,DEBUCBAD            UCB ADDR
         MVI   DEBAMLNG,X'10'           16 BYTE ACCESS METH.     A25961
*                                       LENGTH
         MVI   DEBNMEXT,1                                        A25961
         CLI   UCBTBYT4,D3525          IS THIS A 3525            S21088
         BNE   RP350015                BRANCH IF NOT A 3525      S21088
         TM    DCBMODE+FUNCOFF,RP      READ/PUNCH ASSOCIATED     S21088
*                                       DATA SET                 S21088
         BO    RP350010                BRANCH IF READ/PUNCH      S21088
         TM    DCBMODE+FUNCOFF,RW      REAR/PRINT ASSOCIATED     S21088
*                                       DATA SET                 S21088
         BO    RP350010                BRANCH IF READ/PRINT      S21088
         TM    DCBMODE+FUNCOFF,PW      PUNCH/PRINT ASSOCIATED    S21088
*                                       DATA SET                 S21088
         BNO   RP350015                BRANCH IF NOT PUNCH/PRINT S21088
RP350010 EQU   *                                                 S21088
         MVI   DEBEXSCL,4              MOVE 4 INTO EXTENT SCALE  S21088
*                                       FIELD                    S21088
         B     RP350020                                          S21088
RP350015 EQU   *                                                 S21088
         MVI   DEBEXSCL,2
*
*
RP350020 EQU   *                                                 S21088
         TM    TIOELINK,TIOTTERM        IS THIS TSO?             S20016
         BO    DEBFINI                  BRANCH IF YES            S20016
         MVI   DEBDVMOD,3
         TM    DCBMACRF,EXCPDCB         THIS AN EXCP DCB        ZA00742
         BZ    TAPE0010                 BR - NORMAL DCB         ZA00742
         TM    DCBMACRF,FOUNDSEG        FOUNDATION PRESENT      ZA00742
         BO    TAPE0010                 BR-DEVICE SEG ALSO      ZA00742
*                                       PRESENT                 ZA00742
         OC    DEBDVMOD(1),JFCTRTCH     USE JFCB FOR TRTCH      ZA00742
         OC    DEBDVMOD(1),JFCDEN       USE JFCB FOR DENSITY    ZA00742
         TM    JFCTRTCH,TRTCHTST        TRTCH SPECIFIED IN JFCB ZA00742
         BNZ   TAPE0030                 BR-SPECIFIED            ZA00742
         B     TAPE0020                 TRTCH NOT SPECIFIED     ZA00742
TAPE0010 EQU   *                        *                       ZA00742
         OC    DEBDVMOD(1),DCBTRTCH     TAPE RECORDING TECH
         OC    DEBDVMOD(1),DCBDEN       DENSITY
         TM    DCBTRTCH,X'38'          HAS TRTCH BEEN SPECIFIED   99223
         BNZ   TAPE0030                 YES BRANCH
*
TAPE0020 EQU   *                        *                       ZA00742
         OI    DEBDVMOD,X'30'
*
*      ASSUME TRANSLATE BYTE CONVERT OFF ODD DPARITY LOW DENSITY
*
TAPE0030 EQU   *                        *                       ZA00742
         SR    RE,RE                                              MC0W
         SR    RF,RF                                              MC0W
         IC    RF,JFCBVLCT              GET VOLUME COUNT          MC0W
         IC    RE,JFCBNVOL             GET NUMBER OF VOLUMES      MC0W
         CR    RE,RF                                              MC0W
         STH   RE,DEBTVLNM              IS NO. OF VOLUMES GREATER MC0W
         BH    OBPAM172                 YES USE NO. OF VOLUMES    MC0W
         STH   RF,DEBTVLNM              NO USE VOLUME COUNT       MC0W
OBPAM172 EQU   *                                                  MC0W
         MVC   DEBTVLSQ,DXVOLSEQ        VOL SEQ TO DEB           YM7814
***********************************************************************
*      DUAL DENSITY SUPPORT HERE
***********************************************************************
         TM    UCBTBYT1,X'04'           IS THIS PHASE-ENCODE UNIT 15451
         BO    PENCODE                  YES,BR TO ASSUME 1600 BPI 15451
         TM    UCBTBYT2,DUALDEN         DUAL DENSITY UNIT
         BO    PENCODE                  YES,SET UP 1600 OR 800 BPI99223
         TM    UCBTBYT1,UCB1FEA6        IS THIS 6250 BPI UNIT     99223
         BO    HICODE                   YES,ASSUME 6250 BPI       99223
         TM    UCBTBYT2,UCB2OPT3        6250/1600 BPI FEATURE     99223
         BZ    DEBFINI                  NO,DENSITY SET            99223
         SPACE
HICODE   EQU   *                        6250/1600 BPI LOGIC       99223
         MVI   DEBDVMOD,DEB6250         SET 6250 BPI              99223
         TM    DCBMACRF,EXCPDCB         THIS AN EXCP DCB        ZA00742
         BZ    TAPE0040                 BR - NORMAL DCB         ZA00742
         TM    DCBMACRF,FOUNDSEG        FOUNDATION PRESENT      ZA00742
         BO    TAPE0040                 BR-DEVICE SEG ALSO      ZA00742
         TM    JFCDEN,TST6250           IS 6250 SPECIFIED       ZA00742
         B     TAPE0050                 GO COMPLETE SETTING     ZA00742
TAPE0040 EQU   *                        *                       ZA00742
         TM    DCBDEN,TST6250           IS 6250 SPECIFIED         99223
TAPE0050 EQU   *                        *                       ZA00742
         BO    DEBFINI                  YES,CONTINUE PROCESSING   99223
         MVI   DEBDVMOD,DEB1600         SET 1600 BPI              99223
         B     DEBFINI                  CONTINUE PROCESSING       99223
*
PENCODE  EQU   *                                                  15451
         MVI   DEBDVMOD,X'C3'           ASSUME 1600 BPI DENSITY
         TM    DCBMACRF,EXCPDCB         THIS AN EXCP DCB        ZA00742
         BZ    TAPE0060                 BR - NORMAL DCB         ZA00742
         TM    DCBMACRF,FOUNDSEG        FOUNDATION PRESENT      ZA00742
         BO    TAPE0060                 BR-DEVICE SEG ALSO      ZA00742
         TM    JFCDEN,X'C0'             IS 1600 SPECIFIED       ZA00742
         B     TAPE0070                 GO COMPLETE SETTING     ZA00742
TAPE0060 EQU   *                        *                       ZA00742
         TM    DCBDEN,X'C0'             IS IT 1600 BPI
TAPE0070 EQU   *                        *                       ZA00742
         BC    9,DEBFINI                YES BRANCH
*
         MVI   DEBDVMOD,X'CB'           800 BPI DENSITY INDICATED
DEBFINI  EQU   *
*
         LA    RF,SOP1B
COMSTOP  EQU   *
*
         L     RDCB,0(RPARC)            RESTORE DCB ADDRESS
         TM    TIOELINK,TIOTTERM        IS THIS TSO?             S20016
         BO    FINISH                   BRANCH IF YES            S20016
*
         CLI   UCBTBYT4,D1275           IS THIS A 1275           S19033
         BE    MICR                     YES,BRANCH               S19033
         LA    RE,OABD213               SET INT ABEND CODE     @ZA01371
         CLI   UCBTBYT4,D1419SCU        IS IT 1419 SEC UNIT    @ZA01371
         BE    BADUNIT                  YES ISSUE ABEND        @ZA01371
         CLI   UCBTBYT4,D1419           IS THIS A 1419          XL03123
         BNE   TSTEXCP                  NO, BRANCH               S19033
MICR     EQU   *                                               @ZA06110
*                                                              @ZA06110
*        PERFORM A UCB LOOK-UP PROCEDURE TO OBTAIN THE ADDRESS @ZA06110
*                                                              @ZA06110
         LA    RE,OABD074               SET CODE FOR NO UCB    @ZA06110
         L     CVTREG,CVTPTR            LOAD CVT ADDRESS       @ZA06110
         USING CVT,CVTREG                                      @ZA06110
         LH    RC,UCBCHAN               GET CHAN AND DEVICE ADD@ZA06110
         N     RC,ADDRMASK              CLEAR FLAGS            @ZA06110
         LA    RC,1(RC)                 INCR TO SCU ADDR       @ZA06110
         L     RJ,CVTILK2               LOAD ADDR OF UCB LIST  @ZA06110
LOOP     EQU   *                                               @ZA06110
         LH    RD,0(RJ)                 GET PRT FROM LIST      @ZA06110
         LTR   RD,RD                    END OF LIST            @ZA06110
         BZ    ZPTR                     TABLE MAY HAVE HOLES   @ZA06110
         CH    RD,UCBEND                END OF TABLE           @ZA06110
         BE    NOUCB                    YES - NO UCB FOR SCU   @ZA06110
         DROP  RUCB                                            @ZA06110
         USING UCBOB,RD                                        @ZA06110
         N     RD,UCBMASK               CLEAR HIGH ORDER       @ZA06110
         LH    RB,UCBCHAN               GET CHAN AND DEV ADDR  @ZA06110
         N     RB,ADDRMASK              CLEAR FLAGS            @ZA06110
         CR    RC,RB                    IT THIS UCB FOR SCU?   @ZA06110
         BE    UCBFND                   YES, BR                @ZA06110
ZPTR     EQU   *                                               @ZA06110
         LA    RJ,2(RJ)                 UPDATE PTR INTO UCB    @ZA06110
*                                       TABLE                  @ZA06110
         B     LOOP                     BR,CK NEXT UCB IN LIST @ZA06110
UCBFND   EQU   *                                               @ZA06110
         ST    RD,DEBBINUM              PLACE A(SCU UCB IN DEB @ZA06110
         LA    RB,2                     LOAD NUMBER OF EXTENTS @ZA06110
         STC   RB,DEBNMEXT              PLACE NUM EXT OM DEB   @ZA06110
         STC   RB,DEBEXSCL              PLACE EXT SCALE IN DEB @ZA06110
         MODESET EXTKEY=ZERO            GET IN KEY ZERO        @ZA06110
         OI    UCBSTAT,UCBALOC          SET SCU ALLOCATED      @ZA06110
         MODESET EXTKEY=DATAMGT         BACK TO KEY 5          @ZA06110
         DROP  CVTREG,RD                                       @ZA06110
         USING UCBOB,RUCB
TSTEXCP  EQU   *                                                 S19033
*
***********************************************************************
*
*      TEST FOR USER OF EXCP NOT BSAM OR QSAM
*
***********************************************************************
*
         TM    DCBMACRF,X'80'           EXCP BIT ON
         BC    8,SAMOUT                 NO BRANCH
         TM    UCBTBYT3,UCB3DACC        IS DEVICE D.A.
         BO    FINISH                   YES, BRANCH.
*
***********************************************************************
*
*        TEST FOR PRINTER WITH EXCP DCB                        @Z40MSRZ
*             IF 3800              IGG0196Q                    @Z40MSRZ
*             IF UCS/FCB PRINTER   IGG0191T                    @Z40MSRZ
*
***********************************************************************
*
         CLI   UCBTBYT3,X'08'           TEST IF DEVICE CLASS       DM0B
*                                       IS U/R
         BC    7,EXCPOUT                IF NOT, GO TO LAST EXEC    DM0B
*                                       SELECTION
         CLI   UCBTBYT4,UCB3800         TEST FOR 3800          @Z40MSRZ
         BE    XCTLGO6Q                 YES, SET FOR 196Q      @Z40MSRZ
*
         CLI   UCBTBYT4,PRT3211         TEST FOR 3211 PRINTER    S20202
         BE    XCTLGO1T                 YES GO TO PRT MOD        S20202
*
         CLI   UCBTBYT4,PRT2245        TEST FOR 2245             S21092
         BE    XCTLGO1T                YES, GO TO IGGO191T       S21092
         CLI   UCBTBYT4,X'08'           TEST IF DEVICE IS 1403     DM0B
         BC    7,EXCPOUT                IF NOT, GO TO LAST EXEC    DM0B
*                                       SELECTION
*
         TM    UCBTBYT2,X'80'           IF 1403 HAS UCS            DM0B
         BC    14,EXCPOUT               IF NOT, GO TO LAST EXEC    DM0B
*                                       SELECTION
XCTLGO1T EQU   *                                                 S20202
*
*
         LA    RF,SOP1T                 IF 1403 WITH UCS, XCTL     DM0B
*                                       IGG0191T
         BC    15,FINISH                GO TO WTG ROUTINE          DM0B
*
XCTLGO6Q EQU   *                        SET FOR IGGO196Q       @Z40MSRZ
         LA    RF,SOP6Q                   FOR 3800 PRINTER     @Z40MSRZ
         B     FINISH                   GO TO WTG ROUTINE      @Z40MSRZ
*
***********************************************************************
*
EXCPOUT  EQU   *                                                   DM0B
*
         LA    RF,SOP11                 EXCP AND NOT D.A., GO    Y02072
*                                         DO DCB COPY BACK       Y02072
*
SAMOUT   EQU   *
*
*******************************************************************UT18
*  NOTE - THE FOLLOWING FEW INSTRUCTIONS SETUP FOR AN XCTL TO A NEWUT18
*        (WITH RELEASE 18 ) MODULE WHICH WILL LOAD THE USER TOTALINUT18
*        FACILITY SAVE ROUTINE AND DO A GETMAIN FOR A UTF SAVE AREAUT18
*        FROM THE USER'S SUBPOOL 0 FOR THE SAVING OF HIS TOTALING  UT18
*        AREA AT END-OF-VOLUME TIME.                               UT18
*******************************************************************UT18
*                                                                  UT18
         TM    ZERO(RPARC),INFLAG       TEST FOR INPUT DATA SET    UT18
         BZ    SAMOUT1                  BR IF INPUT                UT18
         TM    DCBMACRF,X'80'           IS IT EXCP                M4116
         BO    SAMOUT1                  IF YES, BRANCH            M4116
         TM    DCBOPTCD,TOTFLAG         TEST FOR UTF (OPTCD=T)     UT18
         BZ    SAMOUT1                  BIF NOT SPECIFIED          UT18
         TM    UCBTBYT3,UCB3DACC        CHECK IF DA                UT18
         BO    SAMOUT1                  BR IF DA                   UT18
*                                                                  UT18
         LA    RF,SOP1Y                 SETUP FOR XCTL TO IGG0191Y UT18
*                                                                  UT18
SAMOUT1  EQU   *                        SYMBOLIC ADDR              UT18
*
FINISH   EQU   *
*
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,0(RF)           ST NEXT MODS ID        @Z30TSCF
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     UPDATE WHERE TO GO ENTRY
         LA    RPARC,PLOFF(0,RPARC)     UPDATE PARA LIST ENTRY
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   8,RBASE                  YES BR BACK TO BEGINNING
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BC    7,RELOOP                 NO BRANCH
*
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      RE INIT  WTG LIST
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN                NO XCTL
*
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         BC    15,ZCHEK
*
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
*        AN 013 ABEND IS ISSUED WHEN NO SEC CONTROL UNIT (SCU)
*        UCB IS FOUND FOR 1419 OR IF TRYING TO OPEN THE SCU ONLY.
BADUNIT  EQU   *
NOUCB    EQU   *                                                 M3591
         DMABCOND (RE),PDLOAD,RETURN=NONE,RES=NO,REGSAVE=YES   @ZA01371X
                                        CALL PROBLEM DETERMINATION
         B     TCTLRTN                                           S21042
*
*
WAOFF    EQU   32                  OFFSET OF FIRST ENTRY IN WTG TABLE
PLOFF    EQU   4                   OFFSET OF DCB ENTRIES
WGOFF    EQU   8                   OFFSET OF WTG ENTRIES
DBCKEXCP DEBCHK AM=EXCP,MF=L            DEB AMTYPE FOR EXCP      Y01021
DBCKSAM  DEBCHK AM=SAM,MF=L             DEB AMTYPE FOR SAM       Y01021
DBCKBPAM DEBCHK AM=BPAM,MF=L            DEB AMTYPE FOR BPAM      Y01021
UCBMASK  DS    0F                       ALIGN                   Z30AAWW
         DC    X'0000'                  FOR ZEROING HI-ORDER    Z30AAWW
UCBEND   DC    X'FFFF'                  END OF LOOKUP TABLE     Z30AAWW
ADDRMASK DC    X'0003FFFF'              TO REMOVE FLAG         @ZA06110
*
*  MODULE ID'S
*
SOP11    DC    C'11',VL3(IGG01911)      LAST EXECUTOR          @Z30TSCF
SOP1Y    DC    C'1Y',VL3(IGG0191Y)      USET TOTALING          @Z30TSCF
SOP1B    DC    C'1B',VL3(IGG0191B)      OTHER THAN DA          @Z30TSCF
SOP1N    DC    C'1N',VL3(IGG0191N)      DA TO FINISH DEB       @Z30TSCF
SOP1T    DC    C'1T',VL3(IGG0191T)      UCS/FCB EXECUTOR       @Z30TSCF
PDLOAD   DC    C'6M',VL3(IGG0196M)      PROB DETERMINATION     @Z30TSCF
SOP6Q    DC    C'6Q',VL3(IGG0196Q)      3800 EXECUTOR          @Z40MSRZ
OPIDCNST DC    C'0S'
AMIDCNST DC    C'6A'
         SPACE
PATCH    DC    25H'0'                   PATCH AREA               YM4640
END      EQU   *                        END OF MODULE            Y02072
         EJECT
*********************************************************************
SRT      DSECT
         IEFUCBOB
*
         EJECT
           DCBD  DSORG=PS
CVT      DSECT
         CVT
*   TASK CONTROL BLOCK
         CNOP  0,4
TCB      DSECT
TCBRBP   DS    F                       PO9NTER TO EXECUTING RB
TCBPIE   DS    F                       POINTER TO PROG. INTERRUPT EL.
TCBDEB   DS    F                       POINTER TO DEB QUEUE
TCBTIO   DS    F                       POINTER TO TIOT
TCBCMP   DS    F                       TASK COMPLETION CODE
TCBTRN   DS    F                       TESTRAN FIELD
TCBMSS   DS    F                       DATA BLOCK QUEUE ELEMENT POINTER
TCBPKF   DS    CL1                     RESERVED FOR FUTURE USE
TCBFLGS  DS    FL5                     FLAG BYTES
TCBLMP   DS    CL1                     RESERVED FOR FUTURE USE
TCBDSP   DS    CL1                     RESERVED FOR FUTURE USE
TCBLLS   DS    F                       POINTER TO LOAD LIST
TCBJLB   DS    F                       POINTER TO JOBLIB DCB
TCBJSE   DS    F                       LIST OF INACTIVE PROGRAMS ADDR.
TCBGRS   DS    CL64                    GENERAL REGISTER SAVE AREA
TCBIDF   DS    CL1                     TCB IDENTIFIER FIELD
TCBFSA   DS    CL3                     POINTER TO 1ST PROB.PROG.SAVEAR
TCBTCB   DS    F                       RESERVED FOR FUTURE USE
TCBTME   DS    F                       POINTER TO TIMER ELEMENT
         DS    CL24
TIOT     DSECT
TIOELNGH DS    CL1                           LENGTH OF THIS ENTRY
TIOESTTA DS    CL1                           STATUS -A-
TIOERLOC DS    CL2                           REL. LOC OF POOL
TIOEDDNM DS    2F                            DDNAME
TIOEJFCB DS    1F                            JFCB DISK ADDR
TIOESTTB DS    0CL1
TIOEFSRT DS    CL4
         DS    CL1
*        TIOT POOL ENTRY
TIOPNSLT DS    XL1                           NR.OF SLOTS FOR POOL
         DS    CL1
TIOPNSRT DS    XL1                           NR.OF DEVICES (FILLED
*                                             SLOTS)
TIOPPOOL DS    CL8                           POOL NAME
TIOPSTTB DS    XL1                           STATUS OF SLOT
TIOPSLOT DS    XL3                           SRT ADDRESS OR EMPTY SLOT
TIOTFEND DS    1F                            FINAL END OF AN ENTRY
TIOELINK EQU   TIOERLOC+1                                        S20016
         SPACE 1
*   DATA EXTENT BLOCK  (UNTIL PROTIOS CHANGES)
DEB      DSECT
DEBNMSUB DS    0CL1
DEBTCBAD DS    CL4
DEBDEBAD DS    0CL4
DEBAMLNG DS    CL1
DEBDEBAB DS    CL3
DEBOFLGS DS    0CL1
DEBIRBAD DS    CL4
DEBOPATB DS    0CL1
DEBSYSPG DS    CL4
DEBNMEXT DS    0CL1
DEBUSRPG DS    CL4
DEBPRIOR DS    0CL1
DEBECBAD DS    CL4
DEBPROTG DS    0CL1
DEBDEBID DS    0CL1
DEBDCBAD DS    CL4
DEBEXSCL DS    0CL1
DEBAPPAD DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    CL4
DEBTVLSQ DS    0CL2                VOL SEQ FOR TAPE
DEBBINUM DS    CL2
DEBTVLNM DS    0CL2                NO OF VOL FOR TAPE
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
*  FOR ADDITIONAL EXTENT ON DIRECT ACCESS THE FOLLOWING FIELDS
*  WILL HAVE TO BE OFFSET ANOTHER 16 BYTES
DEBVOLSQ DS    H
DEBVOLNM DS    H
*  FOR EACH SUBROUTINE LOADED BUMP THIS OFFSET BY 2
DEBSUBID DS    H
         TITLE 'IGG0196A - FORMAT OF GOTTEN CORE'
         IECDSECS MAIN,WTG,PREFX,EXPAND=YES                    @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MOD ID              Y02072
CCWOFSET EQU   DXCCW12-FORCORE     CCW RELOCATABLE OFF SET FOR DXCCW12
DCBOFSET EQU   DCBDEBAD-IHADCB     DEB RLCATBL OFFSET IN DCB
           END
