 TITLE 'IGG0191R - 2ND STAGE, IN/OUT, OUT/IN, TAPE, BSAM, CHAINED SCHEDX
               ULING'
IGG0191R CSECT
*
*MODULE NAME - IGG0191R                                          Y02072
*
*DESCRIPTIVE NAME - BUILD IOBS                                   Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          VS 1 RELEASE 3 CHANGES/DELETIONS
*                                                                XA1824
*                                                                XA2035
*          VS 2 RELEASE 2 DELETIONS                              Y02072
*000200,000340011600,013800,033400,040600,088500-088760,         Y02072
*105200-105400,21000,95600,100400,101000,105592                  Y02072
*                                                                YM4697
*                                                                YM6583
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*          RELEASE 20 DELETIONS                                       *
*0972001600,016400,020200-020600,023800-030200,041400-041600,    S20201
*0972046000-046200,055800-087400,091000-091200,093400-093600,    S20201
*0972103600,104200                                               S20201
*0972090200                                                      A40435
*          RELEASE 21 DELETIONS                                       *
* 055100-055200,091800,092200,092600-094800                      A43732
*  REFER TO CODE FLAGGED XA1824                                 SA63262
*  REFER TO CODE FLAGGED XA2035                                 SA62862
*                                                               SA69762
*
*STATUS CHANGE LEVEL 008
*
*   THIS MOD SPLIT INTO MOD IGG0191X IN REL 20.1                 A43732
*
*FUNCTION                                                             *
*        1 GET CORE FOR IOB'S AND DATA CHAINED CHANNEL PROGRAMS
*        2 CLEAR CORE
*        3 CONSTRUCT IOBS
*        4 CONSTRUCT CHANNEL PROGRAMS FOR DISK OR TAPE
*        3 CLEAR CORE FOR THE ABOVE
*        5 CONSTRUCT IOB'S OR ICB'S
*        6 IT SETS AN AUDIT TRAIL BIT IN THE OPEN WORKAREA       Y02072
*          INDICATING TO FORCE CLOSE THAT THE CORE GETMAINED     Y02072
*          FOR THE IOB'S CAN BE FREEMIANED.                      Y02072
*                                                                     *
*ENTRY POINT                                                          *
*        IGG0191R BY XCTL
*                                                                     *
*INPUT                                                                *
*        SEE REGISTER DESCRIPTION,DCB                                 *
*                                                                     *
*OUTPUT                                                               *
*        SEE REGISTER DESCRIPTION                                     *
*                                                                     *
*        USERS DCB                                                    *
*EXTERNAL ROUTINES-NONE                                               *
*                                                                     *
*EXITS                                                                *
*        NORMAL BY XCTL TO IGG01913
*        NORMAL BY XCTL TO IGG0191J FOR DISK OR IGG0191G FOR TAPE
*                                   WHEN ONE BUFFER IS SPECIFIED
*                                                                     *
*        ERROR-NONE                                                   *
*
*MACROS : ACTION - MODESET, GETMAIN, XCTL                        Y02072
*
*MACROS : MAPPING - IECDSECS, IEZIOB, IHAICB, DCBD, IKJTCB,      Y02072
*                   IEZDEB, IHACCW, IHAFCAUD                     Y02072
*                                                                     *
*TABLES/WORKAREAS- WHERE TO GO TABLE (WTG)
*
*      BYTE  0-7  NAME
*      BYTE  8-10 RELATIVE DISK ADDRESS(TTR) OF FIRST RECORD
*      BYTE 11    CONCATENATION NUMBER
*      BYTE 12    ZERO
*      BYTE 13    ALIAS INDICATOR AND USER DATA FIELD DESCRIPTOR.
*                        ALIAS INDICATOR 1 BIT
*                        NUMBER OF TTRS IN USERS DATA FIELD 2 BITS
*      BYTE 14-16 TTR OF FIRST TEXT RECORD
*      BYTE 17    ZERO
*      BYTE 18-19 TTR OF NOTE LIST OR SCATTER LIST
*      BYTE 20    TRANSLATION TABLE
*      BYTE 21    ZERO OR NUMBER OF ENTRIES IN THE NOTE LIST
*      BYTE 22-23 ATTRIBUTES
*      BYTE 24-26 TOTAL CONTIGUOUS MAIN STORAGE REQUIRED FOR MODULE
*      BYTE 27-28 LENGTH OF FIRST TEXT RECORD
*      BYTE 29    LENGTH OF WTG TABLE (IN DOUBLE WORDS)
*      BYTE 30-31 PATH THROUGH LOADS OF ROUTINES
*      BYTE 32-34 IDTTR OF EXECUTOR FOR FIRST DCB
*      BYTE 35    WORKAREA ADDRESS FOR FIRST DCB
*      BYTE 36-39 TABLE OF IDTTR'S
*      BYTE 32+N(8) IDTTR OF EXECUTOR FOR  N TH DCB  (3 BYTES)
*                   WORKAREA ADDRESS FOR N TH DCB    (1 BYTE )
*                   IDTTR OF LAST ROUTINE LOAD       (3 BYTES)
*                   NOT USED                         (1 BYTE)
*                                                                     *
*ATTRIBUTES                                                           *
*        REENTRANT, REUSABLE, RUNS IN DATA MANAGEMENT KEY UNLESS Y02072
*        OTHERWISE SPECIFIED, SUPER STATE                        Y02072
*NOTES                                                                *
*        ALL REG EQUATES HAVE AN 'R' AT BEGINNING OR END OF SYMBOL    *
*                                                                     *
RDCB     EQU   2                       DCB
RBASE    EQU   3                       BASE
RCORE    EQU   4                       WORK AREA ADDRESS
RPAR     EQU   5                       TOP OF PARAMETER LIST
RWTG     EQU   6                       TOP OF WTG TABLE
RPARC    EQU   7                       CURRENT PARAMETER
RWTGC    EQU   8                       CURRENT TRANSFER LOAD
RWK1     EQU   9                       WORK REGISTER             Y02072
*                                      IOB COUNTER DURING IOB CONST   *
RUCB     EQU   10                      BRANCH REG
RDEB     EQU   11
RB       EQU   12
RC       EQU   13                      BUFFER ADDRESS
RD       EQU   14
RJ       EQU   15                      CCW ADDRESS
RE       EQU   0                       BLOCKING FACTOR DURING CHP CONST
RF       EQU   1                       IOB ADDRESS
RWK2     EQU   RF                       WORK REGISTER            Y02072
DOUBLE   EQU   X'02'
NTPTINDX EQU   6                        NOTE/POINT INDEX         XA2035
FSTD     EQU   X'88'
IOBCEXCP EQU   X'80'                    IOBCNOPA FLAG            YM6583
*
         EJECT
         BALR  RBASE,0
         USING ENTRY1,RBASE
         USING IHADCB,RDCB
         USING FORCORE,RCORE
*                                                                     *
ENTRY1   EQU   *
*                                                                     *
         B     BEGIN                    BR AROUND CONSTANTS      Y02072
         DC    C'IGG0191R'              MODULE NAME              Y02072
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'10/17/74'              LAST DATE MODIFIED     @Z30TSCF
BEGIN    DS    0H                                                Y02072
         L     RDCB,0(0,RPARC)         DCB ADDRESS
         L     RCORE,4(0,RWTGC)         LOAD WORKAREA ADDR       Y02072
*                                                                     *
*TEST FOR ONE BUFFER WHICH IS NOT ALLOWED WITH CHAINED SCHEDULING     *
*                                                                     *
         CLI   DCBNCP,X'01'             IS NUMBER OF CHP ONE
         BH    A0000                    NO-BRANCH
*                                       YES XCTL                      *
*                                                                     *
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,SOP5            ID FOR TAPE EXEC       @Z30TSCF
         B     RELOOP                   XCTL
*                                                                     *
*                                                                     *
A0000    EQU   *
         OI    DCBCIND2,X'04'           SET PCI INDICATOR ON
*                                                                     *
*                                                                     *
         MVI   DCBIOBA,X'28'            START TO SET NOP OFFSR     MC0L
         MVI   DCBIOBA+1,X'38'          START TO SET NOP OFFSR     MC0L
         LA    RF,48                    CNST 48 FOR PART OF MIOB LGTH
          SR    RD,RD                   CLEAR ACC REG
         MVI   DCBCNTRL+3,X'02'         TAPE IN-OUT; DA IN EOB IDS
         TM    DCBMACF1,DCBMRPT1        NOTE/POINT, INPUT        XA2035
         BO    A0005                    YES, GO SET INDEX        XA2035
         TM    DCBMACF2,DCBMRPT2        NOTE/POINT, OUTPUT       XA2035
         BZ    A0010                    NO, BYPASS SETTING INDEX XA2035
A0005    EQU   *                                                 XA2035
         MVI   DCBCNTRL+1,NTPTINDX      SET NOTE/POINT INDEX     XA2035
A0010    EQU   *
         LA    RD,64                    LGTH OF ICB PLUS CHP FOR TAPE
         LR    RB,RD                    COPY AMT
         SRL   RB,3                     DETERMINE IOBL FOR CLOSE
         STC   RB,DCBIOBL               PLACE IN IOBL
*                                                                     *
         SR    RJ,RJ                    CLEAR REG
         IC    RJ,DCBNCP                GET NUMBER OF CHP
         MR    RD,RD                    AMT SPACE NEEDED MINUS MIOB
*                                                              LENGTH *
         AR    RJ,RF                    TOTAL AMT OF SPACE NEEDED
         LR    RD,RJ                    SAVE LENGTH
*
*  THE FOLLOWING SAVES THE AMOUNT OF CORE GETMAINED FOR THE      YM4697
*  IOB AND ICBS IN THE FORCE CLOSE AUDIT TRAIL FOR THE FORCE     YM4697
*  EXECUTOR.                                                     YM4697
*                                                                     *
         ST    RJ,DXATEXC2              SAVE LENGTH              YM4697
*
         MODESET  KEYADDR=DXUKEY,WORKREG=9 GET INTO USER KEY     Y02072
         GETMAIN R,LV=(RJ),SP=0                                  Y02072
*                                                                     *
         LR    RC,RF                    SAVE ADDRESS RETURNED FROM
*                                       GETMAIN FOR CLEARING          *
* THIS SECTION CLEARS CORE                                            *
*                                                                     *
         LR    RJ,RD                    RESTORE LENGTH
         LA    RD,255                   CONSTANT 255
         BCTR  RJ,0                     REDUCE AMOUNT BY ONE
COMPARE  EQU   *
         CR    RJ,RD                    AMT TO CLEAR GREATER THAN 255
         BC    12,ONECLEAR              NO-BRANCH
         EX    RD,CLEAR                 CLEAR 256 BYTES
         SR    RJ,RD                    AMT TO CLEAR MINUS 255
         BCTR  RJ,0                     SUBTRACT ONE MORE
         LA    RC,256(0,RC)             UPDATE AREA ADDRESS
         B     COMPARE                  TRY AGAIN
*                                                                     *
CLEAR    EQU   *
         XC    0(1,RC),0(RC)            CLEAR CORE
*
ONECLEAR EQU   *
         EX    RJ,CLEAR                 CLEARS LESS THAN 256
*                                                                     *
* CLEARING OF CORE FINISHED HERE RF EQUALS ADDRESS                    *
*                                                                     *
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         ST    RF,DCBIOBAD              THIS IS MAIN IOB ADDRESS
         OI    FCAOPEN,FCAOIOB          INDIC IOB'S CAN BE FREED Y02072
          LR    RJ,RF                    COPY MIOB ADDRESS
***********************************************************************
* START CONSTRUCTION OF MAIN IOB HERE                                 *
***********************************************************************
         MODESET  KEYADDR=DXUKEY,WORKREG=9 GET INTO USER KEY     Y02072
         MVC   2(2,RF),DCBIOBA          MOVE NOP OFFSETS TO MIOB
         LA    RE,4(0,RF)               ECB ADDRESS
         ST    RE,20(0,RF)              PLACE ADDRESS IN ECB POINTER
         MVI   4(RF),X'7F'              INDICATE COMP NO ERROR
         L     RWK1,DXUDCBAD            GET USERS DCB ADDRESS    Y02072
         USING IOBQSAMC,RF                                       Y02072
         ST    RWK1,IOBDCBPT            DCB ADDR TO IOB          Y02072
         DROP  RF                                                Y02072
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
*
         MVC   DCBOFFSR(2),OFFSETT       TAPE OFFSR,OFFSW TO DCB
*                                                                     *
         MODESET  KEYADDR=DXUKEY,WORKREG=9 GET INTO USER KEY     Y02072
*                                                                     *
         LA    RD,80(0,RF)              TAPE STARTAD
         ST    RD,32(0,RF)              ADDRESS TO MIOB
         LA    RD,48(0,RF)              FIRST ICB ADDRESS
A0020    EQU   *                        ICB ADDRESS TO DCB
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         ST    RD,DCBIOBA
         MODESET  KEYADDR=DXUKEY,WORKREG=9 GET INTO USER KEY     Y02072
         ST    RD,8(0,RF)               FIRST ICBAD TO MIOB
         LR    RF,RD                    COPY ICB ADDRESS FOR FIRST TIME
         SR    RE,RE                    CLEAR REG
         IC    RE,DCBNCP                NUMBER OF CHP FOR COUNTING
***********************************************************************
* CONSTRUCT ICB HERE                                                  *
***********************************************************************
A0025    EQU   *
         ST    RD,0(0,RF)               LINKAD TO ICB
*                                       REDUNDANT FOR FIRST TIME
         LR    RF,RD                    NEW ADDRESS TO ICB REG
         LA    RD,4(0,RF)               ECB ADDRESS
         ST    RD,12(0,RF)              PLACE IN ICB
         MVI   4(RF),X'7F'              INDICATE COMP NO ERROR
*
*
          MVI   29(RF),X'01'             INC COUNT FOR TAPE
*                                                                     *
* CCWS FOR TAPE HERE                                                  *
*                                                                     *
         MVI   36(RF),X'68'             CC,SIL,PCI FLAGS TO CCW
*
* MUST TEST FOR VIRTUAL ENVIRONMENT.  IF YES, TURN PCI FLAG OFF. Y02072
* IF NO, LEAVE PCI FLAG ON.                                      Y02072
*
         L     RDEB,DCBDEBAD            GET DEB ADDRESS          Y02072
         USING DEBBASIC,RDEB                                     Y02072
         L     RWK1,DEBTCBAD            GET TCB ADDRESS          Y02072
         DROP  RDEB                                              Y02072
         USING TCB,RWK1                                          Y02072
         TM    TCBFLGS6,TCBRV           JOB RUNNING V=R          Y02072
         BO    A0028                    NO, LEAVE FLAG ON        Y02072
         USING ICB,RF                                            Y02072
         NI    CCWFLGS,X'FF'-CCWPCI     YES, TURN FLAG OFF       Y02072
A0028    EQU   *                                                 Y02072
*
*  BUILD WRITE CCW
*
         MVC   48(8,RF),32(RF)          COPY READ CCW INTO      SA69762
*                                         WRITE CCW             SA69762
         MVI   48(RF),X'01'             WRITE CMND BYTE         SA69762
*
*  MUST RESET SILI BIT IF FIXED LENGTH RECORDS                   XA1824
*
         TM    DCBRECFM,DCBRECU         UNDEFINED LEN RECORDS    XA1824
         BNM   A0050                    YES, LEAVE SILI ON       XA1824
         TM    DCBRECFM,DCBRECV         VARIABLE LEN RECORDS     XA1824
         BO    A0050                    YES, LEAVE SILI ON       XA1824
         NI    CCWFLGS,X'FF'-CCWSLI     RESET SILI BIT           XA1824
A0050    EQU   *                                                 XA1824
         DROP  RWK1,RF                                           Y02072
         MVI   32(RF),X'02'             READ CMND BYTE
         LA    RD,96(0,RF)              NOP/TIC ADDRESS FOR CCW
         ST    RD,40(0,RF)              PLACE ADDRESS IN CCW
         MVI   44(RF),X'20'             TURN ON SIL FLAG
         MVI   47(RF),X'01'             COUNT OF ONE
         MVI   40(RF),X'03'             NOP CMND BYTE
         LA    RD,112(0,RF)             NOP/TIC ADDRESS FOR CCW
         ST    RD,56(0,RF)              PLACE IN CCW
         MVC   60(4,RF),44(RF)          COPY FLAG AND LGTH FROM NOP RD
         MVI   56(RF),X'03'             NOP CMND BYTE
*                                                                     *
*                                                                     *
*                                                                     *
         LA    RD,64(0,RF)              NXT ICB ADDRESS
*                                                                     *
         BCT   RE,A0025                 MORE ICBS TO MAKE BRANCH
*                                                                     *
*                                                                     *
         L     RD,DCBIOBA               GET FIRST ICB ADDRESS
         ST    RD,0(0,RF)               PLACE AS LAST LINK ADDRESS
         LA    RC,32(0,RD)              FIRST RD CCW ADDRESS
         ST    RC,40(0,RF)              PLACE IN LAST RD NOP
         MVI   40(RF),X'03'             SET CMND BYTE
*                                                                     *
         LA    RC,48(0,RD)              FIRST WR CCW ADDRESS
         ST    RC,56(0,RF)              PLACE IN LAST WR NOP
         MVI   56(RF),X'03'             SET CMND BYTE
*                                                                     *
***********************************************************************
*                                                                     *
* THIS FINISHES NOP/TIC ADDRESSES LAST NOW POINTS TO FIRST CHPS       *
*                                                                     *
*                                                                     *
A0200    EQU   *
*                                       RD EQUALS FIRST ICB ADDRESS   *
*                                                                     *
         LA    RC,56(0,RD)              NOP ADDRESS FOR MIOB          *
*                                       WHEN OPENED FOR IN/OUT        *
*                                       DEVICE DISK OR FOR OUT/IN     *
*                                       WHEN A TAPE                   *
*                                                                     *
         TM    12(RDEB),X'07'          IS DCB FOR OUT/IN?        A40435
         BC    1,A0220                  YES BRANCH
*                                                                     *
*                                                                     *
*
         LA    RC,40(0,RD)              LAST NOP ADDR FOR TAPE
A0220    EQU   *                                                 A43732
         ST    RC,12(0,RJ)              LAST NOP ON Q TO MIOB
         MVI   12(RJ),IOBCEXCP          SET EXCP NEEDED FLAG     YM6583
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
XCTL     EQU   *
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,NXTEXEC         XCTL TO LOAD EXECUTOR  @Z30TSCF
RELOOP   EQU   *
         L     RCORE,4(0,RWTGC)        LOAD RCORE BASE
         LA    RWTGC,8(0,RWTGC)             INCREMENT CURR WTG ENTRY
         LA    RPARC,4(0,RPARC)             INC CURRENT DCB ENTRY PTR
         CLC   0(2,RWTGC),AMIDCNST     Q- THIS RT NEEDED AGAIN
         BCR   8,RBASE
*
         CLC   0(2,RWTGC),OPIDCNST     Q- END OF TABLE
         BC    7,RELOOP                    NO-CHECK NEXT ENTRY
*
         LR    RPARC,RPAR
         LA    RWTGC,32(0,RWTG)             REINIT WTG LIST PTR
ZCHEK    EQU   *
         CLI   0(RWTGC),X'00'               Q-IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN
*
         LA    RWTGC,8(0,RWTGC)             GET NEXT ENTRY
         LA    RPARC,4(0,RPARC)
         B     ZCHEK
*
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
AMIDCNST DC    C'1R'                    THIS ID
OPIDCNST DC    C'0S'                   END ID
SOP5     DC    C'1G',VL3(IGG0191G)      PCI UNSUPPORTABLE      @Z30TSCF
NXTEXEC  DC    C'13',VL3(IGG01913)     NEXT EXEC, NORMAL PATH  @Z30TSCF
OFFSETT   DC   X'2030'                   TAPE OFFSR,OFFSW
PATCH    DC    0H'0',50X'00'            PATCH AREA               Y02072
END      EQU   *                        END OF MODULE            Y02072
         EJECT
         DCBD  DSORG=PS
         EJECT
CVT      DSECT                                                 @Z30TSCF
         CVT                                                   @Z30TSCF
         EJECT
         IHAICB                                                  Y02072
         EJECT
         ORG   ICBEXTEN                                          Y02072
         IHACCW  DSECT=NO                                        Y02072
         EJECT
         IEZDEB                                                  Y02072
         EJECT
         IKJTCB                                                  Y02072
         EJECT
         IECDSECS  (MAIN,(IOB,NO)),IOB,WTG,PREFX,EXPAND=YES    @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MOD ID              Y02072
         EJECT
FORCORE  DSECT                                                   Y02072
         IHAFCAUD  ORG=YES                                       Y02072
         END
