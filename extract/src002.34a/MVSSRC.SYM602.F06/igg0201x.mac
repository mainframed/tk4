 TITLE 'IGG0201X - SAM CLOSE EXECUTOR, NON-DIRECT ACCESS - LOAD 2'
IGG0201X CSECT
*
*MODULE NAME - IGG0201X
*
*DESCRIPTIVE NAME - RESTORE RESOURCES AND CLEAN UP FOR NON-DA
*
*COPYRIGHT - NONE
*
*STATUS CHANGE LEVEL 003
*
*FUNCTION - THE FOLLOWING WILL BE PERFORMED BY THIS CLOSE EXECUTOR FOR
*           BSAM/QSAM DATA SETS NON-DIRECT ACCESS:
*         - IT WILL FREEMAIN THE RECORD AREA IF GOTTEN BY OPEN.
*         - IT WILL FREEMAIN ANY BUFFERS GOTTEN BY OPEN IF
*           CONCATENATION WITH UNLIKE ATTRIBUTES.
*           FOR ALL OTHER CONDITIONS BUFFERS WILL BE RETURNED TO
*           THE POOL.
*         - IT FREEMAINS THE IOBS/ICBS GOTTEN BY OPEN.
*         - IT FREEMAINS THE FCR TABLE FOR 3886 OCR DEVICES.
*         - CLEAR FIELDS IN THE DCB SO THAT IT CAN'T BE USED UNLESS
*           IT IS OPENED AGAIN.
*         - SETS APPROPRIATE AUDIT TRAIL BITS IN THE O/C/E       Y02072
*           WORKAREA IN CASE THE USERS STAE ROUTINE SHOULD GET   Y02072
*           CONTROL DURING A FORCE CLOSE SITUATION. (THESE       Y02072
*           BITS, ALTHOUGH SET DURING NORMAL CLOSE, HAVE NO      Y02072
*           MEANING).                                            Y02072
*
*ENTRY POINT - IGG0201X FROM IGG0201A OR IGG0201B
*
*INPUT - SEE DESCRIPTION OF REGISTERS, DCB(USERS), IOB(USERS).
*
*OUTPUT - NONE
*
*MACROS - ACTION : FREEMAIN, IECRES, MODESET
*
*MACROS - MAPPING : IECDSECT, IECDSECS, DCBD, IGGBCB, IHACCW, IEZDEB,
*                   IEZIOB, IGGPARML, IGGSCW, IHAFCAUD
*
*EXTERNAL ROUTINES - NONE
*
*EXITS - NORMAL : TO IGG0200B, IGG0200G
*
*EXITS - ABNORMAL : BR 14 TO FORCE CLOSE EXEC IF FORCE CLOSE IN  Y02072
*                   CONTROL                                      Y02072
*
*ATTRIBUTES - REENTRANT, REUSABLE, RUNS IN USER KEY UNLESS       Y02072
*             OTHERWISE SPECIFIED, SUPERVISOR STATE              Y02072
*
*TABLES/WORKAREAS - WHERE TO GO TABLE (WTG)
*
*      BYTE  0-7  NAME
*      BYTE  8-10 RELATIVE DISK ADDRESS(TTR) OF FIRST RECORD
*      BYTE 11    CONCATENATION NUMBER
*      BYTE 12    ZERO
*      BYTE 13    ALIAS INDICATOR AND USER DATA FIELD DESCRIPTOR.
*                        ALIAS INDICATOR 1 BIT
*                        NUMBER OF TTRS IN USERS DATA FIELD 2 BITS
*      BYTE 14-16 TTR OF FIRST TEXT RECORD
*      BYTE 17    ZERO
*      BYTE 18-19 TTR OF NOTE LIST OR SCATTER LIST
*      BYTE 20    TRANSLATION TABLE
*      BYTE 21    ZERO OR NUMBER OF ENTRIES IN THE NOTE LIST
*      BYTE 22-23 ATTRIBUTES
*      BYTE 24-26 TOTAL CONTIGUOUS MAIN STORAGE REQUIRED FOR MODULE
*      BYTE 27-28 LENGTH OF FIRST TEXT RECORD
*      BYTE 29    LENGTH OF WTG TABLE (IN DOUBLE WORDS)
*      BYTE 30-31 PATH THROUGH LOADS OF ROUTINES
*      BYTE 32-36 IDTTR OF EXECUTOR FOR FIRST DCB
*      BYTE 37-39 WORKAREA ADDRESS FOR FIRST DCB
*      BYTE 40-   TABLE OF IDTTR'S
*      BYTE 32+N(8) IDTTR OF EXECUTOR FOR N TH DCB   (5 BYTES)
*                   WORKAREA ADDRESS FOR N TH DCB    (3 BYTES)
*                   IDTTR OF LAST ROUTINE LOAD       (5 BYTES)
*                   NOT USED                         (3 BYTES)
*
*      CLOSE WORK AREA
*
*NOTES - ALL REGISTER EQUATES BEGIN WITH THE CHARACTER R.
*        THE DEB ADDRESS IS GOTTEN FROM THE PROTECTED DCB FOR    Y02072
*        INTEGRITY REASONS.                                      Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:
*
*          VS2 RELEASE 01 DELETIONS/CHANGES
*D097000-097200                                                  YM0928
*
*          VS2 RELEASE 02 DELETIONS/CHANGES
*000080,000200,000340,000480,004900,024900,025200,025204,        Y02072
*025300-025324,025500-025720,097762,100600,103700-103960,115800, Y02072
*115900,116200-116300,117720,117880,124480,133400,133600,        Y02072
*124700-124968,129800,132700,100200,100340,115400,117600,117720, Y02072
*120000-120400,121000                                            Y02072
*24620-24640                                                     YM3079
*                                                                YM4640
*                                                                YM7595
*                                                                YM6889
*C125000                                                       @AZ09146
*          VS2 RELEASE 03 DELETIONS/CHANGES
*                                                              @Z30TSMI
*          RELEASE 21 DELETIONS/CHANGES
*                                                                A48163
*                                                                A45221
*                                                                M0008
* 123400                                                         A40495
*                                                                A45280
*                                                               SA67354
*
         EJECT
*
* REGISTER USAGE
*
RWK1     EQU   0                        WORK REGISTER
R1       EQU   1                        PARAMETER REGISTER
RWK2     EQU   R1                       WORK REGISTER
RDCB     EQU   2                        ADDR OF USERS DCB
RBASE    EQU   3                        BASE REGISTER
RCORE    EQU   4                        ADDR OF CLOSE WORK AREA
RPAR     EQU   5                        PARAMETER LIST
RWTG     EQU   6                        START OF WTG
RPARC    EQU   7                        CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                        CURRENT ENTRY IN WTG TABLE
RWK7     EQU   9                        WORK REGISTER
RWK4     EQU   10                       WORK REGISTER
RDEB     EQU   11                       DEB ADDRESS
RIOB     EQU   12                       IOB REGISTER
RWK3     EQU   RIOB                     WORK REGISTER
RWK5     EQU   14                       WORK REGISTER
RBRNCH   EQU   RWK5                     RETURN REGISTER          Y02072
R15      EQU   15                       PARAMETER REGISTER       Y02072
RWK6     EQU   R15                      WORK REGISTER
         EJECT
*
* EQUATES FOR CONSTANTS
*
RDBACKB  EQU   X'03'                    TEST FOR READ BACKWARD    OC022
OUTINOUT EQU   X'07'                    TEST FOR OUTPUT OR OUTIN  OC022
OCR3886  EQU   X'57'                    DCBDEVT FOR 3886 OCR    XL03127
FCRTBLSZ EQU   2048                     3886 FCR TABLE SIZE     XL03127
DEFLTCP  EQU   1                        DEFAULT NO OF CHAN PGMS
CLOSEFIN EQU   0                        CLOSE FINISHED THIS DCB
MIOBSIZE EQU   48                       SIZE OF MAIN IOB IN CHAIN SCHED
INVALADD EQU   X'01'                    USED TO INVALIDATE       Y02072
*                                         ADDRESS VECTORS        Y02072
CLEARIND EQU   0                        FOR CLEARING ONE BYTE INDIC'S
DCBLNFMT EQU   X'FF'                    FOR CLEARING LINE FORMAT
*                                         NUMBER ON 3886 OCR DEVICES
         EJECT
*
* ADDRESSABILITY FOR DSECTS
*
         USING WTG,RWTG
         USING WTGENTRY,RWTGC
         BALR  RBASE,0
         USING AMCL1,RBASE
AMCL1    EQU   *
         B     BEGIN                    BRANCH AROUND MODULE ID  YM4640
         DC    C'IGG0201X'              MODULE NAME              YM4640
         DC    C'@AZ09146'              LAST SHIP CODE         @AZ09146
         DC    C'03/10/76'              LAST SHIP DATE         @AZ09146
BEGIN    DS    0H                       END OF MODULE ID         YM4640
         L     RCORE,WTGDCBWA           GET DCB'S WORK AREA      Y02072
         USING FORCORE,RCORE                                     Y02072
         L     RDCB,DXPDCBAD            GET PROTECTED DCB ADDR   Y02072
         USING IHADCB,RDCB                                       Y02072
         L     RDEB,DCBDEBAD            GET VALID DEB ADDR       Y02072
         USING DEBBASIC,RDEB
         MODESET  KEYADDR=DXUKEY,WORKREG=13                      Y02072
         SR    RWK7,RWK7                PREPARE FOR IC INST
         L     RDCB,DXUDCBAD            GET DCB BEING CLOSED     Y02072
         L     RIOB,DCBIOBA             GET CURR IOB ADDRESS
         LA    RIOB,0(RIOB)             CLEAR HIGH ORDER BYTE
         USING IOBQSAMN,RIOB
         CLI   DCBDEVT,DCBDVTRM         TSO TERMINAL
         BE    AMCL5000                 YES, BRANCH
         TM    DCBCIND2,DCBCNQSM        DCB USING QSAM
         BZ    AMCL3000                 NO BRANCH
         TM    DCBCIND1,DCBCNEXB        EXCHANGE BUFFERING USED
         BO    AMCL3000                 YES BRANCH
         L     RWK5,DCBBUFCB            GET BUFFER POOL CNTRL BLK
*
* IF A RECORD AREA WAS ACQUIRED BY OPEN, IT SHOULD BE RETURNED TO
* THE SUPERVISOR.
*
         LA    RWK5,0(RWK5)             CLEAR HIGH ORDER BYTE    A48163
         USING BCBLK,RWK5
         TM    BCBFLGS,BCBLRI           RECORD AREA INDICATOR ON A48163
         BZ    AMCL2010                 BRANCH NO TO CHECK OFLGS A48163
         TM    DCBCIND2,DCBCNBFP        DID OPEN ACQUIRE POOL    A48163
         BZ    AMCL2010                 BRANCH NO TO CHECK OFLGS A48163
         L     R1,BCBLRIAR              GET RECORD AREA ADDR     A48163
         USING LRIAREA,R1
         L     RWK4,LRILGTH             GET LENGTH OF RECORD AREA
         LA    RWK4,0(RWK4)             CLR HI BYT FOR OR INST   YM6889
         FREEMAIN R,LV=(RWK4),A=(1),SP=0  FREE RECORD AREA       Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        AUDIT TRAIL IN KEY 5     Y02072
         SPACE 2
         OI    FCACLOS1,FCACRECA        REC AREA FREED           Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=13  BACK TO USER KEY    Y02072
         SPACE 2
         NI    BCBFLGS,X'FF'-BCBLRI     TURN OFF RECORD AREA FLAG
         XC    BCBLRIAR,BCBLRIAR        CLEAR RECORD AREA ADDR
AMCL2010 EQU   *
         SR    RWK6,RWK6                CLEAR REGISTER           YM0928
         IC    RWK6,DCBBUFNO            GET NUMBER OF BUFFERS    YM0928
*                                        TO FREE
         TM    DCBOFLGS,DCBOFEOV        CONCATENATION UNLIKE ATTRIBUTES
         BZ    AMCL2030                 NO BRANCH TO FREE BUFFERS
         TM    DCBCIND2,DCBCNBFP        DID OPEN ACQUIRE POOL
         BZ    AMCL2030                 NO BRANCH TO RETURN BUFFERS
*                                         TO THE POOL
*
* FREE BUFFER POOL  OPEN WILL ACQUIRE A NEW ONE LATER
*
AMCL2020 EQU   *
         LR    R1,RWK5                  SAV CNTRL BLK ADDR FOR FREEMAIN
         DROP  RWK5
         USING BCBLK,RWK2
         LH    RWK5,BCBBUFSZ            GET BUFFER SIZE
         MR    RWK5,RWK5                TOTAL AMT REQUIRED FOR BUFFERS
         LA    RWK4,BCBNLN(RWK6)        ADD EIGHT FOR CNTRL BLK  Y02072
         TM    BCBFLGS,BCBEXTND         IS BUFCB LENGTH 16 BYTES A48163
         BNO   FREE                     NO, GO FREE POOL
         LA    RWK4,BCBEXLN(RWK4)       YES, ADD 8 BYTES MORE    Y02072
FREE     EQU   *                                                 A48163
*
         FREEMAIN R,LV=(RWK4),A=(1),SP=0  FREE BUFFER POOL       Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        AUDIT TRAIL IN KEY 5     Y02072
         SPACE 2
         OI    FCACLOS1,FCACBUFC        BUFFERS FREED            Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=13  BACK TO USER KEY    Y02072
         SPACE 2
         XC    DCBBUFCA,DCBBUFCA        CLEAR ADDRESS IN DCB
         OI    DCBBUFCB+3,INVALADD      SET INVALID ADDRESS
         B     AMCL3000
*
* BEFORE RELINKING BUFFERS IT MUST BE KNOWN IF IT CAN BE DONE    YM7595
* SAFELY. IF THIS IS A FORCE CLOSE SITUATION, OPEN COULD HAVE    YM7595
* BEEN IN THE PROCESS OF UNCHAINING BUFFERS FOR CCW BUFFER       YM7595
* ADDRESSES WHEN THE ERROR OCURRED. IF THIS IS THE CASE, THE     YM7595
* STATE OF THE BUFFER POOL IS NOT KNOWN AND THEREFORE IT WOULD   YM7595
* BE RISKY TO TRY TO RECHAIN. IF THIS IS THE CASE THE BUFFER     YM7595
* POOL SHOULD BE FREEMAINED BECAUSE IT IS UNUSABLE.              YM7595
*
AMCL2030 EQU   *
         TM    FCACLOS1,FCACFORC        FORCE CLOSE IN CONTROL   YM7595
         BZ    AMCL2035                 NO, GO RELINK BUFFERS    YM7595
         TM    FCAOPEN2,FCAOIOBC        WAS IOB CONSTRUCTION     YM7595
*                                         COMPLETED              YM7595
         BO    AMCL2035                 YES, OK TO RELINK BUFS   YM7595
         TM    DCBCIND2,DCBCNBFP        DID OPEN BUILD BUFFERS   YM7595
         BO    AMCL2020                 YES, GO FREEMAIN BUF     YM7595
*                                         POOL                   YM7595
         BZ    AMCL3000                 NO, CANNOT FREE USER     YM7595
*                                         GOTTEN BUFFERS         YM7595
*
* SIMULATE THE FREE BUFFER MACRO TO RETURN BUFFERS TO POOL
*
AMCL2035 EQU   *
         USING BCBLK,RWK5
         IC    RWK7,DCBOFFSW            GET WRITE CCW OFFSET      OC022
         TM    DEBOPATB,OUTINOUT        TEST TYPE OF I/O          OC022
         BO    AMCL2040                 BRANCH IF OUTPUT          OC022
         IC    RWK7,DCBOFFSR            GET READ CCW OFFSET       OC022
AMCL2040 EQU   *                        BUFFER CLEARING LOOP      OC022
         LA    RWK4,0(RWK7,RIOB)        GET TO RD/WRT CCW ADDR
         USING CCW,RWK4
         L     RWK4,CCWADDRA            GET BUFFER ADDRESS
         DROP  RWK4
         LA    RWK4,0(RWK4)             HIGH BYTE CLEARED
         TM    DEBOPATB,RDBACKB         IS DCB OPENED FOR READ BACKWARD
         BNM   AMCL2070                 IF NOT, BRANCH.
         LH    RWK1,DCBBLKSI            GET SIZE OF ONE BUFFER AND
         BCTR  RWK1,0                   DECREMENT BY ONE.
         SR    RWK4,RWK1                POINT TO BEGINNING OF BUFFER.
         B     AMCL2080                 CONT PROC READ BACKWARD  S19017
AMCL2070 EQU   *
         CLI   DCBDEVT,DCBDVOR5         CHECK FOR A              S19017
         BL    AMCL2080                 1285, 1287, OR 1288      S19017
         CLI   DCBDEVT,DCBDVOR8         DCBDEVT IS 5A,5B,OR 5C   S19017
         BH    AMCL2080                 IF NOT, CONT PROCESSING  S19017
         DROP  RIOB
         USING CCW+(IOBQSAMN-IOBEXTEN-L'CCW),RWK3  ADDRESSABILITY TO
*                                                    SECOND CCW IN C.P.
         LH    RWK1,CCWBYTE             OTHERWISE, LOAD COUNT    S19017
         DROP  RWK3
         USING IOBQSAMN,RIOB
         TM    DCBRECFM,DCBRECF         F OR U FORMAT RECORDS
         BO    AMCL2075                 YES, BRANCH
         AH    RWK1,RDWLEN              ADD 4 TO COUNT FOR       S19017
AMCL2075 EQU   *                           VAR RECS              S19017
         BCTR  RWK1,0                   DECREMENT BY 1 TO        S19017
*                                         ACCT FOR RD
         SR    RWK4,RWK1                POINT TO BEGIN OF BUFFER S19017
AMCL2080 EQU   *                                                 S19017
         USING BUFFER,RWK4
         MVC   BUFNXPTB,BCBBUFAD        UPDATE LAST BUF WITH NEW ONE
         ST    RWK4,BCBBUFPT            UPDATE BUFFER POOL CNRL BLK
         L     RIOB,IOBNIOBA            GET NEXT IOB IN CHAIN
         BCT   RWK6,AMCL2040            IF MORE IOB'S FREE BUF'S  OC022
         SPACE 2
         MODESET  EXTKEY=DATAMGT        AUDIT TRAIL IN KEY 5     Y02072
         SPACE 2
         OI    FCACLOS1,FCACBUFP        BUFFERS RET TO POOL      Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=13  BACK TO USER KEY    Y02072
         SPACE 2
*
* PREPARE TO FREE SPACE REQUIRED FOR IOB/ICB S
*
AMCL3000 EQU   *
         SR    RWK5,RWK5                INITIALIZE REGISTERS
         SR    RWK1,RWK1
         SR    RWK2,RWK2
         IC    RWK1,DCBIOBL             GET LEN OF IOB/ICB (DBL WRDS)
         SLL   RWK1,3                   GET LEN IN BYTES
         IC    RWK2,DCBNCP              GET NUMBER OF BSAM CP.S
         TM    DCBCIND2,DCBCNQSM        IS QSAM BEING USED
         BZ    AMCL3010                 NO BRANCH
         IC    RWK2,DCBBUFNO            GET NUMBER OF QSAM CP.S
AMCL3010 EQU   *
         TM    DCBCIND2,DCBCNCHS        WAS DCB USING PCI SCHEDULING
         BO    AMCL3050                 YES BRANCH
AMCL3020 EQU   *
         TM    IOBNFLG1,IOBFIRST        THIS THE FIRST IOB IN CHAIN
         BO    AMCL3060                 YES BRANCH
         L     RIOB,IOBNIOBA            GET NEXT IOB
         B     AMCL3020                 CONTINUE TO LOOP
AMCL3050 EQU   *
         L     RIOB,DCBIOBAD            GET MAIN IOB ADDRESS
         LA    RIOB,0(RIOB)             CLEAR HIGH BYTE
         LA    RWK5,MIOBSIZE            CNST OF 48 FOR MIOB
AMCL3060 EQU   *
         LTR   RWK2,RWK2                WERE NO. OF CP SPECIFIED
         BP    AMCL3065                 YES, BRANCH
         LA    RWK2,DEFLTCP             NO, PROVIDE ONE
AMCL3065 EQU   *
         MR    RWK1,RWK1                NUMBER OF CP'S X IOB/ICB SIZE
         LA    RWK4,0(RWK2,RWK5)        ADD ANY PCI CNSTS REQ    Y02072
*
         FREEMAIN R,LV=(RWK4),A=(RIOB),SP=0  FREE IOB/ICB SPACE  Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        AUDIT TRAIL IN KEY 5     Y02072
         SPACE 2
         OI    FCACLOS1,FCACIOB         IOBS FREED               Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=13  BACK TO USER KEY    Y02072
         SPACE 2
*
* THE FOLLOWING CODE FREES UP THE 2K SPACE FOR THE FCR TABLE    XL03127
* USED BY THE 3886 OCR DEVICE                                   XL03127
*
         CLI   DCBDEVT,OCR3886          IS DEVICE A 3886 OCR    XL03127
         BNE   AMCL5000                 NO,BRANCH AROUND        XL03127
         L     RIOB,DCBFRTBA            GET FCR TABLE PTR       XL03127
         LA    RWK4,FCRTBLSZ            TABLE SIZE 2048 BYTES    Y02072
         XC    DCBFRTBA,DCBFRTBA        CLEAR OUT PTR FIELD     XL03127
         FREEMAIN R,LV=(RWK4),A=(RIOB),SP=0   FREE FCR TABLE     Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        AUDIT TRAIL IN KEY 5     Y02072
         SPACE 2
         OI    FCACLOS1,FCACFCR         FCR FREED                Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=13  BACK TO USER KEY    Y02072
         SPACE 2
         MVI   DCBLNNUM,0               CLEAR FIELD             XL03127
         MVI   DCBLFMAT,DCBLNFMT        RESET FIELD             XL03127
         EJECT
*
* THIS SECTION OF CODE CLEARS BSAM/QSAM VECTORS IN DCB
*
AMCL5000 EQU   *
         SR    RWK2,RWK2                CLEAR REGISTER           A45280
         SR    RWK6,RWK6
         IC    RWK6,DEBNMEXT            NUMBER OF EXTENTS        A45280
         IC    RWK2,DEBEXSCL            OBTAIN SCALING FACTOR    A45280
         SLL   RWK6,0(RWK2)             LNGTH OF DEB EXTENT AREA A45280
         LA    RWK6,DEBBASND-DEBBASIC(RWK6,RDEB)  POINT TO       A45280
*                                         ACCESS METHOD SECTION
         USING DEBACSMD,RWK6
         MVC   DCBLRECL,DEBLRECL        RESTORE LRECL            A45280
         DROP  RWK6
         SR    RWK6,RWK6                CLEAR REGISTER           A45280
         ST    RWK6,DCBWCPO             CLEAR OFFSET FIELDS
         LA    RWK6,INVALADD            TO SET AN INVALID ADDR   Y02072
         ST    RWK6,DCBIOBA             IOB VECTOR CLEARED
         ST    RWK6,DCBIOBAD            IOB FOR PCI CLEARED    @Z30TSMI
         ST    RWK6,DCBEOBW             WRITE EOB VECTOR CLEARED Y02072
         STCM  RWK6,B'0111',DCBCNTRL+1  CNTRL MOD VECTOR CLEARED Y02072
         STCM  RWK6,B'0111',DCBEOBRA    READ EOB VECTOR CLEARED  Y02072
         STCM  RWK6,B'0111',DCBREAD+1   READ MOD VECTOR CLEARED  Y02072
         TM    DCBOFLGS,DCBOFEOV        CONCATENATION UNLIKE ATTRIBUTES
         BO    AMCL5005                 YES BRANCH
         STCM  RWK6,B'0111',DCBPERRA    ERR RTN VECTOR CLEARED   Y02072
AMCL5005 EQU   *
         TM    DCBCIND2,DCBCNQSM        WAS QSAM USED
         BZ    AMCL5006                 NO BRANCH
         ST    RWK6,DCBEOB              CLEAR VECTOR
AMCL5006 EQU   *
         TM    DCBCIND1,DCBCNBRM        WAS BLK REC BIT TURNED   A45221
*                                         OFF DURING OPEN?       A45221
         BZ    AMCL5008                 NO, BR AROUND            A45221
         OI    DCBRECFM,DCBRECBR        YES, RESTORE BIT         A45221
         NI    DCBCIND1,X'FF'-DCBCNBRM  RESET BIT               SA67354
AMCL5008 EQU   *                                                 A45221
         NI    DCBCIND1,CLEARIND        CLEAR SAM INDICATOR      A40495
         NI    DCBCIND2,DCBCNFEO        RESET ALL BUT FEOV INDIC A40495
         CLI   DCBDEVT,DCBDVOR5         CHECK FOR A              S19017
         BL    AMCL5010                  1285, 1287, OR 1288     S19017
         CLI   DCBDEVT,DCBDVOR8         DCBDEVT IS 5A,5B,OR 5C   S19017
         BH    AMCL5010                 IF NOT, CONTINUE         S19017
         XC    DCBDSPLA,DCBDSPLA        CLEAR DCBDSPLY (+13)     S19017
         XC    DCBRESCA,DCBRESCA        CLEAR DCBRESCN (+17)     S19017
         EJECT
AMCL5010 EQU   *                                                 S19017
         SPACE 2
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         SPACE 2
         TM    FCACLOS1,FCACFORC        FORCE CLOSE IN CONTROL   Y02072
         BNO   XCTL                     NO, CONTINUE NORMAL PATH Y02072
         SR    R15,R15                  RET CD FOR FC EXEC TO    Y02072
*                                         PASS TO CLOSES TRR     Y02072
         L     RBRNCH,24(RPAR)          GET RETURN ADDR        @AZ09146
         BR    RBRNCH                   RETURN TO FORCE CLO EXEC Y02072
XCTL     EQU   *                        NON-FORCE CLOSE PATH     Y02072
         MVI   WTGENTRY,0               CLEAR WTG ENTRY          Y02072
RELOOP   EQU   *
         LA    RWTGC,L'WTGENTRY(,RWTGC)  STEP TO NEXT ENTRY
         LA    RPARC,L'PARDCBAD(,RPARC)  STEP TO NEXT ENTRY
         CLC   WTGIDNX,AMIDCNST         THIS ROUTINE NEEDED AGAIN
         BCR   8,RBASE                  RECURSE THRU MODULE
         CLC   WTGIDNX,CLLDB            IS THIS THE END OF THE TABLE
         BL    RELOOP                   NO BRANCH
         CLC   WTGIDNX,CLLDG            IS THIS THE END OF THE TABLE
         BH    RELOOP                   NO BRANCH
         LR    RPARC,RPAR               INITIALIZATION
         DROP  RWTGC
         LA    RWTGC,WTGENTRY
         USING WTGENTRY,RWTGC
AMCL5011 EQU   *
         CLI   WTGIDTTR,CLOSEFIN        IS THIS ENTRY = ZERO?
         BNE   AMCL5019                 NO BRANCH
         LA    RWTGC,L'WTGENTRY(,RWTGC) NEXT ENTRY IN WTG TAB
         LA    RPARC,L'PARDCBAD(,RPARC) NEXT DCB ADDRESS
         B     AMCL5011
AMCL5019 EQU   *
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSMI*
               MODID=WTGENTRY           GO TO NEXT ROUTINE     @Z30TSMI
         EJECT
*
*CONSTANTS
*
AMIDCNST DC    C'1X'
RDWLEN   DC    H'4'                     USED AS A CONST OF 4     S19017
*                                         IN A AH INST
CLLDB    DC    C'0B'                    MOD ID                   Y02072
CLLDG    DC    C'0G'                    MOD ID                   Y02072
         SPACE
PATCH    DC    25H'0'                   PATCH AREA               YM4640
END      EQU   *                        END OF MODULE            Y02072
         TITLE 'IGG0201X - FORECORE DSECT'
         IECDSECS  (MAIN,(IOB,NO)),EXPAND=YES                    Y02072
         EJECT
         IGGSCW
         TITLE 'IGG0201X - DSECT FOR AUDIT TRAIL'
         IHAFCAUD  ORG=YES                                       Y02072
         TITLE 'IGG0201X - DCB DSECT'
         DCBD  DSORG=PS
         TITLE 'IGG0201X - BUFFER CONTROL BLOCK DSECT'
         IGGBCB
         TITLE 'IGG0201X - CCW DSECT'
         IHACCW DSECT=YES
         TITLE 'IGG0201X - CVT DSECT'
CVT      DSECT
         CVT
         TITLE 'IGG0201X - IOB DSECT'
         IEZIOB
         TITLE 'IGG0201X - WHERE TO GO TABLE DSECT'
         IECDSECS WTG,PREFX,EXPAND=YES
         ORG   WTG+8                                             Y02072
WRKRETRN DS    F                        FORCE CLOSE RET ADDR     Y02072
         ORG   WTGIDTTR
WTGIDNX  DS    CL2                      ID NEXT MODULE
WTGTTRNX DS    CL3                      TTR NEXT MODULE
         ORG   WTGIDTTR+4
WTGDCBWA DS    A                        WORK AREA ADDRESS CURRENT DCB
         TITLE 'IGG0201X - CLOSE PARAMETER LIST DSECT'
         IGGPARML
         TITLE 'IGG0201X - DEB DSECT'
         IEZDEB
         END
