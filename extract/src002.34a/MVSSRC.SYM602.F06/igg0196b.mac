         TITLE 'IGG0196B MAIN EXECUTOR LOAD 2'
IGG0196B CSECT
***********************************************************************
*
*MODULE NAME - IGG0196B
*
*DESCRIPTIVE NAME - PATH DETERMINER
*
*COPYRIGHT - NONE
*
*CHANGE ACTIVITY - AS FOLLOWS:
*
*        VS1 RELEASE 2 DELETIONS
*                                                               SA61288
*
*        VS2 RELEASE 02 DELETIONS
*                                                                Y02072
*                                                                YM7889
*
*        VS2 RELEASE 03 DELETIONS
*        THE MODULE WAS RENUMBERED                             @Z30TSMI
*
*        VS2 RELEASE 03 UPDATE                                  ZA03193
*        VS2 RELEASE 04 DELETIONS
* A 142000
*                                                              @Z40MSMI
*
*
*        VS2 RELEASE 04 CHANGES
* A 107600-107992,108100                                        ZA03763
*
*          RELEASE 20 DELETIONS
*                                                                M5103
*                                                                S20202
*                                                                A36344
*                                                                S20016
*                                                                M5103
*                                                                A30962
*                                                                S20201
*          RELEASE 21 DELETIONS
*                                                                A48615
*                                                                S21088
*                                                                M0099
*                                                                S21040
*                                                                S21042
*                                                                M0040
*
*STATUS CHANGE LEVEL 005
*
* THIS MODULE WAS SPLIT IN REL 19 FROM IGG0191B IN SUPPORT OF MICR
*
*FUNCTION/OPERATION: THIS ROUTINE SUPPLEMENTS THE INITIAL OPEN ROUTINE.
*        FOR QSAM IT WILL DEFAULT BUFNO (BUFNO ZERO ON ENTRY) TO
*        THE FOLLOWING VALUES:
*         A) THREE FOR PUNCH DEVICES.
*         B) FIVE FOR ALL OTHER DEVICES.
*        FOR 3886 IT WILL SET BUFNO TO ONE.
*        FOR BSAM IT WILL INSURE THAT BUFNO IS AT LEAST 12 FOR
*        1275 AND 1419 DEVICES.
*        THE ROUTINE WILL GO TO IGG0191I IF IT IS NECESSARY TO
*        GET BUFFERS OTHERWISE IT WILL GO TO IGG0193I WITH
*        THE FOLLOWING EXCEPTIONS:
*         A)IT WILL GO TO IGG0191T FOR PRINTERS WITH UCS OF FCB
*         B)IT WILL GO TO IGG0197L FOR 3505/3525'S WITH OMR OR RCE
*         C)IT WILL GO TO IGG0196Q FOR 3800 PRINTERS
*
*ENTRY POINT - IGG0196B ENTERED FROM IGG0191B BY USE
*
*INPUT: SEE DESCRIPTION OF REGISTERS.
*OUTPUT: SEE DESCRIPTION OF REGISTERS.
*EXTERNAL ROUTINES: IFG019RA
*EXITS-NORMAL:  GO TO:
*                        IGG0193I             STAGE 2 SELECTOR
*                        IGG0191I             BUILD BUFFERS
*                        IGG0191T             PRINTERS WITH UCS OR FCB
*                        IGG0196Q             3800
*                        IGG0197L             OMR OR RCE
*EXITS - ERROR  GO TO PROBLEM DETERMINATION FOR WTP AND 013 ABEND
*
*TABLES/WORKAREAS: SEE DSECTS AT THE END OF THIS LISTING.
*
*MACROS-ACTION:MODESET, IECRES, DMABCOND
*
*ATTRIBUTES: REENTRANT, REUSABLE, RUNS IN DATA MANAGEMENT KEY
*            UNLESS OTHERWISE SPECIFIED, SUPERVISOR STATE
***********************************************************************
         EJECT ,                                                 A26344
*
***********************************************************************
*
*  REGISTER CONVENTIONS USED THROUGH OUT ALL OPEN PASSES
*
***********************************************************************
*
*
RE       EQU   0                       WORK/PARAMETER REGISTER
RF       EQU   1                       WORK/PARAMETER REGISTER
RDCB     EQU   2                       ADDR OF USERS DCB
RBASE    EQU   3                       BASE REGISTER
RCORE    EQU   4                       ADDR OF OPEN WORK AREA
RPAR     EQU   5                       PARAMETER LIST
RWTG     EQU   6                       START OF WTG
RPARC    EQU   7                       CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                       CURRENT ENTRY IN WTG TABLE
RUCB     EQU   10                      UCB ADDR/WORK REGISTER
RDEB     EQU   11                      DEB ADDRESS
RB       EQU   12                      WORK REGISTER
*********************************************************************
*
* *** EQUATES USED TO SET OR CHECK DCBBUFNO ***
*
MICRMIN  EQU   12                      MINIMUM BUFNO FOR 1419 - 1275
PUNDEF   EQU   3                       DEFAULT BUFNO FOR PUNCHES
GENDEF   EQU   5                       NORMAL BUFNO DEFAULT
MICRDEF  EQU   1                       3886 BUFNO DEFAULT
*
* *** GENERAL EQUATES ***
*
NOADDR   EQU   1                       BIT 7 IN LOW ORDER BYTE OF
*                                      AN ADRESS MEANS THE ADDRESS
*                                      IS NOT PRESENT
OABD072  EQU   72                      CAUSES A 013-4C ABEND
OABD073  EQU   73                      CAUSES A 013-50 ABEND
*
***********************************************************************
         EJECT ,                                                 A26344
         BALR  RBASE,0
         USING PO,RBASE
         USING PARML,RPARC
         USING WTGENTRY,RWTGC
         USING WTG,RWTG
*
*
*
PO       EQU   *
*
         B     BEGIN                   BR AROUND CONSTANTS       Y02072
         DC    C'IGG0196B'             MODULE NAME               Y02072
         DC    C'@Z40MSMI'             LAST SHIP CODE          @Z40MSMI
         DC    C'01/31/75'             LAST SHIP DATE          @Z40MSMI
BEGIN    DS    0H                                                Y02072
*
         L     RDCB,PARDCBAD           GET DCB ADDRESS
         USING IHADCB,RDCB
         L     RCORE,WTGCORE-1         GET WORK AREA           @Z30TSMI
         USING FORCORE,RCORE
         L     RDEB,DCBDEBAD           GET DEB ADDRESS
         USING DEBBASIC,RDEB
*
*
         SR    RB,RB                   CLEAR REG
         IC    RB,DCBBUFNO             GET BUFFER NUMBER
*
         TM    DCBCIND2,DCBCNQSM       USING QSAM              @Z30TSMI
         BO    TSTQSAM                 YES BRANCH                S19033
         LA    RF,SOP3I                SELECTOR EXEC           @Z30TSMI
         CLI   DCBDEVT,DCBDVORS        IS THIS A 1275            S19033
         BE    MICR                    YES,BRANCH                S19033
         CLI   DCBDEVT,DCBDVMR         IS THIS A 1419            S19033
         BNE   TSTOCR                  NO, BRANCH                S19033
MICR     EQU   *                                                 S19033
         TM    DCBBUFCB+3,NOADDR       BUFFER CONTROL BLOCK      S19033
*                                       SPECIFIED                S19033
         BNO   XCTLGOGO                YES BRANCH                S19033
         CLI   DCBBUFNO,MICRMIN        TEST NUMBER OF BUFFERS    A48615
         BNL   ID1I                    12 MIN.                   A48615
         MVI   DCBBUFNO,MICRMIN        ELSE DEFAULT OR ADJUST    A48615
         B     ID1I                    GO TO XCTL TO 1I.         A48615
TSTOCR   EQU   *                       CHECK 1285/1287/1288/3886 S19033
         CLI   DCBDEVT,DCBDVOR9        IS DEVICE A 3886        @Z30TSMI
         BE    XCTLGOGO                YES - GO TO 3I          @Z30TSMI
         CLI   DCBDEVT,DCBDVOR5        CHECK FOR A 1285          S19017
         BL    TSTBSAM                 NOT OCR - CONTINUE      @Z30TSMI
         CLI   DCBDEVT,DCBDVOR8        TEST IF DEVT LT/E 5C      S19017
         BNH   XCTLGOGO                OCR - GO TO 3I          @Z30TSMI
*
TSTBSAM  EQU   *
         TM    DCBBUFCB+3,NOADDR       BUFFER CONTROL BLOCK SPECIFIED
         BZ    AMOPI2A                 YES BRANCH
*
         LTR   RB,RB                   IS BUF NUMBER PRESENT
         BZ    AMOPI2A                 NO BRANCH
ID1I     EQU   *
         LA    RF,SOP7                 GET ADDR OF BUFFER EXEC     DM0B
         B     AMOPI2J                                           S20016
*
TSTQSAM  EQU   *
         CLI   DCBDEVT,DCBDVMRS        DEVICE A 3890 ?          XL03123
         BNE   TSTBUFNO                NO-BRANCH                XL03123
         TM    DCBBUFCB+3,NOADDR       BUFFER CONTROL BLOCK ?   XL03123
         BZ    OLDBUFPL                YES-BRANCH               XL03123
         MVI   DCBBUFNO,MICRDEF        SUPPLY ONE BUFFER        XL03123
         B     ID1I                    GET BUFFER POOL          XL03123
TSTBUFNO EQU   *                                                XL03123
         LTR   RB,RB                   IS BUFNO PRESENT
         BNZ   TSTQSAM2                YES-BRANCH
*
         TM    DCBBUFCB+3,NOADDR       BUFFER CONTROL BLOCK?    ZA03763
         BO    SETBUFN1                NO-BRANCH TO SET BUFNO   ZA03763
         MODESET  KEYADDR=DXUKEY,WORKREG=15  GET INTO USER KEY  ZA03763
         L     RB,DCBBUFCB                                      ZA03763
         USING BCBLK,RB                                         ZA03763
         IC    RB,BCBBUFNO             SAVE BUFNO               ZA03763
         DROP  RB                                               ZA03763
         MODESET  EXTKEY=DATAMGT       BACK TO DATA MANAG KEY   ZA03763
         STC   RB,DCBBUFNO             SUPPLY BUFCB BUFNO       ZA03763
         CLI   DCBBUFNO,GENDEF         BUFCB BUFNO LT DEFAULT   ZA03763
         BL    SETBUFN2                YES-USE BUFCB BUFNO      ZA03763
SETBUFN1 EQU   *                                                ZA03763
         MVI   DCBBUFNO,GENDEF         SUPPLY 5 BUFFERS        @Z30TSMI
SETBUFN2 EQU   *                                                ZA03763
         CLI   DCBDEVT,DCBDVCPR        DEVICE A 2520?            A36344
         BE    THREEBUF                SKIP 2540 TEST            A36344
         CLI   DCBDEVT,DCBDVCP0        USING 2540
         BNE   TSTQSAM2                NO-BRANCH
THREEBUF MVI   DCBBUFNO,PUNDEF         SUPPLY 3 BUFFERS          A36344
TSTQSAM2 EQU   *
         TM    DCBBUFCB+3,NOADDR       BUFFER CONTROL BLOCK SPECIFIED
         BO    ID1I                    NO-BRANCH
*
*  IF USING AN OLD BUFFER POOL,                                  A26344
*  ITS BUFL MAY BE TOO SMALL TO HANDLE AN INCREASED BLKSIZE.     A26344
*  THIS ERROR CAN ONLY BE SAFELY DIAGNOSED FOR QSAM.             A26344
OLDBUFPL EQU   *                                                XL03123
         MODESET  KEYADDR=DXUKEY,WORKREG=15  GET INTO USER KEY   Y02072
         L     RB,DCBBUFCB             TEST IF BUFFER POOL BUFL  A26344
         USING BCBLK,RB
         CLC   BCBBUFSZ,DCBBLKSI       IS LESS THAN BLKSIZE      A26344
         DROP  RB
         MODESET  EXTKEY=DATAMGT       BACK TO DATA MANAG KEY    Y02072
         LA    RE,OABD072              013-4C ABEND              S21042
         BL    DCBERROR                BR IF YES TO CALL PROB    S21042
*                                      DET                       S21042
*
* IF LOGICAL RECORD INTERFACE IS REQUIRED, NEED TO GO TO IGG0191I
* TO GETMAIN FOR RECORD AREA
*
         TM    DCBBFTEK,DCBBFTA        LOGICAL RECORD INTERFACE  S19015
         BNO   AMOPI2A                 BRANCH NO                 S19015
*
         L     RF,DCBBUFCB             GET BUFCB                 S19015
         USING BCBLK,RF
         MODESET  KEYADDR=DXUKEY,WORKREG=15  GET INTO USER KEY   Y02072
         TM    BCBFLGS,BCBLRI          IS RECORD AREA PRESENT    S19015
         DROP  RF
         MODESET  EXTKEY=DATAMGT       BACK TO DATA MANAG KEY    Y02072
         BZ    ID1I                    BRANCH NOT TO GO TO 1I    S19015
*
AMOPI2A  EQU   *                       DO NOT NEED BUFFER BUILT
         LA    RF,SOP3I                ID OF STAGE 2 SELECTOR  @Z30TSMI
*
AMOPI2J  EQU   *                                                 S20016
         CLI   DCBDEVT,DCBDVCR2        3505                      S21088
         BE    AMOPI2J0                YES                       S21088
         CLI   DCBDEVT,DCBDVCP1        3525                      S21088
         BNE   AMOPI2J2                NO - CONTINUE             S21088
AMOPI2J0 EQU   *                       3505 OR 3525              S21088
         TM    DCBMODE,DCBMODEO+DCBMODER  OMR OR RCE             S21088
         BNZ   AMOPI2J1                YES - GO TO 7L            S21088
         CLI   DCBFUNC,0               FUNCTION SPECIFIED        S21088
         BE    AMOPI2J2                NO - CONTINUE             S21088
         CLI   DCBFUNC,DCBFNCBR        READ ONLY                 S21088
         BE    AMOPI2J2                YES - CONTINUE            S21088
         CLI   DCBFUNC,DCBFNCBP        PUNCH ONLY                S21088
         BE    AMOPI2J2                YES - CONTINUE            S21088
AMOPI2J1 EQU   *                       NEED 7L                   S21088
         LA    RF,SOP7L                OMR RCE EXEC              S21088
         B     XCTLGOGO                XCTL                      S21088
AMOPI2J2 EQU   *                       CONTINUE                  S21088
*                                                                  DM0B
****************************************************************** DM0B
*                                                                  DM0B
*    TEST IF THE DEVICE IS A PRINTER AND IF IT IS CAUSE AN 013-50
*    ABEND IF IT IS NOT OPEN FOR OUTPUT OR OUTIN.
*    IF IT IS A PRINTER AND HAS UCS OF FCB IT WILL GO TO IGG0191T.
*    IF IT IS A 3800 PRINTER IT WILL GO TO IGG0196Q
*                                                                  DM0B
****************************************************************** DM0B
*                                                                  DM0B
         CLI   DCBDEVT,DCBDVPR5        3800 PRINTER            @Z40MSMI
         BE    CHKAB                   SEE IF OPENED CORRECTLY @Z40MSMI
         CLI   DCBDEVT,DCBDVPR1        IF LESS THAN X'48' NOT   SA61288
*                                         A PRINTER             SA61288
         BL    XCTLGOGO                BR IF NOT A PRINTER      SA61288
         CLI   DCBDEVT,DCBDVPR4        IF HIGHER THAN X'4B'     SA61288
*                                         NOT A PRINTER         SA61288
         BH    XCTLGOGO                BR IF NOT A PRINTER      SA61288
CHKAB    EQU   *
         LA    RE,OABD073              013-50 ABEND              S21042
         TM    PAROPT,PAROUTIN         DCB OPENED FOR OUT OR OUTIN12873
         BNO   DCBERROR                CALL PROBLEM DETERMINATION 12873
         CLI   DCBDEVT,DCBDVPR5        IS IT 3800              @Z40MSMI
         BE    XCTLGO6Q                YES - GO TO 196Q        @Z40MSMI
         CLI   DCBDEVT,DCBDVPR3        IF A 3211, IT HAS UCS    SA61288
         BE    XCTLGO1T                BYPASS UCS TEST          SA61288
         CLI   DCBDEVT,DCBDVPR4        IF A 2245, IT DOES NOT   SA61288
*                                         HAVE UCS BUT IT DOES  SA61288
*                                         HAVE AN FCB           SA61288
         BE    XCTLGO1T                BYPASS UCS TEST          SA61288
         L     RUCB,DEBSUCBA           GET UCB ADDRESS
         USING UCBOB,RUCB
         TM    UCBTBYT2,UCB2OPT0       TEST IF PRINTER HAS UCS     DM0B
         DROP  RUCB
         BZ    XCTLGOGO                IF NOT,GOTO XCTL ROUTINE    DM0B
*
XCTLGO1T EQU   *                                                 S20202
*                                                                  DM0B
         LA    RF,SOPT                 LOAD ADDR OF UCS EXECUTOR   DM0B
         B     XCTLGOGO                GO TO 1T                @Z40MSMI
*
XCTLGO6Q EQU   *
         LA    RF,SOP6Q                3800 EXEC               @Z40MSMI
****************************************************************** DM0B
*                                                                  DM0B
XCTLGOGO EQU   *                                                   DM0B
*                                                                  DM0B
         MVC   WTGIDTTR,0(RF)          ID TO MOD NAME            Y02072
RELOOP   EQU   *
         LA    RWTGC,WTGEND            INCREMENT CURRENT WTG ENTRY
         LA    RPARC,L'PARDCBAD(0,RPARC) INCR CURRENT DCB ENTRY PTR
         CLC   WTGID,AMIDCNST          THIS RT NEEDED AGAIN
         BER   RBASE                   RETURN TO INIT. ANOTHER DCB
*
         CLC   WTGID,OPIDCNST          END OF TABLE
         BNE   RELOOP                  NO, CHECK NEXT ENTRY
*
         LR    RPARC,RPAR
         LA    RWTGC,WTGENTRY-WTG(,RWTG)  REINIT WTG LIST PTR
ZCHEK    CLI   WTGENTRY,X'00'          IS THIS ENTRY COMPLETE
         BNE   TCTLRTN                 IF NOT TRANSFER CONTROL
*
         LA    RWTGC,WTGEND            GET NEXT ENTRY
         LA    RPARC,L'PARDCBAD(0,RPARC)
         B     ZCHEK
*
TCTLRTN  EQU   *
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSMI*
               MODID=WTGENTRY                                  @Z30TSMI
         EJECT
**********************************************************************
*
*
*
DCBERROR EQU   *
*
         DMABCOND (0),PDLOAD,RETURN=NONE,RES=NO,REGSAVE=YES      S21042X
                                        CALL PROBLEM DETERMINATION
         B     TCTLRTN                                           S21042
         DROP  RWTG,RWTGC,RPARC,RBASE,RDCB,RCORE,RDEB
         EJECT
**********************************************************************
*
*  MODULE ID'S
*
SOP7     DC    C'1I',VL3(IGG0191I)      FOR SYSTEM BUILT BUFFER  Y02072
*                                         POOLS                  Y02072
SOP3I    DC    C'3I',VL3(IGG0193I)      STAGE 2 EXEC SELECTOR
SOPT     DC    C'1T',VL3(IGG0191T)      PRINTERS WITH UCSB/FCB'S Y02072
SOP6Q    DC    C'6Q',VL3(IGG0196Q)      3800 PRINTERS          @Z40MSMI
SOP7L    DC    C'7L',VL3(IGG0197L)      3505 OR 3525 WITH OMR OR RCE
PDLOAD   DC    C'6M',VL3(IGG0196M)      PROBLEM DETERMINATION    Y02072
OPIDCNST DC    C'0S'                    COMMON OPEN              Y02072
AMIDCNST DC    C'6B'                    ID OF THIS LOAD
         SPACE
PATCH    DC    25H'0'                   PATCH AREA               Y02072
         EJECT ,                                                 A26344
         IGGBCB
         EJECT
CVT      DSECT
         CVT
         EJECT
         DCBD  DSORG=PS                                         SA61288
DCBDVPR4 EQU   X'4B'                    DEVICE ID FOR 2245      SA61288
**********************TEMPORARY WAITING FOR 3800 DCB UPDATE
         EJECT
         IEZDEB
         EJECT
         IECDSECS MAIN,WTG,PREFX,EXPAND=YES                      Y02072
         ORG   WTGIDTTR
WTGID    DS    CL2                      ID PORTION
         EJECT
         IGGPARML
         EJECT
SRT      DSECT
         IEFUCBOB
         END
