         TITLE 'IGG0191P OPEN EXECUTOR (D/A UPDATE)'
IGG0191P CSECT
*MODULE NAME - IGG0191P                                          Y02072
*
*DESCRIPTIVE NAME - BUILD IOBS                                   Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          VS2 RELEASE 2 DELETIONS                               Y02072
*000204,000400,034000,035300-035800,047000,047400,047800,        Y02072
*081000-085000,120500-121200,089000,101000,1168000,118000,46300  Y02072
*43000,44000,44600-45400                                         YM2268
*                                                                YM4697
*                                                                YM7062
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*
*          RELEASE 20 DELETIONS
*2960001600-002200,004200,032800,053200-112200,120600-121600     S20201
*
*          RELEASE 21 DELETIONS
* 026600,036800,039400,040000,040600                             A31983
*                                                               SA53279
*STATUS CHANGE LEVEL 007
*FUNCTION/OPERATION- THIS ROUTINE WILL SET UP A CODE IN THE DCBCNTRL
*      FIELD FOR IGG01912.  IGG01912 (LOAD EXECUTOR) WILL DETERMINE
*      FROM THIS CODE WHICH ROUTINES WILL BE BROUGHT INTO CORE AT
*      OPEN TIME.  IT WILL DETERMINE HOW MANY IOB'S ARE TO BE CREATED,
*      USE 'GETMAIN' TO GET CORE FOR THE IOB'S, AND CLEAR THE CORE.
*      IT SETS AN AUDIT TRAIL BIT IN THE OPEN WORKAREA           Y02072
*      INDICATING TO FORCE CLOSE THAT THE CORE GETMAINED FOR THE Y02072
*      IOB'S CAN BE FREEMAINED.                                  Y02072
*
*ENTRY POINTS- ENTERED FROM IGG0191B BY USE OF THE XCTL MACRO.
*
*INPUT- SEE DESCRIPTION OF REGISTERS, DCB(USERS).
*
*OUTPUT & IOB(USER)- SEE DESCRIPTION OF REGISTERS, DCB(USERS).
*
*EXTERNAL ROUTINES- NONE.
*
*EXITS NORMAL-XCTL TO IGG0191Z FOR RECORD READY
*             XCTL TO IGG0196P FOR NON RECORD READY
*
*EXITS-ERROR- NONE.
*
*MACROS : ACTION - MODESET, GETMAIN, XCTL                        Y02072
*
*TABLES/WORKAREAS- WHERE TO GO TABLE (WTG)
*
*      BYTE  0-7  NAME
*      BYTE  8-10 RELATIVE DISK ADDRESS(TTR) OF FIRST RECORD
*      BYTE 11    CONCATENATION NUMBER
*      BYTE 12    ZERO
*      BYTE 13    ALIAS INDICATOR AND USER DATA FIELD DESCRIPTOR.
*                        ALIAS INDICATOR 1 BIT
*                        NUMBER OF TTRS IN USERS DATA FIELD 2 BITS
*      BYTE 14-16 TTR OF FIRST TEXT RECORD
*      BYTE 17    ZERO
*      BYTE 18-19 TTR OF NOTE LIST OR SCATTER LIST
*      BYTE 20    TRANSLATION TABLE
*      BYTE 21    ZERO OR NUMBER OF ENTRIES IN THE NOTE LIST
*      BYTE 22-23 ATTRIBUTES
*      BYTE 24-26 TOTAL CONTIGUOUS MAIN STORAGE REQUIRED FOR MODULE
*      BYTE 27-28 LENGTH OF FIRST TEXT RECORD
*      BYTE 29    LENGTH OF WTG TABLE (IN DOUBLE WORDS)
*      BYTE 30-31 PATH THROUGH LOADS OF ROUTINES
*      BYTE 32-36 IDTTR OF EXECUTOR FOR FIRST DCB
*      BYTE 37-39 WORKAREA ADDRESS FOR FIRST DCB
*      BYTE 40-   TABLE OF IDTTR'S
*      BYTE 32+N(8) IDTTR OF EXECUTOR FOR N TH DCB   (5 BYTES)
*                   WORKAREA ADDRESS FOR N TH DCB    (3 BYTES)
*                   IDTTR OF LAST ROUTINE LOAD       (5 BYTES)
*                   NOT USED                         (3 BYTES)
*
*      SEE DEVICETB DSECT
*
*ATTRIBUTES- REENTRANT,REUSABLE, RUNS IN SUPERVISOR STATE,       Y02072
*            RUNS IN USER KEY UNLESS OTHERWISE SPECIFIED.        Y02072
*
*
***********************************************************************
*
*  REGISTER CONVENTIONS USED THROUGH OUT ALL OPEN PASSES
*
***********************************************************************
*
RDCB     EQU   2         DCB REGISTER
RBASE    EQU   3         BASE REGISTER
RCORE    EQU   4         WORK AREA ADDRESS
RPAR     EQU   5         TOP OF PARAMETER LIST
RWTG     EQU   6         TOP OF WTG TABLE
RPARC    EQU   7         CURRENT PARAMETER
RWTGC    EQU   8         CURRENT TRANS LOAD
RTIOT    EQU   9         WRK REG AND COMM VECTOR ADDR.
RUCB     EQU   10        COUNTER IN IOB GENERATION
RDEB     EQU   11        DEB ADDRESS
RB       EQU   12        WORK REG1  **
RC       EQU   13        WORK REG2  **  USED IN IOB GENERATION
RD       EQU   14        WORK REG3  **
RJ       EQU   15        WORK REG4
RE       EQU   0         WORK REG5
RF       EQU   1         WORK REG6
*
***********************************************************************
*
* MASKS FOR ACCESS METHODS IN SAM
*
QSAMB    EQU   X'01'
*
*
***********************************************************************
*
* MASKS FOR MACRO'S TO BE USED
*
POINTB   EQU   X'04'
*
***********************************************************************
*
* DCB OFFSET CONSTANTS
*
WCPO     EQU   48
WCWCPL   EQU   7              NO. OF CCW'S IN WRITE CHANNEL       18445
*                             PROGRAM IF WRITE CHECK IS SPECIFIED 18445
WCPL     EQU   3
OFFSR    EQU   112
WCOFFSR  EQU   144            OFFSET OF READ CCW FROM START OF    18445
*                             IOB PREFIX IF WRITE CHECK IS        18445
*                             SPECIFIED WITH QSAM                 18445
OFFSW    EQU   64
OFFSRB   EQU   104
WRTCKL   EQU   32             LENGTH OF WRITE CHECK SECTION OF    18445
*                             WRITE CHANNEL PROGRAM               18445
WCOFFSRB EQU   136            OFFSET OF READ CCW FROM START OF    18445
*                             IOB PREFIX IF WRITE CHECK IS        18445
*                             SPECIFIED WITH BSAM                 18445
*
***********************************************************************
*
* MASKS USED TO TEST OPTION FIELDS IN DCB
*
DOUBLE   EQU   X'01'
WRTCK    EQU   X'80'
UPDATEB  EQU   X'80'
TRKOVFLB EQU   X'20'                    TRACK OVERFLOW OPTION
*
***********************************************************************
*
* COMMAND BYTES FOR THE 2841
*
*       CONTROL UNIT
*
SEEK     EQU   X'0B'
SRCHE      EQU   X'31'
RDC        EQU   X'92'
RDD        EQU   X'86'
RDDX     EQU   X'06'
TIC        EQU   X'08'
WRTD       EQU   X'05'
WRTKD    EQU   X'0D'
RDKD     EQU   X'8E'
*
***********************************************************************
*
NRECREDY EQU   X'20'                   NON RECORD READY MASK     S20201
UFORMAT  EQU   X'C0'                    UFORMAT                  S20201
TRKOVFL  EQU   X'20'                    TRACK OVERFLOW OPTION    S20201
ZERO     EQU   0                                                 S20201
TWO      EQU   2                        TWO                      S20201
THREE    EQU   3                        THREE                    S20201
FOUR     EQU   4                       FOUR                      S20201
FIVE     EQU   5                                                 S20201
SIX      EQU   6                        SIX                      S20201
EIGHT    EQU   8                        EIGHT                    S20201
NINE     EQU   9                                                 S20201
TWENTY4  EQU  24                        CONSTANT OF 24           S20201
FSTDX    EQU   X'88'                    FIXED STANDARD BIT      SA53279
QIOBCP   EQU  128                       QSAM IOB + CHAN PGM LEN  A31983
BIOBCP   EQU  120                       BSAM IOB + CHAN PGM      A31983
NOSKCYL  EQU  24                        WRT CHK AND NOT TRK OVFL A31983
SKCYLCCW EQU  8                         WRT CHK AND TRK OVFLOW   A31983
ROUNDCNT EQU   8                        USED TO ROUND UP COUNT   Y02072
*   CHANNEL CONTROL WORD FLAGS
*
SLI        EQU   X'20'
CC         EQU   X'40'
SLICC    EQU   X'60'
CCSK     EQU   X'50'
SLICCSK  EQU   X'70'
*
***********************************************************************
*
* MASKS FOR RECORD FORMATS
*
FORMATU    EQU   X'C0'
*
***********************************************************************
*
* OFFSET FOR PARAMETER TESTS
*
ONE      EQU   1
NOT      EQU   X'FF'
*
***********************************************************************
*
* MASKS FOR STATUS BITS TO BE TESTED
*
FIRSTIOB   EQU   X'01'
READONLY EQU   X'20'                    IOB READ ONLY INDICATOR
*
***********************************************************************
*
* MASKS USED TO LOAD IGG01915 IN SUPPORT OF QSAM LOCATE MODE OF
* OPERATION WITH LOGICAL RECORD INTERFACE FOR VARIABLE LENGTH
* SPANNED RECORD
*
FLGOFFST EQU   4                        OFFSET TO FLAG IN BCB    S19015
RAFLAG   EQU   X'80'                    RECORD AREA PRESENT      S19015
NULL     EQU   0                        ZERO DISPLACEMENT        S19015
IDLEN    EQU   5                        LENGTH OF IDTTR          S19015
*
*
*
*
*
*
*
*
*
*
*
*
         BALR  RBASE,0                  REG 3 IS BASE
         USING SOPB,RBASE
         USING IHADCB,RDCB              REG 2 IS DCB
         USING FORCORE,RCORE            REG 4 IS WORK AREA ADDRESS
*
SOPB     EQU   *
         B     BEGIN                    BR AROUND CONSTANTS      Y02072
         DC    C'IGG0191P'              MODULE NAME              Y02072
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'10/18/74'              LAST DATE MODIFIED     @Z30TSCF
BEGIN    DS    0H                                                Y02072
         L     RDCB,0(RPARC)            GET DCB ADDRESS
         LA    RDCB,0(RDCB)             CLEAR HI BYTE
         USING WTGENTRY,RWTGC                                    YM7062
         ICM   RCORE,B'0111',WTGCORE    GET WORKAREA ADDRESS     YM7062
         DROP  RWTGC                                             YM7062
         SR    RC,RC                    READY A WORK REG
         ST    RC,DCBCNTRL              CLEAR PARAMETER LINK FIELD
         MVI   DCBCNTRL+3,X'02'         EOB RTN ID FOR LD EXEC
*
         TM    DCBRECFM,UFORMAT         IS IT FORMAT U           S20201
         BM    LAB01                    NO, BRANCH AROUND        S20201
         TM    DCBRECFM,TRKOVFL         IS IT TRACK OVERFLOW     S20201
         BNO   LAB01                    BRANCH IF NOT            S20201
         OI    JFCBMASK+SIX,NRECREDY                             S20201
LAB01    EQU   *                                                 S20201
         LA   RF,QIOBCP                 QSAM IOB + CP LEN        A31983
         TM    DCBCIND2,QSAMB           QSAM USED
         BC    1,SOPB12                 BRANCH ON YES
*                                  BSAM USED
         TM    DCBMACRF,POINTB          NOTE POINT USED
         BC    1,SOPB10                 YES, BRANCH
         TM    DCBMACRF+1,POINTB        NOTE POINT USED
         BC    14,SOPB11                NO, BRANCH
*
SOPB10   EQU   *
         MVI   DCBCNTRL+1,X'05'         NPDT ID NUMBER
*
SOPB11   EQU   *
         LA   RF,BIOBCP                 BSAM IOB + CP LEN        A31983
*
SOPB12   EQU   *
         TM    JFCBMASK+SIX,NRECREDY    IS IT NON RECORD READY   S20201
         BO    SOPB14                   YES BRANCH AROUND REC    S20201
*                                       READY                    S20201
         LA    RF,TWENTY4(RF)           INCREASE FOR RECORD      S20201
*                                       READY                    S20201
         TM    DCBRECFM,TRKOVFLB        TRK OVFL                SA53279
         BO    SOPB14                   BRANCH YES              SA53279
         TM    DCBRECFM,FSTDX           FIXED STANDARD?         SA53279
         BO    SOPB14                   BRANCH YES              SA53279
         LA    RF,EIGHT(RF)             BUMP BY EIGHT            S20201
SOPB14   EQU   *                                                 S20201
         TM    DCBOPTCD,WRTCK           WRITE CHECK USED
         BC    14,SOPB20                NO, BRANCH
         LA   RF,NOSKCYL(RF)            WRT CHK, NO TRK OVFLW    A31983
         TM   DCBRECFM,TRKOVFLB         TRK OVFLOW USED?         A31983
         BNO  NOTRKOVF                  NO, DON'T ADD FOR SEEK   A31983
*                                          CYLINDER CCW          A31983
         LA   RF,SKCYLCCW(RF)           YES, ADD FOR SK CYL CCW  A31983
NOTRKOVF EQU  *                                                  A31983
         TM    JFCBMASK+SIX,NRECREDY    IS IT NON RECORD READY   S20201
         BO    SOPB20                   YES BRANCH AROUND REC    S20201
*                                       READY                    S20201
         LA    RF,EIGHT(RF)             INCREASE FOR RECORD      S20201
*                                       READY                    S20201
*
*    AT THIS POINT RF=REG.1 CONTAINS THE LENGTH OF ONE IOB
*
SOPB20   EQU   *
*
         SRL   RF,3                     DIVIDE BY 8 (DOUBLE WORDS)
         STC   RF,DCBIOBL               STORE 1 IOB LGN FOR CLOSE EXEC
         SR    RE,RE                    READY A WORK REG
         TM    DCBCIND2,QSAMB           QSAM BEING USED
         BC    14,SOPB21                NO, BRANCH
         IC    RE,DCBBUFNO              OBTAIN NUMBER OF IOB'S TO BUILD
         B     SOPB22
SOPB21   EQU   *
         IC    RE,DCBNCP                OBTAIN NUMBER OF IOB'S TO BUILD
         LTR   RE,RE                    DID PP SPEC NO OF CP     YM2268
         BNZ   SOPB22                   YES, USE SPEC VALUE      YM2268
         LA    RE,1                     NO, DEFAULT TO ONE       YM2268
         STC   RE,DCBNCP                  IF NOT SPECIFIED       YM2268
SOPB22   EQU   *
         LR    RUCB,RE                  RUCB WILL BE USED AS A COUNTER
*
         SLL   RF,3                     CONVERT IOB LENGTH TO BYTES
         MR    RE,RE                    COMPUTE TOTAL SPACE NEEDED
         TM    JFCBMASK+SIX,NRECREDY    IS IT NON RECORD READY   S20201
         BO    SOPB25                   YES BRANCH AROUND        S20201
         LR    RDEB,RF                  BYTES FOR IOB'S          S20201
         LR    RB,RUCB                                           S20201
*
* THE NUMBER OF SECTOR SAVE AREAS REQUIRED IS 1 MORE THAN THE    Y02072
* NUMBER OF IOBS. ALL READ CHAN PGMS USE THE SAME SECTOR SAVE    Y02072
* AREA BUT EACH WRITE CHAN PGM HAS A DIFFERENT SECT SAVE AREA.   Y02072
*
         LA    RB,ROUNDCNT(RB)          ROUND UP COUNT           Y02072
         SRL   RB,THREE                                          S20201
         SLL   RB,THREE                                          S20201
         AR    RF,RB                                             S20201
SOPB25   EQU   *                                                 S20201
         LR    RD,RF                    SAVE FOR
         LR    RB,RF                       LATER USE
*
*  THE FOLLOWING SAVE THE AMOUNT OF CORE GETMAINED FOR THE IOBS  YM4697
*  IN THE FORCE CLOSE AUDIT TRAIL FOR FORCE CLOSE.               YM4697
*
         ST    RD,DXATEXC2              SAVE LENGTH              YM4697
         MODESET  KEYADDR=DXUKEY,WORKREG=9 GET INTO USER KEY     Y02072
*
         GETMAIN R,LV=(RD),SP=0                                  Y02072
*
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
**
*    THIS SECTION CLEARS THE SPACE RECEIVED FROM GETMAIN
**
***********************************************************************
*
         LR    RJ,RB                    RESTORE LENGTH TO CLEAR
         ST    RF,DCBIOBA               SAVE FIRST IOB ADDRESS
         OI    FCAOPEN,FCAOIOB          INDIC IOB'S CAN BE FREED Y02072
         BCTR  RJ,0                     REDUCE AMT BT ONE
         LA    RB,255                   GET MAX AMT CAN CLEAR
         MODESET  KEYADDR=DXUKEY,WORKREG=9 GET INTO USER KEY     Y02072
COMPARE  CR    RJ,RB                    IS AMT TO CLEAR GREATER 255
         BC    12,ONECLEAR              NO BRANCH
*
         EX    RB,CLEAR                 CLEAR 256 BYTES
         SR    RJ,RB                    SUBT 255 FROM AMT TO BE CLEARED
         BCTR  RJ,0                     SUBT ONE MORE BYTE
         LA    RF,256(RF)               UPDATE AREA ADDR BY 256
         B     COMPARE
*
CLEAR    XC    0(1,RF),0(RF)            ACTUAL CLEAR INSTRUCTION
*
ONECLEAR EX    RJ,CLEAR                 CLEARS LESS THAN 256
*
         L     RB,DCBIOBA               GET FIRST IOB ADDR
         ST    RUCB,ZERO(RB)            STORE IOB COUNT          S20201
         TM    JFCBMASK+SIX,NRECREDY    IS THIS NON RECORD READY S20201
         BO    AROUND                   NO, FORGET RPS INSTRS    S20201
         AR    RDEB,RB                  ADDR OF FIRST SECTOR     S20201
*                                       BYTE                     S20201
         ST    RDEB,FOUR(RB)            STORE ADDR OF SECTOR1    S20201
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,IGG0191Z        191Z FOR RPS DEVICES   @Z30TSCF
         B     RELOOP                   BR AROUND INSTR FOR 6P   S20201
AROUND   EQU   *                                                 S20201
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         MVC   WTGIDTTR,IGG0196P      196P FOR NON=RPS DEVICES @Z30TSCF
RELOOP   EQU   *
         LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURR WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCR CURRENT DCB ENTRY PRTR
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   8,RBASE
*
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BC    7,RELOOP                 NO,CHECK NEXT ENTRY
*
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      REINITIALIZE WTG LIST PTR
*
ZCHEK    EQU   *
         CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN                IF NOT TRANSFER CONTROL
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         BC    15,ZCHEK
*
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
**
*
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES
OPIDCNST DC    C'0S'
AMIDCNST DC    C'1P'
IGG0196P DC    C'6P',VL3(IGG0196P)      NON-RPS                @Z30TSCF
IGG0191Z DC    C'1Z',VL3(IGG0191Z)      RPS                    @Z20TSCF
         SPACE 2
PATCH    DC    0H'0',50X'00'            PATCH AREA               Y02072
END      EQU   *                        END OF MODULE            Y02072
         EJECT
           DCBD  DSORG=PS
CVT      DSECT
         CVT
*
*      DATA EXTENT BLOCK
           CNOP  0,4
BLDEB    DSECT
DEBNMSUB DS    0CL1
DEBTCBAD DS    CL4
DEBAMLNG DS    0CL1
DEBDEBAD DS    CL4
DEBOFLGS DS    0CL1
DEBIRBAD DS    CL4
DEBOPATB DS    0CL1
DEBSYSPG DS    CL4
DEBNMEXT DS    0CL1
DEBUSRPG DS    CL4
DEBPRIOR DS    0CL1
DEBECBAD DS    CL4
DEBPROTG DS    0CL1
DEBDEBID DS    0CL1
DEBDCBAD DS    CL4
DEBEXSCL DS    0CL1
DEBAPPAD DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    CL4
DEBBINUM DS    CL2
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
*
*
         IECDSECS  MAIN,WTG,PREFX,EXPAND=YES                   @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MODS ID             Y02072
         EJECT
FORCORE  DSECT                                                   Y02072
         IHAFCAUD  ORG=YES                                       Y02072
         EJECT
           END
