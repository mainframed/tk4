201Z TITLE 'IGG0201Z -- SAM/PAM CLOSE EXECUTOR, DIRECT-ACCESS'
IGG0201Z CSECT                                                    7M142
*
*MODULE NAME - IGG0201Z
*
*DESCRIPTIVE NAME - CLOSE EXECUTOR FOR DA DEVICES
*
*COPYRIGHT - NONE
*
*STATUS CHANGE LEVEL - SEE BEGINNING OF OBJECT CODE
*
*FUNCTION - THE FOLLOWING FUNCTIONS WILL BE PERFORMED BY THIS CLOSE
*           EXECUTOR FOR BSAM, QSAM AND BPAM DATA SETS-DIR. ACC. DEV.
*           * A GETMAIN IS ISSUED IN THE BEGINNING OF THE MODULE TO
*             PROVIDE THE AM ROUTINES WITH A SAVEAREA IN SP 230. Y02072
*             THIS AREA IS ALSO USED AS A PARMETER LIST FOR STOW Y02072
*             IF NECESSARY.                                      Y02072
*             IT IS FREED BEFORE NORMAL EXIT OF THIS MODULE.     Y02072
*           * FOR QSAM INPUT AND BSAM, A PURGE IS ISSUED FOR ALL YM3838
*             OUTSTANDING I/O.                                   YM3838
*           * IF QSAM OUTPUT IN LOCATE MODE, THE USERS BUFFERS
*             WILL BE FLUSHED BY GOING TO THE PUT ROUTINE.
*           * IF QSAM UPDATE, THE USERS BUFFERS WILL BE FLUSHED
*             BY GOING TO THE TRUNC ROUTINE AND THEN GOING TO
*             THE PUT ROUTINE.
*           * IF BDAM CREATE, THE CAPACITY RECORD (R0) WILL BE
*             UPDATED.
*           * IF BPAM, A STOW IS ISSUED FOR THE USER MEMBERNAME.
*           * SETS AN AUDIT TRAIL BIT IN THE O/C/E WORKAREA      Y02072
*             (INDICATING PURGE SUCCESSFUL), IN CASE THE USERS   Y02072
*             STAE ROUTINE SHOULD GET CONTROL DURING A FORCE     Y02072
*             CLOSE SITUATION. (THIS BIT, ALTHOUGH SET DURING    Y02072
*             NORMAL CLOSE, HAS NO MEANING).                     Y02072
*
*ENTRY POINT - NORMAL: IGG0201Z, XCTL FROM IGG0200W               7M142
*                      (COMMON CLOSE)                            Y02072
*              ABNORMAL: IGG0201Z, FROM IGG020T1
*                        (FORCE CLOSE EXECUTOR)
*
*INPUT - SEE DESCRIPTION OF REGISTERS, DCB(USERS), IOB(USERS).
*
*OUTPUT - NONE
*
*EXTERNAL ROUTINES - WRITE ROUTINE FOR BDAM CREATE (TO UPDATE R0)
*                  - PUT ROUTINE FOR QSAM (TO OUTPUT LAST BUFFER)
*
*EXITS : NORMAL - TO IGG0201Y
*
*EXITS : ERROR - ERROR ON CLOSE 'STOW'
*                  CALL PROBLEM DETERMINATION (IGG0206M) FOR B14 ABEND
*                  AND ISSUE WRITE TO PROGRAMMER
*
*              - IOB POSTED OTHER THAN NORMAL COMPLETION
*                  TO IGG0201B (QSAM ONLY)
*
*MACROS : MAPPING - IEZIOB, IHAECB, IHADECB, IEZDEB, IHADCB, IKJTCB,
*                   IECDSECT, IECDSECS, IHARB, IHACCW, IHAPSA,   Y02072
*                   IGGPARML, IGGSCW, IHASTOW, IECDPPL
*
*MACROS : ACTION - GETMAIN, WAIT, STOW, DMABCOND,
*                  FREEMAIN, IECRES, SYNCH, MODESET
*
*TABLES/WORKAREAS - WHERE TO GO TABLE (WTG)
*
*      BYTE  0-7  NAME
*      BYTE  8-10 RELATIVE DISK ADDRESS(TTR) OF FIRST RECORD
*      BYTE 11    CONCATENATION NUMBER
*      BYTE 12    ZERO
*      BYTE 13    ALIAS INDICATOR AND USER DATA FIELD DESCRIPTOR.
*                        ALIAS INDICATOR 1 BIT
*                        NUMBER OF TTRS IN USERS DATA FIELD 2 BITS
*      BYTE 14-16 TTR OF FIRST TEXT RECORD
*      BYTE 17    ZERO
*      BYTE 18-19 TTR OF NOTE LIST OR SCATTER LIST
*      BYTE 20    TRANSLATION TABLE
*      BYTE 21    ZERO OR NUMBER OF ENTRIES IN THE NOTE LIST
*      BYTE 22-23 ATTRIBUTES
*      BYTE 24-26 TOTAL CONTIGUOUS MAIN STORAGE REQUIRED FOR MODULE
*      BYTE 27-28 LENGTH OF FIRST TEXT RECORD
*      BYTE 29    LENGTH OF WTG TABLE (IN DOUBLE WORDS)
*      BYTE 30-31 PATH THROUGH LOADS OF ROUTINES
*      BYTE 32-36 IDTTR OF EXECUTOR FOR FIRST DCB
*      BYTE 37-39 WORKAREA ADDRESS FOR FIRST DCB
*      BYTE 40-   TABLE OF IDTTR'S
*      BYTE 32+N(8) IDTTR OF EXECUTOR FOR N TH DCB   (5 BYTES)
*                   WORKAREA ADDRESS FOR N TH DCB    (3 BYTES)
*                   IDTTR OF LAST ROUTINE LOAD       (5 BYTES)
*                   NOT USED                         (3 BYTES)
*
*      CLOSE WORKAREA
*
*ATTRIBUTES - REENTRANT, REUSABLE, RUNS IN USER KEY UNLESS       Y02072
*             OTHERWISE SPECIFIED, SUPERVISOR STATE              Y02072
*
*NOTES - ALL REGISTER EQUATES BEGIN WITH THE CHARACTER R
*        THE ADDRESS OF THE DEB IS GOTTEN FROM THE PROTECTED     Y02072
*        COPY OF THE DCB FOR INTEGRITY REASONS.                  Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:
*
*          RELEASE 20 DELETIONS                                       *
*1380131000-131880,305200-305500,315500                          M6099
*1380                                                            A36895
*          RELEASE 21 DELETIONS                                       *
*1394                                                            YM0829
*1394544740                                                      M1813
*1394021000,025000-027000,109014-109021,196500-197000,209200-    M0153
*1394210600,292660-292690,545040-545580,545640,563000            M0153
*1394210300,545760                                               M0077
*1394208900-209600,545080-545360,545440-545600                   M0043
*1394040000,273000,285000-287000,545000,555000-561000,563000-    S21042
*1394564000                                                      S21042
*1394193000,196000-197000,209000-211000,292720-292750,521000,    S21045
*1394524000                                                      S21045
*1394021000,025800,027000,544550-544560,544740-544750            M1044
*1394122600,197100-197200,197700,209700-210200,211500,292655,    M1301
*1394292750,513200,544660-544670,544700,544770,544830,545070-    M1301
*1394545080,545100                                               M1301
*D544840                                                        SA52446
*A                                                              SA52320
*                                                               SA63266
*
*          VS2 RELEASE 2 DELETIONS
*001500,012000,021000-028200,041920,071000,109021,118000,118500, Y02072
*120000-120520,120600,121500-122100,122400,194000,198500-199800, Y02072
*212500-214900,258000,266000-267920,343000-344000,356000,        Y02072
*414000-513300,544550-544600,544730,544738,544740,544744,544750, Y02072
*544770,544830,545094-545102,558500,71500,256000,513400,         Y02072
*543000-544500,556000,132000-133000,156000-157000,195500-197500, Y02072
*208700-211500,215000-220000,228000-238000,291000,292908-293363, Y02072
*293545,293727-315000,345000-348000,544640-544680,544710-544720, Y02072
*544880-545110,361568-361600,543000-544500,556000,276000-280000, Y02072
*548300-548600                                                   Y02072
*                                                                YM1125
*                                                                YM1457
*292831                                                          YM3077
*                                                                YM3838
*                                                                YM4640
*                                                                YM2892
*                                                                YM6249
*                                                                YM7064
*C235000,236000,244000,245000                                    YM7362
*D229000-230000,234000,239100-241000,247000-251800,251920-252000 YM7362
*                                                                YM7889
*                                                                YM8298
*          VS2 RELEASE 3   CHANGES
*C201000-202000                                                @ZA01365
*A252000-252193                                                @ZA01366
*D227200-227420,227440-227496                                  @ZA01366
*A289500-291500                                                @ZA00648
*A207501-207507                                                @ZA10582
*C207600                                                       @ZA10582
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSMI
         EJECT
*
* REGISTER USAGE
*
R0       EQU   0                        INPUT TO PROB DETERMINATION
R1       EQU   1                        PARAMETER REGISTER
RWK2     EQU   R1                       WORK REGISTER
R2       EQU   2                        USERS REG 2 FOR AM RTNS  Y02072
RDCB     EQU   R2                       DCB REGISTER             Y02072
RBASE    EQU   3                        BASE REGISTER
RCORE    EQU   4                        ADDR OF CLOSE WORK AREA
RPAR     EQU   5                        PARAMETER LIST
RWTG     EQU   6                        START OF WTG
RPARC    EQU   7                        CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                        CURRENT ENTRY IN WTG TABLE
RWK3     EQU   9                        WORK REGISTER
RWK4     EQU   10                       WORK REGISTER
RC       EQU   10                       WORK REGISTER          @ZA10582
RDEB     EQU   11                       DEB ADDRESS
R12      EQU   12                       USERS REG 12 FOR AM RTNS Y02072
RIOB     EQU   R12                      IOB REGISTER             Y02072
R13      EQU   13                       REG USED TO PASS SAVE    Y02072
*                                         AREA TO ACC METH RTNS  Y02072
R14      EQU   14                       RETURN REG FROM AM RTNS
RWK7     EQU   R14                      WORK REGISTER
R15      EQU   15                       RETURN CODE REGISTER AND Y02072
*                                         FOR BALR INSTRUCTION   Y02072
RWK8     EQU   R15                      WORK REGISTER
         SPACE 3
*
* EQUATES
*
SAVELG   EQU   72                       LENGTH OF SAVE AREA      S21045
KEYMASK  EQU   X'80'                    MASK TO DISTINGUISH SYS  Y02072
*                                         KEY FROM USER KEY      Y02072
CLOSEFIN EQU   0                        CLOSE FINISHED THIS DCB
DEBDSMOD EQU   X'80'                    DEB DISP=MOD
CHARPRES EQU   X'00'                    USED TO TEST DEB TO SEE IF
*                                         MEMBER NAME PRESENT
OUTINOUT EQU   X'07'                    DEB TYPE I/O - OUT, OUTIN
UPDATE   EQU   X'04'                    DEB TYPE I/O - UPDATE
OUTPUT   EQU   X'0F'                    DEB TYPE I/O - OUTPUT
INTRNLCD EQU   120                      INTERNAL CODE FOR PD
DCBTRUNC EQU   8                        TRUNCATE OFFSET IN PUT RTN
PPLLENTH EQU   12                       LENGTH OF PURGE PARM LST YM3838
         EJECT
         USING PSA,0                                             Y02072
         USING WTG,RWTG
         USING WTGENTRY,RWTGC
         BALR  RBASE,0
         USING AMCL,RBASE
AMCL     EQU   *
         B     BEGIN                    BRANCH AROUND MODULE ID  YM4640
         DC    C'IGG0201Z'              MODULE NAME              YM4640
         DC    C'@ZA10582'              LAST SHIP CODE         @Z30TSMI
         DC    C'09/21/76'              LAST SHIP DATE         @Z30TSMI
BEGIN    DS    0H                       END OF MODULE ID         YM4640
         L     RCORE,WTGDCBWA           GET DCB'S WORK AREA      Y02072
         USING FORCORE,RCORE                                     Y02072
         L     RDCB,DXPDCBAD            GET PROTECTED DCB ADDR   Y02072
         USING IHADCB,RDCB
         L     RDEB,DCBDEBAD            GET VALID DEB ADDR
         MODESET  KEYADDR=DXUKEY,WORKREG=13 USER KEY FOR GETMAIN Y02072
         L     RDCB,DXUDCBAD            GET DCB BEING CLOSED     Y02072
*
* THE FOLLOWING GETMAIN WILL RETURN CORE IN USER KEY,            Y02072
* NON-FETCH PROTECTED. THE AREA IS USED AS A REG SAVE AREA WHEN  Y02072
* GOING TO QSAM AND BDAM CREATE ROUTINES; AND ALSO FOR           Y02072
* BUILDING A STOW PARAMETER LIST WHEN NECESSARY.                 Y02072
*
         GETMAIN R,LV=SAVELG,SP=230                              Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT                                 Y02072
         SPACE 2
         ST    R1,SCWGETMA              SAVE AREA ADDRESS        M1301
         EJECT
*
* THE FOLLOWING WILL GET ALL NECESSARY I/O COMPLETED
*
         MODESET  KEYADDR=DXUKEY,WORKREG=13                      Y02072
         SPACE 2
         L     RIOB,DCBIOBA             GET CURR IOB ADDRESS
         LA    RIOB,0(RIOB)             CLEAR HIGH ORDER BYTE
*
* SET INDICATOR SO ENTRY TO SYNAD FROM CLOSE WILL NOT OCCUR
*
         OI    DCBCIND2,DCBCNCLO        TURN ON CLOSE FLAG.
*
* TEST FOR FORCE CLOSE IN CONTROL. IF SO, ALL THAT CAN BE DONE   YM6249
* IS A PURGE. (THIS ROUTINE WILL ONLY BE ENTERED FOR QSAM INPUT  YM6249
* DATA SETS DURING FORCE CLOSE PROCESSING).                      YM6249
*
         TM    FCACLOS1,FCACFORC        FORCE CLOSE IN CONTROL   YM6249
         BO    AMCL1025                 YES, GO ISSUE PURGE      YM6249
         USING DEBBASIC,RDEB
         TM    DEBOPATB,OUTINOUT        DCB OPENED FOR OUT OR OUTIN
         BO    AMCL1000                 YES BRANCH
         TM    DEBOPATB,UPDATE          DCB OPENED FOR UPDATE
         BZ    AMCL1025                 NO BRANCH                YM3838
         OI    DCBCIND2,DCBCNSTO        IF THIS IS A MEMBER, NO STOW
*                                         IS NECESSARY SINCE WE ARE
*                                         UPDATING IN PLACE.
         TM    DCBCIND2,DCBCNWR0        HAVE BUFFERS ALREADY BEEN
*                                         FLUSHED BECAUSE OF END OF VOL
         BO    AMCL2000                 YES, BRANCH AROUND FLUSHING
AMCL1000 EQU   *
         TM    DCBCIND2,DCBCNQSM        DCB USING QSAM TO PROCESS
         BZ    AMCL1020                 NO BRANCH                Y02072
         TM    DEBOPATB,OUTPUT          IS DCB OUTPUTTING.
         BNO   UPTRUNC                  BRANCH IF NO
*
* MUST ISSUE A TRUNC MACRO FOR LAST QSAM OUTPUT BUFFER
*
         TM    DCBCIND2,DCBCNIOE         HAS EOV BEEN ENTERED
*                                          IF SO, MUST HAVE CLOSED IN
*                                          SYNAD
         BO    AMCL1050                  TIPTOE LIGHTLY OUT
*
* FOR UPDATE QSAM - MUST ISSUE A TRUNC ON LAST BUFFER REFERENCED
* WHICH WILL DETERMINE IF A PUTX WAS ISSUED.  IF SO, LAST BUFFER WILL
* BE OUTPUT.
*
UPTRUNC  EQU   *
         TM    DCBCIND2,DCBCNIOE        CLOSE FROM SYNAD           8830
         BO    AMCL2000                 YES,GO CLEAN UP            8830
         LR    R1,RDCB                  SET UP REGISTER LINKAGE
         LA    R15,LOADPUT              INST NEEDED BY SYNCH     Y02072
*                                         ROUTINE                Y02072
         BAL   RWK3,SYNCHRTN            GO TO SYNCH RTN          Y02072
         L     RDEB,DCBDEBAD            RELOAD DEB REG AS TRUNC COULD
*                                         CAUSE AN EOV CONDITION
         TM    DEBOPATB,DEBACCS-UPDATE  ANY BITS OTHER THAN      A26014
*                                         UPDATE                 A26014
         BNZ   AMCL1005                 BRANCH IF YES            A26014
         TM    DEBOPATB,UPDATE          UPDATE MODE              A26014
         BO    PUTLOC                   BRANCH,YES               A26014
AMCL1005 EQU   *                                                 A26014
         CLI   DCBMACF2,DCBMRPUT+DCBMRLCG DCB USING LOCATE MODE@ZA01365
         BNE   TRUNC001                 NO BRANCH              @ZA01365
*
* MUST ISSUE A WAIT ON ALL OUTSTANDING IOBS BEFORE ISSUING PUT @ZA10582
*
PUTLOC   EQU   *
         L     RC,DCBIOBA        GET PREVIOUS IOB
LOOP     TM    4(RC),X'C0'       POSTED OR WAITING             @ZA10582
         BM    NEXT              YES, BRANCH                   @ZA10582
         WAIT  ECB=4(RC)         WAIT FOR IOB TO COMPLETE      @ZA10582
NEXT     CLC   DCBIOBA+1(3),1(RC) ALL IOBS BEEN WAITED ON      @ZA10582
         BE    POSTED1           YES, BRANCH                   @ZA10582
         L     RC,0(RC)          GET NXT IOB                   @ZA10582
         B     LOOP              GO CHECK NEXT IOB             @ZA10582
*
* MUST ISSUE A PUT IF IN LOCATE MODE TO FLUSH BUFFER AFTER TRUNC
*
POSTED1  LR    R1,RDCB                  SET UP PARAMETER       @ZA10582
*                                         REGISTERS              A26014
         L     R15,DCBPUT               PUT ROUTINE ADDR         Y02072
         BAL   RWK3,SYNCHRTN            GO TO SYNCH RTN          Y02072
         L     RDEB,DCBDEBAD            RELOAD DEB REG AS PUT COULD
*                                         CAUSE AN EOV CONDITION
***********************************************************************
*
*      MUST NOW WAIT ON QSAM I/O BEFORE POSSIBLY ATTEMPTING A STOW.
***********************************************************************
TRUNC001 EQU   *
         L     RIOB,DCBIOBA             GET ADDR OF IOB PAST THE LAST
*                                         ONE SCHEDULED
         LA    RWK7,0(RIOB)             SAVE ADDR FOR COMPARE    YM7362
*
         USING IOBQSAMN,RIOB                                     YM7362
TRUNC002 EQU   *                                                  18147
         L     RIOB,IOBNIOBA            GET NEXT IOB             YM7362
AMCL1010 EQU   *                                                 YM7362
         TM    IOBNECB,ECBNORM          IOB COMPLETE NORMALLY    YM7362
         BO    TRUNC003                 YES, SEE IF LAST          18147
         BM    AMCL2020                 NO, GO TO ERROR HANDING  YM7362
*                                         ROUTINE                YM7362
         WAIT  ECB=IOBNECB              WAIT FOR COMPLETION      Y02072
         B     AMCL1010                 GO TEST COMPLETION CODE  YM7362
TRUNC003 EQU   *                                                  18147
         LA    RIOB,0(RIOB)             CLEAR HI BYTE            YM7362
         CR    RWK7,RIOB                HAVE ALL IOBS BEEN CHKD  YM7362
         BNE   TRUNC002                 NO, GO CHECK NEXT IOB     18147
         B     AMCL1050                 YES, CONTINUE            Y02072
         SPACE
*      UNDER NORMAL CIRCUMSTANCES THE ABOVE-ISSUED PUT LOCATE ADVANCED
* DCBIOBA TO THE NEXT IOB OR ICB.  THE NEXT MODULE OF CLOSE WILL ISSUE
* EOV, WHICH EXPECTS DCBIOBA TO POINT TO THE LAST IOB/ICB TO BE
* RESTARTED.  THE ABNORMAL CIRCUMSTANCE THAT WE MUST SUPPORT IS
* COBOL PROGRAMS, WHICH ISSUE PUT LOCATE AND THEN CHANGE THEIR MIND
* JUST BEFORE CLOSE AND RETRACT THE LAST PUT BY REDUCING QSAM'S
* INTERNAL FIELD, DCBRECAD.  THIS MAY PRODUCE A ZERO-LENGTH RECORD
* AND THE PUT ROUTINE DOES NOT ADVANCE THE IOB POINTER IN THAT CASE.
*      FOR DA DEVICES USING CHAINED SCHEDULING, THE ICBS AFTER
* THE ICB IN ERROR MAY HAVE BEEN POSTED WITH X'40' TO INDICATE
* OUT-OF-EXTENT, BUT IF THERE HAS BEEN A REAL I/O ERROR, THE LATER
* ICBS HAVE NOT AND WILL NEVER BE POSTED.  WE CANNOT WAIT ON THEM.
*      FOR DA DEVICES NOT USING CHAINED SCHEDULING, THE IOBS AFTER
* THE IOB IN ERROR HAVE BEEN OR SOON WILL BE POSTED WITH X'48' TO
* INDICATE THAT THEY HAVE BEEN PURGED.
*      IGG0201B WILL DO ANY ADJUSTMENT IN DCBIOBA NECESSARY FOR EOV.
         DROP  RIOB                                              YM7362
AMCL2020 EQU   *
         SPACE
         MODESET  EXTKEY=DATAMGT                                 Y02072
         SPACE
         MVC   WTGIDTTR,ERRMOD          SET UP FOR NEXT EXEC   @Z30TSMI
         B     AMCL2035                 GO CALL IT IN, DO NOT    Y02072
*                                         FREEMAIN WORKAREA      Y02072
AMCL1020 EQU   *
*      AT THIS POINT WE KNOW IT IS BSAM OUTPUT OR OUTIN, INCLUDING
* POSSIBLY BDAM CREATE.
*      IF CLOSE WAS ISSUED IN THE SYNAD ROUTINE, WE WANT TO SET
* DCBFDAD TO THE MBBCCHHR OF THE BLOCK THAT HAD THE ERROR AND TO
* REESTABLISH A VALID TRACK BALANCE.  THIS IS SO THAT COMMON CLOSE
* WILL WRITE THE 'LAST BLOCK TTR' AND TRACK BALANCE CORRECTLY IN THE
* DSCB AND WRITE THE FILE MARK CORRECTLY.  THE USER MAY WANT TO USE
* DISP=MOD TO CONTINUE WRITING THE DATA SET WHETHER IT IS SEQUENTIAL
* OR PARTITIONED.
         TM    DCBCIND2,DCBCNIOE        WAS CLOSE FROM SYNAD   @ZA00648
         BNO   AMCL1022                 BR IF NOT FROM SYNAD   @ZA00648
*            WE NOW KNOW THE SYNAD ROUTINE WAS ENTERED AND NOT EXITED,
*        BUT THE USER MAY HAVE TURNED OFF DCBIFLGS TO MAKE THE DCB
*        USABLE TO IOS AND DONE SOME MESSING AROUND, PERHAPS TO SET
*        DCBFDAD AND DCBTRKBL LIKE WE ARE ABOUT TO DO.
         TM    DCBIFLGS,DCBIFEC         DCB STILL SHOW ERROR   @ZA00648
         BNO   AMCL1022                 BR IF ERROR CLEANED UP @ZA00648
*            IF THE LAST OPERATION WAS NOT A WRITE, CLOSE WILL NOT
*        WRITE A FILE MARK.
         TM    DCBOFLGS,DCBOFLWR  SEE IF LAST OPER WAS WRITE   @ZA00648
         BO    AMCL2020           GO TO 201B IF WRITE WAS LAST @ZA00648
AMCL1022 EQU   *                  DCBFDAD WILL NOT BE RESET    @ZA00648
*
* IF THE REQUEST IS FOR CREATE BDAM AND THE RECFM IS FIXED FORMAT,
* THIS ROUTINE DETERMINES IF IT IS NECESSARY TO UPDATE THE CAPACITY
* RECORD ON THE LAST TRACK OF THE DATA SET. IF REQUIRED, THE ROUTINE
* DOES A BALR TO THE WRITE MODULE AND UPDATES THE CAPACITY RECORD (R0)
*
         CLI   DCBMACF2,DCBMRWRT+DCBMRLDM  IS THIS CREATE BDAM  SA52320
         BNE   AMCL1025                 NO, MUST BE BSAM         Y02072
         TM    DCBRECFM,DCBRECV         IF RECFM IS NOT FIXED    A27066
         BO    AMCL1030                 BR TO CONTINUE           Y02072
         TM    DCBCIND2,DCBCNWR0        WAS R0 WRITTEN           A27066
         BO    AMCL1030                 YES-BR TO CONTINUE       Y02072
         L     RWK8,DCBIOBA             GET IOB ADDRESS          A27066
         USING IOBBSAMN,RWK8
         L     R1,IOBECBPT              GET DECB ADR FOR WRITE   A27066
*                                         MODULE                 A27066
         LTR   R1,R1                    WAS ANY I/O ISSUED       A36895
         BZ    AMCL1030                 NO, DON'T WRITE CAP REC  Y02072
*
* SINCE THE USER'S DECB MAY HAVE BEEN FREED BY NOW, A NEW DECB WILL
* BE BUILT, 3 FULLWORDS LONG, STARTING IN THE 2ND WORD OF THE CHANNEL
* PGM, THAT PART OF THE CHAN PGM NOT NOW BEING USED
*
         USING DECB,R1
         LA    R1,IOBCPDEC+L'CCWADDRA   START OF NEW DECB        A48228
         ST    R1,IOBECBPT              UPDATE IOB'S ECB PTR     A48228
         ST    RDCB,DECDCBAD            SET DCB PTR IN NEW DECB  A48228
         XC    DECSDECB,DECSDECB        ZERO OUT ECB             A48228
         DROP  R1,RWK8
         L     R15,DCBWRITE             GET WRITE RTN ADDR       Y02072
         BAL   RWK3,SYNCHRTN            GO SYNCH                 Y02072
         L     RWK2,DCBIOBA             GET IOB ADDR             YM0829
         USING IOBBSAMN,RWK2
         L     RWK2,IOBECBPT            LOAD ECB ADDR            YM8298
         DROP  RWK2                                              YM8298
         WAIT  ECB=(RWK2)               WAIT ON R0 I/O           YM8298
         B     AMCL1030                 BYPASS PURGE             YM3838
*
* MUST ISSUE PURGE SVC FOR BSAM AND QSAM INPUT DATA SETS.        YM3838
* OPTIONS ARE:                                                   YM3838
*   X'80' (ON) - PURGE BY DEB                                    YM3838
*   X'20' (ON) - HALT I/O                                        YM3838
*   X'10' (OFF) - ALL ASSOCIATED REQUESTS                        YM3838
*
AMCL1025 EQU   *                                                 YM3838
         L     RWK4,SCWGETMA            USER KEY WORKAREA FOR    YM3838
*                                         PARAMETER LIST         YM3838
         USING PPLDSID,RWK4                                      YM3838
         XC    PPLDSID(PPLLENTH),PPLDSID  CLEAR LIST             YM3838
         ST    RDEB,PPLDSID             PURGE BY DEB             YM3838
         MVI   PPLOPT1,PPLDS+PPLHIO     OPTIONS TO LIST          YM3838
         LR    R1,RWK4                  INPUT TO PURGE           YM3838
         PURGE (1)                      ISSUE PURGE              YM3838
         LA    R1,PPLCC                 ECB TO BE POSTED         YM3838
         WAIT  ECB=(1)                  WAIT FOR COMPLETION      YM3838
         DROP  RWK4                                              YM3838
         MODESET  EXTKEY=DATAMGT        KEY OF AUDIT TRAIL       YM3838
         SPACE
         OI    FCACLOS2,FCACPURG        INDIC PURGE COMPLETED    YM3838
         MODESET  KEYADDR=DXUKEY,WORKREG=10  RESUME USER KEY     YM3838
         SPACE
*
* TEST FOR FORCE CLOSE. IF SO, XCTL TO IGG0201Y FOR CLEAN UP.    YM6249
*
         TM    FCACLOS1,FCACFORC        FORCE CLOSE IN CONTROL   YM6249
         BO    AMCL2030                 YES, GO FREE WORKAREA    YM6249
AMCL1030 EQU   *                                                 Y02072
         TM    DEBOPATB,OUTINOUT        OPENED FOR OUT OR OUT/IN
         BNO   AMCL2000                 NO BRANCH
AMCL1050 EQU   *
         TM    DCBOPTCD,DCBOPTT         TEST FOR TOTALING          UT18
         BO    AMCL2000                 BR IF TOTALING SPECIFIED   UT18
         TM    DCBCIND2,DCBCNSTO        HAS STOW BEEN ACCOMPLISHED YET
         BO    AMCL2000                 YES BRANCH
         EJECT
*
* PREPARE TO ISSUE A STOW ON USER MEMBERNAME
*
         SR    RWK2,RWK2
         SR    RWK4,RWK4
         IC    RWK4,DEBNMEXT            GET NUMBER OF EXTENTS
         IC    RWK2,DEBEXSCL            GET SCALING FACTOR  FOR EXTENT
         SLL   RWK4,0(RWK2)             EXTENT SIZE X EXTENT NUMBER
         LA    RWK4,DEBBASND(RWK4)      EXTENTS+DEB+BASE
         USING DEBACSMD,RWK4
         TM    DCBDSORG,DCBDSGPO        DCB FOR  PO DATA SET
         BO    AMCL1060                 YES BRANCH
         LA    RWK4,DEBDSNM             ADD SAM LEN TO DEB SIZE
AMCL1060 EQU   *
         CLI   DEBAMLNG,L'DEBDSNM       ACCESS METHOD DEPENDENT   M3945
*                                         SECTION PRESENT
         BL    AMCL2000                 BRANCH IF NO             M3945
         DROP  RDEB
         USING DEBDSNM,RWK4
         CLI   DEBDSNM,CHARPRES         IS MEMBERNAME IN DEB     A25961
         BE    AMCL2000                 BR IF NO                 A25961
*
* STOW PARM LIST MUST BE IN SAME KEY AS STOW IS ISSUED IN TO     Y02072
* KEEP STOW FROM PROGRAM CHECKING.                               Y02072
*
         L     RWK3,SCWGETMA            USE USER KEY WORKAREA    Y02072
*                                         FOR PARM LIST          Y02072
         USING STOW,RWK3                                         Y02072
         MVC   STONAME,DEBDSNM          STORE MEMBERNAME IN PARM Y02072
*                                         LIST                   Y02072
         MVI   STOCTTRN,0               INDICATE NOT AN ALIAS    Y02072
*                                         AND LEN OF USER FIELD  Y02072
*                                         EQUALS ZERO            Y02072
         DROP  RWK4
         LA    RDCB,0(RDCB)             ZERO HI BYTE             YM1457
         USING DEBBASIC,RDEB
         TM    DEBOFLGS,DEBDSMOD        MEMBER BEING ADDED       Y02072
*                                         (DISP=MOD OR NEW)      Y02072
         BO    AMCL1080                 YES BRANCH               Y02072
*
* MEMBER IS BEING REPLACED IN PARTITIONED DATA SET (DISP=OLD)
*
         STOW  (RDCB),(RWK3),R          REPLACE MEMBER IN PDS    Y02072
*
         LR    RWK4,R15                 SAVE STOW RETURN CODE    Y02072
         BAL   RWK3,CHECKDEB            GO VERIFY DEB ADDR SINCE Y02072
*                                         AN EOV COULD OCCUR IN  Y02072
*                                         STOW                   Y02072
         CH    RWK4,ERRCODE             ROOM AVAIL FOR FM
         BH    ABEND                    YES BRANCH - ABEND B14
         B     AMCL2000                 NO, CONTINUE             Y02072
AMCL1080 EQU   *                                                 Y02072
*
* MEMBERNAME IS BEING ADDED TO DIRECTORY OF DATA SET
*
         STOW  (RDCB),(RWK3),A          ADD MEMBER NAME TO DIR.  Y02072
*
         LR    RWK4,R15                 SAVE STOW RETURN CODE    Y02072
         BAL   RWK3,CHECKDEB            GO VERIFY DEB ADDR SINCE Y02072
*                                         AN EOV COULD OCCUR IN  Y02072
*                                         STOW                   Y02072
         DROP  RWK3
         LTR   RWK4,RWK4                WERE THERE ANY ERRORS
         BNZ   ABEND                    YES, BRANCH              Y02072
         B     AMCL2000                 CONTINUE                 Y02072
ABEND    EQU   *
*
* REG 10 WILL HAVE A RETURN CODE FROM STOW. THE RETURN CODE
* WILL RESULT IN THE FOLLOWING INTERNAL CODES FOR A
* B14 ABEND:  4 - 121
*             8 - 122
*             C - 123
*            10 - 124
*            14 - 125                                           SA63266
*            18 - 126                                           SA63266
* THE FOLLOWING ROUTINE WILL INITIALIZE THE INTERNAL CODE TO 120,
* DIVIDE THE RETURN CODE IN REG 10 BY 4, AND ADD THAT TO 120 TO
* CALCULATE THE INTERNAL CODE NEEDED
*
         LA    R0,INTRNLCD              PUT CODE IN REG          S21042
         SRL   RWK4,2                   DIVIDE RETURN CODE BY 4  S21042
         AR    R0,RWK4                  CALCULATE INTERNAL CODE  S21042
         L     RWK4,PSATOLD             GET CURRENT TCB          Y02072
         USING TCB,RWK4
         TM    TCBFLGS1,TCBFA           IS THIS ENTRY TO CLOSE FROM
*                                         ABEND
         BO    AMCL2000                 IF SO, BRANCH--WE MUST   Y02072
*                                         NOT ABEND AGAIN        Y02072
         MODESET  EXTKEY=DATAMGT                                 Y02072
         SPACE 2
         DMABCOND (0),PDLOAD,RETURN=NONE,RES=NO,REGSAVE=YES      S21042
         STC   R0,SCWSAVCD              SAVE CD ACROSS FREEMAIN  Y02072
         SPACE 2
         MODESET KEYADDR=DXUKEY,WORKREG=10  USER KEY FOR FREEMAN Y02072
         SPACE 2
         B     AMCL2030                 GO FREEMAIN SAVE AREA    Y02072
         EJECT
AMCL2000 EQU   *
*
* TEST FOR QSAM ERRORS AND XCTL TO 201B IF NECESSARY.
*
         TM    DCBCIND2,DCBCNQSM        QSAM                    SA52320
         BZ    AMCL2030                 NO - DO NOT CHECK       SA52320
         TM    DCBIFLGS,DCBIFEC         I/O ERROR                  8383
         BZ    AMCL2030                 NO,BRANCH                  8383
         TM    DCBOFLGS,DCBOFLWR        LAST OP A WRITE            8383
         BZ    AMCL2030                 NO,BRANCH                  8383
         OI    DCBCIND2,DCBCNIOE        TURN ERROR BIT ON          8383
         B     AMCL2020                 SETUP FOR 201B             8383
AMCL2030 EQU   *
         L     R1,SCWGETMA              OBTAIN SAVE AREA ADDRESS M1301
         FREEMAIN R,LV=SAVELG,A=(1),SP=230                       Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT                                 Y02072
         EJECT
*
* FIND THE NEXT MODULE TO PROCESS.
*
         SR    R0,R0                    PREPARE TO LOAD REGISTER Y02072
         IC    R0,SCWSAVCD              ASSUME PROB DET NEXT LD  Y02072
*                                         (NO HARM DONE IF NOT)  Y02072
         CLC   WTGIDNX,PDLOAD           ARE WE GOING TO PROB DET Y02072
         BE    AMCL5019                 YES, BR AROUND RELOOP    Y02072
         MVC   WTGIDTTR,CLL1Y           SET UP ID FOR NEXT     @Z30TSMI
*                                         EXECUTOR               Y02072
AMCL2035 EQU   *                                                 Y02072
         TM    FCACLOS1,FCACFORC        IS FORCE CLOSE IN CNTRL  Y02072
         BO    AMCL5019                 YES, BYPASS RELOOP CODE  Y02072
RELOOP1  EQU   *                                                 S21045
         LA    RWTGC,L'WTGENTRY(,RWTGC)  STEP TO NEXT ENTRY
         LA    RPARC,L'PARDCBAD(,RPARC)  STEP TO NEXT ENTRY
         CLC   WTGIDNX,AMIDCNST         THIS RTN NEEDED AGAIN
         BCR   8,RBASE                  RECURSE THRU MODULE
         CLC   WTGIDNX,CLLDB            IS THIS THE END OF THE TABLE
         BL    RELOOP1                  NO BRANCH                S21045
         CLC   WTGIDNX,CLLDG            IS THIS THE END OF THE TABLE
         BH    RELOOP1                  NO BRANCH                S21045
         LR    RPARC,RPAR               INITIALIZATION
         DROP  RWTGC
         LA    RWTGC,WTGENTRY
         USING WTGENTRY,RWTGC
AMCL5011 EQU   *
         CLI   WTGIDTTR,CLOSEFIN        IS THIS ENTRY = ZERO
         BNE   AMCL5019                 NO BRANCH
         LA    RWTGC,L'WTGENTRY(,RWTGC)  STEP TO NEXT ENTRY
         LA    RPARC,L'PARDCBAD(,RPARC)  STEP TO NEXT ENTRY
         B     AMCL5011
AMCL5019 EQU   *
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSMI*
               MODID=WTGENTRY           GO TO NEXT ROUTINE     @Z30TSMI
         EJECT
*
* THIS ROUTINE TESTS THE CALLERS KEY TO DETERMINE WHETHER TO     Y02072
* BALR (IF KEY IS LT 8) OR SYNCH (IF KEY IS GT 7) TO THE ACCESS  Y02072
* METHOD ROUTINES. CLOSES REGS ARE SAVED IN THE SVRB SAVEAREA    Y02072
* AND THE USERS REGISTERS ARE LOADED AND PASSED TO THE ACCESS    Y02072
* METHOD ROUTINES (FOR THE CASE AN EOV CONDITION ARISES AND A    Y02072
* USER EOV EXIT IS TAKEN). THE SAVEAREA GOTTEN FROM SP230 IS     Y02072
* PASSED IN R13 TO THE ACCESS METHOD ROUTINES. THE SYNCH IS      Y02072
* TAKEN TO THE INSTRUCTIONS SET UP AT THE END OF THIS ROUTINE    Y02072
* (WHEN ISSUING A TRUNC) WHICH WILL CAUSE A BALR TO THE ACCESS   Y02072
* METHOD ROUTINES. WHEN ISSUING A PUT OR WRITE, THE SYNCH GOES   Y02072
* DIRECTLY TO THE PUT OR WRITE MODULE.                           Y02072
*
SYNCHRTN EQU   *                                                 M0153
         L     RWK7,PSATOLD             GET CURRENT TCB ADDR     Y02072
         USING TCB,RWK7                                          Y02072
         L     RWK7,TCBRBP              GET RB ADDR              Y02072
         DROP  RWK7                                              Y02072
         USING RBSECT,RWK7              R14 IS BASE FOR SVRB     M0153
         SPACE 2
         MODESET  EXTKEY=SUPR                                    Y02072
         SPACE 2
         STM   RDCB,RIOB,RBEXSAVE       SAVE CLOSE REG IN SVRB   M1301
         MODESET  KEYADDR=DXUKEY,WORKREG=13                      Y02072
         SPACE
         L     R13,SCWGETMA             GET REG SAVE AREA ADDR   Y02072
         TM    DXUKEY,KEYMASK           IS CALLERS KEY 8 OR      Y02072
*                                         OR HIGHER              Y02072
         BO    PRB                      SYNCH IF GT OR EQ TO 8   Y02072
         LM    R2,R12,RBGRS2            RESTORE USERS REG 2-12   Y02072
         DROP  RWK7                                              Y02072
         BALR  R14,R15                  BR TO DO LAST I/O        M0153
         USING *,R14                                             Y02072
         B     RELOAD                   GO LOAD REGISTERS AND    M0153
*                                         RETURN                 M0153
         DROP  R14                                               Y02072
PRB      EQU   *                                                 M0153
         USING RBSECT,RWK7                                       Y02072
         LM    R2,R12,RBGRS2            GET USERS REGISTERS      Y02072
         DROP  RWK7                                              Y02072
*
* GO TO THE ACCESS METHOD ROUTINES IN PROBLEM PROGRAM STATE TO
* DO THE LAST I/O
*
         SYNCH  (15)                                             M0153
RELOAD   EQU   *
         L     RWK7,PSATOLD             GET CURRENT TCB          Y02072
         USING TCB,RWK7                                          Y02072
         L     RWK7,TCBRBP              GET RB ADDRESS           Y02072
         DROP  RWK7                                              702072
         USING RBSECT,RWK7                                       Y02072
         LM    RDCB,RIOB,RBEXSAVE       RESTORE CLOSES REGS      Y02072
         DROP  RWK7                                              Y02072
*
* MUST VERIFY DEB ADDRESS AS PUT OR TRUNC COULD CAUSE AN EOV.    Y02072
*
CHECKDEB EQU   *                                                 Y02072
         DEBCHK  (RDCB)                 VERIFY DEB ADDR          Y02072
         LR    RDEB,R1                  VALID DEB ADDR           Y02072
         L     RDCB,DXPDCBAD            GET PROTECTED DCB ADDR   Y02072
         MODESET  EXTKEY=DATAMGT        KEY OF PROTECTED DCB     Y02072
         SPACE
         ST    RDEB,DCBDEBAD            UPDATE PROTECTED DCB     Y02072
         MODESET  KEYADDR=DXUKEY,WORKREG=2  RESUME USER KEY      Y02072
         SPACE
         L     RDCB,DXUDCBAD            RELOAD WITH USERS DCB    Y02072
         BR    RWK3                     RETURN TO MAIN ROUTINE   M0153
*
* INSTRUCTIONS USED TO GO TO THE ACCESS METHOD ROUTINES          Y02072
*
         DROP  RDCB                                              Y02072
         USING IHADCB,R1                                         Y02072
LOADPUT  DS    0H                       FOR QSAM                 Y02072
         L     R15,DCBPUT               INST TO LOAD PUT RTN ADD Y02072
BRTRUNC  B     DCBTRUNC(R15)            BR TO TRUNC ROUTINE      Y02072
         EJECT
*
* CONSTANTS
*
AMIDCNST DC    C'1Z'                    ID OF THIS MODULE        S19015
CLLDB    DC    C'0B'                    FOR TEST END OF WTG TAB  S19015
CLLDG    DC    C'0G'                    FOR TEST END OF WTG TAB  S19015
CLL1Y    DC    C'1Y',VL3(IGG0201Y)      MODULE IGG0201Y        @Z30TSMI
ERRMOD   DC    C'1B',VL3(IGG0201B)      MODULE IGG0201B        @Z30TSMI
PDLOAD   DC    C'6M',VL3(IGG0206M)      MODULE IGG0206M        @Z30TSMI
ERRCODE  DC    H'8'                     IF STOW RETURN CODE IS
*                                         HIGHER, GO TO PROBLEM DET
         SPACE 3
PATCH    DC    25H'0'                   PATCH AREA               YM4640
END      EQU   *                                                 Y02072
*
         TITLE 'IGG0201Z - CLOSE PARAMETER LIST DSECT'
         IGGPARML
         TITLE 'IGG0201Z - DSECT FOR STOW PARAMETER LIST'
         IHASTOW                                                 Y02072
         TITLE 'IGG0201Z - WHERE TO GO TABLE DSECT'
         IECDSECS  WTG,PREFX,EXPAND=YES
         ORG   WTGIDTTR
WTGIDNX  DS    CL2                      ID NEXT MODULE
         ORG   WTGIDTTR+4
WTGDCBWA DS    A                        ADDR OF WORK AREA FOR DCB
         TITLE 'IGG0201Z - CLOSE WORK AREA DSECT'
         IECDSECS  (MAIN,(IOB,NO)),EXPAND=YES                    Y02072
FORCORE  DSECT                                                   Y02072
         IHAFCAUD  ORG=YES                                       Y02072
         EJECT
         IGGSCW
         TITLE 'IGG0201Z - IOB DSECT'
         IEZIOB
         ORG   IOBRSV35
IOBCPDEC DS    F                        BEGINNING OF THE DECB BUILT
*                                         DURING CLOSE IN THE CP
         TITLE 'IGG0201Z - ECB DSECT'
         IHAECB
         TITLE 'IGG0201Z - DECB DSECT'
         IHADECB
         TITLE 'IGG0201Z - DEB DSECT'
         IEZDEB
         TITLE 'IGG0201Z - DCB DSECT'
         IHADCB  DSORG=PS
         TITLE 'IGG0201Z - TCB DSECT'
         IKJTCB
         TITLE 'IGG0201Z - REQUEST BLOCK DSECT'
         IHARB
         TITLE 'IGG0201Z - PSA DSECT'
         IHAPSA                                                  Y02072
         TITLE 'IGG0201Z - CCW DSECT'
         IHACCW  DSECT=YES
         TITLE 'IGG0201Z - CVT DSECT'
CVT      DSECT
         CVT
         TITLE 'IGG0201Z - PURGE PARAMETER LIST DSECT'
         IECDPPL  DSECT=YES                                      YM3838
         END
