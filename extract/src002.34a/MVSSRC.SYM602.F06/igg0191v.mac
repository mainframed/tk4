191V   TITLE 'IGG0191V -SAM && EXCP PRINTER UCS BUFFER LOADING'
IGG0191V CSECT
*
*MODULE NAME - IGG0191V                                          Y02072
*
*DESCRIPTIVE NAME - SAM & EXCP PRINT UCS BUFFER LOADING
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          RELEASE 20 DELETIONS
*0216109000                                                      A38811
*0216609000-611000,615000,620000-621000,624000,627000,629000,    A33665
*0216632000,635000                                               A33665
*0216008000-015000,017000,028000-030000,035000,041000-042000,    S20202
*0216045000-048000,123000-133000,177000,189000,231000,235000,    S20202
*0216272000,284000-287000,316000-324000,443000-660000,693000,    S20202
*0216756000-759000,762000-763000,764000-772000,780000,786000     S20202
*0216109000                                                      M5148
*0216317800,318200-318500                                        M5143
*          RELEASE 21 DELETIONS
*0934125600-125800,319800,321600                                 M1803
*0934041000,136000,257000,439000,665000-667000,670000-680000,    S21042
*0934734000-738000,780000-786000,802000-803000                   S21042
*0934                                                            M0092
*A                                                               XM5898
*        VS2 RELEASE 02 DELETIONS
*001600-003400,007400,054000,106200-106600,162500,163500-163800, Y02072
*250000-253000,271800-271920,272120-272984,317700-318000,318100, Y02072
*343000,370060-370840,386050-386600,753000-763600,781000-791000, Y02072
*802000,405000,437000,441000,344000,750500-750900,752000         Y02072
*192390-192470,192500-192560                                     SM5451
*                                                                YM4697
*
*        VS2 RELEASE 3.0 CHANGES
*                                                              @Z30TSCF
*A733500                                                       @ZA04647
*        VS2 RELEASE 4.0 CHANGES
*A190200-191840,270200                                         @ZA02191
*C239000                             ABEND 0C4 IF UCS NOT RENT @ZA02191
*D192180-192250,203000-208000,219120,219400-219500             @ZA02191
*
*STATUS - SEE BEGINNING OF OBJECT CODE
*
*FUNCTION/OPERATION - EXECUTE UCS IMAGE LOAD INTO STORAGE AND THEN INTO
*                     UCS BUFFER
*                   - SET AUDIT TRAIL BIT FOR FORCE CLOSE        YM4697
*                     INDICATING LOAD ISSUED FOR UCS IMAGE       YM4697
*                   - RESET AUDIT TRAIL BITS TO INDICATE UCS     YM4697
*                     IMAGE DELETED AND IMAGELIB CLOSED          YM4697
*                   - UPDATE UCB TO REFLECT STATUS OF IMAGE IN BUFFER
*                     IF VERIFY NOT SPECIFIED
*                   - DETERMINE NEXT OPEN EXECUTOR TO XCTL TO AND IF
*                     NECESSARY ISSUE MESSAGE IEC129 TO OPERATOR
*                   - CLOSE SYS1.IMAGELIB IF PRINTER IS 1403
*
*ENTRY POINT - ENTERED FROM IGG0191T OR IGG0191U BY USE OF IECRES MACRO
*
*INPUT - DCB ADDRESS
*      - OPEN WORK AREA ADDRESS
*      - WHERE-TO-GO TABLE
*      - PARAMETER LIST
*      - CURRENT ENTRY IN W-T-G TABLE
*      - CURRENT PARAMETER(DCB)
*      - THE ADDRESS OF UCS IMAGE RETRIEVED
*      - THE ADDRESS OF SYS1.IMAGELIB DCB IN OPEN WORKAREA(DXCCW8)
*
*OUTPUT - UCS BUFFER LOAD
*       - UPDATED UCB TO REFLECT THE STATUS OF UCS BUFFER
*       - ISSUE REQUEST FOR OPERATOR TO SPECIFY FCB IMAGE
*                     'IEC129  XXX,SPECIFY ,FCB PARAMETERS'  MESSAGE
*       - ISSUE MESSAGE 'IEC126I XXX, UNCORRECTABLE ERROR        Y02072
*                       LOADING UCS BUFFER'                      Y02072
*
*EXTERNAL REFERENCES - MESSAGE CSECT                             Y02072
*
*EXIT - NORMAL - XCTL TO ONE OF THE FOLLOWING MODULES:           Y02072
*               *IGG0197U IF UCS IMAGE TO BE VERIFIED            Y02072
*               *IGG0197E IF AN FCB IMAGE IS TO BE LOADED
*               *IGG01911 IF THE DCB IS EXCP.                    Y02072
*               *IGG0191I IF BUFFER CNTRL BLOCK IS SPECIFIED.
*               *IGG0191G IF NORMAL SCHEDULING IS SPECIFIED.
*               *IGG0191Q IF CHAINED SCHEDULING IS SPECIFIED.
*
*EXIT - ERROR - XCTL TO PROBLEM DETERMINATION FOR WTP AND B13 ABEND
*               FOR ONE OF THE FOLLOWING:
*              *MODULE IGG0191U SET SWITCH TO ABEND
*              *PERMANENT I/O ERROR OCCURED DURING UCS BUFFER LOAD.
*
*TABLES/WORK AREAS - WHERE-TO-GO TABLE (SEE WTGTABLE IN THIS LISTING)
*                  - OPEN WORK AREA (SEE IECDSECT IN THIS LISTING)
*
*MACROS-ACTION:MODESET, LOAD, EXCP, WAIT,DELETE, XCTL, DMABCOND, Y02072
*              XCTLTABL                                          Y02072
*
*MACROS-MAPPING:IEFUCBOB, IGGMSG, IECDSECS(WTG,MAIN),DCBD,IGGSCW Y02072
*               IHAFCAUD                                         YM4697
*
*ATTRIBUTES - REENTRANT, REFRESHABLE, RUNS IN DATA MANAGEMENT KEYY02072
*             UNLESS OTHERWISE SPECIFIED, SUPER STATE            Y02072
*
*NOTES - EXCP/WAIT SVCS ARE USED TO LOAD UCS BUFFER AND WRITE IMAGE
*        ON THE PRINTER FOR VERIFICATION.
*      - DELETE SVC IS USED TO DELETE THE RETRIEVED IMAGE.
*      - WTOR SVC IS USED TO REQUEST OPERATOR REPLY
*      - IOB/ECB IN OPEN WORK AREA IS USED FOR EXCP
*      - THE REPLY IS ORED TO ALLOW LOWER CASE ALPHA
*      - SVC (105) IS USED TO DELETE DCB AND DEB OF SYS1.IMAGELIB
*      - BEFORE ISSUING A WTO/WTOR, THE MESSAGE TEXT MUST FIRST  Y02072
*        BE EXTRACTED FROM A MESSAGE CSECT.                      Y02072
*
***********************************************************************
*
         EJECT
*
***********************************************************************
*
*   REGISTER CONVENTIONS USED THROUGH OUT ALL OPEN PASSES
*
***********************************************************************
*
*
RE       EQU   0                        WORK/PARAMETER REGISTER
RF       EQU   1                        WORK/PARAMETER REGISTER
RWK2     EQU   RF                       WORK REGISTER            Y02072
RDCB     EQU   2                        ADDR OF USER'S DCB
RBASE    EQU   3                        BASE REGISTER
RCORE    EQU   4                        ADDR OF OPEN WORK AREA
RPAR     EQU   5                        PARAMETER LIST
RWTG     EQU   6                        START OF WTG
RPARC    EQU   7                        CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                        CURRENT ENTRY IN WTG TABLE
RWK3     EQU   9                        WORK REGISTER            Y02072
RUCB     EQU   10                       UCB ADDR/WORK REGISTER
RDEB     EQU   11                       DEB ADDR
RWK6     EQU   RDEB                     WORK REGISTER            Y02072
RB       EQU   12                       WORK REGISTER/BREG FOR DSECT
RC       EQU   13                       WORK REGISTER
RD       EQU   14                       WORK/PARAMETER REGISTER
RJ       EQU   15                       WORK/PARAMETER REGISTER
RWK7     EQU   RJ                       WORK REGISTER            Y02072
*
***********************************************************************
*
*
*   EQUATE WTG OFFSET
*
WGOFF    EQU   8                        OFFSET OF CURRENT WTG ENTRIES
PLOFF    EQU   4                        OFFSET OF CURRENT DCB ENTRIES
WAOFF    EQU   32                       OFFSET OF 1ST ENTRY IN WTG TBL
*
*   EQUATE ABEND CODE AND RETURN CODE
*
PCIMASK  EQU   X'20'                    OPTCD=PCI SPECIFIED
BFCNTRL  EQU   X'01'                    BUFFER CNTRL BLOCK SPECIFIED
EXCPMASK EQU   X'80'                    LOAD EXCP                A38811
UNRLMASK EQU   X'42'                    IOB IS UNRELATED AND C.C.
NULL     EQU   X'00'                    X'00'
CNTRLMSK EQU   X'EB'                    COMMAND CODE = CNTRL
SILIMASK EQU   X'60'                    C.C. AND SILI ON
ONEMASK  EQU   X'01'                    X'01'
UCSLNGTH EQU   X'F0'                    UCS IMAGE LENGTH
TWOMASK  EQU   X'02'                    X'02'
UNFLCCW  EQU   X'FB'                    COMMAND CODE = LOAD NO FOLD
FLDCCW   EQU   X'F3'                    COMMAND CODE = LOAD FOLD
FOLDMASK EQU   X'40'                    LOAD MODE = FOLD
VERIFY   EQU   X'10'                    VERIFY = YES
RESETMSK EQU   X'CF'                    RESET VERIFY
*
*   EQUATES FOR SYS1.IMAGELIB SUPPORT AND 3211 PRINTER SUPPORT
*
PARITYER EQU   X'01'                    BUFFER PARITY ERR        Y02072
*                                         IN UCSB                Y02072
ABEND    EQU   X'FF'                    1U ABEND SWITCH          S20202
NONZERO  EQU   X'01'                    NON-ZERO BYTE COUNT      SM5451
SILIMSK  EQU   X'20'                    SILI FLAG FOR CCW'S      SM5451
UCSLDCC  EQU   X'FB'                    UCS LOAD COMMAnD         S20202
ABENDBIT EQU   X'80'                    ABEND IN PROCESS SWITCH  S20202
UCSFLDCC EQU   X'43'                    COMMAND CODE = FOLD UCS  S20202
UCSUNFLD EQU   X'23'                    COM CODE TO UNFOLD UCS   SM5451
PRTM3211 EQU   X'09'                    MASK FOR 3211 PRINTER    S20202
IEC129RQ EQU   X'80'                    MESSAGE IECU29D REQUIRED S20202
*                                       MASK                     S20202
UCSIDOFS EQU   X'00'                    UCS OFFSET FROM START    S20202
*                                       UCB WK A                 S20202
UCSOPOFS EQU   X'04'                    UCS OPTIONS OFFSET       S20202
IMCLOSED EQU   X'FF'                    FLAG SET FOR IMAGELIB    S20202
*                                         NOT OPEN
K1       EQU   1                        CONSTANT EQUATED TO 1    S20202
K2       EQU   2                        CONSTANT EQUATED TO 2    S20202
K4       EQU   4                        CONSTANT EQUATED TO 4    S20202
K6       EQU   6                        CONSTANT EQUATED TO 6    S20202
K8       EQU   8                        CONSTANT EQUATED TO 8    S20202
K35      EQU   35                       CONSTANT EQUATED TO 35   S20202
OABD069  EQU   69                                                S21042
*  THE FOLLOWING MESSAGE NUMBERS ARE EQUAL TO THEIR POSITION     Y02072
*  IN THE MESSAGE CSECT (RELATIVE TO ZERO), MULT BY 2 (LENGTH    Y02072
*  OF AN INDEX)                                                  Y02072
MSG126   EQU   10*2                     MESSAGE NO IN MSG CSECT  Y02072
MSG129   EQU   3*2                      MESSAGE NO IN MSG CSECT  Y02072
*
***********************************************************************
*
*   HOUSE KEEPING
*
***********************************************************************
*
         BALR  RBASE,0
         USING *,RBASE
         USING IHADCB,RDCB
         USING FORCORE,RCORE
         USING DEB,RDEB
         USING UCBOB,RUCB
*
*
*
***********************************************************************
*
*   INITIALIZE REGISTERS
*
***********************************************************************
*
         B     AROUND                   BR AROUND CONSTANTS      Y02072
         DC    C'IGG0191V'              MODULE NAME              Y02072
         DC    C' ZA02191'              LAST SHIP CODE         @ZA02191
         DC    C' 7/29/75'              LAST DATE MODIFIED     @ZA02191
AROUND   DS    0H                                                Y02072
*
         L     RDCB,0(RPARC)            LOAD CURRENT DCB ADDR
         L     RCORE,4(RWTGC)           LOAD CURRENT WORK AREA ADDR
         L     RDEB,DCBDEBAD            LOAD DEB ADDR
         L     RUCB,DEBUCBAD            LOAD UCB ADDR
         DROP  RDEB                                              Y02072
*
*
***********************************************************************
*
*   THIS ROUTINE TESTS FLAG BYTES SET BY IT OR 1U TO DETERMINE WHAT
*   ACTIONS ARE TO BE TAKEN BY THIS MODULE
*
***********************************************************************
*
         CLI   DXSTAGE2,0               IF NOT ZERO, ENTRY IS TO Y02072
*                                         XCTL TO 2ND STAGE EXEC Y02072
*                                         (COMPOSITE CONSOLES)   Y02072
         BNE   TSTEXCP                  GO XCTL TO 2ND STG EXEC  Y02072
         CLI   DXABEND,ABEND            ABEND SWITCH ON          Y02072
         BE    ABENROUT                 GO SET ABEND CODE        S20202
*
***********************************************************************
*
*   LOAD UCS IMAGE FROM SYS1.IMAGELIB
*
***********************************************************************
*
         LA    RE,DXBLDLIM              LOAD BLDL LIST ADDR      Y02072
*
         L     RF,DXIMGDCB              GET ADDR OF IMAGELIB     Y02072
*                                       DCB                      S20202
*
         LOAD  DE=(0),DCB=(1)           LOAD UCS SET FROM        S20202
*                                       SYS1.IMAGELIB            S20202
*
         LR    RB,RE                    RETAIN LOAD ADDR IN REG. S20202
*                                       RB                       S20202
         MVC   DXSAVUCS,DXBLDLIM        SAVE UCS IMAGE NAME      Y02072
         OI    FCAOPEN2,FCAOUCSL        INDIC UCS IMAGE LOADED   YM4697
*
***********************************************************************
*
*   SET UP DXCCW1 FOR EXCP TO LOAD UCSB                          Y02072
*
***********************************************************************
*
         MVI   IOBFLAG1,UNRLMASK        SET IOBFLAG= C.C/UNRELATED 'ON'
         XC    DXCCW1(16),DXCCW1        RESET CCW AREA TO X'00'
*
*
***********************************************************************
*
*   WHEN UCS IS TO BE LOADED, BUILD CCWS IN WORK AREA
*
***********************************************************************
*
         LA    RC,TWOMASK               LOAD NUMBER OF RETRIES   S20202
*
*      WE NOW MUST GET INTO PROTECTION KEY ZERO TO ACCESS THE UCS
* IMAGE BECAUSE THE USER MAY HAVE LINK EDITED IT INTO SYS1.IMAGELIB
* WITHOUT THE 'RENT' ATTRIBUTE, AND IT THEREFORE HAS BEEN LOADED
* INTO VIRTUAL STORAGE IN THE USER'S KEY ACCORDING TO TCBPKF.  IT
* WOULD BE FETCH PROTECTED.
         MODESET EXTKEY=SUPR           GET INTO PROT.KEY ZERO  @ZA02191
         SR    RF,RF                   PREPARE TO GET LINE NUM @ZA02191
         IC    RF,1(RB)                GET NUM OF VERIFY LINES @ZA02191
         LA    RE,2(RF,RB)             PT TO REAL UCS IMAGE    @ZA02191
*
***********************************************************************
*
*   DETERMINE WHICH CCW CHAIN IS TO BE GENERATED TO LOAD THE UCS
*   (1) FOR 1403/2821 THE SEQ IS A GATE LOAD FOLLOWED BY A LOAD OR
*       LOAD AND FOLD.
*   (2) FOR 3211/3811 THE SEQUENCE IS A LOAD FOLLOWED BY A FOLD OR
*       JUST A LOAD COMMAND
*
***********************************************************************
*
         CLI   UCBTBYT4,PRTM3211        CK IF DCB BEING OPENED   S20202
*                                       FOR 321                  S20202
         BNE   GOCNSTGL                 NO GO CONSTRUCT GATE     S20202
*                                       LOAD SEQ.                S20202
*                                       YES FALLS THROUGH TO FORM LOAD
*                                       OR LOAD AND FOLD CCW'S
*
*
*   BUILD CCW'S TO LOAD UCS BUFFER FOR 3211/3811
*
         ST    RE,DXCCW1                STORE ADDRESS OF IMAGE   S20202
*                                       IN CCW                   S20202
         MVI   DXCCW1,UCSLDCC           MOVE LOAD UCS COMMAND CD S20202
*                                       TO CC                    S20202
         MVC   DXCCW1+K6(K2),UCSLTHEX   MOVE LENGTH OF UCS TO    S20202
*                                       CCW                      S20202
         MVI   DXCCW1+K4,SILIMASK       SET CCW FLAGS FOR CC AND S20202
*                                       SILI                     S20202
*
*  FORM CCW TO FOLD OR UNFOLD UCS IMAGE SM5451
*
         MVI   DXCCW2+7,NONZERO         NON-ZERO COUNT TO CCW    SM5451
         MVI   DXCCW2+4,SILIMSK         SET SILI FLAG            SM5451
         MVI   DXCCW2,UCSFLDCC          ASSUME FOLDING OF IMAGE  SM5451
         TM    DXUCSOPT,FOLDMASK        CK IF FOLD SPEC FOR      Y02072
*                                       UCS                      S20202
         BO    EXCPGOGO                 YES, GO ISSUE EXCP TO    SM5451
*                                         LOAD + FOLD IMAGE      SM5451
         MVI   DXCCW2,UCSUNFLD          NO, RESET COM CODE TO    SM5451
*                                         UNFOLD IMAGE           SM5451
         B     EXCPGOGO                 GO ISSUE EXCP TO LOAD    SM5451
*
*   BUILD CCW'S TO LOAD UCS BUFFER FOR PRINTERS USING 2821 CONTROL UNIT
*
GOCNSTGL EQU   *                                                 S20202
*
*   CONSTRUCT CONTROL COMMAND
*
         MVI   DXCCW1,CNTRLMSK          COMMAND CODE = GATE LOAD
         MVI   DXCCW1+4,SILIMASK        SET SILI AND C.C ON
         MVI   DXCCW1+7,ONEMASK         SET LENGTH=X'01'
*
*   CONSTRUCT LOAD COMMAND
*
         MVI   DXCCW2+7,UCSLNGTH        MOVE UCS IMAGE LENGTH = 240
*
         ST    RE,DXCCW2                STORE UCS ADDR IN CCW
*
         MVI   DXCCW2,UNFLCCW           COMMAND CODE = LOAD UCS,UNFOLD
*
         TM    DXUCSOPT,FOLDMASK        TEST IF FOLD IS SPEC     Y02072
         BC    14,EXCPGOGO              IF NOT,GOTO EXCP ROUTINE
*
         MVI   DXCCW2,FLDCCW            COMMAND CODE = LOAD UCS,FOLD
*
EXCPGOGO EQU   *
*
         L     RF,UCBXTADR              LOAD UCB EXTENSION ADDR  M0092
         MVC   UCBUCSID-UCBUCSID(4,RF),DXUCSID   PUT UCS IMAGE ID
*                                       IN UCB EXTENSION AREA
         LA    RF,DXIOB                 LOAD IOB ADDR IN REG.1
         EXCP  (1)
*
         WAIT  ECB=DXECB                WAIT ON THE ABOVE EXCP
*
         CLI   DXECB,X'7F'              TEST COMP CODE OF ABOVE EXCP
         BC    8,UCSVRFY                IF YES,GOTO TEST VERIFY OPTION
*
***********************************************************************
*
*   IF ERROR ,REPEAT UCS LOAD EXCP ONE TIME
*
***********************************************************************
*
         BCT   RC,EXCPGOGO              IF ERROR REPEAT ONCE     S20202
*
***********************************************************************
*
         MODESET EXTKEY=DATAMGT         GET BACK IN OUR KEY    @ZA02191
*
***********************************************************************
*
*   WHEN THERE WAS AN I/O ERROR DURING LOAD UCS,UPDATE UCB WITH X'00'
*   TO SHOW NO IMAGE IN THE BUFFER.RETURN CODE = X'0C' IN REG.15
*   WTO 'IEC126I XXX UCS PARITY ERROR.COMP CODE=B13' TO AN OPERATOR
*   RUN C.E. DIAGNOSTICS FOR THE UCS FEATURE.
*   +280(REG.4) POINTS TO THE IOB USED FOR THE EXCP ABOVE.
*
***********************************************************************
         LA    RWK3,MSG126              MESSAGE NO FOR MSG RTN   Y02072
         BAL   RWK6,GETMSG              GO GET MESSAGE           Y02072
         LA    RF,MSGLSTSZ              LOAD WTO ADDR
         SVC   35                       ISSUE WTO SVC
*
         LA    RB,OABD069               INTERNAL CODE 69         S21042
*
         BC    15,UPUCBEND              GOTO UPDATE UCB WITH X'00'
*
***********************************************************************
*
UCSVRFY  EQU   *
*
***********************************************************************
*
*   TEST IF VERIFY OPTION IS SPECIFIED.
*
***********************************************************************
*
         MODESET EXTKEY=DATAMGT         GET BACK IN OUR KEY    @ZA02191
         TM    DXUCSOPT,VERIFY          TEST IF VERIFY IS SPEC   Y02072
         BC    K8,UPDTUCB               IF NO GO UPDATE UCB-UCS  S20202
*                                       FIELDS                   S20202
*
*   VERIFY SPECIFIED GO LOAD ADDRESS IGG0197U AND XCTL
*
*
         LA    RF,MODNM7U               LOAD ' MOD IGG0197U      S20202
         B     MVEIDTTR                 GO TO WTG SEQUENCE       S20202
*
***********************************************************************
*
UPDTUCB  EQU   *
*
***********************************************************************
*
*   UPDATE UCB - UCS FIELDS
*
***********************************************************************
*
         L     RJ,UCBXTADR              GET ADDRESS OF UCB       S20202
*                                       EXTENTION                S20202
         MODESET  EXTKEY=SUPR           GET INTO SUPER KEY       Y02072
         SPACE
         MVC   UCSOPOFS(1,RJ),DXUCSOPT  MVOE UCS OPTION BITS     Y02072
*                                       TO UCB                   S20202
         OC    UCSOPOFS(K1,RJ),NULL(RB) SET DEFAULT BIT IF ANY   S20202
         NI    UCSOPOFS(RJ),RESETMSK    RESET VERIFY IF ANY      S20202
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
*
***********************************************************************
*
*   DELETE UCS IMAGE
*
***********************************************************************
*
         LA    RE,DXSAVUCS              LOAD ADDR OF ENTRY PT    Y02072
*
         DELETE EPLOC=(0)               DELETE UCS IMAGE
         NI    FCAOPEN2,X'FF'-FCAOUCSL  INDIC IMAGE DELETED      YM4697
*
***********************************************************************
*
UCSEXIT  EQU   *                                                 S20202
*
         CLI   UCBTBYT4,PRTM3211        IF NOT 3211, GO TO 2ND   Y02072
*                                         STAGE EXECUTOR         Y02072
         BNE   RESTERPT                 GO RESTORE DCB EROPT     Y02072
         MVI   DXFLAG1,0                RESET FCB SW TO BE USED  Y02072
*                                       BY IGG0197E
         TM    DXFCBSW1,IEC129RQ        CK IF IEC129 MSG REQ     Y02072
         BNO   XCTL197E                 NO GO LOAD FCB IMAGE IN  S20202
*                                       IGG0197E                 S20202
*
**********************************************************************
*
*        ISSUE MESSAGE IEC129 AND XCTL TOIGG0197E
*
**********************************************************************
*
         LA    RWK3,MSG129              MESSAGE NO FOR MSG RTN   Y02072
         BAL   RWK6,GETMSG              GO GET MESSAGE           Y02072
         XC    RPLY(24),RPLY           CLEAR REPLY AREA AND ECB  M5143
*                                       COMPLETE                 S20202
         LA    RF,REPLYLTH              GET ADDR FOR WTOR        S20202
         SVC   K35                      ISSUE WTOR IEC129        S20202
*
XCTL197E EQU   *                                                 S20202
*
         LA    RF,MODNM7E               GET IDTTR ADDR OF        S20202
*                                       IGG0197E                 S20202
         B     MVEIDTTR                 GO TO WTG SEQ            S20202
***********************************************************************
*
*   RESTORE DCB EROPT
*
***********************************************************************
*
RESTERPT EQU   *                                                 S20202
*
         MVC   DXCCW6(1),DXEROPT        MOVE IN DCB EROPT        Y02072
*
*
*   SEE IF SYS1.IMAGELIB HAS BEEN OPENED IF YES CLOSE IT
*
         CLI   DXIMGDCB,IMCLOSED        CK IF IMAGELIB CLOSED    Y02072
         BE    TSTEXCP                  IF NOT GO TEST IF EXCP   S20202
*                                       SPECIFED                 S20202
*
         L     RF,DXIMGDCB              GET ADDR OF IMAGELIB     Y02072
*                                       DCB                      S20202
         SVC   105                      ISSUE SVC TO CLOSE       S20202
*                                       SYS1.IMAGEL              S20202
         NI    FCAOPEN2,X'FF'-FCAOIMGL  INDIC IMAGELIB CLOSED    YM4697
*
***********************************************************************
*
*   DETERMINE THE NEXT STAGE 2 EXECUTOR TO BE XCTLED
*
***********************************************************************
*
TSTEXCP  EQU   *
*
***********************************************************************
*
*   TEST IF EXCP IS SPECIFIED.IF YES,XCTL TO THE LAST OPEN EXECUTOR
*
***********************************************************************
*
         TM    DCBMACRF,EXCPMASK        TEST IF EXCP IS SPECIFIED
         BC    14,TSTBLKBF              IF NOT,GOTO BUFFER CNTRL BLK
*
         LA    RF,MODNM11               GO DO DCB COPY BACK      Y02072
         BC    15,MVEIDTTR              GOTO WTG TABLE UPDATE    Y02072
*
***********************************************************************
*
TSTBLKBF EQU   *
*
***********************************************************************
*
*   TEST IS BUFFER CONTROL BLOCK IS SPECIFIED.IF YES,XCTL TO IGG0911I
*
***********************************************************************
*
         TM    DCBBUFNO+3,BFCNTRL       BUFFER CONTROL BLOCK SPECIFIED
         BC    14,TSTPCI                IF YES,GOTO TEST PCI ROUTINE
*
         CLI   DCBBUFNO,0               SEE IF BUFFER POOL REQUESTED
         BE    TSTPCI                   IF YES, GO TO TEST CH. SCHED.
*
         LA    RF,MODNM1I               LOAD ADDR OF IDTTR OF IGG0191I
         BC    15,MVEIDTTR              GOTO MOVE IDTTR ROUTINE
*
***********************************************************************
*
TSTPCI   EQU   *
*
***********************************************************************
*
*   TEST IF CHAINED SCHEDULING IS REQUESTED.  IF YES, XCTL TO IGG0191Q.
*
***********************************************************************
*
         TM    DCBOPTCD,PCIMASK         TEST IF CHAINED SCHED.REQUESTED
         BC    14,BQSAMOUT              IF NOT,GOTO BSAM/QSAM ROUTINE
*
         LA    RF,MODNM1Q               LOAD ADDR OF IDTTR OF IGG0191Q
         BC    15,MVEIDTTR              GOTO MOVE IDTTR ROUTINE
*
***********************************************************************
*
BQSAMOUT EQU   *
*
***********************************************************************
*
*   BSAM/QSAM EXIT.XCTL TO IGG0191G
*
***********************************************************************
*
         LA    RF,MODNM1G               LOAD ADDR OF IDTTR IF IGG0191G
*
***********************************************************************
*
MVEIDTTR EQU   *
*
***********************************************************************
*
*   MOVE IDTTR TO XCTL WTG TABLE
*
***********************************************************************
*
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,0(RF)         ST NEXT MODULE ID IN WTG @Z30TSCF
*
***********************************************************************
*
RELOOP   EQU   *
*
***********************************************************************
*
*   UPDATE WTG TABLE AND XCTL TO NEXT MODULE
*
***********************************************************************
*
         LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURRENT WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCREMENT CURRENT DCB ENTRY
         CLC   0(2,RWTGC),AMIDCNST      IS THE RTN NEEDED AGAIN
         BCR   8,RBASE                  IF YES,RETURN TO PROCESS
*
         CLC   0(2,RWTGC),OPIDCNST      TEST END OF WTG TABLE
         BC    7,RELOOP                 IF NOT,CHECK NEXT ENTRY
*
         LR    RPARC,RPAR               RESET REG TO '0'
         LA    RWTGC,WAOFF(0,RWTG)      RESET WTG REG TO POINT +32(WTG)
ZCHECK   CLI   0(RWTGC),X'00'           TEST IF THIS ENTRY COMPLETE
         BC    7,TCTLRTN                IF NOT,XCTL
*
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY OF WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)     GET NEXT ENTRY OF DCB
         B     ZCHECK                   GO CHECK NEXT ENTRY      XM5898
TCTLRTN  EQU   *
*
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
*   IF THE I/O ERROR PERSISTS,RUN C.E. DIAGNOSTICS ON THE PRINTER
*
***********************************************************************
*
*
ABENROUT EQU   *                                                 S20202
*
         SR    RB,RB                    ZERO OUT REG 12          S20202
         IC    RB,DXABRETC              PUT COMP CODE INTO       Y02072
*                                       REG 1                    S20202
         B     ABENDRTN                 GO TO ABEND ROUTINE      S20202
***********************************************************************
*
*
***********************************************************************
*
UPUCBEND EQU   *
*
***********************************************************************
*
*   UPDATE THE UCB WITH X'00' TO INDICATE THAT THE IMAGE IN THE BUFFER
*   IS NOT USABLE
*
***********************************************************************
*
         L     RJ,UCBXTADR              GET ADDRESS OF UCB       S20202
*                                       EXTENTION                S20202
         MODESET  EXTKEY=SUPR           GET INTO SUPER KEY       Y02072
         SPACE
         USING UCBUCS,RJ                                         Y02072
         XC    UCBUCSID,UCBUCSID        CLR UCS IMAGE ID         Y02072
         NI    UCBUCSOP,PARITYER        LEAVE PAR ER BIT ON      Y02072
*                                         IF ON                  Y02072
         DROP  RJ                                                Y02072
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
*
***********************************************************************
*
*   DELETE THE UCS IMAGE LOADED
*
***********************************************************************
*
         LA    RE,DXSAVUCS              LOAD ADDR OF IMAGE ID    Y02072
         DELETE EPLOC=(0)
         NI    FCAOPEN2,X'FF'-FCAOUCSL  INDIC UCS IMAGE DELETED  YM4697
*
***********************************************************************
*
ABENDRTN EQU   *
*
***********************************************************************
*
*   ABEND ROUTINE.COMPLETION CODE=B13.RETURN CODE IS IN REG15.
*
***********************************************************************
*
         TM    DXNABEND,ABENDBIT         TEST IF ABEND IN CNTRL  Y02072
         BC    1,UCSEXIT                IF YES,GOTO EXIT ROUTINE
*
         L     RWK3,DXREG9              SAVE DCB ADDRESS       @ZA04647
         DMABCOND (RB),PDLOAD,RETURN=NONE,RES=NO,REGSAVE=YES     S21042X
                                        XCTL TO PROBLEM DETERMINATION
         B     TCTLRTN                                           S21042
         EJECT
***********************************************************************
*  'GETMSG' SUBROUTINE                                           Y02072
*  THIS SUBROUTINE EXTRACTS THE REQUIRED MESSAGE FROM THE MSG    Y02072
*  CSECT AND MOVES IT TO THE WTO/WTOR AREA.                      Y02072
*
*    INPUT TO THIS ROUTINE:                                      Y02072
*      REG 9 - MESSAGE NUMBER IN CSECT                           Y02072
*      REG 11 - RETURN ADDRESS                                   Y02072
*
*    OUTPUT FROM THIS ROUTINE:                                   Y02072
*      MESSAGE TEXT IN WTO/WTOR AREA                             Y02072
***********************************************************************
GETMSG   EQU   *                        ENTRY POINT, MSG RTN     Y02072
         L     RWK7,DXMSGADR            GET ADDR OF MSG CSECT    Y02072
         LH    RWK2,0(RWK7,RWK3)        GET INDEX TO MESSAGE     Y02072
         LA    RWK2,0(RWK7,RWK2)        ADDR OF MESSAGE ENTRY    Y02072
         USING MSGENTRY,RWK2                                     Y02072
         SR    RWK3,RWK3                PREPARE REGISTER         Y02072
         IC    RWK3,MSGOFF              GET OFFSET TO TEXT IN    Y02072
*                                         ENTRY                  Y02072
         LA    RWK3,0(RWK2,RWK3)        ADDR OF MESSAGE TEXT     Y02072
         USING MSGTXTD,RWK3                                      Y02072
         SR    RWK7,RWK7                PREPARE REG FOR RETURN   Y02072
         IC    RWK7,MSGLNG              LEN OF MSG - 1           Y02072
         EX    RWK7,MVCINST             MOVE MESSAGE TO AREA     Y02072
         IC    RWK7,MSGOFF1             OFFSET TO FIRST VAR FLD  Y02072
         LA    RWK3,MSGAREA(RWK7)       ADDRESS OF FIRST VAR FLD Y02072
         MVC   0(L'UCBNAME,RWK3),UCBNAME  MOVE DEV NAME TO MSG   Y02072
         BR    RWK6                     RETURN                   Y02072
MVCINST  MVC   MSGAREA(0),MSGTXT        EXECUTED BY A EX INST    Y02072
         EJECT
*
***********************************************************************
*
***********************************************************************
*
*   CONSTANTS
*
***********************************************************************
*
*   FOR WTG TABLE
*
OPIDCNST DC    C'0S'
AMIDCNST DC    C'1V'
UCSLTHEX DC    X'0200'                  UCS LENGTH CONSTANT =    S20202
*                                       512                      S20202
*
***********************************************************************
*
*   XCTL TABLE
*
***********************************************************************
*
MODNM1G  DC    C'1G',VL3(IGG0191G)      MOD IGG0191G           @Z30TSCF
MODNM1I  DC    C'1I',VL3(IGG0191I)      MOD IGG0191I           @Z30TSCF
MODNM1Q  DC    C'1Q',VL3(IGG0191Q)      MOD IGG0191Q           @Z30TSCF
MODNM7E  DC    C'7E',VL3(IGG0197E)      MOD IGG0197E           @Z30TSCF
MODNM7U  DC    C'7U',VL3(IGG0197U)      MOD IGG0197U           @Z30TSCF
MODNM11  DC    C'11',VL3(IGG01911)      MOD IGG01911           @Z30TSCF
PDLOAD   DC    C'6M',VL3(IGG0196M)      MOD IGG0196M           @Z30TSCF
         SPACE
PATCH    DC    0H'0',50X'00'            PATCH AREA               Y02072
END      EQU   *                        END OF MODULE            Y02072
*
*   DSECT
*
***********************************************************************
*
*
         DCBD  DSORG=PS,DEVD=PR         MAP A PRINTER DCB
*
***********************************************************************
*
         EJECT
CVT      DSECT                                                 @Z30TSCF
         CVT                                                   @Z30TSCF
         EJECT
         IECDSECS MAIN,EXPAND=YES                                S21042
         ORG   REPLYLTH+52                                       A33665
RPLY     DS    20C                                               A33665
RPLYECB  DS    F                                                 A33665
MCSMASK  DS    H                                                 A33665
LOMASK   DS    8C                                                A33665
UCSID    DS    4C                                                A33665
         ORG   MSGLSTSZ                                          Y02072
MSGAREA  DS    0CL64                    AREA TO MOVE 64 BYTES    Y02072
*                                         TO FOR WTO/WOTR'S      Y02072
*
         EJECT
         IGGSCW                                                  Y02072
         EJECT
         IHAFCAUD  ORG=YES                                       YM4697
         EJECT
         IECDSECS  WTG,PREFX,EXPAND=YES                        @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MOD ID              Y02072
         EJECT
         IGGMSG                                                  Y02072
***********************************************************************
*
SRT      DSECT
         IEFUCBOB
*
***********************************************************************
*
*   DATA EXTENT BLOCK  (UNTIL PROTIOS CHANGES)
DEB      DSECT
DEBNMSUB DS    0CL1
DEBTCBAD DS    CL4
DEBAMLNG DS    0CL1
DEBDEBAD DS    CL4
DEBOFLGS DS    0CL1
DEBIRBAD DS    CL4
DEBOPATB DS    0CL1
DEBSYSPG DS    CL4
DEBNMEXT DS    0CL1
DEBUSRPG DS    CL4
DEBPRIOR DS    0CL1
DEBECBAD DS    CL4
DEBPROTG DS    0CL1
DEBDEBID DS    0CL1
DEBDCBAD DS    CL4
DEBEXSCL DS    0CL1
DEBAPPAD DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    CL4
DEBTVLSQ DS    0CL2                VOL SEQ FOR TAPE
DEBBINUM DS    CL2
DEBTVLNM DS    0CL2                NO OF VOL FOR TAPE
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
*  FOR ADDITIONAL EXTENT ON DIRECT ACCESS THE FOLLOWING FIELDS
*  WILL HAVE TO BE OFFSET ANOTHER 16 BYTES
DEBVOLSQ DS    H
DEBVOLNM DS    H
*  FOR EACH SUBROUTINE LOADED BUMP THIS OFFSET BY 2
DEBSUBID DS    H
*
         END
