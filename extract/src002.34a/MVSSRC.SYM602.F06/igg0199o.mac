         TITLE 'IGG0199O - BSAM/QSAM SEARCH DIRECT ON DA EXEC'
IGG0199O CSECT
*MODULE NAME - IGG0199O                                          Y02072
*
*DESCRIPTIVE NAME - BUILD IOBS                                   Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          VS2 RELEASE 1.6 DELETIONS                                  *
*426000-426400,427600-428400                                    SA51532
*                                                                     *
*          VS2 RELEASE 2 DELETIONS                               Y02072
*019800,093000,232000,275000,276300,280000-280010,307600-307800, Y02072
*312000,337000,369000-375000,378000,408000,458800,466000-530000, Y02072
*537000,540000,561000-565000,568000,602000,624000-628000,        Y02072
*632000-637000,645000-649000,689000,92000,294000,313000,         Y02072
*575000-583000,604000-612000                                     Y02072
*                                                                YM1378
*                                                                YM1158
*                                                                YM3176
*                                                                YM4640
*                                                                YM7595
*          VS2 RELEASE 03 DELETIONS
*
*                                                              @Z30TSCF
*D302500                                                        ZA01496
*A305500                                                        ZA01496
*A305500                                                        ZA01496
*
*          RELEASE 20 DELETIONS                                       *
*                                                                S20201
*          RELEASE 21 DELETIONS                                       *
*0335042000,566000,589000-591000,613000-619000,689000-690000     S21042
*
*STATUS CHANGE LEVEL 002
*
* FUNCTION/OPERATION--FOR DCBS BEING OPENED FOR INPUT WITH SEARCH DIR.
*      IT WILL LINK THE IOB'S AND INITIALIZE THEM.
*      IT WILL CONSTRUCT CHANNEL PROGRAMS NEEDED FOR PROPER EXECUTION.
*      IT WILL SET UP THE NUMBER OF EXTENTS NEEDED AND PLACE THE
*      ADDRESSES IN THE DCB AND IOB'S.
*
*      THIS MODULE WAS CREATED NEW WITH BASE IGG0191O IN RLSE 20
*
*ENTRY POINTS--ENTERED FROM IGG0191D BY USE OF THE XCTL MACRO.
*
*INPUT--SEE DESCRIPTION OF REGISTERS, DCB(USERS).
*OUTPUT--IOB(USER), SEE DESCRIPTION OF THE REGISTERS, DCB(USERS)
*
*
*EXTERNAL ROUTINES--NONE.
*
*EXITS-NORMAL--XCTL TO IGG01915 TO LOAD ROUTINES
*
*EXITS-ERROR--XCTL TO PROBLEM DETERMINATION FOR WTP AND 013 ABEND
*            INSUFFICIENT BUFFERS AVAILABLE IN
*            BUFFER POOL TO SATISFY DCB BUFFER REQUIREMENTS       M1552
*
*MACROS-ACTION--MODESET, XCTL, XCTLTABL, DMABCOND                Y02072
*
*MACROS-MAPPING--IHACCW,DCBD,IECDSECS(IOB,WTG),IGGBCB,IHAFCAUD
*
*TABLES/WORKAREAS-- WHERE TO GO TABLE  (WTG)
*
*   BYTE 0-7   NAME
*   BYTE 8-10  RELATIVE DISK ADDRESS (TTR) OF FIRST RECORD
*   BYTE 11    CONCATENATION NUMBER
*   BYTE 12    ZERO
*   BYTE 13    ALIAS INDICATOR AND USER DATA FIELD DESCRIPTOR
*              ALIAS INDICATOR 1 BIT
*              NUMBER OF TTRS IN USERS DATA FIELD  2 BITS
*   BYTE 14-16 TTR OF FIRST TEXT RECORD
*   BYTE 17    ZERO
*   BYTE 18-19 TTR OF NOTE LIST OR SCATTER LIST
*   BYTE 20    TRANSLATION TABLE
*   BYTE 21    ZERO OR NUMBER OF ENTRIES IN THE NOTE LIST
*   BYTE 22-23 ATTRIBUTES
*   BYTE 24-26 TOTAL CONTIGUOUS MAIN STORAGE REQUIRED FOR MODULE
*   BYTE 27-28 LENGTH OF FIRST TEXT RECORD
*   BYTE 29    LENGTH OF WTG TABLE  (IN DOUBLE WORDS)
*   BYTE 30-31 PATH THROUGH LOADS OF ROUTINES
*   BYTE 32-34 IDTTR OF EXECUTOR FOR 1ST DCB
*   BYTE 35    WORKAREA ADDRESS FOR 1ST DCB
*   BYTE 36-39 TABLE OF IDTTR'S
*
*        32+N(8)   IDTTR OF EXECUTOR FOR NTH DCB      (3 BYTES)
*                  WORKAREA ADDRESS FOR NTH DCB       (1 BYTE)
*                  IDTTR OF LAST ROUTINE  LOAD        (3 BYTES)
*                  NOT USED                           (1 BYTE)
*
*
***********************************************************************
*ATTRIBUTES--REENTRANT,REUSABLE, RUNS IN SUPERVISOR STATE,       Y02072
*            RUNS IN USER KEY UNLESS OTHERWISE SPECIFIED.        Y02072
*
*
***********************************************************************
*
*
*  REGISTER CONVENTIONS USED THROUGH OUT ALL OPEN PASSES
*
***********************************************************************
*
RDCB     EQU   2                        DCB REGISTER
RBASE    EQU   3                        BASE REGISTER
RCORE    EQU   4                        WORK AREA ADDRESS
RPAR     EQU   5                        TOP OF PARAMETER LIST
RWTG     EQU   6                        TOP OF WTG TABLE
RPARC    EQU   7                        CURRENT PARAMETER
RWTGC    EQU   8                        CURRENT TRANS LOAD
RBUFADR  EQU   9                        MAINTAINS ADDR OF NEXT   Y02072
*                                         AVAILABLE BUFFER       Y02072
RWK1     EQU   10                       WORK REGISTER            Y02072
RDEB     EQU   11                       DEB ADDRESS
RB       EQU   12                       WORK REG1  **
RC       EQU   13                       WORK REG2  **  IOB GENERATION
RWK2     EQU   RC                       WORK REGISTER            Y02072
RD       EQU   14                       WORK REG3  **
RJ       EQU   15                       WORK REG4
RWK3     EQU   RJ                       WORK REG                 YM7595
CPR      EQU   RJ                       ADDR OF CHANNEL PROG
RE       EQU   0                        WORK REG5
RF       EQU   1                        WORK REG6
RCCWAD   EQU   RF                       PTR TO COUNT FIELD FOR   Y02072
*                                       SEARCH DIRECT            Y02072
*
PARAR    EQU   RPARC
*
***********************************************************************
*
* DISPLACEMENTS AS EQUATES
*
D0       EQU   0                        DISPLACEMENT OF ZERO
D1       EQU   1                        DISPLACEMENT OF ONE
D4       EQU   4                        DISPLACEMENT OF FOUR
D6       EQU   6                        DISPLACEMENT OF SIX
D7       EQU   7                        DISPLACEMENT OF SEVEN
D8       EQU   8                        DISPLACEMENT OF EIGHT
D16      EQU   16                       DISPLACEMENT OF SIXTEEN    DMOR
D24      EQU   24                       DISPLACEMENT OF 24
D28      EQU   28                       DISPLACEMENT OF TWENTYEIGHT
D31      EQU   31                       DISPLACEMENT OF THIRTYONE
D36      EQU   36                       DISPLACEMENT OF THIRTYSIX
D40      EQU   40                       DISPLACEMENT OF FORTY
D44      EQU   44                       DISPLACEMENT OF FORTYFOUR
D47      EQU   47                       DISPLACEMENT OF FORTYSEVEN
D48      EQU   48                       DISPLACEMENT OF 48
D64      EQU   64                       DISPLACEMENT OF 64
*
* BYTE COUNTS AS EQUATES
*
B0       EQU   0                        BYTE COUNT OF ZERO
B1       EQU   1                        BYTE COUNT OF ONE
B8       EQU   8                        BYTE COUNT OF EIGHT
*
*********************************************************************
* MASK'S TO TEST PARAMETER LIST WITH
INP      EQU   X'0F'
OUTP       EQU   X'0F'
UPDATE     EQU   X'04'
INOUT      EQU   X'03'
OUTIN      EQU   X'07'
RDBACK     EQU   X'01'
*
*
*********************************************************************
*
*
* OFFSET FOR PARAMETER TESTS
*
ONE        EQU   X'01'
FRTYATE    EQU   48
DISPSN     EQU   X'0'
*
*
*********************************************************************
*
* OFFSETS FOR DEB REFERENCE
*
*
BIN        EQU   36                     BIN NOS.                   DM0I
BEXT       EQU   38                     BIN EXTENT                 DM0I
*
*
*
*********************************************************************
*
* MASK'S USED TO TEST OPTION FIELDS IN DCB
*
*
*
DOUBLE     EQU   X'01'
*
*
STOWB      EQU   X'80'
WRTCK      EQU   X'80'                  WRITE CHECK                DM0I
OVRFLO     EQU   X'20'
PCI        EQU   X'20'
MINKEY     EQU   X'01'
CONCAT     EQU   X'FF'
*
*********************************************************************
*
* MASKS FOR ACCESS METHODS IN SAM
*
QSAMB      EQU   X'01'
BSAMBIT    EQU   X'00'
PAM        EQU   X'02'
*
*
* MASKS FOR MACRO'S TO BE USED
*
GETB       EQU   X'40'
PUTB       EQU   X'40'
READB      EQU   X'20'
WRITEB     EQU   X'20'
POINTB     EQU   X'04'
CNTRLB     EQU   X'02'
*
*
*********************************************************************
*
* MASKS FOR DEVICES TO BE SUPPORTED
*
SRCHDRK  EQU   X'40'                    SRCH DIRECT MASK
NOTSILI  EQU   X'DF'                    NOT SIL BIT MASK
NORCDRY  EQU   X'20'                    NO RCD READY FOR ENTIRE
*                                       DATA SET MASK
TICSD    EQU   X'48'                    SEARCH DIRECT TIC
*
*********************************************************************
*
*
COFFSR   EQU   80                       OFFSET OF RD CCW IF
*                                       RPS USED
SDOFFSR  EQU   72                       SD OFFSET TO RD DATA
CSTOFFSR EQU   72                       OFFSET OF RDCCW IF RCD
*                                       READY AND F-STD RCDS
SRCHD    EQU   X'40'                    SEARCH DIRECT MASK         DMOR
*                                                                  DMOR
*
*
*********************************************************************
*
* MASKS FOR STATUS BITS TO BE TESTED
*
*
EXCPN      EQU   X'30'
COMPLETE   EQU   X'60'                  COMPLETE MASK              DM0I
FIRSTIOB   EQU   X'01'
*
*
*
*********************************************************************
*
* MASKS FOR BUFFER TECHNIQUE'S
*
BFTEKR     EQU   X'20'                  BDAM VS
VSBITS     EQU   X'48'                  VS BITS IN DCBRECFM
CYCLICB    EQU   X'20'
EXCHANGB   EQU   X'10'
SIMPLEB    EQU   X'40'
EXCHCYCL   EQU   X'30'
*
*********************************************************************
*
*
* MODES OF OPERATION FOR QSAM
*
LOCATEB    EQU   X'08'
MOVEB      EQU   X'10'
SUBSTUTB   EQU   X'04'
*
*
*********************************************************************
*
*
* COMMAND BYTES FOR THE 2841
*
*       CONTROL UNIT
*
*
*
SRCHE      EQU   X'31'
WRTCKD     EQU   X'1D'
RDC        EQU   X'92'
RDKD       EQU   X'8E'
RDD        EQU   X'86'
MTRCT    EQU   X'92'                    M/T RD CNT CCW CMND
SETSC    EQU   X'23'                    SET SECTOR CCW CMND
RDSC     EQU   X'22'                    READ SECTOR CCW CMND
NOTFIXED EQU   X'40'                    V OR U FORMAT MASK
*
*
*********************************************************************
*
*
* MASKS FOR RECORD FORMATS
*
*
STANDRDF   EQU   X'88'                  STANDARD FORMORT           DM0I
FORMATU    EQU   X'C0'
FORMATF    EQU   X'80'
FORMATV    EQU   X'40'
BLOCKED    EQU   X'10'
*
*
*********************************************************************
*
*
*   CHANNEL CONTROL WORD FLAGS
*
SKIP       EQU   X'10'
SLI        EQU   X'20'
SLIBIT     EQU   X'DF'                  TO TURN OFF SLI BIT
CCWNOP     EQU   X'03'                  NOP
CC         EQU   X'40'
DATACH     EQU   X'A0'
SLICC      EQU   X'60'
SLICCSK    EQU   X'70'
*
*
*********************************************************************
*
*
KEYS       EQU   X'01'
QSAM       EQU   X'01'
*
*
*********************************************************************
*
GETMAIN    EQU   4
QSAMZ      EQU   X'05'                  MASK                       DM0I
NRMLEND    EQU   X'7F'                  NORMAL END CODE FOR ECB
SRCHDRCT   EQU   X'40'                  SEARCH DIRECT--DCBCIND1
POBIT      EQU   X'02'                  PARTITIONED ORGANIZATION
ABCODE     EQU   X'013'                  ABEND CODE
COMMTABL   EQU   16                     MASK                       DM0I
NULL       EQU   0                      CONSTANT 0
K2         EQU   2                      CONSTANT 2
K3         EQU   3                      CONSTANT 3
WORD       EQU   4                      LENGTH OF A FULL WORD
K5         EQU   5                      CONSTANT 5
K6         EQU   6                      CONSTANT 6
K7         EQU   7                      CONSTANT 7
K8         EQU   8                      CONSTANT 8
L1CCW      EQU   8                      LENGTH OF 1 CCW
K12        EQU   12                     CONSTANT 12
K14        EQU   14                     CONSTANT 14
L2CCW      EQU   16                     LENGTH OF 2 CCW'S
K19        EQU   19                     CONSTANT 19
K20        EQU   20                     CONSTANT 20
K23        EQU   23                     CONSTANT 23
L3CCW      EQU   24                     LENGTH OF 3 CCW'S
K24        EQU   24                     CONSTANT 24
K28        EQU   28                     CONSTANT 28
K31        EQU   31                     CONSTANT 31
K32        EQU   32                     CONSTANT 32
K36        EQU   36                     CONSTANT 36
K39      EQU   39                       CONSTANT OF 39
L5CCW      EQU   40                     LENGTH OF 5 CCW'S
K40        EQU   40                     CONSTANT 40
K43        EQU   43                     CONSTANT 43
CPLNGTH  EQU   56                       NON RCD READY CHP LNGTH
K64        EQU   64                     CONSTANT 64
K80        EQU   80                     CONSTANT 80
K83        EQU   83                     CONSTANT 83
K104       EQU   104                    CONSTANT 104
OABD064  EQU   64                                                S21042
ONEIOB   EQU   1                        USED TO CHECK FOR 1 IOB  YM1158
*********************************************************************
*
*
*
*
*
*
*
*
         BALR  RBASE,NULL               REG 3 IS BASE
         USING SOP2,RBASE
         USING IHADCB,RDCB              REG 2 IS DCB
         USING FORCORE,RCORE            REG 4 IS WORK AREA ADDR
SOP2     EQU   *
         B     BEGIN                    BRANCH AROUND MODULE ID  YM4640
         DC    C'IGG0199O'              MODULE NAME              YM4640
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'10/22/74'              LAST SHIP DATE         @Z30TSCF
BEGIN    DS    0H                       END OF MODULE ID         YM4640
*
         L     RDCB,NULL(RPARC)         GET DCB ADDRESS
         LA    RDCB,NULL(RDCB)          CLEAR HI BYTE
         L     RCORE,WORD(RWTGC)        GET WORK AREA ADDRESS
         MODESET  KEYADDR=DXUKEY,WORKREG=10 GET INTO USER KEY   ZA01496
*
         L     RB,DCBIOBA               GET FIRST IOB ADDR
         LR    RC,RB                    SET UP WORK REG
         LR    RD,RB                    SET UP WORK REG
         SR    RF,RF                    CLEAR FOR IC
         TM    DCBCIND2,QSAMB           QSAM USED
         IC    RF,DCBNCP                GET NO OF IOB'S
         BNO   SOP230                   BRANCH IF NO
         IC    RF,DCBBUFNO              USE BUFFER NO FOR IOB CNT
SOP230   EQU   *
         LR    RDEB,RF                  PUT CNT IN DECR REG
SOPSTART EQU   *
*
* START OF IOB BUILD ROUTINE
*
         SPACE 2
*
SOP242   EQU   *
*
         ST    RB,NULL(RD)              LINK NEW IOB TO LAST ONE BUILT
         LR    RD,RB                    UPDATE CURRENT IOB POINTER
         LA    RB,WORD(RB)              POINT TO ECB SLOT
         OI    NULL(RB),NRMLEND         SET ECB COMPLETE WITHOUT ERROR
         ST    RB,K12(RD)               STORE ECB ADDR IN IOB
         LA    RB,K20(RB)               POINT TO CP POINTER SLOT
         LR    RC,RB                    SAVE ADDR
         LA    RB,K24(RB)               POINT TO CP START ADDR
SOP243   EQU   *
         ST    RB,NULL(RC)              STORE CP STARTAD IN IOB
         L     RWK1,DXUDCBAD            GET ADDR OF USERS DCB    Y02072
         USING IOBSIOCC,RWK2                                     Y02072
         ST    RWK1,IOBDCBPT            DCB ADDR TO IOB          Y02072
         DROP  RWK2                                              Y02072
         MVC   K40(L'DCBFDAD,RD),DCBFDAD  INITIALIZE IOBFDAD.
*
***********************************************************************
*
*    CONSTRUCTION OF READ CHANNEL PROGRAM FOLLOWS
*
***********************************************************************
*
SOP255   EQU   *
*
         LA    RC,K19(RC)               POINT TO CCHHR FIELD IN IOB
         TM    JFCBMASK+D6,NORCDRY      ANY NON RPS DEVICES
         BO    SOP2NC1                  BRANCH IF YES
         BAL   RJ,SOP2STA               IF NOT GO BUILD SET SECTOR
SOP2NC1  EQU   *
         ST    RC,NULL(RB)              STORE ADDR IN SRCH CCW
         MVI   NULL(RB),SRCHE           STORE CMD BYTE
         MVI   K7(RB),K5                STORE COUNT OF 5 IN CCW
         OI    WORD(RB),CC              SET CMD CHAIN FLAG ON
         ST    RB,D8(RB)                PUT SEARCH ADDR IN TIC CCW DMOR
         LR    RC,RB                    UPDATE PTR
         MVI   K8(RB),TICSD             PUT IN SEARCH DIRECT TIC
         SR    RE,RE                                              17363
         IC    RE,DCBKEYLE              OBTAIN KL                 17363
         AH    RE,DCBBLKSI              ADD DL                    17363
*     KL+DL TO RH OF TIC FOR USE BY EOB ROUT                      17363
         STH   RE,K14(RB)                                         17363
SOP255B  EQU   *                                                  17363
*
*
*     SRCH AND TIC CCWS HAVE BEEN GENERATED AT THIS POINT
*
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         MVI       DCBOFFSR,SDOFFSR     PUT OFFSET IN DCB
         TM    DCBMACRF,POINTB          POINT MACRO USED ON INPUT
         BZ    SOP255A0                 NO BRANCH
         MVI   DCBCNTRZ,QSAMZ           MOVE ID                    DM0I
SOP255A0 EQU   *                                                 Y02072
         MODESET  KEYADDR=DXUKEY,WORKREG=10 GET INTO USER KEY    Y02072
         USING CPDEF,RB                 EST CHANNEL PGM BASE REG Y02072
         LA    RCCWAD,CPRDKD            POINT TO READ DATA CCW   Y02072
         ICM   RCCWAD,B'1000',TICCMD    INIT NEXT CCW TO A TIC   Y02072
*
*  IF THIS IS THE SECOND PHYSICAL IOB (FIRST LOGICAL IOB), THE   YM3176
*  CHANNEL PROGRAM ASSOCIATED WITH IT MUST BE ALTERED SO THAT    YM3176
*  THE SECOND TIC IS A READ COUNT TO KEEP THE CHANNEL PROGRAM    YM3176
*  FROM READING R0. THE FIRST I/O REQUEST FOR SEARCH DIRECT      YM3176
*  WOULD THEN OPERATE AS A SEARCH PREVIOUS REQUEST.              YM3176
*  IF ONLY ONE IOB IS BEING USED, ITS CHANNEL PROGRAM WILL BE    YM3176
*  ALTERED. THE CHAN PROG WILL BE RESET BY THE CHAN END APP      YM3176
*
         SR    RWK1,RWK1                PREPARE FOR IC INST      YM3176
         IC    RWK1,DCBNCP              NUM OF IOBS, ASSUME BSAM YM3176
         TM    DCBCIND2,DCBCNQSM        IS THIS QSAM             YM3176
         BZ    NUMCPOK                  NO, RWK1 CORRECT         YM3176
         IC    RWK1,DCBBUFNO            GET NUM IOBS, QSAM       YM3176
NUMCPOK  EQU   *                                                 YM3176
         CH    RWK1,CONONE              ONLY ONE IOB BEING USED  YM3176
         BE    BLDRDCNT                 YES, BUILD READ COUNT    YM3176
         L     RWK1,DCBIOBA             GET ADDR OF FIRST IOB    YM1378
         USING IOBQSAMN,RWK1            ESTABLISH IOB BASE REG   YM1378
         LA    RWK1,0(RWK1)             CLEAR HI-BYTE FOR COMP   YM3176
         LA    RD,0(RD)                 CLEAR HI-BYTE FOR COMP   YM3176
         CR    RD,RWK1                  IS CURRENT IOB THE FIRST YM1158
         BE    BUILDTIC                 DON'T BLD RD COUNT IF SO YM1158
         L     RWK1,IOBNIOBA            GET ADDR OF SECOND IOB   YM1378
         LA    RWK1,0(RWK1)             CLEAR HI-BYTE FOR COMP   YM3176
         DROP  RWK1                     DROP IOB BASE REGISTER   YM1378
         CR    RD,RWK1                  IS THIS IOB FOR RECORD 0 YM1378
         BNE   BUILDTIC                 BRANCH IF NOT            YM1378
*
BLDRDCNT EQU   *                                                 YM1158
         LA    RCCWAD,CPCOUNT2          PT TO SECOND COUNT FIELD Y02072
         TM    JFCBMASK+6,JFCNRPS       ANY DEVICES W/O RCD RDY  Y02072
         BO    NOTRPS                   BRANCH IF YES            Y02072
         LA    RCCWAD,L'CPRDSEC(,RCCWAD) INCR PAST RD SECTOR CCW Y02072
*
NOTRPS   EQU   *                                                 Y02072
         ICM   RCCWAD,B'1000',RDCNTCMD  CHANGE CCW TO READ COUNT Y02072
*
BUILDTIC EQU   *                                                 YM1378
         ST    RCCWAD,CPTIC2            SET UP CMND & DATA ADDR  Y02072
         DROP  RB                       DROP CHANNEL PGM BASE    Y02072
         MVI   K23(RB),K8               BYTE COUNT EQUALS EIGHT
         OI    K20(RB),CC               CMND CHAIN CCW
*
         TM    DCBCIND2,QSAMB           QSAM USED
         BO    SOP259                   YES BRANCH
*
         CLI   DCBKEYLE,ONE             IS KEY LENGTH SPECIFIED
         BL    SOP259A                  NO BRANCH
*
         MVI   K24(RB),RDKD             CCW CMND
         B     SOP259C                  GO ATOUND
*
SOP259   EQU   *
         LA    RF,K24(RB)               ADDR OF CCW
*
* FILL IN BUFFER ADDRESS AND BYTE COUNT
*
         L     RWK3,DCBBUFCB            BUFFER CNTRL BLK ADDR    YM7595
         USING BCBLK,RWK3
         L     RBUFADR,BCBBUFPT         ADDR OF NEXT AVAIL BUF   YM7595
         LTR   RBUFADR,RBUFADR          IS BUFFER AVAILABLE      YM7595
         BZ    BUFERR                   NO, ISSUE DMABCOND       YM7595
         USING CCW,RF
         STCM  RBUFADR,B'0111',CCWADDRB  BUFFER ADDR TO CCW      YM7595
         MVC   CCWBYTE,DCBBLKSI         BYTE COUNT TO CCW        YM7595
         USING BUFFER,RBUFADR
         MVC   BCBBUFAD,BUFNXPTB        UPDATE BUF CNTRL BLOCK   YM7595
         DROP  RWK3,RBUFADR,RF
SOP259A  EQU   *
         MVI   K24(RB),RDD              CCW COMMAND
*
*
SOP259C  EQU   *
*
         OI    D28(RB),CC               SET THE COMMAND CHAIN FLG
         TM    DCBRECFM,STANDRDF        IS IT F STANDARD
         BO    SOP259XB                 YES, BR AROUND SILI SETTING
         OI    D28(RB),SLI              SET SILI FLAG
SOP259XB EQU   *
         LA    RF,K40(RB)               CCHHR ADDR
         TM    JFCBMASK+D6,NORCDRY      ANY DEVICES W/O RCD READ
         BO    SOP2NST                  BRANCH IF YES
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         MVI   DCBOFFSR,COFFSR          PUT IN OFFSET TO RD CCW
*                                       IF RPS IS FEATURED
         MODESET  KEYADDR=DXUKEY,WORKREG=10 GET INTO USER KEY    Y02072
         LA    RF,K8(RF)                INCR ADDR BY EIGHT
SOP2NST  EQU   *
*
         ST    RF,K32(RB)               ADDR TO CCW
         MVI   K32(RB),MTRCT            CCW COMMAND
*
         MVI   K39(RB),K8               BYTE COUNT EQS EIGHT
*
*
         TM    JFCBMASK+D6,NORCDRY      NOT STND F, BUT WITH RCD
*                                       READY
         BO    SOP2NRR                  BRANCH IF NO
         BAL   RJ,SOP2RTA               GO BUILD RD SECTOR CCW
SOP2NRR  EQU   *
*
*******************************************************************DMOR
SOP259D  EQU   *
         LA    RB,CPLNGTH(RB)           PT TO NEXT POSS IOB
SOP240   EQU   *
         SH    RDEB,CONONE              DECR IOB COUNT             DMOR
         BP    SOPSTART                 IF MOR IOBS BRANCH
         L     RB,DCBIOBA               IF NOT LINK LAST TO FIRST  DMOR
         ST    RB,D0(RD)                PUT FIRST ADDR IN LAST     DMOR
         B     OFFSETRD                 GO AROUND INTERNAL SUBROUT DMOR
*
* SET SECTOR CCW BUILD RTN
*
SOP2STA  EQU   *
         L     RE,DXCCW11               GET SET SECTOR ADDR
         ST    RE,D0(RB)                PUT IN CCW
         MVI   D0(RB),SETSC             PUT IN CCW CMND
         MVI   D7(RB),B1                SET BYTE CNT TO ONE
         OI    D4(RB),CC                TURN ON CMND CHAIN
         B     CMNDFIN                  GO TO FINISH DETAILS
*
* READ SECTOR CCW BUILD RTN
*
SOP2RTA  EQU   *
         L     RE,DXCCW11               GET READ SECTOR ADDR     Y02072
         ST    RE,D40(RB)               PUT IN CCW
         MVI   D40(RB),RDSC             PUT IN RD SECT CMND
         OI    D36(RB),CC               CMND CHAIN PREVIOUS CCW
         MVI   D47(RB),B1               SET BYTE CNT TO ONE
CMNDFIN  EQU   *
         LA    RB,B8(RB)                INCR PTR
         BR    RJ                       RETURN
*
OFFSETRD EQU   *                                                 Y02072
*
         OI    NULL(RB),FIRSTIOB        SET INDICATOR ON FOR CLOSE
         LR    RC,RB                    SAVE ADDR FOR QSAM USE
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         SPACE
         OI    FCAOPEN2,FCAOIOBC        IOBS COMPLETED           YM7595
         TM    DCBDSORG,DCBDSGPO        DCB FOR PO               Y02072
         BZ    SOP2ENDA                 NO, BRANCH               Y02072
         MVI   DCBCNTRZ,QSAMZ           FOR EXECS TO LOAD PAM RT Y02072
SOP2ENDA EQU   *                                                 Y02072
         TM    DCBRECFM,FORMATV         VARIABLE FORMAT            DM0I
         BZ    SOP2STOP                 BRANCH NOT V FORMAT        DM0I
         TM    DCBRECFM,FORMATF         FIXED FORMAT               DM0I
         BO    SOP2STOP                 YES, BRANCH                DM0I
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,SOPSLDVT        VAR, GET 1915          @Z30TSCF
         B     RELOOP                   BACK TO MAIN LINE          DM0I
SOP2STOP EQU   *
         MVC   WTGIDTTR,SOPSLDFT        FIXED, GET 1910        @Z30TSCF
RELOOP   LA    RWTGC,WGOFF(RE,RWTGC)    INCREMENT CURR WTG ENTRY
         LA    RPARC,PLOFF(NULL,RPARC)  INCR CURRENT DCB ENTRY PRTR
         CLC   NULL(L'AMIDCNST,RWTGC),AMIDCNST  THIS RT NEEDED AGAIN
         BCR   RWTGC,RBASE              BACK TO OPEN CONTROL ROUTINDM0I
*
         CLC   NULL(L'OPIDCNST,RWTGC),OPIDCNST  END OF TABLE
         BNE   RELOOP                   NO,CHECK NEXT ENTRY
*
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(NULL,RWTG)   REINITIALIZE WTG LIST PTR
ZCHEK    CLI   NULL(RWTGC),NULL         IS THIS ENTRY COMPLETE
         BNE   TCTLRTN                  IF NOT TRANSFER CONTROL
*
         LA    RWTGC,WGOFF(NULL,RWTGC)  GET NEXT ENTRY
         LA    RPARC,PLOFF(NULL,RPARC)
         B     ZCHEK                    TO ZERO CHECK              DM0I
*
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
**********************************************************************
*                                                                    *
BUFERR   EQU   *                                                  M1552
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         DMABCOND OABD064,PDLOAD,RES=NO,REGSAVE=YES              S21042X
                                        CALL PROBLEM DETERMINATION     X
                                        TO ISSUE 013 ABEND
         B     TCTLRTN                  GO XCTL                  S21042
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES
OPIDCNST DC    C'0S'                    ID                         DM0I
AMIDCNST DC    C'9O'                    THIS ID
*
***********************************************************************
*      CONSTANTS AND RESERVED STORAGE.
*
*
CONONE   DC    H'1'                     CONSTANT OF ONE
TICCMD   DC    X'08'                    COMMAND CODE FOR A TIC   Y02072
RDCNTCMD DC    X'92'                    CMND CODE FOR READ COUNT Y02072
SOPSLDFT DC    C'10',VL3(IGG01910)      FIXED LEN RECORDS      @Z30TSCF
SOPSLDVT DC    C'15',VL3(IGG01915)      VARIABLE LEN RECORDS   @Z30TSCF
PDLOAD   DC    C'6M',VL3(IGG0196M)      PROBLEM DETERMINATION  @Z30TSCF
         SPACE 2
         DS    0H                       ALIGN PATCH AREA TO H.W. Y02072
PATCH    DC    50X'0'                   PATCH AREA               YM4640
END      EQU   *                        END OF MODULE            Y02072
         EJECT
CVT      DSECT                                                 @Z30TSCF
         CVT                                                   @Z30TSCF
         EJECT
         IHACCW  DSECT=YES                                       Y02072
         EJECT
         IGGBCB  TYPE=SAM
         EJECT
CPDEF    DSECT                          OFFSET READ CHANNEL PROG
CPSETSEC DS    0D                       SET SECTOR CCW           Y02072
CPSCH    DS    D                        SEARCH ID EQUAL
CPTIC1   DS    D                        TIC TO SEARCH
CPTIC2   DS    D                        TIC TO READ COUNT
CPRDKD   DS    0D                       READ (KEY) DATA
RKDCC    DS    0X                       COMMAND CODE
RKDDATAD DS    A                        DATA ADDR
RKDFLAGS DS    BL1                      FLAGS
         DS    1X                       NOT USED
RKDBYTCT DS    2X                       BYTE COUNT
CPRDCNT  DS    D                        READ COUNT CCW           Y02072
CPRDSEC  DS    0D                       READ SECTOR CCW          Y02072
CPCOUNT1 DS    D                        FIRST COUNT FIELD        Y02072
CPCOUNT2 DS    D                        SECOND COUNT FIELD       Y02072
           DCBD  DSORG=PS
DCBCNTRZ EQU   DCBCNTRL+1                                          DM0I
DCBCNTRX EQU   DCBCNTRL+3                                          DM0I
         EJECT
         TITLE 'IGG0199O--FORMAT OF GOTTEN CORE'
*
*
         IECDSECS (MAIN,(IOB,NO)),IOB,WTG,PREFX,EXPAND=YES     @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MODS ID             Y02072
         EJECT
FORCORE  DSECT
         IHAFCAUD ORG=YES
           END
