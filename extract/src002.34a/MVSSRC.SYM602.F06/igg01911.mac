1911 TITLE 'IGG01911-LAST LOAD OF SAM, PAM, AND EXCP OPEN EXECUTORS'
IGG01911 CSECT
*
*MODULE NAME - IGG01911                                          Y02072
*
*DESCRIPTIVE NAME - SAM/EXCP LAST LOAD                           Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*RELEASE ACTIVITY - AS FOLLOWS:                                  Y02072
*
*          RELEASE 20 DELETIONS                                       *
*2964                                                            A36344
*2964                                                            S20038
*2964074000-074400                                               A30962
*2964030400-031000,041200,085400                                 S20201
*2964084600-084800,090600                                        M1557
*
*          RELEASE 21 DELETIONS                                       *
*0934093320-093400                                               M1630
*0934001000-001150,017550,045840-045960,093000-096100,097840-    M0474
*0934097960,108100                                               M0474
*0934003500,102480-102520,103400,105600-106400,107600-108600     S21042
*0934093400,094200,095600,096000                                 S21045
* 062000-062200                                                  A41465
*0934000950,093000,093760-093840                                 M1044
*0934093760-093800,093880-093920,094240-094400,095320            M1812
*
*        VS1 RELEASE 2 DELETIONS
*D59400                                                         XA02987
*
*        VS2 RELEASE 2 DELETIONS
*200-220,280-600,1400,1600,2800,16400-17500,41800-43600,         Y02072
*94240-94330,94480-94680,94920-95720,97600,105400,106000         Y02072
*                                                                YM4635
*                                                                YM4697
*D46000                                                          YM5778
*                                                                YM5940
*                                                                YM6506
*                                                                YM6583
*                                                                YM7595
*                                                                YM7889
*
*
*        VS2 RELEASE 3 DELETIONS/CHANGES
*D086250-086300                                                @Z30TSMI
*
*        VS2 RELEASE 04 CHANGES/DELETIONS
*A072300-072380                                                 ZA01361
*
*FUNCTION/OPERATION-
*
*    - FOR ALL DATA SETS IT WILL ISSUE THE IECRES MACRO TO GET   Y02072
*      THE USERS COPY OF THE DCB REFRESHED.                      Y02072
*    - IT SETS BITS IN THE FORCE CLOSE AUDIT TRAIL TO            YM7595
*      INDICATE TO FORCE CLOSE THAT ANY FORCE CLOSE PROCESSING   Y02072
*      SHOULD BE DONE BY THE NORMAL CLOSE EXECUTORS AND FOR      YM7595
*      TAPE AND UNIT RECORD DEVICES THAT IOB CONSTRUCTION IS     YM7595
*      COMPLETED (BUFFER ADDRESSES TO CCWS).                     YM7595
*    - FOR PRINTERS IT WILL DELETE IGGMSG01 IF IT HAS BEEN       Y02072
*      LOADED AND ZERO THE FIELD ITS ADDRESS WAS IN.             Y02072
*    - IT WILL SET THE TYPE OF RELATED REQUEST IN THE IOBS       Y02072
*    - BUFFERS ARE PRIMED FOR QSAM INPUT BY BRANCHING TO THE EOB
*      MODULE.  THE CALLING SVRB IS TESTED TO DETERMINE IF A BALR
*      (IF KEY <8) OR SYNCH (IF KEY >7) IS TAKEN, THE LATTER     Y02072
*      ALLOWING THE EOB ROUTINE TO EXECUTE IN PROBLEM PROGRAM    Y02072
*      STATE, USER KEY.                                          Y02072
*    - A GETMAIN IS ISSUED IN USER KEY TO PROVIDE THE EOB RTN    Y02072
*      WITH A SAVEAREA IN SP 230. OPENS REGISTERS ARE SAVED IN   Y02072
*      THE SVRB EXTENDED SAVEAREA.                               Y02072
*
*ENTRY POINTS: ENTERED FROM STAGE 1 AND STAGE 3 EXECUTORS BY     Y02072
*              USE OF THE XCTL MACRO.                            Y02072
*
*INPUT: SEE DESCRIPTION OF REGISTERS, DCB(USERS).
*
*OUTPUT: SEE DESCRIPTION OF REGISTERS,DCB(USERS),IOB(USERS).
*
*EXTERNAL ROUTINES - EOB ROUTINE, O/C/E RESIDENT ROUTINE         Y02072
*
*EXTERNAL REFERENCES - IGGMSG01                                  Y02072
*
*MACROS:ACTION - IECRES, DELETE, MODESET, EXCP, DMABCOND, XCTL,  Y02072
*                GETMAIN                                         Y02072
*
*MACROS:MAPPING - IECDSECS(MAIN,WTG,PREFX), DCBD, CVT, IEZDEB,   Y02072
*                 IEZIOB, IHAPSA, IKJTCB, IHARB, IGGBCB, IHACCW  Y02072
*
*EXITS-NORMAL-XCTL TO IGG0190S  (OPEN).
*
*EXITS-ERROR XCTL TO PROB DET (IGG0196M) FOR WTP AND 013 ABEND
*      INSUFFIEIENT BUFFERS AVAILABLE
*      IN BUFFER POOL TO SATISFY DCB BUFFER REQUIREMENTS          16059
*
*TABLES/WORKAREAS- WHERE TO GO TABLE (WTG)
*
*      BYTE  0-7  NAME
*      BYTE  8-10 RELATIVE DISK ADDRESS(TTR) OF FIRST RECORD
*      BYTE 11    CONCATENATION NUMBER
*      BYTE 12    ZERO
*      BYTE 13    ALIAS INDICATOR AND USER DATA FIELD DESCRIPTOR.
*                        ALIAS INDICATOR 1 BIT
*                        NUMBER OF TTRS IN USERS DATA FIELD 2 BITS
*      BYTE 14-16 TTR OF FIRST TEXT RECORD
*      BYTE 17    ZERO
*      BYTE 18-19 TTR OF NOTE LIST OR SCATTER LIST
*      BYTE 20    TRANSLATION TABLE
*      BYTE 21    ZERO OR NUMBER OF ENTRIES IN THE NOTE LIST
*      BYTE 22-23 ATTRIBUTES
*      BYTE 24-26 TOTAL CONTIGUOUS MAIN STORAGE REQUIRED FOR MODULE
*      BYTE 27-28 LENGTH OF FIRST TEXT RECORD
*      BYTE 29    LENGTH OF WTG TABLE (IN DOUBLE WORDS)
*      BYTE 30-31 PATH THROUGH LOADS OF ROUTINES
*      BYTE 32-34 IDTTR OF EXECUTOR FOR FIRST DCB
*      BYTE 35    WORKAREA ADDRESS FOR FIRST DCB
*      BYTE 36-39 TABLE OF IDTTR'S
*      BYTE 32+N(8) IDTTR OF EXECUTOR FOR  N TH DCB  (3 BYTES)
*                   WORKAREA ADDRESS FOR N TH DCB    (1 BYTE )
*                   IDTTR OF LAST ROUTINE LOAD       (3 BYTES)
*                   NOT USED                         (1 BYTE)
*
*ATTRIBUTES: REENTRANT, REUSABLE, RUNS IN SUPERVISOR STATE AND   Y02072
*            USER KEY UNLESS OTHERWISE SPECIFIED.                Y02072
*
         EJECT
***********************************************************************
*
*  REGISTER CONVENTIONS USED
*
***********************************************************************
*
RDCB     EQU   2         DCB REGISTER
RBASE    EQU   3         BASE REGISTER
RCORE    EQU   4         WORK AREA ADDRESS
RPAR     EQU   5         TOP OF PARAMETER LIST
RWTG     EQU   6         TOP OF WTG TABLE
RPARC    EQU   7         CURRENT PARAMETER
RWTGC    EQU   8         CURRENT TRANS LOAD
RTIOT    EQU   9         USED HERE AS WRK REG AND  COMM VECTOR ADDR.
RWK4     EQU   RTIOT     WORK REGISTER                           Y02072
RIOB     EQU   10        IOB REGISTER
RDEB     EQU   11        DEB ADDRESS
RB       EQU   12        WORK REG1  **
RWK2     EQU   RB        WORK REGISTER                           Y02072
RC       EQU   13        WORK REG2  **  USED IN IOB GENERATION
RSAVE    EQU   RC        REG USED TO PASS SAVE AREA TO EOB RTN   Y02072
RBUFADR  EQU   RC        ADDRESS OF CURRENT BUFFER               YM7595
RD       EQU   14        WORK REG3  **
R14      EQU   RD        PARAMETER RIGISTER                      Y02072
RWK3     EQU   RD        WORK REGISTER                           Y02072
RJ       EQU   15        WORK REG4
RE       EQU   0         WORK REG5
R0       EQU   RE        PARAMETER REGISTER                      Y02072
RF       EQU   1         WORK REG6
RWK1     EQU   RF        WORK REGISTER                           Y02072
R1       EQU   RF        PARAMETER REGISTER
*
*
PARAR    EQU   RPARC
*
***********************************************************************
*
OUTP     EQU   X'0F'
DEBOUTPT EQU   X'0F'                    PROCESSING TYPE, OUTPUT  Y02072
DEBUPDAT EQU   X'04'                    PROCESSING TYPE, UPDATE  Y02072
*                                         OR OUTIN               Y02072
DEBINOUT EQU   X'02'                    PROCESSING TYPE, INOUT   Y02072
SAVELG   EQU   72                      LENGTH OF SAVE AREA       S21045
KEYMASK  EQU   X'80'                    MASK TO DISTINGUISH      Y02072
*                                         BETWEEN USER AND       Y02072
*                                         SYSTEM KEYS            Y02072
*********************************************************************
*
*
* OFFSET FOR PARAMETER TESTS
*
ONE        EQU   X'01'
FRTYATE    EQU   48
DISPSN     EQU   X'0'
*
*
*********************************************************************
*
* OFFSETS FOR DEB REFERENCE
*
*
BIN      EQU   36
BEXT     EQU   38
*
*
*
*********************************************************************
*
* MASK'S USED TO TEST OPTION FIELDS IN DCB
*
*
*
LASTDCB    EQU   X'01'
DOUBLE     EQU   X'01'
*
*
STOWB      EQU   X'80'
WRTCK      EQU   X'80'
OVRFLO     EQU   X'20'
PCI        EQU   X'20'
MINKEY     EQU   X'01'
CONCAT     EQU   X'FF'
*
*********************************************************************
*
* MASKS FOR ACCESS METHODS IN SAM
*
QSAMB      EQU   X'01'
BSAMBIT    EQU   X'00'
PAM        EQU   X'02'
PSAM       EQU   X'42'
*
*
* MASKS FOR MACRO'S TO BE USED
*
PUTB       EQU   X'40'
READB      EQU   X'20'
CNTRLB   EQU   X'02'
*
*********************************************************************
*
* MASKS FOR DEVICES TO BE SUPPORTED
*
DABIT    EQU   X'20'
URBIT    EQU   X'40'
TAPE     EQU   X'80'                    TAPE DEVICE              S20038
*
*
*********************************************************************
*
* BYTE COUNTS AS EQUATES
*
B0       EQU   0                        BYTE COUNT OF 0          S20201
B1       EQU   1                        BYTE COUNT OF 1          S20201
B2       EQU   2                        BYTE COUNT OF 2          S20201
B3       EQU   3                        BYTE COUNT OF 3          S20201
B4       EQU   4                        BYTE COUNT OF 4          S20201
B5       EQU   5                        BYTE COUNT OF 5          S20201
B8       EQU   8                        BYTE COUNT OF 8          S20201
B12      EQU   12                       BYTE COUNT OF 12         M0474
B56      EQU   56                       BYTE COUNT OF 56         S20201
B80      EQU   80                       BYTE COUNT OF 80         S20201
*
*
*********************************************************************
*
* MASKS FOR STATUS BITS TO BE TESTED
*
*
COMPLETE   EQU   X'80'
FIRSTIOB   EQU   X'01'
*
*
*
*********************************************************************
*
* MASKS FOR BUFFER TECHNIQUE'S
*
EXCHANGB   EQU   X'10'
SIMPLEB    EQU   X'40'
*
*********************************************************************
*
*
* MODES OF OPERATION FOR QSAM
*
LOCATEB    EQU   X'08'
MOVEB      EQU   X'10'
SUBSTUTB   EQU   X'04'
*
FLGOFFST EQU   4                        OFFSET TO FLAG IN BCB    S19015
RAFLAG   EQU   X'80'                    RECORD AREA INDICATOR    S19015
SPANNED  EQU   X'08'                    SPANNED RECORDS          S19015
*
*********************************************************************
*
* OTHER MASKS                                                     OC022
*
UNUSED   EQU   X'80'                    DCB OPENED-NOT YET USED   OC022
*
*
*********************************************************************
*
*
* MASKS FOR RECORD FORMATS
*
*
STANDRDF   EQU   X'08'
FORMATU    EQU   X'C0'
FORMATF    EQU   X'80'
FORMATV    EQU   X'40'
FORMATD  EQU   X'20'                    D FORMAT RECORDS         S20038
*
*
*********************************************************************
*
*       COMMAND BYTES NEEDED
*
TIC      EQU   X'08'
NOP      EQU   X'03'
OABD066  EQU   66                                                S21042
NOPTIC   EQU   X'88'                    NOP TIC COMMAND          S20201
         EJECT
         BALR  RBASE,0
         USING SOP90,RBASE
         USING IHADCB,RDCB
         USING FORCORE,RCORE
SOP90    EQU   *
*
         B     BEGIN                                             Y02072
         DC    C'IGG01911'              MODULE NAME              Y02072
         DC    C' ZA01361'              LAST SHIP CODE          ZA01361
         DC    C'02/25/75'              LAST DATE MODIFIED      ZA01361
BEGIN    DS    0H                                                Y02072
         L     RDCB,0(RPARC)            GET DCB ADDRESS
         LA    RDCB,0(RDCB)             CLEAR HI BYTE
         L     RCORE,4(RWTGC)           GET WRK AREA ADDRESS
         L     RDEB,DCBDEBAD            GET DEB ADDR             Y02072
         USING DEBBASIC,RDEB                                     Y02072
*
* ISSUE IECRES MACRO TO GET USERS COPY OF THE DCB REFRESHED      Y02072
*
         USING WTG,RWTG                                          Y02072
         IECRES  INIT,DCBCOPY=FRWKAR,STM=(R0,R14,WTGPREFX)       Y02072
         DROP  RWTG                                              Y02072
         MVC   DEBDCBB,DXUDCBAD+1       USERS DCB ADDR TO DEB    Y02072
*
* DELETE THE MESSAGE CSECT (IGGMSG01) IF IT HAS BEEN LOADED      Y02072
*
         L     RWK1,DXMSGADR            NO, GET MSG CSECT ADDR   Y02072
         LTR   RWK1,RWK1                WAS MSG CSECT LOADED     Y02072
         BZ    SOP70                    NO, DON'T DELETE         Y02072
         DELETE  EPLOC=MSGCSECT         YES, DELETE MSG CSECT    Y02072
         SR    RWK2,RWK2                PREPARE TO ZERO FIELD    Y02072
         ST    RWK2,DXMSGADR            INDICATE MSG CSECT HAS   Y02072
*                                         BEEN DELETED           Y02072
SOP70    EQU   *                                                 Y02072
         TM    JFCBMSK4,JFCBDUMY        DUMMY DATA SET           Y02072
         BO    SOPSTOP3                 YES, RETURN TO COM OPEN  Y02072
         TM    DCBMACF1,DCBMRECP        EXCP BEING USED          Y02072
         BO    SOPSTOP3                 YES, RETURN TO COM OPEN  Y02072
         SR    RWK1,RWK1                PREPARE REGISTER         YM4697
*
*  MUST CLEAR AUDIT TRAIL TO KEEP FORCE CLOSE EXECUTOR FROM      YM4697
*  TRYING TO FREEMAIN AN AREA NOT YET GETMAINED (AREA FOR QSAM   YM4697
*  BUFFER PRIMING)                                               YM4697
*
         ST    RWK1,DXATEXC2            CLEAR IN CASE FORCE      YM4697
*                                         EXECUTOR GET CONTROL   YM4697
         OI    FCAOPEN,FCAOCOPY         INDICATE TO FORCE CLOSE  Y02072
*                                         EXEC TO USE NORMAL     Y02072
*                                         CLOSE EXECUTORS        Y02072
         EJECT
*
* SET THE TYPE OF RELEATED REQUEST (RR) FOR NORMAL SCHEDULING    Y02072
* DATA SETS WITH MORE THAN 1 IOB. THE TYPE OF RR SETTING DEFINES Y02072
* THE TIME IN THE PROCESSING OF THE CURRENT REQUEST THAT IOS CAN Y02072
* START THE NEXT RR. THIS IS CONTROLLED BY THE PROCESSING REQD   Y02072
* TO PREPARE THE NEXT RR FOR I/O INITIATION. (IE, IF CHAN END    Y02072
* APPENDAGE UPDATES THE NEXT IOB AFTER COMPLETION OF THE CURRENT Y02072
* IOB, I/O CANNOT BE STARTED FOR THE NEXT REQUEST UNTIL IOS HAS  Y02072
* RECEIVED CONTROL BACK FROM THE CHAN END APPENDAGE).            Y02072
* THE FOLLOWING MATRIX SHOWS THE TYPES OF RELATED REQUESTS.      Y02072
*
*                                _______________________________ Y02072
*                                × TYPE I × TYPE II × TYPE III × Y02072
* _______________________________×________×_________×__________× Y02072
* ×ERP PROCESSING REQD           ×        ×    X    ×          × Y02072
* ×______________________________×________×_________×__________× Y02072
* ×CE APPEND PROCESSING REQD     ×        ×    X    ×          × Y02072
* ×______________________________×________×_________×__________× Y02072
* ×CE INTERRUPT REQD             ×        ×         ×     X    × Y02072
* ×______________________________×________×_________×__________× Y02072
* ×SIO APPEND PROCESSING REQD    ×   X    ×         ×          × Y02072
* ×______________________________×________×_________×__________× Y02072
* ×EOE APPEND PROCESSING REQD    ×   X    ×         ×          × Y02072
* ×______________________________×________×_________×__________× Y02072
*
*  NOTE: THE ONLY EXCEPTION TO THE ABOVE MATRIX IS FOR MAG TAPE. Y02072
*        UNIT EXCEPTION IS PRESENTED FROM TRYING TO WRITE OVER   Y02072
*        THE REFLECTIVE MARKER OR READING A TAPE MARK. TO KEEP   Y02072
*        IOS FROM STARTING THE NEXT RR IT IS NECESSARY FOR THE   Y02072
*        ERP TO MARK THIS IN PERMANENT ERROR. SINCE ERP PROCESS- Y02072
*        ING IS AFTER CE PROCESSING (AND SAM OUTPUT DATA SETS,   Y02072
*        SAM TAPE INPUT DATA SETS DO NOT HAVE CE APPENDAGES),    Y02072
*        THE TYPE MUST BE II.                                    Y02072
*
         L     RDCB,DXUDCBAD            USERS DCB ADDR           Y02072
         MODESET  KEYADDR=DXUKEY,WORKREG=15                      Y02072
*
* TEST FOR CHAINED SCHEDULING. ONLY ONE IOB (MIOB) SO REQUEST    Y02072
* IS UNRELATED.                                                  Y02072
*
         TM    DCBCIND2,DCBCNCHS        CHAIN SCHED BEING USED   Y02072
         BZ    SOP72                    NO, TEST FOR 1 IOB       Y02072
         L     RIOB,DCBIOBAD            GET MIOB ADDRESS         Y02072
         USING IOBQSAMC,RIOB                                     Y02072
         OI    IOBFLAG1,IOBUNREL        SET UNRELATED REQ FLAG   Y02072
         DROP  RIOB                                              Y02072
         L     RIOB,DCBIOBA             GET ICB ADDRESS          Y02072
         B     SOP88                    BYPASS RR SETTING        Y02072
*
* FOR NORMAL SCHEDULING, CHECK FOR MORE THAN 1 IOB. REQUEST IS   Y02072
* UNRELATED IF ONLY ONE.                                         Y02072
*
SOP72    EQU   *                                                 Y02072
         L     RIOB,DCBIOBA             GET FIRST IOB ADDR       YM5778
         USING IOBQSAMN,RIOB                                     Y02072
         IC    RWK1,DCBBUFNO            GET NO OF IOB'S (QSAM)   Y02072
         TM    DCBCIND2,DCBCNQSM        IS THIS QSAM             Y02072
         BO    SOP75                    YES, USE BUFNO           Y02072
         IC    RWK1,DCBNCP              USE NCP FOR BSAM         Y02072
SOP75    EQU   *                                                 Y02072
         CH    RWK1,ONEBUFER            USING ONLY ONE IOB       Y02072
         BNH   SOP88                    YES, BYPASS RR SETTING   YM6506
*
* MUST COMPARE ADDRESS IN USERS APPENDAGE VECTOR TABLE AGAINST   Y02072
* ADDRESSES IN IOS'S AVT TO SEE IF AN APPENDAGE HAS BEEN LOADED. Y02072
*
         L     RWK2,CVTPTR              GET THE CVT ADDR         Y02072
         USING CVTMAP,RWK2                                       Y02072
         L     RWK2,CVTXAPG             IOS'S APPENDAGE VECT TAB Y02072
         DROP  RWK2                                              Y02072
         USING IOSAPTAB,RWK2                                     Y02072
         L     RWK3,DEBAPPAD            GET USERS APP VCT TAB    Y02072
         DROP  RDEB                                              Y02072
         USING DEBAVT,RWK3                                       Y02072
         CLC   DEBSIOA,IOSAPPND         SIO APPEND LOADED        Y02072
         BNE   SOP88                    YES, DO NOT SET FLAGS    Y02072
*                                         IN IOB FOR TYPE I REQ  Y02072
*                                         (DEFAULT IS BITS OFF)  Y02072
         CLC   DEBEOEA,IOSAPPND         EOE APPEND LOADED        Y02072
         BNE   SOP88                    YES, DO NOT SET FLAGS    Y02072
         CLC   DEBCEA,IOSAPPND          WAS A CE APP LOADED      Y02072
         BNE   SOP82                    YES, GO TO SET TYPE II   Y02072
         TM    DCBDEVT,TAPE             IS THIS MAG TAPE         Y02072
         BO    SOP82                    YES, GO SET TYPE II      Y02072
SOP81    OI    IOBFLAG2,IOBRRT3         NO, MUST BE TYPE III REQ Y02072
         L     RIOB,IOBNIOBA            GET NEXT IOB             Y02072
         BCT   RWK1,SOP81               GO TO SET IN NEXT IOB    Y02072
*                                         IF MORE                Y02072
         B     SOP88                    COMPLETED SETTNG REL REQ Y02072
SOP82    OI    IOBFLAG2,IOBRRT2         SET TYPE II REL REQ      Y02072
         L     RIOB,IOBNIOBA            GET NEXT IOB             Y02072
         BCT   RWK1,SOP82               GO TO SET IN NEXT IOB    Y02072
*                                         IF MORE                Y02072
*
* EXIT THIS MODULE IF NOT USING QSAM.                            Y02072
*
SOP88    TM    DCBCIND2,DCBCNQSM        QSAM BEING USED          Y02072
         BZ    SOPSTOP2                 NO, RETURN TO COM OPEN   Y02072
         L     RIOB,DCBIOBA             GET IOB                  Y02072
         DROP  RWK2                                              Y02072
         DROP  RWK3                                              Y02072
         DROP  RIOB                                              Y02072
         EJECT
         LR    RTIOT,RIOB               SAVE FOR WORK REGISTER
*
         XC    DCBEOBAD(8),DCBEOBAD     FORCE END OF BUFFER CONDITION
*
         SR    RB,RB
         TM    DCBDEVT,X'20'            DCB USING A D.A. DEVICE
         BC    1,SOP901                 YES BRANCH
*
         TM    DCBDEVT,X'40'            DCB USING U.R. DEVICE
         BO    SOP9X                    YES BRANCH
*
         TM    DCBCIND1,X'01'           IS EXCHANGE BUFFERING USED
         BO    SOP901                   YES. BRANCH
*
**********************************************************************
*        THIS SECTION WILL SET THE READ OR WRITE CCW'S UP WITH THE
*        BUFFER ADDRESS AND THE BLOCK SIZE FOR TAPE OR UNIT
*        RECORD EQUIPMENT
**********************************************************************
*
SOP9X    EQU   *
         IC    RB,DCBBUFNO              GET NO. OF BUFFERS TO PRIME
         LR    RDEB,RB                  COPY BUFFER NUMBER
         LR    RD,RTIOT                 SAVE ADDRESS OF FIRST IOB/ICB
SOP900A  EQU   *
*
         LA    RTIOT,32(RTIOT)          POINT TO CCW  ASSUME PCI
         TM    DCBCIND2,X'04'           PCI SCHEDULING USED
         BC    1,SOP900B                YES, BRANCH
         LA    RTIOT,8(RTIOT)           ADJUST FOR NORMAL SCHEDULING
         TM    DCBMACRF,CNTRLB          CNTRL MACRO USED FOR INPUT
         BC    14,SOP900B               NO BRANCH
         TM    DCBDEVT,X'40'        DCB USING U.R. DEVICE
         BC    14,SOP900B          NO BRANCH
         LA    RTIOT,8(RTIOT)           ADJUST CHANNEL PROGRAM ADDRESS
*
SOP900B  EQU   *
*
         L     RWK1,DCBBUFCB            ADDR OF BUF CNTRL BLK    YM7595
         USING BCBLK,RWK1
         L     RBUFADR,BCBBUFPT         ADDR OF NEXT AVAIL BUF   YM7595
         LTR   RBUFADR,RBUFADR          VALID BUFFER ADDRESS     YM7595
         BE    BUFERR                   NO, BRANCH TO ABEND       16059
         USING BUFFER,RBUFADR
         MVC   BCBBUFAD,BUFNXPTB        UPDATE TO NEXT AVAIL BUF YM7595
         USING CCW,RTIOT
         STCM  RBUFADR,B'0111',CCWADDRB  BUFFER ADDR TO CCW      YM7595
         MVC   CCWBYTE,DCBBLKSI         BYTE COUNT TO CCW        YM7595
         DROP  RWK1,RBUFADR,RTIOT
         L     RTIOT,0(RD)              GET THE NEXT IOB TO INITIALIZE
         LR    RD,RTIOT                 UPDATE IOB POINTER
         BCT   RB,SOP900A               IF MORE IOBS/ICBS CONTINUE.
         SPACE
         MODESET  EXTKEY=DATAMGT
         SPACE
         OI    FCAOPEN2,FCAOIOBC        INDIC BUFFERS CAN BE     YM7595
*                                         RETURNED TO BUF POOL   YM7595
         SPACE
         MODESET  KEYADDR=DXUKEY,WORKREG=1
         SPACE
*
SOP901   EQU   *
*
*
*
SOP902   EQU   *
*
         TM    0(RPARC),OUTP            FILE FOR OUTPUT ONLY
         BC    14,SOP908                NO BRANCH
*
         OI    DCBIOBA,X'42'           TURN ON OUTPUT BIT
         IC    RB,DCBOFFSW              GET WRITE CCW OFFSET
         TM    DCBCIND1,X'01'           EXCHANGE BUFFERING USED
         BC    8,SOP903                 NO BRANCH
*
         LA    RD,0(RB,RIOB)            POINT TO WRITE CCW
         LH    RC,DCBEROPT+4            GET BLKNG FACTOR
         SLL   RC,3                     MULT. BY '8'
         AR    RC,RD                    POINT TO LAST WRT CCW
         ST    RC,DCBLCCW               SET TO LAST CCW         XA02987
         ST    RC,DCBCCCW               FORCE END OF BLOCK COND XA02987
         B     SOP905
*
SOP903   EQU   *
*
         AR    RB,RIOB                  GET TO WRITE CCW
         L     RB,0(RB)                 GET BUFFER ADDRESS FROM CCW
         LA    RB,0(RB)                HI BYTE CLEARED
*
         LH    RC,DCBBLKSI              GET BLOCK SIZE
         AR    RC,RB                   CCOMPUTE EOB ADDRESS
         ST    RC,DCBEOBAD              STORE AS EOB ADDRESS
*
         TM    DCBRECFM,FORMATU         U RECORDS USED           A41465
         BC    1,SOP904                 YES, BRANCH              A41465
         TM    DCBDEVT,TAPE             TAPE DEVICE              S20038
         BZ    SOP90300                 NO, BRANCH               S20038
         TM    DCBRECFM,FORMATD         D FORMAT RECORDS         S20038
         BO    SOP903A                  YES, BRANCH              S20038
SOP90300 EQU   *                                                 S20038
*
         TM    DCBRECFM,FORMATV         V RECS USED
         BC    8,SOP904                 NO BRANCH
*
SOP903A  EQU   *                                                 S20038
         XC    0(8,RB),0(RB)            ZERO SMALL AND LARGE LL FIELDS
         MVI   1(RB),X'04'             INIT LARGE LL TO FOUR
         LA    RB,4(RB)
         L     RC,DCBBUFCB              GET BUFCB ADDRESS        S19015
         LA    RC,DISPSN(RC)            CLEAR HI ORDER BYTE      S19015
         TM    FLGOFFST(RC),RAFLAG      RECORD AREA PRESENT      S19015
         BNO   SOP904                   NO,GOTO STORE RECAD      S19015
         TM    DCBRECFM,SPANNED         TEST IF SPANNED RECORD   S19015
*                                            FORMAT
         BNO   SOP904                   NO,GOTO STORE RECAD      S19015
*
         O     RB,PRIME                 SET INDICATOR FOR 1ST    S19015
*                                       ENTRY                    S19015
*
SOP904   EQU   *
         ST    RB,DCBRECAD             STORE FIRST RECORD ADDRESS
*
SOP905   EQU   *
*
***********************************************************************
*
*   SET UP FOR PUNCH RETRY OPTION 3 OR MORE BUFFERS
*
***********************************************************************
*
         CLI   DCBDEVT,X'45'            DEVICE A 2520?           A36344
         BE    SOP9051                                           A36344
         CLI   DCBDEVT,X'42'            THIS FOR THE 2540 PUNCH
         BC    7,SOPSTOP1               NO, BRANCH
SOP9051  EQU   *                                                 A36344
         OI    DCBCIND1,UNUSED          SET BIT TO SHOW DCB NOT   OC022
*                                       YET USED                  OC022
         CLI   DCBBUFNO,X'03'           3 IOBS OR MORE
         BC    4,SOPSTOP1               NO,BRAMCH
         TM    DCBOPTCD,PCI             PCI SCHEDULING USED
         BC    1,SOPSTOP1               YES BRANCH
*
         SR    RF,RF                                               9656
         IC    RF,DCBBUFNO             BUFNO = NUMBER OF IOB'S.    9656
         LR    RC,RIOB                  COPY FIRST IOB ADDRESS
         L     RIOB,0(RIOB)             GET TO NEXT IOB
         L     RIOB,0(RIOB)            LINK TO NEXT IOB.           9656
*
*
SOP9063  EQU   *
*
         LA    RD,4(RC)                 GET IOBS ECB ADDRESS
         ST    RD,12(RIOB)              STORE ECB ADDRESS IN CURR IOB
         OI    9(RIOB),X'01'            SET PUNCH RETRY FLG FOR IOS
         L     RIOB,0(RIOB)             UPDATE CURR IOB POINTER
         L     RC,0(RC)                 UPDATE TO CURR ECB ADDRESS
         BCT   RF,SOP9063               PROCESS ALL IOBS
         B     SOPSTOP1                 YES
*
SOP908   EQU   *
*
         TM    DCBRECFM,DCBRECV+DCBRECSB DATA SET VARIABLE SPAN ZA01361
         BNO   NOTVS                    NO, THEN BRANCH         ZA01361
         XC    DCBPRECL(1),DCBPRECL     ZERO BYTE FOR FIRST PASSZA01361
*                                       THROUGH GET PROCESSING  ZA01361
NOTVS    EQU   *                                                ZA01361
         TM    0(RPARC),X'03'           DCB FOR BACKWARDS INPUT
         BC    9,SOP909                 NO BRANCH
*
*
         OI    DCBOFLGS,X'40'  SET BIT ON BEFORE PRIME BUFFERS    0279
         LR    RB,RIOB                  COPY IOB ADDRESS
         LA    RC,1                     GET A CONSTANT OF ONE
         LNR   RC,RC                    NEGATE ONE
*
SOP908A  EQU   *                                                 A30962
         TM    DCBCIND2,X'04'  CHAIN SCHEDULING SUPPORTED        A30962
         BC    8,SOP908B  NO - BRANCH                            A30962
         STH   RC,X'1C'(RB)  STORE AS INCREMENT COUNT - ICB      A30962
         BC    15,SOP908C                                        A30962
SOP908B  EQU   *                                                 A30962
         STH   RC,X'24'(RB)  STORE AS INCREMENT COUNT - IOB      A30962
SOP908C  EQU   *                                                 A30962
         LR    RF,RB                    COPY IOB ADDRESS
         SR    RE,RE
         IC    RE,DCBOFFSR              GET READ CCW OFFSET
         ALR   RF,RE                    POINT TO READ CCW
         LR    RD,RF                    COPY CCW ADDRESS
         L     RF,0(RF)                 GET BUFFER ADDRESS FROM CCW
         AH    RF,DCBBLKSI              ADD BLOCK SIZE TO ADDRESS
         BCTR  RF,0                     REDUCE ADDRESS BY ONE
         ST    RF,0(RD)                 RESTORE BUFFR ADDRESS TO CCW
         MVI   0(RD),X'0C'              READ BACK CMD BYTE
         L     RB,0(RB)                 GET NEXT IOB
         BCT   RDEB,SOP908A             BRANCH IF MORE IOB/ICB'S
*
SOP909   EQU   *
*
         TM    DCBCIND2,X'04'           PCI SCHEDULING USED
         BC    8,SOPPRIME               NO BRANCH
*
*      THIS SECTION WILL INITIALIZE THE ICBS FOR PRIMING
*
*
         SR    RB,RB
         IC    RB,DCBBUFNO              GET NUMBER OF BUFFERS
         LR    RTIOT,RB                 COPY NUMBER OF BUFFERS
         BCTR  RTIOT,0                  SUBTRACT 1 FROM BUFNO FOR LOOP
         L     RD,DCBIOBAD              GET MAIN IOB ADDRESS
         L     RC,0(RIOB)               GET SECOND ICB
         MVC   9(3,RD),1(RIOB)          PUT 2ND ICB IN AS '1ST IOB'
         LA    RC,32(RC)                ADD BASIC ICB SIZE
         TM    DCBDEVT,DABIT            DCB USING DA DEVICE
         BC    8,SOP90900               NO  BRANCH
*
         LA    RC,8(RC)                 ADD '8' TO SKIP MBB---R FLD
         TM    JFCBFLG2,JFCNRPS         ANY DEVICES NON-RPS      YM4635
         BO    NOTRRA                   YES, BRANCH              YM4635
         ST    RC,B80(RD)               PUT ICB CHP ADDR IN RCD  S20201
*                                       READY TIC
         MVI   B80(RD),TIC              RESTORE TIC CMND         S20201
         B     SOP90901                 GO AROUND NEXT           S20201
NOTRRA   EQU   *                                                 S20201
         ST    RC,72(RD)                STORE CP STARTAD IN MIOBS TIC
         MVI   72(RD),TIC               RESTORE TIC CMD BYTE
         B     SOP90901
*
SOP90900 EQU   *
*
         ST    RC,32(RD)                STORE CP STARTAD IN M-IOB SLOT
*
SOP90901 EQU   *
         L     RIOB,0(RIOB)             LINK TO NEXT ICB
         MVI   4(RIOB),X'00'
         IC    RB,2(RD)                 PICK UP INPUT NOP OFFSET
         LA    RC,0(RB,RIOB)            POINT TO NOP CCW
         TM    JFCBFLG2,JFCNRPS         ANY DEVICES NON-RPS      YM4635
         BO    NOTRRE                   YES, BRANCH              YM4635
         MVC   B1(B3,RC),B5(RC)         MOVE ADDR FROM RH OF     S20201
*                                       TIC/NOP OF RCD READY
*
NOTRRE   EQU   *                                                 S20201
*
         MVI   0(RC),TIC                STORE TIC CMD BYTE
*
         BCT   RTIOT,SOP90901           IF MORE ICBS RELOOP
*
         TM    DCBMACRF,X'15'          USING DATA, MOVE, OR      M1557?
*                                       SUBST MODE               M1557
         BC    5,SOP90903              YES,  BRANCH AROUND       M1557
*                                       LOCATE                   M1557
         MVI   0(RC),NOP                RESTORE NOP CMD IN LAST ICB
         ST    RC,12(RD)                STORE LAST NOP ADDR IN MAINIOB
*
         TM    JFCBFLG2,JFCNRPS         ANY DEVICES NON-RPS      YM4635
         BO    NOTRRG                   YES, BRANCH              YM4635
         LA    RB,B8(RC)                GET RD SECTOR CCW ADDR   S20201
         ST    RB,B0(RC)                PUT IN NOP/TIC CCW       S20201
         MVI   B0(RC),NOPTIC            PUT IN TIC CMND          S20201
NOTRRG   EQU   *                                                 S20201
         B     SOP90902
SOP90903   EQU  *
         L     RIOB,0(RIOB)
         MVI   4(RIOB),X'00'
**********************************************************************
*
*      PRIME ICBS BY SCHEDULING THE MAIN IOB FOR EXECUTION
*
**********************************************************************
*
SOP90902 EQU   *
*
**********************************************************************
         MVI   12(RD),0                 CLEAR EXCP NEEDED FLAG @Z30TSMI
         EXCP  16(RD)
**********************************************************************
*
         B     SOPSTOP1                 GO TO XCTL               Y02072
SOPPRIME EQU   *
         SR    RB,RB                    CLEAR REG
         IC    RB,DCBBUFNO              NO. OF IOBS TO PRIME
*
         TM    DCBMACRF,X'15'           USING DATA, MOVE, OR      M1557
*                                         SUBST MODE              M1557
         BC    5,SOP90A                 YES BRANCH
*
         BCTR  RB,0                     REDUCE BY ONE
         LTR   RB,RB                    MORE THAN 1 IOB
         BZ    SOPSTOP1                 NO BRANCH                Y02072
*
SOP90A   EQU   *
*
* MUST ISSUE A GETMAIN FOR A REG SAVE AREA FOR EOB ROUTINE       Y02072
* IN USER KEY TO PREVENT PGM CHECKS. IF THE LENGTH FOR THIS      YM4697
* GETMAIN IS CHANGED, THE FREEMIAN IN IGG020T1 (SAM/PAM/DAM      YM4697
* FORCE CLOSE EXECUTOR) MUST ALSO BE CHANGED.                    YM4697
*
         GETMAIN  R,LV=SAVELG,SP=230  ISSUE GETMAIN              Y02072
*
         SPACE
         MODESET  EXTKEY=DATAMGT        KEY OF AUDIT TRAIL       YM4697
         SPACE
         ST    R1,DXATEXC2              SAVE ADDR IN CASE FORCE  YM4697
*                                         CLO EXEC MUST FREEMAIN YM4697
         SPACE
         MODESET  KEYADDR=DXUKEY,WORKREG=13  RESUME USER KEY     YM4697
         SPACE
         LR    RSAVE,R1                 PREPARE FOR EOB ROUTINE  Y02072
         L     RIOB,0(RIOB)             GET NEXT IOB
*
* THIS ROUTINE TESTS THE CALLING RB TO DETERMINE WHETHER TO BALR
* (IF KEY <8) OR SYNCH (IF KEY >7) TO EOB ROUTINE. OPENS         Y02072
* REGISTERS 2-12 ARE SAVED IN THE EXTENDED SVRB SAVEAREA.        Y02072
*
SOP92    EQU   *                                                 M0474
         ST    RIOB,DCBIOBA             ST IOB PTR IN DCB        M0474
         USING PSA,0                                             Y02072
         L     RWK3,PSATOLD             GET CURRENT TCB ADDR     Y02072
         DROP  0                                                 Y02072
         USING TCB,RWK3                                          Y02072
         L     RWK3,TCBRBP              NEED RB ADDR FOR SAVE    Y02072
*                                         AREA                   Y02072
         DROP  RWK3                                              Y02072
         USING RBSECT,RD                                         M0474
         SPACE 2
         MODESET  EXTKEY=SUPR           KEY 0 TO STORE IN RB     Y02072
         SPACE 2
         STM   RDCB,RB,RBEXSAVE+B4      SAVE OPEN REG 2-12 IN    M0474
*                                       SVRB+4                   M0474
*                                       THE 1ST WORD OF SAVEAREA IS
*                                       PARAMETER LIST IF CONCATENA-
*                                       TION OF UNLIKE ATTRIBUTES
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=7  BACK TO USER KEY     Y02072
         SPACE 2
         SR   RPARC,RPARC               ZERO R7                  M1630
         IC   RPARC,DCBOFFSR            INSERT CP OFFSET IN REG  M1630
*                                         FOR EOB                M1630
         TM    DXUKEY,KEYMASK           IS RB IN USER KEY        Y02072
         BO    PRB                      IF YES, BRANCH TO SYNCH  Y02072
         L     RJ,DCBEOB                LOAD PTR TO EOB MODULE   M0474
         LR    RBASE,RIOB               SET UP IOB PTR IN R3 FOR M0474
*                                       EOB                      M0474
         BALR  RD,RJ                    BR TO EOB                M0474
         USING *,RD                                              Y02072
         B     LOADREG                  BR TO LOAD ALL REGISTERS M1812
         DROP  RD                                                Y02072
         USING SOP90,RBASE                                       Y02072
PRB      EQU   *                                                 M0474
         L     RDCB,DXPDCBAD            USE PROTECTD COPY OF THE Y02072
*                                         DCB FOR FOLLOWING INST Y02072
         L     RJ,DCBEOB                LOAD EOB ADDRESS         M0474
         L     RDCB,DXUDCBAD            GET USERS COPY AGAIN     Y02072
         LR    RBASE,RIOB               PUT IOB IN R3 FOR EOB    M0474
         SYNCH (RJ)                     SYNCH TO EOB             M0474
         USING PSA,0                                             Y02072
LOADREG  L     RWK3,PSATOLD             GET CURRENT TCB ADDR     Y02072
         DROP  0                                                 Y02072
         USING TCB,RWK3                                          Y02072
         L     RWK3,TCBRBP              GET PTR TO SVRB          Y02072
         DROP  RWK3                                              Y02072
         USING RBSECT,RWK3                                       Y02072
         LM    RDCB,RB,RBEXSAVE+B4      RESTORE OPEN REGISTERS   M0474
*                                       2-12                     M0474
         DROP  RD                                                M0474
         L     RIOB,0(RIOB)             GET NEXT IOB
         BCT   RB,SOP92                 MORE TO PRIME  BRANCH
*
         LR    R1,RSAVE                 BEGINNING ADDR TO FREE   Y02072
*
         FREEMAIN  R,A=(1),SP=230,LV=SAVELG  FREEMAIN SAVEAREA   Y02072
         SPACE
         MODESET  EXTKEY=DATAMGT        KEY OF AUDIT TRAIL       YM4697
         SPACE
         SR    RWK3,RWK3                PREPARE REGISTER         YM4697
         ST    RWK3,DXATEXC2            TO KEEP FORCE CLOSE EXEC YM4697
*                                         FROM TRYING TO FREE    YM4697
*                                         AN AREA ALREADY FREED  YM4697
         SPACE
         MODESET  KEYADDR=DXUKEY,WORKREG=14  RESUME USER KEY     YM4697
         SPACE
*
         ST    RTIOT,DCBIOBA           RESET DCB TO ORIGINAL IOB.
*                                      NEXT FUNCTION
SOPSTOP1 EQU   *
         NI    DCBIOBA,X'FE'            TURN OFF FIRST IOB BIT.
*
SOPSTOP2 EQU   *                        COME HERE TO MODESET     Y02072
         MODESET  EXTKEY=DATAMGT        DATA MGT KEY TO XCTL     Y02072
SOPSTOP3 EQU   *                        COME HERE IF IN KEY 5    Y02072
         MVI   0(RWTGC),X'00'           CLEAR WTG ENTRY
*
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURR WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCR CURRENT DCB ENTRY PRTR
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   8,RBASE
*
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BC    7,RELOOP                 NO,CHECK NEXT ENTRY
*
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      REINITIALIZE WTG LIST PTR
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN                IF NOT TRANSFER CONTROL
*
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         BC    15,ZCHEK
*
*                                                                 16059
BUFERR   EQU   *                                                  16059
         SPACE
         MODESET  EXTKEY=DATAMGT        KEY OF OPEN WORKAREA     YM5940
         SPACE
         DMABCOND OABD066,PDLOAD,RETURN=NONE,RES=NO,REGSAVE=YES  S21042X
                                        CALL PROBLEM DETERMINATION
TCTLRTN  EQU   *
         USING WTG,RWTG
         USING WTGENTRY,RWTGC
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTG,RWTGC                                      @Z30TSCF
**
*
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES
         DS    0F                       FULL WORD ALIGNMENT      S19015
PRIME    DC    X'40000000'              1ST ENTRY INDICATOR      S19015
OPIDCNST DC    C'0S'
AMIDCNST DC    C'11'
PDLOAD   DC    C'6M',VL3(IGG0196M)      PROBLEM DETERMINATION  @Z30TSCF
MSGCSECT DC    CL8'IGGMSG01'            NAME OF MSG CSECT        Y02072
ONEBUFER DC    H'1'                     FOR TESTING FOR ONE BUF  Y02072
PATCH    DC    0H'0',54X'00'            PATCH AREA               Y02072
END      EQU   *                        END OF MODULE            Y02072
         SPACE 4
IOSAPTAB DSECT                          FOR IOS'S AVT <- CVT     Y02072
IOSAPPND DS    F                        FOR ADDRESSABILITY TO    Y02072
*                                         BRANCH BACK INST       Y02072
         TITLE 'CHANNEL COMMAND WORK DSECT'                      Y02072
         IHACCW                                                  Y02072
         TITLE 'BUFFER CONTROL BLOCK DSECT'                      Y02072
         IGGBCB  TYPE=SAM                                        Y02072
         TITLE 'IGG01911 - DCB DSECT'                            Y02072
         DCBD  DSORG=PS                                          Y02072
         TITLE 'IGG01911 - FORMAT OF OPEN WORKAREA'              Y02072
         IECDSECS  (MAIN,(IOB,NO)),WTG,PREFX,EXPAND=YES          Y02072
FORCORE  DSECT                                                   Y02072
         ORG   JFCBMASK                                          Y02072
         DS    4B                       FILLER                   Y02072
JFCBMSK4 DS    B                        JFCBMASK + 4             Y02072
JFCBDUMY EQU   X'20'                    INDICATES DD DUMMY       Y02072
         TITLE 'IGG01911 - FORCE CLOSE AUDIT TRAIL DSECT'
         IHAFCAUD  ORG=YES                                       Y02072
         TITLE 'IGG01911 - DEB DSECT'                            Y02072
         IEZDEB                                                  Y02072
         TITLE 'IGG01911 - IOB DSECT'                            Y02072
         IEZIOB                                                  Y02072
         TITLE 'IGG01911 - PSA DSECT'                            Y02072
         IHAPSA                                                  Y02072
         TITLE 'IGG01911 - TCB DSECT'                            Y02072
         IKJTCB                                                  Y02072
         TITLE 'IGG01911 - RB DSECT'                             Y02072
         IHARB                                                   Y02072
         TITLE 'IGG01911 - CVT DSECT'                            Y02072
CVT      DSECT                                                   Y02072
         CVT                                                     Y02072
         END
