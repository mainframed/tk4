         TITLE 'IGG0191T - UCS LOAD,FCB PATH DETERMINER'         M0191
IGG0191T CSECT
*
*MODULE NAME - IGG0191T                                          Y02072
*
*DESCRIPTIVE NAME - UCSB/FCB PATH DETERMINER                     Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS                                    Y02072
*          RELEASE 19 DELETIONS
*                                                                M4179
*          RELEASE 20 DELETIONS
*1355183000,186000,188000,344600-346200                          A33665
*1355020000-023000,027000-028000,041000,045000-048000,052000,    S20202
*1355242000,302000-306000,322000,328000,350000-358000,415000-    S20202
*1355418000,431000,455000-531000,536000,552000,708000,714000-    S20202
*1355717000                                                      S20202
*1355255000                                                      M5143
*          RELEASE 21 DELETIONS
*0791276005-276015,287600,566360                                 M0151
*0791046400,141001-141002,566480-56662                           M1830
*0791552080-552110,552140-552200,552260-552290,552320-552350,    M0009
*0791552380-552410                                               M0009
*0791                                                            S21092
*0791                                                             M0191
*0791001200,001400,001500,004000,004500,004600,004700,004800,    A50376
*0791008000,009000,010000,011000,012000,113200,622050,708000,    A50376
*0791709000,710000,711000,712000,713000,713800,714600,715400,    A50376
*0791716200,717000,718000,719000,720000                          A50376
*
*        VS2    RELEASE 01 DELETIONS
*552440,552470                                                   YM3213
*
*        VS2 RELEASE 02 DELETIONS
*001010,011100,011320,012000,058000,113200-113600,158000,169500, Y02072
*170500-171000,219300-219500,276415-276430,341000-343800,        Y02072
*356000-361000,579000,606070-606980,622050-622600,693000-697600, Y02072
*699500,707600,733000-734000,746000,747000,748000-785100,785200  Y02072
*063000,641000,673000,677000,580000,692000                       Y02072
*                                                                YM4697
*D238000-241000                                                  YM6054
*                                                                YM7889
*
*        VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*
*STATUS CHANGE LEVEL 009
*
*FUNCTION/OPERATION - EXECUTE BLOCK DATA CHECK OR RESET BLOCK DATA
*                     CHECK
*
*                   - LOAD MSG CSECT FOR WTO'S/WTOR'S            Y02072
*
*                   - FOR PRINTERS WITH UCS:                     Y02072
*                     1) ISSUE IEC123D TO REQUEST OPERATOR TO    Y02072
*                        TO SPECIFY A UCS IMAGE TO BE USED IF    Y02072
*                        UCS IS NOT SPECIFIED IN THE DD CARD AND Y02072
*                        CURRENT IMAGE IS NOT A DEFAULT OR NO    Y02072
*                        IMAGE IS CURRENTLY LOADED.              Y02072
*                     2) SET SWITCHES TO INDICATE TO SUBSEQUENT  Y02072
*                        OPEN EXECUTORS WHAT WORK IS TO BE DONE  Y02072
*                   - FOR PRINTERS WITH FCB'S BUT NOT UCSB'S:    Y02072
*                     1) ISSUE IEC129D TO REQUEST OPERATOR TO    Y02072
*                        SPECIFY AN FCB IMAGE TO BE USED IF A    Y02072
*                        FCB IMAGE IS NOT SPECIFIED IN THE DD    Y02072
*                        CARD OR THE CURRENT IMAGE IS NOT A      Y02072
*                        DEFAULT.                                Y02072
*                   - FOR PRINTERS WITH FCB'S AND UCSB'S:        Y02072
*                     1) SET SWITCHES TO INDICATE TO SUBSEQUENT  Y02072
*                        OPEN EXECUTORS WHAT WORK IS TO BE DONE  Y02072
*
*                   _ ISSUES SVC 105 TO BUILD A DCB AND DEB FOR
*                     SYS1.IMGLIB AND SETS AN AUDIT TRAIL BIT    YM4697
*                     INDICATING TO FORCE CLOSE THAT             YM4697
*                     SYS1.IMAGELIB HAS BEEN OPENED              YM4697
*
*ENTRY POINT - ENTERED FROM THE FOLLOWING EXECUTORS BY USE OF THE XCTL
*              MACRO
*              *FROM IGG0196A IF DCB IS EXCP THAT'S BEING OPENED TO
*               A 1403 WITH UCS OR A 3211 PRINTER
*              *FROM IGG0196B IF DCB IS BEING OPENED TO A 1403 WITH
*               UCS OR 3211 PRINTER
*              *FROM 6A OR 6B IF A 2245 PRINTER.
*
*INPUT - DCB ADDRESS
*      - OPEN WORK AREA ADDRESS
*      - WHERE-TO-GO TABLE
*      - PARAMETER LIST
*      - CURRENT ENTRY IN W-T-G TABLE
*      - CURRENT PARAMETER(DCB)
*
*OUTPUT - IF NO UCS IMAGE IS SPECIFIED AND THE UCS IMAGE LOADED BY
*         PREVIOUS JOB STEP WAS NOT A DEFAULT IMAGE,THE MESSAGE
*         'IEC123D XXX SPECIFY UCS PARAMETER' IS DISPLAYED.
*       - IF A UCS IMAGE IS NOT TO BE LOADED, THE MESSAGE        Y02072
*         'IEC129D XXX,SPECIFY FCB PARAMETERS' IS DISPLAYED      Y02072
*
*EXTERNAL ROUTINES _ A SVC 105 IS ISSUED TO CONSTRUCT A DCB AND A DEB
*                    FOR SYS1.IMAGELIB SO A UCS AND/OR A FCB IMAGE
*                    CAN TO BE LOADED INTO CORE
*
*EXTERNAL REFERENCES - MESSAGE CSECT FOR WTO/WTOR'S              Y02072
*
*EXIT - NORMAL - FOR PRINTERS WITH UCSB'S, XCTL TO IGG0191U      Y02072
*              - FOR PRINTERS WITHOUT UCSB'S, XCTL TO IGG0197E   Y02072
*              - FOR COMPOSIT CONSOLES, XCTL TO IGG0191V TO      Y02072
*                DETERMINE 2ND STAGE EXECUTOR.                   Y02072
*
*EXIT - ERROR - IF AN ERROR OCCURED WHILE DCB AND DEB FOR SYS1.IMGLIB
*               WERE BEING CONSTRUCTED AN ABEND RETURN CODE IS SET
*               AND AN EXIT IS MADE TO IGG0191V TO ABEND
*             - IF EXCP IS UNSUCCESSFUL WHEN TRYING TO SET LATCH Y02072
*               FOR BLK/UNBLK OF DATA CHECKS AN EXIT TO IGG0191V Y02072
*               IS MADE TO GET A WTP.                            Y02072
*
*MACROS-ACTION:MODESET, EXCP, WAIT, XCTL                         Y02072
*
*MACROS-MAPPING:IKJTCB, IEZDEB, IHAPSA, IEFUCBOB, IGGMSG,        Y02072
*              IECDSECS(MAIN,WTG), DCBD, IGGSCW, IHAFCAUD,       Y02072
*              IEECUCM                                           YM6054
*
*TABLES/WORK AREAS - WHERE-TO-GO TABLE (SEE LISTING OF EXECUTOR
*                                       IGG0191B)
*                  - OPEN WORK AREA (SEE IECDSECT IN THIS LISTING)
*
*ATTRIBUTES - REENTRANT, REUSABLE, RUNS IN DATA MANAGEMENT KEY   Y02072
*             UNLESS OTHERWISE SPECIFIED, SUPER STATE            Y02072
*
*NOTES - EXCP/WAIT SVCS IS USED TO EXECUTE DATA CHECK COMMAND
*        DATA CHECK.
*      - WTO/WTOR SVC IS USED TO REQUEST AN OPERATOR REPLY
*      - IOB/ECB IN OPEN WORK AREA IS USED FOR EXCP
*      - IF THE PRINTER IS FOR A COMPOSITE CONSOLE,THE UCS SUPPORT IS
*        NOPED.
*      - BEFORE ISSUING A WTO/WTOR, THE MESSAGE TEXT MUST FIRST  Y02072
*        BE EXTRACTED FROM A MESSAGE CSECT.                      Y02072
*
***********************************************************************
*
         EJECT
*
**********************************************************************
*
*   REGISTER CONVENTIONS USED THROUGH OUT OPEN EXECUTORS
*
**********************************************************************
*
RE       EQU   0                        WORK/PARAMETER REGISTER
R0       EQU   RE                       INPUT TO GETMAIN, FREEMN Y02072
RWK1     EQU   RE                       WORK REGISTER            Y02072
RF       EQU   1                        WORK/PARAMETER REGISTER
R1       EQU   RF                       INPUT TO EXCP, WAIT      Y02072
RWK2     EQU   RF                       WORK REGISTER            Y02072
RDCB     EQU   2                        ADDR OF USERS DCB
RBASE    EQU   3                        BASE REGISTER
RCORE    EQU   4                        ADDR OF OPEN WORK AREA
RPAR     EQU   5                        PARAMETER LIST
RWTG     EQU   6                        START OF WTG
RPARC    EQU   7                        CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                        CURRENT ENTRY IN WTG TABLE
RTCB     EQU   9                        TCB ADDR/WORK REGISTER   Y02072
RWK3     EQU   RTCB                     WORK REGISTER            Y02072
RUCB     EQU   10                       UCB ADDR
RDEB     EQU   11                       DEB ADDR
RWK7     EQU   RDEB                     WORK REGISTER            Y02072
RB       EQU   12                       WORK AREA BASE REGISTER
RWK5     EQU   RB                       WORK REGISTER            Y02072
RC       EQU   13                       WORK REGISTER
RWK6     EQU   RC                       WORK REGISTER            Y02072
RD       EQU   14                       WORK/PARAMETER REGISTER
RWK8     EQU   RD                       WORK REGISTER            Y02072
RJ       EQU   15                       WORK/PARAMETER REGISTER
RWK4     EQU   RJ                       WORK REGISTER            Y02072
R15      EQU   RJ                       RETURN CODE REGISTER     Y02072
*
***********************************************************************
*
*   EQUATE WTG OFFSET
*
WGOFF    EQU   8                        OFFSET OF CURRENT WTG ENTRIES
PLOFF    EQU   4                        OFFSET OF CURRENT DCB ENTRIES
WAOFF    EQU   32                       OFFSET OF 1ST ENTRY IN WTG TBL
*
*   MASKS
*
TWENTY   EQU   X'14'                    X'14'
UNRLMASK EQU   X'02'                    IOB IS UNRELATED
SILIMASK EQU   X'20'                    SILI BIT IS ON
ONE      EQU   X'01'                    X'01'
BLOCKCCW EQU   X'73'                    COMMAND CODE = BLOCK DATA CHECK
UNBLKCCW EQU   X'7B'                    COMMAND CODE = UNBLK DATA CHECK
BLOCKMSK EQU   X'40'                    DCB OPTCD=U (X'40')
IEC120RQ EQU   X'40'                    IF ON, MSG IEC120 IS REQ Y02072
IEC123RQ EQU   X'80'                    IF ON, MSG IEC123 IS REQ Y02072
NULL     EQU   X'00'                    X'00'
THREE    EQU   X'03'                    X'03'
BLANK    EQU   X'40'                    X'40'
ABENDBIT EQU   X'80'                    ABEND TERMINATION IN PROCESS
PRTM3211 EQU   X'09'                    MASK FOR 3211 PRINTER    S20202
SHIFTRHT EQU   X'02'                    FLAG USED TO SET FCB     S20202
*                                       OPTIONS                  S20202
SHIFTLFT EQU   X'06'                    FLAG USED TO SET FCB     S20202
*                                       OPTIONS                  S20202
IEC129RQ EQU   X'80'                    MESSAGE IEC129 REQ'D     S20202
IMCLOSED EQU   X'FF'                    FLAG SET FOR IMAGELIB    S20202
*                                       NOT OPENED               S20202
STAGE2   EQU   X'FF'                    TO INDIC TO 191V ENTRY   Y02072
*                                         TO FIND 2ND STG EXEC   Y02072
BUFPAR   EQU   X'01'                    UCSB BUFFER PARITY ERROR Y02072
DEFALT   EQU   X'80'                    IF ON IN IMAGE, IMAGE    Y02072
*                                         IS A DEFAULT           Y02072
ABEND    EQU   X'FF'                    INDICATES TO SUBSEQUENT  Y02072
*                                         LOADS ENTRY FOR ABEND  Y02072
*
*   EQUATES FOR CONSTANTS
*
K1       EQU   1                        CONSTANT EQUATED TO 1    S20202
K7       EQU   7                        CONSTANT EQUATED TO 7    S20202
K15      EQU   15                       CONSTANT EQUATED TO 15   S20202
PRT2245  EQU   X'0B'                    MASK FOR 2245 PRINTER    S21092
*                                                                  DM0Q
*    EQUATES FOR CONSTANTS                                         DM0Q
*                                                                  DM0Q
K4       EQU   4                                                   DM0Q
OABD095  EQU   95                       INTERNAL CODE 95         M0009
EXCPFAIL EQU   100                      INTERNAL CODE FOR EXCP   Y02072
*                                         FAILED                 Y02072
*
*  THE FOLLOWING MESSAGE NUMBERS ARE EQUAL TO THEIR POSITION     Y02072
*  IN THE MESSAGE CSECT (RELATIVE TO ZERO), MULT BY 2 (LENGTH    Y02072
*  OF AN INDEX)                                                  Y02072
*
MSG123   EQU   2*2                      MESSAGE NO IN MSG CSECT  Y02072
MSG129   EQU   3*2                      MESSAGE NO IN MSG CSECT  Y02072
*
***********************************************************************
*
*
***********************************************************************
*
*
*   INITIALIZE REGISTERS
*
*
***********************************************************************
*
         BALR  RBASE,0
         USING UCSTART,RBASE
         USING IHADCB,RDCB
         USING FORCORE,RCORE
         USING DEBBASIC,RDEB                                     Y02072
         USING UCBOB,RUCB
*
***********************************************************************
*
UCSTART  EQU   *
*
***********************************************************************
*
         B     AROUND                   BR AROUND CONSTANTS      Y02072
         DC    C'IGG0191T'              MODULE NAME              Y02072
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'10/10/74'              LAST DATE MODIFIED     @Z30TSCF
AROUND   DS    0H                                                Y02072
*
         L     RDCB,0(RPARC)            GET DCB ADDR
         L     RCORE,4(RWTGC)           GET WORK AREA
         L     RDEB,DCBDEBAD            GET DEB ADDR
         L     RUCB,DEBSUCBA            GET UCB ADDR             Y02072
         DROP  RDEB                                              Y02072
*
*
*   SET FLAG TO INDICATE THAT DCB AND DEB FOR SYS1.IMAGELIB HAVE NOT
*   BEEN CONSTRUCTED
*
         XC    DXFCBUCS,DXFCBUCS        CLEAR PARM LISTS         Y02072
         MVI   DXIMGDCB,IMCLOSED        MOVE IN FLAG FOR         Y02072
*                                       IMAGELIB CLOS            S20202
         MVC   DXEROPT,DXCCW6           SAVE DCB EROPT FIELD     Y02072
*
***********************************************************************
*
WTORTN   EQU   *
*
***********************************************************************
*
*   INTIALIZE WTO/WTOR AREA IN OPEN WORK AREA
*
***********************************************************************
*
         LA    RD,RPLY                  LOAD REPLY ADDRESS       A33665
         ST    RD,REPLYLTH              STORE REPLY ADDR
         MVI   REPLYLTH,TWENTY          MOVE REPLY LENGTH=20
         LA    RD,RPLYECB               LOAD ADDR OF ECB FOR     A33665
*                                       WTOR                     A33665
         ST    RD,REPLYECB              STORE REPLY ECB ADDR
         MVC   LOMASK(8),LOWER          MOVE X'40'S TO WORK AREA A33665
*
***********************************************************************
*
*    IS DEVICE A 2245 PRINTER
*
***********************************************************************
*
         CLI   UCBTBYT4,PRT2245        IS DEVICE A 2245 PRINTER  S21092
         BE    SYSABEND                YES, CHECK FOR ABEND      S21092
*
***********************************************************************
*
BLKDATA  EQU   *
*
**********************************************************************
*
*   EXCP BLOCK/RESET BLOCK DATA CHECK ACCORDING TO DCB SPECIFICATION
*
***********************************************************************
*
         MVI   IOBFLAG1,UNRLMASK        SET UNRELATED BIT ON
         XC    DXCCW1(8),DXCCW1         RESET CCW AREA
         MVI   DXCCW1+4,SILIMASK        SET SILI FLAG ON
         MVI   DXCCW1+7,ONE             SET LENGTH=1
         MVI   DXCCW1,BLOCKCCW          COMMAND CODE = BLOCK DATA CHECK
*                                                                 20961
*   TEST IF OPTCD IN DCB SPECIFIES UCS FEATURE                    20961
*                                                                 20961
         TM    DCBMACRF,X'80'           TEST IF DCB MACRF=E       20961
         BO    EXCPGOGO                 IF YES,GOTO EXCP RTN      20961
*                                                                 20961
*
*   TEST IF OPTCD IN DCB SPECIFIES RESET BLOCK DATA CHECK
*
         TM    DCBOPTCD,BLOCKMSK        TEST IF DCB OPTCD=U
         BC    14,EXCPGOGO              IF NOT,GOTO EXCP RTN
*
         MVI   DXCCW1,UNBLKCCW          IF YES,SET CCW= UNBLK DATA CHK
*
EXCPGOGO EQU   *
*
         LA    RF,DXIOB                 LOAD IOB ADDR
         EXCP  (1)                       EXCP ON DATA CHECK
*
         WAIT  ECB=DXECB                WAIT ON EXCP
         TM    DXECB,X'20'              TEST FOR SUCCESSFUL I/O   8M226
         BO    CONSOLE                  BRANCH IF O.K.            8M226
         LA    RWK4,EXCPFAIL            INTERNAL CODE FOR PROB   Y02072
*                                         DETERMINATION          Y02072
         B     PDEXIT                   GO TO SET UP WTP         Y02072
*
***********************************************************************
*
CONSOLE  EQU   *
*
***********************************************************************
*
*   THIS ROUTINE CHECKS IF THE OPEN IS FOR A COMPOSITE CONSOLE.IF YES,
*   XCTL TO A 2ND STAGE EXECUTOR BECAUSE ANY WTO/WTOR WILL PUT   YM6054
*   THE COMMUNICATIONS TASK INTO A WAIT STATE.                   YM6054
*
***********************************************************************
*
         USING PSA,0                                             Y02072
         L     RTCB,PSATOLD             GET CURRENT TCB ADDR     Y02072
         USING TCB,RTCB                                          Y02072
         L     RWK6,CVTPTR              ADDR OF CVT              YM6054
         USING CVT,RWK6                                          YM6054
         L     RWK6,CVTCUCB             ADDR OF UCM              YM6054
         DROP  RWK6                                              YM6054
         USING UCM,RWK6                                          YM6054
         L     RWK6,UCMPXA              COMM TASKS TCB ADDR      YM6054
         CR    RWK6,RTCB                IS THIS COMM TASK        YM6054
         DROP  RWK6                                              YM6054
         BNE   SYSABEND                 NO, GO TEST FOR ABEND IN Y02072
*                                         CONTROL                Y02072
         MVI   DXSTAGE2,STAGE2          ENTRY TO XCTL TO 2ND STG Y02072
         B     XCTL191V                 GO XCTL                  Y02072
*
***********************************************************************
*
SYSABEND EQU   *
*
***********************************************************************
*
*   THIS ROUTINE CHECKS IF ABEND HAS CONTROL.IF YES,SET A SWITCH TO
*   AVOID ISSUING ANOTHER ABEND.
*
***********************************************************************
*
         TM    TCBFLGS1,TCBFA           TEST IF ABEND IS IN PROCESS
         BNO   LDMSGCST                 IF NO, GO LOAD MSG CSECT Y02072
         MVI   DXNABEND,ABENDBIT        INDIC DO NOT ABEND AGAIN Y02072
*
***********************************************************************
*
LDMSGCST EQU   *                                                 Y02072
*
***********************************************************************
*
*   UCS PARM FIELD  (DXUCSP) - BYTE 0   - DXUCSSW1               Y02072
*                                           B0 - IEC123 REQUIRED Y02072
*                                           B1 - IEC120 REQUIRED Y02072
*                              BYTE 1   - EROPT                  Y02072
*                              BYTE 2   - DXNABEND               Y02072
*                              BYTE 3   - DXUCSOPT               Y02072
*                                           B0 - NOT USED        Y02072
*                                           B1 - FOLD REQUESTED  Y02072
*                                           B3 - NOT USED        Y02072
*                                           B4 - VERIFY REQ      Y02072
*                              BYTE 4-7 - UCS ID
*
***********************************************************************
*
* LOAD THE MESSAGE CSECT FOR WTO'S/WTOR'S                        Y02072
* PASS LOAD THE ADDRESS OF LINK LIB'S DCB. THIS WILL CAUSE LPA   Y02072
* TO BE SEARCHED.                                                Y02072
*
         L     RWK2,CVTPTR              GET CVT ADDRESS          Y02072
         USING CVT,RWK2                                          Y02072
         L     R1,CVTLINK               DCB ADDR FOR LINK LIB    Y02072
         DROP  RWK2                                              Y02072
         LOAD  EPLOC=MSGCSECT,DCB=(1)   MACRO TO GET MSG CSECT   Y02072
         ST    RWK1,DXMSGADR            SAVE CSECT ADDRESS       Y02072
         L     RWK4,UCBXTADR            GET UCB EXT ADDRESS      Y02072
         USING UCBUCS,RWK4                                       Y02072
         CLI   UCBTBYT4,PRT2245        CHECK FOR 2245 PRINTER    S21092
         BE    WKARINFO                YES, SKIP UCS CODE        S21092
         MVC   DXUCSID,JFCUCSID         MOVE UCS ID IN JFCB      Y02072
         MVC   DXUCSOPT,JFCUCSOP        MOVE UCS OPTION BITS     Y02072
         CLI   UCBTBYT4,PRTM3211        CK IF DCB BEING OPENED   S20202
*                                       FOR 3211                 S20202
         BNE   TESTUCS                  NO GO CK UCS PARAMETER   Y02072
*                                       FOR 1403                 S20202
*
***********************************************************************
*
*   FCB PARM FIELD  (DXFCBP) - BYTE 0   - DXFCBSW1               Y02072
*                                           B0 - IEC129 REQUIRED Y02072
*                                           B1 - CORE TRANSFER   Y02072
*                              BYTE 1   - FCB FLAG BYTE          Y02072
*                              BYTE 2   - FLAG BYTE              Y02072
*                              BYTE 3   - DXFCBOPT               Y02072
*                                           B0 - ALIGN REQUESTED Y02072
*                                           B1 - VERIFY REQ      Y02072
*                              BYTE 4-8 - FCB IMAGE ID           Y02072
*
***********************************************************************
*
WKARINFO EQU   *                                                 S21092
*
***********************************************************************
*
         MVC   DXFCBID,JFCFCBID         MOVE FCB ID TO WKAREA    Y02072
*
*    THIS ROUTINE PUTS THE FCB OPTIONS INTO THE FCB WORKAREA FROM JFCB
*
         IC    RB,JFCUCSOP              LOAD FCB OPTIONS INTO    S20202
*                                       REG 12                   S20202
         SRL   RB,SHIFTRHT              SHIFT OUT BITS 6 & 7 OF  S20202
*                                       BYTE 3                   S20202
         SLL   RB,SHIFTLFT              SHIFT BITS 6 & 7 OF BYTE S20202
*                                       3 TO                     S20202
*                                       BITS 0 & 1 OF BYTE 3
         STC   RB,DXFCBOPT              LOAD FCB OPT INTO        Y02072
*                                       WORKAREA                 S20202
*
*
***********************************************************************
*
*   TEST IF FCB PARAMETERS ARE SPECIFIED IN DD STATEMENT FOR DCB
*   AND SET SWITCHES (DXFCBSW1) FOR FCB                          Y02072
*
***********************************************************************
*
         CLI   DXFCBID,0                CK IF FCB SPECIFIED IN   Y02072
*                                       DD ST                    S20202
         BNE   PADFCBID                 YES GO PAD FCB ID WITH   S20202
*                                       BLANKS                   S20202
*
***********************************************************************
*
*   WHEN FCB NOT SPECIFIED IN DD STATEMENT,CHECK IF CURRENT FCB IN THE
*   BUFFER IS A DEFAULT
*
***********************************************************************
*
         CLI   UCBFCBID,0               CK IF FCB ID IN UCB      Y02072
         BE    GTMSG129                 IF NOT, GO WTOR IEC129   Y02072
         TM    UCBFCBOP,DEFALT          CK IF CURRENT FCB IS A   Y02072
*                                       DEFAULT                  S20202
         BZ    GTMSG129                 NO GO TO WTOR IEC129     Y02072
         MVC   DXFCBID,UCBFCBID         SET UP FOR FCB LOAD      Y02072
         MVI   DXFCBOPT,0               CLR FLAG AS USER CANNOT  Y02072
*                                         SPEC OPTIONS IF HE     Y02072
*                                         DOES NOT SPEC IMAGE ID Y02072
         B     UCSIDTST                 GO TEST FOR UCS          S20202
*                                       PARAMETERS               S20202
*
***********************************************************************
*
*   WHEN THE FCB IMAGE IN THE BUFFER IS NOT A DEFAULT OR NO FCB IMAGE
*   LOADED REQUEST OPERATOR TO SPECIFY AN ALTERNATE BY ISSUEING A WTOR
*   'IEC129D XXX SPECIFY FCB PARAMETER'
*
***********************************************************************
*
GTMSG129 EQU   *                                                 Y02072
         OI    DXFCBSW1,IEC129RQ        SET SWITCH FOR IEC129    Y02072
*                                       REQUIRED                 S20202
         B     UCSIDTST                 GO TEST UCS PARAMETERS   S20202
WTOR129  EQU   *                                                 Y02072
         TM    DXFCBSW1,IEC129RQ        IS MSG129 REQUIRED       Y02072
         BZ    XCTL197E                 NO, XCTL TO 197E         Y02072
         LA    RWK5,MSG129              MSG ID FOR MSG ROUTINE   Y02072
         BAL   RWK6,MSGRTN              GO GET MSG TEXT FOR WTOR Y02072
*
* GO FILL IN DEVICE ADDR AND ISSUE WTOR                          Y02072
*
         BAL   RD,WTORSEQ               ISSUE WTOR               Y02072
*
*   LOAD ID OF IGG0197E AND GO ISSUE SVC 105 TO BUILD DCB AND DEB
*
XCTL197E EQU   *                                                 S20202
*
         LA    RDEB,MODNM7E             LOAD ADDR OF TTR FOR     S20202
*                                       IGG0197E                 S20202
         B     OPENIMLB                 GO OPEN SYS1.IMAGELIB    S20202
*
***********************************************************************
*
*   WHEN FCB SPECIFIED IN DD STATEMENT,PAD FCB IMAGE ID WITH BLANKS
*
***********************************************************************
*
PADFCBID EQU   *                                                 S20202
*
         LA    RB,DXFCBID+1             LOAD ADDR OF FCB ID + 1  Y02072
         BAL   RD,PADNAME               GO PAD FCB ID WITH       S20202
*                                       BLANKS                   S20202
UCSIDTST EQU   *
*
***********************************************************************
*
*   TEST IF UCS PARAMETER IS SPECIFIED IN DD STATEMENT FOR THE DCB
*
**********************************************************************
*
         CLI   UCBTBYT4,PRT2245        CHECK FOR 2245 PRINTER    S21092
         BE    WTOR129                 YES, GO TEST FOR MSG      Y02072
*                                         IEC129 REQUIRED        Y02072
TESTUCS  EQU   *                                                 Y02072
         CLI   DXUCSID,0                TEST FOR UCS IN DD STAT  Y02072
         BC    7,PADUCSID               IF YES,GOTO PAD UCS ID ROUTINE
*
NOUCSID  EQU   *
*
***********************************************************************
*
*   WHEN UCS IS NOT SPECIFIED IN DD STATEMENT, CHECK UCB TO SEE IF A
*   DEFALT UCS IMAGE IS IN THE BUFFER
*
***********************************************************************
*
         CLI   UCBUCSID,0               CK IF UCS ID IN UCB      Y02072
         BE    WTOR123                  IF NOT, GO WTOR IEC123   Y02072
         TM    UCBUCSOP,DEFALT          TEST IF UCS IMAGE IS A   Y02072
*                                       DEFAULT                  S20202
         BNO   WTOR123                  NO GO TO WTOR IEC123     S20202
         MVC   DXUCSID,UCBUCSID         MOVE UCS ID TO PARM LIST Y02072
         MVC   DXUCSOPT,UCBUCSOP        MOVE UCS OP TO PARM LIST Y02072
         NI    DXUCSOPT,X'FF'-BUFPAR-DEFALT  LEAVE LOAD MODE ON  Y02072
         B     XCTL191U                 GO XCTL TO NEXT EXEC     Y02072
*
***********************************************************************
*
WTOR123  EQU   *
*
***********************************************************************
*
*   SET SWITCH TO INDICATE ENTRY TO IGG191U WILL BE FROM IEC123
*
***********************************************************************
*
         OI    DXUCSSW1,IEC123RQ        SET SW FOR IEC123 REQ    Y02072
         LA    RD,XCTL191U              GET ADDRESS OF XCTL191U  S20202
*                                       ROUTINE TO LOAD ID OF
*                                       MODULE IGG0191U
*
***********************************************************************
*
*   WHEN THE IMAGE IN THE BUFFER IS NOT A DEFALT, REQUEST AN ALTERNATE
*   UCS IMAGE AND CHAIN TO BE MOUNTED
*   WTOR 'IEC123D SPECIFY UCS PARAMETER'
*
***********************************************************************
         LA    RWK5,MSG123              MESSAGE NO. FOR MSG RTN  Y02072
         BAL   RWK6,MSGRTN              GO GET MESSAGE           Y02072
         USING MSGENTRY,RWK2                                     Y02072
WTORSEQ  EQU   *                                                 S20202
         IC    RWK7,MSGOFF1             OFFSET TO FIRST VAR FLD  Y02072
         LA    RWK3,MSGAREA(RWK7)       ADDRESS OF FIRST VAR FLD Y02072
         MVC   0(L'UCBNAME,RWK3),UCBNAME  MOVE DEV NAME TO MSG   Y02072
         MVI   RPLY,BLANK                                        A33665
         MVC   RPLY+1(19),RPLY          CLEAR REPLY AREA         A33665
         MVI   RPLYECB,NULL             RESET REPLY ECB          A33665
         LA    RF,REPLYLTH              LOAD WTOR ADDR
*
         SVC   35                       ISSUE WTOR SVC
*
*
*
         BR    RD                       GO TO ADDRESS IN         S20202
*                                       REGISTER 14              S20202
         DROP  RWK2                                              Y02072
*
PADUCSID EQU   *
*
***********************************************************************
*
*   WHEN UCS IS SPECIFIED IN DD STATEMENT,PAD UCS IMAGE ID WITH X'40'
*
***********************************************************************
*
         LA    RD,COMPUCS               RET ADDR FROM PAD RTN    Y02072
         LA    RB,DXUCSID+1             LOAD UCS ID + 1 ADDR     Y02072
*
PADNAME  EQU   *                                                 S20202
*
*
         LA    RC,THREE                 LOAD X'03'
*
COMPBYTE EQU   *
*
         CLI   NULL(RB),NULL            TEST IF UCS IMAGE HAS X'00'
         BC    7,NEXTBYTE               IF NOT,GOTO TEST NEXT BYTE
*
         MVI   NULL(RB),BLANK           IF YES,REPLACE X'00' BY X'40'
*
NEXTBYTE EQU   *
*
         LA    RB,ONE(RB)               INCREMENT REG. BY '1'
         BCT   RC,COMPBYTE              GOTO COMPARE NEXT BYTE
         BR    RD                       BRANCH TO SET SWITCHES   S20202
*
COMPUCS  EQU   *
*
***********************************************************************
*
*   TEST IF UCS PARAMETERS IN UCB = UCS PARAMETERS IN DD STATEMENT
*
***********************************************************************
*
         CLC   UCBUCSID,DXUCSID         CK IF SPECIFIED          Y02072
*                                       UCS=CURRENT              S20202
         BE    XCTL191U                 NO IEC120 REQ IF EQ      Y02072
         OI    DXUCSSW1,IEC120RQ        SET IEC120 REQ TO GET    Y02072
*                                         THE TRAIN MOUNTED      Y02072
*
XCTL191U EQU   *
*
***********************************************************************
*
*   XCTL TO EXECUTOR IGG0191U TO LOAD IMAGE.
*
***********************************************************************
*
*
*
*
         LA    RDEB,MODNM1U             LOAD ADDR OF TTR FOR     S20202
*                                       IGG0191U                 S20202
*
*
*
OPENIMLB EQU   *                                                 S20202
*
*
         SR    RF,RF                    ZERO OUT REGISTER 1      S20202
*
*  ISSUE SVC TO OPEN SYS1.IMAGELIB
*
         SVC   105                      ISSUE SVC TO OPEN        S20202
*                                       SYS1.IMAGELI             S20202
         LTR   RF,RF                    TEST IF IMAGELIB OPENED  S20202
*                                       SUCCES                   S20202
         BE    IMAGEROR                 IF NOT GO LOAD TTR OF    S20202
*                                       OPEN MOD                 S20202
         ST    R1,DXIMGDCB              SAVE ADDR OF IMGLIB DCB  Y02072
         OI    FCAOPEN2,FCAOIMGL        INDIC IMAGELIB OPENED    YM4697
         LR    RF,RDEB                  GET ADDR OF OPEN         S20202
*                                       EXECUTOR TTR             S20202
         BC    K15,MVEIDTTR             GOTO MOVE ID TTR ROUTINE S20202
*
IMAGEROR EQU   *                                                 S20202
*
*   THIS ROUTINE CONVERTS RETURN CODE FROM IMAGELIB TO AN
*   INTERNAL CODE FOR PROBLEM DETERMINATION
*              RETURN CODE              INTERNAL CODE
*              X'04'                    95
*              X'08'                    96
*              X'0C'                    97
*
         LA    RF,OABD095-K1            PUT INTERNAL CODE IN REG M0009
         SRL   RJ,2                     DIVIDE RTN CODE BY 4     YM3213
         AR    RJ,RF                    IF REG15=1, THEN         M0009
*                                       INTERNAL CODE IS 95,
*                                       ABEND CODE IS B13-20
*                                       IF REG 15=2, THEN
*                                       INTERNAL CODE IS 96,
*                                       ABEND CODE IS B13-24
*                                       IF REG 15=3, THEN
*                                       INTERNAL CODE IS 97,
*                                       ABEND CODE IS B13-28
PDEXIT   EQU   *                        GO TO PROB DET           Y02072
         MVI   DXABEND,ABEND            SET ABEND FLAG           Y02072
         STC   RJ,DXABRETC              STORE RETURN CODE        Y02072
XCTL191V EQU   *                                                 Y02072
         LA    RF,MODNM1V               GET IDTTR OF IGGO191V    S20202
MVEIDTTR EQU   *
*
***********************************************************************
*
*   MOVE IDTTR TO XCTL WTG TABLE
*
***********************************************************************
*
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,0(RF)           ST NEXT MOD ID IN WTG  @Z30TSCF
*
***********************************************************************
*
RELOOP   EQU   *
*
***********************************************************************
*
*   UPDATE WTG TABLE AND XCTL TO NEXT MODULE
*
***********************************************************************
*
         LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURRENT WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCREMENT CURRENT DCB ENTRY
         CLC   0(2,RWTGC),AMIDCNST      IS THE RTN NEEDED AGAIN
         BCR   8,RBASE                  IF YES,RETURN TO PROCESS
*
         CLC   0(2,RWTGC),OPIDCNST      TEST END OF WTG TABLE
         BC    7,RELOOP                 IF NOT,CHECK NEXT ENTRY
*
         LR    RPARC,RPAR               RESET REG TO '0'
         LA    RWTGC,WAOFF(0,RWTG)      RESET WTG REG TO POINT +32(WTG)
ZCHECK   CLI   0(RWTGC),X'00'           TEST IF THIS ENTRY COMPLETE
         BC    7,TCTLRTN                IF NOT,XCTL
*
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY OF WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)     GET NEXT ENTRY OF DCB
         BC    15,ZCHECK                GOTO TEST COMPLETION
*
TCTLRTN  EQU   *
*
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
         EJECT
***********************************************************************
*  'MSGRTN' SUBROUTINE                                           Y02072
*  THIS SUBROUTINE EXTRACTS THE REQUIRED MESSAGE FROM THE MSG    Y02072
*  CSECT AND MOVES IT TO THE WTO/WTOR AREA.                      Y02072
*
*    INPUT TO THIS ROUTINE:                                      Y02072
*      REG 12 - MESSAGE NUMBER IN MSG CSECT                      Y02072
*      REG 13 - RETURN ADDRESS                                   Y02072
*
*    OUTPUT FROM THIS ROUTINE:                                   Y02072
*      MESSAGE TEXT IN WTO/WTOR AREA                             Y02072
*      REG 1 - POINTER TO MESSAGE ENTRY IN THE MESSAGE CSECT     Y02072
***********************************************************************
MSGRTN   EQU   *                        ENTRY POINT, MSG ROUTINE Y02072
         L     RWK7,DXMSGADR            GET ADDR OF MSG CSECT    Y02072
         LH    RWK2,0(RWK7,RWK5)        GET INDEX TO MESSAGE     Y02072
         LA    RWK2,0(RWK7,RWK2)        ADDR OF MESSAGE ENTRY    Y02072
         USING MSGENTRY,RWK2                                     Y02072
         SR    RWK3,RWK3                PREPARE REGISTER         Y02072
         IC    RWK3,MSGOFF              GET OFFSET TO TEXT IN    Y02072
*                                         ENTRY                  Y02072
         LA    RWK3,0(RWK2,RWK3)        ADDR OF MESSAGE TEXT     Y02072
         SR    RWK7,RWK7                PREPARE REGISTER FOR RET Y02072
         USING MSGTXTD,RWK3                                      Y02072
         IC    RWK7,MSGLNG              GET LEN OF MSG - 1       Y02072
         EX    RWK7,MVCINST             MOVE MESSAGE             Y02072
         BR    RWK6                     RETURN                   Y02072
MVCINST  MVC   MSGAREA(0),MSGTXT        EXECUTED BY EX INST      Y02072
         EJECT
*
***********************************************************************
*
*   CONSTANTS
*
***********************************************************************
*
OPIDCNST DC    C'0S'
AMIDCNST DC    C'1T'
LOWER    DC    X'4040404040404040'      MASK TO OR REPLY
MSGCSECT DC    CL8'IGGMSG01'            NAME OF MSG CSECT        Y02072
*
***********************************************************************
*
*   XCTL TABLE
*
***********************************************************************
*
MODNM1U  DC    C'1U',VL3(IGG0191U)      MODULE IGG0191U        @Z30TSCF
MODNM1V  DC    C'1V',VL3(IGG0191V)      MODULE IGG0191V        @Z30TSCF
MODNM7E  DC    C'7E',VL3(IGG0197E)      MODULE IGG0197E        @Z30TSCF
         SPACE
PATCH    DC    0H'0',50X'00'            PATCH AREA               Y02072
END      EQU   *                        END OF MODULE            Y02072
         EJECT
         IEECUCM FORMAT=NEW                                      YM6054
         EJECT
         IHAPSA                                                  Y02072
         EJECT
         DCBD  DSORG=PS
         EJECT
         IGGMSG                                                  Y02072
         EJECT
         IECDSECS  MAIN,WTG,PREFX,EXPAND=YES                   @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MOD ID              Y02072
FORCORE  DSECT                                                   Y02072
         ORG   REPLYLTH+52                                       A33665
RPLY     DS    20C                                               A33665
RPLYECB  DS    F                                                 A33665
MCSMASK  DS    H                                                 A33665
LOMASK   DS    8C                                                A33665
UCSID    DS    4C                                                A33665
         ORG   MSGLSTSZ                                          Y02072
MSGAREA  DS    0CL64                    TAREA TO MOVE 64 BYTES   Y02072
*                                         TO FOR WTO/WTOR'S      Y02072
         EJECT
         IGGSCW                                                  Y02072
         EJECT
         IHAFCAUD  ORG=YES                                       YM4697
         EJECT
SRT      DSECT
         IEFUCBOB
         EJECT
         IEZDEB                                                  Y02072
         EJECT
         IKJTCB                                                  Y02072
         EJECT
         CVT  DSECT=YES,LIST=NO                                  Y02072
         END
