         TITLE '   IGG0193I         EXECUTOR SELECTION  '
IGG0193I CSECT
*MODULE NAME - IGG0193I
*
*DESCRIPTIVE NAME - PATH DETERMINER
*
*COPYRIGHT - NONE
*
*CHANGE ACTIVITY - AS FOLLOWS:
*
*        VS2 RELEASE 04 DELETIONS
*                                                              @Z40MSRB
*
*        VS2 RELEASE 03 DELETIONS
*        THE MODULE WAS RENUMBERED                             @Z30TSMI
*A860020-862000                                                 ZA04520
*OZ07043 DELETES 06605020
*
*        VS2 RELEASE 02 DELETIONS
*                                                                Y02072
*        VS2 REL 3 CHANGES                                     @ZA05462
*A061510,119000,145010,145020,145030,145040,145050             @ZA05462
*A910100,910200,910300                                         @ZA09379
*OZ02650 DELETES 13300003
*D861000,862000                                                @ZA09379
*A090000 TO 0900400                                            @ZA18300
*C088720,090820                                                @ZA18301
*          RELEASE 19 DELETIONS
*                                                                S19033
*                                                                S19017
*                                                                S19015
*                                                                M4879
*                                                                M4899
*          RELEASE 20 DELETIONS
*                                                                A30808
*                                                                S20016
*                                                                M0017
*                                                                A30962
*                                                                M5302
*          RELEASE 21 DELETIONS
*                                                                S21088
*                                                                S21040
*                                                               SA53270
*
*STATUS CHANGE LEVEL 005
*
*FUNCTION/OPERATION- THIS ROUTINE IS USED TO SELECT THE PROPER
*      STAGE TWO EXECUTOR FOR THE SEQUENTIAL ACCESS METHODS.
*      WITH DEVELOPEMENT CODE @Z30TSMI IT WILL NOW DEFAULT TO CHAIN
*      SCHEDULING BY GOING TO THE CHAIN SCHEDULING EXECUTOR INSTEAD
*      OF THE NORMAL SCHEDULING EXECUTOR WITH THE FOLLOWING EXCEPTIONS:
*        1)PRINTERS. CANNOT DEFAULT CHAINED SCHEDULING BECAUSE OF THE
*            POSSIBILITY OF THE USER USING PRTOV MACRO.
*        2)TAPE OR DA FORMAT U RECORDS FOR INPUT. CANNOT DEFAULT
*            CHAINED SCHEDULING BECAUSE THE USER MAY NEED TO KNOW THE
*            ACTUAL RECORD LENGTH AND IT IS LOST WITH CHAINED
*            SCHEDULING.
*
*ENTRY POINT-  IGG0193I ENTERED FROM IGG0191I OR IGG0196B
*
*INPUT: SEE DESCRIPTION OF REGISTERS.
*OUTPUT: SEE DESCRIPTION OF REGISTERS.
*EXTERNAL ROUTINES: IFG019RA
*EXIT NORMAL XCTL TO:
*                      IGG0191D - NORMAL SCHEDULING D. A.
*                      IGG0191G - NORMAL SCHEDULING TAPE AND U. R.
*                      IGG0191S - TRACK OVERFLOW
*                      IGG0191J - IN/OUT OUT/IN D. A.
*                      IGG0191K - CHAINED SCHEDULING INPUT D. A.
*                      IGG0191L - BSAM LOAD MODE (BDAM CREATE)
*                      IGG0191P - UPDATE
*                      IGG0191Q - CHAINED SCHEDULING TAPE AND U. R.
*                      IGG0191R - CHAINED SCHEDULING IN/OUT OUT/IN TAPE
*                      IGG0196S - TIME SHARING TERMINAL
*                      IGG0197A - 1285 1287 1288
*                      IGG0197C - 1275 1419
*                      IGG0191W - CHAINED SCHEDULING OUTPUT D. A.
*                      IGG0191X - CHAINED SCHEDULING IN/OUT OUT/IN DA
*                      IGG0197N - 3505 3525
*                      IGG0197V - 3890
*                      IGG0196R - 3886
*
*
*TABLES/WORKAREAS: SEE DSECTS AT THE END OF THE LISTING
*
*ATTRIBUTES: REENTRANT, REUSABLE, RUNS IN DATA MANAGEMENT KEY
*            RUNS IN SUPERVISOR STATE
*
***********************************************************************
         EJECT
***********************************************************************
*
*  REGISTER CONVENTIONS USED THROUGH OUT ALL OPEN PASSES
*
***********************************************************************
*
*
RF       EQU   1                       WORK/PARAMETER REGISTER
RDCB     EQU   2                       ADDR OF USERS DCB
RBASE    EQU   3                       BASE REGISTER
RCORE    EQU   4                       ADDR OF OPEN WORK AREA
RPAR     EQU   5                       PARAMETER LIST
RWTG     EQU   6                       START OF WTG
RPARC    EQU   7                       CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                       CURRENT ENTRY IN WTG TABLE
RTIOT    EQU   9                       TIOT ADDR/WORK REGISTER
RUCB     EQU   10                      UCB ADDR/WORK REGISTER
RDEB     EQU   11                      DEB ADDRESS
*
*********************************************************************
*
* MASKS FOR DEVICE TYPES IN DCBDEVT
*
DABIT    EQU   X'20'                   DIRECT ACCESS DEVICE
URBIT    EQU   X'40'                   UNIT RECORD DEVICE
PTAPE    EQU   X'50'                   PAPER TAPE DEVICE      @ZA05462
TAPEB    EQU   X'80'                   MAGNETIC TAPE DEVICE
*
*********************************************************************
         EJECT
*
         BALR  RBASE,0
         USING START,RBASE
         USING PARML,RPARC
         USING WTGENTRY,RWTGC
         USING WTG,RWTG
*
START    EQU   *
*
         B     BEGIN                   BRANCH AROUND MODID     @Z30TSMI
         DC    C'IGG0193I'             MODID                   @Z30TSMI
         DC    C' ZA18301'             LAST SHIP CODE          @ZA09379
         DC    C'&SYSDATE'             LAST SHIP DATE          @ZA09379
BEGIN    EQU   *                       END OF MODID            @Z30TSMI
         L     RDCB,PARDCBAD           GET DCB ADDRESS         @Z30TSMI
         USING IHADCB,RDCB
         L     RCORE,WTGCORE-1         GET WORK AREA           @Z30TSMI
         USING FORCORE,RCORE
         L     RDEB,DCBDEBAD           GET DEB ADDRESS
         USING DEBBASIC,RDEB
*
*
         TM    DCBDEVT,DABIT           USING D. A. DEVICE      @Z30TSMI
         BZ    A0150                   NO - CHECK FOR TAPE     @Z30TSMI
*
*        AT THIS POINT IT IS KNOWN THE DEVICE IS DA
*
         LA    RF,LOADMODE             BDAM CREATE EXEC
         TM    PAROPT,PAROUTPT         DCB USED FOR OUTPUT     @Z30TSMI
         BNO   A0100                   NO BRANCH
*
         TM    DCBMACRF+1,DCBMRWRT+DCBMRLDM  BSAM LOAD MODE    @Z30TSMI
         BO    A0181                   YES - BRANCH
*
A0100    EQU   *
         LA    RF,SOPB                 UPDATE EXEC
         TM    DEBOPATB,PARUPDAT       UPDATE BIT ON           @Z30TSMI
         BZ    A0110                   NO,BRANCH                  12608
         TM    DEBOPATB,PAROUTPT-PARUPDAT  OTHER BITS          @Z30TSMI
         BZ    A0181                   NO - BRANCH             @Z30TSMI
*
A0110    EQU   *
         LA    RF,SOP1S                TRACK OVFLO EXEC
         TM    DCBRECFM,DCBRECTO       USING OVERFLOW OPTION   @Z30TSMI
         BO    A0181                   YES - BRANCH
*
         LA    RF,SOP2                 LOAD FOR 1D              ZA04520
         TM    PAROPT,PAROUTPT         IS FILE FOR IN OR OUT   @Z30TSMI
         BNM   A013A                   YES BRANCH              @ZA09379
*                                      MUST BE FOR IN/OUT OR OUT/IN
         LA    RF,SOPADX               DISK PCI EXEC 191X
         TM    DCBOPTCD,DCBOPTC        PCI REQUESTED           @Z30TSMI
         BO    A0181                   YES - USE IT            @Z30TSMI
         TM    DCBRECFM,DCBRECU        U LENGTH RECORDS        @Z30TSMI
         BO    A012A                   YES - DO NOT USE PCI    @ZA18300
         TM    JFCBFLG2,JFCSDRPS       SEARCH DIRECT?          @ZA18301
         BO    A012A                   BRANCH IF YES           @ZA18300
         B     A0181                   XCTL                        DMOR
A012A    EQU   *                                               @ZA18300
         LA    RF,SOP8                 CAN NOT DEFAULT PCI     @Z30TSMI
         B     A0181                   XCTL                        DMOR
A013A    EQU   *                                               @ZA09379
         TM    JFCBFLG2,JFCSDRPS       IS THIS SEARCH DIRECT   @ZA18301
         BO    A0181                   YES,BRANCH              @ZA09379
*                                                                  DMOR
A0130    EQU   *
         LA    RF,SOPOPCI              DA PCI EXEC 191W        @Z30TSMI
         TM    PAROPT,PAROUTPT         OUTPUT                  @Z30TSMI
         BO    A0181                   YES - BRANCH            @Z30TSMI
         LA    RF,SOPA                 INPUT PCI EXEC 191K     @Z30TSMI
         TM    DCBOPTCD,DCBOPTC        PCI REQUESTED           @Z30TSMI
         BO    A0181                   YES - USE IT            @Z30TSMI
         TM    DCBRECFM,DCBRECU        U LENGTH RECORDS        @Z30TSMI
         BM    A0181                   NO - USE PCI            @Z30TSMI
         LA    RF,SOP2                 CAN NOT DEFAULT PCI     @Z30TSMI
         B     A0181                   GO XCTL                 @Z30TSMI
*                                                                  DMOR
A0150    EQU   *
         TM    DCBDEVT,TAPEB           MAG TAPE INDICATED      @Z30TSMI
         BZ    A0170                   NO - BRANCH U. R.       @Z30TSMI
*
*        AT THIS POINT IT IS KNOWN THE DEVICE IS TAPE
*
         LA    RF,SOP5                 NORMAL SCHED TAPE EXEC  @Z30TSMI
         TM    DCBOPTCD,DCBBCKPT       BYPASS DOS CHKPT RCDS   @Z30TSMI
         BO    A0181                   YES USE NORMAL SCHED    @Z30TSMI
         TM    PAROPT,PAROUTPT         IS THIS OUTPUT          @Z30TSMI
         BO    A0155                   YES - CAN USE PCI       @Z30TSMI
         TM    PAROPT,PARINOUT         IS IT IN/OUT OR OUT/IN  @Z30TSMI
         BO    A0160                   YES - BRANCH            @Z30TSMI
         TM    DCBOPTCD,DCBOPTC        PCI REQUESTED           @Z30TSMI
         BO    A0155                   YES - GO USE IT         @Z30TSMI
         TM    DCBRECFM,DCBRECU        U LENGTH RECORDS INPUT  @Z30TSMI
         BNM   A0181                   YES - USE NORMAL SCHED  @Z30TSMI
A0155    EQU   *                       CAN USE PCI             @Z30TSMI
         LA    RF,SOPC                 PCI EXEC                @Z30TSMI
         B     A0181                   GO GET NEXT EXEC        @Z30TSMI
A0160    EQU   *
         LA    RF,SOP1R                PCI IN/OUT OUT/IN       @Z30TSMI
         TM    DCBOPTCD,DCBOPTC        PCI REQUESTED           @Z30TSMI
         BO    A0181                   YES - GO USE IT         @Z30TSMI
         TM    DCBRECFM,DCBRECU        U LENGTH                @Z30TSMI
         BM    A0181                   NO - USE PCI            @Z30TSMI
         LA    RF,SOP5                 USE NORMAL SCHED        @Z30TSMI
         B     A0181                   XCTL                        DMOR
*                                                                  DMOR
*        AT THIS POINT IT IS KNOWN THE DEVICE IS UNIT RECORD
*
A0170    EQU   *
         LA    RF,EXEC3890             3890 EXEC                XL03123
         CLI   DCBDEVT,DCBDVMRS        DEVICE A 3890 ?          XL03123
         BE    A0181                   YES - BRANCH             XL03123
         CLI   DCBDEVT,DCBDVPR5        IS DEVICE A 3800        @Z40MSRB
         BE    A0175                   YES, SET NEXT EXEC      @Z40MSRB
         CLI   DCBDEVT,DCBDVPR1        LOWEST PRINTER DEVTYP   @Z30TSMI
         BL    NOTPRT                  NOT A PRINTER           @Z30TSMI
         CLI   DCBDEVT,DCBDVPR2        HIGHEST PRINTER DEVTYP  @Z30TSMI
         BH    NOTPRT                  NOT A PRINTER           @Z30TSMI
A0175    EQU   *                       CHECK PCI               @Z40MSRB
         LA    RF,SOPC                 PCI TAPE AND U. R.      @Z30TSMI
         TM    DCBOPTCD,DCBOPTC        PCI REQUESTED           @Z30TSMI
         BO    A0181                   YES - USE IT            @Z30TSMI
PAPERTAP EQU   *                       USING PAPER TAPE        @ZA05462
         LA    RF,SOP5                 NO - USE NORMAL SCHED   @Z30TSMI
         B     A0181                   GO TO NEXT EXEC         @Z30TSMI
NOTPRT   EQU   *                       NOT A PRINTER           @Z30TSMI
         LA    RF,SOP7N                3505 3525 EXEC          @Z30TSMI
         CLI   DCBDEVT,DCBDVCR2        3505 CARD READER          S21088
         BE    A0181                   YES BRANCH                S21088
         CLI   DCBDEVT,DCBDVCP1        3525 CARD PUNCH           S21088
         BE    A0181                   YES BRANCH                S21088
         CLI   DCBDEVT,DCBDVORS        IS THIS A 1275            S19033
         BE    MICR                    YES,BRANCH                S19033
         CLI   DCBDEVT,DCBDVMR         IS THIS A 1419           XL03123
         BNE   CHKOCR                  NO,BRANCH                 S19033
MICR     EQU   *                                                 S19033
         LA    RF,SOP7C                1275 1419 EXEC            S19033
         B     A0181                   XCTL                      S19033
CHKOCR   EQU   *                                                 S19033
         LA    RF,EXEC3886             3886 EXEC               @Z30TSMI
         CLI   DCBDEVT,DCBDVOR9        IS DEVICE A 3886        @Z30TSMI
         BE    A0181                   YES - BRANCH            @Z30TSMI
         LA    RF,SOP7A                1285 1287 1288 EXEC       S19033
         CLI   DCBDEVT,DCBDVOR5        CHECK FOR A               S19017
         BL    A0180                   1285,1287,OR 1288         S19017
         CLI   DCBDEVT,DCBDVOR8        DCBDEVT IS 5A,5B,OR 5C    S19017
         BNH   A0181                   YES - BRANCH            @Z30TSMI
A0180    EQU   *
*******************************************************************
*
*  TSO MODIFICATION - TO XCTL TO TS EXECUTOR IGG0196S WHEN TASK
*                     IS TIME SHARING
*
*******************************************************************
*
         LA    RF,SOPTS                TIME SHARING EXEC         S20016
         CLI   DCBDEVT,DCBDVTRM        CHECK FOR TS TASK         S20016
         BE    A0181                   YES - BRANCH            @Z30TSMI
*
         LA    RF,SOPC                 PCI U. R. EXEC          @Z30TSMI
*                                      TEST FOR PAPER TAPE     @ZA05462
*                                      AND DISALLOW            @ZA05462
*                                      CHAINED SCHEDULING      @ZA05462
         CLI   DCBDEVT,PTAPE           PAPER TAPE DEVICE ?     @ZA05462
         BE    PAPERTAP                BRANCH YES              @ZA05462
A0181    EQU   *                                                 S20016
         MVC   WTGIDTTR,0(RF)           STORE NEXT MODULES ID  @Z30TSMI
*
***********************************************************************
*
***********************************************************************
*
RELOOP   EQU   *
         LA    RWTGC,WTGEND            INCREMENT CURRENT WTG ENTRY
         LA    RPARC,L'PARDCBAD(0,RPARC) INCR CURRENT DCB ENTRY PTR
         CLC   WTGID,AMIDCNST          THIS RT NEEDED AGAIN
         BER   RBASE                   RETURN TO INIT. ANOTHER DCB
*
         CLC   WTGID,OPIDCNST          END OF TABLE
         BNE   RELOOP                  NO, CHECK NEXT ENTRY
*
         LR    RPARC,RPAR
         LA    RWTGC,WTGENTRY-WTG(,RWTG)  REINIT WTG LIST PTR
ZCHEK    CLI   WTGENTRY,0              IS THIS ENTRY COMPLETE
         BNE   TCTLRTN                 IF NOT TRANSFER CONTROL
*
         LA    RWTGC,WTGEND            GET NEXT ENTRY
         LA    RPARC,L'PARDCBAD(0,RPARC)
         B     ZCHEK
*
TCTLRTN  EQU   *
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSMI*
               MODID=WTGENTRY                                  @Z30TSMI
         DROP  RWTG,RWTGC,RPARC,RBASE,RDCB,RCORE,RDEB
         EJECT
**********************************************************************
*
OPIDCNST DC    C'0S'                   COMMON OPEN RETURN MODULE
AMIDCNST DC    C'3I'                   THIS ROUTINE ID          SA53270
SOP2     DC    C'1D',VL3(IGG0191D)     NORMAL SCHEDULING D. A.
SOP5     DC    C'1G',VL3(IGG0191G)     NORMAL SCHEDULING TAPE U. R.
SOP1S    DC    C'1S',VL3(IGG0191S)     TRACK OVERFLOW
SOP8     DC    C'1J',VL3(IGG0191J)     IN/OUT OUT/IN D. A.
SOPA     DC    C'1K',VL3(IGG0191K)     INPUT PCI D. A.
LOADMODE DC    C'1L',VL3(IGG0191L)     BSAM LOAD MODE (BDAM CREATE)
SOPB     DC    C'1P',VL3(IGG0191P)     UPDATE
SOPC     DC    C'1Q',VL3(IGG0191Q)     PCI TAPE U. R.
SOP1R    DC    C'1R',VL3(IGG0191R)     IN/OUT OUT/IN PCI TAPE
SOPTS    DC    C'6S',VL3(IGG0196S)     TIME SHARING TERMINAL
SOP7A    DC    C'7A',VL3(IGG0197A)     1285 1287 1288
SOP7C    DC    C'7C',VL3(IGG0197C)     1275 1419
SOPOPCI  DC    C'1W',VL3(IGG0191W)     OUTPUT PCI D. A.
SOPADX   DC    C'1X',VL3(IGG0191X)     IN/OUT OUT/IN PCI D. A.
SOP7N    DC    C'7N',VL3(IGG0197N)     3505 3525
EXEC3890 DC    C'7V',VL3(IGG0197V)     3890
EXEC3886 DC    C'6R',VL3(IGG0196R)     3886 OCR DEVICE         @Z30TSMI
PATCH    DC    0H'0',XL50'00'          PATCH AREA                Y02072
         EJECT
CVT      DSECT
         CVT
         EJECT
         DCBD  DSORG=PS
         EJECT
         IEZDEB
         EJECT
         IECDSECS  (MAIN,(IOB,NO)),WTG,PREFX,EXPAND=YES          Y02072
         ORG   WTGIDTTR
WTGID    DS    CL2                     ID PORTION OF WTG
         EJECT
         IGGPARML
         END
