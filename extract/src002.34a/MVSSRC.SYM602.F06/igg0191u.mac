191U  TITLE 'IGG0191U - UCS IMAGE RETRIEVAL'
IGG0191U CSECT
*
*MODULE NAME - IGG0191U                                          Y02072
*
*DESCRIPTIVE NAME - UCS IMAGE RETRIEVAL                          Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          RELEASE 20 DELETIONS
*3364511000,514000                                               M5149
*3364305400-306200,319000,337000-340000,344000,347000,377000,    A33665
*3364386000,396000,445000,466000,478000,507000-508000            A33665
*3364412500,774000,783600                                        A33684
*3364013000-016000,040000,044000-045000,051000-058000,066000-    S20202
*3364067000,139000,140000-141000,195000,202000-205000,241000,    S20202
*3364249000,314000-315000,354000-365000,479000-495000,530000-    S20202
*3364576000,582000-592000,604000,719000-750000                   S20202
*3364475000                                                      A34950
*          RELEASE 21 DELETIONS
*1033361800,603930,791000-799000,811000-816000                   S21042
*
*        VS 2 RELEASE 02 DELETIONS
*001200-001800,012000,048000,064000,109200-109600,113000-115000, Y02072
*150000,163500-164800,245000-277000,289000-291000,295000-300000, Y02072
*304000-305250,411000-415000,646060-646840,662050-662600,        Y02072
*769000-775000,783300-804000,810000,812000,814000,003000-005500  Y02072
*008000-011000,681000,713000,717000,812000,620000,765000         Y02072
*                                                                YM7889
*
*        VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*        VS2 RELEASE 4 CHANGES
*C782600-783000                                                @ZA01357
*
*LAST DATE CHANGED - JULY 2, 1975
*
*FUNCTION/OPERATION - ISSUE BLDL MACRO TO LOCATE THE UCS IMAGE
*                     IN SYS1.IMAGELIB
*                   - DISPLAY IEC120 MESSAGE TO REQUEST AN OPERATOR
*                     MOUNT THE CHAIN IF NECESSARY
*                   - WHEN IMAGE NOT IN SYS1.IMAGELIB DISPLAY MESSAGE
*                     IEC122 TO REQUEST OPERATOR SPECIFY AN
*                     ALTERNATE CHAIN.
*                   - ANALIZE REPLY MADE BY AN OPERATOR
*
*ENTRY POINT - ENTERED FROM IGG0191T BY THE USE OF XCTL MACRO
*
*INPUT - DCB ADDRESS
*      - OPEN WORK AREA ADDRESS
*      - WHERE-TO-GO TABLE
*      - PARAMETER LIST
*      - CURRENT ENTRY IN W-T-G TABLE
*      - CURRENT PARAMETER(DCB)
*      - REPLY IN OPEN WORK AREA BY OPERATOR
*      - ADDRESS OF SYS1.IMAGELIB DCB IN OPEN WORKAREA(DXCCW8)
*
*OUTPUT - TO REQUEST AN OPERATOR MOUNT PROPER CHAIN
*         'IEC120A M XXX,YYYY' MESSAGE IS DISPLAYED
*       - TO REQUEST AN OPERATOR SPECIFY AN ALTERNATE UCS IMAGE.
*         'IEC122D XXX,YYYY IMAGE NOT FOUND' MESSAGE IS DISPLAYED
*       - MESSAGE IEC125A - ERROR - REPEAT REPLY, WHEN OPERATOR  Y02072
*         REPLY IS INVALID                                       Y02072
*       - THE ADDRESS OF UCS IMAGE RETRIEVED FROM SYS1.IMAGELIB
*
*EXTERNAL REFERENCES - MESSAGE CSECT                             Y02072
*
*EXIT - NORMAL - XCTL TO IGG0191V TO LOAD IMAGE                  Y02072
*
*EXIT - ERROR - XCTL TO IGG0191V FOR THE FOLLOWING:              Y02072
*               *PERMANENT I/O ERROR OCCURED DURING BLDL
*               *OPERATOR ISSUED CANCEL FOR MESSAGE IEC120,IEC122,OR
*                IEC123
*
*TABLES/WORK AREAS - WHERE-TO-GO TABLE(SEE LISTING OF EXECUTOR
*                                      IGG0191B)
*                  - OPEN WORK AREA (SEE IECDSECT IN THIS LISTING)
*
*ATTRIBUTES - REENTRANT, REUSABLE, RUNS IN DATA MANAGEMENT KEY   Y02072
*             UNLESS OTHERWISE SPECIFIED, SUPER STATE            Y02072
*
*MACROS-MAPPING:IGGMSG, CVT, IEFUCBOB, IECDSECS(WTG,MAIN), DCBD, Y02072
*               IGGSCW                                           Y02072
*
*NOTES - BLDL SVC IS ISSUED TO LOCATE UCS IMAGE IN SYS1.IMAGELIB
*      - WTO/WTOR SVC IS USED TO REQUEST OPERATOR REPLY.
*      - THE REPLY IS ORED TO ALLOW LOWER CASE ALPHA
*      - BEFORE ISSUING A WTO/WTOR, THE MESSAGE TEXT MUST FIRST  Y02072
*        BE EXTRACTED FROM A MESSAGE CSECT.                      Y02072
*
***********************************************************************
*
         EJECT
*
***********************************************************************
*
*   REGISTER CONVENTIONS
*
***********************************************************************
*
*
RE       EQU   0                        WORK/PARAMETER REGISTER
RWK1     EQU   RE                       WORK REGISTER            Y02072
RF       EQU   1                        WORK/PARAMETER REGISTER
RWK2     EQU   RF                       WORK REGISTER            Y02072
RDCB     EQU   2                        ADDR OF USER'S DCB
RBASE    EQU   3                        BASE REGISTER
RCORE    EQU   4                        ADDR OF OPEN WORK AREA
RPAR     EQU   5                        PARAMETER LIST
RWTG     EQU   6                        START OF WTG
RPARC    EQU   7                        CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                        CURRENT ENTRY IN WTG TABLE
RTIOT    EQU   9                        TIOT ADDR/WORK REGISTER
RWK3     EQU   RTIOT                    WORK REGISTER            Y02072
RUCB     EQU   10                       UCB ADDR/WORK REGISTER
RDEB     EQU   11                       DEB ADDR
RWK4     EQU   RDEB                     WORK REGISTER            Y02072
RB       EQU   12                       WORK REGISTER/BREG FOR DSECT
RWK5     EQU   RB                       WORK REGISTER            Y02072
RC       EQU   13                       WORK REGISTER
RWK6     EQU   RC                       WORK REGISTER            Y02072
RD       EQU   14                       WORK/PARAMETER REGISTER
RJ       EQU   15                       WORK/PARAMETER REGISTER
RWK7     EQU   RJ                       WORK REGISTER            Y02072
*
***********************************************************************
*
*
*   EQUATE WTG OFFSET
*
WGOFF    EQU   8                        OFFSET OF CURRENT WTG ENTRIES
PLOFF    EQU   4                        OFFSET OF CURRENT DCB ENTRIES
WAOFF    EQU   32                       OFFSET OF 1ST ENTRY IN WTG TBL
*
*  THE FOLLOWING MESSAGE NUMBERS ARE EQUAL TO THEIR POSITION     Y02072
*  IN THE MESSAGE CSECT (RELATIVE TO ZERO), MULT BY 2 (LENGTH    Y02072
*  OF AN INDEX)                                                  Y02072
*
MSG120I3 EQU   4*2                      MESSAGE NO IN MSG CSECT  Y02072
MSG120I4 EQU   5*2                      MESSAGE NO IN MSG CSECT  Y02072
MSG120I5 EQU   6*2                      MESSAGE NO IN MSG CSECT  Y02072
MSG120I6 EQU   7*2                      MESSAGE NO IN MSG CSECT  Y02072
MSG122   EQU   8*2                      MESSAGE NO IN MSG CSECT  Y02072
MSG125   EQU   9*2                      MESSAGE NO IN MSG CSECT  Y02072
UCSIMLNG EQU   4                        LENGTH OF UCS IMAGE NAME Y02072
VERIFY   EQU   X'10'                    VERIFY
BLDL     EQU   X'80'                    BLDL IS REQUIRED
IEC120   EQU   X'40'                    IEC120 MESSAGE IS REQUIRED
IEC123RQ EQU   X'80'                    IF ON, MUST ISSUE IEC123 Y02072
LOAD     EQU   X'20'                    LOAD FROM SYS1.IMGLIB IS REQUIR
ALPHAF   EQU   X'C6'                    C'F'
NULL     EQU   X'00'                    X'00' MASK
EXCP     EQU   X'10'                    EXCP TO LOAD UCSB        Y02072
FOLDMASK EQU   X'40'                    FOLD MASK
COMMA    EQU   X'6B'                    C','
BLANK    EQU   X'40'                    C' '
REPEATNO EQU   5                        NO OF REPEAT IN REPLYRTN
BLANKMSK EQU   X'40'                    C' '
NOVERLAP EQU   X'FD'                    RESET OVERLAP BIT
FOUR     EQU   X'04'                    X'04'
NODELETE EQU   X'7F'                    NO DELETE IS REQUIRED
ABEND    EQU   X'FF'                    ABEND CODE OR SWITCH     S20202
PRTM3211 EQU   X'09'                    MASK FOR 3211 PRINTER    S20202
TWOCON   EQU   C'2'                                              S20202
OABD070  EQU   70                                                S21042
OABD071  EQU   71                                                S21042
         EJECT
         BALR  RBASE,0
         USING GOGOGO,RBASE
         USING FORCORE,RCORE
         USING UCBOB,RUCB
         USING IHADCB,RDCB
         USING DEB,RDEB
*
***********************************************************************
*
GOGOGO   EQU   *
*
         B     BEGIN                    BR AROUND CONSTANTS      Y02072
         DC    C'IGG0191U'              MODULE NAME              Y02072
         DC    C' OZ01357'              LAST SHIP CODE
         DC    C' 7/02/75'              LAST DATE MODIFIED
BEGIN    DC    0H'0'
*
***********************************************************************
*
*   INITIALIZE REGISTERS
*
***********************************************************************
*
         L     RDCB,0(RPARC)            LOAD CURRENT DCB ADDR
         L     RCORE,4(RWTGC)           LOAD CURRENT WORK AREA ADDR
         L     RDEB,DCBDEBAD            LOAD DEB ADDR
         L     RUCB,DEBUCBAD            LOAD UCB ADDR
         DROP  RDEB                                              Y02072
         L     RTIOT,CVTPTR(0)          LOAD CVT ADDR
*
***********************************************************************
*
TSTENTRY EQU   *
*
***********************************************************************
*
*   TEST THE ENTRY SWITCH TO DETERMINE REQUIRED OPERATION. (DXUCSSW1)
*
***********************************************************************
*
         TM    DXUCSSW1,IEC123RQ        IF ON, GO WAIT ON        Y02072
*                                         MSG IEC123             Y02072
         BO    RETURN                   GO ISSUE WAIT            Y02072
*
BLDLRTN  EQU   *
*
***********************************************************************
*
*   BLDL ROUTINE IS USED TO LOCATE UCS IMAGE IN SYS1.IMAGELIB
*
***********************************************************************
*
         MVC   DXBLDL,BLDLCNST          MOVE BLDL LEN TO LIST    Y02072
         MVC   DXBLDLIM,UCSCONST        MOVE 'UCS1' TO LIST      Y02072
         CLI   UCBTBYT4,PRTM3211        CK IF DCB BEING OPENED   S20202
*                                       FOR 3211                 S20202
         BNE   CONTBLDL                 NO GO CONTINUE WITH BLDL S20202
         MVI   DXBLDLIM+3,TWOCON        CHANGE UCS ID PREFIX TO  Y02072
*                                       UCS2                     S20202
*
CONTBLDL EQU   *                                                 S20202
*
         MVC   DXIMGNAM,DXUCSID         MOVE UCS NAME FROM JFCB  Y02072
*
*   GET ADDRESS OF IMAGELIB DCB
*
         L     RF,DXIMGDCB              GET ADDR OF IMGLIB       Y02072
*                                       DCB                      S20202
*
         LA    RE,DXBLDL                LOAD BLDL LIST ADDR      Y02072
*
         BLDL  (1),(0)                  BLDL THE UCS SET
*
*
*   TEST RETURN CODE FOR THE ABOVE FIND IN RJ
*               X'00'--- FOUND
*               X'04'--- NOT FOUND
*               X'08'--- PERMANENT I/O ERROR
*
         LA    RD,FOUR                  LOAD X'04' TO TEST RETURN CODE
         CLR   RJ,RD                    TEST RETURN CODE
         BC    2,ABEND08                IF X'08',GOTO ABEND ROUTINE
*
         BC    8,WTOR122                IF X'04',GOTO WTOR IEC122
*
***********************************************************************
*
CHCK120  EQU   *
*
***********************************************************************
*
*   WHEN THE IMAGE IS FOUND,TEST IF MESSAGE IEC120 IS REQUIRED
*
***********************************************************************
*
         TM    DXUCSSW1,IEC120          TEST IF IEC120 REQ       Y02072
         BNO   XCTL191V                 IF NOT, GO LOAD IMAGE    Y02072
*
***********************************************************************
*
WTOR120  EQU   *
*
***********************************************************************
*
*   WHEN UCS IS FOUND IN SYS1.IMAGELIB BY BLDL
*   WTOR 'IEC120A M XXX,YYYY' TO REQUEST MOUNTING SPECIFIED CHAIN
*
***********************************************************************
         LA    RWK6,W120RET             RETURN ADDRESS           Y02072
         TM    DXUCSOPT,FOLDMASK+VERIFY  FOLD OR VERIFY SPEC     Y02072
         LA    RWK5,MSG120I5            MSG NO FOR FOLD AND VER  Y02072
         BO    WTORTN                   YES, BRANCH TO GET MSG   Y02072
         LA    RWK5,MSG120I3            MSG NO FOR NEITHER FOLD  Y02072
*                                         OR VERIFY SPECIFIED    Y02072
         BZ    WTORTN                   NO, BRANCH TO GET MSG    Y02072
         TM    DXUCSOPT,FOLDMASK        FOLD SPECIFIED           Y02072
         LA    RWK5,MSG120I6            MSG NO FOR FOLD SPEC     Y02072
         BO    WTORTN                   YES, BRANCH TO GET MSG   Y02072
         LA    RWK5,MSG120I4            MSG NO FOR VERIFY SPEC   Y02072
         B     WTORTN                   BRANCH TO GET MESSAGE    Y02072
W120RET  EQU   *                        FILL IN VAR FLD 2        Y02072
         USING MSGENTRY,RWK2                                     Y02072
         IC    RWK7,MSGOFF2             OFFSET TO SECOND VAR FLD Y02072
         LA    RWK3,MSGAREA(RWK7)       ADDRESS OF SEC VAR FLD   Y02072
         MVC   0(UCSIMLNG,RWK3),DXUCSID  UCS IMAGE TO MSG        Y02072
         B     STVARFLD                 GO STORE VAR FLD 1       Y02072
*
WTOR122  EQU   *
*
***********************************************************************
*
*   WTOR 'IEC122D XXX,YYY IMAGE NOT FOUND' TO REQUEST FOR ALTERNATE
*   CHAIN SET
*
***********************************************************************
*
         LA    RWK5,MSG122              MESSAGE NO               Y02072
         BAL   RWK6,WTORTN              GO GET MESSAGE           Y02072
         NI    DXUCSSW1,NODELETE        RESET BLDL BIT TO INDIC  Y02072
*                                       NO IMAGE HAS BEEN LOADED
         IC    RWK7,MSGOFF2             OFFSET TO SECOND VAR FLD Y02072
         LA    RWK3,MSGAREA(RWK7)       ADDRESS OF SEC VAR FLD   Y02072
         MVC   0(UCSIMLNG,RWK3),DXUCSID  UCS IMAGE TO MSG        Y02072
         XC    DXUCSID,DXUCSID          RESET UCS IMAGE ID       Y02072
STVARFLD EQU   *                        STORE VARIABLE FIELD     Y02072
         IC    RWK7,MSGOFF1             OFFSET TO FIRST VAR FLD  Y02072
         LA    RWK3,MSGAREA(RWK7)       ADDRESS OF FIRST VAR FLD Y02072
         MVC   0(L'UCBNAME,RWK3),UCBNAME  MOVE DEV NAME TO MSG   Y02072
         DROP  RWK2                                              Y02072
*
*   WTOR ROUTINE
*
MESSAGO  EQU   *                                                 A27331
         MVI   RPLY,BLANK                                        A33665
         MVC   RPLY+1(19),RPLY          CLEAR REPLY AREA         A33665
         MVI   RPLYECB,NULL             CLEAR REPLY ECB          A33665
*
         LA    RF,REPLYLTH              LOAD WTOR ADDR
*
         SVC   35                       ISSUE WTOR SVC
*
*
RETURN   EQU   *
*
         LA    RF,RPLYECB               LOAD REPLY ECB ADDRESS   A33665
*
         WAIT  ECB=(1),LONG=YES         WAIT ON REPLY            Y02072
*
GOREPLY  EQU   *
*
***********************************************************************
*
*   REPLY ANALYSIS
*   (1) 'U' - USE THE ONE ON THE PRINTER
*   (2) 'CANCEL' - CHAIN SET NOT AVAILABLE
*   (3) 'UCS-ID' - UCS ID
*
***********************************************************************
*
         OC    RPLY(8),LOMASK           OR IN X'40' TO ALLOW     A33665
         OC    RPLY+8(8),LOMASK         REPLY IN LOWER CASE      A33665
         CLC   RPLY(2),UBLANK           TEST IF REPLY = 'U'      A33665
         BE    BLDLSETU                 IF YES, GO TO SET UP     Y02072
*                                         FOR BLDL               Y02072
         CLC   RPLY(6),CANCELCN         TEST IF REPLY = 'CANCEL' A33665
         BC    8,CANCELGO               IF YES,GOTO CANCEL ROUTINE
         CLC   RPLY(2),CBLANK           TEST IF REPLY = 'C'      A33665
         BC    7,TSTUCSID               IF NOT,GOTO UCS ID TEST
*
***********************************************************************
*
CANCELGO EQU   *
*
***********************************************************************
*
*   SINCE THE SPECIFIED UCS CHAIN IS NOT AVAILABLE,JOB STEP IS
*   CANCELLED BY OPERATOR. THE ABEND SWITCH AND CODE-X'04'- IS
*   SET,AND A XCTL IS MADE TO IGG0191V TO ABEND,CHECK IF IMAGE ID WAS
*   SPECIFIED CORRECTLY, OR ADD IMAGE TO SYS1.IMAGELIB
*
***********************************************************************
*
*
         MVI   DXABRETC,OABD071         MOVE INTERNAL CODE TO    Y02072
*                                       WKAREA                   S21042
         MVI   DXABEND,ABEND            SET ABEND FLAG           Y02072
         B     XCTL191V                 GO XCTL TO IGG0191V      S20202
*
*
***********************************************************************
*
TSTUCSID EQU   *
*
***********************************************************************
*
*   ANALYZE REPLY UCS PARAMETERS
*
***********************************************************************
*
         LA    RD,RPLY                  LOAD REPLY ADDRESS       A33665
         LA    RJ,REPEATNO              LOAD 5 IN REG. RJ
*
         CLI   0(RD),COMMA              TEST IF 1ST CHAR=','
         BC    8,ERRORTN                IF YES,GOTO ERROR ROUTINE
*
         CLI   0(RD),BLANKMSK           TEST IF 1ST CHAR=BLANK
         BC    8,ERRORTN                IF YES,GOTO ERROR ROUTINE
*
         MVI   MCSMASK,NULL             SET LOAD MODE BIT =      A33665
*                                       X'00'                    A33665
         MVC   UCSID(4),BLANKS          BLANK OUT AREA TO MOVE   A33665
*                                       REPLY ID INTO
*
TSTREPLY EQU   *
*
         CLI   0(RD),BLANKMSK           TEST IF CHAR=BLANK
         BC    8,TSTNAME                IF YES,GOTO TEST IMAGE IDS.
*
         CLI   0(RD),COMMA              TEST IF CHAR=','
         BC    8,TSTOPT                 IF YES,GOTO OPTION TEST ROUTINE
*
         MVC   UCSID-RPLY(1,RD),0(RD)                            A33665
*
         LA    RD,1(RD)                 MODIFY REPLY ADDR
*
         BCT   RJ,TSTREPLY              GO BACK TO REPLY UCS ID ANALY
*
***********************************************************************
*
ERRORTN  EQU   *
*
***********************************************************************
*
*   REPLY ERROR ROUTINE
*
***********************************************************************
         LA    RWK6,MESSAGO             RETURN ADDRESS           Y02072
         LA    RWK5,MSG125              MESSAGE NO FOR MSG RTN   Y02072
         B     WTORTN                   GO GET AND ISSUE MSG     Y02072
***********************************************************************
*
TSTOPT   EQU   *
*
***********************************************************************
*
*   TEST FOLD/VERIFY SPECIFIED
*
***********************************************************************
*
         CLI   1(RD),COMMA              TEST IF NEXT CHAR=','
*
         BC    8,TSTVRFY                IF YES,GOTO TEST 'VERIFY'
*
         CLC   1(4,RD),FOLDCN           TEST IF FOLD IS SPECIFIED
         BC    7,TSTALPHF               IF NOT,GOTO TEST IF 'F'
         LA    RD,4(RD)                 IF YES,SET POINTER TO NEXT WORD
         BC    15,SETFOLD               GOTO SET FOLD BIT
*
TSTALPHF EQU   *
*
         CLI   1(RD),ALPHAF             TEST IF 'F' IS SPECIFIED
         LA    RD,1(RD)                 SET POINTER TO NEXT WORD
         BC    7,ERRORTN                IF NOT,GOTO ERROR RTN
*
SETFOLD  EQU   *
*
*
*
         MVI   MCSMASK,FOLDMASK         SET LOAD MODE = FOLD     A33665
*
         CLI   1(RD),COMMA              TEST IF C',' FOLLOWS AFTER FOLD
         BC    8,TSTVRFY                IF YES,GOTO TEST VERIFY RTN
*
         CLI   1(RD),BLANKMSK           TEST IF A BLANK FOLLOWS
         BC    8,TSTNAME                IF YES,GOTO TEST NAME ROUTINE
*
         BC    15,ERRORTN               IF NOT,GOTO ERROR ROUTINE
*
TSTVRFY  EQU   *
*
*   TEST VERIFY IS SPECIFIED
*
         CLC   2(6,RD),VERIFYCN         TEST IF 'VERIFY' SPECIFIED
         BC    8,SETVRFY                IF YES,GOTO SET VERIFY BIT
         CLC   2(2,RD),VBLANK           TEST IF REPLY='V'
         BC    7,ERRORTN                IF NOT,GOTO ERROR ROUTINE
*
SETVRFY  EQU   *
*
         OI    MCSMASK,VERIFY           SET VERIFY = YES         A33665
*
***********************************************************************
*
TSTNAME  EQU   *
*
***********************************************************************
*
*   TEST IF REPLY ID = REQUESTED ID
*        YES - TEST TO SEE IF OPERATOR SPECIFIED VERIFICATION OF A34950
*              IMAGE. (OPERATORS OPTIONS MIGHT BE DIFFERENT THAN A34950
*              USERS OPTIONS). IF NO, GO TO 191V. IF YES, FOLLOW A34950
*              SAME PATH AS IF NEW IMAGE.                        A34950
*                                                                A34950
***********************************************************************
*
         CLC   DXUCSID,UCSID            CK FOR SAME ID'S         Y02072
         BE    XCTL191V                 IF EQUAL, GO LOAD IMAGE  Y02072
         MVC   DXUCSOPT,MCSMASK         SET LODE MODE            Y02072
         MVC   DXUCSID,UCSID            MOVE REPLIED UCS NAME    Y02072
         MVI   DXUCSSW1,0               RESET SWITCH             Y02072
         B     BLDLRTN                  GO ISSUE BLDL TO FIND IM Y02072
*
***********************************************************************
*
*   WHEN OPERATOR REPLIES 'U', THE IMAGE CURRENTLY LOADED IS TO BE
*   USED.  GET THE IMAGE ID AND IMAGE OPTIONS FROM THE UCB AND
*   SET UP THE BLDL PARM LIST.
*
***********************************************************************
*
BLDLSETU EQU   *                                                 Y02072
*
         L     RWK7,UCBXTADR            GET ADDR OF UCB/UCS XTN  Y02072
         USING UCBUCS,RWK7                                       Y02072
         MVC   DXUCSID,UCBUCSID         UCS IMAGE ID TO WORKAREA Y02072
         MVC   DXUCSOPT,UCBUCSOP        UCS OPTIONS TO WORKAREA  Y02072
         MVI   DXUCSSW1,0               RESET SWITCH             Y02072
         DROP  RWK7                                              Y02072
         B     CONTBLDL                 GO ISSUE BLDL            Y02072
*
ABEND08  EQU   *                                                 S20202
*
***********************************************************************
*                                                                     *
*   PERMANENT I/O ERROR WAS FOUND WHEN BLDL WAS ISSUED TO LOCATE AN   *
*   UCS IMAGE IN SYS1.IMAGELIB RETURN CODE=X'08' IS STORED
*   ABEND SWITCH IS SET AND A XCTL IS MADE TO IGG0191V TO ABEND.      *
*   IF THE ERROR PERSISTS, RUN DIAGNOSTICS ON IMAGELIB VOLUME.        *
*   +368(REG.4) POINTS TO THE BLDL LIST                               *
*                                                                     *
***********************************************************************
*
         MVI   DXABRETC,OABD070         MOVE INTERNAL CODE TO    Y02072
*                                       WKAREA                   S21042
         MVI   DXABEND,ABEND            SET ABEND FLAG           Y02072
*
*
*
XCTL191V EQU   *                                                 S20202
*
***********************************************************************
*
*   XCTL TO NEXT EXECUTOR IGG0191V
*
***********************************************************************
*
         LA    RF,MODNM1V               LOAD IDTTR NEXT EXECUTOR S20202
*
MVEIDTTR EQU   *
*
***********************************************************************
*
*   MOVE IDTTR TO XCTL WTG TABLE
*
***********************************************************************
*
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,0(RF)           ST NEXT MOD ID IN WTG  @Z30TSCF
*
***********************************************************************
*
RELOOP   EQU   *
*
***********************************************************************
*
*   UPDATE WTG TABLE AND XCTL TO NEXT MODULE
*
***********************************************************************
*
         LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURRENT WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCREMENT CURRENT DCB ENTRY
         CLC   0(2,RWTGC),AMIDCNST      IS THE RTN NEEDED AGAIN
         BCR   8,RBASE                  IF YES,RETURN TO PROCESS
*
         CLC   0(2,RWTGC),OPIDCNST      TEST END OF WTG TABLE
         BC    7,RELOOP                 IF NOT,CHECK NEXT ENTRY
*
         LR    RPARC,RPAR               RESET REG TO '0'
         LA    RWTGC,WAOFF(0,RWTG)      RESET WTG REG TO POINT +32(WTG)
ZCHECK   CLI   0(RWTGC),X'00'           TEST IF THIS ENTRY COMPLETE
         BC    7,TCTLRTN                IF NOT,XCTL
*
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY OF WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)     GET NEXT ENTRY OF DCB
         BC    15,ZCHECK                GOTO TEST COMPLETION
*
TCTLRTN  EQU   *
*
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
         EJECT
***********************************************************************
*  'WTORTN' SUBROUTINE                                           Y02072
*  THIS SUBROUTINE EXTRACTS THE REQUIRED MESSAGE FROM THE MSG    Y02072
*  CSECT AND MOVES IT TO THE WTO/WTOR AREA.                      Y02072
*
*    INPUT TO THIS ROUTINE:                                      Y02072
*      REG 12 - MESSAGE NUMBER IN CSECT                          Y02072
*      REG 13 - RETURN ADDRESS                                   Y02072
*
*    OUTPUT FROM THIS ROUTINE:                                   Y02072
*      REG 1 - POINTER TO MESSAGE ENTRY IN THE MESSAGE CSECT     Y02072
*      MESSAGE TEXT IN WTO/WTOR AREA                             Y02072
***********************************************************************
WTORTN   EQU   *                        ENTRY POINT, MSG RTN     Y02072
         L     RWK7,DXMSGADR            GET ADDR OF MSG CSECT    Y02072
         LH    RWK2,0(RWK7,RWK5)        GET INDEX TO MESSAGE     Y02072
         LA    RWK2,0(RWK7,RWK2)        ADDR OF MESSAGE ENTRY    Y02072
         USING MSGENTRY,RWK2                                     Y02072
         SR    RWK3,RWK3                PREPARE REGISTER         Y02072
         IC    RWK3,MSGOFF              GET OFFSET TO TEXT IN    Y02072
*                                         ENTRY                  Y02072
         LA    RWK3,0(RWK2,RWK3)        ADDR OF MESSAGE TEXT     Y02072
         USING MSGTXTD,RWK3                                      Y02072
         SR    RWK7,RWK7                PREPARE REG FOR RETURN   Y02072
         IC    RWK7,MSGLNG              LEN OF MSG - 1           Y02072
         EX    RWK7,MVCINST             MOVE MESSAGE TO AREA     Y02072
         BR    RWK6                     RETURN                   Y02072
MVCINST  MVC   MSGAREA(0),MSGTXT        EXECUTED BY A EX INST    Y02072
         EJECT
*
*   CONSTANTS
*
***********************************************************************
*
*   FOR WTG TABLE
*
OPIDCNST DC    C'0S'
AMIDCNST DC    C'1U'
*
*   MESSAGE TEXT
*
VERIFYCN DC    C'VERIFY'                REPLY=UCS,,VERIFY
FOLDCN   DC    C'FOLD'                  REPLY=UCS,FOLD
CANCELCN DC    C'CANCEL'                REPLY='CANCEL'
UBLANK   DC    C'U '                    REPLY = 'U'
CBLANK   DC    C'C '                    REPLY = 'C'
VBLANK   DC    C'V '                    VERIFY='V'
UCSCONST DC    C'UCS1'                  UCS IMAGE ID PREFIX
BLANKS   DC    4C' '                    FOUR BLANKS              A33665
*   THE FIRST TWO BYTES OF A BLDL PARAMETER LIST IS THE NUMBER OF
*   ENTRIES THAT FOLLOW AND THE NEXT TWO BYTES SHOW THE LENGTH OF
*   EACH ENTRY.  IT MUST BE LONG ENOUGH FOR A 'LOAD' MACRO, BUT
*   NOT LONG ENOUGH TO OVERLAY OUR IMAGELIB DCB ADDRESS.
BLDLCNST DC    0CL4' ',YL2(1),YL2(DXIMGDCB-(DXBLDL+4))         @ZA01357
*
***********************************************************************
*
*   XCTL TABLE
*
***********************************************************************
*
MODNM1V  DC    C'1V',VL3(IGG0191V)      MODULE IGG0191V        @Z30TSCF
         SPACE
PATCH    DC    25H'0'                   PATCH AREA               Y02072
END      EQU   *                        END OF MOKULE            Y02072
         IECDSECS  DCB,EXPAND=YES                                Y02072
         EJECT
         IECDSECS MAIN,EXPAND=YES                                Y02072
         ORG   REPLYLTH+52                                       A33665
RPLY     DS    20C                                               A33665
RPLYECB  DS    F                                                 A33665
MCSMASK  DS    H                                                 A33665
LOMASK   DS    8C                                                A33665
UCSID    DS    4C                                                A33665
         ORG   MSGLSTSZ                                          Y02072
MSGAREA  DS    0CL64                    AREA TO MOVE 64 BYTES    Y02072
*                                         TO FOR WOT/WTOR'S      Y02072
         EJECT
         IGGSCW                                                  Y02072
         EJECT
         IGGMSG                                                  Y02072
         EJECT
         IECDSECS  WTG,PREFX,EXPAND=YES                        @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MOD ID              Y02072
*
***********************************************************************
*
SRT      DSECT
         IEFUCBOB
*
***********************************************************************
*
CVT      DSECT
         CVT
*
***********************************************************************
*
*   DATA EXTENT BLOCK  (UNTIL PROTIOS CHANGES)
DEB      DSECT
DEBNMSUB DS    0CL1
DEBTCBAD DS    CL4
DEBAMLNG DS    0CL1
DEBDEBAD DS    CL4
DEBOFLGS DS    0CL1
DEBIRBAD DS    CL4
DEBOPATB DS    0CL1
DEBSYSPG DS    CL4
DEBNMEXT DS    0CL1
DEBUSRPG DS    CL4
DEBPRIOR DS    0CL1
DEBECBAD DS    CL4
DEBPROTG DS    0CL1
DEBDEBID DS    0CL1
DEBDCBAD DS    CL4
DEBEXSCL DS    0CL1
DEBAPPAD DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    CL4
DEBTVLSQ DS    0CL2                VOL SEQ FOR TAPE
DEBBINUM DS    CL2
DEBTVLNM DS    0CL2                NO OF VOL FOR TAPE
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
*  FOR ADDITIONAL EXTENT ON DIRECT ACCESS THE FOLLOWING FIELDS
*  WILL HAVE TO BE OFFSET ANOTHER 16 BYTES
DEBVOLSQ DS    H
DEBVOLNM DS    H
*  FOR EACH SUBROUTINE LOADED BUMP THIS OFFSET BY 2
DEBSUBID DS    H
*
***********************************************************************
         IKJTCB
*
         END
