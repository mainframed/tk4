         TITLE 'IGG0191C-IECOSD06 OPEN EXECUTOR(DUMMY DATA SET)'
*MODULE NAME - IGG0191C                                          Y02072
*
*DESCRIPTIVE NAME - DUMMY OPEN EXECUTOR                          Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          VS2 RELEASE 2 DELETIONS                               Y02072
*
*D000204,009600,025200,026120,026150,026160,026170,027300,       Y02072
*D027340-027360,045000,045200,045400-045600,046000,049200,       Y02072
*D050600,060100,026010,027320,268-458,27400-29200,56400-56600    Y02072
*58400-588000                                                    Y02072
*                                                                YM1140
*                                                                YM7889
*          VS2 RELEASE 3 DELETIONS/CHANGES
*
*A047724,047728                                                 ZA01372
*                                                              @Z30TSCF
*A038801-038832,C039000,040199                                 @ZA07053
*000001786900-01787200,01810213,01810413-01810423,03909003,    &ZA07619
*000003927003,03948103-03948703,04181000-04182000,04358421,    &ZA07619
*000004359231,04570102-04571302,04861000,05020100-05021302     &ZA07619
*A018110,026759,026867                                         @ZA11148
*A039495,039497,039499,039501,041850,046750,033851,039501      @ZA12925
*C039150,039489,041870,045920,033850,039503                    @ZA12925
*D050324-050360                                                @ZA12925
*D040820,041084-041092                                         @ZA16037
*
*          RELEASE 20 DELETIONS
*3525024600,025400,026200,059600,060200,060600                   S20016
*          RELEASE 21 DELETIONS
*2673010000,022800-023000,023800,041200-041600,057000,059600-    S21042
*2673060600,061800-062600                                        S21042
*2673                                                            A48163
*2673                                                            A45054
* 030000,030500-030520,030660-030800,031600,032000-032400,       A46862
* 035000-035200,036400-036600,037640-037680                      A46862
*                                                                A51108
*D25000,25600,26010-26090,26244-26400                           SA68439
*A040810,041030,041040,047860,047920,047930,047940             @ZA19736
*C043520                                                       @ZA27484
*STATUS CHANGE LEVEL 007
***********************************************************************
*                                                                     *
*FUNCTION-OPERATION --OPENS A DCB FOR 'DUMMY'(I.E.,EFFECTIVE NOP  FOR *
*                     OPERATIONS ON DATA SET)                         *
*                                                                     *
*           VECTORS IN DCB ARE SET SUCH THAT THE FOLLOWING        15741
*              OPERATIONS WILL IN EFFECT BE                       15741
*                                                                     *
*             1.BSAM                                                  *
*                  WRITE     POST ECB AND RETURN                  15741
*                  NOTE      NOP (NO OPERATION)                   15741
*                  POINT     NOP                                      *
*                  READ      POST ECB AND RETURN                  15741
*                  CNTRL     NOP                                      *
*                  CHECK     WRITE - NOP                              *
*                  CHECK     READ - GO TO EODAD                       *
*                                                                     *
*             2 QSAM                                                  *
*                   GET(MOVE MODE)      GO TO EODAD                   *
*                   GET(LOCATE MODE)    GO TO EODAD                   *
*                   PUT(MOVE MODE)      NOP                           *
*                   PUT(LOCATE MODE)    ADDRESS OF BUFFER RETURNED IN *
*                                        REGISTER 1                   *
*                   PUTX                NOP                           *
*                   CNTRL               NOP                           *
*                   RELSE               NOP                           *
*                   TRUNC               NOP                           *
*                                                                     *
*             3.DCBIOBA       POINT TO DUMMY IOB WITHIN THIS ROUTINE  *
*                             ITS ECB IS POSTED COMP.NO ERROR FOR     *
*                             PRTOV WITH USER EXIT.                   *
*                                                                     *
*             4.BUFFER POOL   BUILD POOL IF REQUESTED.ONE BUFFER ONLY *
*                             FOR QSAM,FULL SIZE POOL FOR BSAM.       *
*                                                                     *
*             5.DEB           BUILD A 56 BYTE DUMMY DEB               *
*                                NONE - APPENDAGE VECTOR TABLE
*                             16 BYTE - PREFIX
*                             32 BYTE - BASIC SECTION
*                              4 BYTE - ZERO UCB ADDRESS
*                              0 BYTE - ACCESS METHOD SECTION
*                              4 BYTE - SUBROUTINE NAME SECTION
*                                       (ONLY 2 BYTES USED)
*
*           IT ALSO SETS AUDIT TRAIL BITS IN THE OPEN WORKAREA   Y02072
*           AFTER ACQUIRING RESOURCES IN CASE OF A FORCE CLOSE   Y02072
*           SITUATION.                                           Y02072
*                                                                     *
*ENTRY POINT  IGG0191C ENTERED BY XCTL                                *
*                                                                     *
*INPUT   SEE DESCRIPTION OF REGISTERS,DCB(USERS)                      *
*                                                                     *
*OUTPUT  SEE DESCRIPTION OF REGISTERS,DCB(USERS)                      *
*                                                                     *
*EXTERNAL ROUTINES  1.SUPERVISOR (GETMAIN)                            *
*                   2. SUPERVISOR (LOAD)                              *
*                                                                     *
*EXITS NORMAL - XCTL TO IGG01911 FOR DCB COPY BACK               Y02072
*                                                                     *
*EXITS ERROR: XCTL TO PROB DET (IGG0196M) FOR WTP AND 013 ABEND
*                                                                     *
*MACROS : ACTION - GETMAIN, MODESET, DEBCHK, LOAD, DMABCOND,     Y02072
*                  XCTL, XCTLTABL                                Y02072
*
*MACROS : MAPPING - CVT, IKJTCB, IECDSECS, DCBD, IEZDEB, IHAPSA, Y02072
*                   IGGPARML
*
*ATTRIBUTES - RE-ENTRANT, REUSUABLE, SUPERVISOR STATE,           Y02072
*             RUNS IN DATA MANAGEMENT KEY UNLESS OTHERWISE       Y02072
*             SPECIFIED.                                         Y02072
*
*TABLES/WORKAREAS- WHERE TO GO TABLE (WTG)
*
*      BYTE  0-7  NAME
*      BYTE  8-10 RELATIVE DISK ADDRESS(TTR) OF FIRST RECORD
*      BYTE 11    CONCATENATION NUMBER
*      BYTE 12    ZERO
*      BYTE 13    ALIAS INDICATOR AND USER DATA FIELD DESCRIPTOR.
*                        ALIAS INDICATOR 1 BIT
*                        NUMBER OF TTRS IN USERS DATA FIELD 2 BITS
*      BYTE 14-16 TTR OF FIRST TEXT RECORD
*      BYTE 17    ZERO
*      BYTE 18-19 TTR OF NOTE LIST OR SCATTER LIST
*      BYTE 20    TRANSLATION TABLE
*      BYTE 21    ZERO OR NUMBER OF ENTRIES IN THE NOTE LIST
*      BYTE 22-23 ATTRIBUTES
*      BYTE 24-26 TOTAL CONTIGUOUS MAIN STORAGE REQUIRED FOR MODULE
*      BYTE 27-28 LENGTH OF FIRST TEXT RECORD
*      BYTE 29    LENGTH OF WTG TABLE (IN DOUBLE WORDS)
*      BYTE 30-31 PATH THROUGH LOADS OF ROUTINES
*      BYTE 32-36 IDTTR OF EXECUTOR FOR FIRST DCB
*      BYTE 37-39 WORKAREA ADDRESS FOR FIRST DCB
*      BYTE 40-   TABLE OF IDTTR'S
*      BYTE 32+N(8) IDTTR OF EXECUTOR FOR N TH DCB   (5 BYTES)
*                   WORKAREA ADDRESS FOR N TH DCB    (3 BYTES)
*                   IDTTR OF LAST ROUTINE LOAD       (5 BYTES)
*                   NOT USED                         (3 BYTES)
*                                                                     *
         EJECT
*                                                                     *
*   EQUATES
*
NORLD    EQU   X'10'         NO RELOCATION NECCESSARY INDICATOR  A51108
*                                FOR LOAD
VEC1     EQU   26
DCBSVC   EQU   84
READB    EQU   X'20'
WRITEB   EQU   X'20'
LOCATEB  EQU   X'08'
OUTPUT   EQU   X'0F'
PRINTER  EQU   X'48'                   PRINTER DEVT MASK
LRECIND  EQU   X'20'                    LOGICAL RECORD INTERFACE A45054
QSAMB    EQU   X'01'                   DCBCIND2, BIT ON IF QSAM  A45054
FORMATVS EQU   X'48'                   VARIABLE SPANNED RECORDS  A45054
FLGOFFST EQU   4                       OFFSET TO FLAG BYTE IN    A48163
*                                       BUFFER CONTROL BLOCK
NOT      EQU   X'FF'                   MASK TO TURN OF BITS    @ZA07619
ADLEN    EQU   3                       LENGTH OF ADDRESS       @ZA07619
*                                      BUFFER CONTROL BLOCK    @ZA07619
DWDLEN   EQU   8                        DOUBLEWORD LENGTH        A48163
EXTND    EQU   X'40'                    FLAG TO INDICATE BUFCB   A48163
*                                         IS 16 BYTES IN LENGTH  A48163
LENNSPEC EQU   52                       CODE FOR BUFL OR BLKSE  SA53809
*                                         NOT SPECIFIED         SA53809
NOBUFCB  EQU   1                        INVALID ADDRESS          Y02072
DEBID    EQU   X'0F'                    DEB ID                  SA68439
DEBSIZE  EQU   56                       SIZE OF DEB FOR DD DUMMY S20016
PUTLOC   EQU   X'48'                   MACRF MASK,PUT LOCATE   @ZA07053
GETLOC   EQU   X'48'                   MACRF MASK, GET LOCATE  @ZA07619
OABD056  EQU   56                      INTERNAL CODE           @ZA07053
OABD061  EQU   61                      INTERNAL CODE           @ZA07619
RAFLAG   EQU   X'80'                   RECORD AREA PRESENT     @ZA07619
BLKFMT   EQU   X'10'                   DCB BLK FORMAT INDICATOR@ZA07053
VARIABLE EQU   X'40'                   VARIABLE REC INDICATOR  @ZA07053
FORMATF  EQU   X'80'                   FIXED REC INDICATOR     @ZA07053
ONE      EQU   1                       NUMBER OF EXTENTS       @ZA11148
TWO      EQU   2                       EXTENT SCALE            @ZA11148
FOUR     EQU   4                       ADDITION FACTOR         @ZA07053
         EJECT
*
* REGISTER EQUATES
*
RE       EQU   0                       WORK REGISTER/PARAM REG
R0       EQU   RE                       PARM REG FOR DMABCOND   SA53809
RF       EQU   1                       WORK REGISTER/PARAM REG
R1       EQU   RF                       PARM REGISTER            Y02072
RWK1     EQU   RF                      WORK REGISTER             Y02072
RDCB     EQU   2                       ADDR. OF CURRENT DCB
RBASE    EQU   3                       BASE REG.
RCORE    EQU   4                       ADDR. OF OPEN WORK AREA
RPAR     EQU   5                       OPEN PARAMETER LIST
RWTG     EQU   6                       D.E. TABLE FOR XCTL & LOAD
RPARC    EQU   7                       CURRENT ENTRY IN PARAM. LIST
RWTGC    EQU   8                       CURRENT ENTRY IN WTG TABLE
RTIOT    EQU   9                       WORK REG.
RWK2     EQU   RTIOT                   WORK REGISTER             Y02072
RUCB     EQU   10                      WORK REG.
RDEB     EQU   11                      WORK REG.
RWK5     EQU   RDEB                     WORK REGISTER            Y02072
RA       EQU   11                      WORK REG.
RB       EQU   12                      WORK REG
RC       EQU   13                      WORK REG
RWK6     EQU   RC                       WORK REGISTER           SA53809
RD       EQU   14                      WORK REG
RWK3     EQU   RD                      WORK REGISTER             Y02072
RJ       EQU   15                      WORK REG/PARAM REG
RWK4     EQU   RJ                      WORK REGISTER             Y02072
         EJECT
IGG0191C CSECT
         BALR  RBASE,0                 SET UP BASE REG
         USING FORCORE,RCORE
         USING *,RBASE                                           S21042
         USING PSA,0
         USING IHADCB,RDCB
**
         B     BEGIN                    BR AROUND CONSTANTS
         DC    C'IGG0191C'              MODULE NAME
         DC    C'@ZA27484'              LAST SHIP CODE         @ZA16037
         DC    C'10/27/77'              LAST DATE MODIFIED     @ZA16037
BEGIN    DS    0H
**
         L     RCORE,4(RWTGC)          GET WORKAREA
*
         SR    RWK1,RWK1                PREPARE TO CLEAR FIELD   Y02072
         ST    RWK1,DXMSGADR            ZERO SO LAST LOAD OF     Y02072
*                                         OPEN EXECS WILL NOT    Y02072
*                                         THINK MSG CSECT LOADED
*SET A BIT IN COMMON OPENS AUDIT TRAIL INDICATING TO FORCE       Y02072
*CLOSE AND PROBLEM DETERMINATION NOT TO REFRESH THE USERS DCB    Y02072
*FROM THE KEY 5 DCB BEFORE TAKING ANY USER EXITS.                Y02072
*
         OI    DXATCOM1,DXATREFR        DO NOT REFRESH DCB       Y02072
         LA    RJ,DEBSIZE               GET SIZE OF DEB          S20016
         USING PARML,RPARC
         L     RDCB,PARDCBAD            SET UP DCB ADDRESSABILITY
         GETMAIN R,LV=(15),SP=230       GET MAIN FOR DUMMY DEB   Y02072
         XC    0(DEBSIZE,RF),0(RF)      CLEAR CORE               S20016
         USING DEBPREFX,RWK1
         MVI   DEBLNGTH,(DEBSIZE+7)/8   DBL WORD LEN TO DEB     SA68439
         LA    RWK3,DEBBASIC            BASIC SECTION OF DEB    SA68439
         DROP  RWK1
         ST    RWK3,24(RWTG)            TEMP STORE OF DEB ADDR.
         MVC   DCBDEBAD+1(3),25(RWTG)  MOVE DEBAD TO DCB
         L     RWK5,PSATOLD             ADDR OF CURRENT TCB     SA68439
*
* MUST USE CS INSTRUCTION TO PUT DEB ON TCB DEB CHAIN TO PREVENT Y02072
* OVERLAYING THE ADDRESS OF ANOTHER DEB BEING ADDED BY ANOTHER   Y02072
* CPU.                                                           Y02072
*
         USING TCB,RWK5                                          Y02072
         USING DEBBASIC,RWK3                                     Y02072
         L     RWK4,TCBDEB              GET FIRST DEB ON CHAIN   Y02072
         MODESET  EXTKEY=SUPR           GET INTO SUPER KEY       Y02072
SD04110  EQU   *                                                 Y02072
         ST    RWK4,DEBDEBAD            LINK DEBS                Y02072
         CS    RWK4,RWK3,TCBDEB         INSERT DEB INTO DEB CHN  Y02072
         BNE   SD04110                  IF CS INST FAILS, TRY    Y02072
*                                         AGAIN                  Y02072
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         ST    RWK5,DEBTCBAD            TCB ADDR TO DEB          Y02072
         MVC   DEBOPATB,PAROPT          COPY OPEN ATTRIBUTES    SA68439
         NI    DEBOPATB,X'FF'-PARENLST  TURN OFF END LIST BIT   SA68439
         MVI   DEBNMEXT,ONE             NUMBER OF EXTENTS = 1  @ZA11148
         ST    RDCB,DEBDCBAD            DCB ADDR TO DEB         SA68439
         MVC   DEBPROTG,DXUKEY          PROTECT KEY TO DEB      SA68439
         OI    DEBDEBID,DEBID           DEB IDENTIFIER          SA68439
         MVI   DEBEXSCL,TWO             EXTENT SACLE = 2       @ZA11148
         DROP  RWK5,RPARC
         MVC   6(2,RWTG),SPLD          STORE ID OF DUMMY IN DE
         MVI   22(RWTG),X'CB'           PUT ATTRIBUTES IN LIST
         NI    23(RWTG),X'FF'-NORLD    RELOCATE IS REQUIRED      A51108
         MVC   14(3,RWTG),SPLD+2       STORE TTR OF DUMMY IN DE
         DEBCHK (RDCB),TYPE=ADD,AM=SAM   ADD DEB TO DEB TABLE    Y02072
*
*  LOAD IGG019AV, DUMMY ACCESS METHOD ROUTINE
*
         USING WTGMODNM,RWTG                                     Y02072
         MVC   WTGMODID,SPLD            ID TO MOD NAME           Y02072
         DROP  RWTG                                              Y02072
         L     R1,CVTPTR                LOAD CVT ADDRESS         YM1140
         USING CVT,R1                   CVT ADDRESSABILITY       YM1140
         L     R1,CVTLINK               GET LPALIB DCB ADDRESS   YM1140
         DROP  R1                       DROP CVT BASE            YM1140
**
         LOAD  EPLOC=(RWTG),DCB=(1)                              YM1140
**
**
         OI    23(RWTG),NORLD          RESET RELOCATE BIT        A51108
         MVC   DEBSUBID-DEBSUBNM+DEBBASND+L'DEBSUCBA,SPLD SUBID SA68439
         MVI   DEBNMSUB,1               NO OF ACC METH RTNS     SA68439
         DROP  RWK3
         USING ADUMMY,RTIOT            SET THE BASE              A46862
         LR    RTIOT,RE                 SAVE ADDR.OF IGG019AV
         L     RD,ADUMPUT              LOAD DUMMY PUT ADDR       A46862
         ST    RD,24(RWTG)             TEMP.STORE OF DUMMY ADDR. A46862
         MVC   DCBCNTRL+1(3),25(RWTG)  LOAD ADDR.OF CNTRC/NOTE/POINT
         MVC   DCBREAD+1(3),25(RWTG)   LOAD ADDR.OF RD/WR/GET/PUT
         TM    DCBMACRF,X'02'          USING READ/CNTRL          A28185
         BO    SD04120                 YES,BRANCH                A28185
         TM    DCBMACRF+1,X'02'        USING WRITE/CNTRL         A28185
         BZ    SD04121                 NO,BRANCH                 A28185
SD04120  L     RD,ADUMCNTR             ADDR OF DUMMY CNTRL       A46862
         ST    RD,24(RWTG)             TEMP STORE OF CNTRL ADDR  A28185
         MVC   DCBCNTRL+1(3),25(RWTG)  LOAD ADDR OF CNTRL RTN    A28185
SD04121  L     RD,ADUMCHEK             ADDR OF CHECK RTN         A46862
         ST    RD,24(RWTG)              TEMP STORE OF CHECK ADDR.
         MVC   DCBCHECK+1(3),25(RWTG)  LOAD ADDR.OF CHECK/GERR/PERR)
*
         L     RUCB,ADUMIOB            LOAD ADDR OF DUMMY IOB    A46862
         ST    RUCB,DCBIOBA             STORE IOB ADDR.INDCB
*
SD04130  TM    DCBMACRF,READB           IS THIS BSAM
         BC    1,SD04132                YES BRANCH
         TM    DCBMACRF+1,WRITEB        NO
         BC    1,SD04132                YES BRANCH
*
         OI    DCBCIND2,DCBCNQSM        SET QSAM INDICATOR       YM5574
         SR    RB,RB                    CLEAR REGISTER            M1552
         IC    RB,DCBBUFNO              GET NUMBER OF BUFFERS     M1552
         LTR   RB,RB                    IS BUFNO EQUAL TO ZERO    M1552
         BNE   NODEFALT                 NO, BRANCH AROUND         M1552
         LA    RB,5                     DEFAULT IS 5 BUFFERS   @ZA12925
         STC   RB,DCBBUFNO              NUMBER OF BUF TO DCB   @ZA12925
NODEFALT EQU   *                                                  M1552
         TM    0(RPARC),OUTPUT          QSAM OUTPUT
         BC    14,SD04131               NO, BRANCH INPUT OR UPDATE
*
         TM    DCBMACRF+1,LOCATEB       PUT LOCATE
         BZ    SD04200                 NO BRANCH TO GET BUF POOL
         L     RD,ADUMPUTL             LOAD DUMMY PUT LOCATE ADD A46862
         ST    RD,24(RWTG)              TEMP.STORE OF PUT ADDR.
         MVC   DCBPUT+1(3),25(RWTG)    LOAD ADDR OF PUT
         B     SD04200                  BRANCH TO GETT POOL
*
SD04131  EQU   *
         L     RD,ADUMGET              ADDR OF DUMMY GET RTN     A46862
         ST    RD,24(RWTG)              TEMP.STORE OF GET ADDR.
         MVC   DCBGET+1(3),25(RWTG)    LOAD ADDR OF GET
         B     SD04200                 BRANCH TO GET POOL
*
SD04132  EQU   *
         L     RD,ADUMBSAM             LOAD ADDR OF DUM. BSAM    A46862
         ST    RD,24(RWTG)              TEMP.STORE OF ADDRESS     15741
         MVC   DCBREAD+1(3),25(RWTG)    MOVE ADDRESS TO DCB       15741
         SR    RB,RB                    CLEAR RB
         IC    RB,DCBBUFNO              GET NO.BUFFERS REQUESTED
         LTR   RB,RB                    ANY BUFFERS REQUIRED
         BZ    SD04END1                 NO BRANCH TO END
SD04200  EQU   *
         TM    DCBCIND2,QSAMB           IS THIS QSAM?          @ZA07053
         BNO   SD04210                  NO - BY PASS BLOCK CHK @ZA07053
         TM    DCBMACRF+1,PUTLOC        PUT, IN LOCATE MODE    @ZA07053
         BNO   SD04203                  NO - BYPASS BLOCK CHK  @ZA07619
         TM    DCBRECFM,BLKFMT          BLOCK FORMAT?          @ZA07053
         BNO   SD04204                  NO - BYPASS BLOCK CHK  @ZA12925
         TM    DCBRECFM,VARIABLE        "V" RECORDS            @ZA07053
         BNO   FIXED                    NO - CHECK FOR FIXED   @ZA07053
         TM    DCBRECFM,FORMATVS        IS THIS SPAN RECS?     @ZA07053
         BO    SD04205                  YES - BYPASS BLOCK CHK @ZA07619
         LH    RC,DCBLRECL              OBTAIN LRECL           @ZA07053
         LA    RD,FOUR                  ADD LENGHT OF BDW      @ZA07053
         AR    RC,RD                                           @ZA07053
         CH    RC,DCBBLKSI              IS LRECL GT BLKSIZE?   @ZA07053
         BNH   SD04210                  NO - CONTINUE          @ZA07053
         LA    RE,OABD056               YES - ISSUE ABEND      @ZA07053
         B     ERROR                                           @ZA07053
SD04203  TM    DCBMACRF,GETLOC         GET, LOCATE MODE?       @ZA07619
         BNO   SD04210                 NO - CONTINUE           @ZA07619
SD04204  TM    DCBRECFM,FORMATVS       IS THIS SPANNED REC?    @ZA12925
         BNO   SD04210                 NO - CONTINUE           @ZA07619
SD04205  TM    DCBBUFCB+ADLEN,NOBUFCB  BUFCB SPECIFIED?        @ZA07619
         BO    FIXED                   NO - GET THE BUFFERS    @ZA12925
         MODESET  KEYADDR=DXUKEY,WORKREG=15  GOTO USER KEY     @ZA12925
         L     RJ,DCBBUFCB             GET BCB ADDRESS         @ZA12925
         TM    FLGOFFST(RJ),RAFLAG     RECORD ARE PRESENT?     @ZA12925
         BO    SD04285                 YES, EXIT               @ZA12925
         TM    DCBBFTEK,LRECIND        RECORD AREA NEEDED?     @ZA12925
         BNO   SD04285                 NO, EXIT                @ZA12925
         TM    FLGOFFST(RJ),EXTND      SPACE AVAILABLE FOR LRI?@ZA12925
         BNO   DCBERROR                NO,SET DCB IN ERROR     @ZA12925
         B     GETRALEN                BRANCH TO GET RECORD    @ZA12925
*                                      AREA ONLY               @ZA07619
FIXED    EQU   *                        CHECK FIXED BLOCK FMT  @ZA07053
         TM    DCBRECFM,FORMATF         FIXED REC ?            @ZA07053
         BZ    SD04210                  NO - CONTIUE           @ZA07053
         SR    RD,RD                    CLEAR REGISTER         @ZA07053
         LH    RJ,DCBBLKSI              OBTAIN BLKSIZE         @ZA07053
         LTR   RJ,RJ                    CHECK FOR BLKSIZE=ZERO @ZA07053
         BZ    SD04210                  BRANCH IF ZERO         @ZA07053
         LH    RA,DCBLRECL              OBTAIN LRECL           @ZA07053
         LTR   RA,RA                    CHECK FOR LRECL=ZERO   @ZA07053
         BZ    SD04210                  BRANCH IF ZERO         @ZA07053
         DR    RD,RA                    DIVIDE BLKSIZE BY LRECL@ZA07053
         LTR   RD,RD                    IS THERE A REMAINDER ? @ZA07053
         BZ    SD04210                  NO - CONTINUE          @ZA07053
         LA    RE,OABD056               YES - ISSUE ABEND      @ZA07053
         B     ERROR                                           @ZA07053
SD04210  TM    DCBBUFCB+3,1             IS THERE A BUF.CNTL.BLK@ZA07053
         BZ    SD04END1                 YES BRANCH TO END
         LH    RC,DCBBUFL               GET BUFFER LENGTH      @ZA07053
         LTR   RC,RC                    HAS IT BEEN SPECIFIED
         BC    7,SD04220                BRANCH ON YES
         LH    RC,DCBBLKSI              GET LENGTH FROM BLOCK SIZE
         LTR   RC,RC                   IS BLOCK SIZE SPECIFIED
         BP    SD04220                 YES BRANCH TO GET POOL
         LA    RWK2,NOBUFCB            INIT REG FOR NOT PUTL   @ZA19736
*
*  TEST FOR QSAM OUTPUT WITH LOCATE MODE. IF YES,ABEND USER
*  TO KEEP HIM FROM ABENDING WHEN IGG019AV DOES NOT PASS HIM
*  A VALID BUFFER ADDRESS BECAUSE SOME DCB FIELDS ARE NOT SPECIFIED
*                                                              @ZA19736
*
         TM DCBMACF2,PUTLOC + DCBMRLCP    PUT LOCATE?          @ZA19736
         BNO SD04220                      NO BYPASS DMACOND &  @ZA19736
*                                         DO GETMAIN!          @ZA19736
         LA    R0,LENNSPEC              INTERNAL ABEND CODE     SA53809
ERROR    DMABCOND  (R0),PDLOAD,RETURN=NONE,RES=NO,REGSAVE=YES   SA53809
         B     TCTLRTN                  GO TO PROB DETERMINATION S21042
**
DCBERROR MODESET  EXTKEY=DATAMGT       DATA MGT KEY            @ZA12925
         LA    RE,OABD061              LOAD INTERNAL CODE      @ZA12925
         B     ERROR                                           @ZA07619
*
SD04220  LA    RC,7(0,RC)               ADJUST
         SRL   RC,3                            TO
         SLL   RC,3                               DOUBLE ALIGNMENT
*
         LR    RD,RB                     SAVE LENGTH AND NUMBER
         LR    RJ,RC
         MR    RD,RD                    COMPUTE TOTAL LENGTH
*
         TM    DCBCIND2,QSAMB          IT IT QSAM               A45054
         BNO   DEFAULT                                         @ZA27484
         TM    DCBBFTEK,LRECIND        LRI REQUESTED            A45054
         BNO   ALIGN                                            A45054
         TM    DCBRECFM,FORMATVS       VARIABLE SPANNED         A45054
         BNO   DEFAULT                 BRANCH TO TURN OFF LRI  @ZA07619
*              ALLOW 8 EXTRA BYTES FOR EXTENDED CONTROL BLOCK
         B     SD04230                                          A45054
DEFAULT  NI    DCBBFTEK,NOT-LRECIND    TURN OFF LRI            @ZA07619
*
ALIGN    EQU   *                                                A45054
*
         TM    DCBBFTEK,1               FULL WORD ALLIGNMENT REQUIRED
         BC    1,SD04230                BRANCH ON YES
         LA    RJ,8(0,RJ)               ADD 8 ON NO
         B     SD04240
*
SD04230  LA    RJ,16(0,RJ)              ADD  16  ON  YES
         OI    FCAOPEN,FCAOXTND         INDIC BCB IS EXTENDED    Y02072
SD04240  EQU   *                                                 Y02072
         MODESET  KEYADDR=DXUKEY,WORKREG=11  GET INTO USER KEY   Y02072
*
         GETMAIN R,LV=(15),SP=0                                  Y02072
*
SD0426A  EQU   *                                               &ZA07619
         TM    DCBBUFCB+3,NOBUFCB      BUFCB SPECIFIED?        &ZA07619
         BO    SD04246                 NO, GO FORMAT POOL      &ZA07619
         L     RJ,DCBBUFCB             YES, GET BUFCB ADDRESS  &ZA07619
         OI    FLGOFFST(RJ),RAFLAG     SET FLAG SHOWING        &ZA07619
*                                      REC ADDR PRESENT        &ZA07619
         ST    RF,8(RJ)                STORE REC-AREA ADDRESS  &ZA07619
         LH    RJ,DCBLRECL             LOAD LRECL IN WORK REG  &ZA07619
         LA    RJ,32(RJ)               ADD 32 BYTE CNTRL FIELD &ZA07619
         ST    RJ,0(RF)                STORE LRECL IN REC-AREA &ZA07619
         XC    4(32,RF),4(RF)          CLEAR CNTRL FIELD IN    &ZA07619
*                                      RECORD AREA             &ZA07619
         B     SD04285                 EXIT                    @ZA12925
SD04246  ST    RF,0(0,RF)              TEMP. STORE START OF BUF. POOL
         LR    RWK2,RWK1                SAVE FIRST BUFFER ADDR   Y02072
         STH   RB,4(0,RF)              STORE BUFNO IN BUF CONTROL BLK
         STH   RC,6(0,RF)              STORE LNTH IN BUF CONTROL BLK
         SPACE 2
         MODESET  EXTKEY=DATAMGT        AUDIT TRAIL IN KEY 5     Y02072
         SPACE 2
         OI    FCAOPEN,FCAOBUF          INDIC BUFFERS GETMAINED  Y02072
         STCM  RWK2,B'0111',DCBBUFCA    BCB ADR TO DCB         @ZA12925
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=10  BACK TO USER KEY    Y02072
         SPACE 2
         LR    RUCB,RF                  GET ADDR.IN WOKR REG.
         LA    RUCB,8(0,RUCB)           POINT TO SECOND DOUBLE WORD
         TM    DCBBFTEK,1               DOUBLE WORD ALLIGN.
         BC    8,TSTLRI                 YES BRANCH               A48163
         LA    RUCB,4(0,RUCB)           POINT TO FULL NOT DOUBLE
         B     EXTEND                   GO SET EXTEND FLAG       A48163
TSTLRI   EQU  *                                                 ZA01372
         TM    DCBCIND2,DCBCNQSM        IS IT QSAM              ZA01372
         BNO   SD04250                  NO, DO NOT TEST LRI     ZA01372
         TM    DCBBFTEK,LRECIND         LRI SUPPORTED           ZA01372
         BNO   SD04250                  NO, BRANCH TO SAVE FIRST A48163
*                                        BUFFER ADDRESS          A48163
         LA    RUCB,DWDLEN(0,RUCB)      YES, OFFSET TO NEXT DW   A48163
EXTEND   OI    FLGOFFST(RF),EXTND       SET FLAG TO INDICATE     A48163
*                                       BUFFER CONTROL BLOCK EXTENDED
*
*          THE NEXT SEQUENCE OF INSTRUCTIONS BUILDS A BUFFER POOL
SD04250  EQU   *
         LTR   RC,RC                    TST FOR ZERO BUFLN     @ZA19736
         BNZ   SD04255                  BUFLN ZERO BRANCH!     @ZA19736
         ST    RC,0(0,RF)               ZERO NXT BUF PTR       @ZA19736
         B     SD04285                  LEAVE                  @ZA19736
SD04255  ST    RUCB,0(RF)               STORE BUFFER ADDR.     @ZA19736
         LR    RF,RUCB                  SAVE NEXT BUFFER ADDR.
SD04260  EQU   *
         BCT   RB,SD04280               IF MORE BRANCH
         B     SD04283                  EXIT                   &ZA07619
SD04280  EQU   *
         ALR   RUCB,RC                  BUFF LEN + BUFF ADDR.
         ST    RUCB,0(RF)               STORE NEXT BUFF IN LINK
         ALR   RF,RC                    UPDATE TO NEXT LINK
         B     SD04260
SD04283  TM    DCBBFTEK,LRECIND        RECORD AREA NEEDED?     &ZA07619
         BNO   SD04284                 NO, EXIT                &ZA07619
         L     RJ,DCBBUFCB             YES,GET BUFFER CNTL BLK &ZA07619
         TM    FLGOFFST(RJ),EXTND      SPACE AVAILABLE FOR     &ZA07619
*                                      RECORD AREA?            &ZA07619
         BNO   DCBERROR                NO, ABEND               &ZA07619
*                                                              &ZA07619
GETRALEN EQU   *                                               &ZA07619
         LH    RJ,DCBLRECL             LOAD LRECL IN WRK REG   &ZA07619
         LA    RJ,32(RJ)               ADD 32 BYTES FOR CNTRL  &ZA07619
*                                      INFORMATION             &ZA07619
         B     SD04240                 GET SPACE FOR REC AREA  &ZA07619
*                                                              &ZA07619
SD04284  EQU   *                                               &ZA07619
         ST    RB,0(RF)                LAST LINK TO ZERO       &ZA07619
SD04285  EQU   *                                                 Y02072
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
***
SD04END1 EQU   *                                                 Y02072
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,LASTLOAD        MOVE ID TO WTG ENTRY   @Z30TSCF
         LR    RA,RBASE                SET RA FOR POSSIBLE REPEAT
**
*
*          THIS IS AN OPEN PROVIDED SECTION OF CODE THAT SETS UP XCTL
*
*********************************************************************
*
*
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)    INCREMENT TO NXT ENTRY IN WRG
         LA    RPARC,PLOFF(0,RPARC)    INCREMENT TO NXT DCB IN PARAM
         CLC   0(2,RWTGC),AMIDCNST     IS NXT ALSO DUMMY
         BCR   8,RA                    YES RETURN TO START
*
         CLC   0(2,RWTGC),OPIDCNST     IS NXT END OF WTG
         BC    7,RELOOP                NO RETURN TO TEST NXT ENTRY
*
         LR    RPARC,RPAR              INITIALIZE PARAM REGISTER
         LA    RWTGC,WAOFF(0,RWTG)     INIT. WTGC REG TO START OF WTG
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN           IF NOT TRANSFER CONTROL
*
         LA    RWTGC,WGOFF(0,RWTGC)    INCREMENT TO NXT ENTRY IN WTG
         LA    RPARC,PLOFF(0,RPARC)    INC. TO NXT DCB IN PARAM LIST
         BC    15,ZCHEK
*
TCTLRTN  EQU   *
*
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
WAOFF      EQU   32
PLOFF    EQU   4                   OFFSET OF DCB ENTRIES
WGOFF    EQU   8                   OFFSET OF WTG ENTRIES
OPIDCNST DC    C'0S'
AMIDCNST DC    C'1C'
SPLD     DC    C'AV'                    DUMMY ROUTINE            Y02072
PDLOAD   DC    C'6M',VL3(IGG0196M)      PROB DETER             @Z30TSCF
LASTLOAD DC    C'11',VL3(IGG01911)      DCB COPY BACK ROUTINE  @Z30TSCF
*
PATCH    DC    25H'0'                   PATCH AREA               Y02072
END      EQU   *                        END OF MOSULE            Y02072
         SPACE 6
ADUMMY   DSECT                                                   A46862
ADUMPUT  DS    A                   ADDR OF DUMMY PUT RTN         A46862
ADUMPUTL DS    A                   ADDR OF DUMMY PUT LOCATE RTN  A46862
ADUMGET  DS    A                   ADDR OF DUMMY GET RTN         A46862
ADUMCHEK DS    A                   ADDR OF DUMMY CHECK RTN       A46862
ADUMIOB  DS    A                   ADDR OF DUMMY IOB RTN         A46862
ADUMBSAM DS    A                   ADDR OF DUMMY BSAM RTN        A46862
ADUMCNTR DS    A                   ADDR OF DUMMY CNTRL RTN       A46862
*
         EJECT
         IHAPSA
         EJECT
         IGGPARML
         EJECT
CVT      DSECT
***
         CVT
***
         IECDSECS MAIN,WTG,DCB,PREFX,EXPAND=YES                @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MOD ID              Y02072
         EJECT
FORCORE  DSECT                                                   Y02072
         IHAFCAUD  ORG=YES                                       Y02072
         EJECT
         IKJTCB                                                  Y02072
         EJECT
         IEZDEB                                                  Y02072
         END
