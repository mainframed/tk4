  TITLE  'IGG0191J - BSAM, STAGE 2 OPEN EXEC, DIRECT ACCESS, OUTIN,    ,
               INOUT'
IGG0191J CSECT
*MODULE NAME - IGG0191J                                          Y02072
*
*DESCRIPTIVE NAME - BUILD IOBS                                   Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          VS2 RELEASE 2 DELETIONS                               Y02072
*000430,037400,037600,037800-038000,039860-040040,041360,        Y02072
*041420,041850,042650,042700,042750,045400,053690,055690,        Y02072
*056690,086000,086100,086105-086185,085300,57190,62800,          Y02072
*63800,86100                                                     Y02072
*                                                                YM4697
*                                                                YA3136
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*          RELEASE 20 DELETIONS                                       *
*0973012800,016400,017200-018800,020400-021600,024200-025400,    S20201
*0973026400-027000,029400,030200-037200,037600,038800-041400,    S20201
*0973042200,043600-048400,050000-054200,055400-055800,056600-    S20201
*0973057000,059200,063400,066800,071000,071400,073000,079000-    S20201
*0973083200                                                      S20201
*          RELEASE 21 DELETIONS                                       *
*0082041400,042700,055190,089010-089160                          A37507
*0082041390-044400,056190,084800-085800                          M0081
*                                                                A37507
* STATUS CHANGE LEVEL 009
*
*   THIS MODULE WAS SPLIT INTO IGG0196L FOR REL 21.
*FUNCTION/OPERATION: THIS ROUTINE WILL GET CORE(GETMAIN) TO SET UP THE
*      NECESSARY IOB'S. IT WILL CLEAR THE CORE AND LINK THE IOB'S
*      TOGETHER.(DIRECT ACCESS)
*      IT SETS CODES  IN THE DCBCNTRL FIELD OF THE DCB. IT WILL
*      CONSTRUCT - THE SET SECTOR COMMAND (MERLIN/ZEUS)
*                - THE SEARCH COMMAND
*                - THE TIC BACK TO SEARCH COMMAND
*                - THE READ COMMANDS
*      IT SETS AN AUDIT TRAIL BIT IN THE OPEN WORKAREA           Y02072
*      INDICATING TO FORCE CLOSE THAT THE CORE GETMAINED FOR     Y02072
*      THE IOB'S CAN BE FREEMAINED.                              Y02072
*      IT THEN XCTL'S TO IGG0196L FOR COMPLETION OF THE IOB'S
*
*ENTRY POINTS:  IGG0191J BY XCTL FROM IGG0193I OR IGG0196B.
*
*INPUT: SEE DESCRIPTION OF REGISTERS, DCB(USERS).
*
*OUTPUT: SEE DESCRIPTION OF REGISTERS, DCB(USERS), IOB(USERS).
*
*MACROS - ACTION : MODESET, GETMAIN, XCTL, XCTLTABL              Y02072
*
*MACROS - MAPPING : IECDSECS, DCBD, IEZIOB, IECDSECT, IHAFCAUD   Y02072
*
*EXTERNAL ROUTINES : NONE
*
*EXITS-NORMAL: XCTL TO IGG0196L
*
*EXITS-ERROR: NONE
*
*TABLES/WORKAREAS: DUPE WTG TABLE**************************************
*      SEE DEVICETB DSECT.
*
*ATTRIBUTES: REENTRANT, REUSABLE, SUPERVISOR STATE,              Y02072
*            RUNS IN USER KEY UNLESS OTHERWISE SPECIFIED.        Y02072
*
* NOTES- THIS EXECUTOR DEALS WITH DIRECT ACCESS FOR IN/OUT - OUT/IN.
*
         SPACE 1
*
*                                                                     *
*    2841  OP  CODES                                                  *
*                                                                     *
TIC      EQU   X'08'          TIC OP CODE
WRTCK    EQU   X'80'
SRCHE    EQU   X'31'          SEARCH OP CODE
READATA  EQU   X'86'          READ DATA CCW
READKEDA EQU   X'8E'                    READ KEY AND DATA OP CODE
READCOUN EQU   X'92'          READ COUNT OP CODE
SETSC    EQU   X'23'                    SET SECTOR COMMAND       S20201
VFORMAT  EQU   X'40'                    MASK FOR V FORMAT        S20201
*                                                                     *
***********************************************************************
*                                                                     *
*    CCW  CONSTRUCTION  EQUATES                                       *
*                                                                     *
SILI     EQU   X'20'                   SILI FLAG
CMDSKPSL EQU   X'70'                    COMMAND SKIP AND SILI
COMSILI  EQU   X'60'         COMMAND AND SILI
COMCHAIN EQU   X'40'          COMMAND CHAIN BIT
ONE      EQU   1
FIVE     EQU   5
ATE8     EQU   8
CC       EQU   X'40'                    COMMAND CHAIN            S20201
***********************************************************************
*                                                                     *
*    MISC  EQUATES                                                    *
*                                                                     *
MINKEY   EQU   X'01'
UFORMAT  EQU   X'C0'                    U  FORMAT TEST  MASK
BWCHP    EQU   4                        BASIC WRITE CHP LENGTH   S20201
BIOB     EQU   16                       BASIC IOB LENGTH         S20201
MAX      EQU   256                      MAX VALUE FOR LENGTH FLD S20201
NOERR    EQU   X'7F'                    ECB INDICATION OF NO     S20201
*                                       ERROR                    S20201
NONCARN  EQU   X'20'                    NO DISCONNECT USED       S20201
SDOPT    EQU   X'08'                    SEARCH DIRECT OPTION     S20201
*                                       BYTE                     S20201
UNRELFLG EQU   X'02'                   IOB UNRELATED FLG         A37507
***********************************************************************
*
* DISPLACEMENTS AS EQUATES
*
D0       EQU   0                        DISPLACEMENT OF ZERO     S20201
D1       EQU   1                        DISPLACEMENT OF ONE      S20201
D2       EQU   2                        DISPLACEMENT OF TWO      S20201
D3       EQU   3                        DISPLACEMENT OF THREE    S20201
D4       EQU   4                        DISPLACEMENT OF FULLWORD S20201
D5       EQU   5                        DISPLACEMENT OF FIVE     S20201
D6       EQU   6                        DISPLACEMENT OF SIX      S20201
D8       EQU   8                        DISPLACEMENT OF DW       S20201
D12      EQU   12                       DISPLACEMENT OF THREE    S20201
*                                       WORDS                    S20201
D15      EQU   15                       DISPLACEMENT OF FIFTEEN  S20201
D24      EQU   24                       DISPLACEMENT OF SIX      S20201
*                                       WORDS                    S20201
D40      EQU   40                       DISPLACEMENT OF TEN      S20201
*                                       WORDS                    S20201
D43      EQU   43                       DISPLACEMENT OF CCHHR    S20201
*                                       IOB                      S20201
D44      EQU   44                       DISPLACEMENT OF CP PTR   S20201
FLAGOFF  EQU   4                        CCW FLAG OFFSET          S20201
CNTOFF   EQU   7                        CCW COUNT OFFSET         S20201
***********************************************************************
***********************************************************************
*
*
* REGISTER CONVENTIONS USED THROUGHOUT ALL OPEN PASSES
*
*
***********************************************************************
*
RE       EQU   0
RF       EQU   1                        WORK REG ONE             S20201
RWK1     EQU   RF                      WORK REGISTER             Y02072
RDCB     EQU   2                        DCB REGISTER             S20201
RBASE    EQU   3                        BASE REGISTER            S20201
RCORE    EQU   4                        WORK AREA ADDR           S20201
RWTG     EQU   6
RPARC    EQU   7                        CURRENT PARAMETER        S20201
*
RWTGC    EQU   8
RWK2     EQU   9                       WORK REGISTER             Y02072
RUCB     EQU   10                       ADDRESS OF UCB           S20201
RDEB     EQU   11                       ADDRESS OF DEB           S20201
RB       EQU   12 2
RWK3     EQU   RB                      WORK REGISTER             Y02072
RC       EQU   13 3
RD       EQU   14 4
RJ       EQU   15 5
*
*
***********************************************************************
*
***********************************************************************
*
*
         USING SOP810,RBASE                                      S20201
         USING IHADCB,RDCB              REG 2 IS DCB             S20201
         USING FORCORE,RCORE            REG 4 IS WORK AREA ADDR  S20201
         BALR  RBASE,RE                 REG 3 IS BASE   RE=0     S20201
*
SOP810   EQU   *                                                 S20201
*
         B     BEGIN                    BR AROUND CONSTANTS      Y02072
         DC    C'IGG0191J'              MODULE NAME              Y02072
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'10/18/74'              LAST DATE MODIFIED     @Z30TSCF
BEGIN    DS    0H                                                Y02072
         L     RDCB,D0(RPARC)           GET DCB ADDR             S20201
         LA    RDCB,D0(RDCB)            CLEAR HI BYTE            S20201
         L     RCORE,D4(RWTGC)          GET WORK AREA ADDR       S20201
*
*
*
*
         SR    RD,RD
         ST    RD,DCBCNTRL              CLEAR CONTROL            S20201
         NI    DCBCIND2,X'FE'           TURN OFF QSAM BIT IF ON
         MVI   DCBCNTRL+3,2             READ EOB RT ID
         MVI   DCBCNTRL+2,1             ID OF WRT EOB RT.
         TM    DCBMACRF,X'04'           NOTE POINT USED
         BC    1,SOP810A                YES BRANCH
         TM    DCBMACRF+1,X'04'
         BC    14,SOP810B               NO BRANCH
SOP810A  EQU   *
         MVI   DCBCNTRL+1,5             NPDT ID NO.
SOP810B  EQU   *
         LA    RC,BIOB                  BASIC IOB SIZE           S20201
         LA    RD,BWCHP                 BASIC WRT CHP SIZE       S20201
*
*
*
*                                  BSAM USED
         TM    JFCBMASK+D6,NONCARN      ANY DEVICES W/O          S20201
*                                       DISCONNECT               S20201
         BO    SOP811                   BRANCH IF YES            S20201
         LA    RC,D5(RC)                INCREMENT FOR 5 CCW      S20201
         LA    RD,D2(RD)                INCREMENT FOR 2 CCW      S20201
*
*
SOP811   EQU   *                                                 S20201
         TM    DCBOPTCD,WRTCK           WRITE CHK USED           S20201
         BNO   SOP815                   BRANCH IF NO             S20201
         LA    RC,D3(RC)                INCREMENT FOR 3 CCW      S20201
         LA    RD,D3(RD)                INCREMENT FOR 3 CCW      S20201
*
         TM    JFCBMASK+D6,NONCARN      ANY DEVICES W/O          S20201
*                                       DISCONNECT               S20201
         BO    SOP815                   BRANCH IF YES            S20201
         LA    RC,D1(RC)                IOB+1 DW                 S20201
         LA    RD,D1(RD)                WRT CHP +1  DW           S20201
SOP815   EQU   *                                                 S20201
*
         TM    JFCBMASK+D6,SDOPT       SEARCH DIRECT USED        S20201
         BZ    SOP816                   BRANCH IF NO             S20201
*                                       SD                       S20201
         LA    RC,D2(RC)                INCREMENT FOR TWO CCWS   S20201
SOP816   EQU   *                                                 S20201
*
* STORE DW LENGTHS THEN CALCULATE  IOB SIZE TIMES NO. OF IOBS
*
         STC   RC,DCBIOBL               IOB LENGTH               S20201
         STC   RD,DCBWCPL               WRITE CHP LENGTH         S20201
*
         SR    RUCB,RUCB                CLEAR REG                S20201
         IC    RUCB,DCBNCP              GET NO. OF CHPS          S20201
         LTR   RUCB,RUCB                BEEN SPECIFIED           S20201
         BNZ   SOP817                   BRANCH IF SPECIFIED      S20201
         LA    RUCB,ONE                 FURNISH ONE IF NOT       S20201
SOP817   EQU   *                                                 S20201
         LA    RDEB,ATE8                CONSTANT OF EIGHT        S20201
         MR    RB,RDEB                  IOB DW SIZE TIMES  8     S20201
         LR    RE,RC                    KEEP SIZE                S20201
         MR    RB,RUCB                  SIZE TIMES NO. OF IOBS   S20201
*
         TM    JFCBMASK+D6,NONCARN      ANY DEVICES W/O          S20201
*                                       DISCONNENT               S20201
         BO    SOP821                   BRANCH IF YES            S20201
         LA    RC,D8(RC)                INCREMENT 1 DW           S20201
SOP821   EQU   *                                                 S20201
         LR    RB,RC                    SAVE AMOUNT              S20201
         LR    RDEB,RE                  SAVE IOB SIZE            S20201
*
*  THE FOLLOWING SAVES THE AMOUNT OF CORE GETMAINED FOR THE IOBS YM4697
*  IN THE FORCE CLOSE AUDIT TRAIL FOR THE FORCE CLOSE EXECUTOR.  YM4697
*
         ST    RC,DXATEXC2              SAVE LENGTH              YM4697
         MODESET  KEYADDR=DXUKEY,WORKREG=9 GET INTO USER KEY     Y02072
*
         GETMAIN R,LV=(RC),SP=0                                  Y02072
*
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
*
***********************************************************************
* THE FOLLOWING CODE WILL CLEAR STORAGE RECEIVED FROM GETMAIN
*
         ST    RF,DCBIOBA               ADDR IOB TO DCB          S20201
         OI    FCAOPEN,FCAOIOB          INDIC IOB'S CAN BE FREED Y02072
         LR    RD,RF                    COPY ADDRESS USE TO CLR  S20201
         LR    RE,RDEB                  RESTORE IOB SIZE         S20201
         LA    RDEB,ATE8                CONSTANT OF EIGHT        S20201
*
* SET UP THETA ADDRS, MAY BE USED LATER
*
         LR    RJ,RB                    COPY AMOUNT              S20201
         AR    RJ,RF                    GET AREA +1 ADDR         S20201
         SR    RJ,RDEB                  SUB 1 DW                 S20201
         ST    RJ,DXCCW11               TEMP STORE SET THETA     S20201
*                                       ADDR                     S20201
*
* CLEAR CORE HERE     RB=AMT    RD=ADDR
*
         MODESET  KEYADDR=DXUKEY,WORKREG=9 GET INTO USER KEY     Y02072
         LA    RJ,MAX                   CONSTANT OF 256          S20201
         B     CLEAR                    GO AROUND EXEC MODELS    S20201
SOP8LO   XC    D0(ONE,RD),D0(RD)        CLEARS REMAINIG BYTES    S20201
SOP8HI   XC    D0(MAX,RD),D0(RD)        CLEARS 256 BYTES         S20201
         AR    RD,RJ                    ADD 256 TO ADDR          S20201
CLEAR    SR    RB,RJ                    SUB 256 FROM AMT         S20201
         BH    SOP8HI                   IF AMT GT 256 BRANCH     S20201
         AR    RB,RJ                    CORRECT AMT              S20201
         BCTR  RB,RE                    REDUCE BY ONE BYTE       S20201
         EX    RB,SOP8LO                CLEAR REMAINING BYTES    S20201
         LR    RB,RF                    COPY ADDR                S20201
         USING IOBQSAMN,RF             USE IOB PREFIX DSECT      Y02072
         ST    RB,IOBNIOBA             STORE FIRST IOB ADDR IN   Y02072
*                                      FIRST IOB FOR THE CASE    M0081
*                                      OF ONE IOB - INSTRUCTION  M0081
*                                      UNNECESSARY IF MORE THAN  M0081
*                                      ONE IOB                   M0081
         DROP  RF                                                Y02072
         B     SOP830                  BYPASS LINKING IOB'S      M0081
*
*      BEGIN LOOP TO CONSTRUCT IOB'S
*
SOP829   EQU   *                                                 M0081
         LR    RB,RF                    GET POINTER TO LAST      M0081
*                                        IOB BUILT               M0081
         AR    RB,RE                   NEXT IOB TO BE BUILT      M0081
         USING IOBQSAMN,RF             ADDRESSABILITY FOR DSECT  Y02072
         ST    RB,IOBNIOBA             LINK IOB'S                Y02072
         DROP  RF                                                Y02072
         LR    RF,RB                   NEXT IOB TO BE BUILT      M0081
SOP830   EQU   *                       COME HERE IF FIRST IOB    M0081
         LA    RB,D4(RB)                PT TO ECB                S20201
         ST    RB,D12(RF)               PUT PTR IN IOB           S20201
         MVI   D0(RB),NOERR             INDICATE COMP NO ERROR   S20201
*
         L     RWK2,DXUDCBAD           GET USERS DCB ADDRESS     Y02072
         USING IOBNECB,RB                                        Y02072
         ST    RWK2,IOBDCBPT           DCB ADDR TO IOB           Y02072
         DROP  RB                                                Y02072
         LA    RB,D44(RB)               PT TO CHP STARTAD        S20201
         ST    RB,D24(RF)               PUT ADDR IN IOB          S20201
         MVC   D40(ATE8,RF),DCBFDAD     INITIALIZE IOBFDAD       S20201
*
         TM    JFCBMASK+D6,NONCARN      ANY DEVICES W/O          S20201
*                                       DISCONNECT               S20201
         BO    SOP831                   BRANCH IF YES            S20201
         BAL   RD,SOP8STA               IF NO GO BUILD SET CMND  S20201
*
* UPON RETURN SET UP TO BUILD SRCH CMND AND GO BUILD IT
*
SOP831   EQU   *                                                 S20201
         LA    RC,D43(RF)               GET PTR TO CCHHR         S20201
         BAL   RD,SOP8SRTC              GO BUILD SRCH AND TIC    S20201
*
*    THE 'SEARCH AND TIC CCWS  HAVE NOW BEEN CONSTRUCTED.
*
*
         LA    RB,16(0,RB)              WRITE CCW POINTER
*
         LA    RC,D43(RF)               PT TO CCHHR              S20201
         AR    RC,RE                    NOW INCREMENT TO NEXT    S20201
*                                       IOB                      S20201
         CH    RUCB,CON1                IS THIS THE LAST IOB     S20201
         BNE   SOP832A                  BRANCH IF NO             S20201
         L     RC,DCBIOBA               GET FIRST IOB ADDR       S20201
         LA    RC,D43(RC)               PT TO CCHHR              S20201
SOP832A  EQU   *                                                 S20201
         CLI   DCBNCP,ONE               ONLY ONE IOB             S20201
         BH    SOP833                   BRANCH IF MORE           S20201
         L     RDCB,DXUDCBAD           GET USERS DCB ADDR        Y02072
         LA    RC,DCBFDAD+D3            IF NOT PT TO IOB CCHHR   S20201
         L     RDCB,0(RPARC)           GET PROTECTED COPY AGAIN  Y02072
SOP833   EQU   *                                                 S20201
         BAL   RD,SOP8READ              GO BUILD READ CCWS       S20201
         BCT   RUCB,SOP829
         USING IOBQSAMN,RF             ADDRESSABILITY FOR DSECT  Y02072
         MVC   IOBNIOBB,DCBIOBA+1      LINK LAST TO FIRST        A37507
         CLI   DCBNCP,ONE              ONE IOB?                  A37507
         BNE   NTONEIOB                NO - BRANCH               A37507
         OI    IOBFLAG1,IOBUNREL       YES - SET UNREL FLG       Y02072
NTONEIOB EQU   *                                                 A37507
         DROP  RF                                                Y02072
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,MOD196L         NEXT MOD               @Z30TSCF
         LR    RSTART,RBASE             RESTORE BASE REG         S20201
*
RELOOP   LA    RCWG,WGOFF(0,RCWG)      INCREMENT CURR WTG ENTRY
         LA    RCPL,PLOFF(0,RCPL)      INCR CURRENT DCB ENTRY POINTER
         CLC   0(2,RCWG),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   EGHT,RSTART              BACK TO OPEN AGAIN         DM0I
         CLC   0(2,RCWG),OPIDCNST      END OF WTG TABLE
         BC    7,RELOOP                NO,CHECK NEXT ENTRY
         LR    RCPL,RPL
         LA    RCWG,WAOFF(0,RWG)       REINITIALIZE WTG LIST PTR
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN               IF NOT TRANSFER CONTROL
         LA    RCWG,WGOFF(0,RCWG)      GET NEXT ENTRY
         LA    RCPL,PLOFF(0,RCPL)
         B     ZCHEK                    ZERO CHECK                 DM0I
**
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
**
RPL      EQU   5
RWG      EQU   6
RCPL     EQU   7
RCWG     EQU   8
RSTART   EQU   11
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES      DM0I
WGOFF    EQU   8                       OFFSET OF WTG ENTRIES
OPIDCNST DC    C'0S'                    ID                         DM0I
AMIDCNST DC    C'1J'                    ID                         DM0I
MOD196L  DC    C'6L',VL3(IGG0196L)      NEXT MODULE            @Z30TSCF
*
***********************************************************************
*
*    THIS SECTION OF CODE BUILDS A READ CHANNEL PROGRAM               *
*                                                                     *
*    R2 POINTS TO NEXT CCW ON ENTRY AND EXIT                          *
*                                                                     *
*    R4 IS RETURN -- R6 POINTS TO COUNT READ IN                       *
*
*                                                                     *
***********************************************************************
*
SOP8READ MVI   7(RB),ONE                COUNT
         MVI   0(RB),READATA            READ DATA OP CODE
         MVI   4(RB),CMDSKPSL           COMMAND CHAIN,SKIP,SILI
*
         MVI   15(RB),FIVE              COUNT
         MVI   12(RB),COMSILI           COMMAND CHAIN,SILI
         TM    JFCBMASK+D6,NONCARN      ANY DEVICES W/O RCD      S20201
*                                       READY                    S20201
         BO    SOP8RD7                  BRANCH IF YES            S20201
         LR    RC,RF                    GET IOB PTR              S20201
         AR    RC,RE                    INCR TO NEXT IOB         S20201
         SR    RC,RDEB                  BACK UP ONE DW           S20201
         MVI   D15(RB),ATE8                                      S20201
SOP8RD7  EQU   *                                                 S20201
         ST    RC,D8(RB)                ADDRESS                  S20201
         MVI   8(RB),READCOUN           READ COUNT OP CODE
         AR    RB,RDEB                  INCREMENT PTR            S20201
         TM    JFCBMSK6,JFCSDRPS        SEARCH DIRECT            YA3136
         BO    SOP8RD8                  YES, READ DATA CCW       YA3136
*                                        BEFORE READ COUNT CCW   YA3136
         AR    RB,RDEB                  INCR PTR                 S20201
SOP8RD8  EQU   *
         SR    RB,RF                    GET RD CCW OFFSET        S20201
         MODESET  EXTKEY=DATAMGT        BACK TO DATA MANAG KEY   Y02072
         STC   RB,DCBOFFSR              PUT IN DCB               S20201
         MODESET  KEYADDR=DXUKEY,WORKREG=9 GET INTO USER KEY     Y02072
         AR    RB,RF                    RESTORE PTR              S20201
*
SOP8RD10 CLI   DCBKEYLE,MINKEY          ARE KEYS SPECIFIED
         BC    4,SOP8RD15               BRANCH ON NO (LESS THAN 1)
         MVI   0(RB),READKEDA           READ KEY AND DATA OP CODE
         B     SOP8RD18                 SKIP                       DM0I
SOP8RD15 MVI   0(RB),READATA            READ DATA OP CODE
SOP8RD18 TM    DCBRECFM,UFORMAT         U FORMAT SPECIFIED
         BNM   SOP8RDU                  BRANCH IF U FORMAT       S20201
         TM    DCBRECFM,VFORMAT          IS IT V FORMAT          S20201
         BNO   SOP8RD20                 IF NOT, DONT SET SILI    S20201
SOP8RDU  EQU   *                                                 S20201
         MVI   4(RB),SILI               SILI FLAG
SOP8RD20 LA    RB,8(0,RB)               INCREMENT CCW POINTER
         BR    RD                       EXIT                       DM0I
*
***********************************************************************
*
*
***********************************************************************
*
*    THIS SECTION OF CODE BUILDS A SEARCH AND TIC CCW                 *
*
*
*    RC POINTS TO SEARCH ADDRESS-- RB POINTS TO NEXT CCW ON ENTRY     *
*   RB IS NOT CHANGED IN ROUTINE.-- RD  IS  RETURN--                  *
*                                                                     *
***********************************************************************
SOP8SRTC ST    RC,0(0,RB)              SEARCH ADDRESS
         MVI   0(RB),SRCHE             SEARCH OP CODE
         MVI   7(RB),FIVE              COUNT
         MVI   4(RB),COMCHAIN          COMMAND CHAIN
*
*    TIC
*
         LA    RC,5(0,RC)              SEARCH ADDRESS
         ST    RB,8(0,RB)              TIC TO SEARCH
         MVI   8(RB),TIC               TIC OP CODE
         BR    RD                       EXIT                       DM0I
***********************************************************************
*
* THIS RTN BUILDS SET THETA COMMANDS, RB POINTS TO CCW
*
SOP8STA  EQU   *                                                 S20201
         L     RC,DXCCW11               GET SET THETA ADDR       S20201
         ST    RC,D0(RB)                PUT IN CCW               S20201
         OI    FLAGOFF(RB),CC           TURN ON COMMAND CHAIN    S20201
*                                       FLAG                     S20201
         MVI   CNTOFF(RB),ONE           SET COUNT TO ONE         S20201
         MVI   D0(RB),SETSC             PUT IN COMMAND           S20201
         AR    RB,RDEB                  INCREMENT PTR            S20201
         BR    RD                       RETURN                   S20201
*******************************************************************
***
CON1     DC    H'1'                     CONSTANT OF ONE          S20201
         SPACE 2
EGHT     EQU   8                                                   DM0I
PATCH    DC    0H'0',50X'00'            PATCH AREA               Y02072
         SPACE 2
         EJECT
CVT      DSECT                                                 @Z30TSCF
         CVT                                                   @Z30TSCF
         EJECT
         IECDSECS  (MAIN,(IOB,NO)),DCB,IOB,WTG,PREFX,EXPAND=YES
*                                                              @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MODS ID             Y02072
FORCORE  DSECT
         ORG   JFCBMASK
         DS    6B
JFCBMSK6 DS    B                        SIXTH BYTE OF JFCB MASK
         EJECT
FORCORE  DSECT                                                   Y02072
         IHAFCAUD  ORG=YES                                       Y02072
DCBKEYL  EQU   DCBKEYLE
         END
