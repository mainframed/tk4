         TITLE 'IGG0191B  MAIN EXECUTOR'
IGG0191B CSECT
*
*MODULE NAME - IGG0191B                                          Y02072
*
*DESCRIPTIVE NAME - DETERMINES PATH THROUGH OPEN EXECS           Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          VS1 RELEASE 3 CHANGES/DELETIONS
*                                                                XA2865
*          VS2 RELEASE 2 CHANGES/DELETIONS
*011200,034700,034900-034960,000584-000710,113800,056370,058600, Y02072
*073820,074399,099000,105000-105400,106000,110400                Y02072
*                                                                YM6518
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*          VS2 RELEASE 3.7 CHANGES
*A070344-070351                                                @ZA16011
*A017370,063000-063160,063220-063240                           @ZA26771
*C063220-0633240                                               @ZA30524
*D063000-063160,063220-063240                                  @ZA32889
*          RELEASE 21 DELETIONS/CHANGES
*0297062200                                                      A47713
*0297070320-070360                                               M1802
*0297053000,053600,054020-054160                                 M1306
*0297053000,053600                                               A43247
*0297004800,030700,035320,056200,056380-056384,056392-056396,    S21042
*0297105600,107800,108000-108100,108200,113200,117000            S21042
*0297                                                            A44469
*D037300-042800                                                 SA53194
*REFER TO CODE FLAGGED XA2865                                   SA65964
*         RELEASE 20 DELETIONS/CHANGES
*1331036400,117800-121800                                        S20016
*1331000237,036066                                               M0001
*1331045030-045090                                               M0006
*1331125500                                                      M0005
*1331000220                                                      A32172
*1331027050,033060,036000-036180,133460                          M1227
*1331                                                            M3415
*
*STATUS CHANGE LEVEL 010
*
*FUNCTION/OPERATION: THIS ROUTINE SUPPLEMENTS THE INITIAL OPEN ROUTINE.
*      IT PERFORMS GENERAL AND SPECIFIC FUNCTIONS FOR THE SEQUENTIAL
*      ACCESS METHOD(QSAM,BSAM,EXCP).
*      IT SETS INDICATORS FOR THE APPROPRIATE ACCESS METHOD AND DEVICE
*      CLASS.
*
*ENTRY POINT-IGG0191B  ENTERED FROM IGG0191A BY USE OF THE XCTL MACRO.
*
*INPUT: SEE DESCRIPTION OF REGISTERS.
*OUTPUT: SEE DESCRIPTION OF REGISTERS.
*EXTERNAL ROUTINES: NONE.
*EXITS - NORMAL XCTL TO IGG0196B
*        IGG0191B WAS SPLIT INTO IGG0196B FOR REL 19
*
*EXITS - ERROR XCTL TO PROBLEM DETERMINATION (IGG0196M)
*              TO ISSUE WTP AND 013 ABEND
*
*TABLES/WORKAREAS: WHERE TO GO TABLE(WTG)
*      BYTE  0-7  NAME
*      BYTE  8-10 RELATIVE DISK ADDRESS(TTR) OF FIRST RECORD
*      BYTE 11    CONCATENATION NUMBER
*      BYTE 12    ZERO
*      BYTE 13    ALIAS INDICATOR AND USER DATA FIELD DESCRIPTOR.
*                        ALIAS INDICATOR 1 BIT
*                        NUMBER OF TTRS IN USERS DATA FIELD 2 BITS
*      BYTE 14-16 TTR OF FIRST TEXT RECORD
*      BYTE 17    ZERO
*      BYTE 18-19 TTR OF NOTE LIST OR SCATTER LIST
*      BYTE 20    TRANSLATION TABLE
*      BYTE 21    ZERO OR NUMBER OF ENTRIES IN THE NOTE LIST
*      BYTE 22-23 ATTRIBUTES
*      BYTE 24-26 TOTAL CONTIGUOUS MAIN STORAGE REQUIRED FOR MODULE
*      BYTE 27-28 LENGTH OF FIRST TEXT RECORD
*      BYTE 29    LENGTH OF WTG TABLE(IN DOUBLE WORDS)
*      BYTE 30-31 PATH THROUGH LOADS OF ROUTINES
*      BYTE 32-34 IDTTR OF EXECUTOR FOR FIRST DCB
*      BYTE 35    ADDRESS OF STORAGE FOR DCB
*      BYTE 36-39 TABLE OF IDTTR'S
*      BYTE 32+N(8) IDTTR OF EXECUTOR FOR  N TH DCB (3 BYTES)
*                   ADDRESS OF STORAGE FOR DCB      (1 BYTE )
*                   IDTTR OF LAST ROUTINE LOAD      (3 BYTES)
*                   NOT USED                        (1 BYTE )
*
*      WORKAREA- SEE 'FORCORE DSECT' (IECDSECT) IN THIS LISTING
*
*ATTRIBUTES: REENTRANT, REUSABLE, SUPERVISOR STATE,              Y02072
*            RUNS IN DATA MANAGEMENT KEY                         Y02072
*
*  REGISTER CONVENTIONS USED THROUGH OUT ALL OPEN PASSES
*
***********************************************************************
*
*
RE       EQU   0                       WORK/PARAMETER REGISTER
R0       EQU   RE                      INPUT REG TO BLDL AND     YM1167
*                                        CONVERT ROUTINE         YM1167
RF       EQU   1                       WORK/PARAMETER REGISTER
RWK1     EQU   RF                      WORK REGISTER             YM1167
RDCB     EQU   2                       ADDR OF USERS DCB
RBASE    EQU   3                       BASE REGISTER
RCORE    EQU   4                       ADDR OF OPEN WORK AREA
RPAR     EQU   5                       PARAMETER LIST
RWTG     EQU   6                       START OF WTG
RPARC    EQU   7                       CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                       CURRENT ENTRY IN WTG TABLE
RTIOT    EQU   9                       TIOT ADDR/WORK REGISTER
RUCB     EQU   10                      UCB ADDR/WORK REGISTER
RDEB     EQU   11                      DEB ADDRESS
RA       EQU   11
RB       EQU   12                      WORK REGISTER
RC       EQU   13                      WORK REGISTER
RD       EQU   14                      WORK/PARAMETER REGISTER
RJ       EQU   15                      WORK/PARAMETER REGISTER
* MASK'S TO TEST PARAMETER LIST WITH
*
OUTP       EQU   X'0F'
INPUT    EQU   X'0F'                    INPUT MASK               A27549
VOLFULL  EQU   X'20'                    VOLUME FULL MASK         A27549
DISPNEW  EQU   X'C0'                    DATA SET STATUS MASK     A27549
*
*********************************************************************
*
*
* OFFSET FOR PARAMETER TESTS
*
DISPSN     EQU   X'0'
*
*
*********************************************************************
*
* MASK'S USED TO TEST OPTION FIELDS IN DCB
*
*
*
DOUBLE     EQU   X'01'
P2       EQU   2                        CONSTANT OF 2            M0006
DEBLRECL EQU   46                       OFFSET OF LRECL IN DEB   M0006
PORG     EQU   X'02'                    MASK FOR DSORG=PO IN DCB   DM0P
STOWB      EQU   X'80'
OVRFLO     EQU   X'20'
PCI        EQU   X'20'
CONCAT     EQU   X'FF'
*
*******************************************************************DM0P
*                                                                  DM0P
* MASKS USED TO TEST OPTION FIELDS IN DSCB                         DM0P
*                                                                  DM0P
POFILTYP EQU   X'02'                    MASK FOR FILE TYPE         DM0P
*                                                                  DM0P
*******************************************************************DM0P
*                                                                  DM0P
* MASKS USED TO TEST OPTION FIELDS IN JFCB                         DM0P
*                                                                  DM0P
NORECRDY EQU   X'20'                     NON-RECORD-READY BIT    A44469
JFCBMEM  EQU   X'40'                    MASK FOR MEMBER NAME       DMOP
*                                                                  DMOP
*********************************************************************
*
* MASKS FOR ACCESS METHODS IN SAM
*
PAM        EQU   X'02'
PSAM       EQU   X'42'
QSAMB      EQU   X'01'
*
*
* MASKS FOR MACRO'S TO BE USED
*
GETB       EQU   X'40'
PUTB       EQU   X'40'
READB      EQU   X'20'
WRITEB     EQU   X'20'
POINTB     EQU   X'04'
CNTRLB     EQU   X'02'
*
*
*********************************************************************
*
* MASKS FOR DEVICES TO BE SUPPORTED
*
T3525    EQU   X'4C'               3525 DEVICE                  SA53194
PRTPCH   EQU   X'30'               PRT/PCH ASSOC. D.S           SA53194
RDPCH    EQU   X'60'               RD/PCH ASSOC. D.S            SA53194
RDPRT    EQU   X'50'               RD/PRT ASSOC. D.S            SA53194
ASSOCDS  EQU   X'04'               3525 ASSOCIATED D.S.         SA53194
DABIT    EQU   X'20'
URBIT    EQU   X'40'
TAPEB    EQU   X'80'
MICR3890 EQU   X'56'                    DCBDEVT CODE FOR 3890   XL03123
*                                       DEVC                     S20016
TIOTTERM EQU   X'20'                    DEVICE IS TERMINAL       S20016
*
*
* MASKS FOR BUFFER TECHNIQUE'S
*
EXCHANGB   EQU   X'10'
LRECIND  EQU   X'20'                    LOGICAL RECORD INTERFACE S19015
*
*********************************************************************
*
*
* MODES OF OPERATION FOR QSAM
*
LOCATEB    EQU   X'08'
MOVEB      EQU   X'10'
*
*
*********************************************************************
*
* MASKS USED FOR NEW BUFFER CONTROL BLOCK
*
FLGOFFST EQU   4                        OFFSET TO FLAG BYTE IN   S19015
*                                       BCB                      S19015
RAFLAG   EQU   X'80'                    RECORD AREA PRESENT      S19015
*
***********************************************************************
*
OABD053  EQU   53                       INTERNAL CODE 53         S21042
OABD094  EQU   94                       INTERNAL CODE 94         S21042
         EJECT
         BALR  RBASE,0
         USING PO,RBASE
         USING IHADCB,RDCB
         USING FORCORE,RCORE
         USING DEB,RDEB
         USING   TCB,RD
         USING CVT,RTIOT
         USING UCBOB,RUCB
         USING TIOT,RC                                           S20016
*
*
*
*
PO       EQU   *
*
         B     BEGIN                    BR AROUND CONSTANTS      Y02072
         DC    C'IGG0191B'              MODULE NAME              Y02072
         DC    C'@ZA32889'              LAST SHIP CODE         @ZA30534
         DC    CL8'&SYSDATE'            LAST DATE SHIPPED      @ZA26771
BEGIN    EQU   *                                                 Y02072
         L     RDCB,0(RPARC)           GET DCBADDRESS
         L     RCORE,4(RWTGC)           GET WORK AREA
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
*
         L     RUCB,DEBUCBAD            GET UCB ADDRESS
*
* FOR A TSO DCB, DCBEDVT IS SET TO X'4F' IN IGG0190L
*
         L     RTIOT,CVTPTR             GET COMM VECTOR ADDR     M1227
         L     RD,CVTTCBP               GET TCB POINTER          M1227
         L     RD,4(RD)                 GET ADDR OF CURRENT TCB  M1227
         L     RC,TCBTIO                GET ADDR OF TIOT         M1227
         AH    RC,DCBTIOT               GET ADDR OF THIS DD      M1227
*                                       ENTRY                    M1227
         TM    TIOELINK,TIOTTERM        IS THIS A TSO DCB        M1227
         BO    GOGOGO                   BRANCH IF YES            M1227
         LA    RE,OABD094               LOAD INTERNAL CODE 94    S21042
         CLI   UCBTBYT3,X'40'           COMMUNICATIONS DEVICE?   A32172
         BE    CALLPD                   CALL PROB DET            S21042
         MVC   DCBDEVT(1),UCBTBYT3      STORE DEVICE CLASS
         NI    DCBDEVT,X'F7'            TURN OFF UR BIT IF ON
         OC    DCBDEVT(1),UCBTBYT4      STORE DEVICE NUMBER
*******************************************************************
         TM    UCBTBYT3,X'08'           UR EQ BEING USED
         BZ    GOGOGO                   BRANCH IF NOT            S20016
*
         OI    DCBDEVT,X'40'            FAKE UR BIT IN DCB
*
GOGOGO   EQU   *                                                 M0006
*
*     THIS SECTION WILL SAVE DCBLRECL FIELD IN THE DEB FOR QSAM/BSAM  *
*
         TM    DCBDSORG,DCBDSGPO        PARTIONED ORGANIZATION   XA2865
         BO    SOP100                   YES, BR, DEBLRECL DOES   XA2865
*                                         NOT EXIST              XA2865
         SR    RB,RB                    CLEAR REGISTER           M0006
         IC    RB,DEBNMEXT              PICK UP NO. OF EXTENTS   M0006
         SLL   RB,P2                    MULTIPLY BY 4 FOR NON-DA M0006
         TM    TIOELINK,TIOTTERM        IS THIS A TSO DCB        M3415
         BO    SOP90                    BRANCH IF YES            M3415
         TM    UCBTBYT3,DABIT           DIRECT ACCESS DEVICE     M0006
         BNO   SOP90                    NO, BRANCH               M0006
         SLL   RB,P2                    MULTIPLY BY 4 FOR DA     M0006
SOP90    AR    RB,RDEB                  ADD DEB ADDRESS          M0006
         MVC   DEBLRECL(P2,RB),DCBLRECL SAVE DCBLRECL IN DEB     M0006
SOP100   EQU   *                                                 XA2865
         MVC   DXCCW6(1),DCBEROPT       SAVE QSAM ERROR OPTIONS
         XC    DCBCNTRL(4),DCBCNTRL     CLEAR PARA PASSAGE VECTOR
*
         TM    DCBDEVT,X'20'            D. A. DEVICE BEING USED
         BO    A0020                    YES, BRANCH
         OI    JFCBMASK+6,NORECRDY      SET NON-RECORD-READY BIT  M1802
*                                       FOR ALL NON-DIRECT ACCESS
*                                       DEVICES
         B     PAM6                     BRANCH AROUND             M1802
*
A0020    EQU   *
         XC    DCBRELAD(4),DCBRELAD     INITIALIZE RELAD
         L     RTIOT,CVTPTR(0,0)        GET COMM VECTOR ADDRESS
         L     RB,CVTZDTAB             DEVICE CONSTANT TABLE ADDRESS
         IC    RF,DCBDEVT              GET DEVICE CHARACTERISTICS
         N     RF,DEVTCNST             CLEAR HI 28 BYTES
         IC    RF,0(RF,RB)             ADD ID TO TABLE ADDRESS
         LA    RB,0(RF,RB)             ADD CONST AT THIS LOC-PNT DEV
         IC    RC,DCBDVTBL             SAVE POSSABLE REC NO.R
         ST    RB,DCBDVTBL             STORE ADDRESS OF DEVICE TABLE
         STC   RC,DCBDVTBL             RESTORE RECORD NUMBER
         SR    RC,RC                   CLEAR REG
*
*
         CLI   DCBKEYLE,1              ARE KEYS SPECIFIED
         BC    10,DEVCN1               YES BRANCH OVERHEAD INCLUDED
*
DEVCNO   IC    RC,8(RB)                GET KEY OVERHEAD
*
DEVCN1   STC   RC,DCBRELAD+4           STORE EITHER OVHEAD OR ZERO
*
         MVC   DCBTRBAL(2),4(RB)       INITILIZE TRACK BALANCE
DEVCN2   EQU   *
*
PAM1     EQU   *
*
         TM    0(RPARC),X'07'           DCB OPENED FOR OUTPUT OR OUTIN
         BC    1,PAM3                  YES BRANCH
*
*  FIND MEMBER NAME STORE IN DEB ISSUE A BLDL MACRO TO FIND EXTENT
*
         TM    JFCBIND1,X'01'           DCB FOR PDS
         BZ    PAM5                     NO BRANCH                M1306
         CLI   JFCBELNM,X'40'           IS THIS A MEMBER
         BZ    PAM5                     NO BRANCH                M1306
         TM    DSCFILTY,X'02'           DSCB SET UP FOR P. O.
         BC    8,DCBERROR               NO ABEND -- DIAGNOSTIC TEST
         MVC   BLDLLDF,BLDLCNST         FILL IN LIST DESC FIELD  YM1167
         MVC   BLDLMNAM,JFCBELNM        MEMBER NAME TO LIST      YM1167
         LA    R0,BLDLLIST              INPUT REG TO BLDL        YM1167
*
         BLDL  0(RDCB),(0)
*
         LTR   RJ,RJ                    HAS AN ERROR OCCURED IN BLDL
         BZ    PAM2                     NO ERROR HAS OCCURRED    S21042
*                                       IF RETURN CODE FROM BLDL IS
*                                       4, THEN INTERNAL CODE IS 54
*                                       IF RETURN CODE IS 8,
*                                       THEN INTERNAL CODE IS 55
         LA    RE,OABD053               LOAD CODE 53 INTO REG    S21042
         SRL   RJ,2                     DIVIDE BLDL RETURN CODE  S21042
*                                       BY 4                     I21042
         AR    RE,RJ                    CALCULATE INTERNAL CODE  S21042
         B     CALLPD                                            S21042
*
*
*
PAM2     EQU   *
*
*
         SR    RWK1,RWK1                PREPARE REG FOR IC INST  YM1167
         IC    RWK1,BLDLR               GET R FROM TTRN          YM1167
         BCTR  RWK1,0                   DECREMENT R VALUE BY 1   YM1167
         STC   RWK1,BLDLR               NEW R VALUE TO TTRN      YM1167
         L     R0,BLDLTTRN              INPUT REG TO CNVRT RTN   YM1167
         B     PAM4                     GO CONVERT IT
*
PAM3     EQU   *
*
         TM    JFCBIND1,X'01'           DCB FOR PDS
         BZ    PAM3D                    NO BRANCH                  DM0P
         CLI   JFCBELNM,JFCBMEM         OPENING TO A MEMBER        DM0P
         BE    PAM3D                    NO BRANCH                  DM0P
         TM    DSCFILTY,POFILTYP        DSCB SET UP FOR P.O.       DM0P
         BZ    DCBERROR                 NO ABEND                   DM0P
         B     PAM3C                    YES BRANCH                 DM0P
*                                                                  DM0P
PAM3D    EQU   *                                                   DM0P
*                                                                  DM0P
         TM    DCBDSORG,PORG            DCB SET UP FOR P.O.        DM0P
         BZ    PAM3A                    NO BRANCH                  DM0P
         TM    DSCFILTY,POFILTYP        DSCB SET FOR P.O.          DM0P
         BZ    DCBERROR                 NO ABEND                   DM0P
*
PAM3A    EQU   *
*
         TM    DEBOFLGS,X'C0'           NEW DATA SET OPENING
         BC    1,PAM3B                  YES BRANCH
         TM    DEBOFLGS,X'80'           DATA SET BEING MODIFIED
         BZ    PAM3B                   IF NOT, BRANCH AROUND     A47713
*                                       NEXT                     A47713
         OI    DCBOFLGS,X'80'          IF YES, TURN ON           A47713
*                                       LAST-IO-WAS-             A47713
*                                      WRITE BIT. TRKBAL FILLED IN
*                                      FROM DSCB; NO SVC 25 NEEDED
         B     PAM3C                   BR TO MV IN FDAD & TRKBAL A47713
*
PAM3B    EQU   *
*
         TM    DCBDSORG,X'02'           DCB SET UP FOR P. O.
         BC    8,PAM5                   NO BRANCH
*
*
*
PAM3C    EQU   *
*
         MVC   DCBTRBAL(2),DSCLSTAR+3   STORE TRACK BAL OF LAST WRT
         MVC   DCBRELAD+1(3),DSCLSTAR   MOVE TTR TO WORK AREA
         L     RE,DCBRELAD              GET TTR IN REG 0
         SLL   RE,8                     SET TTR UP FOR CONVERT
         LA    RF,256
         AR    RF,RE                    SET UP FOR IMPLIED NOTE
         ST    RF,DCBRELAD              IMPLIED NOTE R+1
*
PAM4     EQU   *
*
         LR    RF,RDEB                  GET DEB ADDR IN PROPER REG.
         STM   2,13,DXCCW7              SAVE REGS
         LA    RDCB,DCBFDAD             GET DCB DISK FIELD
         L     RTIOT,CVTPTR(0,0)        GET COMM VECTOR
         L     RJ,CVTPCNVT              GET CONVERT RT ADDRESS
         BALR  RD,RJ                    TO CONVT RT.
*
         LM    2,13,DXCCW7              RESTORE REGS
*
         B     PAM6
PAM5     EQU   *
***********************************************************************
***********************************************************************
         XC    DCBFDAD(8),DCBFDAD      CLEAR FDAD FIELD
         MVC   DCBFDAD+1(6),DEBBINUM    INITILIZE FDAD FIELD
         TM    DCBCIND1,X'20'           Q-DUMMY EXTENT
         BZ    PAM6                     NO-BRANCH
*
         MVI   DCBFDAD+3,X'FF'          FORCE ILLEGAL FDAD
PAM6     EQU   *
*       ****************************************************
*       **  TURN OFF TRACK OVERFLOW INDICATOR IN DCBRECFM **
*       **  IF SPECIFIED FOR A TAPE DATA SET              **
*       ****************************************************
*
        TM    DCBDEVT,TAPEB            TAPE DEVICE?            @ZA16011
        BNO   PAM6A                    BRANCH IF NO            @ZA16011
        TM    DCBRECFM,OVRFLO          DFORMAT REC OR OVERFLOW @ZA16011
        BNO   PAM6A                    BRANCH IF NEITHER       @ZA16011
        TM    DCBRECFM,X'C0'           DFORMAT REC OR OVERFLOW @ZA16011
        BZ    PAM6A                    BRANCH IF DFORMAT REC   @ZA16011
        NI    DCBRECFM,X'FF'-OVRFLO    TURN OFF TRK OVERFLOW   @ZA16011
PAM6A   EQU   *                                                @ZA16011
        TM    DCBDEVT,X'40'            UNIT RCD EQUIP USED
        BC    8,AMOP15                 NO BRANCH
*
        TM    DEBOPATB,X'0F'           DCB FOR INPUT ONLY
        BC    8,AMOP14                 YES BRANCH
*
        TM    DCBMACRF+1,POINTB        NOTE/POINT USED
        BC    1,AMOP14A                YES BRANCH
*
        TM    DEBOPATB,X'0F'           DCB FOR INOUT OR OUTIN
        BC    1,AMOP15                 NO BRANCH
*
AMOP14  EQU   *
        TM    DCBMACRF,POINTB          NOTE POINT TO BE USED
        BC    8,AMOP15                 NO BRANCH
AMOP14A EQU   *
        MVI   DCBCNTRL+1,10            STORE ID OF DUMMY RTNE
*
*  THE FOLLOWING WILL ADJUST LRECL FOR FIXED UNBLOCKED RECORDS,  XA2865
*  UNDEFINED LENGTH RECORDS, AND VARIABLE LENGTH UNSPANNED       XA2865
*  RECORDS. FOR FIXED BLOCKED RECORDS, IGG0191A INSURES LRECL    XA2865
*  IS VALID.                                                     XA2865
*
AMOP15  EQU   *
         CLI   DCBDEVT,MICR3890         DEVICE A 3890 ?         XL03123
         BE    POPEND                   YES-BRANCH              XL03123
         SR    RWK1,RWK1                CLEAR REG TO ZERO        YM6518
         CH    RWK1,DCBLRECL            LRECL = ZERO             YM6518
         BNE   POPEND                   BRANCH IF NOT ZERO       YM6518
         TM    DCBCIND2,DCBCNQSM        ADJUST FOR QSAM ONLY     XA2865
         BZ    POPEND                   BR NOT QSAM              XA2865
         LH    RWK1,DCBBLKSI            ADJUST USING BLKSIZE     XA2865
         TM    DCBRECFM,DCBRECU         RECFM UNDEFINED          XA2865
         BNM   NOTVARBL                 YES, GO ADJUST LRECL     XA2865
         TM    DCBRECFM,DCBRECF         RECFM FIXED              XA2865
         BO    NOTVARBL                 YES, GO ADJUST LRECL     XA2865
         TM    DCBRECFM,DCBRECSB        RECFM VAR SPANNED        XA2865
         BO    POPEND                   YES, BYPASS ADJUSTMENT   XA2865
         SH    RWK1,BDWLEN              SUB BDW LENGTH           XA2865
NOTVARBL EQU   *                                                 XA2865
         STH   RWK1,DCBLRECL            ADJUST LRECL             XA2865
POPEND   EQU   *
*
****************************************************************** DM0B
*                                                                  DM0B
XCTLGOGO EQU   *                                                   DM0B
         LA    RF,SO6B                  ADDR OF NEXT LOAD ID     S19033
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,0(RF)           ID TO WTG ENTRY        @Z30TSCF
*
*
*
*
*
*
***********************************************************************
*
***********************************************************************
*
RELOOP   EQU   *
         LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURRENT WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)    INCR CURRENT DCB ENTRY PTR
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   8,RBASE                 RETURN TO INITIALIZE ANOTHER DCB
*
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BC    7,RELOOP                 NO, CHECK NEXT ENTRY
*
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)     REINITIALIZE WTG LIST PTR
ZCHEK    CLI   0(RWTGC),X'00'          IS THIS ENTRY COMPLETE
         BC    7,TCTLRTN           IF NOT TRANSFER CONTROL
*
         LA    RWTGC,WGOFF(0,RWTGC)    GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         BC    15,ZCHEK
*
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
**
DCBERROR EQU   *
         LA    RE,OABD053                                        S21042
*                                       0                        I21042
CALLPD   EQU   *                                                 S21042
         DMABCOND (0),PDLOAD,RETURN=NONE,RES=NO,REGSAVE=YES      S21042X
                                        013 ABEND
         B     TCTLRTN                  GO XCTL                  S21042
         EJECT
WAOFF    EQU   32                  OFFSET OF FIRST ENTRY IN WTG TABLE
PLOFF    EQU   4                   OFFSET OF DCB ENTRIES
WGOFF    EQU   8                   OFFSET OF WTG ENTRIES
*
*  MODULE ID'S
*
SO6B     DC    C'6B',VL3(IGG0196B)                             @Z30TSCF
PDLOAD   DC    C'6M',VL3(IGG0196M)                             @Z30TSCF
OPIDCNST DC    C'0S'
AMIDCNST DC    C'1B'
*
*  OTHER CONSTANTS
*
DEVTCNST DC    F'15'
BLDLCNST DC    X'0001',X'000C'          1 ENTRY, 12 BYTE LIST    YM1167
BDWLEN   DC    H'0004'                  LEN OF A BLK DESC WORD   XA2865
         SPACE
PATCH    DC    0H'0',50X'00'            PATCH AREA               Y02072
END      EQU   *                        END OF MODULE            Y02072
         EJECT
           DCBD  DSORG=PS
*
*
         IECDSECS MAIN,WTG,PREFX,EXPAND=YES                    @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MODS ID             Y02072
FORCORE  DSECT                                                   YM1167
         ORG   DXCCW                                             YM1167
BLDLLIST DS    0CL12                    LENGTH OF BLDL LIST      YM1167
BLDLLDF  DS    0CL4                     BLDL LIST DESCRIPTION    YM1167
*                                         FIELD                  YM1167
BLDLNENT DS    CL2                      NUM OF ENTRIES IN LIST   YM1167
BLDLLEN  DS    CL2                      LEN OF BLDL LIST FIELD   YM1167
BLDLMNAM DS    CL8                      MEMBER NAME              YM1167
BLDLTTRN DS    0CL4                     TTRN                     YM1167
BLDLTT   DS    CL2                      RELATIVE TRACK NUM OF    YM1167
*                                         BEGINNING OF DATA SET  YM1167
*                                         MEMBER                 YM1167
BLDLR    DS    C                        RELATIVE BLOCK NUMBER    YM1167
*                                         RELATING TO TT         YM1167
BLDLN    DS    C                        CONCATINATION NUMBER OF  YM1167
*                                         DATA SET               YM1167
         EJECT
*    COMMUNICATION VECTOR TABLE  (SSS)
*   TASK CONTROL BLOCK
TIOT     DSECT
TIOELNGH DS    CL1                           LENGTH OF THIS ENTRY
TIOESTTA DS    CL1                           STATUS -A-
TIOERLOC DS    CL2                           REL. LOC OF POOL
TIOEDDNM DS    2F                            DDNAME
TIOEJFCB DS    1F                            JFCB DISK ADDR
TIOESTTB DS    0CL1
TIOEFSRT DS    CL4
         DS    CL1
*        TIOT POOL ENTRY
TIOPNSLT DS    XL1                           NR.OF SLOTS FOR POOL
         DS    CL1
TIOPNSRT DS    XL1                           NR.OF DEVICES (FILLED
*                                             SLOTS)
TIOPPOOL DS    CL8                           POOL NAME
TIOPSTTB DS    XL1                           STATUS OF SLOT
TIOPSLOT DS    XL3                           SRT ADDRESS OR EMPTY SLOT
TIOTFEND DS    1F                            FINAL END OF AN ENTRY
TIOELINK EQU   TIOERLOC+1                                        M0005
         SPACE 1
SRT      DSECT
         IEFUCBOB
*   DATA EXTENT BLOCK  (UNTIL PROTIOS CHANGES)
DEB      DSECT
DEBNMSUB DS    0CL1
DEBTCBAD DS    CL4
DEBAMLNG DS    0CL1
DEBDEBAD DS    CL4
DEBOFLGS DS    0CL1
DEBIRBAD DS    CL4
DEBOPATB DS    0CL1
DEBSYSPG DS    CL4
DEBNMEXT DS    0CL1
DEBUSRPG DS    CL4
DEBPRIOR DS    0CL1
DEBECBAD DS    CL4
DEBPROTG DS    0CL1
DEBDEBID DS    0CL1
DEBDCBAD DS    CL4
DEBEXSCL DS    0CL1
DEBAPPAD DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    CL4
DEBTVLSQ DS    0CL2                VOL SEQ FOR TAPE
DEBBINUM DS    CL2
DEBTVLNM DS    0CL2                NO OF VOL FOR TAPE
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
*  FOR ADDITIONAL EXTENT ON DIRECT ACCESS THE FOLLOWING FIELDS
*  WILL HAVE TO BE OFFSET ANOTHER 16 BYTES
DEBVOLSQ DS    H
DEBVOLNM DS    H
*  FOR EACH SUBROUTINE LOADED BUMP THIS OFFSET BY 2
DEBSUBID DS    H
CVT      DSECT
         CVT
         IKJTCB                                                , S20016
           END
