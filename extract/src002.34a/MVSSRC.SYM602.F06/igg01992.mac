1992 TITLE 'IGG01992-SAM STAGE 3 OPEN EXEC FOR FORMAT-V/CHAINED SCHEDULX
               ING/USER TOTALING'
IGG01992 CSECT                                                     UT18
***********************************************************************
*
*MODULE NAME - IGG01992                                          Y02072
*
*DESCRIPTIVE NAME - LOAD ACCESS METHOD ROUTINES                  Y02072
*
*COPYRIGHT - NONE                                                Y02072
*
*CHANGE ACTIVITY - AS FOLLOWS:                                   Y02072
*
*          RELEASE 20 DELETIONS                                       *
*1273161200,240500-240700                                        A32160
*1273392000,422000-430000,691800,815000,820000-822000            S20201
*          RELEASE 21 DELETIONS                                       *
*0084024600-025800,027900,028800,240100-241400,302000-338000,    M1753
*0084346000-388000,492000-548000,664000-684800,687600,688200,    M1753
*0084690000-691432,691900-694000,790000-814700,819000-820000     M1753
*
*        VS 2 RELEASE 02 DELETIONS
*002500,023600,237000,238500-240000,396180-396280,396720-396820, Y02072
*440100-441300,646180-647890,815000,815400-815700,396960-396990  Y02072
*480000,614000,618000-640000,646000,492000,578000-580000,        Y02072
*586000,616000,824000-826000                                     Y02072
*                                                                YM1141
*                                                                YM1140
*                                                                YM2507
*C396660                                                         YM5922
*                                                                YM1002
*
*          VS2 RELEASE 3 DELETIONS
*                                                              @Z30TSCF
*
*        VS 2 RELEASE 03
*593100                                                         ZA02510
*C467996                                                        ZA05244
*
*STATUS CHANGE LEVEL 006
*
*FUNCTION/OPERATION - THIS MODULE IS A CONTINUATION OF MODULE
*    IGG01916 AND LOADS I/O APPENDAGES AND AN ASYNCHRONOUS ROUTINE,
*    AS APPROPRIATE.  THE ENTRY POINTS OF THE LOADED APPENDAGES ARE
*    PLACED IN THE APPENDAGE VECTOR TABLE.  FOR TRACK OVERFLOW, A
*    CIRB MACRO IS ISSUED, AND THE ENTRY POINT OF THE ASYNCHRONOUS
*    ROUTINE IS PLACED IN THE IRB.  ID'S FOR ALL LOADED MODULES ARE
*    PLACED IN THE SUBROUTINE ID SECTION OF THE DEB.
*
*ENTRY POINTS - THE MODULE IS ENTERED BY MEANS OF AN XCTL FROM
*    IGG01916.
*
*INPUT - SEE DESCRIPTION OF REGISTER CONTENTS.
*
*OUTPUT - SUBROUTINES LOADED, ENTRY POINT ADDRESSES, ID'S IN DEB.
*
*EXTERNAL ROUTINES - NONE
*EXITS - NORMAL - XCTL TO IGG01994
*      - ERROR - NONE
*
*TABLES/WORKAREAS - DESCRIBED BY DSECTS AT END OF MODULE LISTING
*
*ATTRIBULES - REENTRANT, RUNS IN DATA MANAGEMENT KEY,            Y02072
*             RUNS IN SUPERVISOR STATE                           Y02072
*
*
***********************************************************************
         EJECT
*
* REGISTER CONVENTIONS
*
*
RE       EQU   0                        WORK REG5
RF       EQU   1                        WORK REG6
R1       EQU   RF                       PARM REGISTER            Y02072
RDCB     EQU   2                        DCB
RBASE    EQU   3                        BASE
RCORE    EQU   4                        WORE AREA
RPAR     EQU   5                        TOP OF PARAMETER
RWTG     EQU   6                        TOP OF WTG TABLE
RPARC    EQU   7                        CURRENT PARAMETER
RWTGC    EQU   8                        CURRENT TRANS LOAD
RTIOT    EQU   9                        TIOT
RUCB     EQU   10                       UCB
RDEB     EQU   11                       DEB
RB       EQU   12                       WORK REG1
RC       EQU   13                       WORK REG2
RBRNCH   EQU   RC                       BRANCH REGISTER          Y02072
RD       EQU   14                       WORK REG3
RJ       EQU   15                       WORK REG4
RWK2     EQU   RJ                       WORK REGISTER            Y02072
         SPACE 2
T3       EQU   3
T25      EQU   25                       25
OUTP     EQU   X'0F'                    MASK
ASISVLN  EQU   9                        DBLWORDS FOR IRB WKA     YM1141
SIX      EQU   6                        CONST OF SIX             S20201
NRECREDY EQU   X'20'                    *                        S20201
SDINDIC  EQU   X'08'                    SEARCH DIR MASK          S20201
ZERO     EQU   0                        DISPLACEMENT OF 0        S20201
         EJECT
         BALR  RBASE,RE                 SET BASE
         USING *,RBASE
         SPACE
         B     BEGIN                    BR AROUND CONSTANTS      Y02072
         DC    C'IGG01992'              MODULE NAME              Y02072
         DC    C'@Z30TSCF'              LAST SHIP CODE         @Z30TSCF
         DC    C'10/24/74'              LAST DATE MODIFIED     @Z30TSCF
BEGIN    DS    0H                                                Y02072
         L     RDCB,0(RPARC)            GET DCB ADDRESS
         LA    RDCB,0(RDCB)             CLEAR HI BYTE
         USING IHADCB,RDCB
         L     RCORE,4(RWTGC)           GET WORK AREA
         USING FORCORE,RCORE
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
         USING BLDEB,RDEB
         MVI   22(RWTG),X'CB'          CHANGE DE ATTR.TO ONLY LOADABLE
         L     RTIOT,DEBAPPAD           GET APPENDAGE ADDRESS
         USING AVT,RTIOT
         SR    RUCB,RUCB                CLEAR REG
         SR    RF,RF                    CLEAR RER
         IC    RUCB,DEBNMEXT            GET NUMBER OF EXTENTS
         IC    RF,DEBEXSCL              GET SCALING FACTOR
         SLL   RUCB,0(RF)               SHIFT ACCORDING TO EXTENT SIZE
         IC    RF,DEBAMLNG              GET ACCESS METHOD LENGTH
         LA    RUCB,0(RF,RUCB)          ADD TO POINT TO SUBRT ID FLD
         LA    RUCB,32(RUCB,RDEB)       ADD BASIC DEB SZ TO DEB AD
*
*  RUCB NOW POINTS TO SUBROUTINE ID SECTION OF THE DEB,            UT18
*  BUT MUST BE INCREMENTED TO POINT BEYOND THE LAST ENTRY          UT18
*
SLDAA    EQU   *                                                   UT18
         CLI   0(RUCB),X'00'            IS RUCB BEYOND LAST ENTRY  UT18
         BE    SLDAB                    BR IF YES                  UT18
         LA    RUCB,2(RUCB)             INCREMENT TO NEXT ID FIELD UT18
         B     SLDAA                    TEST NEXT FIELD            UT18
SLDAB    EQU   *                                                   UT18
         TM    0(RPARC),OUTP           DCB FOR OUTPUT ONLY
         BO    LAB001                   YES,BRANCH               S20201
         TM    DCBRECFM,DCBRECTO        DCB FOR TRACK OVERFLOW
         BZ    SLDAPP04                 NO,MUST BE CHAINED SCHEDULING
         TM    JFCBMASK+SIX,SDINDIC     IS SEARCH DIR SPECIFIED  S20201
         BNO   NOSDIR                   IF NOT LOAD REGULAR      S20201
*                                           ROUTINES
         LA    RB,CEAFP                 SD CHANNEL END APPENDAGE
         BAL   RC,SLDROUT               LOAD THE ROUTINE
         ST    RE,AVTCE                 STORE ADDR AS AVT ENTRY
         LA    RB,AEOESF                SD END OF EXTENT APPEND
         BAL   RC,SLDROUT               LOAD THE ROUTINE
         ST    RE,AVTEOE                STORE ADDR AS AVT ENTRY
         B     SLDAPP19                   LOADING ANYTHING ELSE  S20201
LAB001   EQU   *                                                 S20201
         TM    JFCBMASK+SIX,NRECREDY    IS IT RECORD READY       S20201
         BO    SLDAPP06                 NO, BRANCH TO PROCESS    S20201
*                                       OUTPUT
         TM    DCBCIND2,DCBCNCHS        IS IT CHAINED SCHEDULING YM5922
         BO    SLDAPP04                 YES, BR AROUND           S20201
         B     LAB004                   CHECK FOR CE APPENDAGE
NOSDIR   EQU   *
         LA    RB,TOABNE                GET ID OF T.O. ABNE      S20201
*                                       APPENDAGE                S20201
         BAL   RC,SLDROUT               LOAD ROUTINE             S20201
         ST    RE,AVTABNE               STORE ADDR AS AVT ENTRY
         TM    JFCBMASK+SIX,NRECREDY    IS IT RECORD READY       S20201
         BO    LAB01                    NO BRANCH AROUND         S20201
LAB004   EQU   *
         TM    DCBRECFM,DCBRECU         RECORD READY WITH FORMAT S20201
*                                       U                        S20201
         BM    LABNOTU                  BRANCH IF NO             S20201
         TM    DCBRECFM,DCBRECTO        WITH TRK OVERFLOW ALSO   S20201
         BO    LAB01                    THEN BRANCH              S20201
         LA    RB,ACEU                  GET FORMATU RCD RDY CE   S20201
         B     SLDAPP02                 GO LOAD IT               S20201
LABNOTU  EQU   *                                                 S20201
         TM    DCBRECFM,DCBRECF         FIXED LINGTH RECORDS     S20201
         BZ    LAB01                    NO BRANCH AROUND LOAD OF S20201
*                                       CI                       S20201
         LA    RB,ACEF                  LOAD CI                  S20201
         B     SLDAPP02                 BRANCH TO LOAD CI        S20201
LAB01    EQU   *                                                 S20201
         TM    DCBRECFM,DCBRECU         U RECORDS
         BO    SLDAPP03                 YES,NO LENGTH CHECK APPENDAGE
         LA    RB,ACEF                 GET F LN CHECK APPEND
         TM    DCBRECFM,DCBRECF+DCBRECBR  FIXED BLOCK RECORDS
         BO    SLDAPP02                YES,GO LOAD ROUTINE
         TM    DCBRECFM,DCBRECV         VARIABLE RECORDS
         BZ    SLDAPP03                 NO,NO LENGTH CHECK APPENDAGE
         LA    RB,ACEV                 GET V LN CHECK APPENDAGE
SLDAPP02 EQU   *
         BAL   RC,SLDROUT              LOAD THE ROUTINE
         ST    RE,AVTCE                 APPENDAGE TABLE ENTRY FOR CE
         TM    ZERO(RPARC),OUTP         DCB FOR OUTPUT ONLY      S20201
         BO    SLDAPP06                 YES, BRANCH              S20201
SLDAPP03 EQU   *
         LA    RB,ASYNCH               GET ID OF ASYNCHRONOUS ROUTINE
         BAL   RC,SLDROUT              LOAD ROUTINE
*
*        BUILD IRB FOR 1GG019C1 - WORKAREA IS USED IN 19C3
*
         CIRB  EP=(0),KEY=PP,MODE=PP,STAB=(RE),WKAREA=ASISVLN
         ST    RF,24(RWTG)             STORE ADDRESS OF IRB TEMPORARY
         MVC   DEBIRBAZ(T3),T25(RWTG)   INSERT IRB ADDR.
         LA    RB,AEOET                GET T. O. EOE ID
         B     SLDAPP05                 BRANCH
SLDAPP04 EQU   *
         TM    DCBDEVT,X'20'           D.A. DEVICE
         BZ    SLDAPP14                NO,BRANCH
         LA    RB,AEOEP                GET END OF EXTENT ID FOR PCI
SLDAPP05 EQU   *
         BAL   RC,SLDROUT              LOAD ROUTINE
         ST    RE,AVTEOE                APPENDAGE TABLE ENTRY FOR EOE
SLDAPP06 EQU   *
         TM    DCBRECFM,DCBRECTO        DCB FOR TRACK OVERFLOW
         BO    SLDAPP19                YES,PROCEED TO EOB
SLDAPP14 EQU   *
*
* BEFORE LOADING CHAINED SCHEDULING APPENDAGE, TEST FOR A REAL   Y02072
* ADDRESS ENVIRONMENT.  IF V=R, LOAD IGG019V6 FROM SVCLIB        Y02072
* INSTEAD OF IGG019CU FROM LPALIB AND STORE PCI ENTRY POINT.     Y02072
* IF VIRTUAL, LOAD IGG019CU AND DO NOT STORE PCI ENTRY POINT     Y02072
* IN THE AVT.                                                    Y02072
*
         LA    RB,ACEO                  GET ID OF PCI/CE & ABNE APPEND
         LA    RBRNCH,SLDAPP17          RETURN ADDRESS           Y02072
         L     RWK2,DEBTCBAD            GET TCB ADDRESS          Y02072
         USING TCB,RWK2                                          Y02072
         TM    TCBFLGS6,TCBRV           IS TASK VIRT=REAL        Y02072
         DROP  RWK2                                              Y02072
         BZ    SLDROUT                  N0, BRANCH               Y02072
         LA    RB,VRPCI              GET V=R PCI APPENDAGE      OZ05244
         BAL   RBRNCH,SLDROUT1          GO GET PROPER APPENDAGE  Y02072
         ST    RE,AVTPCI                PCI ENTRY IN AVT         Y02072
SLDAPP17 LR    RF,RE                                             Y02072
         LA    RF,4(RF)                 PLUS 4 IS ENTRY POINT    Y02072
         ST    RF,AVTCE                 STORE EPA AS AVT ENTRY
*                                       FOR CHANNEL END APPENDAGE
         ST    RF,AVTABNE               STORE EPA AS AVT ENTRY
*                                       FOR ABNORMAL END
SLDAPP19 EQU   *
         USING WTGENTRY,RWTGC                                    Y02072
         MVC   WTGIDTTR,NEXTMOD         ID OF NEXT EXECUTOR    @Z30TSCF
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURR WTG ENTRY
         LA    RPARC,PLOFF(0,RPARC)     INCR CURRENT DCB ENTRY PRTR
         CLC   0(2,RWTGC),AMIDCNST      THIS RT NEEDED AGAIN
         BCR   RWTGC,RBASE              RETURN
         CLC   0(2,RWTGC),OPIDCNST      END OF TABLE
         BNE   RELOOP                   NO,CHECK NEXT ENTRY
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(0,RWTG)      REINITIALIZE WTG LIST PTR
ZCHEK    CLI   0(RWTGC),X'00'           IS THIS ENTRY COMPLETE
         BNE   TCTLRTN                  IF NOT TRANSFER CONTROL
         LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT ENTRY
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHEK                    TO ZERO CHECK
TCTLRTN  EQU   *
         USING WTG,RWTG
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,            @Z30TSCF*
               MODID=WTGENTRY                                  @Z30TSCF
         DROP  RWTGC,RWTG                                      @Z30TSCF
*
*  COME HERE TO 'LOAD' MODULES                                   Y02072
*
SLDROUT  EQU   *                        DCB ADDR FOR LOADS      ZA02510
         L     R1,CVTPTR                GET COMM VECTOR         ZA02510
         USING CVT,R1                   CVT ADDRESSABILITY      ZA02510
         L     R1,CVTLINK               GET LINKLIB DCB ADDRESS ZA02510
         B     SLDROUT2                 GO LOAD ROUTINE          Y02072
*
*  COME HERE FOR LOAD OF IGG019V6 FROM SVCLIB                    Y02072
*
SLDROUT1 L     R1,CVTPTR                GET COMM VECTOR          Y02072
         USING CVT,R1                   CVT ADDRESSABILITY       YM1140
         L     R1,CVTSVDCB              GET SVCLIB DCB ADDRESS   YM1002
         USING WTG,RWTG                                          Y02072
SLDROUT2 MVC   WTGMODID,0(RB)           ID TO MODULE NAME        Y02072
         DROP  RWTG,R1                                           YM1140
         LOAD  EPLOC=(RWTG),DCB=(1)     LOAD ACCESS METHOD RTN   YM1140
         ST    RE,24(RWTG)              TEMP STORE RTN ADDR
         SR    RD,RD
         IC    RD,DEBNMSUB              GET NO OF SUBROUTINES LOADED
         LA    RD,1(RD)                 ADD ONE TO TOTAL
         STC   RD,DEBNMSUB              STORE NEW TOTAL OF SUBRT LOADED
         MVC   0(2,RUCB),0(RB)          STORE ROUTINES ID IN DEB Y02072
         LA    RUCB,2(RUCB)             UPDATE SUBRT PNTR
         BR    RC                      RETURN
         EJECT
WAOFF    EQU   32
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES
OPIDCNST DC    C'0S'                     ID
AMIDCNST DC    C'92'                    ID OF THIS MODULE          UT18
         SPACE 2
NEXTMOD  DC    C'94',VL3(IGG01994)    NEXT EXECUTOR - IGG01994 @Z30TSCF
*        APPENDAGES USED BY BSAM AND QSAM   ---------------------
AEOEP    DC    C'CZ'                   EOE ID FOR PCI
AEOET    DC    C'CH'                   EOE APPENDAGE NON-SF-INPUT
ACEO     DC    C'CU'                    ID
ACES     DC    C'CK'                    CHANNEL END APPNDG SYSIN DELIM
ACEF     DC    C'CI'                   CHANNEL END APPNDG LEN CHK FREC
ACEV     DC    C'CJ'                   CHANNEL END APPNDG LEN CHK VREC
TOABNE   DC    C'C3'                   TRACK OVERFLOW ABNE APPNDG
ASYNCH   DC    C'C1'                   T.O. ASYNCHRONOUS ERROR ROUTINE
ACEU     DC    C'C0'                    CHANNEL END FOR FORMAT U S20201
         DC    C'  '                    FILLER FOR IGG019FN      YM2507
CEAFP    DC    C'FP'                    SEARCH DIR CHAN END      S20201
*                                       APPEN                    S20201
AEOESF   DC    C'C4'                     SCH DIR EOE APPEN       S20201
VRPCI    DC    C'V6'                    V=R PCI APPENDAGE        Y02072
         SPACE
PATCH    DC    25H'0'                   PATCH AREA               Y02072
END      EQU   *                        END OF MODULE            Y02072
         EJECT
         IECDSECS  MAIN,WTG,PREFX,EXPAND=YES                   @Z30TSCF
         ORG   WTGIDTTR                                          Y02072
WTGID    DS    CL2                      NEXT MOD ID              Y02072
         EJECT
AVT      DSECT                          APPENDAGE VECTOR TABLE
AVTEOEPG DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTEOE   DS    A                        ADDR OF END OF EXTENT
*                                       APPENDAGE
AVTSIOPG DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTSIO   DS    A                        ADDR OF START I/O
*                                       APPENDAGE
AVTPCIPG DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTPCI   DS    A                        ADDR OF PCI APPENDAGE
AVTCEPG  DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTCE    DS    A                        ADDR OF CHANNEL END
*                                       APPENDAGE
AVTABNPG DS    0CL1                     NO.PAGES MODULE REQUIRES
AVTABNE  DS    A                        ADDR OF ABNORMAL END
*                                       APPENDAGE
         EJECT
CVT      DSECT
         CVT
         EJECT
BLDEB    DSECT
DEB      DS    0F                       ALIGN
DEBNMSUB DS    0CL1                     NO. OF SUB.
DEBTCBAD DS    CL4                      TCB ADDR.
DEBAMLNG DS    0CL1                     ACCESS METHOD LEN.
DEBDEBAD DS    CL4                      DEB ADDR.
DEBOGLGS DS    0CL1                     OPTION
DEBIRBAD DS    CL4                      IRB ADDRESS
DEBOPATB DS    0CL1                     OPTION
DEBSYSPG DS    CL4                      SYS ABEND
DEBNMEXT DS    0CL1                     NO. OF EXTENT
DEBUSRPG DS    CL4                      USER PROG.
DEBPRIOR DS    0CL1                     PRIORITY
DEBECBAD DS    CL4                      ECB ADDRESS.
DEBPROTG DS    0CL1                     PROTECTION KEY
DEBDEBID DS    0CL1                     DEB ID
DEBDCBAD DS    CL4                      DCB ADDRESS
DEBEXSCL DS    0CL1                     EXCEPTION
DEBAPPAD DS    CL4                      APPENDAGE ADDRESS
DEBDVMOD DS    0CL1                     DEVICE
DEBUCBAD DS    CL4                      UCB ADDRESS
DEBBINUM DS    CL2                      BIN NUMBER
DEBSTRCC DS    CL2                      START CYC.
DEBSTRHH DS    CL2                      START TRACK
DEBENDCC DS    CL2                      END CYCLINDER
DEBENDHH DS    CL2                      END TRACK
DEBNMTRK DS    CL2                      NO. OF TRACKS
DEBIRBAZ EQU   DEBIRBAD+1
*
         EJECT
           DCBD  DSORG=PS
DCBEOBZ  EQU   DCBEOB+1
DCBCNTZZ EQU   DCBCNTRL+3
DCBEOBWZ EQU   DCBEOBW+1
DCBCNTRZ EQU   DCBCNTRL+1
DCBEOBRZ EQU   DCBEOBR+1
DCBSYZ   EQU   DCBSYNAD+2
DCBSYX   EQU   DCBSYNAD+3
DCBGETZ  EQU   DCBGET+1
DCBMACRZ EQU   DCBMACRF+1
DCBPERZ  EQU   DCBPERR+1
DCBCHECZ EQU   DCBCHECK+1
         EJECT
         IKJTCB
           END
