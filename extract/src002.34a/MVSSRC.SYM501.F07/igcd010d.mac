         TITLE 'IGCD010D - DISPLAY SCAN/MAP/DISPATCH ROUTINE'
IGCD010D CSECT
*A000000-999999                                                  S99240
*A325000,409000,443000,445000,537000,542300-543500,756100-757000 S22024
*A778200-779950,871500,872800-873900,906000-909000               S22024
*C018000,292000,338000-374000,386000-390000,394000,398000-400000,S22024
*C406000-408000,442000,444000,536000,702000,756000,768000-774000,S22024
*C778000                                                         S22024
*D476000-496000,512000-518000,758000                             S22024
*A854000                                                       @OY12425
*C506000,684000-694000,700000-820000,826000                    @OY12425
*D592000,871000,872800-873900,884000,912000,932000             @OY12425
*A378000,409500                                                @OY12649
*A014000,070000,854000,892000,933000                           @Y17XARX
*C018000,740000                                                @Y17XARX
*D014000,374000,409000                                         @Y17XARX
*A855200                                                       @PI06869
*A371000,408000                                                @G36XRRU
*C855500                                                       @OZ26490
*A826400,930000                                                @OY20827
***********************************************************************
*                                                                     *
* MODULE NAME = IGCD010D (TCAM, OPERATOR CONTROL)              @Y17XARX
*                                                              @Y17XARX
* DESCRIPTIVE NAME = DISPLAY SCAN/MAP/DISPATCH ROUTINE         @Y17XARX
*                                                              @Y17XARX
* COPYRIGHT = NONE                                             @Y17XARX
*                                                                     *
* STATUS = VERSION 10.0                                        @Y17XARX
*                                                                     *
*                                                                     *
*FUNCTION--THIS MODULE GAINS CONTROL FROM IGC0110D WHEN THE COMMAND IS*
*   FOUND TO BE A 'DISPLAY'. THIS ROUTINE WILL PERFORM VALIDITY       *
*   CHECKING OF OPERANDS, MAP NEEDED INFORMATION USED BY THE DISPLAY  *
*   FUNCTIONAL ROUTINES INTO THE OPERATOR CONTROL ELEMENT (OPCE), AND *
*   TRANSFER CONTROL TO THE APPROPRIATE FUNCTIONAL ROUTINE VIA AN XCTL*
*                                                                     *
*   IF DURING ITS VALIDITY CHECKING IT FINDS THE COMMAND TO BE INVALID*
*   IT XCTLS TO THE ERROR WRITER ROUTINE IGC0310D TO WRITE OUT AN     *
*   ERROR MESSAGE.                                                    *
*                                                                     *
*   IT DETERMINES WHAT PARTICULAR DISPLAY COMMAND IS REQUESTED AND    *
*   BALRS TO SUBROUTINES IN IEDQCA TO DO THE ACTUAL VALIDITY CHECKING *
*   OF STATION NAMES, GROUPNAMES, RELATIVE LINE NUMBER, AND LINE      *
*   ADDRESS. IEDQCA WILL, BY ITS RETURN POINT, INDICATE WHETHER THE   *
*   REQUESTED VALIDITY CHECK WAS SUCCESSFUL OR NOT. IF UNSUCCESSFUL   *
*   AN XCTL TO IGC0310D WILL BE TAKEN TO HAVE AN APPROPRIATE ERROR    *
*   MESSAGE WRITTEN. IF SUCCESSFUL IGCD010D INSERTS INTO THE OPCE     *
*   INFORMATION THAT THE VARIOUS FUNCTIONAL ROUTINES WILL NEED TO     *
*   PROCESS THE COMMAND.                                              *
*                                                                     *
*   IT CAN INSERT INTO THE OPCE DEPENDING ON THE COMMAND THE TERMINAL *
*   NAME, TERMINAL ENTRY ADDRESS, GROUPNAME OF A LINE, A RELATIVE LINE*
*   NUMBER, A HARDWARE LINE ADDRESS, AS WELL AS SETTING BITS IN       *
*   OCSWITCH TO IDENTIFY THE COMMAND                                  *
*   D TP,ACT & D TP,INACT CAN ACCEPT ANY NAME OF A TTE AS      @Y17XARX
*   INPUT.   THEREFORE, A SUBROUTINE WAS ADDED (CKTTE) TO      @Y17XARX
*   DETERMINE IF THE NAME IN OPCDOUBL COULD BE MAPPED TO A     @Y17XARX
*   VALID TTE.  IF SO, THE TTE ADDR IS STORED IN OCELEM AND    @Y17XARX
*   OCLINK IS CLEAR AS AN INDICATOR.                           @Y17XARX
*                                                              @Y17XARX
*    D TP,PEND SUPPORT HAS BEEN ADDED TO DISPLAY HUNG SNA      @Y17XART
*    RESOURCES.  INPUT TO THIS COMMAND IS AN NCPNAME,          @Y17XART
*    SNA LINENAME, PUNAME,OR LUNAME.  CKTTE SUBROUTINE         @Y17XART
*    WILL BE USED TO VALIDITY CHECK THE INPUT.                 @Y17XART
*                                                              @Y17XART
*ENTRY POINTS--                                                       *
*   'IGCD010D TO SCAN,VALIDITY CHECK,MAP,AND DISPATCH.                *
*                                                                     *
*INPUT-- REGISTER 1- ADDRESS OF OPERATOR CONTROL AVT                  *
*   OPERATOR CONTROL RESIDENT WORKAREA - CONTAINS COMMAND TO VERIFY   *
*   STARTING WITH 'TP'                                                *
*                                                                     *
*OUTPUT-- REGISTER 1- ADDRESS OF OPERATOR CONTROL AVT                 *
*                                                                     *
*EXTERNAL REFERENCES--                                                *
*   IEDQCA'S SUBROUTINES                                              *
*        TRMOFLOC--VALIDITY CHECKS TERMINAL NAME PASSED TO IT. RETURNS*
*                  ADDRESS OF TERMINAL ENTRY IF VALID. IF NOT VALID   *
*                  RETURNS PLUS 4 PAST ADDRESS IN REGISTER 14.        *
*                                                                     *
*        DCBLOCAT--VALIDITY CHECKS HARDWARE LINE ADDRESSES,GROUPNAMES *
*                  RELATIVE LINE NUMBERS.ALSO INSURES LINE DCBS ARE   *
*                  OPEN,RLN IS NOT MORE THAN NUMBER OF EXTENTS,AND    *
*                  LINE WAS NOT OPENED DUMMY. FOR ANY OF THESE ERRORS *
*                  IT RETURNS TO ADDRESS PLUS 4 PAST ADDRESS IN       *
*                  REGISTER 14. IF VALID IT RETURNS ON R14            *
*                               REGISTER 0 - HAS UCB ADDRESS          *
*                               REGISTER 1 - HAS DCB ADDRESS          *
*                                                                     *
*EXITS,NORMAL--COMMAND IS SYNTAX CHECKED, AND OPERANDS ARE VERIFIED   *
*   AND CONTROL PASSES TO DISPLAY FUNCTIONAL ROUTINE VIA AN XCTL      *
*   THE OPERATOR CONTROL AVT ADDRESS IS PASSED IN REGISTER 1          *
*                                                                     *
*EXITS,ERROR--                                                        *
*   CONTROL PASSES TO THE ERROR WRITER IGC0310D TO WRITE OUT AN ERROR *
*   MESSAGE.                                                          *
*   ERROR MESSAGES ARE-                                               *
*   IED016I STATION NOT FOUND                                         *
*   IED017I LINE 'GRPNAME/RLN' NOT OPEN                               *
*   IED018I 'OPERATION' COMMAND INVALID                               *
*                                                                     *
*TABLES/WORK AREAS-- OPERATOR CONTROL AVT,TCAM AVT,OPCE,OPERATOR      *
*   CONTROL RESIDENT WORK AREA.                                       *
*                                                                     *
*ATTRIBUTES--SUPERVISOR MODE,DISABLED,TRANSIENT(TYPE 4),NON-REENTRANT *
*   SERIALLY REUSABLE,REFRESHABLE                                     *
*                                                                     *
*CHARACTER CODE DEPENDENCY                                            *
*        THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        *
*        REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS        *
*        EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING     *
*        HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER          *
*        CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE     *
*        FOR THE NEW DEFINITION.                                      *
*                                                                     *
*NOTES                                                                *
*        COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS      *
*        PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                  *
*                                                                     *
***********************************************************************
         EJECT
*        REGISTER EQUATES
RZERO    EQU   0                   REGISTER 0 - BRANCH PARAMETER
REG1     EQU   1                   WORK REGISTER
RPARM    EQU   1                   OPC AVT ADDRESS
ROPCAVT  EQU   2                   POINTER TO OPC AVT
ROPCE    EQU   3                   POINTER TO CURRENT OPCE
RAVT     EQU   4                   POINTER TO TCAM AVT
RWORKA   EQU   5                   POINTER TO RESIDENT WORKAREA
RWORK6   EQU   6                   WORK REGISTER
RTERM    EQU   6                   POINTER TO TERMINAL ENTRY
RWORK7   EQU   7                        WORK REG                 S22024
RTNT     EQU   7                   POINTER TO TERMNAME TABLE
RUCB     EQU   7                   POINTER TO UCB
RWORK8   EQU   8                   WORK REGISTER
RTABLE   EQU   9                   WORK REGISTER
RWORK9   EQU   9                   WORK REGISTER
RWORK10  EQU   10                  WORK REGISTER
RWORK11  EQU   11                  WORK REGISTER
RBASE    EQU   12                  BASE REGISTER
RSAVE    EQU   13                  POINTER TO SAVEAREA
RRETURN  EQU   14                  RETURN REGISTER
RLINK    EQU   15                  BRANCHING REGISTER
*
         SPACE 3
         USING IEDQOPCD,ROPCAVT    OPC AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE      OPC ELEMENT ADDRESSABILITY
         USING IEDQAVTD,RAVT       AVT ADDRESSABILITY
         BALR  RBASE,0             SET BASE REGISTER
         USING *,RBASE             ADDRESSABILITY
*                                       BASE REENTRANT OPERATOR CONTROL
IGCD010D IEDHJN SKIPID
         LR    ROPCAVT,RPARM       SET BASE FOR OPC AVT
         L     RAVT,OPCAVTPT       SET BASE FOR TCAM AVT
         L     RWORKA,OPCWORK      GET ADDRESS OF WORKAREA
         L     ROPCE,OPCCOPCE      GET ADDR OF CURRENT OPC ELEMENT
         LA    RSAVE,OPCSAVE            GET ADDR OF OPC SAVE AREA
         XC    OCSWITCH,OCSWITCH   CLEAR FOR POSSIBLE SETTING FOR RTNS
         MVI   OPCDOUBL,AVTEBLNK   MOVE A BLANK INTO 1ST BYTE
*                                  OF OPCDOUBL FOR MVC
         MVC   OPCDOUBL+ONE(SEVEN),OPCDOUBL PROPAGATE BLANKS
         LH    RWORK7,TWO(RWORKA)   GET LENGHT OF DATA FIELD
         LA    RWORK7,DATAOFF(RWORKA,RWORK7) GET ADDR OF WHERE TO PUT
*                                  PUT BLANK
         MVI   ZERO(RWORK7),AVTEBLNK  INSERT A BLANK AT END OF DATA
         LA    RWORK8,DATAOFF(RWORKA) BUMP ADDR TO START OF DATA FIELD
         CLC   ZERO(THREE,RWORK8),TP VALIDITY CHECK FOR 'TP'
         BNE   CMDINVAL            COMMAND NOT VALID
         LA    RWORK8,THREE(RWORK8) BUMP PTR TO AFTER COMMA
         LA    RTABLE,TABLE        GET ADDR OF TABLE OF POSSIBLE CMNDS
         CLI   ZERO(RWORK8),AVTEBLNK IS THERE A BLANK AFTER'TP,'
         BE    CMDINVAL                 YES - COMMAND INVALID    S22024
         SR    REG1,REG1           CLEAR REGISTER 1
         SR    RWORK10,RWORK10     CLEAR REGISTER 10
         LR    RWORK11,RWORK8      SAVE PTR TO 1ST CHAR
TABLOOP  IC    REG1,0(RTABLE)      GET 1 BYTE LENGTH OFF NAME INDICATOR
         LTR   REG1,REG1           END OF TABLE
         BZ    CMDINVAL            YES, MATCH NOT FOUND
         EX    REG1,COMP           NO- COMPARE TAB NAME AGAINST WORK
*                                  AREA NAME
         LA    RTABLE,FOUR(REG1,RTABLE) BUMP TABLE TO NEXT ENTRY S22024
         LA    RWORK10,FOUR(RWORK10)    OFFSET IN BRANCHING TABLE TO
*                                       TAKE WHEN MATCH FOUND SO CAN
*                                       PROCESS REST OF OPERANDS
         BNE   TABLOOP             TABLE ENTRY DIDN'T MATCH WORKAREA
*                                  ENTRY
         AR    RWORK8,REG1         BUMP PAST 1ST OPERAND
         LA    RWORK11,ONE(RWORK8) UP PAST DELIMITER
         ST    RWORK11,OCELEM           POINTER TO ADDRESS
         SH    RTABLE,AVTHA2       BACK UP TO MODULE ID          S22024
         MVC   OPCLDNME+OFF03(AL2),0(RTABLE) SET MODULE ID XCTL  S22024
         LA    RWORK6,BRNCHTAB-FOUR     BRANCH TABLE BASE ADDR   S22024
         BC    15,0(RWORK10,RWORK6) BRANCH TO PROPER BRNCH PT
COMP     CLC   ONE(ZERO,RTABLE),ZERO(RWORK8) WORKAREA AND TABLE ENTRY
*                                       MATCH
         EJECT
TABLE    EQU   *                   TABLE OF POSSIBLE VALID DISPLAY
*                                  COMMANDS
         DC    X'03',C'ACT,',C'D3' ACTIVE ENTRIES                S22024
         DC    X'04',C'LIST,',C'D7' AUTOPOLL                     S22024
         DC    X'04',C'ADDR,',C'D6' STATION RLN                  S22024
         DC    X'04',C'TERM,',C'D5' STATION STATUS               S22024
         DC    X'04',C'LINE,',C'D9' LINE STATUS                  S22024
         DC    X'05',C'INACT,',C'D3' INACTIVE ENTRIES            S22024
         DC    X'05',C'QUEUE,',C'D2' QUEUEING STATUS             S22024
         DC    X'06',C'OPTION,',C'D8' OPTION STATUS              S22024
         DC    X'07',C'PRITERM ',C'D1' PRIMARY TERMINAL          S22024
         DC    X'07',C'SECTERM ',C'D1' SECONDARY TERMINAL        S22024
         DC    X'05',C'INTER ',C'D4' INTERCEPTED STATIONS        S22024
         DC    X'06',C'STATUS,',C'DB' SWITCH STATUS              S22024
         DC    X'05',C'STORE,',C'DC' 3705 STORAGE              @G36XRRU
         DC    X'04',C'POLL,',C'M4'     POLL                   @OY12649
         DC    X'04',C'PEND,',C'DL' PENDING STATIONS           @YM07028
         DC    X'00'               END OF TABLE
         EJECT
BRNCHTAB B     INACTACT                 ACTIVE ENTRIES           S22024
         B     GRPADDR             AUTOPOLL                      S22024
         B     ADDRB               RELATIVE LINE NUMBER
         B     ADDRB               STATION STATUS                S22024
         B     LINE                LINE AND ERROR STATUS
         B     GRPADDR             INACT COMMAND                 S22024
         B     ADDRB               QUEUEING STATUS               S22024
         B     OPTIONB             OPTION STATUS
         B     PRISEC              PRIMARY TERMINAL
         B     XCTLA               SECONDARY TERMINAL REQUEST    S22024
         B     XCTLA               INTERCEPTED STATIONS          S22024
         B     XCTLA               STATUS COMMAND                S22024
         B     XCTLA               STORAGE COMMAND             @G36XRRU
         B     ADDRB                    POLL COMMAND           @OY12649
         B     PEND                     PENDING STATIONS       @Y17XART
         EJECT
PRISEC   EQU   *                   THIS COMMAND REQUESTS THE NAME OF
*                                  THE CURRENT PRIMARY OR SECONDARY
*                                  OPERATOR CONTROL TERMINAL
         OI    OCSWITCH,XEIGHTY         PRIMARY REQUEST
         B     XCTLA               BRANCH TO XCTL
         SPACE 3
LINE     EQU   *                   THIS CMD REQUESTS DISPLAY OF THE
*                                  STATUS FIELD FOR SPECIFIED LINES
         CLC   ZERO(NINE,RWORK11),INACTVE IS THIS LINE INACTIVE  S22024
         BNE   GRPADDR             BRANCH ON NO                  S22024
         MVC   OPCLDNME+OFF03(AL2),INACTV SET MODULE ID          S22024
         B     XCTLA               TRANSFER CONTROL              S22024
         SPACE 3
INACTACT EQU   *                   RWORK8 POINTS TO 1ST CHAR AFTER(,)
*                                       AFTER 'ACT'. XCTL TO IGCD310D
*                                  VALIDITY CHECK OPERANDS AFTER 'ACT,'
         OI    OCSWITCH,XEIGHTY         ACT COMMAND
         B     GRPADDR             BRANCH TO GROUP NAME CHECK
         SPACE 3
OPTIONB  EQU   *                   REQUESTS DISPLAY OF TERMINAL OPTION
*                                  FIELDS
         LA    RWORK6,ZERO              BRANCHING INDEX
         B     ADTERMQU            VALIDITY CHECK
         SPACE 3
ADDRB    EQU   *                   REQUESTS DISPLAY OF THE LINE ADDRESS
*                                  AND RLN FOR A SPECIFIED TERMINAL
         LA    RWORK6,FOUR         SETUP FOR PROPER BRANCH     @OY12425
         B     ADTERMQU            VALIDITY CHECK
         SPACE 3
PEND     EQU   *                        DISPLAY OF HUNG STAT   @Y17XART
         OI    OCSWITCH,XTWENTY         SET UNIQUE VALUE       @Y17XART
         B     GRPADDR                  PROCESS STATIONS NAME  @Y17XART
OPTIONME EQU   *                   ENTERED BY OPTION CMD 1ST CHECK
*                                  IF STATNAME IS VALID
         CLI   ZERO(RWORK8),COMMA   COMMA PRESENT
         BNE   CMDINVAL            YES ERROR
         LA    RWORK10,NINE             PREPARE FOR COMPARE
         SR    RWORK10,RWORK9      NUM OF DIGITS IN CMD
         BZ    CMDINVAL            COMMAND INVALID WRITE ERR MSG
         LA    RWORK6,FOURTEEN          SET UP TO BRANCH TO      S22024
*                                       OPTION COMMAND CODE      S22024
         B     TERMVALD            GO FIND TERMINAL ENTRY
         SPACE 5
XCTLA    EQU   *
         CLC   OPCLDNME+OFF03(AL2),LINEID  LINE COMMAND          S22024
         BNE   CLEARALL                 NO CLEAR AS USUAL        S22024
         MVI   FOUR(RWORKA),AVTEBLNK    INSERT A BLANK           S22024
         MVC   FIVE(WKSIZE,RWORKA),FOUR(RWORKA) PROPAGATE BLANKS S22024
CLEARALL EQU   *                                                 S22024
         MVI   FOURTY(RWORKA),AVTEBLNK  INSERT BLANK IN WORKAREA
         MVC   FOURTY1(WKSIZE,RWORKA),FOURTY(RWORKA) PROPAGATE BLANKS
         LA    RWORKA,WKSIZE-ONE(RWORKA) BUMP WORKAREA ADDR
         MVC   ONE(WKSIZE,RWORKA),ZERO(RWORKA) MOVE MORE BLANKS
         LR    RPARM,ROPCAVT       PUT ADDR OPC AVT IN REG 1
         XCTL  SF=(E,OPCXCTL)      CALL PROPER SVC LOAD MODULE
         EJECT
ADTERMQU EQU   *
         CLI   ZERO(RWORK11),AVTEBLNK A BLANK PRESENT
         BE    CMDINVAL            YES COMMAND IS INVALID
         CLI   ZERO(RWORK11),COMMA  COMMA PRESENT
         BE    CMDINVAL            YES THE CMD IS INVALID
OPT      LA    RWORK7,FOUND        SET BRANCHING REGISTER
OPFIELD  LA    RWORK9,TEN               SET COUNTER
SCAN     LA    RWORK8,ONE(RWORK8)  BUMP TO NXT VALUE
GROUP    BCT   RWORK9,COMPARE      HAVE WE EXCEEDED OUR LIMIT BRANCH NO
         B     CMDINVAL            SEND ERROR MESSAGE
COMPARE  CLI   ZERO(RWORK8),AVTEBLNK   FOUND A BLANK
         BE    0(RWORK6,RWORK7)      YES
         CLI   ZERO(RWORK8),COMMA       FOUND A COMMA?
         BE    0(RWORK6,RWORK7)         YES
         B     SCAN                     NO- KEEP SEARCHING
FOUND    DS    0H
         B     OPTIONME                 COMMAND IS FOR OPTION
         CLI   ZERO(RWORK8),AVTEBLNK  BLANK PRSENT
         BNE   CMDINVAL            YES
         LA    RWORK10,NINE             PREPARE FOR COMPARE
         SR    RWORK10,RWORK9      NUM OF DIGITS IN CMD
         BZ    CMDINVAL            INVALID OPERAND
         LA    RWORK6,ZERO              SET BRANCHING REGISTER
TERMVALD BCTR  RWORK10,ZERO             DECREMENT FOR EXECUTE
         EX    RWORK10,MOVENAME    MOVE NAME INTO OPCDOUBL
GETTERM  L     RLINK,OPCTOFLK      GET TERM LOOKUP RTN
         LA    RWORK9,BRANCHTA          ADDRESS OF BRANCHING TABLE
         BALR  RRETURN,RLINK       BRANCH TO IT
         BC    15,0(RWORK6,RWORK9) TERMINAL NAME IS VALID
         B     TERMERR             COMMAND IS INVALID
BRANCHTA EQU   *
         MVC   OCBACKUP(OPCEIGHT),OPCDOUBL  SAVE TERMINAL NAME
         ST    REG1,OCELEM         SAVE TERM  ENTRY ADDRESS
         B     XCTLA                    XCTL TO PROCESSING RTN
         EJECT
         LA    RWORK11,TWO(RWORK10,RWORK11) BUMP PTR PAST COMMA
*                                       AFTER TERM NAME
         MVC   OCBACKUP(OPCEIGHT),OPCDOUBL SAVE TERMINAL NAME
         SR    RWORK6,RWORK6       CLEAR REGISTER FOR BRANCH DISP
         BAL   RWORK7,OPFIELD      BRANCH TO CHECK OPFIELD
FNDBLNK  EQU   *
         CLI   ZERO(RWORK8),AVTEBLNK  BLANK PRESENT
         BE    CMDINVAL                 YES, SO CMD IS INVAL FOR OPT
         LA    RWORK10,NINE             PREPARE FOR COMPARE
         SR    RWORK10,RWORK9      NUM OF DIGITS IN CMD
         BNP   CMDINVAL            ERROR
         MVI   OPCDOUBL,AVTEBLNK   MOVE A BLANK INTO 1ST BYTE
*                                  OF OPCDOUBL FOR MVC
         MVC   OPCDOUBL+ONE(SEVEN),OPCDOUBL PROPAGATE BLANKS
         BCTR  RWORK10,ZERO             DECREMENT FOR EXECUTE
         EX    RWORK10,MOVENAME    MOVE NAME INTO OPCDOUBL
         ST    RPARM,OCELEM             SAVE TERM ENTRY ADDRESS
         ST    RWORK8,OCLINK            SAVE ADDROOF COMMA AFTER OPTNME
         B     XCTLA               BRANCH TO XCTL
         SPACE 3
GRPADDR  EQU   *                   CODE WHICK IS ENTERED IF COMMAND IS
*                                  ANY OF THE FOLLOWING:
*                                           ACT
*                                           INACT
*                                           LINE
*                                           LIST
*                                           PEND               @Y17XART
*                                  THIS SECTION OF CODE WILL   @OY12425
*                                  BRANCH TO OPCSCAN RTN IN QCA@OY12425
*                                  WHERE CMD WILL BE VALIDITY  @OY12425
*                                  CHECKED. DCBLOOKUP WILL THEN@OY12425
*                                  VERIFY NAME AND RETURN TO   @OY12425
*                                  D010D.                      @OY12425
         CLI   ZERO(RWORK11),AVTEBLNK  A BLANK PRESENT
         BE    CMDINVAL            YES
         MVI   OCSCAN,OCADDR+OCDDNRLN  ITS AN ADDR OR DDN,RLN  @OY12425
         L     RLINK,OPCDCBLK                                  @OY12425
         BAL   RRETURN,FOUR(RLINK) BRANCH TO OPCSCAN RTN       @OY12425
         B     SET4ADDR            NAME WAS FOUND              @OY12425
         B     CKTTE2              MAYBE SNA NAME              @Y17XART
         B     CKTTE               CHECK SNA NAME              @Y17XART
         EJECT
SET4ADDR EQU   *
         CLC   OPCMODID,MODDL           EXIT TO DL             @YM07027
         BE    CMDINVAL                 GRP,RLN INVALID FOR DL @YM07027
         MVC   OCBACKUP(OPCEIGHT),OPCDOUBL  SAVE GRPNME        @OY12425
         CLI   ZERO(RWORK11),AVTEBLNK  FLD COMPLETELY SCANNED? @OY12425
         BNE   CMDINVAL            NO, ERROR                   @OY12425
         BCTR  RWORK11,ZERO        CHECK IF DELIMITER AFTER    @OY20827
*                                  RLN                         @OY20827
         CLI   ZERO(RWORK11),COMMA IS THERE A COMMA DELIMITER? @OY20827
         BE    CMDINVAL            YES, ERROR                  @OY20827
         CLI   ZERO(RWORK11),PARENR IS THERE A RIGHT PAREN?    @OY20827
         BE    CMDINVAL            YES, ERROR                  @OY20827
         CLI   ZERO(RWORK11),SLASH IS THERE A SLASH DELIMITER? @OY20827
         BE    CMDINVAL            YES, ERROR                  @OY20827
         LA    RWORK11,ONE(0,RWORK11) POINT BACK TO BLANK      @OY20827
         TM    OCSCAN,OCNOTUCB     DOES HI-ORDER BYTE          @OY12425
*                                  SPECIFY AN RLN?             @OY12425
         BZ    GOXCTL              NO, CONTINUE                @OY12425
         OI    OCSWITCH,XFOURTY    INDICATE NOT LINE @         @OY12425
GOXCTL   EQU   *                                               @YM07340
         ST    RZERO,OCLINK        SAVE UCB ADDR
         ST    REG1,OCELEM         SAVE DCB ADDR
         B     XCTLA               BRANCH TO XCTL
         EJECT
CMDINVAL EQU   *                   ENTERED BY VARIOUS SUBRTNS WHEN AN
*                                  A PART OF A CMD IS INVALID
         LA    RZERO,EIGHT10            MSG NO. 18
MOVEID03 MVC   OPCLDNME+OFF03(AL2),ERRMSG MOVE IGC0310D ID
         B     XCTLA               BRANCH TO XCTL
         SPACE 3
TERMERR  EQU   *                   INVALID COMMAND
         LA    RZERO,SIXTEEN            STATION FOUND
         B     MOVEID03            CMD INVALID BRANCH TO ERROR WTR
NOTFOUND EQU   *                                               @OY12425
         LA    RZERO,SEVEN10       GET MSG # IN REGISTER       @OY12425
         B     MOVEID03            REG0 HAS PROPER ERROR MSG # @OY12425
*                                  INSERT ID OF IGC0310D       @OY12425
CKTTE    EQU   *                                               @Y17XARX
         CLC   OPCMODID,MODDL           IF XCTL TO DL          @Y17XART
         BE    OK                       THEN PROCESS           @Y17XART
         CLC   OPCMODID,MODD3           CHK XCTL NAME FOR 'D3' @Y17XARX
         BNE   CMDINVAL       EXIT BECAUSE PART OF CMD INVALID @Y17XARX
OK       EQU   *                                               @Y17XART
         L     RLINK,OPCTOFLK       ADDR TERM LOOKUP ROUTINE   @Y17XARX
         BALR  RRETURN,RLINK        BRANCH TO LOOKUP ROUTINE   @Y17XARX
         B     TTESETUP                 TERM NAME IS VALID     @Y17XARX
         LA    RPARM,ONE                SET RLN TO ONE         @YM07289
         L     RLINK,OPCDCBLK           DCB LOOKUP ADDRESS     @YM07289
         BALR  RRETURN,RLINK            LINK TO DDNAME LOOKUP  @YM07289
         B     CK3705                   VALID DDNAME, CHECK    @YM07289
*                                        FOR 3705 DCB          @YM07289
         B     TERMERR                  TERM NAME IS INVALID   @OZ26490
TTESETUP EQU   *                                               @Y17XARX
         ST    REG1,OCELEM         STORE ADDRESS OF VALID TTE  @Y17XARX
         SR    REG1,REG1                CLEAR REGISTER         @Y17XARX
         ST    REG1,OCLINK         INDICATOR TO NEXT MODULE,   @Y17XARX
*                                       OCELEM HAS THE ADDRESS.@Y17XARX
         NI    OCSWITCH,OCFOXES-XFOURTY  INDICATE INPUT TO     @PI06869
*                                       IGCD310D IS A TTE, NOT @PI06869
*                                       A GROUPNAME, RLN       @PI06869
         MVC   OCBACKUP(OPCEIGHT),OPCDOUBL SET TRM NAME IN OPCE@YM07289
         B     XCTLA                    NORMAL EXIT            @Y17XARX
CKTTE2   EQU   *                                               @Y17XART
         CLC   OPCMODID,MODDL           IF XCTL TO DL          @Y17XART
         BE    OK2                      THEN CHECK SNA NAME    @Y17XART
         CLC   OPCMODID,MODD3           IF XCTL TO D3          @Y17XART
         BNE   NOTFOUND                 NO, PROCESS AS OLD     @Y17XART
OK2      EQU   *
         L     RLINK,OPCTOFLK           ADDR OF TERM LOOKUP    @Y17XART
         BALR  RRETURN,RLINK            BRNCH TO RTN           @Y17XART
         B     TTESETUP                 TERM NAME IS VALID     @Y17XART
         B     NOTFOUND                 PROCESS AS OLD         @Y17XART
CK3705   EQU   *
         USING IHADCB,REG1              DCB ADDRESSABILITY     @YM07289
         TM    DCBDSRG2,DCBDSGTR        3705 DCB               @YM07289
         BNO   CMDINVAL                 NO,INVALID COMMAND     @YM07289
         B     SET4ADDR                 VALID FOR 3705         @YM07289
         EJECT
*        EQUATES
AL1      EQU   1                        OFFSET
OFF03    EQU   3                   OFFSET 3
OFF05    EQU   5                        OFFSET FOR ID
OFF07    EQU   7                   OFFSET 7
AL2      EQU   2                   LENGTH OF 2
DATAOFF  EQU   8                        OFFSET OF DATA IN WORKAREA
TWO      EQU   2                        VALUE TWO
FOUR     EQU   4                        VALUE FOUR
FIVE     EQU   5                        VALUE FIVE
SEVEN    EQU   7                        VALUE SEVEN
NINE     EQU   9                        VALUE NINE
TEN      EQU   10                       VALUE TEN
FOURTEEN EQU   14                       CODE OFFSET              S22024
SIXTEEN  EQU   16                       VALUE SIXTEEN
SEVEN10  EQU   17                       VALUE SEVEN10
EIGHT10  EQU   18                       VALUE EIGHT10
XFOURTY  EQU   X'40'                    HEX '40'
XEIGHTY  EQU   X'80'                    HEX '80'
XTWENTY  EQU   X'20'                    HEX '20'               @Y17XART
XFC      EQU   X'FC'                    HEX 'FC'
*
*
*
MOVENAME MVC   OPCDOUBL(ZERO),ZERO(RWORK11) MOVE NAME TO OPCDOUBL
MOVERLN  PACK  OPCDOUBL,ONE(ZERO,RWORK11) PACK NUM IN A DOUBLE WRD
         SPACE 3
*        CONSTANTS
TP       DC    C'TP,'                   FIRST THREE CHAR IN CMD
ERRMSG   DC    C'03'               BUILD ICC0310D MODULE
MODD3    DC    C'D3'                    CHECK FOR MOD 'D3'     @Y17XARX
MODDL    DC    C'DL'                    MODULE IGCDL10D        @Y17XART
LINEID   DC    C'D9'                    DISPLAY LINE RTN ID
INACTV   DC    C'DD'               LINE INACTIVE IDENTIFIER      S22024
INACTVE  DC    C'INACTIVE '             INACTIVE                 S22024
FOURTY   EQU   40                       OFFWET FOR BLANK
FOURTY1  EQU   41                       OFFSET TO START PROPAGATING BLK
WKSIZE   EQU   250                      SIZE OF AREA TO BLANK OUT
*        EQUATES
ZERO     EQU   0                        OFFSET
ONE      EQU   1                        OFFSET
THREE    EQU   3                        OFFSET
OPCEIGHT EQU   8                   NUM OF CHAR TO MOVE
COMMA    EQU   X'6B'               EBCDIC COMMA
PARENR   EQU   C')'                                            @OY20827
SLASH    EQU   C'/'                                            @OY20827
COMMAL   DC    C',L '                   COMMA L AND BLANK
         DC    25X'FF'                  PATCH AREA             @Y17XARX
         EJECT
*        MACROS
         TOPCED
         EJECT
         TOPCAVTD
         EJECT
         TAVTD
         EJECT
         DCBD  DSORG=TX                                        @YM07289
         END
