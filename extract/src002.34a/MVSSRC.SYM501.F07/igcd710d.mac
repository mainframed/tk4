         TITLE 'IGCD710D - DISPLAY INVITATION LIST ENTRIES'
IGCD710D CSECT
*A-000000-999999                                               @X31X8R0
         SPACE 3                                                 S21903
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A475000,526500,802000,806000                                    X03039
*C031000,480000-484000,525000-526000,812000                      X03039
*D352000,420000,436000,488000,500000,700000,708000,716000-728000,X03039
*D738100,774000,798000,816000                                    X03039
*A 544000,552000                                                SA63010
*A008000                                                       @Z30X8RR
*C031000,156000,208000,236000,248000,384000,508000,512000,     @Z30X8RR
*C516000,520000,524000                                         @Z30X8RR
*D010000,216000-224000,240000                                  @Z30X8RR
*A011001,100000,160000,176000,208000,212000,525500,792000      @Y17XARX
*C028000,031000,128000,132000,140000,144000,152000,156000,     @Y17XARX
*C164000,172000,208000,525500,612500                           @Y17XARX
*D016000-020000,244000-296000                                  @Y17XARX
         SPACE 3                                                 S21903
***********************************************************************
*                                                                     *
*  MODULE NAME = IGCD710D (TCAM, OPERATOR CONTROL)             @Y17XARX
*                                                                     *
*  DESCRIPTIVE NAME = DISPLAY INVITATION LIST ENTRIES                 *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS = VERSION 10.0                                       @Y17XARX
*                                                                     *
*FUNCTION -- THIS MODULE PROCESSES OPERATOR CONTROL COMMANDS WHICH    *
*   REQUEST DISPLAY OF THE STATUS FIELD OF INVITATION LISTS.          *
*                                                                     *
*   WHEN ENTERED IGCD010D HAS VALIDITY CHECKED OPERANDS IN THE COMMAND*
*   BY BALRING TO SUBROUTINES IN IEDQCA. ERROR MESSAGES ARE GENERATED *
*   BY IGCD010D XCTLING TO THE ERROR WRITER ROUTINE IGC0310D FOR THE  *
*   FOLLOWING REASONS:                                                *
*                                                                     *
*   RLN IS ALL,ZERO,OR GREATER THAN 255.                              *
*   LINE NOT FOUND SEARCHING DEB CHAIN,RLN NOT IN LINE GROUP,LINE NOT *
*             OPEN OR DD DUMMY.                                       *
*                                                                     *
*   IT OBTAINS FROM OCELEM IN THE OPCE THE ADDRESS OF THE ASSOCIATED  *
*   DCB. FROM THE DCB THE INVITATION LIST IS OBTAINED (DCBINVLI) AND  *
*   THE STATUS BYTE VALVES ARE CONVERTED TO THEIR PRINTABLE           *
*   EQUIVALENTS AND PLACED IN THE RESPONSE. THE ROUTINE XCTLS TO THE  *
*   MESSAGE WRITER IGC0710D.                                          *
*                                                              @Y17XARX
* NOTES = COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES @Y17XARX
* IS PRIMARILY THROUGH THE OPERATOR CONTROL AVT.               @Y17XARX
*                                                              @Y17XARX
*    DEPENDENCIES = EBCDIC CHARACTER CODE DEPENDENCIES ARE     @Y17XARX
*    CORRECTABLE BY REASSEMBLY.                                @Y17XARX
*                                                              @Y17XARX
*    RESTRICTIONS = NA                                         @Y17XARX
*                                                              @Y17XARX
*    REGISTER CONVENTIONS = STANDARD                           @Y17XARX
*                                                              @Y17XARX
*    PATCH LABEL = NONE                                        @Y17XARX
*                                                              @Y17XARX
* MODULE TYPE = PROCEDURE                                      @Y17XARX
*                                                              @Y17XARX
*    PROCESSOR = ASSEMBLER                                     @Y17XARX
*                                                              @Y17XARX
*    MODULE SIZE = 371 BYTES                                   @Y17XARX
*                                                              @Y17XARX
*    ATTRIBUTES = SERIALLY REUSABLE, ENABLED, TRANSIENT(TYPE4),@Y17XARX
*    SUPERVISOR MODE.                                          @Y17XARX
*                                                                     *
*ENTRY POINTS --                                                      *
*        'IGCD710D' TO PROCESS DISPLAY LIST OPERATOR CONTROL COMMANDS *
*                                                                     *
*INPUT-- REGISTER 1 - ADDRESS OF THE OPCAVT.                          *
*        OPCAVT                                                       *
*           OPCCOPCE - ADDRESS OF CURRENT ELEMENT (OPCE)       @Y17XARX
*           OPCWORK - ADDRESS OF RESIDENT WORK AREA            @Y17XARX
*        OPCE                                                         *
*          OCELEM - ADDRESS OF THE ASSOCIATED DCB              @Y17XARX
*           OCSWITCH -X'40' HARDWARE LINE ADDRESS              @Y17XARX
*                     X'00' GROUPNAME,RLN                             *
*           OCRLN - RELATIVE LINE NUMBER                       @Y17XARX
*           OCBACKUP- GROUPNAME OR LINE ADDRESS                @Y17XARX
*                                                                     *
*OUTPUT-- REGISTER 0 - MESSAGE NUMBER                          @Y17XARX
*         REGISTER 1 - ADDRESS OF OPCAVT                       @Y17XARX
*        OPCAVT                                                       *
*           OPCWORK - ADDRESS OF RESIDENT WORK AREA            @Y17XARX
*        RESIDENT WORK AREA CONTAINS MESSAGE TO BE WRITTEN            *
*           OPPRM,OCWTG - CLEARED - ZERO                       @Y17XARX
*           OPCLDNME - IGC0710D MSG WRITER    ERROR MESSAGE    @Y17XARX
*                                                                     *
*EXITS,NORMAL-- A RESPONSE MESSAGE CONTAINING THE REQUESTED DATA IS   *
*   IN THE RESIDENT WORK AREA                                         *
*                                                                     *
*   NORMAL EXIT MESSAGES ARE-                                         *
*   IED059I 'GRPNAME,RLN/ADDRESS' LIST STATUS=AUTOPL/NO BITS ON       *
*                                                                     *
*EXITS,ERROR--                                                 @Y17XARX
*   A RESPONSE MESSAGE IS IN THE RESIDENT WORKAREA             @Y17XARX
*                                                                     *
*ERROR MESSAGE--                                               @Y17XARX
*   IEDE01I D TP,LIST INVALID FOR 3705 OR 3705 RESOURCE        @Y17XARX
*EXTERNAL ROUTINES -- N/A.                                            *
*                                                                     *
*TABLES/WORK AREAS--RESIDENT WORK AREA,OPCAVT,TCAM AVT,LCB,DCB,@Z30X8RR
*   OPCE                                                       @Z30X8RR
*                                                                     *
*        THIS IS A RE-WRITE OF IEDQC3 AS OF OS/TCAM LEVEL II          *
***********************************************************************
*
         EJECT
*        REGISTER EQUATES
RZERO    EQU   0                        WORK                     S21903
RPARM    EQU   1                        PARAMETER                S21903
R1       EQU   1                        PARAMETER
ROPCAVT  EQU   2                        OPERATOR CONTROL AVT     S21903
ROPCE    EQU   3                        OPERATOR CONTROL ELEMENT S21903
RAVT     EQU   4                        AVT BASE
RWKAREA  EQU   5                        WORK AREA                S21903
RRLN     EQU   6                        RLN SAVE
RDCB     EQU   7                        DCB BASE
RLNGTH   EQU   8                        LENGTH REGISTER        @Z30X8RR
RLIST    EQU   9                        INVITATION LIST BASE
RLCB     EQU   10                       LCB BASE
RWORK    EQU   10                       WORK
RWORK2   EQU   11                       WORK
RBASE    EQU   12                       ROUTINE BASE
R13      EQU   13                       SAVE AREA
RRETURN  EQU   14                       RETURN REG               S21903
R15      EQU   15                       ENTRY POINT
         USING IEDQAVTD,RAVT            ADDRESSABILITY FOR AVT
         USING IHADCB,RDCB              ADDRESSABILITY FOR DCB
         USING IEDQLCB,RLCB             ADDRESSABILITY FOR LCB
         USING IEDQOPCE,ROPCE
         USING IEDQOPCD,ROPCAVT     ADDRESSABILITY FOR OPC AVT
         BALR  RBASE,ZERO               ESTABLISH ADDRESSABILITY
         USING *,RBASE
IGCD710D IEDHJN SKIPID
         LR    ROPCAVT,R1               GET OP CONTROL AVT ADDR. X03039
         L     ROPCE,OPCCOPCE      GET ADDRESS OF OPC ELEMENT
         L     RDCB,OCELEM         GET ADDRESS OF DCB
         L     RAVT,OPCAVTPT       GET ADDR OF AVT
         L     RWKAREA,OPCWORK     GET ADDR OF OPC WORKAREA
         LA    R1,LMSG             GET ADDR OF MSG
         SR    RLNGTH,RLNGTH            CLEAR REGISTER         @Z30X8RR
         IC    RLNGTH,LENOFF(R1)        GET LENGTH OF MESSAGE  @Z30X8RR
         STH   RLNGTH,LENOFF(RWKAREA)   STORE LEN IN WORKAREA  @Z30X8RR
         BCTR  RLNGTH,ZERO              DECREMENT FOR EXECUTE  @Z30X8RR
         EX    RLNGTH,MOVEMSG           MOVE MSG TO WORKAREA   @Z30X8RR
         TM    DCBDSRG1,AVTE80          IS DCB REALLY AN LGB     X03039
         BO    ERR3705                  CMD INVALID FOR 3705   @Y17XARX
         TM    DCBDSRG2,DCBDSGTR        IS INPUT A 370X DCB    @Y17XARX
         BO    ERR3705                  CMD INVALID FOR 3705   @Y17XARX
         TM    DCBDSRG2,DCBDSGTX        IS IT A 270X DCB         X03039
         BZ    NODATA                   BRANCH IF NO             X03039
         SR    RRLN,RRLN                CLEAR FOR INDEXING
         IC    RRLN,OCRLN          GET RLN FORM ELEMENT
         SLL   RRLN,OPCTWO
         LA    RLIST,DCBINVLI-FOUR(RRLN) GET LIST ADDR
         L     RLIST,OPCZERO(,RLIST)    GET LIST
         SR    RWORK,RWORK              CLEAR FOR IC            SA63010
         LR    RWORK2,RWORK             CLEAR FOR IC            SA63010
         IC    RWORK,OCRLN              GET RELATIVE LINE NO.   SA63010
         IC    RWORK2,DCBEIOBX          GET LCB SIZE            SA63010
         MR    RWORK,RWORK              GET OFFSET TO LCB       SA63010
         AL    RWORK2,DCBIOBAD          GET IOB ADDRESS         SA63010
         LA    RLCB,AVTEZERO(RWORK2)    IOB ADDRESS TO BASE REG SA63010
         LA    RWORK2,LCBFLAG1-IEDQLCB  OFFSET TO IOB IN LCB    SA63010
         SR    RLCB,RWORK2              BACK UP TO LCB          SA63010
         CLI   LCBRSKEY,OCX1A           BUFFERED TERMINAL       SA63010
         BNE   CKAUTO                   NO, CHECK FOR AUTOPOLL  SA63010
         TM    OPCFIVE(RLIST),AUTO      BUFFERED AUTOPOLL       SA63010
         BO    AUTOPOLL                 YES, BRANCH             SA63010
CKAUTO   EQU   *                                                SA63010
         TM    OPCTHREE(RLIST),AUTO        AUTOPOLL BIT ON
         BNO   NODATA                      NO
AUTOPOLL EQU   *                                                SA63010
         MVC   MSGL37(OPCSIX,RWKAREA),AUTOW PUT AUTOPOLL IN MSG
*
         MVC   MSGL(OPCEIGHT,RWKAREA),OCBACKUP PUT NAME IN MSG
         TM    OCSWITCH,NORLN      LINE ADDR ONLY
         BNO   EXIT                YES
         BAL   RRETURN,GETRLN      GET RLN AND INSERT IN MSG
         B     EXIT                PREPARE TO LEAVE MODULE
NODATA   EQU   *
         MVC   MSGL(OPCEIGHT,RWKAREA),OCBACKUP PUT NAME IN MSG
         TM    OCSWITCH,NORLN      LINE ADDR ONLY
         BNO   NODATA1             YES
         BAL   RRETURN,GETRLN      NO GRPNAME/RLN IN COMMAND
NODATA1  EQU   *
         MVC   MSGL37(OPCTEN,RWKAREA),NOBITSON  MOVE IDICATION IN MSG
         B     EXIT                PREPARE TO EXIT
ERR3705  EQU   *                        PREPARE FOR ERROR EXIT @ZM30396
         LA    RZERO,MSG201             MSG NUMBER FOR IGC0310D@ZM30396
         MVC   OPCEIGHT(OPCEIGHT,RWKAREA),INSRT201  PUT MSG201 @ZM30396
*                                       INSERT IN WORK AREA    @ZM30396
         MVC   OPCLDNME+OPCTHREE(OPCTWO),ERRWRIT  ID OF ERROR  @ZM30396
*                                       WRITER                 @ZM30396
         B     ERREXIT                  BRANCH TO EXIT TO 03   @ZM30396
GETRLN   EQU   *
         SR    RWORK,RWORK         CLEAR REGISTER
         IC    RWORK,OCRLN         GET RLN FROM ELEMENT
         CVD   RWORK,OPCDOUBL      CONVERT TO DEC TO PRINT
         UNPK  OPCDOUBL(OPCTHREE),OPCDOUBL+SIX(OPCTWO)
         OI    OPCDOUBL+OPCTWO,FOXZERO INVERT FOR PRINTING
         MVC   LMSGRLN(OPCTHREE,RWKAREA),OPCDOUBL
         BR    RRETURN                  RETURN TO CALLER
EXIT     EQU   *
         MVC   OPCLDNME+OPCTHREE(OPCTWO),MSGWRIT  INSERT ID OF IGC07
ERREXIT  EQU   *                        EXIT TO ERROR WRITER   @ZM30396
         XC    OCPARM(FOUR),OCPARM
         LR    RPARM,ROPCAVT
         XCTL  SF=(E,OPCXCTL)
         EJECT
OPCZERO  EQU   0                   ZERO
OPCONE   EQU   1                   ONE
OPCTWO   EQU   2                   TWO
OPCTHREE EQU   3                   THREE
OPCFOUR  EQU   4                        FOUR                     S21903
OPCFIVE  EQU   5                   FIVE
OPCSIX   EQU   6                   SIX
OPCEIGHT EQU   8                   EIGHT
OPCTEN   EQU   10                  TEN
ERRWRIT  DC    C'03'                    ERROR WRITER ID        @ZM30396
MSGWRIT  DC    C'07'               ID OF IGC0710D
FOXZERO  EQU   X'F0'               VALUE TO USE TO INVERT NUM
MSGL37   EQU   37                  OFFSET FOR INSERT
MSGL     EQU   12                  OFFSET FOR INSERT
NORLN    EQU   X'40'                    RLN PRESENT FLAG
AUTO     EQU   X'01'               AUTOPOLL FLAG
MSG201   EQU   201                      INVALID FOR VTAM MSG   @ZM30396
LMSGRLN  EQU   21                  OFFSET FOR INSERT
LENOFF   EQU   0                   OFFSET TO MSG LENGTH
ZERO     EQU   0                        OFFSET
FOUR     EQU   4                        OFFSET
SIX      EQU   6                        OFFSET
         SPACE 1                                                 S21903
MOVEMSG  MVC   OPCFOUR(OPCZERO,RWKAREA),OPCONE(R1)  MOVE MSG
INSRT201 DC    CL8'TP,LIST,'            FIRST 8 CHARACTERS OF  @ZM30396
*                                       INVLD DISPLAY LIST CMD @ZM30396
NOBITSON DC    C'NO BITS ON'       TEXT
AUTOW    DC    C'AUTOPL'           INSERT TEXT
LMSG     DS    0H
         DC    AL1(ENDLMSG-*-1)    LENGTH
         DC    CL21'IED059I',CL25'LIST STATUS='   MESSAGE TEXT
ENDLMSG  EQU   *
         DC    18X'FF'                  PATCH AREA             @Y17XARX
         EJECT                                                   S21903
         TOPCED
         EJECT
         TOPCAVTD
         EJECT
         TLCBD
         EJECT
         TAVTD
         EJECT
         DCBD  DSORG=TX
         END
