         TITLE 'IGCMB10D - 3705 GOTRACE/NOTRACE SUPPORT MODULE'
IGCMB10D CSECT
***********************************************************************
*                                                                     *
*  MODULE NAME = IGCMB10D(TCAM, OP CONTROL)                    @Y17XARW
*                                                                     *
*  DESCRIPTIVE NAME = 3705 GOTRACE/NOTRACE SUPPORT MODULE             *
*                                                                     *
*  COPYRIGHT = NONE                                                   *
*                                                                     *
*  STATUS = VERSION 10.0                                       @Y17XARW
*                                                                     *
*  FUNCTION = SUPPORT OF THE OPERATOR CONTROL FUNCTIONS GOTRACE AND   *
*     NOTRACE FOR LINES CONNECTED TO A 3705.                          *
*     THE MODULE COMPLETES THE SCANNING OF THE OPERATOR CONTROL       *
*     REQUEST INPUT DATA AND VALIDITY-CHECKS THE INPUT.               *
*                                                                     *
*     FOR AN ACTIVATE-LINE-TRACE REQUEST THE ROUTINE THROWS AWAY      *
*     THE OLD INACTIVE TRACE TABLE FOR THIS 3705 IF ONE IS PRESENT.   *
*     IT THEN GETS CORE FOR A NEW TABLE AND INITIALIZES THE CONTROL   *
*     INFORMATION IN IT AND LOADS THE TRACE ROUTINE.           @Y17XARW
*                                                                     *
*     A PACKED REQUEST UNIT IS THEN BUILT AND SENT TO IGC0210D @Y17XARW
*     WHO SENDS A REQUEST TO THE 3705.  WHEN THE RESPONSE UNIT @Y17XARW
*     IS RETURNED, THE MODULE REGAINS CONTROL AND ISSUES THE   @Y17XARW
*     THE PROPER COMPLETION MESSAGE.                           @Y17XARW
*                                                                     *
*     FOR A DEACTIVATE LINE TRACE REQUEST, A PACKED RU IS BUILT@Y17XARW
*     AND SENT TO IGC0210D WHO SENDS A REQUEST TO THE 3705.    @Y17XARW
*     WHEN A RESPONSE UNIT IS RETURNED THE MODULE REGAINS      @Y17XARW
*     CONTROL AND ISSUES THE PROPER COMPLETION MESSAGE.        @Y17XARW
*                                                                     *
*  NOTES = SEE BELOW                                                  *
*                                                                     *
*    DEPENDENCIES = CHARACTER-CODE DEPENDENCY                         *
*     THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL           *
*     REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS           *
*     EQUIVALENT TO THAT USED AT ASSEMBLY TIME.  THE CODING HAS BEEN  *
*     ARRANGED SO THAT REDEFINITION OF CHARACTER CONSTANTS BY         *
*     REASSEMBLY WILL RESULT IN A CORRECT MODULE FOR THE NEW          *
*     DEFINITION.                                                     *
*                                                                     *
*     RESTRICTIONS = NONE                                             *
*                                                                     *
*     REGISTER CONVENTIONS:                                           *
*        SEE PAGE FOLLOWING THESE SPECIFICATIONS                      *
*                                                                     *
*     PATCH LABEL = ZAPAREA                                    @Y17XARW
*                                                                     *
*  MODULE TYPE = PROCEDURE                                            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY              @Y17XARW
*                                                                     *
*     ATTRIBUTES:                                                     *
*        REFRESHABLE, DISABLED, REUSEABLE, TRANSIENT(TYPE 4),         *
*       SUPERVISOR STATE,REENTRANT                                    *
*                                                                     *
*  ENTRY POINT = IGCMB10D                                             *
*                                                                     *
*     PURPOSE = ENTERED FROM IGCM610D WHEN A GOTRACE OR NOTRACE       *
*        OPERATOR CONTROL REQUEST IS ENTERED FOR A GROUPNAME/RLN ON A *
*        3705.                                                        *
*        ENTERED FROM IGC0210D WHEN A RESPONSE UNIT IS         @Y17XARW
*        RETURNED FROM TCAM FOR A GOTRACE/NOTRACE REQUEST      @Y17XARW
*                                                                     *
*     LINKAGE = ALL MODULES XCTL TO IGCMB10D                   @Y17XARW
*               IGCM610D XCTL'S TO MB ON A FIRST REQUEST FOR   @Y17XARW
*                 3705 LINE TRACE                              @Y17XARW
*               IGC0010D XCTL'S TO MB AFTER CHECKING OCWTG     @Y17XARW
*               IGC0210D XCTL'S TO MB AFTER CHECKING OCWTG     @Y17XARW
*                                                                     *
*                                                                     *
*  INPUT =                                                            *
*        COMMON - R1 = OP CONTROL AVT ADDRESS                  @Y17XARW
*        ENTRY FROM M6 - OCMODNME= BYTES LEFT TO SCAN          @Y17XARW
*                        OCMODNME+4= NEXT BYTE SCANNED         @Y17XARW
*                        OCLINTTE = LINE TTE ADDRESS           @Y17XARW
*                        OCBACKUP = GRPNME                     @Y17XARW
*        ENTRY FROM 00 - OCLINTTE = LINE TTE ADDRESS           @Y17XARW
*                        OCBACKUP = GRPNME                     @Y17XARW
*        ENTRY FROM 02 - OCMODNME = NCP ADDRESS                @Y17XARW
*                        OCUNIT = BUFFER ADDRESS               @Y17XARW
*                        OCBACKUP = GRPNME                     @Y17XARW
*                                                                     *
*                                                                     *
*     OUTPUT =                                                        *
*        COMMON     - R1 = OP CONTROL AVT ADDRESS              @Y17XARW
*                          OPCCOPCE = ADDRESS OF CURRENT ELEM  @Y17XARW
*        EXIT TO 00 - OCWTG = ID OF NEXT MODULE TO GAIN CONTROL@Y17XARW
*        EXIT TO 02 - OCWTG = ID OF NEXT MODULE TO GAIN CONTROL@Y17XARW
*                     OCUNIT= ADDRESS OF PACKED RU             @Y17XARW
*        EXIT TO M1 - R0 = OUTPUT MESSAGE NUMBER               @Y17XARW
*                  - OCBACKUP = GRPNAME INSERT FOR MSG         @Y17XART
*                                                                     *
*  EXITS, NORMAL:                                                     *
*     TO IGC0010D TO PROCESS THE NEXT OPCE AFTER ENQUEUEING AN OPCE   *
*     ON THE OPCFMDFC CHAIN BEHIND AN IN-PROCESS OPCE FOR THE  @Y17XARW
*     SAME 3705.                                               @Y17XARW
*     TO IGC0210D TO SEND AN RU TO THE 3705 IF REQUEST HAS     @Y17XARW
*     BEEN ANALYZED BUT NOT YET CARRIED OUT.                   @Y17XARW
*     TO IGCM110D TO WRITE OUT THE PROPER MESSAGE IF REQUEST IS ALL   *
*     PROCESSED.                                                      *
*                                                                     *
*  EXIT, ERROR:                                                       *
*     TO IGC0310D TO SEND AN ERROR MESSAGE TO THE ORIGINATOR OF THE   *
*     REQUEST.                                                        *
*                                                                     *
*  EXTERNAL REFERENCES:                                               *
*     ROUTINES:                                                       *
*        XCTL                                                         *
*        OPCDCBLK - DCB LOOKUP ROUTINE IN IEDQCA                      *
*        OPCGETBF - GET-BUFFER ROUTINE IN IEDQCA                      *
*        OPCFREBF - FREE-BUFFER ROUTINE IN IEDQCA                     *
*        GETMAIN                                                      *
*        FREEMAIN                                                     *
*        COHORTLK                                              @Y17XARW
*        LOAD                                                  @Y17XARW
*        IEDQTL-DEVICE DEPENDENTS FIELD LOOKUP ROUTINE         @Y17XARW
*                                                                     *
*     DATA AREAS:                                                     *
*        OPERATOR CONTROL AVT                                         *
*                                                                     *
*     CONTROL BLOCKS:                                                 *
*        AVT                                                   @Y17XARW
*        NCP                                                   @Y17XARW
*        OP CONTROL AVT                                        @Y17XARW
*        OPCE (OPERATOR CONTROL ELEMENT)                              *
*        PRU                                                   @Y17XARW
*        TRM                                                   @Y17XARW
*                                                                     *
*  TABLES/WORK AREAS = LINE TRACE TABLE CONTROL AND PREFIX AREAS      *
*                                                                     *
*  MACROS:                                                            *
*     GETMAIN                                                         *
*     FREEMAIN                                                        *
*     LOAD                                                     @Y17XARW
*     XCTL                                                            *
*                                                                     *
*  CHANGE ACTIVITY = AS FOLLOWS:                                      *
************************MICROFICHE FLAGS*********************SUPT CODE*
*A000000-999999                                                @Y16X5R0
*A158500,181500,185100,257500,395200-402600,419200-419600,       Y06328
*A480200-480600,484200-484800                                    Y06328
*C199000-228000,416000,455000,477000                             Y06328
*D418000,483000                                                  Y06328
*     A319000,417000                                           SA67105*
*A523500                                                       @YA04878
*C291000,292000                                                @YA04878
*D418000                                                       @YA09099
*A419600,319000                                                @YA09099
*A052500,101000,106000,116000,142000,155000,158000,            @Y17XARW
*A172000,185600,299000,414000,471000,476000-484800,516000      @Y17XARW
*C005000,011000,023000-031000,047000,063000-064000,066000,     @Y17XARW
*C069000-071000,074000-077000,084000-085000,109000-110000,     @Y17XARW
*C119000-119001,145000-148000,191000-193000,199000-229000,     @Y17XATW
*C254000,257000-264000,272000,                                 @Y17XARW
*C276000-282000,291000-293000,297000-298000,                   @Y17XARW
*C306000,309000,311000-3121000,317000-320000,327000,329000,    @Y17XARW
*C330000,342000-343000,358000,380000,382000,415000-419800,     @Y17XARW
*C422000,426000-427000,430000-432000,435000,436000,            @Y17XARW
*C438000-439000,443000,450000,452000-453000,455000,459000,     @Y17XARW
*C463000,469000-471000                                         @Y17XARW
*D011500,518000                                                @Y17XARW
*A412000,579000,585000                                         @OZ30601
*                                                                     *
***********************************************************************
         EJECT
RZERO    EQU   0                        PARM REG 0
RPARM    EQU   1                        PARM REG 1
ROPCAVT  EQU   2                        OP CONTROL AVT ADDR
ROPCE    EQU   3                        OP CONTROL ELEMENT ADDR
RAVT     EQU   4                        TCAM AVT ADDR
RTERML   EQU   5                        ADDR OF LINE'S TERMINAL ENTRY
RTRCA    EQU   6                        ADDR OF TRACE TABLE AREA
RWORK6   EQU   6                        WORK REG
RDCB     EQU   7                        DCB
RWORK8   EQU   8                        WORK REG
RSAVT    EQU   8                        SAVT ADDRESS           @Y17XARW
RNCP     EQU   9                        NCP DEV DEP FIELDS ADDR@Y17XARW
RNCPTTE  EQU   9                        NCP TTE ADDRESS        @Y17XARW
RSIZE    EQU   10                       SIZE OR LENGTH REG
RWORK10  EQU   10                       WORK REG
RSCAN    EQU   11                       SCAN-POSITION REGISTER
RWORK11  EQU   11                       WORK REG
RBASE    EQU   12                       BASE REG
RSAVE    EQU   13                       SAVE AREA ADDR
RRET     EQU   14                       SUBROUTINE ENTRY ADDR
RBRNCH   EQU   15                       SUBROUTINE BRANCH ENTRY POINT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESS       @Y17XARW
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT
         USING IEDQOPCN,ROPCE           OPCE NEGATIVE PREFIX   @Y17XARW
         USING IEDNCP,RNCP              NCP DEVICE DEP FIELDS  @Y17XARW
         USING IEDNSVTD,RSAVT           SAVT                   @Y17XARW
         USING IEDNTRM,RTERML           TTE NEGATIVE PREFIX    @Y17XARW
         USING IEDPRUND,RPARM           PACKED RU NEG PREFIX   @Y17XARW
         USING IEDHED,RTRCA             TRACE AREA
         EJECT
ZERO     EQU   0                        VALUE 0
ONE      EQU   1                        VALUE 1
TWO      EQU   2                        VALUE 2
THREE    EQU   3                        VALUE 3
FOUR     EQU   4                        VALUE 4
FIVE     EQU   5                        VALUE 5                @Y17XARW
SIX      EQU   6                        VALUE 6
SEVEN    EQU   7                        VALUE 7
EIGHT    EQU   8                        VALUE 8
FMASK    EQU   B'1111'                  FULLWORD ICM MASK      @Y17XARW
SIXTEEN  EQU   16                       VALUE 16                 S05331
SEVNTEEN EQU   17                       DCB-NOT-OPEN ERROR MSG NO
EIGHTEEN EQU   18                       INVALID-REQUEST MESSAGE NO.
TWENTY3  EQU   23                       TRACE-STARTED MSG NO.
TWENTY4  EQU   24                       TRACE ALREADY STARTED  @YM07023
TWENTY8  EQU   28                       LENGTH OF CTL DATA / HALF TBL
TWENTY9  EQU   29                       TRACE-STOPPED MSG NO.
THIRTY   EQU   30                       TRACE-ALREADY-OFF MSG NO.
FORTY8   EQU   48                       LENGTH OF TABLE CONTROL INFO
SEVENTY6 EQU   76                       TBL CTL INFO + HALF-TBL INFO
HUNDRED  EQU   100                      TABLE-SIZE DEFAULT
LTRCOSZ  EQU   104                      LINE TRACE OVERHEAD AREA SIZE
NOCORE   EQU   423                      CORE-UNAVAILABLE MSG NUMBER
TWOFIFTY EQU   250                      SUBPOOL NO FOR GETMAIN/FREEMAIN
TWO55    EQU   255                      INTERVAL MAX (AND DEFAULT)
COMWTBSY EQU   376                      COMWRITE-USING-TABLE MSG NO
MSG400   EQU   400                      3705 NOT ACTIVE        @Y17XARW
FOUR03   EQU   403                      TRACE-DEACTIVATED ERROR MSG NO
MSG404   EQU   404                      ALREADY IN PROGRESS MESSAGE
X01      EQU   X'01'                    TRACE-INACTIVE SWITCH
TRACEOFF EQU   X'19'                    CODE FOR DEACTIVATE REQUEST
XF0      EQU   X'F0'                    HEX F0
XFA      EQU   X'FA'                    HEX FA
XFF      EQU   X'FF'                    HEX FF
COMMA    EQU   C','                     SCAN DELIMITER
CHAR0    EQU   C'0'                     CHARACTER 0
CHAR9    EQU   C'9'                     CHARACTER 9
RHFLAG0  EQU   X'0B'                    RH FLAG 0              @Y17XARW
RHFLAG1  EQU   X'80'                    RH FLAG 1              @Y17XARW
THFLAGS  EQU   X'1C'                    TH FLAGS               @Y17XARW
         EJECT
         BALR  RBASE,0                  SET BASE REG
         USING *,RBASE                  MODULE ADDRESSABILITY
IGCMB10D IEDHJN SKIPID                  MODULE IDENTIFIER
         LR    ROPCAVT,RPARM            OP CONTROL AVT BASE
         L     RAVT,OPCAVTPT            OP CONTROL AVT         @Y17XARW
         L     ROPCE,OPCCOPCE           OP CONTROL ELEMENT ADDRESS
         LA    RWORK8,OCPREFSZ          SETUP NEG PREF SIZE    @Y17XARW
         SR    ROPCE,RWORK8             BACKUP TO NEGATIVE PREF@Y17XARW
         TM    OCFLAG,OCRESP            RU RESPONSE?           @Y17XARW
         BO    PROCRU                   YES, GO PROCESS IT     @Y17XARW
         SPACE 2
***********************************************************************
*              THE OPCE IS A NEW REQUEST FOR GOTRACE OR NOTRACE.      *
***********************************************************************
         SPACE
         TM    OCSWITCH,OCSCANON        SCAN ALREADY DONE      @Y17XARW
         BO    ENDSCAN                  YES, BYPASS SCANNING
         SPACE 2
***********************************************************************
*              SCAN THE REQUEST MESSAGE STARTING AFTER THE 'ON' OR    *
*              'OFF'.                                                 *
***********************************************************************
         SPACE
         LM    RSIZE,RSCAN,OCMODNME     PICK UP SAVED SCAN REGS
         BAL   RRET,SCANOP              SCAN FOR INTERVAL OPERAND
         CH    RZERO,CON255             INTERVAL > 255
         BH    INVALID                  YES, INVALID REQUEST
         LTR   RZERO,RZERO              INTERVAL = 0
         BP    INTVALOK                 NO, DATA IS OK
         LA    RZERO,TWO55              YES, USE DEFAULT OF 255
         SPACE
INTVALOK STC   RZERO,OCMODNME+SIX       SAVE INTERVAL IN OPCE
         BAL   RRET,SCANOP              SCAN FOR TABLE-SIZE OPRND
         LTR   RSIZE,RSIZE              ANY REMAINING OPERANDS ?
         BNZ   INVALID                  YES, INVALID REQUEST
         BCTR  RSCAN,ZERO               POINT BACK TO LAST CHAR
         CLI   ZERO(RSCAN),COMMA        WAS LAST CHAR A COMMA ?
         BE    INVALID                  YES, INVALID REQUEST
         CH    RZERO,CONMAXH            SIZE > LIMIT
         BH    INVALID                  YES, INVALID REQUEST
         LTR   RZERO,RZERO              SIZE = 0
         BP    SIZEOK                   NO, DATA IS OK
         LA    RZERO,HUNDRED            YES, USE DEFAULT OF 100
         SPACE
SIZEOK   STH   RZERO,OCMODNME+FOUR      SAVE TABLE SIZE IN OPCE
         SPACE
***********************************************************************
*                                                                     *
*   THE COHORTLK RTN WILL BE USED TO FIND THE NCP TTE WHICH    @Y17XARW
*   WILL BE SAVED BY IGCMB10D IN THE OPCE.                     @Y17XARW
*                                                                     *
***********************************************************************
         SPACE
         L     RPARM,OCLINTTE           RESTORE LINE TTE ADDR  @Y17XARW
*                                        CALCULATED IN M6      @Y17XARW
         LA    RSAVE,OPCSAVE            SETUP SAVEAREA ADDR    @Y17XARW
         L     RBRNCH,OPCHORT           SETUP LOOKUP RTN ADDR  @Y17XARW
         BALR  RRET,RBRNCH              CALL COHORTLK TO FIND  @Y17XARW
*                                        NCP TTE ADDRESS       @Y17XARW
         LTR   RZERO,RZERO              FOUND NCP TTE          @Y17XARW
         BNZ   GOODRTN                  YES, GOOD RETURN       @Y17XARW
         LA    RZERO,MSG400             3705 NOT ACTIVE MSG    @YM07393
         B     XCTLERR                  ERROR EXIT             @Y17XARW
GOODRTN  EQU   *                                               @Y17XARW
         STH   RZERO,OCTRMTBL           SAVE NCP TNT           @Y17XARW
         LA    RPARM,ZERO(RPARM)        CLEAR HI-ORDER BYTE    @Y17XARW
         ST    RPARM,OCMODNME           SAVE NCP TTE           @Y17XARW
         SPACE
ENDSCAN  EQU   *
         NI    OCSWITCH,XFF-OCSCANON    TURN OFF SCAN-COMPLETE @Y17XARW
         L     RNCPTTE,OCMODNME         RESTORE NCP TTE ADDR   @Y17XARW
         SPACE 2
***********************************************************************
*              SEARCH THE OPCFMDFC CHAIN TO SEE IF  THERE IS A @Y17XARW
*              PREVIOUS REQUEST FOR THIS NCP.                  @Y17XARW
***********************************************************************
         SPACE
         LA    RWORK6,OPCFMDFC          INITIALIZE FOR SEARCH  @Y17XARW
         SPACE
NLOOPA   L     RWORK6,ZERO(RWORK6)      NEXT OPCE ON WAIT CHAIN
         LA    RWORK6,ZERO(RWORK6)      CLEAR HIGH-ORDER BYTE
         LTR   RWORK6,RWORK6            END OF CHAIN
         BZ    NLOOPANO                 BRANCH IF YES
         CLC   OCWTG-IEDQOPCE(TWO,RWORK6),THISMOD  OPCE FROM MB
         BNE   NLOOPA                   BRANCH IF NO
         C     RNCPTTE,OCMODNME-IEDQOPCE(FOUR,RWORK6)          @Y17XARW
*                                       OPCE FOR SAME 3705?    @Y17XARW
         BNE   NLOOPA                   NO, KEEP SEARCHING
         SPACE 2
***********************************************************************
*        AN OPCE HAS BEEN FOUND ON THE OPCFMDFC CHAIN WHICH    @Y17XARW
*        CARRIES A REQUEST FOR THE SAME 3705 AS THE NEW OPCE   @Y17XARW
*        WE ARE PROCESSING.  ENQUEUE THE NEW OPCE OFF THE ONE  @Y17XARW
*        CURRENTLY IN THE CHAIN.                               @Y17XARW
***********************************************************************
         SPACE
         OI    OCSWITCH,OCSCANON        FLAG OPCE SCAN COMPLETE@Y17XARW
         SPACE
NLOOPAQ  TM    OCFLAG-IEDQOPCE(RWORK6),OCATTACH  SIDE-CHAIN END
         BZ    ADDA                     BRANCH IF YES
         L     RWORK6,OCELEM-IEDQOPCE(RWORK6)  GET NEXT ONE
         B     NLOOPAQ                  KEEP LOOKING
         SPACE
ADDA     OI    OCFLAG-IEDQOPCE(RWORK6),OCATTACH  SIDE-CHAIN END
         MVC   OCWTG(TWO),THISMOD       MARK FOR RETURN TO MB  @Y17XARW
         LA    ROPCE,OCPREFSZ(ROPCE)    ADJUST FOR OPCE        @Y17XARW
         ST    ROPCE,OCELEM-IEDQOPCE(RWORK6) ATTACH NEW OPCE   @Y17XARW
         MVC   OPCMODID(TWO),FIRSTLD        SET FOR XCTL TO    @Y17XARW
         B     EXIT                     FIRST LOAD OF OPEN
         SPACE 2
***********************************************************************
*        NO PREVIOUS OPCE FOR THIS SAME 3705 WAS FOUND. SETUP  @Y17XARW
*        ADDRESSABILITY TO THE NCP DEVICE DEPENDENTS FIELDS.   @Y17XARW
*        THEN CHECK REQUEST TYPE.                              @Y17XARW
***********************************************************************
         SPACE
NLOOPANO EQU   *                                               @Y17XARW
         BAL   RWORK8,NCPDEV            DEVICE DEP FIELDS ADDR @Y17XARW
         CLI   OCSWITCH,TRACEOFF        DEACTIVATE REQUEST
         BNE   ACTIVATE                 BRANCH IF NO
         SPACE 2
***********************************************************************
*              DEACTIVATE REQUEST - CHECK THE REQUEST & BUILD & SEND  *
*              THE PROPER REQUEST UNIT.                        @Y17XARW
***********************************************************************
         SPACE
         OC    NCPLTRAC,NCPLTRAC        TRACE ADDR IN NCP      @Y17XARW
         BZ    ALOF                     NO, ALREADY DEACTIVATED
         TM    NCPFLAG1,NCPTRACE        IS IT ACTIVE           @Y17XARW
         BZ    ALOF                     BRANCH IF NO           @Y17XARW
         ICM   RTRCA,FMASK,NCPLTRAC     TRACE TABLE ADDRESS    @Y17XARW
         CLC   OCRLN,HEDRLN             SAME RELATIVE LINE NUMBER?
         BNE   ALOF                     BRANCH IF NO
         ICM   RWORK8,7,HEDFSTHF        ADDR OF 1ST HALF OF TABLE
         CLC   OCBACKUP,TABLINUM-TABHALF(RWORK8)  SAME GROUP NAME?
         BE    DEACTVAT                 BRANCH IF YES
         SPACE
ALOF     LA    RZERO,THIRTY             ALREADY-OFF ERROR MSG
         B     XCTLERR                  ERROR EXIT
         SPACE
DEACTVAT BAL   RWORK8,BUILDRU           BUILD A REQUEST UNIT   @Y17XARW
         MVC   PRUDATA(THREE),DEACT     SETUP COMMAND          @Y17XARW
         MVI   PRUDATA+SIX,ZERO         RESERVED BYTE          @Y17XARW
         L     RTERML,OCLINTTE          SETUP LINE TTE ADDRESS @Y17XARW
         LA    RWORK8,TRMPRFSZ          LEN OF NEG PREFIX      @Y17XARW
         SR    RTERML,RWORK8            POINT TO NEG PREFIX    @Y17XARW
         NI    TRMLINK,XFF-TRMLINT      MARK LINE TRACE INACT  @Y17XARW
         B     SETSND                   EXIT TO SEND THE RU    @Y17XARW
         SPACE 2
***********************************************************************
*              ACTIVATE REQUEST - CHECK THE REQUEST VS PRESENT STATE  *
*              OF THE SYSTEM.                                         *
***********************************************************************
         SPACE
ACTIVATE OC    NCPLTRAC,NCPLTRAC        TRACE ADDR IN NCP      @Y17XARW
         BZ    ACT1                     NO, GO ACTIVATE TRACE
         TM    NCPFLAG1,NCPTRACE        IS IT ACTIVE           @Y17XARW
         BZ    NOTACTIV                 BRANCH IF NO           @Y17XARW
         LA    RZERO,MSG404             ALREADY IN PROGRESS ERROR MSG
         B     XCTLERR                  ERROR EXIT
         SPACE 2
***********************************************************************
*              THERE IS A TRACE TABLE AREA FOR THIS 3705 LEFT OVER    *
*              FROM A PREVIOUS GOTRACE COMMAND, NOW DEACTIVATED.      *
*              THROW AWAY THE OLD TRACE TABLE UNLESS IT IS STILL IN   *
*              USE BY COMWRITE.                                       *
***********************************************************************
         SPACE
NOTACTIV EQU   *
         ICM   RTRCA,FMASK,NCPLTRAC     GET TRACE AREA ADDR    @Y17XARW
         OI    NCPFLAG1,NCPTRACE        TURN ON ACTIVE FLAG    @Y17XARW
         TM    HEDFLAG,HEDCWOK          COMWRITE IN PROGRESS?
         BO    CLEARTRC                 NO, GO CLEAR OLD TABLE
         LA    RZERO,COMWTBSY           SET ERROR MSG NO
         B     XCTLERR                  ERROR EXIT
         DROP  RPARM
         USING LSTDSECT,RPARM           GETMAIN/FREEMAIN LIST
         SPACE
CLEARTRC L     RPARM,OPCOPTLK           ADDR OF FREEMAIN LIST
         MVC   LSTSIZE,HEDSIZE          SIZE OF AREA TO FREE
         LA    RWORK8,OPCDOUBL          ADDR OF PTR TO AREA
         ST    RWORK8,LSTADDR           ADDR OF PTR TO AREA
         ST    RTRCA,OPCDOUBL           POINTER TO AREA TO FREE
         MVI   LSTSP,TWOFIFTY           SPECIFY SUBPOOL 250
         FREEMAIN  ,MF=(E,(1))          FREE CORE FROM OLD TABLE
         XC    NCPLTRAC,NCPLTRAC        ZERO TABLE ADDRESS     @Y17XARW
         SPACE 2
***********************************************************************
*              GET THE CORE NEEDED FOR THE TRACE TABLE, INITIALIZE    *
*              THE CONTROL INFORMATION, AND LOAD THE TRACE ROUTINE.   *
***********************************************************************
         SPACE
ACT1     LH    RSIZE,OCMODNME+FOUR      NUMBER OF TABLE ENTRIES
         LA    RSIZE,ONE(RSIZE)         ROUND UP TO EVEN NUMBER ...
         SRL   RSIZE,ONE                ... (IF NECESSARY) AND ...
         SLL   RSIZE,THREE              ... MULTIPLY BY FOUR
         LA    RSIZE,LTRCOSZ(RSIZE)     ADD SIZE OF CONTROL INFO
         L     RPARM,OPCOPTLK           ADDR OF GETMAIN LIST
         ST    RSIZE,LSTSIZE            STORE SIZE NEEDED
         LA    RWORK8,OPCDOUBL          ADDR OF PTR TO AREA
         ST    RWORK8,LSTADDR           STORE ADDR IN LIST
         XC    OPCDOUBL(FOUR),OPCDOUBL  CLEAR FIRST WORD OF PARM LIST
         MVI   LSTSP,TWOFIFTY           SPECIFY SUBPOOL 250
         GETMAIN  ,MF=(E,(1))           GET CORE FOR TABLE
         LTR   RBRNCH,RBRNCH            SUCCESSFUL GETMAIN
         BZ    OKGETMN                  BRANCH IF YES
         LA    RZERO,NOCORE             NO-CORE-AVAILABLE MSG NO
         B     XCTLERR                  ERROR EXIT
         USING IEDPRUND,RPARM           RU BUFF ADDRESSABILITY @Y17XARW
         SPACE
OKGETMN  MVC   NCPLTRAC,OPCDOUBL        MOVE TRACE ADDR TO NCP @Y17XARW
         L     RTRCA,OPCDOUBL           TRACE ADDR BASE
         XC    IEDHED(SEVENTY6),IEDHED  ZERO OUT CONTROL INFO
*                                       & FIRST HALF-TBL HEADER
         MVI   HEDSWCHS,HEDTBL1         SET UP SWITCHES
         OI    HEDFLAG,HEDCWOK          SET COMWRITE COMPLETION FLAG
         MVC   HEDID,LINTID             INITIALIZE ID FOR COMWRITE
         ST    RSIZE,HEDSIZE            OVER-ALL TRACE AREA SIZE
         SH    RSIZE,CON48              SUBTRACT SIZE OF CTL AREA
         SRL   RSIZE,ONE                DIVIDE BY TWO TO GET
*                                       SIZE OF EACH HALF-TABLE
         STH   RSIZE,HEDHLFSZ           SET UP HALF-TABLE SIZE FIELD
         LA    RWORK11,FORTY8(RTRCA)    ADDR OF 1ST HALF-TABLE
         STCM  RWORK11,7,HEDFSTHF       STORE IN CTL INFO
         USING TABHALF,RWORK11
         MVC   TABLINUM,OCBACKUP        GROUP NAME
         MVI   TABCOMMA,C','            INITIALIZE COMMA CONSTANT
         SR    RZERO,RZERO              CLEAR REGISTER
         IC    RZERO,OCRLN              GET RELATIVE LINE NO (BINARY)
         STC   RZERO,HEDRLN             SAVE RLN IN CTL AREA
         CVD   RZERO,OPCDOUBL           CONVERT IT TO PACKED DECIMAL
         UNPK  OPCDOUBL(THREE),OPCDOUBL+SIX(TWO)  UNPACK THE RLN DATA
         OI    OPCDOUBL+TWO,XF0         MAKE SIGNED LAST DIGIT READABLE
         MVC   TABRLN,OPCDOUBL          MOVE RLN INTO TABLE AREA
         SPACE 1                                                 S05331
SECHALF  EQU   *                                                 S05331
         LA    RBRNCH,ZERO(RWORK11,RSIZE)  ADDR OF 2ND TBL-HALF
         MVC   ZERO(TWENTY8,RBRNCH),TABHALF  COPY 1ST HALF HEADER
*                                       INFO INTO 2ND HALF
         LA    RWORK11,TABDATA          ADDR OF 1ST TABLE ENTRY
         ST    RWORK11,HEDTBPOS         STORE 1ST TABLE ENTRY
         DROP  RWORK11
         STCM  RBRNCH,7,HEDSECHF        ADDR OF 2ND TABLE-HALF
         SH    RSIZE,CON28              DATA LENGTH/HALF-TABLE
         STH   RSIZE,HEDREMLN           INITIALIZE DATA-LENGTH
         STH   RSIZE,HEDDATLN           ... FIELDS IN CTL INFO
         EJECT                                                 @OZ30601
************************************************************** @OZ30601
* THE FOLLOWING SECTION OF CODE CHECKS THAT THE BLOCKSIZE    * @OZ30601
* FOR THE COMWRITE DATA SET(IF OPEN) IS LARGE ENOUGH TO      * @OZ30601
* CONTAIN 1/2 OF THE 3705 LINE TRACE TABLE DEFINED BY THE    * @OZ30601
* START LINE TRACE COMMAND.  IF THE BLOCKSIZE IS LARGE       * @OZ30601
* ENOUGH, ACTION PROCEEDS AS BEFORE.  IF THE BLOCKSIZE IS    * @OZ30601
* NOT LARGE ENOUGH, THEN SOME LINE TRACE DATA WILL BE LOST.  * @OZ30601
* THIS FIX WILL WRITE MESSAGE IED624I TO THE OPERATOR,       * @OZ30601
* INFORMING HIM THAT DATA WILL BE LOST.  THE MESSAGE WILL    * @OZ30601
* ALSO TELL HIM THE MAXIMUM NUMBER OF TRACE TABLE ENTRIES HE * @OZ30601
* CAN REQUEST WITHOUT LOSING TRACE DATA WITH HIS CURRENT     * @OZ30601
* COMWRITE DATA SET.  THE COMMAND WILL THEN CONTINUE         * @OZ30601
* EXECUTING AS NORMAL.  THE SHORT TERM SOLUTION TO THE       * @OZ30601
* PROBLEM IS FOR THE OPERATOR TO STOP LINE TRACE AND THEN    * @OZ30601
* START IT AGAIN SPECIFYING AN ACCEPTABLE NUMBER OF ENTRIES. * @OZ30601
* THE LONG TERM SOLUTION TO THE PROBLEM IS FOR THE OPERATOR  * @OZ30601
* TO SPECIFY A BLKSIZE ON HIS COMWRITE DD CARD WHICH WILL BE * @OZ30601
* LARGE ENOUGH TO CONTAIN 1/2 OF THE TRACE TABLE HE DESIRES. * @OZ30601
************************************************************** @OZ30601
         SPACE 1                                               @OZ30601
* REGISTER EQUATES FOR THIS PTM FIX ONLY                       @OZ30601
         SPACE 1                                               @OZ30601
RWORK    EQU   0                                               @OZ30601
ROFF     EQU   0                                               @OZ30601
RTCB     EQU   1                                               @OZ30601
RDEB     EQU   1                                               @OZ30601
RTIOT    EQU   8                                               @OZ30601
RLEN     EQU   11                                              @OZ30601
RDCB2    EQU   11                                              @OZ30601
         SPACE 1                                               @OZ30601
         USING TCB,RTCB                                        @OZ30601
         USING TIOT,RTIOT                                      @OZ30601
         USING IHADCB,RDCB2                                    @OZ30601
         EJECT                                                 @OZ30601
         TM    AVTCWFL2,AVTCWACT IS COMWRITE ACTIVE?           @OZ30601
         BZ    NOCOMWR       NO-NO NEED TO MAKE CHECK          @OZ30601
         L     RTCB,AVTCWTCB GET COMWRITE TCB ADDRESS          @OZ30601
         L     RTIOT,TCBTIO  GET TCAM TIOT ADDRESS             @OZ30601
         SLR   RLEN,RLEN     CLEAR LENGTH WORKING REGISTER     @OZ30601
DDLOOP   ICM   RLEN,1,TIOELNGH GET LENGTH OF NEXT DD           @OZ30601*
                             ENTRY IN TIOT                     @OZ30601
         BZ    NOCOMWR       COMWRITE DD ENTRY NOT FOUND       @OZ30601
         CLC   TIOEDDNM,COMWRITE IS THE CURRENT ENTRY          @OZ30601*
                             THE COMWRITE ENTRY?               @OZ30601
         BE    DDFOUND       BRANCH IF YES                     @OZ30601
         AR    RTIOT,RLEN    GET NEXT DD ENTRY IN TIOT         @OZ30601
         B     DDLOOP        CHECK NEXT DD ENTRY IN TIOT       @OZ30601
DDFOUND  SLR   RWORK,RWORK   CLEAR REGISTER                    @OZ30601
         ICM   RWORK,7,TCBTIO+1 GET TIOT START ADDR            @OZ30601
         LA    RTIOT,TIOELNGH GET TIOELNGH ADDRESS             @OZ30601
         SR    RTIOT,RWORK   CALCULATE TIOT OFFSET OF          @OZ30601*
                             COMWRITE DD ENTRY                 @OZ30601
         LA    RDEB,TCBDEB-4 SET UP DEB CHAIN SCAN             @OZ30601
         SLR   ROFF,ROFF     CLEAR WORK REGISTER FOR LOAD      @OZ30601
         USING DEBBASIC,RDEB                                   @OZ30601
NEXTDEB  ICM   RDEB,7,DEBDEBB GET NEXT DEB ADDRESS             @OZ30601
         BZ    NOCOMWR       NO DEB FOUND FOR COMWRITE DD      @OZ30601
         L     RDCB2,DEBDCBAD GET DCB ADDRESS FOR DEB          @OZ30601
         ICM   ROFF,3,DCBTIOT GET TIOT OFFSET OF DD            @OZ30601*
                             ENTRY FOR THIS DCB                @OZ30601
         CR    RTIOT,ROFF    IS DCB TIOT OFFSET SAME AS        @OZ30601*
                             COMWRITE DD OFFSET?               @OZ30601
         BNE   NEXTDEB       NO--GET NEXT DEB                  @OZ30601
         SLR   ROFF,ROFF     CLEAR REGISTER                    @OZ30601
         ICM   ROFF,3,DCBBLKSI GET COMWRITE BLOCKSIZE          @OZ30601
* WE NOW HAVE THE BLOCKSIZE OF THE COMWRITE DATASET            @OZ30601
         SH    ROFF,CON28    SUBTRACT 1/2 TABLE CONTROL WORD   @OZ30601*
                             LENGTH                            @OZ30601
         CR    ROFF,RSIZE    IS THE BLOCKSIZE LARGE ENOUGH?    @OZ30601
         BNL   NOCOMWR       YES                               @OZ30601
         SRL   ROFF,2        DIVIDE TRACE ENTRY SPACE BY 4     @OZ30601
         CVD   ROFF,OPCDOUBL CONVERT BINARY TO PACKED DECIMAL  @OZ30601
         UNPK  OPCDOUBL(5),OPCDOUBL+5(3) CONVERT 3 BYTES OF    @OZ30601*
                             PACKED DECIMAL TO 5 BYTES OF      @OZ30601*
                             ZONED DECIMAL                     @OZ30601
         OI    OPCDOUBL+4,XF0 CONVERT SIGNED BYTE TO EBCDIC    @OZ30601
         MVC   ENTRYNUM(5),OPCDOUBL MOVE MAXIMUM ENTRY NUMBER  @OZ30601*
                             INTO MESSAGE                      @OZ30601
         WTO   MF=(E,MSG624) WRITE MESSAGE TO OPERATOR         @OZ30601
         B     NOCOMWR       SKIP DATA                         @OZ30601
         EJECT                                                 @OZ30601
MSG624   WTO   'IED624I TRACE DATA WILL BE LOST-SPECIFY NO MORE THAN YY*
               YYY ENTRIES',MF=L,ROUTCDE=(8),DESC=(5)          @OZ30601
         SPACE 1                                               @OZ30601
ENTRYNUM EQU   MSG624+57                                       @OZ30601
         SPACE 1                                               @OZ30601
COMWRITE DC    CL8'COMWRITE'                                   @OZ30601
         SPACE 1                                               @OZ30601
         DROP  RDCB2                                           @OZ30601
         USING IEDPRUND,RPARM                                  @OZ30601
         USING IEDNSVTD,RSAVT                                  @OZ30601
         EJECT                                                 @OZ30601
NOCOMWR  EQU   *     END OF COMWRITE BLOCKSIZE CHECK           @OZ30601
         LOAD  EP=IEDNLT                LOAD TRACE ROUTINE
         ST    RZERO,HEDTRCAD           STORE ROUTINE ADDRESS
         L     RSAVT,AVTSAVTP           POINT TO SAVT          @Y17XARW
         ST    RZERO,SAVTRNTA           STORE RTN ADDR IN SAVT @Y17XARW
         BAL   RWORK8,BUILDRU           BUILD A REQUEST UNIT   @Y17XARW
         MVC   PRUDATA(THREE),ACT       SETUP COMMAND TYPE     @Y17XARW
         MVC   PRUDATA+SIX(ONE),OCMODNME+SIX  SETUP INTERVAL   @Y17XARW
         L     RTERML,OCLINTTE          SETUP LINE TTE         @Y17XARW
         LA    RWORK8,TRMPRFSZ          LEN OF NEG PREFIX      @Y17XARW
         SR    RTERML,RWORK8            POINT TO NEG PREFIX    @Y17XARW
         OI    TRMLINK,TRMLINT          MARK LINE TRACE ACTIVE @Y17XARW
         SPACE 2
***********************************************************************
*              SET UP TO SEND THE PACKED RU TO THE 3705.       @Y17XARW
***********************************************************************
         SPACE
SETSND   MVC   OCWTG(TWO),THISMOD       SET RETURN TO THIS MOD
         MVC   OPCMODID(TWO),RUDISP     SET DISP MOD NAME      @Y17XARW
         B     EXIT                     GO XCTL TO RU DISP     @Y17XARW
         EJECT
***********************************************************************
*              THE OPCE REPRESENTS A RESPONSE UNIT RETURNED    @Y17XARW
*              FROM THE 3705.  ANALYZE THE RESPONSE AND SETUP  @Y17XARW
*              THE PROPER MESSAGE NUMBER AND EXIT.             @Y17XARW
***********************************************************************
         SPACE
PROCRU   EQU   *                                               @Y17XARW
         BAL   RWORK8,NCPDEV            GET NCP DEVICE DEPEN-  @Y17XARW
*                                       DENT FIELDS ADDRESS    @Y17XARW
         L     RPARM,OCUNIT             BUFFER ADDRESS
         LA    RWORK8,PRUNLEN           SETUP NEG PREF LEN     @Y17XARW
         SR    RPARM,RWORK8             POINT TO NEG PREFIX    @Y17XARW
         MVC   OPCDOUBL(EIGHT),OCBACKUP SET GRPNAME PARAMETER
         CLI   OCSWITCH,TRACEOFF        DEACTIVATE REQUEST
         BNE   ENDACT                   NO, ITS AN ACTIVATE REQUEST
         NI    NCPFLAG1,XFF-NCPTRACE    MARK TRACE INACTIVE    @Y17XARW
         LA    RZERO,TWENTY9            TRACE-STOPPED MSG NO
         B     XCTLMOD                  EXIT TO CHECKPOINT AND
*                                       MESSAGE-WRITER MODULES
         SPACE
ENDACT   LA    RZERO,TWENTY3            TRACE-STARTED MSG NO
         OI    NCPFLAG1,NCPTRACE        TRACE ACTIVE           @YM07023
         TM    PRURHFG0,PRURHSDI        ERROR ON RESPONSE      @Y17XARW
         BZ    XCTLMOD                  NO, EXIT TO CHKPT AND MSG
         CLC   PRUDATA(TWO),ACTIVE      ALREADY ACTIVE         @YM07023
         BNE   AROUND                   MUST BE AN ERROR       @YM07023
         LA    RZERO,TWENTY4            ALREADY STARTED        @YM07023
         B     XCTLERR                  PUT OUT MSG            @YM07023
AROUND   EQU   *                                               @YM07023
*                                       WRITER MODULES         @Y17XARW
         NI    NCPFLAG1,XFF-NCPTRACE    MARK TRACE INACTIVE    @Y17XARW
         LA    RZERO,FOUR03             TRACE-DEACTIVATED ERROR MSG NO
         CLC   PRUDATA(TWO),BADRSP      ERROR ON RESPONSE      @Y17XARW
         BNE   XCTLERR                  BRANCH IF NO             S05331
         SPACE
INVALID  LA    RZERO,EIGHTEEN           INVALID-REQUEST MSG NO
         SPACE
XCTLERR  MVC   OPCMODID(TWO),ERRWRT     SET FOR XCTL TO ERR RTN@Y17XARW
         B     EXIT                     EXIT
         SPACE
XCTLMOD  EQU   *
         MVC   OPCMODID(TWO),WRTMOD     SET XCTL TO MSG MOD    @Y17XARW
         SPACE
EXIT     LR    RPARM,ROPCAVT            PASS OP CONTROL AVT ADDR
         XCTL  SF=(E,OPCXCTL)           CALL NEXT MODULE
         EJECT
***********************************************************************
*      THIS SUBROUTINE GETS A BUFFER UNIT, SAVES THE UNIT ADDR @Y71XATW
*      IN THE OPCE, AND BUILDS A PACKED RU IN THE BUFFER.      @Y17XARW
***********************************************************************
         SPACE
BUILDRU  EQU   *                                               @Y17XARW
         LA    RPARM,ONE                SET NUMBER OF UNITS REQD
         LA    RSAVE,OPCSAVE            SETUP SAVEAREA         @Y17XARW
         L     RBRNCH,OPCGETBF          LOAD GET-BFR RTN ADDRESS
         BALR  RRET,RBRNCH              LINK TO GET BUFFER UNIT
         ST    RPARM,OCUNIT             SET UNIT ADDR IN OPCE
***********************************************************************
*   SETUP RH AND TH FLAGS IN NEGATIVE PART OF RU PREFIX        @Y17XARW
***********************************************************************
         LA    RWORK6,PRUNLEN           SETUP LENGTH OF NEGATIVE
*                                        PREFIX                @Y17XARW
         SR    RPARM,RWORK6             BACKUP TO NEG PREFIX   @Y17XARW
         XC    IEDPRUND(PRUNLEN),IEDPRUND  ZERO OUT NEG PREFIX @Y17XARW
         MVI   PRURHFG0,RHFLAG0         SETUP FOLLOWING IN RU: @Y17XARW
*                                        FM DATA, FORMAT INDICATOR,
*                                        BEGIN CHAIN, AND END CHAIN
         MVI   PRURHFG1,RHFLAG1         SETUP FOLLOWING IN RU: @Y17XARW
*                                        DEFINITE RESPONSE
         MVI   PRUFIDN,THFLAGS          SETUP FOLLOWING IN RU: @Y17XARW
*                                        FID1 INDICATOR, BEGIN SEGMENT,
*                                        AND END SEGMENT
         SPACE
***********************************************************************
*   SETUP 12 BYTE PACKED RU                                    @Y17XARW
***********************************************************************
         MVC   PRUTTCIN,OCTRMTBL        SETUP NCP TNT AS DEST  @Y17XARW
         LA    RWORK6,EIGHT             SETUP LENGTH OF DATA   @Y17XARW
         STH   RWORK6,PRUDATCT            IN RU                @Y17XARW
         MVC   PRUTIC(FOUR),INVALTIC    INDICATE ONE UNIT      @Y17XARW
         MVC   PRUDATA+THREE(TWO),OCRSID  SETUP NETWORK ADDRESS@Y17XARW
         MVI   PRUDATA+FIVE,ONE         INDICATE LINK TRACE    @Y17XARW
         MVI   PRUDATA+SEVEN,ZERO       INDICATE NCP LINES ONLY@Y17XARW
         BR    RWORK8                   RETURN TO CALLER       @Y17XARW
         EJECT
***********************************************************************
*              THIS SUBROUTINE SCANS A NUMERIC FIELD IN THE REQUEST   *
*              MESSAGE AND CONVERTS THE DATA TO BINARY IN REGISTER 0  *
***********************************************************************
         SPACE
SCANOP   SR    RZERO,RZERO              INITIALIZE SCAN LIMIT
         MVI   OPCDOUBL,XF0             SET OPCDOUBL TO ZEROS
         MVC   OPCDOUBL+ONE(SEVEN),OPCDOUBL    (ZONED FORMAT)
         SPACE
SCANLOOP LTR   RSIZE,RSIZE              ANY MORE BYTES TO SCAN
         BZ    SCANEXIT                 NO, EXIT
         CLI   ZERO(RSCAN),COMMA        NEXT BYTE A COMMA
         BE    SCANOUT                  YES, GET OUT
         CLI   ZERO(RSCAN),CHAR0        IS BYTE NUMERIC ?
         BL    INVALID                  NO, INVALID REQUEST
         CLI   ZERO(RSCAN),CHAR9        VALID NUMERIC ?
         BH    INVALID                  NO, INVALID REQUEST
         MVC   OPCDOUBL(SEVEN),OPCDOUBL+ONE  SHIFT DATA 1 BYTE
         MVC   OPCDOUBL+SEVEN(ONE),ZERO(RSCAN)  MOVE IN THIS BYTE
         BCTR  RSIZE,RZERO              DECREMENT RSIZE
         LA    RSCAN,ONE(RSCAN)         BUMP SCAN POINTER
         LA    RZERO,ONE(RZERO)         BUMP NO. OF BYTES SCANNED
         CH    RZERO,CON5               HAVE MORE THAN 5 BYTES BEEN
*                                       SCANNED ?
         BH    INVALID                  YES, INVALID REQUEST
         B     SCANLOOP                 LOOP
         SPACE
SCANOUT  BCTR  RSIZE,RZERO              DECREMENT RSIZE
         LA    RSCAN,ONE(RSCAN)         BUMP SCAN POINTER
         SPACE
SCANEXIT PACK OPCDOUBL+FOUR(FOUR),OPCDOUBL+ONE(SEVEN)  PACK DATA
         XC    OPCDOUBL(FOUR),OPCDOUBL  ZERO FIRST WORD
         CVB   RZERO,OPCDOUBL           CONVERT DATA TO BINARY
         BR    RRET                     RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*     THIS ROUTINE CALLS IEDQTL, THE DEVICE DEPENDENT FIELD    @Y17XARW
*     LOOKUP ROUTINE, WHICH WILL RETURN IN REG15 THE ADDRESS   @Y17XARW
*     OF THE REQUESTED DEVICE DEPENDENT FIELDS.  THESE FIELDS  @Y17XARW
*     CONTAIN TRACE INFORMATION NEEDED BY IGCMB10D.            @Y17XARW
*                                                                     *
***********************************************************************
         SPACE
NCPDEV   EQU   *                                               @Y17XARW
         LA    RZERO,TRMNCPI            SETUP MASK             @Y17XARW
         L     RPARM,OCMODNME           SETUP NCP TTE ADDRESS  @Y17XARW
         LA    RSAVE,OPCSAVE            SETUP A SAVEAREA       @Y17XARW
         L     RBRNCH,AVTDDFT           SETUP ADDR OF QTL RTN  @Y17XARW
         BALR  RRET,RBRNCH              LOOKUP ADDR OF DEVICE  @Y17XARW
*                                       DEPENDENT FIELDS       @Y17XARW
         LR    RNCP,RBRNCH              RNCP WAS RNCPTTE AND   @Y17XARW
*                                       CONTAINED NCP TTE ADDR,@Y17XARW
*                                       RNCP NOW CONTAINS ADDR @Y17XARW
*                                       OF DEVICE DEPEND FLDS  @Y17XARW
         BR    RWORK8                   RETURN                 @Y17XARW
         EJECT
CON5     DC    H'5'                     MAX OPERAND LENGTH
CON28    DC    H'28'                    HALF-TABLE CTL INFO LENGTH
CON48    DC    H'48'                    LINE-TRACE CTL AREA SIZE
CON255   DC    H'255'                   INTERVAL-SIZE MAXIMUM
CONMAXH  DC    H'16366'                 TABLE-SIZE MAXIMUM
WRTMOD   DC    C'M1'                    MSG WRITER MODULE
THISMOD  DC    C'MB'                    ID FOR THIS MODULE
FIRSTLD  DC    C'00'                    FIRST LOAD OF OPEN
RUDISP   DC    C'02'                    RU DISPATCHER          @Y17XARW
ERRWRT   DC    C'03'                    ERROR WRITER MODULE
LINTID   DC    C'LINT'                  ID FOR COMWRITE RECORDS
DEACT    DC    X'010303'                DEACTIVATE CMD TYPE    @Y17XARW
ACT      DC    X'010302'                ACTIVATE CMD TYPE      @Y17XARW
INVALTIC DC    X'08000002'              INVALID TIC            @Y17XARW
BADRSP   DC    X'0801'                  ERROR ON RESPONSE      @Y17XARW
ACTIVE   DC    X'0815'                  ALREADY ACTIVE         @YM07023
ZAPAREA  DC    50X'00'                  ZAP AREA               @Y17XARW
         EJECT
TABHALF  DSECT                          HALF-TABLE CONTROL AREA
         DS    CL16                     ---FILLER---
TABLINUM DS    CL8                      GROUP NAME
TABCOMMA DS    CL1                      COMMA
TABRLN   DS    CL3                      RELATIVE LINE NUMBER
TABDATA  DS    0CL1                     START OF TABLE DATA
         EJECT
LSTDSECT DSECT                          DSECT OF GETMAIN/FREEMAIN LIST
LSTSIZE  DS    F                        SIZE OF CORE AREA
LSTADDR  DS    A                        ADDRESS OF AREA POINTER
         DS    X                        ---FILLER---
LSTSP    DS    X                        SUBPOOL NUMBER
         EJECT
         TAVTD
         EJECT
         IEZDEB                                                @OZ30601
         EJECT                                                 @OZ30601
         DCBD  DSORG=(PS),DEVD=(DA,TA)                         @OZ30601
         EJECT                                                 @OZ30601
         THEDD
         EJECT
         TNCPID
         EJECT
         TOPCED
         EJECT
         TOPCAVTD
         EJECT
         TPRUD
         EJECT
         IKJTCB                                                @OZ30601
         EJECT                                                 @OZ30601
TIOT     DSECT                                                 @OZ30601
         IEFTIOT1                                              @OZ30601
         EJECT                                                 @OZ30601
         TTRMD
         END
