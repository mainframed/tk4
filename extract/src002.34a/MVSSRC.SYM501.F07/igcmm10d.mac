         TITLE 'IGCMM10D - MODIFY FUNCTION SCAN/MAP/DISPATCHER'
IGCMM10D CSECT
*A000000-999999                                                  S99240
*A244000,282000,298000,402000,476000,484000,526000,582000,636000 S22024
*A652000,692000,702000,710000,720000,726000,764000               S22024
*C014000-016000,690000,696000,718000                             S22024
*A245000,262000,359000,574080-575760,581000,649020-649060,       Y06328
*A668100-669800,739020-739096,761000,772600-773200               Y06328
*C330000-350000                                                  Y06328
*D276000,280000,324000,578000                                    Y06328
*A188000,652000,668000                                         @OY12425
*C516000,574160,580000                                         @OY12425
*D426000-510000,514000,562000-566000,620000,624000-636000,     @OY12425
*D224000,252000,646000-648000,760000                           @OY12425
*D402000-404000                                                @OY14097
*A010000,116000,574960                                         @Y17XARX
*C058000,014000,574960,738000                                  @Y17XARX
*D084000-090000,113000,113500,574560-574880,575040-575760      @Y17XARX
*A574100-574110,617000-617600,624000,694000                    @Y17XARA
*A649080-649100,649700,718400-718600,761500                    @Y17XARA
*C688000-739800                                                @Y17XARA
*A652240,718000                                                @OY19405
***********************************************************************
*                                                                     *
* MODULE NAME = IGCMM10D (TCAM, OPERATOR CONTROL)              @Y17XARX
*                                                              @Y17XARX
* DESCRIPTIVE NAME = MODIFY FUNCTION SCAN/MAP/DISPATCHER       @Y17XARX
*                                                              @Y17XARX
* COPYRIGHT = NONE                                             @Y17XARX
*                                                                     *
* STATUS = VERSION 10.0                                        @Y17XARX
*                                                                     *
* FUNCTION - THIS MODULE RECEIVES CONTROL FROM IGCM010D WHENEVER THE  *
*   COMMAND IS A 'MODIFY'. A VALIDITY CHECK OF THE OPERAND IS PER-    *
*   FORMED AND THE FUNCTIONAL MODULE I.D. DETERMINED. CERTAIN VALIDITY*
*   CHECKING OF LINES ARE PERFORMED USING SUBROUTINES IN IGDQCA TO DO *
*   THE ACTUAL CHECKING. BITS ARE SET IN THE ELEMENT (OPCE) TO IDENT- *
*   IFY THE COMMAND. IF THE VALIDITY CHECKING PERFORMED BY THIS MODULE*
*   IS UNSUCCESSFUL,THE APPROPRIATE ERROR MESSAGE NUMBER IS PLACED IN *
*   REGISTER ZERO AND XCTL IS TO MODULE IGC0310D. IF THE COMMAND CAN  *
*   BE COMPLETELY SCANNED AND MAPPED INTO THE OPCE, XCTL IS MADE TO   *
*   THE FUNCTION MODULE. SUCH IS THE CASE WITH 'AUTOPOLL', 'TRACE',   *
*   AND 'OLT' OPERANDS. OTHER MODIFY COMMAND OPERANDS ARE NOT SCANNED *
*   COMPLETELY BY THIS MODULE AND XCTL IS TO IGCMA10D TO FURTHER SCAN *
*   AND MAP THESE OPERANDS.                                           *
*                                                                     *
*   IN A RESTART SITUATION,NO MAPPING FUNCTIONS ARE REQUIRED AND      *
*   CONTROL IS PASSED DIRECTLY TO THE FUNCTION MODULE AS SOON AS THE  *
*   FUNCTION IS DETERMINED.                                           *
*                                                                     *
* ENTRY POINTS -                                                      *
*   IGCMM10D IS THE ONLY ENTRY POINT FOR THIS MODULE           @Y17XARX
*                                                                     *
* INPUT -                                                             *
*    REGISTER 1 CONTAINS A POINTER TO THE OPERATOR CONTROL AVT AND    *
*   OPERATOR CONTROL RESIDENT WORK AREA CONTAINS THE OP CTL COMMAND   *
*   BEGINNING WITH THE OPERAND.                                       *
*                                                                     *
* OUTPUT -                                                            *
*         NONE                                                        *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         SVC 7(XCTL)                                                 *
*   IEDQCA'S SUBROUTINES                                              *
*                                                                     *
*        DCBLOCAT--VALIDITY CHECKS HARDWARE LINE ADDRESSES, GROUPNAMES*
*                  AND RELATIVE LINE NUMBERS. ALSO INSURES LINE DCBS  *
*                  ARE OPEN, RLN IS NOT MORE THAN NUMBER OF EXTENTS,  *
*                  AND LINE WAS NOT OPENED DUMMY. FOR ANY OF THESE    *
*                  ERRORS RETURNS PLUS 4 OF RETURN ADDRESS IN R14.    *
*                             GOOD RETURN- R0= UCB ADDRESS            *
*                                          R1= DCB ADDRESS            *
*                                                                     *
* EXITS,NORMAL - EXITS TO MODULE IGC0710D FOR OLT OPERAND             *
*               EXITS TO MODULE IGCM210D FOR AUTOPOLL OPERAND         *
*               EXITS TO MODULE IGCM610D FOR TRACE OPERAND            *
*               EXITS TO MODULE IGCM610D FOR DTRACE COMMAND    @Y17XARA
*               EXITS TO MODULE IGCM610D FOR I/O ERROR TRACE   @Y17XARA
*               EXITS TO MODULE IGCMA10D FOR FURTHER SCAN OF ALL OTHER*
*               MODIFY FUNCTIONS                                      *
*               EXITS TO MODULE IGCMV10D FOR CHNGMODE OPERAND  @Y17XARX
*               EXITS TO MODULE IGCMR10D FOR SWAP OPERAND      @Y17XARX
*               EXITS TO MODULE IGCMK10D FOR BACKUP OPERAND    @Y17XARX
*               EXITS TO MODULE IGCMN10D FOR BTRACE OPERAND    @Y17XARX
*               EXITS TO MODULE IGCML10D FOR CHNGLOAD OPERAND  @Y17XARX
*               EXITS TO MODULE IGCMY10D FOR ACTIV OPERAND     @Y17XARX
*               EXITS TO MODULE IGCMH10D FOR DUMP OPERAND      @Y17XARX
*               EXITS TO MODULE IGCMY10D FOR DEACT OPERAND     @Y17XARX
*               EXITS TO MODULE IGCMH10D FOR IPL OPERAND       @Y17XARX
*               EXITS TO MODULE IGCMT10D FOR SWDEVICE OPERAND  @Y17XARX
*               EXITS TO MODULE IGCML10D FOR SWBACK OPERAND    @Y17XARX
*               EXITS TO MODULE IGCMU10D FOR SPEED OPERAND     @Y17XARX
*               EXITS TO MODULE IGCSW10D FOR SWITCH OPERAND    @Y17XARX
*               EXITS TO MODULE IGCN010D FOR LNSWITCH OPERAND  @Y17XARX
*               EXITS TO MODULE IGCM710D FOR TIMEDATE OPERAND  @Y17XARX
*               EXITS TO MODULE IGCMS10D FOR BHSET OPERAND     @Y17XARX
*               EXITS TO MODULE IGCMS10D FOR TRANLMT OPERAND   @Y17XARX
*               EXITS TO MODULE IGCMS10D FOR SESSION OPERAND   @Y17XARX
*                                                                     *
* EXITS,ERROR - EXITS TO IGC0310D ERROR MESSAGE MODULE TO WRITE OUT AN*
*              ERROR MESSAGES:                                        *
*                   IED017I LINE XXXXXXXX  NOT OPEN                   *
*                   IED018I XXXXXXXX COMMAND INVALID                  *
*                                                                     *
*                                                                     *
* TABLES/WORK AREAS - OPERATOR CONTROL AVT, TCAM AVT, OPCE, OPERATOR  *
*                    CONTROL RESIDENT WORK AREA.                      *
*                                                                     *
* ATTRIBUTES - SUPERVISOR MODE, DISABLED, TRANSIENT(TYPE 4 SVC), NON- *
*             REENTRANT, SERIALLY REUSABLE, REFRESHABLE.              *
*                                                                     *
*CHARACTER CODE DEPENDENCY                                            *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT *
*    TO THAT USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED SO  *
*    THAT REDEFINITION OF CHARACTER CONSTANTS, BY REASSEMBLY, WILL    *
*    RESULT IN A CORRECT MODULE FOR THE NEW DEFINITION.               *
*                                                                     *
* NOTES -                                                             *
*    COMMUNICATIONS BETWEEN THE VARIOUS MODULES/TRANSIENTS IS         *
*    PRIMARILY THROUGH THE OPERATOR CONTROL ADDRESS VECTOR TABLE (AVT)*
*                                                                     *
***********************************************************************
         EJECT
         SPACE 1                                               @Y17XARA
RZERO    EQU   0                        PARM REG 0
RPARM    EQU   1                        PARM REG 1
ROPCAVT  EQU   2                        OP CONTROL AVT POINTER
ROPCE    EQU   3                        CURRENT ELEMENT ADDRESS
RAVT     EQU   4                        TCAM AVT ADDRESS
RWORKA   EQU   5                        WORK REGISTER
RLEN     EQU   6                        DATA LENGTH
RAPTR    EQU   7                        ADVANCE SCAN PTR
RRPTR    EQU   8                        REAR SCAN PTR
RTEMP    EQU   8                        TEMP REG               @OY12425
RBYSCN   EQU   9                        NUMBER OF BYTES SCANNED
RSCNRTN  EQU   10                       SCAN ROUTINE RETURN REG
RWRK11   EQU   11                       WORK REGISTER
RBASE    EQU   12                       MODULE BASE
RSAVE    EQU   13                       REG SAVE PTR
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RLINK    EQU   15                       LINK REG
         SPACE
ZERO     EQU   0                        ZERO VALUE
ONE      EQU   1                        VALUE ONE
TWO      EQU   2                        VALUE TWO
NAMELEN  EQU   2                        NAME FIELD LENGTH
THREE    EQU   3                        VALUE THREE
VARINAME EQU   3                        OFFSET TO CHANGE XCTL NAME
FOUR     EQU   4                        VALUE FOUR
FIVE     EQU   5                        VALUE FIVE
SEVEN    EQU   7                        VALUE SEVEN
EIGHT    EQU   8                        VALUE EIGHT
TWELVE   EQU   12                       VALUE TWELVE
SIXTEEN  EQU   16                       VALUE SIXTEEN
SEVNTEEN EQU   17                       VALUE SEVENTEEN
EIGHTEEN EQU   18                       VALUE EIGHTEEN
BLANK    EQU   C' '                     BLANK CHAR
COMMA    EQU   C','                     COMMA
EQUAL    EQU   C'='                     EQUAL
ALPHAA   EQU   C'A'                     CHAR 'A'                 S22024
RNDCB    EQU   X'04'                    3705 DCB ID              S05331
SLASH    EQU   C'/'                     VALID DELIMITER FOR OLT=
X80      EQU   X'80'                    HEX '80'
X0E      EQU   X'0E'                    HEX '0E'
FOXFOX   EQU   X'FF'                    HEX 'FF'
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         USING IHADCB,RPARM             DCB ADDRESSABILITY       S05331
         USING IGCMM10D,RBASE                                  @Y17XARA
A        EQU   IGCMM10D                                        @Y17XARA
         LR    RBASE,RLINK              SET BASE REGISTER      @Y17XARA
         SPACE 1                                               @Y17XARA
IGCMM10D IEDHJN SKIPID,HJN                                     @Y17XARA
         LR    ROPCAVT,RPARM            SET BASE FOR OP CTL AVT
         LM    ROPCE,RBYSCN,OPCSAVE     RESTORE REGS FROM M0
         SR    RWRK11,RWRK11            CLEAR REG                S22024
         IC    RWRK11,OCSWITCH          GET TABLE INDEX          S22024
         SLL   RWRK11,THREE             CALC TABLE DISPLACMENT   S22024
         LA    RWRK11,TBLMAP(RWRK11)    CALC TABLE ENTRY ADD   @Y17XARA
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROCESS
         BZ    MATCH                    NO, BRANCH
         SPACE
RESTART  EQU   *
         LA    RWRK11,TBLMAP-EIGHT      SET UP FOR SCAN          S22024
         MVC   OPCLDNME+VARINAME(NAMELEN),CKPMOD SET FOR XCTL TO S22024
*                                        06 IN CASE NO MATCH     S22024
*                                        FOUND IN TABLE          S22024
RSTSCAN  EQU   *                                                 S22024
         LA    RWRK11,EIGHT(,RWRK11)    BUMP TO NEXT ENTRY     @Y17XARA
         CLI   ZERO(RWRK11),FOXFOX      CHECK FOR END OF TABLE   S22024
         BE    XCTLMOD                  EXIT IF NO MATCH FOUND   S22024
         CLC   OCSWITCH,ONE(RWRK11)     COMPARE SWITCHES         S22024
         BNE   RSTSCAN                  NO MATCH, CHECK NEXT     S22024
*                                        TABLE ENTRY             S22024
         MVC   OPCLDNME+VARINAME(NAMELEN),FOUR(RWRK11) MOVE MOD  S22024
*                                       NAME INTO XCTL LIST      S22024
         B     XCTLMOD                  TO FUNCTION MODULE
         SPACE
****    MATCH FOUND BEGIN PROSESSING    ****
MATCH    EQU   *
         LA    RLEN,ONE(,RLEN)          RESTORE BUMP FACTOR    @Y17XARA
         ALR   RRPTR,RLEN               BUMP PTR TO NEXT SCAN
         SLR   RAPTR,RLEN               ADJUST NUM BYTES LEFT TO SCAN
         MVC   OCSWITCH(ONE),ONE(RWRK11) SET OPCE SWITCH FOR OP FOUND
         MVC   OPCLDNME+VARINAME(NAMELEN),FOUR(RWRK11) SET UP FOR XCTL
         LR    RLEN,RAPTR               SET LENGTH OF DATA FOR SCAN
         LR    RAPTR,RRPTR              SET START SCAN POINTERS
         BAL   RRET,GOSCAN              SCAN NEXT FIELD          S05331
         B     CKOLT                    +0 RETURN - SCAN ERROR   S05331
*                                       +4 RETURN - SCAN OK      S05331
         SPACE 1                                                 S05331
OKOLT    EQU   *
         LA    RSAVE,OPCSAVE            SET R 13 FOR DCB RTN
         SR    RZERO,RZERO              CLEAR REG 0
         IC    RZERO,SEVEN(RWRK11)      GET ROUTINE OFFSET
         LH    RRET,TWO(RWRK11)         GET ROUTINE OFFSET       S05331
         B     ZERO(RBASE,RRET)         BRANCH TO IT
CKOLT    EQU   *
         CLC   OLK(FOUR),EIGHT(RWORKA)  CHECK FOR OLT= CMD
         BE    OKOLT                    OLT= - YES, O.K.
NOMATCH  EQU   *
         LA    RZERO,EIGHTEEN           SET MSG NUM FOR MOD 03
         B     XCTLERR                  GO OUTPUT MESSAGE
         EJECT
SCNTODEL EQU   *
         SR    RBYSCN,RBYSCN            INITIALIZE BYTES SCANNED REG
LOOPSCN  EQU   *
         LTR   RLEN,RLEN                TEST FOR END OF DATA
         BNP   ENDSCN                   YES, END SCAN
         CLI   ZERO(RAPTR),COMMA        CHECK COMMA DELIMITER
         BE    SETRTN                   YES, SET UP RETURN
         CLI   ZERO(RAPTR),EQUAL        CHECK FOR EQUAL DELIMITER
         BE    SETRTN                   YES, SET UP RETURN
         CLI   ZERO(RAPTR),BLANK        CHECK BLANK DELIMITER
         BE    SETRTN                   YES, SET UP RETURN
         CLI   ZERO(RAPTR),SLASH        SLASH
         BE    SETRTN                   YES, BRANCH
         LA    RBYSCN,ONE(,RBYSCN)      BUMP BYTES SCANNED ONE @Y17XARA
         LA    RAPTR,ONE(,RAPTR)        BUMP ADVANCE SCAN PTR  @Y17XARA
         BCTR  RLEN,RZERO               REDUCE DATA LENGTH BY ONE
         B     LOOPSCN                  LOOP TO SCAN NEXT POSITION
SETRTN   EQU   *
         BCTR  RLEN,RZERO               REDUCE DATA LENGTH BY ONE
         LA    RAPTR,ONE(,RAPTR)        BUMP ADVANCE PAST DEL  @Y17XARA
ENDSCN   EQU   *
         BR    RSCNRTN                  RETURN TO CALLING RTN
         SPACE
NAMEGOOD EQU   *                                               @OY12425
         LR    RTEMP,RWRK11             ADJUST NUMBER OF BYTES @OY12425
         SR    RTEMP,RAPTR                LEFT TO SCAN         @OY12425
         SR    RLEN,RTEMP               STORE IN LENGTH REG    @OY12425
         LR    RAPTR,RWRK11             UPDATE MM SCAN PTR     @OY12425
         MVC   OCBACKUP,OPCDOUBL        SAVE GRPNME            @OY12425
         SPACE
CKOFFON  EQU   *
         LR    RRPTR,RAPTR              SET UP FOR SCAN ROUTINE
         BAL   RSCNRTN,SCNTODEL         GO SCAN
         STM   RLEN,RAPTR,OCMODNME      SAVE SCAN REGS FOR M6    S22024
         LA    RWRK11,THREE             SET UP TO
         CR    RBYSCN,RWRK11              CHECK BYTES SCANNED
         BE    CKOFF                    THREE - CHECK OFF
         BH    NOMATCH                  MORE THAN THREE - INVALID
         CLC   ZERO(TWO,RRPTR),ON       CHECK ON
         BNE   NOMATCH                  NO - INVALID COMMAND
SWUPONE  EQU   *
         SR    RWRK11,RWRK11            YES -
         IC    RWRK11,OCSWITCH            INCREMENT
SWTWO    EQU   *
         LA    RWRK11,ONE(,RWRK11)          SWITCH             @Y17XARA
         STC   RWRK11,OCSWITCH                  BY ONE
         B     XCTLMOD                  GO TO FUNCTION MODULE
CKOFF    CLC   ZERO(THREE,RRPTR),OFF    CHECK OFF
         BE    XCTLMOD                  YES - TO FUNCTION MODULE
         B     NOMATCH                  NO - INVALID CMD
         EJECT
* OPERAND HAS BEEN DETERMINED AT THIS POINT. OPERAND CODE HAS BEEN
* SET IN ELEMENT, FUNCTION MODULE DETERMINED AND SET IN ELEMENT
TRACE    EQU   *                                                 S05331
         CLC   OPCDOUBL,ERRORDC         I/O ERROR TRACE        @Y17XARA
         BE    ERRORTRC                 YES , GO TO ERRORTRC   @Y17XARA
         LR    RWRK11,RRPTR             SETUP PTR FOR OPSCAN   @OY12425
         MVI   OCSCAN,OCADDR+OCDDNRLN   GRPNME,RLN OR ADDR     @OY12425
         L     RLINK,OPCDCBLK           SETUP FOR SCAN RTN     @OY12425
         BAL   RRET,FOUR(RLINK)         BRANCH TO OPSCAN       @OY12425
         B     CONTINUE                 NAME GOOD, CONTINUE    @OY12425
         B     ERROR                    NAME NOT FOUND         @OY12425
         B     NOMATCH                  SYNTAX ERROR           @OY12425
CONTINUE EQU   *                                               @OY12425
         LR    RTEMP,RWRK11             ADJUST NUMBER OF BYTES @OY12425
         SR    RTEMP,RAPTR                 LEFT TO SCAN        @OY12425
         SR    RLEN,RTEMP               STORE IN LENGTH REG    @OY12425
         LR    RAPTR,RWRK11             RESET MM SCAN PTR      @OY12425
         LR    RRET,RAPTR               GET ADDRESS OF ...       S05331
         BCTR  RRET,RZERO               ... LAST DELIMITER       S05331
         CLI   ZERO(RRET),SLASH         WAS IT A SLASH?          S05331
         BNE   MVDBL                    BRANCH IF NO             S05331
         B     NOMATCH                  INVALID REQUEST        @Y17XARX
AUTOPOLL EQU   *                        AUTOPOLL ENTRY
         LR    RWRK11,RRPTR             SETUP PTR FOR OPSCAN   @OY12425
         MVI   OCSCAN,OCADDR+OCDDNRLN   GRPNME,RLN OR ADDR     @OY12425
         L     RLINK,OPCDCBLK           SETUP FOR SCAN RTN     @OY12425
         BAL   RRET,FOUR(RLINK)         BRANCH TO OPSCAN       @OY12425
         B     NAMEGOOD                 NAME WAS FOUND         @OY12425
         B     ERROR                    NAME WAS NOT FOUND     @OY12425
         B     NOMATCH                  INVALID SYNTAX         @OY12425
MVDBL    EQU   *                                                 S05331
         MVC   OCBACKUP,OPCDOUBL        SAVE GROUP NAME FOR M6   S22024
         B     CKOFFON                  BR TO CK OFF/ON INDICATION
INTERVAL EQU   *                        INTERVAL ENTRY
OPERATOR EQU   *                        OPERATOR ENTRY
DEBUGD   EQU   *                        DEBUGD ENTRY
DEBUGL   EQU   *                        DEBUGL ENTRY
INTENSET EQU   *                        INTENSET ENTRY
INTENSEL EQU   *                        INTENSEL ENTRY
OLT      EQU   *                        OLT ENTRY
OPT      EQU   *                        OPT ENTRY
         STM   ROPCE,RBYSCN,OPCSAVE     SAVE REGS THREE - NINE
         LR    RPARM,ROPCAVT            PASS OP CTL AVT PTR
         XC    OCBACKUP(SIXTEEN),OCBACKUP CLEAR XCTL LIST
         MVC   OCMODNME(EIGHT),NXTMOD   SET MODULE NAME
         LA    RWRK11,OCMODNME          PUT ADDR OF MOD NAME
         ST    RWRK11,OCBACKUP            INTO XCTL LIST
         LA    RLINK,OCBACKUP           SET R15 FOR XCTL
         SVC   SEVEN                    ISSUE XCTL
         EJECT                                                 @Y17XARA
ERRORTRC EQU   *                                               @Y17XARA
         MVI   OCSWITCH,OCIOTR          I/O ERROR TRACE        @Y17XARA
         LR    RRPTR,RAPTR              SET UP FOR SCAN        @Y17XARA
         BAL   RRET,GOSCAN              SCAN NEXT FIELD        @Y17XARA
         B     NOMATCH                  ERROR RETURN           @Y17XARA
         SPACE
BTRACE   EQU   *                        BTRACE ENTRY
BHSET    EQU   *                        BLOCK HANDLER SET ASSOC  S22024
SESSION  EQU   *                        SESSION LIMIT            S22024
TRANLMT  EQU   *                        TRANSMISSION LIMIT       S22024
SWBACK   EQU   *                        SWITCH BACK UP         @XM05801
CHNGLOAD EQU   *                        MODIFY LOAD NAME       @ZM47834
SWDEVICE EQU   *                        SWITCH TO BACKUP PATH    S22024
SWAP     EQU   *                        SWAP DEVICES ID          S22024
CHNGMODE EQU   *                        CHANGE DIAL MODE CMD     Y06327
SPEED    EQU   *                        CHANGE SPEED CMD         Y06327
LNSWITCH EQU   *                        SWITCH LINE MODE         Y06328
SUBT     EQU   *                        SUBTASK TRACE          @Y17XARA
BUFFTR   EQU   *                        BUFFER TRACE ENTRY     @OY19405
         MVC   OCBACKUP,OPCDOUBL        SAVE DDNAME/ADDRESS      S22024
*                                SAVE DTRACE= OR ERROR, OPTION @Y17XARA
         STM   ROPCE,RBYSCN,OPCSAVE     SAVE REGS THREE - NINE
         B     XCTLMOD                  PREPARE TO EXIT          S22024
BACKUP   EQU   *                                               @OY12425
CHANL    EQU   *                                               @OY12425
DUMP     EQU   *                                               @OY12425
IPL      EQU   *                                               @OY12425
SWITCH   EQU   *                                               @OY12425
TIMEDATE EQU   *                                               @OY12425
ACTIV    EQU   *                                               @OY12425
DEACT    EQU   *                                               @OY12425
         LR    RWRK11,RRPTR             SETUP SCAN PTR         @OY12425
         MVI   OCSCAN,OCADDR+OCDDNAME   ITS A DDNAME OR ADDR   @OY12425
         L     RLINK,OPCDCBLK           PREPARE FOR SCAN RTN   @OY12425
         BAL   RRET,FOUR(RLINK)         GO TO OPSCAN           @OY12425
         B     GOODNAME                 NAME FOUND             @OY12425
         B     NOTFOUND                 NAME NOTFOUND          @OY12425
         B     NOMATCH                  INVALID CMD            @OY12425
GOODNAME EQU   *                                               @OY12425
         MVI   OCSCAN,AVTEZERO          INDICATE GOOD RTN CODE @OY12425
         STCM  RPARM,SEVEN,OCELEM+ONE   SAVE DCB FOR NEXT MOD  @OY12425
         LR    RAPTR,RWRK11             SAVE SCAN PTR          @OY12425
         B     SWAP                     GO XCTL                @OY12425
NOTFOUND EQU   *                                               @OY12425
         OI    OCSCAN,OCNONAME          NAME NOT FOUND         @OY12425
         B     SWAP                     GO XCTL                @OY12425
ERROR    EQU   *                                               @OY12425
         LA    RZERO,SEVNTEEN           SET ERR MSG CODE       @OY12425
         SPACE
XCTLERR  EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),ERRWRT SET UP FOR XCTL
XCTLMOD  EQU   *
         LR    RPARM,ROPCAVT            PASS OP CTL AVT PTR
         XCTL  SF=(E,OPCXCTL)           CALL FUNCTION MODULE
         SPACE
COMP     CLC   ZERO(ZERO,RSCNRTN),ZERO(RRPTR) COMPARE OPERATION
MVADDR   MVC   OPCDOUBL(ZERO),ZERO(RRPTR) MOVE ADDR FOR DCB RTN@OY12425
         EJECT
GOSCAN   EQU   *                                                 S05331
*****************************************************************S05331
*              THIS SUBROUTINE LINKS TO THE SCAN SUBROUTINE,     S05331
*              CHECKS FOR A VALID NUMBER OF BYTES SCANNED,       S05331
*              AND MOVES THE SCANNED DATA INTO OPCDOUBL          S05331
*****************************************************************S05331
         BAL   RSCNRTN,SCNTODEL         LINK TO SCAN ROUTINE     S05331
         LTR   RBYSCN,RBYSCN            0 BYTES SCANNED?         S05331
         BCR   EIGHT,RRET               BRANCH IF YES, ERROR RTN S05331
         MVI   OPCDOUBL,BLANK           SET OPCDOUBL ...         S05331
         MVC   OPCDOUBL+ONE(SEVEN),OPCDOUBL  ... TO BLANKS       S05331
         CH    RBYSCN,HALF8             MORE THAN 8 BYTES SCANNEDS05331
         BCR   TWO,RRET                 BRANCH IF YES            S05331
         BCTR  RBYSCN,RZERO             REDUCE COUNT FOR EXECUTE S05331
         EX    RBYSCN,MVADDR            MOVE SCANNED FIELD       S05331
         LA    RBYSCN,ONE(,RBYSCN)      RESTORE COUNT          @Y17XARA
         B     FOUR(RRET)               RETURN TO CALLER         S05331
         EJECT
* THE FOLLOWING IS AN OPERAND TABLE MAP. EACH ENTRY CONSISTS OF;
* LENGTH OF OPERAND
* HEX CODE OF OPERAND
* ADDRESS OF OPERAND ENTRY
* MODULE NAME OF PROCESS FUNCTION
* BRANCH TABLE OFFSET INTO 2ND SCAN MODULE FOR CONTINUING SCAN PROCESS
       DS    0H
TBLMAP EQU   *
       DC    AL1(9),AL1(OCX01),AL2(AUTOPOLL-A),C'M2',AL2(0) AUTOPOLL
       DC    AL1(7),AL1(OCX03),AL2(BACKUP-A),C'MK',AL2(0) BACKUP S22024
       DC    AL1(7),AL1(OCX04),AL2(BTRACE-A),C'MN',AL2(0) BTRACE S22024
       DC    AL1(5),AL1(OCX07),AL2(CHNGLOAD-A),C'ML',AL2(0) LOAD S22024
       DC    AL1(8),AL1(OCX08),AL2(DEBUGD-A),C'M9',AL2(44)  DEBUGD
       DC    AL1(8),AL1(OCX09),AL2(DEBUGL-A),C'M9',AL2(40)  DEBUGL
       DC    AL1(5),AL1(OCDMPC),AL2(DUMP-A),C'MH',AL2(0) DUMP3705S22024
       DC    AL1(13),AL1(OCX0C),AL2(INTENSET-A),C'M5',AL2(28) INTENSET
       DC    AL1(13),AL1(OCX0D),AL2(INTENSEL-A),C'M5',AL2(24) INTENSEL
       DC    AL1(9),AL1(OCX0E),AL2(INTERVAL-A),C'M4',AL2(60) IN@YM06004
       DC    AL1(4),AL1(OCIPLC),AL2(IPL-A),C'MH',AL2(0)    IPL   S22024
       DC    AL1(4),AL1(OCX12),AL2(OLT-A),C'07',AL2(16)       OLT
       DC    AL1(9),AL1(OCX13),AL2(OPERATOR-A),C'M7',AL2(64) OP@YM06004
       DC    AL1(4),AL1(OCX15),AL2(OPT-A),C'M8',AL2(12)       OPT
       DC    AL1(7),AL1(OCX16),AL2(SWBACK-A),C'ML',AL2(0) BACKUPS22024
       DC    AL1(7),AL1(OCX17),AL2(SWITCH-A),C'SW',AL2(0) SWITCH S22024
       DC    AL1(8),AL1(OCX18),AL2(TIMEDATE-A),C'M7',AL2(0) TIME S22024
       DC    AL1(6),AL1(OCX19),AL2(TRACE-A),C'M6',AL2(0)    TRACE
       DC    AL1(6),AL1(OCBHSET),AL2(BHSET-A),C'MS',AL2(0) BHSET S22024
       DC    AL1(8),AL1(OCSESSNL),AL2(SESSION-A),C'MS',AL2(0)    S22024
       DC    AL1(8),AL1(OCTRANL),AL2(TRANLMT-A),C'MS',AL2(0)     S22024
       DC    AL1(9),AL1(OCSWDEVB),AL2(SWDEVICE-A),C'MT',AL2(0)   S22024
*                                                                S22024
       DC    AL1(6),AL1(OCACTVTE),AL2(ACTIV-A),C'MY',AL2(0) ACTV S22024
       DC    AL1(6),AL1(OCDEACTE),AL2(DEACT-A),C'MY',AL2(0)    @Y17XARX
*                                      DEACT 3705              @Y17XARX
       DC    AL1(5),AL1(OCSWAP),AL2(SWAP-A),C'MR',AL2(0) SWAP DD S22024
       DC    AL1(9),AL1(OCCHANGE),AL2(CHNGMODE-A),C'MV',AL2(0)   Y06327
*                                      CHANGE DIAL MODE          Y06327
       DC    AL1(6),AL1(OCSSELCT),AL2(SPEED-A),C'MU',AL2(0)      Y06327
*                                      SPEED SELECT              Y06327
       DC    AL1(9),AL1(OCSWLNME),AL2(LNSWITCH-A),C'N0',AL2(0) - Y06328
*                                      SWITCH LINE MODE EP       Y06328
       DC    AL1(7),AL1(OCSUBT),AL2(SUBT-A),C'M6',AL2(0) DTRACE@Y17XARA
       DC    AL1(7),AL1(OCBUF),AL2(BUFFTR-A),C'MZ',AL2(0) BUFTR@OY19405
       DC    AL1(6),AL1(OCIOTR),AL2(TRACE-A),C'M6',AL2(0) ERROR@Y17XARA
       DC    AL1(9),AL1(OCSWLNMN),AL2(LNSWITCH-A),C'N0',AL2(0) - Y06328
*                                      SWITCH LINE MODE NCP      Y06328
       DC    AL1(9),AL1(OCX02),AL2(AUTOPOLL-A),C'M2',AL2(0)    AUTOPOLL
       DC    AL1(7),AL1(OCX05),AL2(BTRACE-A),C'MN',AL2(0)        BTRACE
       DC    AL1(9),AL1(OCX0F),AL2(INTERVAL-A),C'M4',AL2(60)   @YM06004
       DC    AL1(9),AL1(OCX10),AL2(INTERVAL-A),C'M4',AL2(60)   @YM06004
       DC    AL1(6),AL1(OCX1A),AL2(TRACE-A),C'M6',AL2(0)          TRACE
       DC    AL1(9),AL1(OCSWDEVP),AL2(SWDEVICE-A),C'MT',AL2(0) SWDEVICE
       DC    X'FF'                     TABLE END
         SPACE 3
OLK      DC    C'OLT='                  OLT
ERRWRT   DC    C'03'                    WRITE ERROR MESSAGE MOD
CKPMOD   DC    C'06'                    CHECKPOINT MODULE ID     S22024
WRITE    DC    C'07'                    WRITE MESSAGE MOD
MODM1    DC    C'M1'                    FUNCTION COMPLETE MSG MOD
ON       DC    C'ON'                    CHARACTERS ON
ALL      DC    C'ALL'                   CHARACTERS ALL
OFF      DC    C'OFF'                   CHARACTERS OFF
HALF8    DC    H'8'                     CONSTANT VALUE           S05331
ERRORDC  DC    CL8'ERRORS'              CONSTANT ' ERRORS '    @Y17XARA
NXTMOD   DC    C'IGCMA10D'              II MODIFY SCAN MODULE
         ORG   IGCMM10D+X'3FF'          PATCH AREA               S22024
         DC    X'FF'                    PATCH AREA               S22024
         ORG
         EJECT
         TAVTD
         EJECT
         DCBD  DSORG=TR                                          S05331
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
         END
