         TITLE ' IGCMF10D - 3705 IPL - OUTPUT LOAD MODULE'
IGCMF10D CSECT
         SPACE
         SPACE
************************START OF SPECFICATIONS*************************
*                                                                     *
*-000000-999999                                                @X26X5R0
*C278600                                                       @YA10666
*A277200                                                       @YA10666
*D756600                                                       @YA10666
*A053000,C056000,A066000,D069000,C076000,                      @Y17XARS
*D077000-078000,A076000,D079000,A079000,C088000                @Y17XARS
*C095000,C097000,D100000-101000,C111000,C123000-125000         @Y17XARS
*C132000,C134000,C136000,D140000-168000,D177000-178000,        @Y17XARS
*C197000,C209000-210000,C222000,A223000,C232000-233000         @Y17XARS
*C243000,C249000,C259000-260000,C262000,D276500,               @Y17XARS
*277500,278200-278400,A278500,D278600-278640,A284000           @Y17XARS
*D285000-285700,D287000,D288000,A289000,A289200,               @Y17XARS
*D290000,D320200-320600,A400000,C355000,                       @Y17XARS
*A373000,D375500,A292000,D394000-397000,A744000,               @Y17XARS
*A746000,A758000                                               @Y17XARS
*C066700,C339000,A086200,A278500,A278700,A434000@Y17XARS
* MODULE NAME = IGCMF10D (TCAM,OPERATOR CONTROL)                      *
*                                                                     *
* DESCRIPTIVE NAME = 3705 IPL I/O MODULE                              *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = VERSION 10.0                                               *
*                                                                     *
* FUNCTION = THIS ROUTINE HANDLES ALL I/O TO THE 3705 VIA EXCP.       *
*    IT RECEIVES CONTROL FROM IGCMD10D OR IGCMG10D OR FROM IGC0010D   *
*    WHEN ENTERED IT SETS UP THE BASE FOR THE                         *
*    COMMON WORKAREA USED BY THE IPL LOADS AND CHECKS TO SEE IF AN    *
*    IPL IS REQUIRED. IT WILL BE REQUIRED ON THE 1ST ENTRY FROM       *
*    IGCMD10D. IF YES PHASE 1 IS SENT ACROSS TO THE 3705.             *
*    OTHERWISE THE CORE CONTAINING THE                                *
*    PHASE 1 ROUTINE IS USED AS THE OUTPUT BUFFER AND GETMAINS ARE    *
*    DONE FOR AN INPUT BUFFER AND A WORK BUFFER. NEXT A CHECK IS      *
*    MADE TO SEE IF INITIAL TEST WAS SELECTED. THIS OPTION IS         *
*    SPECIFIED ON THE 3705 TTE. INITIAL TEST IS A HARDWARE EXERCISOR  *
*    WHICH WILL CHECK OUT THE 3705. IT RESIDES ON LINKLIB, AND IS     *
*    NAMED IFL3705D. THE RECORDS ARE 1024 BYTES OR LESS.              *
*                                                                     *
*                                                                     *
*    IGCMF10D NOW XCTLS TO IGCME10D TO BEGIN READING INITIAL TEST     *
*    LOAD MODULE RECORDS AND THEY ARE SENT TO THE 3705. WHEN IGCME10D *
*    DETECTS THE LAST RECORD IT SETS LASTTX SWITCH IN THE WORKAREA.   *
*    IGCMF10D DETECTS THAT THIS SWITCH IS SET AND SENDS THE LAST TEXT *
*    RECORD. INITIAL TEST IS THEN GIVEN 22 SECONDS TO PROCESS.        *
*    IGCMF10D TPOSTS A SPECIAL TIME DELAY REQUEST ELEMENT TO THE TCAM *
*    TIME DELAY QCB. WHEN THE INTERVAL EXPIRES A WRITE BREAK IS SENT  *
*    TO THE 3705 AND IGCMF10D XCTLS BACK TO IGCMD10D TO DO A BLDL AND *
*    A 'FIND' FOR THE 3705 NCP LOAD MODULE SPECIFIED IN THE 3705 DCB. *
*    THE NEXT TIME IGCMF10D IS ENTERED IT IS FROM IGCMG10D AND        *
*    REGISTER 0 CONTAINS THE ADDRESS OF THE END OF THE OUTPUT BUFFER  *
*    THIS ADDRESS IS INSERTED INTO THE CCW IN THE LCB, THE LENGTH IS  *
*    SET TO X'200' AND AN EXCP IS ISSUED. BY XCTLING FROM IGCMF10D,   *
*    TO IGCME10D, TO IGCMG10D, AND BACK TO IGCMF10D, THE RECORDS ARE  *
*    SENT TO THE 3705 512 BYTES AT A TIME.                            *
*    AGAIN,AS WITH INITIAL TEST PROCESSING, IGCME10D WILL DETECT THE  *
*    LAST RECORD AND WILL SET LASTTX BIT. WHEN IGCMF10D FINDS IT SET  *
*    IT WILL SEND OVER THE ENTRY POINT, WHICH WAS OBTAINED DURING THE *
*    BLDL DONE BY IGCMD10D AND SAVED IN THE WORKAREA.                 *
*    AN XCTL WILL BE DONE TO IGCMI10D TO SCHEDULE                     *
*    WRITTING OF THE IPL COMPLETED MESSAGE AND WHO WILL DO CLEANUP    *
*    PROCESSING                                                       *
*                                                                     *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = EBCDIC CHARACTER CODE DEPENDENCIES - CORRECTABLE  *
*                   BY REASSEMBLY                                     *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = REGISTER 1 OPCAVT                         *
*                           REGISTER 2 OPCAVT                         *
*                           REGISTER 3 OPCE                           *
*                           REGISTER 4 TCAM AVT                       *
*                           REGISTER 5 IPL COMMON AREA 1              *
*                           REGISTER 6 IPL COMMON AREA 6              *
*                           REGISTER 7 IPL/DUMP COMMON AREA           *
*                           REGISTER 11 TCAM DISPATCHER               *
*                           REGISTER 13 OPERATOR CONTROL REG SAVEARE  *
*                           REGISTER 14 RETURN REGISTER               *
*                           REGISTER 15 BRANCHING REGISTER            *
*                                                                     *
*    PATCH LABEL = NONE                                               *
*                                                                     *
* MODULE TYPE = PROCEDURE                                             *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    ATTRIBUTES = REFRESHABLE,SERIALLY REUSABLE,TRANSIENT(TYPE 4),    *
*                 SUPERVISOR MODE,REENTRANT                           *
*                                                                     *
* ENTRY POINT = IGCMF10D                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = XCTL SF=(E,OPCXCTL)                                    *
*                                                                     *
* INPUT = REGISTER 1 OPCAVT                                           *
*         OPCCOPCE POINTS TO THE CURRENT ELEMENT                      *
*             OPCE FROM IGCMH10D                                      *
*             TIME DELAY ELEMENT                                      *
*             IPL/DUMP COMMON WORK AREA                               *
*             OUTPUT BUFFER FROM IGCMG10D                             *
*                                                                     *
* OUTPUT = REGISTER 1 = OPCAVT                                        *
*          REGISTER0 =1 TO GO TO IGCME10D TO READ NEXT LOAD MODULE RCD*
*          REGISTER0 = ADDRESS OF OUTPUT BUFFER FOR IGCMG10D          *
*          REGISTER3 = ADDRESS OF OPCE AND OCTRINMF SET TO IGCMD10D   *
*          OPCCOPCE = 0 TO GO TO IGC0010D TO PREFORM NEXT FUNCTION    *
*          OPCCOPCE = ADDRESS OF CURRENT ELEMENT (R0 = MSG NO.)       *
*          TO GO TO IGCMI10D TO WRITE OUT ERROR MESSAGE               *
*                                                                     *
* EXIT-NORMAL = IGCMG10D, IGCMD10D, IGC0010D, IGCMI10D                *
*                                                                     *
* EXIT-ERROR = IGCMI10D                                               *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA AREAS = DUMP/IPL COMMON AREA                                *
*                 IPL 1ST WORK AREA                                   *
*                 IPL 2ND WORK AREA                                   *
*                 PHASE 1 AREA - OUTPUT BUFFER                        *
*                 1K INPUT BUFFER                                     *
*                 1K WORK BUFFER                                      *
*                                                                     *
*    CONTROL BLOCKS = AVT                                             *
*                     DCB                                             *
*                     LCB                                             *
*                     OPCAVT                                          *
*                     OPCE                                            *
*                     QCB                                             *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = XCTL GETMAIN FREEMAIN AQCTL EXCP                           *
*                                                                     *
* CHANGE ACTIVITY = NONE                                              *
*                                                                     *
*************************END OF SPECIFICATIONS*************************
         EJECT
         EJECT
         USING DATADS,R6                ADDRESSABILITY FOR C-WKAREA
         USING IEDQLCB,RA               LCB ADDRESSABILITY
         USING IEDQCCW,RB               ADDRESSABILITY TO      @Y17XARS
*                                       BUILD CCWS
         USING IEDQOPCD,R2              ADDRESSABILITY FOR OPCAVT
         USING IEDQOPCE,R3              ADDRESSABILITY FOR OPCE
         USING IEDCOM,R7                COMMON AREA ADDRESS    @Y17XARS
         BALR  RC,0                     ESTABLISH ADDRESSABILITY
         USING *,RC
IGCMF10D IEDHJN  SKIPID
         LR    R2,R1                    SET BASE FOR OPCAVT
         L     R3,OPCCOPCE              SET BASE FOR PASSED ELEMENT
         CLI   FOUR(R3),TIMELEM         TIME DELAY ELEMENT?
         BE    TIMESUP                  YES
         CLI   OCELMPRI,PRIHIFLG        RETURN FROM DLC        @YM03676
         BE    RETURNIO                 YES                    @Y17XARS
         USING IEDOCTR,R1                                      @Y17XARS
         L     R1,OCTRUNIT              LOAD TRAILER BASE      @Y17XARS
         L     RA,OCTRLCB               LOAD LCB BASE          @Y17XARS
         DROP  R1                                              @Y17XARS
         LA    RB,LCBCCW3               CHANNEL PROG CCWS      @Y17XARS
         L     R7,OCMODNME              SET BASE FOR COMMON AREA
         LA    R5,COMCCWS               SET BASE FOR LWKAREA
         LA    R6,DATAOFF(R5)           SET BASE FOR C-WKAREA
         TM    SWITCH,ONIPL             TEST FOR IPL
         BO    WRTIPLA                  YES
         TM    SWITCH,ONIO              I/O OUTSTANDING ?
         BNO   WRT026                   NO--ENTERED FROM 'OUTPUT'
         SPACE 2
*        SET UP TO RETURN FROM DLC                             @Y17XARS
EXCPRTN  EQU   *                                               @YM02941
         USING IEDQAVTD,R4                                     @Y17XARS
         L     R4,OPCAVTPT              AVT ADDRESSABILITY     @Y17XARS
         LA    RF,AVTOPCOB              QCB POST               @YM02935
         STCM  RF,SEVEN,COMQCB          PUT IT IN ELEMENT      @YM02935
         MVI   COMPRI,PRIHIFLG          PRIORITY               @Y17XARS
         MVI   COMFLAGS,COMDLCMF        RETURN FLAG            @Y17XARS
         ST    R3,COMOPCE               SAVE OPCE ADDRESS      @YM02941
         XC    LCBCCW1(4),LCBCCW1       ZERO ECB               @Y17XARS
         MVC   LCBCCW1+4(4),OCMODNME    POINTS TO IPL/DUMP WA  @Y17XARS
         MVC   OPCMODID,XCTL00          XCTL DESTINATION       @Y17XARS
         OI    SWITCH,ONIO              I/O IN PROGRESS        @YM02941
         MVI   LCBSIO,SIOCODE           REQ I/O IN PARM        @ZM46810
         MVC   LCBCP+1(3),LCBSTART      SET UP CH PGM START    @ZM46810
         XC    LCBCP(1),LCBCP           CLEAR HIGH BYTE        @ZM46810
         LA    R1,LCBQEBPL              POINT TO PARM LIST     @ZM46810
         AQCTL                          DO STARTIO             @ZM46810
         BR    RE                       RETURN TO CALLER       @YM02941
         SPACE
RETURNIO EQU   *                                               @Y17XARS
         LR    R7,R3                    COMMON WKAREA BASE     @YM02941
         L     R3,COMOPCE               OPCE BASE              @Y17XARS
         ST    R3,OPCCOPCE              MAKE OPCE CURRENT ELE  @Y17XARS
         USING IEDOCTR,R1                                      @Y17XARS
         L     R1,OCTRUNIT              LOAD TRAILER BASE      @Y17XARS
         L     RA,OCTRLCB               LOAD LCB BASE          @Y17XARS
         DROP  R1                                              @Y17XARS
         LA    RB,LCBCCW3               CHANNEL PROG CCWS      @YM02941
         LA    R5,COMCCWS               BASE FOR L-WKAREA      @YM02941
         LA    R6,DATAOFF(R5)           BASE FOR C-WKAREA      @YM02941
*                                                              @Y17XARS
*        CHECK CONDITION CODE 3 AND UNIT EXCEPTION ERRORS      @Y17XARS
*                                                              @Y17XARS
         TM    LCBSIOCC,LCBSIOC1+LCBSIOC2 IS THIS CC3          @Y17XARS
         BO    CC3ERR                   YES, ERR MSG AND EXIT  @Y17XARS
         TM    LCBCSWUS,LCBUSUE         IS THIS UNIT EXCEPTION @Y17XARS
         BO    UEERR                    YES, ERR MSG AND EXIT  @Y17XARS
         SPACE 2                                               @Y17XARS
         LA    R1,LCBCCW3               CHAN PROG AREA         @Y17XARS
         STCM  R1,SEVEN,LCBSTART        START CHANNEL PROGRAM  @Y17XARS
*                                       ADDRESS FOR ALL EXCEPT @Y17XARS
*                                       FIRST                  @Y17XARS
         CLI   LCBECBCC,LCBECB7F        WAS THE I/O GOOD ?     @YM02950
         BNE   SETERROR                 N/ - GET ERROR MSG NUMBER
         NI    SWITCH,ONES-ONIO         TURN OFF I/O PENDING
         SPACE
         SPACE
         CLI   CCWOPCDE,CCWIPL          IPL CMD ?              @Y17XARS
         BE    WRTIPL2                  YES GO HANDLE
         CLI   CCWOPCDE,CCWBRK          WRITE BREAK CMD ?      @Y17XARS
         BE    WRTBRK                   YES GO HANDLE
         SPACE
         TM    SWITCH2,COMPLETE         WAS THIS THE ENTRY PT.
         BO    ENDIPL                   YES ITS OVER          @XM05712
         SPACE
         TM    SWITCH,ONLAST+ONEND      IS THIS THE END
         BO    LASTTXT                  YES
         SPACE
         SPACE
         MVC   OPCMODID,XCTLOUT         XCTL TO IGCMG10D       @Y17XARS
         LR    R0,R3                    ADDR OF OUTPUT BUFFER
         B     EXIT                     PREPARE T O EXIT
         SPACE  5
WRT026   EQU   *
         LR    RE,R0                    RE CONTAINS END OF O/P @YM02949
*                                       PUT BUFFER
         TM    SWITCH2,COMPLETE         WAS THIS THE ENTRY POINT ?
         BO    ENDOF                    YES ITS OVER
         TM    SWITCH,ONLAST+ONEND      IS THIS THE END ?
         BO    LASTTXT                  YES
         SH    RE,OUTBFSZ               ADJ TO START OF BUFFR  @YM02949
         STCM  RE,SEVEN,CCWADDR         SAVE THE BUFFER ADDR   @YM02949
         MVI   CCWOPCDE,CCWWRITE        SET WRITE CMD CODE     @Y17XARS
         MVC   CCWCOUNT,OUTBFSZ         SET THE COUNT          @Y17XARS
         B     SENDITW                  GO ISSUE AN EXCP
         SPACE
         SPACE 4
WRTIPLA  EQU   *
         L     RF,PHASPTR               GET PHASE POINTER      @YM02941
         L     RF,ZERO(RF)              GET PHASE 1 ADDRESS    @YM02941
         STCM  RF,SEVEN,CCWADDR         BUILD THE CCW          @YM02941
*                                       OF THE PHASE
         MVI   CCWOPCDE,CCWIPL          SET CMD CODE           @Y17XARS
WRTPHAS  EQU   *
         MVC   CCWCOUNT,TWO(RF)         LNGTH FROM 2ND HALFWRD @YM02941
SENDITW  BAL   RE,EXCPRTN               SEND IT
         XR    R0,R0                    CLEAR FOR IGC0010D     @YM02941
         B     EXIT                     GET OUT                @YM02941
         SPACE 2
WRTIPL2  EQU   *
         SPACE 3
*    PHASE 1 JUST SENT; TURN OFF IPL BIT. REG 7 POINTS TO PHASE 1     *
*    1ST HALF WORD OF PHASE 1 HAS IT'S LENGTH. USE 1ST 512 BYTES FOR  *
*    OUTPUT BUFFER. ISSUE GETMAIN FOR 1028 BYTES FOR AN INPUT AND A   *
*    WORK BUFFER.                                                     *
         SPACE 3
         NI    SWITCH,ONES-ONIPL
         LA    RF,BUFFOFF(R7)           POINT TO TEXT BUFFER   @XM05687
         ST    RF,OUTBUF(R5)            SAVE ADDR OF OUTPUT    @YM02941
*                                       BUFFER IN L-WKAREA
         SPACE  3
         BAL   RE,GETCORE               GET 1028 BYTES FOR AN INPUT
*                                       BUFFER
         ST    R8,INBUF(R5)             SAVE ADDR OF INPUT BUFFER
*                                       IN L-WKAREA
         BAL   RE,GETCORE               GET 1028 BYTES FOR A WORK
*                                       BUFFER
         ST    R8,WORKBUF(R5)           SAVE ADDR OF WORK BUFFER
*                                       FOR RELOCATE
         SPACE  3
         TM    ZERO(R5),BRNGUP          BRING-UP SELECTED
         BNO   WRTIPL3                  NO
         SPACE  3
XCTLME   EQU   *                        PREPARE TO XCTL TO IGCME10D
         SPACE 3
         MVC   OPCMODID,XCTLINPT        XCTL TO IFLINPUT TO    @Y17XARS
*                                       GET 1ST RECORD FOR BRING-UP
*                                       BLDL AND FIND HAVE ALREADY BEEN
*                                       DONE. BUT WILL HAVE TO XCTL
*                                       BACK TO 1ST LOAD TO DO BLDLAND
*                                       FIND FOR THE L.M.
         LA    R0,ONE                   INSERT 1 FOR IGCME10D
         B     EXIT                     PREPARE TO XCTL
         SPACE  3
WRTIPL3  EQU   *                        NO - SEND PHASE 2
         L     RF,PHASPTR                                      @YM02941
         L     RF,FOUR(RF)              SET UP TO SEND         @YM02941
         STCM  RF,SEVEN,CCWADDR         PHASE 2                @YM02941
         MVI   CCWOPCDE,CCWBRK          WRITE BREAK PHASE 2    @Y17XARS
         B     WRTPHAS                  GO SET LENGTH AND SEND IT
         SPACE
         SPACE
WRTIPL4  EQU   *
         NI    ZERO(R5),XFA             TURN OFF INITIAL TEST REQ.
WRITIPL5 EQU   *
         SPACE 5
         SPACE 5
         MVI   SWITCH,ZERO              CLEAR SWITCHES
         NI    SWITCH2,ONES-DIAGOK-COMPLETE
         MVC   OPCMODID,XCTLMD10        XCTL TO 1ST LOAD       @Y17XARS
*                                       AND DO A BLDL AND FIND FOR L.M.
         L     R3,OPCCOPCE              GET OPCE OR L-WKAREA
         L     R1,OCTRUNIT              TRAILER ADDRESSABILITY @Y17XARS
         OI    OCTRSW-IEDOCTR(R1),OCTRINMF SET MD INPUT FLAG   @Y17XARS
         B     EXIT                     PREPARE TO EXIT
         SPACE 4
LASTTXT  EQU   *
         L     RE,REGSAVE(R5)           OUTPUT BUFFER END ADDR @YM03680
         SH    RE,OUTBFSZ               ADJUST TO BEGINNING    @YM03680
         STCM  RE,SEVEN,CCWADDR         SET UP CMD ADDRESS     @YM03680
         MVI   CCWOPCDE,CCWBRK          SET WRITE BREAK CMD    @Y17XARS
         SR    R4,R4                    CLEAR IT
         L     R4,LENSAVE(R5)           GET TEXT LEN SAVED BY
*                                       IGCMG10D
         SH    R4,OUTBFSZ               COUNT.
         LCR   R4,R4
         CH    R4,N4H                   LESS THAN 4 BYTES LEFT ?
         BNL   LASTTXT2                 NO - CONTINUE
         LA    R4,FOUR                  YES - SET 4 AS A COUNTER
LASTTXT2 EQU   *
         STH   R4,CCWCOUNT                                     @Y17XARS
         B     SENDITW                  GO SEND IT
         SPACE 2
WRTBRK   EQU   *
         SPACE 4
         L     RF,PHASPTR                                      @YM02941
         CLC   CCWADDR,FIVE(RF)         PHASE 2 WRITE BREAK ?  @YM02941
         BNE   WRTBRK2                  NO - TEST FOR B.U.
         TM    SWITCH2,DIAGOK           AFTER BRING-UP "
         BO    WRTIPL4                  YES
         B     XCTLME                   PREPARE TO EXIT
         SPACE
WRTBRK2  EQU   *
         CLI   DIAGIND,CHARN            BRING-UP JUST LOADED ?
         BNE   SETTIM                   YES - GO TO TIME DELAY
         LA    R9,ENTRYPT               SET UP CCW
         STCM  R9,SEVEN,CCWADDR         FOR SENDING            @Y17XARS
         MVI   CCWOPCDE,CCWWRITE        SENDING THE ENTRY      @Y17XARS
         MVI   CCWFLAGS,AVTEZERO        NO CHAINING            @XM05712
         LA    R9,FOUR                  LENGTH OF 4
         STH   R9,CCWCOUNT              INSERT IN CCW          @Y17XARS
         OI    SWITCH2,COMPLETE         INDICATE THE END
         B     SENDITW                  GO SEND IT
GETCORE  EQU   *
         L     R8,OPCOPTLK              ADDR OF GETMAIN LIST IN CA
         MVC   ONE(THREE,R8),INBUFSZE   SIZE OF INPUT BUFFER
         LA    R1,OPCDOUBL              PLACE TO PUT ADDR. CORE
         ST    R1,FOUR(R8)              PUT THIS ADDR IN PARALIST
         LR    R1,R8                    ADDR OF PARALIST IN REG 1
         GETMAIN   ,MF=(E,(1))
         L     R8,OPCDOUBL              ADDRESS OF GETMAIN AREA
         LTR   RF,RF                    WAS GETMAIN SUCCESSFUL
         BCR   8,RE                     YES, RETURN TO CALLER
         SPACE 2
         LA    R0,MSG164                ERROR MESSAGE NUMBER
         L     R3,OPCCOPCE              GET ELEMENT
TOTE     EQU   *
         XC    OCWTG,OCWTG              CLEAR WTG FIELD
         B     CLEANUP                  CLEANUP PROCESSING
SETERROR EQU   *
         LA    R0,IOERRMSG              I/O ERROR MSG NUMBER
SETEXIT  EQU   *                                               @Y17XARS
         L     R3,OPCCOPCE              OPCE ADDRESSABILITY    @Y17XARS
         USING IEDOCTR,R9                                      @Y17XARS
         L     R9,OCTRUNIT              TRAILER ADDRESSABILITY @Y17XARS
         OI    OCTRRTN,OCTRERI          IPL ERROR FLAG         @Y17XARS
         B     CLEANUP                  SET NEXT XCTL MOD ID   @YM03684
         DROP  R9
CC3ERR   EQU   *                                               @Y17XARS
         LA    R0,MSG534                CONDITION CODE 3 ERROR @Y17XARS
         B     SETEXIT                  EXIT TO IGCMI10D       @Y17XARS
UEERR    EQU   *                                               @Y17XARS
         LA    R0,MSG536                ASSUME ERROR ON SENSE  @Y17XARS
         ICM   RB,7,LCBCSWCA            CCW ENDING ADDRESS     @Y17XARS
         SH    RB,LENCCW                CCW BEGINNING ADDRESS  @Y17XARS
         CLI   CCWOPCDE,CCWSENSE        IS THIS UE ON SENSE    @Y17XARS
         BE    SETEXIT                  YES, ISSUE MSG 536     @Y17XARS
         LA    R0,MSG535                NO,ISSUE MSG 535       @Y17XARS
         B     SETEXIT                                         @Y17XARS
         SPACE 5
ENDIPL   EQU   *                                               @XM05712
         L     R9,LCBCSWCA-1           INTERRUPT CCW ADDR+8    @XM05712
         SH    R9,LENCCW                INTERRUPT CCW ADDRESS  @XM05712
         CLI   CCWOPCDE-IEDQCCW(R9),CCWNOP THIS NOP CCW        @XM05712
         BE    ENDOF                    BR YES,ALL DONE        @XM05712
         AH    R9,LENCCW                POINT TO NOP CCW       @XM05712
         ST    R9,LCBSTART-1            SET TO RESTART         @XM05712
         B     SENDITW                  RESTART                @XM05712
ENDOF    EQU   *                        IPL IS COMPLETE PREPARE TO
*                                       WRITE OUT MSG. AND INITIATE
*                                       3705 ACTIVITY
         L     R3,OPCCOPCE              OPCE ADDRESSABILITY    @Y17XARS
         MVI   OCSWITCH,OCIPLC          SAYS IPL COMMAND       @Y17XARS
         LA    R0,MSG194                MSG NUMBER FOR TOTE    @Y17XARS
         TM    OCFLAG,OCTOTE            TOTE REQUEST
         BO    TOTE                     SET UP TO GO TO IGCMI10D
         SR    R0,R0                    ZERO R0 FOR INDICATOR  @Y17XARS
         STH   R0,OCPARM                SAVE MESSAGE NUMBER
         EJECT
***********************************************************************
*                                                                     *
*    THIS SECTION OF CODE, AFTER AN IPL                               *
*    FREEMAINS THE TWO WORKAREA THAT WERE GETMAINED FOR BY IGCMF10D   *
*                                                                     *
***********************************************************************
*                                                                     *
CLEANUP  EQU   *
         MVC   OPCMODID,XCTLMI          INSERT ID OF IGCMI10D  @YM03684
         LR    RA,R0                    SAVE THE MESSAGE NUMBER
         SPACE 3
         LA    R1,INBUF(R5)             ADDR OF ADDR TO FREE
         ICM   R7,7,ZERO(R1)            AN AREA TO FREE
         BZ    TRYNEXT                  NO - GET NEXT ONE
         BAL   RE,FREECORE              BRANCH TO FREE IT
TRYNEXT  EQU   *
         LA    RE,EXIT                  LOAD RETURN POINT
         LA    R1,WORKBUF(R5)           ADDR OF ADDR TO FREE
         ICM   R7,7,ZERO(R1)            AN AREA TO FREE
         BCR   8,RE                     NO
         SPACE 3
FREECORE EQU   *
         L     R8,OPCOPTLK              GET FREEMAIN LIST IN IEDQCA
         MVC   ONE(THREE,R8),INBUFSZE   SIZE OF AREA TO FREE
         ST    R1,FOUR(R8)              PUT THIS ADDR IN PARALIST
         LR    R1,R8                    ADDRESS OF PARALIST IN REG1
         FREEMAIN  ,MF=(E,(1))
         LR    R0,RA                    PUT MSG NUM BACK IN REG0
         BR    RE                       RETURN TO CALLER
         SPACE 3
         EJECT
         SPACE 4
SETTIM   EQU   *
         SPACE  3
*    TPOST SPECIAL TIME DELAY ELEMENT FOR 30 SECONDS TO TIME          *
*    DELAY QCB WHOSE ADDR IS IN THE AVT                               *
         SPACE  3
*    WAIT ON BRING-UP TO COMPLETE PROCESSING. GET A BUFFER UNIT       *
*    FROM IEDQCA; BUILD A TIME DELAY REQUEST ELEMENT AND TPOST IT     *
*    TO THE TIME DELAY QCB REQUESTING A 30 SEC. DELAY RELINQUISH CTRL *
         SPACE  3
         USING IEDQAVTD,R4              ADDRESSABILITY FOR AVT
         L     R4,OPCAVTPT              BASE FOR AVT
         LA    R1,ONE                   GET 1 UNIT
         LA    RD,OPCSAVE               ADDR OF REGISTER SAVEAREA
         L     RF,OPCGETBF              ADDR BUFFER REQUEST RTN
         BALR  RE,RF                    BRANCH TO IT
         LA    R7,AVTDELYB              ADDR TIME DELAY QCB
         USING IEDQQCB,R1               ADDRESSABILITY FOR QCB
         ST    R7,QCBELCHN-ONE          INSERT TIME DELAY QCB ADDR
         MVI   QCBPRI,TIMELEM           PRIORITY IS 70
         SPACE 5
         MVC   QCBEOLDT,TIME2           TIME TO DELAY FOR PART 1
*                                       AND PART 2  INITIAL TEST
         LA    R7,AVTOPCOB              OPC QCB ADDR
         ST    R7,OFFSET(R1)            STORE IN TIME DELAY ELEMENT
         L     R3,OPCCOPCE              GET ADDR OF PASSED ELEMENT
         ST    R3,ELEMSAVE(R1)          SAVE IT IN TIME DELAY ELEM
         MVI   QCBRETCT,OFFSET          PUT OFFSET TO OPC QCB
         MVI   QCBTSOF1,ZEROS           ZERO OUT RECYCLE FLAG BYTE
*                                       INITIAL TEST
         LA    R8,POSTWAIT
         SPACE 3
*        TPOST ELEMENT TO THE DISABLED READY QUEUE                    *
         SPACE 3
POSTIT   EQU   *
         LA    RB,OPCAQCTL              SVC 102 POST LIST
         ST    R1,ZERO(RB)              INIT LIST
         ST    R1,FOUR(RB)              INIT LIST
         MVI   ZERO(RB),X0C             INDICATE POST OF ELEM
         MVI   FOUR(RB),X80             INDICATE LAST ENTRY
         LR    R1,RB                    ADDR OF POST LIST
         AQCTL                          POST ELEMENT
         LA    RB,LCBCCW3               RESTORE CCW POINTER    @Y17XARS
         BR    R8                       BRANCH TO PREDETERMINED CODE
         SPACE 3
*    SET UP TO RELINQUISH CONTROL BACK TO IGC0010D             @YM02941
         SPACE 3
POSTWAIT EQU   *
         MVC   OPCMODID,XCTL00          INSERT ID OF IGC0010D  @Y17XARS
         XR    R0,R0                    CLEAR FOR 0010D        @YM02941
         SPACE 5
EXIT     EQU   *
         LR    R1,R2                    SET PARAMETER REGISTER
         XCTL  SF=(E,OPCXCTL)
         SPACE 2
         SPACE
         SPACE  3
*    THIS SECTION OF CODE RE-INITIALIZES REGISTER 5 AND 6  AFTER      *
*    THE 30 SEC TIME DELAY FOR INITIAL TEST PROCESSING HAS EXPIRED    *
         SPACE  3
TIMESUP  EQU   *
         USING IEDQOPCE,R3              ADDRESSABILITY FOR OPCE OR
         LR    R1,R3                    SAVE BUFR POINTER      @YM02941
         L     R3,ELEMSAVE(R3)          GET ADDR OF SAVED ELEMENT
         ST    R3,OPCCOPCE              RE-INITIALIZE CURRENT ELEMENT
         OI    QCAFLAG(R1),ENDCHN       LAST BUFFR TO FREE     @YM02941
         L     RF,OPCFREBF              ADDR OF FREEBUF RTNE   @YM02941
         BALR  RE,RF                    GO FREE THE BUFFER     @YM02941
         USING IEDCOM,R7                COMMON AREA ADDRESSABILITY
         L     R7,OCMODNME              GET COMMON AREA ADDRESS
         USING IEDOCTR,R6                                      @Y17XARS
         L     R6,OCTRUNIT              TRAILER ADDRESSABILITY @Y17XARS
         L     RA,OCTRLCB               LCB ADDRESSABILITY     @Y17XARS
         DROP  R6                                              @Y17XARS
         USING DATADS,R6                ADDRESSABILITY FOR COM @Y17XARS
*                                       WORKAREA               @Y17XARS
         LA    R5,COMCCWS               GET ADDRESS OF LWKAREA
         LA    R6,DATAOFF(R5)           SET BASE FOR C-WKAREA
         LA    RB,LCBCCW2               CHAN PROG CCWS         @YM04661
         SPACE 5
         CLC   MEMBER(EIGHT),DIAGMEM+EIGHT SENT PART 2 OF INITIAL TEST
         BNE   SENT1ST                  NO SENT PART 1
         OI    SWITCH2,DIAGOK           PART 2 JUST LOADED SWITCH
         MVI   DIAGIND,AVTEZERO         CLEAR INITIAL TEST REQUEST
         NI    ZERO(R5),XFA             TURN OFF INITIAL TEST REQ.
         B     WRTIPL3                  PREPARE TO SEND PHASE 2
SENT1ST  EQU   *
*                                       PART OF INITIAL TEST TO RUN
         MVI   DIAGIND,CHARN            INDICATE TO KEEP LOADING
*                                       INITIAL TEST
         B     WRITIPL5                 CONTINUE
         SPACE 5
         SPACE 3
         SPACE 4
         EJECT
***************************** LOCAL DATA & DSECTS **************
         SPACE
BUFFOFF  EQU   1536                     OFFSET IN WKARE TO BUF @XM05687
WRTCC    EQU   X'01'                    CCW COMMAND CODES
IPLCC    EQU   X'05'
WRTBRKCC EQU   X'09'
SENCC    EQU   X'04'
         SPACE
IPLREQ   EQU   X'02'                    IPL REQ. SENSE BIT
XCTLMH   DC    C'MH'                    ID OG IGCMH10D RTN
INBUFSZE DC    X'000408'                BYTES TO GETMAIN FOR
POSTED   EQU   X'40'                    ECB POSTED INDICATOR
BRNGUP   EQU   X'05'                    BRING-UP REQUESTED
DIAGMEM  DC    C'IFL3705D'              INITIAL TEST PART 1
         DC    C'IFL3705E'              INITIAL TEST PART 2
         SPACE 5
TIME2    DC    H'0030'                  TIME TO WAIT FOR INITIAL TEST
*                                       TO RUN
         SPACE 5
LENSAVE  EQU   100                      OFFSET IN L-WK TO LENGTH OF
*                                       TEXT SAVED BY IGCMG10D
REGSAVE  EQU   X'60'
MSG164   EQU   164                      CORE NOT AVAIL. FOR IPL
MSG534   EQU   534                      3705 DISABLED          @Y17XARS
MSG535   EQU   535                      NCP TOO LARGE          @Y17XARS
MSG536   EQU   536                      IN PROGRESS ON OTHER CA@Y17XARS
IOERRMSG EQU   168                      I/O ERROR ENCOUNTERED
ELEMSAVE EQU   24                       OFFSET IN TIME DELAY ELEMENT TO
*                                       WHERE ADDR OF OUR PASSED ELEM
X0C      EQU   X'0C'                    TO SET SVC 102 LIST
X80      EQU   X'80'                    TO SET SVC 102 LIST
*                                       OPCE OR L-WKAREA RESIDES.
LCBSAVE  EQU   X'58'                    OFFSET IN L-WK TO SAVEAREA
*                                       FOR 3705 LCB
WAITING  EQU   X'40'                    WAIT STATE BIT
ZEROS    EQU   X'00'                    ZERO OUT RECYCLE FLAG BYTE
DELAY    EQU   X'32'                    OFFSET IN WKAREA FOR
*                                       TIME TO WAIT
XCTL00   DC    C'00'                    ID OF IGC0010D RTN.
XFE      EQU   X'FE'                    MASK TO TURN OFF READ REQ.
X10      EQU   X'10'                    OFFSET TO DATA
XFA      EQU   X'FA'                    MASK TO TURN OFF
X08      EQU   X'08'                    OFFSET TO READ DATA FROM
*                                       PRFBTUA
LENCCW   DC    AL2(CCWLEN)              CCW LENGTH             @Y17XARS
READSZE  DC    H'32'                    NUM OF BYTES TO READ
MSG194   EQU   194                      IPL SUCCESS MSG
CLNUPPRI EQU   X'88'                    CLEAN UP INDICATOR TO MH
AUTOIPL  EQU   X'DF'                    PRIORITY OF AUTO-IPL ELEMENT
XCTLMY   DC    C'MY'                    ID OF 3705 ACTIVATE
XCTLMI   DC    C'MI'                    ID OF IGCMI10D ROUTINE
READCC   EQU   X'02'                    READ COMMAND CODE
IPLED    EQU   X'08'                    BIT IN 3705 DCB TO INDICATE
*                                       THAT THIS 3705 IS IPLED
IPLPRIO  EQU   X'DD'                    OBR DATA FROM A READ IN IPL
TIMELEM  EQU   70                       PRIORITY OF TIME DELAY ELEM.
OFFSET   EQU   X'08'                    OFFSET IN TIME DELAY REQ. ELEM
*                                       TO OPC QCB ADDR
***************************************************************
         SPACE 2
OUTBUF   EQU   28                       ADDR OF OUTBUF IN L-WKAREA
INBUF    EQU   32                       ADDR OF INPUT BUFFER IN L-WKAR
WORKBUF  EQU   36                       ADDR OF WORK BUFFER IN L-WKAREA
FOURTY   EQU   40
DATAOFF  EQU   184                      OFFSET TO START OF C-WKAREA
IGC06    DC    C'06'                    CHKPT MODULE ID
NOTFIRST EQU   X'80'                    INDICATES TO MD THIS IS A
*                                       SUBSEQUENT ENTRY AND BLDL
*                                       AND FIND FOR L.M. IN NEEDED
XCTLINPT DC    C'ME'
XCTLMD10 DC    C'MD'                    MODULE ID FOR IGCMD10D
XCTLOUT  DC    C'MG'                    ID OF OUTPUT RTN.
         EJECT
*****************  I/O BUFFERS AND WORK AREAS  *****************
         SPACE  3
BUFFER   DSECT
         DS    257F                     TEXT AREA
INBUF1   DSECT
         DS    257F                     INPUT BUFFER
OUTBUF1  DSECT
         DS    256H                     TEXT AREA
         SPACE
REPLYD   DSECT
REPLY    DS    X
         DS    AL3
ECBRR    DS    AL4
         SPACE 4
         SPACE 2
         EJECT
DATADS   DSECT
****************************************************************
*        REGISTER AND NUMBER EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         SPACE
         SPACE
ZERO     EQU   0
ONE      EQU   1
TWO      EQU   2
THREE    EQU   3
FOUR     EQU   4
FIVE     EQU   5
SIX      EQU   6
SEVEN    EQU   7
EIGHT    EQU   8
NINE     EQU   9
TEN      EQU   10
ELEVEN   EQU   11
TWELVE   EQU   12
         SPACE 2
****************************************************************
**    MISCELLANEOUS  EQUATES  AND CONSTANTS                   **
         SPACE
BIT10    EQU   X'10'          TEST FOR OPEN IN DCB
SIOCODE  EQU   X'30'          SIO ENTRY FOR 3705 STARTIO       @ZM46810
QCAFLAG  EQU   11                                              @YM02941
ENDCHN   EQU   3                                               @YM02941
IFLGSD   EQU   49             DCB IFLGS DISPLACEMENT
NOERRTN  EQU   X'0C'          INDICATE NO SYS ERROR RTNS.
UCBCA    EQU   19             UCB DISP OF CA INDICATOR.
MINICA   EQU   X'15'          MINI CA INDICATOR IN UCB.
         SPACE
ABORTVAL EQU   12             ABORT RET CODE THRESHOLD.
RETCODE  DC    H'0'           RETURN CODE.
MAXRC    DC    H'0'           MAXIMUM RETURN CODE SET.
RNDDN    DC    C'        '    RN DDNAME
RNADR    DC    C'   '         UNIT ADDRESS AREA.
ENTRYPT  DC    F'0'           ENTRY POINT FOR LOAD.
ARADDR   EQU   12             DECB AREA ADDR. DISP.
RNSWITCH EQU   X'14'          DISP OF SWITCH IN DIAG PHASE 1.
SIXTBIT  EQU   X'01'          SIXTEEN BIT INDICATOR.
ALIASIND EQU   X'80'          THIS IS AN ALIAS-INDICATOR.  BLDL
RENREUS  EQU   X'C0'          REENTRANT & REUSABLE         BLDL
ABORT    EQU   X'03'          ABORT SENSE INDICATORS.
         DS    0H
TXTLNGTH DC    H'0'           LENGTH OF TEXT RECORD
         DS    0F
TOTLNGTW DC    AL1(0)         TOTAL LENGTH OF DATA
TOTLNGTH DC    AL3(0)         TRANSFERRED
         DS    0F
TXTADDRW DC    AL1(0)         ADDRESS (REL TO ZERO) OF
TXTADDR  DC    AL3(0)         THIS TEXT RECORD.
         SPACE
N256HW   DC    H'256'         MAXIMUM MVC COUNT
INBUFSZ  DC    H'1024'        SIZE OF INPUT BUFFER
OUTBFSZ  DC    H'512'         SIZE OF OUTPUT BUFFER.
DOUBLE   DC    D'0'           DOUBLE WORD WORK AREA
LINECNT  DC    H'0'           LINE COUNT FOR OUTPUT
MAXLIN   DC    H'52'          MAXIMUM LINES PER PAGE.
N4H      DC    H'4'           FOUR CONSTANT
         SPACE
**   BLDL  LIST  FOR  LOAD  MODULE  DATA  SET                 **
         DS    0H
BLDLIST  DC    H'1'           ONE ENTRY IN THIS LIST.
         DC    H'58'          EACH ENTRY IS 58 BYTES LONG
MEMBER   EQU   *              MEMBER NAME PLACE HERE.
         DS    CL8
RALTTRK  DC    4X'00'         USED IN FIND
*
Z        DC    X'00'          FLAG BYTE
C        DC    X'00'          FLAG BYTE
         ORG   MEMBER+22
ATTRIB   EQU   *              ATTRIBUTE FIELD
         ORG   MEMBER+29
EPAMEM   EQU   *              ENTRY PT IF MEMBER DIR.ENTRY.
         ORG   MEMBER+43
EPAALS   EQU   *              ENTRY PT IF ALIAS DIR.ENTRY.
         ORG   MEMBER+58
         SPACE
MAXIVT   EQU   *
         DC    V(CXWMAXI1)         ADDRESS OF MAXI PHASE 1
         DC    V(CXWMAXI2)         ADDRESS OF MAXI PHASE 2..
         SPACE
MINIVT   EQU   *
         DC    V(CXWMINI1)         ADDRESS OF MINI PHASE 1
         DC    V(CXWMINI2)         ADDRESS OF MINI PHASE 2..
         SPACE
PHASPTR  DC    A(MAXIVT)           PHASE VECTOR TABLE PTR..
PHASCNT  DC    H'8'                NUMBER OF PHASES TIMES FOUR..
MAXIPH1  DC    V(CXWMAXI1)         ADDRESS OF MAXI PHASE 1.
MINIPH1  DC    V(CXWMINI1)         ADDRESS OF MINI PHASE 1.
MAXIBUPH DC    CL8'IFL3705B'       NAME OF DIAG MAXI PHASE 1.
MINIBUPH DC    CL8'IFL3705A'       NAME OF DIAG MINI PHASE 1.
         SPACE
*                               *                              *
****************************************************************
***      SWITCHES AND TEST EQUATES                            **
         SPACE
SWITCH   EQU   *         * SWITCH BYTE *
         SPACE
SW1      EQU   *          INDICATES ONE PARM PROCESSED
ON1      EQU   X'80'          TURN ON SW1
OFF1     EQU   X'7F'          TURN OFF SW1
         SPACE
TXTSW    EQU   *            INDICATES TEXT RECORD IN NEXT BUFFER
ONTXT    EQU   X'40'          TURN ON TXTSW
OFFTXT   EQU   X'BF'          TURN OFF TXTSW
         SPACE
LASTSW   EQU   *            INDICATES LAST TEXT RECORD
ONLAST   EQU   X'20'          TURN ON LASTSW
         SPACE
ENDIN    EQU   *            INDICATES INPUT BUFFER IS EMPTY.
ONEND    EQU   X'10'          TURN ON ENDIN.
OFFEND   EQU   X'EF'          TURN OFF ENDIN.
         SPACE
IPLSW    EQU   *            INDICATES BOOTSTRAP IS TO BE SENT
ONIPL    EQU   X'08'          TURN ON IPLSW.
OFFIPL   EQU   X'F7'          TURN OFF IPLSW.
         SPACE
IOPEND   EQU   *            INDICATES AN EXCP IS OUTSTANDING.
ONIO     EQU   X'04'          TURN ON IOPEND
OFFIO    EQU   X'FB'          TURN OFF IOPEND
         SPACE
ERRENT   EQU   *            INDICATES ERROR RTN HAS BEEN ENTERED
ONENT    EQU   X'02'          TURN ON ERRENT.
         SPACE
BUFFERSW EQU   *                 INDICATES PRIMING OF BUFFERS..
BUFSW    EQU   X'01'
         SPACE
ONES     EQU   X'FF'          USED IN SWITCH MANIPULATIONS.
         SPACE
         DC    AL1(0)      * ACTUAL SWITCH BYTE *
         SPACE 2
SWITCH2  EQU   *
CASW     EQU   *
         SPACE
LOADFLG  EQU   X'80'          LOADMOD KEYWORD PROCESSED.
UNITFLG  EQU   X'40'          UNIT    KEYWORD PROCESSED.
DIAGFLG  EQU   X'20'          DIAG    KEYWORD PROCESSED.            BUP
DIAGOK   EQU   X'10'          BRING-UP COMPLETED INDICATOR.         BUP
ONCA     EQU   X'01'               ON INDICATING MINI CA.
COMPLETE EQU   X'02'          LAST WRITE JUST ISSUED.
NXTPHS   EQU   X'04'          NEXT PHASE REQUEST.
         SPACE
         DC    AL1(0)          * ACTUAL SWITCH2 BYTE *
         SPACE
DIAGIND  DC    C'Y6'              INITIAL-TEST INDICATOR.
DIAGDEF  DC    C'Y6'               DEFAULT DIAG VALUE.
CHARN    EQU   C'N'           NO BRING-UP INDICATOR.                BUP
         SPACE 2
WARN     DC    AL1(0)        BYTE USED FOR WARNING INDICATORS
RNFULL   EQU   X'80'            RN IS FULL WARNING
         SPACE
GOODCODE EQU   X'7F'          NORMAL ECB COMPLETION CODE.
PERMERR  EQU   X'41'          PERMANENT ERROR ECB CODE.
         SPACE 2
*****************  CCW'S AND RELATED EQUATES  ******************
RNCCW    DS    0D             CHANNEL COMMAND WORDS
         DC    AL1(0)           COMMAND CODE
RNCCWAD  DC    AL3(0)           DATA ADDRESS
         DC    X'60'            FLAGS
         DC    AL1(0)             --
RNCCWLN  DC    AL2(1025)        COUNT FOR WRITE
         SPACE
NOPCCW   DS    0D             NOP CHANNEL COMMAND IS
         DC    AL1(3)           CHAINED TO THE NORMAL
         DC    AL3(*)           COMMANDS TO ENSURE
         DC    X'20'            DEVICE END IS RETURNED
         DC    AL3(1)           BEFORE ECB POSTED.
         SPACE
RNEND    EQU   X'C0'        * TEMPORARY - WILL NEED TO BE *
*                               MORE EXTENSIVE.
****************************************************************
         SPACE
**********  CONTROL BLOCKS FOR ASSOCIATED DATA SETS  ***********
         SPACE
ECBRN    DC    F'0'       **  ECB FOR EXCP'S TO 27RN.
         SPACE
IOBRN    DS    0F         **  IOB FOR EXCP'S TO 27RN.
         DC    X'42'         -COMMAND CHAINING & NOT RELATED.
         DC    AL1(0)        -FLAGS
IOBSENSE DC    AL2(0)        -SENSE BYTES
         DC    AL1(0)        -ECB CODE
         DC    AL3(ECBRN)    -ECB ADDRESS
         DC    AL1(0)        -FLAGS
         DC    AL3(0)        -CSW COMMAND ADDRESS.
IOBCSWS  DC    AL2(0)        -CSW STATUS BYTES
IOBCSWC  DC    AL2(0)        -CSW BYTE COUNT.
         DC    AL1(0)        -SIO CODE
         DC    AL3(RNCCW)    -CHANNEL PROGRAM ADDRESS.
         DC    AL1(0)        -RESERVED
         DC    AL3(0)                   DCB ADDRESS
         DC    AL1(0)        -REPOSITION MODIFIER
         DC    AL3(0)        -RESTART ADDRESS.
         DC    AL2(0)        -BLOCK COUNT INCREMENT N/A.
         DC    AL2(0)        -ERROR COUNTS.
         DC    AL1(0)        -EXTENT 0 OF DEB.
         DC    AL3(0)        -BBCCHHR
         DC    AL4(0)        -(SAME)
* END OF IOB DEFINITION  ***************************************
         EJECT
         TCOMD                                                 @Y17XARS
         TCOMIPLD                                              @Y17XARS
         EJECT
         TCCWD
         EJECT
         TOCTRD
         EJECT
         TAVTD
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
         EJECT
         TPRIOR                                                @Y17XARS
         EJECT                                                 @Y17XARS
         TQCBD
          EJECT
         TDISPD
         EJECT
         TLCBD
         EJECT
         DCBD  DSORG=(TR)
         EJECT
         TPRFD
         END
