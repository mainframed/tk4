         TITLE 'IGCD610D - DISPLAY ADDR'
IGCD610D CSECT
**A000000-999999                                                 S99240
*C001050,001476,001872,001944,001350                           @Y17XART
***********************************************************************
*                                                                     *
* MODULE NAME = IGCD610D (TCAM, OPERATOR CONTROL)              @Y17XARX
*                                                              @Y17XARX
* DESCRIPTIVE NAME = PROCESSES DISPLAY ADDRESS COMMANDS        @Y17XARX
*                                                              @Y17XARX
* COPYRIGHT = NONE                                             @Y17XARX
*                                                                     *
* STATUS = VERSION 10.0                                        @Y17XARX
*                                                                     *
*FUNCTION -- THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH   *
*   REQUEST DISPLAY OF THE LINE ADDRESS AND RELATIVE LINE NUMBER      *
*   FOR A SPECIFIED TERMINAL OR GENERAL POLL CLUSTER. FOR 3705 @Y17XARX
*   PU'S & LU'S ARE ALSO VALID INPUT.                          @Y17XARX
*                                                                     *
*   WHEN ENTERED THE TERMINAL SPECIFIED IN THE COMMAND HAS BEEN FOUND *
*   VALID AND IT HAS BEEN INSERTED IN THE OPCAVT AT OPCDOUBL          *
*                                                                     *
*   THE NAME IN OPCDOUBL IS MOVED INTO THE MESSAGE WHICH IS BEING     *
*   BUILT IN THE OPERATOR CONTROL RESIDENT WORK AREA IN IEDQCA.       *
*   NEXT THE TERMINAL ENTRY ADDRESS IS OBTAINED FROM OCELEM IN THE    *
*   OPERATOR CONTROL ELEMENT (OPCE). IF THE ENTRY IS A                *
*   A PROCESS ENTRY (X'20' ON IN TRMSTATE) THE ENTRY HAS NO LINE      *
*   INFORMATION.  A MESSAGE SO STATING IS BUILT AND THE ROUTINE       *
*   RETURNS TO THE MESSAGE WRITER ROUTINE IGC0710D                    *
*                                                                     *
*   THE QCB ADDRESS IS OBTAINED FROM THE TERMINAL ENTRY (TRMDESTQ),   *
*   AND THE RELATIVE LINE NUMBER (QCBRELLN) IS OBTAINED, CONVERTED    *
*   TO PRINTABLE EBCDIC, AND STORED IN THE GOOD RESPONSE.  THE DCB    *
*   ADDRESS IS OBTAINED (QCBDCBAD) AND TESTED FOR A LINE OPEN         *
*   CONDITION.  IF THE LINE IS NOT OPEN, A NOT OPEN MESSAGE IS        *
*   BUILT AND CONTROL RETURNS TO THE ERROR WRITER ROUTINE IGC0310D.   *
*   THE LINE IS THEN TESTED FOR OPEN DD DUMMY (DEBUCBAD IS ZERO,DEB   *
*   OBTAINED FROM DCBDEBAD). IF SO, THE LINE NOT OPEN MESSAGE IS      *
*   GENERATED BY XCTCING TO IGC0310D.                                 *
*                                                                     *
*   IF THE LINE IS OPEN, THE UCB ADDRESS IS PLACED IN THE GOOD        *
*   RESPONSE, THE TCB ADDRESS IS OBTAINED FROM THE AVT (AVTTCB) AND   *
*   USED TO ACCESS THE TIOT ADDRESS AT TCB+12.  THE DCBTIOT VALUE     *
*   IS ADDED TO THE TIOT START AND THE DDNAME FIELD IS MOVED FROM     *
*   THE RESULTING LOCATION INTO THE RESPONSE. FOR 3705'S, THE  @Y17XARX
*   UCB ADDRESS OF THE NCP WILL BE PRINTED ALONG WITH THE      @Y17XARX
*   GROUP NAME AND RLN. FOR LU'S, THE PU NAME WILL BE ADDED TO @Y17XARX
*   THE MESSAGE. THE ROUTINE EXITS TO THE MESSAGE WRITER       @Y17XARX
*   IGC0710D.                                                  @Y17XARX
*                                                              @Y17XARX
* NOTES = COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES @Y17XARX
* IS PRIMARILY THROUGH THE OPERATOR CONTROL AVT.               @Y17XARX
*                                                              @Y17XARX
*    DEPENDENCIES = EBCDIC CHARACTER CODE DEPENDENCIES ARE     @Y17XARX
*    CORRECTABLE BY REASSEMBLY.                                @Y17XARX
*                                                              @Y17XARX
*    RESTRICTIONS = NA                                         @Y17XARX
*                                                              @Y17XARX
*    REGISTER CONVENTIONS = STANDARD OP CTL                    @Y17XARX
*                                                              @Y17XARX
*    PATCH LABEL = ZAPPATCH                                    @Y17XARX
*                                                              @Y17XARX
* MODULE TYPE = PROCEDURE                                      @Y17XARX
*                                                              @Y17XARX
*    PROCESSOR = ASSEMBLER                                     @Y17XARX
*                                                              @Y17XARX
*    MODULE SIZE = 930 BYTES                                   @Y17XARX
*                                                              @Y17XARX
*    ATTRIBUTES = SERIALLY REUSABLE,DISABLED,TRANSIENT(TYPE 4) @Y17XARX
*    SUPERVISOR MODE,NON-REENTRANT,REFRESHABLE                 @Y17XARX
*                                                                     *
*ENTRY POINTS --                                                      *
*        'IGCD610D' TO PROCESS DISPLAY ADDR OPERATOR CONTROL COMMANDS *
*                                                                     *
*INPUT-- REGISTER 1 - ADDRESS OF OPCAVT. AT THE LABEL OPCCOPCE IS THE *
*   ADDRESS OF THE OPERATOR CONTROL ELEMENT (OPCE). AT THE LABEL      *
*   OPCDOUBL IS THE DESIRED TERMINAL NAME.                            *
*   AT OCELEM IN OPCE IS THE ADDRESS OF THE TERMINAL ENTRY            *
*
*        OPCAVT                                                       *
*          OPCDOUBL - TERMINAL NAME                            @Y17XARX
*           OPCCOPCE - ADDRESS OF OPCE                         @Y17XARX
*           OPCWORK - ADDRESS OF RESIDENT WORKAREA             @Y17XARX
*                                                                     *
*        OPCE                                                         *
*          OCELEM - ADDRESS OF TERMINAL ENTRY                  @Y17XARX
*                                                                     *
*OUTPUT--REGISTER 1 - ADDRESS OF OPCAVT                               *
*        OPCAVT                                                       *
*           OPCWORK - ADDRESS OF RESIDENT WORKAREA             @Y17XARX
*        RESIDENT WORK AREA CONTAINS MESSAGE TO BE WRITTEN            *
*                                                                     *
*EXTERNAL ROUTINES -- N/A.                                            *
*                                                                     *
*EXITS,NORMAL--COMMAND IS PROCESSED. A RESPONSE MESSAGE IS BUILT IN   *
*   THE RESIDENT WORK AREA AND AN XCTL IS DONE TO IGC0710D            *
*                                                                     *
*   NORMAL EXIT MESSAGES ARE-                                         *
*   IED038I 'STATNAME' IS ON LINE 'DDNAME, RLN ADDRESS'        @Y17XARX
*   IED177I 'STATNAME' IS ON LINE 'GRPNAME, RLN' ON 3705       @Y17XARX
*        'UCBADDR' ( ON PU 'PUNAME' )                          @Y17XARX
*
*EXITS,ERROR-- COMMAND IS PROCESSED. AN ERROR MESSAGE IS GENERATED BY *
*   LOADING THE APPROPRIATE MESSAGE NUMBER IN REGISTER 0 AN XCTLING   *
*   TO IGC0310D THE ERROR WRITER.                                     *
*                                                                     *
*   ERROR MESSAGES ARE-                                               *
*   IED016I STATION 'STATNAME' NOT FOUND --GENERATED BY IGCD010D      *
*   IED090I 'STATNAME' IS NOT A SINGLE ENTRY                          *
*   IED091I LINE FOR 'STATNAME' NOT OPEN                              *
*   IED400I 'STATNAME' 3705 NOT ACTIVE COMMAND REJECTED.       @Y17XARX
*                                                                     *
*TABLES/WORK AREAS--OPCAVT,TCAM AVT,OPCE,DEB,DCB,QCB,TERMNAME TABLE,  *
*   TERMINAL ENTRY,RESIDENT WORK AREA IN IEOQCA                       *
*                                                                     *
* MACROS = DCBD, GETMAIN, XCTL                                 @Y17XARX
*                                                              @Y17XARX
*        THIS IS A MODULE RE-WRITE OF IEDQCG AS OF OS/TCAM LEVEL II   *
***********************************************************************
         EJECT
*        REGISTER EQUATES
RZERO    EQU   0                        REG ZERO                 S22024
R1       EQU   1                        PARAMETER
ROPC     EQU   2                        OPERATOR CONTROL AVT BASE
ROPCAVT  EQU   2                   OPC AVT ADDRESSABILITY
ROPCE    EQU   3                   OPC ELEM ADDRESSABILITY
RAVT     EQU   4                        AVT BASE
RWKAREA  EQU   5                   BASE FOR WORKAREA
RTERM    EQU   6                        TERMINAL ENTRY BASE
RQCB     EQU   7                        QCB BASE
RWORK2   EQU   7                        WORK
RDCB     EQU   8                        DCB BASE
RWORK3   EQU   8                        WORK
REGEX    EQU   9                   REGISTER USED FOR EXECUTES
RDEB     EQU   9                        DEB BASE
RSIDR    EQU   9                        RSID PTR                 S22024
RTNT     EQU   10                  TERMNAME TABLE BASE
RUCB     EQU   10                       UCB BASE
RWORK    EQU   11                       WORK
RBASE    EQU   12                       ROUTINE BASE
R13      EQU   13                       SAVE AREA
R14      EQU   14                       RETURN
R15      EQU   15                       ENTRY
RMSG     EQU   15                  MSG BASE REGISTER
         USING IEDQOPCD,ROPC       ADDRESSABILITY FOR OP CTL
         USING IEDQAVTD,RAVT            ADDRESSABILITY FOR AVT
         USING IEDNTRM,RTERM            ADDRESSABILITY FOR TERM@Y17XARX
         USING IEDQQCB,RQCB             ADDRESSABILITY FOR QCB
         USING IHADCB,RDCB              ADDRESSABILITY FOR DCB
         USING DEBNMSUB,RDEB            ADDRESSABILITY FOR DEB
         USING IEDQOPCE,ROPCE
         EJECT
         BALR  RBASE,ZERO               ESTABLISH
         USING *,RBASE             ADDRESSABILITY
IGCD610D IEDHJN SKIPID
         LR    ROPC,R1                  GET OP CTL AVT
         L     RAVT,OPCAVTPT       GET ADDR OF AVT
         L     RWKAREA,OPCWORK     GET ADDR OF WORKAREA
         LA    RMSG,FOUR(RWKAREA)  GET ADDR OF START OF MSG FIELD
         ST    RMSG,OCMODNME            SAVE REG 15            @Y17XARX
         L     ROPCE,OPCCOPCE      GET ADDR OF OPC CTRL ELEM
         L     RTERM,OCELEM        GET ADDR OF TERM ENTRY
         LA    RWORK,TRMPRFSZ           GET ABSOLUTE TTE LENGTH@Y17XARX
         SR    RTERM,RWORK              BACKUP TO NEG PART TTE @Y17XARX
         LA    RZERO,BADSTAT            SET UP ERROR MSG NO      S22024
         MVC   OPCLDNME+THREE(TWO),ERRWRIT INSERT ERROR ID       S22024
         TM    TRMSTATE,SINGLINE        SINGLE LINE              S22024
         BO    ERROR                    YES                      S22024
         TM    TRMSTATE,PROC+DIST       PROCESS, DIST OR CASCADE S22024
*                                       ENTRY                    S22024
         BNZ   PE                  GET MSG NUM & PREPARE EXIT  @Y17XARX
         TM    TRMDSORG,TRMLGB          LGB                      S22024
         BO    ERROR                    YES                      S22024
         TM    TRMSTATE,TRMPREF         CHECK IF XTND RESOURCE @Y17XARX
         BZ    OLDIRON                  NO,270X RESOURCE,BRANCH@Y17XARX
         ST    RTERM,OPCWRKSP      SAVE TTE ADDR FOR RNCMD5 RTN@Y17XART
         CLI   TRMTYPE,TRMLNCP          IS IT TTE FOR NCP      @Y17XARX
         BE    ERROR               NCP TTE IS INVALID INPUT    @Y17XARX
         TM    TRMSTATE,TRMLINE         IS THIS LINE ENTRY     @Y17XARX
         BO    ERROR               LINE INPUT INVALID, GO EXIT @Y17XARX
         CLI   TRMTYPE,TRMSSCP          IS IT TTE FOR SSCP     @Y17XARX
         BE    ERROR                    SSCP INPUT INVALID     @Y17XARX
         B     RNCMD0                   ALL OTHER 3705 INPUT   @Y17XARX
*                                       IS INVALID, GO PROCESS @Y17XARX
*                                       VALID 3705 INPUT       @Y17XARX
         SPACE 3
         SPACE
OLDIRON  EQU   *                                               @Y17XARX
         LA    R1,GOODMSG          GET ADDRESS OF GOODMSG
         B     MOVEAMSG                 TO MOVE MSG IN WORKAREA
         SPACE
RNCMD0   EQU   *
         LA    R1,RNMESG                GET ADDR OF RN GOOD MSG
MOVEAMSG SR    RWORK2,RWORK2       CLEAR REGISTER
         IC    RWORK2,LENOFF(R1)   GET LENGTH OF GOODMSG
         STH   RWORK2,LENOFF(RWKAREA) PLACE LENGHT IN WORKAREA
         BCTR  RWORK2,ZERO              DECREMENT FOR EXECUTE
         EX    RWORK2,MOVEMSG      MOVE MSG TO OPC WORKAREA
*
         SPACE 3
         L     RQCB,TRMDESTQ-ONE        GET QCB ADDRESS
         SR    RWORK,RWORK              CLEAR
         IC    RWORK,QCBRELLN           GET RELATIVE LINE NUMBER
         STC   RWORK,OCWTG              SAVE IT IN OPCE          S22024
         CVD   RWORK,OPCDOUBL           COVERT RLN TO PACKED DEC
         UNPK  OPCDOUBL(THREE),OPCDOUBL+SIX(TWO)  MADE ZONED
         OI    OPCDOUBL+TWO,NUMERIC     SET ZONE FOR NUMERIC     S22024
         TM    TRMSTATE,TRMPREF         IS THERE NEG TTE PREFIX@Y17XARX
         BO    RNCMD1                   YES                      S22024
         SPACE
         MVC   LINEDISP(THREE,RMSG),OPCDOUBL  PUT RLN IN MSG
         B     REGCMD                   SKIP RN MSG SETUP        S22024
         SPACE
RNCMD1   EQU   *                                                 S22024
         MVC   RNLINE(THREE,RMSG),OPCDOUBL  PUT RLN IN MSG       S22024
         B     DCB3705                                         @Y17XARX
REGCMD   EQU   *                                                 S22024
         L     RDCB,QCBDCBAD-ONE        GET DCB ADDRESS
         TM    DCBOFLGS,OPEN            IS DCB OR LGB OPEN       S22024
         BNO   NOTOP                    BRANCH ON NO             S22024
         B     XDISPLAY                 BRANCH                 @Y17XARX
DCB3705  EQU   *                        3705 DCB               @Y17XARX
         LR    R1,RTERM       LOAD ADDR OF TTE INTO REG  1     @Y17XARX
         LA    R15,TRMPRFSZ             NEG PREFIX SIZE        @YM06150
         AR    R1,R15                   BUMP TTE PRT           @YM06150
DCB3705A EQU   *              3705 PROCESSING                  @Y17XARX
         LA    R13,OPCSAVE              ADDR OPC SAVE AREA     @Y17XARX
         L     R15,OPCHORT    SET UP ADDR IEDQCA COHORT SEARCH @Y17XARX
         BALR  R14,R15        BRANCH TO IEDQCA TO OPEN         @Y17XARX
         LTR   RZERO,ZERO     CHECK FOR ERROR CONDITION        @Y17XARX
         BZ    NOTFND         ERROR CONDITION WAS FOUND        @Y17XARX
         LR    RTERM,R1       LOAD NEW TTE ADDR INTO BASE REG  @Y17XARX
         LA    R15,TRMPRFSZ             GET ABSOLUTE LENGTH    @Y17XARX
         SR    RTERM,R15                ADJUST FOR NEG PREFIX  @Y17XARX
         CLI   TRMTYPE,TRMLNCP          CHECK FOR NCP TTE      @Y17XARX
         BE    FNDNCP         YES, BRANCH TO GET QCB PTR       @Y17XARX
         CLI   TRMTYPE,TRMPUNT          CHECK FOR PU TTE       @Y17XARX
         BE    FNDPU          YES, BRANCH TO GET PU TNT ADDR   @Y17XARX
         TM    TRMSTATE,TRMLINE         IS THIS LINE ENTRY     @Y17XARX
         BNO   DCB3705A                 NO LINE, GO RELOOP     @Y17XARX
         ST    R1,OPCWRKSP+EIGHT        SAVE LINE TTE ADDRESS  @YM06854
         B     DCB3705A            RESTART COHORT SEARCH LOOP  @Y17XARX
NOTFND   EQU   *                                               @Y17XARX
         LA    RZERO,FOURHNRD           GET MESSAGE NUMBER     @Y17XARX
         MVC   OPCMODID,ERRWRIT             INSERT ID IGC030D  @Y17XARX
         B     ERROR                    PREPARE TO EXIT        @Y17XARX
FNDPU    EQU   *                                               @Y17XARX
         ST    RZERO,OPCWRKSP+FOUR      STORE OFFSET  PU TNT   @Y17XART
         B     DCB3705A            START LOOP AGAIN            @Y17XARX
FNDNCP   EQU   *                                               @Y17XARX
         L     RMSG,OCMODNME            RELOAD ORIG IN MSG REG @Y17XARX
         L     RQCB,TRMDESTQ-ONE   LOAD ADDR QCB FROM NCP TTE  @Y17XARX
         L     RDCB,QCBDCBAD-ONE        GET DCB PTR FROM QCB   @Y17XARX
         LA    RWORK,ONE                SET RLN TO ONE - ONLY 1  S22024
*                                       UCB PER 3705             S22024
         STC   RWORK,OCWTG              SAVE IT IN OPCE          S22024
XDISPLAY EQU   *                                                 S22024
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
         SLL   RWORK,TWO           MULTIPLY RLN BY FOUR
         L     RUCB,DEBUCBAD-FOUR-DEBNMSUB(RWORK,RDEB)  GET UCB ADDR
         LA    RUCB,ZERO(RUCB)     CLEAR HIGH ORDER BYTE
         LTR   RUCB,RUCB                OPENED DD DUMMY
         BZ    NOTOP                    YES - REJECT COMMAND
         CLC   OCWTG(ONE),DEBNMEXT      EXTENT HIGH              S22024
         BH    NOTOP                    YES, OPENED DD DUMMY
*
         TM    TRMSTATE,TRMPREF         NEG PREFIX TEXT        @Y17XARX
         BO    RNCMD3                   YES                    @Y17XARX
         SPACE
         MVC   UCBDISP(THREE,RMSG),ADDR(RUCB) PUT UCB ADDR IN MSG
         B     RNCMD4                   SKIP RN MESSAGE SETUP    S22024
         SPACE
RNCMD3   EQU   *                                                 S22024
         MVC   RNADDR(THREE,RMSG),ADDR(RUCB) PUT RN ADDR IN MSG  S22024
         MVC   STATOFF(EIGHT,RMSG),OCBACKUP  PUT STATNAME IN MSG S22024
         B     RNCMD5                   FIND LGB NAME            S22024
         SPACE
RNCMD4   EQU   *                                                 S22024
         LH    RWORK,DCBTIOT            GET DD OFFSET IN TIOT
         L     RWORK2,AVTTCB            GET TCB ADDRESS
         L     RWORK2,TWELVE(,RWORK2)   GET TIOT ADDRESS
         AR    RWORK2,RWORK             GET TO DDNAME
         MVC   MSGDISP(EIGHT,RMSG),FOUR(RWORK2)  PUT IN MSG
*
         MVC   PEMDISP(EIGHT,RWKAREA),OCBACKUP MOVE TERMNAME     S22024
*                                       INTO MESSAGE             S22024
         B     EXIT                PREPARE TO XCTL TO IGC0701D1D
         SPACE 3
PE       EQU   *
         LA    RZERO,NINETY             GET MSG NUM
         MVC   OPCLDNME+THREE(TWO),ERRWRIT    INSERT ID OF IGC0310D
         B     ERROR               PREPARE TO EXIT
         SPACE 3
RNCMD5   EQU   *                                                 S22024
         LA    R13,OPCSAVE              IDENTIFY REGISTER SAVE @Y17XARX
         L     R1,OPCWRKSP+EIGHT        GET LINE TTE ADDRESS   @YM06854
         L     R15,OPCHORTG        SETUP ADDR IEDQCA COHORTG   @Y17XARX
*                                  SEARCH                      @Y17XARX
         BALR  R14,R15             BRANCH TO IEDQCA TO OPEN    @Y17XARX
         LR    R1,RZERO                 PUT PARAMETER IN REG   @Y17XARX
         L     R15,OPCTNTEA             SETUP FOR BRANCH       @Y17XARX
         BALR  R14,R15                  GO TO CALC OFFSET TNT  @Y17XARX
         L     RMSG,OCMODNME            RESTORE REG FOR MSGBASE@Y17XARX
         USING IEDQTNTD,RTNT            SETUP TO USE TNT       @Y17XARX
         L     RTNT,AVTRNMPT            ESTAB ADDR TO TNT      @Y17XARX
         SR    RWORK3,RWORK3            CLEAR REGISTER         @Y17XARX
         IC    RWORK3,TNTENLEN          SAVE LENGTH            @Y17XARX
         DROP  RTNT                                            @Y17XARX
         LR    RTNT,R1                  LOAD OFFSET TNT        @Y17XARX
         BCTR  RWORK3,ZERO              DECREMENT FOR EXEC NEXT@Y17XARX
         EX    RWORK3,MOVENAME          MOVE LGBNAM TO WORKAREA@Y17XARX
         L     RTERM,OPCWRKSP           GET ORIG TTE           @Y17XART
         CLI   TRMTYPE,TRMLUNT          CHK FOR LU TTE         @Y17XARX
         BNE   EXIT                NO, BRANCH                  @Y17XARX
         LA    R1,PUMSG            GET MESSAGE ADDRESS         @Y17XARX
         SR    RWORK2,RWORK2       CLEAR REGISTER              @Y17XARX
         LR    RWORK3,RWORK2       CLEAR REGISTER              @Y17XARX
         LH    RWORK2,0(RWKAREA)   GET PRESENT MESSAGE LENGTH  @Y17XARX
         IC    RWORK3,0(R1)        GET PU MESSAGE LENGTH       @Y17XARX
         AR    RWORK2,RWORK3       UPDATE TOTAL MESSAGE LENGTH @Y17XARX
         STH   RWORK2,0(RWKAREA)   STORE TOTAL MESSAGE LENGTH  @Y17XARX
         BCTR  RWORK3,ZERO         DECREMENT LENGTH FOR EXECUTE@Y17XARX
         EX    RWORK3,MOVEPUMG     MOVE PU MSG SKELETON TO MSG @Y17XARX
         L     R1,OPCWRKSP+FOUR         GET PU TTCIN           @Y17XART
         L     R15,OPCTNTEA             GET ADDR CONV RTN      @Y17XARX
         BALR  R14,R15                  CALC TNT ADDRESS       @Y17XARX
         USING IEDQTNTD,RTNT            TNT DSECT              @Y17XARX
         L     RTNT,AVTRNMPT            TNT BASE ADDR          @Y17XARX
         SR    RWORK3,RWORK3            CLEAR REGISTER         @Y17XARX
         IC    RWORK3,TNTENLEN          GET LENGTH             @Y17XARX
         DROP  RTNT                                            @Y17XARX
         LR    RTNT,R1                  TNT ADDRESS            @Y17XARX
         BCTR  RWORK3,ZERO              DECREMENT FOR EXEC     @Y17XARX
         EX    RWORK3,PUNAME            MOVE PU NAME TO MESSAGE@Y17XARX
         B     EXIT                     GO PRINT OUT GOOD RN MSG S22024
         SPACE
NOTOP    EQU   *
         LA    RZERO,NINETY1            GET MSG NUM
         MVC   OPCLDNME+THREE(TWO),ERRWRIT  INSERT ID OF IGC0310D
         B     ERROR               PREPARE TO EXIT
         SPACE 3
EXIT     EQU   *
         MVC   OPCLDNME+THREE(TWO),MSGWRIT INSERT ID OF IGC0701D
ERROR    XC    OCPARM(THREE),OCPARM     CLEAR WTG FIELD AND PARM
         LR    R1,ROPCAVT          SETUP PARAM REGISTER
         L     R13,OPCSAVE         GET ADDR OF OPC SAVE AREA
         XCTL  SF=(E,OPCXCTL)      XCTL TO IGC0701D
         EJECT
*        MESSAGES
GOODMSG  DS    0H
         DC    AL1(EGOODMSG-*-1)        LENGTH
         DC    CL17'IED038I',CL27'IS ON LINE'  MESSAGE TEXT    @YM09031
EGOODMSG EQU   *
INVALMSG DS    0H
         DC    AL1(EIVALMSG-*-1)        LENGTH
         DC    C'COMMAND REJECTED FOR TERMINAL ATTACHED TO 3705' S22024X
                                        MESSAGE TEXT             S22024
EIVALMSG EQU   *
RNMESG   DS    0H
         DC    AL1(ENRNMESG-*-1)        LENGTH                   S22024
         DC    CL17'IED177I',CL11'IS ON LINE ',9CL1' ',4CL1' ',CL8'ON 3X
               705 ',3CL1' '            MESSAGE TEXT           @YM08415
ENRNMESG EQU   *                                                 S22024
PUMSG    DS    0H                                              @Y17XARX
         DC    AL1(PUMSGLN1-*-1)   LENGTH                      @Y17XARX
         DC    CL7' ON PU ',CL8' ' MESSAGE TEXT                @Y17XARX
PUMSGLN1 EQU   *                                               @Y17XARX
         EJECT
*        CONSTANTS
ERRWRIT  DC    C'03'               ID OF ERROR WRITER IGC0310D
MSGWRIT  DC    C'07'               IF FOR IGC0701D RTN
*        EQUATES
RWORK6   EQU   1                   WORK REGISTER
LENOFF   EQU   0                   OFFSET INTO WORKAREA FOR LEN
TWO      EQU   2                   OFFSET
EIGHT    EQU   8                   OFFSET
TWELVE   EQU   12                       TIOT OFFSET FROM TCB
ADDR     EQU   13                       THIRTEEN                 S22024
PROC     EQU   X'20'                    FLAGS                    S22024
ZONE     EQU   X'30'                    FLAG                     S22024
DIST     EQU   X'40'                    FLAG                     S22024
A        EQU   C'A'                     CHARACTER A              S22024
MSGDISP  EQU   28                       MSG NO. 28               S22024
UCBDISP  EQU   41                  OFFSET IN MSG FOR UCB ADDR
LINEDISP EQU   37                  OFFSET FOR RLN INSERTION
ZONEDISP EQU   39                  OFFSET FOR CONVERSION
TERMDISP EQU   9                        OFFSET                   S22024
EXTRAMG  EQU   56                       PU MESSAGE DISPLACEMENT@Y17XARX
ZERO     EQU   0                        OFFSET                   S22024
ONE      EQU   1                        OFFSET                   S22024
THREE    EQU   3                        OFFSET                   S22024
FOUR     EQU   4                        OFFSET
PEMDISP  EQU   12                  OFFSET FOR DDNAME IN MSG
NOADISP  EQU   22                       OFFSET                   S22024
SIX      EQU   6                        OFFSET
PUDISP   EQU   63                       OFFSET                 @Y17XARX
NINETY   EQU   90                       OFFSET
NINETY1  EQU   91                       OFFSET
FOURHNRD EQU   400                      OFFSET NOT FIND COHORT @Y17XARX
DSORGLGB EQU   X'80'                    DSORG VALUE FOR LGB      S22024
RNLINE   EQU   37                       OFFSET IN RN MSG FOR RLN S22024
RNADDR   EQU   49                       OFFSET IN RN MSG FOR ADDRS22024
STATOFF  EQU   8                        OFFSET FOR RN STATION  @YM08415
LGBNME   EQU   28                       MSG OFFSET FOR LGB NAME  S22024
ENLEN    EQU   X'28'                    ENTRY LENGTH             S22024
FIRSTEN  EQU   X'52'                    FIRST ENTRY              S22024
OPEN     EQU   X'10'                    FLAG                     S22024
NUMERIC  EQU   X'F0'                    ZONE FOR NUMERIC         S22024
BADSTAT  EQU   18                       INVALID STAT NAME MSG NO S22024
SINGLINE EQU   X'80'                    TEST FOR SINGLE LINE ENTRS22024
*        EXECUTED INSTRUCTIONS                                 @YA09108
         SPACE 2                                               @YA09108
*        MOVE  LGBNAME                                         @YA09108
         SPACE 1                                               @YA09108
MOVENAME MVC   LGBNME(0,RMSG),ZERO(RTNT) PUT NAME IN MSG       @YA09108
*                                                              @Y17XARX
PUNAME   MVC   PUDISP(ZERO,RWKAREA),ZERO(RTNT)  ACTUAL PUNAME  @YM06913
         SPACE 3
*                                  MSG IN WORKAREA
MOVEMSG  MVC   FOUR(ZERO,RWKAREA),ONE(R1) MOVE MSG TO WORKAREA
*                            PU NAME INSERTED INTO RNMESSAGE   @Y17XARX
MOVEPUMG MVC   EXTRAMG(ZERO,RWKAREA),ONE(R1) MOVE PUNAME MSG TO@Y17XARX
*                                   WORKAREA BEHIND RNMSG      @Y17XARX
ZAPPATCH DC    50X'FF'             ZAP AREA FOR PATCHES        @Y17XARX
*        MACROS
         TAVTD
         TOPCAVTD
         TDEBD
         TOPCED
         DCBD  DSORG=TX
         TQCBD
         TTRMD
         TTNTD                                                 @Y17XARX
         TLGBD
         EJECT
         TLCBD
         EJECT
         END
