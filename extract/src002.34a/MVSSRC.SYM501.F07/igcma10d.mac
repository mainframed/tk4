         TITLE 'IGCMA10D - MODIFY FUNCTION SCAN/MAP/DISPATCHER II'
IGCMA10D CSECT
         SPACE 3
* CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*000000-999999                                                 @Y16X5R0
*A499000                                                         S05331
* A250000,656000,657200                                         SA69086
*A000000-999999                                                  S99240
*A174000                                                       @OY12425
*C596000,626000,680000-698000,806000,814000                    @OY12425
*D706000-746000,750000,752000                                  @OY12425
*D476000,320000,5860000                                        @Y17XART
*C008200,012400,512800                                         @Y17XART
*A046000,148000,856000                                         @Y17XART
*A386000                                                       @OS77841
         SPACE 3
***********************************************************************
*                                                                     *
*  MODULE NAME = IGCMA10D (TCAM,OP CTL)                        @Y17XART
*                                                                     *
*  DESCRIPTIVE NAME = MODIFY FUNCTION SCAN/MAP/DISPATCHER II          *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*                                                                     *
* STATUS -                                                            *
*    CHANGE LEVEL 10                                           @Y17XART
*                                                                     *
* FUNCTION--THIS MODULE RECEIVES CONTROL FROM IGCM010D TO COMPLETE    *
*   SCAN/MAP/DISPATCH FUNCTION STARTED BY IGCM010D. THIS ROUTINE MAPS *
*   NEEDED INFORMATION USED BY THE MODIFY FUNCTIONAL ROUTINES INTO THE*
*   OPERATOR CONTROL ELEMENT (OPCE), AND TRANSFERS CONTROL TO THE     *
*   APPROPRIATE FUNCTIONAL ROUTINE VIA AN XCTL.                       *
*                                                                     *
*   IF DURING VALIDITY CHECKING THE COMMAND IS FOUND TO BE INVALID,   *
*   XCTL TO IGC0310D,THE ERROR WRITER ROUTINE, IS MADE TO INDICATE AN *
*   ERROR.                                                            *
*                                                                     *
*   SUBROUTINES IN IEDQCA ARE USED TO VALIDITY CHECK STATION NAMES,   *
*   GROUPNAMES,RELATIVE LINE NUMBERS,AND LINE ADDRESSES               *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGCMA10D IS THE ONLY ENTRY POINT FOR THIS MODULE            *
*    PURPOSE - SEE FUNCTION                                    @Y17XART
*    LINKAGE - XCTL SF=(E,OPCXCTL)                             @Y17XART
*                                                                     *
* INPUT - REG 1 CONTAINES A POINTER TO THE OPERATOR CONTROL AVT.      *
*                                                                     *
* OUTPUT - PASSES OP CTL AVT ADDRESS IN REG 1.                        *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         SVC 7(XCTL)                                                 *
*   IEDQCA'S SUBROUTINES                                              *
*        TRMOFLOC--VALIDITY CHECKS TERMINAL NAME PASSED TO IT. RETURNS*
*                  ADDRESS OF TERMINAL ENTRY IF VALID. IF NOT VALID   *
*                  RETURNS PLUS 4 OF RETURN ADDRESS IN REGISTER 14.   *
*                                                                     *
*        DCBLOCAT--VALIDITY CHECKS HARDWARE LINE ADDRESSES, GROUPNAMES*
*                  AND RELATIVE LINE NUMBERS. ALSO INSURES LINE DCBS  *
*                  ARE OPEN, RLN IS NOT MORE THAN NUMBER OF EXTENTS,  *
*                  AND LINE WAS NOT OPENED DUMMY. FOR ANY OF THESE    *
*                  ERRORS RETURNS PLUS 4 OF RETURN ADDRESS IN R14.    *
*                             GOOD RETURN- R0= UCB ADDRESS            *
*                                          R1= DCB ADDRESS            *
*                                                                     *
*EXITS,NORMAL--EXITS VIA XCTL TO MODIFY FUNCTIONAL ROUTINES.          *
*              IGCM410D - MODIFY INTERVAL FUNCTIONAL ROUTINE          *
*              IGCM510D - MODIFY INTENSE FUNCTIONAL ROUTINE           *
*              IGCM710D - MODIFY OPERATOR FUNCTIONAL ROUTINE          *
*              IGCM810D - MODIFY OPTION FUNCTIONAL ROUTINE            *
*              IGCM910D - MODIFY DEBUG FUNCTIONAL ROUTINE             *
*                                                                     *
*EXITS,ERROR -- THE ERROR MESSAGE NUMBER IS PLACED IN REGISTER 0,     *
*   THE OP CTL AVT ADDRESS IN REG 1 AND XCTL IS TO THE OP CTL ERROR   *
*   MESSAGE MODULE (IGC0310D).                                        *
*              IED016I STATION XXXXXXXX NOT FOUND                     *
*                   IED017I LINE XXXXXXXX  NOT OPEN                   *
*                   IED018I XXXXXXXX COMMAND INVALID                  *
*                                                                     *
* TABLES/WORK AREAS - OPERATOR CONTROL AVT, TCAM AVT, OPCE, OPERATOR  *
*                    CONTROL RESIDENT WORK AREA.                      *
*                                                                     *
* ATTRIBUTES - SUPERVISOR MODE, DISABLED, TRANSIENT(TYPE 4 SVC), NON- *
*             REENTRANT, SERIALLY REUSABLE, REFRESHABLE.              *
*                                                                     *
*                                                                     *
*CHARACTER CODE DEPENDENCY                                            *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT *
*    TO THAT USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED SO  *
*    THAT REDEFINITION OF CHARACTER CONSTANTS, BY REASSEMBLY, WILL    *
*    RESULT IN A CORRECT MODULE FOR THE NEW DEFINITION.               *
*                                                                     *
* NOTES -                                                             *
*    COMMUNICATIONS BETWEEN THE VARIOUS MODULES/TRANSIENTS IS         *
*    PRIMARILY THROUGH THE OPERATOR CONTROL ADDRESS VECTOR TABLE (AVT)*
*    RESTRICTIONS - NONE                                       @Y17XART
*    REGISTER CONVENTIONS - STANDARD OP CTL                    @Y17XART
*    PATCH LABEL - ZAPAREA                                     @Y17XART
* MODULE TYPE - PROCEEDURE                                     @Y17XART
*    PROCESSOR - ASSEMBLER                                     @Y17XART
*    MODULE SIZE - 3F3                                         @Y17XART
*                                                                     *
***********************************************************************
         EJECT
         SPACE
RZERO    EQU   0                        PARM REG 0
RPARM    EQU   1                        PARM REG 1
ROPCAVT  EQU   2                        OP CONTROL AVT POINTER
ROPCE    EQU   3                        CURRENT ELEMENT ADDRESS
RAVT     EQU   4                        TCAM AVT ADDRESS
RWORKA   EQU   5                        WORK REGISTER
RLEN     EQU   6                        DATA LENGTH
RAPTR    EQU   7                        ADVANCE SCAN PTR
RRPTR    EQU   8                        REAR SCAN PTR
RTEMP    EQU   8                        TEMP REG               @OY12425
RBYSCN   EQU   9                        NUMBER OF BYTES SCANNED
RSCNRTN  EQU   10                       SCAN ROUTINE RETURN REG
RWRK11   EQU   11                       WORK REGISTER
RBASE    EQU   12                       MODULE BASE
RSAVE    EQU   13                       REG SAVE PTR
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RLINK    EQU   15                       LINK REG
         SPACE
         SPACE
ZERO     EQU   0                        ZERO VALUE
ONE      EQU   1                        VALUE ONE
TWO      EQU   2                        VALUE TWO
NAMELEN  EQU   2                        NAME LENGTH
THREE    EQU   3                        VALUE THREE
VARINAME EQU   3                        OFFSET TO CHANGE XCTL NAME
FOUR     EQU   4                        VALUE FOUR
FIVE     EQU   5                        VALUE FIVE
SIX      EQU   6                        VALUE SIX
SEVEN    EQU   7                        VALUE SEVEN
EIGHT    EQU   8                        VALUE EIGHT
TWELVE   EQU   12                       VALUE TWELVE
SIXTEEN  EQU   16                       VALUE SIXTEEN
NFNDCD   EQU   16                       NOT FOUND ERROR CODE
SEVNTEEN EQU   17                       VALUE SEVENTEEN
NOPNCD   EQU   17                       NOT OPERATIONAL ERROR CODE
EIGHTEEN EQU   18                       VALUE EIGHTEEN
THIRTY2  EQU   32                       VALUE THIRTY TWO
THIRTY4  EQU   34                       VALUE THIRTY FOUR
NINTY5   EQU   95                       VALUE 95
ONEOTWO  EQU   102                      VALUE ONE HUNDRED TWO
ONE28    EQU   128                      VALUE 128
BLANK    EQU   C' '                     BLANK CHAR
COMMA    EQU   C','                     COMMA
ALPHAA   EQU   C'A'                     CHAR 'A'                 S22024
LGB3705  EQU   X'80'                    3705 LINE GROUP BLOCK    S22024
TERM3705 EQU   X'10'                    TERM IN 3705 MODE        S22024
MSG201   EQU   201                      INVALID FOR 3705 MSG     S22024
EQUAL    EQU   C'='                     EQUAL
X80      EQU   X'80'                    HEX 80
X0E      EQU   X'0E'                    HEX 0E
XF0      EQU   X'F0'                    HEX F0
FOXFOX   EQU   X'FF'                    HEX FF
DOLLAR   EQU   C'$'                     DOLLAR SIGN             SA69086
POUND    EQU   C'#'                     POUND SIGN              SA69086
AT       EQU   C'@'                     AT SIGN                 SA69086
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         SPACE
         BALR  RBASE,0                  SET BASE REGISTER
         USING *,RBASE                  MODULE ADDRESSABILITY
IGCMA10D IEDHJN SKIPID
         LR    ROPCAVT,RPARM            GET OP CTL AVT ADDR
         LM    ROPCE,RBYSCN,OPCSAVE     RESTORE REGS STORED IN M0
         LR    RWRK11,RZERO             SET UP OFFSET FOR
         LA    RSAVE,OPCSAVE            SET R13 FOR SERVICE MODULE
         B     BRTBL(RWRK11)              BRANCH INTO OP BR TBL
         SPACE
****        THIS IS AN OPERAND BRANCH TABLE        ****
BRTBL    EQU   *                        OPERAND BRANCH TABLE
         B     TIMEDAT                  BRANCH TIMEDAT  OPERAND
         B     SWITCH                   BRANCH SWITCH   OPERAND
         B     SWBACK                   BRANCH SWBACK   OPERAND
         B     OPT                      BRANCH OPT      OPERAND
         B     OLT                      BRANCH OLT      OPERAND
         B     IPL                      BRANCH IPL      OPERAND
         B     INTENSEL                 BRANCH INTENSEL OPERAND
         B     INTENSET                 BRANCH INTENSET OPERAND
         B     ID                       BRANCH ID       OPERAND
         B     DUMP                     BRANCH DUMP     OPERAND
         B     DEBUGL                   BRANCH DEBUGL   OPERAND
         B     DEBUGD                   BRANCH DEBUGD   OPERAND
         B     CHNGLOAD                 BRANCH CHNGLOAD OPERAND
         B     BTRACE                   BRANCH BTRACE   OPERAND
         B     BACKUP                   BRANCH BACKUP   OPERAND
         B     INTERVAL                 BRANCH INTERVAL OPERAND
AUTOPOLL EQU   *
         CLC   OPCDOUBL(EIGHT),SYSCON   EQUAL SYSCON
         BE    SWUPONE                  YES, BR TO SET SYSCON SW
         B     OPSTAT                   OPERATOR = STATNAME
INTERVAL EQU   *
         CLC   OPCDOUBL(EIGHT),SYSTEM   EQUAL SYSTEM
         BNE   NOSYSCON                 NOT SYSTEM, GO CK POLL
         LTR   RLEN,RLEN                YES, CHECK FOR DATA ALSO
         BNP   SWUPONE                  NO, SET OP SWITCH NO DATA
         LA    RZERO,FIVE               LOAD FIVE TO REG FOR COMP
         CR    RLEN,RZERO               COMPARE DATA LEN TO FIVE
         BH    NOMATCH                  GREATER THAN FIVE - NG
         EX    RLEN,MVDATA                    FOR MOVE TO OPCE
         STH   RLEN,OCBACKUP            SAVE LENGTH
         SR    RWRK11,RWRK11            UP SWITCH
         IC    RWRK11,OCSWITCH            COUNT BY TWO
         LA    RWRK11,ONE(RWRK11)           FOR SYSTEM
         B     SWTWO                           PLUS DATA
MVDATA   MVC   OCBACKUP+TWO(ZERO),ZERO(RAPTR) MOVE DATA          S22024
NOSYSCON EQU   *
         CLC   OPCDOUBL(EIGHT),POLL      TEST FOR POLL
         BNE   NOMATCH                  INTVL NOT SYS/POLL - REJECT
         CLI   OCSWITCH,X0E             YES, CHECK OP SWITCH FOR )
         BNE   NOMATCH                  NOT E - ERROR
         LR    RRPTR,RAPTR              SET UP FOR SCAN
         BAL   RSCNRTN,SCNTODEL         SCAN TO NEXT DELIMITER
         LTR   RBYSCN,RBYSCN            CHECK FOR ZERO BYTES
         BNP   NOMATCH                     SCANNED
         MVI   OPCDOUBL,BLANK           SET DOUBLE WORD WORK AREA
         MVC   OPCDOUBL+ONE(SEVEN),OPCDOUBL TO BLANKS
         LA    RZERO,EIGHT              PREPARE COMPARE REG    @OS77841
         CR    RBYSCN,RZERO             CHAR LENGTH GREATER    @OS77841
*                                       EIGHT                  @OS77841
         BH    NOMATCH                  YES,BRANCH             @OS77841
         BCTR  RBYSCN,RZERO             REDUCE COUNT FOR EXECUTE
         EX    RBYSCN,MVADDR            MOVE SCANNED FIELD TO WORK
OPSTAT   EQU   *
         L     RLINK,OPCTOFLK           GET ADDR OF TERM ENTRY RTN
         BALR  RRET,RLINK               LINK TO TERM ENTRY RTN
         B     STAT                     GOOD RETURN - GO PROCESS
         LA    RZERO,SIXTEEN            SET STA NOT FOUND
         B     XCTLERR                  GO OUTPUT MESSAGE
STAT     EQU   *
         ST    RPARM,OCMODNME           SAVE TERM ADDR
         LR    RRPTR,RAPTR              SET UP FOR SCAN          S22024
         BAL   RSCNRTN,SCNTODEL         SCAN TO NEXT DEL.        S22024
         LTR   RBYSCN,RBYSCN            ZERO BYTES SCANNED?      S22024
         BNP   XCTLMOD                  YES - TO FUNCTION MODULE S22024
         LA    RWRK11,FIVE              SET UP TO CHECK DATA     S22024
         CR    RBYSCN,RWRK11             LENGTH                  S22024
         BH    NOMATCH                  OVER 5 - INDICATE INVALIDS22024
         EX    RBYSCN,MOVEDATA          MOVE INTERVAL TO ELEMENT S22024
         STH   RBYSCN,OCBACKUP          SET LENGTH OF DATA       S22024
         MVI   OCBACKUP+SEVEN,ZERO      CLEAR 3705 INDO          S22024
         LTR   RLEN,RLEN                ANYTHING LEFT AFTER DATA S22024
         BNP   XCTLMOD                  NO, TO FUNCTION MOD      S22024
         MVC   OCBACKUP+SEVEN(ONE),ZERO(RAPTR) SET TO ELEMENT    S22024
         LA    RWRK11,ONE               SET UP TO CHECK FOR ONLY S22024
         CR    RLEN,RWRK11              ONE BYTE INDICATED       S22024
         BH    NOMATCH                  NO, ERROR - GO INDICATE  S22024
         B     XCTLMOD                  GO TO FUNCTION MODULE    S22024
BACKUP   EQU   *
TIMEDAT  EQU   *
SWITCH   EQU   *
SWBACK   EQU   *
OPT      EQU   *
         SPACE
****     MODIFY OPTION       ******
         BAL   RRET,STATNME             GO PROCESS STATNAME
         LR    RRPTR,RAPTR              UPDATE DATA POINTER
         BAL   RSCNRTN,SCNTODEL         SCAN TO NEXT DELIMITER
         LTR   RBYSCN,RBYSCN            TEST NUMBER BYTES SCANNED
         BNP   NOMATCH                   ZERO - INVALID CMD
         LA    RZERO,EIGHT              SET UP TO TEST
         CLR   RBYSCN,RZERO              BYTES SCANNED FOR EIGHT
         BH    NOMATCH                  GR THAN 8 - INVALID CMD
         MVI   OPCDOUBL,BLANK           BLANK OUT OPCDOUBL FIELD
         MVC   OPCDOUBL+ONE(SEVEN),OPCDOUBL FOR SUBROUTINE USE
         BCTR  RBYSCN,RZERO             REDUCE BYSCND CNT FOR EXECUTE
         EX    RBYSCN,MVADDR            MOVE SCANNED DATA TO OPCDOUBL
         LA    RBYSCN,ONE(RBYSCN)       RESTORE BYTES SCANNED COUNT
BFRDATA  EQU   *
         STM   ROPCE,RBYSCN,OPCSAVE     SAVE REGS FOR FUNCTION MODULE
         B     XCTLMOD                  XCTL TO FUNCTION MODULE
OLT      EQU   *
         CLI   AVTOLTST,ZERO            OLT IN SYSTEM
         BZ    NOOLT                    NO, PREPARE TO EXIT - ERROR
         MVC   ZERO(TWO,RWORKA),TWO(RWORKA) SHIFT COUNT
         LA    RWRK11,ONE28             SET LOOP FACTOR
MVELP    EQU   *
         L     RPARM,EIGHT(RWORKA)      PICK UP FOUR BYTES OF DATA
         ST    RPARM,FOUR(RWORKA)       STORE DATA
         LA    RWORKA,FOUR(RWORKA)      BUMP DATA PTR BY FOUR
         BCT   RWRK11,MVELP             LOOP UNTIL ALL DATA IS MOVED
         B     XCTLMOD                  GO TO FUNCTION MODULE
NOOLT    EQU   *
         LA    RZERO,NINTY5             SET MSG NUMBER
         MVI   OCSWITCH,ZERO            CLEAR OLT INDICATION     S05331
         B     XCTLERR                  GO OUTPUT ERROR MSG
IPL      EQU   *
INTENSEL EQU   *
         BAL   RRET,GPNRLADR            LINK TO GET RLN ADDRESS
         USING IHADCB,RPARM                                      S22024
         TM    DCBDSORG,LGB3705         IS THIS A 3705 LGB ?     S22024
         BO    INV3705                  YES, CMD INVALID FOR 3705S22024
         DROP  RPARM                                             S22024
         B     SNSENCNT                 GO GET SENSE AND COUNT
INTENSET EQU   *
         BAL   RRET,STATNME             GO PROCESS STATNAME
         USING IEDQTRM,RPARM                                     S22024
         TM    TRMSTATE,TRMPREF         TERMINAL NOT 270X MDE  @Y17XART
         BO    INV3705                  YES, CMD INVALID FOR 3705S22024
         DROP  RPARM                                             S22024
         SPACE
SNSENCNT EQU   *
         LR    RRPTR,RAPTR              SET UP FOR SCAN RTN
         BAL   RSCNRTN,SCNTODEL         SCAN TO NEXT DELIMITER
         LA    RWRK11,TWO               CHECK BYTES SCALLED
         CR    RBYSCN,RWRK11              EQUAL TWO
         BNE   NOMATCH                  INVALID SENSE INDICATION
         MVC   OCMODNME(TWO),ZERO(RRPTR) MOVE SENSE TO OPCE
         LTR   RLEN,RLEN                TEST FOR COUNT
         BZ    COUNT                    NO COUNT - BR TO STORE ZERO
         CR    RLEN,RWRK11              TEST FOR 2 BYTES OF COUNT
         BH    NOMATCH                  MORE THAN TWO - INVALID
         BE    TWOCNT                   TWO - BR TO MOVE COUNT
         LA    RLEN,XF0                 SET 1ST DIGIT TO ZERO
         SLL   RLEN,EIGHT               SHIFT TO PICK NEXT CHAR
         IC    RLEN,ZERO(RAPTR)         MUST BE ONE BYTE
         STH   RLEN,OCMODNME+TWO        COUNT TO OPCE
         B     XCTLMOD                  TO FUNCTION ROUTINE
         SPACE
COUNT    EQU   *
         MVC   OCMODNME+TWO(TWO),CHAR15 DEFAULT TO FIFTEEN
         B     XCTLMOD                  TO FUNCTION ROUTINE
         SPACE
TWOCNT   EQU   *
         MVC   OCMODNME+TWO(TWO),ZERO(RAPTR)  SET COUNT INTO ELEMENT
         B     XCTLMOD                  XCTL TO FUNCTION MOD
ID       EQU   *                        ****
DUMP     EQU   *                        ****
DEBUGL   EQU   *                        ****
DEBUGD   EQU   *                        ****
         MVC   OCMODNME(EIGHT),OPCDOUBL MOVE PGMNAME TO ELEMENT
         CLC   OCMODNME(SIX),FENAME     CHECK FOR VALID NAME
         BE    XCTLMOD                  YES-XCTL TO FUNCTION MOD
         LA    RZERO,ONEOTWO            NO-SET ERROR MSG CODE
         B     XCTLERR                  OUTPUT ERROR MSG
CHNGLOAD EQU   *                        ****
BTRACE   EQU   *                        ****
         SPACE
* DCB LOOKUP ROUTINE *
GRPNAME  EQU   *
         ST    RRET,OCPARM              SAVE RETURN ADDRESS    @OY12425
         LA    RPARM,ONE                SET A ONE VALUE IN PARM REG
         L     RLINK,OPCDCBLK           GET ADDR OF DCB LOOKUP RTN
         LA    RSAVE,OPCSAVE            SET PTR TO OP CTL SAVE AREA
         BALR  RRET,RLINK               BRANCH TO LOCATE DCB
         B     GOODDCB                  DCB LOCATED - BR TO PROCESS
         LA    RWRK11,TWO               SET VALUE FOR REG COMP
         CLR   RZERO,RWRK11             CHECK RETURN CODE
         BH    NOTOPN                   GT TWO - NOT OPEN
         LA    RZERO,NFNDCD             OTHERWISE NOT FOUND
         B     XCTLERR                  GO INDICATE NOT FOUND
NOTOPN   EQU   *
         LA    RZERO,NOPNCD             SET UP NOT OPEN ERROR
         B     XCTLERR                  GO INDICATE NOT OPEN
GOODDCB  EQU   *
         L     RRET,OCPARM              LOAD RETURN ADDRESS    @OY12425
         BR    RRET                     RETURN TO CALLER
         SPACE
         SPACE
****      SCAN TO DELIMETER ROUTINE      ******
SCNTODEL EQU   *
         SR    RBYSCN,RBYSCN            INITIALIZE BYTES SCANNED REG
LOOPSCN  EQU   *
         LTR   RLEN,RLEN                TEST FOR END OF DATA
         BNP   ENDSCN                   YES, END SCAN
         CLI   ZERO(RAPTR),COMMA        CHECK COMMA DELIMITER
         BE    SETRTN                   YES, SET UP RETURN
         CLI   ZERO(RAPTR),EQUAL        CHECK FOR EQUAL DELIMITER
         BE    SETRTN                   YES, SET UP RETURN
         CLI   ZERO(RAPTR),BLANK        CHECK BLANK DELIMITER
         BE    SETRTN                   YES, SET UP RETURN
         CLI   ZERO(RAPTR),DOLLAR       DOLLAR SIGN ?           SA69086
         BE    BUMPIT                   YES, BRANCH             SA69086
         CLI   ZERO(RAPTR),POUND        POUND SIGN ?            SA69086
         BE    BUMPIT                   YES, BRANCH             SA69086
         CLI   ZERO(RAPTR),AT           AT SIGN ?               SA69086
         BE    BUMPIT                   YES, BRANCH             SA69086
         CLI   ZERO(RAPTR),ALPHAA       LESS THAN A              S22024
         BL    NOMATCH                  YES - INVALID            S22024
BUMPIT   EQU   *                                                SA69086
         LA    RBYSCN,ONE(RBYSCN)       BUMP BYTES SCANNED BY ONE
         LA    RAPTR,ONE(RAPTR)         BUMP ADVANCE SCAN PTR BY ONE
         BCTR  RLEN,RZERO               REDUCE DATA LENGTH BY ONE
         B     LOOPSCN                  LOOP TO SCAN NEXT POSITION
SETRTN   EQU   *
         BCTR  RLEN,RZERO               REDUCE DATA LENGTH BY ONE
         LA    RAPTR,ONE(RAPTR)         BUMP ADVANCE PTR PAST DELIMITER
ENDSCN   EQU   *
         BR    RSCNRTN                  RETURN TO CALLING RTN
         SPACE
GPNRLADR EQU   *
         ST    RRET,OCPARM              SAVE RETURN ADDRESS    @OY12525
         MVI   OCSCAN,OCADDR+OCDDNRLN   GRPNME,RLN OR ADDR     @OY12425
         LR    RWRK11,RRPTR             SETUP SCAN PTR         @OY12425
         L     RLINK,OPCDCBLK           PREPARE TO GO          @OY12425
         BAL   RRET,FOUR(RLINK)          OPSCAN RTN IN CA      @OY12425
         B     SETPTR                   NAME FOUND             @OY12425
         B     NOTFOUND                 NAME NOT FOUND         @OY12425
         B     NOMATCH                  SYNTAX ERROR           @OY12425
SETPTR   EQU   *                                               @OY12425
         LR    RTEMP,RWRK11             ADJUST NUMBER OF BYTES @OY12425
         SR    RTEMP,RAPTR                 LEFT TO SCAN        @OY12425
         SR    RLEN,RTEMP               STORE IN LENGTH REG    @OY12425
         LR    RAPTR,RWRK11             RESET SCAN PTR FOR MA  @OY12425
         B     GOODRTRN                 CONTINUE WITH GOOD CDE @OY12425
NOTFOUND EQU   *                                               @OY12425
         LA    RZERO,SEVNTEEN           SET ERR MSG CODE       @OY12425
         B     XCTLERR                  BR TO CALL ERR MSG MOD @OY12425
         SPACE
MVADDR   MVC   OPCDOUBL(ZERO),ZERO(RRPTR) MOVE ADDRESS FOR DCB ROUTINE
MOVEDATA MVC   OCBACKUP+TWO(ZERO),ZERO(RRPTR) MOVE DATA          S22024
         SPACE
         SPACE
         SPACE
CKOFFON  EQU   *
         LR    RRPTR,RAPTR              SET UP FOR SCAN ROUTINE
         BAL   RSCNRTN,SCNTODEL         GO SCAN
         LA    RWRK11,THREE             SET UP TO
         CR    RBYSCN,RWRK11              CHECK BYTES SCANNED
         BE    CKOFF                    THREE - CHECK OFF
         BH    NOMATCH                  MORE THAN THREE - INVALID
         CLC   ZERO(TWO,RRPTR),ON       CHECK ON
         BNE   NOMATCH                  NO - INVALID COMMAND
SWUPONE  EQU   *
         SR    RWRK11,RWRK11            YES -
         IC    RWRK11,OCSWITCH            INCREMENT
SWTWO    EQU   *
         LA    RWRK11,ONE(RWRK11)           SWITCH
         STC   RWRK11,OCSWITCH                  BY ONE
         B     XCTLMOD                  GO TO FUNCTION MODULE
CKOFF    CLC   ZERO(THREE,RRPTR),OFF    CHECK OFF
         BE    XCTLMOD                  YES - TO FUNCTION MODULE
         SPACE
* SET COMMAND INVALID MESSAGE
NOMATCH  EQU   *
         LA    RZERO,EIGHTEEN           SET MSG NUM FOR MOD 03
         B     XCTLERR                  GO OUTPUT MESSAGE
         SPACE 2                                                 S22024
INV3705  EQU   *                                                 S22024
         LA    RZERO,MSG201             SET EEROR MSG 201        S22024
         B     XCTLERR                  GO TO MSG MODULE         S22024
         SPACE
GOODRTRN EQU   *
         L     RRET,OCPARM              LOAD RETURN ADDRESS    @OY12425
         XC    OCPARM(4),OCPARM         CLEAR OCPARM FIELD     @OY12425
         BR    RRET                     RETURN TO CALLING ROUTINE
         SPACE
STATNME  EQU   *
         ST    RRET,OCPARM              SAVE RETURN ADDRESS    @OY12425
         L     RLINK,OPCTOFLK           GET ADDR OF TERM ENTRY RTN
         BALR  RRET,RLINK               LINK TO TERM ENTRY RTN
         B     GOODRTRN                 GOOD ENTRY - BR TO PROCESS
         LA    RZERO,SIXTEEN            SET STA NOT FOUND
         SPACE
XCTLERR  EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),ERRWRT SET UP FOR XCTL
XCTLMOD  EQU   *
         LR    RPARM,ROPCAVT            PASS OP CTL AVT PTR
         XCTL  SF=(E,OPCXCTL)           CALL FUNCTION MODULE
         SPACE
         SPACE
MAXVAL   DC    H'255'                   MAXIMUM VALUE FOR RLN IS 255
ON       DC    C'ON'                    CHARACTERS 'ON'
OFF      DC    C'OFF'                   CHARACTERS 'OFF'
ERRWRT   DC    C'03'                    WRITE ERROR MESSAGE MOD
CHAR15   DC    C'15'                    MAXIMUM INTENSE VALUE
POLL     DC    C'POLL    '              CHARACTERS 'POLL'
SYSTEM   DC    C'SYSTEM  '              CHACACTERS 'SYSTEM'
SYSCON   DC    C'SYSCON  '              CHARACTERS 'SYSCON'
FENAME   DC    C'IEDQFE'                F E ROUTER MOD NAME
ZAPAREA  DC    50X'FF'                  PATCH AREA             @Y17XART
         EJECT
         TAVTD
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
         EJECT
         DCBD  DSORG=TX                 S22024
         EJECT
         TTRMD                                                   S22024
         END
