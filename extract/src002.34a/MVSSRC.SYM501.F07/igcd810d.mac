         TITLE 'IGCD810D DISPLAY OPTION COMMAND'
IGCD810D CSECT
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C402000-406000                                                @YA05454
*A000000-999999                                                  S99240
***********************************************************************
*                                                                     *
*TITLE--'IGCD810D' PROCESSOR OF DISPLAY OPTION COMMANDS               *
*                                                                     *
*STATUS--CHANGE LEVEL 5                                          S22024
*                                                                     *
*FUNCTION -- THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH   *
*   REQUEST DISPLAY OF TERMINAL OPTION FIELDS.                        *
*                                                                     *
*   THIS ROUTINE INITIALLY VALIDITY CHECKS TO INSURE THERE ARE NO     *
*   IMBEDDED BLANKS FOLLOWING THE OPTION NAME IN THE COMMAND AND THAT *
*   A 'C','D',OR AN'X' WAS CODED. IF THE VALIDITY CHECK PROVES        *
*   UNSUCCESSFUL AN XCTL TO IGC0310D IS DONE IN ORDER TO WRITE OUT AN *
*   ERROR MESSAGE.                                                    *
*                                                                     *
*   THE DISPLAY OPTIONS ROUTINE THEN SERIALLY SEARCHES THE OPTION     *
*   NAMES TABLE FOR THE SPECIFIED OPTION FIELD.IF NO ENTRY IS FOUND,AN*
*   ERROR MESSAGE IS WRITTEN OUT BY IGC0310D THE ERROR WRITER. CONTROL*
*   IS PASSED TO IGC0310D VIA AN XCTL. IF THERE ARE                   *
*   NO OPTIONS FOR THE TERMINAL ENTRY (TRMOPTFN OFF IN TRMSTATE) AN   *
*   ERROR RESPONSE IS BUILT AND THE ROUTINE RETURNS TO THE ERRORWRITERX
*   MODULE.  IF THE SPECIFIED OPTION FIELD IS NOT DEFINED FOR THE     *
*   TERMINAL, AN ERROR MESSAGE IS WRITTEN OUT BY THE ERROR WRITER     *
*   OTHERWISE THE ADDRESS OF THE OPTION FIELD IS                      *
*   OBTAINED AND PROCESSING CONTINUES.                                *
*                                                                     *
*   FOR DISPLAY COMMANDS, THE OPTION FIELD IS MOVED INTO THE RESIDENT *
*   WORK AREA IN IEDQCA. IF THE DATA FORMAT IS CHARACTER AND THE FIELD*
*   IS ALL BLANK, 'ALL BLANKS' IS MOVED INTO THE RESPONSE. SINCE NO   *
*   CONVERSION IS NECESSARY FOR CHARACTER DATA, A RESPONSE MESSAGE    *
*   IS BUILT AND THE ROUTINE EXITS TO THE MESSAGE WRITER IGC0710D. ALL*
*   OTHER TYPES OF FIELD ARE TESTED FOR ZERO CONTENT, AND A DATA IS   *
*   ZERO MESSAGE IS BUILT AND CONTROL RETURNS TO THE MESSAGE WRITER   *
*   MODULE.  OTHERWISE THE DATA IS CONVERTED FROM ITS CURRENT         *
*   FORMAT INTO A PRINTABLE EQUIVALENT.  A RESPONSE IS BUILT AND      *
*   CONTROL RETURNS TO THE MESSAGE WRITER.                            *
*                                                                     *
*ENTRY POINTS --                                                      *
*        'IGCD810D' TO PROCESS DISPLAY OPTION COMMAND                 *
*                                                                     *
*INPUT-- REGISTER 1 - ADDRESS OF THE OPERATOR CONTROL AVT. AT THE     *
*   LABEL OPCCOPCE IS THE ADDRESS OF THE CURRENT ELEMENT.             *
*   FIELDS RELEVANT TO THIS ROUTINE IN THE OPCE ARE:                  *
*                                                                     *
*        +0 OCLINK - ADDRESS OF COMMA AFTER OPTION NAME               *
*        +4 OCELEM - ADDRESS OF ASSOCIATED TERMINAL ENTRY             *
*        +20 OCBACKUP - TERMINAL NAME                                 *
*
*   IN THE OPCAVT RELEVANT FIELDS ARE:                                *
*        +0 OPCDOUBL - OPTION NAME                                    *
*                                                                     *
*OUTPUT-- REGISTER 1 - ADDRESS OF OPCAVT                              *
*                                                                     *
*EXTERNAL ROUTINES -- NONE                                            *
*                                                                     *
*EXITS,NORMAL-- COMMAND IS PROCESSED. A RESPONSE MESSAGE IS BUILT IN  *
*   THE RESIDENT WORK AREA IN IEDQCA WHOSE ADDRESS IS IN OPCAVT       *
*                                                                     *
*   NORMAL EXIT MESSAGES ARE-                                         *
*   IED035I 'STATNAME' OPTION 'OPFLDNAME'='ENTRY'                     *
*                                                                     *
*EXITS,ERROR-- OPTION NOT FOUND, OR OPTION DOES NOT EXIST FOR THE     *
*   TERMINAL OR 'X','C',OR 'D' NOT SPECIFIED. ERROR MESSAGES ARE      *
*   WRITTEN OUT BY IGC0310D.
*                                                                     *
*   ERROR MESSAGES ARE-                                               *
*   IED016I STATION 'STATNAME' NOT FOUND --GENERATED BY IGCD010D      *
*   IED018I 'COMMAND' COMMAND INVALID'                                *
*   IED034I 'STATNAME' HAS NO 'OPFLDNAME' OPTION                      *
*                                                                     *
*TABLES/WORK AREAS--OPCAVT,TCAM AVT,TRANSLATE TABLES,TERMNAME TABLE   *
*   TERMINAL ENTRY,OPCE                                               *
*                                                                     *
*ATTRIBUTES--SERIALLY REUSABLE,TRANSIENT(TYPE 4),SUPERVISOR MODE,     *
*   DISABLED,NON-REENTRANT                                            *
*                                                                     *
*CHARACTER CODE DEPENDENCY                                            *
*        THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        *
*        REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS        *
*        EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING     *
*        HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER          *
*        CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE     *
*        FOR THE NEW DEFINITION.                                      *
*                                                                     *
*NOTES - COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS      *
*        PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                  *
*                                                                     *
*        THIS IS A MODULE RE-WRITE OF IEDQCS AS OF OS/TCAM LEVEL II   *
***********************************************************************
*
         EJECT
*        REGISTER EQUATES
RZERO    EQU   0                        REG ZERO                 S22024
RPARM    EQU   1                   PARAMETER REGISTER
ROPCAVT  EQU   2                   OPERATOR CONTROL AVT
ROPCE    EQU   3                   OPERATOR CONTROL ELEMENT ADDR
RWKAREA  EQU   11                  OPERATOR CONTROL WORKAREA
R0       EQU   0                        REG ZERO                 S22024
R1       EQU   1                        PARAMETER
ROPC     EQU   2                        OPERATOR CONTROL AVT BASE
RCIB     EQU   3                        BUFFER OR CIB BASE
RAVT     EQU   4                        AVT BASE
ROPT     EQU   5                        OPTION CSECT BASE
RTNT     EQU   6                        TERMNAME TABLE BASE
RTERM    EQU   7                        TERMINAL ENTRY BASE
RWORK    EQU   8                        WORK
RWORK2   EQU   9                        WORK
RWORK3   EQU   10                       WORK
RBASE    EQU   12                       ROUTINE BASE
R13      EQU   13                       SAVE AREA
R14      EQU   14                       RETURN
R15      EQU   15                        ENTRY POINT
ZERO     EQU   0                   ZERO
ONE      EQU   1                   ONE
TWO      EQU   2                   TWO
FOUR     EQU   4                   FOUR
FIVE     EQU   5                   FIVE
EIGHT    EQU   8                   EIGHT
TWELVE   EQU   12                  TWELVE
FOURTEEN EQU   14                  FOURTEEN
TWOEIGHT EQU   28                  TWENTY-EIGHT
FOURTWO  EQU   42                  FORTY-TWO
FOURTHR  EQU   43                  FORTY-THREE
OFF03    EQU   3                        OFFSET
AL2      EQU   2                        OFFSET
OPCZERO  EQU   0                        OFFSET
OPCONE   EQU   1                   ONE
OPCTWO   EQU   2                   TWO
OPCFOUR  EQU   4                   FOUR
OFF04    EQU   4                   OFFSET FOUR
OFF00    EQU   0                   OFFSET ZERO
OFF02    EQU   2                   OFFSET TWO
AL8    EQU     8                   OFFSET EIGHT
TEN      EQU   10                       OFFSET TEN               S22024
LENOFF   EQU   0                        OFFSET INTO WORKAREA
C255     EQU   255                      LIMIT                    S22024
HEXF0    EQU   X'F0'                    FLAG BIT                 S22024
HEX0F    EQU   X'0F'                    FLAG BIT                 S22024
CZERO    EQU   C'0'                CHARACTER ZERO
SEVEN    EQU   7                        OFFSETS
EIGHTEEN EQU   18                       OFFSETS
         EJECT
         USING IEDQAVTD,RAVT            ADDRESSABILITY FOR AVT
         USING IEDQOPCD,ROPCAVT         ADDRESSABILITY FOR OP CTL AVT
         USING IEDQTNTD,RTNT            ADDRESSABILITY FOR TNT
         USING IEDQTRM,RTERM            ADDRESSABILITY FOR TERM
         USING IEDQOPCE,ROPCE           ADDRESSABILITY FOR OPC ELEMENT
         BALR  RBASE,ZERO               ADDRESSABILITY FOR THIS MOD
         USING *,RBASE
IGCD810D IEDHJN SKIPID
         LR    ROPC,R1                  GET OP CTL AVT BASE
         L     RAVT,OPCAVTPT            GET AVT BASE
         L     RTNT,AVTRNMPT            GET TNT ADDRESS
         L     RTERM,OCELEM             GET ADDR OF TERM ENTRY
         L     RWORK,OCLINK             GET ADDR OF COMMA AFTER OPNME
         CLI   OPCTWO(RWORK),AVTEBLNK     BALNK IN CMD
         BNE   ERROPT                   ERROR
         TM    TRMDSORG,TRMLGB          LINE GROUP BLOCK ?       S22024
         BO    ERROPT                   YES ERROR                S22024
         MVC   OCRLN,OPCONE(RWORK)      GET X,C, OR D AND SAVE
PROCESS  EQU   *
         L     RWKAREA,OPCWORK          GET ADDR OF MSG AREA IN WKAREA
         LA    R1,MSG                   GET ADDR OF MSG
         SR    R14,R14                  CLEAR REGISTER
         IC    R14,LENOFF(R1)           GET LENGTH OF MSG
         STH   R14,LENOFF(RWKAREA)      STORE LENGTH IN OPC WKAREA
         BCTR  R14,ZERO                 DECREMENT FOR EXECUTE
         EX    R14,MOVEMSG              MOVE MSG INTO WORKAREA
*                                      THIS SECTION OF CODE FINDS
*        THE TERMINAL NAME AND INSERT IT IN MSG
         MVC   TWELVE(EIGHT,RWKAREA),OCBACKUP INSERT TERM NAME
*                                       IN MESSAGE
OPTFLLOC EQU   *
         TM    TRMSTATE,TRMOPTFN        OPTIONS FIELDS PRESENT
         BNO   OPTERROR                 NO, RETURN TO ERROR RTN
         L     RWORK,AVTOPTPT           GET ADDR OF OPTION TABLES
         L     RWORK3,OFF04(RWORK)      GET OPT NAMES ADDRESS
         SR    RWORK2,RWORK2            CLEAR OFFSET REGISTER
OPTLOOP  EQU   *
         LA    RWORK2,ONE(RWORK2)       BUMP OSFFET CONTER
         CLI   OFF00(RWORK3),AVTEFF     END OF OPTIONS
         BE    OPTERROR                 YES, RETURN ERROR
         CLC   OFF02(AL8,RWORK3),OPCDOUBL COMPARES NAMES
         BE    GOODNAME                 GOOD COMPARE, BRANCE
         LA    RWORK3,TEN(RWORK3)  POINT TO NEXT NAME IN TABLE
         B     OPTLOOP                  CHECK FOR END OF TABLE
GOODNAME EQU   *
         ST    RWORK3,OCELEM            SAVE ADDR OF GOOD OPT NAME
         STC   RWORK2,OCSWITCH          SAVE OPTION FIELD NAME OFFSET
         MVC   OCPARM(AL2),OFF00(RWORK3) SAVE LENGTH AND TYPE
         SR    RWORK3,RWORK3            CLEAR FOR NUM OF OPTIONS
         IC    RWORK3,TRMOPNO           PICK UP NUM OF OPTIONS FOR TERM
         CR    RWORK2,RWORK3            OFFWET GREATER THAN NUM SPEC
         BH    OPTERROR                 YES, RETURN ERROR
         AR    RWORK2,RTERM             ADD TERM ENTRY BASE
         CLI   TRMOPT-ONE-IEDQTRM(RWORK2),AVTEFF  OPTION SPECIFIED
         BE    OPTERROR                 NO ,RETURN WITH WRROR INDICAT
         SR    RPARM,RPARM              CLEAR FOR TABLE OFFSET
         IC    RPARM,TRMOPT-ONE-IEDQTRM(RWORK2)  PICK UP OPTION OFFSET
         SR    RWORK2,RTERM             SUBTRACT TO GET OFFSET
         LH    RWORK,TRMOPTBL           GET ENTRY OFFSET       @YA05454
         N     RWORK,AVTCLRHI           CLEAR HI HALF          @YA05454
         A     RWORK,AVTOPTPT           ADD OPT TBL OFFSET     @YA05454
         AR    RWORK,RPARM              GET ADDR BY ADDING OFFSETS
*                                       OF OPTION FIELD
*                                       OF OPTION FIELD
         LA    R13,GOODOPT              SETUP BRANCHING REGISTER
         B     OPTNAME                  FOUND A MATCH SO INSERT NAME
OPTERROR EQU   *
         LA    R13,EXIT                 ERROR SO PREPARE EXIT BRANCE
         LA    R1,ERRMSG                GET ADDR OF ERROR MSG
         SR    R14,R14                  CLEAR REGISTER
         IC    R14,LENOFF(R1)           GET LENGTH OF MSG
         STH   R14,LENOFF(RWKAREA)       INSERT LENGTH IN MSG
         BCTR  R14,ZERO                 DECREMENT FOR EXECUTE
         EX    R14,MOVEMSG              MOVE MSG INTO WORKAREA
         MVC   TWELVE(EIGHT,RWKAREA),OCBACKUP  MOVE TERMNAME
OPTNAME  EQU   *
         MVC   TWOEIGHT(EIGHT,RWKAREA),OPCDOUBL MOVE OPTION NAME
         BR    R13                      BRANCH TO GOODOPT OR OUT
GOODOPT  EQU   *
         SR    R1,R1                    CLEAR REG FOR IC
         L     RWORK3,OCELEM            GET ADDR OF GOOD OPT NAME
         LA    RWORK3,ZERO(RWORK3)      CLEAR HI ORDER BYTE
         IC    R1,ZERO(RWORK3)          GET LENGTH - 1
         LR    R0,RWKAREA               SAVE ADDR OF WORKAREA AND MSG
         CLI   OCRLN,DEC                DEC FORMAT DESIRED
         BE    DFCK                     DECIMAL INFO WANTED
         CLI   OCRLN,CHARACTR           CHARACTER INFO DESIRED
         BE    CKLEAD                   YES
         CLI   OCRLN,HEXFLG             HEXADECIMAL FORMAT DESIRED
         BNE   ERROPT                   ERROR CONDITION
         B     HEXPACK                  YES PROCESS REQUEST
*              DISPLAY CHARACTER FORMAT OPTION FIELDS
*
CKNEXT   EQU   *
         LA    ROPT,ZERO(RWORK,R1)      ADDRESS OF OPTION FIELD END
         CLI   ZERO(ROPT),C' '          TEST FOR BLANK CHARACTER
         BNE   NOTALL                   BRANCH NOT A BLANK CHARACTER
         LTR   R1,R1                    WAS THAT THE LAST CHARACTER
         BZ    ALLBLNK                  BRANCH IF YES
         BCTR  R1,R0                    REDUCE CHARACTER COUNT
         B     CKNEXT                   GO TEST NEXT CHARACTER
CKLEAD   EQU   *
         CLI   ZERO(RWORK),C' '         CHECK FOR BLANK
         BNE   CKNEXT                   NO - TEST CHARACTER
         LA    RWORK,ONE(RWORK)         YES - UPDATE DATA POINTER
         BCT   R1,CKLEAD                REDUCE DATA COUNT
ALLBLNK  EQU   *
         MVC   FOURTWO(FOURTEEN,RWKAREA),ALLB PREPARE TO PRINT ALL
*                                       BLANK MESSAGE
         LH    R1,ALLBLEN               DET MSG LENGTH
          STH  R1,LENOFF(RWKAREA)       SET LEN IN WORKAREA
         B     EXIT                     PREPARE TO XCTL TO ×07'
NOTALL   EQU   *
         EX    R1,MOVEFROP              MOVE DATA TO DISPLAY MESSAGE
         AH    R1,LENOFF(RWKAREA)       COMBINE LEN OF DATA TO
*                                       EXISTING MSG LENGTH
         LA    R1,ONE(R1)               BUMP LENGTH BY ONE
         STH   R1,LENOFF(RWKAREA)       STORE LENGTH OF MSG
         B     EXIT                     PREPARE TO XCTL
*
*             DISPLAY ALL TYPES OF DATA IN HEXIDECIMAL FORMAT
*
HEXPACK  EQU   *
         SR    R14,R14
HEX1     EQU   *
         IC    R14,ZERO(RWORK)          PICK UP BYTE FROM OPTION FLD
         LTR   R14,R14                  STILL LEADING ZEROS
         BNZ   NONZERO                  BRANCH IF END OF LESDING ZEROS
         LTR   R1,R1                    LAST BYTE IN THE FIELD
         BZ    NONZERO                  BRANCH IF YES
         BCTR  R1,R0                    SUB ONE FROM FIELD SIZE
         LA    RWORK,ONE(RWORK)         SKIP OVER LEADING ZERO
         B     HEX1                     GO CHECK THE NEXT BYTE
NONZERO  EQU   *
         LR    R15,R1                   COPY COUNT OF NONZERO DIGITS
AGAIN    EQU   *
         IC    R14,ZERO(RWORK)          PICK UP DATA BYTE
         STC   R14,FOURTHR(RWKAREA)     STORE RIGHT MOST DIGIT
         NI    FOURTHR(RWKAREA),HEX0F   CLEAR UNWANTED BITS
         SRL   R14,FOUR                 POSITION LEFT HALF BYTE
         STC   R14,FOURTWO(RWKAREA)     STORE LEFT HALF BYTE
         LTR   R15,R15                  MORE BYTES TO SPILT
         BZ    LAST                     BRANCH IF ALL SPILT
         LA    RWORK,ONE(RWORK)         INCREMENT OPTION FIELD PTR
         LA    RWKAREA,TWO(RWKAREA)     INCREMENT WORKAREA PTR
         BCTR  R15,R0                   REDUCE CHARACTER COUNT
         B     AGAIN                    GO SPILT NEXT CHARACTER
LAST     EQU   *
         SLL   R1,ONE                   CHARACTER COUNT TIMES TWO
         LA    R1,ONE(R1)               ADD ONE IN CASE OF HEX
         LR    R15,R1                   SAVE NEW CHARACTER COUNT
         LA    RWORK2,TRANSHEX          SET FOR HEX TRANSLATE
         L     RWORK,OPCWORK            REESTABLISH ADDR OF RESIDENT
*                                       WORKAREA WHERE MSG STARTS
         LA    RWORK,FOURTWO(RWORK)     GET ADDR OF START OF MSG
         B     TRANLATE                 GO TRANSLATE THE DATA
*
*
*
*              DISPLAY DECIMAL EQUIVALENT OF HEXIDECIMAL
*              EXCEPT FIELDS WHICH ARE DEFINED CHARACTER OR THOSE
*              WHOSE HEXIDECIMAN VLLUES ARE GREATER THAN 7FFFFF (31BIT)
*
*
*
DFCK     EQU   *
         CLI   ONE(RWORK3),CHAR         IS FIELD CHARACTER TYPE
         BE    ERROPT                   YES REJECT COMMAND
         XC    OPCDOUBL(EIGHT),OPCDOUBL CLEAR FOR USEAGE
         LR    R15,RWORK                PTR TO OPTION FIELD
         LA    R1,ONE(R1)               LENGTH OF OPT FIELD
         BAL   R14,ZLOOP                FIND FIRST NON ZERO CHAR
*
*                                       ON RETURN
*                                            R1 = LENGTH
*                                            R15= PTR TO FIRST NONZERO
*                                            CHARACTER
*
*
         CH    R1,H4                     IS LENGTH GREATER THAN 4
         BH    ERROPT                   YES, REJECT COMMAND
         LTR   R1,R1                    IS LENGTH ZERO
         BZ    DFZEROF                  YES, SKIP MOVE CHAR
         LA    R14,OPCDOUBL+FOUR        LENGTH PAST OPCDOUBL ADDR
         SR    R14,R1                   PTR TO FIRST CHARACTER
         BCTR  R1,R0                    DECREMENT LENGTH FOR MOVE
         EX    R1,MOV1DF                MOVE IT
         L     R15,OPCDOUBL             FIELD IN REGISTER
         LTR   R15,R15                  FIELD GREATER THAN 31 BITS
         BM    ERROPT                   YES, GET OUT
         CVD   R15,OPCDOUBL             CONVERT TO DECIMAL
         LA    R15,OPCDOUBL             PTER TO BEG OF FLD FOR ZLOOP
         LA    R1,EIGHT                 LENGTH
         BAL   R14,ZLOOP                FIND FIRST OON ZERO CHAR
         OI    OPCDOUBL+SEVEN,HEX0F     CHANGE SIGN TO PRINTABLE
         BCTR  R1,R0                    LENGTH -1
         SLL   R1,ONE                   2(N-1) UNPACK TO LENGTH
         TM    ZERO(R15),HEXF0           ODD NUMBER OF DIGETS
         BNZ   DFL1                     NO, IS LENGTH -1
         BCTR  R1,R0                    SUBTRACT 1 FOR EXECUTE
DFL1     EQU   *
         LR    R14,R1                   USE R14, SAVE R1 FOR LATER
         SLL   R14,FOUR                 SET BITS TO 'OR' WITH 8-11
         EX    R14,UNPK1DF              UNPK INTO MSG AREA
         B     SENDOUT                  FINISH MESSAGE
*
*
ZLOOP    EQU   *
         CLI   ZERO(R15),ZERO           IS CHAR ZERO
         BCR   7,R14                    NO, DONE
         LA    R15,ONE(R15)             PTR TO NEXT CHAR
         BCT   R1,ZLOOP                 DECREMENT LENGTH
         BR    R14                      ALL ZERO
DFZEROF  EQU   *
         MVI   FOURTWO(RWKAREA),CZERO   PLACE DUMMY ZERO IN MSG
         SR    R1,R1                    LENGTH - 1  TO ZERO
         B     SENDOUT                  SEND OUT THE MESSAGE
*
*              TRANSLATE HEX AND PACKED EDCIMAL TYPE DATA
*
TRANLATE EQU   *
         SR    RWORK3,RWORK3            CLEAR REGISTER
         LR    R14,RWORK                COPY OPTION FIELD ADDR
         LA    RWORK3,C255              SET TO TRANSLATE MAX.
         EX    RWORK3,TRAN              TRANSLATE FIRST 256 BYTES
ONETR    EQU   *
         LA    R14,ONE(R14,RWORK3)       COMPUTE ADDR RENAINING IF ANY
         EX    RWORK3,TRAN              TRAN REST OR GARBAGE
         L     RWKAREA,OPCWORK          REESTABLISH ADDR OF WORKAREA
         LA    R1,ONE(R1)               REAL DATA LENGTH
         AH    R1,LENOFF(RWKAREA)       COMBINE TO GET TOTAL LEN
         STH   R1,LENOFF(RWKAREA)       STORE LEN IN WORKAREA
         B     EXIT                     EXIT FROM ROUTINE
PREPEXIT EQU   *
         LA    RWORK2,OPCFOUR(R0)       GET ADDR OF WHERE MSG STARTS
         SR    RWORK,RTNT               LENGTH OF OPTION FIELD
         AH    R1,ALLBLEN               LEN OF MSG SO FAR
         STH   R1,LENOFF(RWORK2)        STORE LEN FO MSG
         B     EXIT                     EXIT FROM RTN
SENDOUT  EQU   *                        CODE TO PREPARE TO SEND OUT MSG
         LR    RWORK2,R0                GET ADDR OF START OF MSG
         LA    R1,ONE(R1)               GET REAL DATA LENGTH
         AH    R1,ZERO(RWORK2)          ADD LENGTH OF FIXED MSG
         STH   R1,ZERO(RWORK2)          STORE TOTAL MSG LENGTH
         B     EXIT                     XCTL TO IGC0710D
ERROPT   EQU   *
         LA    RZERO,EIGHTEEN           GET MSG NUMBER
         L     R13,OPCWORK              GET ADDRESS OF WORKAREA
         MVC   EIGHT(EIGHT,R13),INVALCMD MOVE CMD IN WORKAREA
         MVC   OPCLDNME+OFF03(AL2),ERMSG GET ERROR MSG ID
         B     OUT                      PREPARE TO BRANCH OUT
EXIT     EQU   *
         MVC   OPCLDNME+OFF03(AL2),MSGWRIT INSSRT ID OF IGC0710D
OUT      XC    OCPARM(FOUR),OCPARM      ZERO OUT WTGNAD PARM FIELDS
         LA    R13,OPCSAVE              ESTABLISH SAVEAREA ADDRESS
         LR    RPARM,ROPCAVT            SET UP PARM REG
         XCTL  SF=(E,OPCXCTL)           XCTL TO IGC0710D
         EJECT
*
ERRMSG   DS    0H
         DC    AL1(ENDERR-*-1)          LENGTH
         DC    CL17'IED034I',CL17'HAS NO',CL7'OPTION'  MESSAGE TEXT
ENDERR   EQU   *
TRANSHEX DC    C'0123456789ABCDEF'      TRANSLATION
TRANSDEC DC    C'0123456789******'           TABLES
MSG      DS    0H
         DC AL1(ENDMSG-*-1)             LENGTH
         DC    CL17'IED035I',CL16'OPTION',C'DATA='   MESSAGE TEXT
ENDMSG   EQU   *
OI       OI    FOURTWO(RWKAREA),ZERO    EXECUTED
MOVEFROP MVC   FOURTWO(ONE,RWKAREA),ZERO(RWORK)        EXECUTED
MOV1DF   MVC   ZERO(ONE,R14),ZERO(R15)  EXECUTED
UNPK1DF  UNPK  FOURTWO(ONE,RWKAREA),OPCDOUBL(EIGHT)     EXECUTED
H4       DC    H'4'                     LENGTH CHECK
ALLBLEN  DC    H'52'                    LENGTH OF MSG PAST =
ERMSG    DC    C'03'                    ERROR MSG ID
MSGWRIT  DC    C'07'                    ID OF MSG WRITER
CHAR     EQU   X'00'                    MASK TO CHECK FOR CHARACTER
CHARACTR EQU   C'C'                     MASK TO CHECK FOR CHARACTER
DEC      EQU   C'D'                     MASK TO CHECK FOR DECIMAL
HEXFLG   EQU   C'X'                     MASK TO CHECK FOR HEXADECIMAL
INVALCMD DC    C'TP,OPTIO'              CMD THAT'S INVALID
ALLB     DC    C'**ALL BLANKS**'        MESSAGE TEXT
TRAN     TR    ZERO(ONE,R14),ZERO(RWORK2)    EXECUTED
MOVEMSG  MVC   OPCFOUR(OPCZERO,RWKAREA),OPCONE(R1) MOVE MSG TO WKAREA
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
         EJECT
         TAVTD
         EJECT
         TTNTD
         EJECT
         TTRMD
         EJECT
         TPRFD
         EJECT
         TPRIOR
         EJECT
         END
