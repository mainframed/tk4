         TITLE 'IGCD410D - DISPLAY INTERCEPTED'
IGCD410D CSECT
*A000000-999999                                                  S99240
*A768000,860000                                                 OX02205
***********************************************************************
*                                                                     *
*TITLE--'IGCD410D TO PROCESS DISPLAY INTER OPERATOR CONTROL COMMANDS  *
*                                                                     *
*  MODULE NAME = IGCD410D                                             *
*                                                                     *
*  DESCRIPTIVE NAME = DISPLAY INTERECPTED                             *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS = CHANGE LEVEL 5                                            *
*                                                                     *
*STATUS--CHANGE LEVEL 5                                          S22024
*                                                                     *
*FUNCTION -- THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH   *
*   REQUEST DISPLAY OF THE LIST OF TERMINALS WHICH ARE CURRENTLY      *
*   BEING HELD.                                                       *
*                                                                     *
*   THE COPY HELD TERMINALS ROUTINE GETS THE ADDRESS OF THE           *
*   TERMINAL NAME TABLE FROM THE AVT AND STEPS THROUGH IT TO TEST     *
*   EACH ENTRY TO DETERMINE WHETHER IT IS BEING HELD (INTERCEPTED).   *
*   IF IT IS A PROCESS ENTRY (X'20' ON IN TRMSTATE) IT CANNOT BE      *
*   HELD.  IF AN ENTRY IS BEING HELD, THE ROUTINE SETS A BIT TO       *
*   INDICATE AN ENTRY FOUND AND PLACES THE NAME OF THE ENTRY IN THE   *
*   RESPONSE MESSAGE.  WHEN THE END OF THE TERMNAME TABLE IS          *
*   REACHED, THE BIT IS TESTED TO DETERMINE WHETHER ANY ENTRIES       *
*   WERE HELD. IF NO ENTRIES ARE HELD, THE ROUTINE LOADS INTO REGISTER*
*   0 AN APPROPRIATE RESPONSE MESSAGE NUMBER AND XCTLS TO IGC0310D THE*
*   ERROR WRITER ROUTINE. IF THE                                      *
*   BIT IS ON, IT IS TURNED OFF AND THE GOOD RESPONSE MESSAGE IS      *
*   RETURNED TO THE MESSAGE WRITER IGC0710D. THE MESSAGE IS BUILT IN  *
*   THE OPERATOR CONTROL RESIDENT WORK AREA AND THE ADDRESS IS PASSED *
*   IN THE OPCAVT.                                                    *
*                                                                     *
*ENTRY POINTS --                                                      *
*        'IGCD410D TO PROCESS DISPLAY INTER COMMANDS.                 *
*   GAINS CONTROL FROM IGCD010D VIA AN XCTL.                          *
*                                                                     *
*INPUT -- REGISTER 1 - ADDRESS OF OPERATOR CONTROL AVT.               *
*                                                                     *
*OUTPUT-- REGISTER 1 ADDRESS OF OPCAVT                                *
*                                                                     *
*EXTERNAL ROUTINES -- N/A.                                            *
*                                                                     *
*EXITS,NORMAL -- COMMAND IS PROCESSED.  A RESPONSE MESSAGE EITHER     *
*   INDICATING NO HELD ENTRIES OR CONTAINING A LIST OF HELD ENTRIES   *
*   IS CONSTRUCTED AND ITS ADDRESS RETURNED TO EITHER IGC0310D OR     *
*   IGC0710D IN OPCAVT                                                *
*                                                                     *
*   NORMAL EXIT MESSAGES ARE-                                         *
*   IED039I NO STATIONS INTERCEPTED                                   *
*   IED040I INTERCEPTED STATIONS='STATNAME,...'                       *
*                                                                     *
*EXITS,ERROR -- N/A.                                                  *
*                                                                     *
*TABLES/WORK AREAS--OPCAVT,TCAM AVT,TERMNAME TABLE,TERMINAL ENTRY,    *
*   OPCE,RESIDENT WORK AREA                                           *
*                                                                     *
*ATTRIBUTES-- SERIALLY REUSABLE,SUPERVISOR MODE,DISABLED,TRANSIENT    *
*   (TYPE 4),NON-REENTRANT,REFRESHABLE                                *
*                                                                     *
*CHARACTER CODE DEPENDENCY                                            *
*        THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        *
*        REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS        *
*        EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING     *
*        HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER          *
*        CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE     *
*        FOR THE NEW DEFINITION.                                      *
*                                                                     *
*NOTES - COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS      *
*        PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                  *
*                                                                     *
*        THIS IS A MODULE RE-WRITE OF IEDQCK AS OF OS/TCAM LEVEL II   *
***********************************************************************
*
         SPACE 3
*        REGISTER EQUATES
R0       EQU   0                        REG ZERO                 S22024
RPARM    EQU   1                        PARAMETER                S22024
R1       EQU   1                        PARAMETER
ROPC     EQU   2                        OPERATOR CONTROL AVT BASE
ROPCAVT  EQU   2                        OPERATOR CONTEOL AVT     S22024
RADDR    EQU   3                        WORK
RWKAREA  EQU   13                       OPERATOR CONTROL WORKAREA REG
ROPCE    EQU   3                        OPERATOR CONTROL ELEMENT
RAVT     EQU   4                        AVT BASE
RTNT     EQU   5                        TERMNAME TABLE BASE
RMSG     EQU   6                        MESSAGE BUILDING BASE
RWORK    EQU   7                        WORK
RWORK2   EQU   8                        WORK
RWORK3   EQU   9                        WORK
RTERM    EQU   10                       TERM ENTRY BASE
RCTR     EQU   11                       COUNTER
RBASE    EQU   12                       ROUTINE BASE
R13      EQU   13                       SAVE AREA
R14      EQU   14                       RETURN
R15      EQU   15                       ENTRY POINT
         USING IEDQAVTD,RAVT            ADDRESSABILITY FOR AVT
         USING IEDQOPCD,ROPC       ADDRESSABILITY FOR OP CTL
         USING IEDQTNTD,RTNT            ADDRESSABILITY FOR TNT
         USING IEDQTRM,RTERM            ADDRESSABILTIY FOR TERM
         USING IEDQOPCE,ROPCE      OPC CTRL ELEM ADDRESSABILITY
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE                  TELL THE ASSEMBLER
IGCD410D IEDHJN SKIPID
         LR    ROPC,R1                  GET OPERATOR CONTROL AVT
         L     RAVT,OPCAVTPT       GET AVT BASE
         L     ROPCE,OPCCOPCE      GET OPC CTRL ELEM BASE
         L     RTNT,AVTRNMPT            GET TERMNAME TABLE
         L     RWKAREA,OPCWORK     GET ADDR WORKAREA
         LA    RMSG,FOUR(RWKAREA)     GET ADDR OF MSG AREA
         LA    RCTR,MSGBUF(RMSG)   GET ADDR END OF WORKAREA
         LA    RWORK,ONE           SET UP COUNTER
         LH    RWORK2,TNTLEN            GET NUMBER OF TERMINALS
         SR    R1,R1               CLEAR FOR COMPARE IF NOMSG
         SR    RWORK3,RWORK3            CLEAR
         IC    RWORK3,TNTENLEN          GET ENTRY LENGTH
         LA    RTNT,TNTFIRST            GET TO FIRST ENTRY
         TM    OCPARM,NOTFIRST     SUBSEQUENT ENTRY
         BO    REENTER             YES
         LA    R15,GOODMSG         GET ADDR OF MSG
         SR    R14,R14             CLEAR REGISTER
         IC    R14,LENOFF(R15)     GET LENGTH OF MSG
         STH   R14,LENOFF(RWKAREA)   STORE LENGTH IN WORKAREA
         BCTR  R14,ZERO                 DECREMENT FOR EXECUTE
         EX    R14,MOVEMSG         MOVE MSG TO WORKAREA
         LA    RMSG,MSGLEN(RMSG)   BUMP PTR TO POSSIBLE 1ST ENTRY
LOOP     EQU   *
         CR    RWORK,RWORK2             END OF TABLE
         BH    EXITCODE                YES - COMPLETE MESSAGE
*
         LR    RTERM,RTNT               SET WORK REG
         AR    RTERM,RWORK3             GET TO ADDRESS
         MVC   OPCTRMWA+ONE(THREE),ZERO(RTERM) PUT ON BDRY
         L     RTERM,OPCTRMWA      GET TERM ADDRESS
         TM    TRMSTATE,PROCN           PROCESS ENTRY
         BO    BUMP                     YES - CANNOT BE HELD
         TM    TRMDSORG,TRMLGB          LINE GROUP BLOCK         S22024
         BO    BUMP                     YES, BRANCH              S22024
         TM    TRMSTATE,TRMHELDN        TERMINAL INTERCEPTED
         BNO   BUMP                     NO - BUMP TO NEXT ENTRY
*
         TM    OCSWITCH,XEIGHTY         SWITCH ALREADY SET
         BO    MOVENAME            YES SO MOVE NAME INTO MSG
         OI    OCSWITCH,TERMFND    TURN ON TERM FOUND BIT
MOVENAME EQU   *
         BCTR  RWORK3,ZERO              DECREMENT FOR EXECUTE
         EX    RWORK3,MOVE              MOVE NAME TO MSG
*
         LA    RWORK3,ONE(RWORK3)  BUMP LENGTH TO ORIGINAL VALUE
         AR    RMSG,RWORK3              ADD TO MSG OFFSET
         LA    RMSG,TWO(RMSG)      INCREMENT FOR BLANK
         CR    RMSG,RCTR                ENC OF MSG
         BNL   SETUP               PREPARE TO EXIT AND THEN
*                                  RETURN TO THIS MODULE
BUMP     EQU   *
         LA    RTNT,THREE(RWORK3,RTNT) BUMP PAST ADDRESS
         LA    RWORK,ONE(,RWORK)    INCREMENT COUNTER
         B     LOOP                     CARRY ON
*
EXITCODE EQU   *
         TM    OCSWITCH,TERMFND    ANY ENTRIES
         BO    EXIT                YES
*
         TM    OCPARM,NOTFIRST          IS THIS 1ST TIME ENTERED
         BO    EXIT                     NO
         LA    R0,THIRTY9               GET NUM OF MSG
         MVC   OPCLDNME+THREE(TWO),ERRWRIT  INSERT ID OF IGC0310D
         B     ERROR               PREPARE TO EXIT
EXIT     EQU   *
         LR    R1,RWKAREA               SAVE ADDR OF WORKAREA
         LA    RWKAREA,FOUR(RWKAREA)    GET ADDR OF START OF MSG
         SR    RMSG,RWKAREA             CALCULATE LEN OF MSG
         STH   RMSG,LENOFF(R1)          INSERT LEN IN WORKAREA
         XC    OCPARM(FOUR),OCPARM      CLEAR WTG AND PARM FIELDS
OUT      MVC   OPCLDNME+OFF03(AL2),MSGWRIT INSERT 07 FOR XCTL
ERROR    LR    RPARM,ROPCAVT       PUT OPCAVT IN REG1
         XCTL  SF=(E,OPCXCTL)      XCTL TO MSG WRITER IGC0701D
*
*
*
SETUP    EQU   *                   SET OCPARM AND OCWTG FIELDS SO
*                                  THIS RTN CAN REGAIN CONTROL
         ST    RTNT,OCLINK         SAVEADDR OF NXT TERM TO CHECK
         NI    OCSWITCH,XFE             TURN OFF TERM FOUND BIT
         ST    RWORK2,OCELEM       SAVE COUNT
         STH   RWORK,OCLINENO           SAVE NUM OF TRERM WORKING ON
         LR    R1,RWKAREA               SAVE ADDR OF WORKAREA
         LA    RWKAREA,FOUR(RWKAREA)    GET ADDR OF START OF MSG
         SR    RMSG,RWKAREA             CALCULATE LENGTH OF MSG
         STH   RMSG,LENOFF(R1)          SAVE LENGTH IN WORKAREA
         MVC   OCWTG(TWO),ID            WILL INSURE BRANCH BACK
         OI    OCPARM,NOTFIRST     INDICATES NOT FIRST TIME ENT
         B     OUT                 XCTL TO IGC0701D
         SPACE 3
REENTER  EQU   *                   ENTERED MORE THAN ONCE
         LA    RTNT,MSGBUF-FOUR         SET LOOP COUNTER        OX02205
BLNKLOOP EQU   *                                                OX02205
         MVI   ZERO(RMSG),BLANK         CLEAR MESSAGE AREA FOR  OX02205
         LA    RMSG,ONE(,RMSG)              ADDITIONAL ENTRIES  OX02205
         BCT   RTNT,BLNKLOOP                IF ANY FOUND        OX02205
         LA    RMSG,FOUR(RWKAREA)       RESTORE MSG AREA ADDR   OX02205
         L     RTNT,OCLINK         RESTORE ADDR
         NI    OCSWITCH,XFE             TURN OFF TERM FOUND BIT
         L     RWORK2,OCELEM       RESTORE COUNT
         LA    RWORK2,ZERO(RWORK2) CLEAR HI ORDER BYTE
         LH    RWORK,OCLINENO           GET NUM WORKING WITH
         B     LOOP                CHECK ENTRIES
         EJECT
*        EQUATES
ZERO     EQU   0                   OFFSET
ONE      EQU   1                        OFFSET                   S22024
AL2      EQU   2                   OFFSET
TWO      EQU   2                   OFFSET
OFF03    EQU   3                   OFFSET
THREE    EQU   3                   OFFSET
FOUR     EQU   4                   OFFSET
MSGBUF   EQU   476                      LEN OF MSG AREA
*                                  END OF MESSAGE AREA
THIRTY9  EQU   39                       VALUE THIRTY9
XFE      EQU   X'FE'                    HEX 'FE'
XEIGHTY  EQU   X'80'                    HEX '80'
NOTFIRST EQU   X'80'               THIS IS FIRST TIME BIT
MSGLEN   EQU   30                  OFFSET TO 1ST ENTRY
TERMFND  EQU   X'80'               TERMINAL FOUND BIT
T9       EQU   33                       OFFSET                   S22024
PROCN    EQU   X'20'                    FLAG                     S22024
LENOFF   EQU   0                   0 DISPLACEMENT INTO WORKAREA
BLANK    EQU   X'40'                                            OX02205
MOVE     MVC   ZERO(ZERO,RMSG),ZERO(RTNT) MOVE NAME INTO MESSAGE
MOVEMSG  MVC   ZERO(ZERO,RMSG),ONE(R15) MOVE MSG INTO WK AREA
         SPACE 3
*        MESSAGES
GOODMSG  DS    0H
         DC    AL1(EGOODMSG-*-1)        LENGTH
         DC    CL149'IED040I INTERCEPTED STATIONS='  MESSAGE TEXT
EGOODMSG EQU   *
         SPACE 3
*        CONSTANTS
ERRWRIT  DC    C'03'               ID OF MOD IGC0310D ERROR WRITER
MSGWRIT  DC    C'07'               ID  FOR IGC0701D
ID       DC    C'D4'               ID OF IGCD410D
         EJECT
*        MACROS
         TAVTD
         TOPCAVTD
         TOPCED
         TTNTD
         TTRMD
         END
