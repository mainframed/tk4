         TITLE 'IGCM410D - MODIFY INTERVAL/3705 NAKLIM/SSPAUSE'
*A000000-999999                                                  S99240
*C002000,010000,012000                                           S22024
*A078100-079000,096500-097500,104600-105200,150000-151500,170000 S22024
*A300100-301500,312000-314000,346600-347200,546184-548180        S22024
*A548240-549740,550100-551200,692300-693200,694100-695500        S22024
*A301500-301900,374000-378000,495000,542000,548520-548530      @OY12649
*A548580-548590,549660-549670,549750-549870,576500-577500      @OY12649
*A609000,621460-621700                                         @OY12649
*C490000                                                       @OY14486
*A484000                                                       @OY14486
*A300000,36200                                                 @OX14773
*C430000,432000,605000                                         @OZ31597
***********************************************************************
*                                                                     *
*                                                                     *
*STATUS -- CHANGE LEVEL  010                                   @Y17XARJ
*                                                                     *
*                                                                     *
* FUNCTION -THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH    *
*   REQUEST CHANGE TO NEG RESP LIMIT, SERVICE SEEKING PAUSE, OR  S22024
*   REQUEST ACTIVATION OF THE SYSTEM INTERVAL OR MODIFICATION OF THE  *
*   VALUE OF THE SYSTEM OR POLLING INTERVAL. THIS MODULE IS ENTERED   *
*   FROM THE MODIFY SCAN/MAP ROUTINES (IGCMA10D & IGCM010D), OR  S22024
*   THE BTU DISPATCHER ROUTINE,IGC0210D, WHEN A RESPONSE BTU FOR S22024
*   NAKLIM/SSPAUSE IS TO BE PROCESSED.
*                                                                     *
*   THIS ROUTINE CHECKS THE OPCE TO DETERMINE WHETHER POLLING OR      *
*   SYSTEM INTERVAL IS BEING ALTERED. IF THE COMMAND IS TO CHANGE THE *
*   SYSTEM INTERVAL VALUE, THE INTERVAL INTERVAL SPECIFIED BY THE     *
*   COMMAND IS TESTED FOR ZERO OR GREATER THAN THE MAXIMUM OF 65,535. *
*   IF EITHER, AN ERROR EXIT IS TAKEN. IF THE FIELD AVTHI IS ZERO,    *
*   IMPLYING THAT THE SYSTEM INTERVAL IS NOT SUPPORTED, AN ERROR      *
*   RESPONSE IS INDICATED AND THE ROUTINE EXITS TO THE ERROR MESSAGE  *
*   MODULE (IGC0310D). OTHERWISE THE NEW INTERVAL VALUE IS CONVERTED  *
*   AND STORED IN THE AVT AT AVTINTLV.                                *
*                                                                     *
*   IF THE POLL INTERVAL IS TO BE MODIFIED, THE INTERVAL VALUE IS     *
*   CONVERTED AND TESTED FOR ZERO AND GREATER THAN 255. IN EITHER CASE*
*   AN ERROR INDICATION IS SET AND XCTL IS TO IGC0310D. THE LINE IS   *
*   TESTED FOR DAIL OR NOT OPEN. IF DAIL OR NOT OPEN, AN ERROR IS     *
*   INDICATED AND XCTL IS TO IGC0310D. OTHERWISE THE NEW INTERVAL IS  *
*   IN THE DCB (DCBINTVL) AND THE ROUTINE EXITS TO THE INCIDENT       *
*   CHECKPOINT MODULE (IGC0610D).                                     *
*                                                                     *
*   IF THE SYSTEM INTERVAL IS TO BE ACTIVATED,(SYSTEM,WITH NO DATA    *
*   SPECIFIED) AVTHI IS TESTED FOR ZERO. IF ZERO, THE SYSTEM INTERVAL *
*   FUNCTION IS NOT SUPPORTED, AN ERROR IS INDICATED AND XCTL IS TO   *
*   IGC0310D. IF SYS INT FUNCTION IS SUPPORTED, A CROSS PARTITION     *
*   POST PARAMETER LIST IS BUILT TO ACTIVATE IEDQHI (THE INTERVAL     *
*   CONTROL MODULE) AND THE AQCTL MACRO (SVC 102) ISSUED TO POST THE  *
*   INTERVAL PROCESSOR.   RESPONSE MESSAGE NUMBER 93 IS SET IN OCPARM *
*   M1 IS SET IN OCWTG AND XCTL IS TO THE OP CTL INCIDENT CHECKPOINT  *
*   INTERFACE ROUTINE (IGC0610D).                                     *
*                                                                S22024
*   IF THE NAKLIM OR SSPAUSE IS TO BE MODIFIED, THIS ROUTINE     S22024
*   FORMATS A 3705 REQUEST BTU AND DISPATCHES THE BTU TO THE     S22024
*   3705 BY XCTLING TO ROUTINE IGC0210D. WHEN THE RESPONSE BTU   S22024
*   IS RECEIVED FROM THE 3705, VIA IGC0210D, IT IS EXAMINED FOR  S22024
*   ERRORS. IF NO ERROR OCCURED, THE PROPER SUCCESS RESPONSE IS  S22024
*   SET IN OCPARM, THE MESSAGE MODULE NAME IS SET IN OCWTG, AND  S22024
*   XCTL IS TO THE OP CTL INCIDENT CHECKPOINT INTERFACE ROUTINE  S22024
*   (IGC0610D). IF THE RESPONSE BTU INDICATES AN ERROR CONDITION S22024
*   THE ERROR RESPONSE IS INDICATED AND XCTL IS TO IGC0310D.     S22024
*   THIS MODULE WILL ALSO DISPLAY THE POLLING INTERVAL OUT OF  @OY12649
*   LINE DCB FOR EP. FOR NCP, THE SERVICE SEEKING PAUSE AND    @OY12649
*   NAK LIMIT WILL BE DISPLAYED AFTER ISSUEING A DISPLAY LINE  @OY12649
*   COMMAND BTU TO OBTAIN THE INFORMATION. THESE DISPLAY       @OY12649
*   FUNCTIONS WILL BE DONE AFTER A D TP,POLL,STATNAME HAS BEEN @OY12649
*   ISSUED.                                                    @OY12649
*                                                                     *
*   MESSAGE INITIATED BY THIS ROUTINE ARE:                            *
*     ERROR RESPONSE -                                                *
*        IED090I XXXXXXXX IS NOT A SINGLE ENTRY                       *
*        IED045I SYSTEM INTERVAL ALREADY ACTIVE                       *
*        IED016I STATION XXXXXXXX NOT FOUND                           *
*        IED061I POLLING DELAY FOR XXXXXXXX CANNOT BE ALTERED         *
*        IED011I SYSTEM INTERVAL CANNOT BE ALTERED                    *
*        IED017I LINE XXXXXXXX NOT OPEN                               *
*        IED199I CONTROL CMD XX MOD XX FOR XXXXXXXX ERROR        S22024
*                RESPONSE XXXX                                   S22024
*        IED400I 3705 XXXXXX NOT ACTIVE COMMAND REJECTED       @Y17XARZ
*        IED508I POLLING DELAY FOR XXXXXXXX CANNOT BE DISPLAYED@OY12649
*     SUCCESS RESPONSE -                                              *
*        IED093I SET SYSTEM INTERVAL COMMAND ACCEPTED                 *
*        IED047I SYSTEM INTERVAL IS XXXXXXXX                          *
*        IED048I POLLING DELAY FOR XXXXXXX IS XXXX                    *
*        IED197I SSPAUSE FOR XXXXXXXX IS XXX                     S22024
*        IED198I NAKLIM FOR XXXXXXXX IS XXX                      S22024
*        IED507I FOR XXXXXXXX SSPAUSE IS XXX AND NAKLIM IS XXX @OY12649
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGCM410D IS THE ONLY ENTRY POINT FOR THIS MODULE            *
*                                                                     *
* INPUT- REG 1 CONTAINES A POINTER TO THE OPERATOR CONTROL AVT.       *
*                                                                     *
* OUTPUT- PASSES OP CTL AVT ADDRESS IN REG 1.                         *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         SVC 7(XCTL)                                                 *
*   IEDQCA'S SUBROUTINES                                              *
*        TRMOFLOC--VALIDITY CHECKS TERMINAL NAME PASSED TO IT. RETURNS*
*                  ADDRESS OF TERMINAL ENTRY IF VALID. IF NOT VALID   *
*                  RETURNS PLUS 4 OF RETURN ADDRESS IN REGISTER 14.   *
*                                                                     *
*        DCBLOCAT--VALIDITY CHECKS HARDWARE LINE ADDRESSES, GROUPNAMES*
*                  AND RELATIVE LINE NUMBERS. ALSO INSURES LINE DCBS  *
*                  ARE OPEN, RLN IS NOT MORE THAN NUMBER OF EXTENTS,  *
*                  AND LINE WAS NOT OPENED DUMMY. FOR ANY OF THESE    *
*                  ERRORS RETURNS PLUS 4 OF RETURN ADDRESS IN R14.    *
*                             GOOD RETURN- R0= UCB ADDRESS            *
*                                          R1= DCB ADDRESS            *
* EXITS,NORMAL - FOR NAKLIM AND SSPAUSE OPERATIONS, EXIT IS TO   S22024
*   THE  BTU DISPATCHER MODULE (IGC0210D) TO XMIT THE REQUEST    S22024
*   TO THE 3705.  UPON COMPLETION OF ALL FUNCTIONS PERFORMED BY  S22024
*   THIS MODULE, A  SUCCESSFUL MESSAGE INDICATION IS PLACED IN   S22024
*   THE ELEMENTS OCPARM FIELD, THE ELEMENTS OCWTG FIELD IS SET TO M1  *
*   AND AN XCTL (SVC 7) IS MADE TO THE OP CTL INCIDENT CHECKPOINT     *
*   ROUTINE (IGC0610D), REG 1 = @ OP CTL AVT.                         *
*                                                                     *
* EXITS,ERROR - THE ERROR MESSAGE NUMBER IS PLACED IN REGISTER 0,     *
*   THE OP CTL AVT ADDRESS IN REG 1 AND XCTL IS TO THE OP CTL ERROR   *
*   MESSAGE MODULE (IGC0310D).                                        *
*                                                                     *
* TABLES/WORK AREAS - OPERATOR CONTROL AVT, TCAM AVT, OPCE, OPERATOR  *
*   CONTROL RESIDENT WORK AREA, LGB AND BTU.                     S22024
* ATTRIBUTES - SUPERVISOR MODE, DISABLED, TRANSIENT(TYPE 4 SVC), NON- *
*             REENTRANT, SERIALLY REUSABLE, REFRESHABLE.              *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT *
*    TO THAT USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED SO  *
*    THAT REDEFINITION OF CHARACTER CONSTANTS, BY REASSEMBLY, WILL    *
*    RESULT IN A CORRECT MODULE FOR THE NEW DEFINITION.               *
*                                                                     *
* NOTES -                                                             *
*    COMMUNICATIONS BETWEEN THE VARIOUS MODULES/TRANSIENTS IS         *
*    PRIMARILY THROUGH THE OPERATOR CONTROL ADDRESS VECTOR TABLE (AVT)*
*                                                                     *
*        THIS IS A MODULE RE-WRITE OF IEDQCZ AS OF OS/TCAM LEVEL II   *
***********************************************************************
         EJECT
IGCM410D CSECT
         SPACE
RZERO    EQU   0                        PARM REG 0
RPARM    EQU   1                        PARM REG 1
ROPCAVT  EQU   2                        OP CONTROL AVT POINTER
ROPCE    EQU   3                        CURRENT ELEMENT ADDRESS
RAVT     EQU   4                        TCAM AVT ADDRESS
RWORK5   EQU   5                        WORK REGISTER
RSAVT    EQU   6                        TCAM SAVT ADDRESS      @YM06110
RWORK6   EQU   6                        WORK REGISTER
RTNT     EQU   7                        TNT BASE REG
RTERM    EQU   8                        TERM TABLE POINTER REG
RQCB     EQU   9                        QCB BASE REG
RDCB     EQU   10                       DCB BASE REG
RLCB     EQU   11                       LCB BASE REG
RBASE    EQU   12                       MODULE BASE
RSAVE    EQU   13                       SAVE AREA REG
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RBRNCH   EQU   15                       SUBROUTINE BRANCH ENTRY POINT
         SPACE
         SPACE
*****   MISCELLANEOUS EQUATES    ********
ZERO     EQU   0                        ZERO VALUE
ONE      EQU   1                        VALUE ONE
TWO      EQU   2                        VALUE TWO
VARINAME EQU   3                        OFFSET TO CHANGE XCTL NAME
THREE    EQU   3                        VALUE THREE
FOUR     EQU   4                        VALUE FOUR
SIX      EQU   6                        VALUE SIX                S22024
SEVEN    EQU   7                        VALUE SEVEN
EIGHT    EQU   8                        VALUE EIGHT
NINE     EQU   9                        VALUE NINE
TEN      EQU   10                       VALUE TEN              @Y17XARJ
ELEVEN   EQU   11                       VALUE ELEVEN
TWELVE   EQU   12                       VALUE TWELVE
SIXTEEN  EQU   16                       VALUE SISTEEN
SEVNTEEN EQU   17                       VALUE SEVENTEEN
EIGHTEEN EQU   18                       VALUE EIGHTEEN
FORTY5   EQU   45                       VALUE FORTY FIVE
FORTY7   EQU   47                       VALUE FORTY SEVEN
FORTY8   EQU   48                       VALUE FORTY EIGHT
SIXTY1   EQU   61                       VALUE SIXTY ONE
NINTY    EQU   90                       VALUE NINTY
NINTY1   EQU   91                       VALUE NINTY ONE
NINTY3   EQU   93                       VALUE NINTY THREE
BLANK    EQU   C' '                     BLANK CHARACTER
A        EQU   C'A'                     CHARACTER A
X0E      EQU   X'0E'                    HEX 0E
X0F      EQU   X'0F'                    HEX 0F
PROC     EQU   X'20'                    PROCESS TERM FLAG
DIST     EQU   X'40'                    DISTRIBUTION TERM FLAG
PRISYSD  EQU   X'DC'                    PRIORITY
FOXOH    EQU   X'F0'                    HEX F0
FOX9H    EQU   X'F9'                    HEX F9                 @OX14773
FORTEEN  EQU   14                       VALUE FOURTEEN           S22024
X01      EQU   X'01'                    RETURN BTU FLAG          S22024
X04      EQU   X'04'                    3705 DCB FLAG            S22024
X10      EQU   X'10'                    HEX '10'                 S22024
X60      EQU   X'60'                    GOOD RESP FLAG           S22024
THIRTY4  EQU   34                       VALUE THIRTY FOUR        S22024
X80      EQU   X'80'                    HEX 80                   S22024
X84      EQU   X'84'                    HEX EIGHTY FOUR          S22024
X85      EQU   X'85'                    HEX EIGHTY FIVE          S22024
ESS      EQU   C'S'                     CHARACTER S              S22024
EN       EQU   C'N'                     CHARACTER N              S22024
ONE99    EQU   199                      VALUE 199                S22024
ONE97    EQU   197                      VALUE 197                S22024
ONE98    EQU   198                      VALUE 198                S22024
FOURHUND EQU   400                      VALUE 400              @Y17XARJ
BTUCLR   EQU   X'0E'                    LENGTH TO CLEAR        @OY12649
BTUOFS   EQU   X'14'                    OFFSET TO START CLEAR  @OY12649
MPOINT   EQU   X'02'                    MULTIPOINT LINE        @OY12649
DPOLL    EQU   X'68'                    DISPLAY COMMAND        @OY12649
DISPMSG  EQU   507                      DISPLAY SSP & NAKLIM   @OY12649
XF0      EQU   X'F0'                    COVER SIGN WITH        @OY12649
FIVE     EQU   5                                               @OY12649
NPOLL508 EQU   508                      CANNOT DISPLAY POLL INT@OY12649
ALLFF    EQU   X'FF'                    ONE'S MASK             @Y17XARJ
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         USING IEDQTNTD,RTNT            TERM NAME ADDRESSABILITY
         USING IEDNTRM,RTERM            TERM ADDRESSABILITY      S22024
         USING IEDNQCB,RQCB             QCB  ADDRESSABILITY      S22024
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         USING IEDQLCB,RLCB             LCB ADDRESSABILITY
         USING CVT,RWORK5               CVY ADDRESSABILITY
         SPACE
         BALR  RBASE,0                  SET BASE REGISTER
         USING *,RBASE                  MODULE ADDRESSABILITY
IGCM410D IEDHJN SKIPID
****    TEST INTERVAL ENTRY TYPE ROUTINE ****
         LR    ROPCAVT,RPARM            SET BASE FOR OP CTL AVT
         L     RAVT,OPCAVTPT            SET BASE FOR TCAM AVT
         L     ROPCE,OPCCOPCE           PICK UP ELEMENT POINTER
         TM    OCFLAG,OCRESP            TEST FOR RETURN BTU      S22024
         BO    RETRNBTU                 YES, PROCESS RETURNED ELES22024
         L     RTERM,OCELEM             PICK UP TERM ENTRY     @OY12649
         CLI   OCOPTCDE,DPOLL           DISPLAY POLL           @OY12649
         BE    POLL                     YES                    @OY12649
         CLI   OCSWITCH,X0F             CHECK INTERVAL ENTRY TYPE
         BE    SYSNODAT                 SYSTEM W/NO DATA SPECIFIED
         LH    RWORK5,OCBACKUP          PICK UP DATA LENGTH
         LA    RTERM,OCBACKUP+ONE(RWORK5) GET ADDR OF RIGHT DATA BYTE
         SPACE
CLILOOP  EQU   *
         CLI   ZERO(RTERM),FOXOH        NUMERIC CHARACTER
         BL    COMINV                   NO, CMD IS INVALID
         CLI   ZERO(RTERM),FOX9H        NUMERIC CHARACTER      @OX14773
         BH    COMINV                   NO, CMD IS INVALID     @OX14773
         BCTR  RTERM,RZERO              DECREMENT DATA ADDRESS
         BCT   RWORK5,CLILOOP           ALL BYTES CHECKED, NO - LOOP
         LH    RWORK5,OCBACKUP          YES, PICK UP DATA LENGTH
         BCTR  RWORK5,RZERO             DECREMENT FOR EXECUTE
         EX    RWORK5,PACK              PACK DATA
         CVB   RPARM,OPCDOUBL           CONVERT TO BINARY
         CLI   OCSWITCH,X0E             POLL DELAY SPECIFIED
         BE    SETPOLL                  YES, GET TERMINAL ENTRY
         LTR   RPARM,RPARM              CK DATA FOR ZERO
         BZ    COMINV                   YES, CMD INVALID
         C     RPARM,SYSMAX             MAXIMUM VALUE EXCEEDED
         BH    COMINV                   YES, INVALID CMD - REJECT
         L     RWORK6,AVTHI             INTERVAL CODE ADDRESS
         LA    RWORK6,ZERO(RWORK6)      CLEAR HI-ORDER BYTE
         LTR   RWORK6,RWORK6            INTERVAL SPECIFIED
         BZ    REJECT1                  NO, REJECT COMMAND
         STH   RPARM,AVTINTLV           RESET INTERVAL
         B     SYINTDAT                 GO SET - INT IS XXXXX MESSAGE
         SPACE
****    INTERVAL TYPE SYSTEM NO DATA    ****
SYSNODAT EQU   *
         TM    AVTBIT1,AVTDLAYN         SYSTEM INTERVAL ACTIVE
         BO    SETMSG45                 YES, SET SYS INT ALRDY ACTIVE
         L     RWORK5,CVTPTR            SET CVT BASE
         L     RWORK5,CVTAQAVT-CVT(RWORK5)  TCX ADDRESS        @ZM46812
         L     RWORK5,TCXRPT-IEDQTCX(RWORK5) TSO CONTROL BLK   @ZM46812
         LA    RWORK5,ZERO(RWORK5)      CLEAR FOR TEST         @ZM46812
         LTR   RWORK5,RWORK5            TSO IN SYSTEM          @ZM46812
         BNZ   REJECT                   YES-DO NOT ALLOW INTV  @ZM46812
         L     RWORK5,AVTHI             GET INTERVAL CODE ADDR
         LA    RWORK5,ZERO(RWORK5)      CLEAR HI-ORDER BYTE
         LTR   RWORK5,RWORK5            CODE IN SYSTEM
         BZ    REJECT                   NO, REJECT CMD
         CLC   AVTINTLV(TWO),AVTFZERO   INTERVAL ZERO
         BE    REJECT                   YES, REJECT COMMAND
         ST    RWORK5,OPCSAVE           BUILD
         SR    RWORK5,RWORK5             PARAMETER
         ST    RWORK5,OPCSAVE+FOUR        LIST
         MVI   OPCSAVE+FOUR,PRISYSD        FOR
         LA    RWORK5,OPCSAVE               CROSS
         ST    RWORK5,OPCAQCTL               PARTITION
         ST    RWORK5,OPCAQCTL+FOUR           POST
         MVI   OPCAQCTL,TWELVE
         MVI   OPCAQCTL+FOUR,EIGHT
         LA    RPARM,OPCAQCTL
         AQCTL
         B     SYINTMSG                 GO SET SYS INT CMD ACCEPTED
         SPACE
****    INTERVAL TYPE IS POLL ****
SETPOLL  EQU   *
         C     RPARM,MAXVAL             MAXIMUM VALUE EXCEEDED
         BH    COMINV                   YES, INVALID CMD - REJECT
         L     RTNT,AVTRNMPT            SET TNT BASE
         LH    RTERM,OCTRMTBL           GET TNT OFFSET FROM ELEMENT
         SLL   RTERM,SIXTEEN            CLEAR OUT HI-ORDER
         SRL   RTERM,SIXTEEN              SIXTEEN BITS
         SR    RDCB,RDCB                CLEAR REG
         IC    RDCB,TNTENLEN            PICK UP ENTRY LENGTH
         LA    RQCB,THREE(RDCB)         ALLOW FOR ADDRESS
         BCTR  RTERM,RZERO              REDUCE TNT OFFSET BY ONE
         MR    RTERM,RTERM              CALCULATE OFFSET
         LA    RTERM,TNTFIRST           GET ADDRESS OF FIRST ENTRY
         AR    RTERM,RQCB               ADD OFFSET TO FIRST ENTRY
         MVC   OCMODNME,BLANKS          BLANK OCMODNME FIELD     S22024
         BCTR  RDCB,RZERO               DECREMENT NAME LENGTH  @OY14486
         EX    RDCB,MOVE                MOVE IN TERM NAME
         LA    RTERM,ONE(RDCB,RTERM)    POINT TO ENTRY ADDR    @OY14486
         MVC   OPCSAVE+ONE(THREE),ZERO(RTERM) MVE ADDR TO BOUNDRY
         L     RTERM,OPCSAVE            LOAD TERM ENTRY ADDRESS
POLL     EQU   *                                               @OY12649
         LA    RTERM,ZERO(RTERM)        CLEAR HI-ORDER BYTE
         LR    RWORK5,RPARM             SAVE INTERVAL VALUE
         LA    RPARM,IEDQTRM-IEDNTRM    GET  NUMBER PREFIX BYTES S22024
         SR    RTERM,RPARM              THEN ADJUST TERM BASE    S22024
         TM    TRMSTATE,PROC+DIST       DISTRIBUTION,PROCESS OR CASCADE
         BNZ   NOTSNGL                  YES, REJECT COMMAND
         TM    TRMSTATE,TRMPREF         TEST 3705 MODE SET     @Y17XARJ
         BO    CKDIAL                   YES, CHECK OLD IRON    @Y17XARJ
         L     RQCB,TRMDESTQ-ONE        GET QCB ADDR FROM TERM
         LA    RPARM,IEDQQCB-IEDNQCB    GET  NUMBER PREFIX BYTES S22024
         SR    RQCB,RPARM               THEN ADJUST QCB BASE     S22024
         L     RDCB,QCBDCBAD-ONE        GET DCB ADDR FROM QCB
         CLI   DCBTIOT,A                LINE OPEN
         BNL   LNENOP                   NO, CANNOT CHANGE INTERVAL
         USING DEBNMSUB,RPARM
         L     RPARM,DCBDEBAD           GET DEB ADDRESS
         CLC   DEBNMEXT,QCBRELLN        CK FOR RLN GREATER THEN NUM EXT
         BL    LNENOP                   YES, NOT OPEN
         SR    RZERO,RZERO              CLEAR REG 0
         IC    RZERO,QCBRELLN           GET RLN
         SR    RPARM,RPARM              CLEAR REG 1
         IC    RPARM,DCBEIOBX           GET INDEX TO IOB
         MR    RZERO,RZERO              GET INDEX TO THIS IOB
         L     RLCB,DCBIOBAD            FIRST IOB ADDRESS
         LA    RLCB,ZERO(RLCB)          CLEAR HI-ORDER BYTE
         AR    RLCB,RPARM               GET THIS IOB ADDRESS
         LA    RPARM,LCBFLAG1-IEDQLCB
         SR    RLCB,RPARM               BACK UP TO LCB
         TM    LCBSTAT2,LCBDIAL         DAIL LCB
         BO    INVALID                  YES, REJECT COMMAND
         CLI   OCOPTCDE,DPOLL           DISPLAY POLL           @OY12649
         BNE   CHNGPOLL                 NO                     @OY12649
         SR    RWORK5,RWORK5            CLEAR REGISTER         @OY12649
         IC    RWORK5,DCBINTVL          PICK UP INTERVAL       @OY12649
         BAL   RRET,CONVERT             CONVERT THE DATA       @OY12649
         B     PODLYMSG                 PUT OUT POLL DELAY     @OY12649
CHNGPOLL EQU   *                                               @OY12649
         STC   RWORK5,DCBINTVL          PUT INTERVAL IN DCB
         B     PODLYMSG                 BR TO IND POLL DLAY CHNGD
         EJECT
         SPACE
CKDIAL   EQU   *                                                 S22024
         CLI   TRMTYPE,TRMCLUST         ERROR IF NOT SOME      @Y17XARJ
         BL    NOTSNGL                  TYPE OF PRE SNA        @Y17XARJ
         CLI   TRMTYPE,TRMPSNA          TERMINAL               @Y17XARJ
         BH    NOTSNGL                                         @Y17XARJ
         CLI   OCOPTCDE,DPOLL           DISPLAY POLL           @OY12649
         BE    TSTDIAL                  TEST DIAL              @OY12649
         CLI   OCBACKUP+SEVEN,EN        TEST NAKLIM              S22024
         BE    NAKLIM                   ANSR YES,BYPASS DIAL CK  S22024
TSTDIAL  EQU   *                                               @OY12649
         TM    TRMBYTE0,TRMDIAL         TEST DIAL TERMINAL     @Y17XARJ
         BO    INVALID                  ANSR YES, REJECT COMMAND S22024
NAKLIM   EQU   *                                                 S22024
         LA    RSAVE,OPCSAVE            SET UP SAVE AREA       @Y17XARJ
         LA    RPARM,TRMPRFSZ(RTERM)    SET R1 AS TTE PTR      @Y17XARJ
         L     RBRNCH,OPCHORT           ADDR OF COHORT LOOK    @Y17XARJ
TRYAGAIN EQU   *                                               @Y17XARJ
         BALR  RRET,RBRNCH              CALL COHORT LOOK       @Y17XARJ
         LTR   RZERO,RZERO              IS TTE IN HIERARCHY    @Y17XARJ
         BZ    MSG400                   NO,3705 ACT ERROR      @Y17XARJ
         LR    RTERM,RPARM              EST ADDRESSABILITY     @Y17XARJ
         LA    RRET,TRMPRFSZ            BACK UP TO NEG         @Y17XARJ
         SR    RTERM,RRET               PREFIX                 @Y17XARJ
         CLI   TRMTYPE,TRMNSDLC         IS THIS A PRESNA LINE  @Y17XARJ
         BNE   TRYAGAIN                 IF NOT TRY AGAIN       @Y17XARJ
         STH   RZERO,OCTRMTBL           SAVE TTCIN OF LINE     @Y17XARJ
         CLI   OCOPTCDE,DPOLL           DISPLAY POLL           @Y17XARJ
         BE    DCB3705                  YES                    @Y17XARJ
         USING IEDNSVTD,RSAVT           SAVT ADDRESSABILITY
         L     RSAVT,AVTSAVTP           GET SAVT ADDRESS       @Y17XARJ
         L     RBRNCH,SAVTTNTX          GET NAT CONV RTNE      @Y17XARJ
         LR    RPARM,RZERO              PUT TTCIN IN REG1      @Y17XARJ
         BALR  RRET,RBRNCH              CALL TTCIN TO NA SUBR  @Y17XARJ
         STH   RBRNCH,OCRSID            SAVE NA OF LINE        @Y17XARJ
         SPACE 2
**************************************************************** S22024
*  THIS ROUTINE OBTAINS A UNIT AND SETS UP A  SSPAUSE/NAKLIM     S22024
*  BTU.  THEN STORES THE ADDRESS OF THIS BTU IN THE ELEMENT      S22024
*  AND SET UP XCTL TO IGC0210D.                                  S22024
**************************************************************** S22024
         USING IEDPRUND,RPARM           SET UP PRU ADDRESS     @Y17XARJ
DCB3705  EQU   *                                                 S22024
         LA    RPARM,ONE                SET NUMBER OF UNITS REQ. S22024
         L     RBRNCH,OPCGETBF          LOAD GET BUFF RTN ADDR   S22024
         BALR  RRET,RBRNCH              LINK TO GET BUFFER       S22024
         ST    RPARM,OCUNIT             SET BFR ADR TO ELEMENT   S22024
         LA    RWORK6,PRUNLEN           NEG PREFIX LEN         @Y17XARJ
         SR    RPARM,RWORK6             BACK UP TO NEG PREFIX  @Y17XARJ
         MVC   PRURHFG0(FOUR),FID1PRUH SET JP PREFIX           @Y17XARJ
         XC    PRURHFG0+FOUR(PRUNLEN-FOUR),PRURHFG0+FOUR       @Y17XARJ
*                                       CLEAR REST OF PREFIX   @Y17XARJ
         MVC   PRUTIC,INVALTIC          INDICATE ONE UNIT      @Y17XARJ
         LA    RWORK6,SIX               SET UP RU LENGTH       @Y17XARJ
         STH   RWORK6,PRUDATCT                                 @Y17XARJ
         CLI   OCOPTCDE,DPOLL           DISPLAY POLL           @Y17XARJ
         BE    DLINERU                  YES                    @Y17XARJ
         LR    RWORK6,RPARM             SAVE PTR TO RU         @Y17XARJ
         MVC   PRUDATA(THREE),SSPRU     MOVE BASE RU           @Y17XARJ
         MVC   PRUDATA+THREE(TWO),OCRSID MOVE DATA             @Y17XARJ
         STC   RWORK5,PRUDATA+FIVE      MOVE INTERVAL          @Y17XARJ
         STC   RWORK5,OCMODNME+8        SAVE INTERVAL IN OPCE  @Y17XARJ
         L     RBRNCH,OPCHORT           GET ADDR OF COHORT LOOK@Y17XARJ
         LA    RPARM,TRMPRFSZ(RTERM)    SET R1 AS TTE PTR      @Y17XARJ
         BALR  RRET,RBRNCH              CALL COHORT LOOK       @Y17XARJ
         LR    RPARM,RWORK6             RESTORE PTR TO RU      @Y17XARJ
         STH   RZERO,PRUTTCIN           SET UP NCP AS DEST     @YM0XXXX
         MVI   OCSWITCH,OCCHGSSP        ASSUME SSPAUSE COMMND  @Y17XARJ
         CLI   OCBACKUP+SEVEN,ESS       SSPAUSE                @Y17XARJ
         BE    SETSND                   YES                    @Y17XARJ
         MVI   PRUDATA+TWO,TWO          MAKE NAKLIMIT CMMD     @Y17XARJ
         MVI   OCSWITCH,OCCHGNP         SET OCSWITCH AS NAKLMT @Y17XARJ
         B     SETSND                   SEND RU                @Y17XARJ
DLINERU  EQU   *                                               @Y17XARJ
         NI    PRUFIDN,ALLFF-PRUTHFD1   MARK RU AS FID0        @Y17XARJ
         MVC   PRUTTCIN,OCTRMTBL        SET LINE AS TARGET     @Y17XARJ
         MVC   PRUDATA(THREE),DISPSESS  BUILD DISPLAY SESSN    @OZ31597
*                                       RU                     @Y17XARJ
         XC    PRUDATA+THREE(THREE),PRUDATA+THREE CLEAR RU     @OZ31597
         SPACE
SETSND   EQU   *                                                 S22024
         MVC   OCWTG(TWO),THISMOD       SET RETURN TO THIS MOD   S22024
         MVI   OCALSTAT,ZERO            RST ACT/DACT FLAG      @Y17XARJ
         MVC   OPCLDNME+VARINAME(TWO),BTUDISP SET BTU DISPATCHER S22024
         B     EXIT                     GO XCTL TO BTU DISP MOD  S22024
         SPACE 2
**************************************************************** S22024
*  THIS ROUTINE GETS CONTROL WHEN IT IS DETERMINED THAT THE      S22024
*  BTU BEING PROCESSED IS A RETURNED BTU AND SETS UP TO INDICATE S22024
*  RESULTS OF THE SSPAUSE/NAKLIM REQUEST.                        S22024
**************************************************************** S22024
RETRNBTU EQU   *                                                 S22024
         L     RPARM,OCUNIT             GET BTU ADDR             S22024
         LA    RWORK6,PRUNLEN           SET NEG PREFIX LEN     @Y17XARJ
         SR    RPARM,RWORK6             POINT TO NEG PREFIX    @Y17XARJ
         TM    PRURHFG0,PRURHSDI        ERROR ON RESPONSE      @Y17XARJ
         BO    NOWAY                    GO SET CANNOT ALTER    @Y17XARJ
         CLI   OCOPTCDE,DPOLL           DISPLAY POLL           @Y17XARJ
         BE    DISPLAY                  YES                    @OY12649
         CLI   PRUDATA+TWO,FOUR         SSPAUSE?               @Y17XARJ
         BE    SSPSTD                   YES, GO SET SSPAUSE CHNG S22024
         B     SNKLMSTD                 GO SET NAKLIM CHNGD      S22024
DISPLAY  EQU   *                                               @OY12649
         CLI   PRUDATA+FOUR,X60         CHECK SYSTEM RESPONSE  @Y17XARJ
         BNE   NOWAY                    GO SET CANNOT DISPLAY  @Y17XARJ
         TM    PRUDATA+EIGHT,MPOINT     MULTIPOINT LINE        @Y17XARJ
         BZ    INVALID                  NOT MULTIPOINT LINE    @OY12649
         SR    RWORK5,RWORK5            CLEAR                  @OY12649
         IC    RWORK5,PRUDATA+TEN       SSPAUSE VALUE          @Y17XARJ
         BAL   RRET,CONVERT             CONVERT IT             @OY12649
         MVC   OPCDOUBL(THREE),OCMODNME+FIVE SAVE SSP          @OY12649
         IC    RWORK5,PRUDATA+ELEVEN    NAKLIM VALUE           @Y17XARJ
         BAL   RRET,CONVERT             CONVERT IT             @OY12649
         MVC   OCMODNME(THREE),OPCDOUBL PUT VALUE IN           @OY12649
         LA    RZERO,DISPMSG            DISPLAY MESSAGE        @OY12649
         MVC   OPCLDNME+VARINAME(TWO),WRTM3 SET UP MSG MOD     @OY12649
         B     EXIT                     XCTL TO MESSAGE MODULE @OY12649
****    SET MESSAGE SECTION    ****
MSG400   EQU   *                                               @Y17XARJ
         MVC   OCBACKUP,OCMODNME        GET STATION NAME         S22024
         LA    RZERO,FOURHUND           SET 3705 NOTACTIVE     @Y17XARJ
         B     XCTLERR                  TAKE ERROR MSG EXIT      S22024
SSPSTD   EQU   *                                                 S22024
         LA    RZERO,ONE97              SET SSPAUSE STARTED      S22024
         B     OKEXITM3                 PREPARE TO EXIT          S22024
SNKLMSTD EQU   *                                                 S22024
         LA    RZERO,ONE98              SET NAKLIM STARTED       S22024
         B     OKEXITM3                 PREPARE TO EXIT          S22024
NOWAY    EQU   *                                                 S22024
         LA    RZERO,ONE99              SET ERROR RESP MESSAGE   S22024
         B     XCTLERR                  TAKE ERROR MSG EXIT      S22024
REJECT1  EQU   *
         MVC   OCBACKUP(NINE),ALT       SET ALTERED FOR MSG ELEVEN
         B     MSG11                    GO PREPARE FOR MSG
         SPACE
REJECT   EQU   *
         MVC   OCBACKUP(NINE),ACT       SET ACTIVATED FOR MSG ELEVEN
         SPACE
MSG11    EQU   *
         LA    RZERO,ELEVEN             SET INT CANNOT BE ALT/ACT
         B     XCTLERR                  TAKE ERROR MSG EXIT
         SPACE
INVALID  EQU   *
         LA    RZERO,SIXTY1             SET POLL DLY CANNOT BE ALTERED
         CLI   OCOPTCDE,DPOLL           DISPLAY POLL           @OY12649
         BNE   XCTLERR                  NO                     @OY12649
         LA    RZERO,NPOLL508           CANNOT POLL MESSAGE    @OY12649
         B     DSPMSG                   XCTL TO MSG MOD        @OY12649
         SPACE
SETMSG45 EQU   *
         LA    RZERO,FORTY5             SET PARAMETER FOR MSG MOD
         B     XCTLERR                  GO XCTL TO MESSAGE MOD
         SPACE
SYINTDAT EQU   *
         LA    RZERO,FORTY7             SET SYS INTERVAL IS XXXXX
         B     OKEXIT                   PREPARE TO EXIT
         SPACE
PODLYMSG EQU   *
         LA    RZERO,FORTY8             SET POLL DLY FOR XXXXXXXX IS XX
         B     OKEXIT                   PREPARE TO EXIT
*                                                                     *
         SPACE
SYINTMSG EQU   *
         LA    RZERO,NINTY3             SET SYS INTVL CMD ACCEPTED
DSPMSG   EQU   *                                               @OY12649
         MVC   OPCLDNME+VARINAME(TWO),WRTMOD MOD NAME FOR XCTL
         B     EXIT                     GO XCTL
         EJECT
***********************************************************************
*  THE FOLLOWING CODE MOVES THE TERMINAL NAME FROM OCMODNME TO   S22024
*  OCBACKUP AND THE DATA FROM OCBACKUP TO OCMODNME. ALSO, THE    S22024
*  DATA IS ADJUSTED LEFT FOLLOWED BY BLANKS.                     S22024
***********************************************************************
OKEXITM3 EQU   *                                                 S22024
         XC    OCMODNME,OCBACKUP        FLIP                     S22024
         XC    OCBACKUP,OCMODNME          OCMODNME AND           S22024
         XC    OCMODNME,OCBACKUP            OCBACKUP             S22024
         LH    RPARM,OCMODNME           GET DATA LENGTH          S22024
         BCTR  RPARM,ZERO               DECREMENT FOR EX         S22024
         EX    RPARM,MOVE1              MOVE DATA LEFT           S22024
         LA    RBRNCH,SIX               GET NUMBER OF BLANKS     S22024
         SR    RBRNCH,RPARM                  TO MOVE - 1         S22024
         LA    RWORK6,OCMODNME+ONE      GET STARTING POINT       S22024
         AR    RPARM,RWORK6                  FOR BLANKS          S22024
         EX    RBRNCH,MOVE2             MOVE IN BLANKS           S22024
         SPACE
         MVC   OCWTG,WRTM3              MOD NAME FOR XCTL        S22024
         B     XCTLMOD                  GO XCTL                  S22024
*                                                                     *
CONVERT  EQU   *                                               @OY12649
         CVD   RWORK5,OCMODNME          CONVERT TO PACKED DEC  @OY12649
         UNPK  OCMODNME(EIGHT),OCMODNME(EIGHT) ZONED DECIMAL   @OY12649
         OI    OCMODNME+SEVEN,XF0       COVER SIGN             @OY12649
         BR    RRET                     RETURN                 @OY12649
         SPACE 2
OKEXIT   EQU   *                                                 S22024
         MVC   OCWTG,WRTMOD             MOD NAME FOR XCTL        S22024
*                                                                     *
         SPACE 2
XCTLMOD  EQU   *                                                 S22024
         STH   RZERO,OCPARM             STORE MSG CODE FOR MSG   S22024
*                                       MODULE                   S22024
         MVC   OPCLDNME+VARINAME(TWO),CKPTMOD SET XCTL TO CKPOINT MOD
*                                                                     *
         SPACE
EXIT     EQU   *
         LR    RPARM,ROPCAVT            PASS OP CTL AVT PTR
         XCTL  SF=(E,OPCXCTL)           CALL NEXT MODULE
         SPACE
XCTLERR  EQU   *
         MVC   OPCLDNME+VARINAME(TWO),ERRWRT SET ERROR RESPONSE
         B     EXIT                     EXIT
*                                                                     *
         SPACE
COMINV   EQU   *
         LA    RZERO,EIGHTEEN           SET CMD INVALID
         B     XCTLERR                  EXIT TO ERROR MSG MOD
*                                                                     *
         SPACE
NOTSNGL  EQU   *
         LA    RZERO,NINTY              SET NOT SINGLE ENTRY MSG
         B     XCTLERR                  EXIT TO ERR MSG MOD
*                                                                     *
         SPACE
LNENOP   EQU   *
         MVC   OPCDOUBL(EIGHT),OCMODNME MOVE STATNAME FOR ARR MSG MOD
         LA    RZERO,NINTY1             SET LINE NOT OPEN FOR MSG
         B     XCTLERR                  GO EXIT ERR
         SPACE
PACK     PACK  OPCDOUBL(EIGHT),OCBACKUP+TWO(ZERO) PACK           S22024
MOVE     MVC   OCMODNME(ZERO),ZERO(RTERM) MOVE                   S22024
MOVE1    MVC   OCMODNME(ZERO),OCMODNME+TWO MOVE                  S22024
MOVE2    MVC   ZERO(ZERO,RPARM),BLANKS  MOVE                     S22024
         SPACE
MAXVAL   DC    F'255'                   MAXIMUM POLLING DELAY
SYSMAX   DC    F'65535'                 MAXIMUM SYSTEM INTERVAL
BLANKS   DC    CL8' '                   BLANKS                   S22024
ALT      DC    C'ALTERED  '             ALTERED CONSTANT
ACT      DC    C'ACTIVATED'             ACTIVATED CONSTANT
CKPTMOD  DC    C'06'                    CHECKPOINT MOD NAME
ERRWRT   DC    C'03'                    ERROR MESSAGE MOD NAME
WRTMOD   DC    C'M1'                    MODIFY O.K. WRIGHT MOD NAME
WRTM3    DC    C'M3'                    MESSAGE MODULE NAME      S22024
THISMOD  DC    C'M4'                    THIS MODULE NAME         S22024
BTUDISP  DC    C'02'                    BTU DISPATCHER MOD NAME  S22024
FID1PRUH DC    X'0B80001C'              PRU PREFIX FLAGS FOR:  @Y17XARJ
*                                       FM DATA, FMID          @Y17XARJ
*                                       BC,EC,DR1,FID1,        @Y17XARJ
*                                       BS,ES                  @Y17XARJ
INVALTIC DC    X'08000002'              INVALID TIC            @Y17XARJ
SSPRU    DC    X'010004'                SSPAUSE RU TEXT        @Y17XARJ
DISPSESS DC    X'080140'                DISPLY SESSN RU TEXT   @OZ31597
         ORG   IGCM410D+X'7FF'          PATCH AREA             @Y17XARJ
         DC    X'FF'                    PATCH AREA               S22024
         ORG
         EJECT
         EJECT
CVT      DSECT
         CVT
         EJECT
         TLCBD
         EJECT
         TQCBD
         TPRUD                                                 @Y17XARJ
         EJECT
         TTRMD
         EJECT
         TLGBD
         EJECT
         DCBD   DSORG=TX
         EJECT
         TDEBD
         EJECT
         TAVTD
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
         EJECT
         TTNTD
         EJECT
         TTCXD                                                 @ZM46812
         END
