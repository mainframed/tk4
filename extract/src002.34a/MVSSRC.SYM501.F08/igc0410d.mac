         TITLE 'IGC0410D - ERROR MESSAGE GENERATOR (STATNAME)'
IGC0410D CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C000000-999999                                                  S99240
*C 784400                                                       SA62994
***********************************************************************
*                                                                     *
*  TITLE: 'IGC0410D - ERROR MESSAGE GENERATOR ( STATNAME) '           *
*                                                                     *
*  MODULE NAME = IGC0410D                                             *
*                                                                     *
*  DESCRIPTIVE NAME = OP CONTROL ERROR MSG MODULE 2                   *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 4                                            *
*                                                                     *
*FUNCTION -                                                           *
*        THIS MODULE TAKES THE ERROR MESSAGE NUMBER PASSED IN         *
*        REGISTER ZERO AND DETERMINES IF THAT ERROR MESSAGE IS CONTAIN*
*        -ED IN THIS MODULE.  IF NOT, AN XCTL TO IGC0510D IS EXECUTED *
*        FOR MESSAGE LOCATION.  IF THE MESSAGE IS CONTAINED HERE,  THE*
*        MESSAGE IS MOVED TO THE RESIDENT WORKAREA AT OFFSET 4.  THE  *
*        LENGTH OF THE MESSAGE IS PLACED IN THE WORKAREA AT OFFSET 0. *
*        ANY INFORMATION NEEDED IN THE MESSAGE IS PLACED DIRECTLY IN  *
*        THE WORKAREA.  CONTROL IS THEN PASSED TO IGC0710D FOR OUTPUT.*
*                                                                     *
*ENTRY POINTS -                                                       *
*        IGC0410D IS THE ONLY ENTRY POINT OF THIS MODULE.             *
*                                                                     *
*INPUT -                                                              *
*        POINTER TO THE OPERATOR CONTROL AVT IN REGISTER 1 AND        *
*        AN ERROR MESSAGE NUMBER IN REGISTER 0.                       *
*                                                                     *
*OUTPUT -                                                             *
*        ERROR MESSAGES CONTAINING ADDRESS OR DDNAME ERRORS           *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*        SVC 7 (XCTL)                                                 *
*                                                                     *
*EXITS,NORMAL --                                                      *
*              XCTL TO IGC0710D OR IGC0510D                           *
*EXITS,ERROR -- NONE.                                                 *
*              NONE                                                   *
*                                                                     *
*TABLES/WORKAREAS -                                                   *
*        TCAM AVT, OP CONTROL AVT, ERRORTAB,OPCE                      *
*                                                                     *
*ATTRIBUTES -                                                         *
*        REFRESHABLE,DISABLED,REUSABLE,TRANSIENT(TYPE 4),SUPERVISOR   *
*        MODE                                                         *
*                                                                     *
*NOTES -                                                              *
*CHARACTER CODE DEPENDENCY --                                         *
*        THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        *
*        REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH           *
*        IS EQUIVALENT TO THE ONE USED AT ASSEMBLY                    *
*        TIME.  THE CODING HAS BEEN ARRANGED SO THAT REDEFINITION     *
*        OF CHARACTER CONSTANTS, BY REASSEMBLY WILL RESULT IN A       *
*        CORRECT MODULE FOR THE NEW DEFINITION.                       *
*                                                                     *
*NOTES --                                                             *
*        COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES         *
*        IS PRIMARILY THROUGH THE OPERATOR CONTROL AVT.               *
*                                                                     *
*        THIS IS A MODULE RE-WRITE OF IGC0410D AS OF OS/TCAM LEVEL II *
***********************************************************************
         EJECT
         SPACE
*                              E Q U A T E S
         SPACE
RZERO    EQU   0                        PARAMETER REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
ROPCAVT  EQU   2                        OP CONTROL AVT POINTER
ROPCE    EQU   3                        CURRENT ELEMENT ADDRESS
RAVT     EQU   4                        TCAM AVT ADDRESS
RWORKA   EQU   5                        WORKAREA ADDRESS
RMSGPT   EQU   6                        POINTER TO ERROR MESSAGE
RMSGLEN  EQU   7                        LENGTH OF CURRENT ERROR MESSAGE
ROFF     EQU   8                        STATNAME OFFSET
RTNT     EQU   9                        BASE FOR TERM NAME TABLE
RWORK    EQU   10                       WORKREGISTER
RWORK1   EQU   11                       WORK REGISTER
RMSGNO   EQU   11                       NUMBER OF ERROR MESSAGE
RBASE    EQU   12                       MODULE BASE REGISTER
         SPACE 2
ADDROFF  EQU   2                        OFFSET IN TABLE TO MESSAGE ADDR
COMMA    EQU   X'6B'                    COMMA
DDIN     EQU   X'10'                    DDNAME PROCESSED IN COMMAND
DDOFF    EQU   0                        OFFSET IN MESSAGE TO DDNAME
DDSIZE   EQU   8                        MAX DDNAME SIZE
EIGHT    EQU   8                        LENGTH OF EIGHT
ENDTABLE EQU   255                      END OF TABLE INDICATOR
ENTLEN   EQU   4                        LENGTH OF ERROR TABLE ENTRY
LASTCHAR EQU   7                        OFFSET TO LAST DIGIT
LENOFF   EQU   0                        OFFSET TO MESSAGE LENGTH
MAXLEN   EQU   7                        MAXIMUM CLEARED SPACE
MESSAGE  EQU   2                        OFFSET TO MESSAGE PAST LENGTH
MSGLOC   EQU   4                        OFFSET TO MESSAGE IN WORKAREA
NAMELEN  EQU   2                        LENGTH OF NAME CHANGE
NEXT     EQU   1                        OFFSET OF 1
NULL     EQU   0                        ZERO VALUE FOR EXECUTES
NUMOFF   EQU   0                        OFFSET IN TABLE TO MESSAGE NO.
OFFOFF   EQU   1                        OFFSET TO STATNAME OFFSET
PICKUP   EQU   5                        OFFSET TO CONVERTED DATA
RLNOFF   EQU   9                        OFFSET TO RLN
RLNSIZE  EQU   3                        MAX RLN SIZE
SETCOMMA EQU   8                        OFFSET TO COMMA IN DATA
THREE    EQU   3                        LENGTH OF THREE
VARINAME EQU   3                        OFFSET TO CHANGE XCTL NAME
ZONE     EQU   X'F0'                    COVER FOR SIGN
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         USING IEDQTNTD,RTNT            TERM NAME ADDRESSABILITY
         SPACE
         BALR  RBASE,RZERO         SET BASE REGISTER
         USING *,RBASE                  MODULE ADDRESSABILITY
BASE     EQU   *
IGC0410D IEDHJN  SKIPID
         LR    ROPCAVT,RPARM            SET UP AVT ADDRESSING
         L     RAVT,OPCAVTPT            TCAM AVT ADDRESS
         L     ROPCE,OPCCOPCE           GET CURRENT ELEMENT POINTER
         L     RWORKA,OPCWORK           GET WORKAREA POINTER FROM AVT
         MVC   OPCLDNME+VARINAME(NAMELEN),LOADID NEXT LOAD MODULE
         LR    RMSGNO,RZERO             PICK UP PASSED MESSAGE NUMBER
         LA    RMSGPT,ERRORTAB          PICK UP ERROR MESSAGE TABLE ADR
*              FIND THE CORRECT ERROR MESSAGE TO PRINT OUT            *
LOCATMSG EQU   *
         CLI   NUMOFF(RMSGPT),ENDTABLE  CHECK FOR END OF TABLE
         BE    XCTL                     YES, CALL NEXT LOAD OF ERRORS
         SPACE
         CH    RMSGNO,NUMOFF(RMSGPT)    CORRECT ERROR MESSAGE?
         BE    PROCMSG                  EQUAL, PROCESS MESSAGE
         SPACE
         LA    RMSGPT,ENTLEN(RMSGPT)    POINT TO NEXT TABLE POSITION
         B     LOCATMSG                 CHECK NEXT POSITION
         SPACE
PROCMSG  EQU   *
         LH    RMSGPT,ADDROFF(RMSGPT)   GET ADDRESS OF ERROR MESSAGE
         AR    RMSGPT,RBASE             ADD BASE TO OFFSET
         SR    RMSGLEN,RMSGLEN          CLEAR FOR LENGTH INSERT
         IC    RMSGLEN,LENOFF(RMSGPT)   PICK UP MESSAGE LENGTH
         STH   RMSGLEN,LENOFF(RWORKA)   PLACE ERROR MESSAGE LENG IN WA
         BCTR  RMSGLEN,RZERO            DECREMENT FOR MOVE
         EX    RMSGLEN,MOVEMSG          MESSAGE TO WORKAREA
         SPACE
         SR    ROFF,ROFF                CLEAR TO PICK UP OFFSET
         IC    ROFF,OFFOFF(RMSGPT)      PICK UP OFFSET
         LTR   ROFF,ROFF                ZERO OFFSET?
         BE    WRTXCTL                  YES, NO DATA REQUIRED
         SPACE
         LA    RWORKA,MSGLOC(ROFF,RWORKA) CALCULATE NAME OFFSET ADDR
         CH    RMSGNO,MSGNO19           MESSAGE 19?
         BE    TESTTERM                 YES, CHECK DATA IN COMMAND
         SPACE
         CH    RMSGNO,MSGNO25           STOPPED MSG?
         BE    TESTTERM                 YES, CHECK DATA IN COMMAND
         SPACE
         CH    RMSGNO,MSGNO156          CONCENTRATOR ERROR MESSAGE
         BE    TESTTERM                 PICK UP STATION NAME FROM ELEM
         SPACE
         CH    RMSGNO,MSGNO46           OUTPUT ONLY MESSAGE?
         BNE   GETNAME                  NO, INSERT TERMINAL NAME
         SPACE
TESTTERM EQU   *
         MVC   DDOFF(DDSIZE,RWORKA),OCBACKUP PLACE DDNAME IN MSG
         TM    OCSWITCH,DDIN            DDNAME IN COMMAND?
         BNO   WRTXCTL                  NO, CALL MESSAGE WRITER
         SPACE
         TM    OCFLAG,OCLINE            LINE PROCESSED IN DATA
         BO    WRTXCTL                  YES, DATA HAS BEEN SET
         SPACE
         MVC   RLNOFF(RLNSIZE,RWORKA),ALL SET RLN TO ALL
         SR    RWORK,RWORK              CLEAR FOR INSERT
         IC    RWORK,OCRLN              PICK UP RELATIVE LINE NO
         LTR   RWORK,RWORK              RLN SPECIFIED?
         BZ    WRTXCTL                  PUT OUT MESSAGE
         SPACE
         MVI   SETCOMMA(RWORKA),COMMA   SET COMMA IN DATA
         CVD   RWORK,OPCDOUBL           CONVERT RLN
         UNPK  OPCDOUBL(EIGHT),OPCDOUBL(EIGHT) UNPACK RLN
         OI    OPCDOUBL+LASTCHAR,ZONE   COVER SIGN
         MVC   RLNOFF(RLNSIZE,RWORKA),OPCDOUBL+PICKUP PLACE RLN
         B     WRTXCTL                  CALL WRITER
*              FIND TERMINAL NAME AND PUT IN WORKAREA                 *
         SPACE
GETNAME  EQU   *
         LH     RWORK,OCTRMTBL          GET TERM NAME TABLE OFFSET
         N     RWORK,AVTCLRHI           CLEAR HIGH BYTES
         LTR   RWORK,RWORK              ZERO OFFSET (MEANS SYSCON)
         BZ    WRTXCTL                  YES, SYSCON (MSG CORRECT)
         SPACE
         MVI   NULL(RWORKA),AVTEBLNK    MOVE FIRST BLANK
         MVC   NEXT(MAXLEN,RWORKA),NULL(RWORKA) BLANK REST OF FIELD
         BCTR  RWORK,RZERO              SUBTRACT ONE FROM OFFSET
         SR    RWORK1,RWORK1            CLEAR REGISTER FOR ENTRY LENGTH
         L     RTNT,AVTRNMPT            PICK UP TNT ADDRESS
         IC    RWORK1,TNTENLEN          PICK UP LENGTH OF TERM NAMES
         LA    RWORK1,THREE(RWORK1)     ADD THREE TO ALLOW FOR ADDRESS
         MR    RWORK,RWORK              CALCULATE OFFSET IN ENTRIES
         LA    RPARM,TNTFIRST           GET ADDRESS OF FIRST TNT ENTRY
         AR    RPARM,RWORK1             ADD OFFSET TO ADDR FOR LOCATION
         IC    RWORK,TNTENLEN           GET NAME LENGTH
         BCTR   RWORK,RZERO             DECREMENT FOR MOVE
         EX    RWORK,MOVENAME           PLACE STATNAME IN OUTPUT
         SPACE
WRTXCTL  EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),WRITE SET UP WRITE MOD NAME
XCTL     EQU   *
         LR    RPARM,ROPCAVT            POINT TO AVT
         SPACE
         XCTL  SF=(E,OPCXCTL)           CALL NEXT MODULE
         SPACE 2
MOVEMSG  MVC   MSGLOC(NULL,RWORKA),MESSAGE(RMSGPT) MOVING MESSAGE TO WA
         SPACE
MOVENAME MVC   NULL(NULL,RWORKA),NULL(RPARM) PLACE NAME IN MESSAGE
*  THE FOLLOWING TABLE CONTAINS THE MESSAGE IDENTIFIERS AND THE       *
*    OFFSET FROM THE BEGINNING OF THIS MODULE TO THE APPROPRIATE      *
*    ERROR MESSAGES.                                                  *
         SPACE 2
ERRORTAB DS    0F
         DC    AL2(19),AL2(MSG19-BASE)
         DC    AL2(25),AL2(MSG25-BASE)
         DC    AL2(42),AL2(MSG42-BASE)
         DC    AL2(44),AL2(MSG44-BASE)
         DC    AL2(46),AL2(MSG46-BASE)
         DC    AL2(52),AL2(MSG52-BASE)
         DC    AL2(53),AL2(MSG53-BASE)
         DC    AL2(61),AL2(MSG61-BASE)
         DC    AL2(60),AL2(MSG60-BASE)
         DC    AL2(88),AL2(MSG88-BASE)
         DC    AL2(90),AL2(MSG90-BASE)
         DC    AL2(91),AL2(MSG91-BASE)
         DC    AL2(95),AL2(MSG95-BASE)
         DC    AL2(101),AL2(MSG101-BASE)
         DC    AL2(156),AL2(MSG156-BASE)
         DC    X'FF'                    LAST ENTRY
         SPACE 2
MSG19    DC    AL1(END19-*-2),AL1(8)
         DC    C'IED019I              ALREADY STARTED'
END19    EQU   *
         SPACE
MSG25    DC    AL1(END25-*-2),AL1(8)
         DC    C'IED025I               ALREADY STOPPED'
END25    EQU   *
         SPACE
MSG42    DC    AL1(END42-*-2),AL1(8)
         DC    C'IED042I SYSCON   ALREADY PRIMARY'
END42    EQU   *
         SPACE
MSG44    DC    AL1(END44-*-2),AL1(8)
         DC    C'IED044I          NOT ELIGIBLE FOR PRIMARY'
END44    EQU   *
         SPACE
MSG46    DC    AL1(END46-*-2),AL1(17)
         DC    C'IED046I LINE FOR              IS OUTPUT ONLY STATION'
END46    EQU   *
         SPACE
MSG52    DC    AL1(END52-*-2),AL1(8)
         DC    C'IED052I          ALREADY SET FOR HOLD'
END52    EQU   *
         SPACE
MSG53    DC    AL1(END53-*-2),AL1(8)
         DC    C'IED053I          ALREADY RELEASED'
END53    EQU   *
         SPACE
MSG60    DC    AL1(END60-*-2),AL1(8)
         DC    C'IED060I          CANNOT BE HELD'
END60    EQU   *
         SPACE
MSG61    DC    AL1(END61-*-2),AL1(26)
         DC    C'IED061I POLLING DELAY FOR          CANNOT BE ALTERED'
END61    EQU   *
         SPACE
MSG88    DC    AL1(END88-*-2),AL1(8)
         DC    C'IED088I          ON DIAL LINE-CANNOT BE VARIED'
*                                                               SA62994
END88    EQU   *
MSG90    DC    AL1(END90-*-2),AL1(8)
         DC    C'IED090I          IS NOT A SINGLE ENTRY'
END90    EQU   *
         SPACE
MSG91    DC    AL1(END91-*-2),AL1(17)
         DC    C'IED091I LINE FOR           NOT OPEN'
END91    EQU   *
         SPACE
MSG95    DC    AL1(END95-*-2),AL1(0)
         DC    C'IED095I MODIFY OLT REJECTED-OLT NOT ACTIVE'
END95    EQU   *
         SPACE
MSG101   DC    AL1(END101-*-2),AL1(0)
         DC    C'IED101I RESTART IN PROGRESS'
END101   EQU   *
         SPACE 2
MSG156   DC    AL1(END156-*-2),AL1(8)
         DC    C'IED156I          ON CONCENTRATOR-CANNOT BE VARIED'
END156   EQU   *
LOADID   DC    C'05'                    NEXT ERROR MODULE
WRITE    DC    C'07'                    WRITE MODULE
ALL      DC    C'ALL'                   RELATIVE LINE NO. OF 'ALL'
MSGNO19  DC    H'19'                    ALREADY STARTED
MSGNO25  DC    H'25'                    ALREADY STOPPED
MSGNO46  DC    H'46'                    OUTPUT ONLY MESSAGE
MSGNO156 DC    H'156'                   CONCENTRATOR ERROR MESSAGE
         TITLE 'IGC0410D - DSECTS REFERENCED BY THIS MODULE'
         TAVTD
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
         EJECT
         TTNTD
         END
