         TITLE 'IGCM010D - MODIFY FUNCTION SCAN/MAP/DISPATCHER'
IGCM010D CSECT
*A000000-999999                                                  S99240
*C012000,014000                                                  S22024
*A289000,292000-293200,322000-337000,464000-584000,690000-696000 S22024
*A702000,712000,720000-724000,726050-726310,726650-726800,726950 S22024
*A727150,727350-727450,727550-727820,730000-780000,852000-861000 S22024
*A726312-726316,727822-727826                                    Y06328
*C736000                                                         Y06330
*A248000,294000,318000,855000                                  @OY12425
*C337000                                                       @OY12425
*D434000-524000                                                @OY12425
*A008000,080000,112000                                         @Y17XARX
*C012000,040000-042000,112000,770000                           @Y17XARX
*D080000-102000,108000-110000,694000,726750                    @Y17XARX
*A726317-726318,727830-727840                                  @Y17XARA
*A726317,727834                                                @OY19405
***********************************************************************
*                                                                     *
* MODULE NAME = IGCM010D (TCAM, OPERATOR CONTROL)              @Y17XARX
*                                                              @Y17XARX
* DESCRIPTIVE NAME = MODIFY FUNCTION SCAN/MAP/DISPATCHER       @Y17XARX
*                                                              @Y17XARX
* COPYRIGHT = NONE                                             @Y17XARX
*                                                                     *
* STATUS = VERSION 10.0                                        @Y17XARX
*                                                                     *
*                                                                     *
*                                                            @YM05654 *
* FUNCTION - THIS MODULE RECEIVES CONTROL FROM IGC0110D OR IGC0010D   *
*   WHENEVER THE COMMAND IS A 'MODIFY'. A VALIDITY CHECK OF THE       *
*   OPERAND IS PERFORMED AND THE FUNCTIONAL MODULE ID IS DETERMINED.  *
*   CERTAIN VALIDITY CHECKING OF LINES ARE PERFORMED USING SUBROUTINES*
*   IN IEDQCA TO DO THE ACTUAL CHECKING. BITS ARE SET IN THE ELEMENT  *
*   (OPCE) TO IDENTIFY THE COMMAND.   IF THE VALIDITY CHECKING        *
*   PERFORMED BY THIS MODULE IS UNSUCESSFUL, THE APPROPRIATE ERROR    *
*   MESSAGE NUMBER IS PLACED IN REGISTER ZERO AND IGC0310D IS GIVEN   *
*   CONTROL TO PUT MESSAGE IN THE WORKAREA.  FURTHER SCANNING AND     *
*   MAPPING IS DONE IN IGCMM10D WHICH GETS CONTROL FROM IGCM010D.     *
*                                                            @YM05654 *
*                                                                     *
*   IN A RESTART SITUATION,NO MAPPING FUNCTIONS ARE REQUIRED AND      *
*   CONTROL IS PASSED DIRECTLY TO THE FUNCTION MODULE AS SOON AS THE  *
*   FUNCTION IS DETERMINED.                                           *
*                                                                     *
* ENTRY POINTS -                                                      *
*   IGCM010D IS THE ONLY ENTRY POINT FOR THIS MODULE                  *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 1 CONTAINS A POINTER TO THE OPERATOR CONTROL AVT AND    *
*   OPERATOR CONTROL RESIDENT WORK AREA CONTAINS THE OP CTL COMMAND   *
*   BEGINNING WITH THE OPERAND.                                       *
*                                                                     *
* OUTPUT -                                                            *
*         NONE                                                        *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         SVC 7(XCTL)                                                 *
*                                                                     *
* EXITS,NORMAL - EXITS TO MODULE IGC0710D FOR OLT OPERAND             *
*               EXITS TO MODULE IGCMM10D FOR FURTHER SCAN OF   @Y17XARX
*               ALL OTHER MODIFY FUNCTIONS                     @Y17XARX
*                                                                     *
* EXITS,ERROR - EXITS TO IGC0310D ERROR MESSAGE MODULE TO WRITE OUT AN*
*              ERROR MESSAGES:                                        *
*                   IED017I LINE XXXXXXXX  NOT OPEN                   *
*                   IED018I XXXXXXXX COMMAND INVALID                  *
*                                                                     *
*                                                                     *
* TABLES/WORK AREAS - OPERATOR CONTROL AVT, TCAM AVT, OPCE, OPERATOR  *
*                    CONTROL RESIDENT WORK AREA.                      *
*                                                                     *
* ATTRIBUTES - SUPERVISOR MODE, DISABLED, TRANSIENT(TYPE 4 SVC), NON- *
*             REENTRANT, SERIALLY REUSABLE, REFRESHABLE.              *
*                                                                     *
*CHARACTER CODE DEPENDENCY                                            *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT *
*    TO THAT USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED SO  *
*    THAT REDEFINITION OF CHARACTER CONSTANTS, BY REASSEMBLY, WILL    *
*    RESULT IN A CORRECT MODULE FOR THE NEW DEFINITION.               *
*                                                                     *
* NOTES -                                                             *
*    COMMUNICATIONS BETWEEN THE VARIOUS MODULES/TRANSIENTS IS         *
*    PRIMARILY THROUGH THE OPERATOR CONTROL ADDRESS VECTOR TABLE (AVT)*
*                                                                     *
***********************************************************************
         EJECT
         SPACE
RZERO    EQU   0                        PARM REG 0
RPARM    EQU   1                        PARM REG 1
ROPCAVT  EQU   2                        OP CONTROL AVT POINTER
ROPCE    EQU   3                        CURRENT ELEMENT ADDRESS
RAVT     EQU   4                        TCAM AVT ADDRESS
RWORKA   EQU   5                        WORK REGISTER
RLEN     EQU   6                        DATA LENGTH
RAPTR    EQU   7                        ADVANCE SCAN PTR
RRPTR    EQU   8                        REAR SCAN PTR
RBYSCN   EQU   9                        NUMBER OF BYTES SCANNED
RSCNRTN  EQU   10                       SCAN ROUTINE RETURN REG
RWRK11   EQU   11                       WORK REGISTER
RBASE    EQU   12                       MODULE BASE
RSAVE    EQU   13                       REG SAVE PTR
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RLINK    EQU   15                       LINK REG
         SPACE
         SPACE
ZERO     EQU   0                        ZERO VALUE
ONE      EQU   1                        VALUE ONE
TWO      EQU   2                        VALUE TWO
NAMELEN  EQU   2                        NAME FIELD LENGTH
THREE    EQU   3                        VALUE THREE
VARINAME EQU   3                        OFFSET TO CHANGE XCTL NAME
FOUR     EQU   4                        VALUE FOUR
FIVE     EQU   5                        VALUE FIVE
SIX      EQU   6                        VALUE SIX
SEVEN    EQU   7                        VALUE SEVEN
EIGHT    EQU   8                        VALUE EIGHT
TWELVE   EQU   12                       VALUE TWELVE
SIXTEEN  EQU   16                       VALUE SIXTEEN
SEVNTEEN EQU   17                       VALUE SEVENTEEN
EIGHTEEN EQU   18                       VALUE EIGHTEEN
BLANK    EQU   C' '                     BLANK CHAR
COMMA    EQU   C','                     COMMA
EQUAL    EQU   C'='                     EQUAL
X80      EQU   X'80'                    HEX '80'
X0E      EQU   X'0E'                    HEX '0E'
XF0      EQU   X'F0'                    HEX 'F0'
FOXFOX   EQU   X'FF'                    HEX 'FF'
MTCODE   EQU   X'51'               CHECKS FOR MT10D            @OY12425
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         USING IGCM010D,RBASE                                  @Y17XARA
A        EQU   IGCM010D                                        @Y17XARA
         LR    RBASE,RLINK              SET BASE REGISTER      @Y17XARA
         SPACE 1                                               @Y17XARA
IGCM010D IEDHJN SKIPID,HJN                                     @Y17XARA
         LR    ROPCAVT,RPARM            SET BASE FOR OP CTL AVT
         L     RAVT,OPCAVTPT            SET BASE FOR TCAM AVT
         L     ROPCE,OPCCOPCE           PICK UP ELEMENT POINTER
         L     RWORKA,OPCWORK           GET ADDR OF WORK AREA
         LA    RRPTR,EIGHT(,RWORKA)     SET PTR TO DATA        @Y17XARA
         LA    RSCNRTN,OPRNDTBL         PICK UP OPERAND TABLE ADDR
         LA    RWRK11,TBLMAP            GET MODULE TABLE ADDRESS
         MVC   OPCLDNME+VARINAME(NAMELEN),SCANMOD XCTL TO SCAN   S22024
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROCESS
         BO    RESTART                  EXIT TO SCAN MODULE      S22024
         CLC   ZERO(THREE,RRPTR),TSO    IS OPERAND TS=         @ZM46678
         BNE   NOTTS                    NO, BRANCH             @ZM46678
         MVC   OPCLDNME+VARINAME(NAMELEN),MODMC SET MOD NAME   @ZM46678
         B     XCTLMOD                  EXIT                   @ZM46678
NOTTS    EQU   *                                               @ZM46678
         TM    OCFLAG,OCTOTE            TOTE INPUT               S22024
         BO    TOTE                     YES, BRANCH              S22024
         LH    RAPTR,TWO(RWORKA)        SET TOTAL LEN OF DATA AREA
         LA    RBYSCN,EIGHT(RWORKA,RAPTR) SET EOM PTR          @OY12425
         MVI   AVTEZERO(RBYSCN),AVTEBLNK SET ENDING BLANK      @OY12425
         SR    RLEN,RLEN                ZERO RLEN
* COMPARISON OF INSTRUCTIONS FOR A MATCH
NXTENTRY EQU   *
         IC    RLEN,ZERO(RWRK11)        PICK UP LEN FOR COMP INST
         BCTR  RLEN,RZERO               REDUCE COUNT FOR EXECUTE CMD
         EX    RLEN,COMP                EXECUTE COMPARE INST
         BE    MATCH                    FOUND ENTRY - BR TO PROCESS
         LA    RWRK11,ONE(,RWRK11)      INDEX TO NEXT ENTRY    @Y17XARA
         CLI   ZERO(RWRK11),FOXFOX      CHECK END OF TBL BEFOR MATCH
         BE    NOMATCH                  YES, ERROR - PREPARE TO EXIT
         LA    RLEN,ONE(,RLEN)          RESTORE BUMP FACTOR    @Y17XARA
         ALR   RSCNRTN,RLEN             BUMP PTR TO NXT FUNCTION ENTRY
         B     NXTENTRY                 CHECK NEXT ENTRY
MATCH    EQU   *                                               @OY12425
         LA    RPARM,TBLMAP             TABLE ADDRESS          @OY12425
         SR    RWRK11,RPARM             CALC TABLE OFFSET      @OY12425
         STC   RWRK11,OCSWITCH          PASS TABLE DISP        @OY12425
         B     RESTART                  GO TO NEXT MOD         @OY12425
         SPACE
TOTE     EQU   *                        PROCESS TOTE INPUT       S22024
         LA    RWRK11,TOTETBL           TOTE VERB TABLE          S22024
         MVC   OPCLDNME+VARINAME(NAMELEN),WRITE SET EXIT NAME    S22024
         LA    RZERO,INVCMD             SET RETURN CODE          S22024
TOTELOOP EQU   *                                                 S22024
         CLI   ZERO(RWRK11),FOXFOX      END OF TABLE             S22024
         BE    XCTLMOD                  YES, BRANCH              S22024
         CLC   ZERO(ONE,RWRK11),OCOPTCDE  GOOD VERB CODE         S22024
         BE    GOODVERB                 YES, BRANCH              S22024
         LA    RWRK11,FOUR(,RWRK11)     INCREMENT TO NXT ENTRY @Y17XARA
         B     TOTELOOP                 SCAN NEXT ENTRY          S22024
GOODVERB EQU   *                                                 S22024
         MVC   OPCLDNME+VARINAME(NAMELEN),TWO(RWRK11) MOD NME    S22024
         MVC   OPCDOUBL(EIGHT),OCBACKUP SET DD OR TERM NAME      S22024
         MVC   OCSWITCH(ONE),ONE(RWRK11) SET CMD TYPE            S22024
         CLI   OCOPTCDE,MTCODE     IS NEXT MOD MT10D?          @OY12425
         BNH   RESTART             YES, PROCESS SWDEVICE       @OY12425
         MVI   OCMODNME,AVTEBLNK  SET BLANK DELIM AFTER FIELD  @OY12425
         MVI   OCSCAN,OCADDR+OCDDNAME DDNAME OR ADDR IN FIELD  @OY12425
         LA    RWRK11,OCBACKUP     SET SCAN PTR FOR OPSCAN     @OY12425
         L     RLINK,OPCDCBLK     SET UP FOR SCAN ROUTINE      @OY12425
         BAL   RRET,FOUR(RLINK)     GOTO OPSCAN                @OY12425
         B     GOODNAME            NAME FOUND                  @OY12425
         B     NOTFOUND            NAME NOTFOUND               @OY12425
         B     NOMATCH             SYNTAX ERROR                @OY12425
GOODNAME EQU   *                                               @OY12425
         MVI   OCSCAN,AVTEZERO    INDICATE TO NXT MOD THAT     @OY12425
*                                  NAME WAS FOUND              @OY12425
         STCM  RPARM,SEVEN,OCELEM+ONE SAVE DCB ADDR            @OY12425
         B     RESTART             GO XCTL                     @OY12425
NOTFOUND EQU   *                                               @OY12425
         OI    OCSCAN,OCNONAME    INDICATE NAME NOT FOUND      @OY12425
         B     RESTART                                         @OY12425
         SPACE
NOMATCH  EQU   *
         LA    RZERO,EIGHTEEN           SET MSG NUM FOR MOD 03
         MVC   OPCLDNME+VARINAME(NAMELEN),ERRWRT ERROR EXIT MOD NME
RESTART  EQU   *                                                 S22024
         STM   ROPCE,RBYSCN,OPCSAVE     SAVE REGS FOR SCAN MOD   S22024
XCTLMOD  EQU   *
         LR    RPARM,ROPCAVT            PASS OP CTL AVT PTR
         XCTL  SF=(E,OPCXCTL)           CALL FUNCTION MODULE
         SPACE
COMP     CLC   ZERO(ZERO,RSCNRTN),ZERO(RRPTR) COMPARE OPERATION
         EJECT
*****     OPERAND TABLE FOR MODIFY COMMAND    ****
OPRNDTBL EQU   *
         DC    C'AUTOPOLL='             AUTOPOLL
         DC    C'BACKUP='               BACKUP                   S22024
         DC    C'BTRACE='               BTU TRACE                S22024
         DC    C'LOAD='                 CHANGE NCP LOAD MOD      S22024
         DC    C'DEBUG=D,'              DEBUGD
         DC    C'DEBUG=L,'              DEBUGL
         DC    C'DUMP='                 3705 DUMP CMD            S22024
         DC    C'INTENSE=TERM,'         INTENSE TERM
         DC    C'INTENSE=LINE,'         INTENSE LINE
         DC    C'INTERVAL='             INTERVAL
         DC    C'IPL='                  3705 IPL CMD             S22024
OLK      DC    C'OLT='                  OLT
         DC    C'OPERATOR='             OPERATOR
         DC    C'OPT='                  OPT
         DC    C'SWBACK='               BACKUP CAPABILITY        S22024
         DC    C'SWITCH='               SWITCH TO ANOTHER 3705   S22024
         DC    C'TIMEDAT='              MODIFY TIME AND DATE     S22024
         DC    C'TRACE='                TRACE
         DC    C'BHSET='                BLOCK HANDLER ASSOC.     S22024
         DC    C'SESSION='              CHANGE SESSION LIMIT     S22024
         DC    C'TRANLMT='              CHANGE TRANSMISSION LMT  S22024
         DC    C'SWDEVICE='             APR                      S22024
         DC    C'ACTIV='                ACTIVATE A 3705          S22024
         DC    C'DEACT='                DEACTIVATE A 3705        S22024
         DC    C'SWAP='                 SWAP DEVICE ID           S22024
         DC    C'CHNGMODE='             CHANGE DIAL MODE         Y06327
         DC    C'SPEED='                CHANGE SPEED             Y06327
         DC    C'LNSWITCH='             SWITCH LINE MODE         Y06328
         DC    C'DTRACE='               START/STOP SUBTASK TRC @Y17XARA
         DC    C'BUFFTR='               CHANGE BUFFER TR OPTION@OY19405
         DC    X'FF'                    END OF TABLE
         EJECT
*   THIS TABLE HAS THE LENGTH OF THE CORRESPONDING OPERANDS IN
*   OPERAND LOOKUP TABLE.  IT IS USED TO DETERMINE HOW MANY CHARS.
*   TO SCAN FOR EACH POSSIBLE COMMAND.
TBLMAP   EQU   *
         DC    AL1(9)                   AUTOPOLL
         DC    AL1(7)                   BACKUP                   S22024
         DC    AL1(7)                   BTRACE                   S22024
         DC    AL1(5)                   CHANGE LOAD              S22024
         DC    AL1(8)                   DEBUGD
         DC    AL1(8)                   DEBUGD
         DC    AL1(5)                   DUMP                     S22024
         DC    AL1(13)                  INTENSET
         DC    AL1(13)                  INTENSEL
         DC    AL1(9)                   INTERNAL
         DC    AL1(4)                   IPL                      S22024
         DC    AL1(4)                   OLT
         DC    AL1(9)                   OPERATOR
         DC    AL1(4)                   OPT
         DC    AL1(7)                   SWBACK                   S22024
         DC    AL1(7)                   SWITCH                   S22024
         DC    AL1(8)                   TIME                     S22024
         DC    AL1(6)                   TRACE
         DC    AL1(6)                   BHSET                    S22024
         DC    AL1(8)                   SESSION                  S22024
         DC    AL1(8)                   TRANS LIMIT              S22024
         DC    AL1(9)                   NCP PEP                  S22024
         DC    AL1(6)                   ACTIVE 3705              S22024
         DC    AL1(6)                   DEACT 3705               S22024
         DC    AL1(5)                   SWAP DEVICE ID           S22024
         DC    AL1(9)                   CHANGE DIAL MODE         Y06327
         DC    AL1(6)                   SPEED SELECT             Y06327
         DC    AL1(9)                   SWITCH LINE MODE         Y06328
         DC    AL1(7)                   START/STOP SUBTASK TRC @Y17XARA
         DC    AL1(7)                   CHANGE BUFFER TR OPTION@OY19405
         DC    X'FF'                    TABLE END
         EJECT
TOTETBL  EQU   *                        TOTE ONLY COMMANDS       S22024
         DC    X'50',AL1(33),C'MT'      SWDEVICE TO PRIMARY      Y06330
         DC    X'51',AL1(32),C'MT'      SWDEVICE TO BACKUP       S22024
         DC    X'52',AL1(17),C'MH'      IPL                      S22024
         DC    X'53',AL1(34),C'MY'      ACTIVATE                 S22024
         DC    X'54',AL1(35),C'MY'      DEACTIVATE             @Y17XARX
         DC    X'FF'                    END OF TABLE             S22024
         SPACE
ERRWRT   DC    C'03'                    WRITE ERROR MESSAGE MOD
WRITE    DC    C'07'                    WRITE MESSAGE MOD
INVCMD   EQU   X'04'                    INVALID COMMAND          S22024
SCANMOD  DC    C'MM'                    SCAN MODULE              S22024
MODMT    DC    C'MT'               SWDEVICE MODULE             @OY12425
TSO      DC    C'TS='                   ACT/DEACT TSO OPERAND  @ZM46678
MODMC    DC    C'MC'                    'TS=' ROUTINE          @ZM46678
         ORG   IGCM010D+X'3FF'          PATCH AREA               S22024
         DC    X'FF'                    PATCH AREA               S22024
         ORG
         TAVTD
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
         END
