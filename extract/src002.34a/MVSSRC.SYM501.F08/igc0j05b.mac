         TITLE 'IGC0J05B-TCAM DATA SET PROCESSOR'
         GBLC  &LIB
&LIB     SETC  'LIB1'                   SET VARIABLE TO LIB1
IGC0J05B CSECT                                                   Y05330
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A434000,467000,500000                                           Y05330
*C125000-128000,149000,152000-154000,156000-163000,166000-167000,Y05330
*C367000-369000                                                  Y05330
*D085000,092000,095000,098000,170000,508000                      Y05330
*A467180,467400,467520,500630,518000                            OY01838
*C130000,181000                                                 OY01838
*D477000                                                        OY01838
*C016000-483500                                                  Y02027
*D425500                                                         Y02027
*A425000                                                        SA66593
*C402000                                                       @ZA00225
*D364000                                                       @ZA00815
*A186000,216000,474000                                         @ZA00206
*D270000-279000                                                @ZA00206
*C192000                                                       @ZA00206
*C131000,220400-221100                                         @ZA02601
*A516750                                                       @ZA02601
*A267000                                                       @ZA02627
*     REASSEMBLE WITH NEW DSECT IEEVRSWA                       @OZ09936
*A424000,518500                                                @OZ18744
***********************************************************************
*  MODULE NAME = IGC0J05B                                             *
*                                                                     *
*  DESCRIPTIVE NAME = TCAM DATA SET RESTART PROCESSOR                 *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
* FUNCTION - THIS MODULE GAINS CONTROL AT RESTART TIME IF THERE ARE   *
*    ANY TCAM DATA SETS OPEN FOR THE JOB STEP BEING RESTARTED.  THESE *
*    DATA SETS USE BSAM OR QSAM DCB'S WITH THE 'QNAME=' KEYWORD ON THE*
*    DD STATEMENT. THE DEB IS IDENTIFIED BY X'C0' OR X'CF' IN DEBNM-  *
*   SUB.  THIS MODULE GETS MAIN FOR AN RCB IN KEY 6 ONCE PER ENTRY,   *
*   AND FREES THE RCB BEFORE EXITING.                                 *
*    THIS MODULE LOCATES THE JFCB FOR THE FIRST TCAM DATA SET, USING  *
*    THE RESTART WORK AREA AND DCB TABLES. IT PASSES THE VALUE OF THE *
*   QNAME KEYWORD TO TCAM IN THE RESTART WORKAREA.  THE RCB IS        *
*   INITIALIZED WITH DATA NEEDED BY IEDQEU.  SVC 102 IS ISSUED TO     *
*   POST THE RCB TO OPEN/CLOSE QCB. A WAIT IS ISSUED TILL THE MCP     *
*   SUBTASK POSTS THE ECB COMPLETE. UPON RETURN ERROR CODES ARE SET.  *
*    THIS MODULE CHECKS FOR MORE TCAM DATA SETS IN THE CHAIN OF DCB   *
*    TABLES. IF THERE IS ANOTHER DATA SET IT REPEATS THE PROCESSING;  *
*    OTHERWISE, IT EXITS TO THE NEXT RESTART MODULE.                  *
*                                                                     *
* ENTRY POINT - IGC0J05B                                              *
*                                                                     *
* INPUT - REGISTER 1 CONTAINS THE ADDRESS OF THE RESTART WORK AREA    *
*                                                                     *
* OUTPUT - REGISTER 1 CONTAINS THE ADDRESS OF THE RESTART WORK AREA   *
*                                                                     *
* EXTERNAL ROUTINES -                                                 *
*         WAIT                                                        *
*         GETMAIN                                                     *
*         FREEMAIN                                                    *
*                                                                     *
* EXITS-NORMAL - THIS MODULE EXITS TO IGC0I05B                        *
*                                                                     *
* EXITS-ERROR - THIS MODULE EXITS TO IGC0V05B WITH ONE OF THE         *
*                FOLLOWING ERROR CODES IN RSRETCOD:                   *
*                82-TCAM IS NOT ACTIVE IN ANOTHER REGION.             *
*                83-THE VALUE OF THE QNAME KEYWORD PARAMETER ON THE   *
*                   DD STATEMENT IS NOT DEFINED IN THE TCAM TABLES    *
*                84-THE PROCESS ENTRY DEFINED BY THE QNAME KEYWORD IS *
*                   ALREADY BEING USED BY ANOTHER APPLICATION PROGRAM *
*                85-A GETMAIN FAILED IN THE A.P.
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*        RESTART WORK AREA                                            *
*        RESTART DCB TABLE                                            *
*        DCB                                                          *
*        DEB                                                          *
*        TCB                                                          *
*         TCX                                                         *
*         RCB                                                         *
*         JSCB                                                        *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 4
RZERO    EQU   0                        GENERAL WORK REGISTER
RPARM    EQU   1                        PARAMETER/WORK REGISTER
RPENTRY  EQU   2                        ADDRESS OF PROCESS ENTRY
RBASE    EQU   3                        BASE REGISTER            Y02027
RDCB     EQU   4                        DCB BASE REGISTER
RWORK    EQU   5                        GENERAL WORK REGISTER
RDEB     EQU   6                        DEB BASE REGISTER
RTCB     EQU   7                        ADDRESS OF CURRENT TCB   Y02027
RPEWA    EQU   8                        PROCESS ENTRY WRKA BASE REG
RTCX     EQU   9                        TCX BASE                 Y02027
RWAB     EQU   10                       RESTART WORKAREA BASE    Y02027
RTABL    EQU   11                       CURRENT ENTRY IN DCB TABLY02027
RPCB     EQU   12                       BASE FOR PROCESS CONTROL BLK
RTNT     EQU   13                       TERMNAME TABLE BASE REG
RETURN   EQU   14                       RETURN POINT REGISTER
RENTRY   EQU   15                       RETURN CODE REGISTER
         EJECT
         BALR  RBASE,RZERO              ESTABLISH MODULE         Y02027
         USING *,RBASE                  ADDRESSABILITY           Y02027
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         USING IEDQTCX,RTCX                                      Y02027
         USING RSTWA,RWAB               RESTART WA ADDRESSABILITYY02027
         USING IEDQTCB,RTCB             TCB                      Y02027
IGC0J05B IEDHJN BEGIN                                            Y05330
         MVC   RSPPM(8),NORMALXT        SET UP NAME FOR NORMAL EXIT
         L     RTCX,CVTPTR              CVT ADDRESS              Y02027
         ICM   RTCX,ALL,CVTAQAVT-CVTMAP(RTCX) GET TCX ADDRESS  @ZA02601
         BZ    ABORT                    TCAM NOT ACTIVE        @ZA02601
         TM    TCXFLAG1,TCXMCPCD        TCAM TERMINATED        @ZA02601
         BO    ABORT                    TCAM NOT ACTIVE        @ZA02601
         TM    TCXBIT1,AVTREADN         TCAM READY             @ZA02601
         BNO   ABORT                    TCAM NOT ACTIVE        @ZA02601
         ICM   RWORK,SEVEN,TCXOCQ+ONE   APPL INTERFACE ACTIVE  @ZA02601
         BZ    ABORT                    TCAM NOT ACTIVE        @ZA02601
         L     RWORK,RSWAOFST           OFFSET TO 1ST DCB TABLE  Y02027
         XC    RSREGSAV(16),RSREGSAV    ZERO WORK AREA           Y02027
         LA    RTABL,ZERO(RWORK,RWAB)   ADDR OF FIRST DCB TABLE  Y02027
         USING TABL,RTABL               BASE FOR DCB TABLE       Y02027
         SPACE 3
*BEGINNING OF LOOP THROUGH ALL DCB TABLES
         SPACE
LOOP     EQU   *
         USING IEDQDEB,RDEB             DEB ADDRESSABILITY
         L     RDCB,TABDCBAD-1          ADDRESS OF CURRENT DCB
         L     RDEB,DCBDEBAD            ADDRESS OF CURRENT DEB
         TM    DEBTAMID,DEBIDI          CHECK FOR TCAM DEB
         BNO   END1                     BRANCH IF NOT TCAM       Y05330
*                                                                Y02027
         ICM   RPARM,SEVEN,RSREGSAV+THIRTEEN RCB OBTAINED YET    Y02027
         BNZ   SETRCB                   BRANCH IF YES            Y02027
*                                                                Y02027
*              SET UP FOR GETMAIN FOR RCB IN CSA IN KEY 6        Y02027
*                                                                Y02027
         LA    RZERO,RCBSIZE            SIZE OF RCB              Y02027
         LA    RPENTRY,SP241            SUBPOOL FOR CSA NOT      Y02027
*                                       FETCH PROTECTED          Y02027
*              GET INTO KEY 6 FOR GETMAIN                        Y02027
*                                                                Y02027
         MODESET    EXTKEY=TCAM                                  Y02027
         GETMAIN    RC,LV=(0),SP=(2)                             Y02027
*                                                              @ZA00206
*                        RETURN TO RESTART KEY                 @ZA00206
*                                                              @ZA00206
         MODESET  EXTKEY=ZERO                                  @ZA00206
         LTR   RENTRY,RENTRY            SUCESSFUL GETMAIN        Y02027
         BNZ   ABORT4                   BRANCH IF NO           @ZA00206
SETRCB   EQU   *                                                 Y02027
         XC    ZERO(RCBSIZE,RPARM),ZERO(RPARM) CLEAR CORE        Y02027
         USING IEDQRECB,RPARM           ESTABLISH RCB BASE       Y02027
*                                                                Y02027
*              INITIALIZE RCB WITH DATA REQUIRED BY IEDQEU IN MCPY02027
*                                                                Y02027
         MVI   RECBKEY,PROCKEY          SET RCB KEY              Y02027
         MVI   RECBPRI,PROCPRI          SET RCB PRIORITY         Y02027
         MVC   RECBQCBA(3),TCXOCQ-IEDQTCX+ONE(RTCX)  MOVE OPEN @ZA00206
*                                       /CLOSE QCB ADDR TO RCB @ZA00206
         MVC   RCBTCB+ONE(3),DEBTCBAD   MOVE TCB ADDR TO RCB     Y02027
         ICM   RTCB,SEVEN,DEBTCBAD      GET TCB ADDR             Y02027
         L     RENTRY,TCBTIO            GET TIOT ADDRESS       @ZA02601
         MVC   RCBJOBNM,ZERO(RENTRY)    MOVE JOBNAME TO RCB    @ZA02601
         MVC   RCBPECB(4),DCBPECB       MOVE PECB ADDR TO RCB    Y02027
         MVC   RCBDSNM,TABVLID1         GET NAME OF PROCESS ENTRYY02027
         ST    RDEB,RCBDEBAD            STORE DEB ADDRESS IN RCB Y02027
         MVC   RCBDCBMA,DCBMACRF        MOVE MACRF TO RCB        Y02027
         MVC   RCBDCBDS,DCBDSORG        MOVE DSORG TO RCB        Y02027
         MVC   RCBDCBBU,DCBBUFL         MOVE BUF. LGTH TO RCB    Y02027
         MVC   RCBDCBLR,DCBLRECL        MOVE LOG. REC SIZE TO RCBY02027
         ICM   RENTRY,EIGHT,DCBOPTCD    PUT DCBOPTCD,            Y02027
         ICM   RENTRY,FOUR,DCBRECFM     DCBRECFM,                Y02027
         ICM   RENTRY,TWO,DCBFLAG1      DCBFLAG1,                Y02027
         ST    RENTRY,RCBDCBOP          IN RCB FOR EU            Y02027
         USING PSA,ZERO                                          Y02027
         L     RENTRY,PSAAOLD           ADDR OF CURRENT ASCB     Y02027
         ST    RENTRY,RCBASCB           STORE ASCB IN RCB        Y02027
         LH    RENTRY,ASCBASID-ASCB(RENTRY) ADDR OF SPACE IDENT. Y02027
         ST    RENTRY,RCBASID           STORE ASID IN RCB        Y02027
         L     RENTRY,DCBPECB           GET ECB ADDRESS        @ZA02627
         XC    ZERO(FOUR,RENTRY),ZERO(RENTRY) CLEAR ECB        @ZA02627
*                                                                Y02027
*              SET UP PARAMETER LIST FOR 102                     Y02027
*                                                                Y02027
         ST    RPARM,RSREGSAV+TWELVE    SAVE ADDR OF RCB AND     Y02027
*                                       AS PARM FOR SVC 102      Y02027
*              PUT RCB ON ASYNCHRONOUS READY QUEUE TO ACTIVATE   Y02027
*              IEDQEU SUBTASK IN MCP                             Y02027
         L     RENTRY,TCXREADY-IEDQTCX(RTCX)  FIRST ELEMENT      Y02027
POSTLOOP STCM  RENTRY,SEVEN,RECBLINK-IEDQRECB(RPARM) POINT NEW   Y02027
*                                       ELEMENT TO FIRST ELEMENT Y02027
         CS    RENTRY,RPARM,TCXREADY-IEDQTCX(RTCX) CURRENT       Y02027
*                                       ELEMENT UPDATED?         Y02027
         BNE   POSTLOOP                 BRANCH IF YES, TRY AGAIN Y02027
*                                       NO, ASYNC READY QUEUE    Y02027
*                                       UPDATED WITH ELEMENT     Y02027
*                                                                Y02027
*              POST MCP ECB COMPLETE THROUGH CROSS MEMORY POST   Y02027
*                                                                Y02027
         L     RENTRY,TCXAVT-IEDQTCX(RTCX)  GET AVT ADDRESS      Y02027
         LA    RENTRY,AVTOSECB-IEDQAVTD(RENTRY) MCP-ECB ADDRESS  Y02027
         ST    RENTRY,RSREGSAV          STORE IN PARAMETER LIST  Y02027
         L     RENTRY,CVTPTR            ADDRESS OF CVT           Y02027
         LA    RENTRY,CVTBRET-CVT(RENTRY) ERROR ADDRESS          Y02027
         ST    RENTRY,RSREGSAV+EIGHT    AND STORE IN LIST        Y02027
         MVC   RSREGSAV+FOUR(4),TCXASCB-IEDQTCX(RTCX) STORE ADDR Y02027
*                                       OF ASCB IN LIST          Y02027
         SR    RZERO,RZERO              ZERO COMPLETION CODE     Y02027
         LA    RPARM,RSREGSAV           ADDRESS OF LIST          Y02027
         POST  ,(0),MF=(E,(1))
         L     RPARM,DCBPECB            LOAD ECB ADDR            Y02027
         WAIT  ECB=RECBKEY                                       Y02027
*                                                                Y02027
*              IEDQEU HAS POSTED ECB COMPLETE                    Y02027
*                                                                Y02027
         CLI   THREE(RPARM),AFLAG       WAS PROC. ENTRY IN USE   Y02027
         BE    ABORT2                   BRANCH IF YES            Y02027
         CLI   THREE(RPARM),ALL         WAS PROC. ENTRY FOUND    Y02027
         BE    ABORT1                   BRANCH IF NO             Y02027
         CLI   THREE(RPARM),ZERO        GETMAIN FAIL             Y02027
         BNE   ABORT4                   BR IF YES              @ZA00225
         ICM   RPARM,SEVEN,RSREGSAV+THIRTEEN RESTORE RCB ADDRESS Y02027
         MVC   DEBTAMOS,RCBDEBTA        MOVE TT OFFSET TO DEB    Y02027
         MVC   DEBPEWA,RCBDEBPW         MOVE PEWA ADDR           Y02027
         MVC   DEBPCBAD(3),RCBDEBPC     MOVE PCB ADDR            Y02027
         MVC   DEBPEBAD(3),RCBPEBAD+ONE  MOVE PEB ADDR TO DEB    Y02027
         L     RDCB,DEBDCBAD-1          GET DCB ADDR           @OZ18744
         L     RDCB,DCBPECB             GET PECB ADDR          @OZ18744
         ICM   RENTRY,SEVEN,RCBDEBPW    GET PEWA ADDR          @OZ18744
         MVC   PEWADTSA-IEDQPEWA(3,RENTRY),PECBDTSA-IEDQPECB(RDCB)
*                                       RESTORE DTS AREA       @OZ18744
         TM    PECBOPTC-IEDQPECB(RDCB),DELETE  DELETE=YES SPEC @OZ18744
         BZ    NODELETE                 BR, NO                 @OZ18744
         OI    PEWAOPT2-IEDQPEWA(RENTRY),DELETE  SET DELETE=YES@OZ18744
*                                        IN PEWA               @OZ18744
NODELETE EQU   *                                               @OZ18744
END1     EQU   *                                                 Y05330
         TM    TABFLG1,LASTENT          CHECK FOR LAST DCB TABLE
         BO    XCTL                     BRANCH IF LAST
         LA    RTABL,TABENT(RTABL)      ADDR OF NEXT DCB IN TABLEY02027
         B     LOOP                     PROCESS NEXT DCB
         SPACE
*END OF LOOP THROUGH DCB TABLES
         SPACE 3
*EXIT TO NEXT RESTART MODULE
         SPACE
XCTL     EQU   *
         EJECT
         XC    RSREGSAV+THIRTEEN(3),RSREGSAV+THIRTEEN GETMAIN    Y02027
*                                       FAILURE ON RCB           Y02027
         BZ    KEYZERO                  BRANCH IF YES -          Y02027
*                                                                Y02027
*              FREEMAIN RCB WHEN ALL DCB'S HAVE BEEN PROCESSED   Y02027
*                                                                Y02027
         MODESET    EXTKEY=TCAM
         ST    RPARM,RSREGSAV+FOUR      ADDR OF RCB              Y02027
         LA    RPARM,RCBSIZE            RCB SIZE                 Y02027
         ST    RPARM,RSREGSAV           STORE LENGTH IN LIST     Y02027
         MVI   RSREGSAV+EIGHT,FREETYPE  SET FREEMAIN FLAG        Y02027
         MVI   RSREGSAV+NINE,SP241      MOVE SUBPOOL TO LIST     Y02027
         LA    RPARM,RSREGSAV           LOAD PARM LIST ADDRESS   Y02027
         FREEMAIN   ,MF=(E,(1))                                  Y02027
KEYZERO  EQU   *                                                 Y02027
         MODESET  EXTKEY=ZERO
         L     RWORK,RSWAOFST           OFFSET TO 1ST DCB TABLE  Y02027
         LA    RTABL,ZERO(RWORK,RWAB)   ADDR OF FIRST DCB TABLE  Y02027
         LA    RENTRY,RSPPM+EIGHT       ADDR OF XCTL WORKAREA    Y02027
         LR    RPARM,RWAB               SET UP PARAMETER         Y02027
*                                                                Y02027
         XCTL  EPLOC=RSPPM,SF=(E,(15))  XCTL TO NEXT MODULE
         SPACE 3
*SET UP ERROR CODES FOR IGC0V05B TO ISSUE ERROR MESSAGE
         SPACE
ABORT    EQU   *
         LA    RWORK,NOTCAM             TCAM NOT IN SYSTEM
         B     OUTERR                   PREPARE TO ABORT
ABORT1   EQU   *
         LA    RWORK,DDNERR             INVALID QNAME
         B     OUTERR                   PREPARE TO EXIT
ABORT2   EQU   *
         LA    RWORK,USERR              PROCESS ENTRY IN USE
         B     OUTERR                   PREPARE TO EXIT
ABORT4   EQU   *
         LA    RWORK,GMERR1             GETMAIN FAILURE IN A.P.  Y02027
OUTERR   EQU   *
        STC   RWORK,RSRETCOD+1         SAVE ERROR CODE
        MVC   RSPPM(8),ERRORXT         MODULE NAME FOR ERROR EXIT
         B     XCTL                     EXIT TO IGC0V05B
        SPACE 3
*                                                                     *
*  E Q U A T E S   A N D   C O N S T A N T S                          *
*                                                                     *
         SPACE 2
NORMALXT DC    CL8'IGC0I05B'            NORMAL EXIT
ERRORXT  DC    CL8'IGC0V05B'            ERROR EXIT
PATCH    DS    10F                      PATCH AREA             @ZA00206
ZERO     EQU   0                        DISPLACEMENT             Y02027
ONE      EQU   1                        DISPLACEMENT             Y02027
TWO      EQU   2                        MASK AND DISPLACEMENT    Y02027
THREE    EQU   3                        DISPLACEMENT             Y02027
FOUR     EQU   4                        CONSTANT
SEVEN    EQU   7                        MASK OF 0111             Y02027
EIGHT    EQU   8                        MASK AND DISPLACEMENT    Y02027
NINE     EQU   9                        DISPLACEMENT             Y02027
TWELVE   EQU   12                       DISPLACEMENT             Y02027
THIRTEEN EQU   13                       DISPLACEMENT             Y02027
DEBIDI   EQU   X'C0'                    TAM DEB ID FOR GET/READ
AFLAG    EQU   X'0C'
ALL      EQU   X'0F'                    PROC ENTRY NOT FOUND     Y02027
SP241    EQU   241                      CSA- NOT FETCH PROTECTED Y02027
TCBJSCB  EQU   X'B4'                    JOB STEP CONTROL BLOCK   Y02027
FREETYPE EQU   X'20'                    FREEMAIN INDICATOR       Y02027
PROCKEY  EQU   X'08'                    KEY AND PRIORITY FIELDS
PROCPRI  EQU   X'03'                    FOR O/C ST SPECIAL ELEM
NOTCAM   EQU   82                       TCAM NOT IN SYSTEM
DDNERR   EQU   83                       INVALID QNAME
USERR    EQU   84                       PROCESS ENTRY IN USE
GMERR1   EQU   85                       GETMAIN FAILURE A.P.     Y02027
LASTENT  EQU   X'08'                    FLAG FOR LAST ENTRY IN DCB TBL
TABENT   EQU   48                       LENGTH OF ENTRY IN DCB TABLES
         SPACE 3
*                 *
*   D S E C T S   *
*                 *
         SPACE 2
         DCBD DSORG=PS                  DATA CONTROL BLOCK
DCBPECB  EQU   DCBIOBAD                 TCAM FIELD IN SAM DCB
         TDEBAPD
         TRECBD
*****************************************************************Y02027
*****    RCB EXTENSION FOR TCAM APPLICATION OPEN/CLOSE.          Y02027
*****************************************************************Y02027
RCBPECB  DS    F                        APPLIC. PGM PECB ADDR.   Y02027
RCBASCB  DS    F                        APPLIC. PGM ASCB ADDR.   Y02027
RCBERRET DS    0F                       ERROR RTN ADDR           Y02027
RCBASID  DS    F                        APPLIC. PGM ASID ADDR.   Y02027
RCBTCB   DS    F                        APPLIC. PGM TCB ADDR.    Y02027
RCBDCBMA DS    H                        DCBMACRF FROM APPLIC.    Y02027
RCBTCBFL DS    0X                       APPLIC. TCB FLAGS.       Y02027
RCBDCBDS DS    H                        DCBDSORG FROM APPLIC.    Y02027
RCBDSNM  DS    CL8                      PROCESS QUEUE NAME.      Y02027
RCBDEBAD DS    F                        DEB ADDR FOR APPLIC. DCB.Y02027
RCBDCBBU DS    H                        DCBBUFL FROM APPLIC.     Y02027
RCBDCBLR DS    H                        DCBLRECL FROM APPLIC.    Y02027
RCBDCBOP DS    X                        DCBOPTCD FROM APPLIC.    Y02027
RCBDCBRF DS    X                        DCBRECFM FROM APPLICATIONY02027
RCBDCBF1 DS    X                        DCBFLAG1 FROM APPLICATIONY02027
RCBERR   DS    X                        ERROR INDICATOR          Y02027
RCBJOBNM DS    CL8                      JOBNAME                  Y02027
RCBSAVE  DS    18F                      SAVE AREA FOR IEDQEU   @ZA02601
RCBEND   DS    0X                       END OF DSECT.            Y02027
         ORG   RCBTCB
RCBDEBTA DS    H                        OFFSET IN T.T. FOR ENTRY.Y02027
RCBOPECB DS    0AL3                     OPCTL ECB ADDRESS        Y02027
RCBDEBPW DS    AL3                      ADDR. OF PEWA.           Y02027
RCBDEBPC DS    AL3                      ADDR. OF PCB.            Y02027
         DS    2F                       QNAME                    Y02027
RCBSQ    DS    0X                       SECURE QUEUE FLAG        Y02027
RCBAVTBT DS    0X                       AVTBIT1 ON RETURN FROM EUY02027
RCBPEBAD DS    AL3                      ADDR OF PEB IN CSA.      Y02027
RCBSIZE  EQU   RCBEND-RECBKEY           SIZE OF DSECT.           Y02027
         IHAPSA
         TTCBD                                                   Y02027
         TTCXD
         IEZJSCB
         IHAASVT
         IHAASCB
         TAVTD
CVTMAP   CVT   DSECT=YES
         TPECBD                                                @OZ18744
         TPEWAD                                                @OZ18744
         IEEVRSWA
TABL     DSECT
*                                       ***** AS OF 3 OCT 67 *****
         DS    0CL48
TABOSORG DS    CL1                      ORGN OF DATA SET
*                                         X'40' = PS
*                                         X'80' = IS
*                                         X'20' = DA
*                                         X'02' = PO
*                                       X'01' = U
TABDCBAD DS    CL3                      ADDR OF DCB FOR THIS ENTRY
TABFLG1  DS    CL1                      FLAG BYTE NO. 1
*                                         X'80' = NULLFILE & SYSCHECK
*                                         X'40' = SYSIN OR SYSOUT D.S.
*                                         X'20' = DA DEVICE
*                                         X'10' = TAPE DEVICE
*                                         X'08' = END OF TABLE FLAG BIT
*                                         X'04' = UNIT RECORD OR OTHER
TABSEGAD DS    CL3                      ADDR OF W.A. SEG ASSIGNED
TABNVOLS DS    CL1                      TOTAL NO OF VOLUMES
TABJFCB  DS    CL3                      TTR ADDR OF JFCB - THIS DATASET
TABOTIOT DS    0CL2                     OLD USER TIOT OFFSET (DA ONLY)
TABTPLBL DS    CL1                      TAPE LABEL INFORMATION BYTE
*                                         X'80' = ERROR IN NSL PROC RTN
*                                         X'40' = NSL MOUNT SWITCH
*                                         X'20' = NSL COMMUNICATION BIT
*                                         X'10' = BYPASS LABEL PROSS.
*                                         X'04' = NONSTANDARD LABEL
*                                         X'02' = STANDARD LABEL
*                                         X'01' = NO LABEL
TABFSQNO DS    CL1                      FILE SEQUENCE NO. (IF TAPE)
TABFLG2  DS    CL1                      FLAG BYTE NO. 2
*                                         X'08' = GREATER THAN 5 VOLS
*                                         X'40' = PO CONCATENATED
TABFLG3  DS    CL1                      FLAG BYTE NUMBER 3
TABFLG4  DS    CL1                      FLAG BYTE NUMBER 4
TABLG5   DS    CL1                      FLAG BYTE NUMBER 5
TABVLID1 DS    CL6                      VOLUME ID NUMBER ONE
TABVLID2 DS    CL6                      VOLUME ID NUMBER TWO
TABPOS   EQU   TABVLID2
TABOPS   EQU   TABPOS
TABFSFN1 EQU   TABPOS+1
TABFSFN2 EQU   TABPOS+2
TABBLK1  EQU   TABPOS+4
TABBLK2  EQU   TABPOS+8
TABUCB   EQU   TABPOS+12
TABVLID3 DS    CL6                      VOLUME ID NUMBER THREE
TABVLID4 DS    CL6                      VOLUME ID NUMBER FOUR
TABVLID5 DS    CL6                      VOLUME ID NUMBER FIVE
RSDSECT  DSECT
*
*    DATA EXTENT BLOCK
*
RSDEB    DS    0CL48
RSDEBTCB DC    X'00000000'              TCB ADDRESS
RSDEBDEB DC    X'00000000'              DEB ADDRESS
*
*    DATA CONTROL BLOCK
*
RSDEBOFL DS    0CL1
RSDEBIRB DC    X'00000000'              IRB ADDRESS
RSEBSYS  DC    X'00000000'              1ST IOB ADDR IN SYS.PURGE CHAIN
RSDEBUSR DC    X'00000000'              1ST IOB ADDR IN USER PRGE CHAIN
RSDEBECB DC    X'00000000'              PARM.LIST ADDR TO PURGE ECB
RSDEBID  DS    0CL1
RSDEBDCB DC    AL4(RSDCBREP)            DCB ADDR
RSDEBAPP DS    CL4                      I/O APPENDAGE VECTOR TABLE
RSDEBMOD DS    0CL1
RSDEBUCB DS    F                        UCB ADDRESS
RSDEBBIN DS    H                        BIN NUMBER
RSDEBSCC DS    H                        CYLINDER START ADDRESS
RSDEBSHH DS    H                        TRACK START ADDRESS
RSDEBECC DS    H                        CYLINDER END ADDRESS
RSDEBEHH DS    H                        TRACK END ADDRESS
RSDEBNTR DS    H                        NO. OF TRACKS IN THIS EXTENT
*
*    EVENT CONTROL BLOCK
*
RSECBAD  DS    CL4                      ECB ADDRESS
RSDCBDEB DC    AL4(RSDEB)               DEB ADDRESS
*
RSDCBREP EQU   RSDEB+8                  POINT TO RELATIVE BEGINNING DCB
*
*    INPUT OUTPUT BLOCK
*
RSIOB    DS    0CL40
RSIOBFG1 DC    X'00'                    I/O FLAGS BYTE 1
RSIOBFG2 DC    X'00'                    I/O FLAGS BYTE 2
RSIOBSN1 DS    CL1                      FIRST SENSE BYTE
RSIOBSN2 DS    CL1                      SECOND SENSE BYTE
RSIOBECB DC    AL4(RSECBAD)             ECB ADDRESS
RSIOBCSW DS    D                        LAST CSW
RSIOBCPA DS    AL4(RSCCW1)              CHANNEL PROGRAM ADDRESS
RSIOBDCB DS    AL4(RSDCBREP)            DCB ADDRESS
RSIOBRCP DS    CL4                      RESTART ADDRESS FOR IOS
RSIOBINC DS    H                        INCREMENT BLOCK COUNT
RSIOBECT DS    H                        NO. OF ERROR ENTRIES
RSIOBDAD DS    D                        SEEK ADDRESS FOR DA
*
*    CHANNEL PROGRAM
*
RSCCWLST DS    0CL24
RSCCW1   DS    D                        CCW1
RSCCW2   DS    D                        CCW2
RSCCW3   DS    D                        CCW3
*
*    BUFFER SEGMENT FOR EXTERNAL BLOCKS
*
RSSTATUS DS    0CL8
RSSTAT1  DS    CL1                      STATUS BYTE 1
RSSTAT2  DS    CL1                      STATUS BYTE 2
RSSTAT3  DS    CL1                      STATUS BYTE 3
RSSTAT4  DS    CL1                      STATUS BYTE 4
         DS    F                        RESERVED FOR FUTURE USE
*
         END
