         TITLE 'IGCV010D - VARY SCAN MODULE'
IGCV010D CSECT
         SPACE 3                                                 S21903
*CHANGE ACTIVITY=AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A000000-999999                                                  S99240
*C479000-485000                                                 SA56606
*D449100-449800                                                 SA61099
*C 377000-380000                                                SA63002
*A379360                                                       @SA73342
*A379970                                                        OY03916
*A191000,331500,378600                                         @OY12425
*C090000,097000,161000,185000,193000,197000-211800,            @OY12425
*C280000-281000,333000,379002,500000                           @OY12425
*D123000,129000,131000,142300,212600-266000,267000-274000,     @OY12425
*D316000-316300,325050-325750,327000-328000,348000-358000,     @OY12425
*D455000,458000-475000,475500                                  @OY12425
*A098000                                                       @Y17XART
*A379174                                                       @Y17XART
*A379300                                                       @Y17XART
*A379540,388000                                                @Y17XART
*A381500                                                       @Y17XART
*A456500                                                       @Y17XART
*A500000                                                       @Y17XART
*D389000-389400                                                @Y17XART
*C443000                                                       @Y17XART
*A142800,387000,390000,426000,442200,498000                    @OY14092
*A360400                                                       @OY15465
*C456818-456824                                                @OY18838
*C185500                                                       @OZ27693
***********************************************************************
*                                                                     *
* MODULE NAME=IGCV010D (TCAM,OP CTL)                           @Y17XART
*                                                                     *
* DESCRIPTIVE NAME=VARY SCAN MODULE                                   *
*                                                                     *
* COPYRIGHT='NONE'                                                    *
*                                                                     *
*  STATUS:  CHANGE LEVEL 10                                    @Y17XART
*                                                                     *
* FUNCTION -- THIS ROUTINE CHECKS VARY COMMANDS FOR SYNTAX ERRORS AND *
*    OTHER ROUTINE ERRORS COMMON TO DIFFERENT TYPES OF VARY COMMANDS. *
*    THE WORKAREA IS CHECKED TO DETERMINE THE TYPE OF INPUT.  IF IT IS*
*    DCB ADDRESS, THE DEB ADDRESS IS OBTAINED FROM DCBDEBAD.  IF IT IS*
*    TERMINAL NAME INPUT, THE TERMINAL NAME TABLE IS SEARCHED FOR A   *
*    MATCHING ENTRY.  IF NONE IS FOUND THE ERROR REGISTER IS SET UP   *
*    WITH APPROPRIATE NUMBER AND CONTROL IS TRANSFERRED TO AN ERROR   *
*    MODULE.  IF INPUT IS DDNAME/RLN FORMAT AND STILL IN CHARACTER    *
*    FORMAT, IT IS CONVERTED TO HEXADECIMAL AND TESTED FOR ZERO OR    *
*    GREATER THAN 255.  IF EITHER, AN ERROR EXIT IS TAKEN.  THE DDNAME*
*    IS VERIFIED THROUGH A DCB LOOKUP ROUTINE IN THE CONTROL MODULE.  *
*    IF IT IS NOT A VALID DDNAME, AN ERROR EXIT IS TAKEN.  IF INPUT   *
*    IS IN LINE ADDRESS FORMAT, THE SAME DCB LOOKUP ROUTINE IS USED   *
*    TO TEST EACH UCB ASSOCIATED WITH A DEB FOR AN ID MATCHING THE    *
*    ADDRESS SPECIFIED.  IF A MATCH IS NOT FOUND, AN ERROR EXIT IS    *
*    TAKEN.  AFTER THE INPUT IS COMPLETELY VERIFIED, CONTROL IS PASSED*
*    TO THE APPROPRIATE FUNCTION MODULE AS DETERMINED BY THE COMMAND. *
*    VARY COMMANDS FOR DEVICES OFF AN RN ARE ALSO SCANNED BY THISS22024
*    MODULE.  VARY COMMANDS FOR THE RN ITSELF ARE TREATED AS AN  S22024
*    ERROR SITUATION.                                            S22024
*    VARY COMMANDS FOR THE SSCP ARE TREATED AS AN ERROR.       @Y17XART
*    IN A RESTART SITUATION THE NCP WILL NOT BE CHECKED FOR    @Y17XART
*    ACTIVE. THE COMMAND WILL BE PASSED ON TO THE FUNCTION     @Y17XART
*    MODULE.                                                   @Y17XART
*                                                                     *
* ENTRY POINTS --                                                     *
*    IGCV010D TO SCAN VARY COMMANDS.                                  *
*                                                                     *
* INPUT --                                                            *
*    REGISTER 1 - ADDRESS OF OPERATOR CONTROL AVT.  RELEVANT FIELDS   *
*    ARE OPCDOUBL.  IT CONTAINS EITHER THE DDNAME OR UCB ADDRESS LEFT *
*    JUSTIFIED.  OPCFLAG1 IS USED TO INDICATE THAT CERTAIN PUNCTUATION*
*    IN THE COMMAND HAS BEEN FOUND.  OPCCOPCE IS THE ADDRESS OF THE   *
*    CURRENT ELEMENT.  RELEVANT FIELDS ARE OCOPTCDE.  THIS HAS THE    *
*    VERB CODE.  OCRLN CONTAINS THE RLN.  OCFLAG INDICATES THE SOURCE *
*    OF THE INPUT.  OCSWITCH CONTAINS THE SUBOPERATION CODE.          *
*                                                                     *
* OUTPUT --                                                           *
*    REGISTER 0 - NUMBER OF AN ERROR MESSAGE.                         *
*    REGISTER 1 - RETURN CODE SET.                                    *
*    THE FOLLOWING MODULES ARE GIVEN CONTROL (BY XCTL) FROM THIS      *
*    MODULE DEPENDING ON THE FUNCTION TO BE PERFORMED.                *
*         IGCV110D STOPLINE                                           *
*         IGCV210D STOP TERMINAL                                      *
*         IGCV310D START LINE                                         *
*    FOR RN DEVICES MODULE IGCV810D IS XCTLED TO FROM IGCV110D   S22024
*    FOR STOPLINE COMMANDS.  MODULE IGCV910D IS XCTLED TO FROM   S22024
*    IGCV210D FOR START/STOP TERMINAL COMMANDS.  IGCV310D DOES   S22024
*    THE RN STARTLINE.                                           S22024
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         NONE.                                                       *
*                                                                     *
* EXITS,NORMAL --                                                     *
*         COMMAND COMPLETELY VERIFIED.  XCTL TO APPROPRIATE FUNCTION  *
*         MODULE WITH OCSWITCH SET UP.                                *
*                                                                     *
* EXITS,ERROR --                                                      *
*    COMMAND INVALID FOR 3705 ITSELF.                            S22024
*         INVALID STATION NAME, LINE NOT OPEN, INVALID FORMAT, INVALID*
*         VERB, OR INVALID RLN.  ERROR REGISTER SET UP, X'04' RETURN  *
*         CODE SET.                                                   *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    OPERATOR CONTROL AVT, OPERATOR CONTROL ELEMENT, DEB, DCB, LCB,   *
*    QCB,TERMNAME TABLE, TERMINAL ENTRY, AVT, AND RSIDT.         S22024
*                                                                     *
* ATTRIBUTES --                                                       *
*    SERIALLY REUSABLE, REFRESHABLE, ENABLED, TRANSIENT,              *
*    SUPERVISOR MODE.                                                 *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS            *
*    EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING         *
*    HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER              *
*    CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE         *
*    FOR THE NEW DEFINITION.                                          *
*                                                                     *
* NOTES --                                                            *
*    COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS          *
*    PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                      *
*                                                                     *
***********************************************************************
         EJECT
         SPACE
*                                 E Q U A T E S
         SPACE
RZERO    EQU   0                        PARAMETER REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
ROPCAVT  EQU   2                        POINTER TO OP CONTROL AVT
ROPCE    EQU   3                        POINTER TO CURRENT ELEMENT
RAVT     EQU   4                        POINTER TO TCAM AVT
RWORKA   EQU   5                        POINTER TO RESIDENT WORKAREA
RLCB     EQU   6                        LCB ADDRESS              S22024
REND     EQU   6                        END ADDR OF DATA RECVD @OY12425
RDCB     EQU   7                        POINTER TO DCB
RSTART   EQU   7                        START ADDR OF CURRENT SCAN
RWORK    EQU   8                        WORK REGISTER
RLEN     EQU   9                        LENGTH OF PARAMETER
RHOLD    EQU   10                       REGISTER TO SAVE VALUE
RSCAN    EQU   11                       SCAN PTR TO DATA       @OY12425
RBASE    EQU   12                       MODULE BASE REGISTER
RSAVE    EQU   13                       SAVE AREA REG          @Y17XART
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RBRNCH   EQU   15                       SUBROUTINE BASE REGISTER
         SPACE 2
ALLRLN   EQU   X'20'                    ALL WAS SPECIFIED AS RLN
BADCMND  EQU   18                       ERROR FOR INVALID COMMAND
CLEAR    EQU   7                        PROPAGATE BLANKS
COMMA    EQU   C','                     COMMA IN DATA
CMNDOFF  EQU   4                        OFFSET IN TABLE TO COMMAND
CURRENT  EQU   0                        CURRENT SCANNED BYTE
DDSWIT   EQU   X'10'                    PROCESSING LINE NO. OR DDNAME
DIALERR  EQU   88                       DIAL VARY ERROR MSG    @YM08035
DOUBLWD  EQU   8                        SAVE ENTIRE EIGHT BYTES
ENDTABLE EQU   X'FF'                    END OF TABLE INDICATOR
ENTRYLEN EQU   13                       LNG OF VERIFY TBL ENTRY SA56606
EQUAL    EQU   8                        EQUAL COMPARE
FOUR     EQU   4                        LENGTH OF FOUR
INVSTAT  EQU   16                       ERROR IN STATION NAME
LENOFF   EQU   2                        OFFSET TO COMMAND LENGTH IN WA
LPAREN   EQU   128                      INDICATE L PAREN FOUND
MODOFF   EQU   2                        OFFSET TO MODULE NAME
MSGOFF   EQU   8                        OFFSET TO START OF MSG
NAMELEN  EQU   2                        LENGTH OF MOD NAME CHANGE
NEXT     EQU   1                        NEXT BYTE TO BE SCANNED
NORLN    EQU   X'40'                    FLAG FOR NO RLN IN MSG @YM08035
NOTOPEN  EQU   17                       BAD DD ERROR
OFFON    EQU   C'O'                     O FOR ONTP OR OFFTP
ONE      EQU   1                        VALUE OF ONE
OPEN     EQU   X'10'                    DCB OPEN FLAG
PARENL   EQU   C'('                     LEFT PARENTHESIS
PARENR   EQU   C')'                     RIGHT PARENTHESIS
RPAREN   EQU   64                       FLAG FOR RIGHT PAREN FOUND
SIZEOFF  EQU   0                        OFFSET IN TABLE TO COMMAND SIZE
SWITOFF  EQU   1                        OFFSET TO COMMAND SWITCH VALUE
TIOT     EQU   12                       OFFSET TO TIOT IN TCB
TWO      EQU   2                        LENGTH OF 2
VARINAME EQU   3                        OFFSET TO CHANGE MODULE NAME
VERBOFF  EQU   0                        OFFSET TO VERB IN TABLE ENTRY
ZERO     EQU   0                        NULL VALUE
NOTCA    EQU   2                        INDICATES ENTRY NOT FROM CA
TIOTDDNM EQU   4                        DDNAME OFFSET IN TIOT
MODUL    EQU   1                        MODULE OFFSET IN RESTART TABLE
NOPEN05  EQU   161                      3705 DCB NOT OPEN MSG    S22024
RNDSORG  EQU   X'04'                    DSORG VALUE              S22024
OPENLGB  EQU   X'10'                    OPEN FLAG                S22024
THREE    EQU   3                        CONSTANT FOR INDEXING    S22024
FIVE     EQU   5                        CONSTANT FOR INDEXING    S22024
NOTSINGL EQU   90                       'NOT SINGLE ENTRY' CODE  S22024
SINGLE   EQU   X'E0'                    SINGLE ENTRY             S22024
DSLGB    EQU   X'80'                    LINE GROUP BLOCK ID      S22024
ADDRESS  EQU   7                        LOW ORDER 3 BYTES      @Y17XARR
NACTIV05 EQU   400                      3705 NOT ACTIVE MESSAGE  Y06330
NOSTART  EQU   X'BF'                    FLAG TO RESET START    @OY14092
*                                       LINE SWITCH            @OY14092
IPLDMP   EQU   210                      IPL/DUMP-ACT/DEACT IN  @Y17XART
*                                       PROGRESS MSG NUMBER    @Y17XART
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT DSECT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         ADDRESSABILITY FOR OPC AVT
         USING IHADCB,RDCB              ADDRESSABILITY FOR DCB   S22024
         SPACE
         BALR  RBASE,RZERO              SET BASE REGISTER
         USING *,RBASE                  MODULE ADDRESSABILITY
*              INITIAL SETUP                                          *
         L     RWORKA,OPCWORK           WORKAREA ADDRESS
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS
         BO    NOTMPP                   YES, SAVE NAME         @OY12425
         SPACE
         MVI   OPCDOUBL,AVTEBLNK        CLEAR FIRST BYTE
         MVC   OPCDOUBL+ONE(CLEAR),OPCDOUBL CLEAR DOUBLE WORD
         TM    OCFLAG,OCMPP+OCTOTE      TOTE OR MPP INPUT
         BZ    TERMIN                   NO, TERMINAL INPUT
         SPACE
         CLI   OCBACKUP,AVTEZERO        DCB ADDRESS? (QTAM)
         BNE   NOTMPP                   NO, DATA IS GOOD
         SR    RZERO,RZERO              CLEAR REG 0            @YM08560
         SPACE
         L     RDCB,OCBACKUP            GET DCB ADDRESS
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         TM    DCBOFLGS,OPEN             DCB OPEN?
         BNO   ERROR17                  NO, ERROR
         LA    RWORK,LGBNAME-IEDNLGB-TIOTDDNM(RDCB) SET IN CASE  S22024
*                                        DCB IS LGB              S22024
         TM    DCBDSORG,TRMLGB          IS THIS AN LGB           S22024
         BO    SETBCKUP                 YES, BRANCH              S22024
         SPACE
         L     RWORK,AVTTCB             GET TCAM TCB ADDRESS
         L     RWORK,TIOT(RWORK)        GET TIOT ADDRESS
         AH    RWORK,DCBDDNAM           ADD TIOT OFFSET FROM DCB
SETBCKUP EQU   *                                                 S22024
         MVC   OCBACKUP(DOUBLWD),TIOTDDNM(RWORK) SAVE DDNAME
NOTMPP   EQU   *
SETDOUBL MVC   OPCDOUBL(DOUBLWD),OCBACKUP DDNAME FOR VERIFY      S22024
         B     LASTCHEK                 END OF INPUT CHECK     @OY12425
IGCV010D IEDHJN AFTERHJN,HJN            INCLUDE NAME IN HJN    @OZ27693
         SPACE
TERMIN   EQU   *
         LH    REND,LENOFF(RWORKA)      GET COMMAND LENGTH
         LA    REND,MSGOFF(RWORKA,REND) SET END OF MESSAGE POINTER
         MVI   AVTEZERO(REND),AVTEBLNK  SET ENDING BLANK
         LA    RSCAN,MSGOFF(RWORKA)     GET SCAN STARTING ADDRESS
         LR    RSTART,RSCAN             LOAD START ADDR        @OY12425
         OI    OCSCAN,OCADDR+OCDDNRLN+OCDDNAME+OCALLRLN        @OY12425
*    FIELD CAN BE ADDR, DDN,RLN, OR DDNAME, AND RLN CAN BE ALL @OY12425
         CLI   CURRENT(RSCAN),PARENL    LEFT PAREN FOUND?
         BNE   CKOFFON                  NO PARENS              @OY12425
         SPACE
         OI    OPCFLAG1,LPAREN          INDICATE PAREN FOUND
         LA    RSCAN,NEXT(RSCAN)        POINT TO NEXT BYTE
         NI    OCSCAN,AVTEFF-OCADDR     NOT A UCB ADDRESS      @OY12425
         LR    RSTART,RSCAN             LOAD START ADDR        @OY12425
CKOFFON  EQU   *                                               @OY12425
*   THIS CODE FINDS FIRST DELIMITER IN COMMAND AND SAVES       @OY12425
*        FIELD IN OPCDOUBL                                     @OY12425
         CLI   ZERO(RSCAN),COMMA        IS IT A COMMA?         @OY12425
         BE    MOVENAME                 YES, SAVE NAME         @OY12425
         CLI   ZERO(RSCAN),PARENR       IS IT A RIGHT PAREN?   @OY12425
         BE    SETPAREN                 NO, GO CHK NEXT BYTE   @OY12425
         CR    RSCAN,REND               HAVE WE REACHED EOM?   @OY12425
         LA    RSCAN,ONE(RSCAN)         INCR PTR BY ONE        @OY12425
         BL    CKOFFON                  CHECK NEXT BYTE        @OY12425
         B     ERROR18                  ELSE, ERROR            @OY12425
SETPAREN EQU   *                                               @OY12425
         OI    OPCFLAG1,RPAREN          INDICATE PAREN FOUND   @OY12425
MOVENAME EQU   *                                               @OY12425
***********************************************************************
*                                                                     *
*   THE ADDRESS OF FIRST DELIM IS SAVED SO THAT A CHECK CAN BE @OY12425
*   MADE AT A LATER POINT FOR EXTRA FIELDS BETWEEN THE FIRST   @OY12425
*   DELIM AND 'OFFTP' OR 'ONTP'                                @OY12425
*                                                                     *
***********************************************************************
         ST    RSCAN,OCLINK             SAVE ADDR OF 1RST DELIM@OY12425
         LR    RLEN,RSCAN               GET SCAN PTR           @OY12425
         SR    RLEN,RSTART              GET LENGTH OF DATA     @OY12425
         BZ    ERROR18                  IF LENGTH ZERO, ERROR  @OY12425
         CH    RLEN,MAX                 IS LENGTH > MAX?       @OY12425
         BH    ERROR18                  YES,ERROR              @OY12425
         BCTR  RLEN,RZERO               PREPARE FOR EXECUTE    @OY12425
         EX    RLEN,PLACNAME            SAVE NAME IN OPCDOUBL  @OY12425
CKOH     EQU   *                                               @OY12425
         CLC   ZERO(TWO,RSCAN),OH       SEARCH FOR ',O'        @OY12425
         BE    TSTINPUT                 SCAN FOR TYPE OF CMD   @OY12425
         CR    RSCAN,REND               HAVE WE REACHED EOM?   @OY12425
         LA    RSCAN,ONE(RSCAN)         INCR TO NXT BYTE       @OY12425
         BL    CKOH                     CHECK NEXT BYTE        @OY12425
         B     ERROR18                  REACHED EOM-BAD CMD    @OY12425
         SPACE
*              CHECK TO SEE IF VALID COMMAND                          *
TSTINPUT EQU   *
         LA    RSCAN,ONE(RSCAN)         SET PTR PAST DELIM     @OY12425
         LA    RWORK,CMNDTBL-FIVE       POINT IN FRONT OF        S22024
*                                       CMD TABLE                S22024
         MVC   OCBACKUP(DOUBLWD),OPCDOUBL  SAVE INPUT
         SR    RLEN,RLEN                CLEAR LENGTH REGISTER    S22024
         SPACE
*              TERMINAL OR CONSOLE INPUT                              *
FINDCMND EQU   *
         LA    RWORK,FIVE(RLEN,RWORK)   POINT TO NEXT ENTRY      S22024
         CLI   SIZEOFF(RWORK),ENDTABLE  END OF TABLE?
         BE    ERROR18                  YES,INVALID CMD
         IC    RLEN,SIZEOFF(RWORK)      GET COMMAND LENGTH
         EX    RLEN,CMNDCHEK            VERIFY REMANINING CMD
         BNE   FINDCMND                 IF NOT THIS CMD,         S22024
*                                       CHECK NEXT CMD           S22024
         MVC   OCSWITCH(ONE),SWITOFF(RWORK)  SAVE SWITCH VALUE @OY12425
         MVC   OPCLDNME+VARINAME(NAMELEN),MODOFF(RWORK) SET NXT MODULE
*                                                              @OY12425
         TM    OCSWITCH,DDSWIT          CMD FOR DCB OR LINE NO?@OY12425
         BNO   CKPAREN                  NO, GO CHK FOR PARENS  @OY12425
***********************************************************************
*                                                                     *
*   ADDR OF LAST DELIM FOUND IS SAVED IN OCLINK. A CHECK WILL  @OY12425
*   LATER BE MADE FOR EXTRA COMMAS BY COMPARING THE ADDRESS IN @OY12425
*   OCLINK TO ADDR RETURNED BY OPCSCAN                         @OY12425
*                                                                     *
***********************************************************************
         BCTR  RSCAN,RZERO              SETUP SCAN PTR         @OY12425
         ST    RSCAN,OCLINK             SAVE ADDR OF ',O'      @OY12425
         LR    RSCAN,RSTART             SETUP START ADDR       @OY12425
         L     RBRNCH,OPCDCBLK          PREPARE TO             @OY12425
         BAL   RRET,FOUR(RBRNCH)        BRANCH TO OPCSCAN      @OY12425
         B     CKCOMMA                  NAME WAS FOUND         @OY12425
         B     ERROR17                  NAME NOT FOUND         @OY12425
         B     ERROR18                  INVALID SYNTAX         @OY12425
CKCOMMA  EQU   *                                               @OY12425
         CLI   ZERO(RSCAN),COMMA        IS IT A COMMA?         @OY12425
         BE    LEFTPAR                  YES, CHK PARENS        @OY12425
         TM    OPCFLAG1,LPAREN+RPAREN   LEFT OR RIGHT PAREN?   @OY12425
         BNZ   ERROR18                  YES, ERROR             @OY12425
         CLI   ZERO(RSCAN),OFFON        IS IT AN 'O'?          @OY12425
         BE    RESOURCE                 YES, CONTINUE          @OY12425
         B     ERROR18                  BAD COMMAND            @OY12425
LEFTPAR  EQU   *                                               @OY12425
*   A CHECK IS MADE FOR TOO MANY COMMAS INSERTED BETWEEN FIRST @OY12425
*   DELIMITER AND 'OFFTP' OR 'ONTP'                                   *
         C     RSCAN,OCLINK             LAST BYTE SCANNED?     @OY12425
         BNE   ERROR18                  NO, ERROR              @OY12425
         LA    RSCAN,ONE(RSCAN)         INCR PTR BY ONE        @OY12425
         TM    OPCFLAG1,LPAREN+RPAREN   BOTH PARENS THERE?     @OY12425
         BO    RESOURCE                 YES, CONTINUE          @OY12425
ERROR18  EQU   *
         LA    RZERO,BADCMND            SET ERROR NUMBER
ERRXCTL  EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),XCTL03 SET MODULE NAME
XCTL     EQU   *
         NI    OPCFLAG1,AVTEZERO        CLEAR FLAGS
         LR    RPARM,ROPCAVT            PASS AVT ADDRESS
         SPACE
         XCTL  SF=(E,OPCXCTL)           CALL NEXT MODULE
         SPACE
LASTCHEK EQU   *
         LA    RWORK,CIBTABLE           POINT TO MPP, TOTE VERB TABLE
         TM    OCFLAG,OCMPP+OCTOTE      TOTE OR MPP INPUT
         BM    FINDMACH                 YES, USE LOOK-UP TABLE
         SPACE
*   AT THIS POINT, WE HAVE DETERMINED RESTART IS IN PROGRESS   @OY12425
         SPACE
         LA    RWORK,ONE(RWORK)         POINT TO SWITCH VALUE
FINDMACH EQU   *
         CLI   VERBOFF(RWORK),ENDTABLE  END OF TABLE?
         BE    ERROR18                  YES, BAD INPUT
         SPACE
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS
         BZ    CKVERB                   NO
         SPACE
         CLC   OCSWITCH(ONE),VERBOFF(RWORK)  IS THIS COMMAND THE ONE
         BNE   INCENTRY                 NO GO TO NEXT ENTRY
         SPACE
         MVC   OPCLDNME+VARINAME(NAMELEN),MODUL(RWORK) PICK UP MODULE
         B     CKSWITCH                 GO VALIDATE NAME         S22024
         SPACE
CKVERB   EQU   *
         CLC   OCOPTCDE(ONE),VERBOFF(RWORK) CORRECT VERB?
         BE    SETDATA                  YES, SAVE PERTINENT DATA
         SPACE
INCENTRY EQU   *
         LA    RWORK,FOUR(RWORK)        POINT TO NEXT ENTRY
         B     FINDMACH                  CONTINUE SEARCH
         SPACE
         SPACE
SETDATA  EQU   *
         SPACE
         MVC   OCSWITCH(ONE),SWITOFF(RWORK) SAVE SWITCH VALUE
         MVC   OPCLDNME+VARINAME(NAMELEN),MODOFF(RWORK) SET NEXT MODULE
CKSWITCH EQU   *                                                 S22024
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS?   @OY12425
         BO    RESTART                  YES, CHECK TERMNAME    @OY12425
         TM    OCSWITCH,DDSWIT          COMMAND FOR DCB OR LINE NUMBER
         BNO   RESTART                  NO - TERMINATE         @OY12425
         TM    OCFLAG,OCMPP+OCTOTE      TOTE OR MPP INPUT      @OY12425
         BZ    ERROR18                  NO, ERROR              @OY12425
         SR    RPARM,RPARM              CLEAR RLN              @OY12425
         IC    RPARM,OCRLN              NO, MPP - SETUP RLN REG@OY12425
         L     RBRNCH,OPCDCBLK          GET VALIDATE ROUTINE ADDRESS
         LR    RDCB,RBRNCH              SET TO CALL DCBLKUP    @OY12425
         TM    OCFLAG,OCTOTE            TOTE INPUT?            @OY12425
         BZ    GOSCAN                   NO, GO TO DCBLOOKUP    @OY12425
         CLI   OCBACKUP+THREE,AVTEBLNK  POSSIBLE UCB?          @OY12425
         BNE   GOSCAN                   NO, GOTO DCBLKUP       @OY12425
         CLI   OCBACKUP+TWO,AVTEBLNK    POSSIBLE TWO CHAR DDN  @OY15465
         BE    GOSCAN                   YES, BRANCH            @OY15465
         LA    RDCB,FOUR(,RDCB)         SET TO CALL OPCSCAN    @OY12425
         LA    RSCAN,OCBACKUP           GET DATA ADDRESS       @OY12425
         MVI   OCSCAN,OCADDR+OCDDNAME  INDICATE UCB OR GRPNME  @OY12425
GOSCAN   EQU   *                                               @OY12425
         BALR  RRET,RDCB                GO FIND RESOURCE       @OY12425
         SPACE
         B     RESOURCE                 GOOD RETURN, CHECK RESOURCES
         SPACE
ERROR17  EQU   *
         TM    OPCFLAG1,ALLRLN          WAS RLN=ALL?
         BNO   NORESET                  NO, LEAVE RLN
         SPACE
         MVI   OCRLN,AVTEZERO           RESET RLN TO ALL
         BCT   RZERO,NORESET            BR IF NOT DD DUMMY     @YM08560
         B     CKCOMMA                  CONTINUE PROCESSING    @YM08560
NORESET  EQU   *
         LA    RZERO,NOTOPEN            SET ERROR NUMBER
SETXCTL  EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),WRTXCTL SET WRITER NAME
         TM    OCFLAG,OCMPP+OCTOTE      MPP OR TOTE INPUT?
         BZ    ERRXCTL                  BRANCH IF NO            SA63002
         LA    RZERO,FOUR               SET ERROR RETURN CODE   SA63002
         B     XCTL                     GO CALL NEXT MODULE     SA63002
         SPACE 1                                                SA63002
CKPAREN  EQU   *                                                 S22024
*   A CHECK IS MADE FOR TOO MANY FIELDS INSERTED BETWEEN FIRST @OY12425
*   DELIM AND 'OFFTP' OR 'ONTP'                                @OY12425
         BCTR  RSCAN,RZERO              SET SCAN PTR TO COMMA  @OY12425
         C     RSCAN,OCLINK             FIRST COMMA FOUND?     @OY12425
         BNE   ERROR18                  NO,ERROR               @OY12425
         LA    RSCAN,ONE(RSCAN)         RESET SCAN PTR         @OY12425
         TM    OPCFLAG1,LPAREN+RPAREN   EITHER PAREN IN DATA
         BNZ   ERROR18                  YES, ERROR FOR STATNAME@OY12425
RESTART  EQU  *                                                @OY12425
         SPACE
         L     RBRNCH,OPCTOFLK          GET TNT SEARCH ADDRESS
         BALR  RRET,RBRNCH              VALIDATE STATION NAME
         SPACE
         B     RSOURC1                  GOOD RETRN CHECK RESOURCES22024
         SPACE
ERROR16  EQU   *
         LA    RZERO,INVSTAT            BAD STATION ERROR
         B     SETXCTL                  SET UP NEXT MODULE
RSOURC1  EQU   *                                                 S22024
*****************************************************************S22024
*  FOR TERMINAL COMMANDS THE 3705 LGB ADDRESS IS OBTAINED AND    S22024
*  THE OTHER RN PROCESSING IS DONE.                              S22024
*****************************************************************S22024
         LA    RZERO,NOTSINGL           SET 'NOT SINGLE' CODE    S22024
         TM    TRMSTATE-IEDQTRM(RPARM),SINGLE IS THIS A SINGLE   S22024
*                                       ENTRY                    S22024
         BNZ   SETXCTL                  NO, GO SET UP FOR NEXT   S22024
*                                       MODULE                   S22024
         TM    TRMDSORG-IEDQTRM(RPARM),TRMLGB IS THIS AN LGB     S22024
         BO    SETXCTL                  YES, GO SET UP FOR NEXT  S22024
*                                       MODULE                   S22024
         TM    TRMSTATE-IEDQTRM(RPARM),TRMPREF 3705 DEVICE?    @Y17XART
         LR    RZERO,RPARM              SAVE TERMINAL ENTRY ADDR S22024
         L     RPARM,TRMDESTQ-1-IEDQTRM(RPARM) QCB ADDRESS       S22024
         L     RPARM,QCBLGBAD-ONE-IEDQQCB(RPARM) LGB ADDRESS     S22024
         BNO   NOT3705                  BRANCH NO              @Y17XART
         LR    RWORK,RZERO              GET TTE ADDR           @Y17XART
         SH    RWORK,NEGPRFSZ           BACKUP TO NEG PREFIX   @Y17XART
         CLI   TRMTYPE-IEDNTRM(RWORK),TRMSSCP TTE FOR SSCP?    @Y17XART
         BE    ERROR16                  YES,INVALID STAT NAME  @Y17XART
         TM    TRMBYTE0-IEDNTRM(RWORK),TRMDIAL DIAL TERMINAL   @YM08035
         BO    ERROR88                  YES, INVALID VARY      @YM08035
         CLC   TRMCOHRT-IEDNTRM(2,RWORK),HALFZERO CHRT EXISTS? @Y17XART
         BE    ERR3705                  NO,ERROR EXIT          @Y17XART
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS      S22024
         BO    RESOURCE                 YES, SKIP OPEN TEST      S22024
         TM    LGBOFLGS-IEDNLGB(RPARM),OPENLGB IS LGB OPEN       S22024
         BZ    ERROR17                  BRANCH NO                S22024
         TM    OCSWITCH,DDSWIT          COMMD FOR DCB OR LINE  @SA73342
         BO    RESOURCE                 YES, BRANCH            @SA73342
         L     RDCB,OPCTRMWA            PICK UP TERM ENTRY     @SA73342
         TM    TRMSTATE-IEDQTRM(RDCB),TRMLINE  LINE ENTRY      @SA73342
         BO    ERROR16                  INVALID TERMINAL NAME  @SA73342
RESOURCE EQU   *                                                 S22024
         TM    DCBDSORG+ONE-IHADCB(RPARM),RNDSORG IS IT THE CCU  S22024
         BO    ERROR18                  BRANCH YES-NOT VALID     S22024
         TM    DCBDSORG-IHADCB(RPARM),DSLGB IS THIS AN LGB?      S22024
         BZ    NOT3705                  BRANCH NOT 3705          S22024
         ST    RZERO,OCMODNME           SAVE TE ADDR             S22024
         ST    RPARM,OCMODNME+4         SAVE DCB ADDR          @Y17XART
         LTR   RZERO,RZERO              TRM ADDR RETURNED      @YM08560
         BZ    NOT3705                  BRANCH NO              @YM08560
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROG        @Y17XART
         BO    XCTL                     YES, EXIT              @Y17XART
LOOP     EQU   *                                               @Y17XART
         LR    RPARM,RZERO              GET TTE ADDR           @Y17XART
         LA    RSAVE,OPCSAVE            GET SAVE AREA ADDR     @Y17XART
         L     RBRNCH,OPCHORT           ADDR OF COHORT SEARCH  @Y17XART
         BALR  RRET,RBRNCH              FIND POTENTIAL NCP     @Y17XART
         LTR   RZERO,RZERO              VALID TTE FOUND?       @Y17XART
         BZ    ERR3705                  NO,ERROR EXIT          @Y17XART
         LR    RZERO,RPARM              SAVE TTE BASE ADDR     @Y17XART
         SH    RPARM,NEGPRFSZ           PT TO NEGATIVE PREFIX  @Y17XART
         CLI   TRMTYPE-IEDNTRM(RPARM),TRMLNCP  NCP TTE ?       @Y17XART
         BNE   LOOP                     SEARCH UNTIL NCP FOUND @Y17XART
         TM    TRMBYTE2-IEDNTRM(RPARM),TRMINPG  IPL/DUMP INPRG @Y17XART
         BO    ERR37051                 YES,ERROR EXIT SETUP   @Y17XART
         TM    TRMBYTE2-IEDNTRM(RPARM),TRMRSACT NCP ACTIVE?    @Y17XART
         BNO   ERR3705                  NO, ERROR EXIT         @Y17XART
         L     RPARM,OCMODNME+FOUR      ESTABLISH LGB ADDR     @Y17XARR
         B     PARMSTOR                 CONTINUE               @Y17XART
         SPACE
NOT3705  EQU   *                                                 S22024
         ST    RPARM,OCMODNME+FOUR      SET UP DCB ADDRESS       S22024
PARMSTOR EQU   *                        RPARM ALREADY STORED   @Y17XART
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS      S22024
         BO    XCTL                     YES, EXIT                S22024
         TM    OPCFLAG1,ALLRLN          WAS RLN=ALL?
         BNO   GOODRLN                  NO, DO NOT RESET RLN
         SPACE
         MVI   OCRLN,AVTEZERO           RESET RLN TO ALL
GOODRLN  EQU   *
         LA    RZERO,ONE                SET PARAMETER
         TM    DCBDSORG-IHADCB(RPARM),DSLGB IS THIS AN LGB     @OY14092
         BZ    NOTLGB                   BRANCH                 @OY14092
         CLI   OCSWITCH,OCX12           OFFTP,I                @OY14092
         BE    XCTL                     YES, GO PROCESS        @OY14092
NOTLGB   EQU   *                                               @OY14092
         LA    RWORK,OPCWAITL           GET FIRST ELEMENT OFF WAIT QUE
         LA    RRET,XCTL                RETURN ADDRESS         @Y17XARR
         TM    DCBDSORG-IHADCB(RPARM),DSLGB LGB?               @Y17XARR
         BZ    GETEND                   SEARCH THE CHAIN       @Y17XARR
         LA    RWORK,OPCWAITV            GET ADDR OF 3705 VARY WAIT
*                                        CHAIN                 @Y17XARR
LOOKMORE L     RWORK,ZERO(RWORK)        GET FIRST ELEMENT      @Y17XART
         LA    RWORK,ZERO(RWORK)        CLEAR HIGH ORDER BYTE  @Y17XART
         LTR   RWORK,RWORK              IS THIS A VALID ELEM   @Y17XART
         BZ    XCTL                     EXIT AT END OF CHAIN   @Y17XART
         CLC   OCBACKUP-IEDQOPCE(8,ROPCE),OCBACKUP-IEDQOPCE(RWORK)
*                                       GROUP NAME MATCH       @Y17XART
         BNE   LOOKMORE                 KEEP LOOKING           @Y17XART
         B     ENQUEUE                  PROCESS WHEN FOUND     @Y17XART
GETEND   EQU   *
         LR    RHOLD,RWORK              SAVE PREVIOUS          @OY14092
         L     RWORK,ZERO(RWORK)        FIRST ELEMENT
         LA    RWORK,ZERO(RWORK)        CLEAR HIGH BYTE
         LTR   RWORK,RWORK              IS THERE AN ELEMENT
         BCR   EQUAL,RRET               RETURN                   S22024
         CLC   GRPNAME(THREE,ROPCE),GRPNAME(RWORK)  DO ID'S MATCHS22024
         BNE   GETEND                   NO
         SPACE
ENQUEUE  EQU   *
         CLI   OCSWITCH,OCX12           OFFTP,I                @OY14092
         BE    DETACH                   YES, DETACH IT         @OY14092
         TM    OCFLAG-IEDQOPCE(RWORK),OCATTACH IS THIS ATTACHED ELEM?
         BNO   ATTACH                   NO
         SPACE
         L     RWORK,FOUR(RWORK)        NO PICK UP NEXT ELEMENT
         B     ENQUEUE                  CHECK TO SEE IF ATTACHED
         SPACE
*              ATTACH NEW REQUEST FOR RESOURCE                        *
ATTACH   EQU   *
         ST    ROPCE,FOUR(RWORK)        QUEUE UP NEW ELEMENT
         OI    OCFLAG-IEDQOPCE(RWORK),OCATTACH UNIT IS ATTACHED
         MVC   OCWTG(NAMELEN),OPCLDNME+VARINAME  SET UP FUNCTION MOD NM
         STH   RZERO,OCPARM             SET FOR NEXT ENTRY TO STOPLINE
         MVC   OPCLDNME+VARINAME(NAMELEN),FIRSTLD SET TO GO TO 00
         XC    OCLINK+ONE(VARINAME),OCLINK+ONE ZERO LINK FIELD
         B     XCTL                     RETURN                 @Y17XART
DETACH   EQU   *                                               @OY14092
         ST    RWORK,OCELEM             CHAIN NEW TO NEXT      @OY14092
         MVC   OCLINK-IEDQOPCE(FOUR,RHOLD),OCLINK-IEDQOPCE(RWORK)
         XC    OCLINK-IEDQOPCE(FOUR,RWORK),OCLINK-IEDQOPCE(RWORK)
         OI    OCFLAG,OCATTACH          SET ATTACH FLAG        @OY14092
         NI    OCSWITCH-IEDQOPCE(RWORK),NOSTART RESET SWITCH TO@OY14092
*                                       RESTART THE LINE       @OY14092
         NC    OCWTG-IEDQOPCE(TWO,RWORK),OCWTG-IEDQOPCE(RWORK) @OY14092
         BNZ   XCTL                     WTG IS ALREADY SET     @OY14092
         MVC   OCWTG-IEDQOPCE(TWO,RWORK),THISMOD SET TO V0     @OY14092
         B     XCTL                     CALL NEXT MODULE       @OY14092
         SPACE
         SPACE 2
PLACNAME MVC   OPCDOUBL(AVTEZERO),AVTEZERO(RSTART) SAVE PROCESSED NAME
CMNDCHEK CLC   CMNDOFF(AVTEZERO,RWORK),AVTEZERO(RSCAN) CHECK CMD S22024
         SPACE 2
ERROR88  EQU   *                                               @YM08035
         LA    RZERO,DIALERR            DIAL VARY ERROR        @YM08035
         B     SETXCTL                  EXIT TO MSG MODULE     @YM08035
ERR3705  EQU   *                        3705 ERROR CODE        @Y17XART
         LA    RZERO,NACTIV05           PUT OUT 3705 NOT ACTIV @Y17XART
         OI    OCSWITCH,NORLN           DON'T INSERT RLN       @YM08035
         B     SETXCTL                  GOTO ERROR EXIT        @Y17XART
ERR37051 EQU   *                        MORE 3705 ERROR CODE   @Y17XART
         ICM   RPARM,ADDRESS,TRMDESTQ-IEDNTRM(RPARM) QCB ADDR  @Y17XARR
         ICM   RPARM,ADDRESS,QCBDCBAD-IEDQQCB(RPARM) DCB ADDR  @Y17XARR
         ICM   RPARM,ADDRESS,DCBDEBA-IHADCB(RPARM) DEB ADDR    @OY18838
         ICM   RPARM,ADDRESS,DEBUCBAD+1-DEBNMSUB(RPARM) UCB ADR@OY18838
         LA    RPARM,AVTEZERO(,RPARM) CLEAR HIGH ORDER BYTE    @Y17XARR
         ICM   RPARM,ADDRESS,UCBNAME-UCB(RPARM) GET EBCIDIC    @Y17XARR
         XC    OCBACKUP(8),OCBACKUP     CLEAR FOR INSERT       @Y17XARR
         ST    RPARM,OCBACKUP           INSERT UCB ADDR FOR MSG@Y17XARR
         LA    RZERO,IPLDMP             IPLDUMP IN PROG MSG    @Y17XART
         B     SETXCTL                  GOTO ERROR CODE        @Y17XART
         EJECT
         SPACE 2
CMNDTBL  EQU   *
         DC    X'07',X'05',C'V2',CL8'OFFTP,E' STOP TERM(ENTER)   S22024
         DC    X'07',X'06',C'V2',CL8'OFFTP,B' STOP TERM(BOTH)    S22024
         DC    X'06',X'0A',C'V2',CL7'ONTP,E'  START TERM(ENTER)  S22024
         DC    X'06',X'0B',C'V2',CL7'ONTP,B'  START TERM(BOTH)   S22024
         DC    X'07',X'11',C'V1',CL8'OFFTP,C' STOP LINE(CONT)    S22024
         DC    X'07',X'12',C'V1',CL8'OFFTP,I' STOP LINE(IMMED)   S22024
         DC    X'04',X'18',C'V3',CL5'ONTP'    START LINE         S22024
         DC    X'08',X'25',C'V2',CL9'OFFTP,EM' STOP TERM(MIDMSG)SA56606
         DC    X'08',X'26',C'V2',CL9'OFFTP,BM' STOP TERM(MIDMSG)SA56606
         DC    X'FF'                    END OF TABLE
         SPACE 2
CIBTABLE EQU   *
         DC    X'4018',C'V3'            START LINE
         DC    X'4118',C'V1'            STOP LINE
         DC    X'450B',C'V2'            START TERM (B)           S22024
         DC    X'460A',C'V2'            START TERM (E) TOTE      S22024
         DC    X'4706',C'V2'            STOP TERM (B)
         DC    X'4805',C'V2'            STOP TERM (E)  TOTE
         DC    X'550A',C'V2'            SPECIAL VERB CODE TO     S22024
*                                       VARY ONTP,E AND LEAVE    S22024
*                                       TRM ALLOC TO TOTE        S22024
         DC    X'FF'                    END OF TABLE
         SPACE 2
FIRSTLD  DC    C'0'                     FIRST LOAD - IGC0010D    S22024
XCTL03   DC    C'03'                    ERROR MODULE NAME
WRTXCTL  DC    C'07'                    OUTPUT MODULE
THISMOD  DC    C'V0'                    ID FOR RETURN          @OY14092
OH       DC    C',O'                    SCAN FOR OFF OR ON     @OY12425
MAX      DC    H'8'                                            @OY12425
HALFZERO DC    H'0'                     HALF WORD OF ZERO      @Y17XART
NEGPRFSZ DC    AL2(TRMPRFSZ)            TTE NEG PREFIX SIZE    @Y17XART
         TITLE 'IGCV010D - DSECTS REFERENCED BY THIS MODULE'
         TAVTD
         EJECT
         DCBD  DSORG=TR                                          Y06330
         EJECT
         TDEBD
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
GRPNAME  EQU   OCMODNME+5-IEDQOPCE      OFFSET TO GROUP ID       S22024
         TLCBD                                                   S22024
         EJECT
         TTRMD                                                   S22024
         EJECT
         TQCBD                                                   S22024
         EJECT
         TLGBD                                                   S22024
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         END
