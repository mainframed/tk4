         TITLE 'IGCM710D - MODIFY PRIMARY/3705 SET TIMEDATE'
*A000000-999999                                                  S99240
*D616000                                                        SA59165
*A622000                                                        SA59165
*C601000                                                       @Y17XARZ
*C724800-727000                                                @0Y12425
*D505900                                                       @0Y12425
*A016000,505600,531200,600000,784000,804280,884180             @Y17XARY
*C724400,725800-726600,804300,805980-806120,806460-806520      @Y17XARY
*D531200                                                       @Y17XARY
***********************************************************************
*                                                                     *
*                                                                     *
* MODULE NAME = IGCM710D (TCAM,OPERATOR CONTOL)                @Y17XARY
*                                                                     *
* DESCRIPTIVE NAME = MODIFY PRIMARY/3705 SET TIME DATE         @Y17XARY
*                                                                     *
* COPYRIGHT = NONE                                             @Y17XARY
*                                                                     *
* STATUS = VERSION 10.0                                        @Y17XARY
*                                                                     *
*                                                                     *
* FUNCTION - THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH   *
*   REQUEST THAT THE TIME AND DATE BE SET IN THE 3705 AND WHICH  S22024
*   REQUEST THE PRIMARY OPERATOR CONTROL TERMINAL BE CHANGED FROM ITS *
*   CURRENT MEMBER TO THE ONE SPECIFIED IN THE COMMAND. IGCM710D      *
*   RECEIVES CONTROL FROM THE MODIFY SCAN/MAP MODULE (IGCMA10D).      *
*                                                                     *
*   THIS ROUTINE CHECKS THE ELEMENT (OPCE) TO DETERMINE IF THE        *
*   REQUEST IS FOR PRIMARY CHANGE AND IF SO DETERMINES IF THE    S22024
*   REQUESTED PRIMARY TERMINAL IS THE SYSTEM CONSOLE. IF IT IS, THE   *
*   PRIMARY OFFSET IN THE AVT (AVTOPCON) IS CHECKED FOR ZERO. IF ZERO,*
*   THE CONSOLE IS ALREADY PRIMARY. A MESSAGE NUMBER IS SET IN REG 0  *
*   AND XCTL IS TO IGC0310D.                                          *
*                                                                     *
*   IF THE SYSTEM CONSOLE IS NOT ALREADY PRIMARY, AVTOPCON IS SET TO  *
*   ZERO. IF THE OPERATOR AWARENESS WRITER MODULE IS IN THE SYSTEM    *
*   (AVTNX IS NOT ZERO) THE IEDQNX ROUTINE IS DELETED AND AVTNX IS SET*
*   TO ZERO. A MESSAGE NUMBER IS SET IN OCPARM, OCWTG IS SET TO M1 AND*
*   XCTL IS TO THE OP CTL INCIDENT CHECKPOINT INTERFACE ROUTINE.      *
*                                                                     *
*   IF THE PRIMARY REQUESTED IS NOT SYSCON, THE TERMINAL IS LOCATED   *
*   AND TESTED FOR VALID SECONDARY TERMINAL.(TRMSCNYN SET IN TRMSTATE)*
*   IF NOT, AN ERROR MSG IS SET IN REG 0 AND XCTL IS TO IGC0310D.     *
*                                                                     *
*   THE ENTRY OFFSET FROM THE START OF THE TERMNAME TABLE IS COMPARED *
*   TO AVTOPCON. IF THEY ARE THE SAME, THE TERMINAL IS ALREADY PRIMARY*
*   AND A MESSAGE NUMBER INDICATING THIS IS SET IN REGISTER 0 AND XCTL*
*   IS TO IGC0310D. OTHERWISE, THE NEW OFFSET IS PLACED IN AVTOPCON,  *
*   AND AVTNX IS TESTED FOR THE PRESENCE OF THE IEDQNX ROUTINE. IF IT *
*   IS NOT IN THE SYSTEM, THE IEDQNX ROUTINE IS LOADED AND ITS ADDRESS*
*   STORED IN AVTNX. A MESSAGE NUMBER IS SET IN OCPARM,OCWTG IS SET TO*
*   M1, AND XCTL IS TO MODULE IGC0610D (THE OP CTL CKPT INTERFACE RTN)*
*                                                                S22024
*   IF THE REQUEST IS FOR 3705 TIME/DATE SET, THIS ROUTINE       S22024
*   ISSUES A TIME MACRO TO OBTAIN THE SYSTEM TIME AND DATE,      S22024
*   FORMATS THE DATE/TIME ALONG WITH A REQUEST BTU AND DISPATCHESS22024
*   THIS REQUEST TO THE  3705 BY XCTLING TO IGC0210D. WHEN THE   S22024
*   RESPONSE BTU IS RECEIVED FROM THE 3705, VIA IGC0210D, IT IS  S22024
*   EXAMINED FOR ERRORS. IF NO ERROR OCCURED, A TIME/DATE SET    S22024
*   RESPONSE IS SET IN REG ZERO AND XCTL IS TO IGCM310D. IF THE  S22024
*   RESPONSE BTU INDICATES AN ERROR CONDITION, THE ERROR RESPONSES22024
*   IS SET AND XCTL IS TO IGC0310D.                              S22024
*                                                                     *
*   MESSAGES INITIATED BY THIS MODULE ARE:                            *
*     ERROR INDICATIONS                                               *
*        IED016I STATION XXXXXXXX NOT FOUND                           *
*        IED044I XXXXXXXX NOT ELIGIBLE FOR PRIMARY                    *
*        IED042I XXXXXXXX ALREADY PRIMARY                             *
*        IED173I XXXXXXXX NOT VALID 3705 - COMMAND REJECTED      S22024
*        IED199I CONTROL CMD XX MOD XX FOR XXXXXXXX ERROR        S22024
*                RESPONSE XXXX                                   S22024
*     SUCCESS INDICATIONS                                        S22024
*        IED041I PRIMARY=XXXXXXXX                                     *
*        IED200I XXXXXXXX TIME AND DATE SET                      S22024
*                                                                     *
* ENTRY POINTS -                                                      *
*   IGCM710D IS THE ONLY ENTRY POINT FOR THIS MODULE                  *
*                                                                     *
* INPUT - REG 1 CONTAINES A POINTER TO THE OPERATOR CONTROL AVT.      *
*                                                                     *
* OUTPUT - PASSES OP CTL AVT ADDRESS IN REG 1.                        *
*                                                                     *
* EXTERNAL REFERENCES -- XCTL(SVC 7),LOAD(SVC 8),DELETE(SVC 9)        *
*   AND TIME(SVC 11)                                             S22024
*   IEDQCA'S SUBROUTINES                                              *
*        TRMOFLOC--VALIDITY CHECKS TERMINAL NAME PASSED TO IT. RETURNS*
*                  ADDRESS OF TERMINAL ENTRY IF VALID. IF NOT VALID   *
*                  RETURNS PLUS 4 OF RETURN ADDRESS IN REGISTER 14.   *
*                                                                     *
*        DCBLOCAT--VALIDITY CHECKS HARDWARE LINE ADDRESSES, GROUPNAMES*
*                  AND RELATIVE LINE NUMBERS. ALSO INSURES LINE DCBS  *
*                  ARE OPEN, RLN IS NOT MORE THAN NUMBER OF EXTENTS,  *
*                  AND LINE WAS NOT OPENED DUMMY. FOR ANY OF THESE    *
*                  ERRORS RETURNS PLUS 4 OF RETURN ADDRESS IN R14.    *
*                             GOOD RETURN- R0= UCB ADDRESS            *
*                                          R1= DCB ADDRESS            *
*                                                                     *
*                                                                     *
* EXITS,NORMAL - FOR THE TIME/DATE OPERATION, EXIT IS TO THE BTU S22024
*   DISPATCHER (IGC0210D) TO XMIT THE REQUEST BTU TO THE 3705.   S22024
*   UPON COMPLETION OF THE TIME/DATE FUNCTION, THE TIME AND DATE S22024
*   SET MESSAGE NUMBER IS PLACED IN REG ZERO, AND XCTL IS TO     S22024
*   IGCM310D. FOR MODIFY PRIMARY, THE MESSAGE NUMBER IS PLACED INS22024
*   THE ELEMENTS OCPARM FIELD, THE ELEMENTS OCWTG FIELD IS SET TO M1  *
*   AND AN XCTL (SVC 7) IS MADE TO THE OP CTL INCIDENT CHECKPOINT     *
*   ROUTINE (IGC0610D), REG 1 = @ OP CTL AVT.                         *
*                                                                     *
* EXITS,ERROR - THE ERROR MESSAGE NUMBER IS PLACED IN REGISTER 0,     *
*   THE OP CTL AVT ADDRESS IN REG 1 AND XCTL IS TO THE OP CTL ERROR   *
*   MESSAGE MODULE (IGC0310D).                                        *
*                                                                     *
* TABLES/WORK AREAS - OPERATOR CONTROL AVT, TCAM AVT, OPCE, OPERATOR  *
*   CONTROL RESIDENT WORK AREA, DCB, LCB, LGB, AND BTU.          S22024
*                                                                     *
* ATTRIBUTES - SUPERVISOR MODE, DISABLED, TRANSIENT(TYPE 4 SVC), NON- *
*             REENTRANT, SERIALLY REUSABLE, REFRESHABLE.              *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT *
*    TO THAT USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED SO  *
*    THAT REDEFINITION OF CHARACTER CONSTANTS, BY REASSEMBLY, WILL    *
*    RESULT IN A CORRECT MODULE FOR THE NEW DEFINITION.               *
*                                                                     *
* NOTES -                                                             *
*    COMMUNICATIONS BETWEEN THE VARIOUS MODULES/TRANSIENTS IS         *
*    PRIMARILY THROUGH THE OPERATOR CONTROL ADDRESS VECTOR TABLE (AVT)*
*                                                                     *
*        THIS IS A MODULE REWRITE OF IEDQCN AS OF OS/TCAM LEVEL II    *
***********************************************************************
         EJECT
IGCM710D CSECT
         SPACE
RZERO    EQU   0                        PARM REG 0
RPARM    EQU   1                        PARM REG 1
ROPCAVT  EQU   2                        OP CONTROL AVT POINTER
ROPCE    EQU   3                        CURRENT ELEMENT ADDRESS
RAVT     EQU   4                        TCAM AVT ADDRESS
RTNT     EQU   5                        TERN NAME TABLE BASE
RTERM    EQU   6                        TERMENTRY BASE
RWORK7   EQU   7                        WORK REGISTER
RDCB     EQU   7                        LGB BASE                 S22024
RWORK8   EQU   8                        WORK REGISTER
RWORK9   EQU   9                        WORK REGISTER
RWORK10  EQU   10                       WORK REGISTER
RWORK11  EQU   11                       WORK REGISTER
RSAVE    EQU   13                       SAVE AREA POINTER        S22024
RBASE    EQU   12                       MODULE BASE
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RBRNCH   EQU   15                       SUBROUTINE BRANCH ENTRY POINT
         SPACE
****    MISCELLANEOUS EQUATES    ****
         SPACE
ZERO     EQU   0                        ZERO VALUE
ONE      EQU   1                        VALUE ONE
TWO      EQU   2                        VALUE TWO
THREE    EQU   3                        VALUE THREE
VARINAME EQU   3                        OFFSET TO CHANGE XCTL NAME
FOUR     EQU   4                        VALUE FOUR
X14      EQU   X'14'                    HEX '14'
SIXTEEN  EQU   16                       VALUE SISTEEN
FORTY1   EQU   41                       VALUE FORTY ONE
FORTY2   EQU   42                       VALUE FORTY TWO
FORTY4   EQU   44                       VALUE FORTY FOUR
FIVE     EQU   5                        VALUE 5                  S22024
SIX      EQU   6                        VALUE 6                  S22024
SEVEN    EQU   7                        VALUE 7                  S22024
EIGHT    EQU   8                        VALUE 8                  S22024
NINE     EQU   9                        VALUE 9                  S22024
ELEVEN   EQU   11                       VALUE 11                 S22024
TWELVE   EQU   12                       VALUE TWELVE             S22024
THIRTEEN EQU   13                       VALUE 13                 S22024
FORTEEN  EQU   14                       VALUE 14                 S22024
FIFTEEN  EQU   15                       VALUE 15                 S22024
SEVNTEEN EQU   17                       VALUE SEVENTEEN          S22024
EIGHTEEN EQU   18                       VALUE 18                 S22024
NINETEEN EQU   19                       VALUE NINETEEN           S22024
TWENTY   EQU   20                       VALUE 20                 S22024
TWENTY1  EQU   21                       VALUE 21                 S22024
TWENTY4  EQU   24                       OFFSET TWENTY FOUR       S22024
TWENTY6  EQU   26                       VALUE 26               @Y17XARY
THIRTY1  EQU   31                       VALUE 31                 S22024
THIRTY4  EQU   34                       VALUE 34                 S22024
TWO40    EQU   X'F0'                    TWO HUNDRED FORTY        S22024
X01      EQU   X'01'                    HEX 01                   S22024
X04      EQU   X'04'                    HEX 04                   S22024
X09      EQU   X'09'                    HEX 09                   S22024
X18      EQU   X'18'                    HEX 18                   S22024
X60      EQU   X'60'                    HEX 60                   S22024
X80      EQU   X'80'                    HEX 80                   S22024
PERIOD   EQU   C'.'                     PERIOD                   S22024
SLASH    EQU   C'/'                     SLASH                    S22024
ZONE     EQU   X'F0'                    DECIMAL ZONE             S22024
PLUS     EQU   X'0F'                    DECIMAL PLUS SIGN        S22024
PRIBTU   EQU   X'E0'                    POST PRIORITY FOR BTU    S22024
ONE73    EQU   173                      VALUE 173                S22024
ONE99    EQU   199                      ONE HUNDRED NINTY NINE   S22024
TWOM     EQU   200                      TWO HUNDRED              S22024
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         USING IEDQTNTD,RTNT            TERM NAME ADDRESSABILITY
         USING IEDNTRM,RTERM
         USING IEDNLGB,RDCB             LGB ADDRESSABILITY       S22024
         USING IHADCB,RWORK7            DCB ADDRESSABILITY       S22024
         USING IEDQLCB,RWORK8           LCB ADDRESSABILITY       S22024
         USING IEDPRUND,RPARM           RU ADDRESSABILITY      @Y17XARY
         USING IEDNSVTD,RWORK10         SAVT ADDRESSABILITY    @Y17XARY
         SPACE
         BALR  RBASE,0                  SET BASE REGISTER
         USING *,RBASE                  MODULE ADDRESSABILITY
A        EQU   *
IGCM710D IEDHJN SKIPID
         LR    ROPCAVT,RPARM            SET BASE FOR OP CTL AVT
         L     RAVT,OPCAVTPT            SET BASE FOR TCAM AVT
         L     ROPCE,OPCCOPCE           PICK UP ELEMENT POINTER
         CLI   OCSWITCH,X18             TEST FOR TIMDAT COMMAND  S22024
         BE    TD3705                   YES, GO GET TIME & DATE  S22024
         CLI   OCSWITCH,X14             CHANGE TO CONSOLE ?
         BNE   GETTRM                   NO - FIND TERM ENTRY
         SR    RWORK7,RWORK7            CLEAR REG
         CH    RWORK7,AVTOPCON          ALREADY CONSOLE
         BE    ALR                      YES - PUT PROPER MSG
         STH   RWORK7,AVTOPCON          CLEAR AVT FIELD - DEFAULT
         STH   RWORK7,AVTOPCIN          CLEAR AVT FIELD        @YM06091
         OC    AVTSEND,AVTSEND   IS 3705 IN THE SYSTEM         @Y17XARZ
         BNZ   MESSAGE                  YES, DON'T DELETE IEDQNX S22024
         CLC   AVTNX(FOUR),AVTFZERO     IEDQNX IN SYSTEM
         BE    MESSAGE                  NO - EXIT
         LA    RWORK7,EPNX              GET ENTRY POINT
         MVC   AVTNX(FOUR),AVTFZERO     CLEAR AVTNX FIELD
         DELETE EPLOC=(RWORK7)          DELETE IEDQNX           SA59165
         B     MESSAGE                  EXIT
         SPACE
****    GET TERMINAL ENTRY    ****
GETTRM   EQU   *
         L     RTNT,AVTRNMPT            SET TNT BASE
         LH    RWORK8,OCTRMTBL          GET TNT OFFSET FROM ELEMENT
         SLL   RWORK8,SIXTEEN           CLEAR OUT HI-ORDER
         SRL   RWORK8,SIXTEEN              SIXTEEN BITS
         SR    RWORK7,RWORK7            CLEAR REG
         IC    RWORK7,TNTENLEN          PICK UP ENTRY LENGTH
         LA    RWORK9,THREE(RWORK7)     ALLOW FOR ADDRESS
         BCTR  RWORK8,RZERO             REDUCE TNT OFFSET BY ONE
         MR    RWORK8,RWORK8            CALCULATE OFFSET
         LA    RWORK8,TNTFIRST          GET ADDRESS OF FIRST ENTRY
         AR    RWORK8,RWORK9            ADD OFFSET TO FIRST ENTRY
         LR    RWORK9,RWORK8            SAVE NAME POINTER
         AR    RWORK8,RWORK7            POINT TO ADDRESS OF TERM ENTRY
         MVC   OCBACKUP+ONE(THREE),ZERO(RWORK8) MOVE ADDRESS TO BNDRY
         L     RTERM,OCBACKUP           LOAD TERM ENTRY BASE
         LA    RTERM,ZERO(RTERM)        CLEAR HI-ORDER BYTE
         LA    RBRNCH,TRMPRFSZ          GET NEG PREFIX SIZE    @YM06469
         SR    RTERM,RBRNCH             BACK UP TO NEG PREFIX  @YM06469
         TM    TRMSTATE,TRMSCNYN        VALID SECONDARY
         BZ    ERROR                    NO, ERROR                S22024
         TM    TRMDSORG,TRMLGB          ENTRY AN LGB ?           S22024
         BZ    SET                      NO, STORE OFFSET IN AVT  S22024
         SPACE
ERROR    EQU   *
         LA    RZERO,FORTY4             SET NOT ELIGIBLE FOR PRIMARY
         B     XCTLERR                  BR TO INDICATE ERROR
TD3705   EQU   *                                                 S22024
         TM    OCFLAG,OCRESP            TEST FOR RETURN PIU    @Y17XARY
         BO    RETRNBTU                 YES,PROCESS RTRND ELEM.  S22024
         TM    OCSCAN,OCNONAME          NOT DDNAME OR ADDRESS? @OY12425
         BO    NOTFOUND                 TRY NCP NAME           @OY12425
         L     RPARM,OCELEM             SETUP DCB ADDRESS      @OY12425
         B     GOODRTN                  GOOD RETURN,CONTINUE   @OY12425
NOTFOUND EQU   *                                               @OY12425
         LA    RSAVE,OPCSAVE            OP CTL SAVE AREA       @Y17XARY
         L     RBRNCH,OPCTOFLK          ADDR TO TNT OFFSET     @Y17XARY
         BALR  RRET,RBRNCH              TRMOFLOC SUBROUTINE    @Y17XARY
         B     RTNX                     YES,GET RESOURCE TYPE  @Y17XARY
         LA    RZERO,SEVNTEEN           SET NOT OPEN MESSAGE     S22024
         B     XCTLERR                  BR TO OUTPUT ERROR MSG   S22024
         SPACE
         SPACE
****    SET TERMINAL FOR PRIMARY    ****
SET      EQU   *
         SR    RWORK9,RTNT              CALC OFFSET FROM TABLE START
         LR    RTNT,RWORK9              SAVE OFFSET
         CH    RTNT,AVTOPCON            ALREADY THIS TERMINAL
         BE    ALR                      YES - PUT ALREADY MESSAGE
         CLC   AVTNX(FOUR),AVTFZERO     CHECK IEDQNX LOADED
         BNE   MESSAGE1                 YES - BYPASS LOAD
         LA    RWORK9,EPNX              GET ENTRY POINT
         LOAD  EPLOC=(RWORK9)           LOAD IEDQNX
         ST    RZERO,AVTNX              PUT ITS ADDRESS IN AVT
         SPACE
MESSAGE1 EQU   *
         STH   RTNT,AVTOPCON            PUT NEW OFFSET IN AVT
         LH    RWORK9,OCTRMTBL          TNT OFFSET TO TERMNAME @YM06156
         STH   RWORK9,AVTOPCIN          SAVE TNT INDEX         @YM06156
         SPACE
MESSAGE  EQU   *
         LA    RZERO,FORTY1             SET PRIMARY =
         STH   RZERO,OCPARM               FOR MESSAGE MODULE
         MVC   OCBACKUP(EIGHT),OPCDOUBL  SAVE TERMINAL NAME    @YM06497
         B     XCTLOK                   BR TO EXIT
         SPACE
GOODRTN  EQU   *                                                 S22024
         LR    RWORK7,RPARM             SET DCB BASE             S22024
         TM    DCBDSORG,X80             CK FOR DCB               S22024
         BO    NO3705                   NOT DCB - ERROR          S22024
         TM    DCBDSORG+ONE,X04         IS IT 3705               S22024
         BNO   NO3705                   NO - ERROR               S22024
         SR    RPARM,RPARM              ZERO REG FOR IC          S22024
         IC    RPARM,DCBEIOBX           GET IOB LENGTH           S22024
         L     RWORK8,DCBIOBAD          GET IOB                  S22024
         LA    RWORK8,ZERO(RWORK8)      CLEAR HI-ORDER BYTE      S22024
         AR    RWORK8,RPARM             ADD LENGTH TO IOB ADDR   S22024
         LA    RWORK7,LCBFLAG1-IEDQLCB  IOB/LCB OFFSET           S22024
         SR    RWORK8,RWORK7            BACK UP TO START OF LCB  S22024
         LH    RPARM,LCBTTCIN         TERM CURR CONNECTED INDEX@Y17XARY
         STH   RPARM,OCTRMTBL           OFFSET IN TNT TO TNAME @Y17XARY
         L     RBRNCH,AVTRNMPT          TERM NAME TABLE ROUTINE@Y17XARY
         BALR  RRET,RBRNCH              GET TERMINAL ADDRESS   @Y17XARY
         SPACE
RTNX     EQU   *                                               @Y17XARY
         LA    RWORK9,TRMPRFSZ          NEG PREFIX OF IEDQTRM  @Y17XARY
         SR    RPARM,RWORK9             GET NEGATIVE PREFIX    @Y17XARY
         LR    RTERM,RPARM                                     @Y17XARY
         CLI   TRMTYPE,TRMLNCP          NCP RESOURCE?          @YM06954
         BNE   NO3705                   NOT NCP                @YM06954
         SR    RPARM,RPARM              CLEAR REGISTER         @Y17XARY
         LH    RPARM,OCTRMTBL           OFFSET IN TNT TO TNAME @Y17XARY
         L     RWORK10,AVTSAVTP         SECONDARY AVT POINTER  @Y17XARY
         L     RBRNCH,SAVTTNTX          TTCIN TO NA CONVERSION @Y17XARY
         BALR  RRET,RBRNCH              GO TO CONVERSION RTINE @Y17XARY
         STH   RBRNCH,OCRSID            STORE NETWORK ADDRESS  @Y17XARY
**************************************************************** S22024
*  THE FOLLOWING ROUTINE GETS THE TIME AND DATE AND MAPS         S22024
*  SAME INTO REQUIRED FORMAT FOR TEXT OF THE SET TIME/DATE       S22024
*  OP CTL COMMAND FOR THE 3705.                                  S22024
**************************************************************** S22024
         SPACE
         TIME  DEC                      ISSUE TIME MACRO         S22024
         L     RWORK11,OPCWORK          GET WORK AREA ADDRESS    S22024
         LA    RWORK11,TWENTY4(RWORK11) BUMP BY TWENTY FOUR      S22024
         USING TIMEDATE,RWORK11                                  S22024
         ST    RZERO,TDTIME             SAVE TIME FOR LATER USE  S22024
         SR    RZERO,RZERO              CLEAR REGISTER           S22024
         ST    RZERO,TDDOUBL            ZERO FIRST WORD OF AREA  S22024
         SLDL  RZERO,SIXTEEN            SHIFT YEAR INTO RZERO    S22024
         SLL   RZERO,FOUR               MAKE ROOM FOR SIGN       S22024
         SRL   RPARM,SIXTEEN            RIGHT-JUSTIFY DAY IN REG S22024
         SPACE 1
*              WE NOW HAVE THE YEAR IN RZERO AND THE JULIAN DAY  S22024
*              IN RPARM, BOTH IN PACKED FORMAT (EXCEPT THAT THE  S22024
*              SIGN ON THE YEAR IS INVALID).                     S22024
         SPACE 1
         STM   RZERO,RPARM,TDSAVEY      SAVE YEAR & JULIAN DATE  S22024
         OI    TDSAVEY+THREE,PLUS       PUT VALID SIGN ON YEAR   S22024
         MVC   TDDOUBL+FOUR(FOUR),TDSAVEJ  MOVE DAYS TO AREA     S22024
         CVB   RTERM,TDDOUBL            CONVERT DAYS TO BINARY   S22024
         MVC   TDDOUBL+FOUR(FOUR),TDSAVEY  MOVE YEAR TO AREA     S22024
         CVB   RZERO,TDDOUBL            CONVERT YEAR TO BINARY   S22024
         SR    RPARM,RPARM              CLEAR REG                S22024
         SRDL  RZERO,TWO                DIVIDE BY FOUR           S22024
         LTR   RPARM,RPARM              ANY REMAINDER            S22024
         BNZ   NOLEAP                   YES - ITS NOT LEAP YEAR  S22024
         LA    RPARM,ONE(RPARM)         SET UP FOR LEAP YEAR     S22024
         SPACE
NOLEAP   EQU   *                                                 S22024
         LA    RWORK7,ONE               LOAD  MONTH REG          S22024
         LA    RWORK10,MONTHS           GET ADDRESS OF D/MO TBL  S22024
         LA    RWORK9,TWELVE            LOAD NUM OF MONTHS       S22024
         SR    RWORK8,RWORK8            CLEAR REGISTER           S22024
         SPACE
MODAY    EQU   *                                                 S22024
         AH    RWORK8,ZERO(RWORK10)     ADD NUM DAYS IN MONTH    S22024
         CR    RTERM,RWORK8             IS THIS CORRECT MONTH    S22024
         BNH   FOUND                    YES, BR TO FIG DAY OF MO S22024
         BCT   RPARM,LEAPNOT            BR NOT LEAP YEAR         S22024
         LA    RWORK8,ONE(RWORK8)       ADD ONE DAY FOR LEAP YR  S22024
         SPACE
LEAPNOT  EQU   *                                                 S22024
         LA    RWORK7,ONE(RWORK7)       ADD ONE TO MONTH         S22024
         LA    RWORK10,TWO(RWORK10)     BUMP MONTH TBL POINTER   S22024
         BCT   RWORK9,MODAY             LOOP THRU MONTHS         S22024
         SPACE
FOUND    EQU   *                                                 S22024
         AH    RTERM,ZERO(RWORK10)      ADJUST FOR DAY CALC      S22024
         SR    RTERM,RWORK8             CALC DAY OF MONTH        S22024
         CVD   RWORK7,TDDOUBL           CONVERT MONTH TO PACKED  S22024
         UNPK  TDMONTH,TDDOUBL+SIX(TWO) UNPACK MONTH             S22024
         OI    TDMONTH+ONE,ZONE         CHANGE TO PRINTABLE      S22024
         MVI   TDSLASH1,SLASH           INSERT SLASH             S22024
         CVD   RTERM,TDDOUBL            CONVERT DAY-OF-MONTH     S22024
         UNPK  TDDAY,TDDOUBL+SIX(TWO)   UNPACK DAY-OF-MONTH      S22024
         OI    TDDAY+ONE,ZONE           CHANGE TO PRINTABLE      S22024
         MVI   TDSLASH2,SLASH           INSERT SLASH             S22024
         UNPK  TDYEAR,TDSAVEY+TWO(TWO)  UNPACK YEAR              S22024
         MVI   TDDOT1,PERIOD            INSERT PERIOD            S22024
         UNPK  TDJULIAN,TDSAVEJ+TWO(TWO)  UNPACK JULIAN DAY      S22024
         UNPK  TDHOUR(THREE),TDTIME(TWO)  UNPACK HOUR            S22024
         MVI   TDDOT2,PERIOD            INSERT PERIOD            S22024
         UNPK  TDMINUTE(THREE),TDTIME+ONE(TWO)  UNPACK MINUTE    S22024
         MVI   TDDOT3,PERIOD            INSERT PERIOD            S22024
         UNPK  TDSECOND(THREE),TDTIME+TWO(TWO)  UNPACK SECOND    S22024
         SPACE
**************************************************************** S22024
*  THIS ROUTINE OBTAINS A UNIT AND SETS UP A SET TIME/DATE       S22024
*  BTU.  STORES THE ADDRESS OF THIS BTU IN THE TIMEDAT ELEMENT,  S22024
*  AND SETS UP XCTL TO IGC0210D.                                 S22024
**************************************************************** S22024
         SPACE
         LA    RPARM,ONE                SET NUM UNITS REQUESTED  S22024
         L     RBRNCH,OPCGETBF          LOAD GET BUFF RTN ADDR   S22024
         BALR  RRET,RBRNCH              LINK TO GET BUFFER       S22024
         ST    RPARM,OCUNIT             SET BFR ADDR TO ELEMENT  S22024
         LA    RWORK9,PRUNLEN           NEGATIVE PREFIX LENGTH @Y17XARY
         SR    RPARM,RWORK9             GET NEG PREFIX OF BUFF @Y17XARY
         XC    IEDPRUND(PRUNLEN),IEDPRUND  ZERO OUT NEG PREFIX @Y17XARY
         MVC   PRURHTH,PRUVALUE         SET RH AND TH FIELDS   @Y17XARY
         MVC   PRUTTCIN,OCTRMTBL        SET RETURN RESPONSE    @Y17XARY
         MVI   PRUDATCT,ZERO            SET DATA COUNT, BYTE 1 @YM06519
         MVI   PRUDATCT+ONE,TWENTY6     SET DATA COUNT         @Y17XARY
         MVC   PRUTIC(FOUR),INVALTIC    INDICATES ONE UNIT     @Y17XARY
         MVC   PRUDATA(THREE),RUCMDCDE  RU COMMAND CODE        @Y17XARY
         MVC   PRUDATA+THREE(TWO),OCRSID   NETWORK ADDRESS     @Y17XARY
         MVI   PRUDATA+FIVE,X01         RU COMMAND CODE        @Y17XARY
         MVC   PRUDATA+SIX(TWENTY),ZERO(RWORK11)  DATE AND TIME@Y17XARY
         MVC   OCWTG(TWO),THISMOD       SET RETURN TO THIS MOD   S22024
         OI    OCFLAG,X01               FLAG ELEMENT FOR RETURN  S22024
         MVC   OPCLDNME+VARINAME(TWO),BTUDISP SET BTU DISPATCHER S22024
         SR    RZERO,RZERO              CLEAR REGISTER           S22024
         B     EXIT                     EXIT TO BTU DISPATCHER   S22024
         SPACE
**************************************************************** S22024
*  THIS ROUTINE GETS CONTROL WHEN IT IS DETERMINED THAT THE      S22024
*  BTU BEING PROCESSED IS A RETURN BTU AND SETS UP TO INDICATE   S22024
*  RESULTS OF THE SET TIME/DATE REQUEST                          S22024
**************************************************************** S22024
         SPACE
RETRNBTU EQU   *                                                 S22024
         L     RPARM,OCUNIT             GET BTU ADDRESS          S22024
         LA    RPARM,ZERO(RPARM)        CLEAR HI-ORDER BYTE      S22024
         LA    RWORK9,PRUNLEN           GET PACKED RU LENGTH   @Y17XARY
         SR    RPARM,RWORK9             GET NEGATIVE PREFIX    @Y17XARY
         TM    PRURHFG0,PRURHSDI        RH ERROR               @Y17XARY
         BO    NOTSET                   BAD RETURN - INDICATE  @Y17XARY
         LA    RZERO,TWOM               INDICATOR FOR T&D SET    S22024
         MVC   OPCLDNME+VARINAME(TWO),GOODM SET XCTL TO MSG MOD  S22024
         B     EXIT                     EXIT TO IND T&D SET      S22024
         SPACE
NOTSET   EQU   *                                                 S22024
         LA    RZERO,ONE99              SET ERROR MSG NUMBER     S22024
         B     XCTLERR                  EXIT TO INDICATE ERROR   S22024
         SPACE
NO3705   EQU   *
         LA    RZERO,ONE73              SET NOT 3705             S22024
         B     XCTLERR                  EXIT TO INDICATE SAME    S22024
         SPACE
COMINV   EQU   *                                                 S22024
         LA    RZERO,EIGHTEEN           SET CMD INVALID          S22024
         B     XCTLERR                  EXIT TO INDICATE SAME    S22024
         SPACE
ALR      EQU   *
         LA    RZERO,FORTY2             SET ALREADY PRIMARY
         SPACE
XCTLERR  EQU   *
         MVC   OPCLDNME+VARINAME(TWO),ERRWRT SET ERROR RESP MODULE
         B     EXIT                     EXIT
         SPACE
XCTLOK   EQU   *
         MVC   OCWTG(TWO),OKWRT         SET RETURN TO WRT MSG MOD
         MVC   OPCLDNME+VARINAME(TWO),CKPTMOD SET XCTL TO CKPOINT MOD
         SPACE
EXIT     EQU   *
         LR    RPARM,ROPCAVT            PASS OP CTL AVT PTR
         XCTL  SF=(E,OPCXCTL)           CALL WRT MODULE
         SPACE
ERRWRT   DC    C'03'                    ERROR MESSAGE MODULE
OKWRT    DC    C'M1'                    MODIFY WRIGHT MODULE
CKPTMOD  DC    C'06'                    INCIDENT CHECKPOINT INTERFACE
EPNX     DC    C'IEDQNX  '              MODULE LOAD NAME
THISMOD  DC    C'M7'                    THIS MODULE NAME         S22024
BTUDISP  DC    C'02'                    BTU DISPATCHER MODULE    S22024
GOODM    DC    C'M3'                    MSG MODULE               S22024
PRUVALUE DC    X'0B80001C'              RH AND TH VALUES       @Y17XARY
RUCMDCDE DC    X'010211'                RU COMMAND CODE        @Y17XARY
INVALTIC DC    X'08000002'              INVALID TIC            @Y17XARY
         SPACE
MONTHS   DS    0H                                              @YM06953
         DC    H'31'                    DAYS IN JAN              S22024
         DC    H'28'                    DAYS IN FEB              S22024
         DC    H'31'                    DAYS IN MAR              S22024
         DC    H'30'                    DAYS IN APR              S22024
         DC    H'31'                    DAYS IN MAY              S22024
         DC    H'30'                    DAYS IN JUN              S22024
         DC    H'31'                    DAYS IN JUL              S22024
         DC    H'31'                    DAYS IN AUG              S22024
         DC    H'30'                    DAYS IN SEP              S22024
         DC    H'31'                    DAYS IN OCT              S22024
         DC    H'30'                    DAYS IN NOV              S22024
         DC    H'31'                    DAYS IN DEC              S22024
         ORG   IGCM710D+X'3FF'          PATCH AREA               S22024
         DC    X'FF'                    PATCH AREA               S22024
         ORG
         EJECT
         DCBD  DSORG=TX                                          S22024
         EJECT
         TLCBD                                                   S22024
         EJECT
         TLGBD                                                   S22024
         EJECT
         TPRUD                                                 @Y17XARY
         EJECT
TIMEDATE DSECT                                                   S22024
TDMONTH  DS    CL2                      MONTH                    S22024
TDSLASH1 DS    CL1                      /                        S22024
TDDAY    DS    CL2                      DAY                      S22024
TDSLASH2 DS    CL1                      /                        S22024
TDYEAR   DS    CL2                      YEAR                     S22024
TDDOT1   DS    CL1                      .                        S22024
TDJULIAN DS    CL3                      JULIAN DAY               S22024
TDHOUR   DS    CL2                      HOUR                     S22024
TDDOT2   DS    CL1                      .                        S22024
TDMINUTE DS    CL2                      MINUTE                   S22024
TDDOT3   DS    CL1                      .                        S22024
TDSECOND DS    CL2                      SECOND                   S22024
*                                                                S22024
TDTIME   DS    F                        SAVE AREA FOR TIME       S22024
TDDOUBL  DS    D                        DOUBLE WORD FOR CVB,CVD  S22024
TDSAVEY  DS    F                        SAVE AREA FOR YEAR       S22024
TDSAVEJ  DS    F                        SAVE AREA FOR JULIAN DAY S22024
         DS    0D                                                S22024
UCACOREQ DS    CL12                     QCB ADDRESS              S22024
BTUOFF   DS    CL8                      BTU OFFSET               S22024
BTUSRC   DS    H                        BTU SOURCE               S22024
BTUDID   DS    H                        RSID                     S22024
BTURIDTG DS    H                        SEQUENCE NUMBER          S22024
BTUSLRSP DS    CL2                      SYS/LINE RESPONSE        S22024
BTUCMD   DS    C                        BTU COMMAND              S22024
BTUMODIF DS    C                        BTU COMMAND MODIFIER     S22024
BTUFNFLG DS    X                        FUNCTION FLAGS           S22024
BTUBDUFG DS    X                        BDU FLAGS                S22024
BTUCOUNT DS    H                        TEXT COUNT               S22024
BTUTXT   DS    X                        TEXT FIELD               S22024
         EJECT
         TAVTD
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
         EJECT
         TTNTD
         EJECT
         TTRMD
         END
