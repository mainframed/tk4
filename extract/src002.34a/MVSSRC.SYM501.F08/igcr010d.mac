         TITLE 'IGCR010D - OPERATOR CONTROL RELEASE TERMINAL MODULE'
IGCR010D CSECT
*A000000-999999                                                @Y16X5RR
         SPACE 3
* CHANGE ACTIVITY=AS FOLLOWS:
******************* MICROFICHE FLAGS ************************ SPT CODE
*A081200-083800,262000,417600-425400,876007-878933               Y05330
*A000000-999999                                                  S99240
*A781401                                                        SA61821
*A380000,006000                                                 SA58991
*C007000-008200,015000,159000,168000,549000,580000              SA58991
*A 327000                                                       OX02177
*C 636000                                                       OX02177
*A380600-380700,581060-581420                                    Y06330
*C877197,877204,877232,877239                                   SA66358
*C576000                                                        SA66617
*D360000                                                        SA66617
*A570000                                                        OX03970
*D576000-581000                                                 OX03970
*A591000                                                       @SA70887
*A159000,177000,276000,285000,288000,291000,297000,438000      @Y17XARR
*A447000,477000,504000,513000,571800,852000,861000,921000      @Y17XARR
*A006000                                                       @Y17XARR
*C012000,017000,021000,078000,096000,183000,534000,558000      @Y71XARR
*C570800,573000,581120,581160,581240,602000,780300,780200      @Y17XARR
*C781500,787000,846000                                         @Y17XARR
*D417600-425400,876637,878870,878884,878891,878926,878933      @Y17XARR
*D903000,909000                                                @Y17XARR
*A508300                                                       @OY19901
*A526320,527120,289000                                         @OZ27690
***********************************************************************
*                                                                     *
* MODULE-NAME=IGCR010D(TCAM, OPERATOR CONTROL)                @Y17XARR*
*                                                                     *
* DESCRIPTIVE-NAME=OPERATOR CONTROL RELEASE TERMINAL MODULE           *
*                                                                     *
* COPYRIGHT='NONE'                                                    *
*                                                                     *
* STATUS= VERSION 10.0                                        @Y17XARR*
*                                                                     *
*    FUNCTION -- THE PURPOSE OF THIS ROUTINE IS TO PROCESS OPERATOR   *
*    CONTROL COMMANDS WHICH REQUEST THAT A SPECIFIED TERMINAL @Y17XARR*
*    OR LU OR LUS UNDER A PU OR PUS UNDER A SDLC LINE BE      @Y17XARR*
*    RELEASED FROM ITS INTERCEPTED STATUS.  THE TERMNAME TABLE IS     *
*    ACCESSED AND IS SERIALLY SEARCHED FOR AN ENTRY MATCHING THAT IN  *
*    THE COMMAND.  IF THE END OF THE TABLE IS REACHED BEFORE A MATCH  *
*    IS FOUND, AN ERROR MESSAGE NUMBER IS SET UP AND CONTROL IS       *
*    TRANSFERRED TO THE ERROR MODULE.  IF THE REQUEST IS FOR A GENERAL*
*    POLL ENTRY, AN ERROR MESSAGE IS SET UP AND CONTROL IS            *
*    TRANSFERRED TO THE ERROR MODULE.  WHEN THE ENTRY IS FOUND, ITS   *
*    ADDRESS IS OBTAINED.  THE INTERCEPT BIT IN THE TERMINAL ENTRY IS *
*    TESTED.  IF IT IS OFF, AN ERROR MESSAGE NUMBER IS SET UP AND     *
*    CONTROL IS TRANSFERRED TO THE ERROR MODULE.  OTHERWISE, A BUFFER *
*    REQUEST IS MADE BY BRANCHING TO A SUBROUTINE OF CA.  THE TERMINAL*
*    OFFSET OF THE TERMINAL TO BE RELEASED IS PLACED IN THE NEW BUFFER*
*    AND IT IS QUEUED TO IEDQAS01 (THE RELEASE MODULE).  AQCTL (SVC   *
*    102) IS ISSUED TO GIVE THE NEW BUFFER TO RELEASE.  THE FIELD     *
*    OCWTG IS CHECKED TO SEE IF THIS IS A RELEASE COMMAND ONLY OR AN  *
*    ENTRY FOR VARY TERMINAL.  IF THIS FIELD IS NOT BLANK, CONTROL    *
*    IS PASSED BACK TO START TERMINAL.  OTHERWISE, A MESSAGE RESPONSE *
*    NUMBER IS SET UP AND CONTROL IS TRANSFERRED TO THE VARY, HOLD,   *
*    RELEASE MESSAGE MODULE.                                          *
*    FOR THE CASCADE FUNCTION (GROUPNAME OR LINENAME OR PU    @Y17XARR*
*   SPECIFIED AS THE INPUT) ---- IF GRPNAME,RLN IS THE INPUT  @Y17XARR*
*    THEN THE INPUT IS VALIDATED BY CALLING DCBLOCAT.   IF THE@Y17XARR*
*   INPUT REPRESENT A SDLC LINE, THEN EVERY RESOURCE UNDER    @Y17XARR*
*   THAT LINE IS RELEASED; OTHERWISE, ANY OTHER LINE IS AN    @Y17XARR*
*    ERROR .   IF SDLC LINENAME, PUNAME IS THE INPUT THEN THE @Y17XARR*
*    INPUT IS VALIDATED BY CALLING TERMFLOC.   IF THE INPUT   @Y17XARR*
*    IS VALID, THEN EVERY RESOURCE UNDER THAT SPECIFIC        @Y17XARR*
*    RESOURCE IS RELEASED.                                    @Y17XARR*
*                                                                     *
* ENTRY POINTS --                                                     *
*    IGCR010D TO PROCESS RELEASE COMMANDS.                            *
*                                                                     *
* INPUT --                                                            *
*    REGISTER 1 - ADDRESS OF OPERATOR CONTROL AVT.            @Y17XARR*
*                 OPCCOPCE CONTAINS THE ADDRESS OF THE        @Y17XARR*
*                 OPERATOR CONTROL ELEMENT.   OCBACKUP        @Y17XARR*
*                 CONTAINS GROUPNAME, DDNAME,LINENAME,LUNAME  @Y17XARR*
*                 PUNAME,BSC/SS TERMINAL.   OCWTG IS CHECKED  @Y17XARR*
*                 TO SEE IF THIS IS A RELEASE COMMAND ONLY    @Y17XARR*
*                 OR FROM VARY.     IF OCWTG IS NOT ZEROS     @Y17XARR*
*                 THEN CONTROL IS PASSED BACK TO MOD ID IN    @Y17XARR*
*                  OCWTG.                                     @Y17XARR*
*                                                                     *
* OUTPUT --                                                           *
*    REGISTER 0 - NUMBER OF APPROPRIATE ERROR MESSAGE.
*    REGISTER 1 - RETURN CODE VALUES.                                 *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         OPCDCBLK,OPCTOFLK,OPCGETBF                          @Y17XARR*
*                                                                     *
* EXITS,NORMAL --                                                     *
*         RELEASE DONE.  RESPONSE MESSAGE NUMBER SET UP, X'00' RETURN *
*    CODE SET.                                                        *
*         TERMINAL ALREADY RELEASED.  RESPONSE MESSAGE NUMBER SET UP, *
*         X'04' RETURN CODE SET                                 SA58991
*         ALL RELEASED -- MSG 522--RETURN CODE -- 00          @Y17XARR*
*          SOME RELEASED--MSG 521--RETURN CODE-- 28           @Y17XARR*
*                                                                     *
* EXITS,ERROR --                                                      *
*         TERMINAL NOT FOUND, ERROR MESSAGE SET UP,             SA58991
*         X'20' RETURN CODE SET                                 SA58991
*         TERMINAL IS NOT A SINGLE ENTRY OR IT IS A GENERAL POLL ENTRY*
*         ERROR MESSAGE NUMBER IS SET AND X'04' RETURN CODE IS SET    *
*         NONE RELEASED--MSG 523--RETURN CODE-- 24            @Y17XARR*
*         RLN INVALID -- MSG 524--RETURN CODE-- 8             @Y17XARR*
*         NCP INACTIVE -- MSG 400-- RETURN CODE-- 1C          @Y17XARR*
* MACROS = AQCTL,XCTL                                         @Y17XARR*
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    OPERATOR CONTROL AVT, OPERATOR CONTROL ELEMENT, AVT      @Y17XARR*
*    DCB, TERMINAL ENTRY, QCB.                                @Y17XARR*
* PATCH LABEL = ZAPAREA                                       @Y17XARR*
* MODULE TYPE = PROCEDURE                                     @Y17XARR*
* PROCESSOR = ASSEMBLER                                       @Y17XARR*
* MODULE SIZE =                                               @Y17XARR*
* PURPOSE = SEE FUNCTION                                      @Y17XARR*
* LINKAGE = XCTL SF=(E,OPXCTL)                                @Y17XARR*
*                                                                     *
* ATTRIBUTES --                                                       *
*    SERIALLY REUSABLE,REENTERANT,ENABLED,TRANSIENT,                  *
*    SUPERVISOR MODE.                                                 *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS            *
*    EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING         *
*    HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER              *
*    CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE         *
*    FOR THE NEW DEFINITION.                                          *
*                                                                     *
* NOTES --                                                            *
*    COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS          *
*    PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                      *
*                                                                     *
*    THIS IS A MODULE RE-WRITE OF IEDQCQ AS OF OS/TCAM LEVEL II       *
***********************************************************************
         EJECT
         SPACE
*                                 E Q U A T E S
         SPACE
RZERO    EQU   0                        PARAMETER REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
REG2     EQU   2                        PARM REG                 Y05330
ROPCAVT  EQU   2                        POINTER TO OP CONTROL AVT
ROPCE    EQU   3                        CURRENT ELEMENT POINTER
RAVT     EQU   4                        TCAM AVT
RWORKA   EQU   5                        RESIDENT WORKAREA POINTER
RLEN     EQU   6                        LENGTH OF INPUT COMMAND
RDCTINDX EQU   6                        STATION DCT ADDRESS    @Y17XARR
RQCB     EQU   6                        QCB BASE               @Y17XARR
RTERM    EQU   7                        TERM ENTRY POINTER
RWORK    EQU   8                        ROWKREGISTER
RSCANLEN EQU   8                        NUMBER OF BYTES SCANNED@Y17XARR
REND     EQU   9                        POINTER TO END OF INPUT
RSAVER14 EQU   9                        SAVE RETURN ADDRESS    @Y17XARR
RALSTAT  EQU   9                        REGISTER FOR SAVING 'ALL
*                                       OPTION' STATUS FLAG    @OZ27690
RSCAN    EQU   10                       DATA SCAN POINTER
RPREFIX  EQU   11                       POINTER TO UNIT        @Y17XARR
RSCANWRK EQU   11                       SCAN WORK REGISTER     @Y17XARR
RBASE    EQU   12                       MODULE BASE REGISTER
ROPCSAVE EQU   13                       OPCSAVE PTR            @Y17XARR
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RBRNCH   EQU   15                       SUBROUTINE ENTRY REGISTER
         SPACE 2
ALL      EQU   255                      ALL ONE BITS
BADTERM  EQU   16                       ERROR FOR BAD TERMNAME
BYTE     EQU   1                        OFFSET OF ONE
ZERO     EQU   0                        ZERO
X01      EQU   X'01'                    HEX ONE
GENPOLMG EQU   147                      MESSAGE NUMBER
EIGHT    EQU   8                        LONGEST TERMINAL NAME
END      EQU   X'80'                    SET LIST END
FIVE     EQU   5                        LENGTH OF FIVE
FOUR     EQU   4                        LENGTH OF 4
THREE    EQU   3                        LENGTH OF 3             OX02177
GOODREL  EQU   54                       TERM RELEASED
INVALCMD EQU   18                       INVALID INPUT COMMAND
LENOFF   EQU   2                        OFFSET TO LENGTH IN WORKAREA
MSGOFF   EQU   8                        OFFSET TO INPUT DATA
NAMELEN  EQU   2                        LENGTH OF NAME CHANGE
NEXT     EQU   1                        OFFSET OF ONE
ONE      EQU   1                        LENGTH OF ONE
POST     EQU   X'0C'                    POST TO AQCTL
RELEASED EQU   53                       TERM ALREADY RELEASED
SEVEN    EQU   7                        LENGTH FOR MOVE
SIX      EQU   6                        OFFSET OF SIX
TPLEN    EQU   3                        LENGTH OF TP= FOR INPUT
TWO      EQU   2                        LENGTH OF 2
VARINAME EQU   3                        OFFSET TO CHANGE NEXT MODULE ID
ZONE     EQU   X'F0'                    COVER ISGN AFTER UNPACK
X40      EQU   X'40'                    INDICATE ENTRY FROM START TERM
ALRSTRT  EQU   19                       ALREADY STARTED MESSAGE NO
STARTED  EQU   20                       STARTED MESSAGE NO
THRTYTWO EQU   32                       INVALID TERMNAME        SA58991
NOHOLD   EQU   60                       FLAG                     S22024
OLTRES   EQU   49                       OLT CONTROLS RESOURCE    Y06330
*                                         ERROR MESSAGE NUMBER   Y06330
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQPRF,RPREFIX          UNIT ADDRESSABILITY
         USING IEDNTRM,RTERM            TERM ENTRY ADDRESS     @Y17XARR
         SPACE
         BALR  RBASE,RZERO              SET MODULE BASE
         USING *,RBASE                  SET MODULE ADDRESSABILITY
IGCR010D IEDHJN  SKIPID
*              INITIAL SETUP                                          *
         LR    ROPCAVT,RPARM             GET PASSED AVT ADDRESS
         L     ROPCE,OPCCOPCE           GET CURRENT ELEMENT ADDRESS
         L     RAVT,OPCAVTPT            GET TCAM AVT ADDRESS
         L     RWORKA,OPCWORK           GET WORKAREA ADDRESS
         LA    ROPCSAVE,OPCSAVE         GET SAVEAREA ADDRESS   @Y17XARR
         MVC   OPCDOUBL(EIGHT),OCBACKUP YES, MOVE TERM NAME FOR VERIFY
         MVC   OPCLDNME+VARINAME(NAMELEN),XCTL03 SET ERROR MODULE NAME
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS
         BZ    NORMINPT                 NORMAL PROCESSING OF   @Y17XARR
*                                       A RELEASE COMMAND      @Y17XARR
         TM    OCALSTAT,OCALLFLG        RESTART A CASCADE REQ? @Y17XARR
         BZ    NORMIN                   CONTINUE ^CASCADE RST  @Y17XARR
         SR    RPARM,RPARM              CLEAR TO PICKUP T-INDX @Y17XARR
         ICM   RPARM,HALFWORD,OCTRMTBL  GET HGHST RES TNTINDEX @Y17XARR
         LA    RRET,RESTART             GET RETURN ADDRESS     @Y17XARR
         B     GETTTE                   GET STATION ADDRESS    @Y17XARR
NORMINPT EQU   *                                               @Y17XARR
         SPACE
         TM    OCFLAG,OCMPP+OCTOTE      TOTE OR MPP INPUT?
         BM    NORMIN                   YES, VERIFY INPUT
         SPACE
         CLI   OCOPTCDE,OCVARY          REQUEST FROM ACTIVATE TERM
         BE    NORMIN                   YES
         SPACE
         LA    RSCAN,MSGOFF(RWORKA)     SET BEGINNING SCAN POINTER
         LH    RLEN,LENOFF(RWORKA)      PICK UP INPUT LENGTH
         LA    REND,DATAINDX(RWORKA,RLEN) GET ADDRESS OF END OF@Y17XARR
*                                       DATA IN THE WORKAREA   @Y17XARR
         MVI   ZERO(REND),AVTEBLNK      INSERT A BLANK TO      @Y17XARR
*                                       INDICATOR END OF DATA  @Y17XARR
*                                       IN WORKAREA            @Y17XARR
         MVI   OPCDOUBL,AVTEBLNK        SET FIRST BLANK
         MVC   OPCDOUBL+NEXT(SEVEN),OPCDOUBL BLANK ALL WORKAREA
         LA    RZERO,INVALCMD           SET INVALID COMMAND ERROR
         CLC   AVTEZERO(TPLEN,RSCAN),TPID TP= FOR FIRST THREE CHARACTER
         BNE   XCTL                     NOT TP=, INVALID INPUT
         SPACE
         LA    RSCAN,TPLEN(RSCAN)       POINT BEYOND EQUALS
         LR    RSCANWRK,RSCAN           GET WORK REGISTERS FOR @Y17XARR
         LR    RSCANLEN,RSCAN           SCAN PTR,LENGTH TO BE  @Y17XARR
*                                       SCANNED AND KEEP ONE   @Y17XARR
*                                       REG TO POINT TO BEGIN  @Y17XARR
*                                       OF THE DATA            @Y17XARR
         SH    RLEN,LENTP               TAKE THREE FROM LENGTH
         BZ    XCTL                     NO MORE INPUT, ERROR
         SPACE
SCANINPT EQU   *                                               @Y17XARR
         CLI   ZERO(RSCANWRK),COMMA     DELIMITER FOUND?       @Y17XARR
         BE    GETLEN                   DETERMINE NO FO BYTES  @Y17XARR
*                                       SCANNED                @Y17XARR
         CLI   ZERO(RSCANWRK),AVTEBLNK  DELIMITER FOUND?       @Y17XARR
         BE    GETLEN                   DETERMINE NO OF BYTES  @Y17XARR
*                                       SCANNED                @Y17XARR
         LA    RSCANWRK,INDEX(,RSCANWRK) BUMP INPUT SCAN PTR   @Y17XARR
         LR    RSCANLEN,RSCANWRK        BUMP LEN SCAN PTR      @Y17XARR
         B     SCANINPT                 TEST NEXT BYTE OF      @Y17XARR
*                                       DATA                   @Y17XARR
GETLEN   EQU   *                                               @Y17XARR
         SR    RSCANLEN,RSCAN           GET TOTAL LEN SCANNED  @Y17XARR
         BZ    XCTL                     ZERO BYTES FOUND       @OY19901
         CH    RSCANLEN,NAME            IS LEN SCANNED >LEN    @Y17XARR
*                                       OF VALID NAME          @Y17XARR
         BH    XCTL                      YES, INVALID COMMAND
         SPACE
         BCTR  RSCANLEN,RZERO           DECREMENT FOR MOVE     @Y17XARR
         EX    RSCANLEN,MOVENAME        MOVE NAME TO VERIFY    @Y17XARR
         MVC   OCBACKUP,OPCDOUBL        SAVE THE NAME          @Y17XARR
         LA    RSCANLEN,INDEX(,RSCANLEN) ADJUST COUNTER TO ORIG@Y17XARR
         SR    RLEN,RSCANLEN            SUBSTRACT LEN SCANNED  @Y17XARR
         BZ    NORMINA                  TERMNAME TO VALIDATE   @YM07016
         LA    RSCANWRK,INDEX(RSCANLEN,RSCAN) PT PASSED COMMA  @Y17XARR
         LR    RSCAN,RSCANWRK           ADJUST POINTERS TO     @Y17XARR
         LR    RSCANLEN,RSCAN           POINT TO DATA LEFT     @Y17XARR
*                                       TO SCANNED             @Y17XARR
         CR    REND,RSCANWRK            REACHED END OF DATA?   @Y17XARR
         BE    XCTL                     REACHED END OF DATA    @Y17XARR
*                                       BEFORE FINDING VALID   @Y17XARR
*                                       INPUT                  @Y17XARR
CHKRLN   EQU   *                                               @Y17XARR
         SPACE
         CR    REND,RSCANWRK            SCANNED TO THE END?    @Y17XARR
         BE    CONVERT                  YES, TRY TO CONVERT    @Y17XARR
         CLI   ZERO(RSCANWRK),LOWEST    IS RLN < ZERO?         @Y17XARR
         BL    XCTL                     YES,INVALID RLN        @Y17XARR
         CLI   ZERO(RSCANWRK),HIGHEST   IS RLN > NINE?         @Y17XARR
         BH    XCTL                     YES,INVALID RLN        @Y17XARR
         LA    RSCANWRK,INDEX(,RSCANWRK) BUMP THE SCAN PTR BY 1@Y17XARR
         LR    RSCANLEN,RSCANWRK        BUMP LENGTH PTR        @Y17XARR
         B     CHKRLN                   CONTINUE TO SCAN TO    @Y17XARR
*                                       A MATCH IS FOUND       @Y17XARR
CONVERT  EQU   *                                               @Y17XARR
         SR    RSCANLEN,RSCAN           GET TOTAL LEN SCANNED  @Y17XARR
         CH    RSCANLEN,RLNLEN          IS SCANNED LEN > THREE @Y17XARR
         BH    XCTL                     YES, INVALID RLN       @Y17XARR
         XC    OPCDOUBL,OPCDOUBL        CLEAR TO MOVE RLN      @Y17XARR
         BCTR  RSCANLEN,RZERO           DECREMENT LEN FOR MOVE @Y17XARR
         EX    RSCANLEN,MOVERLN         MOVE RLN AND PACK RLN  @Y17XARR
         EX    RSCANLEN,PACKRLN         TO CONVERT TO A MORE   @Y17XARR
*                                       WORKABLE FORM          @Y17XARR
         XC    OPCDOUBL(UNUSED),OPCDOUBL CLEAR FOR CVB         @Y17XARR
         CVB   RWORK,OPCDOUBL           CONVERT TO BINARY NUM  @Y17XARR
         LTR   RWORK,RWORK              IS RLN EQUAL TO ZERO?  @Y17XARR
         BZ    INVALRLN                 YES, INVALID RLN SPEC  @Y17XARR
         CH    RWORK,MAXRLN                 RLN>255?           @Y17XARR
         BNH   VALIDATE                 NO,VALIDATE GROUPNAME  @Y17XARR
INVALRLN EQU   *                                               @Y17XARR
         LA    RPARM,NVALDRLN            SETUP RETURN CODE     @Y17XARR
*                                       FOR TOTE OR MPP        @Y17XARR
         LA    RZERO,RLNERR             SETUP MSG APPROPRIATE  @Y17XARR
*                                       FOR INVALID RLN        @Y17XARR
         B     ALREADY                  EXIT ACCORDING TO      @Y17XARR
*                                       THE REQUESTOR          @Y17XARR
VALIDATE EQU   *                                               @Y17XARR
         IC    RALSTAT,OCALSTAT         SAVE 'ALL OPTION' STATUS
*                                       FLAG                   @OZ27690
         LR    RPARM,RWORK              SETUP INTERFACE TO     @Y17XARR
         MVC   OPCDOUBL,OCBACKUP        DCBLOCAT REG1=RLN AND  @Y17XARR
*                                       OPCDOUBL CONTAINS GRP  @Y17XARR
         L     RBRNCH,OPCDCBLK          GET ADDRESS OF DCBLOCAT@Y17XARR
         BALR  RRET,RBRNCH              CALL DCBLOCAT          @Y17XARR
         B     VALIDGRP                 GOOD RETURN,BRANCH     @Y17XARR
         CH    RZERO,INACTNCP           NCP ACTIVE?            @Y17XARR
         BE    INACTIVE                 PUT OUT MSG            @Y17XARR
         CH    RZERO,RLNRETCD             RLN INVALID?         @Y17XARR
         BNE   BADNAME                  NO, GRPNAME INVALID    @Y17XARR
         LA    RPARM,NVALDRLN           SETUP INVALID RLN MSG  @Y17XARR
         LA    RZERO,RLNERR             SETUP RLN ^ IN GRP     @Y17XARR
         B     ALREADY                  EXIT ACCORDING TO REQ  @Y17XARR
BADNAME  EQU   *                                               @Y17XARR
         LA    RZERO,BADGRP             NON-EXISTING NAME      @Y17XARR
         LA    RPARM,BADGRPRC           RETURN CODE INVALID    @Y17XARR
*                                       NAME                   @Y17XARR
         B     ALREADY                  EXIT ACCORDINGLY       @Y17XARR
VALIDGRP EQU   *                                               @Y17XARR
         USING IHADCB,RPARM             EST. ADDRESSABILITY    @Y17XARR
         STC   RALSTAT,OCALSTAT         RESTORE 'ALL OPTION'FLG@OZ27690
         TM    DCBDSORG,DSLGB           LGB?                   @Y17XARR
         BO    NON270X                  VALID MODE             @Y17XARR
         DROP  RPARM                    ADDRESSABILTY FOR TEST @Y17XARR
         LA    RZERO,INVALID            270X LINE INVALID      @Y17XARR
         LA    RPARM,BADREQST           RETURN CODE FOR TOTE   @Y17XARR
*                                       OR MPP                 @Y17XARR
         B     ALREADY                  GIVE CONTROL TO REQ    @Y17XARR
NON270X  EQU   *                                               @Y17XARR
         LR    RPARM,RZERO              USE LINE TTE TO CON'T  @Y17XARR
         B     RELEASE1                 PERFORM FUNCTION       @Y17XARR
NORMINA  EQU   *                                               @YM07016
         MVI   OCRLN,X'00'              SET NO RLN             @YM07016
NORMIN   EQU   *
         L     RBRNCH,OPCTOFLK          GET LOOK UP ADDRESS
         BALR  RRET,RBRNCH              CHECK TERMINAL NAME
         SPACE
         B     RELEASE1                 GOOD, RELEASE TERMINAL @Y17XARR
         SPACE
         LA    RZERO,BADTERM            SET BAD TERM ERROR
         LA    RPARM,THRTYTWO           SET MPP RETURN CODE     SA58991
         B     ALREADY                  PUT OUT ERROR
         SPACE
*              PERFORM RELEASE FUNCTION                               *
RELEASE1 EQU   *                                               @Y17XARR
         LR    RTERM,RPARM              SAVE TERM ENTRY ADDRESS
         LA    RWORK,TRMPRFSZ            GET PRFEFIX LENGTH    @Y17XARR
         SR    RTERM,RWORK              BACKUP TO THE PREFIX   @Y17XARR
RESTART  EQU   *                                               @Y17XARR
         LA    RPARM,FOUR               SET MPP RETURN CODE
         LA    RZERO,NOHOLD             SET CANNOT BE HELD      OX03970
         TM    TRMDSORG,TRMLGB          IS THIS AN LGB          OX03970
         BO    ALREADY                  YES, PRINT ERROR MSG    OX03970
         TM    TRMSTATE,TRMLIST+TRMPROC LIST OR PROCESS ENTRY   OX03970
         BZ    FINDTERM                 NO, SINGLE ENTRY       @Y17XARR
         LA    RZERO,RELEASED           SET ALREADY RELEASED    OX03970
         B     ALREADY                  WRITE OUT ERROR MSG     OX03970
FINDTERM EQU   *                                               @Y17XARR
         TM    TRMSTATE,TRMPREF         3705 RESOURCE?         @Y17XARR
         BZ    SINGL                    NO, CONTINUE RELEASE   @Y17XARR
         TM    TRMSTATE,TRMLINE         LINE?                  @Y17XARR
         BO    CASCADE                  PERFORM RELEASE        @Y17XARR
         CLI   TRMTYPE,TRMPUNT          PU?                    @Y17XARR
         BNE   CHKNCP                   VERIFY INPUT           @Y17XARR
CASCADE  EQU   *                                               @Y17XARR
         STCM  RTERM,FULLWORD,OPCTRMWA  SAVE HGHST RES TTE ADDR@Y17XARR
         MVC   OCSEQ,OCTRMTBL           SAVE IT'S TNT INDEX    @Y17XARR
         NI    OCALSTAT,AVTEFF-OCALL    INDICATE ORIG STATUS   @Y17XARR
         OI    OCALSTAT,OCALLFLG        INDICATE CASCADE FUNCT @Y17XARR
         CLI   TRMTYPE,TRMPUNT          PU?                    @Y17XARR
         BE    SUBSTRUC                 CONTINUE,CASCADE FUNC  @Y17XARR
         CLI   TRMTYPE,TRMSDLC          SDLC LINE?             @Y17XARR
         BNE   NEXTCHK                  INVALID TYPEBSC/SS     @Y17XARR
GETSUBST EQU   *                                               @Y17XARR
         SR    RPARM,RPARM              CLEAR TO GET SUBENTRY  @Y17XARR
         ICM   RPARM,HALFWORD,TRMSUBST  GET SUBENTRY TNTINDEX  @Y17XARR
         BZ    INACTIVE                 INACTIVE 3705          @Y17XARR
         BAL   RRET,GETTTE              GET STATION ADDRESS    @Y17XARR
SUBENTRY EQU   *                                               @Y17XARR
         STCM  RTERM,FULLWORD,OCMODNME+FRSTWORD  SAVE PU @     @Y17XARR
         SR    RPARM,RPARM              CLEAR TO GET SUBENTRY  @Y17XARR
         ICM   RPARM,HALFWORD,TRMSUBST  SUBSTRUCTURE EXIST?    @Y17XARR
         BZ    ADJSUBST                 TRY NEXT PU'S SUBENTRY @Y17XARR
         STCM  RPARM,HALFWORD,OCTRMTBL  INTERFACE FOR IEDQAS   @Y17XARR
         LA    RRET,SINGL               SETUP RETURN ADDRESS   @Y17XARR
         B     GETTTE                   GET STATION ADDRESS    @Y17XARR
SUBSTRUC EQU   *                                               @Y17XARR
         SR    RPARM,RPARM              CLEAR TO GET SUBENTRY  @Y17XARR
         ICM   RPARM,HALFWORD,TRMSUBST  SUBSTRUCTURE EXIST?    @Y17XARR
         BZ    INACTIVE                 INACTIVE 3705          @Y17XARR
         STCM  RPARM,HALFWORD,OCTRMTBL  INTERFACE FOR IEDQAS   @Y17XARR
         LA    RRET,SINGL               SETUP RETURN ADDRESS   @Y17XARR
         B     GETTTE                   GET STATION ADDRESS    @Y17XARR
INACTIVE EQU   *                                               @Y17XARR
         LA    RZERO,NOSUBSTR           3705 INACTIVE MSG NUM  @Y17XARR
         LA    RPARM,INACTRCD           TOTE OR MPP INACT RC   @Y17XARR
         MVC   OPCMODID,XCTL03          MSG MOD ID             @Y17XARR
         B     NEXTCHK                  GIVE CONTROL TO MSG MOD@Y17XARR
CHKNCP   EQU   *                                               @Y17XARR
         CLI   TRMTYPE,TRMLNCP          NCP?                   @Y17XARR
         BE    ALREADY                  INVALID ENTRY          @Y17XARR
         CLI   TRMTYPE,TRMSSCP          SSCP?                  @Y17XARR
         BE    ALREADY                  INVALID ENTRY          @Y17XARR
SINGL    EQU   *                                                OX03970
         LA    RZERO,RELEASED           SET MESSAGE NUMBER
         LA    RPARM,ASUMEBAD           ASSUME AN ERROR        @Y17XARR
         SR    RDCTINDX,RDCTINDX        CLEAR FOR DCT INDEX    @Y17XARR
         IC    RDCTINDX,TRMCHCIN        GET DCT INDEX          @Y17XARR
         BCTR  RDCTINDX,RZERO           GET CORRECT INDEX      @Y17XARR
         MH    RDCTINDX,AVTDCTLN        GET ACTUAL INDEX TO ADD@Y17XARR
*                                       TO DCT ADDRESS         @Y17XARR
         A     RDCTINDX,AVTCSTCS        PT TO STATION DEVICE   @Y17XARR
*                                       CHARACTERISTICS        @Y17XARR
         USING IEDDCT,RDCTINDX          EST. ADDRESSABILITY    @Y17XARR
         TM    DCTBYTE1,DCTUMASK        IS THIS GENERAL POLL   @Y17XARR
         BZ    ALRDYCHK                 NO, BRANCH             @Y17XARR
         DROP  RDCTINDX                 DROP ADDRESS.          @Y17XARR
         LA    RZERO,GENPOLMG           SET INVALID COMMAND MSG
         MVC   OPCLDNME+VARINAME(NAMELEN),WRTMSG SET ERROR MOD
         B     NEXTCHK                  GO SEND OUT MESSAGE
ALRDYCHK EQU   *
         SPACE  1
         TM    OCFLAG,OCTOTE            TOTE REQUEST?            Y06330
         BO    CONTINUE                 YES, CONTINUE PROCESSING Y06330
         TM    TRMSTATE,TRMPREF         IS TERMINAL ON 3705    @Y17XARR
         BNO   CONTINUE                 NO, CONTINUE            OY01948
         TM    TRMBYTE1,TRMSTOTE                OLT OWNS?      @Y17XARR
         BNO   CONTINUE                 NO, CONTINUE PROCESSING  Y06330
         TM    OCALSTAT,OCALLFLG        CASCADE RELEASE FUNC?  @Y17XARR
         BO    ALREADY                  YES,GETNEXT SUBSTRUCT  @Y17XARR
         LA    RZERO,OLTRES             GET ERROR MESSAGE NUMBER Y06330
         B     ALREADY                  PUT OUT ERROR            Y06330
CONTINUE EQU   *                                                 Y06330
         SPACE 1                                                 S22024
         SPACE
         TM    TRMSTATE,TRMHELDN        TERMINAL INTERCEPTED?
         BNO   ALREADY                  NO, ALREADY RELEASED
         SPACE
         CLC   AVTAS,AVTFZERO           IS RELEASE IN SYSTEM   @SA70887
         BNE  CHKRESRT                  CHECK FOR RESTART      @SA70887
         LA    RZERO,NOHOLD             SET MESSAGE            @SA70887
         B     ALREADY                                         @SA70887
         SPACE 2                                               @SA70887
CHKRESRT EQU   *                                               @SA70887
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS?
         BNO   LETGO                    NO, RELEASE NORMALLY
         SPACE
         NI    TRMSTATE,AVTEFF-TRMHELDN TURN OFF INTER FLAG    @Y17XARR
         USING IEDQQCB,RQCB             EST. ADDRESSABILITY    @Y17XARR
         L     RQCB,TRMDESTQ-BYTE       GET QCB ADDRESS
         TM    QCBFLAG,QCBTERMQ         QUEUING BY TERMINAL?
         BZ    ALREADY                  NO, ALREADY RELEASED
         SPACE
         NI    QCBSTAT,AVTEFF-QCBTRMHO  TURN OFF QCB HOLD BIT  @Y17XARR
         B     ALREADY                  ALREADY RELEASED
         SPACE
LETGO    EQU   *
         LH    RWORK,TRMOUTSQ           PICK UP OUTPUT SEQUENCE NUMBER
         CVD   RWORK,OPCDOUBL           CONVERT TO PACKED DECIMAL
         UNPK  OPCDOUBL(FIVE),OPCDOUBL+FIVE(THREE) UNPACK NO.   OX02177
         OI    OPCDOUBL+FOUR,ZONE       MAKE SIGN PRINTABLE
         MVC   OCMODNME(FOUR),OPCDOUBL+NEXT PLACE SEQUENCE IN ELEMENT
         LA    RPARM,ONE                SET UNIT REQUEST
         L     RBRNCH,OPCGETBF          GET ALLOCATION ROUTINE ADDRESS
         BALR  RRET,RBRNCH              GET UNIT FOR POST
         SPACE
         LR    RPREFIX,RPARM            SAVE UNIT ADDRESS
         MVC   PRFDEST(TWO),OCTRMTBL    SET DESTINATION OFFSET
         MVI   PRFNBUNT,ONE             SET NO. OF BUFFERS TO ONE
         XC    PRFPRI(FOUR),PRFPRI      CLEAR UNIT LINK FIELD
         MVI   PRFPRI,PRIRELSE          SET RELEASE PRIORITY
         L     RWORK,AVTAS              GET RELEASE QCB ADDRESS
         ST    RWORK,PRFQCBA-BYTE       PLACE QCB ADDRESS IN UNIT
         ST    RPREFIX,OPCAQCTL         PLACE UNIT ADDRESS FOR POST
         ST    RPREFIX,OPCAQCTL+FOUR    PLACE UNIT ADDRESS FOR POST
         MVI   OPCAQCTL,POST            SET POST INDICATION
         MVI   OPCAQCTL+FOUR,END        SET END
         LA    RPARM,OPCAQCTL           POINT TO AQCTL LIST
         SPACE
         AQCTL                          POST UNIT TO IEDQAS FOR RELEASE
         SPACE 2
*              CHECK ALL EXIT SITUATIONS                              *
         SR    RPARM,RPARM              CLEAR TEST REGISTER
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS
         BZ    ALREADY                  NO
         MVC   OPCLDNME+VARINAME(NAMELEN),CHECKPT SET UP FOR CHECKPOINT
         B     ALREADY                  GO TO CHECKPOINT
         SPACE
NORESTRT EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),OCWTG  SET RETURN NAME
         OC    OCWTG(NAMELEN),OCWTG     IS RETURN REQUESTED
         MVC   OCWTG(NAMELEN),WRTMSG    SET FOR START TERMINAL
         BNZ   NEXTCHK                  YES
GOODRLSE EQU   *
         SPACE
         MVI   OCPARM,AVTEZERO          CLEAR HIGH BYTE
         MVI   OCPARM+BYTE,GOODREL      SET MESSAGE NUMBER
         MVC   OCWTG(NAMELEN),WRTMSG    SET FOR MESSAGE MODULE
         MVC   OPCLDNME+VARINAME(NAMELEN),CHECKPT SET FOR CHECKPOINT
         B     NEXTCHK                  GO MAKE FURTHER CHECKS
         SPACE
ALREADY  EQU   *
         CLI   OCOPTCDE,OCVARY          VARY COMMAND
         BNE   CASCADNG                 RELEASE COMMAND        @Y17XARR
         LTR   RPARM,RPARM              ERROR
         BZ    RELESED                  NO
POSSERR  EQU   *                                               @Y17XARR
         LH    RWORK,OCPARM             GET MESSAGE NUMBER
         LA    REND,STARTED             TO CHECK FOR GOOD MESSAGE
         CR    RWORK,REND               CHECK FOR GOOD MSG NO
         BE    NORESTRT                 GOOD MSG NO
         SPACE
         LR    RZERO,RWORK              SET UP VARY ERROR MESSAGE
         MVC   OPCLDNME+VARINAME(NAMELEN),XCTL03 SET TO GO TO ERROR MOD
         XC    OCWTG(NAMELEN),OCWTG     CLEAR NAME FIELD        SA61821
         B     NEXTCHK                  EXIT TO MODULE
         SPACE
CASCADNG EQU   *                                               @Y17XARR
         TM    OCALSTAT,OCALLFLG        CASCADE FUNCTION REQ   @Y17XARR
         BO    CHKRETCD                 CHECK STAUS            @Y17XARR
         LTR   RPARM,RPARM              ERROR?                 @Y17XARR
         BNZ   NEXTCHK                  TO GET OUT             @Y17XARR
RELESED  EQU   *
         CLI   OCOPTCDE,OCVARY          REQUEST FROM ACTIVATE TERMINAL
         BNE   GOODRLSE                 NO
         SPACE
         TM    OCFLAG,OCTOTE            TOTE INPUT
         BO    TOTEINPT                 YES
         LH    RWORK,OCPARM             MSG NO
         LA    REND,STARTED             SET UP STARTED MESSAGE
COMPARE  EQU   *
         CR    RWORK,REND               ALREADY STARTED
         BNE   GOODRLSE                 NO
         TM    OCFLAG,OCTOTE            TOTE INPUT
         BZ    NOTTOTE                  NO
         SPACE
         MVI   OCPARM+ONE,AVTEZERO      SET GOOD RETURN CODE
NOTTOTE  EQU   *
         B     NORESTRT                 BRANCH TO NORESTRT
         SPACE
TOTEINPT EQU   *
         LA    REND,AVTEZERO            SET TO COMPARE FOR SUCCESSFUL
         B     COMPARE                  CHECK FOR ALREADY DONE RET CODE
         SPACE
CHKRETCD EQU   *                                               @Y17XARR
         LTR   RPARM,RPARM              ERROR?                 @Y17XARR
         BZ    CHKSTAT                  NO ERROR CONDITION     @Y17XARR
         TM    OCALSTAT,OCALL           ORIG STATUS?           @Y17XARR
         BNZ   ERRSTAT                  FIND ERROR STATUS      @Y17XARR
         OI    OCALSTAT,OCNONE          SET NONE WERE RELEASED @Y17XARR
         B     ADJSUBST                 GET NEXT SUBENTRY      @Y17XARR
ERRSTAT  EQU   *                                               @Y17XARR
         TM    OCALSTAT,OCALL           ALL RELEASED?          @Y17XARR
         BNO   ADJSUBST                 NO,SOME RELEASED       @Y17XARR
         NI    OCALSTAT,AVTEFF-OCALL    CHANGE TO NEW STATUS   @Y17XARR
         OI    OCALSTAT,OCSOME          SET SOME RELEASE       @Y17XARR
         B     ADJSUBST                 GET ADJACENT ENTRY     @Y17XARR
CHKSTAT  EQU   *                                               @Y17XARR
         TM    OCALSTAT,OCALL           ORIG STATUS IN AFFECT? @Y17XARR
         BNZ   CHKSTAT1                 NO CHECK OTHER STATUS  @Y17XARR
         OI    OCALSTAT,OCALL           TO ALL RELEASED        @Y17XARR
         B     ADJSUBST                 GET ADJACENT ENTRY     @Y17XARR
CHKSTAT1 EQU   *                                               @Y17XARR
         TM    OCALSTAT,OCALL           ALL RELEASED?          @Y17XARR
         BO    ADJSUBST                 LEAVE STATUS AS IS     @Y17XARR
         TM    OCALSTAT,OCSOME          SOME RELEASED?         @Y17XARR
         BO    ADJSUBST                 LEAVE STATUS AS IS     @Y17XARR
         NI    OCALSTAT,AVTEFF-OCNONE   CHANGE STATUS          @Y17XARR
         OI    OCALSTAT,OCSOME          TO SOME RELEASED       @Y17XARR
ADJSUBST EQU   *                                               @Y17XARR
         SR    RPARM,RPARM              CLEAR TO GET TNTINDEX  @Y17XARR
         ICM   RPARM,HALFWORD,TRMCOHRT  GET TNTINDEX           @Y17XARR
         BZ    SETMSGID                 HERIARCHY GONE         @Y17XARR
         STCM  RPARM,HALFWORD,OCTRMTBL  SAVE TNT INDEX         @Y17XARR
         CLC   OCSEQ,OCTRMTBL           SAME TNT INDEX?        @Y17XARR
         BE    SETMSGID                 YES,SETUP MSG          @Y17XARR
         BAL   RRET,GETTTE              GET TTE ADDRESS        @Y17XARR
         ICM   RPARM,FULLWORD,OPCTRMWA  GET ORIG TTE           @Y17XARR
         CLI   TRMTYPE-IEDNTRM(RPARM),TRMSDLC    ORIG LINE?    @Y17XARR
         BNE   SINGL                    CURRENT ENTRY IS LU    @Y17XARR
         CLI   TRMTYPE,TRMLUNT          CURRENT ENTRY LU?      @Y17XARR
         BE    SINGL                    CURRENT ENTRY IS LU    @Y17XARR
         C     RTERM,OCMODNME+FRSTWORD  IS THIS PU THE ORIG?   @Y17XARR
         BNE   SUBENTRY                 GET NEXT SUBSTRUCTURE  @Y17XARR
         SR    RPARM,RPARM              CLEAR TO GET TNT INDEX @Y17XARR
         ICM   RPARM,HALFWORD,TRMCOHRT  GET NEXT PU            @Y17XARR
         BZ    SETMSGID                 HEIRARCHY GONE         @Y17XARR
         BAL   RRET,GETTTE              GET STATION ADDRESS    @Y17XARR
         C     RTERM,OPCTRMWA           IS THIS ORIG?          @Y17XARR
         BNE   SUBENTRY                 NO, A PU SUBSTRUCT     @Y17XARR
         MVC   OCTRMTBL,OCSEQ           RESTORE ORIG TNT INDEX @Y17XARR
SETMSGID EQU   *                                               @Y17XARR
         TM    OCALSTAT,OCALL           ALL RELEASED?          @Y17XARR
         BO    ALLSET                   PUT OUT ALL MSG        @Y17XARR
         TM    OCALSTAT,OCSOME          SOME RELEASED?         @Y17XARR
         BZ    NONESET                  PUT OUT NONE MSG       @Y17XARR
         LA    RPARM,SOMERELS           SOME RC FOR TOTE/MPP   @Y17XARR
         LA    RZERO,SOMEMSG            SOME RELEASED MSG NO   @Y17XARR
         B     SETEXTID                 SETUP MODS TO GET CNTRL@Y17XARR
NONESET  EQU   *                                               @Y17XARR
         LA    RPARM,NONERELS           NONE RELSE RC TOTE/MPP @Y17XARR
         LA    RZERO,NONEMSG            NONE RELSE MSG NUM     @Y17XARR
         MVC   OPCMODID,MOVEMSG         MOD TO GET CONTROL NEXT@Y17XARR
         XC    OCWTG,OCWTG              00 TO GET AFTERWARDS   @Y17XARR
         B     NEXTCHK                  GIVE CONTROL TO MSG MOD@Y17XARR
ALLSET   EQU   *                                               @Y17XARR
         LA    RPARM,ALLRELSE           ALL RC FOR TOTE/MPP    @Y17XARR
         LA    RZERO,ALLMSG             ALL MSG NUMBER         @Y17XARR
SETEXTID EQU   *                                               @Y17XARR
         STH   RZERO,OCPARM             SETUP MSG FOR CHKPT    @Y71XARR
         MVC   OPCMODID,CHECKPT         CHECKPOINT NEEDED      @Y17XARR
         MVC   OCWTG,MOVEMSG            MSG MOD TO GET CNTRL   @Y17XARR
         SPACE
NEXTCHK  EQU   *
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS
         BO    XCTL                     YES
         SPACE
         TM    OCFLAG,OCTOTE+OCMPP      TOTE OR MPP?
         BZ    XCTL                     NO, GO TO NEXT MODULE
         SPACE
         MVC   OCWTG(NAMELEN),WRTXCTL   SET TO GO DIRECTLY TO WRITER
         STH   RPARM,OCPARM             SET RETURN CODE
         LR    RZERO,RPARM              SET RETURN CODE
         LTR   RPARM,RPARM              ERROR SET?
         BZ    XCTL                     NO, CALL CHECKPOINT
         SPACE
         MVC   OPCLDNME+VARINAME(NAMELEN),WRTXCTL GO DIRECTLY TO WRTIE
XCTL     EQU   *
         NI    OPCFLAG1,AVTEZERO        CLEAR ALL FLAGS
         LR    RPARM,ROPCAVT            SET AVT POINTER FOR XCTL
         SPACE
         XCTL  SF=(E,OPCXCTL)           CALL FOR NEXT MODULE
         SPACE 2
GETTTE   EQU   *                                               @Y17XARR
         LR    RSAVER14,RRET            SAVE RETURN ADDRESS    @Y17XARR
         L     RBRNCH,AVTRNMPT          GET TNT BASE           @Y17XARR
         BALR  RRET,RBRNCH              CALL TO GET TTE ADDR   @Y17XARR
         LR    RTERM,RPARM              GET TTE ADDRESS        @Y17XARR
         LA    RPARM,TRMPRFSZ           GET PREFIX LENGTH      @Y17XARR
         SR    RTERM,RPARM              BACKUP TO PREFIX       @Y17XARR
         LR    RRET,RSAVER14            RESTORE RETURN ADDRESS @Y17XARR
         BR    RRET                     RETURN TO THE CALLER   @Y17XARR
         SPACE 2
MOVENAME MVC   OPCDOUBL(AVTEZERO),AVTEZERO(RSCAN) PLACE NAME
MOVERLN  MVC   OPCDOUBL(ZERO),ZERO(RSCAN)  MOVE RLN            @Y17XARR
PACKRLN  PACK  OPCDOUBL+UNUSED(PACKLEN),OPCDOUBL(ZERO) PACK    @Y17XARR
         SPACE 2
ZAPAREA  DC    100X'FF'                 ZAPAREA                @Y17XARR
         SPACE 3
LENTP    DC    H'03'                    LENGTH OF TP=
RLNLEN   DC    H'3'                     LEN OF POSSIBLE RLN    @Y17XARR
MAXRLN   DC    H'255'                   MAXIMUM RLN            @Y17XARR
RLNRETCD DC    H'8'                     R  C   FROM DCBLOCAT   @Y17XARR
NAME     DC    H'8'                     LENGTH OF ENTRY        @Y17XARR
INACTNCP DC    H'2'                     R C NCP INACTIVE       @Y17XARR
MOVEMSG  DC    C'0H'                    MSG MOD FOR ALL,SOME,  @Y17XARR
*                                       NONE MSGS              @Y17XARR
TPID     DC    C'TP='                   COMMAND ID
XCTL03   DC    C'03'                    ERROR MODULE NAME
CHECKPT  DC    C'06'                    CHECKPOINT MODULE NAME
WRTXCTL  DC    C'07'                    OUTPUT MODULE
WRTMSG   DC    C'09'                    HOLD, RELEASE, AND VARY MESSAGE
COMMA    EQU   X'6B'                    COMMA                  @Y17XARR
LOWEST   EQU   X'F0'                    ZERO INVALID RLN       @Y17XARR
HIGHEST  EQU   X'F9'                    NINE VALID RLN         @Y17XARR
UNUSED   EQU   6                        BYTES NOT USED  - PACK @1Y7XARR
PACKLEN  EQU   2                        BYTES USED IN THE PACK @Y17XARR
HALFWORD EQU   3                        LENGTH OF TNT INDEX    @Y17XARR
FULLWORD EQU   15                       GET FOUR BYTESOF DATA  @Y17XARR
DATAINDX EQU   8                        WHERE THE DATA IS IN   @Y17XARR
*                                       THE WORKAREA           @Y17XARR
INDEX    EQU   1                        TO POINT TO NEXT BYTE  @Y17XARR
NVALDRLN EQU   8                        R C FOR INVALID RLN    @Y17XARR
RLNERR   EQU   524                      RLN INCORRECTLY SPEC   @Y17XARR
NOSUBSTR EQU   561                      GEN'D INCORRECTLY      @Y17XARR
BADGRP   EQU   16                       BADNAME                @Y17XARR
BADGRPRC EQU   X'20'                    R C BADNAME            @Y17XARR
DSLGB    EQU   X'80'                    LGB INDICATOR          @Y17XARR
INVALID  EQU   18                       INVALID COMMAND MSG    @Y17XARR
BADREQST EQU   4                        TYPICAL ERROR          @Y17XARR
ALLMSG   EQU   522                      ALL RELEASED MSG NUM   @Y17XARR
SOMERELS EQU   X'28'                    RC FOR SOME RELEASED   @Y17XARR
ALLRELSE EQU   0                        GOOD R C ALL RELEASED  @Y17XARR
SOMEMSG  EQU   521                      SOME RELEASED          @Y17XARR
NONEMSG  EQU   523                      NONE RELEASED MSG      @Y17XARR
NONERELS EQU   X'24'                    R C NONE WERE RELEASED @Y17XARR
INACTRCD EQU   X'1C'                    R C 3705 DOWN          @Y17XARR
FRSTWORD EQU   4                        PT TO 2ND WORD OF DWORD@Y17XARR
ASUMEBAD EQU   4                        INDICATE AN ERROR      @Y17XARR
         SPACE 3                                                 Y05330
         EJECT                                                   Y05330
***                 DSECTS                             ***       Y05330
         TDCTD
         EJECT
         DCBD  DSORG=TX                                          Y05330
         EJECT
         TITLE 'IGCR010D - DSECTS REFERENCED BY THIS MODULE'
         TAVTD
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
         EJECT
         TPRFD
         EJECT
         TPRIOR
         EJECT
         TTRMD
         EJECT
         TQCBD
         END
