         TITLE 'IGCV410D- OPERATOR CONTROL START TERMINAL MODULE'
IGCV410D CSECT
         SPACE 3
* CHANGE-ACTIVITY = AS FOLLOWS:
********************* MICROFICHE FLAGS ********************** SUPT CODE
*000000-999999                                                 @Y16X5R0
*D425300-425600                                                 SA60316
*D471000-478000,482000,469000                                   SA60006
*A203000-253000                                                 SA61772
*A481000                                                        SA67777
*C312000                                                        SA70182
*A402000                                                        SA70182
*A427000                                                       @SA74217
*A130000,484000,537000                                         @Y17XARP
*D485000-495000                                                @Y17XARP
*C005000,005600,002500                                         @Y17XART
*A044000,047000,099000                                         @Y17XART
*C318100-318700 MOVED AFTER 342000                             @OX16689
*A501000                                                       @OY16378
*C312000,427600                                                @OY16378
*D402200-402600,403300,403600,409000                           @OY16378
         SPACE 3                                                 S21903
*D470000,4960000                                               @Y17XARP
***********************************************************************
*                                                                     *
*  MODULE NAME = IGCV410D (TCAM ,OP CTL)                       @Y17XART
*                                                                     *
*  DESCRIPTIVE NAME = OPERATOR CONTROL START TERMINAL MODULE          *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS: CHANGE LEVEL 10                                     @Y17XARP
*                                                                     *
* FUNCTION -- THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH  *
*    REQUEST THAT A SPECIFIED TERMINAL BE ACTIVATED FOR ENTERING, OR  *
*    ACTIVATED FOR BOTH ENTERING AND ACCEPTING.  THE ROUTINE SEARCHES *
*    THE TERMNAME TABLE SERIALLY FOR AN ENTRY MATCHING THAT SPECIFIED *
*    IN THE COMMAND.  IF NO MATCH IS FOUND, AN ERROR MESSAGE NUMBER   *
*    IS SET UP AND CONTROL IS TRANSFERRED TO THE ERROR MODULE.  WHEN  *
*    THE ENTRY IS FOUND IT IS TESTED FOR A PROCESS ENTRY (X'20' IN    *
*    TRMSTATE).  IF IT IS, SINCE PROCESS ENTRIES HAVE NO INVITATION   *
*    LISTS, AN ERROR MESSAGE NUMBER IS SET UP AND CONTROL IS          *
*    TRANSFERRED TO THE ERROR MODULE.  THE QCB AT TRMDESTQ IS USED TO *
*         OBTAIN THE RLN (QCBRELLN) AND THE DCB (QCBDCBAD).  THE DCB  *
*    IS TESTED FOR OPEN STATUS, AND IF NOT AN ERROR MESSAGE NUMBER IS *
*    SET UP AND CONTROL IS TRANSFERRED TO THE ERROR MODULE.  THE DEB  *
*    IS ACCESSED FROM DCBDEBAD AND ITS UCB AT DEBUCBAD IS TESTED FOR  *
*    ZERO.  IF ZERO, THE LINE HAS BEEN OPENED DD DUMMY, SO THAT AN    *
*    ERROR MESSAGE NUMBER IS SET UP AND CONTROL IS TRANSFERRED TO THE *
*    ERROR MODULE.  THE LCB ADDRESS IS DETERMINED FROM DCBIOBAD AND   *
*    THE LCB TESTED FOR DIAL (LCBDIAL ON IN LCBSTAT2).  IF IT IS DIAL,*
*    AN ERROR MESSAGE NUMBER IS SET UP AND CONTROL IS TRANSFERRED TO  *
*    THE ERROR MODULE.  THE LINE IS TESTED FOR STOPPED.  IF NOT,      *
*    CONTROL IS TRANSFERRED TO STOPLINE TO STOP THE LINE.  THE X'40'  *
*    BIT IS TURNED ON IN OCSWITCH TO INDICATE THAT THE LINE SHOULD BE *
*    STARTED BACK AFTER ACTIVATING THE TERMINAL.  ALL THE        S22024
*    PREVIOUS PREPROCESSING IS DONE IN IGCV210D AND CONTROL IS   S22024
*    PASSED TO THIS MODULE.  THE LIST IS TESTED                  S22024
*    TO SEE IF ALL ENTRIES ARE ACTIVE.  IF THEY ARE, A RESPONSE       *
*    NUMBER IS SET UP AND CONTROL IS TRANSFERRED TO THE VARY MESSAGE  *
*    MODULE.  IF THE REQUEST IS TO START GENERAL POLL CONTROL IS      *
*    PASSED TO THE START GENERAL POLL MODULE (IGCV610D) IN ORDER TO   *
*    START IT.  EACH ENTRY IS TESTED TO SEE IF IT IS THE CORRECT      *
*    TERMINAL.  IF IT IS AND IS NOT ALREADY ACTIVE, IT IS SWAPPED FROM*
*    THE INACTIVE TO THE ACTIVE PART OF THE LIST AND SWAPPED FORWARD  *
*    IF NECESSARY UNTIL IT REACHES ITS PROPER LOCATION IN THE LIST,   *
*    AND A BIT IS SET TO INDICATE THAT THE LIST WAS CHANGED.  WHEN THE*
*    END OF THE LIST IS REACHED, OCSWITCH IS TESTED TO SEE IF THE     *
*    LINE NEEDS TO BE RESTARTED.  IF SO, THE LINE IS RESTARTED.       *
*    OTHERWISE, OR AFTER THE LINE IS RESTARTED, THE MOVED BIT IS      *
*    CHECKED TO SEE IF THE INVITATION LIST HAS BEEN CHANGED.  IF OFF, *
*    THE TERMINAL WAS ALREADY ACTIVE, SO THAT A RESPONSE MESSAGE IS   *
*    SET UP TO INDICATE THE TERMINAL WAS ALREADY STARTED.             *
*    OTHERWISE, A RESPONSE MESSAGE IS SET UP TO INDICATE THAT THE     *
*    TERMINAL IS STARTED.                                             *
*    THE POST HAS BEEN CHANGED FROM THE DISABLED READY Q TO    @Y17XART
*    ASYNCRONOUS READY Q.                                      @Y17XART
*                                                                     *
* ENTRY POINTS --                                                     *
*    IGCV410D TO PROCESS VARY TERMINAL,ONTP OPERATOR CONTROL COMMANDS.*
*    PURPOSE - SEE FUNCTION                                    @Y17XART
*    LINKAGE - XCTL SF=(E,OPCXCTL)                             @Y17XART
*                                                                     *
* INPUT --                                                            *
*    REGISTER 1 - ADDRESS OF OPERATOR CONTROL AVT.  RELEVANT FIELDS   *
*    ARE OPCDOUBL.  THIS FIELD CONTAINS THE DDNAME OR THE UCB ADDRESS.*
*    ANOTHER FIELD IS OPCCOPCE WHICH IS THE ADDRESS OF THE CURRENT    *
*    OPERATOR CONTROL ELEMENT.  RELEVANT FIELDS ARE OCBACKUP.  THIS   *
*    FIELD CONTAINS EITHER A DDNAME OR A UCB ADDRESS.  ANOTHER        *
*    RELEVANT FIELD IS OCSWITCH.  THIS INDICATES WHETHER TO START     *
*    THE TERMINAL FOR ENTERING OR START THE TERMINAL FOR BOTH ENTERING*
*    AND RECEIVING.  IF THE X'40' BIT IS ON, THE LINE SHOULD BE       *
*    RESTARTED AFTER ACTIVATING THE TERMINAL.  FOR TOTE, OCOPTCDE IS  *
*    IS CHECKED TO SEE IF THE TERMINAL SHOULD BE BOTH ACTIVATED AND   *
*    RELEASED OR JUST ACTIVATED.                                      *
*                                                                     *
* OUTPUT --                                                           *
*    REGISTER 0 - NUMBER OF APPROPRIATE ERROR MESSAGE.                *
*    REGISTER 1 - RETURN CODE VALUES.                                 *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         NONE.                                                       *
*                                                                     *
* EXITS,NORMAL --                                                     *
*         TO MODULE IGCV610D TO PROCESS GENERAL POLL REQUESTS.        *
*         TERMINAL WAS ACTIVATED AS REQUESTED.  RESPONSE MESSAGE      *
*         NUMBER SET UP, X'00' RETURN CODE SET.                       *
*         TERMINAL WAS ALREADY ACTIVATED.  RESPONSE MESSAGE NUMBER    *
*         SET UP, X'08' RETURN CODE SET.                              *
*                                                                     *
* EXITS,ERROR --                                                      *
*         TERMINAL COULD NOT BE FOUND OR IS A PROCESS ENTRY.  ERROR   *
*         MESSAGE NUMBER SET UP, X'04' RETURN CODE SET.               *
*         LINE IS NOT OPEN, OPEN DD DUMMY, OR DIAL LINE.  ERROR       *
*         MESSAGE NUMBER SET UP, X'04' RETURN CODE SET.               *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    OPERATOR CONTROL AVT, OPERATOR CONTROL ELEMENT, TERMNAME TABLE,  *
*    DCB, TERMINAL ENTRY, QCB, LCB, DEB.                              *
*                                                                     *
* ATTRIBUTES --                                                       *
*    SERIALLY REUSABLE, REFRESHABLE, ENABLED, TRANSIENT,              *
*    SUPERVISOR MODE.                                                 *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS            *
*    EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING         *
*    HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER              *
*    CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE         *
*    FOR THE NEW DEFINITION.                                          *
*                                                                     *
* NOTES --                                                            *
*    COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS          *
*    PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                      *
*    RESTRICTIONS - NONE                                       @Y17XART
*    REGISTER CONVENTIONS - STANDARD OP CTL                    @Y17XART
*    PATCH LABEL - ZAPAREA                                     @Y17XART
*
* MODULE TYPE - CSECT                                          @Y17XART
*    PROCESSOR - ASSEMBLER                                     @Y17XART
*    MODULE SIZE -                                             @Y17XART
*                                                                     *
*    THIS IS A MODULE RE-WRITE OF IEDQCO AS OF OS/TCAM LEVEL II       *
***********************************************************************
         EJECT
         SPACE
*                                 E Q U A T E S
         SPACE
RZERO    EQU   0                        PARAMETER REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
ROPCAVT  EQU   2                        POINTER TO OP CONTROL AVT
ROPCE    EQU   3                        POINTER TO CURRENT OPCE
RAVT     EQU   4                        TCAM AVT ADDRESS
RWORKA   EQU   5                        POINTER TO RESIDENT WORKAREA
RRLN     EQU   5                        RELATIVE LINE NUMBER
RLCB     EQU   5                        BASE FOR LCB
RLCB1    EQU   6                        BASE FOR LCB
RSCB     EQU   6                        SCB BASE
RENTRY   EQU   6                        CURRENT ENTRY POINTER
RTERM    EQU   6                        TERM ENTRY BASE
RTNT     EQU   6                        TERM NAME TABLE ADDR
RUCB     EQU   6                        UCB ADDRESS
RDCB     EQU   7                        DCB BASE
ROFF     EQU   7                        OFFSET TO DDNAME, ADDR, RLN
RWORK    EQU   8                        WORK REGISTER
RWORK1   EQU   9                        WORK REGISTER
RPREFIX  EQU   9                        BUFFER ADDRESS
RLENGTH  EQU   10                       LENGTH OF ENTRY
RDEB     EQU   10                       DEB BASE
RLIST    EQU   11                       INVITATION LIST PTR
RQCB     EQU   11                       QCB ADDRESS
RBASE    EQU   12                       BASE FOR MODULE
RSAVE    EQU   13                       OP CTL SAVE AREA REG
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
CURR     EQU   14                       REGISTER 14            @Y17XARP
RBRNCH   EQU   15                       SUBROUTINE BRANCH ENTRY POINT
         SPACE 2
EIGHT    EQU   8                        LENGTH OF EIGHT
NAMELEN  EQU   2                        NO. OF NAME CHANGE CHARACTERS
PICKUP   EQU   5                        LOCATION OF DATA AFTER CONVERT
VARINAME EQU   3                        OFFSET TO CHANGE LOAD NAME
FOUR     EQU   4                        VALUE 4                  S22024
ONE      EQU   1                        VALUE 1                  S22024
THREE    EQU   3                        VALUE 3                  S22024
TWO      EQU   2                        VALUE 2                  S22024
ZERO     EQU   0                        ZERO
X01      EQU   X'01'                    HEX ONE
X80      EQU   X'80'                    HEX EIGHTY
ERROR16  EQU   16                       ERROR -STATNAME NOT FOUND
ERROR90  EQU   90                       ERROR -NOT SINGLE ENTRY
ERROR46  EQU   46                       ERROR-OUTPUT ONLY STATION
LINOPN   EQU   X'C1'                    TO CHECK FOR OPEN DCB
STRTBOTH EQU   X'08'                    INDICATES ACTIVATE AND RELEASE
ERROR19  EQU   19                       TERMINAL ALREADY STARTED
ANYMOVED EQU   X'01'                    TO CHECK IF ENTRIES MOVED
EOT      EQU   X'80'                    TO CHECK EOT IN INVLIST
ENDACT   EQU   X'FE'                    END OF ACTIVE ENTRIES
PROC     EQU   X'20'                    PROCESS ENTRY
DIST     EQU   X'40'                    DISTRIBUTION ENTRY
ERROR91  EQU   91                       ERROR-LINE NOT OPEN
ERROR88  EQU   88                       ERROR-TERMINAL IS DIAL
SEVEN    EQU   7                        VALUE 7                  S22024
GOODRST  EQU   20                       TERMINAL STARTED MESSAGE NUMBER
X40      EQU   X'40'                    INDICATE LINE NEEDS RESTARTED
STATERM  EQU   X'0A'                    ACTIVATE TERM SWITCH VALUE
CVTPTR   EQU   16                       POINTER TO CVT
BUSY     EQU   4                        QUEUE BUSY CONDITION
ON       EQU   1                        DISPLACEMENT
TR       EQU   3                        DISPLACEMENT
STRTSNGL EQU   X'46'                    CODE FOR TOTE START TERM (E)
UCBNM    EQU   13                       OFFSET TO NAME IN UCB
LCBPRIOR EQU   X'E0'                    PRIORITY FOR LCB
ORDERBIT EQU   X'20'                    SET WHEN ARRANGING ENTRIES
NORDER   EQU   X'DF'                    TO TURN OFF FLAG
STARTB   EQU   X'0B'                    START TERM (B)
GOODREL  EQU   52                       SUCCESSFUL RELEASE PERFORMED
CONCERR  EQU   156                      VALUE 156                S22024
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         USING DEBNMSUB,RDEB            DEB ADDRESSABILITY
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY
         USING IEDQTNTD,RTNT            TNT ADDRESSABILITY
         USING IEDQPRF,RPREFIX          BUFFER ADDRESSABILITY
         USING IEDQTRM,RTERM            TERM ENTRY ADDRESSABILITY
         USING IEDQSCB,RSCB             SCB ADDRESSABILITY
         SPACE
         BALR  RBASE,RZERO              SET BASE REGISTER
         USING *,RBASE                  MODULE ADDRESSABILITY
BASE     EQU   *
IGCV410D IEDHJN  SKIPID
*              SET UP INITIAL REGISTERS                               *
         LR    ROPCAVT,RPARM            SET UP AVT ADDRESSING
         L     RAVT,OPCAVTPT            TCAM AVT ADDRESSING
         L     ROPCE,OPCCOPCE           GET CURRENT ELEMENT POINTER
         LM    RLCB1,RDCB,ZERO(RSAVE)   RESTORE REGISTERS        S22024
         MVC   OPCDOUBL(EIGHT),OCBACKUP SET UP TERMINAL NAME
         TM    OPCSPEC,OPCRSTRT         RESTART IN PROGRESS     SA61772
         BZ    INVLIST                  NO, BRANCH              SA61772
         TM    AVTBIT3,AVTSTAIN         S=I SPECIFIED           SA61772
         BZ    STATANAL                 NO, DON'T UPDATE I-LIST SA61772
*                                       FROM INCIDENT RECORD IF SA61772
*                                       S=I NOT SPECIFIED       SA61772
         SPACE
         SPACE
*              SET UP CONTROL FOR INVITATION LIST                     *
INVLIST  EQU   *
         LA    RLIST,DCBINVLI-FOUR      START OF LIST
         SR    RRLN,RRLN                CLEAR REGISTER
         IC    RRLN,OCRLN               RELATIVE LINE NUMBER
         SLL   RRLN,TWO                 MULTIPLY RELATIVE LINE NUMBER
         L     RLIST,AVTEZERO(RRLN,RLIST)  LIST ADDRESS
         LA    RLIST,AVTEZERO(RLIST)    CLEAR HIGH BYTE
         LR    RLCB,RLCB1               SAVE ADDRESS OF LCB FOR LATER
         CLI   AVTEZERO(RLIST),AVTEZERO NO ENTRIES
         BNE   OKTOSTRT                 ENTRIES EXIST
         SPACE
         LA    RZERO,ERROR46            OUTPUT ONLY ERROR MSG
         B     ERROR                    GO PRINT OUT ERROR MSG
         SPACE
OKTOSTRT EQU   *
         LA    RPARM,EIGHT(RLIST)       PTR TO BEGINNING OF LIST
         ST    RPARM,LCBINVPT-IEDQLCB-ONE(RLCB) SAVE FOR LINE ACTIVITY
*              START MANIPULATING INVITATION LIST TO START TERMINAL   *
STRTTERM EQU   *
         SR    ROFF,ROFF                CLEAR
         STH   ROFF,OCMODNME+TWO        SET UP FIRST ENTRY
         SR    RLENGTH,RLENGTH          INIT REG
         SR    RWORK1,RWORK1            INIT REG
         IC    RLENGTH,TWO(RLIST)       LENGTH OF ENTRY
         LR    RWORK1,RLENGTH           LENGTH OF ENTRY
         BCTR  RWORK1,AVTEZERO          DECREMENT FOR EXECUTE
*              CHECK STATUS OF INVITATION LIST                        *
NXENTRY1 EQU   *
         LH    ROFF,OCMODNME+TWO        GET NEXT ENTRY
         LA    ROFF,ONE(ROFF)           BUMP ONE
         STH   ROFF,OCMODNME+TWO        STORE BACK
         CLC   AVTEZERO(ONE,RLIST),ONE(RLIST)  ALL ENTRIES ACTIVE
         BE    CKMOVED                  YES
         SPACE
         CLC   OCMODNME+THREE(ONE),AVTEZERO(RLIST) OUT OF ENTRIES
         BH    CKMOVED                  YES, CHK ANY MOVED     @OY16378
         SPACE
         SLL   ROFF,ONE                 MULTIPLY BY TWO
         LR    RENTRY,RLIST             BEGINNING
         SR    RENTRY,ROFF              BACK TO ENTRY
         CLC   AVTEZERO(TWO,RENTRY),OCTRMTBL TO BE ACTIVATED
         BNE   NXENTRY1                 GET NEXT ENTRY
         SPACE
         LA    RENTRY,SEVEN(RLIST)      POINTER TO FIRST ENTRY INDEX
         SR    RRET,RRET                CLEAR
         IC    RRET,ONE(RLIST)          NUMBER OF ACTIVE ENTRIES
         LTR   RRET,RRET                ANY ACTIVE ENTRIES
         BZ    CKINACT                  NO CHECK INACTIVE ENTRIES
         SPACE
CKNEXT1  EQU   *
         AR    RENTRY,RLENGTH           GO TO NEXT ENTRY'S INDEX
         CLC   AVTEZERO(ONE,RENTRY),OCMODNME+THREE IS THIS AN ACTIVE EN
         BE    NXENTRY1                 ACTIVE GET ANOTHER
         SPACE
         BCT   RRET,CKNEXT1             NOT END OF AVTIVE
         SPACE
CKINACT  EQU   *
         LA    RENTRY,ONE(RENTRY)       SKIP ACTIVE FLAG
         TM    THREE(RLIST),EOT         EOT ENTRY PRESENT
         BZ    CKINACT1                 NO GO ON
         SPACE
         LA    RENTRY,ONE(RENTRY)       SKIP EOT ENTRY
CKINACT1 EQU   *
         AR    RENTRY,RLENGTH           GET TO NEXT INDEX
         CLC   AVTEZERO(ONE,RENTRY),OCMODNME+THREE IS THIS AN ENTRY
         BNE   CKINACT1                 NO GET ANOTHER
         TM    OCSWITCH,X80             IS THIS GENERAL POLL
         BZ    SPECPOLL                 NO, BRANCH
GPEXIT   EQU   *                        GENERAL POLL EXIT
         NI    OCSWITCH,OCFOXES-X80     RESET SWITCH
         MVC   OPCLDNME+VARINAME(NAMELEN),STRTGP SET UP G.P. MOD
         B     EXIT                     XCTL TO GENERAL POLL MOD
SPECPOLL EQU   *
         SPACE
         OI    OPCFLAG1,ANYMOVED        SET 'MOVED' INDICATOR
         SR    RENTRY,RLENGTH           POINT TO PREVIOUS ENTRY
         LR    RWORK,RENTRY
         SR    RWORK,RLENGTH            POINT TO PREVIOUS ENTRY
ACTIVATE EQU   *
         CLI   AVTEZERO(RENTRY),ENDACT  FIRST INACTIVE ENTRY
         BE    MACTIVE                  YES MAKE IT ACTIVE
         SPACE
SWAPLOOP EQU   *
         EX    RWORK1,SWAP1             SWAP ONE POSITION
         EX    RWORK1,SWAP2             WITH THE NEXT
         EX    RWORK1,SWAP3             SUCCESSIVE POSITION
         SPACE
         TM    OPCFLAG1,ORDERBIT        TRYING TO ARRANGE
         BO    CKPOS                    YES
         SPACE
         SR    RENTRY,RLENGTH           BACK UP ONE ENTRY
         SR    RWORK,RLENGTH            BACK PREVIOUS ENTRY,ONE ENTRY
         SPACE
         B     ACTIVATE                 GO CHECK FOR INACTIVE ENTRY
         SPACE
MACTIVE  EQU   *
         TM    THREE(RLIST),EOT         EOT ENTRY PRESENT
         BO    MACTIVE1                 YES
         SPACE
         EX    RWORK1,MOVACT            PLACE ENTRY ON ACTIVE SIDE
         LA    RRET,AVTEZERO(RLENGTH,RENTRY) POINT TO NEW END OF ACTIVE
         BCTR  RENTRY,AVTEZERO          GET ONE LESS
         SPACE
         BCTR  RWORK,AVTEZERO           GET ONE LESS
         SPACE
         MVI   AVTEZERO(RRET),ENDACT    RESET ACTIVE POINTER
MACTIVE2 EQU   *
         SR    RRET,RRET                CLEAR REG
         IC    RRET,ONE(RLIST)          GET NUMBER OF AVTIVE ENTRIES
         LA    RRET,ONE(RRET)           INCREMENT FOR BCT
ACTLOOP  EQU   *
         BCT   RRET,ACTPOS              IF NOT LAST POSITION COMPARE
         SPACE
RETURNA  EQU   *
         IC    RRET,ONE(RLIST)          GET
         LA    RRET,ONE(RRET)           AND INCREMENT
         STC   RRET,ONE(RLIST)          NUMBER OF ACTIVE ENTRIES
         NI    OPCFLAG1,NORDER          TURN OFF BIT
         B     NXENTRY1                 RETURN FOR ANOTHER
         SPACE
ACTPOS   EQU   *
         CLC   AVTEZERO(ONE,RENTRY),OCMODNME+THREE ENTRY IN RIGHT POSIT
         BL    RETURNA                  PREVIOUS ENTRY LESS,FINISHED
         SPACE
         OI    OPCFLAG1,ORDERBIT        TRYING TO ARRANGE
         B     SWAPLOOP                 GO SWAP ENTRIES
         SPACE
CKPOS    EQU   *
         SR    RENTRY,RLENGTH           BACK UP ONE
         SR    RWORK,RLENGTH            ENTRY
         B     ACTLOOP                  GO CHECK FOR CORRECT ORDER
         SPACE
*              DETERMINE IF INVITATION LIST CHANGED                   *
CKMOVED  EQU   *
         TM    OPCFLAG1,ANYMOVED        ANY ENTRIES MOVED
         BNO   ALREADY                  NO
         LA    RZERO,GOODRST            TERMINAL STARTED
         SPACE
         SPACE
         B     STATANAL                 SEE IF LINE NEEDS RESTARTED
ALREADY  EQU   *
         TM    OCFLAG,OCMPP+OCTOTE      TOTE OR MPP
         LA    RZERO,ERROR19            ALREADY STARTED MESSAGE
         BZ    ERROR                    NO
         LA    RPARM,EIGHT              SET ALREADY DONE CODE
         SPACE
         B     STATANL1                 CHECK IF LINE NEEDS RESTARTED
         SPACE
GORELEAS EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),RELEASE  TO GO TO RELEASE
         MVC   OCWTG(TWO),CHECKPT       TO INDICATE RELEASE TO CHECKPNT
         TM    OCFLAG,OCTOTE+OCMPP      TOTE OR MPP
         BO    TOTEMPP                  YES
         SPACE
         STH   RZERO,OCPARM             SET UP PARAMETER
         B     TERMINAL                 GO TO RELEASE
         SPACE
TOTEMPP  EQU   *
         STH   RPARM,OCPARM             SET RETURN CODE
TERMINAL EQU   *
         OI    OCSWITCH,X40             TO INDICATE TERMINAL
         B     EXIT                     GO XCTL TO PROPER MODULE
         SPACE
ERROR    EQU   *
         LA    RPARM,FOUR               SET BAD RETURN CODE
         MVC   OPCLDNME+VARINAME(NAMELEN),XCTL03 TO GO TO ERROR MODULE
         TM    OCSWITCH,X80             IS THIS FOR GEN POLL   @SA74217
         BO    NORESTRT                 YES, CK LNE RESTART    @OY16378
         B     STATANL1                 GO PROCESS ERROR CONDITION
CHPTMSG  EQU   *
         NI    OCFLAG,OCFOXES-OCLINE    TURN OFF BIT
         OI    OCSWITCH,X40             INDICATES A TERMINAL TO MSG MOD
         MVC   OPCLDNME+VARINAME(NAMELEN),CHECKPT  SET CHECKPOINT NAME
         MVC   OCWTG(NAMELEN),WRTMSG    WTG TO MESSAGE MODULE
         STH   RZERO,OCPARM             SET OCPARM FOR CHECKPOINT
TESTERR  EQU   *
         TM    OCFLAG,OCTOTE+OCMPP      TOTE OR MPP?
         BZ    EXIT                     NO
         SPACE
         MVC   OCWTG(NAMELEN),WRITE     SET TO GO TO WRITER
         STH   RPARM,OCPARM             GET PARAMETER FIELD
         LR    RZERO,RPARM              SET RETURN CODE IN REG 0
         SPACE
         MVC   OPCLDNME+VARINAME(NAMELEN),WRITE  GO TO WRITER
EXIT     EQU   *
         NI    OPCFLAG1,AVTEZERO        RESET FLAGS
         LR    RPARM,ROPCAVT            OPERATOR CONTROL AVT
         XCTL  SF=(E,OPCXCTL)
STATANAL EQU   *
         SR    RPARM,RPARM              SET GOOD RETURN
STATANL1 EQU   *
         TM    OPCSPEC,OPCRSTRT         RESTART SITUATION
         BZ    NORESTRT                 NO
         SPACE
         TM    OCSWITCH,STARTB          START TERM AND RELEASE TERM
         BO    GORELEAS                 YES
         SPACE
         SPACE
         B     CHPTMSG                  GO TO CHECKPOINT
         SPACE
NORESTRT EQU   *
         TM    OCSWITCH,X40             SHOULD LINE BE RESTARTED
         MVC   OPCDOUBL(EIGHT),OCBACKUP SET UP GOOD NAME
         BZ    CKRELEAS                 NO
         SPACE
         SPACE
NOTMPS   EQU   *
         MVI   LCBPRI-IEDQLCB(RLCB),LCBPRIOR SET E0 PRIORITY IN BUFFER
         USING IEDQLCB,RLCB
         NI    LCBERBST,AVTEFF-LCBPRCPG  TURN OFF FLAG          SA67777
         DROP  RLCB
         OI    LCBSTAT1-IEDQLCB(RLCB),LCBRECVN   SET STATUS FOR START
         ST    RLCB,AVTEZERO(RLCB)      MAKE LCB POINT TO ITSELF
         L     RWORK,CVTPTR             GET CVT ADDR           @Y17XARP
         L     RWORK,AVTCVTPT(RWORK)    GET ADDR OF TCX        @Y17XARP
         USING IEDQTCX,RWORK            ESTABLISH ADDR         @Y17XARP
         L     CURR,TCXREADY            GET ADDR OF CURRENT    @Y17XARP
*                                       ENTRY ON ASYN READY Q  @Y17XARP
NEWCURR  STCM  CURR,SEVEN,LCBLINK-IEDQLCB(RLCB)                @Y17XARP
*                                       LINK NEW ELEM TO CURR  @Y17XARP
         CS    CURR,RLCB,TCXREADY       WAS CURR ELEM UPDATED  @Y17XARP
*                                       ASYNCHRONOUSLY         @Y17XARP
         BNE   NEWCURR                  YES,LINK NEW ELEM TO   @Y17XARP
*                                       CURR ELEM. NO, ASYN    @Y17XARP
*                                       READY QUEUE NOW HAS    @Y17XARP
*                                       NEW ELEM               @Y17XARP
CKRELEAS EQU   *
         CLI   OCOPTCDE,STRTSNGL        ACTIVATE TERMINAL ONLY FOR TOTE
         BE    TESTERR                  GO CHECKPOINT
         SPACE
         TM    OCFLAG,OCTOTE+OCMPP      IS THIS TOTE OR MPP?
         BNZ   GORELEAS                 YES
         TM    OCSWITCH,X80             IS THIS FOR GEN POLL   @OY16378
         BO    TESTERR                  YES, ERROR SITUATION   @OY16378
         SPACE
         TM    OCSWITCH,STARTB          RELEASE TERMINAL ALSO
         BO    GORELEAS                 YES
         SPACE
         LTR   RPARM,RPARM              IS THIS AN ERROR SITUATION
         BZ    CHPTMSG                  NO
         SPACE
         B     TESTERR                  YES, ERROR SITUATION
         SPACE
MACTIVE1 EQU   *
         LA    RRET,TWO                 TO DECREMENT BY TWO
         SR    RENTRY,RRET              BACK UP TWO FOR EOT-FE
         SR    RWORK,RRET
         MVC   OCMODNME+FOUR(TWO),ONE(RENTRY)  SAVE EOT-FE
         EX    RWORK1,MOVEACT1          MOVE TO ACTIVE
         LA    RRET,ONE(RLENGTH,RENTRY) ADDRESSING FOR EOT-FE
         MVC   AVTEZERO(TWO,RRET),OCMODNME+FOUR  RESET EOT-FE
         B     MACTIVE2                 GO TO MACTIVE2
         SPACE
SWAP1    XC    ONE(AVTEZERO,RWORK),ONE(RENTRY)   SWAP
SWAP2    XC    ONE(AVTEZERO,RENTRY),ONE(RWORK) ONE ENTRY
SWAP3    XC    ONE(AVTEZERO,RWORK),ONE(RENTRY)   WITH ANOTHER
MOVACT   MVC   AVTEZERO(AVTEZERO,RENTRY),ONE(RENTRY) MOVE        S22024
MOVEACT1 MVC   ONE(AVTEZERO,RENTRY),THREE(RENTRY)   MOVE         S22024
ERRORTAB DS    0F
         SPACE 2
XCTL03   DC    C'03'                    ERROR MODULE NAME
CHECKPT  DC    C'06'                    CHECKPOINT MODULE NAME
WRITE    DC    C'07'                    OUTPUT WRITER NAME
WRTMSG   DC    C'09'                    HOLD, RELEASE, AND VARY MESSAGE
STOPLN   DC    C'V1'                    STOPLINE MODULE
RELEASE  DC    C'R0'                    RELEASE MODULE
STRTERM  DC    C'V4'                    START TERMINAL MODULE
STRTGP   DC    C'V6'                    START GENERAL POLL MODULE
ZAPAREA  DC    50X'FF'                  PATCH AREA             @Y17XART
         TITLE 'IGCV410D - DSECTS REFERENCED BY THIS MODULE'
         TAVTD
         EJECT
         TTCXD SVS
         EJECT
         DCBD DSORG=TX
         EJECT
         TDEBD
         EJECT
         TDISPD
         EJECT
*        DELETE IEAQFX CALL-NOT REQUIRED FOR VS                       *
         EJECT
         TLCBD
         EJECT
         TOPCED
         EJECT
         TOPCAVTD
         EJECT
         TPRFD
         EJECT
         TPRIOR
         EJECT
         TQCBD
         EJECT
         TSCBD
         EJECT
         TTNTD
         EJECT
         TTRMD
         END
