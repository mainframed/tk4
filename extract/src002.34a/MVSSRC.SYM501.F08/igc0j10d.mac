         TITLE 'OP CTL INPUT HANDLER-II'
IGC0J10D CSECT
*C000000-999999                                                @Y17XAR0
*A331000                                                       @Y17XARX
*C330000                                                       @Y17XARX
*A131000,597000                                                @OY14521
*C514000                                                       @OY14521
***********************************************************************
*                                                                     *
*                                                                     *
* MODULE NAME = IGC0J10D (TCAM,OPERATOR CONTROL)
*                                                                     *
* DESCRIPTIVE-NAME=OPERATOR CONTROL INPUT HANDLER - PART II           *
*                                                                     *
* COPYRIGHT='NONE'                                                    *
*                                                                     *
* STATUS = VERSION 10.0
*                                                                     *
*FUNCTION - THIS MODULE IS THE 2ND LOAD OF THE OPERATOR
*           CONTROL INPUT HANDLER (IGC0010D).  THIS MODULE
*            PROCESSES ALL APPLICATION INPUT TO OPERATOR CONTROL.
*                                                                     *
* NOTES = COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES @Y17XARX
* IS PRIMARILY THROUGH THE OPERATOR CONTROL AVT.               @Y17XARX
*   DEPENDENCIES = NONE                                        @Y17XARX
*   RESTRICTIONS = NONE                                        @Y17XARX
*   REGISTER CONVENTIONS = OPERATOR CONTROL                    @Y17XARX
*   PATCH LABEL = ZAPAREA
*                                                              @Y17XARX
* MODULE TYPE = PROCEDURE                                      @Y17XARX
*   PROCESSOR = ASSEMBLER                                      @Y17XARX
*   MODULE SIZE = 2 K MAXIMUM                                  @Y17XARX
*   ATTRIBUTES = REUSABLE, REFRESHABLE, DISABLED,              @Y17XARX
*   TRANSIENT(TYPE 4), SUPERVISOR MODE                         @Y17XARX
*ENTRY POINTS - IGC0J10D IS THE ONLY ENTRY POINT FOR THE MODULE.      *
*                                                                     *
*INPUT - THE ONLY INPUT REQUIRED FOR THIS MODULE IS APPLICATION
*        PARAMETER LIST.
*                                                                     *
*OUTPUT - FUNCTION IS PERFORMED WHICH RESULTS IN SOME CASES
*      OF DATA BEING PASSED FROM MPP TO MCP.
*                                                                     *
*EXTERNAL REFERENCES --  SVC 102(AQCTL), SVC 7(XCTL)                  *
*                                                                     *
*EXITS,NORMAL --XCTL TO SVC LOADS LISTED OR RETURN TO CALLER.    X02004
*                 IGCR010D,IGC0010D,IGC0A10D.
*EXITS,ERROR --
*        COMPLETION CODE - 4-INVALID TERMINAL NAME (QCOPY)       X02004
*                           -MCP NOT ACTIVE ( MCOUNT,TPDATE,CKREQX02004
*                           -CHECK POINT NOT VALID  (CKREQ)      X02004
*                           -QRESET UNABLE TO FIND ONE ONE OR    X02004
*                            MORE OF THE REQUESTED SEQUENCE NO.  X02004
*                            REG 2 WILL CONTAIN COUNT OF MSG PROCX02004
*        COMPLETION CODE - 8-MCP NOT ACTIVE CTCOPY,QCOPY,ICOPY,  X02004
*                              TCHNG)                            X02004
*                           -TCAM DEB NOT FOUND ( MCOUNT,TPDATE) X02004
*                           -REUSABLE DISK REORGANIZATION IN PROGX02004
*                            REG 2 WILL CONTAIN COUNT OF MESSAGESX02004
*                            PROCESS  BEFORE START OF REORGANIZATX02004
*                             ION.                               X02004
*                        -12-MCP NOT ACTIVE (MCPCLOSE,MRELEASE,  X02004
*                             ICHNG)                             X02004
*                           -NO TCAM DEB FOUND (QCOPY)           X02004
*                          -SEQUENCE NUMBER ERROR (QRESET)       X02004
*                        -16-QREST NOT ALLOWED FOR THIS MPP      X02004
*                        -20-INVALID TERMINAL TYPE (QCOPY)       X02004
*                           -MCP NOT ACTIVE (CQTAM RETRIEVE)     X02004
*                           -INVALID TERMINAL NAME OR TERM HELD  X02004
*                        -24-AN ERROR EXISTS REGARDING THE       X02004
*                            TYPE OF QUEUING OR THERE ARE NO     X02004
*                            MESSAGE QUEUED FOR THIS TERM (QRESETX02004
*                        -28-TERMINATE FUNCTION TO AVOID POSSIBLEX02004
*                            LOGICAL READ ERROR. REG 2 WILL CON- X02004
*                            TAIN COUNT OF MESSAGES MARKED UNSERVX02004
*                            ICED. NO MESSAGES ALE SCHEDULED TOBEX02004
*                            RESENT.
*                                                                     *
*TABLES/WORK AREAS - EXTERNAL--TCAM AVT AND OP CONTROL AVT            *
*                       TNT,QCB,TRM,TCB,DEB,DCB,PEWA,AMWA        X02004
*                   INTERNAL--CIBTBL,MPPTBL, AND XCTLTBL              *
*                                                                     *
*CHARACTER CODE DEPENDENCY --                                         *
*        THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        *
*        REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS        *
*        EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING     *
*        HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER          *
*        CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE     *
*        FOR THE NEW DEFINITION.                                      *
*                                                                     *
*        THIS IS A MODULE REWRITE AS OF OS/VS/TCAM LEVEL 2       X02004
*        THIS A MODULE SPLIT OF IGC0010D.
***********************************************************************
RZERO    EQU   0                       PARAMETER REGISTER
RPARM    EQU   1                       PARAMETER REGISTER
ROPCAVT  EQU   2                       POINTER TO OPC AVT
RPARMA   EQU   2                        PROCESS PROG PARM LIST   X02004
ROPCE    EQU   3                       POINTER TO OPCE
RCVT     EQU   3                        OS/VS COM VECTOR TABLE   X02004
RAVT     EQU   4                       POINTER TO TCAM AVT
RDEB     EQU   5                        DATA EXTENT BLOCK        X02004
RWORKAD  EQU   5                        XCTL AREA ADDRESS      @XA06556
RPRITABL EQU   5                        TOP OF PRIORITY TABLE  @Y17XARX
RTCB     EQU   6                        TASK CONTROL BLOCK       X02004
RPRITEND EQU   6                        END OF PRIORITY TABLE  @Y17XARX
RWORKA   EQU   7                       POINTER TO RESIDENT WORKAREA
RWORK    EQU   8                       WORK REGISTER
RWORK1   EQU   9                       WORK REGISTER
RPREFIX  EQU   10                      POINTER TO BUFFER
RQCB     EQU   10                       QUEUE CONTROL BLOCK      X02004
RCIB     EQU   11                      POINTER TO INPUT CIB
RCOUNT   EQU   11                       COUNT REGISTER           X02004
RBASE    EQU   12                      BASE REGISTER
R13      EQU   13                       BASE FOR REG SAVEAREA
RRET     EQU   14                      RETURN REGISTER
RBRNCH   EQU   15                      BRANCH REGISTER
         SPACE 2
AL2      EQU   2                       MOVE LENGTH OF TWO
APPLICAT EQU   X'20'                   INDICATES APPLICATION PROG IN
APPBTU   EQU   X'D3'                    BTU FOR APPLICATION      S22024
CVTPTR   EQU   16                       LOW CORE ADDRESS OF CVT
FOUR     EQU   4                        VALUE FOUR
EIGHT    EQU   8                        VALUE EIGHT            @OY14521
OFF0B    EQU   11                       MOVE OFFSET            @XA06556
ONE      EQU   1                        VALUE ONE
THREE    EQU   3                        VALUE THREE
ZERO     EQU   0                       CLEAR HIGH BYTE
XCTLOFF  EQU   560                      OFFSET TO XCTL AREA    @XA06556
         EJECT
         USING IEDQAVTD,RAVT           TCAM AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE          OPCE ADDRESSABILITY
         BALR  RBASE,ZERO               SET BASE
         USING *,RBASE             ADDRESSABILITY
IGC0J10D IEDHJN SKIPID
APPLPROC EQU   *                                                 X02004
         SPACE 2
*****************************************************************X02004
*****************************************************************X02004
*                                                                X02004
*        APPLICATION PROGRAM MACRO PROCESSING ROUTINES           X02004
*                                                                X02004
*        THIS ROUTINE VALIDATES USERS REQUEST AND GIVES  CONTROL X02004
*        TO THE APPROPRIATE  PROCESSING ROUTINES. THE FOLLOWING  X02004
*        MACROS ARE PROCESSED BY IGC0A10D VIA XCTL.              X02004
*              TCOPY   MCPCLOSE                                  X02004
*              ICOPY   MRELEASE                                  X02004
*              TCHNG                                             X02004
*              ICHNG                                             X02004
*        THE REMAINDER ARE PROCESSED HERE                        X02004
*              QCOPY   QRESET                                    X02004
*              MCOUNT  TPDATE                                    X02004
*              CKREQ                                             X02004
*                                                                X02004
*****************************************************************X02004
         SPACE 3
         LR    RRET,RWORK               APPL EXIT ADDR         @Y17XART
         LR    RPARMA,RPARM             SET R2 WITH PRM LIST   @Y17XART
         USING APLCOPYD,RPARMA          USER PARM LIST
         SR    RBRNCH,RBRNCH            CLEAR COMPLETION REG     X02004
         L     RCVT,CVTPTR              GET CVT POINTER          Y05330
         ICM   RTCB,ADDRESS,ONE(RCVT)   NEW-OLD POINTER          Y05330
         ICM   RTCB,ADDRESS,OCX05(RTCB) CURRENT TCB POINTER      Y05330
         ICM   RWORKA,ADDRESS,AVTCVTPT+ONE(RCVT) TCAM IN SYSTEM  Y05330
         L     RAVT,ZERO(,RWORKA)       GET TCAM AVT             Y05330
         CLM   RTCB,ADDRESS,AVTOCTCB+ONE OPERATOR CONTROL TASK   Y05330
         BNE   APPLGO                   NO, BRANCH- APPLICATION  Y05330
*                                       PROGRAM                  Y05330
         SR    RZERO,RZERO              CLEAR FOR REENTRY TO   @Y17XART
*                                       IGC0010D               @Y17XART
         L     RPARM,AVTOCGET           OP CTL AVT             @Y17XART
         L     RBRNCH,OPCAPXTL-IEDQOPCD(RPARM) APPL XCTL LIST  @Y17XART
         MVC   ELEVEN(2,RBRNCH),LOADI   ENTRY TO 00            @Y17XART
         XCTL  SF=(E,(15))              XCTL                   @Y17XART
         SPACE 2                                                 X02004
APPLGO   EQU   *                                                 X02004
         CLM   RZERO,BYTE3,NINER        ENTRY FROM QRESET        Y05330
         BE    QRESET                   YES, BRANCH
         CLM   RZERO,BYTE3,FIVE         ENTRY FROM ICOPY,QCOPY,  X02004
*                                       TCHNG,ICHNG,MCPCLOSE,    X02004
*                                       MRELEASE OR TOTE         Y05330
         BNE   CONTTEST                 NO, BRANCH               X02004
         TM    APLCODE,QCOPYCD          ENTRY FROM QCOPY         X02004
         BO    QCOPY                    YES,BRANCH               X02004
SAVEREGS EQU   *                                               @YA10702
         SAVE  (14,12)                                           X02004
         L     RWORKAD,AVTOCGET         OPERATOR CONTROL AVT   @XA06556
         L     RWORKAD,OPCWORK-IEDQOPCD(RWORKAD)               @XA06556
*                                       ADDR OF OPC WORKAREA   @XA06556
         LA    RWORKAD,XCTLOFF(RWORKAD) XCTL AREA              @XA06556
         CLM   RZERO,BYTE3,SIX          MCOUNT                 @YA10702
         BE    XCTLA10D                 YES, BRANCH            @YA10702
         TM    APLCODE,RETRVECD         RETRIEVE               @YA10702
         BZ    XCTLA10D                 NO, BRANCH             @YA10702
         MVC   OFF0B(AL2,RWORKAD),RELEASE RELEASE MOD          @XA06556
         B     GOXCTL                   GO XCTL                @XA06556
XCTLA10D EQU    *                                                Y05330
          MVC    OFF0B(AL2,RWORKAD),APPLINT APPL PGM CMD INT   @XA06556
GOXCTL   EQU    *                                              @XA06556
         LR    RBRNCH,RWORKAD           SET UP TO XCTL         @XA06556
         XCTL  SF=(E,(15))              XCTL                   @Y17XART
         SPACE 2                                                 X02004
CONTTEST EQU   *                                                 X02004
         CLM   RZERO,BYTE3,SEVEN        ENTRY CODE               X02004
         BH    CKREQ                    8 = CKREQ                X02004
         BE    TPDATE                   7 = TPDATE               X02004
         LR    RPARM,RZERO              SET UP PARAMETER VALUE @YA10702
         B     SAVEREGS                 MCOUNT,GO TO IGC0A10D  @YA10702
         USING IEDQDEB,RDEB             DEB                      X02004
         EJECT
TPDATE   EQU   *                                                 X02004
         SPACE 2                                                 X02004
*****************************************************************X02004
*                                                                X02004
*        THIS SUBROUTINE PROCESS THE TPDATE MACRO WHEN ISSUED IN X02004
*        THE APPLICATION PROGRAM. IF DELETE=YES WAS SPECIFIED,   X02004
*        BIT 1 OF GWAOPT2 IS TURNED ON. IF DELETE=NO WAS         X02004
*        SPECIFIED BIT1 OF GWAOPT2 IS TURNED OFF. IF DISAREA WAS X02004
*        SPECIFIED, THE ADDRESS IS SAVED IN GWADTSA. IF RECDEL=  X02004
*        YES WAS SPECIFIED, THE RECORD DELIMITER IS RETURNED TO  X02004
*        THE APPLICATION PROGRAM.                                X02004
*                                                                X02004
*****************************************************************X02004
         SPACE 2
         USING APLTPDTE,RPARM           TPDATE PARM LIST         X02004
         USING IHADCB,RWORK1            DCB                      X02004
         USING IEDQWRKA,RWORKA          ACCESS METHOD WORKAREA @YA10702
         USING IEDQPEWA,RWORK           PEWA                   @YA10702
         USING IEDQQCB,RQCB             QCB                    @YA10702
         ICM   RWORK1,ADDRESS,APLDCBAD  GET DCB ADDRESS          X02004
         L     RDEB,DCBDEBAD            GET DEB ADDRESS          X02004
         TM    DEBTAMID,ID1             TCAM DEB                 X02004
         BNO   ERROR8                   NO, BRANCH               X02004
         ICM   RWORKA,ADDRESS,DEBTAMWA  GET ACCESS METHOD WORKA  X02004
         TM    APLOPTN,DTS              DTSAREA SPECIFIED        X02004
         BZ    NODTS                    NO, BRANCH               X02004
         L     RWORK,APLDTSAR           GET DISAREA ADDRESS      X02004
         STCM  RWORK,ADDRESS,GWADTSA    PUT IN AMWA              X02004
NODTS    EQU   *                                                 X02004
         NI    GWAOPT2,X'FF'-GWADLTE    RESET DELETE             X02004
         TM    APLOPTN,GWADLTE          DELETE=YES SPECIFIED     X02004
         BZ    NODELETE                 NO, BRANCH               X02004
         OI    GWAOPT2,GWADLTE          INDICATE DELETE          X02004
NODELETE EQU   *                                                 X02004
         TM    APLOPTN,RECDEL           DELIMITER SPECIFIED      X02004
         BO    DELIMITR                 YES, BRANCH              X02004
         SR    RPARM,RPARM              CLEAR DELIMITER REG      X02004
         BR    RRET                     RETURN                   X02004
DELIMITR EQU   *                                                 X02004
         L     RWORK,PWAPEWA            PROCESS ENTRY WORK AREA  X02004
         L     RWORK,PEWAPROC           PROCESS ENTRY            X02004
         SR    RPARM,RPARM              CLEAR DELIMITER REG      X02004
         IC    RPARM,TRMCHCIN-IEDQTRM(,RWORK) GET DELIMITER      X02004
         BR    RRET                     RETURN                   X02004
         DROP  RPARM                                            SA60320
         DROP  RWORK1                                            X02004
         EJECT
CKREQ    EQU   *                                                 X02004
*****************************************************************X02004
*        THIS SUBROUTINE BUILDS A PARAMETER LIST FOR IEDQNB WHEN X02004
*        PROCESSING THE CKREQ MACRO FROM THE APPLICATION PROGRAM.X02004
*        THE ADDRESS OF THE ACCESS METHOD WORK AREA IS PASSED TO X02004
*        IEDQNB FOR FURTHER PROCESSING.                          X02004
*                                                                X02004
*****************************************************************X02004
         SPACE 2                                                 X02004
         USING IEDQPCB,RTCB             PCB                      X02004
         USING IEDQCKPD,RWORK           CHECKPOINT WORK AREA     X02004
         USING IEDQCRED,RWORK1          CKPT REQUEST ELEMENT     X02004
         SPACE 1                                                 X02004
         TM    AVTCKFLG,AVTCKTAC        IS TCAM CKPT ACTIVE    @Y17XARX
         BZ    ERROR4                   NO, BRANCH               X02004
         L     RWORK,AVTCKAVT-1         GET CHKPT AVT PTR      @Y17XARX
         L     RWORK,CAVTTCWA-IEDQCKAD(,RWORK)                 @Y17XARX
*                                       GET CHKPT WORK AREA    @Y17XARX
         ICM   RDEB,ADDRESS,NINE(RTCB)  ADDRESS OF DEB FROM TCB  X02004
         SPACE 1                                                 X02004
DEBLOOP  EQU   *                                                 X02004
         BZ    ERROR4                   IF ZERO - ERROR          X02004
         CLI   DEBTAMID,ID1             TCAM DEB                 X02004
         BE    TCAMDEB                  YES, BRANCH              X02004
         ICM   RDEB,ADDRESS,DEBDEBAD    GET NEXT DEB             X02004
         B     DEBLOOP                  CHECK IT                 X02004
         SPACE 2
TCAMDEB  EQU   *                                                 X02004
         ICM   RTCB,ADDRESS,DEBPCBAD    ADDRESS OF PCB           X02004
         ICM   RWORKA,ADDRESS,DEBTAMWA  ACCESS METHOD WORK AREA  X02004
         XC    GWAELEM(ELEMLNG),GWAELEM CLEAR ECB,REQUEST ELEMENTX02004
         CLI   CKPCKRQS,ZERO            CKREQ=0 ON INTRO         X02004
         BE    ERROR4                   BRANCH IF NO CKREQ       X02004
         LA    RWORK1,PWAELEM           TEMP REQ ELEMENT         X02004
         MVI   CREKEY,CRECKREQ          KEY FOR CKREQ            X02004
         ST    RDEB,CREDEB              ADDR OF DEB IN REQ ELE   X02004
         LA    RDEB,AVTCKPTB            ADDR OF CHPT QCB         X02004
         STCM  RDEB,ADDRESS,CREQCBAD    TO ELEMENT               X02004
         MVI   CRELINK,PRIAPCKP         SET PRIORITY             X02004
         LA    RQCB,PCBWRKA             ADDRESS OF REQ ELEM      X02004
         STM   RWORK1,RQCB,PWALIST      PUT IN PARM LIST         X02004
         MVI   PWALIST,OCX0C            MOVE + POST CODE         X02004
         LA    RDEB,PWAELEM+OCX10       ADDRESS OF ECB           X02004
         ST    RDEB,CREECB              TO REQ ELEM              X02004
         LR    RPARM,RWORKA             RETURN AMWA TO IEDQNB    X02004
         BR    RRET                     RETURN                   X02004
         DROP  RWORK1                   DROP WORK REGS           X02004
         DROP  RTCB                     *                        X02004
         DROP  RWORK                    *                        X02004
         EJECT
QRESET   EQU   *
*****************************************************************X02004
*                                                                X02004
*        THIS SUBROUTINE BUILDS A PARAMETER LIST IN THE ACCESS   X02004
*        METHOD WORK AREA FOR IEDQGR WHILE PROCESSING THE        X02004
*        QRESET MACRO FROM THE APPLICATION PROG. THE ADDRESS OF  X02004
*        THE ACCESS METHOD WORK AREA IS PASSED BACK TO IEDQGR FORX02004
*        FURTHER PROCESSING.                                     X02004
*                                                                X02004
*****************************************************************X02004
         SPACE 2
         USING IEDQTRM,RTCB             TERM TABLE               X02004
         USING IEDQPEWA,RWORK           PROCESS ENTRY WORK AREA  X02004
         USING IHADCB,RWORK1            DCB                      X02004
         SPACE 2                                                 X02004
         L     RWORK1,ZERO(,RPARMA)     GET DCB ADDRESS          X02004
         TM    DCBOFLGS,OPEN            DCB OPEN                 X02004
         BNO   ERROR16                  NO, BRANCH               X02004
         SPACE 1                                                 X02004
         L     RTCB,QE8DISP(,RWORKA)    POINTER TO BINARY SRCH   X02004
         L     RDEB,DCBDEBAD            GET THE DEB              X02004
         ICM   RWORKA,ADDRESS,DEBTAMWA  GET ACCESS METHOD WORK ARX02004
         L     RWORK,PWAPEWA            PROCESS ENTRY WORK AREA  X02004
         ICM   RDEB,ALLBYTES,PEGQWKAR   IEDQGQ'S WORK AREA       X02004
         BZ    ERROR16                  QRESET ALLOWED, NO -ERRORX02004
         SPACE 1                                                 X02004
         TM    ZERO(RDEB),AVTE80        QRSET ACTIVE FOR THIS MPPX02004
         BO    ERROR16                  YES, BRANCH              X02004
         SPACE 1                                                 X02004
         L     RCVT,FOUR(,R13)          GET USER'S REG 2         X02004
         LR    RBRNCH,RTCB              GET ADDRESS OF BINARY    X02004
*                                       SEARCH ROUTINE           X02004
         L     RZERO,SAVEREG2(,RCVT)    REG 2                    X02004
         LR    RCVT,RZERO               SAVE USER'S REG 2        X02004
         LR    RTCB,RRET                SAVE REG 14              X02004
         BALR  RRET,RBRNCH              GO TO SEARCH ROUTINE     X02004
         LR    RRET,RTCB                RESTORE REG 14           X02004
         LTR   RBRNCH,RBRNCH            TERM FOUND               X02004
         BZ    ERROR20                  NO, BRANCH               X02004
         SPACE 1                                                 X02004
         STH   RBRNCH,GWAELEM+ELETRM    PUT IN SPECIAL ELEMENT   X02004
         LR    RPARM,RBRNCH             GET TERMTBL OFFSET       X02004
         L     RBRNCH,AVTRNMPT          GET TERM LOGIC ROUTINE   X02004
         BALR  RRET,RBRNCH              GET ENTRY ADDRESS        X02004
         ST    RPARM,GWAELEM+OCX0C      ENTRY ADDR TO SPEC ELEM  X02004
         LR    RRET,RTCB                RESTORE REG 14           X02004
         LR    RTCB,RPARM               TERM ENTRY ADDRESS       X02004
         L     RQCB,ZERO(,RTCB)         GET QCB ADDRESS          X02004
         SR    RBRNCH,RBRNCH            CLEAR COMPLETION REG     X02004
         TM    TRMSTATE,ID1             TERM ENTRY FOR LOG LINE  X02004
*                                       OR LIST TYPE ENTRY       X02004
         BNZ   ERROR20                  YES, BRANCH              X02004
         SPACE 1                                                 X02004
         TM    TRMSTATE,TRMPROC         IS THIS A PROCESS ENTRY  X02004
         BNO   CHKHELD                  NO, BRANCH               X02004
         ICM   RWORK1,ADDRESS,QCBDCBAD  GET DCB ADDRESS          X02004
         CLI   TRMSTAT+1,AVTEZERO       IS DCB OPEN?           @SA70211
         BNE   ERROR16                  BR YES, DCB MUST       @SA70211
*                                       BE CLOSED FOR PROCESS    X02004
         B     CHKQTYPE                 GO CHECK TYPE            X02004
         SPACE 1                                                 X02004
CHKHELD  EQU   *                                                 X02004
         TM    TRMSTATE,TRMHELDN        IS TERMINAL HELD         X02004
         BO    ERROR20                  YES, BRANCH              X02004
         SPACE 1                                                 X02004
CHKQTYPE EQU   *                                                 X02004
         TM    QCBFLAG,QCBPROC+QCBTERMQ CHECK FOR Q BY TERMINAL  X02004
*                                       OR PROCESS ENTRY         X02004
         BZ    ERROR24                  BRANCH IF NEITHER        X02004
         SPACE 1                                                 X02004
         TM    QCBPRIPQ,AVTEFF          PRIORITY LEVEL QEEUING   X02004
         BNZ   ERROR24                  YES, BRANCH              X02004
         SPACE 1                                                 X02004
         TM    QCBDSFLG,QCBDISK         DISK QUEUES              X02004
         BZ    ERROR24                  NO, BRANCH               X02004
         TM    QCBDSFLG,QCBDRQQ         CONC DATA READY QUEUE    X02004
         BO    ERROR24                  YES, BRANCH              X02004
         SPACE 1                                                 X02004
         SR    RPARM,RPARM              CLEAR REG                X02004
         ICM   RPARM,ADDRESS,QCBQBACK   QUEUE EMPTY              X02004
         BZ    ERROR24                  YES, BRANCH              X02004
         SPACE 1                                                 X02004
         LH    RPARM,OCX08(,RCVT)       GET SEQ NO. FROM USER    X02004
         LTR   RPARM,RPARM              SEQ NO. = 0              X02004
         BZ    ERROR12                  YES, BRANCH              X02004
         SPACE 1                                                 X02004
         STH   RPARM,GWAELEM+OCX0A      PUT IN SPECIAL ELEMENT   X02004
         LH    RCVT,TRMOUTSQ            GET NEXT SEQ NO.         X02004
         BCTR  RCVT,RZERO               DECREMENT BY ONE         X02004
         CR    RPARM,RCVT               IS REQUESTED SEQUENCE    X02004
*                                       NUMBER LESS THAN LAST    X02004
*                                       OUTPUT SEQ NO. RECEIVED  X02004
*                                       AT THIS TERMINAL         X02004
         BH    ERROR12                  NO, BRANCH               X02004
         SPACE 1                                                 X02004
         LA    RCVT,ONE(,RCVT)          INCREMENT BY ONE         X02004
         L     RCOUNT,FOUR(,RPARMA)     GET MAX FROM USER        X02004
         SR    RCVT,RPARM               FIND DIFFERENCE BETWEEN  X02004
*                                       LAST OUT SEQ NUMBER AND  X02004
*                                       THE REQUESTED NUMBER     X02004
         CR    RCVT,RCOUNT              GREATER THAN MAX         X02004
         BH    ERROR12                  YES, BRANCH              X02004
         SPACE 1                                                 X02004
         L     RPARM,ZERO(,RDEB)        ADDRESS OF IEDQGQ        X02004
         ST    RPARM,GWAELEM            STORE IN SPECIAL ELEMENT X02004
         MVI   GWAELEM+FOUR,PRIRTBFR    SET PRIORITY             X02004
         STCM  RBRNCH,ADDRESS,GWAELEM+OCX05 CLEAR LINK FIELD     X02004
         SPACE 1                                                 X02004
         LA    RQCB,GWAELEM             GET ELEMENT ADDRESS      X02004
         LA    RCOUNT,FOUR(,RDEB)       GET ADDRESS OF TARGET    X02004
*                                       FIELD IN IEDQGQ WORK AREAX02004
         STM   RQCB,RCOUNT,GWALIST      PUT IN PARAMETER LIST    X02004
         MVI   GWALIST,OCX0C            MOVE & POST CODE         X02004
         LR    RPARM,RWORKA             PASS ADDRESS OF ACCESS   X02004
*                                       METHOD WORK AREA TO QGR  X02004
         BR    RRET                     RETURN TO USER           X02004
         DROP  RTCB                     DROP WORK REGS           X02004
         DROP  RWORK                    *                        X02004
         DROP  RWORK1                   *                        X02004
         EJECT
QCOPY    EQU   *                                                 X02004
*****************************************************************X02004
*                                                                X02004
*        THIS SUBROUTINE COPIES A QUEUE CONTROL BLOCB (QCB) INTO X02004
*        A USER WORK AREA. IT USES THE BINARY SEARCH ROUTINE TO  X02004
*        FIND THE TERM TABLE. FOR A COMPLETE DESCRIPTION OF      X02004
*        THE FUNCTIONSEE THE PROLOG FOR THE QCOPY FUNCTION       X02004
*        MODULE, IEDQE2.                                         X02004
*                                                                X02004
*****************************************************************X02004
         SPACE 2                                                 X02004
         USING IEDQTNTD,RWORK           TERNNAME TABLE           X02004
         USING IEDQTRM,RWORK1           TERMNAME TABLE ENTRY     X02004
         USING APLCOPYD,RPARMA          USER PARAMETER LIST      X02004
         SPACE 1                                                 X02004
         L     RWORK,AVTRNMPT           TERMNAME TABLE           X02004
         LR    RCOUNT,RRET              SAVE REG 14             SA58435
         CLC   APLLIMIT,AVTFZERO        LIMIT SPECIFIED          X02004
         BE    REGULAR                  NO, BRANCH               X02004
         SPACE 1                                                 X02004
         L     RTCB,ZERO(,RPARMA)       TERMINAL NAME            X02004
         CLI   ZERO(RTCB),BLANK           FIRST TIME THROUGH    SA58435
         BE    NAMEOK3                  YES, BRANCH             SA58435
REGULAR  EQU   *                                                 X02004
         L     RBRNCH,QE8DISP(,RWORKA)  ADDR OF BINARY SEARCH    X02004
*                                       ROUTINE                  X02004
         L     RZERO,ZERO(,RPARMA)      GET TERMNAME FOR SEARCH  X02004
         BALR  RRET,RBRNCH              GO TO SEARCH ROUTINE     X02004
         LR    RRET,RCOUNT              RESTORE REG 14           X02004
         LTR   RBRNCH,RBRNCH            TERM NAME VALID          X02004
         BZ    ERROR32                  NO, BRANCH             @OY14521
         SPACE 1                                                 X02004
NAMEOK1  EQU   *                                                 X02004
         CLC   APLLIMIT,AVTFZERO        ITERATIVE PROCESS        X02004
         BE    NAMEOK                   YES PROCESS NORMALLY     X02004
         SPACE 1                                                 X02004
NAMEOK3  EQU   *                                                 X02004
         LA    RBRNCH,ONE(,RBRNCH)      ICR ENTRY BY ONE         X02004
         CH    RBRNCH,TNTLEN            LAST ENTRY               X02004
         BNH   NAMEOK4                  NO, BRANCH               X02004
         LA    RBRNCH,LAST              GET LAST COMP CODE       X02004
         BR    RRET                     RETURN TO USER           X02004
         SPACE 1                                                 X02004
NAMEOK4  EQU   *                                                 X02004
         LR    RCVT,RBRNCH              SAVE TNT OFFSET          X02004
         LR    RPARM,RBRNCH             PUT IN R1 FOR SRCH LOGIG X02004
         L     RBRNCH,AVTRNMPT          ADR OF CONVER LOGIC      X02004
         BALR  RRET,RBRNCH              GET ENTRY ADDRESS        X02004
         LR    RRET,RCOUNT              RESTORE REG 14           X02004
         LR    RWORK1,RPARM             ADDRESSIBILITY FOR TERM  X02004
         LR    RBRNCH,RCVT              RESTORE OFFSET           X02004
         TM    TRMSTATE,TRMLIST         LIST ENTRY               X02004
         BO    NAMEOK3                  YES, BRANCH GET NEXT     X02004
         TM    TRMDSORG,TRMLGB          LINE GROUP BLOCK         Y05330
         BO    NAMEOK3                  YES, GET NEXT ENTRY      Y05330
         CLC   TRMDESTQ(THREE),AVTFZERO DEST. QCB FOR THIS TYPE  X02004
*                                       TERMINAL ENTRY           X02004
         BZ    NAMEOK3                  NO, BRANCH               X02004
         ICM   RQCB,ADDRESS,TRMDESTQ    GET QCB ADDRESS          X02004
         TM    QCBDSFLG,QCBDRQQ         CONC DATA READY QUEUE    X02004
         BO    NAMEOK3                  YES, BRANCH GET NEXT     X02004
         CLC   APLLIMIT+TWO(TWO),QCBMSGCT THIS QCB HAVE THE      X02004
*                                       LIMIT                    X02004
         BH    NAMEOK3                  NO, BRANCH               X02004
NAMEOK5  EQU   *                                                 X02004
         LR    RZERO,RQCB               SAVE QCB ADDRESS         X02004
         SR    RCOUNT,RCOUNT            CLEAR COUNT REG          X02004
         IC    RCOUNT,TNTENLEN          GET MAX LENGTH OF ENTRY  X02004
         LA    RCOUNT,THREE(,RCOUNT)    INCREMENT FOR ADDRESS    X02004
         LR    RQCB,RBRNCH              GET OFFSET               X02004
         BCTR  RQCB,RZERO               DECREMENT                X02004
         MR    RQCB,RQCB                MULTIPLY OFFSET BY LENGTHX02004
         LA    RQCB,TNTFIRST            GET FIRST ADDRESS        X02004
         LA    RQCB,ZERO(RCOUNT,RQCB)   INCR TO BEGIN OF ENTRY   X02004
         SR    RCOUNT,RCOUNT            CLEAR LENGTH REG         X02004
         IC    RCOUNT,TNTENLEN          GET MAX LENGTH           X02004
         BCTR  RCOUNT,RZERO             DECREMENT FOR EXECUTE    X02004
         LR    RDEB,RQCB                SET SOURCE REG           X02004
         EX    RCOUNT,MOVE              MOVE NAME TO USR'S AREA  X02004
         LR    RQCB,RZERO               RESTORE QCB ADDR.        X02004
         B     COMP1                    PROCESS REMAINDER        X02004
         EJECT                                                   X02004
NAMEOK   EQU   *                                                 X02004
         LR    RPARM,RBRNCH             TERMNME TABLE OFFSET     X02004
         L     RBRNCH,AVTRNMPT          ADDR OF CONV. LOGIC      X02004
         BALR  RRET,RBRNCH              GET ENTRY ADDRESS        X02004
         LR    RRET,RCOUNT              RESTORE R14              X02004
         LR    RWORK1,RPARM             ADDRESS OF TERM ENTRY    X02004
         SR    RBRNCH,RBRNCH            CLEAR COMPLETION REG     X02004
         TM    TRMSTATE,TRMLIST         VALID TERMINAL           X02004
         BO    ERROR4                   NO, BRANCH               X02004
         TM    TRMDSORG,TRMLGB          LINE GROUP ENTRY         Y05330
         BO    ERROR4                   YES, BRANCH              Y05330
         SPACE 1                                                 X02004
COMPSIZE EQU   *                                                 X02004
         CLC   TRMDESTQ(THREE),AVTFZERO DEST QCB FOR THIS        X02004
*                                       TYPE OF TERMINAL ENTRY   X02004
         BZ    ERROR4                   NO, BRANCH               X02004
COMP1    EQU   *                                                 X02004
         SPACE 1                                                  02004
         LA    RDEB,QCBMSIZE            INIT COUNT TO SIZE OF    X02004
*                                       MASTER QCB               X02004
         ICM   RQCB,ADDRESS,TRMDESTQ    ADDRESS OF MASTER QCB    X02004
         LR    RAVT,RQCB                SAVE QCB ADDR FOR MOVE   X02004
         LA    RQCB,QCBMEND             ADDRESS OF FIRST PRIORITYX02004
*                                       LEVEL QCB                X02004
LOOPX    EQU   *                                                 X02004
         LA    RDEB,QCBPSIZE(,RDEB)     INCR BY SIZE OF ONE      X02004
*                                       PRIORITY QCB             X02004
         CLI   QCBPRIPQ-IEDQPQCB(RQCB),ZERO ANOTHER PRIORITY QCB X02004
         BZ    MVCL                     NO, BRANCH               X02004
         LA    RQCB,QCBPSIZE(,RQCB)     NEXT PRIORITY QCB        X02004
         B     LOOPX                    GET NEXT QCB             X02004
         SPACE 2                                                 X02004
ERROR32  LA    RBRNCH,EIGHT(RBRNCH)     CC=32                  @OY14521
ERROR24  LA    RBRNCH,FOUR(RBRNCH)      CC = 24                  X02004
ERROR20  LA    RBRNCH,FOUR(RBRNCH)      CC = 20                  X02004
ERROR16  LA    RBRNCH,FOUR(RBRNCH)      CC = 16                  X02004
ERROR12  LA    RBRNCH,FOUR(RBRNCH)      CC = 12                  X02004
ERROR8   LA    RBRNCH,FOUR(RBRNCH)      CC = 8                   X02004
ERROR4   LA    RBRNCH,FOUR(RBRNCH)      CC = 4                   X02004
         BR    RRET                     RETURN TO USER           X02004
         SPACE 2
MVCL     EQU   *                                                 X02004
         L     RPARMA,APLUSEWK          ADDRESS OF USER'S WORKA  X02004
         LR    RCVT,RDEB                SET OPERAND 1 LENGTH     X02004
         MVCL  RPARMA,RAVT              MOVE QCB TO USER WORK ARAX02004
         SR    RBRNCH,RBRNCH            CLEAR COPMLRTION REG     X02004
         BR    RRET                     RETURN TO USER           X02004
MOVE     MVC   ZERO(ONE,RTCB),ZERO(RDEB) MOVE FROM QCB TO USER   X02004
*                                       WORK AREA                X02004
TWO      EQU   2                        HALFWORD DISPLACEMENT    X02004
LAST     EQU   X'80'                    CC FOR LAST              X02004
DTS      EQU   X'20'                    DTSAREA=YES              X02004
RECDEL   EQU   X'80'                    RECDEL=YES               X02004
TCOPYCD  EQU   X'80'                    ENTRY FROM TCOPY         X02004
QCOPYCD  EQU   X'40'                    ENTRY FROM QCOPY         X02004
TCHNGCD  EQU   X'20'                    ENTRY FROM TCHNG         X02004
ICOPYCD  EQU   X'10'                    ENTRY FROM ICOPY         X02004
APLQETCD EQU   X'08'                    ENTRY FROM IEDQET        X02004
RETRVECD EQU   X'04'                    ENTRY FROM RETRIEVE      Y05330
ID1      EQU   X'C0'                    TCAM DEB                 X02004
ADDRESS  EQU   7                        ADDRESS MASK             X02004
ALLBYTES EQU   15                       ALLBYTES MASK            X02004
HALFWORD EQU   3                        HALFWORD MASK            X02004
PARTBUF  EQU   X'02'                    PARTIAL DATA FLAG        X02004
BYTE3    EQU   1                        REG LOWORDER BYTE        X02004
ELEMLNG  EQU   20                       ELEMENT LENGTH           X02004
ELETRM   EQU   8                        DISP FOR TERM ADDR.      X02004
NINE     EQU   9                        DISPLACEMENT             X02004
OPEN     EQU   X'10'                    OPEN FLAG                X02004
SAVEREG2 EQU   28                       OFFSET TO REG 2 IN SAVARAX02004
QE8DISP  EQU   48                       ADDR OF THE BINARY SEARCHX02004
*                                       IN DISPATCHER WORK AREA  X02004
BLANK    EQU   X'40'                    FIRST TIME THROUGH      SA58435
PUT      EQU   X'40'                    OUTPUT DCB              SA60320
PRIRTBFR EQU   X'D4'                    QRESET PRIORITY          X02004
FIVE     DC    X'05'                    RZERO ENTRY CODES        X02004
SIX      DC    X'06'                                           @YA10702
SEVEN    DC    X'07'                    *                        X02004
NINER    DC    X'09'                                             X02004
LOADI    DC    C'00'                    ENTRY TO IGC0010D      @Y17XART
ELEVEN   EQU  11                        DISP INTO XCTL LIST    @Y17XART
         SPACE 2
APPLINT  DC    C'0A'                  APPL PROG PROC           @XA06556
RELEASE DC     C'R0'                                           @XA06556
CLSMSG   WTO   'IED063I CLOSEDOWN IN PROGRESS - COMMAND REJECTED',     X
               MF=L,DESC=5,MCSFLAG=(REG0,RESP)
ZAPAREA  DC    50F'0'                   PATCH AREA
         TITLE 'IGC0010D - DSECTS USED BY THIS MODULE'
         TAVTD
         EJECT
         TCAVTD                                                @Y17XARX
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
         EJECT
         TOPRED                                                @Y17XARX
         TPCBD
         DCBD  DSORG=TX
         TDEBAPD
         TACSMD
         TTRMD
         TPEWAD
         TQCBD
         TTNTD
         TPRFD
         TCKPD 3330
         TPRIOR
APLCOPYD DSECT                          USER PARM LIST           X02004
APLCODE  DS    FL1                      ENTRY CODE               X02004
APLTERM  DS    AL3                      TERMINAL ADDRESS         X02004
APLUSEWK DS    F                        USER WORK AREA           X02004
APLLIMIT DS    F                        LIMIT OPERAND            X02004
APLTPDTE DSECT                          TPDATE PARM LIST         X02004
APLOPTN  DS    FL1                      OPTION BYTE              X02004
APLDCBAD DS    AL3                      DCB ADDRESS              X02004
APLDTSAR DS    F                        DTSAREA ADDRESS          X02004
      END
