         TITLE 'IGCV110D - OPERATOR CONTROL STOPLINE MODULE'
IGCV110D CSECT
         SPACE 3                                                 S21903
*  CHANGE ACTIVITY AS FOLLOWS
*A000000-999999                                                @Y16X5R0
*A300000                                                        SA63024
*A143000,302000                                                @SA71514
*A186000                                                       @SA71947
*C300500                                                       @YA08784
*A255000                                                       @OY14483
******************** MICROFICHE FLAGS *********************** SUPT CODE
         SPACE 3                                                 S21903
*C005000                                                         S22024
*A027300-027360,051500,216100-216600,481300,8484000-485000       S22024
*D389500,414500                                                 SA67131
*A295000                                                       @SA69627
*A304000,381000,150800                                         @OX14763
*C332300                                MOVED AFTER 331000     @OS77381
*A005300,005700,005900,028000,031000,208400,208500,488000      @Y17XARW
*A150960,297000,326000,428000                                  @OY16355
*D027000                                                       @Y17XARW
*A1509600216600,216960,336000,371000,385000,329000,470000      @OY14092
*A317000,342000                                                @OY14092
*C119000,302100-302700,384000                                  @OY14092
*C336140-336170,336260-336290                                  @OY16079
*C216615                                                       @OZ29091
*C461000                                                       @OZ28375
***********************************************************************
*                                                                     *
*  TITLE:  IGCV110D - OPERATOR CONTROL STOPLINE MODULE                *
*                                                                     *
*                                                                     *
*  MODULE NAME = IGCV110D( TCAM, OP CONTROL )                  @Y17XARW
*                                                                     *
*  DESCRIPTIVE NAME = OPERATOR CONTROL STOPLINE MODULE                *
*                                                                     *
*  COPYRIGHT = NONE                                            @Y17XARW
*                                                                     *
*  STATUS = VERSION 10.0                                       @Y17XARW
*                                                                     *
* FUNCTION -- THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH  *
*    REQUEST THE STOPPING OF ACTIVITY ON A LINE EITHER IMMEDIATELY    *
*    OR AT COMPLETION OF THE CURRENT OPERATION.  THE MODULE STOPS     *
*    EITHER A SINGLE LINE OR ALL LINES ASSOCIATED WITH ONE DCB DEPEND-*
*    ING ON WHAT KIND OF REQUEST.  THE OPERATOR CONTROL ELEMENT IS    *
*    LINKED INTO A WAIT LIST TO WAIT FOR COMPLETION OF THE STOPLINE   *
*    REQUEST.  THIS MODULE REQUESTS A UNIT WHICH IS SENT OVER TO TCAM *
*    STOPLINE (HK).  THIS UNIT CONTAINS THE ADDRESS OF HK, CERTAIN    *
*    FLAGS, THE ADDRESS OF THE LCB, AND THE ADDRESS OF THE DEB FOR THE*
*    LINE TO BE STOPPED.  WHEN A STOPPED LCB COMES BACK TO STOPLINE,  *
*    THE MODULE RECEIVES IT JUST LIKE A REGULAR ELEMENT.  IF IT IS THE*
*    SAME ELEMENT AS WAS SENT OVER, AN ERRORMESSAGE IS PRINTED SINCE  *
*    THIS INDICATES THAT TSO IS ON THE LINE.  OTHERWISE, IT COMPARES  *
*    THE RETURNED LCB WITH THE LCB ADDRESS IN THE UNITS LINKED TO     *
*    OPERATOR CONTROL ELEMENTS ON THE WAIT LIST.  WHEN A MATCH IS     *
*    FOUND, THE UNIT IS DEALLOCATED.  IN THE CASE OF STOPPING ALL     *
*    LINES ON A DCB, THE RELATIVE LINE NUMBER IS INCREMENTED AND THE  *
*    REQUEST IS MADE TO HK TO STOP THE NEXT LINE ON THE LINE GROUP.   *
*    FOR A SINGLE LINE STOP, THE RLN IS COMPARED TO THE NUMBER OF     *
*    LINES IN THE GROUP.  IF THE RLN IS HIGHER THAN THE NUMBER OF     *
*    LINES, AN ERROR EXIT IS TAKEN.  FOR COMMANDS FOR DEVICES OFFS22024
*    OF AN RN, A NEW MODULE IGCV810D IS XCTLED TO FROM IGCV110D. S22024
*                                                                     *
*    NOTES = SEE BELOW                                         @Y17XARW
*                                                              @Y17XARW
*            RESTRICTIONS = NONE                               @Y17XARW
*                                                              @Y17XARW
*            REGISTER CONVENTIONS = SEE REGISTER DECLARES      @Y17XARW
*                                                              @Y17XARW
*            PATCH LABEL = ZAPAREA                             @Y17XARW
*                                                              @Y17XARW
*    MODULE TYPE = PROCEDURE                                   @Y17XARW
*                                                              @Y17XARW
*            PROCESSOR = ASSEMBLER XF                          @Y17XARW
*                                                              @Y17XARW
*            MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY      @Y17XARW
*                                                              @Y17XARW
* ENTRY POINTS --                                                     *
*    IGCV110D FOR ALL STOPLINE REQUESTS.                              *
*                                                                     *
*            PURPOSE = SAME AS FUNCTION                        @Y17XARW
*                                                              @Y17XARW
*            LINKAGE = IGCV010D, IGCV210D, IGCV410D, IGCI010D, @Y17XARW
*                      XCTL TO IGCV110D                        @Y17XARW
*                                                              @Y17XARW
*                      IGCV110D XCTL'S TO IGC0010D, IGC0310D,  @Y17XARW
*                      IGC0610D, IGC0710D, IGC0910D, IGCVG10D, @Y17XARW
*                      IGCV210D, IGCV410D                      @Y17XARW
*                                                              @Y17XARW
* INPUT --                                                            *
*    ON RETURNING STOPPED LCBS.  OPCCOPCE IS THE ADDRESS OF THE       *
*    CURRENT ELEMENT.  RELEVANT FIELDS ARE OCSWITCH.  THIS INDICATES  *
*    WHETHER IT IS AN 'I' TYPE OR 'C' TYPE STOPLINE.                  *
*                                                                     *
*    BESIDES AN EXPLICIT STOPLINE REQUEST, A REQUEST TO STOP A LINE   *
*    CAN COME FROM THE FOLLOWING MODULES.                             *
*         IGCV210D STOP TERMINAL                                      *
*         IGCV410D START TERMINAL                                     *
*         IGCI010D ICHNG DEACTIVATE                                   *
*         IGCI110D ICHNG MOVE OR ACTIVATE                             *
*                                                                     *
* OUTPUT --                                                           *
*    REGISTER 0 - NUMBER OF AN ERROR MESSAGE.                         *
*    REGISTER 1 - RETURN CODE SET UP.                                 *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         NONE.                                                       *
*                                                                     *
* EXITS,NORMAL --                                                     *
*        FOR STOPLINE REQUESTS, MODULE IGCV810D IS XCTLED TO.    S22024
*         LINE OR LINE GROUP STOPPED.  APPROPRIATE MESSAGE NUMBER     *
*         SET UP TO GO TO MESSAGE MODULE, X'00' RETURN CODE SET.      *
*                                                                     *
*         LINE OR LINE GROUP ALREADY STOPPED.  APPROPRIATE MESSAGE    *
*         NUMBER SET UP TO GO TO MESSAGE MODULE, X'08' RETURN CODE SET*
*                                                                     *
* EXITS,ERROR --                                                      *
*         TERMINAL NOT FOUND, LINE OR LINE GROUP NOT OPEN, OR OPEN    *
*         DD DUMMY, OR RLN INVALIDLY SPECIFIED.  ERROR REGISTER SET   *
*         UP, X'04' RETURN CODE SET.                                  *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    OPERATOR CONTROL AVT, OPERATOR CONTROL ELEMENT, STOPLINE REQUEST *
*    ELEMENT, DEB, DCB, LCB, QCB, TERMNAME TABLE, TERMINAL ENTRY AND  *
*    AVT.                                                             *
*                                                                     *
* ATTRIBUTES --                                                       *
*    SERIALLY REUSABLE, REFRESHABLE, ENABLED, TRANSIENT,              *
*    SUPERVISOR MODE.                                                 *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS            *
*    EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING         *
*    HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER              *
*    CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE         *
*    FOR THE NEW DEFINITION.                                          *
*                                                                     *
* NOTES --                                                            *
*    COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS          *
*    PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                      *
*                                                                     *
*    THIS IS A MODULE RE-WRITE OF IEDQCV AS OF OS/TCAM LEVEL II       *
***********************************************************************
         EJECT
         SPACE
*                                 E Q U A T E S
         SPACE
RZERO    EQU   0                        PARAMETER REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
ROPCAVT  EQU   2                        POINTER TO OP CONTROL AVT
ROPCE    EQU   3                        POINTER TO CURRENT OPCE
RAVT     EQU   4                        TCAM AVT ADDRESS
RWORKA   EQU   5                        POINTER TO RESIDENT WORKAREA
RRLN     EQU   5                        RELATIVE LINE NUMBER
RSCB     EQU   6                        SCB BASE
RLCB     EQU   6                        LCB ADDRESS
RTERM    EQU   6                        TERM ENTRY BASE
RTNT     EQU   6                        TERM NAME TABLE ADDR
RUCB     EQU   6                        UCB ADDRESS
RDCB     EQU   7                        DCB BASE
RWORK    EQU   8                        WORK REGISTER
RWORK1   EQU   9                        WORK REGISTER
RPREFIX  EQU   9                        BUFFER ADDRESS
RDEB     EQU   10                       DEB BASE
RQCB     EQU   11                       QCB ADDRESS
RUNIT    EQU   11                       UNIT ADDRESS
RBASE    EQU   12                       BASE FOR MODULE
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RBRNCH   EQU   15                       SUBROUTINE BRANCH ENTRY POINT
         SPACE 2
EIGHT    EQU   8                        LENGTH OF EIGHT
NAMELEN  EQU   2                        NO. OF NAME CHANGE CHARACTERS
RLNOFF   EQU   9                        OFFSET TO RELATIVE LINE NO.
VARINAME EQU   3                        OFFSET TO CHANGE LOAD NAME
ZONE     EQU   X'F0'                    CHARACTER TO COVER CONVERT SIGN
DEBOFF   EQU   12                       OFFSET OF DEB IN UNIT TO HK
LCBOFF   EQU   16                       OFFSET OF LCB IN UNIT TO HK
RLN      EQU   22                                              @OY14092
ALLRLN   EQU   X'20'                    TO CHECK FOR RLN = ALL
FOUR     EQU   4                        TO CHECK STOP TERM COMMANDS
ICHNG    EQU   X'49'                    ICHNG OPT CODE
HALT     EQU   X'12'                    CHECK HALT IMMEDIATE IN SWITCH
IMMEDIAT EQU   X'80'                    INDICATE HALT IMMEDIATE TO HK
INDIC    EQU   8                        FLAG OFFSET
ONE      EQU   1                        DISPLACEMENT
THREE    EQU   3                        DISPLACEMENT
HKOFF    EQU   0                        OFFSET OF HK ADDRESS IN UNIT
LINK     EQU   0                        OFFSET TO LINK FIELD
PRIROFF  EQU   4                        OFFSET OF PRIORITY IN HK UNIT
ONESTOP  EQU   X'20'                    TO INDICATE THAT ONE LINE STOP
STOPONE  EQU   9                        OFFSET IN HK UNIT OF FLAG
NEXTRLN  EQU   1                        TO BUMP RLN BY ONE
TWO      EQU   2                        DISPLACEMENT
WORD     EQU   4                        FULL WORD
BADCMND  EQU   18                       INVALID COMMAND ERROR
FIRSTRLN EQU   1                        TO START INCREMENTING RLN
NOTCA    EQU   X'02'                    ENTRY IS NOT FROM CA
POST     EQU   X'0C'                    TO POST SVC 102
END      EQU   X'80'                    PARAMETER LIST
ERROR47  EQU   47                       ERROR -IN SYSTEM DELAY
ERROR25  EQU   25                       ERROR-LINE ALREADY STOPPED
ERROR13  EQU   13                       STOP REQUEST FOR SELF
XEF      EQU   X'EF'               RESET MASK FOR STOP TERM TSO@SA71514
X50      EQU   X'50'               STOPPING TSO TERM           @SA71514
X40      EQU   X'40'                    STOPLINE REQUEST FROM A MODULE
ERROR17  EQU   17                       ERROR-DCB NOT OPEN
GOODSTOP EQU   26                       SUCCESSFUL STOP MESSAGE NUMBER
TOTE     EQU   X'F1'                    TOTE HAS LCB FOR LINE
ERROR49  EQU   49                       ERROR-TOTE CONTROLS LINE
FFBYTE   EQU   X'FF'                    TO DESIGNATE END OF TABLE
ZERO     EQU   0                        DISPLACEMENT
STOPALL  EQU   X'01'                    ALL LINES TO BE STOPPED
MPPERR   EQU   X'40'                    ONE LINE IN ERROR
ERROR12  EQU   12                       TSO SESSION ON LINE
BITSOFF  EQU   X'FC'                    TO TURN OFF BITS
LCBBFRED EQU   X'1A'                    BFRD SCHEDULER VTO     @OX14763
UNITOFF  EQU   24                       OFFSET OF EXTRA UNIT   @OX14763
ERROR16  EQU   16                       STATION NOT FOUND      @OX14763
AD       EQU   7                        MASK                   @OX14763
TTCIN    EQU   X'14'                    OFFSET INTO HK ELEMENT @OY14092
TSCHK    EQU   X'20'                    CHECK FOR TSO          @OY14092
LOGOFF   EQU   X'10'                    ISSUE QTIP IF NEEDED   @OY14092
LINEOPCE EQU   X'10'                    TEST FOR STOPLINE
PRISTOP  EQU   X'E8'                    PRIORITY USED TO       @OY16355
*                                       POST LCB TO OPCOB      @OY16355
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         USING DEBNMSUB,RDEB            DEB ADDRESSABILITY
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY
         USING IEDQTNTD,RTNT            TNT ADDRESSABILITY
         USING IEDQPRF,RPREFIX          BUFFER ADDRESSABILITY
         USING IEDQTRM,RTERM            TERM ENTRY ADDRESSABILITY
         USING IEDQSCB,RSCB             SCB ADDRESSABILITY
         SPACE
         BALR  RBASE,ZERO               SET BASE REGISTER
         USING *,RBASE                  MODULE ADDRESSABILITY
BASE     EQU   *
IGCV110D IEDHJN  SKIPID
*  DETERMINE IF RETURNED LCB, REGULAR COMMAND, OR QUEUED COMMAND      *
         LR    ROPCAVT,RPARM            SET UP AVT ADDRESSING
         L     RAVT,OPCAVTPT            TCAM AVT ADDRESSING
         L     ROPCE,OPCCOPCE           GET CURRENT ELEMENT POINTER
         L     RWORKA,OPCWORK           GET WORKAREA ADDRESS
         BCT   RZERO,LCBRETUN           GO PROCESS RETURNED LCB
*              THE ELEMENT IS PROCESSED FROM HERE                     *
         LA    RPARM,FOUR               SET ERROR
         LA    RZERO,ERROR47            SET DELAY ERROR MESSAGE
         TM    AVTBIT1,AVTDLAYN         IN A SYSTEM DELAY
         BO    ERROR                    YES COMMAND INVALID
         CLI   OCOPTCDE,ICHNG          IS IT FOR ICHNG         @SA71947
         BE    UCBADD                  BRANCH YES              @SA71947
         SPACE
         SR    RPARM,RPARM              CLEAR FOR RLN
         TM    OCSWITCH,X40             STOPLINE REQUEST FROM A MODULE
         BO    VERIFY                   YES UCB ADDRESS
         SPACE
         MVC   OPCDOUBL(EIGHT),OCBACKUP PLACE INPUT FOR LOCATING DCB
UCBADD   EQU   *
         TM    OCFLAG,OCLINE            LINE NUMBER FOR STOP
         BO    VERIFY                   YES GET DCB,UCB ADDRESSES
         SPACE
         IC    RPARM,OCRLN              GET RELATIVE LINE NUMBER
         LTR   RPARM,RPARM              'ALL' SPECIFIED
         BNZ   VERIFY                   NO GET DCB ADDRESS
         SPACE
         LA    RPARM,FIRSTRLN           SET DUMMY RLN
         OI    OPCFLAG1,ALLRLN          INDICATE DUMMY RLN SET
VERIFY   EQU   *
         L     RBRNCH,OPCDCBLK          GET DCB LOOK-UP ADDRESS
         BALR  RRET,RBRNCH              LOOK UP DCB AND UCB ADDRESS
         SPACE
         B     PROCSTOP                 GOOD RETURN
         SPACE
         LTR   RDCB,RPARM               SET DCB BASE REG
         BZ    SET17                    BRANCH IF DDNAME NOT FOUND
         TM    DCBOFLGS,OPEN            WAS DCB OPEN
         BZ    SET17                    BRANCH IF NOT OPEN
         TM    DCBDSORG,LGB3705         IS THIS AN LGB?        @Y17XARW
         BO    ALLPROC                  YES, CHK RLN OF 'ALL'  @Y17XARW
         BCT   RZERO,SET17              BRANCH IF RLN TOO HIGH
ALLPROC  EQU   *                        CHECK RLN OF 'ALL'     @Y17XARW
         TM    OPCFLAG1,ALLRLN          RLN OF 'ALL'
         BO    PROCSTOP                 YES, ENTER LOOP
OPEN     EQU   X'10'                    OPEN BIT                 S22024
SET17    EQU   *
         LA    RPARM,FOUR               ERROR RETURN CODE
         LA    RZERO,ERROR17            DCB NOT OPEN
         B     ERROR                    NO DD,INVALID RLN,DCB NOT OPEN
         SPACE
PROCSTOP EQU   *
         STH   RZERO,OCX1A(ROPCE)       SET UP UCB ADDRESS TO COMPARE
         SR    RRLN,RRLN                INIT REG
         IC    RRLN,OCRLN               SET UP RELATIVE LINE NUMBER
         LR    RDCB,RPARM               DCB ADDRESS
LGB3705  EQU   X'80'                    DATA SET ORG FOR AN LGB  S22024
         TM    DCBDSORG,LGB3705         IS IT AN LGB             S22024
         BZ    NOTANLGB                 BRANCH NO                S22024
         TM    OPCFLAG1,ALLRLN          RLN EQUAL ALL            S22024
         BZ    NOCLEAR                  BRANCH NO                S22024
         MVI   OCRLN,AVTEZERO           CLEAR RLN                S22024
NOCLEAR  EQU   *                                                 S22024
         MVI   OCALSTAT,AVTEZERO        CLEAR FLAG BYTE        @YM08548
         MVC   OPCLDNME+VARINAME(NAMELEN),RNSTPLNE  SET UP RN ID S22024
         B     XCTL                     GO TO RN STOPLINE        S22024
NOTANLGB EQU   *                                                 S22024
         L     RUNIT,OCUNIT             GET SAVED UNIT ADDRESS @OY14092
         TM    OCUNIT+THREE,TWO         IS UNIT THERE          @OY14092
         BNZ   GETUNT                   UNIT NOT THERE         @OZ29091
         TM    OCSWITCH,X40             STRTTRM,STOPTRM,ICHNG? @OZ29091
         BZ    GOTUNIT                  NO                     @OZ29091
         MVC   OCLINK+ONE(THREE),OPCWAITL+ONE                  @OZ29091
*                                   QUEUE OPCE ON TOP OF WAITL @OZ29091
         STCM  ROPCE,AD,OPCWAITL+ONE                           @OZ29091
         B     CALL00                                          @OZ29091
GETUNT   EQU   *                                               @OZ29091
         LA    RPARM,ONE                NUMBER OF UNITS TO OBTAIN
         L     RBRNCH,OPCGETBF          BUFFER REQUEST
         BALR  RRET,RBRNCH              OBTAIN NEW UNIT
         ST    RPARM,OCUNIT             SAVE UNIT ADDRESS
         LR    RUNIT,RPARM              SET UP UNIT REGISTER
PLCBFLG  EQU   X'20'                    OFFSET TO PLCB INDICATOR S22024
GOTUNIT  EQU   *                                               @OY14092
         MVI   PLCBFLG(RUNIT),OCFOXES   INDICATE NOT PLCB        S22024
         L     RDEB,DCBDEBAD            DEB ADDRESS
         TM    OPCFLAG1,ALLRLN          RLN OF 'ALL'
         BNO   CHEKLAST                 NO CONTINUE
         SPACE
         MVI   OCRLN,AVTEZERO           RESET RLN TO ALL INDICATION
         LA    RRLN,NEXTRLN             INCREMENT RLN
CHEKLAST EQU   *
         STC   RRLN,RLN(RUNIT)          SAVE LAST RLN
         LR    RWORK,RRLN               SET UP ALTERNATE REGISTER
         SLL   RWORK,TWO                MULTIPLY BY FOUR
         L     RUCB,DEBUCBAD-FOUR-DEBNMSUB(RWORK,RDEB) UCB ADDRESS
         LA    RUCB,AVTEZERO(RUCB)      CLEAR HIGH BYTE
         LTR   RUCB,RUCB                OPENED DD DUMMY
         BZ    CHEKALL                  YES REJECT
         SPACE
*              GET CORRECT LCB ADDRESS                                *
         LR    RWORK,RRLN               RESET RLN
         L     RLCB,DCBIOBAD            IOB ADDRESS
         SR    RWORK1,RWORK1            CLEAR REG
         IC    RWORK1,DCBEIOBX          GET IOB LENGTH
         MR    RWORK,RWORK              GET IOB INDEX
         AR    RLCB,RWORK1              ADD RESULT TO IOB ADDRESS
         LA    RWORK,LCBFLAG1-IEDQLCB   GET LENGTH OF IOB
         SR    RLCB,RWORK               START OF LCB
         ST    RDEB,DEBOFF(RUNIT)       SAVE DEB ADDRESS
         LA    RLCB,AVTEZERO(RLCB)      CLEAR HIGH BYTE
         ST    RLCB,LCBOFF(RUNIT)       SAVE LCB ADDR
PROCMND  EQU   *
         CLI   LCBCPA-IEDQLCB(RLCB),TOTE LCB UNDER TOTE CONTROL
         BNE   NOTOTE                   NO
         SPACE
         LA    RPARM,FOUR               SET ERROR RETURN CODE
         LA    RZERO,ERROR49            SET ERROR CODE FOR TOTE CONTROL
         CLI   AVTOLTST,AVTEZERO        TOTE ACTIVE?
         BE    ERROR                    NO, ERROR
         SPACE
NOTOTE   EQU   *
         TM    OCFLAG,OCCONSOL+OCTOTE+OCMPP CONSOLE,TOTE,MPP INPUT
         BNZ   NOTTERM                  NOT REQUEST FROM TERMINAL
         TM    OCSWITCH,FOUR+EIGHT      START/STOPTERM REQUEST @OY14483
         BNZ   NOTTERM                  YES,BRANCH             @OY14483
         SPACE
         L     RWORK,OCMODNME           PICK UP LCB ADDRESS
         LA    RWORK,AVTEZERO(RWORK)    CLEAR HI BYTE
         LA    RLCB,0(RLCB)             CLEAR HIGH BYTE
         CR    RLCB,RWORK               IS REQUESTED REQUESTOR
         LA    RPARM,FOUR               SET ERROR RETURN CODE
         LA    RZERO,ERROR13            STOP REQUEST FOR SELF
         BE    ERROR                    YES REJECT REQUEST
         SPACE
NOTTERM  EQU   *
         MVI   INDIC(RUNIT),AVTEZERO    SET SINGLE INDICATOR
         CLI   OCOPTCDE,ICHNG           IS THIS FOR ICHNG
         BNE   AQCTL                    NO CONTINUE
         SPACE
         TM    OPCSPEC,OPCRSTRT         RESTART SITUATION
         BO    RESTART                  YES
         SPACE
AQCTL    EQU   *
         CLI   OCSWITCH,HALT            HALT LINE
         BNE   ENQUEUE                  NO
         SPACE
         OI    INDIC(RUNIT),IMMEDIAT    TURN ON HALT LINE INDICATOR
ENQUEUE  EQU   *
         LA    RWORK,OPCWAITL           WAIT LIST
LOCATEND EQU   *
         L     RWORK1,AVTEZERO(RWORK)   GET NEXT ELEMENT
         LA    RWORK1,AVTEZERO(RWORK1)  CLEAR HIGH BYTE
         LTR   RWORK1,RWORK1            IS THIS AN ELEMENT
         BZ    ENDQUEUE                 GO PUT ELEMENT ON WAIT LIST
         SPACE
         CLC   OCX1A(TWO,ROPCE),OCX1A(RWORK1)  HAS REQUEST BEEN MADE
         BE    ATTACH                   YES
         SPACE
         LR    RWORK,RWORK1             NEW ELEMENT BECOMES OLD ELEMENT
         B     LOCATEND                 FIND END OF LIST
         SPACE
*              SET UP ELEMENT TO GO TO HK TO PERFORM STOPLINE         *
ENDQUEUE EQU   *
         CLI   LCBSTAT1-IEDQLCB(RLCB),AVTEZERO LINE ALREADY STOPPED
         BNE   PERFOSTP                 NO PERFORM STOPLINE
         TM    LCBSTAT2-IEDQLCB(RLCB),LCBMSGNN  CHECK MSGEN    @SA69627
         BO    PERFOSTP                 STOP THE LINE          @SA69627
         SPACE
         NI    OCSWITCH,OCFOXES-X40     TURN OFF START LINE BIT
         CLI   LCBPRI-IEDQLCB(RLCB),PRISTOP  LCB ON AVTOPCOB   @OY16355
         BNE   CHEKALL                  NO, KEEP PROCESSING    @OY16355
         STCM  ROPCE,AD,OCLINK-IEDQOPCE+1(RWORK)               @OY16355
         XC    OCLINK+ONE(THREE),OCLINK+ONE  ZERO LINK FIELD   @OY16355
*        PUT THIS REQUEST ON WAITL BECAUSE STOPLINE HAS        @OY16355
*        ALREADY BEEN REQUESTED AND LCB IS MARKED STOPPED      @OY16355
*        AND SHOULD BE ON AVTOPCOB.                            @OY16355
         B     LCBPOSTD                                        @OY16355
         SPACE
PERFOSTP EQU   *
         TM    LCBSTAT1-IEDQLCB(RLCB),LCBSENDN+LCBRECVN         SA63024
*                                       NO ENDING STATUS        SA63024
*                                       RECEIVED BY LINE OPEN   SA63024
         BNO   NOTBOTH                  BRANCH IF NO            SA63024
         OI    INDIC(RUNIT),IMMEDIAT    TURN ON HALT INDICATOR @YA08784
NOTBOTH  EQU   *                                                SA63024
         ST    ROPCE,OCLINK-IEDQOPCE(RWORK)  ADD ELEMENT TO WAIT LIST
         TM    OCSWITCH,FOUR+EIGHT      STOP OR START TERM     @OY14092
         BZ    NOTNT                    NO, BRANCH             @OY14092
         MVC   TTCIN(TWO,RUNIT),OCTRMTBL SET TNT OFFSET        @OY14092
NOTNT    EQU   *                                               @OY14092
         NC    OCWTG(TWO),OCWTG         CALLER ID SPECIFIED    @OY14092
         BNZ   OKWTG                    YES, BRANCH            @XM05684
         MVC   OCWTG(TWO),STOPLINE      RETURN TO THIS MODULE  @OY14092
         LA    RZERO,ONE                SET UP RETURN PARM     @OY14092
         STH   RZERO,OCPARM             SAVE                   @OY14092
OKWTG    EQU   *                                               @OY14092
         OI    INDIC(RUNIT),TSCHK       SET FOR TSO CHECK      @OY14092
         TM    OCSWITCH,HALT            STOPLINE,I             @OY14092
         BNO   NOTQUCK                  NO, BRANCH             @OY14092
         XI    INDIC(RUNIT),LOGOFF+TSCHK FORCE LOGOFF          @OY14092
NOTQUCK  EQU   *                                               @OY14092
         XC    OCLINK+ONE(THREE),OCLINK+ONE  ZEROES LINK FIELD
         CLI   LCBRSKEY-IEDQLCB(RLCB),LCBBFRED  LCB FOR        @OX14763
*                                        BUFFERED TERMS        @OX14763
         BNE   GOSTOP                   NO, DO STOPLINE        @OX14763
         TM    OCSWITCH,X40             STOP TERM OR ICHNG     @OX14763
         BZ    GOSTOP                   NO, DO STOPLINE        @OX14763
         CLI   OCOPTCDE,OCICHNG         ICHNG FUNCTION         @OX14763
         BNE   GETUNIT                  NO, GET EXTRA UNIT     @OX14763
         NC    OCMODNME(FOUR),OCMODNME  ICHNG ACTIVATE         @OX14763
         BNZ   GOSTOP                   NO, DO STOPLINE        @OX14763
GETUNIT  EQU   *                                               @OX14763
         LA    RPARM,ONE                NUMBER OF UNITS        @OX14763
         L     RBRNCH,OPCGETBF          BUFFER REQUEST ROUTINE @OX14763
         BALR  RRET,RBRNCH              GET NEW UNIT           @OX14763
         ST    RPARM,UNITOFF(RUNIT)     SAVE EXTRA UNIT ADDR   @OX14763
         MVC   OCOPTCDE-IEDQOPCE(TWO,RPARM),OCOPTCDE SAVE      @OX14763
*                                         ACTION CODES         @OX14763
         CLI   OCOPTCDE,OCICHNG         ICHNG ACTIVATE         @OX14763
         BE    GOSTOP                   YES, DO STOPLINE       @OX14763
         LR    RWORK,RPARM              SAVE UNIT ADDR         @OX14763
         MVC   OPCDOUBL(EIGHT),OCBACKUP SET FOR TERMNAME LKUP  @OX14763
         L     RBRNCH,OPCTOFLK          TERM LOOKUP CODE       @OX14763
         BALR  RRET,RBRNCH              LINK TO ROUTINE        @OX14763
         B     SETTRM                   GOOD RETURN            @OX14763
         LA    RZERO,ERROR16                                   @OX14763
         STCM  RWORK,AD,OCUNIT+ONE-IEDQOPCE(RWORK)             @OX14763
*                                       LINK UNITS TO FREE     @OX14763
         B     ERROR                                           @OX14763
SETTRM   EQU   *                                               @OX14763
         ST    RPARM,OCDEST-IEDQOPCE(RWORK) SAVE TRM ENTRY     @OX14763
*                                          ADDR FOR IEDQHK     @OX14763
GOSTOP   EQU   *                                               @OX14763
         L     RWORK,AVTHK              GET STOPLINE SUBTASK ADDRES
         ST    RWORK,HKOFF(RUNIT)       PUT IN ELEMENT
         SR    RWORK,RWORK              CLEAR LINK FIELD IF NECCESSARY
         MVI   PRIROFF(RUNIT),PRIOPCTL  SET PRIORITY
         LR    RWORK,RUNIT              TO SET UP SVC 102 PARM LIST
         ST    RWORK,OPCAQCTL           PUT IN SVC 102 PARM LIST
         ST    RWORK,OPCAQCTL+WORD      PUT IN SVC 102 PARM LIST
         MVI   OPCAQCTL,POST            STOP LINE REQUEST TO MPC
         MVI   OPCAQCTL+FOUR,END        END OF PARM LIST
         LA    RPARM,OPCAQCTL           GET ADDRESS OF LIST
         SPACE
         AQCTL                          POST STOP REQUEST
         SPACE 2
CALL00   EQU   *                                               @OY14092
         MVC   OPCLDNME+VARINAME(NAMELEN),FIRSTLD
         LA    RZERO,NOTCA              SET UP NOT FROM CA INDICATOR
         B     XCTL                     XCTL TO 00
         SPACE
*              THIS CODE PROCESSES A RETURNED LCB                     *
LCBRETUN EQU   *
         BCT   RZERO,NOTVARY            CHECK MULTIPLE USE OF RESOURCE
         SPACE
         LR    RLCB,ROPCE               STOPPED LCB RETURN
         MVI   LCBPRI-IEDQLCB(RLCB),PRILNFRE  MARK FREE        @OY16355
         LA    RWORK,OPCWAITL           WAIT LIST
LOCATREQ EQU   *
         L     ROPCE,AVTEZERO(RWORK)    NEXT ELEMENT ON WAIT LIST
         LA    ROPCE,0(,ROPCE)          CLEAR HI ORDER BYTE    @OY14092
         LTR   ROPCE,ROPCE              QUEUE ELEMENT          @OY14092
         BZ    CALL00                   BRANCH IF NO           @OY14092
         NI    OCUNIT-IEDQOPCE+THREE(ROPCE),BITSOFF  TURN OFF LOW BITS
         L     RUNIT,OCUNIT-IEDQOPCE(ROPCE)  OBTAIN LCB
         LA    RUNIT,AVTEZERO(RUNIT)    CLEAR HIGH BYTE
         ST    ROPCE,OPCCOPCE           SET UP ELEMENT         @OS77381
         LA    RZERO,ERROR12            SET POSSIBLE TSO ERROR
         CR    RUNIT,RLCB               CHECK FOR ELEMENT PASSED
         BNE   LCBCHECK                 NO
         SPACE
         MVC   ONE(THREE,RWORK),ONE(ROPCE) DEQUEUE THE ELEMENT
         B     ERROR                    GO TO ERROR
         SPACE
LCBCHECK EQU   *
         SPACE
         C     RLCB,LCBOFF(RUNIT)       CHECK FOR LCB IN WAIT LIST
         BE    DEQUEUE                  LINE STOPPED
         LR    RWORK1,ROPCE            SET UP FOR LOOP THROUGH @OY14092
*                                       ATTACHED UNITS         @OY14092
CKATCH   LR    RWORKA,RWORK1            SAVE LAST ATTACHED     @OY14092
         TM    OCFLAG-IEDQOPCE(RWORK1),OCATTACH ANY ATTACHED?  @OY14092
         BZ    NXTLINK                  NO, GET NEXT LINKED    @OY14092
         L     RWORK1,OCELEM-IEDQOPCE(RWORKA) GET ATTACHED UNIT@OY14092
         L     RUNIT,OCUNIT-IEDQOPCE(RWORK1)  GET UNIT ADDRESS @OY14092
         N     RUNIT,CLEAR              CLEAR EXTRANEOUS INFO  @OY14092
         BZ    CKATCH                   NO UNIT,CHECK NEXT     @OY14092
         CR    RUNIT,RLCB               UNIT RETURNED          @OY14092
         BNE   CKLCB                    NO CHECK FOR LCB       @OY14092
         LR    ROPCE,RWORK1             SET AS CURRENT         @OY14092
         MVC   OCELEM-IEDQOPCE(4,RWORKA),OCELEM  DELINK        @OY16079
         TM    OCFLAG,OCATTACH          ATTACH FLAG ON         @OY16079
         BO    RESET                    YES, BRANCH            @OY16079
         NI    OCFLAG-IEDQOPCE(RWORKA),AVTEFF-OCATTACH         @OY16079
*                                       CLEAR ATTACH BIT       @OY16079
RESET    EQU   *
         NI    OCFLAG,AVTEFF-OCATTACH   CLEAR ATTACH BIT       @OY16079
         ST    ROPCE,OPCCOPCE           SET AS CURRENT         @OY14092
         TM    OCSWITCH,LINEOPCE        STOPLINE REQUEST       @OY14092
         BZ    ERROR                    YES BRANCH             @OY14092
         OI    OCSWITCH,X40             SET TO START LINE      @OY14092
         B     ERROR                    SEND ERROR MSG         @OY14092
CKLCB    C     RLCB,LCBOFF(RUNIT)       CHECK LCB IN WAITLIST  @OY14092
         BNE   CKATCH                   NO, GET NEXT ATTACHED  @OY14092
         LR    ROPCE,RWORK1             SET AS CURRENT         @OY14092
         MVC   OCELEM-IEDQOPCE(4,RWORKA),OCELEM  DELINK        @OY16079
         TM    OCFLAG,OCATTACH          ATTACH FLAG ON         @OY16079
         BO    RESET1                   YES, BRANCH            @OY16079
         NI    OCFLAG-IEDQOPCE(RWORKA),AVTEFF-OCATTACH         @OY16079
*                                       CLEAR ATTACH BIT       @OY16079
RESET1   EQU   *                                               @OY16079
         NI    OCFLAG,AVTEFF-OCATTACH   CLEAR ATTACH BIT       @OY16079
         ST    ROPCE,OPCCOPCE           SET AS CURRENT         @OY16079
         TM    OCSWITCH,LINEOPCE        STOPLINE CMD           @OY14092
         BZ    STOP1                    YES, BRANCH            @OY14092
         OI    OCSWITCH,X40             SET TO STARTLINE       @OY14092
         B     STOP1                    RETURN WITH STOP CMD   @OY14092
NXTLINK  EQU   *                                               @OY14092
         SPACE
         LR    RWORK,ROPCE              NEW ELEMENT BECOMES OLD ELEMENT
         B     LOCATREQ                 CHECK NEXT LCB IN WAIT LIST
* IS ONE LINE OR ALL LINES STOPPED?
         SPACE
DEQUEUE  EQU   *
         MVC   ONE(THREE,RWORK),ONE(ROPCE)   DEQUEUE THE ELEMENT
STOP1    EQU   *                                               @OY14092
         OI    STOPONE(RUNIT),ONESTOP   INDICATE ONE LINE STOPPED
CHEKALL  EQU   *
         CLI   OCRLN,AVTEZERO           ARE ALL LINES TO BE STOPPED
         BNE   CHEKSTOP                 NO
         SPACE
         SR    RRLN,RRLN                CLEAR REG
         IC    RRLN,RLN(RUNIT)          GET LAST RLN
         LA    RRLN,NEXTRLN(RRLN)
         LR    RPARM,RRLN               RELATIVE LINE NUMBER
         MVC   OPCDOUBL(EIGHT),OCBACKUP PLACE INPUT FOR LOCATING DCB
         L     RBRNCH,OPCDCBLK          DCB LOOKUP ADDRESS
         BALR  RRET,RBRNCH              LOOK UP DCB AND UCB ADDRESS
         SPACE
         B     STOPNEXT                 GOOD RETURN CONTINUE
         MVI   OCRLN,AVTEZERO           RESET 'ALL' INDICATION
         BCT   RZERO,CHEKSTOP           OPEN DD DUMMY?
         STC   RRLN,RLN(RUNIT)          SAVE LAST RLN
         B     CHEKALL                  YES,GO CHECK NEXT RLN
STOPNEXT EQU   *
         STH   RZERO,OCX1A(ROPCE)       SET UP UCB ADDRESS FOR COMPARE
         MVI   OCRLN,AVTEZERO           RESET 'ALL' INDICATION
         LR    RDCB,RPARM               DCB ADDRESS
         L     RDEB,DCBDEBAD            DEB ADDRESS
         B     CHEKLAST                 SEE IF LAST LINE
         SPACE
CHEKSTOP EQU   *
         ST    ROPCE,OPCCOPCE           SET UP CURRENT ELEMENT POINTER
         SR    RPARM,RPARM              SET ZERO RET CODE
         TM    OCFLAG,OCTOTE            INPUT FROM TOTE?
         BNO   NOTTOTE                  NO, BYPASS SET FOR TOTE
         MVI   LCBCPA-IEDQLCB(RLCB),TOTE SET LCB FOR TOTE CONTROL
         LA    RPARM,EIGHT              SET ERROR RETURN CODE
NOTTOTE  EQU   *
         MVI   OCUNIT+THREE-IEDQOPCE(RUNIT),THREE              @OY14092
*                                       SET INVALID TIC        @OY14092
         LA    RZERO,ERROR25            LINES ALREADY STOPPED
         CLI   OCPARM+ONE,THREE         WAS ENTRY BECAUSE OF ATTACHED
         BE    SETFORHK                 YES
         SPACE
         TM    STOPONE(RUNIT),ONESTOP   HAS ONE LINE BEEN STOPPED
         BNO   ERROR                    ALL STOPPED
         SPACE
SETFORHK EQU   *
         SR    RPARM,RPARM              SET GOOD RETURN
         LH    RZERO,OCPARM             SET RETURN PARAMETER
         TM    OCUNIT+THREE,TWO         UNIT USED FOR STOPLN   @OX14763
         BO    NOUNIT                   NO, NONE TO RETURN     @OX14763
         L     RWORK,OCUNIT             YES, GET UNIT ADDR
         OC    UNITOFF(FOUR,RWORK),UNITOFF(RWORK) UNIT FOR BFRD@OX14763
         BZ    NOUNIT                   NO, NONE TO RETURN     @OX14763
         MVC   OCUNIT+ONE-IEDQOPCE(THREE,RWORK),UNITOFF+ONE(RWORK)
*                                       SET TO RETURN UNIT     @OX14763
NOUNIT   EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),OCWTG  SET RETURN NAME
         OC    OCWTG(TWO),OCWTG         RETURN ASKED?
         BZ    COMPLETE                 NO,CONTINUE            @OY14092
         CLC   OCWTG(TWO),STOPLINE      THIS MODULE            @OY14092
         BNE   XCTL                     NO, CONTINUE           @OY14092
         SPACE
COMPLETE EQU   *                                               @OY14092
         MVC   OPCLDNME+VARINAME(NAMELEN),CHECKPT  SET CHECKPOINT NAME
         MVC   OCWTG(NAMELEN),WRTMSG    WTG TO MESSAGE MODULE
         MVI   OCPARM,AVTEZERO          CLEAR FIRST BYTE
         MVI   OCPARM+ONE,GOODSTOP      MSG NO FOR SUCCESSFUL STOP
         TM    OCFLAG,OCTOTE+OCMPP      TOTE OR MPP
         BZ    XCTL                     NO
         TM    OCFLAG,OCTOTE            IS REQUEST FROM TOTE
         BZ    MPPRETUN                 NO
         SPACE
         MVI   LCBCPA-IEDQLCB(RLCB),TOTE  LCB IS UNDER TOTE CONTROL
MPPRETUN EQU   *
         SR    RZERO,RZERO              CLEAR REGISTER
         SPACE
RESTART  EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),WRITE   SET MODULE NAME
XCTL     EQU   *
         NI    OPCFLAG1,AVTEZERO        CLEAR FLAGS
         LR    RPARM,ROPCAVT            PASS AVT ADDRESS
         XCTL  SF=(E,OPCXCTL)           CALL NEXT MODULE
         SPACE
ERROR    EQU   *
         TM    OCFLAG,OCTOTE+OCMPP      TOTE OR MPP
         BZ    ERXCTL                   NO
         SPACE
         LR    RZERO,RPARM              SET UP FOR ERROR RETURN CODE
         B     RESTART                  TOTE OR MPP RETURN
         SPACE
ERXCTL   EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),XCTL03 SET MODULE NAME
         B     XCTL                     GO TO MODULE
         SPACE
*              THIS CODE ATTACHES NEW REQUEST ELEMENT TO OLD REQUEST  *
*              AFTER DETERMINING MULTIPLE REQUEST ON RESOURCE         *
ATTACH   EQU   *
         TM    OCFLAG-IEDQOPCE(RWORK1),OCATTACH ATTACHED ELEMENT?
         BNO   ATTACHIT                 NO, QUEUE IT UP
         SPACE
         L     RWORK1,FOUR(RWORK1)      GET NEXT ELEMENT IN CHAIN
         B     ATTACH                   SEE IF IT CAN BE ATTACHED
         SPACE
ATTACHIT EQU   *
         ST    ROPCE,FOUR(RWORK1)       QUEUE UP NEW ELEMENT
         OI    OCFLAG-IEDQOPCE(RWORK1),OCATTACH  UNIT IS ATTACHED
LCBPOSTD EQU   *                                               @OY16355
         MVC   OCWTG(TWO),STOPLINE      SET RETURN MODULE FOR 00
         LA    RZERO,THREE              SET UP PARAMETERS
         STH   RZERO,OCPARM             TO RETURN TO THIS MODULE
         LA    RZERO,TWO                INDICATES ENTRY TO 00 NOT CA
         MVC   OPCLDNME+VARINAME(NAMELEN),FIRSTLD    SET UP FOR 00
         B     XCTL                     GO TO CORRECT MODULE
         SPACE
*              THIS SETS UP ELEMENT FROM OTHER SOURCE TO STOP LINE    *
NOTVARY  EQU   *
         L     RUNIT,OCUNIT             RELOAD SAVED UNIT FOR HK
         L     RLCB,LCBOFF(RUNIT)       RELOAD LCB ADDRESS
         LA    RBRNCH,MULTIRES          PICK UP SCAN TABLE
TBLLOOP  EQU   *
         CLI   ZERO(RBRNCH),FFBYTE      END OF TABLE
         BE    EROPT                    YES
         SPACE
         CLI   OCOPTCDE,OCVARY          IS THIS A VARY COMMAND
         BE    CHEKVARY                 YES
         SPACE
         CLC   OCOPTCDE,ZERO(RBRNCH)    GOOD COMMAND?
         MVC   OCWTG(TWO),ONE(RBRNCH)   SET FOR NEXT MODULE TO GET CAL
         BE    PROCMND                  YES
         SPACE
         LA    RBRNCH,THREE(RBRNCH)     INCREMENT TO NEXT ENTRY
         B     TBLLOOP                  GET NEXT COMMAND
         SPACE
CHEKVARY EQU   *
         TM    OCSWITCH,FOUR            STOP TERMINAL
         MVC   OCWTG(TWO),STOPMOD       GET STOP TERM MOD NAME
         BO    PROCMND                  YES IT IS STOP TERM
         SPACE
         TM    OCSWITCH,EIGHT           IS IT START TERM
         MVC   OCWTG(TWO),STOPMOD       GET STOP TERM MOD NAME @OZ28375
         BO    PROCMND                  YES IT IS START TERMINAL
         SPACE
EROPT    EQU   *
         LA    RZERO,BADCMND            SET ERROR CODE
         B     ERROR                    GO PUT OUT ERROR MESSAGE
         EJECT                                                   S21903
ERRORTAB DS    0F
CLEAR    DC    X'00FFFFFC'              TO CLEAR EXTRA BITS    @OY14092
         SPACE 2
XCTL03   DC    C'03'                    ERROR MODULE NAME
CHECKPT  DC    C'06'                    CHECKPOINT MODULE NAME
WRITE    DC    C'07'                    OUTPUT WRITER NAME
WRTMSG   DC    C'09'                    HOLD, RELEASE, AND VARY MESSAGE
STOPMOD  DC    C'V2'                    STOP TERMINAL MODULE
STRTMOD  DC    C'V4'                    START TERMINAL MODULE
ALL      DC    C'ALL'                   RLN WAS ALL
ICHANGE  DC    C'I0'                    ICHNG
FIRSTLD  DC    C'00'                    FIRST LOAD OF OP CTL
STOPLINE DC    C'V1'                    OP CTL STOPLINE
RNSTPLNE DC    C'VG'                    3705 STOPLINE MOD ID     S22024
         SPACE 2
MULTIRES DC    X'49',C'I0'              ICHNG
         DC    X'45',C'V4'              STRT TERM (B)            S22024
         DC    X'46',C'V4'              STRT TERM (E)            S22024
         DC    X'48',C'V2'              STOP TERM (B)
         DC    X'47',C'V2'              STOP TERM (B)
         DC    X'FF'                    END OF TABLE
ZAPAREA  DC    50X'00'                  PATCH AREA             @Y17XARW
         SPACE 2
         TITLE 'IGCV110D - DSECTS REFERENCED BY THIS MODULE'
         EJECT
         TAVTD
         EJECT
         DCBD DSORG=TX
         EJECT
         TDEBD
         EJECT
         TDISPD
         EJECT
*    DELETE IEAQFX CALL - NOT REQUIRED IN VS
         EJECT
         TLCBD
         EJECT
         TOPCED
         TOPCAVTD
         EJECT
         TPRFD
         EJECT
         TPRIOR
         EJECT
         TQCBD
         EJECT
         TSCBD
         EJECT
         TTNTD
         EJECT
         TTRMD
         END
