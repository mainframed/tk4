         TITLE 'IGCV210D - OPERATOR CONTROL STOP TERMINAL MODULE'
IGCV210D CSECT
         SPACE 3
*CHANGE ACTIVITY=AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A-000000-999999                                               @Y16X5R0
*A000000-999999                                                  S99240
*A168700,425100-425800,519700                                   SA56606
*D418300-418600                                                 SA60316
*D466000-473000,478000,464000                                   SA60006
*A192000                                                        SA63000
*C424000                                                        SA63000
*C265000                                                        SA61772
*A265100-265900,281250                                          SA61772
*A477000                                                        SA67777
*A137400,273000                                                @SA71514
*D192300-192600,519800                                          SA67131
*A168700,411000,494000,518000                                  @SA64357
*A420000                                                       @SA74217
*A168800                                                       @XA09341
*C481000-492000                                                @XA09341
*C318100-318700 MOVED AFTER 332000                             @OX16689
*D465000                                                       @XA09341
*A488200,490600                                                @YA10700
*C211450-211820                                                @0Y14483
*A145000,510000                                                @OY14483
*C002050,211920,211820                                         @Y17XARR
*A519701                                                       @Y17XART
*D273200-273800                                                @OY14092
*C148000                                                       @OS77951
*A211676                                                       @OS77951
*A168000,266000                                                @OZ29091
***********************************************************************
*                                                                     *
* MODULE NAME=IGCV210D   (TCAM, OPCTL)                         @Y17XART
*                                                                     *
* DESCRIPTIVE NAME=OP CONTROL STOP TERMINAL MODULE                    *
*                                                                     *
* COPYRIGHT='NONE'                                                    *
*                                                                     *
*  STATUS:  CHANGE LEVEL 10                                    @Y17XART
*                                                                     *
* FUNCTION -- THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH  *
*    REQUEST THAT A SPECIFIED TERMINAL BE DEACTIVATED FOR ENTERING, OR*
*    DEACTIVATED FOR BOTH ENTERING AND ACCEPTING.  THE ROUTINE        *
*    SEARCHES THE TERMNAME TABLE SERIALLY FOR AN ENTRY MATCHING THAT  *
*    SPECIFIED IN THE COMMAND.  IF NO MATCH IS FOUND, AN ERROR MESSAGE*
*    NUMBER IS SET UP AND CONTROL IS TRANSFERRED TO THE ERROR MODULE. *
*    WHEN THE ENTRY IS FOUND IT IS TESTED FOR A PROCESS ENTRY(X'20'   *
*    IN TRMSTATE).  IF IT IS, SINCE PROCESS ENTRIES HAVE NO INVITATION*
*    LISTS, AN ERROR MESSAGE NUMBER IS SET UP AND CONTROL IS          *
*    TRANSFERRED TO THE ERROR MODULE.  THE QCB AT TRMDESTQ IS USED TO *
*    OBTAIN THE RLN (QCBRELLN) AND THE DCB (QCBDCBAD).  THE DCB IS    *
*    TESTED FOR OPEN STATUS, AND IF NOT AN ERROR MESSAGE NUMBER IS SET*
*    UP AND CONTROL IS TRANSFERRED TO THE ERROR MODULE.  THE DEB IS   *
*    ACCESSED FROM DCBDEBAD AND ITS UCB AT DEBUCBAD IS TESTED FOR     *
*    ZERO.  IF ZERO, THE LINE HAS BEEN OPENED DD DUMMY, SO THAT AN    *
*    ERROR MESSAGE NUMBER IS SET UP AND CONTROL IS TRANSFERRED TO THE *
*    ERROR MODULE.  THE LCB ADDRESS IS DETERMINED FROM DCBIOBAD AND   *
*    THE LCB TESTED FOR DIAL (LCBDIAL ON IN LCBSTAT2).  IF IT IS DIAL,*
*    AN ERROR MESSAGE NUMBER IS SET UP AND CONTROL IS TRANSFERRED TO  *
*    THE ERROR MODULE.  THE LINE IS TESTED FOR STOPPED.  IF NOT,      *
*    CONTROL IS TRANSFERRED TO STOPLINE TO STOP THE LINE.  THE X'40'  *
*    BIT IS TURNED ON IN OCSWITCH TO INDICATE THAT THE LINE SHOULD BE *
*    STARTED BACK AFTER DEACTIVATING THE TERMINAL.  THE LIST IS TESTED*
*    FOR ACTIVE STATUS.  IF ALL ENTRIES ARE INACTIVE, A RESPONSE
*    MESSAGE NUMBER IS SET UP AND CONTROL IS TRANSFERRED TO THE VARY  *
*    MESSAGE MODULE.  IF THE REQUEST IS TO STOP GENERAL POLL CONTROL  *
*    IS PASSED TO THE STOP GENERAL POLL MODULE (IGCV510D) IN ORDER TO *
*    STOP THE ENTRY.  EACH ENTRY IS TESTED TO SEE IF IT IS AN ENTRY   *
*    FOR THE TERMINAL.  IF IT IS AND IT IS ACTIVE , IT IS SWAPPED FROM*
*    THE ACTIVE TO THE INACTIVE SIDE OF THE LIST, THE ACTIVE COUNT IS *
*    DECREMENTED, AND A BIT IS SET TO INDICATE THAT THE LIST WAS      *
*    CHANGED.  WHEN ALL ENTRIES IN THE LIST HAVE BEEN EXAMINED, THE   *
*    BIT IS TESTED TO SEE IF THE LINE NEEDS TO BE RESTARTED.  IF SO,  *
*    THE LINE IS RESTARTED.  OTHERWISE, OR AFTER THE LINE IS          *
*    RESTARTED, THE MOVED BIT IS CHECKED TO SEE IF THE INVITATION LIST*
*    HAS BEEN CHANGED.  IF OFF, THE TERMINAL WAS ALREADY INACTIVE, SO *
*    THAT A RESPONSE MESSAGE IS SET UP TO INDICATE THE TERMINAL WAS   *
*    ALREADY STOPPED.  OTHERWISE, A RESPONSE MESSAGE IS SET UP TO     *
*    INDICATE THAT THE TERMINAL IS STOPPED.  THIS ROUTINE ALSO   S22024
*    DOES THE INITIAL PROCESSING FOR A START TERMINAL COMMAND.   S22024
*    IF THE COMMAND IS FOR A DEVICE OFF THE RN MODULE IGCV910D   S22024
*    IS XCTLED TO FROM IGCV210D.                                 S22024
*                                                                     *
* ENTRY POINTS --                                                     *
*    IGCV210D TO PROCESS VARY TERMINAL,OFFTP OPERATOR CONTROL         *
*    COMMANDS.                                                        *
*
*    PURPOSE = SEE FUNCTION                                    @Y17XART
*    LINKAGE = VIA XCTL                                        @Y17XART
*                                                                     *
* INPUT --                                                            *
*    REGISTER 1 - ADDRESS OF OPERATOR CONTROL AVT.  RELEVANT FIELDS   *
*    ARE OPCDOUBL.  THIS FIELD CONTAINS THE DDNAME OR THE UCB         *
*    ADDRESS.  ANOTHER FIELD IS OPCCOPCE WHICH IS THE THE ADDRESS OF
*    OF THE CURRENT CONTROL ELEMENT.  RELEVANT FIELDS ARE OCBACKUP.   *
*    THIS FIELD CONTAINS EITHER A DDNAME OR A UCB ADDRESS.  ANOTHER   *
*    RELEVANT FIELD IS OCSWITCH.  THIS INDICATES WHETHER TO STOP THE  *
*    TERMINAL FROM ENTERING OR STOP THE TERMINAL FROM ENTERING OR     *
*    STOP THE TERMINAL FROM BOTH ENTERING AND RECEIVING.  IF THE X'40'*
*    BIT IS ON THE LINE SHOULD BE RESTARTED AFTERDEACTIVATING THE     *
*    THE TERMINAL.  FOR TOTE OCOPTCDE CHECKED TO SEE IF TERMINAL      *
*    SHOULD BE BOTH DEACTIVATED AND HELD OR JUST DEACTIVATED.         *
*                                                                     *
* OUTPUT --                                                           *
*    REGISTER 0 - NUMBER OF APPROPRIATE ERROR MESSAGE                 *
*    REGISTER 1 - RETURN CODE.                                        *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         NONE.                                                       *
*
*     ROUTINES = TRMOFLOK                                      @Y17XART
*
*MODULE TYPE = PROCEDURE                                       @Y17XART
*    PROCESSOR = ASSEMBLER                                     @Y17XART
*    SIZE = SEE LISTING                                        @Y17XART
*                                                                     *
* EXITS,NORMAL --                                                     *
*    TO MODULE IGCV910D TO PROCESS COMMANDS FOR DEVICES OFF AN RNS22024
*         TO MODULE IGCV510D TO PROCESS GENERAL POLL REQUESTS.        *
*         TERMINAL WAS DEACTIVATED AS REQUESTED.  RESPONSE MESSAGE    *
*         NUMBER SET UP, X'00' RETURN CODE SET.                       *
*         TERMINAL WAS ALREADY DEACTIVATED.  RESPONSE MESSAGE NUMBER  *
*         SET UP, X'08' RETURN CODE SET.                              *
*                                                                     *
* EXITS,ERROR --                                                      *
*         TERMINAL COULD NOT BE FOUND OR IS A PROCESS ENTRY.  ERROR   *
*         MESSAGE NUMBER SET UP, X'04' RETURN CODE SET.               *
*         LINE IS NOT OPEN, OPEN DD DUMMY, OR DIAL LINE.  ERROR       *
*         MESSAGE NUMBER SET UP,X'04' RETURN CODE SET.                *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    OPERATOR CONTROL AVT, OPERATOR CONTROL ELEMENT, TERMNAME TABLE,  *
*    DCB, TERMINAL ENTRY, QCB, LCB, DEB.                              *
*                                                                     *
* ATTRIBUTES --                                                       *
*    SERIALLY REUSABLE, REFRESHABLE, ENABLED, TRANSIENT,              *
*    SUPERVISOR MODE.                                                 *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS            *
*    EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING         *
*    HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER              *
*    CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE         *
*    FOR THE NEW DEFINITION.                                          *
*                                                                     *
* NOTES --                                                            *
*    COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS          *
*    PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                      *
*
*    DEPENDENCIES = NONE                                       @Y17XART
*    RESTRICTIONS = NONE                                       @Y71XART
*    REGISTER CONVENTIONS = STANDARD OP CTL                    @Y17XART
*    PATCH LABEL = ZAPAREA                                     @Y17XART
*                                                                     *
*    THIS IS A MODULE RE-WRITE OF IEDQCO AS OF OS/TCAM LEVEL II       *
***********************************************************************
         EJECT
         SPACE
*                                 E Q U A T E S
         SPACE
RZERO    EQU   0                        PARAMETER REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
ROPCAVT  EQU   2                        POINTER TO OP CONTROL AVT
ROPCE    EQU   3                        POINTER TO CURRENT OPCE
RAVT     EQU   4                        TCAM AVT ADDRESS
RWORKA   EQU   5                        POINTER TO RESIDENT WORKAREA
RRLN     EQU   5                        RELATIVE LINE NUMBER
RLCB     EQU   5                        LCB BASE
RLCB1    EQU   6                        LCB BASE
RENTRY   EQU   6                        CURRENT ENTRY POINTER
RTERM    EQU   6                        TERM ENTRY BASE
RTNT     EQU   6                        TERM NAME TABLE ADDR
RUCB     EQU   6                        UCB ADDRESS
RDCB     EQU   7                        DCB BASE
ROFF     EQU   7                        OFFSET TO DDNAME, ADDR, RLN
RWORK    EQU   8                        WORK REGISTER
RWORK1   EQU   9                        WORK REGISTER
RPREFIX  EQU   9                        BUFFER ADDRESS
RLENGTH  EQU   10                       LENGTH OF INVITATION LIST ENTRY
RDEB     EQU   10                       DEB BASE
RLIST    EQU   11                       PTR TO INVITATION LIST
RQCB     EQU   11                       QCB ADDRESS
RBASE    EQU   12                       BASE FOR MODULE
RSAVE    EQU   13                       SAVE AREA FOR OP CTL     S22024
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RBRNCH   EQU   15                       SUBROUTINE BRANCH ENTRY POINT
         SPACE 2
EIGHT    EQU   8                        LENGTH OF EIGHT
NAMELEN  EQU   2                        NO. OF NAME CHANGE CHARACTERS
VARINAME EQU   3                        OFFSET TO CHANGE LOAD NAME
FOUR     EQU   4                        VALUE 4                  S22024
ONE      EQU   1                        VALUE ONE                S22024
ZERO     EQU   0                        ZERO
X80      EQU   X'80'                    HEX EIGHTY
X10      EQU   X'10'               STOPPING TSO TERM           @SA71514
X01      EQU   X'01'                    HEX ONE
THREE    EQU   3                        VALUE 3                  S22024
HKOFF    EQU   0                        OFFSET OF HK ADDRESS IN UNIT
TWO      EQU   2                        VALUE 2                  S22024
MVLEN    EQU   36                       MOVE LENGTH              S22024
ERROR25  EQU   25                       ERROR - ALREADY STOPPED
ERROR16  EQU   16                       ERROR -STATNAME NOT FOUND
ERROR90  EQU   90                       ERROR-NOT SINGLE ENTRY
ERROR46  EQU   46                       ERROR -OUTPUT ONLY STATION
ERROR13  EQU   13                       STOP REQUEST FOR SELF  @OY14483
LINOPN   EQU   X'C1'                    TO CHECK FOR OPEN DCB
STOPBOTH EQU   X'06'                    INDICATES DEACTIVATE AND HOLD
ERROR19  EQU   19                       ALREADY STARTED MSG    @OS77951
ANYMOVED EQU   X'01'                    TO CHECK IF ENTRIES WERE MOVED
EOT      EQU   X'80'                    TO CHECK EOT IN INVLIST
ENDACT   EQU   X'FE'                    END OF ACTIVE ENTRIES
PROC     EQU   X'20'                    PROCESS ENTRY
DIST     EQU   X'40'                        DISTRIBUTION ENTRY
ERROR91  EQU   91                       ERROR-LINE NOT OPEN
STOP     EQU   X'05'                    DEACTIVATE TERMINAL
ERROR88  EQU   88                       ERROR-DIAL TERMINAL
SEVEN    EQU   7                        VALUE 7                  S22024
STATSTOP EQU   26                       RESPONSE MESSAGE NUMBER
X40      EQU   X'40'                    TO INDICATE LINE NEEDS RESTART
BUSY     EQU   4                        QUEUE BUSY CONDITION
ON       EQU   1                        DISPLACEMENT
TR       EQU   3                        DISPLACEMENT
CVTPTR   EQU   16                       ADDR OF PTR TO CVT
STOPSNGL EQU   X'48'                    ONLY DEACTIVATE FOR TOTE
UCBNM    EQU   13                       OFFSET OF NAME IN UCB
GOODHOLD EQU   51                       SUCCESSFUL HOLD MESSAGE
CCODE    EQU   11                       TO INDICATE A BRANCH
LCBPRIOR EQU   X'E0'                    PRIORITY FOR LCB
PRISTOP  EQU   X'E8'                    LINE STOPPED PRTY      @OZ29091
CONCERR  EQU   156                      VALUE 156                S22024
MIDMSG   EQU   X'20'                    MID MSG CONDITION       SA56606
STRTBOTH EQU   X'0B'                    ONTP,B COMMAND         @SA64357
POST     EQU   X'0C'                    SVC 102 POST PRIORITY  @XA09341
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         USING DEBNMSUB,RDEB            DEB ADDRESSABILITY
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY
         USING IEDQTNTD,RTNT            TNT ADDRESSABILITY
         USING IEDQPRF,RPREFIX          BUFFER ADDRESSABILITY
         USING IEDQTRM,RTERM            TERM ENTRY ADDRESSABILITY
         SPACE
         BALR  RBASE,ZERO               SET BASE REGISTER
         USING *,RBASE                  MODULE ADDRESSABILITY
BASE     EQU   *
IGCV210D IEDHJN  SKIPID
*              INITIAL SETUP                                          *
         LR    ROPCAVT,RPARM            SET UP AVT ADDRESSING
         L     RAVT,OPCAVTPT            TCAM AVT ADDRESSING
         L     ROPCE,OPCCOPCE           GET CURRENT ELEMENT POINTER
         NI    OCFLAG,OCFOXES-OCLINE    TURN OFF LINE NO. BIT   SA63000
         MVC   OPCDOUBL(EIGHT),OCBACKUP SET UP TERMINAL NAME
         L     RBRNCH,OPCTOFLK          GO GET TERM ENTRY ADDRESS
         BALR  RRET,RBRNCH              LINK TO SUBROUTINE IN CA
         SPACE
         B     GOODRTN                  VALID TERMINAL ENTRY
         SPACE
         LA    RZERO,ERROR16            STATNAME NOT FOUND
         B     ERROR                    SET UP ERROR MESSAGE
         SPACE
GOODRTN  EQU   *
         LR    RTERM,RPARM              GET TERM ENTRY ADDRESS
         TM    TRMSTATE,TRMLOG          DISTRIBUTION, PROCESS    S22024
*                                       OR CASCADE ENTRY ?       S22024
         BNZ   NOTSING                  YES, ERROR               S22024
         TM    TRMDSORG,TRMLGB          ENTRY AN LGB ?           S22024
         BO    NOTSING                  YES,ERROR                S22024
         SPACE
LINE     EQU   *
         L     RQCB,TRMDESTQ-IEDQTRM-ONE(RTERM) GET QCB ADDRESS
         TM    TRMDEVFL-IEDQTRM+ONE(RTERM),TRMCONC   TERM OR CONC
         BZ    CHK3705                  NEITHER CONC OR CONC TERMS22024
         SPACE
         LA    RZERO,CONCERR            CONC ERROR MESSAGE NO    S22024
         TM    QCBDSFLG,QCBDRQQ         CONCENTRATOR?
         BZ    ERROR                                           @OY14483
CHK3705  EQU  *
         TM    OCFLAG,OCCONSOL+OCTOTE+OCMPP SOURCE OF REQUEST  @OY14483
         BNZ   NOTTERM                  BRANCH NOT TERMINAL    @OY14483
         MVC   OPCLDNME+VARINAME(NAMELEN),XCTL03 ERR MSG MOD   @OY14483
         LA    RZERO,ERROR13                                   @OY14483
         CLC   OCTRMTBL(TWO),OCDEST     IS THE TNT OF THE      @OY14483
*                                       TERMINAL TO BE STOPPED @OY14483
*                                       SAME AS TNT OF REQUEST @OY14483
*                                       TERMINAL ?             @OY14483
         BNE   NOTTERM                  NO,CONTINUE            @OY14483
         MVC   OCUNIT(FOUR),INVALTIC    SET UP END OF UNIT     @OY14483
         TM    OCSWITCH,EIGHT           START TERMINAL REQUEST @OS77951
         BZ    EXIT                     NO, GIVE MSG 013       @OS77951
         LA    RZERO,ERROR19            YES, GIVE MSG 019      @OS77951
         B     EXIT                     GET OUT, SEND MSG      @OY14483
NOTTERM  EQU   *                                               @OY14483
         MVC   OPCLDNME+VARINAME(NAMELEN),ACDEACTM 3705 MOD ID @OY14483
         TM    TRMSTATE,TRMPREF         3705 RESOURCE?         @Y17XARR
         BZ    TESTGP                   NO, GO TEST FOR GEN POLL S22024
         TM    TRMSTATE,TRMLINE         ENTRY A 3705 LINE ?      S22024
         BZ    EXIT                     NO, VALID TERMINAL ENTRY S22024
*                                       ON 3705:GOTO 3705 MODULE S22024
NOTSING  EQU   *                                                 S22024
         LA    RZERO,ERROR90            NOT-SINGLE-ENTRY MSG NO  S22024
         B     ERROR                    SET UP ERROR MSG         S22024
         SPACE
TESTGP   EQU   *                                                 S22024
         SR    RPARM,RPARM             CLEAR REGISTER            S22024
         IC    RPARM,TRMCHCIN-IEDQTRM(RTERM)  GET DCT INDEX      S22024
         BCTR  RPARM,ZERO               REDUCE FOR FIRST ENTRY   S22024
         MH    RPARM,AVTDCTLN           MULTIPLY BY LEN OF ENTR@Y17XARR
         A     RPARM,AVTCSTCS           ADD START ADDRESS        S22024
         TM    ONE(RPARM),X01           IS THIS GENERAL POLL     S22024
         BZ    CHKOPEN                  NO, BRANCH               S22024
         OI    OCSWITCH,X80             SET SWITCH               S22024
         SPACE 1
CHKOPEN  EQU   *                                                 S22024
         SR    RWORK,RWORK              CLEAR REG
         IC    RWORK,QCBRELLN           GET RELATIVE LINE NUMBER
         STC   RWORK,OCRLN              RESERVE RLN
         LR    RRLN,RWORK               SET TO WORK WITH
         L     RDCB,QCBDCBAD-ONE        GET DCB ADDRESS
         LA    RZERO,ERROR91            LINE-NOT-OPEN MSG NO     S22024
         CLI   DCBTIOT,LINOPN           LINE OPEN
         BNL   ERROR                    NO                       S22024
         SPACE
OPEN     EQU   *
         SLL   RRLN,TWO                 MULTIPLY BY FOUR
         L     RDEB,DCBDEBAD            ADDRESS OF DEB
         CLC   OCRLN(ONE),DEBNMEXT-DEBNMSUB(RDEB)  EXTENT HIGH ?
         BH    ERROR                    OPENED DD DUMMY          S22024
         SPACE
         L     RUCB,DEBUCBAD-FOUR-DEBNMSUB(RDEB,RRLN)  GET UCB ADDRESS
         LA    RUCB,AVTEZERO(RUCB)      CLEAR HIGH BYTE
         LTR   RUCB,RUCB                OPENED DD DUMMY
         BZ    ERROR                    YES                      S22024
         SPACE
CKOUT    EQU   *
         MVC   OPCDOUBL(THREE),UCBNM(RUCB) UCB NAME
         CLI   DEBOPATB-DEBNMSUB(RDEB),THREE   FOR OUTPUT ONLY
         BNE   FINDLCB                  NO
         SPACE
OUTONLY  EQU   *
         LA    RZERO,ERROR46                OUTPUT ONLY MESSAGE
         B     ERROR                    SET UP ERROR MESSAGE
         SPACE
FINDLCB  EQU   *
         L     RLCB1,DCBIOBAD           GET ADDRESS OF IOB
         SR    RWORK1,RWORK1            CLEAR
         IC    RWORK1,DCBEIOBX          INDEX TO IOB
         MR    RWORK,RWORK              INDEX TO PROPER IOB
         LA    RLCB1,AVTEZERO(RLCB1)    CLEAR HIGH BYTE
         AR    RLCB1,RWORK1             GET ADDRESS OF PROPER IOB
         LA    RWORK,LCBFLAG1-IEDQLCB   GET LCB INDEX
         SR    RLCB1,RWORK              GET LCB ADDRESS
CKDIAL   EQU   *
         LA    RZERO,ERROR88            DIAL-LINE MESSAGE NO.    S22024
         TM    LCBSTAT2-IEDQLCB(RLCB1),LCBDIAL DIAL LINE
         BNZ   ERROR                    YES                      S22024
         SPACE
         TM    OPCSPEC,OPCRSTRT         RESTART IN EFFECT
         BZ    NORSTRT                  NO, BRANCH              SA61772
         TM    OCSWITCH,OCX08           IS IT START TERMINAL    SA61772
         BO    STTERM                   YES, GO TO V4           SA61772
         TM    AVTBIT3,AVTSTAIN         S=I SPECIFIED           SA61772
         BO    CKSTOP                   YES, BRANCH             SA61772
         B     STATANAL                 DON'T UPDATE I-LIST     SA61772
*                                       FROM INCIDENT RECORDS   SA61772
*                                       ON RESTART IF S=I       SA61772
*                                       NOT SPECIFIED           SA61772
NORSTRT  EQU   *                                                SA61772
         SPACE
         CLI   LCBPRI-IEDQLCB(RLCB1),PRISTOP    LINE STOPPED   @OZ29091
         BE    STOPLINE                 YES                    @OZ29091
         CLI   LCBSTAT1-IEDQLCB(RLCB1),AVTEZERO  LINE STOPPED
         BE    INVLIST                  YES
         SPACE
*                            GO TO STOPLINE                           *
STOPLINE EQU   *
         LA    RZERO,ONE                SET FOR ENTRY TO STOPLINE
         MVC   OPCLDNME+VARINAME(NAMELEN),STOPLN FOR ENTRY TO V1
         OI    OCSWITCH,X40             START LINE BACK
         MVC   OCWTG(TWO),STPTERM       SET RETURN MODULE NAME
         B     EXIT                     GO PERFORM STOPLINE
         SPACE
         SPACE
*              DEACTIVATE TERMINAL FROM INVITATION LIST               *
INVLIST  EQU   *
CHKSTART TM    OCSWITCH,EIGHT           START TERMINAL COMMAND   S22024
         BZ    CKSTOP                   NO, STOP TERMINAL        S22024
STTERM   EQU   *                                                SA61772
         MVC   OPCLDNME+VARINAME(NAMELEN),STRTTERM  START TERM IDS22024
         STM   RLCB1,RDCB,ZERO(RSAVE)   SAVE REGS FOR V4         S22024
         B     EXIT                     GO TO V4                 S22024
         SPACE 1
CKSTOP   EQU   *
         LA    RLIST,DCBINVLI-FOUR      START OF LIST
         SR    RRLN,RRLN                CLEAR REGISTER
         IC    RRLN,OCRLN               RELATIVE LINE NUMBER
         MVI   OCRLN,AVTEZERO           CLEAR RLN FOR HOLD     @YM07436
         SLL   RRLN,TWO                 MULTIPLY RELATIVE LINE NUMBER
         L     RLIST,AVTEZERO(RRLN,RLIST)  LIST ADDRESS
         LA    RLIST,AVTEZERO(RLIST)    CLEAR HIGH BYTE
         LR    RLCB,RLCB1               SAVE ADDRESS OF LCB FOR LATER
         CLI   AVTEZERO(RLIST),AVTEZERO NO ENTRIES
         BE    OUTONLY                  TO SET UP ERROR MESSAGE
         SPACE
         LA    RPARM,EIGHT(RLIST)       PTR TO BEGINNING OF LIST
         ST    RPARM,LCBINVPT-IEDQLCB-ONE(RLCB) SAVE FOR LINE ACTIVATE
STOPTERM EQU   *
         SR    ROFF,ROFF                CLEAR
         STH   ROFF,OCMODNME+TWO        SET UP FIRST ENTRY
         SR    RLENGTH,RLENGTH          INIT REG
         SR    RWORK1,RWORK1            INIT REG
         IC    RLENGTH,TWO(RLIST)       LENGTH OF ENTRY
         LR    RWORK1,RLENGTH           LENGTH OF ENTRY
         BCTR  RWORK1,AVTEZERO          DECREMENT FOR EXECUTE
         SPACE
*              CHECK STATUS OF INVITATION LIST                        *
NXTENTRY EQU   *
         LH    ROFF,OCMODNME+TWO        GET NEXT ENTRY
         CLI   ONE(RLIST),AVTEZERO      ALL ENTRIES INACTIVE
         BE    CKMOVED                  YES
         SPACE
         LA    ROFF,ONE(ROFF)           BUMP ONE
         STH   ROFF,OCMODNME+TWO        STORE BACK
         CLC   OCMODNME+THREE(ONE),AVTEZERO(RLIST) OUT OF ENTRIES
         BH    CKMOVED                  YES
         SPACE
         SLL   ROFF,ONE                 MULTIPLY BY TWO
         LR    RENTRY,RLIST             BEGINNING
         SR    RENTRY,ROFF              BACK TO ENTRY
         CLC   AVTEZERO(TWO,RENTRY),OCTRMTBL TO BE DEACTIVATED
         BNE   NXTENTRY                 NO
         SPACE
         LA    RENTRY,SEVEN(RLIST)      POINTER TO FIRST ENTRY INDEX
         SR    RRET,RRET                CLEAR
         IC    RRET,ONE(RLIST)          NUMBER OF ACTIVE ENTRIES
CKNEXT   EQU   *
         AR    RENTRY,RLENGTH           GO TO NEXT ENTRY'S INDEX
         CLC   AVTEZERO(ONE,RENTRY),OCMODNME+THREE IS THIS AN ACTIVE EN
         BE    DACTIV                   ACTIVE.SO DEACTIVATE
         SPACE
         BCT   RRET,CKNEXT              NOT END OF ACTIVE.GO BACK
         SPACE
         B     NXTENTRY                 GET NEXT ENRRY
         SPACE
DACTIV   EQU   *
         TM    OCSWITCH,X80             IS THIS GENERAL POLL
         BZ    SPECPOLL                 NO, BRANCH
GPEXIT   EQU   *                        GENERAL POLL EXIT
         NI    OCSWITCH,OCFOXES-X80     RESET SWITCH
         MVC   OPCLDNME+VARINAME(NAMELEN),STOPGP SET UP G.P. MOD
         B     EXIT                     XCTL TO GENERAL POLL MOD
SPECPOLL EQU   *
         OI    OPCFLAG1,ANYMOVED        SET MOVED INDICATOR
         LR    RWORK,RENTRY
         SR    RENTRY,RLENGTH           SET TO ENTRY LENGTH -1
         BCT   RRET,SWAPLOOP            SWAP ENTRIES
         SPACE
         B     LOOPOUT                  GO TO NEXT ENTRY
         SPACE
SWAPLOOP EQU   *
         EX    RWORK1,SWAP1             SWAP TWO
         EX    RWORK1,SWAP2             SUCCESSIVE
         EX    RWORK1,SWAP3             ENTRIES
         AR    RENTRY,RLENGTH           POINT TO THIS ENTRY
         AR    RWORK,RLENGTH            POINT TO THIS ENTRY
         BCT   RRET,SWAPLOOP            NOT DONE RETURN
         SPACE
LOOPOUT  EQU   *
         IC    RRET,TWO(RLIST)          DONE GET ENTRY LENGTH
         LA    RBRNCH,ONE               USE AS DECREMENT
         TM    THREE(RLIST),EOT         IS THIS EOT-FE(BSC)
         BO    SETEOT                   YES
         SPACE
SETLOOP  EQU   *
         MVC   ONE(ONE,RWORK),AVTEZERO(RWORK)  MOVE ONE CHARACTER
         SR    RWORK,RBRNCH             AT A TIME,STARTING
         BCT   RRET,SETLOOP             WITH THE LAST CHARACTER
         MVI   ONE(RWORK),ENDACT        INSERT ACTIVE END CHARACTER
RSETACT  EQU   *
         IC    RRET,ONE(RLIST)          COUNT OF ACTIVE ENTRIES
         SR    RRET,RBRNCH              ONE LESS
         STC   RRET,ONE(RLIST)          STORE COUNT BACK
         B     NXTENTRY                 TRY NEXT
         SPACE
SETEOT   EQU   *
         MVC   OCMODNME+FOUR(TWO),ONE(RWORK)  SAVE EOT-FE
SETEOT1  EQU   *
         MVC   TWO(ONE,RWORK),AVTEZERO(RWORK)  MOVE ONE CHARACTER
         SR    RWORK,RBRNCH             AT A TIME
         BCT   RRET,SETEOT1             STARTING WITH LAST CHARACTER
         SPACE
         MVC   ONE(TWO,RWORK),OCMODNME+FOUR  RESET EOT-FE
         B     RSETACT                  SET ACTIVE COUNT
         SPACE
         SPACE
*              DETERMINE IF ANY INVITATION LIST ENTRIES MOVED         *
CKMOVED  EQU   *
         TM    OCSWITCH,X80             IS THIS FOR GENERAL POLL
         BO    GPEXIT                   YES, BRANCH
         TM    OPCFLAG1,ANYMOVED        ANY ENTRIES MOVED
         BNO   ALREADY                  NO
         LA    RZERO,STATSTOP           SET UP STOPPED MESSAGE
         SPACE
         B     STATANAL                 SEE IF LINE WAS STARTED BEFORE
         SPACE
ALREADY  EQU   *
         TM    OCFLAG,OCMPP+OCTOTE      TOTE OR MPP?
         LA    RZERO,ERROR25            ALREADY STOPPED MESSAGE
         BZ    ERROR                    NO
         LA    RPARM,EIGHT              SET ALREADY CODE FOR TOTE
         B     STATANL1                 YES ONLY DEACTIVATE
         SPACE
*              THIS SETS UP TO EXIT TO HOLD                           *
GOHOLD   EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),HOLD     SET TO GO TO HOLD
SETCKPT  EQU   *                                               @SA64357
         MVC   OCWTG(TWO),CHECKPT       SET RETURN MODULE NAME
         TM    OCFLAG,OCTOTE+OCMPP      TOTE OR MPP
         BO    TOTEMPP                  YES
         SPACE
         STH   RZERO,OCPARM             SET UP MESSAGE NUMBER
         B     TERMINAL                 GO TO HOLD
         SPACE
TOTEMPP  EQU   *
         STH   RPARM,OCPARM             SET UP RETURN CODE
TERMINAL EQU   *
         OI    OCSWITCH,X40             TO INDICATE TERMINAL
         B     EXIT                     XCTL TO NEW MODULE
         SPACE
ERROR    EQU   *
         LA    RPARM,FOUR               SET BAD RETURN CODE
         MVC   OPCLDNME+VARINAME(NAMELEN),XCTL03 TO GO TO ERROR MODULE
         TM    OCSWITCH,X80             IS THIS FOR GEN POLL   @SA74217
         BO    EXIT                     YES                    @SA74217
         B     STATANL1                 GO PROCESS ERROR CONDITION
         SPACE
CHPTMSG  EQU   *
         OI    OCSWITCH,X40             INDICATES A TERMINAL TO MSG MOD
         TM    OCSWITCH,MIDMSG          IS THIS TERMINAL TO BE  SA56606
*                                       REMOVED FROM MIDDLE OF  SA56606
*                                       MSG CONDITION           SA56606
         BNO   CKPT                     NO-BRANCH               SA56606
         MVC   OPCLDNME+VARINAME(NAMELEN),MMRTN SET MIDDLE MSG  SA56606
*                                       RTN NAME                SA56606
         B    EXIT                      XCTL                    SA56606
CKPT     EQU   *                                                SA56606
         MVC   OPCLDNME+VARINAME(NAMELEN),CHECKPT  SET CHECKPOINT NAME
         MVC   OCWTG(NAMELEN),WRTMSG    WTG TO MESSAGE MODULE
         STH   RZERO,OCPARM             SET UP PARM FOR CHECKPOINT
TESTERR  EQU   *
         TM    OCFLAG,OCTOTE+OCMPP      TOTE OR MPP?
         BZ    EXIT                     NO
         SPACE
         MVC   OCWTG(NAMELEN),WRITE     SET MESSAGE MODULE FOR CHECKPT
         STH   RPARM,OCPARM             GET PARAMETER FIELD
         LR    RZERO,RPARM              SET RETURN CODE IN REG 0
         SPACE
         MVC   OPCLDNME+VARINAME(NAMELEN),WRITE
EXIT     EQU   *
         NI    OPCFLAG1,AVTEZERO        RESET FLAGS
         LR    RPARM,ROPCAVT            OPERATOR CONTROL AVT
         XCTL  SF=(E,OPCXCTL)
*              TEST ALL EXIT SITUATIONS                               *
STATANAL EQU   *
         SR    RPARM,RPARM              SET GOOD RETURN
STATANL1 EQU   *
         TM    OPCSPEC,OPCRSTRT         RESTART SITUATION
         BZ    NORESTRT                 NO
         SPACE
         TM    OCSWITCH,STOPBOTH        STOP TERM AND HOLD TERM
         BO    GOHOLD                   YES
         SPACE
         SPACE
         B     CHPTMSG                  GO TO CHECKPOINT
         SPACE
NORESTRT EQU   *
         TM    OCSWITCH,X40             SHOULD LINE BE RESTARTED
         MVC   OPCDOUBL(EIGHT),OCBACKUP SET UP GOOD NAME
         BZ    CKHOLD                   NO
         SPACE
         SPACE
*              START LINE BACK IF NEEDED                              *
NOTMPS   EQU   *
         MVI   LCBPRI-IEDQLCB(RLCB),LCBPRIOR SET E0 PRIORITY IN BUFFER
         USING IEDQLCB,RLCB
         NI    LCBERBST,AVTEFF-LCBPRCPG  TURN OFF FLAG          SA67777
         DROP  RLCB
         OI    LCBSTAT1-IEDQLCB(RLCB),LCBRECVN   SET STATUS FOR START
         ST    RLCB,AVTEZERO(RLCB)      MAKE LCB POINT TO ITSELF
         XC    LCBLINK-IEDQLCB(TR,RLCB),LCBLINK-IEDQLCB(RLCB)  @XA09341
*                                       CLEAR THE LINK FIELD   @XA09341
*                                         OF THE ELEMENT       @XA09341
         ST    RLCB,OPCAQCTL            PUT ELEMENT ADDR IN    @XA09341
*                                         FIRST WORD OF LIST   @XA09341
         ST    RLCB,OPCAQCTL+FOUR       ALSO IN THE SECOND     @XA09341
*                                         WORD OF PARM LIST    @XA09341
         MVI   OPCAQCTL,POST            INDICATE FIRST WORD IN @XA09341
*                                         THE LIST             @XA09341
         MVI   OPCAQCTL+FOUR,X80        SET END OF PARM LIST   @XA09341
         LR    RLCB1,RZERO              SAVE THE MESSAGE       @YA10700
*                                         NUMBER IN REG 6      @YA10700
         LR    RLCB,RPARM               SAVE RETURN CODE IN    @YA10700
*                                         REGISTER 5           @YA10700
         LA    RPARM,OPCAQCTL           PUT THE PARM LIST      @XA09341
*                                         ADDRESS IN REG 1     @XA09341
         AQCTL                          POST THE LCB           @XA09341
         LR    RZERO,RLCB1              RESTORE THE MSG NUMBER @YA10700
         LR    RPARM,RLCB               RESTORE RETURN CODE    @YA10700
         EJECT
CKHOLD   EQU   *
         CLI   OCOPTCDE,STOPSNGL        DEACTIVATE TERM ONLY FOR TOTE
         BE    TESTERR                  STOP TERMINAL ONLY - FOR TOTE
         SPACE
         TM    OCSWITCH,STRTBOTH        RELEASE ALSO           @SA64357
         BNO   TSTHOLD                  NO                     @SA64357
         SPACE
         MVC   OPCLDNME+VARINAME(NAMELEN),RELEASE SET RLSE     @SA64357
         B     SETCKPT                                         @SA64357
         SPACE
TSTHOLD  EQU   *                                               @SA64357
         TM    OCFLAG,OCMPP+OCTOTE      IS THIS TOTE OR MPP?
         BNZ   GOHOLD                   YES
         SPACE
         TM    OCSWITCH,STOPBOTH        HOLD TERMINAL ALSO
         BO    GOHOLD                   YES
         SPACE
         LTR   RPARM,RPARM              ERROR OR GOOD
         BZ    CHPTMSG                  GO CHECKPOINT
         SPACE
         B     TESTERR                  ERROR MESSAGE HAS TO BE OUTPUT
         SPACE
SWAP1    XC    ONE(AVTEZERO,RWORK),ONE(RENTRY) SWAP TWO
SWAP2    XC    ONE(AVTEZERO,RENTRY),ONE(RWORK) SUCCESSIVE
SWAP3    XC    ONE(AVTEZERO,RWORK),ONE(RENTRY) ENTRIES
ERRORTAB DS    0F
INVALTIC DC    X'08000002'              INVALID TIC ADDR       @OY14483
         SPACE 2
XCTL03   DC    C'03'                    ERROR MODULE NAME
CHECKPT  DC    C'06'                    CHECKPOINT MODULE NAME
WRITE    DC    C'07'                    OUTPUT WRITER NAME
WRTMSG   DC    C'09'                    HOLD, RELEASE, AND VARY MESSAGE
STOPLN   DC    C'V1'                    STOPLINE MODULE
FIRSTLD  DC    C'00'                    FIRST LOAD OF OP CTL
HOLD     DC    C'H0'                    HOLD MODULE
RELEASE  DC    C'R0'                    RELEASE MODULE         @SA64357
STPTERM  DC    C'V2'                        DEACTIVATE TERMINAL MODULE
STOPGP   DC    C'V5'                    STOP GENERAL POLL MODULE
STRTTERM DC    C'V4'                    START TERM MOD           S22024
MMRTN    DC    C'V7'                    MIDDLE MSG RTN          SA56606
ACDEACTM DC    C'V9'                    ACT / DEACT TERM MOD     S22024
ZAPAREA  DC    50F'0'                   PATCH AREA             @Y17XART
         TITLE 'IGCV210D - DSECTS REFERENCED BY THIS MODULE'
         TAVTD
         EJECT
         DCBD DSORG=TX
         EJECT
         TDEBD
         EJECT
         TDISPD
         EJECT
*     DELET IEAQFX CALL - NOT REQUIRED FOR VS
         EJECT
         TLCBD
         EJECT
         TOPCED
         EJECT
         TOPCAVTD
         EJECT
         TPRFD
         EJECT
         TPRIOR
         EJECT
         TQCBD
         EJECT
         EJECT
         TTNTD
         EJECT
         TTRMD
         END
