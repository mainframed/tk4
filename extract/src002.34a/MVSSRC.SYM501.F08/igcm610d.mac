         TITLE 'IGCM610D - MODIFY TRACE'
IGCM610D CSECT
*A000000-999999                                                @Y16X5R0
*A373600-374200,511000,525410-525750,526230-526530,819600-821400,Y06328
*A829600-830200                                                  Y06328
*C846000                                                         Y06328
*A373000,606000                                                @XA11945
*A006000,012000,015000,129000,138000,146000,189000,373480,     @Y17XARW
*A426000,829000                                                @Y17XARW
*C018000,384000,204000,066000,147000-162000,384000,            @Y17XARW
*C526230-526530                                                @Y17XARW
*D373600-374600,525410-525750,819600-821400                    @Y17XARW
*A028000,112000-112090                                         @Y17XARA
*A267010,390010,427005-428830,580000,602000,606050-606080      @Y17XARA
*A651000-653400,829300-829360,829410-829540                    @Y17XARA
*A279500,524500,607900,865000-865002                           @OY15041
*C373480,608000                                                @OY15041
*D606400,608200                                                @OY15041
*A374200,602000,821400
*C579000,580500,594000-597000,820200
*A564000,821800
*C579000-581500,595600-596400,588800,602008,602040,820200
*C821800
*D819200-819400
*C602480
*A602840
*C576300,577800,819600                                         @OZ26897
***********************************************************************
*                                                                     *
* MODULE NAME - IGCM610D(TCAM, OP CONTROL)                     @Y17XARW
*                                                                     *
* DESCRIPTIVE NAME - TRACE COMMAND PARSE ROUTINE               @Y17XARW
*                                                                     *
* STATUS - VERSION 10.0                                        @Y17XARW
*                                                                     *
* FUNCTION -                                                          *
* THIS ROUTINE PROCESSES THE FOLLOWING OPERATOR CONTROL COMMANDS      *
* 1.UPON ENTRY THIS ROUTINE TESTS THE OPERATOR CONTROL ELEMENT SET UP *
*   BY IGCMM10D TO DETERMINE IF THE DDNAME/RLN OR THE HARDWARE ADDRESS*
*   FORMAT WAS USED. IF IT IS DDNAME THE NAME IS INSERTED IN OPCDOUBL *
*   OF THE OPCAVT AND THE RLN IS INSERTED INTO REGISTER 1 AND A BALR  *
*   IS DONE TO THE DCBLOOKUP SUBROUTINE IN THE OPERATOR CONTROL       *
*   RESIDENT ROUTINE IEDQCA. IF THE HARDWARE ADDRESS WAS SPECIFIED    *
*   THE ADDRESS IS CONVERTED TO EBCDIC AND INSERTED IN OPCDOUBL       *
*   REGISTER 1 IS ZEROED OUT AND A BALR IS DONE TO THE DCBLOOKUP      *
*   SUBROUTINE. THE SUBROUTINE WILL VALIDITY CHECK THE DDNAME/RLN OR  *
*   HARDWARE ADDRESS AND IF INVALID WILL RETURN ON REGISTER 14 PLUS 4 *
*   IF VALID ON REGISTER 14. IF INVALID ERROR MSG IED017I WILL BE     *
*   WRITTEN OUT BY XCTLING TO THE ERROR WRITER IGC0310D.              *
*                                                                     *
*   IF VALID, A CHECK IS MADE FOR AN LGB BEING RETURNED BY     @Y17XARW
*   DCBLOCATE.  IF IT IS AN LGB, THE LINE TTE WILL BE SAVED IN @Y17XARW
*   THE OPCE, THE SCAN FLAG WILL BE SETUP TO INDICATE SCAN NOT @Y17XARW
*   YET DONE, AND AN XCTL WILL BE MADE TO IGCMB10D.            @Y17XARW
*                                                                     *
*   IF IT IS NOT AN LGB, IT IS A 270X OR A 3705 DCB.  THE LCB  @Y17XARW
*   ADDRESS IS THEN COMPUTED FROM DCBIOBAD+(DCBEIOBX*RLN)-     @Y17XARW
*   SIZE OF AN IOB. THE AVT IS TESTED FOR THE PRESENCE OF A TRACE     *
*   TABLE (AVTRACE NOT ZERO) AND IF NOT, AN ERROR MESSAGE NUMBER  55  *
*   IS LOADED INTO REGISTER 0 AND XCTL TO THE ERROR WRITER IGC0310D   *
*   TO WRITE OUT MESSAGE IED055I.                                     *
*                                                                     *
*   IF TRACE IS TO BE STARTED, THE TRACE BIT IS TESTED . IF ALREADY   *
*   ON, MESSAGE NUMBER 24 IS LOADED INTO REGISTER 0 AND XCTL TO       *
*   IGC0310D TO WRITE OUT MESSAGE IED024I. OTHERWISE THE TRACE BIT IS *
*   TURNED ON (LCBTRACE IN LCBSTAT2).                                 *
*                                                                     *
*   IF TRACE IS TO BE STOPPED BUT THE LCB TRACE BIT IS NOT ON, AN     *
*   ALREADY STOPPED MESSAGE NUMBER ,MSG 30 IS LOADED INTO REGISTER 0  *
*   AND XCTL TO IGC0310D TO WRITE OUT MESSAGE IED030I. OTHERWISE      *
*   THE BIT IS TURNED OFF.                                            *
*                                                                     *
* 2. DTRACE= THIS COMMAND MODIFIES THE SUBTASK TRACE OPTION  @Y17XARA *
*    (AVTSUBT) AND OBTAINS A BUFFER UNIT AND POSTS IT TO     @Y17XARA *
*    THE ADDRESS AT LABEL DSPTRACE IN IGG019RO . WHEN THE    @Y17XARA
*    ELEMENT IS DISPATCHED THE ROUTINE TRACE IN IGG019RO WILL@Y17XARA
*    GET CONTROL AND MODIFY INSTRUCTIONS WITHIN ITSELF TO    @Y17XARA
*    ACTUALLY START OR STOP THE TRACE(S)                     @Y17XARA
*    THE FOLLOWING OPTIONS ARE ;                             @Y17XARA
*        1. ON   = X'80' SUBTASK TRACE                       @Y17XARA *
*        2. POST = X'40' POST TRACE                          @Y17XARA *
*        3. ALL  = X'FF' ALL TRACES                          @Y17XARA *
*        4. OFF  = X'00' NO TRACES                           @Y17XARA *
*    RESPONSE :                                              @Y17XARA *
*        1. IED018I DTRACE COMMAND INVALID                   @Y17XARA
*        2. IED024I TRACE ALREADY STARTED FOR DTRACE =       @Y17XARA
*        3. IED030I TRACE ALREADY STOPPED FOR DTRACE =       @Y17XARA
*        4. IED055I SUBTASK TRACE CANNOT BE ALTERED          @Y17XARA
*        5. IED537I SUBTASK TRACE STARTED OPT= (ON,POST,ALL) @Y17XARA *
*        6. IED538I SUBTASK TRACE STOPPED                    @Y17XARA *
*                                                            @Y17XARA *
* 3. TRACE=ERROR, THIS COMMAND MODIFIES THE I/O TRACE OPTION @Y17XARA *
*    (AVTIOTR) THE FOLLOWING OPTIONS ARE                     @Y17XARA *
*        1. ON   = X'80' START TRACE ON ERRORS               @Y17XARA *
*        2. HOLD = X'40' CONTINUE TRACE AFTER ERRORS         @Y17XARA *
*        3. ALL  = X'FF' ALL TRACES                          @Y17XARA *
*        4. OFF  = X'00' NO TRACES                           @Y17XARA *
*    RESPONSE :                                              @Y17XARA *
*        1. IED018I TRACE COMMAND INVALID                    @Y17XARA
*        2. IED024I TRACE ALREADY STARTED FOR ERROR ,        @Y17XARA
*        3. IED030I TRACE ALREADY STOPPED FOR ERROR ,        @Y17XARA
*        4. IED055I I/O TRACE CANNOT BE ALTERED              @Y17XARA
*        5. IED546I I/O ERROR TRACE STARTED OPT=(ON,POST,ALL)@Y17XARA *
*        6. IED547I I/O ERROR TRACE STOPPED                  @Y17XARA *
*                                                            @Y17XARA *
*   IF REQUEST IS SUCCESSFUL EITHER MESSAGE NUMBER 23 OR 29 IS LOADED *
*   INTO REGISTER 0, THE WTG FIELD IN THE OPCE IS SET UP SO THAT      *
*   IGCM110D GAINS CONTROL AFTER XCTLING TO IGC0610D THE CHECKPOINT   *
*    ROUTINE PASSING IN OPCCOPCE IN OPCAVT THE ADDRESS OF RECORD TO   *
*   CHECKPOINT.                                                       *
*                                                                     *
*   NOTES -                                                           *
*           REGISTER CONVENTIONS = SEE REGISTER DECLARES       @Y17XARW
*                                                                     *
*           PATCH LABEL = ZAPAREA                              @Y17XARW
*                                                                     *
*   MODULE TYPE - PROCEDURE                                    @Y17XARW
*           PROCESSOR = ASSEMBLER XF                           @Y17XARW
*                                                                     *
*           MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY       @Y17XARW
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGCM610D IS THE ONLY ENTRY POINT FOR THIS MODULE            *
*                                                                     *
*         LINKAGE - IGCMM10D XCTL'S TO IGCM610D                @Y17XARW
*                   IGCM610D XCTL'S TO IGCMB10D FOR LGBS       @Y17XARW
*                   IGCM610D XCTL'S TO IGC0610D FOR DCBS AND   @Y17XARW
*                   THEN TO IGCM110D FOR MSG PROCESSING        @Y17XARW
*                                                                     *
* INPUT -                                                             *
*    REGISTER 1 CONTAINS A POINTER TO THE OPERATOR CONTROL AVT        *
*    OCMODNME - FIRST WORD CONTAINS LENGTH OF REMAINING         S22024*
*               OPERANDS IN TRACE COMMAND                       S22024*
*    OCBACKUP - GRPNAME OR ADDRESS                             @Y17XARW
*    OCRLN    - RELATIVE LINE NUMBER                           @Y17XARW
*    OPCCOPCE - ADDRESS OF CURRENT ELEMENT                     @Y17XARW
*                                                                     *
* OUTPUT -                                                            *
*    EXIT TO IGCMB10D -                                               *
*                 R10= BYTES LEFT TO SCAN                      @Y17XARW
*                 R11= PTR TO NEXT BYTE SCAN                   @Y17XARW
*                 OCLINTTE= LINE TTE ADDRESS                   @Y17XARW
*                 OCBACKUP= GRPNME                             @Y17XARW
*   EXIT TO IGC0610D -                                                *
*                 R0 = MESSAGE TO BE WRITTEN                   @Y17XARW
*                 R1 = OP CONTROL AVT ADDRESS                  @Y17XARW
*                 OPCCOPCE= ADDRESS OF RECORD TO BE WRITTEN    @Y17XARW
*                 OCWTG= ID OF NEXT MODULE TO GAIN CONTROL     @Y17XARW
*                 OCBACKUP = GRPNAME                           @Y17XART
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         SVC 7(XCTL)                                                 *
*         OPCDCBLK - DCB LOOKUP ROUTINE IN IEDQCA                     *
*                                                                     *
* EXITS,NORMAL --                                                     *
*              STATUS MODIFIED AS REQUESTED. RESPONSE BUILT           *
*         IGC0610D - CHECKPOINT - TO CHECKPOINT RECORD WHOSE ADDRESS  *
*                    IS AT OPCCOPCE IN OPCAVT                         *
*         IGCMB10D - HANDLES ALL LGB PROCESSING                @Y17XARW
*                                                                     *
* EXITS,ERROR --                                                      *
*              INVALID COMMAND. XCTL TO IGC0310D                      *
*                                                                     *
* TABLES/WORKAREAS -                                                  *
*    TCAM AVT, OP CONTROL AVT,DCB,LCB,LGB,OPCE TNT             @Y17XARW
*                                                                     *
* ATTRIBUTES -                                                        *
*   REFRESHABLE,DISABLED,REUSABLE,TRANSIENT(TYPE 4),SUPERVISOR MODE   *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT *
*    TO THAT USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED SO  *
*    THAT REDEFINITION OF CHARACTER CONSTANTS, BY REASSEMBLY, WILL    *
*    RESULT IN A CORRECT MODULE FOR THE NEW DEFINITION.               *
*                                                                     *
* NOTES -                                                             *
*    COMMUNICATIONS BETWEEN THE VARIOUS MODULES/TRANSIENTS IS         *
*    PRIMARILY THROUGH THE OPERATOR CONTROL ADDRESS VECTOR TABLE (AVT)*
*                                                                     *
*        THIS IS A MODULE RE-WRITE OF IEDQCP AS OF OS/TCAM LEVEL II   *
***********************************************************************
         EJECT
         SPACE
RZERO    EQU   0                        PARM REG 0
RPARM    EQU   1                        PARM REG 1
ROPT     EQU   1                        OPTION REG 1           @Y17XARA
ROPCAVT  EQU   2                        OP CONTROL AVT POINTER
ROPCE    EQU   3                        CURRENT ELEMENT ADDRESS
RAVT     EQU   4                        TCAM AVT ADDRESS
RUCB     EQU   5                        UCB BASE               @OY15041
RWORK5   EQU   RUCB                     WORK REGISTER          @OY15041
RWORK6   EQU   6                        WORK REGISTER
RDCB     EQU   7                        DCB BASE
RWORK8   EQU   8                        WORK REGISTER
RLCB     EQU   9                        LCB BASE
RWORK10  EQU   10                       WORK REGISTER
RWORK11  EQU   11                       WORK REGISTER
RBASE    EQU   12                       MODULE BASE
RSAVE    EQU   13                       SAVE AREA POINTER
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RBRNCH   EQU   15                       SUBROUTINE BRANCH ENTRY POINT
         SPACE
****    MISCELLANEOUS EQUATES    ****
ZERO     EQU   0                        ZERO VALUE
ONE      EQU   1                        VALUE ONE
TWO      EQU   2                        VALUE TWO
THREE    EQU   3                        VALUE THREE
VARINAME EQU   3                        OFFSET TO CHANGE XCTL NAME
FOUR     EQU   4                        VALUE FOUR
SEVEN    EQU   7                        VALUE SEVEN
EIGHT    EQU   8                        VALUE EIGHT
TEN      EQU   10                       VALUE TEN
TWELVE   EQU   12                       VALUE TWELVE
TWENTY4  EQU   24                       VALUE TWENTY FOUR
BLANK    EQU   C' '                     CHARACTER BLANK
X80      EQU   X'80'                    HEX 80                   S22024
LTPCI    EQU   4                        FLAG BYTE IN I/O TIME  @XA11945
LTINUSE  EQU   X'01'                    ENTRY IN USE FLAG      @XA11945
LTCUU    EQU   0                        CUU OF TRACE ENTRY     @XA11945
ENDTAB   EQU   X'FF'                    END OF TABLE INDICATOR @XA11945
LTLCBAD  EQU   8                        LCB ADDRESS            @XA11945
LTSIZE   EQU   8                        SIZE OF ENTRY          @OY15041
MASK8    EQU   B'1000'                  HI-ORDER BYTE MASK     @Y17XARW
         SPACE 3                                               @Y17XARA
IED017I  EQU   17                       MSG IED017I EQU        @Y17XARA
IED018I  EQU   18                       MSG IED018I EQU        @Y17XARA
IED023I  EQU   23                       MSG IED023I EQU        @Y17XARA
IED024I  EQU   24                       MSG IED024I EQU        @Y17XARA
IED029I  EQU   29                       MSG IED029I EQU        @Y17XARA
IED030I  EQU   30                       MSG IED030I EQU        @Y17XARA
IED055I  EQU   55                       MSG IED055I EQU        @Y17XARA
IED537I  EQU   537                      MSG IED537I EQU        @Y17XARA
IED538I  EQU   538                      MSG IED538I EQU        @Y17XARA
IED546I  EQU   546                      MSG IED546I EQU        @Y17XARA
IED547I  EQU   547                      MSG IED547I EQU        @Y17XARA
         EJECT                                                 @Y17XARA
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQOPCN,ROPCE           OPCE NEGATIVE PREFIX   @Y17XARW
         USING IEDQLCB,RLCB             ADDRESSABILITY FOR LCB
         USING IHADCB,RDCB              ADDRESSABILITY FOR DCB
         USING OPTFLAG,ROPT             AVTSUBT OR AVTIOTR     @Y17XARA
         USING IGCM610D,RBASE                                  @Y17XARA
         SPACE 1                                               @Y17XARA
         LR    RBASE,RBRNCH             LOAD BASE REGISTER     @Y17XARA
IGCM610D IEDHJN SKIPID                                         @Y17XARA
         LR    ROPCAVT,RPARM            SET BASE FOR OP CTL AVT
         L     RAVT,OPCAVTPT            SET BASE FOR TCAM AVT
         L     ROPCE,OPCCOPCE           GET ELEMENT POINTER
         LA    RWORK8,OCPREFSZ          SETUP NEG PREF SIZE    @Y17XARW
         SR    ROPCE,RWORK8             BACKUP TO NEG PREF     @Y17XARW
         LA    RSAVE,OPCSAVE            ADDRESS OF OP/CTL SAVE @Y17XARA
         CLI   OCSWITCH,OCSUBT          DTRACE= COMMAND        @Y17XARA
         BNE   TESTIOTR                 NO , TEST FOR ERROR    @Y17XARA
         CLC   AVTDISTR,AVTFZERO        SUBTASK TRACE TABLE    @Y17XARA
         BE    NOWAY                    NO CANNOT MODIFY TRACE @Y17XARA
         LA    ROPT,AVTSUBT             LOAD ADDRESS AVTSUBT   @Y17XARA
         CLC   POST,OCBACKUP            POST SPECIFIED         @Y17XARA
         BNE   TESTALL                  NO , TEST FOR ALL      @Y17XARA
         B     TESTOPT                  BRANCH TO TEST OPTION  @Y17XARA
TESTIOTR EQU   *                                               @Y17XARA
         CLI   OCSWITCH,OCIOTR          TRACE=ERROR, COMMAND   @Y17XARA
         BNE   NOTIOTR                  NO , PROCESS NORMALLY  @Y17XARA
         CLC   AVTRACE,AVTFZERO         I/O TRACE TABLE        @Y17XARA
         BE    NOWAY                    NO CANNOT MODIFY TRACE @Y17XARA
         LA    ROPT,AVTIOTR             LOAD ADDRESS AVTIOTR   @Y17XARA
         CLC   HOLD,OCBACKUP            HOLD SPECIFIED         @Y17XARA
         BNE   TESTALL                  NO , TEST FOR ALL      @Y17XARA
TESTOPT  EQU   *                                               @Y17XARA
         TM    OPTFLAG,OPTALL-(OPT1+OPT2) POST + OR HOLD +     @Y17XARA
         BNZ   STOPALL2                 YES                    @Y17XARA
         TM    OPTFLAG,OPT1+OPT2        ALREADY SPECIFIED      @Y17XARA
         BO    ALON                     YES , SET RESPONSE     @Y17XARA
         OI    OPTFLAG,OPT1+OPT2        SET OPTION ON          @Y17XARA
         B     TRACESTR                 GO ISSUE SUCESSFUL MSG @Y17XARA
STOPALL2 EQU   *                                               @Y17XARA
         NI    OPTFLAG,OPT1+OPT2        RESET TO HOLD OR POST  @Y17XARA
         B     TRACESTR                 GO ISSUE SUCESSFUL MSG @Y17XARA
TESTALL  EQU   *                                               @Y17XARA
         CLC   ALL,OCBACKUP             ALL SPECIFIED          @Y17XARA
         BNE   TESTON                   NO , TEST FOR ON       @Y17XARA
         TM    OPTFLAG,OPTALL           ALL ALREADY SPECIFIED  @Y17XARA
         BO    ALON                     YES , SET RESPONSE     @Y17XARA
         MVI   OPTFLAG,OPTALL           SET OPTION TO ALL      @Y17XARA
         B     TRACESTR                 GO ISSUE SUCESSFUL MSG @Y17XARA
TESTON   EQU   *                                               @Y17XARA
         CLC   ON,OCBACKUP              ON SPECIFIED           @Y17XARA
         BNE   NOTON                    NO , TEST FOR OFF      @Y17XARA
         TM    OPTFLAG,(OPTALL-OPT1)    ON + SPECIFIED         @Y17XARA
         BNZ   STOPALL1                 YES , STOP OTHER TRACE @Y17XARA
         TM    OPTFLAG,OPT1             ON ALREADY SPECIFIED   @Y17XARA
         BO    ALON                     YES , SET RESPONSE     @Y17XARA
         OI    OPTFLAG,OPT1             SET OPTION TO ON       @Y17XARA
         B     TRACESTR                 GO ISSUE SUCESSFUL MSG @Y17XARA
STOPALL1 EQU   *                                               @Y17XARA
         NI    OPTFLAG,OPT1             SET TRACE OPTION TO ON @Y17XARA
TRACESTR EQU   *                                               @Y17XARA
         LA    RZERO,IED546I            SET TO TRACE=ERROR CMD @Y17XARA
         CLI   OCSWITCH,OCIOTR          ERROR , COMMAND        @Y17XARA
         BE    XCTLMOD                  YES,ISSUE SUCESSFUL MSG@Y17XARA
         LA    RWORK5,IED537I           SET TO DTRACE = MSG    @Y17XARA
         B     TRACEALT                 POST UNIT TO IGG019RO  @Y17XARA
NOTON    EQU   *                                               @Y17XARA
         CLC   OFF,OCBACKUP             OFF SPECIFIED          @Y17XARA
         BNE   CMDINV                   NO , COMMAND INVALID   @Y17XARA
         TM    OPTFLAG,OPTALL           OPTION OFF SPECIFIED   @Y17XARA
         BZ    ALOF                     YES , SET RESPONSE     @Y17XARA
         MVI   OPTFLAG,OPTOFF           SET TRACE OPTION TO OFF@Y17XARA
         LA    RZERO,IED547I            SET TO TRACE=ERROR CMD @Y17XARA
         CLI   OCSWITCH,OCIOTR          ERROR , COMMAND        @Y17XARA
         BE    XCTLMOD                  YES,ISSUE SUCESSFUL MSG@Y17XARA
         LA    RWORK5,IED538I           SET TO DTRACE = MSG    @Y17XARA
TRACEALT EQU   *                                               @Y17XARA
         LA    RPARM,ONE                SET COUNT TO ONE       @Y17XARA
         L     RBRNCH,OPCGETBF          ADDRESS OF ALLOCBUF    @Y17XARA
         BALR  RRET,RBRNCH              GET AVAILABLE UNIT     @Y17XARA
         LR    RZERO,RWORK5             RESTORE MESSAGE NUMBER @Y17XARA
         USING IEDQPRF,RPARM            USING AVAILABLE UNIT   @Y17XARA
         L     RWORK5,AVTEA             ADDRESS OF DISPATCHER  @Y17XARA
         USING IEDQDISP,RWORK5          USING DISPATCHER DSECT @Y17XARA
         MVC   PRFQCBA,DSPTRACE+1       DSPTRACE QCB ADDRESS   @Y17XARA
         MVI   PRFPRI,PRITRACE          SET PRIORITY           @Y17XARA
         XC    PRFLCB,PRFLCB            ZERO LCB ADDRESS       @Y17XARA
         DROP  RWORK5                   DROP DISPATCHER DSECT  @Y17XARA
         L     RWORK5,CVTPTR            ADDRESS OF CVT         @Y17XARA
         USING IEDQCVT,RWORK5           USING CVT DSECT        @Y17XARA
         L     RWORK5,CVTAQAVT          ADDRESS OF TCX         @Y17XARA
         USING IEDQTCX,RWORK5           USING TCX              @Y17XARA
         L     RWORK6,TCXREADY          ADDRESS OF CURRENT     @Y17XARA
NEWCURRT STCM  RWORK6,SEVEN,PRFLINK     LINK NEW TO CURRENT    @Y17XARA
         CS    RWORK6,RPARM,TCXREADY    UPDATE ASSYNC ?        @Y17XARA
         BNE   NEWCURRT                 NO , NEW CURRENT       @Y17XARA
         DROP  RPARM                    DROP BUFFER DSECT      @Y17XARA
         B     XCTLMOD                  CONTINUE               @Y17XARA
NOTIOTR  EQU   *                                               @Y17XARA
         MVI   OPCDOUBL,BLANK          BLANK OUT OPC FIELD
         MVC   OPCDOUBL+ONE(SEVEN),OPCDOUBL FOR SUBROUTINE USE
         TM    OCFLAG,OCLINE            CHECK FOR LINE NUMBER
         BNO   DDTIOT                   NOT LINE NUMBER - BYPASS CONVER
         LA    RDCB,OPCDOUBL           SET UP
         LA    RWORK8,OCLINENO          TO CONVERT HEX
         LA    RLCB,TWO                  LINE ADDRESS
         BAL   RWORK10,HEXTOBCD            TO EBCDIC
         MVC   OPCDOUBL(FOUR),OPCDOUBL+ONE MAKE LINE NUMBER THREE BYTES
         SR    RPARM,RPARM             SET UP FOR
         B     LINENO                   GO GET DCB ADDR FROM LINE NO
DDTIOT   EQU   *
         MVC   OPCDOUBL,OCBACKUP        DDNAME FOR DCB LOOKUP RTNS22024
         SR    RPARM,RPARM              CLEAR REG
         IC    RPARM,OCRLN              GET RLN NO FOR DCB LOOKUP RTN
LINENO   EQU   *
         L     RBRNCH,OPCDCBLK         GET DCB RTN ADDRESS
         LA    ROPCE,OCPREFSZ(ROPCE)   POINT TO OPCE          @YM06880
         BALR  RRET,RBRNCH             BR TO DCB LOOKUP RTN
         B     GOODRTN                  GOOD RETURN
INVALID  EQU   *                                                 S05331
         LA    RZERO,IED017I            SET ERROR MESSAGE      @Y17XARA
         B     XCTLERR                  BR TO OUTPUT ERROR MSG
****    GOOD RETURN FROM DCB LOOKUP     ****
GOODRTN  EQU   *
         LA    RWORK8,OCPREFSZ          OPCE NEG PREFIX SIZE   &YM06880
         SR    ROPCE,RWORK8             BACK UP TO PREFIX      &YM06880
         LR    RUCB,RZERO               SET UCB BASE           @OY15041
         LR    RDCB,RPARM               SET DCB BASE
         TM    DCBDSORG,X80             TEST FOR 3705 LGB        S22024
         BNO   NOTLGB                   NO, DCB CONTINUE         S22024
         ICM   RZERO,MASK8,CLEARB       CLEAR HI-ORDER BYTE    @Y17XARW
         ST    RZERO,OCLINTTE           SAVE LINE TTE IN OPCE  @Y17XARW
         NI    OCSWITCH,ENDTAB-OCSCANON TURN SCAN OFF          @Y17XARW
         MVC   OPCLDNME+THREE(TWO),LTRC3705  3705 TRACE MOD NAME S22024
         B     EXIT                     GO DO XCTL               S22024
NOTLGB   EQU   *                                                 S22024
         CLC   OCMODNME(FOUR),AVTFZERO  ANY OPERANDS REMAINING ? S22024
         BNE   CMDINV                   YES, COMMAND INVALID     S22024
         L     RLCB,DCBIOBAD            GET IOB
         LA    RLCB,ZERO(RLCB)          CLEAR HI-ORDER BYTE
         SR    RZERO,RZERO              CLEAR REG
         IC    RZERO,OCRLN              INSERT RLN
         SR    RPARM,RPARM              ZERO REG
         IC    RPARM,DCBEIOBX           GET IOB LENGTH
         MR    RZERO,RZERO              MULTIPLY IOB BY RLN
         AR    RLCB,RPARM               ADD RESULTS TO IOB ADDRESS
         LA    RWORK8,LCBFLAG1-IEDQLCB  GET LENGTH OF IOB
         SR    RLCB,RWORK8              GET TO LCB START
         CLC   AVTRACE,AVTFZERO         TRACE TABLE SPECIFIED  @Y17XARA
         BZ    NOWAY                    NO, CANNOT MODIFY TRACE
         SR    RPARM,RPARM              CLEAR WORK REGISTER    @SA77825
         IC    RPARM,DCBEIOBX           INSERT IOB LENGTH      @SA77825
         SH    RPARM,HA8                LOCATE LCB EXTENSION   @SA77825
         AR    RPARM,RLCB               SET EXTENSION BASE     @SA77825
         USING IEDQLCBX,RPARM                                  @SA77825
         CLI   OCSWITCH,OCX19           TRACE ON OR OFF        @Y17XARA
         BE    OFFT                     OFF - GO PROCESS
         TM    LCBSTAT2,LCBTRACE        TRACE ALREADY ON
         BO    ALON                     YES - RESPOND ALREADY ON
         L     RWORK10,LCBSTAT2-THREE   LOAD FLAG WORD VALUE   @OZ26897
COPYOFF  EQU   *                                               @Z30X8RR
         LR    RWORK11,RWORK10          MAKE COPY FOR SETTING  @Z30X8RR
*                                       LCBTRACE BIT           @Z30X8RR
         O     RWORK11,TRACEON          SET LCBTRANE ON IN COPY@Z30X8RR
         CS    RWORK10,RWORK11,LCBSTAT2-THREE  IF LCB FIELD    @OZ26897
*                                       UNCHANGED,STORE FROM   @Z30X8RR
*                                       REG WITH LCBTRACE ON   @Z30X8RR
         BNE   COPYOFF                  ELSE,LOOP TO PRESERVE  @Z30X8RR
         LA    RZERO,IED023I            SET TRACE STARTED      @Y17XARA
         B     XCTLMOD                  EXIT
OFFT     EQU   *
         TM    LCBSTAT2,LCBTRACE        TRACE ALREADY OFF      @Y17XARA
         BNO   ALOF                     YES - SET RESPONSE     @Y17XARA
SETOFF   EQU   *                                               @YA12276
         L     RWORK10,LCBSTAT2-THREE   SAVE VALUE OF LCBSTAT2 @Z30X8RR
COPYON   EQU   *                                               @Z30X8RR
         LR    RWORK11,RWORK10          MAKE COPY FOR SETTING  @Z30X8RR
*                                       LCBTRACE BIT           @Z30X8RR
         N     RWORK11,TRACEOFF         SET LCBTRACE OFF IN    @Z30X8RR
*                                       COPY                   @Z30X8RR
         CS    RWORK10,RWORK11,LCBSTAT2-THREE IF LCB FIELD     @Z30X8RR
*                                       UNCHANGED,STORE FROM   @Z30X8RR
*                                       REG WITH LCBTRACE ON   @Z30X8RR
         BNE   COPYON                   ELSE,LOOP TO PRESERVE  @Z30X8RR
*                                       UPDATED LCB FIELD      @Z30X8RR
         L     RWORK10,LCBXCON          SAVE FLAG WORD VALUE   @SA77825
REQON    EQU   *                                               @YA12276
         LR    RWORK11,RWORK10          MAKE COPY FOR SETTING  @YA12276
         N     RWORK11,REQOFF           SET REQUEST OFF@YA12276
         CS    RWORK10,RWORK11,LCBXCON  RESET TRACE REQUESTED  @SA77825
         DROP  RPARM                                           @SA77825
         BNE   REQON                    LOOP TO PRESERVE LCB   @YA12276
         LA    RZERO,IED029I            SET TRACE STOPPED      @Y17XARA
         L     RPARM,AVTIOT                                    @XA11945
         L     RPARM,FOUR(,RPARM)       ADDR OF TRACE TBL SIZE @XA11945
GETNEXT  EQU   *                                               @XA11945
         LA    RPARM,LTSIZE(,RPARM)     LOOK AT FIRST/NEXT ENTR@XA11945
         CLI   LTCUU(RPARM),ENDTAB      END OF TABLE           @XA11945
         BE    XCTLMOD                  BRANCH YES             @XA11945
         TM    LTPCI(RPARM),LTINUSE     IS ENTRY BEING USED    @XA11945
         BZ    GETNEXT                  BRANCH NO-NEXT         @XA11945
         USING IEDQUCB,RUCB             ADDRESSABILITY FOR UCB @OY15041
         CLC   UCBCHA(TWO),LTCUU(RPARM) IS ENTRY FOR THIS UCB  @OY15041
         BNE   GETNEXT                  BRANCH NO              @XA11945
         MVI   LTPCI(RPARM),ZERO        FREE ENTRY             @XA11945
****    EXIT TO CHECKPOINT INCIDENT    ****
XCTLMOD  EQU   *
         STH   RZERO,OCPARM             SAVE MESSAGE NUMBER    @Y17XARA
         MVC   OCWTG(TWO),WRTMOD        SET RETURN TO WRT MSG MOD
         MVC   OPCLDNME+VARINAME(TWO),CKPTMOD SET XCTL TO CKPOINT MOD
EXIT     EQU   *
         LR    RPARM,ROPCAVT            PASS OP CTL AVT PTR
         XCTL  SF=(E,OPCXCTL)           CALL NXT MODULE
CMDINV   EQU   *                                                 S22024
         LA    RZERO,IED018I            SET COMMAND INVALID    @Y17XARA
         B     XCTLERR                  EXIT ERROR               S22024
ALON     EQU   *
         LA    RZERO,IED024I            SET ALREADY ON RESPONSE@Y17XARA
         B     XCTLERRX                 EXIT ERROR             @Y17XARA
ALOF     EQU   *
         LA    RZERO,IED030I            SET ALREADY OFF RESPONS@Y17XARA
         B     XCTLERRX                 EXIT ERROR             @Y17XARA
NOWAY    EQU   *
         LA    RZERO,IED055I            SET CANT BE STARTED    @Y17XARA
         B     XCTLERR                  EXIT ERROR             @Y17XARA
XCTLERRX EQU   *                                               @Y17XARA
         CLI   OCSWITCH,OCSUBT          DTRACE = COMMAND       @Y17XARA
         BNE   XCTLIOTR                 NO , CHECK FOR ERROR , @Y17XARA
         MVC   OCBACKUP,DTRACE          MOVE DTRACE = TO MSG   @Y17XARA
         B     XCTLERR                  EXIT ERROR             @Y17XARA
XCTLIOTR EQU   *                                               @Y17XARA
         CLI   OCSWITCH,OCIOTR          TRACE=ERROR, COMMAND   @Y17XARA
         BNE   XCTLERR                  NO , CONTINUE          @Y17XARA
         MVC   OCBACKUP,ERROR           MOVE ERROR , TO MSG    @Y17XARA
XCTLERR  EQU   *                        EXIT ERROR             @Y17XARA
         MVC   OPCLDNME+THREE(TWO),ERRWRT SET UP FOR XCTL TO ERR WRT
         B     EXIT                     EXIT
         EJECT
**********************************************************************
* HEXTOBCD -   PARAMETERS PASSED IN REGS 7-10                        *
* HEX TO EBCDIC - SINK-R7 SOURCE-R8 COUNT-R9 RETURN-RA               *
*        SINK   - LOCATION FOR CONVERTED DATA - MUST BE AT LEAST     *
*                 TWICE THE SIZE OF COUNT.                           *
*        SOURCE - LOCATION WHERE HEX DATA IS STORED                  *
*        COUNT  - SIZE OF SOURCE AREA - IN BYTES                     *
*        RETURN - RETURN IS MADE TO THIS LOCATION                    *
**********************************************************************
         SPACE
R1       EQU   13                       REGISTER 13
R2       EQU   14                       REGISTER 14
R3       EQU   15                       REGISTER 15
R4       EQU   4                        REGISTER 4
R5       EQU   5                        REGISTER 5
R6       EQU   6                        REGISTER 6
R7       EQU   7                        REGISTER 7
R8       EQU   8                        REGISTER 8
R9       EQU   9                        REGISTER 9
RA       EQU   10                       REGISTER 10
ONE99    EQU   199                      ONE HUNDRED NINTY NINE
TWO50    EQU   250                      TWO HUNDRED FIFTY
         SPACE
         SPACE
HEXTOBCD EQU   *
         STM   R1,R3,OPCSAVE            SAVE CALLERS
         STM   R4,R6,OPCSAVE+TWELVE        REGISTERS
         LA    R1,TEN                  SET UP REGISTERS
         LA    R4,TWO50                  FOR CONVERSION
         LA    R5,ONE99                     CALCULATION
         SPACE
XX       IC    R3,ZERO(R8)             PICK UP HEX CHARS
         SLL   R3,TWENTY4              SET UP TO PICK
         LA    R6,TWO                  SET TWO PASSES TO CONVERT BYTE
         SPACE
YY       SR    R2,R2                     FIRST FOUR
         SLDL  R2,FOUR                     BITS TO BE CONVERTED
         SR    R2,R1                   IS HEX CHAR 0-9
         BM    ZZ                      YES, CONVERT TO FX
         AR    R2,R5                   NO, SET UP TO CONVERT TO CX
         SPACE
ZZ       AR    R2,R4                   CONVERT
         STC   R2,ZERO(R7)             STORE CONVERTED CHAR IN SINK
         LA    R7,ONE(R7)              BUMP SINK POINTER
         BCT   R6,YY                   BR TO PROCESS NEXT 4 BITS
         LA    R8,ONE(R8)              BUMP SOURCE POINTER
         BCT   R9,XX                   BR IF MORE DATA TO CONVERT
         LM    R1,R3,OPCSAVE            RESTORE CALLERS
         LM    R4,R6,OPCSAVE+TWELVE        REGISTERS
         BR    RA                      RETURN TO CALLER
         SPACE
**********************************************************************
TRACEON  DS    0F                                              @Z30X8RR
         DC    X'00000080'              TRACE REQUESTED        @OZ26897
TRACEOFF DS    0F                                              @Z30X8RR
         DC    X'FFFFFF7F'              TRACE NOT ACTIVE       @Z30X8RR
REQOFF   DS    0F                                              @YA12276
         DC    X'FEFFFFFF'              REQUEST NOT ACTIVE     @SA77825
HA8      DC    H'0008'                  HALFWORD 8             @SA77825
WRTMOD   DC    C'M1'                    MODIFY WRIGHT MODULE
ERRWRT   DC    C'03'                    ERROR WRIGHT MODULE
CKPTMOD  DC    C'06'                    INCIDENT CHECKPOINT INTERFACE
LTRC3705 DC    C'MB'                    3705 GOTRACE/NOTRACE MOD S22024
CLEARB   DC    X'00'                    CLEARS HI-ORDER BYTE   @Y17XARW
ALL      DC    C'ALL'                   ALL  OPTION            @Y17XARA
HOLD     DC    C'HOLD'                  HOLD OPTION            @Y17XARA
POST     DC    C'POST'                  POST OPTION            @Y17XARA
OFF      DC    C'OFF'                   OFF  OPTION            @Y17XARA
ON       DC    C'ON'                    ON   OPTION            @Y17XARA
DTRACE   DC    CL8'DTRACE ='            DTRACE =               @Y17XARA
ERROR    DC    CL8'ERROR ,'             TRACE=ERROR,           @Y17XARA
         SPACE 3                                               @Y17XARA
ZAPAREA  DC    50X'00'                  ZAPAREA                @Y17XARW
         SPACE 3                                               @Y17XARA
OPTFIELD DSECT                                                 @Y17XARA
OPTFLAG  DS    CL1                      DTRACE= OR ERROR FLAG  @Y17XARA
OPTALL   EQU   255                      ALL TRACES             @Y17XARA
OPT1     EQU   128                      OPTION 1               @Y17XARA
OPT2     EQU   64                       OPTION 2               @Y17XARA
OPT3     EQU   32                       OPTION 3               @Y17XARA
OPT4     EQU   16                       OPTION 4               @Y17XARA
OPT5     EQU   8                        OPTION 5               @Y17XARA
OPT6     EQU   4                        OPTION 6               @Y17XARA
OPT7     EQU   2                        OPTION 7               @Y17XARA
OPT8     EQU   1                        OPTION 8               @Y17XARA
OPTOFF   EQU   0                        NO TRACES              @Y17XARA
         EJECT
         TAVTD
         EJECT                                                 @Y17XARA
IEDQCVT  CVT                           DSECT CVT               @Y17XARA
         EJECT
         DCBD  DSORG=TX
         EJECT
         TDISPD                        DSECT DISPATCHER        @Y17XARA
         EJECT                                                 @Y17XARA
         TPRIOR                        DSECT PRIORITY          @Y17XADA
         EJECT                                                 @Y17XADA
         TLCBD
         EJECT
         TLGBD
         EJECT
         TPRFD                         DSECT BUFFER PREFIX     @Y17XARA
         EJECT                                                 @Y17XARA
         TOPCED
         EJECT
         TOPCAVTD
         EJECT
         TTCXD SVS                     DSECT TCX               @Y17XARA
         EJECT                                                 @Y17XARA
         TTNTD
         EJECT                                                 @OY15041
IEDQUCB  DSECT                                                 @OY15041
         IEFUCBOB                                              @OY15041
         END
