 /*******************************************************************/
 /*                                                          @Y30LB32*/
 /*  PROCEDURE NAME - CKCSNLEN (FSR SUBROUTINE)              @Y30LB32*/
 /*                                                          @Y30LB32*/
 /*  FUNCTION - CHECKS LENGTH OF CARTRIDGE SERIAL NUMBER     @Y30LB32*/
 /*             IF THE LENGTH OF THE CSN IS LESS THEN ELEVEN,@Y30LB32*/
 /*             ISSUES MESSAGE IDC2099I, IF CSN IS ELEVEN    @Y30LB32*/
 /*             A BLANK CHARACTER IS INSERTED AS THE FOURTH  @Y30LB32*/
 /*             CHARACTER                                    @Y30LB32*/
 /*                                                          @Y30LB32*/
 /*  INPUT - CSN, LENGTH OF CSN                              @Y30LB32*/
 /*                                                          @Y30LB32*/
 /*  OUTPUT - CSN, RETURN CODE IN REGISTER 15(RETURN CODE    @Y30LB32*/
 /*           ZERO IF CSN VALID AND FOUR IF CSN INVALID)     @Y30LB32*/
 /*                                                          @Y30LB32*/
 /********************************************************************/
 CKCSNLEN:                                                 /*@Y30LB32*/
    PROCEDURE(CKCSNVAL,                /* CSN VALUE          @Y30LB32*/
              CKLENGTH)                /* LENGTH OF CSN      @Y30LB32*/
    OPTIONS(NOSAVE,NOSAVEAREA);                            /*@Y30LB32*/

    /* DECLARATIONS FOR CKCSNLEN SUBROUTINE                  @Y30LB32*/
    DCL CKCSNVAL CHAR(12);             /* CSN VALUE          @Y30LB32*/
    DCL CKCSNADJ CHAR(12);             /* FOR ADJUSTED CSN   @Y30LB32*/
    DCL CKLENGTH FIXED(8);             /* LENGTH OF CSN      @Y30LB32*/
    DCL CKRETPTR PTR(31);              /* SAVE RETURN ADDR   @Y30LB32*/
    DCL CKSAVPTR PTR(31);              /* TO SAVE POINTER    @Y30LB32*/
    DCL CKPRTTAB CHAR(20) BDY(WORD);   /* PRINT TABLE AREA   @Y30LB32*/
    DCL CKCOND FIXED(15);
    /* DECLARATIONS OF CONSTANTS                             @Y30LB32*/
    DCL CKBLANK CHAR(1) CONSTANT(' '); /* BLANK TO INSERT IN @Y30LB32*/
                                       /* CSN                @Y30LB32*/
    DCL CKELEVEN BIT(8) CONSTANT('0B'X);/* TO CHECK LENGTH   @Y30LB32*/
                                       /* OF CSN AGAINST     @Y30LB32*/
    DCL CKMSGID CHAR(3) CONSTANT('CM0');/* MODULE MESSAGE ID @Y30LB32*/
    DCL MSG2099I PTR(8) CONSTANT(1);   /* INVALID CSN MSG    @Y30LB32*/
 @EJECT;
    /* THIS IS THE START OF THE CHECK CSN SUBROUTINE         @Y30LB32*/
    /* THE CALLERS RETURN ADDRESS IS SAVED                   @Y30LB32*/
    CKRETPTR = REG14;                                      /*@Y30LB32*/
    UTRACE = 'CKLN';                                       /*@Y30LB32*/
    /* SAVE POINTER TO CALLERS PRINT TABLE,                  @Y30LB32*/
    /* INITIALIZE THE PRINT ARGUMENT LIST                    @Y30LB32*/
    /* AND CONDITION CODE TO ZEROES                           @Y30LB32*/
    CKSAVPTR = DDSTRU;                                     /*@Y30LB32*/
    DDSTRU = ADDR(CKPRTTAB);                               /*@Y30LB32*/
    CKPRTTAB = NULLAREA;                                   /*@Y30LB32*/
    CKCOND = NULLAREA;                                     /*@Y30LB32*/
    /* CSN IS CHECKED FOR LENGTH OF ELEVEN CHARACTERS        @Y30LB32*/
    /* OR MORE                                               @Y30LB32*/
    IF CKLENGTH >= CKELEVEN THEN                           /*@Y30LB32*/
    /* CSN IS CHECKED FOR LENGTH OF ELEVEN AND IF IT IS      @Y30LB32
       ELEVEN A BLANK CHARACTER IS INSERTED AS FOURTH        @Y30LB32
       CHARACTER                                             @Y30LB32*/
      IF CKLENGTH = CKELEVEN THEN                          /*@Y30LB32*/
        DO;                                                /*@Y30LB32*/
          CKCSNADJ(4:4) = CKBLANK;                         /*@Y30LB32*/
          CKCSNADJ(5:12) = CKCSNVAL(4:11);                 /*@Y30LB32*/
          CKCSNVAL(4:12) = CKCSNADJ(4:12);                 /*@Y30LB32*/
        END;                                               /*@Y30LB32*/
      ELSE;                                                /*@Y30LB32*/
    /* IF CSN IS LESS THEN ELEVEN CHARACTERS ERROR           @Y30LB32*/
    /* MESSAGE IDC2099I IS WRITTEN AND CONDITION CODE SET    @Y30LB32*/
    ELSE                                                   /*@Y30LB32*/
      DO;                                                  /*@Y30LB32*/
        DARGSMOD = CKMSGID;                                /*@Y30LB32*/
        DARGSENT = MSG2099I;                               /*@Y30LB32*/
        DARGDBP = ADDR(CKCSNVAL);                          /*@Y30LB32*/
        DARGILP = LENGTH(CKCSNVAL);                        /*@Y30LB32*/
        DARGRETP = NULLAREA;                               /*@Y30LB32*/

        UPRINT (GDTTBL,PRTFILE,DDSTRU);                    /*@Y30LB32*/

        CKCOND = RCFAIL;                                   /*@Y30LB32*/
      END;                                                 /*@Y30LB32*/
    /* RESTORE PRINT AREA POINTER AND RETURN ADDRESS AND     @Y30LB32*/
    /* RETURN CONTROL TO CALLING FSR                         @Y30LB32*/
    DDSTRU = CKSAVPTR;                                     /*@Y30LB32*/
    REG14 = CKRETPTR;                                      /*@Y30LB32*/
    RETURN CODE(CKCOND);                                   /*@Y30LB32*/
    END CKCSNLEN;                                          /*@Y30LB32*/
