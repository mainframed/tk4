         TITLE 'IEDAY8  -  TIOC LOGOFF/TERMINATION ROUTINE'
IEDAY8   CSECT
**********************************************************************
*
*01* MODULE-NAME = IEDAY8
*
*01* DESCRIPTIVE-NAME = TIOC LOGOFF/TERMINATION ROUTINE
*
*01* COPYRIGHT = NONE
*
*01* STATUS
*            -  AUGUST 17,1977                               ZA25683
*
*01* FUNCTION = TERMINATING TIME SHARING MEMORIES. IEDAY8 INFORMS
*               TCAM THAT THIS USER SESSION IS ENDING, AND THAT
*               ALL HIS MESSAGES SHOULD BE SENT. IF THE MEMORY
*               IS TERMINATING ABNORMALLY,IEDAY8 SETS TSBOCAB
*               SO TSOUTPUT WILL SEND A SPECIAL MESSAGE TO THE
*               USER. IEDAY8 WAITS UNTIL ALL THE MESSAGES HAVE BEEN
*               SENT(WHERE UPON IT IS POSTED BY TSOUTPUT), AND THEN
*               FREES THE TSB.
*                                                               ZA25683
*               FOR TSO/VTAM RELATED ADDRESS SPACES, THERE ARE THREE
*               FUNCTION PERFORMED.                             ZA25683
*                  A. IF THE TERMINATING ADDRESS SPACE IS TCAS, ZA25683
*                     IKTAY81  IS INVOKED.                      ZA25683
*                  B. IF A TSO/VTAM ADDRESS SPACE IS TERMINATING,
*                     IKTAY82  IS INVOKED.                      ZA25683
*                  C. FOR A TSO/TCAM ADDRESS SPACE TERMINATING, ZA25683
*                     IKTAY83  IS INVOKED TO HANDLE TSO/VTAM ASID TPUTS
*                     TO THE TCAM ADDRESS SPACE.                ZA25683
*
*01* NOTES = N/A
*
*01* RESTRICTIONS = N/A
*
*02*   DEPENDENCIES = NONE
*
*02*   RESTRICTIONS = NONE
*
*02*   REGISTER-CONVENTIONS = SEE REGISTER EQUATES
*
*02*   PATCH-LABEL = AY8END
*
*01* MODULE-TYPE = ASSEMBLER LANGUAGE CSECT
*
*02*   PROCESSOR = ASSEMXF-370R
*
*02*   MODULE-SIZE = SEE EXTERNAL SYMBOL DICTIONARY TYPE SD ID-01
*
*02*   ATTRIBUTES = REENTRANT,REUSABLE,ENABLED
*
*01* ENTRY-POINT = IEDAY8,IEDAY802
*
*02*   PURPOSE = SEE FUNCTION
*
*02*   LINKAGE = IEDAY8 FROM TASK AND MEMORY TERMINATION
*                IEDAY802 FROM DISPATCHER
*
*01* INPUT = REGISTER 1 CONTAINS PARAMETER LIST
*            REGISTER 14 CONTAINS RETURN ADDRESS
*            REGISTER 13 CONTAINS ADDRESS OF STANDARD SAVE AREA
*            REGISTER 15 CONTAINS ENTRY POINT
*
*01* OUTPUT = REGISTER 0-14 WILL BE RESTORED
*             REGISTER 15 WILL CONTAIN A RETURN CODE
*
*01* EXIT-NORMAL = TO CALLERS ADDRESS IN REGISTER 14
*                  TO IKTAY81                                   ZA25683
*                  TO IKTAY82                                   ZA25683
*
*01* EXIT-ERROR = NONE
*
*01* EXTERNAL-REFERENCES = NONE
*
*02*   CONTROL-BLOCKS = TSB,CVT,TIOCRPT,ASCB,RMPL,TCX,AVT,TCAST ZA25683
*
*01* TABLES = NONE
*
*01* MACROS = MODESET,QTIP,WAIT
*
*01* CHANGE-ACTIVITY VS01852 - 5/24/73
*D007500                                                        ZA02526
*A018600,098500,888944,888960,888968,888972,889619-889679       ZA02526
*A910500                                                        ZA02526
*C259000,023497                                                 ZA02526
*
*01* CHANGE-ACTIVITY VS2 REL 4 - 9/2/75                         ZA25683
*D315500,316000,427000,532000-553000,672000,679000              ZA25683
*C018600,259000,301600,317000,318500,336000,469000,525000       ZA25683
*A020921-020938,022987-022988,023967-023972,023497              ZA25683
* 024876,301500,302000,315010-315290,319000,336500-337500       ZA25683
* 469599-471000,511500-514000,525500,526000,889839-889959       ZA25683
* 980500,981000                                                 ZA25683
*                                                               ZA25683
**********************************************************************
         EJECT
         ENTRY IEDAY802
*
*   REGISTER EQUATES
*
RWRK0    EQU   0                        WORK REGISTER
RPARM    EQU   1                        PARAMETER REG.
RWRK3    EQU   3                        WORK REGISTER
RWRK4    EQU   4                        WORK REGISTER
RTCX     EQU   5                        TCX ADDRESS             YS02019
RCVT     EQU   7                        CVT ADDR
RTSB     EQU   8                        TSB ADDR
RTIOC    EQU   9                        TIOCRPT ADDR
RASCB    EQU   10                       ASCB ADDR
RRMPL    EQU   11                       RMPL ADDR
RAVT     EQU   11                       AVT ADDR                ZA02526
RBASE    EQU   12                       BASE REG.
RSAVE    EQU   13                       SAVE AREA ADDR
RLINK    EQU   14                       LINK ADDR
REPA     EQU   15                       ENTRY POINT ADDR
RCODE    EQU   15                       RETURN CODE
*
*   MASKS AND DISPLACEMENTS
*
EC23     EQU   23                       QTIP ENTRY CODE
EC29     EQU   29                       QTIP ENTRY CODE
E12      EQU   12                       DECIMAL 12
E0       EQU   0                        NULL VALUE
UPHALF   EQU   B'1100'                  UPPER HALF OF WORD
E2       EQU   2                        DECIMAL 2
EC27     EQU   27                       QTIP 27 ENTRY CODE      YS02019
*
*   ESTABLISH ADDRESSABILITY
*
         STM   RLINK,RBASE,E12(RSAVE)   SAVE CALLER'S REG.5
         LR    RBASE,REPA               LOAD BASE REG.
         USING IEDAY8,RBASE             MODULE ADDRESSABILITY
         B     GETPTRS                  GO TO GET POINTERS
         DC    C'IEDAY8'                MODULE I.D.
         DC    X'5245'                  DATE - SEPTEMBER 2,1975 ZA25683
*
*   GET CONTROL BLOCK POINTERS
*
GETPTRS  EQU   *
         L     RRMPL,E0(,RPARM)         GET RMPL ADDRESS
         USING RMPL,RRMPL               RMPL ADDRESSABLE
         TM    RMPLFLG1,RMPLTERM        IS MEMORY TERMINATING   ZA25683
         BNO   EXIT                     GO EXIT                 ZA25683
         L     RASCB,RMPLASCB           GET ASCB ADDR
         USING ASCB,RASCB               ASCB ADDRESSABLE
         L     RCVT,CVTPTR              GET CVT ADDR            ZA25683
         USING CVT,RCVT                 CVT ADDRESSABLE         ZA25683
         L     RWRK4,CVTTCASP           GET TCAS TABLE ADDRESS  ZA25683
         L     RTSB,ASCBTSB             GET TSB ADDR            ZA25683
         LTR   RTSB,RTSB                IS THIS A TS MEMORY     ZA25683
         BNZ   CKVTAM                   YES,SEE IF IT IS A      ZA25683
*                                       TSO/VTAM MEMORY         ZA25683
         LTR   RWRK4,RWRK4              IS THERE A TCAS TABLE   ZA25683
         BZ    CKASTP                   NO TCAS TABLE AND NO    ZA25683
*                                       TSB - RETURN TO CALLER  ZA25683
         USING TCAST,RWRK4              ADDRESS THE TCAST       ZA25683
         L     RWRK3,TCASASCB-TCAST(,RWRK4) GET TCAS ASCB       ZA25683
         C     RWRK3,RMPLASCB           IS TCAS THE MEMORY THAT ZA25683
*                                       IS TERMINATING          ZA25683
         BNE   CKASTP                   NOT TCAS AND NO TSB -   ZA25683
*                                       RETURN TO CALLER        ZA25683
         L     REPA,TCSDOWN             TCAS IS GOING DOWN -    ZA25683
         BR    REPA                     GO TO IKTAY81,RETURN TO ZA25683
*                                       RTM2 FROM THERE.        ZA25683
CKVTAM   EQU   *                        SEE IF TSB IS FOR       ZA25683
*                                       TSO/VTAM                ZA25683
         USING TSB,RTSB                 GET TSB ADDRESSABILITY  ZA25683
         TM    TSBFLG5,TSBVTAM          IS THIS TSO/VTAM TSB    ZA25683
         BNO   CKASTP                   NO - LOOK FOR ASID TPUTSZA25683
         L     REPA,TSVDOWN             TSO/VTAM A.S. GOING     ZA25683
         BR    REPA                     DOWN - GO TO IKTAY82,   ZA25683
*                                       RETURN TO RTM2 FROM THERE
CKASTP   EQU   *                        LOOK FOR POSSIBLE ASID  ZA25683
*                                       TPUTS - EITHER TSO/TCAM ZA25683
*                                       UP OR TCAS TABLE EXITS  ZA25683
         L     RWRK3,CVTAQAVT           GET TCX ADDRESS         ZA25683
         LTR   RWRK3,RWRK3              IS TCAM RUNNING         YM01852
         BZ    CKTCAS                   NO, LOOK FOR TCAS TABLE ZA25683
         L     RWRK3,TCXRPT-IEDQTCX(,RWRK3) GET RPT ADDRESS     YM01852
         LTR   RWRK3,RWRK3              IS TS RUNNING           YM01852
         BNZ   MEMORY                   YES, PROCESS THE TSO/   ZA25683
*                                       TCAM ADDRESS SPACE      ZA25683
CKTCAS   LTR   RWRK4,RWRK4              TCAS TABLE AROUND       ZA25683
         BZ    EXIT                     NO - NEITHER TCAS TABLE ZA25683
*                                       NOR TSO/TCAM AROUND. NO ZA25683
*                                       ASID TPUTS POSSIBLE.EXITZA25683
*
*   MEMORY TERMINATION PROCESSING
*
MEMORY   EQU   *
         TM    RMPLFLG1,RMPLTYPE        IS MEMORY ABENDING
         BNO   QTIP23                   NO, DON'T CALL QTIP 29  ZA25683
         LTR   RWRK3,RWRK3              TSO/TCAM UP             ZA25683
         BZ    VTQT29                   NO,TCAS TABLE MUST BE   ZA25683
*                                       AROUND,LOOK FOR TSO/VTAMZA25683
*                                       ASID TPUTS              ZA25683
         LR    REPA,RRMPL               PASS RMPL ADDR IN 15
         QTIP  EC29                     CALL QTIP TO CLEAN UP
*                                       IN CASE A TASK IN THIS
*                                       MEMORY HAD A TPUT IN
*                                       PROGRESS TO (OR WAS
*                                       WAITING ON) ANOTHER TSB.
         LTR   RWRK4,RWRK4              TCAS TABLE AROUND       ZA25683
         BZ    QTIP23                   NO - GO CLEAN UP TIOC   ZA25683
*                                       CONTROL BLOCKS          ZA25683
VTQT29   L     REPA,VTQTIP29            LOOK FOR TSO/VTAM ASID  ZA25683
         BALR  RLINK,REPA               TPUTS BY INVOKING       ZA25683
*                                       IKTAY83                 ZA25683
*
QTIP23   EQU   *                                                ZA25683
         LTR   RTSB,RTSB                IF THIS IS NOT A TSO    ZA25683
         BZ    EXIT                     MEMORY, RETURN TO RTM2  ZA25683
         LR    REPA,RRMPL               PASS RMPL ADDR IN 15
         QTIP  EC23                     CALL QTIP TO CLEAN UP
*                                       TIOC CONTROL BLOCKS
         LTR   RCODE,RCODE              MUST WE WAIT FOR TCAM
*                                       TO FINISH PROCESSING
*                                       FOR THIS USER
         BZ    CLEAN                    NO, GO CLEAN UP
*
         WAIT  ECB=TSBLECB,LONG=YES     WAIT FOR TCAM
*
CLEAN    EQU   *
         MODESET EXTKEY=TCAM            CHANGE TO TCAM'S KEY    YS02019
         SR    RWRK0,RWRK0              CLEAR REG.
         ST    RWRK0,TSBASCBA           CLEAR TSB ADDR IN ASCB  YS02019
*
*   UPDATE TIOC USER COUNT
*
         TM    CVTTCMFG,CVTTCRDY        HAS TCAM ENDED
         BNO   EXIT                     YES, TS ENDED WITH IT
         L     RTIOC,CVTAQAVT           GET ADDR OF TCX
         L     RTIOC,TCXRPT-IEDQTCX(,RTIOC)  GET TIOCRPT ADDR
         USING TIOCRPT,RTIOC            TIOCRPT ADDRESSABLE
         L     RWRK3,TIOCUSCT-E2        GET WORD CONTAINING CNT
REDO     EQU   *
         LA    RWRK4,E0(,RWRK3)         INSURE VALUE IS POSITIVE
*                                       BEFORE DECREMENTING
         BCTR  RWRK4,E0                 DECREMENT USER COUNT
         ICM   RWRK4,UPHALF,TIOCUSCT-E2 RESTORE UPPER HALF
         CS    RWRK3,RWRK4,TIOCUSCT-E2  UPDATE COUNT
         BNE   REDO                     RETRY IF NOT SUCCESSFUL
*
*   IF TIME SHARING IS ENDING & THIS WAS THE LAST USER,
*   ISSUE TSEND TO TERMINATE TIME SHARING
*
         TM    TIOCFLG,TIOCTSAB+TIOCSTOP IS TS ENDING
         BZ    EXIT                     NO, EXIT
         LH    RWRK4,TIOCUSCT           GET USE COUNT           YM06501
         LTR   RWRK4,RWRK4              IS LAST USER GONE       YM06501
         BNZ   EXIT                     NO, EXIT
         TSEND                          ,TERMINATE TIME SHARING
*
EXIT     EQU   *
         MODESET EXTKEY=SUPR            RESTORE CALLER'S KEY
         LM    RLINK,RBASE,E12(RSAVE)   RESTORE REGISTERS
         SR    RCODE,RCODE              ZERO RETURN CODE        YS02019
         BR    RLINK                    RETURN TO CALLER
*
         EJECT
IEDAY802 EQU   *
***********************************************************************
*        IEDAY802 IS USED IN LINE DISCONNECT RECOVERY FOR TIMING THE  *
*          RECONNECT PERIOD AND PASSING CONTROL TO IEDAY88 BY         *
*          ISSUING A QTIP 27. QTIP 27 PASSES BACK A RETURN CODE OF 0  *
*          OR 4 IN REG 15. IF IT IS ZERO, THEN ALL USERS HAVE NOT     *
*          BEEN PROCESSED.                                            *
*          IF RETURN CODE IS FOUR THEN ALL USERS HAVE                 *
*          BEEN LOGGED OFF OR HAVE RE-LOGGED ON.                      *
***********************************************************************
         STM   RLINK,RBASE,E12(RSAVE)  SAVE CALLERS REGISTERS   YS02019
         BALR  RBASE,RWRK0             .SET UP                  YS02019
         USING *,RBASE                 .ADDRESSABILITY          YS02019
         B     FINDRPT                 BR AROUND MODULE ID      YS02019
         DC    C'IEDAY802'                                      YS02019
FINDRPT  EQU   *
         L     RCVT,CVTPTR             GET CVT ADDRESS          YS02019
         L     RTCX,CVTAQAVT-CVT(,RCVT) GET TCX ADDRESS         YS02019
         L     RTIOC,TCXRPT-IEDQTCX(,RTCX) GET TIOCRPT ADDRESS  YS02019
         L     RAVT,TCXAVT-IEDQTCX(,RTCX) GET AVT ADDRESS       ZA02526
         USING IEDQAVTD,RAVT           AVT ADDRESSABILITY       ZA02526
         USING TIOCRPT,RTIOC           TIOCRPT ADDRESSABILITY   YS02019
TIOCWAIT EQU   *
         TM    AVTBIT3,AVTTSAB         HAS TSO ABENDED?         ZA02526
         BO    END                     YES, THEN RETURN         ZA02526
         WAIT  ECB=TIOCTECB            WAIT FOR HANGUP TO OCCUR YS02019
TIME     EQU   *
         STIMER WAIT,BINTVL=ONEMIN     1 MINUTE FOR RECONNECT   YS02019
         LR    RCODE,RTIOC             PASS RPT ADDR TO QTIP    YS02019
         QTIP  EC27                    GO TO IEDAY88            YS02019
*                                      TSB                      YS02019
         LTR   RCODE,RCODE             DO MORE USERS NEED TO BE YS02019
*                                      PROCESSED                YS02019
         BZ    TIME                    YES,BRANCH               YS02019
         B     TIOCWAIT                NO,WAIT FOR NEXT HANGUP  YS02019
END      EQU   *                                                ZA02526
         LM    RLINK,RBASE,E12(RSAVE)  RESTOR CALLERS REGISTERS ZA02526
         SR    RCODE,RCODE             ZERO RETURN CODE         ZA02526
         BR    RLINK                   RETURN TO CALLER         ZA02526
         SPACE 3
ONEMIN   DC    F'6000'                 ONE MINUTE TIME INTERVAL YS02019
TCSDOWN  DC    V(IKTAY81)               TCAS A.S. DOWN ROUTINE  ZA25683
TSVDOWN  DC    V(IKTAY82)               TSO/VTAM A.S. DOWN RTN  ZA25683
VTQTIP29 DC    V(IKTAY83)               CLEAN UP TSO/VTAM ASID  ZA25683
*                                       TPUTS ROUTINE           ZA25683
AY8END   DC    9F'0'                    PATCH AREA
         EJECT
         IHAASCB
         EJECT
         TAVTD                                                  ZA02526
         EJECT
CVT      DSECT
         CVT
         EJECT
         IHARMPL
         EJECT
         TTCXD
         EJECT
         IKJTIOCP
         EJECT
         IKJTSB
         EJECT                                                  ZA25683
         IKTTCAST                                               ZA25683
         END
