         TITLE 'IGC0009D - FIRST LOAD OF SVC 94'
IGC0009D CSECT
***********************************************************************
*                                                                     *
*        VS/2 RELEASE 4.0 CHANGES                                     *
* A 990254, D 990244                                            ZA26063
* A 988435-998440,990102,990120-990186,994031,994189,994262     ZA24648
* A 994431,994456                                               ZA24648
*D988044                             CODE FOR SU13**********    ZA16607
*C989874                                                        ZA16607
*A989614,990640,990783-990795,994247                            ZA16607
*                                      CODE FOR SU11********  X ZA16607
*D988198,990098,990102,991083-991099,991883,991887            X ZA16607
*C988224,989874,990309,990987,991599,992099,992159            X ZA16607
*A990273-990292,990296,990297,990904-990909,990993,992083-992095ZA16607
* 990276-990293,990295,990297,990993,992083-992095,           X ZA16607
* 991107-991307,994428-994450                                 X ZA16607
* 994428-994447                                               X ZA16607
* C989874,990105                                                ZA16639
* A990097,990100,994261,994263,994428-994430,994453-994455      ZA16639
*                                                                     *
*        VS/2 DELETIONS                                               *
*990210,990260,990290,990360                                     YM0341
*D995439,995779-995879,995919                                    YM2851
*                                                                     *
*              TIOC/VTIOC SPLIT                                 OZ27085
*  RESEQUENCE 988030-988346                                     OZ27085
*C988224,C988240-988260,D988294-988338,D988426-988434,C988516   OZ27085
*A988517,D988738,D988754-988762,D988848-988924,D989030          OZ27085
*D989110-989220,D989240-989610,D989620-989660,D989724           OZ27085
*D989900-990110,C990309,D990803-990977,D990987-991067,C992091   OZ27085
*D992023-992087,A991919-992091,A992099,D992319-994027           OZ27085
*D994059-994187,D994195-994243,D994251-998359,D998399-998699    OZ27085
*D998743-998744,D998859-998879,D998939-998999,D999351-999371    OZ27085
*A994195-994203,A988211-988219                                  OZ27085
*D988858,989720,989954-989994                                   OZ27085
*STATUS -                                                             *
*         OCTOBER 1977                                          OZ27085
*A991076-991090 A994055-994056                                 @ZA29992
*                                                                     *
* FUNCTION - THIS MODULE IS THE ENTRY POINT OF SVC 94. ITS FUNCTION   *
*    IS TO OBTAIN THE LOCAL AND CMS LOCKS, AND PASS CONTROL TO EITHER *
*    THE TSO/TCAM SVC 94 ROUTER OR THE TSO/VTAM SVC 94 ROUTER.  OZ27085
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGC0009D - CONTROL IS RECEIVED WHEN SVC 94 IS ISSUED.       *
* INPUT -                                                             *
*    REG 0 - ENTRY CODE IN HIGH ORDER BYTE,PARAMETERS IN REMAINDER    *
*    REG 1 - PARAMETERS                                               *
*    REG 3 - CVT                                                      *
*    REG 4 - TCB                                                      *
*    REG 5 - SVRB                                                     *
*                                                                     *
* OUTPUT - RETURN CODE IN REGISTER 15                                 *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IKTTMPX2                                              ZA24648
*            REG 15 RETURN CODE FROM IKTTMPX2                   ZA24648
*                    RC < 0 NORMAL PROCESSING SHOULD CONTINUE   ZA24648
*                       >=0 THE SVC SHOULD RETURN TO THE CALLER ZA24648
*                           PASSING THRU THE VALUE IN REG 15    ZA24648
*                                                                     *
* EXITS - NORMAL                                                      *
*         BRANCH TO NEXT CSECT WITH LOCAL AND CMS LOCKS HELD.         *
*    REG 0 - ENTRY CODE IN HIGH ORDER BYTE,PARAMETERS IN REMAINDER    *
*    REG 1 - PARAMETERS                                               *
*    REG 3 - CVT                                                YS02019
*    REG 4 - TCB                                                YS02019
*    REG 5 - SVRB                                               OZ27085
*    REG 8 - TSB                                                OZ27085
*    REG 10 - CONSTANTS                                         YS02019
*    REG 14 - RETURN ADDRESS                                    YS02019
*                                                                     *
* EXITS - ERROR                                                       *
*         BR 14 WITH RETURN CODE IN REGISTER 15                 YS02019
*                                                                     *
* TABLES AND WORK AREAS - SEE DSECTS AT END OF MODULE                 *
*                                                                     *
* ATTRIBUTES - REENTRANT                                        YS02019
*                                                                     *
* CHARACTER CODE DEPENDENCY - NONE                                    *
*                                                                     *
* NOTES                                                               *
*    YS02019 - VS/2 RELEASE 2 CHANGES                                 *
*                                                                     *
***********************************************************************
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
********
RPARM0   EQU   0                        PARAMETER REG 0
RWORK0   EQU   0                        WORK REGISTER
RPARM1   EQU   1                        PARAMETER REG 1
RWORK2   EQU   2                        WORK REGISTER
RCVT     EQU   3                        POINTS TO CVT
RTCB     EQU   4                        POINTS TO TCB
RTSB     EQU   8                        POINTS TO TSB           OZ27085
RRB      EQU   5                        POINTS TO SVRB          YS02019
RWORK6   EQU   6                        WORK REGISTER           YS02019
RWORK11  EQU   11                       WORK REGISTER           YS02019
RBASE    EQU   12                       BASE REGISTER
R13      EQU   13                       REGISTER 13
R14      EQU   14                       REGISTER 14
RLINK    EQU   14                       LINK REGISTER
RTRNSTBL EQU   14                       POINTS TO TRANSLATE TABLES
RBRANCH  EQU   15                       BRANCH REGISTER
RCODE    EQU   15                       RETURN CODE REGISTER
         SPACE 2
********
******** DISPLACEMENT,LENGTH AND MASK EQUATES
********
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG.
D14      EQU   14                       DISPLACEMENT OF 14
LNSZ132  EQU   132                      LINE SIZE 132           ZA16607
         SPACE 3
********
******** DEFINE ALTERNATE ENTRY POINTS
********
         SPACE 3
********
******** ESTABLISH BASE REGISTERS AND ADDRESSABILITY
********
         BALR  RBASE,RWORK0             ADDRESSABILITY
         USING *,RBASE                  FOR THIS CSECT
         USING CVT,RCVT                 CVT ADDRESSABILITY
         USING TCB,RTCB                 TCB ADDRESSABILITY
         USING RBSECT,RRB               SVRB ADDRESSABLE        YS02019
         B     PASTID                   SKIP MODULE ID.         YS02019
         DC    C'IGC0009D'              MODULE ID.              YS02019
         DC    X'7269'                  DATE - SEPT  26, 1977   OZ27085
PASTID   EQU   *                                                YS02019
         L     RBRANCH,USEREXIT         GET @ OF TERM MGR EXIT  ZA24648
         LTR   RBRANCH,RBRANCH          IS THERE A USER EXIT    ZA24648
         BZ    GETLKS                   BRANCH ON NO            ZA24648
         LR    RWORK11,RLINK            SAVE RETURN @           ZA24648
         BALR  RLINK,RBRANCH            BRANCH TO USER EXIT     ZA24648
         LR    RLINK,RWORK11            RESTORE RETURN @        ZA24648
         LTR   RCODE,RCODE              TEST RETURN CODE        ZA24648
         BNMR  RLINK                    RETURN ON CODE >=0      ZA24648
********
******** GET LOCKS AND ENSURE TCAM AND TSO ARE ACTIVE
********
GETLKS   EQU   *                                                YS02019
         LA    R13,RBEXSAVE             SAVE AREA FOR SETLOCK   ZA26063
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=SAVE,       YS02019X
               RELATED=('TSB,TIOCRPT','SVC EXIT PROLOGUE')
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,REGS=SAVE,         YS02019X
               RELATED=('TSB,TIOCRPT','SVC EXIT PROLOGUE')
         L     RWORK6,PSAAOLD-PSA       ADDRRESS OF ASCB      X ZA16607
         USING ASCB,RWORK6              ASCB ADDRESSABILITY   X ZA16607
         L     RWORK6,ASCBTSB           ADDRESS OF TSB        X ZA16607
         USING TSB,RWORK6               TSB IS ADDRESSABLE    X ZA16607
         LTR   RWORK6,RWORK6            CHECK IF BACKGRND TASKX ZA16607
         BZ    CKTCAM                   CHECK FOR TSO/TCAM UP X ZA16607
*                                       RWORK11 IS STILL ZERO X ZA16607
*                                       (NO TSO/TCAM TSB FOUND) ZA16607
         TM    TSBFLG5,TSBVTAM          IS THIS A TSO/VTAM TSBX ZA16607
         BO    BRANCH                   YES, INVOKE PROPER MODX ZA16607
CKTCAM   L     RWORK6,CVTAQAVT          GET ADDR OF TCX       X ZA16607
         LTR   RWORK6,RWORK6            IS TCAM ACTIVE          YS02019
         BZ    BACKGRND                 NO, TREAT AS NO-OP      YS02019
         L     RWORK6,TCXRPT-IEDQTCX(,RWORK6) ADDR OF TIOCRPT   YS02019
         LTR   RWORK6,RWORK6            IS TIME SHARING ACTIVE  YS02019
         BNZ   BRANCH                   YES, PROCEED            OZ27085
BACKGRND EQU   *
         CLM   RPARM0,BYTE1,T11         IS ENTRY CODE 11?       ZA16607
         BE    GETSIZ                   YES, DEFAULT GTSIZE     ZA16607
         SR    RCODE,RCODE              INDICATE SVC94 ISSUED BY BKGND
         SR    RPARM0,RPARM0            CLEAR REG 0             YM07152
         SR    RPARM1,RPARM1            CLEAR REG 1             YM07152
         BR    R14                      RETURN TO CALLER        YS02019
*******
*******  WHEN GTSIZE HAS BEEN ISSUED FROM A BACKGROUND TASK
*******  PROVIDE A DEFAULT TERMINAL LINESIZE
*******
GETSIZ   SR    RCODE,RCODE              SUCCESSFUL RETURN CODE  ZA16607
         SR    RPARM0,RPARM0            DEFAULT SCREENLENGTH=0  ZA16607
         LA    RPARM1,LNSZ132           DEFAULT LINESIZE=132    ZA16607
         BR    R14                      RETURN TO CALLER        ZA16607
***********************************************************************
*        GET POINTER TO TSB                                     YS02019
***********************************************************************
BRANCH   EQU   *                                              X ZA16607
         CLM   RPARM0,BYTE1,T0          IF TCABEND, NOTSB      @ZA29992
         BE    BRTCAM                   GO TO TCAM MODULE      @ZA29992
         CLM   RPARM0,BYTE1,T2          IF TSEND, NOTSB        @ZA29992
         BE    BRTCAM                   GO TO TCAM MODULE      @ZA29992
         L     RWORK6,PSAAOLD-PSA       ADDRESS OF ASCB       X ZA16607
         USING ASCB,RWORK6              ASCB ADDRESSABILITY   X ZA16607
         L     RTSB,ASCBTSB             ADDRESS OF TSB        X ZA16607
         DROP  RRB                      RB ADDRESSABILITY ENDSX ZA16607
         DROP  RWORK6                                         X ZA16607
         USING TSB,RTSB                 TSB IS ADDRESSABLE    X ZA16607
         LTR   RTSB,RTSB                WAS TCAM TSB FOUND    X ZA16607
         BZ    BACKGRND                 NO,  BACKGRND TASK      YS02019
********
******** BRANCH TO PROPER MODULE
********
         TM    TSBFLG5,TSBVTAM          IS THIS A TSO VTAM TSB  OZ27085
         BO    BRVTAM                   YES- VTAM BRANCH        OZ27085
BRTCAM   L     RWORK2,TCAMMOD           GET TSO/TCAM BRNCH ADDR OZ27085
         B     BRNCH                    BRANCH TO MODULE      X ZA16607
BRVTAM   L     RWORK2,VTAMMOD           GET BRANCH ADDRESS      OZ27085
BRNCH    LR    RBASE,RWORK2             SET UP NEW BASE REG   X ZA16607
         BR    RBASE                    GO TO PROPER ROUTINE     Y01018
PAT      DC    13F'0'                   MAINTENANCE PATCH AREA  ZA24648
         DS    0F
CONST    EQU   *
USEREXIT DC    V(IKTTMPX2)              TERMINAL CONTROLER EXIT ZA24648
TCAMMOD  DC    V(IGG0940H)              TCAM SVC94 ROUTER       OZ27085
VTAMMOD  DC    V(IKT0009D)              VTAM SVC94 ROUTER       OZ27085
T0       DC    AL1(0)                   CONSTANT OF 0 FOR CLM  @ZA29992
T2       DC    AL1(2)                   CONSTANT OF 2 FOR CLM  @ZA29992
T11      DC    AL1(11)                  CONSTANT OF 11 FOR CLM  ZA16607
CONSTANT DSECT
         SPACE 3
         EJECT                                                  YS02019
         IKJTCB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IKJRB
         EJECT
         IKJTSB
         EJECT
         IHAASCB
         EJECT
         IHAPSA
         EJECT
         TTCXD
         END
