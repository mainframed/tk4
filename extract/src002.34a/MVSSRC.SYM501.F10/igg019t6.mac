19T6     TITLE 'IGG019T6 - TS QSAM GET/PUT ROUTINE'
IGG019T6 CSECT
***********************************************************************
*                                                                     *
* RELEASE 21.8 CHANGES                                                *
*A828500-829000,931100-932900,942500                            SA66590
*C003000                                                        SA66590
*D287500,287700,571300-571600                                   SA69503
*A315500-317940,571300-571764                                   SA69503
* RELEASE 21 DELETIONS/CHANGES                                        *
*1395287000,725000-727000                                        S21042
*1395931200-932400                                               A42898
*                                                                 M2432
*                                                                M6374
*                                                                     *
*STATUS CHANGE LEVEL 001
*                                                                     *
* NAME - IGG019T6 (TS GET/PUT)                                        *
*                                                                     *
* FUNCTION/OPERATION - THIS INTERFACE ROUTINE PERFORMS THE FOLLOWING  *
*                      FUNCTIONS -                                    *
*                                                                     *
*                      FOR GET -                                      *
*                                                                     *
*                      PREPARE PARAMETERS FOR TGET AND ISSUE TGET,    *
*                      WHICH CAUSES A RECORD TO BE RETRIEVED FROM     *
*                      THE TIOC BUFFER AND PLACED IN EITHER THE FIRST *
*                      BUFFER OF THE BUFFER POOL CONTROL BLOCK (LOCATE*
*                      MODE) OR IN A USER SPECIFIED AREA (MOVE MODE). *
*                      UPON REGAIN CONTROL CONTROL FROM TGET, RETURN  *
*                      ADDRESS OF THE RECORD TO CALLER IN REGISTER 1. *
*                                                                     *
*                      FOR PUTX -                                     *
*                                                                     *
*                      PREPARE PARAMETERS FOR TPUT AND ISSUE TPUT,    *
*                      WHICH CAUSES A RECORD FROM THE FIRST BUFFER OF *
*                      THE BUFFER POOL CONTROL BLOCK (LOCATE MODE),   *
*                      OR A USER SPECIFIED AREA (MOVE MODE), TO BE    *
*                      WRITTEN ON THE TERMINAL.                       *
*                                                                     *
*                      UPON REGAIN CONTROL FROM TPUT, RETURN ADDRESS  *
*                      OF BUFFER TO CALLER IN REGISTER 1.             *
*                                                                     *
* ENTRY POINTS - GET - IGG019T6                                       *
*                PUTX - IGG019T6 + 4                                  *
*                TRUNC/RELSE - IGG019T6 +8                            *
*                                                                     *
*                THIS INTERFACE ROUTINE RECEIVES CONTROL WHEN THE     *
*                FOLLOWING CONDITIONS ARE MET.                        *
*                                                                     *
*                THE TASK IS TIME SHARING.                            *
*                THE DCB OPENED IS FOR QSAM UPDAT.                    *
*                A GET, PUTX, TRUNC OR RELSE MACRO INSTRUCTION IS     *
*                ISSED.                                               *
*                                                                     *
* INPUT - DCB ADDRESS IN REGISTER 1.                                  *
*                                                                     *
* OUTPUT - FOR GET  - AN EDITED (SPECIALLY-INDICATED CHARACTERS ARE   *
*                     DELETED) BLOCK OF TEXT MOVED IN THE BUFFER.     *
*                                                                     *
*          FOR PUTX - AN EDITED (SPECIALLY-INDICATED CHARACTERS ARE   *
*                     DELETED) BLOCK OF TEXT WRITTEN TO THE TERMINAL. *
*                                                                     *
* EXIT, NORMAL - RETURN CONTROL TO CALLER VIA A 'BR 14' INSTRUCTION.  *
*                                                                     *
*                WHEN END OF FILE IS REACHED ON GET, TAKE EOD EXIT    *
*                VIA A 'BR 15' INSTRUCTION.                           *
*                                                                     *
* EXIT, ERROR - WHEN END OF FILE IS REACHED ON GET, BUT NO EOD        *
*               ROUTINE IS FURNISHED, JOB WILL BE ABENDED WITH        *
*               SYSTEM CODE 337.                                      *
*             - WHEN VARIABLE-LENGTH RECORDS ARE TOO SHORT (4 OR      *
*               5 BYTES, DEPENDING ON THE PRESENCE OF CONTROL         *
*               CHARACTERS), JOB WILL BE ABENDED WITH SYSTEM CODE 002.*
*                                                                     *
* EXTERNAL REFERENCE - NONE.                                          *
*                                                                     *
* TABLE/WORK AREAS - NONE.                                            *
*                                                                     *
* ATTRIBUTES - REENTRANT, REUSABLE.                                   *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
********
R0       EQU   0                        POINTS AT IN DCB FROM PUTX
*                                       MACRO EXPANSION
RSW      EQU   0                        SWITCH FOR FOLDING CHARS
R1       EQU   1                        POINTS DCB ADDR FROM GET/PUTX
*                                       MACRO EXPANSION
R2       EQU   2                        REGISTER 2
RDCB     EQU   2                        POINTS AT DCB
RBUFAD   EQU   3                        POINTS AT BFR
RBUFSZ   EQU   4                        CONTAINS BFR SIZE
RPUTX    EQU   5                        SIGNALS FOR PUTX
RDEB     EQU   6                        POINTS AT DEB
RPTR     EQU   9                        POINTS AT BFR FOR FOLDING MSG
RWORK    EQU   10                       WORK REGISTER
RENDMSG  EQU   11                       POINTS AT END OF MESSAGE
RBASE    EQU   12                       BASE REGISTER
R12      EQU   12                       REGISTER 12
R13      EQU   13                       POINTS AT CALLING RTN SAVE AREA
R14      EQU   14                       POINTS AT RETURN ADDR OF
*                                       CALLING RTN
R15      EQU   15                       POINTS AT BRANCH ADDR
RRETURN  EQU   15                       CONTAINS RETURN CODE
         SPACE 3
********
******** EQUATES FOR DISPLACEMENTS, LENGTH, AND MASKS
********
C3       EQU   3                        CONSTANT OF 3
C15      EQU   15                       CONSTANT OF 15
M01      EQU   X'01'                    MASK USED TO TEST EOD RTN ADDR
*                                       MASK FOR OUTPUT MODE IN
*                                       DEBOPATB
*                                       MASK FOR DATA MODE IN DECMACR
MC0      EQU   X'C0'                    U-TYPE RECORD
M40      EQU   X'40'                    V-TYPE RECORD
M06      EQU   X'06'                    ASA OR MACHINE CHAR USED
M7F      EQU   X'7F'                    MASK FOR TURNING OFF WRITE BIT
M80      EQU   X'80'                    WRITE BIT IN DCBOFLGS
M12      EQU   X'12'                    MASK FOR SHIFTING REGISTER
EABD174  EQU   174                                               S21042
M8       EQU   8                        USED TO SHIFT REGISTER
D0       EQU   0                        DISPLACEMENT
D1       EQU   1                        DISPLACEMENT
D2       EQU   2                        DISPLACEMENT
D4       EQU   4                        DISPLACEMENT
D5       EQU   5                        DISPLACEMENT
D12      EQU   12                       DISPLACEMENT
D20      EQU   20                       DISPLACEMENT
D28      EQU   28                       DISPLACEMENT
L2       EQU   2                        LENGTH OF 2
*****
*****       MASKS FOR ISOLATING LOWERCASE ALPHA CHARACTERS
*****       FOR FOLDING TO UPPERCASE
*****
M81      EQU   X'81'                    LOWERCASE A             SA69503
MA9      EQU   X'A9'                    LOWERCASE Z             SA69503
M89      EQU   X'89'                    LOWERCASE I             SA69503
M90      EQU   X'90'                    SPECIAL CHARACTER       SA69503
M99      EQU   X'99'                    LOWERCASE R             SA69503
MA1      EQU   X'A1'                    SPECIAL CHARACTER       SA69503
UPPER    EQU   X'40'                    USED TO FOLD MSG        SA69503
         SPACE 3
********
******** GET ENTRY
********
         USING IGG019T6,R15             USE ENTRY ADDR AS BASE ADDR
         USING IHADCB,RDCB              ESTABLISH ADDRESSABILITY FOR
*                                       DCB
         USING DEB,RDEB                 ESTABLISH ADDRESSABILITY
         B     STGET                    BR PAST PUTX ENTRY
********
******** PUTX ENTRY
********
         B     STPUTX                   UNCONDITIONAL BRANCH
         SPACE 3
********
******** TRUNC/RELSE ENTRY
********
         BR  R14                        NOP RETURN CONTROL TO CALLER
STGET    EQU   *
         STM   R14,R12,D12(R13)         SAVE REGISTERS
         SR    RPUTX,RPUTX              CLEAR REG FOR GET ENTRY
         B     STUPBASE                 GO TO SET UP BASE
STPUTX   EQU   *
         STM   R14,R12,D12(R13)         SAVE REGISTERS
         LA    RPUTX,D1                 PUT '1' IN REG TO SIGNAL PUTX
STUPBASE EQU   *
         LR    RBASE,R15                LOAD BASE REGISTER
         DROP  R15                      GET RID OF PREVIOUS BASE REG
         USING IGG019T6,RBASE           USE REG 12 AS BASE REG
         SPACE 3
********
******** GET BUFFER ADDRESS
********
         LR    RDCB,R1                  SAVE DCB ADDR
         L     RBUFAD,DCBBUFCB          LOAD ADDR OF BFR CONTROL BLK
         L     RBUFAD,D0(RBUFAD)        LOAD ADDR OF BFR
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
         SPACE 3
         SPACE 3
********
******** TEST FOR GET OR PUT AND TAKE DIFFERENT ROUTES
********
         LTR   RPUTX,RPUTX              IS THIS A GET OPERATION
         BNZ   PUTOP                    BRANCH IF PUT OPERATION
         SPACE 3
********
******** TEST FOR RECORD TYPE AND GET BFR SIZE FOR GET OPERATION
********
GETOP    EQU   *
         LH    RBUFSZ,DEBLRECL          GET BFR SIZE FROM DEBLRECL
         TM    DCBRECFM,M80             CHECK FOR U-TYPE OR F-TYPE
*                                       RECORD
         BO    GETREADY                 BRANCH IF YES
VRECORD  EQU   *
         TM    DCBMACRF,M01             TEST FOR DATA MODE
         BO    GETREADY                 BRANCH IF YES
         SH    RBUFSZ,FOUR              SUBTRACT 4 FOR CONTROL WORD
         LA    RBUFAD,D4(RBUFAD)        UPDATE BFR PTR TO PASS CONTROL
*                                       WORD
GETREADY EQU   *
         LR    R1,RBUFAD                LOAD BFR ADDR
         LR    R0,RBUFSZ                LOAD BFR SIZE
         TGET  (1),(0)                  ISSUE TGET (UPON RETURN R1 HAS
*                                       MSG LENGTH)
         SPACE 3
********
******** TEST RETURN CODE FROM TGET
********
CKATTN   EQU   *
         CH    RRETURN,EIGHT            CHECK FOR ATTENTION INTERRUPT
         BE    GETREADY                 YES, GO TO REISSUE TGET
         NI    DCBOFLGS,M7F             TURN OFF WRITE BIT
*                                       (THIS IS A READ OPERATION)
         SPACE 3
********
******** FOLD INPUT LOWERCASE ALPHA TO UPPERCASE
********
         LTR   R1,R1                    IS THERE ANY MSG TO FOLD
         BZ    CKMORMSG                 NO, DO NOT FOLD
         LR    RPTR,RBUFAD              POINTS AT START OF BFR
         LA    RWORK,D1                 SET UP LOOP INDEX
         LR    RENDMSG,RBUFAD           POINTS AT START OF BUFFER
         AR    RENDMSG,R1               POINTS AT END OF BFR PLUS 1
         BCTR  RENDMSG,R0               POINTS AT END OF BUFFER
LOOP     EQU   *
         CLI   D0(RPTR),M81             COMPARE TO LOWER A      SA69503
         BL    TEST                     IF LOW, DONT FOLD       SA69503
         CLI   D0(RPTR),MA9             COMPARE TO LOWER Z      SA69503
         BH    TEST                     IF HIGH, DONT FOLD      SA69503
*****
*****       ONLY LOWERCASE ALPHA & IMBEDDED SPECIAL CHARACTERS
*****       WILL FALL THROUGH THE ABOVE TESTS
*****
         CLI   D0(RPTR),M89             COMPARE TO LOWER I      SA69503
         BNH   FOLD                     FOLD LOWER A-I          SA69503
         CLI   D0(RPTR),MA1             COMPARE TO SPEC         SA69503
         BH    FOLD                     FOLD LOWER S-Z          SA69503
         CLI   D0(RPTR),M90             COMPARE TO SPEC         SA69503
         BNH   TEST                     DONT FOLD SPEC          SA69503
         CLI   D0(RPTR),M99             COMPARE TO LOWER R      SA69503
         BH    TEST                     DONT FOLD SPEC          SA69503
*                                       FOLD LOWER J-R          SA69503
FOLD     EQU   *                                                SA69503
         OI    D0(RPTR),UPPER           FOLD LOWERCASE          SA69503
*                                       ALPHA TO UPPER          SA69503
TEST     EQU   *
         BXLE  RPTR,RWORK,LOOP          INCREMENT PTR AND TEST
         SPACE 3
         SPACE 3
CKMORMSG EQU   *
         CH    RRETURN,TWELVE           IS BFR TOO SMALL FOR MSG
         BNE   SUCCESSG                 BRANCH IF NOT
         SPACE 3                                                 A42898
********
******** CHECK FOR V-TYPE RECORD, IF IT IS, FILL IN CONTROL WORD
********
DESCRIBE EQU   *
         TM    DCBRECFM,M40             TEST FOR V-TYPE AND U-TYPE
*                                       RECORD
         BNO   RETURN                   BRANCH IF NO
         STH   R1,DCBLRECL              UPDATE DCBLRECL
         TM    DCBRECFM,MC0             TEST FOR U-TYPE RECORD
         BO    RETURN                   BRANCH IF YES
         TM    DCBMACRF,M01             TEST FOR DATA MODE
         BO    RETURN                   BRANCH IF YES
         SH    RBUFAD,FOUR              DECREMENT BFR ADDR PTR TO
*                                       POINT AT CONTROL WORD
         AH    R1,FOUR                  ADD LENGTH OF RECORD DESCRIPTOR
*                                       WORD
         STC   R1,D1(RBUFAD)            FILL IN LL FIELD OF CONTROL
*                                       WORD
         SRL   R1,M8                    GET THE OTHER L
         STC   R1,D0(RBUFAD)            FILL IN LL FIELD OF CONTROL
*                                       WORD
         XC    D2(L2,RBUFAD),D2(RBUFAD) ZERO OUT BB IN CONTROL WORD
         MVC   DCBLRECL,D0(RBUFAD)      UPDATE LRECL
         SPACE 3
********
******** PUT BUFFER ADDRESS IN REGISTER 1, RESTORE REGISTERS AND
******** RETURN TO CALLER
********
RETURN   EQU   *
         LR    R1,RBUFAD                LOAD RECORD ADDRESS
         L     R14,D12(R13)             RESTORE REG 14
         L     R0,D20(R13)              RESTORE REG 0
         LM    R2,R12,D28(R13)          RESTORE REGS 2-12
         BR    R14                      RETURN TO CALLER
         SPACE 3
SUCCESSG EQU   *
         CH    R1,TWO                   IS MSG LENGTH 2
         BNE   DESCRIBE                 BRANCH IF NOT
         SPACE 3
CKBUF    EQU   *
         CLC   D0(L2,RBUFAD),END        IS THERE A /*
         BNE   DESCRIBE                 BRANCH IF NOT
CKEOD    EQU   *
         TM    DCBEODAD+C3,M01          IS EOD RTN ADDR FURNISHED
         BZ    GOTHERE                  YES, GO TO GOTHERE
ABEND    EQU   *
         DMABCOND EABD174,DCB=(RDCB),SVC=YES CALL PD FOR 337     S21042
*                                       ABEND                    S21042
GOTHERE  EQU   *
         L     R15,DCBEODAD             LOAD ADDR OF EOD RTN
         L     R14,D12(R13)             RESTORE REG 14
         LM    R0,R12,D20(R13)          RESTORE REGS 0-12
         BR    R15                      BRANCH TO CALLER EOD ADDR
         SPACE 3
********
******** FOR PUT OPERATION
********
PUTOP    EQU   *
         TM    DCBRECFM,M80             CHECK FOR U-TYPE OR F-TYPE
*                                       RECORD
         BO    SZINDCB                  BRANCH IF YES
         TM    DCBMACRF,M01             IS IT DATA MODE
         BO    SZINDCB                  BRANCH IF YES
         SR    RBUFSZ,RBUFSZ            CLEAR REG
         IC    RBUFSZ,D0(RBUFAD)        GET ONE L FROM CONTROL WORD
         SLL   RBUFSZ,M8                MAKE ROOM FOR OTHER L
         IC    RBUFSZ,D1(RBUFAD)        GET SECOND L FROM CONTROL WORD
         AH    RBUFAD,FOUR              ADD 4 TO BFR ADDR
         SH    RBUFSZ,FOUR              DECREMENT 4 FROM BFR SIZE
         B     CKCHAR                   GO TO CHECK MACHINE CHAR
SZINDCB  EQU   *
         LH    RBUFSZ,DCBLRECL          GET BUFFER SIZE
CKCHAR   EQU   *
         TM    DCBRECFM,M06             TEST FOR ASA OR MACHINE CHAR
         BZ    PUTREADY                 NO, GO TO ISSUE TPUT
         LA    RBUFAD,D1(RBUFAD)        UPDAT BFR PTR BY 1
         BCTR  RBUFSZ,R0                DECREMENT BFR SIZE BY 1
         SPACE 3
********
******** PREPARE PARAMETERS AND ISSUE TPUT
********
PUTREADY EQU   *
         LTR   RBUFSZ,RBUFSZ            CHECK BFR  SIZE         SA66590
         BM    ABEND002                 NEGATIVE, TERMINATE JOB SA66590
         LR    R0,RBUFSZ                LOAD BFR SIZE
         LR    R1,RBUFAD                LOAD BFR ADDRESS
         TPUT  (1),(0)                  ISSUE TPUT MACRO
********
******** UPON RETURN FROM TPUT
********
SUCCESSP EQU   *
         OI    DCBOFLGS,M80             TURN ON WRITE BIT
         SPACE 3
*********
*********TEST FOR V-TYPE RECORD AND ASA OR MACHINE CHARACTER, AND
*********RESTORE BUFFER POINTER ACCORDINGLY
*********
CHECK    EQU   *
         TM    DCBRECFM,M06             TEST FOR ASA OR MACHINE CHAR
         BZ    CKVREC                   NO, GO TO CHECK V-TYPE RECORD
         BCTR  RBUFAD,R0                SET BACK BFR ADDRESS BY 1
CKVREC   EQU   *
         TM    DCBRECFM,M80             CHECK FOR U-TYPE OR F-TYPE
*                                       RECORD
         BNZ   RETURN                   BRANCH IF YES
         TM    DCBMACRF,M01             DATA MODE?
         BO    RETURN                   BRANCH IF YES
         SH    RBUFAD,FOUR              SET BACK BFR ADDR BY 4
         B     RETURN                   GO TO RESTORE REGISTERS
         SPACE 3
********
******** USER RECORD WAS TOO SHORT (WOULD HAVE BEEN NEGATIVE TPUT) -
******** ISSUE 002 ABEND.
********
ABEND002 EQU   *
         L     R1,ABCODE                LOAD ABEND CODE         SA66590
         ABEND (1)                      ISSUE ABEND SVC         SA66590
         SPACE 3
********
******** CONSTANTS
********
ABCODE   DS    0F                       ALIGN                   SA66590
         DC    X'80002000'              DUMP/STEP, AND COMP COD SA66590
FOUR     DC    H'4'                     CONSTANTS OF 4
TWO      DC    H'2'                     CONSTANT OF 2
EIGHT    DC    H'8'                     CONSTANT OF 8
TWELVE   DC    H'12'                    CONSTANT OF 12
SIXTEEN  DC    H'16'                    CONSTANT OF ×6
END      DC    C'/*'                    DEFINES EOD
         SPACE 3
         DS    0F
DEB      DSECT
         DS    CL32                     DEB BASIC SECTION
         DS    CL4                      DEB EXTENT
         DS    CL12                     DEB BSAM/QSAM DEPENDENT SECTION
DEBBLKSI DS    CL2                      BLOCK SIZE SAVE AREA
DEBLRECL DS    CL2                      LRECL SAVE AREA
         SPACE 3
         DCBD  DSORG=PS
         END
