         TITLE 'IGG019T3 - TS QSAM GET ROUTINE'
IGG019T3 CSECT
***********************************************************************
*                                                                     *
*      VS2  030  PTF
*
*0000592000                                                    @ZA06713
*0000592000,948100                                             @ZA09612
*
* RELEASE 21 DELETIONS/CHANGES                                        *
*1395366000,900000-904000                                        S21042
*1395925600-927200                                               A42898
*                                                                M6374
*A828500                                                        SA69128
*D370000,722600-723333                                          SA69503
*A368500-370700,722900-723331                                   SA69503
*STATUS CHANGE LEVEL VS2  030  SU                              @ZA06713
*                                                                     *
* NAME - IGG019T3 (TS GET)                                            *
*                                                                     *
* FUNCTION/OPERATION - THE FOLLOWING FUNCTIONS WILL BE PERFORMED BY   *
*                      THIS INTERFACE ROUTINE.                        *
*                                                                     *
*                      FURNISH TGET USER BUFFER ADDRESS (MOVE MODE),  *
*                      AND FIRST BUFFER ADDRESS OF THE BUFFER POOL    *
*                      CONTROL BLOCK (LOCATE MODE).                   *
*                                                                     *
*                      FURNISH TGET BUFFER SIZE.                      *
*                                                                     *
*                      ISSUE TGET,WHICH RETRIEVES A RECORD FROM TIOC  *
*                      BUFFER AND PLACES IT EITHER IN THE FIRST       *
*                      BUFFER OF THE BUFFER POOL CONTROL BLOCK        *
*                      (LOCATE MODE), OR IN A USER-DESIGNATED AREA    *
*                      (MOVE MODE). UPON REGAIN CONTROL FROM TGET,    *
*                      RETURN TO USER THE RECORD ADDRESS IN REGISTER  *
*                      1.                                             *
*                                                                     *
* ENTRY POINTS - GET - IGG019T3                                       *
*                TRUNC/RELSE - IGG019T3 + 8                           *
*                                                                     *
*                CONTROL IS RECEIVED WHEN THE FOLLOWING CONDITIONS    *
*                ARE MET.                                             *
*                                                                     *
*                THE TASK IS TIME SHARING.                            *
*                THE DCB OPENED IS FOR QSAM INPUT.                    *
*                A GET, TRUNC, OR RELSE MACRO IS ISSUED.              *
*                                                                     *
* INPUT - USER BUFFER ADDRESS IN REGISTER 0 (OMIT FOR LOCATE MODE).   *
*         DCB ADDRESS IN REGISTER 1.                                  *
*                                                                     *
* OUTPUT - AN DEITED BLOCK OF TEXT IN BUFFER.                         *
*                                                                     *
* EXTERNAL REFERENCE - IGC0009C.                                      *
*                                                                     *
* EXIT - NORMAL - RETURN CONTROL TO CALLER VIA 'BR 14' INSTRUCTION.   *
*                 REGISTER 1 CONTAINS RECORD ADDRESS.                 *
*                                                                     *
*                 WHEN AN EOD CONDITION IS DETECTED, BRANCH TO USER'S *
*                 EOD ROUTINE ADDRESS VIA 'BR 15' INSTRUCTION.        *
*                                                                     *
*        ERROR - WHEN END OF FILE IS REACHED BUT NO EOD ROUTINE IS    *
*                FURNISHED, JOB WILL BE ABENDED WITH SYSTEM CODE 337. *
*                                                                     *
* TABLE/WORK AREAS - NONE.                                            *
*                                                                     *
* ATTRIBUTES - REENTRANT, REUSABLE.                                   *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*                                                                     *
* NOTES -                                                             *
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
********
R0       EQU   0                        CONTAINS USER BFR ADDR
*                                       ALSO CONTAINS BFR SIZE FOR TGET
R1       EQU   1                        CONTAINS DCB ADDR WHEN THIS
*                                       MODULE GETS CONTROL
*                                       ALSO CONTAINS BFR ADDR FOR
*                                       TGET
R2       EQU   2                        REGISTER 2
RDCB     EQU   2                        POINTS AT DCB
RBUFAD   EQU   3                        CONTAINS BFR ADDR
RBUFSZ   EQU   4                        CONTAINS BFR SIZE
RDEB     EQU   7                        POINTS AT DEB
RPTR     EQU   9                        POINTS AT BFR FOR FOLDING MSG
RWORK    EQU   10                       WORK REGISTER
RENDMSG  EQU   11                       POINTS AT END OF MESSAGE
R12      EQU   12                       REGISTER 12
RBASE    EQU   12                       BASE REGISTER
R13      EQU   13                       REGISTER 13
R14      EQU   14                       CONTAINS RETURN ADDR
RRETURN  EQU   15                       CONTAINS RETURN CODE
R15      EQU   15                       CONTAINS BRANCH ADDR
         SPACE 3
********
******** LENGTH FIELD, DISPLACEMENT, AND MASK EQUATES
********
D0       EQU   0                        DISPLACEMENT OF 0
D1       EQU   1                        DISPLACEMENT OF 1
D2       EQU   2                        DISPLACEMENT OF 2
D4       EQU   4                        DISPLACEMENT OF 4
D12      EQU   12                       DISPLACEMENT OF 12
D20      EQU   20                       DISPLACEMENT OF 20
D28      EQU   28                       DISPLACEMENT OF 28
L2       EQU   2                        LENGTH OF 2
C3       EQU   3                        CONSTANT OF 3
C15      EQU   15                       CONSTANT OF 15
M01      EQU   X'01'                    MASK USED TO TEST EOD RTN ADDR
*                                       MASK FOR DATA MODE IN DCBMACR
M80      EQU   X'80'                    MASK FOR F-TYPE RECORD
M40      EQU   X'40'                    V-TYPE RECORD IN DCBRECFM
MC0      EQU   X'C0'                    U-TYPE RECORD IN DCBRECFM
M7F      EQU   X'7F'                    MASK FOR TURNING OFF WRITE BIT
M8       EQU   8                        USED TO SHIFT REGISTER
M12      EQU   X'12'                    MASK FOR SHIFTING REGISTER
EABD174  EQU   174                                               S21042
QSAMLOCT EQU   X'48'                    DCBMACRF=GL
*****  MASKS FOR ISOLATING LOWERCASE ALPHA CHAR. FOR FOLDING    SA69503
M81      EQU   X'81'                    LOWERCASE A             SA69503
MA9      EQU   X'A9'                    LOWERCASE Z             SA69503
M89      EQU   X'89'                    LOWERCASE I             SA69503
M90      EQU   X'90'                    SPECIAL CHAR            SA69503
M99      EQU   X'99'                    LOWERCASE R             SA69503
MA1      EQU   X'A1'                    SPECIAL CHAR            SA69503
UPPER    EQU   X'40'                    USED TO FOLD MSG
         SPACE 3
         SPACE 3
********
******** GET ENTRY
********
         USING IGG019T3,R15             USE ENTRY ADDR AS BASE ADDR
         B     STARTGET                 BRANCH PAST TRUNC/RELSZ ENTRY
         DC    X'00000000'              PADDING TO PUSH LOC CTR TO 8
         SPACE 3
********
******** TRUNC/RELSE ENTRY
********
         BR    R14                      NOP FOR TRUNC & RELSE ROUTINES
*                                       (RETURN TO USER)
         B     BRARID                   BRANCH AROUND ID       @ZA06713
         DC    C'IGG019T3'              MODULE ID              @ZA06713
         DC    X'6063'                  DATE 03/03/76          @ZA09612
BRARID   EQU   *                                               @ZA06713
         SPACE 3
********
******** SAVE REGISTER & ESTABLISH ADDRESSABILITY
********
STARTGET EQU   *
         STM   R14,R12,D12(R13)         SAVE REGISTERS
         LR    RBASE,R15                LOAD BASE REGISTER
         DROP  R15                      GET RID OF PREVIOUS BASE REG
         USING IGG019T3,RBASE           USE REG 12 AS BASE REG
         SPACE 3
********
******** GET DCB ADDRESS
********
         LR    RDCB,R1                  SAVE DCB ADDR
         USING IHADCB,RDCB
         USING DEB,RDEB                 ESTABLISH ADDRESSABILITY
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
         SPACE 3
********
******** TEST FOR LOCATE MODE
********
         TM    DCBMACRF,QSAMLOCT        CHECK FOR LOCATE MODE
         BNO   MOVEMD                   NO, GO TO MOVEMD
         SPACE 3
********
******** GET BUFFER ADDRESS
********
LOCATEMD EQU   *
         L     RBUFAD,DCBBUFNO          LOAD ADDR OF BFR CONTROL BLOCK
         L     RBUFAD,D0(RBUFAD)        LOAD ADDR OF BFR
         B     GETSIZE                  GO TO GET BFR SIZE
MOVEMD   EQU   *
         LR    RBUFAD,R0                LOAD USER BFR ADDR
********
******** GET BUFFER SIZE
********
GETSIZE  EQU   *
         LH    RBUFSZ,DEBLRECL          GET BUFFER SIZE        @ZA09612
         SPACE 3
********
******** TEST FOR RECORD TYPE AND UPDATE BFR ADDR AND BFR SIZE FOR
******** V-TYPE RECORD
********
         TM    DCBRECFM,M80             U-TYPE OR F-TYPE RECORD
         BO    FIXED                    YES, GO TO FIXED
VREC     EQU   *
         TM    DCBMACRF,M01             TEST FOR DATA MODE
         BO    DATAMODE                 BRANCH IF YES
         LA    RBUFAD,D4(RBUFAD)        INCREMENT BFR ADDR TO PAST
*                                       CONTROL WORD
         SH    RBUFSZ,FOUR              DECREMENT BFR SIZE
         SPACE 3
********
******** PREPARE PARAMETERS AND ISSUE TGET
********
DATAMODE EQU   *
FIXED    EQU   *
         LR    R1,RBUFAD                LOAD BFR ADDR
         LR    R0,RBUFSZ                LOAD BFR SIZE
         TGET  (1),(0)                  ISSUE TGET (UPON RETURN R1 HAS
*                                       MSG LENGTH)
         SPACE 3
********
******** TEST RETURN CODE FROM TGET
********
CKATTN   EQU   *
         CH    RRETURN,EIGHT            CHECK FOR ATTENTION INTERRUPT
         BE    FIXED                    GO TO REISSUE TGET
         NI    DCBOFLGS,M7F             TURN OFF WRITE BIT (THIS
*                                       IS A READ OPERATION)
         SPACE 3
********
******** FOLD INPUT LOWERCASE ALPHA TO UPPERCASE
********
         LTR   R1,R1                    IS THERE ANY MSG TO FOLD
         BZ    CKMORMSG                 NO, DO NOT FOLD
         LR    RPTR,RBUFAD              POINTS AT START OF BFR
         LA    RWORK,D1                 SET UP LOOP INDEX
         LR    RENDMSG,RBUFAD           POINTS AT START OF BUFFER
         AR    RENDMSG,R1               POINTS AT END OF BFR PLUS 1
         BCTR  RENDMSG,R0               POINTS AT END OF BUFFER
LOOP     EQU   *
         CLI   D0(RPTR),M81             COMPARE TO LOWER A      SA69503
         BL    TEST                     IF LOW, DONT FOLD       SA69503
         CLI   D0(RPTR),MA9             COMPARE TO LOWER Z      SA69503
         BH    TEST                     IF HIGH, DONT FOLD      SA69503
*****
*****         ONLY LOWERCASE ALPHA & IMBEDDED SPECIAL CHARACTERS
*****         WILL FALL THROUGH THE ABOVE TESTS
*****
         CLI   D0(RPTR),M89             COMPARE TO LOWER I      SA69503
         BNH   FOLD                     FOLD LOWER A-I          SA69503
         CLI   D0(RPTR),MA1             COMPARE TO SPEC.        SA69503
         BH    FOLD                     FOLD LOWER S-Z          SA69503
         CLI   D0(RPTR),M90             COMPARE TO SPEC         SA69503
         BNH   TEST                     DONT FOLD SPEC          SA69503
         CLI   D0(RPTR),M99             COMPARE TO LOWER R      SA69503
         BH    TEST                     DONT FOLD SPEC          SA69503
*                                       FOLD LOWER J-R          SA69503
FOLD     EQU   *                                                SA69503
         OI    D0(RPTR),UPPER           FOLD LOWERCASE          SA69503
*                                       ALPHA TO UPPER          SA69503
TEST     EQU   *
         BXLE  RPTR,RWORK,LOOP          INCREMENT PTR AND TEST
         SPACE 3
CKMORMSG EQU   *
         CH    RRETURN,TWELVE           IS BFR TOO SMALL FOR MSG
         BNE   SUCCESS                  NO, GO TO SUCCESS
         SPACE 3
********
******** CHECK FOR V-TYPE RECORD, IF IT IS, FILL IN CONTROL WORD
********
DESCRIBE EQU   *
         TM    DCBRECFM,M40             TEST FOR V-TYPE OR U-TYPE
*                                       RECORD
         BNO   RETURN                   BRANCH IF NO
         STH   R1,DCBLRECL              UPDATE DCBLRECL
         TM    DCBRECFM,MC0             TEST FOR U-TYPE RECORD
         BO    RETURN                   BRANCH IF YES
         TM    DCBMACRF,M01             TEST FOR DATA MODE
         BO    RETURN                   BRANCH IF YES
         SH    RBUFAD,FOUR              DECREMENT BFR ADDR PTR TO
*                                       POINT AT CONTROL WORD
         AH    R1,FOUR                  ADD LENGTH OF RECORD DESCRIPTOR
*                                       WORD
         STC   R1,D1(RBUFAD)            FILL IN LL FIELD OF CONTROL
*                                       WORD
         SRL   R1,M8                    GET THE OTHER L
         STC   R1,D0(RBUFAD)            FILL IN LL FIELD OF CONTROL
*                                       WORD
         XC    D2(L2,RBUFAD),D2(RBUFAD) ZERO OUT BB IN CONTROL WORD
         MVC   DCBLRECL,D0(RBUFAD)      UPDATE LRECL
RETURN   EQU   *
         LR    R1,RBUFAD                LOAD RECORD ADDRESS
         ST    R1,DCBRECAD              STORE ADD IN INPUT DCB  SA69128
         L     R14,D12(R13)             RESTORE REG 14
         L     R0,D20(R13)              RESTORE REG 0
         LM    R2,R12,D28(R13)          RESTORE REGS 2-12
         BR    R14                      RETURN TO CALLER
         SPACE 3
SUCCESS  EQU   *
         CH    R1,TWO                   IS MESSAGE LENGTH 2
         BNE   DESCRIBE                 BRANCH IF NOT
CKBUF    EQU   *
         CLC   D0(L2,RBUFAD),END        IS IT /*
         BNE   DESCRIBE                 BRANCH IF NOT
CKEOD    EQU   *
         TM    DCBEODAD+C3,M01          IS EOD RTN ADDR FURNISHED
         BZ    GOTHERE                  YES, GO TO GOTHERE
ABEND    EQU   *
         DMABCOND EABD174,DCB=(RDCB),SVC=YES                     S21042
GOTHERE  EQU   *
         L     R15,DCBEODAD             LOAD ADDR OF EOD RTN
         L     R14,D12(R13)             RESTORE REG 14
         LM    R0,R12,D20(R13)          RESTORE REGS 0-12
         BR    R15                      BRANCH TO CALLER EOD ADDR
         SPACE 3
         SPACE 3
********
******** CONSTANTS
********
FOUR     DC    H'4'                     USED TO DECREMENT BFR SIZE
TWO      DC    H'2'                     CONSTANT OF 2
EIGHT    DC    H'8'                     CONSTANT OF 8
TWELVE   DC    H'12'                    CONSTANT OF 12
SIXTEEN  DC    H'16'                    CONSTANT OF 16
END      DC    C'/*'                    DEFINES EOD
PATCHIT  DC    10F'0'                   PATCH AREA             @AZ09612
         SPACE 3
         DS    0F
DEB      DSECT
         DS    CL32                     DEB BASIC SECTION
         DS    CL4                      DEB EXTENT
         DS    CL12                     DEB BSAM/QSAM DEPENDENT SECTION
DEBBLKSI DS    CL2                      BLOCK SIZE SAVE AREA
DEBLRECL DS    CL2                      LRECL SAVE AREA
         SPACE 3
         DCBD  DSORG=PS
         END
