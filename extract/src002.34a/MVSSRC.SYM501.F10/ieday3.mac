         TITLE 'IEDAY3  -  TIOC LOGON SYNCHRONIZATION ROUTINE'
* ********************************************************************
*
*01* MODULE-NAME = IEDAY3
*
*01* DESCRIPTIVE-NAME = TIOC LOGON SYNCHRONIZATION ROUTINE
*
*01* COPYRIGHT = NONE
*
*01* STATUS = 00
*
*01* FUNCTION = SYNCHRONIZE THE LOGON PROCESS FOR TIOC BY
*               PREVENTING MEMORY CREATION FROM PROCEEDING UNTIL
*               THE TSB AND ASCB ARE FULLY INITIALIZED BY
*               TIOC LOGON.
*
*01* NOTES =
*
*02*   DEPENDENCIES = ASCBECB MUST BE ZEROED WHEN ASCB IS CREATED.
*
*02*   RESTRICTIONS = N/A
*
*02*   REGISTER-CONVENTIONS = SEE REGISTER EQUATES
*
*02*   PATCH-LABEL = PATCH
*
*01* MODULE-TYPE = ASSEMBLER LANGUAGE CSECT
*
*02*   PROCESSOR = SEE LISTING
*
*02*   MODULE-SIZE = SEE EXTERNAL SYMBOL DICTIONARY TYPE SD ID 01
*
*02*   ATTRIBUTES = REENTRANT, REFRESHABLE, SUPERVISOR STATE
*
*01* ENTRY-POINT = IEEDAY3
*
*02*   PURPOSE = SEE FUNCTION
*
*02*   LINKAGE = ATTACHED BY MASTER SCHEDULER.
*
*01* INPUT = REGISTER 01 CONTAINS ADDRESS OF HALFWORD CONTAINING
*                        THE ASID OF THE NEW USER.
*            REGISTER 15 CONTAINS ENTRY POINT ADDRESS.
*
*01* OUTPUT = REGISTER 01 SAME AS AT ENTRY.
*
*01* EXIT-NORMAL = XCTL TO MEMORY CREATE (IEAVEMCR).
*
*01* EXIT-ERROR = NONE
*
*01* EXTERNAL-REFERENCES = N/A
*
*02*   ROUTINES = N/A
*
*02*   DATA-AREAS = N/A
*
*02*   CONTROL-BLOCKS = SEE MAPPING MACROS AT END OF LISTING
*
*01* TABLES = N/A
*
*01* MACROS = WAIT, XCTL
*
*01* CHANGE-ACTIVITY -  VS01217 - 05/25/73
*
*********************************************************************
         EJECT
IEDAY3   CSECT
         COPY  LCGASMS2                 LIBRARY CONTROL
         ENTRY IEEDAY3                  ENTRY POINT
*
*   REGISTER EQUATES
*
RPARM    EQU   1                        PARAMETER REGISTER
RHOLD    EQU   2                        WORK REGISTER
RASCB    EQU   3                        ASCB POINTER
RASVT    EQU   4                        ASVT POINTER
RBASE    EQU   12                       BASE REGISTER
RENTY    EQU   15                       ENTRY POINT ADDRESS
*
*   OTHER EQUATES
*
E0       EQU   0                        NULL DISPLACEMENT
*
         DC    C'IEDAY3'                MODULE I.D.
         DC    X'3145'                  DATE 05/25/73
IEEDAY3  EQU   *
         LR    RBASE,RENTY              SET BASE REGISTER
         USING IEEDAY3,RBASE            SET ADDRESSABILITY
*
*   WAIT FOR TSB TO BE INITIALIZED
*
         LR    RHOLD,RPARM              SAVE PARM POINTER
         LH    RASCB,E0(,RPARM)         GET ASID
         L     RASVT,CVTPTR             GET CVT ADDRESS
         USING CVT,RASVT                CVT ADDRESSABLE
         L     RASVT,CVTASVT            GET ASVT ADDRESS
         DROP  RASVT                    CVT NOT ADDRESSABLE
         USING ASVT,RASVT               ASVT ADDRESSABLE
         AR    RASCB,RASCB              DOUBLE ASID
         AR    RASCB,RASCB              GET OFFSET IN ASVT
         L     RASCB,ASVTENTY-L'ASVTENTY(RASCB)    GET ASCB ADDR
         USING ASCB,RASCB               ASCB ADDRESSABLE
         WAIT  ECB=ASCBECB,LONG=YES     WAIT FOR IEDAYLL
         SPACE 2
         MVI   ASCBECB,E0               ZERO OUT WAIT AND POST  vs01217
*                                       bits                    vs01217
         SPACE 2
*
*   TRANSFER CONTROL TO MEMORY CREATION
*
         LR    RPARM,RHOLD              RESTORE REG. 1
         XCTL  EP=IEAVEMCR              XCTL TO MEM CREATE
         DROP  RBASE                    ADDRESSABILITY ENDS
PATCH    DC    9F'0'                    PATCH AREA
         EJECT
         IHAASCB
         EJECT
         IHAASVT
         EJECT
CVT      DSECT
         CVT
         END
