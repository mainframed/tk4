         TITLE 'IGG019T8 - BSAM TS WRITE'
IGG019T8 CSECT
***********************************************************************
*                                                                     *
* RELEASE 23 DELETIONS/CHANGES                                        *
* RELEASE 22 DELETIONS/CHANGES                                        *
* RELEASE 21 DELETIONS/CHANGES                                        *
*2870320000,550000-560000,800000                                 A42898
*                                                                     *
*0000                                                            TS1616
*STATUS CHANGE LEVEL 002
*                                                                     *
* NAME - IGG019T8 (TS WRITE).                                         *
*                                                                     *
* FUNCTION/OPERATION - THIS INTERFACE ROUTINE PERFORMS THE FOLLOWING  *
*                      FUNCTION.                                      *
*                                                                     *
*                      FURNISH TPUT BUFFER ADDRESS.                   *
*                      FURNISH TPUT BUFFER SIZE.                      *
*                      ISSUE TPUT WHICH WRITES A BLOCK OF DATA FROM   *
*                      USER'S BUFFER TO THE TERMINAL. UPON REGAIN     *
*                      CONTROL FROM TPUT, SET USER ECB COMPLETION     *
*                      CODE TO X'7F'                                  *
*                                                                     *
* ENTRY POINT - IGG019T8 BEGINNING OF MODULE. CONTROL IS RECEIVED     *
*               WHEN THE FOLLOWING CONDITIONS ARE MET.                *
*                                                                     *
*               THE TASK IS TIME SHARING.                             *
*               THE DCB OPENED IS FOR BSAM OUTPUT.                    *
*               A WRITE MACRO INSTRUCTION IS ISSUED.                  *
*                                                                     *
*               UPON ENTRY TO THIS MODULE, REGISTER 1 CONTAINS THE    *
*               ADDRESS OF DECB                                       *
*                                                                     *
* INPUT - DECB NAME, ('SF' IS IGNORED), DCB ADDRESS, BUFFER ADDRESS,  *
*         'S' OR LENGTH OF MESSAGE.                                   *
*                                                                     *
* OUTPUT - A BLOCK OF EDITED (SPECIALLY-INDICATED CHARACTERS ARE      *
*          DELETED FROM THE MESSAGE WRITTEN) TEXT WRITTEN ON TERMINAL.*
*                                                                     *
* EXIT, NORMAL - RETURN CONTROL TO CALLER VIA A 'BR 14' INSTRUCTION.  *
*                                                                     *
* EXIT, ERROR - NONE.                                                 *
*                                                                     *
* EXTERNAL REFERENCE - IGC0009C.                                      *
*                                                                     *
* TABLE/WORK AREAS - NONE.                                            *
*                                                                     *
* ATTRIBUTES - REENTRANT, REUSABLE.                                   *
*                                                                     *
* CHARACTER CODE DEPENDENCY.                                          *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
********
R0       EQU   0                        CONTAINS BFR SIZE FOR TPUT
R1       EQU   1                        CONTAINS BFR ADDR FOR TPUT,
*                                       POINTS AT DECB ADDR WHEN THIS
*                                       MODULE GETS CONTROL
RDCB     EQU   2                        POINTS AT DCB
RECB     EQU   3                        POINTS AT ECB
RDECB    EQU   4                        POINTS AT DECB
RBUFAD   EQU   5                        CONTAINS BFR ADDR
RBUFSZ   EQU   6                        CONTAINS BFR SIZE
RDEB     EQU   7                        POINTS AT DEB
RBASE    EQU   12                       BASE REGISTER
R12      EQU   12                       REGISTER 12
R13      EQU   13                       POINTS AT SAVE AREA ADDR
R14      EQU   14                       CONTAINS RETURN ADDR
R15      EQU   15                       POINTS AT ENTRY ADDR OF THIS
*                                       MODULE WHEN IT GETS CONTROL
RRETURN  EQU   15                       CONTAINS RETURN CODE
         SPACE 3
********
******** EQUATES FOR MASKS, DISPLACEMENTS
********
M7F      EQU   X'7F'                    ECB COMPLETION BIT
MC0      EQU   X'C0'                    DENOTES V-TYPE RECORD
M80      EQU   X'80'                    'S' SPECIFIED IN DECB
*                                       WRITE BIT IN DCBOFLGS
M40      EQU   X'40'                    DENOTES V-TYPE RECORD
M06      EQU   X'06'                    ASA OR MACHINE CHAR USED
M8       EQU   8                        USED TO SHIFT REGISTER
D0       EQU   0                        DISPLACEMENT OF 0
D1       EQU   1                        DISPLACEMENT OF 1
D8       EQU   8                        DISPLACEMENT OF 8        A42898
D12      EQU   12                       DISPLACEMENT OF 12
D20      EQU   20                       DISPLACEMENT OF 20
         SPACE 3
         SPACE 3
********
******** SAVE REGISTERS AND ESTABLISH ADDRESSABILITY
********
         STM   R14,R12,D12(R13)         SAVE REGISTERS
         LR    RBASE,R15                USE ENTRY ADDR AS BASE ADDR
         USING IGG019T8,RBASE           ESTABLISH ADDRESSABILITY
         USING IHADCB,RDCB              ESTABLISH ADDRESSABILITY
         USING DECB,RDECB               ESTABLISH ADDRESSABILITY
         USING DEB,RDEB                 ESTABLISH ADDRESSABILITY
         SPACE 3
********
******** INITIALIZATION AND GET BUFFER ADDRESS
********
         LR    RDECB,R1                 SAVE DECB ADDRESS
         L     RDCB,DECDCBAD            GET DCB ADDRESS
         L     RBUFAD,DECAREA           GET BUFFER ADDRESS
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
         LA    RECB,DECSDECB            GET ADDR OF ECB
         MVI   D0(RECB),M80             SET ECB WAIT BIT ON
         SPACE 3
********
******** TEST RECORD TYPE AND GET BUFFER SIZE
********
         TM    DCBRECFM,MC0             IS IT A U-TYPE RECORD
         BNO   TESTMORE                 NO, GO TO TESTMORE
UTYPE    EQU   *
         TM    DECTYPE,M80              IS 'S' SPECIFIED
         BO    LNGINDCB                 YES, GET LENGTH FROM DCBBLKSI
LNGNDECB EQU   *
         LH    RBUFSZ,DECLNGTH          GET BUFFER SIZE
         N     RBUFSZ,ZEROHALF          CLEAR HIGH ORDER 2 BYTES
         B     CKCHAR                   GO TO CKCHAR
LNGINDCB EQU   *
         LH    RBUFSZ,DCBBLKSI          GET BUFFER SIZE FROM DCB TS1616
         LTR   RBUFSZ,RBUFSZ            IS BLOCK SIZE SPECIFIED  TS1616
         BNZ   CKCHAR                   BRANCH IF YES            TS1616
         LH    RBUFSZ,DEBLRECL          GET BUFFER SIZE
         B     CKCHAR                   GO TO CHECK CONTROL CHAR
TESTMORE EQU   *
         TM    DCBRECFM,M40             IS IT V-TYPE RECORD
         BZ    LNGINDCB                 NO, LENGTH IS IN DCBBLKSI
VTYPE    EQU   *
         SR    RBUFSZ,RBUFSZ            CLEAR REGISTER
         IC    RBUFSZ,D0(RBUFAD)        GET 1 L FROM RECORD DESCRIPTOR
*                                       WORD
         SLL   RBUFSZ,M8                PUT 1ST L IN PLACE
         IC    RBUFSZ,D1(RBUFAD)        GET OTHER L FROM RECORD
*                                       DESCRIPTOR WORD
         SH    RBUFSZ,EIGHT             TAKE OFF LNG FOR BLK AND A42898
*                                       RECORD DESCRIPTOR WORDS
         LA    RBUFAD,D8(RBUFAD)        MOVE UP PTR              A42898
         SPACE 3
********
******** TEST FOR ASA OR MACHINE CHARACTER
********
CKCHAR   EQU   *
         TM    DCBRECFM,M06             TEST FOR ASA OR MACHINE CHAR
         BZ    LOADPARM                 NO, GO TO LOADPARM
         LA    RBUFAD,D1(RBUFAD)        MOVE UP PTR TO BUFFER ADDR
         BCTR  RBUFSZ,R0                DECREMENT BUFFER SIZE BY 1
         SPACE 3
********
******** PREPARE PARAMETER AND ISSUE TPUT
********
LOADPARM EQU   *
         LR    R0,RBUFSZ                LOAD BFR SIZE
         LR    R1,RBUFAD                LOAD BFR ADDRESS
         SPACE 3
         TPUT  (1),(0)                  ISSUE TPUT
         SPACE 3
********
******** UPON RETURN FROM TPUT
********
SUCCESS  EQU   *
         OI    DCBOFLGS,M80             TURN ON WRITE BIT
         MVI   D0(RECB),M7F             SET ECB COMPLETION BIT
         SPACE 3
********
******** RESTORE REGISTERS AND RETURN
********
RETURN   EQU   *
         L     R14,D12(R13)             RESTORE REG 14
         LM    R0,R12,D20(R13)          RESTORE REG 0 - 12
         BR    R14                      RETURN CONTROL TO USER
         SPACE 3
********
******** CONSTANTS
********
         DS    0F                       FORCE THE FOLLOWING ON WORD
*                                       BOUNDARY
ZEROHALF DC    X'0000FFFF'              USED TO CLEAR HIGH ORDER 2
*                                       BYTES OF REGISTER
EIGHT    DC    H'8'                     CONSTANT OF 8            A42898
         SPACE 3
         SPACE 3
         DCBD  DSORG=PS
         SPACE
         DS    0F
DECB     DSECT
DECSDECB DS    CL4                      EVENT CONTROL BLOCK
DECTYPE  DS    CL2                      TYPE OF I/O REQUEST
DECLNGTH  DS   CL2                      LENGTH OF KEY AND DATA
DECDCBAD DS    CL4                      ADDR OF THE DCB TO WHICH THIS
*                                       I/O REQUEST IS RELATED
DECAREA  DS    CL4                      ADDR OF KEY AND DATA
DECIOBPT DS    CL4                      ADDR OF THE IOB
         SPACE 3
         DS    0F
DEB      DSECT
         DS    CL32                     DEB BASIC SECTION
         DS    CL4                      DEB EXTENT
         DS    CL12                     DEB BSAM/QSAM DEPENDENT SECTION
DEBBLKSI DS    CL2                      BLOCK SIZE SAVE AREA
DEBLRECL DS    CL2                      LRECL SAVE AREA
         END
