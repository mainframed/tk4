         TITLE 'IGG019T7 - TS READ'
IGG019T7 CSECT
***********************************************************************
*                                                                     *
* RELEASE 21 DELETIONS/CHANGES                                        *
*1395504000-508000,596000,620000-624000,755000-763000            A42898
*                                                                     *
*                                                                M3038
*                                                                M5422
*0000                                                            TS1616
*                                                                M2307
* RELEASE 21.8 MODIFICATIONS
*
*D315000,315500,573040-573280                                   SA69503
*A344500-347900,573060-573580                                   SA69503
*
*STATUS CHANGE LEVEL 001
*                                                                M1565
*                                                                M6374
*
* NAME - IGG019T7 - TS READ.                                          *
*                                                                     *
* FUNCTION/OPERATION - THIS MODULE PERFORMS THE FOLLOWING FUNCTIONS-  *
*                                                                     *
*                      FURNISH TGET USER BUFFER ADDRESS.              *
*                      FURNISH TGET USER BUFFER SIZE                  *
*                      ISSUE TGET, UPON REGAIN CONTROL FROM TGET, SET *
*                      ECB COMPLETION CODE TO X'7F'. WHEN END OF      *
*                      FILE , SET ECB COMPLETION CODE TO X'41'        *
*                                                                     *
* ENTRY POINT - IGG019T7, BEGINNING OF MODULE. CONTROL IS RECEIVED    *
*               WHEN THE FOLLOWING CONDITIONS ARE MET.                *
*                                                                     *
*               THE TASK IS TIME SHARING                              *
*               THE DCB OPENED IS FOR BSAM, OUTPUT                    *
*               A READ MACRO INSTRUCTION IS ISSUED                    *
*                                                                     *
*               UPON ENTRY TO THIS MODULE, REGISTER 1 CONTAINS USER   *
*               DECB ADDRESS.                                         *
*                                                                     *
* INPUT - DECB NAME, TYPE FIELD IS IGNORED, DCB ADDRESS, BUFFER       *
*         ADDRESS, 'S' OR LENGTH OF DATA.                             *
*                                                                     *
* OUTPUT - AN EDITED (SPECIALLY INDICATED CHARACTERS ARE DELETED FROM *
*          THE DATA READ) BLOCK OF TEXT IN THE USER DESIGNATED AREA.  *
*                                                                     *
* EXIT - NORMAL - RETURN CONTROL TO CALLER VIA 'BR 14' INSTRUCTION.   *
*                 WHEN EOD CONDITION IS DETECTED, BRANCH TO USER EOD  *
*                 ROUTINE VIA 'BR I5' INSTRUCTION.                    *
*                                                                     *
*        ERROR - NONE.                                                *
*                                                                     *
* EXTERNAL REFERENCE - IGC0009C.                                      *
*                                                                     *
* TABLE /WORK AREA - NONE.                                            *
*                                                                     *
* ATTRIBUTES - REENTRANT, REUSABLE.                                   *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
********
R0       EQU   0                        CONTAINS BFR SIZE FOR TGET
RSW      EQU   0                        SWITCH FOR FOLDING CHARS
R1       EQU   1                        CONTAINS BFR ADDR FOR TGET
*                                       ALSO CONTAINS MSG LENGTH AFTER
*                                       A TGET MACRO INSTRUCTION
*                                       ALSO CONTAINS DECB ADDR WHEN
*                                       THIS MODULE GETS CONTROL
RDCB     EQU   2                        POINTS AT DCB
RDECB    EQU   3                        POINTS AT DECB
RPOINT   EQU   4                        POINTER TO CHAR WHEN FOLDING
RBUFAD   EQU   5                        CONTAINS BFR ADDRESS
RBUFSZ   EQU   6                        CONTAINS BFR SIZE
RECB     EQU   7                        POINTS AT ECB
RDEB     EQU   8                        POINTS AT DEB
RPTR     EQU   9                        POINTS AT BFR FOR FOLDING MSG
RWORK    EQU   10                       WORK REGISTER
RENDMSG  EQU   11                       POINTS AT END OF MSG
RBASE    EQU   12                       BASE REGISTER
R12      EQU   12
R13      EQU   13                       CONTAINS USER PGM SAVE AREA
R14      EQU   14                       CONTAINS RETURN ADDR
R15      EQU   15                       CONTAINS ADDR TO BRACH TO
RRETURN  EQU   15                       CONTAINS RETURN CODE
         SPACE 3
********
******** EQUATES FOR DISPLACEMENT, LENGTH AND MASK
********
C3       EQU   3                        CONSTANT OF 3
C15      EQU   15                       CONSTANT OF 15
ZERO     EQU   X'00'                    CONSTANT 0
MFE      EQU   X'FE'                    MASK TO ZERO OUT RESIDUAL CT
M0C      EQU   X'0C'                    DENOTES CHANNEL END DEVICE END
M3F      EQU   X'3F'                    MASK TO TURN OFF WT BIT AND
*                                       TURN OFF READ BACKWARD BIT IN
*                                       DCB
M7F      EQU   X'7F'                    COMPLETION BIT IN ECB
M41      EQU   X'41'                    DENOTES ECB COMPLETED WITH I/O
*                                       ERROR (EOD)
M80      EQU   X'80'                    DENOTES 'S' SPECIFIED IN DECB
*                                       MASK FOR F-TYPE RECORD
MC0      EQU   X'C0'                    DENOTES UNDEFINED RCD LENGTH
M40      EQU   X'40'                    DENOTES VARIABLE RCD LENGTH
EOF      EQU   X'20'                    EOF HAS BEEN ENCOUNTERED
M8       EQU   8                        USED IN SHIFTING
D0       EQU   0                        DISPLACEMENT OF 0
D1       EQU   1                        DISPLACEMENT OF 1
D2       EQU   2                        DISPLACEMENT OF 2
D4       EQU   4                        DISPLACEMENT OF 4
D8       EQU   8                        DISPLACEMENT OF 8        A42898
D12      EQU   12                       DISPLACEMENT OF 12
D20      EQU   20                       DISPLACEMENT OF 20
L2       EQU   2                        LENGTH OF 2
*****
*****       MASKS FOR ISOLATING LOWER CASE ALPHA CHARACTERS
*****       FOR FOLDING TO UPPERCASE
*****
M81      EQU   X'81'                    LOWERCASE A             SA69503
MA9      EQU   X'A9'                    LOWERCASE Z             SA69503
M89      EQU   X'89'                    LOWERCASE I             SA69503
M90      EQU   X'90'                    SPECIAL CHARACTER       SA69503
M99      EQU   X'99'                    LOWERCASE R             SA69503
MA1      EQU   X'A1'                    SPECIAL CHARACTER       SA69503
UPPER    EQU   X'40'                    USED TO FOLD MSG        SA69503
         SPACE 3
         SPACE 3
********
******** SAVE REGISERS AND ESTABLISH ADDRESSABILITY
********
         STM   R14,R12,D12(R13)         SAVE REGS
         LR    RBASE,R15                ENTRY ADDR AS BASE ADDR
         USING IGG019T7,RBASE           ESTABLISH ADDRESSABILITY
         USING IHADCB,RDCB              ESTABLISH ADDRESSABILITY
         USING DECB,RDECB               ESTABLISH ADDRESSABILITY
         USING DEB,RDEB                 ESTABLISH ADDRESSABILITY
         SPACE 3
********
******** GET BUFFER ADDRESS AND BUFFER SIZE
********
         LR    RDECB,R1                 SAVE DECB ADDR
         L     RDCB,DECDCBAD            GET DCB ADDR
         LA    RECB,DECSDECB            GET ADDR OF ECB
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
         TM    DCBCIND1,EOF             IS EOF REACHED
         BO    RETURN                   BRANCH IF YES
         MVI   D0(RECB),M80             TURN ON ECB WAIT BIT
         LA    RWORK,DCBIOBL            PUT DUMMY IOB ADDR IN REG
         ST    RWORK,DECIOBPT           PUT DUMMY IOB ADDR IN DECB
         L     RBUFAD,DECAREA           GET BUFFER ADDR
         TM    DCBRECFM,MC0             TEST FOR U-TYPE RECORD
         BNO   NOTU                     NO, GO TO NOTU
         TM    DECTYPE,M80              TEST FOR 'S' SPECIFIED IN DECB
         BZ    DECBLNG                  NO, GO TO GET LNG FROM DECB
         LH    RBUFSZ,DCBBLKSI          GET BUFFER SIZE FROM DCB
         B     FIXED                    GO TO FIXED
DECBLNG  EQU   *
         LH    RBUFSZ,DECLNGTH          LOAD BUFFER SIZE
         N     RBUFSZ,ZEROHALF          ZERO OUT HIGH ORDER 2 BYTES
         B     FIXED                    GO TO ISSUE TGET
NOTU     EQU   *
         LH    RBUFSZ,DCBBLKSI          GET BUFFER SIZE FROM DCB
         TM    DCBRECFM,M40             TEST FOR V-TYPE RECORD
         BZ    FIXED                    NO, IT IS F-TYPE, GO TO FIXED
VTYPE    EQU   *
         LA    RBUFAD,D8(RBUFAD)        UPDATE PTR TO PASS       A42898
*                                       CONTROL WDS              A42898
         SH    RBUFSZ,EIGHT             SUBTRACT LNG FOR CONTROL A42898
*                                       WDS                      A42898
********
******** PREPARE PARAMETERS AND ISSUE TGET
********
FIXED    EQU   *
         LR    R0,RBUFSZ                LOAD BFR SIZE
         LR    R1,RBUFAD                LOAD BFR ADDRESS
         TGET  (1),(0)                  ISSUE TGET
         SPACE 3
******** TEST RETURN CODE FROM TGET
********
CKATTN   EQU   *
         CH    RRETURN,EIGHT            TEST FOR ATTENTION INTERRUPT
         BE    FIXED                    YES, REISSUE TGET
         NI    DCBOFLGS,M3F             TURN OFF WRITE BIT (THIS IS A
*                                       READ OPERATION), AND TURN OFF
*                                       READ BACKWARD BIT
         SPACE 3
********
******** FOLD INPUT LOWERCASE ALPHA TO UPPERCASE
********
         LTR   R1,R1                    IS THERE ANY MSG TO FOLD
         BZ    URECORD                  NO, DO NOT FOLD
         LR    RPTR,RBUFAD              POINTS AT START OF BFR
         LA    RWORK,D1                 SET UP LOOP INDEX
         LR    RENDMSG,RBUFAD           POINTS AT START OF BUFFER
         AR    RENDMSG,R1               POINTS AT END OF BFR PLUS 1
         BCTR  RENDMSG,R0               POINTS AT END OF BUFFER
LOOP     EQU   *
         CLI   D0(RPTR),M81             COMPARE TO LOWER A      SA69503
         BL    TEST                     IF LOW, DONT FOLD       SA69503
         CLI   D0(RPTR),MA9             COMPARE TO LOWER Z      SA69503
         BH    TEST                     IF HIGH, DONT FOLD      SA69503
*****
*****       ONLY LOWERCASE ALPHA & IMBEDDED SPECIAL CHARACTERS
*****       WILL FALL THROUGH THE ABOVE TESTS
*****
         CLI   D0(RPTR),M89             COMPARE TO LOWER I      SA69503
         BNH   FOLD                     FOLD LOWER A-I          SA69503
         CLI   D0(RPTR),MA1             COMPARE TO SPEC         SA69503
         BH    FOLD                     FOLD LOWER S-Z          SA69503
         CLI   D0(RPTR),M90             COMPARE TO SPEC         SA69503
         BNH   TEST                     DONT FOLD SPEC          SA69503
         CLI   D0(RPTR),M99             COMPARE TO LOWER R      SA69503
         BH    TEST                     DONT FOLD SPEC          SA69503
*                                       FOLD LOWER J-R          SA69503
FOLD     EQU   *                                                SA69503
         OI    D0(RPTR),UPPER           FOLD LOWERCASE          SA69503
*                                       ALPHA TO UPPER          SA69503
TEST     EQU   *
         BXLE  RPTR,RWORK,LOOP          INCREMENT PTR AND TEST
         SPACE 3
********
******** FOR U/V TYPES OF RECORD, RETURN MESSAGE LENGTH IN
******** DCBLRECL, RETURN RESIDUAL COUNT IN DUMMY IOB
********
URECORD  EQU   *
         TM    DCBRECFM,M40             TEST IF TYPE U OR V
         BO    RESIDUAL                 BRANCH IF YES
         NI    DCBIOBA+C3,MFE           ZERO OUT RESIDUAL COUNT
         B     CKMORMSG                 DO NOT UPDATE LRECL
RESIDUAL EQU   *
         SR    RBUFSZ,R1                GET RESIDUAL COUNT
         MVI   DCBIOBA,M0C              SET STATUS TO CHANNEL END,
         MVI   DCBIOBA+1,ZERO           DEVICE END IN DUMMY IOB
         STH   RBUFSZ,DCBIOBA+2         PUT RESIDUAL BYTE COUNT IN
*                                       DUMMY IOB
         TM    DCBRECFM,M80             IS IT U OR F TYPE RECORD
         BO    RTNLNGTH                 BRANCH IF YES
         LR    RWORK,R1                 GET MESSAGE LENGTH
         AH    RWORK,FOUR               ADD LENGTH OF RECORD
*                                       DESCRIPTOR WORD
         STH   RWORK,DCBLRECL           RETURN PHYSICAL RECORD LENGTH
         B     CKMORMSG                 GO TO FURTHER CHECK
RTNLNGTH EQU   *
         STH   R1,DCBLRECL              RETURN PHYSICAL RECORD LENGTH
CKMORMSG EQU   *
         CH    RRETURN,TWELVE           ARE THERE MORE DATA TO GET
         BNE   SUCCESS                  NO, GO TO SUCCESS
         SPACE 3                                                 A42898
********
******** TEST FOR V-TYPE RECORD, IF SO FILL IN CONTROL WORD
********
DESCRIBE EQU   *                                                 A42898
         TM    DCBRECFM,M80             TEST FOR FIXED RECORD
         BO    RETURN                   BRANCH IF YES
         SH    RBUFAD,FOUR              RESTORE PTR TO POINT AT  A42898
*                                       RECORD DESCRIPTOR WORD
         AH    R1,FOUR                  ADD LENGTH FOR RCD DESCRIPTOR
*                                       WORD
         LR    RWORK,R1                 PUT LENGTH IN WORK       A42898
*                                       REGISTER                 A42898
         STC   RWORK,D1(RBUFAD)         PUT LOW ORDER BYTE IN LL A42898
*                                       FIELD                    A42898
         SRL   RWORK,M8                 GET HIGH ORDER BYTE      A42898
         STC   RWORK,D0(RBUFAD)         PUT LOW ORDER BYTE IN LL A42898
*                                       FIELD                    A42898
         XC    D2(L2,RBUFAD),D2(RBUFAD) ZERO OUT BB FIELD        A42898
         SH    RBUFAD,FOUR              RESTORE PTR TO POINT AT  A42898
*                                       BLOCK DESCRIPTOR WORD
         AH    R1,FOUR                  ADD LENGTH FOR BLOCK     A42898
*                                       DESCRIPTOR WORD
         STC   R1,D1(RBUFAD)            PUT LOW ORDER BYTE IN LL FIELD
         SRL   R1,M8                    GET HIGH ORDER BYTE
         STC   R1,D0(RBUFAD)            PUT LOW ORDER BYTE IN LL FIELD
         XC    D2(L2,RBUFAD),D2(RBUFAD) ZERO OUT BB FIELD
         SPACE 3
RETURN   EQU   *
         MVI   D0(RECB),M7F             SET COMPLETION BIT IN ECB
RESTORE  EQU   *
         L     R14,D12(R13)             RESTORE REG 14
         LM    R0,R12,D20(R13)          RESTORE REGS 0-12
         BR    R14                      RETURN CONTROL TO CALLER
         SPACE 3
SUCCESS  EQU   *
         CH    R1,TWO                   IS MESSAGE LENGTH 2
         BNE   DESCRIBE                 BRANCH IF NO
CKBUF    EQU   *
         CLC   D0(L2,RBUFAD),END        IS THERE A /* IN BUFFER
         BNE   DESCRIBE                 BRANCH IF NO
         SPACE 3
         MVI   D0(RECB),M41             SET ECB I/O ERROR BIT
         OI    DCBCIND1,EOF             SIGNAL EOF
         B     RESTORE                  GO TO RESTORE REGISTERS
         SPACE 3
         SPACE 3
********
******** CONSTANTS
********
         DS    0F                       FORCE WORD BOUNDARY
ZEROHALF DC    X'0000FFFF'              USED TO CLEAR HIGH ORDER 2
*                                       BYTES OF REGISTER
TWO      DC    H'2'                     CONSTANT OF 2
EIGHT    DC    H'8'                     CONSTANT OF 8
TWELVE   DC    H'12'                    CONSTANT OF 12
FOUR     DC    H'4'                     CONSTANT OF 4
SIXTEEN  DC    H'16'                    CONSTANT OF ×6
END      DC    C'/*'                    DENOTE END OF DATA SET
         SPACE 3
         DS    0F
DECB     DSECT
DECSDECB DS    CL4
DECTYPE  DS    CL2
DECLNGTH DS    CL2
DECDCBAD DS    CL4
DECAREA  DS    CL4
DECIOBPT DS    CL4
         SPACE 3
         DS    0F
BUFCB    DSECT                          BUFFER POOL CONTROL BLOCK
BUFAD    DS    CL4                      ADDR OF FIRST AVAILABLE BFR
BUFNO    DS    CL2                      NUMBER OF BUFFER REQUESTED
BUFL     DS    CL2                      LENGTH OF EACH BUFFER
         SPACE 3
         DS    0F
DEB      DSECT
         DS    CL32                     DEB BASIC SECTION
         DS    CL4                      DEB EXTENT
         DS    CL12                     DEB BSAM/QSAM DEPENDENT SECTION
DEBBLKSI DS    CL2                      BLOCK SIZE SAVE AREA
DEBLRECL DS    CL2                      LRECL SAVE AREA
         SPACE 3
         DCBD  DSORG=PS
         END
