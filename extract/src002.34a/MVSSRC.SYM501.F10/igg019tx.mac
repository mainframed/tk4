         TITLE 'IGG019TX - TS CHECK ROUTINE'
IGG019TX CSECT
***********************************************************************
*                                                                     *
* RELEASE 23 DELETIONS/CHANGES                                        *
* RELEASE 22 DELETIONS/CHANGES                                        *
* RELEASE 21 DELETIONS/CHANGES                                        *
*2182535000,756000-774000                                        S21042
*                                                                     *
*STATUS CHANGE LEVEL 002
*                                                                     *
* NAME - IGG019TX - TS CHECK.                                         *
*                                                                     *
* FUNCTION - THE MODULE CHECKS THE ECB COMPLETION CODE FOR END OF     *
*            FILE CONDITION. WHEN THIS CONDITION ARISES, THE MODULE   *
*            GIVES CONTROL TO USER'S EOD ROUTIN                       *
*                                                                     *
* ENTRY POINT - IGG019TX, BEGINNING OF MODULE                         *
*                                                                     *
*               CONTROL IS RECEIVED WHEN THE FOLLOWING CONDITIONS ARE *
*               MET.                                                  *
*                                                                     *
*               THE TASK IS TIME SHARING                              *
*               THE DCB OPENED IS FOR BSAM                            *
*               A CHECK MACRO INSTRUCTION IS ISSUED                   *
*                                                                     *
*               UPON ENTRY TO THIS MODULE                             *
*                                                                     *
*                        REGISTER 1 POINTS AT USER DECB ADDRESS       *
*                        REGISTER 15 POINTS AT ENTRY POINT            *
*                                                                     *
* INPUT - DECB NAME, TYPE FIELD, DCB ADDRESS, BUFFER ADDRESS, 'S' OR  *
*         LENGTH OF DATA.                                             *
*                                                                     *
* OUTPUT - N/A.                                                       *
*                                                                     *
* EXIT - NORMAL - RETURN CONTROL TO CALLER VIA 'BR 14' INSTRUCTION.   *
*                 WHEN EOD IS DETECTED, BRANCH TO USER EOD ROUTINE    *
*                 VIA 'BR 15' INSTRUCTION.                            *
*                                                                     *
*        ERROR - AT END OF FILE, NO EOD ROUTINE IS FURNISHED, JOB     *
*                WILL BE ABENDED WITH SYSTEM CODE 337                 *
*                                                                     *
* EXTERNAL REFERENCE - NONE                                           *
*                                                                     *
* TABLE/WORK AREAS - NONE                                             *
*                                                                     *
* ATTRIBUTES - REENTRANT, REUSABLE                                    *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
********
R0       EQU   0                        REGISTER 0
R1       EQU   1                        REGISTER 1
RDECB    EQU   1                        POINTS AT DECB
RDCB     EQU   2                        POINTS AT DCB
R3       EQU   3                        WORK REGISTER
RECB     EQU   4                        POINTS AT ECB
RBASE    EQU   12                       BASE REGISTER
R12      EQU   12                       REGISTER 12
R13      EQU   13                       CONTAINS ADDR OF SAVE AREA
R14      EQU   14                       CONTAINS RETURN ADDR
R15      EQU   15                       CONTAINS ADDR TO BRACH TO
         SPACE 3
********
******** EQUATES FOR MASKS, DISPLACEMENTS, CONSTANTS
********
D0       EQU   0                        DISPLACEMENT OF 0
D3       EQU   3                        DISPLACEMENT OF 3
D12      EQU   12                       DISPLACEMENT OF 12
D20      EQU   20                       DISPLACEMENT OF 20
M01      EQU   X'01'                    MASK OF X'01'
M12      EQU   12                       MASK OF 12
EOD      EQU   X'41'                    DENOTES END OF DATA SET
EABD174  EQU   174                                               S21042
         SPACE 3
********
******** SAVE REGISTERS AND ESTABLISH ADDRESSABILITY
********
         STM   R14,R12,D12(R13)         SAVE REGISTERS
         LR    RBASE,R15                ENTRY ADDR AS BASE ADDR
         USING IGG019TX,RBASE           ESTABLISH ADDRESSABILITY
         USING IHADCB,RDCB              ESTABLISH ADDRESSABILITY
         USING DECB,RDECB               ESTABLISH ADDRESSABILITY
         SPACE 3
********
******** GET POINTER TO NECESSARY BLOCKS
********
         L     RDCB,DECDCBAD            GET ADDR OF DCB
         LA    RECB,DECSDECB            GET ADDR OF ECB
         SPACE 3
********
******** TEST FOR COMPLETION OF EVENT
********
         CLI   D0(RECB),EOD             IS EOF REACHED
         BNE   RETURN                   BRANCH ON NO
CKEOD    EQU   *
         TM    DCBEODAD+D3,M01          IS EOD EXIT ADDR FURNISHED
         BZ    GOTHERE                  BRANCH ON YES
         DMABCOND EABD174,DCB=(RDCB),SVC=YES                     S21042
GOTHERE  EQU   *
         L     R15,DCBEODAD             LOAD EOD EXIT ADDR
         L     R14,D12(R13)             RESTORE REG 14
         LM    R0,R12,D20(R13)          RESTORE REGS 0-12
         BR    R15                      TAKE EOD EXIT
         SPACE 3
RETURN   EQU   *
         LM    R14,R12,D12(R13)         RESTORE REGISTER
         BR    R14                      RETURN CONTROL TO CALLER
         SPACE 3
         DS    0F
DECB     DSECT
DECSDECB DS    CL4
DECTYPE  DS    CL2
DECLNGTH DS    CL2
DECDCBAD DS    CL4
DECAREA  DS    CL4
DECIOBPT DS    CL4
         SPACE 3
         DCBD  DSORG=PS
         END
