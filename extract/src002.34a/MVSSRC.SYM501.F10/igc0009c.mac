         TITLE 'IGC0009C - FIRST LOAD OF TGET AND TPUT'
***********************************************************************
*
*
*          RELEASE 4 VS/2 CHANGES                               ZA16607
* A 70750-70920,113000,207414-207462,207485,715050,718800       ZA24648
* A 153900,217970,218000,219470,219500                          ZA32421
* C 218040,219540                                               ZA32421
* D 218140,219640                                               ZA32421
* A 203200,203300,654700-658200                                 ZA11383
* C 206928,206948                                               ZA11383
* D 206960,206970                                               ZA11383
*                                                               ZA16607
*C019000,022000,259700,260192,                                  ZA16607
*C260497,260592,884200                                          ZA16607
*                                                               ZA16607
*A019500,022100-022400,072300,072600,074100-074500,             ZA16607
*A106500,113040,206917,206924,206931,207010-238310              ZA16607
*A259904-259616,259930,260562                                   ZA16607
*260712,714860-715040,894700,895200                             ZA16607
*                                                               ZA16607
*D022000,206920,206928,206930,206940-206970,260498,             ZA16607
*D268000-345300                                                 ZA16607
*                                                               ZA16607
*                                                               ZA16607
*          RELEASE 2 VS/2 DELETIONS                                   *
*          RELEASE 1 VS/2 DELETIONS                                   *
*          RELEASE 21 DELETIONS                                       *
*          UY70029 IS THE SAME AS US02278                       YA00677
*          US02278 FIXES APAR 60002                             SA60002
*D001760-001800,263000-264000                                   SA60002
*0834                                                            M1807
* A498820                                                       ZA12514
*0834393600,425500,622000                                        M1800
*0834383300                                                      M1757
*0834304000                                                      A46079
*0834155000,158000-159000,170000-173600,174000-176600,183000,    M0094
*0834185000,200100,200400,202200,202300-202900,203560,213000,    M0094
*0834215000,263000,265000,271000,278000-279000,316000-317000,    M0094
*0834320000,322000,332000-334000,346000-347000,369000,383600,    M0094
*0834461000-471000,498400,507000,520000-520500,525000,526400,    M0094
*0834589000-590000,593000,639000,643000,645000-646000,654000-    M0094
*0834657000,694300,707000-708000,749000-750000,764000-765000     M0094
*0834042000,159000                                               S21A12
*0834381600-382200,384000-418000,420000-490000,491000,589000,    S21008
*0834612100-633000,639000,759000-763000                          S21008
*0834763400                                                      S22028
*          RELEASE 22 DELETIONS                                       *
*                                                                     *
*                                                                M3281
*                                                                M2152
*                                                                M3279
*0000105500,113900,205200-205600                                 M0019
*0000204200-205200,359400-360200,412200-413000                   M0607
*                                                                M0661
*                                                                TS1628
*                                                                 M2265
*                                                                 M3491
*                                                                M2794
*                                                                M2331
*                                                                 M3830
*                                                                M4395
*                                                                M4424
*                                                                 M5312
* STATUS CHANGE LEVEL 003                                       ZA16607
*                                                                     *
* FUNCTION -                                                          *
*    GET INPUT FROM TERMINAL VIA THE TIOC(TCAM TERMINAL) OR     ZA16607
*    VTIOC(VTAM TERMINAL) AND PASS IT TO THE USER'S BUFFER      ZA16607
*    PUT OUTPUT FROM THE USER'S BUFFER TO THE TERMINAL VIA THE  ZA16607
*    TIOC(TCAM TERMINAL) OR VTIOC(VTAM TERMINAL)                ZA16607
*    TCAM VS. VTAM IS DETERMINED VIA TSBVTAM:                   ZA16607
*        TSBVTAM=0: TCAM                                        ZA16607
*        TSBVTAM=1: VTAM                                        ZA16607
*                                                                     *
*    PROVIDE LIMITED EDITING OPTIONS FOR BOTH INPUT AND OUTPUT.       *
*    PROVIDE FOR OUTPUT OF MESSAGES TO TERMINALS OTHER THAN THE       *
*    ONE ASSOCIATED WITH THE TASK.                                    *
*                                                                     *
* ENTRY POINTS -                                                      *
*    SVC - INITIAL ENTRY TO THIS MODULE WHEN AN SVC 93 OR A           *
*           TPUT OR TGET MACRO INSTRUCTION IS ISSUED.                 *
*                                                                     *
* INPUT -                                                             *
*    REGISTERS 0, 1, AND POSSIBLY 15 CONTAIN PARAMETERS AS FOLLOWS:   *
*        **********************************                           *
*    REG0*RAAAAAAAAAAAAAAARSSSSSSSSSSSSSSS*                           *
*        **********************************                           *
*           WHERE - R EQUALS RESERVED                                 *
*                   A EQUALS ASID OF DESTINATION TERMINAL             *
*                   S EQUALS USER'S BUFFER SIZE, IN BYTES             *
*        **********************************                           *
*    REG1*OULWHBEEPPPPPPPPPPPPPPPPPPPPPPPP*                           *
*        **********************************                           *
*           WHERE - O EQUALS OPTION - 0=TPUT, 1=TGET                  *
*                   U EQUALS USERID SPECIFIED BY REG 15               *
*                   L EQUALS LEVEL OF PRIORITY /0 HIGHP/1 LOWP
*                   W EQUALS WAIT  /0 WAIT/1 NOWAIT                   *
*                   H EQUALS HOLD  /0 NOHOLD/1 HOLD                   *
*                   B EQUALS BREAR  /0 NOBREAK/1 BREAKIN              *
*                  EE EQUALS EDIT  /00 EDIT/01 ASIS/10 CONTROL        *
*                   P EQUALS POINTER TO USER'S BUFFER                 *
*                                                                     *
*    REG 15 CONTAINS A POINTER TO THE USER ID, IF 'U,' ABOVE, IS      *
*    ONE.                                                             *
*                                                                     *
* OUTPUT -                                                            *
*    RETURN CODE IN REGISTER 15 -                                     *
*        00 SUCCESSFUL.                                               *
*        04 TPUT - NOWAIT SPECIFIED AND NO TIOC OUTPUT                *
*                  BUFFER WAS AVAILABLE.                              *
*           TGET - NOWAIT SPECIFIED AND NO INPUT READY.               *
*        08 ATTENTION INTERRUPT                                       *
*        0C TPUT - NO INTER-TERMINAL COMMUNICATION BIT ON             *
*           TGET THE BUFFER WAS TOO SMALL FOR INPUT GIVEN.            *
*        10 INVALID PARAMETERS SPECIFIED                              *
*        14 USER NO LONGER ACTIVE                                     *
*           OR THE TERMINAL USER HUNG UP                              *
*                                                                     *
*   TGET -                                                            *
*        LENGTH IN BYTES OF MSG GOTTEN IS GIVEN IN REG 1.             *
*                                                                     *
* EXTERAL ROUTINES -                                                  *
*         MODESET                                                     *
*         QTIP                                                        *
*         SETLOCK                                                     *
*         STATUS                                                      *
*         XCTL                                                        *
*         IKTTMPX1                                              ZA24648
*            REG 15 RETURN CODE FROM IKTTMPX1                   ZA24648
*                    RC < 0 NORMAL PROCESSING SHOULD CONTINUE   ZA24648
*                       >=0 THE SVC SHOULD RETURN TO THE CALLER ZA24648
*                           PASSING THRU THE VALUE IN REG 15    ZA24648
*                                                                     *
* EXITS,NORMAL -                                                      *
*                                                                     *
*  FOR TCAM REQUESTS:                                                 *
*    RETURN TO SUPERVISOR VIA BR 14, OR                               *
*    BRANCH TO MODULE IGG09301 TO DO A NORMAL TPUT, OR                *
*    BR TO MODULE IGG09302 TO DO AN ASID TPUT OR A USERID TPUT, OR    *
*    BR TO MODULE IGG09303 AFTER A TGET, TO REMOVE WAITS              *
*                                                                     *
*  FOR VTAM REQUESTS:                                           ZA16607
*    BR TO MODULE IKTVTPUT TO DO A NORMAL TPUT, OR              ZA16607
*    BR TO MODULE IKTASTPT TO DO AN ASID TPUT OR A USERID TPUT, OR
*    BR TO MODULE IKTVTGET TO DO A TGET.                        ZA16607
*                                                                     *
* EXITS,ERROR                                                         *
*    THERE ARE NO ERROR EXITS FROM THIS ROUTINE.                      *
*                                                                     *
* TABLES/WORKAREAS -                                                  *
*    THE FORMAT AND CONTENTS OF THE WORK AREA USED ARE DESCRIBED      *
*    BY THE DSECTS AT THE END OF THE LISTING.                         *
*    ALSO, THE EXTENDED SAVE AREA OF THE SVRB IS USED AS A SAVE AND   *
*    WORK AREA.                                                       *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, ENABLED, PRIVILEGED, LOCAL LOCK HELD IMPLICITLY,      *
*    CMS LOCK GOTTEN EXPLICITLY.                                      *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*                                                                     *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS
*    EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE
*    CODING HAS BEEN ARRANGED SO THAT REDEFINITION OF
*    'CHARACTER' CONSTANTS, BY REASSEMBLY, WILL RESULT IN A
*    CORRECT MODULE FOR THE NEW DEFINITIONS.
*                                                                     *
*                                                                     *
* NOTES -                                                             *
*    NONE                                                             *
*                                                                     *
***********************************************************************
         SPACE 2
IGC0009C CSECT
         SPACE 2
***
***                 REGISTER EQUATES
***
R0       EQU   0                        REG USED FOR ASM SPEED
RP0      EQU   0                        FIRST PARAMETER REGISTER
RWRK4    EQU   0                        WORK REGISTER           ZA16607
RP1      EQU   1                        SECOND PARAMETER REGISTER
RSDWA    EQU   1                        POINT TO SDWA IN ESTAE  YS02019
RBUF     EQU   2                        POINTER TO TS BUFFER
RWSA     EQU   2                        POINTER TO ESTAE WSA    YS02019
RWRK02   EQU   2                        WORK REGISTER           YS02019
RCVT     EQU   3                        POINTER TO CVT
RWRK3    EQU   3                        WORK REGISTER           YS02019
RTCB     EQU   4                        POINTER TO TCB
RSVRB    EQU   5                        POINTER TO SVRB
RBASE    EQU   6                        BASE REGISTER
RWRK2    EQU   7                        WORK REGISTER           YS02019
RTSBX    EQU   7                        POINTER TO TSB EXTEN    ZA16607
REG7     EQU   7                                                ZA24648
RASCB    EQU   8                        POINTER TO ASCB         YS02019
RTSB     EQU   9                        POINTER TO TSB
RRPT     EQU   10                       POINTER TO TIOCRPT
RESTAE   EQU   10                       POINTER TO TSO/VTAM ESTAZA16607
RTCX     EQU   11                       POINTER TO TCAM CVT EXT YS02019
RECB     EQU   11                       ECB TO BE POSTED POINTER
RJSCB    EQU   12                       POINTER TO JSCB
RTJID    EQU   12                       TJID OF TASK
RAVT     EQU   13                       POINTER TO AVT
RSAVE    EQU   13                       SAVE REGISTER
RWRK     EQU   14                       WORK REGISTER
RETURN   EQU   14                       RETURN ADDRESS REGISTER
RET      EQU   15                       ENTRY POINTER
RWRK1    EQU   15                       WORK REGISTER
RCODE    EQU   15                       RETURN CODE REGISTER
***
***                 CONSTANTS
***
K0       EQU   0                        CONSTANT OFFSET
K1       EQU   1                        CONSTANT OFFSET AND SIZE
K3       EQU   3                        CONSTANT OFFSET         YS02019
K4       EQU   4                        CONSTANT OFFSET         YS02019
K12      EQU   12                       CONSTANT OFFSET         YS02019
WORD4    EQU   12                       OFFSET                  YS02019
SDTCB    EQU   15                       ENTRY CODE FOR 'STATUS' YS02019
CLEANTSB EQU   29                       QTIP 29 ENTRY CODE      YS02019
***
***                 RETURN CODES
***
RC04     EQU   X'04'                    NOWAIT AND NO DATA
RC08     EQU   X'08'                    ATTENTION HIT DURING OPERATION
RC0C     EQU   X'0C'                    BFR TOO SMALL FOR DATA   M0607
RC10     EQU   X'10'                    INVALID PARAMETERS
RC14     EQU   X'14'                    USER GONE AWAY
***
***                 MASKS
***
OFF      EQU   X'FF'                    TO TURN OFF BITS        YS02019
NDISPBIT EQU   X'800'                   MASK FOR 'STATUS'       YS02019
NP       EQU   13                       FOR BCR INSTRUCTIONS    YS02019
ICM7     EQU   7                        FOR ICM INSTRUCTIONS    ZA32421
         SPACE 2
***
***                 OTHER EQUATES
***
RETPSW   EQU   20                       LOWER HALF OF RB OLD PSW
EDITCODE EQU   18                       QTIP ENTRY CODE FOR EDIT
MULTBY4  EQU   2                        SHIFT VAL FOR TIMES 4   YS02019
PARTT    EQU   X'400'                   PARTIAL FLAG FOR SYSEVENT
*                                       NEEDS TO BE SHIFTED LEFT
CCODE15D EQU   X'15D'                   COMPLETION CODE 15D     YS02019
         SPACE 2
         BALR  RBASE,R0                 SET BASE REGISTER
         USING *,RBASE                  ADDRESSABILITY
         SPACE 3
         EJECT
***********************************************************************
***                                                                 ***
***      THIS MODULE HOLDS THE LOCAL LOCK AT ENTRY                  ***
***                                                                 ***
***********************************************************************
         SPACE 3
***********************************************************************
***                                                                 ***
***                 COMMON  TGET / TPUT  ROUTINE                    ***
***                                                                 ***
***********************************************************************
         SPACE 3
         USING CVT,RCVT
         USING TCB,RTCB
         USING TIOCBUF,RBUF
         USING RBSECT,RSVRB                                      M0094
         ST    RETURN,XSARETRG          SAVE RETURN ADDRESS     YS02019
         ST    RET,XSAUSERP             SAVE USERID POINTER     YS02019
         STM   RP0,RP1,XSAPRM0          SAVE PARAMETERS IN RB    Y01018
         B     TGT0005                  BRANCH AROUND I.D.       Y01018
         DC    C'IGC0009C'              MODULE I.D.              Y01018
         DC    X'7007'                  JULIAN DATE - 01/07/77  ZA24648
TGT0005  EQU   *                                                YS02019
         MVI   XSAFLAG,K0               INITIALIZE FLAGS         M0094
         MVI   XSAFLAG2,K0              INITIALIZE FLAGS         S22028
         SPACE 2                                                YS02019
***
***                 TURN ON TCBFX TO PREVENT ASYNCHRONOUS EXITS,
***                 BUT RECORD PREVIOUS SETTING SO IT CAN BE RESTORED
***
         TM    TCBFLGS1,TCBFX           WAS TCBFX ON AT ENTRY
         BO    TGT0010                  IF SO, SKIP
         OI    XSAFLAG,XSATCBFX         IF NOT, INDICATE THAT WE M0094
*                                       SHOULD TURN IT OFF LATER
TGT0010  EQU   *
         OI    TCBFLGS1,TCBFX           PREVENT ASYNCHRONOUS EXIT
         SPACE
***
***                 GET TSO CONTROL BLOCK POINTERS
***
         L     RASCB,PSAAOLD-PSA        GET ASCB ADDRESS        YS02019
         USING ASCB,RASCB               ASCB ADDRESSABLE        YS02019
         L RTSB,ASCBTSB             GET TSB POINTER             ZA11383
         USING TSB,RTSB                                         ZA11383
         LH    RTJID,ASCBASID           GET ASID                YS02019
         L     RTCX,CVTASVT             GET ASVT POINTER        YS02019
         USING ASVT,RTCX                                        YS02019
         LH    RWRK,XSAPRMTJ            GET DEST ASID FOR CHECK YS02019
         C     RWRK,ASVTMAXU            CHECK FOR TOO HIGH      YS02019
         BH    TGT7000                  IF IT IS, YOU'RE OUT    YS02019
         DROP  RTCX                                             YS02019
***                                                             ZA16607
***                CHECK FOR VALID USER BUFFER SIZE             ZA16607
***                                                             ZA16607
         LH    RWRK1,XSAPRMSZ           GET BUFFER SIZE         ZA16607
         LTR   RWRK1,RWRK1              CHECK FOR EXCESSIVE     ZA16607
*                                       BUFFER SIZE             ZA16607
         BM    TGT7000                  IF TOO LARGE GO TO      ZA16607
*                                       ILLEGAL PARAMETER RETURNZA16607
         L     RET,USEREXIT             GET @ OF TERM MGR EXIT  ZA24648
         LTR   RET,RET                  IS THERE A USER EXIT    ZA24648
         BZ    TGTTPT                   BRANCH ON NO            ZA24648
         LR    REG7,RASCB               PUT ASCB @ IN REG 7     ZA24648
         BALR  RETURN,RET               BRANCH TO USER EXIT     ZA24648
         LTR   RCODE,RCODE              TEST RETURN CODE        ZA24648
         BNM   TGT9000                  BRANCH ON CODE >=0      ZA24648
*                                                               ZA16607
****************************************************************ZA16607
****                                                        ****ZA16607
****            DETERMINE IF TGET OR TPUT OR TPUT W/ASID    ****ZA16607
****                                                        ****ZA16607
****************************************************************ZA16607
*                                                               ZA16607
TGTTPT   EQU   *                                                ZA24648
         LTR   RP1,RP1                  IS  THIS A TGET REQUEST ZA16607
         BM    TGT0100                  IF  TGET  GO  TEST      ZA16607
*                                       TCAM/VTAM               ZA16607
         TM    XSAOPTNS,MUSERID         WAS USERID  SPECIFIED   ZA16607
         BNO   TGT0008                  NO SKIP CHECK           ZA16607
         L     RWRK,ASCBCSCB            GET CSCB POINTER        ZA16607
         L     RET,XSAUSERP             GET USERID POINTER      ZA16607
         LTR   RWRK,RWRK                IS THERE A CSCB         ZA16607
         BZ    TGT0012                  NO CSCB..CAN'T BE  SAME ZA16607
         USING CSCB,RWRK                                        ZA16607
         CLC   CHKEY,0(RET)             CHECK CALLER'S USERID   ZA16607
         DROP  RWRK                                             ZA16607
         BE    TGT0100                  IF SAME, NORMAL TPUT    ZA16607
*                                       CHECK TCAM/VTAM         ZA16607
         B     TGT0012                  IF NOT, ASID TPUT       ZA16607
TGT0008  LH    RWRK,XSAPRMTJ            PICK UP  SPECIFIED ID   ZA16607
         LTR   RWRK,RWRK                IS ASID POSITIVE        ZA16607
         BM    TGT7000                  NEGATIVE...INVALID      ZA16607
         BZ    TGT0100                  NO ASID SPECIFIED...    ZA16607
*                                       NORMAL TPUT. CHECK      ZA16607
*                                       TCAM/VTAM               ZA16607
         CR    RTJID,RWRK               IS ASID SAME AS THAT OF ZA16607
*                                       THE CALLING TASK        ZA16607
         BE    TGT0100                  YES. NORMAL TPUT. CHECK ZA16607
*                                       TCAM/VTAM               ZA16607
         EJECT
*                                                               ZA16607
****************************************************************ZA16607
****                                                        ****ZA16607
****           CROSS MEMORY TPUT...DETERMINE WHETHER TARGET ****ZA16607
****           MEMORY IS USING TCAM OR VTAM AS ACCESS METHOD****ZA16607
****           BY SEARCHING THE TSB'S                       ****ZA16607
****                                                        ****ZA16607
****************************************************************ZA16607
*                                                               ZA16607
***                                                             ZA16607
***            NEED CMS LOCK FOR TSB                            ZA16607
***                                                             ZA16607
TGT0012  SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                   ZA16607-
               RELATED=(TSB,IGC0009C(TGT0030))                  ZA16607
*                                                               ZA16607
****************************************************************ZA16607
****                                                        ****ZA16607
****                SEARCH FOR TCAM TSB                     ****ZA16607
****                                                        ****ZA16607
****************************************************************ZA16607
*                                                               ZA16607
         L     RTCX,CVTAQAVT            GET TCX POINTER         ZA16607
         LTR   RTCX,RTCX                IS TCAM UP              ZA16607
         BZ    TGT0038                  NO, SCAN FOR VTAM       ZA11383
         USING IEDQTCX,RTCX                                     ZA16607
         L     RRPT,TCXRPT              GET TIOCRPT POINTER     ZA16607
         LTR   RRPT,RRPT                IS TSO/TCAM UP          ZA16607
         BZ    TGT0038                  NO, SCAN FOR VTAM       ZA11383
         SPACE
         USING TIOCRPT,RRPT                                     ZA16607
***                                                             ZA16607
***                INITIALIZE REGS FOR SEARCH                   ZA16607
***                                                             ZA16607
         SR    RBUF,RBUF                                        ZA16607
         IC    RBUF,TIOCTSBS            INCREMENT AMOUNT=SIZE   ZA16607
         LR    RWRK,RBUF                FOR MULTIPLICATION      ZA16607
         LH    RWRK1,TIOCNTSB           GET NO. OF TSBS         ZA16607
         BCTR  RWRK1,R0                 SUBTRACT ONE            ZA16607
         MR    RWRK,RWRK                GET OFFSET OF LAST TSB  ZA16607
         LR    RWRK3,RWRK1              CHANGE REGS FOR BXLE    ZA16607
         L     RTSB,TIOCTSB-K1          GET ORIGIN OF TABLE     ZA16607
         LA    RTSB,K0(,RTSB)           CLEAR HIGH ORDER BYTE   ZA16607
         USING TSB,RTSB                                         ZA16607
         AR    RWRK3,RTSB               ADD TO OFFSET           ZA16607
*                                                               ZA16607
****************************************************************ZA16607
*                                                               ZA16607
*                   REGISTERS 2,3 AND RTSB ARE SET UP FOR A BXLEZA16607
*                     LOOP TO SCAN THE TSB TABLE                ZA16607
*                     RBUF(2),INCREMENT, HAS THE TSB SIZE       ZA16607
*                    RWRK3(3),COMPARAND, POINTS TO THE LAST TSB ZA16607
*                       IN THE TSB TABLE, AND                   ZA16607
*                     RTSB,INDEX, POINTS TO THE FIRST TSB IN THEZA16607
*                       TSB TABLE                               ZA16607
*                                                               ZA16607
*                     NOW, THE TSB'S AND ASSOCIATED ASCB'S      ZA16607
*                     (AND CSCB'S) WILL BE SCANNED FOR A MATCH  ZA16607
*                     ON THE ASID OR THE USERID                 ZA16607
*                                                               ZA16607
*                     THE ASVT CANNOT BE USED, BECAUSE IT MUST  ZA16607
*                     BE STABILIZED WITH THE DISPATCHER LOCK,   ZA16607
*                     AND HOLDERS OF THIS LOCK MUST BE FIXED    ZA16607
*                     AND DISABLED.                             ZA16607
*                                                               ZA16607
****************************************************************ZA16607
*                                                               ZA16607
         TM    XSAOPTNS,MUSERID         WAS USERID SPECIFIED    ZA16607
         BO    TGT0020                  YES, GO LOOK FOR IT.    ZA16607
***                                                             ZA16607
***                 SCAN FOR ASID MATCH                         ZA16607
***                                                             ZA16607
         LH    RWRK4,XSAPRMTJ           GET SPECIFIED ASID      ZA16607
TGT0014  EQU   *                                                ZA16607
         TM    TSBSTAT,TSBINUSE         TSB VALID?              ZA32421
         BNO   TGT0016                  NO, CONTINUE SCAN       ZA32421
         ICM   RASCB,ICM7,TSBASCB       GET ASCB POINTER, GOOD? ZA32421
         BZ    TGT0016                  NO, CONTINUE SCAN       ZA16607
         USING ASCB,RASCB                                       ZA16607
         CH    RWRK4,ASCBASID           CHECK ASID              ZA16607
         BE    TGT0030                  IF SAME, THIS IS IT     ZA16607
TGT0016  EQU   *                                                ZA16607
         BXLE  RTSB,RBUF,TGT0014        CONTINUE SCAN           ZA16607
         B     TGT0040                  NOT IN TCAM LIST        ZA16607
*                                       GO LOOK AT VTAM         ZA16607
***                                                             ZA16607
***                SCAN FOR USERID MATCH                        ZA16607
***                                                             ZA16607
TGT0020  EQU   *                                                ZA16607
         L     RWRK1,XSAUSERP           GET POINTER TO USERID   ZA16607
TGT0022  TM    TSBSTAT,TSBINUSE         TSB VALID?              ZA32421
         BNO   TGT0024                  NO, CONTINUE SCAN       ZA32421
         ICM   RASCB,ICM7,TSBASCB       GET ASCB POINTER, GOOD? ZA32421
         BZ    TGT0024                  NO, GET  NEXT           ZA16607
         L     RWRK,ASCBCSCB            GET CSCB POINTER        ZA16607
         USING CSCB,RWRK                                        ZA16607
         CLC   CHKEY,0(RWRK1)           CHECK USERID            ZA16607
         BE    TGT0030                  GO PROCESS IT           ZA16607
         DROP  RWRK                                             ZA16607
TGT0024  EQU   *                                                ZA16607
         BXLE  RTSB,RBUF,TGT0022        LOOK AT EACH TSB        ZA16607
         B     TGT0040                  NOT IN TCAM LIST        ZA16607
*                                       GO LOOK AT VTAM         ZA16607
TGT0030  EQU   *                                                ZA16607
***                                                             ZA16607
***  RTSB=DESTINATION TSB, RASCB=DESTINATION ASCB               ZA16607
***                                                             ZA16607
         SETLOCK RELEASE,TYPE=CMS,                              ZA16607-
               RELATED=(TSB,IGC0009C(TGT0012))                  ZA16607
         SPACE 2                                                ZA16607
         L     RASCB,PSAAOLD-PSA        GET ASCB ADDRESS        ZA16607
         LH    RTJID,ASCBASID           GET ASID                ZA16607
         L     RCVT,CVTPTR              RESTORE CVT POINTER     ZA16607
         BAL   RWRK2,TGT0190            SET UP TCAM ESTAE       ZA16607
*                                                               ZA16607
****************************************************************ZA16607
***                                                         ****ZA16607
***                BRANCH TO IGG09302 HOLDING THE LOCAL LOCK****ZA16607
***                ASID TPUT TO A TCAM TERMINAL             ****ZA16607
***                                                         ****ZA16607
****************************************************************ZA16607
         L     RET,VCON9302             GET IGG09302'S EPA      ZA16607
         BR    RET                      GOTO IGG09302           ZA16607
TGT0038  EQU   *                                                ZA11383
         L     RCVT,CVTPTR              RESTORE CVT POINTER     ZA11383
         L     RWRK,CVTTCASP            GET POINTER TO TCAS TBL ZA11383
         LTR   RWRK,RWRK                IS TSO/VTAM UP          ZA11383
         BNZ   TGT0041                  NO. TERMINAL CAN'T BE   ZA11383
         LTR   RTSB,RTSB                IS THIS TSTASK          ZA11383
         BZ    TGT9200                  NO, MAKE IT NOP         ZA11383
         B     TGT6800                  GO RETURN TO CALLER     ZA11383
         DROP  RTSB                                             ZA16607
         EJECT                                                  ZA16607
*                                                               ZA16607
****************************************************************ZA16607
***                                                         ****ZA16607
***                SEARCH FOR VTAM TSB                      ****ZA16607
***                                                         ****ZA16607
****************************************************************ZA16607
*                                                               ZA16607
TGT0040  EQU   *                                                ZA16607
         L     RCVT,CVTPTR              RESTORE CVT POINTER     ZA16607
         L     RWRK,CVTTCASP            GET POINTER TO TCAS TBL ZA16607
         LTR   RWRK,RWRK                IS TSO/VTAM UP          ZA16607
         BZ    TGT6800                  NO. TERMINAL CAN'T BE   ZA16607
*                                       FOUND. RETURN TO        ZA16607
*                                       CALLER                  ZA16607
TGT0041  EQU   *
         USING TCAST,RWRK                                       ZA16607
         L     RTSB,TCASTSB             INITIALIZE RTSB TO FIRSTZA16607
*                                       TSO/VTAM TSB FOR  SEARCHZA16607
         USING TSB,RTSB                                         ZA16607
         TM    XSAOPTNS,MUSERID         WAS USERID SPECIFIED    ZA16607
         BO    TGT0050                  YES, GO LOOK FOR IT     ZA16607
         LH    RWRK4,XSAPRMTJ           GET SPECIFIED ASID      ZA16607
***                                                             ZA16607
***                SCAN FOR ASID MATCH                          ZA16607
***                                                             ZA16607
TGT0042  EQU   *                                                ZA16607
         LTR   RTSB,RTSB                ANY MORE TSB'S          ZA16607
         BZ    TGT6800                  NO MORE. TERMINAL CAN'T ZA16607
*                                       BE FOUND. RETURN TO     ZA16607
*                                       CALLER                  ZA16607
         L     RASCB,TSBASCBA           GET  ASCB POINTER       ZA16607
         CH    RWRK4,ASCBASID           SAME ASID?              ZA16607
         BE    TGT0055                  YES, THIS IS IT         ZA16607
*                                                               ZA16607
         L     RTSBX,TSBEXTNT           GET ADDRESS OF TSB      ZA16607
*                                       EXTENSION               ZA16607
         USING TSBX,RTSBX                                       ZA16607
         L     RTSB,TSBXFWD             GET TSB POINTER TO      ZA16607
*                                       NEXT IN LIST            ZA16607
         B     TGT0042                                          ZA16607
***                                                             ZA16607
***                SCAN FOR USERID MATCH                        ZA16607
***                                                             ZA16607
TGT0050  EQU   *                                                ZA16607
         L     RWRK1,XSAUSERP           GET POINTER TO USERID   ZA16607
TGT0052  EQU   *                                                ZA16607
         LTR   RTSB,RTSB                ANY MORE TSB'S          ZA16607
         BZ    TGT6800                  NO MORE.TERMINAL CAN'T  ZA16607
*                                       BE FOUND. RETURN TO     ZA16607
*                                       CALLER                  ZA16607
         L     RASCB,TSBASCBA           GET ASCB POINTER        ZA16607
         L     RWRK,ASCBCSCB            GET CSCB POINTER        ZA16607
         USING CSCB,RWRK                                        ZA16607
         CLC   CHKEY,0(RWRK1)           COMPARE USERID S        ZA16607
         BE    TGT0054                  FOUND DESIRED ONE       ZA16607
         DROP  RWRK                                             ZA16607
         L     RTSBX,TSBEXTNT           GET ADDRESS OF          ZA16607
*                                       TSB EXTENSION           ZA16607
         L     RTSB,TSBXFWD             SET TSB POINTER TO      ZA16607
*                                       NEXT IN LIST            ZA16607
         B     TGT0052                  CONTINUE  SCAN          ZA16607
*                                                               ZA16607
TGT0054  EQU   *                                                ZA16607
***                                                             ZA16607
***                WHEN A MATCHING USERID IS FOUND,             ZA16607
***                SAVE ASID IN TPUT WORK AREA                  ZA16607
***                                                             ZA16607
         MVC   XSAPRMTJ,ASCBASID        GET DESTINATION ASID    ZA16607
*                                       SAVE IT IN WORK AREA    ZA16607
***                                                             ZA16607
***    RTSB =  DESTINATION  TSB                                 ZA16607
***                                                             ZA16607
TGT0055  SETLOCK RELEASE,TYPE=CMS,                              ZA16607-
               RELATED=(TSB,IGC0009C(TGT0012))                  ZA16607
         SPACE 2                                                ZA16607
         L     RASCB,PSAAOLD-PSA        RESTORE CALLERS ASCB    ZA16607
         BAL   RWRK2,TGT0180            SET UP VTAM ESTAE       ZA16607
*                                                               ZA16607
****************************************************************ZA16607
****                                                        ****ZA16607
****               BRANCH TO IKTASTPT HOLDING THE LOCAL LOCK****ZA16607
****                                                        ****ZA16607
****************************************************************ZA16607
*                                                               ZA16607
         L     RET,VCONASTP             GET IKTASTPT'S EPA      ZA16607
         BR    RET                      GOTO IKTASTPT           ZA16607
         SPACE 2                                                ZA16607
*                                                               ZA16607
****************************************************************ZA16607
****                                                        ****ZA16607
****               NORMAL TPUT OR TGET                      ****ZA16607
****               CHECK CALLER'S TSB FOR ACCESS METHOD     ****ZA16607
****                                                        ****ZA16607
****************************************************************ZA16607
*                                                               ZA16607
TGT0100  EQU   *                                                ZA16607
         L     RTSB,ASCBTSB             GET TSB POINTER         ZA16607
         LTR   RTSB,RTSB                IS THIS A TS TASK       ZA16607
         BZ    TGT9200                  IF NOT, MAKE THIS A NOOPZA16607
*                                       USING ON TSB ALREADY    ZA16607
*                                       IN EFFECT               ZA16607
         TM    TSBFLG5,TSBVTAM          IS THIS A VTAM TSB      ZA16607
         BO    TGT0150                  YES, USE VTAM ROUTINES  ZA16607
***                                                             ZA16607
***                NORMAL TCAM REQUEST                          ZA16607
***                                                             ZA16607
         L     RTCX,CVTAQAVT            GET TCX POINTER         ZA16607
         LTR   RTCX,RTCX                IS TCAM UP              ZA16607
         BZ    TGT9200                  NO. NOOP THE REQUEST    ZA16607
         USING IEDQTCX,RTCX                                     ZA16607
         L     RRPT,TCXRPT              GET TIOCRPT POINTER     ZA16607
         LTR   RRPT,RRPT                IS TSO/TCAM UP          ZA16607
         BZ    TGT9200                  NO. NOOP THE REQUEST    ZA16607
         SPACE
         BAL   RWRK2,TGT0190            SET UP TCAM ESTAE       ZA16607
         SPACE
         LM    RP0,RP1,XSAPRM0          RESTORE PARM REGISTERS  ZA16607
         LTR   RP1,RP1                  IS THIS A TGET REQUEST  ZA16607
         BM    TGT0200                  IF TCAM TGET, REMAIN IN ZA16607
*                                       THIS MODULE             ZA16607
*                                                               ZA16607
****************************************************************ZA16607
****                                                        ****ZA16607
****               BRANCH TO IGG09301 HOLDING THE LOCAL LOCK****ZA16607
****                                                        ****ZA16607
****************************************************************ZA16607
*                                                               ZA16607
         L     RET,VCON9301             GET ADDRESS OF IGG09301 ZA16607
         BR    RET                      GO TO IGG09301          ZA16607
         SPACE 4                                                ZA16607
***                                                             ZA16607
***                NORMAL VTAM REQUEST                          ZA16607
***                                                             ZA16607
TGT0150  EQU   *                                                ZA16607
         L     RWRK,CVTTCASP            GET POINTER TO TCAS TBL ZA16607
         LTR   RWRK,RWRK                IS  TSO/VTAM UP         ZA16607
         BZ    TGT9200                  NO, NOOP THIS REQUEST   ZA16607
         SPACE
         BAL   RWRK2,TGT0180            SET UP VTAM ESTAE       ZA16607
         SPACE
         LM    RP0,RP1,XSAPRM0          RESTORE PARM REGS       ZA16607
         LTR   RP1,RP1                  IS THIS A TGET REQUEST  ZA16607
         BM    TGT0160                  BRANCH TO TGET ROUTINE  ZA16607
*                                                               ZA16607
****************************************************************ZA16607
****                                                        ****ZA16607
****               BRANCH TO IKTVTPUT HOLDING THE LOCAL LOCK****ZA16607
****                                                        ****ZA16607
****************************************************************ZA16607
*                                                               ZA16607
         L     RET,VCONVPUT             GET ADDRESS OF IKTVTPUT ZA16607
         BR    RET                      GOTO IKTVTPUT           ZA16607
*                                                               ZA16607
****************************************************************ZA16607
****                                                        ****ZA16607
****               BRANCH TO IKTVTGET HOLDING THE LOCAL LOCK****ZA16607
****                                                        ****ZA16607
****************************************************************ZA16607
*                                                               ZA16607
TGT0160  EQU   *                                                ZA16607
         L     RET,VCONVGET             GET ADDRESS OF IKTVTGET ZA16607
         BR    RET                      GOTO IKTVTGET           ZA16607
         EJECT                                                  ZA16607
*                                                               ZA16607
****************************************************************ZA16607
****                                                        ****ZA16607
****               SET UP FOR VTAM TGET/TPUT ESTAE          ****ZA16607
****                                                        ****ZA16607
****************************************************************ZA16607
*                                                               ZA16607
TGT0180  EQU   *                                                ZA16607
****                                                            ZA16607
****               RWRK2 CONTAINS RETURN ADDRESS                ZA16607
****                                                            ZA16607
         L     RWSA,ASCBASXB            GET ASXB POINTER        ZA16607
         USING ASXB,RWSA                ASXB ADDRESSABLE        ZA16607
         L     RWSA,ASXBSPSA            GET WSA VECTOR TABLE    ZA16607
         DROP  RWSA                     ASXB NOT ADDRESSABLE    ZA16607
         USING WSAL,RWSA                WSA VECTOR TBL BASE     ZA16607
         L     RWSA,WSALSTAE            GET ESTAE WORK SAVE ADD ZA16607
         DROP  RWSA                                             ZA16607
         MVC   XSAESTAL,ESTAELST        MOVE ESTAE LIST TO WA   ZA16607
         SPACE 1
         L     RESTAE,VCONESTE          GET ADDRESS OF SVC'S    ZA16607
*                                       ESTAE FOR VTAM          ZA16607
         TM    XSAFLAG,XSATCBFX         SHOULD TCBFX BE RESET   ZA16607
         BZ    TGT0184                  NO, DON'T RESET         ZA16607
         SPACE 2
         ESTAE (RESTAE),TERM=YES,BRANCH=YES,ASYNCH=YES,         ZA16607-
               SVEAREA=(RWSA),PARAM=(RSVRB),MF=(E,XSAESTAL)     ZA16607
         BR    RWRK2                    RETURN TO CALLING POINT ZA16607
         SPACE
TGT0184  EQU   *                                                ZA16607
         ESTAE (RESTAE),TERM=YES,BRANCH=YES,ASYNCH=NO,          ZA16607-
               SVEAREA=(RWSA),PARAM=(RSVRB),MF=(E,XSAESTAL)     ZA16607
         BR    RWRK2                    RETURN TO CALLING POINT ZA16607
*                                                               ZA16607
         SPACE 2
***********************************************************************
***
***                 SET UP FOR TCAM TGET/TPUT ESTAE             ZA16607
***
***********************************************************************
TGT0190  EQU   *                                                ZA16607
****                                                            ZA16607
****               RWRK2 CONTAINS RETURN ADDRESS                ZA16607
****                                                            ZA16607
         L     RWSA,ASCBASXB            GET ASXB POINTER        YS02019
         USING ASXB,RWSA                ASXB ADDRESSABLE        YS02019
         L     RWSA,ASXBSPSA            GET WSA VECTOR TABLE    YS02019
         DROP  RWSA                     ASXB NOT ADDRESSABLE    YS02019
         USING WSAL,RWSA                WSA VECTOR TBL BASE     YS02019
         L     RWSA,WSALSTAE            GET ESTAE WORKSAVE ADR  YS02019
         DROP  RWSA                                             YS02019
         MVC   XSAESTAL,ESTAELST        MOVE ESTAE LIST TO WA   YS02019
         SPACE
         TM    XSAFLAG,XSATCBFX         SHOULD TCBFX BE  RESET  YM05366
         BZ    TGT0194                  NO, DON'T RESET         ZA16607
         ESTAE TGT9500,TERM=YES,BRANCH=YES,ASYNCH=YES,          YS02019X
               SVEAREA=(RWSA),PARAM=(RSVRB),MF=(E,XSAESTAL)     YS02019
         BR    RWRK2                    RETURN TO CALLING POINT ZA16607
         SPACE 1
TGT0194  EQU   *                                                YS02019
         ESTAE TGT9500,TERM=YES,BRANCH=YES,ASYNCH=NO,           YS02019X
               SVEAREA=(RWSA),PARAM=(RSVRB),MF=(E,XSAESTAL)     YS02019
         BR    RWRK2                    RETURN TO CALLING POINT ZA16607
         SPACE 3
         EJECT
         SPACE 2
***********************************************************************
*****                                                             *****
*****               TGET ROUTINE                                  *****
*****                                                             *****
***********************************************************************
TGT0200  EQU   *
         LTR   RTSB,RTSB                IS THIS A TS TASK       YS02019
         BZ    TGT9200                  IF NOT MAKE THIS A NOP
         STH   RTJID,XSAPRMTJ           SAVE TJID                M0094
         SPACE 2                                                YS02019
***                                                             YS02019
***                 GET CMS LOCK, SO TCAM WON'T GO DOWN, AND    YS02019
***                 SO TSB WILL BE CONSTANT                     YS02019
***                                                             YS02019
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                   YS02019*
               RELATED=(TSB,IGC0009C(TGT0340))                  YS02019
         DROP  RTCX                     SETLOCK DESTROYS 11-14  YS02019
         LH    RTJID,XSAPRMTJ           GET ASID                YS02019
         SPACE 1
***
***                 CHECK EXCEPTIONAL CONDITIONS
***
TGT0210  EQU   *
         L     RTCX,CVTAQAVT            GET TCX POINTER         YS02019
         USING IEDQTCX,RTCX                                     YS02019
         TM    CVTTCMFG,CVTTCRDY        IS TCAM STILL AROUND
         BNO   TGT6800                  NO, ABORT THIS REQUEST
         L     RRPT,TCXRPT              GET RPT POINTER AGAIN   YS02019
         TM    TSBFLG4,TSBCANC          IS USER CANCELLED       YS02019
         BNZ   TGT6800                  YES, ABORT TGET         YS02019
         TM    TSBFLG4,TSBHUNG          DID USER HANG UP        YS02019
         BO    TGT0320                  YES, ENTER IWAIT        YS02019
TGT0215  EQU   *                                                YS02019
         LTR   RRPT,RRPT                SEE IF RPT IS THERE     YS02019
         BZ    TGT6800                  NOT THERE, ABORT REQ    YS02019
         TM    TCBTSFLG,TCBTIOTG        HAS ATTN BEEN HIT
         BO    TGT7500                  YES, GIVE ATTN RETURN CODE
         SPACE 3
***********************************************************************
***                                                                 ***
***                DETERMINE WHETHER DATA IS AVAILABLE              ***
***                                                                 ***
***********************************************************************
         L     RBUF,TSBITBFP-K1         TRAILER QUEUE POINTER
         LA    RBUF,0(,RBUF)            CLEAR FIRST BYTE        YS02019
         LTR   RBUF,RBUF                ANYTHING ON QUEUE
         BNZ   TGT0450                  IF DATA GO PROCESS IT
         L     RBUF,TSBIBFP-K1          INPUT QUEUE POINTER
         LA    RBUF,K0(R0,RBUF)         CLEAR FIRST BYTE
         LTR   RBUF,RBUF                IS QUEUE EMPTY
         BNZ   TGT0400                  IF NOT PROCESS
         SPACE 2
TGT0220  EQU   *
***
***                 NO DATA AVAILABLE. ASK FOR SOME
***
         LA    RWRK2,TGT0320            SET RETURN ADDRESS      YS02019
         SPACE 3                                                YS02019
TGT0245  EQU   *                                                YS02019
***********************************************************************
***                                                                 ***
***                 SUBROUTINE TO TPOST TSB TO IEDAYP               ***
***                                                                 ***
***********************************************************************
         NI    TSBSTAT,OFF-TSBLWAIT     REMOVE PSEUDO-LWAIT     YS02019
*                                       THAT MIGHT HAVE BEEN    YS02019
*                                       SET BY TCLEARQ          YS02019
         LM    RP0,RP1,TSBTPOST         GET TPOSTING FIELD      YS02019
TGT0250  EQU   *                                                YS02019
         STM   RP0,RP1,XSAWD6           STORE, TO SET BITS      YS02019
         LA    RWRK1,XSAWD6             GET ADDRESSABILITY      YS02019
         USING TSBTPOST,RWRK1           REFERENCES TO TSBTPOST  YS02019
*                                       WILL REALLY BE TO XSA,  YS02019
*                                       SINCE DISPLACEMENT IS   YS02019
*                                       SMALLER                 YS02019
         TM    TIOCFLG,TIOCSYLW         IS SYSTEM IN LWAIT      YS02019
         BO    TGT0255                  YES, SKIP               YS02019
         NI    TSBF1V,OFF-QCBNOBUF      ALLOW INPUT             YS02019
         OI    TSBF1M,QCBNOBUF          TELL AYP TO CHANGE THIS YS02019
*                                       BIT                     YS02019
TGT0255  EQU   *                                                YS02019
         OI    TSBF1V,QCBTGET           REQUEST DATA            YS02019
         OI    TSBF1M,QCBTGET           TELL AYP TO CHANGE      YS02019
         OI    TSBTPFLG,TSBTPQCB+TSBPOSTO TELL AYP WHAT TO DO   YS02019
         LM    RBUF,RCVT,TSBTPOST       GET NEW BITS IN REG     YS02019
         DROP  RCVT                                             YS02019
         DROP  RWRK1                    NOW REFERENCE TSB       YS02019
         CDS   RP0,RBUF,TSBTPOST        EXCHANGE BIT-SETTINGS   YS02019
         BNE   TGT0250                  RELOAD IF NO GOOD       YS02019
         SPACE 3                                                YS02019
***                                                             YS02019
***                 TPOST TSB TO IEDAYP, IF TSBPOSTO WAS NOT ON YS02019
***                 DURING THE COMPARE-AND-SWAP                 YS02019
***                                                             YS02019
         L     RCVT,CVTPTR              RESTORE CVT POINTER     YS02019
         USING CVT,RCVT                                         YS02019
         LTR   RP0,RP0                  WAS TSBPOSTO ON - NOTE  YS02019
*                                       THIS TEST SENSITIVE TO  YS02019
*                                       POSITION OF TSBPOSTO    YS02019
         BMR   RWRK2                    YES, RETURN TO CALLER   YS02019
         SPACE 3                                                YS02019
         L     RTCX,CVTAQAVT            GET TCX POINTER         YS02019
         USING IEDQTCX,RTCX                                     YS02019
         L     RWRK,TCXTSI              PTR TO TSINPUT'S QCB    YS02019
         USING IEDQTSI,RWRK                                     YS02019
         LA    RWRK,TSITSAP-(QCBSTVTO-IEDQQCB)                  YS02019
*                                       ADDRESS OF AYP'S 'QCB'  YS02019
         DROP  RWRK                                             YS02019
         ST    RWRK,TSBRQCB                                     YS02019
         LA    RWRK1,TSBRCB             ADDRESS OF ELEMENT      YS02019
         L     RWRK,TCXREADY            GET OLD HEAD POINTER    YS02019
TGT0260 EQU    *                                                YS02019
         ST    RWRK,TSBLINKA            STORE IN LINK FIELD     YS02019
         MVI   TSBPRI,PRIDESTQ          SET PRIORITY            YS02019
         CS    RWRK,RWRK1,TCXREADY      PUT NEW ELEM ON QUEUE   YS02019
         BNE   TGT0260                  IF CHANGED, TRY AGAIN   YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 POST TCAM'S ECB                             YS02019
***                                                             YS02019
         SR    RRPT,RRPT                COMPLETION CODE         YS02019
         LA    RTJID,CVTBRET            'ERRET' ADDRESS         YS02019
         L     RAVT,TCXASCB             LOAD ASCB OF TCAM       YS02019
         LR    R0,RTCX                  SAVE TCX POINTER        YS02019
         L     RTCX,TCXAVT              AVT POINTER             YS02019
         USING IEDQAVTD,RTCX                                    YS02019
         LA    RTCX,AVTOSECB            ECB POINTER ** NOTE     YS02019
*                                       THAT AVT IS NOT REALLY  YS02019
*                                       ADDRESSABLE **          YS02019
         DROP  RTCX                                             YS02019
         O     RTCX,HIGHON              HI-ORDER BIT=XMPOST     YS02019
         L     RET,CVT0PT01             POST ROUTINE ADDRESS    YS02019
         BALR  RETURN,RET               GO POST                 YS02019
         SPACE 2                                                YS02019
         LR    RTCX,R0                  RESTORE TCX PTR         YS02019
         USING IEDQTCX,RTCX                                     YS02019
         LH    RTJID,ASCBASID           RESTORE TJID            YS02019
         L     RASCB,TSBASCBA           RESTORE ASCB POINTER    YS02019
         L     RRPT,TCXRPT              RESTORE RPT POINTER     YS02019
         BR    RWRK2                    RETURN TO MAINLINE      YS02019
         SPACE 2
TGT0320  EQU   *
***
***                 ENTER IWAIT IF WAIT OPTION SPECIFIED
***
         TM    XSAOPTNS,MWAIT           WAS 'WAIT' SPECIFIED     M0094
         BZ    TGT0340                  YES, GO WAIT
         LA    RCODE,RC04               NOWAIT RETURN CODE
         SR    RP1,RP1                  LENGTH OF DATA
         B     TGT9000                  GO TO EXIT
TGT0340  EQU   *
***                                                             YS02019
***            INFORM SYSTEM RESOURCE MANAGER OF WAIT           YS02019
***                                                             YS02019
         LA    RP1,0(,RP1)                INDICATE IWAIT TO SRM ZA12514
         LA    RSAVE,TIOCSAVE           SAVE AREA FOR SYSEVENT  YS02019
         SYSEVENT TERMWAIT,ASIDL=ASCBASID,ENTRY=BRANCH          YM03948
         SPACE 2                                                YS02019
***                                                             YS02019
***                 SET UP RESUME ENVIRONMENT                   YS02019
***                                                             YS02019
         STM   R0,RCODE,TCBGRS          SAVE ALL REGISTERS      YS02019
         LA    RET,TGT0350              RESUME ADDRESS          YS02019
         ST    RET,RBOPSW2              SET UP RESUME PSW       YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 BRANCH TO 'STATUS' ROUTINE TO SET UP WAIT.  YS02019
***                 STATUS WON'T RETURN CONTROL UNTIL DATA HAS  YS02019
***                 BEEN MADE AVAILABLE.                        YS02019
***                                                             YS02019
***      LOCAL LOCK IS HELD AT ENTRY TO STATUS, NO LOCK IS      YS02019
***      HELD UPON RETURN FROM STATUS                           YS02019
***
         OI    TSBFLG4,TSBIWAIT         REASON FOR NONDISP      YS02019
         SETLOCK RELEASE,TYPE=CMS,RELATED=(TSB,IGC0009C(TGT0200))
         L     RWRK,CVTABEND            SECONDARY CVT POINTER   YS02019
         USING SCVTSECT,RWRK                                    YS02019
         LR    RP1,RTCB                 FOR STATUS ROUTINE      YS02019
         LA    RP0,SDTCB                ENTRY CODE FOR STATUS   YS02019
         L     RWRK1,SCVTSTAT           BRANCH ENTRY ADDRESS    YS02019
         LA    RAVT,NDISPBIT            NONDISPATCHABILITY MASK YS02019
         BALR  RETURN,RWRK1             GO TO STATUS ROUTINE    YS02019
         SPACE 3                                                YS02019
***                                                             YS02019
***                 REOBTAIN LOCKS ON RETURN FROM STATUS        YS02019
***                                                             YS02019
TGT0350  EQU   *                                                YS02019
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                 YS02019*
               RELATED=(TSB,IGG09303(TGT4000),IGC0009C(TGT9100))YS02019
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                   YS02019*
               RELATED=(TSB,IGG09303(TGT4000),IGC0009C(TGT9100))YS02019
         B     TGT0210                  RECHECK BFR AVAIL       YS02019
         SPACE 2
TGT0400  EQU   *
***********************************************************************
*****                                                             *****
*****               SET UP FOR MOVE AND EDIT OF DATA              *****
*****                                                             *****
***********************************************************************
         SPACE 2
         USING TIOCBUF,RBUF
         TM    BUFFFL2,BUFFFRAG         IS MSG COMPLETE
         BO    TGT0220                  IF NOT
         SPACE 2
TGT0450  EQU   *
***
***                                                             YS02019
***
         LA    RP0,EDITCODE             ENTRY CODE
         L     RET,TIOCQTIP             BRANCH ENTRY TO QTIP    YS02019
         STM   RCVT,RTCB,XSAUBFRP       SAVE REG.S 3-4          YS02019
         STM   RBASE,RTSB,XSAWD6        SAVE REG.S 6-9          YS02019
         OI    XSAF,XSADMOVE            SHOW DATA MOVEMENT      YS02019
         BALR  RETURN,RET               GO TO QTIP
         NI    XSAF,OFF-XSADMOVE        SHOW MOVE ALL DONE      YS02019
         LM    RCVT,RTCB,XSAUBFRP       RESTORE REGS 3-4        YS02019
         LM    RBASE,RTSB,XSAWD6        RESTORE REG.S 6-9       YS02019
         L     RTCX,CVTAQAVT            RESTORE TCX ADDR        YS02019
         L     RRPT,TCXRPT-IEDQTCX(,RTCX) RESTORE TIOCRPT ADDR  YS02019
         SPACE 2
***********************************************************************
***                                                                 ***
***                 REGISTER CONTENTS AT ENTRY TO QTIP              ***
***      REG 0 ENTRY CODE    REG 2 TS BFR PTR    REG 3 CVT PTR      ***
***      REG 4 TCB PTR       REG 5 SVRB PTR      REG 8 ASCB PTR     ***
***      REG 9 TSB PTR       REG 10 TIOCRPT PTR  REG 11 TCX PTR     ***
***                                                                 ***
***********************************************************************
         SPACE 2
***
***                 UPON RETURN FROM EDIT CHECK IF SMF IN EFFECT
***
         NC    TCBTCT,TCBTCT            IS SMF UP
         BZ    TGT1000                  IF NOT
         SPACE 2
***
***                 UPDATE LINE COUNT FOR SMF
***
         L     RWRK,TCBTCT              GET TCT POINTER
         USING SMFTCT,RWRK
         L     RWRK1,TCTLIN             GET INPUT LINE COUNT
         LA    RWRK1,K1(R0,RWRK1)       ADD ONE TO COUNT
         ST    RWRK1,TCTLIN             UPDATE COUNT
         DROP  RWRK
         SPACE 2
TGT1000  EQU   *
         SPACE 2                                                YS02019
***                                                             YS02019
***           INFORM SYSTEM RESOURCE MANAGER OF COMPLETED TGET  YS02019
***                                                             YS02019
         STM   RP0,RP1,XSAUBFRP         SAVE QTIP RET CODE & CNT M0094
         CLI   XSAUBFRP+K3,RC0C         WAS RC FROM QTIP 12 (NOT M0094
*                                       ALL DATA MOVED)
         BNE   TGT1020                  IF ALL DATA MOVED        M0607
         LA    RWRK1,PARTT              PARTIAL FLAG
         SLL   RWRK1,20                 SHIFT TO HI-ORDER
         OR    RP1,RWRK1                ADD FLAG TO COUNT
TGT1020  EQU   *
         LA    RSAVE,TIOCSAVE           SAVE AREA FOR SYSEVENT  YS02019
         SYSEVENT TGETTPUT,ASIDL=ASCBASID,ENTRY=BRANCH          YS02019
         SPACE 2
***
***                REMOVE LWAIT IF POSSIBLE
***
         TM    TSBSTAT,TSBLWAIT         IS USER IN LWAIT
         BZ    TGT2000                  IF NOT
         SR    RWRK1,RWRK1              CLEAR REGISTER
         IC    RWRK1,TSBNIBF            NO. OF BFRS ON INPUT Q
         CH    RWRK1,TIOCRSTH           IF NIBF LTE RSTH, LWAIT
         BH    TGT2000                  IF LWAIT NOT LEAVABLE
         NI    TSBSTAT,OFF-TSBLWAIT     SHOW LWAIT REMOVED       M1800
         LA    RWRK2,TGT2000            RETURN ADDRESS          YS02019
         TM    TSBFLG3,TSBNOBRK         NOBREAK TERMINAL        YS02019
         BNO   TGT0245                  TPOST TSB TO UNLOCK KB  YS02019
         SPACE 2
TGT2000  EQU   *
         LM    RP0,RP1,XSAUBFRP         RESTORE CODE AND COUNT   M0094
         LR    RCODE,RP0                RETURN CODE
         NC    TIOCNFBF,TIOCNFBF        ARE THERE ANY FREE TS BUFFERS
         BZ    TGT9000                  EXIT IF NONE
         TM    TIOCFLG,TIOCTJBF+TIOCSYLW+TIOCNOBF
*                                       IN ANY OF THESE WAITS
         BZ    TGT9000                  EXIT IF NOT (OTHERWISE,  M0094
*                                       XCTL TO IGG09303)
         ST    RP1,XSAPRM0              SAVE DATA COUNT          M0094
         ST    RCODE,XSAPRM1            SAVE RETURN CODE         M0094
         SPACE 2
***********************************************************************
*****                                                             *****
*****    BRANCH TO IGG09303 HOLDING BOTH CMS AND LOCAL LOCKS      *****
*****                                                             *****
***********************************************************************
         SPACE 2
         L     RET,VCON9303             GET IGG09303'S EPA      YS02019
         BR    RET                      GO TO IGG09303          YS02019
         SPACE 2
TGT6800  EQU   *
***
***                 USER HUNG UP RETURN
***
         LA    RCODE,RC14               LOAD RETURN CODE
         B     TGT9000                  GO TO RETURN
         SPACE 2
TGT7000  EQU   *
***
***                 ILLEGAL PARAMETER RETURN
***
         LA    RCODE,RC10               RETURN CODE
         B     TGT9000                  GO TO EXIT
         SPACE 2
TGT7500  EQU   *
***
***                 ATTN HIT RETURN
***
         LA    RCODE,RC08               RETURN CODE
         SPACE 2
TGT9000  EQU   *
***
***                 RESET TCB ATTENTION INDR PRIOR TO RETURN
***
         NI    TCBTSFLG,OFF-TCBTIOTG    TURN OFF ATTN INDR
         TM    XSAFLAG,XSATCBFX         SHOULD WE TURN OFF TCBFX M0094
         BZ    TGT9100                  IF NOT, DON'T
         NI    TCBFLGS1,OFF-TCBFX       ALLOW ASYNCHRONOUS EXITS
TGT9100  EQU   *
         SETLOCK RELEASE,TYPE=CMS,RELATED=(TSB,IGC0009C(TGT0350))
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(TSB,IGC0009C(TGT0350))
         L     RETURN,XSARETRG          GET RETURN ADDRESS      YS02019
         BR    RETURN                   RETURN                  YS02019
         SPACE 2
*****
*****               NOP TGET/TPUT FOR NON-TSO TASKS
*****
TGT9200  EQU   *
         SR    RCODE,RCODE              GIVE ZERO RETURN CODE
         SR    RP1,RP1                  NO DATA MOVED           YM07152
         B     TGT9000                  EXIT
         DROP  RBASE                    MODULE NOT ADDRESSABLE  YS02019
         SPACE 5
***********************************************************************
***                                                                 ***
***                 ESTAE EXIT ROUTINE FOR TGET/TPUT                ***
***                                                                 ***
***      UPON RECEIVING CONTROL FROM RTM, THE FOLLOWING             ***
***      CONDITIONS EXIST -                                         ***
***                                                                 ***
***      * SUPERVISORY STATE                                        ***
***      * IN SUPERVISOR'S KEY                                      ***
***      * NO LOCKS ARE HELD                                        ***
***                                                                 ***
***      REGISTER CONTENTS -                                        ***
***                                                                 ***
***      REGISTER  0 - RETURN CODE (12 MEANS SDWA NOT AVAILABLE)    ***
***      REGISTER  1 - ADDRESS OF SDWA                              ***
***      REGISTER 13 - SAVE AREA                                    ***
***      REGISTER 14 - RETURN ADDRESS                               ***
***                                                                 ***
***********************************************************************
TGT9500  EQU   *                                                YS02019
         BALR  RBASE,R0                 SET UP BASE REGISTER    YS02019
         USING *,RBASE                  MODULE ADDRESSABLE      YS02019
         C     R0,NOSDWA                IS SDWA AVAILABLE       YS02019
         BE    TGT9590                  NO, NOTHING CAN BE DONE YS02019
         SPACE 1
         SAVE  (14,12)                  SAVE REGISTERS 14-12    YS02019
         USING SDWA,RSDWA               SDWA ADDRESSABILITY     YS02019
         L     RSVRB,SDWAPARM           GET ADDR OF SVRB        YS02019
         LR    RWRK2,RP1                SAVE SWDA ADDRESS       VS08528
         SPACE 3
***
***            IF PROGRAM CHECK OCCURRED BECAUSE OF BAD BUFFER
***            ADDRESS, CHANGE THE COMPLETION CODE TO 15D
***
         TM    XSAF,XSADMOVE            DID DATA MOVE OCCUR     YM05387
         BZ    TGT9520                  NO, DON'T CHANGE        YM05387
*                                       COMPLETION CODE         YM05387
         TM    SDWAERRA,SDWAPCHK        DID PROGRAM CHECK OCCUR YM05387
         BZ    TGT9520                  NO, DON'T CHANGE        YM05387
         L     RWRK02,SDWACMPF          GET COMPLETION CODE     YM05387
         LA    RWRK02,K0(,RWRK02)       CLEAR FLAG BYTE         YM05387
         SRL   RWRK02,K12               PUT COMPLETION CODE     YM05387
*                                       IN LOW ORDER            YM05387
         C     RWRK02,CCODE0C4          CHECK FOR 0C4           YM05387
         BE    TGT9510                  YES, CHANGE CODE        YM05387
         C     RWRK02,CCODE0C5          CHECK FOR 0C5           YM05387
         BE    TGT9510                  YES, CHANGE CODE        YM05387
         C     RWRK02,CCODE0D0          CHECK FOR 0D0           YM05387
         BE    TGT9510                  YES, CHANGE CODE        YM05387
         C     RWRK02,CCODE0D1          CHECK FOR 0D1           YM05387
         BNE   TGT9520                  NO, NO CHANGE CODE      YM05387
         SPACE 1
TGT9510  EQU   *
         OI    XSAFLAG2,XSA15D          REMEMBER TO CHANGE      YM05387
*                                       COMPLETION CODE         YM05387
         SPACE 1
***                                                             YS02019
***            IF ASID TPUT DEQUEUE THE ASID                    YS02019
***                                                             YS02019
TGT9520  EQU   *                                                YS02019
         TM    XSAF,XSAIDENQ            IS ASID ENQUEUED        YS02019
         BZ    TGT9530                  NO, NO NEED TO DEQ      YS02019
         LA    RP1,XSAENQAD             ADDR OF DEQ WORK AREA   YS02019
         MVC   XSAENQNM(K12),QNAME      RESET ENQ/DEQ QNAME     YM03998
         DEQ   (XSAENQNM,XSAPRMTJ,2,SYSTEM),RET=HAVE,MF=(E,(1)) YS02019
         SPACE 3
TGT9530  EQU   *                                                YS02019
         LR    RASCB,RSAVE              SAVE RTM'S SAVE AREA    YM08216
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,    * GET LOCAL  YM08216-
               RELATED=(TIOCRPT,IGC0009C(TGT9534)) * LOCK       YM08216
         SPACE 1
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,      * GET CMS    YM08216-
               RELATED=(TIOCRPT,IGC0009C(TGT9534)) * LOCK       YM08216
         SPACE 1
         L     RTCB,CVTPTR              GET CVT ADDRESS         YS02019
         L     RRPT,CVTAQAVT-CVT(,RTCB) GET TCX ADDRESS         YS02019
         LA    RRPT,K0(,RRPT)           CLEAR HI ORDER BYTE     YM08216
         LTR   RRPT,RRPT                IS TCAM UP              YM08216
         BZ    TGT9534                  NO, TCAM IS GONE        YM08216
         L     RRPT,TCXRPT-IEDQTCX(,RRPT) GET TIOCRPT ADDRESS   YS02019
         LTR   RRPT,RRPT                IS TS STILL UP          YM08216
         BZ    TGT9534                  NO, TS IS GONE          YM08216
         L     RTCB,CVTTCBP-CVT(,RTCB)  ADDRESS OF TCB WORD     YS02019
         L     RWRK3,K4(,RTCB)          PASS ABENDING TASK'S    YM08216
*                                       TCB IN REGISTER 3       YM08216
         L     RJSCB,PSAAOLD-PSA        GET ASCB ADDRESS        YS02019
         LH    RWRK02,ASCBASID-ASCB(,RJSCB) GET CALLER'S ASID   YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***            GO TO QTIP 29 TO CLEAN UP TSB VIA BRANCH ENTRY   YS02019
***                                                             YS02019
***            INTERFACE WITH QTIP 29                           YS02019
***                                                             YS02019
***            REGISTER  0 HAS ENTRY CODE                       YS02019
***            REGISTER  2 HAS ASID OF ABENDING TASK            YS02019
***            REGISTER  3 HAS TCB ADDRESS OF ABENDING TASK     YS02019
***            REGISTER 14 HAS RETURN ADDRESS                   YS02019
***            REGISTER 15 HAS QTIP BRANCH ENTRY ADDRESS        YS02019
***                                                             YS02019
***            REGISTERS 5 THROUGH 8 ARE PRESERVED ACROSS       YS02019
***            QTIP 29 CALL                                     YS02019
***                                                             YS02019
         LA    R0,CLEANTSB              ENTRY CODE              YS02019
         L     RWRK1,TIOCQTIP           QTIP BRANCH ADDRESS     YS02019
         BALR  RETURN,RWRK1             GO TO QTIP 29           YS02019
         SPACE 3
***
***            UPON RETURN FROM QTIP 29, RESTORE REGISTERS
***
TGT9534  EQU   *                                                YS02019
         SETLOCK RELEASE,TYPE=CMS,                 * FREE CMS   YS02019-
               RELATED=(TIOCRPT,IGC0009C(TGT9532)) * LOCK       YS02019
         SPACE 1
         SETLOCK RELEASE,TYPE=LOCAL,               * FREE LOCAL YS02019-
               RELATED=(TIOCRPT,IGC0009C(TGT9532)) * LOCK       YS02019
         SPACE 1
         LR    RSAVE,RASCB              RESTORE RTMS' SAVEAREA  YS02019
         LR    RSDWA,RWRK2              RESTORE SDWA ADDRESS    YS02019
         SPACE 3                                                YS02019
***
***            CHANGE THE COMPLETION CODE TO 15D
***
         TM    XSAFLAG2,XSA15D          SHOULD COMPLETE CODE BE YM05387
*                                       CHANGED TO 15D          YM05387
         BZ    TGT9570                  NO, DON'T CHANGE        YM05387
         LA    RWRK02,CCODE15D          RESET ABEND CODE TO 15D YM05387
         SPACE 1
         SETRP RC=0,COMPCOD=((RWRK02),SYSTEM)                   YS02019-
                                        CONTINUE TERMINATION    YS02019
         B     TGT9580                  READY TO GO BACK TO RTM YM05387
         SPACE 1                                                YS02019
TGT9570  EQU   *
         SETRP RC=0                     CONTINUE TERMINATION    YM05387
         SPACE 1
TGT9580  EQU   *
         LM    RETURN,RTJID,WORD4(RSAVE)   RESTORE REGISTERS    YS02019
         SPACE 1                                                YS02019
TGT9590  EQU   *                                                YS02019
         BR    RETURN                   GO BACK TO RTM          YS02019
         SPACE 2
***
***            ESTAE LIST
***
ESTAELST ESTAE TGT9500,TERM=YES,MF=L                            YS02019
         SPACE 2
***
***                 CONSTANTS
***
VCON9301 DC    V(IGG09301)              ADDR OF IGG09301         Y01018
VCON9302 DC    V(IGG09302)              ADDR OF IGG09302        YS02019
VCON9303 DC    V(IGG09303)              ADDR OF IGG09303        YS02019
VCONASTP DC    V(IKTASTPT)              ADDR OF IKTASTPT        ZA16607
VCONVGET DC    V(IKTVTGET)              ADDR OF IKTVTGET        ZA16607
VCONVPUT DC    V(IKTVTPUT)              ADDR OF IKTVTPUT        ZA16607
VCONESTE DC    V(IKT93EST)              ADDR OF IKT93EST        ZA16607
USEREXIT DC    V(IKTTMPX1)              TERMINAL CONTROLER EXIT ZA24648
NOSDWA   DC    F'12'                    CONSTANT                YS02019
CCODE0C4 DC    X'000000C4'              COMPLETION CODE 0C4     YS02019
CCODE0C5 DC    X'000000C5'              COMPLETION CODE 0C5     YS02019
CCODE0D0 DC    X'000000D0'              COMPLETION CODE 0D0     YS02019
CCODE0D1 DC    X'000000D1'              COMPLETION CODE 0D1     YS02019
HIGHON   DC    X'80000000'              HI BIT FOR POST         YS02019
         DS    0F                                               YM03998
QNAME    DC    CL8'SYSZIGGI'            ENQ/DEQ QNAME           YM03998
         DC    X'FF000000'              DEQ/ENQ PARM LIST       YM03998
PATCHREA DC    20F'0'                   PATCH AREA              ZA24648
         EJECT                                                  YS02019
         IHAASCB                                                YS02019
         EJECT                                                  YS02019
         IHAASXB                                                YS02019
         EJECT                                                  YS02019
         IHAASVT                                                YS02019
         EJECT                                                  YS02019
         TAVTD                                                  YS02019
         EJECT                                                  YS02019
         IKJTIOCB                                               YS02019
         EJECT                                                  YS02019
CSCB     DSECT                                                  YS02019
         IEECHAIN                                               YS02019
         EJECT                                                  YS02019
CVT      DSECT                                                  YS02019
         CVT                                                    YS02019
         EJECT                                                  YS02019
         IHAPSA                                                 YS02019
         EJECT                                                  YS02019
         TPRIOR                                                 YS02019
         EJECT                                                  YS02019
         TQCBD                                                  YS02019
         EJECT                                                  YS02019
         IKJRB
         SPACE 2
RBOPSW2  EQU   RBOPSW+4                 SECOND HALF OF RBOPSW   YS02019
         SPACE 2
***
***      EXTENDED SAVE AREA.  THSES STATEMENTS SHOULD FOLLOW
***      THE MACRO 'IKJRB.'
***
         ORG   RBEXSAVE                 START OF SAVE AREA       M0094
XSAPRM0  DS    0F                       REG 0 AT ENTRY           M0094
XSAPRMTJ DS    H                        TJID PASSED              M0094
XSAPRMSZ DS    H                        SIZE PASSED              M0094
XSAPRM1  DS    0F                       REG 1 AT ENTRY           M0094
XSAOPTNS DS    X                        USER OPTIONS             M0094
MASIS    EQU   X'01'                    ASIS OPTION SPECIFIED    M0094
MCNTRL   EQU   X'02'                    1 MEANS CONTROL          M0094
*
*   BOTH OF THE ABOVE BITS OFF MEANS EDIT OPTION
*
MBREAK   EQU   X'04'                    BREAKIN SPECIFIED        M0094
MHOLD    EQU   X'08'                    1 MEANS HOLD             M0094
MTGET    EQU   X'80'                    1 MEANS TGET REQUESTED   M0094
MUSERID  EQU   X'40'                    USERID SPECIFIED        YS02019
MLOWP    EQU   X'20'                    0 HIGHPRI, 1 LOWPRI      M0094
MWAIT    EQU   X'10'                    1 MEANS NOWAIT           M0094
         DS    AL3                      REST OF REGISTER ONE     M0094
XSAWD3   DS    0F                       FOR SAVING REGISTERS     M0094
XSAFLAG  DS    X                        FLAG PASSED BY 9C TO 01  M0094
*                                       AND 02                   M0094
XSATCBFX EQU   X'20'                    TURN OFF TCBFX AT EXIT   M0094
         DS    AL3                      REST OF WORD             M0094
         ORG   XSAWD3+2                 INPUT ED, OUTPUT MOVE    M0094
XSAUBFRS DS    H                        AMT DATA LEFT TO MOVE    M0094
XSAUBFRP DS    0F                       LOC DATA LEFT TO MOVE    M0094
XSAESTAL DS    CL16                     LIST FOR ESTAE MACRO    YS02019
         ORG   XSAUBFRP+4               USED FOR SAVE AREA WHEN YS02019
*                                       NOT NEEDED BY ESTAE     YS02019
XSAWD5   DS    F                        FOR SAVING REGISTERS     M0094
XSAWD6   DS    F                        FOR SAVING REGISTERS     M0094
         ORG   XSAWD5                   USE WORDS 5-6 FOR NAME   M0094
XSAENQNM DS    CL8                      ENQ NAME FOR TJID TPUT   M0094
XSAWD7   DS    F                        FOR SAVING REGISTERS     M0094
XSAWD8   DS    F                        FOR SAVING REGISTERS     M0094
         ORG   XSAWD7                   USE WDS 7-8 FOR ENQ PRMS M0094
XSAENQAD DS    2F                       WORK AREA FOR ENQ/DEQ    M0094
XSAUSERP DS    0F                       PTR TO USER ID, IF PRES YS02019
XSAWD9   DS    F                        FOR SAVING REGISTERS     M0094
XSARETRG DS    0F                       FOR SAVING RETURN ADDR  YS02019
XSAWD10  DS    F                        FOR SAVING REGISTERS     M0094
         DS    CL1                      RESERVED                YS02019
XSAUSERK DS    CL1                      KEY OF TGET CALLER      YS02019
XSARC    DS    H                        RETURN CODE SAVE AREA   YS02019
XSATJID  DS    H                        CALLER'S TJID            M0094
XSAFLAG2 DS    X                        FLAGS                   YM05387
XSAATR   EQU   X'80'                    NEXT CHAR IN TS BUFFER   S22028
*                                       IS A 3270 ATTRIBUTE BYTE
XSABFRAL EQU   X'40'                    TIOC BUFFER HAS BEEN    YM05387
*                                       ALLOCATED               YM05387
XSA15D   EQU   X'20'                    CHANGE COMPLETTION TO   YM05387
*                                       15D                     YM05387
*                  NEXT 5 BITS UNUSED                           YM05387
XSAF     DS    X                        FLAGS USED BY TPUT. INI- M0094
*                                       TIALIZED FROM XSAFLAG.
HSW      EQU   X'80'                    HEADER BFR DONE          M0094
PSW      EQU   X'40'                    NEED TO TPOST QCB        M0094
*SATCBFX EQU   X'20'                    TURN OFF TCBFX AT EXIT
OSW      EQU   X'10'                    TSBOWIP TURNED ON BY US  M0094
*                                       (NOT SOME OTHER TPUT)
XSADMOVE EQU   X'08'                    DATA IS MOVING          YS02019
XSAAUTH  EQU   X'04'                    USER IS AUTHORIZED      YS02019
XSAIDENQ EQU   X'02'                    ASID TPUT ASID ENQUEUED YS02019
XSAPARTO EQU   X'01'                    QCBPARTO IS SET ON      YM01246
         EJECT                                                  YS02019
         IHASCVT                                                YS02019
         EJECT                                                  YS02019
         IKJTCB                                                 YS02019
         EJECT                                                  YS02019
         IEFTCT                                                 YS02019
         EJECT                                                  YS02019
         TTCXD                                                  YS02019
         IKJTIOCP                                               YS02019
         EJECT                                                  YS02019
         IKJTSB EXT=YES,LIST=YES                                YS02019
         EJECT                                                  YS02019
         IKTTCAST                                               ZA16607
         EJECT                                                  ZA16607
         TTSID                                                  YS02019
         EJECT                                                  YS02019
         IHAWSAVT                                               YS02019
         EJECT                                                  YS02019
         IHASDWA                                                YS02019
         END
