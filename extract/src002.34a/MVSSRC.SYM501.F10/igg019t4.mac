19T4     TITLE 'IGG019T4 - QSAM TSPUT'                          SA66590
IGG019T4 CSECT
***********************************************************************
*                                                                     *
*        VS2  030   SU
*
*0000511100-511200                                             @ZA06431
*
*
* RELEASE 21.8 CHANGES                                          SA66590
*C003000,122600                                                 SA66590
*A123100,537500-541500,492500,729700                            SA66590
*D33600-342000                                                  SA69128
*A333500                                                        SA69128
* RELEASE 21.0 CHANGES                                                *
*                                                                 M2432
*                                                                M3504
*                                                                 M5508
*                                                                M7471
*A433500,510500-512000                                          ZA02232
*                                                                     *
* STATUS CHANGE LEVEL - VS2  030 SU                            @ZA06431
*                                                                     *
* NAME - IGG019T4.                                                    *
*                                                                     *
* FUNCTION/OPERATION - THIS INTERFACE ROUTINE PERFORMS THE FOLLOWING  *
*                      FUNCTIONS                                      *
*                                                                     *
*                      FURNISH TPUT USER BUFFER ADDRESS (MOVE MODE),  *
*                      OR ADDRESS OF SYSTEM BUFFER IN BFFER POOL      *
*                      (LOCATE MODE).                                 *
*                                                                     *
*                      FURNISH TPUT BUFFER SIZE.                      *
*                                                                     *
*                      ISSUE TPUT WHICH CAUSES A RECORD TO BE WRITTEN *
*                      TO A TERMINAL.                                 *
*                                                                     *
*                      UPON REGAIN CONTROL FROM TPUT, RETURN TO USER  *
*                      THE ADDRESS OF RECORD WRITTEN IN REGISTER 1.   *
*                                                                     *
*                      FOR THE FIRST PUT ISSUED IN LOCATE MODE, ONLY  *
*                      ADDRESS OF BUFFER IS RETURNED IN REGISTER 1.   *
*                      (THE RECORD IS ACTUALLY WRITTEN OUT ON THE     *
*                      SECOND AND FOLLOWING PUTS.)                    *
*                                                                     *
* ENTRY POINTS - PUT - IGG019T4                                       *
*                PUTX - IGG019T4 + 4                                  *
*                TRUNC/RELSE - IGG019T4 + 8                           *
*                                                                     *
*                CONTROL IS RECEIVED WHEN THE FOLLOWING CONDITIONS    *
*                ARE MET.                                             *
*                                                                     *
*                THE TASK IS TIME SHARING.                            *
*                THE DCB OPENED IS FOR QSAM OUTPUT.                   *
*                A PUT, PUTX, TRUNC, OR RELSE MACRO INSTRUCTION IS    *
*                ISSED.                                               *
*                                                                     *
* INPUT -                                                             *
*                                                                     *
*         FOR PUT -                                                   *
*                                                                     *
*                   REGISTER 0 HAS USER BUFFER ADDRESS                *
*                   REGISTER 1 HAS DCB ADDRESS                        *
*                                                                     *
*         FOR PUTX -                                                  *
*                                                                     *
*                   REGISTER 0 HAS IN DCB ADDRESS                     *
*                   REGISTER 1 HAS OUT DCB ADDRESS                    *
*                                                                     *
* OUTPUT - A RECORD WRITTEN TO A TERMINAL.                            *
*          THE ADDRESS OF THE RECORD IN REGISTER 1.                   *
*                                                                     *
* EXTERNAL REFERENCE - IGC0009C.                                      *
*                                                                     *
* EXIT, NORMAL - RETURN CONTROL TO CALLER VIA A 'BR 14' INSTRUCTION.  *
*                                                                     *
* EXIT, ERROR - ABEND 002 IF RECFM V LT 4 BYTES, OR             SA66590
*        RECFM VA OR VM (CONTROL CHAR) LT 5 BYTES.              SA66590
*                                                                     *
* TABLE/WORK AREAS - NONE.                                            *
*                                                                     *
* ATTRIBUTES - REENTRANT, REUSABLE.                                   *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
********
R0       EQU   0                        CONTAINS BFR ADDR WHEN THIS
*                                       MODULE GETS CONTROL, ALSO
*                                       CONTAINS BFR SIZE FOR TPUT
R1       EQU   1                        CONTAINS DCB ADDR WHEN THIS
*                                       MODULE GETS CONTROL, ALSO
*                                       CONTAINS BFR ADDR FOR TPUT
R2       EQU   2                        REGISTER 2
RDCB     EQU   2                        POINTS AT DCB
RBUFAD   EQU   3                        POINTS AT BUFFER
RBUFSZ   EQU   4                        CONTAINS BFR SIZE
RPUTX    EQU   5                        INDICATE ENTRY MADE BY PUTX
*                                       IF CONTENT IS 1
RWORK    EQU   6                        WORK REGISTER
REOBAD   EQU   7                        POINTS AT END OF BLOCK ADDR
*                                       FOR LOCATE MODE
RRECAD   EQU   8                        POINTS AT RECORD ADDR FOR
*                                       LOCATE MODE
RDEB     EQU   9                        CONTAIN DEB ADDRESS
RBASE    EQU   12                       BASE REGISTER
R12      EQU   12                       REGISTER 12
R14      EQU   14                       CONTAINS RETURN ADDR
R13      EQU   13                       REGISTER 13
R15      EQU   15                       CONTAINS ADDR TO BRANCH TO
RRETURN  EQU   15                       CONTAINS RETURN CODE
         SPACE 3
********
******** EQUATES FOR DISPLACEMENTS, LENGTH AND MASKS
********
QSAMLOCT EQU   X'48'                    PUT/LOCATE MODE IN DCBMACRF
D0       EQU   0                        DISPLACEMENT
D1       EQU   1                        DISPLACEMENT
D2       EQU   2                        DISPLACEMENT
D4       EQU   4                        DISPLACEMENT
D12      EQU   12                       DISPLACEMENT
D20      EQU   20                       DISPLACEMENT OF 20
D28      EQU   28                       DISPLACEMENT
M80      EQU   X'80'                    DENOTES 1ST PUT IN BFR POOL
*                                       CONTROL BLOCK, AND WRITE BIT
*                                       IN DCBOFLGS, F-TYPE RECORD IN
*                                       DCBRECFM
M7F      EQU   X'7F'                    MASK TO TURN OFF 1ST PUT
M06      EQU   X'06'                    ASA OR MACHINE CHAR USED
MC0      EQU   X'C0'                    U-TYPE RECORD IN DCBRECFM
M40      EQU   X'40'                    V-TYPE RECORD IN DCBRECFM
M8       EQU   8                        MASK USED TO SHIFT REGISTER
         SPACE 3
         SPACE 3
********
******** PUT ENTRY
********
         USING IGG019T4,R15             USE ENTRY ADDR AS BASE ADDR
         B     STARTPUT                 BR PAST PUTX & TRUNC/RELSE
*                                       ENTRY
********
******** PUTX ENTRY
********
         B     STRTPUTX                 BR PAST TRUNC/RELSE ENTRY
         SPACE 3
********
******** TRUNC/RELSE ENTRY
********
         BR    R14                      NOP FOR TRUNC & RELSE ROUTINES
*                                       (RETURN TO USER)
         SPACE 3
********
******** SAVE REGISTERS, GET BUFFER ADDRESS FOR PUTX
********
STRTPUTX EQU   *
         STM   R14,R12,D12(R13)         SAVE REGISTERS
         LA    RPUTX,D1                 SIGNAL ENTRY MADE BY PUTX
         USING IHADCB,RDCB              ESTABLISH ADDRESSABILITY
         LR    RDCB,R0                  SAVE INPUT DCB ADDR
         L     R0,DCBRECAD              GET REC @ FROM INPUT DCBSA69128
         B     STUPBASE                 UNCONDITIONAL BRANCH
         SPACE 3
********
******** SAVE REGISTERS AND ESTABLISH ADDRESSABILITY
********
STARTPUT EQU   *
         STM   R14,R12,D12(R13)         SAVE REGISTERS
         SR    RPUTX,RPUTX              SIGNAL ENTRY MADE BY PUT
STUPBASE EQU   *
         LR    RBASE,R15                GET BASE ADDR
         DROP  R15                      GET RID OF PREVIOUS BASE REG
         USING IGG019T4,RBASE           ESTABLISH ADDRESSABILITY
         B     BRARID                   BRANCH AROUND ID       @ZA06431
         DC    C'IGG019T4'              MODULE ID              @ZA06431
         DC    X'5317'                  DATE 11/13/75          @ZA06431
BRARID   EQU   *                                               @ZA06431
         SPACE 3
********
******** GET BUFFER ADDRESS FOR PUT
********
         LR    RDCB,R1                  GET DCB ADDRSS
         TM    DCBMACRF+1,QSAMLOCT      TEST FOR PUT/LOCATE MODE
         BNO   MOVEMD                   NO, GO TO MOVEMD
LOCATEMD EQU   *
         LTR   RPUTX,RPUTX              IS ENTRY MADE BY PUTX
         BNZ   MOVEMD                   BRANCH IF YES
         L     RBUFAD,DCBBUFNO          GET BFR POOL CONTROL BLOCK ADDR
         L     RBUFAD,D0(RBUFAD)        GET BFR ADDR
********
******** TEST FOR FIRST PUT
********
         L     RWORK,DCBBUFNO           GET ADDR OF BFR POOL CONTROL
*                                       BLOCK
         USING BUFPOOL,RWORK
         TM    PUTFLAG,M80              TEST FOR FIRST PUT
         BNO   NOTFIRST                 BRANCH IF NO
         NI    PUTFLAG,M7F              TURN OFF FLAG FOR FIRST PUT
         LA    RBUFAD,D0(,RBUFAD)       ZERO FLAG BYTE
         MVC   DCBPRECL(2),DCBLRECL     SAVE LENGTH OF NEXT REC ZA02232
         B     RETBUFAD                 GO TO RETBUFAD
NOTFIRST EQU   *
         TM    DCBRECFM,M80             CHECK FOR U OR F RECORD
         BNO   VRECORD                  BRANCH IF V-TYPE
* BYPASS TPUT IF RECAD OR EOBAD HAVE BEEN MODIFIED BY USER.
* TSPUT DOES NOT HANDLE BLOCKED RECORDS, SO END OF BLOCK CONDITION
* IS SET UP AT EACH EXIT FROM THIS MODULE.
         LM    REOBAD,RRECAD,DCBEOBAD   GET RECAD AND EOBAD
         CR    REOBAD,RRECAD            ARE THEY THE SAME
         BNE   RETBUFAD                 BRANCH IF NO
         B     FIXED                    GO TO GET MSG LENGTH
         SPACE 3
MOVEMD   EQU   *
         LR    RBUFAD,R0                GET USER BFR ADDRESS
         SPACE 3
********
******** TEST FOR U-TYPE, V-TYPE OR F-TYPE RECORD AND GET BUFFER SIZE
********
         TM    DCBRECFM,M80             CHECK FOR U-TYPE OR F-TYPE
*                                       RECORD
         BO    FIXED                    YES, GO TO FIXED
VRECORD  EQU   *
         SR    RBUFSZ,RBUFSZ            CLEAR REG
         IC    RBUFSZ,D0(RBUFAD)        GET ONE L FROM CONTROL WORD
         SLL   RBUFSZ,M8                MAKE ROOM FOR OTHER L
         IC    RBUFSZ,D1(RBUFAD)        GET SECOND L FROM CONTROL WORD
         SH    RBUFSZ,FOUR              SUBTRACT 4 FOR CONTROL WORD
         BM    ABEND                    NEG RECORD LENGTH, ABND SA66590
         LA    RBUFAD,D4(RBUFAD)        UPDATE BFR PTR TO PASS CONTROL
*                                       WORD
         B     CKCHAR                   GO TO READY
FIXED    EQU   *
         LH    RBUFSZ,DCBLRECL          GET RECORD SIZE
         TM    DCBRECFM,MC0             FORMAT U RECORD         ZA02232
         BM    CKCHAR                   NO, BRANCH              ZA02232
         TM    DCBMACF2,QSAMLOCT        PUT,LOCATE MODE?       @ZA06431
         BNO   CKCHAR                   NO, BRANCH             @ZA06431
         LH    RBUFSZ,DCBPRECL          GET LENGTH CURRENT REC  ZA02232
         MVC   DCBPRECL(2),DCBLRECL     SAVE LENGTH NEXT RECORD ZA02232
         SPACE 3
********
******** CHECK FOR ASA OR MACHINE CHARACTER
********
CKCHAR   EQU   *
         TM    DCBRECFM,M06             TEST FOR ASA OR MACHINE CHAR
         BZ    READY                    NO, GO TO READY
         LA    RBUFAD,D1(RBUFAD)        UPDAT BFR PTR BY 1
         BCTR  RBUFSZ,R0                DECREMENT BFR SIZE BY 1
         LTR   RBUFSZ,RBUFSZ            CHECK FOR NEG SIZE      SA66590
         BNM   READY                    O.K., GO PUT IT OUT     SA66590
         SPACE 3
********
******** ABEND THE USER - HIS RECORD WAS TOO SHORT              SA66590
********
ABEND    EQU   *                                                SA66590
         L     R1,ABCODE                LOAD ABEND CODE         SA66590
         ABEND (1)                      ISSUE ABEND SVC         SA66590
         SPACE 3
********
******** PREPARE PARAMETERS AND ISSUE TPUT
********
READY    EQU   *
         LR    R0,RBUFSZ                LOAD BFR SIZE
         LR    R1,RBUFAD                LOAD BFR ADDRESS
         TPUT  (1),(0)                  ISSUE TPUT
         SPACE 3
********
******** UPON RETURN FROM TPUT
********
SUCCESS  EQU   *
         OI    DCBOFLGS,M80             TURN ON WRITE BIT
         SPACE 3
*********
*********TEST FOR V-TYPE RECORD AND ASA OR MACHINE CHARACTER, AND
*********RESTORE BUFFER POINTER ACCORDINGLY
*********
CHECK    EQU   *
         TM    DCBRECFM,M06             IS ASA OR MACHINE CODE USED
         BZ    CKVREC                   BRANCH IF NO
         BCTR  RBUFAD,R0                SET BACK BFR ADDR BY 1
CKVREC   EQU   *
         TM    DCBRECFM,M80             CHECK FOR U-TYPE OR F-TYPE
*                                       RECORD
         BO    RETBUFAD                 BRANCH IF YES
         SH    RBUFAD,FOUR              SET BACK BFR ADDR BY 4
         SPACE 3
********
******** PUT BUFFER ADDRESS IN REGISTER 1, RESTORE REGISTERS AND
******** RETURN TO CALLER
********
RETBUFAD EQU   *
         LR    R1,RBUFAD                PUT BFR ADDR IN REG 1
         TM    DCBMACRF+1,QSAMLOCT      CHECK FOR LOCATE MODE
         BNO   EXIT                     BRANCH IF MOVE MODE
         L     RDEB,DCBDEBAD            GET DEB ADDRESS
         USING DEB,RDEB
         AH    RBUFAD,DEBLRECL          POINT AT END OF BFR
         ST    RBUFAD,DCBEOBAD          UPDATE DCBEOBAD
         ST    RBUFAD,DCBRECAD          UPDATE DCBRECAD
EXIT     EQU   *
         L     R14,D12(R13)             RESTORE REG 14
         L     R0,D20(R13)              RESTORE REG 0
         LM    R2,R12,D28(R13)          RESTORE REGS 2-12
         BR    R14                      RETURN CONTROL TO CALLER
         SPACE 3
********
******** CONSTANTS
********
FOUR     DC    H'4'                     CONSTANTS OF 4
         SPACE 3
         DS    0F
ABCODE   DC    X'80002000'              DUMP/STEP & COMPL. CODE SA66590
PATCH    DC    10F'0'                   PATCH AREA
DEB      DSECT
         DS    CL32                     DEB BASIC SECTION
         DS    CL4                      DEB EXTENT
         DS    CL12                     DEB BSAM/QSAM DEPENDENT SECTION
DEBBLKSI DS    CL2                      BLOCK SIZE SAVE AREA
DEBLRECL DS    CL2                      LRECL SAVE AREA
         SPACE 3
         DCBD  DSORG=PS
         DS    0F
BUFPOOL  DSECT
PUTFLAG  DS    CL1                      FLAG FOR 1ST PUT SET BY OPEN
         END
