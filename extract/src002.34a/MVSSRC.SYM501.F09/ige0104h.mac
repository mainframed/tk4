104H   TITLE 'IGE0104H BSC UNIT CHECK - UNIT EXCEPTION -READ-WRITE'
IGE0104H CSECT
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*
*A344000,482000                                                  S22025
*C462000,558000,742000,748000,750000                             S22025
*D824000                                                         S22025
*C262000                                                       @SA72489
*C262400                                                       @SA73351
*C612000,616000                                                @SA72446
*A820000                                                       @SA72446
*D702000-704000,A210000,C438000-444000,A821000,A840000         @OY10984
*C414000,424000,A730000,736000,746000                          @OY12439
*C443800,D438600-443600,821060-821120,840500                   @OY14057
*C018000,356000,494000                                         @G36XREV
*                                                                     *
***********************************************************************
*                                                                     *
*MODULE-NAME = IGE0104H
*                                                                     *
*DESCRIPTIVE-NAME = ERP FOR BSC UNIT CHECK/UNIT EXCEPTION             *
*                                                                     *
*COPYRIGHTS = 'NONE'                                                  *
*                                                                     *
*  STATUS:  CHANGE LEVEL 10                                    @G36XREV
*                                                                     *
*FUNCTION -- TO ATTEMPT RETRY ON A FAILING CCW SEQUENCE AND TO TAKE   *
*   INTERMEDIATE ERP ACTION IF REQUIRED                               *
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
*EXTERNAL ROUTINE -- IGG019QE (AOS/TCAM SIO APPENDAGE) -- TO CONVERT  *
*   VIRTUAL CCW ADDRESSES TO REAL BEFORE RETRY.                       *
*                                                                     *
*A052000-053000,059000,078000,110000,115000,151000,170600-171200,Y02027
*A183000,189500,651000,752600-753200,841000                      Y02027
*C036000,042000,050000-051000,058000,076000-077000,114000,158000,Y02027
*C690000,690300,690420-690480,691440                             Y02027
*D212000                                                         Y02027
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'ERP IN CONTROL (IOSERR)' AND 'EXCEPTIONAL   Y02027
*                CONDITION(IOSEX)'. RETURN TO IOS TO RETRY THE   Y02027
*                   ERROR.                                       Y02027
*        SVC   3                                                      *
*                                                                     *
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*        SVC   3                                                      *
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO I/O SUPERVISOR BLOCK                Y02027
*        SVC   15   'EXCEPTIONAL CONDITION(IOSEX)'. RETURN TO    Y02027
*                   LINE END APPENDAGE. ERROR PERMANENT.         Y02027
*        SVC   3                                                      *
*                                                                     *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*   TO SCHEDULE ERROR POST                                            *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TTRMD                                                             *
*   TCCWD                                                             *
*   TLCBD                                                             *
*   DCBD                                                              *
*   TSCBD                                                             *
*   IECDIOSB                                                     Y02027
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
*   IF INTERMEDIATE ERP ACTION IS REQUIRED, THE SEQUENCE IS BUILT     *
*   IN LCBERCCW AND A RETURN INDICATOR IS SET IN LCBINCAM+1 FOR       *
*   IGE0404H.                                                         *
*                                                                     *
***********************************************************************
         EJECT
         USING LCBFLAG1,RLCB
         USING IEDQCCW,RCCW
         USING IEDQSCB,RSCB
         USING IHADCB,RDCB
         USING IEDQAVTD,RAVT
         USING IOSB,RIOSB                                        Y02027
         USING *,R15
*        R E G I S T E R S
R0       EQU   0
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RLCB     EQU   2                        LCB CBASE
RCCW     EQU   5                        CCW BASE
RSCB     EQU   3                        SCB BASE
RDCB     EQU   4                        DCB BASE
RWKA     EQU   6                        WORK REGISTER
RWKB     EQU   7                        WORK REGISTER
RTST     EQU   7                        ADDR OF REQUEST QUEUE    Y02027
*                                       ELEMENT                  Y02027
RTERM    EQU   8                        TERMINAL BASE
RWKC     EQU   9                        WORK REGISTER
RWKD     EQU   10                       WORK REGISTER
RAVT     EQU   11                       AAVT BASE
RCUR     EQU   12                       WORK REGISTER
RLINK    EQU   13                       LINKAGE FOR SUBSEQUENT LOAD
RSAVE    EQU   13                       SAVE REGISTER FOR IOSB   Y02027
RXCTL    EQU   14                       XCTL REGISTER
R15      EQU   15                       ADDRESSABILITY REGISTER
         EJECT
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         L     RLCB,IOBOFFST(,RTST)     ADDRESS OF IOB Z(LCB)
         L     RSCB,LCBSCBA-1           SCB B ASE
         L     RDCB,LCBDCBPT            DCB  BASE
         L     RCCW,LCBCSW-1            FAILING CCW
         SH    RCCW,H8                  BACK UP TO FAILING CCW
         LA    RCCW,0(,RCCW)            CLEAR HI ORDER BYTE
         CLI   CCWOPCDE,CCWTIC          TIC (CHANNEL PROGAM CHECK)
         BNE   NOPC                     NO
         SH    RCCW,H8                  BACK U TO TEXT CCW
         MVI   LCBCSW+6,0               CLEAR RESIDUAL COUNT
NOPC     EQU   *
         L     RXCTL,CVTPTR             CVT ADDRESS            @OY10984
         L     RAVT,AVTCVTPT(,RXCTL)    LOAD LIST ADDRESS      @OY10984
         L     RAVT,AVTEZERO(,RAVT)     AVT BASE               @OY10984
         LR    RCUR,RCCW                ASSUME RESTART POINT
         TM    LCBCSW+3,UNITCHK         UNIT CHEKC SET
         BZ    UXWRITE                  NO UNIT EXCEPTION ON WRITE
         SPACE
         TM    LCBSENS0,EQCHK           EQUIPMENT CHECK
         BZ    TRYLD                    NO, TRY LOST DATA
         SPACE
         CLI   LCBRESTR,TPRDSKIP        TEXT RELATED CCW
         BNL   TXTSNO                   BRANCH IF YES
         SPACE
         CLI   LCBRESTR,TPRDLC          READ LLC OUT
         BNE   TRYRETRY                 BRANCH NO
TXTSNO   EQU   *
         OI    SCBERR4,SCBUNDFN         SHOULD NOT OCCUR
         B     TXTRESET                 WRITE EOT TO RESET LINE
         SPACE 2
TRYLD    EQU   *
         TM    LCBSENS0,LOSTD           LOST DATA SET
         BZ    TOTEST                   NO, TRY TIME OUT
         SPACE
         CLI   CCWOPCDE,CCWWRITE        WRITE COMMAND
         BE    SNOEXIT                  YES 'SHOULD NOT OCCUR'
         SPACE
         CLI   LCBRESTR,TPRDENQ         READ ENQ COMMAND
         BNE   TRYENQ                   NO, CHECK ENQ RESPONSE @SA72489
         CLC   CCWCOUNT,LCBCSW+5        RESIDUAL COUNT=ORIGINAL@SA72489
         BE    TRYRETRY                 YES, CHECK RETRY COUNT @SA73351
         NI    LCBCSW+3,AVTEFF-UNITCHK  RESET UNIT CHECK ERROR @SA72489
         NI    IOSFLA,AVTEFF-(IOSERR+IOSEX) RESET ERP IN CONT  @SA72489
         B     NOTRETRY                 RETURN TO LINE END     @SA72489
*                                       WITH GOOD COMPLETION   @SA72489
TRYENQ   EQU   *                                               @SA72489
         SPACE 2
         CLI   LCBRESTR,TPRSPENQ        RESPOSNE TO ENQ
         BNE   LDRSPEB                  NO
         SPACE
DIALTEST EQU   *
         LA    RCUR,LCBCPA+16           ASSUME NOT DIAL
         TM    LCBCPA+24,X'20'          DIAL SEQUENCE
         BZ    TRYRETRY                 BRANCH N/
         SPACE
         LA    RCUR,24(,RCUR)           BUMP PAST DIAL SEQUENCE
         B     TRYRETRY                 RETRY AFTER DIAL SEQUENC
         SPACE
LDRSPEB  EQU   *
         CLI   LCBRESTR,TPRDRPEB        RESPONSE TO ETB
         BNE   LDTEXT                   NO, CHECK IF TEXT
         SPACE
WRENQ    EQU   *
         LA    RWKC,ENQCH               PARAMETER
         BAL   RWKD,CCWBLD              BUILD WRITE ENQ
         MVI   LCBINCAM+1,LDRSPRET      SET 404H RETURN
         MVI   LCBERCCW+4,CCWCC+CCWSLI  SET FLAGS
         ST    RCCW,LCBERCCW+8          SET TIC TO FAILING CCW
         MVI   LCBERCCW+8,CCWTIC        SET TIC COMMAND CODE
         B     COMEXIT                  USE COMMON CIIT
         SPACE 2
LDTEXT   EQU   *
         CLI   LCBRESTR,TPRDLC          READ LC OUT
         BE    TRYRETRY                 YES
         SPACE
         CLI   LCBRESTR,TPTEXT          TEXT CCW
         BNE   NOTEXT                   NO
         SPACE
         MVI   LCBINCAM+1,LDTXTRET      SET 404H RERURN
         MVC   LCBERCCW+4(4),SKIPCNT    SET LARGE COHNT
         B     READEXIT                 COMMON EXIT
         SPACE
NOTEXT   EQU   *
         CLI   LCBRESTR,TPRDRSPD        ERSPONSE TO ADDRESSING
         BNE   TRYIDAK                  BRANCH NO
         SPACE
         LH    RWKC,LCBTTCIN            CURRENTRLY CONNECTED
         LTR   RWKC,RWKC                IS THE SOURCE ON A DIAL  S22025
*                                         LINE SET               S22025
         BZ    TXTRESET                 NO, RESET THE LINE AND   S22025
*                                         TREAT AS A PERM ERROR  S22025
         BAL   RWKD,FINDTERM            GET TERMINAL ADDRESS
         USING IEDQTRM,RTERM
         SR    RWKB,RWKB                CLEAR INDEX EGISTER
         IC    RWKB,TRMCHCIN            INDEX TO CHARACTDRISTICS
         BCTR  RWKB,0                   DECREMENTFRO INDEX
         MH    RWKB,AVTDCTLN            LENGTH OF DCT ENTRY    @G36XREV
         A     RWKB,AVTCSTCS            POINT TO CHARACTERISTIC
         TM    2(RWKB),X'10'            STATION CONTROL-MULTIPT
         BO    DIALTEST                 YES, RE-SELECT
         SPACE
         TM    3(RWKB),X'20'            END-END CONTROL
         BZ    DIALTEST                 NO, RE-SELECT
         SPACE
         B     WRENQ                    TREAT SAME AS TRESPONSE TO
*                                         TO TEXT
TRYIDAK  EQU   *
         CLI   LCBRESTR,TPRDIDAK        READ ID ACK
         BE    DIALTEST                 BRANCH YES
         SPACE
         B     TRYRETRY                 RETRY ID ENQ
         SPACE 5
TOTEST   EQU   *
         TM    LCBSENS0,TIMEOUT         TIMEOUT SET
         BZ    INTRTEST                 NO
         CLI   CCWOPCDE,CCWREAD         READ CCW
         BNE   TOWRITE                  NO, IT'S A WRITE
         SPACE
         CLI   LCBRESTR,TPRDLC          READ LCOUT
         BE    TXTRTRY                  RETRY COMMAND
         SPACE
         CLI   LCBRESTR,TPTEXT          READ TEXT
         BNE   NOTEXT1                  NO
         SPACE
         CLC   CCWCOUNT,LCBCSW+5   RESIDUAL COUT=ORIGINAL
         BNE   LOAD204H                 NO, 204H WILL HANDLE   @OY12439
         SPACE
         L     RWKD,LCBCPA+12      RESTART CCW
         LA    RWKD,0(,RWKD)       CLEAR HI ORDER BYTE
         CLR   RWKD,RCCW           NO DATA TRANSFERRED
         BNE   LOAD204H                 YES, 204H WILL HANDLE  @OY12439
         SPACE
TXTRTRY  EQU   *
         CLI   LCBINCAM,RETRY           RERRY COUNT
         BL    TRYRETRY                 NO RETRY
         SPACE
TXTRESET EQU   *
         MVI   LCBINCAM+ONE,TXTRST      404H RETURN            @OY10984
         SR    RWKC,RWKC                SET EOT OFFSET FOR SCT @OY10984
         STH   RWKC,LCBERCCW+4          CLEAR CHAIN FLAGS      @OY10984
         BAL   RWKD,CCWBLD              BUILD WRITE EOT        @OY14057
         B     COMEXIT                  COMMON CODE
         SPACE
NOTEXT1  EQU   *
TRYRSPTX EQU   *
         CLI   LCBRESTR,TPRDRPEB        RESPONSE TO ETB
         BNE   TRYADDR                  TRY RESP TO ADDRESSING
TOTXT    EQU   *
         LA    RWKC,ENQCH               INDEX PARAMETER
         BAL   RWKD,CCWBLD              BUILB CCW IN LCBERCCW    S22025
         MVI   LCBINCAM+1,TORSPRET SET RETURN FOR 404H
         MVI   LCBERCCW+4,CCWCC+CCWSLI  SET FLAGS
         ST    RCCW,LCBERCCW+8          SET FOR TIC ADDRESS
         MVI   LCBERCCW+8,CCWTIC        SET TIC COMMAND CODE
         B     COMEXIT             USE COMMON EXIT
         SPACE
TRYADDR  EQU   *
         CLI   LCBRESTR,TPRDRSPD        RESPONSE TO ADDRESSING
         BNE   SNOEXIT                  SHOULD NOT OCCUR
         LH    RWKC,LCBTTCIN            CURRENTLY CONNECTED
         LTR   RWKC,RWKC                IS THE SOURCE ON A DIAL  S22025
*                                         LINE SET               S22025
         BZ    TXTRESET                 NO, RESET THE LINE AND   S22025
*                                         TREAT AS A PERM ERROR  S22025
         BAL   RWKD,FINDTERM            GET TERMINAL ADDRESS
         USING IEDQTRM,RTERM
         SR    RWKB,RWKB                CLEAR FOR INDEX
         IC    RWKB,TRMCHCIN            INDEX TO CHARACTERISTICS
         BCTR  RWKB,0                   DECREMENT FOR INDEX
         MH    RWKB,AVTDCTLN            LENGTH OF DCT ENTRY@G36XREV
         A     RWKB,AVTCSTCS            POINT TO CHARACTERISTICS
         TM    2(RWKB),X'10'            STATION CONTROL-MULTIPT
         BZ    NOMPT                    BRANCH NOT MULTIPT
         LA    RCUR,LCBCPA+16           RESTART AT TOP
         B     TRYRETRY                 ATTEMPT RETRY
NOMPT    EQU   *
         SPACE
         TM    3(RWKB),X'20'            END-END CONTROL
         BNZ   TOTXT                    TREAT SAME AS TEXT TIMEOT
         SPACE
         SH    RCUR,H8                  BACK UP TO WRITE ENQ
         B     TRYRETRY                 ATTEMPT RETRY
         SPACE
TOWRITE  EQU   *
         TM    SCBSTATE,SCBTRANP        TRANSPARENT SNED
         BZ    SNOEXIT                  NO NO DLE ENQ REQUIRED
         LA    RWKC,DLEENQCH            INDEX PARAMTERE
         BAL   RWKD,CCWBLD              BILD SEQUENCE
         MVI   LCBERCCW+4,0             NO FLAGS
         MVI   LCBINCAM+1,TOWRRET       SET 404H RETURN
         B     COMEXIT                  COMMON EXIT
         SPACE 5
INTRTEST EQU   *
         TM    LCBSENS0,INTREQ          INTERVENTION REQUIRED
         BZ    TRYBO                    NO, TRY BUSOUT
         CLI   CCWOPCDE,CCWWRITE        WRITE COMMAND
         BNE   TXTSNO                   BRANCH IF READ
         SPACE
         CLI   CCW+8,CCWDISAB           THIS A WRITE DLE EOT
         BNE   PERMEXIT                 NO,
         LA    RCUR,8(,RCCW)            BUMP TO DISABLE
         B     TRYRETRY                 RETRY AT DISABLE         S22025
         SPACE 2
         SPACE 5
TRYBO    EQU   *
         CLI   CCWOPCDE,CCWREAD         READ
         BE    TXTRTRY                  RETRY COMMAND
         SPACE
         CLC   CCWCOUNT,LCBCSW+5        ORIGINAL COUNT=RESIDUAL
         BE    TRYRETRY                 YES, RETRY
         SPACE
         TM    SCBSTATE,SCBTRANP        TRANSPARENT
         BZ    SNOEXIT                  NO, SHOULD NOT OCCUR
         SPACE
         LA    RWKC,DLEENQCH            SUBROUTINE PARAMETER
         BAL   RWKD,CCWBLD              BUILD WRITE DLEENQ
         MVI   LCBERCCW+4,0             NO FLAGS
         MVI   LCBINCAM+1,BOXPRET
         B     COMEXIT                  COMMON EXIT
         SPACE 2
         SPACE 5
UXWRITE  EQU   *
         CLI   LCBRESTR,TPWRAKNK        WRITE TEXT RESPONSE
         BNE   NOTRESP                  BRNCH NO
         MVI   LCBINCAM+1,UNEXRET       ASSUME WRTE TEXT RESPONSE
         B     READBLD                  BUILD READ
         SPACE
NOTRESP  EQU   *
         LA    RCUR,NEXTCCW(RCUR)       ASSUME WRITE ENQ       @SA72446
         CLI   LCBRESTR,TPWRENQ         WRITE ENQ
         BE    COMEXIT                  BRANCH IF YES          @SA72446
         SPACE
         MVI   LCBINCAM+1,SKIPRET       SET READ SKIP RETURN
         MVC   LCBERCCW+4(4),SKPFLGCT   SET FLAGS AND COUNT
         B     READEXIT                 USE COMMON EXIT
READBLD  EQU   *
         MVC   LCBERCCW+4,SETCNT        SET READ COUNT
READEXIT EQU   *
         MVC   LCBCSWSV,LCBCSW          SAVE ENDING STATUS
         LA    RWKA,LCBERCCW+8          DATA ADDRESS
         ST    RWKA,LCBERCCW            SET IN CCCW
         MVI   LCBERCCW,CCWREAD         SET COMMAND CODE
         LA    RCUR,LCBERCCW            SET START ADDRESS
         B     COMEXIT                  UE COMMON EXIT
         SPACE 5
CCWBLD   EQU   *
         MVC   LCBCSWSV,LCBCSW          SAVE CURRENT STATUS
         MVC   LCBSNSV,LCBSENS0         SAVE SENSE BYTE
         LR    RWKA,RIOSB               SAVE IOSB ADDRESS        Y02027
         L     RWKA,DCBSCTAD-1          SCT ADDRESS
         SR    RWKB,RWKB                CLEAR INDEX REGISTER
         IC    RWKB,0(RWKC,RWKA)        INDEX TO SEQUENCE
         IC    RWKC,0(RWKB,RWKA)        GET COUNT
         STH   RWKC,LCBERCCW+6          SET COUNT
         LA    RWKA,1(RWKA,RWKB)        POINT TO SEQUENCE
         ST    RWKA,LCBERCCW            SET IN CCW
         MVI   LCBERCCW,CCWWRITE        SET COMMAND CODE
         LA    RCUR,LCBERCCW            FOR EXIT CODE
         BR    RWKD                     RETURN
         SPACE 5
TRYRETRY EQU   *
         CLI   LCBINCAM,RETRY           RETRY LIMIT RECHED
         BE    PERMEXIT                 YES,EXIT TO POST ERROR
         IC    RWKB,LCBINCAM            RETRY COUNT
         LA    RWKB,1(,RWKB)            BUMP
         STC   RWKB,LCBINCAM            SET BACK
COMEXIT  EQU   *
         ST    RCUR,LCBSTART-1          RESTART
         MVI   LCBSENS0,AVTEZERO        CLEAR SENSE FOR RETRY    Y02027
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE IS ADDED FOR AOS.                         *
*                                                                     *
         CLI   LCBECBCC,HIOCC           IOHALT ISSUED            Y02027
         BNE   SVCS                     BRANCH NO                Y02027
         SPACE 1                                                 Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CTL      Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
SVCS     EQU   *                                                 Y02027
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         SR    RLCB,RLCB                CLEAR FOR ICM.
         ICM   RLCB,SEVEN,IOBPTR(RTST)  GET IOB ADDR FROM RQE,   X01004
*                                       FOR USE BY SIO APPENDAGE.
         SR    RTERM,RTERM              CLEAR FOR ICM.
         ICM   RTERM,SEVEN,DEBPTR(RTST)  GET DEB ADDR FROM RQE   X01004
         LA    R15,DEBNMSUB-IEDQDEB     GET APDG. TABLE SIZE     Y02027
         SLR   RTERM,R15                POINT TO APDG. TABLE     Y02027
         L     RTERM,SIOPTR(ZERO,RTERM)  GET ADDR OF SIO APPEND- X01004
*                                       AGE FROM APPENDAGE VECTOR
*                                       TABLE.
         SR    R15,R15                  CLEAR R15 TO TELL SIO AP-
*                                       PENDAGE IT'S A SUBROUTINE.
         BAL   RXCTL,SIOENTRY(ZERO,RTERM)  LNK TO SIO APPNDG AT  X01004
*                                       OFFSET +16 TO CONVERT CCW'S
*                                       FROM VIRTUAL TO REAL.
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        LINE CONTROL BLOCK TO THE I/O SUPERVISOR BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY OPERATIONS FROM ERP          Y02027
**************************************************************** Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                       INTO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM LCB TO IOSB    Y02027
         SVC   EREXCP
         SPACE
         SVC   RETURN
         SPACE 2
FINDTERM EQU   *
         L     RWKB,AVTRNMPT            ADDRESS OF CODE BASE
         USING IEDQTNTD,RWKB
         BAL   RWKA,TNTDCODE            LINK TO IT
         BR    RWKD                     REUTRN
         SPACE 2
PERMEXIT EQU   *
         MVI   LCBINCAM+1,POPERM        RETRY EXHAUST
         B     LOADMOD                  COMMON CODE
         SPACE
SNOEXIT  EQU   *
         MVI   LCBINCAM+1,POSNO         'SHOULD NOT OCCUR' REUTRN
LOADMOD  EQU   *
         LH    RLINK,POSTMOD            POST MODULE
LOADMOD1 EQU   *                                               @OY12439
         L     RXCTL,CVTPTR             CVT ADDRESS
         L     RXCTL,XCTLADD(,RXCTL)    ADDRESS OF XCTL
        BR    RXCTL                    LINK TO XCTL
LOAD204H EQU   *                                               @OY12439
         CLI   LCBINCAM,RETRY           RETRY COUNT EXCEEDED   @OY12439
         BNL   TXTRESET                 YES, BRANCH            @OY12439
         LH    RLINK,MOD204H            IGE0204H INDEX         @OY12439
         B     LOADMOD1                 EXIT TO IGE0204H       @OY12439
         EJECT
*        C O N S T A N T S
SKPFLGCT DC    AL1(CCWSKIP),AL3(65000)  FLAG & CT. FOR READ SKIP S22025
SETCNT   DC    A(16)                    READ COUNT
POSTMOD  DC    AL2(5048)                POST MODULE ID
MOD204H  DC    AL2(2048)                IGE0204H INDEX         @OY12439
H8       DC    H'8'                     LENGTH OF CCW            S22025
SKIPCNT  DC    AL1(CCWSKIP),X'00FFFF'   FLAG AND LARGE COUNT     S22025
WRENQRET EQU   8                        404H RETURN WRITE ENQ
ZERO     EQU   0                        LENGTH EQUATES USED IN   X01004
ONE      EQU   1                                                 Y02027
TWO      EQU   2                                                 Y02027
HIOCC    EQU   X'48'                    HALT I/O COMPLETION      Y02027
SEVEN    EQU   7                        ADDRESSING AND DISPLACMNTY02027
RSPNQRET EQU   16                       404H RETURN ENQ RESPONSE
BOXPRET  EQU   36                       404H RETURN
DLEENQCH EQU   17                       INDEX INTO SCT
TORSPRET EQU   24                  RETURN INDICATOR FOR 404H
TOWRRET  EQU   32                       404H RETURN
POSNO    EQU   4                        RETURN FOR 504H
POPERM   EQU   8                        RETURN FOR 504H
UNEXMSK  EQU   X'90'                    SELECTIVE RECORDING MASK
UNEXRET  EQU   12                       404H UNEX TEXT RESPONSE
LDRSPRET EQU   4                        404H RETURN
RETRY    EQU   6                        RETRY COUNT
CVTPTR   EQU   X'10'                    POINTER TO CVT
XCTLADD  EQU   X'2C'                    OFFSET IN CVT-XCTL ROUTINE
IOBOFFST EQU   4                        IOB OFFSET IN RQE
ERPCTL   EQU   X'20'                    ERP IN CONTRO BIT
UNITCHK  EQU   X'02'                    UNIT CHECK MASK IN CSW
TIMEOUT  EQU   X'01'                    TIMEOUT ERROR MASK IN SENSE
LDTXTRET EQU   44                       404H RETURN
UCBOFFST EQU   2                        OFFSET TO UCB ADDRESS -RQE
EREXCP   EQU   15                       ERROR EXCP
RETURN   EQU   3                        MASK FOR RETURN SVC
INTREQ   EQU   X'40'                    MASK FOR INTERVENTION REQ
BUSOUT   EQU   X'20'                    MASK FOR BUSOUT
EQCHK    EQU   X'10'                    EQUIPMENT CHECK MSSK
LOSTD    EQU   X'02'                    LOST DATA MASK
X24      EQU   X'24'                    MASK FOR ERP IN CONTROL
CHEND    EQU   X'80'                    MASK FOR CHANNEL END
DEVEND   EQU   X'04'                    MASK FOR DEVICE END
ENQCH    EQU   7                        OFFSET INTO SCT
RDENQ1   EQU   16                       RETURN FO R 404H
LDRSPETB EQU   4                        RETURN INDICATOR
SKIPRET  EQU   20                       RETURN FOR 404H
X01      EQU   X'01'                    FLAG FOR IO GENERATOR
TXTRST   EQU   40                       404H RETURN
NEXTCCW  EQU   8                        LENGTH OF A CCW        @SA72446
IOBPTR   EQU   5                        OFFSET OF IOB ADDR. IN RQE.
DEBPTR   EQU   9                        OFFSET OF DEB ADDR. IN RQE.
SIOPTR   EQU   4                        OFFSET OF SIO APDG. ADDRESS
*                                       IN APDG. VECTOR TABLE.
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
*                                       POINT IN SIO APPENDAGE.
         EJECT
         TDEBD                          DEB DSECT
         EJECT
         TSCBD
         TCCWD
         TLCBD
         TTNTD
         DCBD  DSORG=TX
         TAVTD
         TTRMD
         TTPD
         IECDIOSB                                                Y02027
         END
