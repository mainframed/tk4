         TITLE 'IGG01938 FOURTH LOAD OF LINE GROUP OPEN'
IGG01938 CSECT
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS                                    S21903
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A430000,451000                                                 SA54300
*A092000                                                         A44912
*A126000,152000                                                  A44912
*D154000-159000,166000                                           A44912
*C139000                                                         A44912
*A535000                                                         S21101
*C391000                                                         S21101
*A562000                                                         A44908
*C092000,442000-447000                                           A44908
*C092400,436000-446800                                            M1178
*D468000,476000,092050-092150                                     M1178
*A435600,477420,477740                                          SA51084
*A390200                                                        SA52995
*C092400,390200                                                 SA52995
*D391400-394000                                                 SA52995
*A002000                                                         S21903
*C003000,090000,147000,149000,345000,347000,355000,515000-516000 S21903
*C522000-523000,528000-533000,568000,570000,573000-576000        S21903
*D390300                                                        SA61064
*A559000                                                         Y01004
*C092000-093000,101000                                           Y01004
*A105000                                                        SA65392
*D107000,335000,522000-523000,526000,534000-535000,536000       SA65392
*D543000,548300-548600,562300-562600                            SA65392
*C004000,015000,018000,029000,030000,031000,031500,047000,048000 Y02027
*C087000,088000,096000,097000,098000,099000,100000,101000,358000 Y02027
*C490000,521000,537000,538000-541000,544000-556000,558000        Y02027
*D559000-559600                                                  Y02027
*A029500,032500-035500,083500,091500,180500                      Y02027
*A194500,223500,278500,334500-335500,435840-435960               Y02027
*A537300,537600,580100-580600                                    Y02027
*C181000,189000,192000,196000,201000,230000,237050,248000        Y02027
*C273000,277000,372000,396000,403000,410000,424000,538000,539000 Y02027
*D086500,537000,537300                                           Y02027
*A166000,181500,192500,372500,396500,403500,410500,477190,539500 Y02027
*A576200-576600                                                  Y02027
*A101000                                                       @ZA01156
*C105350                                                       @ZA01156
*A141000,151000,533000                                         @SA73558
*C190000-194000                                                @ZA02632
*A461000                                                       @OS76977
*D564000-568500,573000-576000                                  @OS76977
*A430000,5620000                                                X30X5MT
*A221100                                                       @YA14062
*C183000-188000,190000,193500,196000-197000,220000-221000      @YA14062
*D198000-203000                                                @YA14062
*C105770                                                       @OX16393
*A187800                                                       @OY16102
*D190600-192400,211000-219000                                  @OY16102
*A220400,221600                                                @OS78352
*C105770                                                       @OZ24288
*A126500,525000                                                @OY18871
*D562300                                                       @OY18871
         SPACE 3
***********************************************************************
*                                                                     *
*MODULE-NAME = IGG01938                                          S21903
*                                                                S21903
*DESCRIPTIVE-NAME = FOURTH LOAD OF LINE GROUP OPEN               S21903
*                                                                S21903
*COPYRIGHTS = 'NONE'                                             S21903
*                                                                S21903
*STATUS = CHANGE LEVEL 6                                         Y02027
*                                                                     *
*FUNCTION/OPERATION - THIS EXECUTOR CALCULATES THE NUMBER OF          *
*   STCB'S IN THE STCB CHAIN OF EACH LCB IN THIS LINE GROUP           *
*   AND STORES THIS NUMBER IN THE INVITATION LIST FOR THIS            *
*   LINE.                                                             *
*   IT MODIFIES THE ERRTAB AND ATNTAB FIELDS IN THE UCB FOR           *
*   EACH LINE.                                                        *
*   IT BUILDS THE INITIAL CONTACT CHANNEL PROGRAM IN THE LCB          *
*   FOR EACH LINE.                                                    *
*   IT DETERMINES HOW EACH LINE IS TO BE STARTED AND THIS             *
*   STATUS IS INDICATED IN THE LCB.                                   *
*                                                                     *
*INPUT - THE REGISTERS 5, 6, 7, AND 8 ARE THE INPUT AS FOLLOWS        *
*   5 -  THE ADDRESS OF THE WTG TABLE PREFIX                     Y02027
*   6 -  THE ADDRESS OF THE BEGINNING OF THE WHERE-TO-GO TABLE.       *
*   7 -  THE ADDRESS OF THE COPY OF THE CURRENT DCB              Y02027
*   8 -  THE ADDRESS OF THE CURRENT ENTRY IN THE WHERE-TO-GO          *
*        TABLE .                                                      *
*                                                                     *
*ENTRY POINTS - THIS ROUTINE IS ENTERED AT IGG01938 VIA AN            *
*   XCTL FROM THE OPEN EXECUTOR IGG01937.  IT MAY ALSO BE             *
*   RE-ENTERED FROM THE RELOOP PORTION OF THIS ROUTINE IF             *
*   IT IS DETERMINED THAT THERE IS ANOTHER DATA CONTROL               *
*   BLOCK TO BE OPENED.                                               *
*                                                                     *
*OUTPUT -                                                        Y02027
*   REGISTER 7 WILL BE POSITIONED AT THE ADDRESS OF THE          Y02027
*   COPY OF THE NEXT DCB TO BE PROCESSED                         Y02027
*                                                                Y02027
*   REGISTER 8 WILL BE POSITIONED AT THE NEXT ENTRY IN           Y02027
*   THE WHERE-TO-GO PARAMETER LIST                               Y02027
*                                                                Y02027
*   INITIAL CHANNEL PROGRAMS WILL HAVE BEEN BUILT IN             Y02027
*                   EACH LCB                                     Y02027
*EXTERNAL ROUTINES - NONE                                             *
*                                                                     *
*EXITS - NORMAL                                                       *
*   THIS ROUTINE EXITS VIA AN XCTL TO THE EXECUTOR IDENTIFIED         *
*   BY THE NEXT NON-ZERO ENTRY IN THE WHERE-TO-GO TABLE,              *
*   NAMELY IGG01939.                                                  *
*                                                                     *
*      - ERROR                                                        *
*                                                                     *
*TABLES/WORK AREAS - THE WHERE-TO-GO TABLE CONTAINS THE ID AND        *
*   TTR OF THE OPEN EXECUTORS AS WELL AS THE ADDRESS             Y02027
*   OF A COPY OF EACH DCB SPECIFIED IN THE OPEN                  Y02027
*   MACRO INSTRUCTION.  THE SPECIAL CHARACTERS TABLE CONSISTS         *
*   OF ENTRIES GIVING THE SPECIAL CHARACTERS REQUIRED FOR             *
*   DEVICE I/O FOR EACH TERMINAL                                      *
*                                                                     *
*ATTRIBUTES - THIS ROUTINE IS EXECUTED IN THE TRANSIENT AREA AS       *
*   ENABLED, PRIVILEGED, AND RE-ENTRANT.                              *
*                                                                     *
***********************************************************************
         EJECT
*
*    REGISTER USAGE
*
*
RE       EQU   0                        WORK REGISTER
RF       EQU   1                        WORK REGISTER
RDCB     EQU   2                        ADDRESS OF THE CURRENT DCB
RTIOT    EQU   3                        WORK REGISTER
RCORE    EQU   4                        ADDRESS OF DCB WORK AREA
RPAR     EQU   5                        ADDRESS OF FIRST ENTRY IN
*                                         DCB PARAMETER LIST
RWTG     EQU   6                        ADDRESS OF THE WHERE-TO-GO
*                                         TABLE
RPARC    EQU   7                        ADDRESS OF THE CURRENT
*                                         ENTRY IN DCB PARAMETER
*                                         LIST
RWTGC    EQU   8                        ADDRESS OF THE CURRENT
*                                         ENTRY IN THE WTG TABLE
RAVT     EQU   9                        ADDRESS OF THE AVT
RUCB     EQU   10                       WORK REGISTER
RDEB     EQU   11                       WORK REGISTER
RBASE    EQU   12                       BASE OF THIS ROUTINE
RC       EQU   13                       WORK REGISTER
RD       EQU   14                       WORK REGISTER
RJ       EQU   15                       WORK REGISTER
*
         USING IEDQTCX,RF               TCX DSECT                Y02027
         USING IHADCB,RDCB              DCB DSECT
         USING IEDQLCB,RCORE            LCB/IOB DSECT
         USING IEDQAVTD,RAVT            AVT DSECT
         USING DEBNMSUB,RDEB            DEB DSECT                Y02027
         USING FORCORE,RJ               OPEN WORK AREA DSECT     Y02027
*
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY S21903
         USING *,RBASE
         SPACE 2
IGG01938 IEDHJN RESTART                 ASSEMBLY DATE            S21903
         EJECT
         L     RJ,4(RWTGC)              RESTORE THE ADDRESS OF THE
*                                       OPEN WORK AREA           Y02027
         L     RDCB,DXUDCBAD            GET THE CURRENT DCB ADDR Y02027
         L     RF,CVT                   GET THE CVT ADDRESS      Y02027
         L     RF,AVTCVTPT(RF)          GET THE TCX ADDRESS      Y02027
         L     RAVT,TCXAVT              GET THE AVT ADDRESS      Y02027
         L     RDEB,DCBDEBAD            GET THE DEB ADDRESS      Y02027
         SR    RJ,RJ                    CLEAR FOR IC           @ZA01156
         SR    RD,RD                    CLEAR FOR IC           @ZA01156
         IC    RJ,DEBNMEXT              NUMBER OF EXTENTS      @ZA01156
         IC    RD,DCBEIOBX              SIZE OF LCB            @ZA01156
         MR    RD,RD                    SIZE OF LCB POOL       @ZA01156
         SR    RD,RD                    CLEAR FOR IC           @ZA01156
         IC    RD,DCBEIOBX              SIZE OF LCB            @ZA01156
         L     RCORE,DCBIOBAD           IOB AREA               @ZA01156
         LA    RJ,0(RJ,RCORE)           IOB AREA + LCB POOL SZ @ZA01156
         AR    RJ,RD                    ADD SIZE OF LCB        @ZA01156
         LR    RD,RJ                    SAVE REG               @ZA01156
         LA    RJ,LCBFLAG1-IEDQLCB      LCB PREFIX SIZE        @ZA01156
         SR    RD,RJ                    THRESHOLD AREA         @ZA01156
         SR    RC,RC                    CLEAR WORK REGISTER     SA65392
         IC    RC,DEBNMEXT              GET NUMBER EXTENTS      SA65392
         LR    RCORE,RC                 COPY NUMBER EXTENTS     SA65392
         SLL   RCORE,TWO                SIZE OF UCB ADDRESSES   SA65392
         ST    RD,DEBUCBAD(RCORE)       SAVE THRESHOLD COUNTER @ZA01156
*                                         POINTER IN ACCESS     SA65392
*                                         METHOD SECTION OF DEB SA65392
INIT     EQU   *                                                SA65392
         MVC   AVTEZERO(FOUR,RD),AVTHRESH INITIALIZE THRESHHOLD SA65392
         MVI   AVTEZERO(RD),AVTEZERO    TRANSMISSIONS ARE ZERO  SA65392
         LA    RD,12(,RD)               BUMP TO NEXT ENTRY     @OZ24288
         BCT   RC,INIT                  CONTINUE TIL END        SA65392
         LA    RD,DEBUCBAD              ADDRESS OF DEB EXTENTS
         IC    RC,DCBEIOBX              GET THE SIZE OF AN LCB
         L     RCORE,DCBIOBAD           LOAD THE ADDRESS OF THE
*                                         FIRST IOB
         LA    RF,PTRIOB                LOAD THE DISPLACEMENT OF
*                                         THE IOB
         SR    RCORE,RF                 SET THE BEGINNING ADDRESS
*                                         OF THE LCB
         LA    RCORE,0(RC,RCORE)        ADD BACK THE SIZE OF 1 LCB
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF CODE CALCULATES THE NUMBER OF STCB'S     *
*        CHAINED OFF THIS PARTICULAR LINE CONTROL BLOCK AND       *
*        STORES THIS NUMBER IN THE INVITATION LIST FOR THIS DCB   *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SR    RE,RE                    CLEAR A REGISTER FOR AN  A44912
*                                       EXTENT COUNT             A44912
         IC    RE,DEBNMEXT              THE NUMBER OF EXTENTS TO A44912
*                                       BE USED AS A LOOP CONTROLA44912
         SR    RF,RF                    CLEAR A REGISTER FOR THE A44912
*                                       INVITATION LIST OFFSETS  A44912
NEXTLIN  EQU   *                                                 A44912
         CLI   LCBRSKEY,BUFRDTRM        IS THIS LCB FOR A BUFFERED
*                                         TERMINAL
         BNE   DEBSET                   NO, BRANCH
         LR    RC,RF                    SAVE INVLIST ENTRY INDEX A44912
         TM    0(RPARC),OUTONLY         IS THIS LINE OPENED    @OY18871
*                                        FOR OUTPUT ONLY       @OY18871
         BNO   STCBADDR                 NO, BRANCH             @OY18871
         L     RJ,DCBINVLI(RC)          LOAD ADDR OF INVITATION@OY18871
*                                        LIST FOR THIS DCB     @OY18871
         CLC   0(8,RJ),DBLZERO          IS THIS A DUMMY        @OY18871
*                                        INVITATION LIST       @OY18871
         BE    NOEOT                    YES, BRANCH            @OY18871
STCBADDR EQU   *                                               @OY18871
         SR    RF,RF                    CLEAR A COUNT REGISTER
         L     RJ,LCBRSLNK-1            LOAD THE ADDRESS OF THE
*                                         FIRST STCB ON THE CHAIN
STCBCHK  EQU   *
         LA    RF,ONE(RF)               INCREMENT THE STCB COUNT
         CLC   FIVE(THREE,RJ),AVTDELAD+1     IS THIS THE LAST STCB
*                                            ON THE CHAIN
         BE    STCBEND                  YES, BRANCH
         L     RJ,FOUR(RJ)              LOAD THE ADDRESS OF THE
*                                         NEXT STCB
         B     STCBCHK                  INCREMENT THE COUNT
STCBEND  EQU   *
         L     RJ,DCBINVLI(RC)          LOAD THE ADDRESS OF THE  A44912
*                                         INVITATION LIST FOR THIS
*                                         DCB
         LR    RTIOT,RJ                 SAVE INVITATION LIST   @SA73558
         STC   RF,FOUR(RJ)              STORE THE STCB COUNT IN THE
*                                         INVITATION LIST
         STC   RF,SEVEN(RJ)             STORE THE STCB COUNT AGAIN
ILOOP    EQU   *
         CLI   8(RJ),X'FE'
*                                       END OF ACTIVE ENTRIES    S21903
         BE    ISTORE                   YES,BRANCH               S21903
         LA    RJ,1(0,RJ)
*                                       BUMP TO NEXT BYTE AND    S21903
         B     ILOOP                    CHECK FOR END OF LIST    S21903
ISTORE   EQU   *
         LA    RJ,8(0,RJ)
         TM    THREE(RTIOT),HEX80       EOT PRESENT            @SA73558
         BNO   NOEOT                    BR NO, NO EOT          @SA73558
         BCTR  RJ,0                     BACK UP TO EOT         @SA73558
NOEOT    EQU   *                                               @SA73558
         ST    RJ,LCBINVPT-1
         LR    RF,RC                    RESTORE INVLIST INDEX    A44912
         IC    RC,DCBEIOBX              GET SIZE OF ONE LCB      A44912
DEBSET   EQU   *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        BUILD A NOP COMMAND FOR ANY DIAL LINES                   *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         USING UCB,RUCB                 UCB DSECT                Y02027
         L     RJ,LCBSIOCC              ADDRESS OF THE CHANNEL
*                                         PROGRAM AREA
         LA    RTIOT,FOUR               LOAD AN INCREMENT OF FOUR
         SR    RJ,RTIOT                 DECREMENT THE CPA POINTER
         L     RUCB,0(RD)               GET THE ADDRESS OF THE
*                                         FIRST/NEXT UCB
         LTR   RUCB,RUCB                IS THIS A // DD DUMMY ENTRY
         BZ    SETADDR                  YES, BRANCH
         ST    RAVT,FOUR(RJ)            STORE ANY VALID ADDRESS IN
*                                         THIS CCW
         MVI   4(RJ),X'03'              SET THE COMMAND CODE FOR A
*                                         NOP
         MVC   8(4,RJ),NOP              BUILD THE REST OF THE NOP
*                                         COMMAND
         SPACE 2
         LR    RTIOT,RUCB               SET UCB ADDRESS          Y02027
         IOSGEN TP,VAR=2F,UCB=(RTIOT)   SET ERROR TAB            Y02027
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  @YA14062
*                                                           *  @YA14062
*        NEW OPTION AUTO= YES OR NO IN INVITATION LIST      *  @YA14062
*        ALLOWS SPECIFICATION OF AUTOPOLL ON A LINE         *  @YA14062
*        THE FOLLOWING ROUTINE WILL RESET THE AUTOPOLL      *  @YA14062
*        BIT IN THE CONTROL WORD IF THE AUTOPOLL BIT        *  @YA14062
*        IS NOT ON IN THE UCB                               *  @YA14062
*                                                           *  @YA14062
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  @YA14062
         LA    RTIOT,DCBINVLI           LOAD THE ADDRESS OF    @OY16102
*                                       THE 1ST INVITATION     @OY16102
*                                       LIST FOR THIS DCB.     @OY16102
         AR    RTIOT,RF                 ACCESS THE ADDRESS OF  @OY16102
*                                       THE INVITATION LIST    @OY16102
*                                       FOR THE CURRENT ENTRY. @OY16102
         L     RTIOT,0(RTIOT)           LOAD THE ADDRESS OF    @OY16102
*                                       THE CONTROL WORD FOR   @OY16102
*                                       THE CURRENT ENTRY.     @OY16102
         TM    UCBDVCLS,GRAPHICS        IS THIS 2260 LOCAL       Y02027
         BZ    TSTAPOLL                 BRANCH NO              @YA14062
         NI    THREE(RTIOT),AVTEFF-APBIT RESET AUTOPOLL BIT	   @YA14062
         B     STARTLIN                 START TERMINAL         @YA14062
         EJECT
TSTAPOLL EQU   *
         TM    17(RUCB),AUTOPOLL        UCB INDICATE AUTOPOLL  @YA14062
         BO    TESTBFRD                 YES, BRANCH            @YA14062
         TM    AVTBIT2,AVTSTRTN         IS A RESTART IN PROGRESS
         BZ    INVLIST1                 NO, BRANCH
         TM    AVTBIT3,AVTSTAIN         HAS THE USER REQUESTED THAT
*                                         HIS INVITATION LISTS BE
*                                         CHECKPOINTED
         BO    CUTEST                   YES, BRANCH
INVLIST1 EQU   *
         NI    THREE(RTIOT),AVTEFF-APBIT  TURN OFF AUTOPOLL    @YA14062
*                                         BIT IN CONTROL WORD  @YA14062
         B     CUTEST                   BYPASS BUF TERM TEST   @OS78352
TESTBFRD EQU   *                                               @YA14062
         CLI   LCBRSKEY,BUFRDTRM        IS THIS AN LCB FOR A
*                                         BUFFERED TERMINAL
         BNE   CUTEST                   NO, BRANCH
         TM    THREE(RTIOT),APBIT       USER SPECIFIED AUTOPLL @OS78352
         BZ    CUTEST                   NO, BRANCH             @OS78352
         OI    5(RTIOT),APBIT           TURN ON THE SECOND AUTOPOLL
*                                         BIT IN THE INVITATION LIST
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*    TEST FOR A 2702 CONTROL UNIT
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
CUTEST   EQU   *
         IC    RTIOT,UCBUNTYP           LOAD THE CNTL UNIT BITS  Y02027
*                                         FROM THE UCB
         N     RTIOT,ANDMASK            CLEAR ALL UNWANTED BITS
*                                         FROM RTIOT
         LTR   RTIOT,RTIOT              HAS A CONTROL UNIT BEEN
*                                         SPECIFIED FOR THIS DEVICE
         BZ    STARTLIN                 NO, THEN ASSUME THE DEVICE
*                                         IS A 7770
         CLI   UCBUNTYP,CONTTYP3        TEST FOR A 2701 CONTROL  Y02027
*                                         UNIT WITH AN IBM TYPE III
*                                         ADAPTER
         BE    STARTLIN                 IF YES, START THE LINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*                  BUILD A DISABLE COMMAND                        *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         ST    RAVT,FOUR(RJ)            STORE ANY VALID ADDRESS IN
*                                         THIS CCW
         MVI   FOUR(RJ),DISABLE         SET THE COMMAND CODE FOR A
*                                         DISABLE
         MVC   EIGHT(FOUR,RJ),NOP       BUILD THE REST OF THE
*                                         DISABLE COMMAND
         LA    RJ,EIGHT(RJ)             INCREMENT THE CCW POINTER
         BCT   RTIOT,NOT2702            IS EITHER A 2701 OR 2703
*                                         CONTROL UNIT
*
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*    SET UP THE CORRECT SAD COMMAND FOR THIS 2702 CONTROL UNIT
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         IC    RTIOT,UCBTBYT2           LOAD FEATURE BYTE FROM   Y02027
*                                         THE UCB
         N     RTIOT,ANDMASK            CLEAR EXTRANEOUS FEATURE
*                                         BITS FROM RTIOT
         IC    RTIOT,SADCODE(RTIOT)     INSERT ACTUAL SAD COMMAND
         ST    RAVT,FOUR(RJ)            STORE ANY VALID ADDRESS IN
*                                         THIS CCW
         STC   RTIOT,4(RJ)              STORE THE SAD CODE IN THE
*                                         CHANNEL PROGRAM AREA
         MVC   8(4,RJ),NOP              BUILD THE REST OF THE SAD
*                                         COMMANDS
         OI    0(RJ),CMDCHAIN           TURN ON THE COMMAND CHAINING
*                                         FLAG IN THE PREVIOUS CCW
         LA    RJ,8(RJ)                 BUMP POINTER TO NEXT DOUBLE
*                                         WORD IN THE CPA AREA
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*    SET CORRECT COMMANDS FOR THE 2701 AND 2703 CONTROL UNITS     *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
NOT2702  EQU   *
         TM    UCBUNTYP,BSCTYP2         DO WE HAVE A BINARY      Y02027
*                                         SYNCHRONOUS ADAPTER,
*                                         TYPE II
         BO    SETBISCP                 YES, BRANCH
         TM    UCBTBYT2,DIALLINE        TEST FOR A DIAL LINE     Y02027
         BNZ   STARTLIN                 GO TO START THE LINE
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        BUILD AN ENABLE COMMAND IF NOT A DIAL LINE               *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ENABLE   EQU   *
         ST    RAVT,FOUR(RJ)            STORE ANY VALID ADDRESS IN
*                                         THIS CCW
         MVI   4(RJ),X'27'              SET THE COMMAND CODE FOR
*                                         AN ENABLE
         MVC   8(4,RJ),NOP              BUILD THE REST OF THE ENABLE
*                                         COMMAND
         OI    0(RJ),CMDCHAIN           TURN ON THE COMMAND CHAIN
*                                         FLAG IN THE PRIOR
*                                         COMMAND
         EJECT
STARTLIN EQU   *
         OI    LCBSTAT1,LCBRECVN        INDICATE THAT THIS LINE IS
*                                         TO BE OPENED NORMALLY
         MVI   LCBTSTSW,SWITCHON        SET THE TEST AND SET SWITCH
*                                         IN THE LCB
         OI    LCBSTAT2,LCBNEGRP        SET THE NEGATIVE RESPONSE
*                                         TO POLLING BIT IN THE
*                                         SECOND STATUS BYTE
         TM    0(RPARC),OUTPUT          IS THIS LINE OPENED FOR
*                                         OUTPUT
         BZ    TESTIDLE                 NO, BRANCH
         TM    0(RPARC),OUTONLY         IS THIS LINE OPENED FOR
*                                         OUTPUT ONLY
         BNO   SETOUT                   NO, BRANCH
         OI    DEBOPATB,OUTONLYM        SET THE OUTPUT ONLY FLAG IN
*                                         THE DEB
SETOUT   EQU   *
         OI    DEBOPATB,OUTPUTM         SET THE OUTPUT FLAG IN THE
*                                         DEB
TESTIDLE EQU   *
         TM    AVTBIT2,AVTSTRTN         IS A RESTART IN PROGRESS
         BNZ   QCBCHK                   YES, BRANCH
IDLETEST EQU   *
         TM    0(RPARC),TCAMIDLE        IS THIS LINE TO BE OPENED
*                                         IDLE
         BNO   SETADDR                  NO, IT IS TO BE OPENED
*                                         NORMALLY
SETIDLE  EQU   *
         MVI   LCBSTAT1,X'00'           INDICATE THAT THIS LINE IS
*                                         TO BE OPENED IDLE
SETADDR  EQU   *
         AR    RCORE,RC                 ADDRESS OF THE NEXT LCB
         LA    RD,4(RD)                 ADDRESS OF THE NEXT UCB
         LA    RF,FOUR(RF)              INCREMENT THE INVITATION
*                                         LIST OFFSET TO THE NEXT
*                                         INVITATION LIST ADDRESS
         BCT   RE,NEXTLIN               BRANCH IF THERE ARE MORE
*                                         CHANNEL PROGRAMS TO BE
*                                         BUILT
         EJECT
         DROP  RJ                       DROP MASTER QCB DSECT    Y02027
         USING FORCORE,RJ               OPEN WORK AREA DSECT     Y02027
         SPACE 2
         MVC   0(5,RWTGC),TCAMLD5       SET THE ID FOR THE FIFTH
*                                         LOAD OF OPEN
         L     RJ,4(RWTGC)              ADDRESS OF OPEN WORK AREA
RELOOP   EQU   *
         LA    RWTGC,8(RWTGC)           NEXT WTG ENTRY
         LA    RPARC,4(RPARC)           NEXT DCB LIST ENTRY
         CLC   0(2,RWTGC),AMIDCNST      IS THIS EXECUTOR NEEDED
*                                         AGAIN
         BE    RESTART                  BRANCH IF YES            S21903
         CLC   0(2,RWTGC),OPIDCNST      END OF WTG TABLE
         BNE   RELOOP                   IF NO, BRANCH            S21903
         LR    RPARC,RPAR               REINITIALIZE WTG AND
         LA    RWTGC,32(RWTG)           PARAMETER REGISTERS
ZCHECK   EQU   *
         CLI   0(RWTGC),X'00'           IS THIS ENTRY ZERO
         BNE   XCTLRTNE                 IF NOT ZERO, TRANSFER CNTRL
         LA    RWTGC,8(RWTGC)           ADDRESS OF NEXT ENTRY
         LA    RPARC,4(RPARC)           ADDRESS OF NEXT DCB
         B     ZCHECK                   CHECK NEXT ENTRY         S21903
*
XCTLRTNE EQU   *
         LA    RJ,DXCCW12               ADDRESS OF A DOUBLE WORD Y02027
*                                         SAVE AREA
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID TO NAME FIELD
         MVC   14(3,RWTG),2(RWTGC)      MOVE TTR TO WTG TABLE
         XCTL  DE=(RWTG),SF=(E,(15))
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS ROUTINE BUILDS THE CHANNEL PROGRAM FOR              *
*        AND MODIFIES THE ERROR TAB IN THE UCB FOR ALL TCAM       *
*              BINARY SYNCHRONOUS DEVICES                         *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
SETBISCP EQU   *
         LR    RTIOT,RUCB               SET UCB ADDRESS          Y02027
         IOSGEN TP,VAR=30,UCB=(RTIOT)   MODIFY ERROR TAB         Y02027
*
         OI    LCBSTAT2,LCBSYNC         TURN ON THE BIT TO INDICATE
*                                         THAT THIS IS A BINARY
*                                         SYNCHRONOUS LINE
         LA    RTIOT,DCBINVLI           LOAD THE ADDRESS OF THE
*                                         FIRST INVITATION LIST IN
*                                         THE DCB
         AR    RTIOT,RF                 ACCESS THE ADDRESS OF THE
*                                         FIRST/NEXT INVITATION
*                                         LIST ADDRESS
         TM    UCBTBYT2,DIALLINE        IS THIS A DIAL LINE      Y02027
         BZ    TESTADPT                 BRANCH IF NOT           SA52995
         CLI   DCBCPRI,EQUALPRI         EQUAL PRIORITY FOR BSC DIS21101
         BNE   TESTADPT                 BRANCH NO, DEFAULT IS SENS21101
*                                         -SET BY PREVIOUS LOAD  S21101
         SPACE 1                                                 S21101
         MVI   LCBRSPRI,PRIEQ           SET STCB PRIORITY        S21101
TESTADPT EQU   *
         TM    UCBUNTYP,CU2703          IS THIS A 2703 CONTROL   Y02027
*                                       UNIT                     Y02027
         BO    SETMODE                  YES, BRANCH
         TM    0(RTIOT),X'20'           IS THE DUAL COMMUNICATION
*                                         INTERFACE SET TO
*                                         INTERFACE B
         BNO   TESTCODE                 NO, INTERFACE A WAS
*                                         SPECIFIED
*                                       YES                      Y02027
         TM    UCBTBYT2,GOODINTF        IS IT LEGAL              Y02027
         BZ    BISERROR                 NO, IT IS AN ERROR
TESTCODE EQU   *
         TM    0(RTIOT),X'08'           IS THE DUAL CODE FEATURE
*                                         SET TO TRANSMISSION CODE
*                                         B
         BZ    SETMODE                  NO, BRANCH
*                                       YES                      Y02027
         TM    UCBTBYT2,GOODTRAN        IS IT LEGAL              Y02027
         BZ    BISERROR                 NO, IT IS AN ERROR
SETMODE  EQU   *
         ST    RTIOT,4(RJ)              STORE THE DATA ADDRESS (THE
*                                         ADDRESS OF THE INVITATION
*                                         LIST ADDRESS FOR THIS
*                                         LINE) IN THE SECOND CCW
         MVI   4(RJ),X'23'              PUT THE SET MODE COMMAND
*                                         CODE IN THE SECOND CCW
         MVC   8(4,RJ),NOP              BUILD THE REST OF THE SET
*                                         MODE COMMAND
         OI    0(RJ),CMDCHAIN           TURN ON THE COMMAND CHAIN
*                                         FLAG IN THE FIRST CCW
         LA    RJ,8(RJ)                 INCREMENT THE CCW POINTER
         TM    UCBTBYT2,DIALLINE        IS THIS A DIAL LINE      Y02027
         BNZ   STARTLIN                 YES, BRANCH
         B     ENABLE                   BRANCH TO BUILD AN ENABLE
*                                         AS THE THIRD CCW IN THE
*                                         CHANNEL PROGRAM
         EJECT
QCBCHK   EQU   *
         L     RJ,AVTRDYA              GET ADDR OF EXIT ROUTINE X30X5MT
         CLI   OPENCHK(RJ),IDLE        COMPARE FOR RESTART CONDIX30X5MT
         BH    IDLETEST                RESTART WITH OPEN MACRO  X30X5MT
*                                      SPECIFIED CONDITION      X30X5MT
         BE    SETIDLE                 RESTART WITH ALL LINES IDX30X5MT
*                                      RESTART WITH CHECK POINT X30X5MT
         CLI   LCBRSKEY,BUFRDTRM        BUFFERRED TERM LCB      SA54300
         BE    NOTSEND1                 BRANCH IF YES           SA54300
         CLI   LCBRSPRI,SEND            DOES THIS LCB SPECIFY SEND
*                                         PRIORITY
         BNE   NOTSEND                  NO, BRANCH
         CLI   LCBRSKEY,DIAL            IS THIS A DIAL LCB
         BNE   NDIALSND                 NO, BRANCH
         TM    AVTBIT1,AVTTSON          IS THIS A TSO DIAL LINE
         BO    FTSOQCB1                 YES, BRANCH
MIXED    EQU   *                                                SA51084
         SPACE 2
         DROP  RJ                       DROP WORK AREA DSECT     Y02027
         USING IEDQQCB,RJ               MASTER QCB DSECT         Y02027
         SPACE 2
         L     RJ,LCBLINK-ONE           GET ADDRESS OF QCB        M1178
         XC    LCBLINK,LCBLINK          CLEAR LCB LINK FIELD      M1178
         B     FTSOQCB3                 CHECK STATUS OF LINE      M1178
NOTSEND  EQU   *
         TM    0(RPARC),OUTPONLY        IS THIS LINE TO BE OPENED
*                                         FOR OUTPUT ONLY
         BO    NDIALSND                 YES, BRANCH
NOTSEND1 EQU   *                                                SA54300
         L     RJ,LCBRSPRI              GET THE STCB ADDRESS FOR
*                                         THE FIRST QCB
         B     SETQCBAD                 BRANCH
NDIALSND EQU   *
         L     RJ,LCBRSKEY              GET THE STCB ADDRESS FOR
*                                         THE FIRST QCB IN THE QUEUE
SETQCBAD EQU   *
         LA    RUCB,EIGHT               LOAD A VALUE OF 8
         SR    RJ,RUCB                  SET THE BEGINNING ADDRESS
*                                         OF THE QCB
         TM    0(RPARC),OUTPONLY        INPUT ONLY             @OS76977
         BNZ   FTSOQCB3                 BRANCH IF NO           @OS76977
         STM   RE,RD,AVTSAVE3           SAVE ALL REGS BUT REG15@OS76977
         SR    RC,RC                    CLEAR WORK REG         @OS76977
         IC    RC,LCBUCBX               RLN - 1                @OS76977
         SLL   RC,TWO                   MULTIPLY BY FOUR       @OS76977
         L     RC,DCBINVLI(RC)          GET INVLIST FOR THIS   @OS76977
*                                        LINE                  @OS76977
         BCTR  RC,RE                    BACK UP TO GET         @OS76977
         BCTR  RC,RE                    A TNT OFFSET           @OS76977
         LH    RF,0(,RC)                GET TNT OFFSET         @OS76977
         N     RF,AVTCLRHI              CLEAR HIGH HALFWORD    @OS76977
         L     RJ,AVTRNMPT              TNT CONVERSION CODE    @OS76977
         BALR  RD,RJ                    GET TERM ENTRY ADDRESS @OS76977
         L     RJ,0(,RF)                GET QCB ADDRESS        @OS76977
         LM    RE,RD,AVTSAVE3           RESTORE ALL BUT REG 15 @OS76977
FTSOQCB3 EQU   *
         TM    QCBLINK+1,LCBTRACE       IS THE I/O TRACE ACTIVE FOR
*                                         THIS LINE
         BZ    CHKLINK                  NO, BRANCH
         OI    LCBSTAT2,LCBTRACE        INDICATE THE I/O TRACE IS
*                                         ACTIVE FOR THIS LINE
CHKLINK  EQU   *
         CLI   QCBLINK,NORMAL           IS THIS LINE TO BE OPENED
*                                         NORMALLY
         BL    SETIDLE                  NO, BRANCH
         BE    SETADDR                  DON'T CHECK PARAMETER LIST
         BH    IDLETEST                 TEST THE PARAMETER LIST
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*            THIS SECTION OF CODE IS USED TO LOCATE THE           *
*            QCB FOR EACH TSO DIAL LINE IN ORDER TO               *
*            DETERMINE THE LINE STATUS FOR EACH LINE              *
*            IN THE EVENT OF A RESTART                            *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
FTSOQCB1 EQU   *
         USING IEDQTNTD,RUCB            TNT DSECT                Y02027
         L     RUCB,AVTRNMPT            LOAD THE ADDRESS OF THE
*                                         TERMNAME TABLE
         SR    RJ,RJ                    CLEAR A LENGTH REGISTER
         IC    RJ,TNTENLEN              GET THE MAXIMUM LENGTH OF A
*                                         TERMNAME TABLE ENTRY
         LA    RJ,THREE(RJ)             ADD 3 BYTES TO THE LENGTH
         LA    RUCB,TNTFIRST            GET THE ADDRESS OF THE
*                                         FIRST ENTRY IN THE TABLE
         SR    RUCB,RJ                  SET THE POINTER FOR THE
*                                         LINE ENTRY INDEX
         LH    RTIOT,LCBLNENT           GET THE LINE ENTRY OFFSET
*                                         FROM THE LCB
         N     RTIOT,AVTCLRHI           CLEAR HIGH-ORDER HALF   SA51084
         LTR   RTIOT,RTIOT              MIXED LINE, NOT TSO     SA51084
         BZ    MIXED                    BRANCH IF YES           SA51084
FTSOQCB2 EQU   *
         AR    RUCB,RJ                  INCREMENT THE TERMNAME
*                                         TABLE ADDRESS
         BCT   RTIOT,FTSOQCB2           DECREMENT THE LINE ENTRY
*                                         INDEX
         LA    RTIOT,THREE              GET THE LENGTH OF AN ADDRESS
         SR    RJ,RTIOT                 GET THE LENGTH OF A
*                                         TERMNAME TABLE ENTRY
         AR    RUCB,RJ                  LOCATE THE TERMINAL TABLE
*                                         ADDRESS FOR THIS ENTRY
         MVC   AVTGETMN+1(THREE),0(RUCB)     MOVE THE TERMINAL TABLE
*                                         ADDRESS TO A SCRATCH AREA
         L     RJ,AVTGETMN              LOAD THE TERMINAL TABLE
*                                         ADDRESS
         L     RJ,0(RJ)                 LOAD THE QCB ADDRESS FOR THIS
*                                         TERMINAL
         TM    0(RJ),QCBDISK+QCBCORE    TSO LINE                SA51084
         BNZ   MIXED                    BRANCH IF NO            SA51084
         B     FTSOQCB3                 CHECK THE LINE STATUS
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF ERROR CODE IS EXECUTED ONLY WHEN         *
*        AN ERROR IS DETECTED WHILE BUILDING THE INITIAL          *
*        CHANNEL PROGRAM FOR BINARY SYNCHRONOUS DEVICES,          *
*        EITHER THE DUAL COMMUNICATION INTERFACE OR THE           *
*        DUAL CODE FEATURE WAS SPECIFIED INCORRECTLY              *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
BISERROR EQU   *
         MVI   AVTOPERT,TCAMER09        MOVE THE ERROR IDENTIFIER
*                                         FOR TCAM INTO THE AVT
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF CODE IS USED TO SAVE THE ID OF THE       *
*        CURRENT OPEN EXECUTOR AND TO MOVE THE ID OF THE          *
*        ERROR RECOVERY MODULE INTO THE WHERE-TO-GO PARAMETER     *
*                            LIST                                 *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         L     RJ,FOUR(RWTGC)           LOAD THE ADDRESS OF THE
*                                         OPEN WORK AREA
         MVC   AVTOPXCL(TWO),AMIDCNST   MOVE THE ID OF THE CURRENT
*                                         EXECUTOR INTO THE AVT FOR
*                                         THE RETURN TO THIS
*                                         EXECUTOR
         MVC   0(FIVE,RWTGC),OPENERP    MOVE THE ID AND TTR FOR THE
*                                         ERROR MODULE INTO THE
*                                         WHERE-TO-GO PARAMETER LIST
         B     XCTLRTNE                 BRANCH TO THE XCTL ROUTINE
         EJECT
AMIDCNST DC    C'38'                    ID OF THIS EXECUTOR
         DS    0F
ANDMASK  DC    X'00000003'              MASK TO CLEAR ALL BUT    S21903
*                                       LOW ORDER TWO BYTES      S21903
OPIDCNST DC    C'0S'                    ID OF LAST OPEN MODULE   S21903
SADCODE  DC    X'13'                    SADZERO CODE
         DC    X'17'                    SADONE CODE
         DC    X'1B'                    SADTWO CODE
         DC    X'1F'                    SADTHREE CODE
SWITCHON EQU   X'F0'                    TEST & SET SWITCH IN LCB Y02027
NOP      DC    X'20000001'              NOP COMMAND
CMDCHAIN EQU   X'40'                    COMMAND CHAINING BIT
DBLZERO  DC    D'0'                     CONSTANT FOR DUMMY     @OY18871
*                                        INVITATION LIST       @OY18871
ONE      EQU   1                        NUMERIC CONSTANT
TWO      EQU   2                        NUMERIC CONSTANT         S21903
THREE    EQU   3                        NUMERIC CONSTANT         S21903
FOUR     EQU   4                        NUMERIC CONSTANT         S21903
FIVE     EQU   5                        NUMERIC CONSTANT         S21903
SEVEN    EQU   7                        NUMERIC CONSTANT         S21903
EIGHT    EQU   8                        NUMERIC CONSTANT         S21903
HEX80    EQU   X'80'                    EOT CONSTANT           @SA73558
EQUALPRI EQU   X'02'                    DCB EQUAL PRIORITY MASK  S21101
PRIEQ    EQU   X'40'                    LCB STCB EQUAL PRIORITY  S21101
CVT      EQU   16                       ADDRESS OF CVT POINTER   Y02027
CONTTYP3 EQU   X'82'                    2701, IBM TYPE 3 ADAPTER Y02027
BSCTYP2  EQU   X'90'                    BSC ADAPTER, TYPE 2      Y02027
DIALLINE EQU   X'90'                    DIAL LINE                Y02027
CU2703   EQU   X'03'                    2703 CONTROL UNIT        Y02027
GOODINTF EQU   X'20'                    LEGAL INTERFACE          Y02027
GOODTRAN EQU   X'04'                    LEGAL TRANSMISSION CODE  Y02027
PTRIOB   EQU   32                       IOB OFFSET IN LCB        Y02027
TCAMIDLE EQU   32                       OPEN IDLE SWITCH         Y02027
DEBPRFIX EQU   36                       DEB PREFIX LENGTH
OUTPUTM  EQU   X'01'                    OUTPUT FLAG IN DEB       Y02027
OUTONLYM EQU   X'02'                    OUTPUT ONLY FLAG IN DEB  Y02027
NORMAL   EQU   X'02'                    NORMAL OPEN              Y02027
OUTPUT   EQU   X'03'                    OPENED FOR OUTPUT        Y02027
BSCMPDEV EQU   X'07'                    BISYNC MULTIPOINT        Y02027
TCAMER09 EQU   X'09'                    ERROR ID                 Y02027
OUTONLY  EQU   X'0C'                    OPEN OUTPUT ONLY         Y02027
OUTPONLY EQU   X'0F'                    OUTPUT ONLY              Y02027
GRAPHICS EQU   X'10'                    2260 LOCAL               Y02027
DIAL     EQU   X'18'                    DIAL LCB                 Y02027
BUFRDTRM EQU   X'1A'                    BUFFERED TERMINAL        Y02027
SEND     EQU   X'20'                    SEND PRIORITY IN LCB     Y02027
DISABLE  EQU   X'2F'                    DISABLE COMMAND CODE
AUTOPOLL EQU   X'40'                    AUTOPOLL BIT IN THE UCB
BSCADPTR EQU   X'90'                    BISYNC ADAPTER           Y02027
APBIT    EQU   X'01'                    AUTOPOLL BIT IN THE CONTROL
*                                         WORD OF THE INVITATION
*                                         LIST
IDLE     EQU   X'40'                                            X30X5MT
OPENCHK  EQU   16                      OFFSET TO OPEN CHK FLAG  X30X5MT
END      EQU   *
         EJECT
TCAMLD5  DC    C'39'                    ID OF IGG01939
         DC    XL4'0'                   TTR                      S21903
OPENERP  DC    C'33'                    ERROR RECOVERY MODULE
         DC    XL4'0'                     TTR AND LENGTH
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         TTNTD
         EJECT
         TQCBD
         EJECT
         TAVTD                          AVT DSECT
         EJECT
         TTCXD                                                   Y02027
         EJECT
FORCORE  DSECT
         IECDSECT
         EJECT                                                   Y02027
         TLCBD
         TDEBD
         DCBD  DSORG=TX
*
         END
