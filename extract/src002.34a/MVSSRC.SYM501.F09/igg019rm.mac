RM01     TITLE '''IGG019RM'' - TCAM POINT ROUTINE'
IGG019RM CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS:                                        *
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A148000,201000,235800-239000,290800-293200,311000-312000,       Y01004
*A320500-327000,365300-368300,371000-373000,490500-491500,       Y01004
*A545000-550000,773000,830010-830170,836000-837000,              Y01004
*A851000-854000,890300-890800                                    Y01004
*C145000-147000,320000,325000,365000,370000,458000-461000,540000,Y01004
*C555000,565000,780000                                           Y01004
*D330000-335000,375000,379200-385000,395000,430000,505000,525000,Y01004
*D560000,570000,785000-795000                                    Y01004
*A294000,890100-890200,915000                                    X02004
*C365000,366200,366800,830040,830060,831800,832600-833000        X02004
*D811000-813000                                                  X02004
*D376800-378600,695000-765000                                    S21101
*A147000,490000                                                  S22025
*C320000-350000                                                  S22025
*D355000,475000-485000,495000-520000,900000                      S22025
*A492000,495000-515000                                           S22025
*D355000,475000-485000,495000-520000                             S22025
*C32000-350000                                                   S22025
*A147000                                                         S22025
*C335000                                                         S22025
*D900000                                                         S22025
*A012000,660000,940000                                SA56615 / SA56626
*D012700-014700,665000-690000,905000-935000           SA56615 / SA56626
*C695000-755000                         PECB DSECT CHG         @OZ07830
*C095000-110000                                                @OZ14194
         SPACE 4
***********************************************************************
*TITLE: 'IGG019RM' POINT ROUTINE                                      *
*                                                                     *
*  MODULE NAME = IGG019RM                                             *
*                                                                     *
*  DESCRIPTIVE NAME = TCAM POINT ROUTINE                              *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 4                                            *
*                                                                     *
*FUNCTION:  AFTER SAVING THE REGISTERS IN THE USER'S SAVEAREA, A      *
*   TEST IS MADE FOR A REQUEST OF ZERO SEQUENCE NUMBER. IF NON        *
*   ZERO, A PARAMETER LIST IS SET UP IN THE PECB.  AN SVC 102 IS      *
*   ISSUED TO ACTIVATE THE MCP ROUTINE. UPON RETURN, IEDQEB HAS       *
*   SET RETURN CODE IN REGISTER 15.                                   *
*ENTRY POINT:                                                         *
*        IGG019RM                                                     *
*INPUT:                                                               *
*   0-   ADDRESS OF 11-BYTE RETRIEVE DATA                             *
*        0-7: TERMNAME LEFT-ADJUSTED AND PADDED WITH BLANKS FOR       *
*              RETRIEVE INITIATION.                            @OZ14194
*        8:    SEQ NUM TYPE(C'I' FOR INPUT,C'O' FOR OUTPUT,    @OZ14194
*                C'BLANK' FOR RETRIEVE TERMINATION)            @OZ14194
*        9-10: MSG SEQUENCE NUMBER, HI-ORDER BIT ON FOR        @OZ14194
*              MULTIPLE RETRIEVE.
*   1-   DCB ADDRESS                                                  *
*   13-  ADDRESS OF 18-WORD REGISTER SAVEAREA                         *
*   14-  RETURN ADDRESS                                               *
*   15-  ENTRY POINT ADDRESS                                          *
*OUTPUT: RETURN CODE IN REG 15                                        *
*EXTERNAL ROUTINES:                                                   *
*        IEDQEB - AQCTL (SVC 102) ROUTINE                             *
*EXITS-NORMAL: RETURN CODE OF X'00'                                   *
*EXITS-ERROR:  RETURN CODE OF X'04' FOR INVALID SEQUENCE #       S22027
*              RETURN CODE OF X'08' FOR INVALID TERMNAME         S22027
*              RETURN CODE OF X'0C' FOR INVALID QUEUE TYPE       S22027
*TABLES/WORK AREAS:                                                   *
*   DCB                                                               *
*   DEB                                                               *
*                                                                Y01004
*ATTRIBUTES: REENTRANT,REFRESHABLE,PROBLEM PROGRAM MODE               *
*                                                                     *
*NOTES: THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL         *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
RZERO    EQU   0                        ADDRESS OF RETRIEVE INPUT
RPARM    EQU   1                        ADDRESS OF DCB
RWORK    EQU   2                        WORK REGISTER
RDCB     EQU   5                        ADDRESS OF DCB
RDEB     EQU   6                        DATA EXTENT BLOCK
RPECB    EQU   7                        ADDRESS OF PECB          Y02027
RCVT     EQU   8                        SYSTEM CVT ADDRESS.      Y01004
RDWA     EQU   9                        DISPATCHER WORK AREA     Y01004
*                                       ADDRESS.                 Y01004
R10      EQU   10                       GENERAL WORK REGISTER.   Y01004
R11      EQU   11                       WORK REGISTER            X02004
RBASE    EQU   12                       POINT BASE REGISTER
RSAVE    EQU   13                       SAVEAREA REGISTER
RETURN   EQU   14                       RETURN POINT REGISTER
RENTRY   EQU   15                       ENTRY POINT/RETURN CODE REG
         SPACE 2
         USING *,RENTRY                 ADDRESSABILITY.          Y01004
         DC    2H'0'                    PADDING FOR POINT MACRO
IGG019RM IEDHJN SAVEREGS
         STM   RETURN,RBASE,E12(RSAVE)  SAVE CALLER'S REGISTERS. Y01004
         LR    RBASE,RENTRY             GET ENTRY POINT.         Y01004
         USING IGG019RM,RBASE           SET ADDRESSABILITY.      Y01004
         DROP  RENTRY                   DISCARD OLD BASE.        Y01004
         USING IEDQPECB,RPECB           PECB ADDRESSABILITY      Y02027
         LR    RDCB,RPARM               ADDRESS OF DCB.          Y01004
         LR    RWORK,RZERO              ADDRESS OF RETRIEVE DATA Y01004
         USING IHADCB,RDCB              DCB BASE REGISTER
         LA    RENTRY,INVSEQ            POSSIBLE SEQ # ERROR     Y02027
         CLI   EIGHT(RWORK),BLANK       RETRIEVE TERMINATION     Y02027
         BE    CONT                     BRANCH IF YES            Y02027
         ICM   R10,HALF,SEQNO(RWORK)    GET SEQUENCE NUMBER FROM Y01004
*                                       RETRIEVE DATA.           Y01004
         BZ    EXIT                     BRANCH IF SEQ. NUMBER=0  Y02027
CONT     EQU   *                                                 Y02027
         L     RPECB,DCBPECB            GET PECB ADDRESS         Y02027
*                                                                Y02027
*              SET UP PARAMETER LIST                             Y02027
*                                                                Y02027
         LA    R10,PECBECB              GET ECB ADDRESS AND      Y02027
         ST    R10,PECBPARM             STORE IN PARAMETER LIST  Y02027
         MVI   PECBPARM,PTCODE          SET POINT CODE IN PARM   Y02027
*                                       LIST                     Y02027
         LA    RPARM,PECBPARM           SET UP PARAMETER REG     Y02027
         AQCTL                                                   Y02027
EXIT     EQU   *                                                 Y02027
         RETURN (14,12),RC=(15)         RESTORE USER'S REGISTERS Y01004X
                                        AND RETURN CONTROL TO    Y01004X
                                        NSI FOLLOWING POINT      Y01004X
                                        MACRO.                   Y01004
         EJECT
* E Q U A T E S                                                  Y01004
         SPACE
BLANK    EQU   X'40'
INVSEQ   EQU   4                        SEQUENCE NUMBER ERROR    Y02027
PTCODE   EQU   X'98'                    POINT CODE FOR SVC 102   Y02027
EIGHT    EQU   8                        DISPLACEMENT             Y02027
HALF     EQU   3                        HALFWORD ICM/STCM MASK.  Y01004
SEQNO    EQU   9                        OFFSET OF SEQUENCE NO.   Y01004
*                                       IN RETRIEVE DATA.        Y01004
E12      EQU   12                       OFFSET OF TWELVE.        Y01004
         EJECT
*        DCBD  DSORG=PS
         DCBD  DSORG=PS
         EJECT
DCBPECB  EQU   DCBIOBAD                                          Y02027
         TPECBD
         END
