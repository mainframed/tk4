904G     TITLE ' IGE0904G TERMINAL STATISTICS RECORDING MODULE'
IGE0904G CSECT
*                                                                     *
*A 728200-733400,750000,795500-799500                            X01004
*C 108000-111000,676000-678000                                   X01004
*C165000,210000,280000,385000,395000,400000,435000,554000        S21903
*C576000,656000,710000,785000                                    S21903
*C677000                                                        SA58994
*C488000-572000,760000                                         @SA69629
*D580000-620000,710000,820000                                  @SA69629
*A632000                                                       @SA69629
*A731000,762000                                                @ZA03124
***********************************************************************
*                                                                     *
*TITLE -- 'IGE0904G', END OF DAY RECORDING MODULE                     *
*                                                                     *
*STATUS -- CHANGE LEVEL 0                                             *
*                                                                     *
*FUNCTION -- TO INTERFACE WITH THE OBR/SDR MODULES TO RECORD TERMINAL *
*   COUNTERS FOR START/STOP AND BSC DURING CLOSEDOWN.                 *
*                                                                     *
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO REQUEST ELEMENT                                   *
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO REQUEST ELEMENT                               *
*                                                                     *
*EXTERNAL ROUTINE -- IGG019QE (AOS/TCAM SIO APPENDAGE) -- TO CONVERT  *
*   VIRTUAL CCW ADDRESSES TO REAL BEFORE RETRY.                       *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO REQUEST ELEMENT                         *
*        SVC   15    LCBFLAG1  X'E6'  RETRY ERROR                     *
*        SVC   3                                                      *
*                                                                     *
*        SVC   15  LCBFLAG1  X'C2'  RETURN TO LINE END ERROR CLEARED  *
*        SVC   3                                                      *
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L     14,X'10'   CVT ADDRESS                           S21903*
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO REQUEST ELEMENT                          *
*        SVC   15  LCBFLAG1 X'C6'                                     *
*        SVC   3                                                      *
*                                                                     *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L     14,X'10'   CVT ADDRESS                           S21903*
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*   TO SCHEDULE ERROR POST                                            *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TTRMD                                                             *
*   TLCBD                                                             *
*                                                                     *
*ATTRIBUTES -- RE-USABLE, REFRESHABLE, SUPERVISOR MODE, DISABLED      *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
*   PARAMETERS PASSED TO OBR/SDR RECORDING MODULE:                    *
*                                                                     *
*   LCBERCCW+16 - 1 BYTE X'40' INDICATES END OF DAY RECORDING         *
*   LCBERCCW+17 - 3 BYTES - ADDRESS OF TERMINAL COUNTERS              *
*   LCBERCCW+20 - 1 BYTE SIZE OF TERMINAL NAME                        *
*   LCBERCCW+21 - 3 BYTES - ADDRESS OF TERMINAL NAME                  *
*                                                                     *
***********************************************************************
         EJECT
         USING LCBFLAG1,RLCB
         USING IEDQAVTD,RAVT
         USING IEDQTRM,RTERM
         USING *,R15
         USING IOSB,RIOSB                                        Y02027
         USING EWA,RERPWA                                        Y02027
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RLCB     EQU   2                        LCB BASE
RA       EQU   3                        WORK REGISTER            S21903
RC       EQU   5                        WORK REGISTER            S21903
RWKB     EQU   6                        WORK REGISTER            S21903
RB       EQU   7                        WORK REGISTER
RTST     EQU   7                        ADDR OF REQUEST QUEUE    Y02027
*                                       ELEMENT                  Y02027
RLINK    EQU   13                       LINKAGE REGISTER         Y02027
RTERM    EQU   8                        TERMINAL BASE
RWKC     EQU   9                        WORK REGISTER
RD       EQU   10                       WORK REGISTER
RERPWA   EQU   11                       ADDR OF ERP WORK AREA    Y02027
RAVT     EQU   12                       AVT BASE
RXCTL    EQU   14                       XCTL REGISTER
R15      EQU   15                       MODULE BASE REGISTER     S21903
         EJECT
IGE0904G IEDHJN  ENTER
*
*  OPRN ISSUES SIO ON A NO-OP WITH A SPECIAL TPOP CODE AND SETS
* LCBINCAM+1=X'FF', LCBERCCW=F'0'
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         L     RERPWA,IOSERP            PICKUP ADDR OF ERP WORK  Y02027
*                                       AREA                     Y02027
         L     RLCB,IOBOFFST(,RTST)     SET BASE FOR LCB
         L     RAVT,CVTPTR              A LOCATION OF CVT ADDRESS
         LR    RXCTL,RAVT               COPY FOR EXIT
         L     RAVT,AVTCVTPT(,RAVT)     CVT LOAD LIST
         L     RAVT,0(,RAVT)            LOAD THE ADDRESS OF THE AVT
         L     RB,AVTRNMPT              GET TNT BASE
         USING IEDQTNTD,RB
         ICM   RWKC,ALL,LCBERCCW        GET TNT OFFSET         @SA69629
         BZ    TRYNEXT                  RECORDING ALREADY DONE @SA69629
         BAL   RWKB,TNTDCODE            LINK TO GET ADDRESS      S21903
         LA    RWKC,79(RB,RWKC)         ADJUST FOR ADDRESS OF NAME
         SR    RC,RC                    CLEAR WORK REGISTER
         ST    RC,LCBERCCW              CLEAR TO INDICATE      @SA69629
*                                       RECORDING DONE FOR     @SA69629
*                                       REENTRY TO 904G        @SA69629
         IC    RC,TNTENLEN              GET SIZE OF EACH NAME
         SR    RWKC,RC                  BACK UP TO ADDRESS OF NAME
         MVI   EWTCTRM,BLANK                                     Y02027
         MVC   EWTCTRM+ONE(SEVEN),EWTCTRM BLANK OUT TERMINAL     Y02027
*                                       NAME FIELD IN EWA        Y02027
         BCTR  RC,ZERO                  REDUCE NAME CONT BY ONE  Y02027
*                                       FOR EXECUTE INSTRUCTION  Y02027
         EX    RC,MOVENAME              MOVE THE TERMINAL NAME   Y02027
*                                       TO THE ERP WORK AREA     Y02027
         MVC   EWTCSIO(THREE),TRMSIO    MOVE START I/O AND TEMP  Y02027
*                                       ERROR COUNTERS TO THE    Y02027
*                                       ERP WORK AREA            Y02027
         XC    TRMSIO(THREE),TRMSIO     CLEAR ERROR COUNTERS IN  Y02027
*                                       THE TERMINAL TABLE       Y02027
         OI    EWTCFLG,EWTCEOD          INDICATE END-OF-DAY      Y02027
*                                       RECORDEING FOR OBR/SDR   Y02027
         LA    RWKC,EWTCSIO             INDICATE ADDR OF START   Y02027
*                                       DATA TO OBR/SDR DATA     Y02027
         STCM  RWKC,SEVEN,EWADDISP      DEVICE DEPENDENT INFO    Y02027
*                                       FOR OBR/SDR              Y02027
         MVI   EWADCNT,OBRDATA          INDICATE SIZE OF DEVICE  Y02027
*                                       DEPENDENT DATA FOR OBR   Y02027
         OI    IOSFLB,IOSLOG            SET RECORDING FLAG FOR   X01004
*                                       OBR.                     X01004
         LA    RLINK,SDRLOAD            MODULE ID
         L     RXCTL,XCTLADD(,RXCTL)    XCTL ADDRESS
         BR    RXCTL                    LINK TO IT
         SPACE      5
TRYNEXT  EQU   *
         SPACE
         XC    LCBTPCD+TEN(TWO),LCBTPCD+TEN CLEAR TP OP CODES    Y02027
*                                       FOR LINEEND APPENDAGE    Y02027
         MVI   LCBINCAM+1,ZERO          CLEAR SPECIAL REQUEST    Y02027
*                                       FLAG FOR LINEEND         Y02027
         LA    RWKC,LCBCPA+EIGHT        GET CCW ADDRESS TO BE    Y02027
*                                       USED BY LINE END         Y02027
         STCM  RWKC,SEVEN,IOSCSW        SET IN IOSB FOR LINE END Y02027
         NI    IOSFLA,AVTEFF-IOSERR     POST PERMANENT ERROR TO  Y02027
*                                       ABNORMAL END APPENDAGE   Y02027
         NI    IOSCC,AVTEFF-IOSCC3      CLEAR SIO COND. CODE   @ZA03124
         NI    IOSTATUS,CEDE            CLEAR ALL STATUS EXCEPT@ZA03124
         MVI   IOSTATUS+ONE,ZERO        CHAN. END AND DEV. ENDT@ZA03124
         SVC   EREXCP                   FOR LINE END TO POST ECB
         SPACE
         SVC   RETURN
         SPACE
*        EXECUTE INSTRUCTIONS
         SPACE
MOVENAME MVC   EWTCTRM(ZERO),ZERO(RWKC) MOVE TERMINAL NAME INTO  Y02027
*                                       THE ERP WORK AREA        Y02027
         EJECT
IOBOFFST EQU   4                        OFFSET TO IOB IN RQE
ALL      EQU   15                       ICM/STCM MASK          @SA69629
DEBOFFST EQU   8                        OFFSET TO DEB IN RQE   @SA69629
CEDE     EQU   X'0C'                    CHAN. AND DEV. END MASK@ZA03124
SDRLOAD  EQU   256                      SDR MODULE ID
CVTPTR   EQU   16                       LOCATION OF CVT ADDRESS
EREXCP   EQU   15                       SVC MASK
RETURN   EQU   3                        SVC MASK
XCTLADD  EQU   X'2C'                    CVT OFFSET TO XCTL ADR   S21903
BLANK    EQU   X'40'                                             Y02027
ZERO     EQU   0                                                 Y02027
ONE      EQU   1                                                 Y02027
THREE    EQU   3                                                 Y02027
SEVEN    EQU   7                                                 Y02027
TWO      EQU   2                        LENGTH TO CLEAR LCBTPCD  Y02027
EIGHT    EQU   8                        CONSTANT OF EIGHT        Y02027
TEN      EQU   10                       OFFSET TO CLEAR LCBTPCD  Y02027
OBRDATA  EQU   18                       SIZE OF DEVICE DEPENDENT Y02027
*                                       DATA FOR OBR/SDR         Y02027
         TTNTD
X01      EQU   X'01'                    RECORDING MASK SET OFR OBR
         EJECT
         TLCBD
         TAVTD
         TTRMD
         IECDIOSB                                                Y02027
         IECDERWA EWTC
         END
