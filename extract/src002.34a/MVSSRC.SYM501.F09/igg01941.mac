         TITLE 'IGG01941-OPEN CHECKPOINT DATA SET'
IGG01941 CSECT
*A-000000-999999                                               @X31X8N0
*A277000,294800,725300                                         @OZ31115
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C224000,756000                                                  CLUP21
*D426000-454000,766000-770000                                    CLUP21
*C012000,214000,218000,220000,224000,694000,                     S21903
*C764000-776000,784000-790000                                    S21903
*A716000                                                         Y01004
*C316000                                                         Y01004
*D660000,718000                                                  Y01948
*C613000                                                        SA62397
*C016000,082000,086000,218000-232000,316000,352000               Y02027
*D083000,087000,756000-764000,788000,790000                      Y02027
*A139000,214000-216000,292200-293600,724450,745300,725600        Y02027
*A794700,796800,797200                                           Y02027
*C574000                                                       @SA72448
*A607400                                                       @SA72448
*A038000                                                       @Z30X8NM
*C016000,241200,494000,540000,614000                           @Z30X8NM
*D240200-240800,241000-241200,312300-313500,496100-497100      @Z30X8NM
*D542600-543200,638200-639200,770000,776000,782000-786000      @Z30X8NM
*A293200                                                       @OZ11174
*            TCAM LEVEL 10.0 CHANGES                           @G36XRNP
*A512000,542000,714000,240000,278000                           @G36XRNP
*C278000                                                       @G36XRNP
*D514000,498000-500000,640000-644000                           @G36XRNP
*                                                              @G36XRNP
         SPACE 3
***********************************************************************
*                                                                     *
*MODULE-NAME = IGG01941 (TCAM,INITIALIZATION)                  @G36XRNP
*                                                                S21903
*DESCRIPTIVE-NAME = OPEN CHECKPOINT DATA SET-FIRST LOAD          S21903
*                                                                S21903
*COPYRIGHTS = 'NONE'                                             S21903
*                                                                     *
*  STATUS = CHANGE LEVEL 10.0                                  @G36XRNP
*                                                                     *
*FUNCTION:THIS MODULE DETERMINES THE SIZE OF THE GETMAIN WORK AREA    *
*   (THE SIZE VARIES AS A RESULT OF THE INTR OPERANDS 'CKREQS' AND    *
*   'CPRCDS'.),ISSUES A GETMAIN,AND PUTS THE ADDRESS IN AVTCKGET.     *
*   IT DETERMINES THE BEGINNING OF THE CKREQ-TTR TABLE.               *
*   IT INITIALIZES THE IOB AND DISK CHANNEL PROGRAM(IN THE WORK       *
*   AREA).  IT DETERMINES THE TYPE OF START-UP (COLD,WARM,            *
*   CONTINUATION) REQUIRED BY INVESTIGATING THE DISPOSITION,THE       *
*   START PARAMETERS(ON INTRO) AND THE NORMAL CLOSEDOWN BIT IN THE    *
*   CONTROL RECORD.  DEPENDING ON THESE RESULTS,IT XCTLS TO EITHER    *
*   THE COLD START MODULE OR THE RESTART MODULE.                      *
*   NO OPEN PROCESSING IS PERFORMED FOR 3705 CHECKPOINT DCBS.  @Z30X8NM
*                                                                     *
*   DISP=NEW -FORMAT DISK                                             *
*   DISP=OLD                                                          *
*    A.S=C                                                            *
*      1.NORMAL CLOSEDOWN -FORMAT DISK                                *
*      2.ABNORMAL CLOSEDOWN -RESTART FROM CHECKPOINT RECORDS-SCAN     *
*        MESSAGE QUENES                                               *
*    B.S=W                                                            *
*      1.NORMAL CLOSEDOWN -RESTART FROM CHECKPOINT RECORDS-DO NOT     *
*        SCAN QUENES                                                  *
*      2.ABNORMAL CLOSEDOWN -RESTART FROM CHECKPOINT RECORDS-SCAN     *
*        QUENES                                                       *
*    C.S=CY-FORMAT THE DISK                                           *
*    D.S=WY-RESTART FROM CHECKPOINT RECORDS-DO NOT SCAN QUEUES        *
*                                                                     *
*ENTRY POINT                                                          *
*                                                                     *
*        IGG01941                                                     *
*                                                              @G36XRNP
*PURPOSE = SEE FUNCTION                                        @G36XRNP
*                                                              @G36XRNP
*LINKAGE = VIA XCTL                                            @G36XRNP
*                                                                     *
*INPUT: REGISTERS 5,6,7,8 CONTAIN THE FOLLOWING:                      *
*                                                                     *
*   5-ADDRESS OF THE WHERE-TO-GO TABLE PREFIX                    Y02027
*   6-ADDRESS OF BEGINNING OF WHERE-TO-GO TABLE                       *
*   7-ADDRESS OF THE COPY OF THE CURRENT DCB                     Y02027
*   8-ADDRESS OF CURRENT ENTRY IN WHERE-TO-GO TABLE                   *
*                                                                     *
*OUTPUT                                                               *
*                                                                     *
*   REGISTERS 5 AND 6 ARE SAME AS INPUT                               *
*   REGISTERS 7 AND 8 ARE UPDATED TO THE NEXT ENTRIES                 *
*   THE CHECKPOINT WORK AREA IS OBTAINED VIA A GETMAIN AND            *
*   INITIALIZED.                                                      *
*                                                                     *
*EXTERNAL ROUTINES: NONE                                              *
*                                                                     *
*EXITS-NORMAL:DEPENDING UPON THE DISPOSITION,START-UP PARAMETERS,     *
*   AND NORMAL CLOSEDOWN BIT DESCRIBED ABOVE,THIS MODULE XCTLS TO     *
*   ONE OF THE FOLLOWING MODULES.                                     *
*        IGG01949-COLD START                                          *
*        IGG01943-WARM START OR CONTINUATION                          *
*                                                                     *
*EXITS-ERROR:IF THIS MODULE DETERMINES AN ERROR,IT SENDS A MESSAGE    *
*   VIA WTO AND XCTLS TO THE NEXT MODULE IN THE WHERE TO GO TABLE.    *
*   THIS MODULE RECOGNIZED THE FOLLOWING ERRORS:                      *
*   1.INSUFFICIENT CORE FOR CHECKPOINT WORK AREA                      *
*   2.DISK ERROR WHEN READING CHECKPOINT CONTROL RECORD               *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   CVT                                                               *
*   TCX        TCAM CVT EXTENSION                                Y02027
*   AVT (AVTNCKPR,AVTCPRCD,AVTCKGET,AVTBIT3                           *
*   DCB-CHECKPOINT                                                    *
*   DEB-CHECKPOINT                                                    *
*   CHECKPOINT WORK AREA                                              *
*   I/O WORKAREA                                                      *
*   JFCB                                                              *
*                                                                     *
*ATTRIBUTES:RE-ENTRANT                                                *
*                                                                     *
*NOTES:THE OPERATION OF THES MODULE DEPENDS UPON AN INTERNAL REPRE-   *
*   SENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO    *
*   THE ONE USED AT ASSEMBLE TIME. THE CODING HAS BEEN ARRANGED SO    *
*   THAT REDEFINITION OF 'CHARACTER' CONSTANTS,BY REASSEMBLY,WILL     *
*   RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.               *
*                                                                     *
*   DEPENDENCIES = NONE                                        @G36XRNP
*                                                              @G36XRNP
*   RESTRICTIONS = NONE                                        @G36XRNP
*                                                              @G36XRNP
*   REGISTER CONVENTIONS = SEE REGISTER EQUATES                @G36XRNP
*                                                              @G36XRNP
*   PATCH LABEL = NONE                                         @G36XRNP
*                                                              @G36XRNP
*MODULE TYPE = PROCEDURE                                       @G36XRNP
*                                                              @G36XRNP
*   PROCESSOR = ASSEMBLER                                      @G36XRNP
***********************************************************************
         SPACE 3
*REGISTER ASSIGNMENTS
         SPACE
R0       EQU   0                        REG ZERO                 S22024
R1       EQU   1                        REG ONE                  S22024
R2       EQU   2                        ADDRESS OF CKPT WORK AREA
R3       EQU   3                        ADDRESS OF I/O WORK AREA
R4       EQU   4                        ADDRESS OF JFCB
R5       EQU   5                        INPUT-ADDR OF 1ST ENTRY-DCB
R6       EQU   6                        INPUT-ADDR OF DEGINNING-WTG
R7       EQU   7                        INPUT-ADDRESS OF DCB ADDR
R8       EQU   8                        INPUT-CURRENT ENTRY IN WTG
RAVT     EQU   9                        ADDRESS OF AVT
RDCB     EQU   10                       ADDRESS OF DCB
R11      EQU   11                       ADDR OF GETMAIN FOR RESTART
RBASE    EQU   12                       BASE REG
R13      EQU   13                       WORK REG                 S22024
R14      EQU   14                       ADDRESS OF DEB
R15      EQU   15                       WORK REG                 S22024
         SPACE 3
         USING IHADCB,RDCB              DCB DSECT                Y02027
         USING IEDQAVTD,RAVT            AVT DSECT                Y02027
         USING FORCORE,R4               OPEN WORK AREA DSECT     Y02027
         SPACE 2
         USING IGG01941,R15             SET TEMPORARY BASE       Y02027
         BAL   RBASE,QMA00              SET BASE REGISTER        Y02027
         DROP  R15                      DROP 15 AS BASE          Y02027
         USING *,RBASE                  ESTABLISH ADDRESSABILITY Y02027
         SPACE 2
         DC    C'IGG019'                MODULE                   Y02027
AMIDCNST DC    C'41'                       IDENTIFIER            Y02027
         IEDHJN ,                       SET THE DATE             Y02027
QMA00    EQU   *                                                 Y02027
         SPACE 2
         L     R4,FOUR(R8)              ADDR OF OPEN WORK AREA   Y02027
         L     RDCB,DXUDCBAD            ADDR OF THE CURRENT DCB  Y02027
         L     RAVT,DCBIOBAD            ADDRESS OF AVT
         SPACE 3
*FIGURE SIZE OF GETMAIN WORK AREA NEEDED-DEPENDS ON INTRO OPERAND:
*CKREQS= AND CPRCDS=.
         SPACE 2
         LA    R4,CKPLNGTH+CKPTTRLN     SET MINIMUM AREA SIZE  @G36XRNP
         LA    R2,AVTDOUBL              SET ADDR FOR GETMAIN   @G36XRNP
*                                       TO PUT AREA ADDRESS    @G36XRNP
         TM    DCBOPTCD,CKP3705         IS THIS 3705 CKPT DCB  @G36XRNP
         BO    QMA12                    YES, SET WORKAREA LEN  @G36XRNP
         SPACE
         SR    R1,R1                    CLEAR
         IC    R1,AVTNCKPR              GET NO. OF CKREQ QUEUES
         LTR   R0,R1                    CHECK FOR CKREQ=0        S22024
         BZ    QMA10                    BRANCH IF ZERO
         LA    R1,CKPCKRNO(0,R1)        GET NO. OF EXTRA RECORDS       *
                                        (TO USE IN CASE OF DISK ER)
         LA    R0,CKPCTTRL              LENGTH OF CKREQ TTR TABLE      *
                                        ENTRY
         MR    R0,R0                    FIND NO. BYTES FOR CKREQ       *
                                        TTR TABLE
QMA10    EQU   *
         LA    R4,CKPLNGTH(0,R1)        LENGTH OF FIXED WORK AREA
         IC    R0,AVTCPRCD              GET NO. ENV RCDS REQUESTED
         LA    R1,CKPTTRLN              LENGTH FOR EACH TTR 0415 S21101
         MR    R0,R0                    GET LENGTH FOR ALL RCDS
         AR    R4,R1                    ADD TO TOTAL LENGTH
         LR    R3,R1                    SAVE LENGTH
QMA12    EQU   *                                               @G36XRNP
         LA    R4,TRACELEN(R4)          ADD CKPT TRACE LENGTH  @OZ31115
         LR    R1,R4                    COPY LENGTH                0731
         STM   R1,R2,AVTGETMN           SET UP GETMAIN PARM        0731
         SPACE 3
         GETMAIN EC,MF=(E,AVTGETMN),SP=250 CKPT WORK AREA          0731
         SPACE 3
         SR    R2,R2                    CLEAR FOR GET FAILURE505 S21101
         LTR   R15,R15                  CHECK RETURN CODE
         BNZ   QMA41                    BRANCH IF NO CORE          0115
         L     R2,CVT                   GET THE CVT ADDRESS      Y02027
         L     R2,AVTCVTPT(R2)          GET THE TCX ADDRESS      Y02027
         SPACE 2
         USING IEDQTCX,R2               TCX DSECT                Y02027
         OI    TCXCKPM,CKPTFLG          TURN ON 'CHECKPOINT IN   Y02027
*                                         SYSTEM' FLAG           Y02027
         MVC   TCXNCKPR,AVTNCKPR        NO. OF CHECK REQUESTS  @OZ11174
         SPACE 2
         DROP  R2                                                Y02027
         L     R2,AVTGETMN+FOUR         GET POINTER TO ADDRESS   S22024
*                                        OF GOTTEN CORE          S22024
         L     R2,ZERO(R2)              GET WORKAREA ADDRESS     S22024
         LR    R15,R4                   SAVE LENGTH            @OZ31115
         SH    R15,LESSLEN              OFFSET TO CKPT TRACE   @OZ31115
         AR    R15,R2                   START OF CKPT TRACE    @OZ31115
         XC    ZERO(TRACELEN,R15),ZERO(R15) CLEAR TRACE TABLE  @OZ31115
         MVI   ONE(R15),TRACELEN        SET UP TABLE LENGTH    @OZ31115
         MVI   THREE(R15),FOUR          SET UP FIRST OFFSET    @OZ31115
         ST    R2,DCBIOBAD              SET WKA ADDRESS IN DCB   S22024
         MVI   DCBEIOBX,CKPIOB-CKPSAVE1 OFFSET TO IOB IN WKA     S22024
         USING IEDQCKPD,R2              BASE FOR CKP
         SPACE 3
*INITIALIZE CHECKPOINT WORK AREA
         SPACE
         XC    CKPSAVE1(256),CKPSAVE1   CLEAR 1ST PART OF WORK AREA
         XC    CKPSAVE1+AVTEFF(CKPLNGTH-AVTEFF),CKPSAVE1+AVTEFF  S21101
         MVI   CKPMSGLN+1,CKPMSGL       LENGTH OF ERROR MSG
         STH   R4,CKPWKALN              SAVE TOTAL LENGTH FOR CLOSE
         LA    R3,CKPCREND-CKPCNTLR(0,R3) LENGTH OF C ONTROL RCD
         TM    DCBOPTCD,CKP3705         IS THIS 3705 CKPT DCB  @G36XRNP
         BZ    QMA13                    NO, BRANCH             @G36XRNP
         LA    R3,CDRNCTLL              SET UP CONTROL RECORD  @G36XRNP
QMA13    EQU   *                                               @G36XRNP
         STC   R3,CKPCRLEN              SAVE LENGTH
         L     R14,DCBDEBAD             GET THE DEB ADDRESS      Y02027
         LA    R15,CKPIOM               RESULT FROM CONVERSION RTN
         STM   R14,R15,CKPCPARM         SAVE FOR NEXT LOAD OF OPEN
         MVI   CKPSWCH1,CKPSET1         INITIALIZE QCB SWITCH      0804
         MVI   CKPSWCH2,CKPSET2         INITIALIZE INVLI SWITCH    0804
         SPACE
         SPACE 3
*FIGURE BEGINNING OF CKREQ-TTR TABLE
         SPACE
         LA    R1,CKPCNTLR(R3)          END OF CONTROL RCD
         ST    R1,CKPCTTRB              BEGINNING OF CDREQ TABLE
         SPACE 3
*SET UP IOB IN CKPT WORK AREA
         SPACE
         LA    R0,CKPECB                GET ADDRESS OF ECB
         ST    R0,CKPIOECB              PUT ADDR IN IOB
         OI    CKPIOFL1,CKPCMDCH        INDICATE COMMAND CHAINING
         LA    R0,CKPCCWS               ADDRESS OF CHANNEL PROG
         LR    R1,RDCB                  COPY THE DCB ADDRESS     Y02027
         STM   R0,R1,CKPIOCPA-1         ADDR CPA AND DCB IN IOB    0731
         USING IEDQDEB,R14              BASE
         MVC   CKPIOM,DEBBINUM+1        BIN NUMBER
         MVC   CKPIOCC(4),DEBSTRCC      MOVE CYLINDER AND TRACK
         MVI   CKPIOR,1                 RECORD NO. 1
         SPACE 3
*SET UP CHANNEL PROGRAM-INITIALIZED TO READ THE CONTROL RECORD
         SPACE
         MVC   CKPCCWS(CKPCCWND-CKPCCWS),IEDQMACP MOVE CCW'S TO  S21101X
                                        CKPT WORK AREA      0415 S21101
         LA    R0,CKPIOBB               SEEK ADDR
         O     R0,IEDQMASC              SET SEEK CYL CMD CDE     S22024
         ST    R0,CKPSEEKC              STORE SEEK ADDR          S22024
         SPACE
         LA    R0,CKPIOCC               SEARCH ID ADDR
         O     R0,IEDQMASI              SET SRCH ID EQUAL CMD    S22024
         ST    R0,CKPSCHID              STORE SRCH ID ADDR       S22024
         SPACE
         LA    R0,CKPSCHID              TIC ADDRESS
         O     R0,IEDQMATI              SET TIC CMD CODE         S22024
         ST    R0,CKPTIC                STORE TIC ADDR           S22024
         SPACE
         LA    R0,CKPCNTLR              ADDRESS OF CONTROL RCD
         O     R0,IEDQMARC              SET CONTROL CMD CODE     S22024
         ST    R0,CKPRW                 STORE CONTROL REC ADDR   S22024
         STH   R3,CKPRW+6               LENGTH OF CONTROL RECORD
         L     R1,DEBUCBAD-1            GET UCB ADDRESS     0415 SI1101
         USING IEDQUCB,R1               BASE FOR UCB        0415 SI1101
         TM    UCBTBYT2,UCBRPS          CHECK FOR RPS FEATURE415 S21101
         B     QMA15                    BRANCH TO CRIPPLE MERLINS21101
*                                       SHOULD BE  BRANCH ZERO
         MVI   CKPSETSC+7,1             PUT COUNT INTO CCW  0415 S21101
         IC    R0,UCBTBYT4              GET UCB INDEX       0415 S21101
         LA    R1,CKPSECTR              ADDR OF SECTOR ID        S21101
         SLL   R1,CKP1BYTE              GET ADDR IN LOW 3BYTE419 S21101
         SRDL  R0,CKP1BYTE              PUT UCB INDEX,ADDR IN419 S21101X
                                        REGISTER FOR SECTOR CNVRTS21101
         ST    R1,CKPPARM2              SAVE PARM FOR OTHERS0415 S21101
         LA    R1,CKPSECTR              ADDR OF SECTOR ID   0415 S21101
         O     R1,QMASETSC              GET CCW             0415 S21101
         ST    R1,CKPSETSC              SET UP CCW          0415 S21101
         SPACE 3
*FILL IN APPENDAGE TABLE IN DEB PREFIX
         SPACE
QMA15    EQU   *                                            0415 S21101
         SH    R14,QMATHSIX             GET BEGINNING OF DEB PREFIX
         L     R3,16                    ADDRESS OF CVT
         LA    R0,82(R3)                ADDRESS OF BR 14 INSTR
         LR    R15,R0
         STM   R15,R0,0(R14)            PUT ADDR OF INSTRUCTION        X
                                        INTO DEB PREFIX
         ST    R15,16(R14)
         SPACE 3
*GET JFCB FROM I/O WORK AREA
         SPACE
         L     R4,4(0,R8)               GET ADDRESS OF I/O WORKAREA0731
         TM    JFCBIND2,X'C0'           CHECK DISPOSITION
         BO    QMA40                    BRANCH IF NEW (FORMAT)
         SPACE
         TM    AVTBIT3,AVTSTAYN+AVTSTACN CHECK FOR COLD START WITH     X
                                        NO CONTINUATION
         BO    QMA40                    BRANCH IF COLD,NO CONTINUAT
         SPACE 3
*SET UP TO READ CONTROL RECORD TO CHECK NORMAL CLOSEDOWN
*ISSUE EXCP AND WAIT UNTIL I/O IS COMPLETE
         SPACE
         EXCP  CKPIOB
         WAIT  ECB=CKPECB
         TM    CKPECB,X'7F'             CHECK RETURN CODE
         BO    QMA20                    BRANCH IF NO ERROR         0819
         MVC   0(TWO,R8),QMAERR         ID FOR IGG01942        @Z30X8NM
         OI    CKPFLAGS,CKPERR          TURN ON ERROR INDICATOR    0819
         B     QMA51                    XCTL TO NEXT MODULE        0819
QMA20    EQU   *                                                   0819
         MVI   CKPECB,0                 CLEAR ECB
         SPACE 3
*CHECK FOR TYPE OF START
         SPACE
         LA    R14,CKPTTRCT             SET FOR TCAM CHECKPOINT@G36XRNP
         TM    DCBOPTCD,CKP3705         3705 CKPOINT DATA SET  @G36XRNP
         BZ    CKZERO                   NO, CHECK FOR ZERO     @G36XRNP
*                                       RECORD                 @G36XRNP
         LA    R14,CKPIPLTX             SET FOR 3705 CHECKPOINT@G36XRNP
CKZERO   EQU   *                                               @G36XRNP
         CLI   AVTEZERO(R14),AVTEZERO   CHECK FOR ZEROED       @G36XRNP
*                                       RECORD                 @G36XRNP
         BE    QMA40                    BRANCH IF NO DATA IN RCD
         TM    AVTBIT3,AVTSTACN         CHECK FOR COLD START
         BZ    QMA30                    BRANCH IF WARM (RESTART)
         SPACE 3
*CHECK TYPE OF CLOSEDOWN
         SPACE
         TM    CKPCNTLR,CKPNORML        CK FOR NORMAL CLOSE DOWN S22024
         BO    QMA40                    BRANCH IF NORMAL
         SPACE 3
*A WARM START OR CONTINUATION IS NEEDED
         SPACE
QMA30    EQU   *
         MVC   0(TWO,R8),QMAWARM        ID FOR IGG01943        @G36XRNP
*                                       CONTINUATION START LOAD@G36XRNP
         TM    DCBOPTCD,CKP3705         3705 CHECKPOINT DATA   @G36XRNP
*                                       SET                    @G36XRNP
         BO    QMA80                    MARK OPEN COMPLETE     @G36XRNP
         SPACE 3
*PERFORM INITIALIZATION FOR NEXT LOAD
         SPACE
         LH    R14,CKPBPERR             LENGTH OF ENVIRON RCD SEG
         CH    R14,CKPINCLN             COMPARE WITH INCDNT 0223 S21101
         BH    QMA32                    BRANCH IF INC LOW   0223 S21101
         LH    R14,CKPINCLN             USE INCIDENT LENGTH 0223 S21101
QMA32    EQU   *                                            0223 S21101
         CH    R14,CKPCKRLN             COMPARE WITH CKREQ L0223 S21101
         BH    QMA33                    BRANCH IF CKREQ LOW 0223 S21101
         LH    R14,CKPCKRLN             USE CKREQ LENGTH    0223 S21101
QMA33    EQU   *                                            0223 S21101
         LA    R3,CKPEXCP               ADDR FOR GETMAIN RCK
         GETMAIN EC,LV=(R14),A=(R3),MF=(E,CKPGETML),SP=250 I/O BFR 0806
         SPACE
         LTR   R15,R15                  CHECK RETURM CODE
         BNZ   QMA42                    BRANCH IF NO CORE AVAIL
         CLC   CKPCKRQS,AVTNCKPR        CHECK VALUE OF CKREQS-INTRO0819
         BNH   QMA35                    BRANCH IF COLD START VALUE 0819X
                                        IS NOT MORE THAN WARM START0819
         MVC   CKPCKRQS,AVTNCKPR        USE THE SMALLER VALUE      0819
QMA35    EQU   *                                                   0819
         CLC   CKPCPRCD,AVTCPRCD        CHECK VALUE OF CPRCDS-INTRO0819
         BNE   QMA39                    BR IF COLD AND WARM    @SA72448
*                                        START VALUES DIFFER   @SA72448
QMA37    EQU   *                                                   0819
         MVC   CKPRW+1(3),CKPEXCP+1     ADDR OR AREA TO READ INTO
         STH   R14,CKPRW+6              PUT READ LENGTH INTO CCW
         OI    AVTBIT2,AVTSTRTN         TURN ON RESTART IN PROGRESS
         SR    R4,R4                    CLEAR
         IC    R4,CKPCPRCD              GET NO. ENV RECORDS
         IC    R15,CKPTTRCT             INDEX TO CURRENT ENV TTR
         CR    R15,R4                   CHECK CURRENT RCD WITH TOTL0125
         BH    QMA39                    BRANCH IF CURRNT IS UNAVAIL0125
         L     R1,CVTPTR                GET CVT ADDRESS          S22024
         L     R1,CVTSVDCB(R1)          GET SVCLIB DCB ADDRESS   S22024
         LA    R0,QMASUB                GET SUBROUTINE NAME ADDR S22024
         LOAD  EPLOC=(0),DCB=(1)        LOAD Q8 SUBROUTINE       S22024
         ST    R0,CKPSAVE1+8            SAVE ADDR FOR NEXT LOAD  S22024
         SR    R0,R0                    CLEAR
         IC    R0,AVTCKRST              GET RCD FOR RESTART
         NI    AVTBIT3,X'FF'-AVTSTAIN   TURN OFF S=I INDICATOR     0817
         TM    CKPFLAGS,CKPSTAIN        CHECK FOR S=I FROM LAST TIM0817
         BZ    QMA51                    BRANCH IF NOT SPECIFIED    0817
         OI    AVTBIT3,AVTSTAIN         MAKE AVT REFLECT LAST RUN  0817
         B     QMA51                    XCTL TO NEXT LOAD
         SPACE 3
*ERROR CONDITION-INDEX TO THE CURRENT ENVIRONMENT RCD IS LARGER
*THAN THE TOTAL COUNT OF ENVIRONMENT RECORDS AVAILABLE. USER PROBABLY
*SPECIFIED A SMALL VALUE FOR CPRCDS OPERAND AT RESTART TIME.
         SPACE
QMA39    EQU   *                                                   0125
         OI    CKPFLAGS,CKPERR          SET FLAG FOR IGG01942 TO   0125X
                                        ISSUE ERROR MSG            0125
         MVC   0(5,R8),QMAERR           ID,TTR OF ERROR MODULE @SA72448
         B     QMA51                    BR TO XCTL TO 1942     @SA72448
*A COLD START IS NEEDED-FORMAT THE DISK
         SPACE
QMA40    EQU   *
         MVI   CKPFLAGS,AVTEZERO        RESET FLAGS             SA62397
         MVC   0(TWO,R8),QMACOLD        ID FOR IGG01949        @Z30X8NM
         B     QMA51                    XCTL TO NEXT LOAD
         SPACE 3
*GETMAIN COULD NOT BE SATISFIED-WTO DATA SET IS NOT OPENED
         SPACE
QMA42    EQU   *
         CVD   R14,CKPCNVRT             CONVERT LENGTH OF GETMAIN  0819
         UNPK  QMAMSGGL(4),CKPCNVRT(8)  PUT LENGTH INTO MSG        0819
         OI    QMAMSGGL+3,X'F0'         CLEAR SIGN                 0819
QMA41    EQU   *                                            0505 S21101
         MVC   AVTSAVE4(70),QMAMSG      MOVE MSG OUT OF TRANSNT ARA0115
         L     R1,AVTTCB                GET TCB ADDRESS          S22024
         L     R1,TCBTIO(R1)            GET TIOT ADDRESS         S22024
         AH    R1,DCBTIOT               GET TIOT ENTRY ADDRESS   S22024
         MVC   AVTSAVE4+DDNAME(DDL),TIOEDDNM(R1) PUT DDNAME IN   S22024
*                                        MESSAGE                 S22024
         LA    R1,AVTSAVE4              ADDRESS OF MSG             0115
         WTO   MF=(E,(1)),ROUTCDE=11,DESC=4
         SPACE 3
*OPEN PROVIDES SECTION TO SET UP XCTL TO NEXT MODULE
         SPACE
QMA50    EQU   *
         XC    0(2,R8),0(R8)            CLEAR TO INDICATE COMPLETE
QMA51    EQU   *
         STM   R14,R11,CKPSAVE1+12      SAVE REGS FOR NEXT LOAD  S22024
QMA55    LA    R8,8(R8)                 INCREMENT CURRENT WTO ENTRY
         LA    R7,4(R7)                 INCREMENT CURRENT DCB LIST
         CLC   ZERO(TWO,R8),AMIDCNST    IS THIS EXECUTOR NEEDED  Y02027
*                                         AGAIN                  Y02027
         BE    QMA00                    IF YES, RETURN TO        S22024
*                                        PROCESS IT              S22024
         SPACE
         CLC   0(2,R8),QMA0S            END OF WTG TABLE
         BNE   QMA55                    NO,GO CHECK NEXT ENTRY
         LR    R7,R5                    RESET PARM LIST ADDR
         LA    R8,32(R6)                RESET WTO TABLE POINTER
         SPACE
QMA65    EQU   *
         CLI   0(R8),0                  IS THIS ENTRY ZERO
         BNE   QMA70                    NO-BRANCH TO XCTL
         SPACE
         LA    R8,8(0,R8)               ADDRESS OF NEXT ENTRY
         LA    R7,4(0,R7)               ADDRESS OF NEXT DCB
         B     QMA65                    CHECK NEXT ENTRY         S21903
         SPACE 3
*TRANSFER CONTROL TO NEXT EXECUTOR OF FIRST INCOMPLETE DCB
         SPACE
QMA70    EQU   *
         LA    R15,CKPGETML             ADDR FOR XCTL PARM LIST
         MVC   6(2,R6),0(R8)            EXECUTOR ID TO PARM
         MVC   14(3,R6),2(R8)           TTR TO WTG TABLE
         SPACE
         XCTL  DE=(R6),SF=(E,(15))
QMA80    EQU   *                        SET 3705 CKPT COMPLETE @G36XRNP
         OI    AVTCKFLG,AVTCKNAC        INDICATE 3705 CHCKPNT  @G36XRNP
*                                       COMPLETE               @G36XRNP
         B     QMA50                    EXIT                   @G36XRNP
         EJECT
QMA0S    DC    CL2'0S'                  ID OF LAST OPEN MODULE
QMATHSIX DC    H'36'                    OFFSET OF APPENDAGE TABLE      X
                                        FROM BEGINNING OF DEB
         SPACE 2
CKP3705  EQU   X'10'                    OPTCD=I                  S22024
DDNAME   EQU   41                       OFFSET TO DDNAME IN MSG  S22024
DDL      EQU   8                        LENGTH OF DDNAME         S22024
TIOEDDNM EQU   4                        OFFSET TO DDNAME IN TIOT S22024
TWO      EQU   2                        NUMERIC CONSTANT         Y02027
THREE    EQU   3                        CONSTANT OF THREE        S22024
FOUR     EQU   4                        CONSTANT OF FOUR         S22024
ZERO     EQU   0                        CONSTANT OF ZERO         S22024
CDRNCTLL EQU   38                       LENGTH OF 3705 CTL RECORDS22024
TCBTIO   EQU   12                       OFFSET TO TIOT ADDR      S22024
NINE     EQU   9                        CONSTANT OF NINE         S22024
CVT      EQU   16                       CVT OFFSET               Y02027
ONE      EQU   1                        CONSTANT OF ONE        @OZ31115
TRACELEN EQU   200                      LENGTH OF TABLE        @OZ31115
LESSLEN  DC    H'200'                   LENGTH OF TABLE        @OZ31115
CKPTFLG  EQU   X'01'                    CHECKPOINT IN SYSTEM FLG Y02027
         SPACE 3
*CHANNEL PROGRAM TO READ CONTROL RECORD
         SPACE
         DS    0D
IEDQMACP EQU   *
IEDQMASC CCW   11,0,X'60',6             SEEK CYLINDER
QMANOOP  CCW   3,0,X'60',1              NO-OP FOR NON-MERLIN0422 S21101
IEDQMASI CCW   49,0,X'60',5             SEARCH ID EQUAL
IEDQMATI CCW   08,0,X'60',0             *****                    S22024
IEDQMARC CCW   06,0,X'20',0                  CONTROL RECORD
         SPACE
QMASETSC CCW   35,0,X'60',1             SET SECTOR FOR MERLIN415 S21101
         SPACE 3
         DS    0F
QMAMSG   WTO   'IED010I CHECKPOINT-INSUFFICIENT CORE-DATA SET NOT OPENEX
               D- 300',MF=L,ROUTCDE=11,DESC=4                      0819
QMAMSGGL EQU   QMAMSG+60                LENGTH FOR GETMAIN         0819
         DS    0H                       FOR BOUNDARY ALLIGNMENT  S22024
QMASUB   DC    CL8'IGG019Q8'            CONTINUATION RESTART     S22024
CVTPTR   EQU   16                       ADDR OF CVT POINTER      S22024
CVTSVDCB EQU   84                       SVCLIB DCB ADDR IN CVT   S22024
         EJECT
QMACOLD  DC    C'49'                    CHECKPOINT ALLOCATION    S21903
QMAWARM  DC    C'43'                    CHECKPOINT INIT          S21903
QMAERR   DC    C'42'                    ERROR MSG RTN              0819
         EJECT
IEDQUCB  DSECT                          UCB                 0415 S21101
         IEFUCBOB
         EJECT
         TTCXD                          TCX                      Y02027
         EJECT
         TAVTD
         EJECT
FORCORE  DSECT                                                   Y02027
         IECDSECT                                                Y02027
         EJECT
         TCKPD 3330                     MERLIN VERSION      0422 S21101
         TTRMD
         TTNTD
         TQCBD
         DCBD  DSORG=TX
         SPACE 3
         EJECT
*DSECT FOR DEB
         SPACE
IEDQDEB  DSECT
         ORG   IEDQDEB+16               ORG TO START OF DEB      S21903
DEBNMEXT DS    X                        NO OF EXTENTS IN DATA SET
         ORG   IEDQDEB+25               ORG TO DCB POINTER       S21903
DEBDCBAD DS    AL3                      ADDR OF DCB
         SPACE
*FOR EACH EXTENT THERE IS A 16-BYTE SEGMENT AS FOLLOWS.
         SPACE
         ORG   IEDQDEB+32               ORG TO UCB POINTER       S21903
DEBDVMOD DS    XL1                      DEVICE MODIFIER
DEBUCBAD DS    XL3                      ADDRESS OF UCB
DEBBINUM DS    XL2                      BUN NO
DEBSTRCC DS    XL2                      CYLINDER ADDR FOR START
DEBSTRHH DS    XL2                      TRACK ADDR FOR START OF EXT
DEBENDCC DS    XL2                      CYLINDER ADDR FOR EXTNT END
DEBENDHH DS    XL2                      TRACK ADDR FOR EXTENT END
DEBNMTRK DS    XL2                      NO TRACKS IN THIS EXTENT
DEBEND  EQU   *
DEBSEGLN EQU   DEBEND-DEBDVMOD          LNGTH OF SEG FOE EACH EXTNT
         END
