         TITLE 'IGG01935 LOAD 1 OF LINE GROUP OPEN'
IGG01935 CSECT
*A-000000-999999                                               @X31X8N0
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A088000,141000                                                  A51063
*D499992,598000-603000,611000-612000                             A51063
*A318000,538840,590500,605000                                    A44886
*A095300,095600                                                   M1571
*C499996,500432                                                   M1571
*A605800                                                          M1571
*C153000,162000                                                  S22026
*C087000,088000,225000-227000,240000,311000,368000,442000,456000 S22025
*C499999,584000-589600,591000-592400,594000-597000,602000,603000 S22025
*C608000,611000-612600,615000-618000,633000,634000,638000,640000 S22025
*C641000                                                         S22025
*A002930,002960,055500                                           S22025
*C284300,360300,591700                                          SA65392
*A284000,329000,360000,581000                                   SA62357
*C329000,591700                                                 SA62357
*D327000                                                        SA62357
*A462000,637000                                                  X01004
*C249000-252000,374000,591700,628000                             X01004
*D080000,082000,329000,388000-425900,538070-538840,593200,       X01004
*D605300-605600,614000-618000,643000-645000,654000               X01004
*A107000,297000,606000                                           Y01004
*C252200,628500                                                  Y01004
*A095600,467000,572000                                           X02004
*A095900,457000,628900                                           Y05330
*C095800,452000                                                  Y05330
*D430100-430700                                                  Y05330
*C004600,024000,027000,033000,035000,049000,091600-094000,095780 Y02027
*C107000,252000,279000-283000,460000,576000-581000,582000-583000 Y02027
*C593500,604000,605000,605100,607000,609000,610000               Y02027
*D095000-095600                                                  Y02027
*A082000,091200,094500,297400,472300,473400,523160-523880        Y02027
*A612800-613200                                                  Y02027
*C300000,311000,342000,350000,351000,352000,353000,354000        Y02027
*C360000-360600,366000,376300,377000,380000,384000,427000,428000 Y02027
*C499700,499940,499984,499996,499998,500432,500532,572000,586400 Y02027
*D085000,581500,588000,588800,602100                             Y02027
*A228500*A228500,278500,297050,327000,647200-647600              Y02027
*C360700,360800                                                  Y02027
*C619800                                                        SA68037
*A373000,603000                                                @SA63601
*A360700,360800,602100                                          OY03305
*D373100-373700                                                @SA73348
*A364000                                                       @SA73348
*C004600,229000,243000,570040                                    X03039
*D095620-095860,225000,637300-637600                             X03039
*A002000,094500                                                @Z30X8NM
*C004600,430000,452000-452800,567000                           @Z30X8NM
*D453500,457000-457800,628640-628740,632500,635000             @Z30X8NM
*D637000-638000                                                @Z30X8NM
         SPACE 3
*A352000,628560                                                @XA09807
***********************************************************************
*                                                                     *
*  TITLE:  ' IGG01935 '  LOAD 1 OF LINE GROUP OPEN                    *
*                                                                     *
*  MODULE NAME = IGG01935                                             *
*                                                                     *
*  DESCRIPTIVE NAME = FIRST LOAD OF LINE GROUP OPEN                   *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS = CHANGE LEVEL 8                                     @Z30X8NM
*                                                                     *
*FUNCTION/OPERATION - THIS ROUTINE EXAMINES THE TASK INPUT/           *
*   OUTPUT TABLE TO DETERMINE THE NUMBER OF LINES IN THIS             *
*   LINE GROUP, AND GETS MAIN STORAGE FOR AND INITIALIZES A           *
*   DATA EXTENT BLOCK.  IT CHECKS EACH UNIT CONTROL BLOCK TO          *
*   VERIFY THAT SIMILAR DEVICES ARE ATTACHED TO EACH LINE,            *
*   AND THAT EITHER A 2701, 2702, OR 2703 CONTROL UNIT IS             *
*   BEING USED.  IGG01935 ALSO LOCATES A TYPICAL ENTRY IN             *
*   THE DEVICE CHARACTERISTICS TABLE FOR EACH LINE GROUP, SETS        *
*   AN INDEX INTO THE BRANCH TABLE IN IGG01936, AND CLEARS            *
*   A REGISTER TO CONTAIN THE LINE CONTROL BLOCK SIZE.                *
*                                                                     *
*ENTRY POINTS - THIS ROUTINE IS ENTERED AT IGG01935 VIA AN            *
*   XCTL FROM AN I/O SUPPORT MODULE OR FROM ANOTHER ACCESS            *
*   METHOD'S OPEN EXECTUTOR.  IT MAY ALSO BE RE-ENTERED FROM          *
*   THE RE-LOOP PORTION OF THIS ROUTINE IF IT DETERMINES THAT         *
*   THERE IS ANOTHER DATA CONTROL BLOCK TO BE OPENED.                 *
*                                                                     *
*INPUT - THE REGISTERS 5, 6, 7, AND 8 ARE THE INPUT AS FOLLOWS        *
*   5 - THE ADDRESS OF THE WTG TABLE PREFIX                      Y02027
*   6 -  THE ADDRESS OF THE BEGINNING OF THE WHERE-TO-GO TABLE.       *
*   7 - THE ADDRESS OF THE COPY OF THE CURRENT DCB               Y02027
*   8 -  THE ADDRESS OF THE CURRENT ENTRY IN THE WHERE-TO-GO          *
*        TABLE .                                                      *
*                                                                     *
*OUTPUT - REGISTERS 7 AND 8 WILL BE POSITIONED AT THE NEXT            *
*   ENTRIES IN THE DATA CONTROL BLOCK COPY LIST AND THE          Y02027
*   WHERE-TO-GO TABLE RESPECTIVELY.  A DEB WILL HAVE BEEN             *
*   BUILT AND INITIALIZED IN SUBPOOL 230.  THIS EXECUTOR'S       Y02027
*   ENTRY OR ENTRIES IN THE WHERE-TO-GO TABLE WILL HAVE BEEN          *
*   UPDATED TO IDENTIFY LINE GROUP LOAD II (IGG01936).                *
*                                                                     *
*EXTERNAL ROUTINES - NONE                                             *
*                                                                     *
*EXITS - NORMAL                                                       *
*   THIS ROUTINE EXITS VIA AN XCTL TO THE EXECUTOR IDENTIFIED         *
*   BY THE NEXT NON-ZERO ENTRY IN THE WHERE-TO-GO TABLE.              *
*                                                                     *
*      - ERROR                                                        *
*TABLES/WORKAREAS - THE WHERE-TO-GO TABLE CONTAINS THE ID AND         *
*   TTR OF THE OPEN EXECUTORS.  THE DATA CONTROL BLOCK                *
*   COPY LIST CONTAINS THE ADDRESSES OF THE COPY OF EACH DCB     Y02027
*   SPECIFIED IN THE OPEN MACRO INSTRUCTION.  THE ADDRESS             *
*   VECTOR TABLE IS A RESIDENT VECTOR LIST USED TO LOCATE             *
*   CERTAIN PARAMETERS WHICH ARE NEEDED IN BUILDING THE DEB.          *
*                                                                     *
*ATTRIBUTES - THIS ROUTINE IS EXECUTED IN THE TRANSIENT AREA          *
*   AS ENABLED, PRIVILEGED, AND REENTRANT.                            *
***********************************************************************
         EJECT
*  REGISTER USAGE
*
RE       EQU   0                        WORK REGISTER
RF       EQU   1                        WORK REGISTER
RDCB     EQU   2                        ADDRESS OF THE CURRENT DCB
RTIOT    EQU   3                        ADDRESS OF THE TIOT
RCORE    EQU   4                        ADDRESS OF DCB WORK AREA
RPAR     EQU   5                        ADDRESS OF FIRST ENTRY IN
*
RWTG     EQU   6                        ADDRESS OF WHERE-TO-GO
*                                         TABLE
RPARC    EQU   7                        ADDRESS OF CURRENT ENTRY IN
*                                         DCB PARAMETER LIST
RWTGC    EQU   8                        ADDRESS OF THE CURRENT
*                                         ENTRY IN WTG TABLE
RAVT     EQU   9                        ADDRESS OF THE AVT
RUCB     EQU   10                       ADDRESS OF CURRENT UCB
RDEB     EQU   11                       ADDRESS OF THE DEB
RBASE    EQU   12                       BASE OF THIS ROUTINE
RC       EQU   13                       WORK REGISTER
RD       EQU   14                       WORK REGISTER
RJ       EQU   15                       WORK REGISTER
*
         USING IHADCB,RDCB              DCB DSECT
         USING FORCORE,RCORE            OPEN WORK AREA DSECT     Y02027
         USING IEDQAVTD,RAVT            AVT DSECT
         USING IEDQDEB,RDEB             DEB DSECT
*
         BALR  RBASE,0                  SET UP BASE REGISTER     S22025
         USING *,RBASE                  AND ADDESSABILITY        S22025
         SPACE 2
IGG01935 IEDHJN DATE                    MODULE ID AND DATE       S22025
*
RESTART  EQU   *
         EJECT                                                   Y02027
         MODESET EXTKEY=SUPR            GET INTO KEY 0           Y02027
         SPACE 2
         L     RCORE,OPWKAD(RWTGC)      LOAD THE ADDRESS OF      Y02027
*                                         THE OPEN WORK AREA     Y02027
         L     RDCB,DXUDCBAD            LOAD THE ADDRESS OF THE  Y02027
*                                         USER'S DCB             Y02027
         L     RAVT,DCBIOBAD            LOAD THE AVT ADDRESS     Y02027
         XC    DXCCW10(FOUR),DXCCW10    CLEAR IN CASE OF OPEN    Y02027
*                                         ERRORS                 Y02027
         CLI   DXUKEY,KEY6              IS INVOKER IN KEY 6      Y02027
         BNE   NOTKEY6                  NO, GO SET ERROR CODE    Y02027
         LA    RF,AVTEZERO(RAVT)        ADDRESS OF  AVT        @Z30X8NM
         SH    RF,AVTHA4                BACKUP TO PREFIX       @Z30X8NM
         CLI   ONE(RF),VSTCAM           CHECK VS ASSEMBLY      @Z30X8NM
         BNE   OSMCP                    NO, SET ERROR          @Z30X8NM
         CLC   0(TWO,RCORE),OPENERP     IS THIS A RETURN FROM THE
*                                         ERROR MODULE
         BNE   TSTBUFIN                 NO, BRANCH
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF CODE IS EXECUTED ONLY WHEN THIS          *
*        MODULE IS RE-ENTERED FROM THE OPEN ERROR RECOVERY        *
*                  MODULE IGG01933                                *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         L     RDEB,DCBDEBAD            LOAD ADDRESS OF THE      Y02027
*                                         CURRENT DEB
         LA    RE,DEBPRFIX              LOAD THE LENGTH OF THE DEB
*                                         PREFIX
         SR    RDEB,RE                  SET THE DEB BASE REGISTER
*                                         ADDRESS
         L     RD,DEBUCBAD              LOAD THE ADDRESS OF THE
*                                         FIRST UCB
         B     TESTDVCL                 TEST THE DEVICE CLASS FIELD
*                                         OF THE UCB
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*         THIS SECTION OF CODE INSURES THAT CERTAIN                *
*        MINIMUM VALUES ARE IN THIS DCB PRIOR TO                   *
*                  OPENING THE DCB                                *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
TSTBUFIN EQU   *
         TM    DCBBUFIN,X'F0'           HAVE BUFFERS BEEN ASSIGNED
*                                         FOR RECEIVE OPERATIONS
         BNZ   TSTBUFOT                 YES, BRANCH
         OI    DCBBUFIN,X'10'           NO, ASSIGN AT LEAST ONE
*                                         BUFFER FOR RECEIVE
*                                         OPERATIONS
TSTBUFOT EQU   *
         TM    DCBBUFOU,X'0E'           HAVE BUFFERS BEEN ASSIGNED
*                                         FOR SENDING OPERATIONS
         BNZ   TSTBFMAX                 YES, BRANCH
         NI    DCBBUFOU,X'F0'           TURN OFF X'01' BIT IF ON A51063
         OI    DCBBUFOU,X'02'           NO, ASSIGN AT LEAST TWO
*                                         BUFFERS FOR SENDING
*                                         OPERATIONS
TSTBFMAX EQU   *
         TM    DCBBUFMA,X'0E'           HAVE THE MINIMUM NUMBER OF
*                                         BUFFERS BEEN ASSIGNED FOR
*                                         DATA TRANSFER
         BNZ   TESTPCI                  YES, BRANCH
         OI    DCBBUFMA,X'02'           ASSIGN AT LEAST TWO
*                                         BUFFERS FOR DATA TRANSFER
TESTPCI  EQU   *
         TM    DCBPCI,X'AA'             HAS A PCI OPTION BEEN    S22026
*                                         SPECIFIED FOR RECEIVING
*                                         OPERATIONS
         BNZ   TESTPCIS                 YES, BRANCH
         OI    DCBPCI,X'20'             NO, PCI WILL OCCUR DURING
*                                         FILLING OF THE SECOND
*                                         BUFFER FOR RECEIVE
*                                         OPERATIONS
TESTPCIS EQU   *
         TM    DCBPCI,X'55'             HAS A PCI OPTION BEEN    S22026
*                                         SPECIFIED FOR SENDING
*                                         OPERATIONS
         BNZ   SETBFMAX                 YES, BRANCH
         OI    DCBPCI,X'10'             NO, PCI WILL OCCUR DURING
*                                         EMPTYING OF THE SECOND
*                                         BUFFER FOR SENDING
*                                         OPERATIONS
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF CODE INSURES THAT BUFMAX IS AS GREAT     *
*        AS OR GREATER THAN THE GREATER OF BUFIN AND BUFOUT       *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
SETBFMAX EQU   *
         SR    RF,RF                    CLEAR A REGISTER
         LA    RE,FIFTEN                LOAD AN ANDING VALUE
         IC    RD,DCBBUFOU              GET THE BUFOUT VALYE
         NR    RD,RE                    CLEAR THE BUFIN VALUE
         IC    RF,DCBBUFIN              GET THE BUFIN VALUE
         SRL   RF,FOUR                  CLEAR THE LOW ORDER 4 BITS
         CR    RF,RD                    COMPARE THE 2 VALUES
         BL    USEOTHER                 BRANCH IF BUFIN IS LOW
         LR    RD,RF                    COPY THE LARGER VALUE
USEOTHER EQU   *
         IC    RF,DCBBUFMA              LOAD THE BUFMAX VALUE
         NR    RF,RE                    CLEAR THE LEADING 4 BITS
         CR    RF,RD                    COMPARE THE 2 VALUES
         BNL   BUFFERTN                 BRANCH IF RF IS NOT LESS
         STC   RD,DCBBUFMA              STORE THE NEW VALUE FOR
*                                         BUFMAX
         EJECT
BUFFERTN EQU   *
         IC    RD,DCBUNTCT              INSERT THE SPECIAL
*                                         CHARACTERS OFFSET
         STC   RD,DCBSCTAD+2            STORE THE SPECIAL
*                                         CHARACTERS OFFSET IN
*                                         ANOTHER LOCATION
         SR    RE,RE                    CLEAR REMAINDER REGISTER
         LH    RJ,AVTKEYLE              GET THE SIZE OF 1 UNIT
         LH    RF,DCBBUFSI              LOAD THE DESIRED BUFFER
*                                         SIZE
         LTR   RF,RF                    CHECK FOR NO BUFSIZ SPECIFD
         BNZ   BUFSIZ                   BRANCH IF IN DCB
         LR    RF,RJ                    DEFAULT TO AVTKEYLE VALUE
         STH   RF,DCBBUFSI              STORE THE DEFAULT VALUE IN
*                                         THE DCB
BUFSIZ   EQU   *
         DR    RE,RJ                    DIVIDE THE BUFFER SIZE BY
*                                         THE SIZE OF 1 LINE UNIT
         LTR   RE,RE                    WAS THERE A REMAINDER
         BZ    UNITCNT                  NO, BRANCH
         LA    RF,1(RF)                 INCREMENT UNIT COUNT BY 1
UNITCNT  EQU   *
         STC   RF,DCBUNTCT              STORE THE NUMBER OF UNITS
*                                         REQUIRED FOR 1 BUFFER
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF CODE INSURES THAT THE HEADER PREFIX      *
*        SIZE AND THE NUMBER OF IDLE CHARACTERS RESERVED FOR      *
*        RECEIVING OPERATIONS PLUS ONE DO NOT EXCEED ONE          *
*                       LOGICAL UNIT                              *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SR    RF,RF                    CLEAR A SIZE REGISTER
         IC    RF,DCBRESER              INSERT THE NUMBER OF IDLE
*                                         CHARACTERS RESERVED FOR
*                                         RECEIVING OPERATIONS
         LA    RF,AVTHDRSZ+1(RF)        ADD TO THIS THE SIZE OF A
*                                         HEADER PREFIX + 1
         CR    RF,RJ                    COMPARE THIS SIZE TO THE
*                                         SIZE OF ONE LOGICAL UNIT
         BNH   LOADTCB                  BRANCH, UNIT SIZE IS LARGER
         MVI   AVTOPERT,TCAMER12        MOVE THE ERROR IDENTIFIER
*                                         FOR TCAM INTO THE AVT
         B     WTGSETUP                 SET THE WHERE-TO-GO ENTRY
         EJECT
LOADTCB  EQU   *
         L     RD,AVTTCB                LOAD THE TCB ADDRESS FROM
*                                         THE AVT
         L     RTIOT,TIOTPTR(RD)        GET POINTER TO THE TIOT
         AH    RTIOT,DCBTIOT            ADD DDOFFSET TO STARTING
*                                         POINT OF TIOT
         LR    RD,RTIOT                 COPY TIOT ENTRY POINTERS
*
         SR    RC,RC                    WORK                     S22025
         SR    RCORE,RCORE              REGISTERS                S22025
         LA    RJ,TIOTENT               LOAD THE SIZE OF A STANDARD
*                                         TIOT ENTRY             X03039
         EJECT
DDNAM01  EQU   *                                                 X03039
         SR    RF,RF                    CLEAR FOR AN INSERT      X03039
         LA    RCORE,ONE(RCORE)         INCREMENT COUNTER FOR EACH
*                                         CONCATENATED DDNAME
         IC    RF,0(RD)                 GET LENGTH OF THE DD ENTRY
         AR    RD,RF                    GET POINTER TO NEXT ENTRY
*                                         IN TIOT
         SR    RF,RJ                    SUBTRACT THE SIZE FROM THE
*                                         TOTAL ENTRY LENGTH
         AR    RC,RF                    TOTAL UCB'S FOR THE NUMBER
*                                         OF EXTENTS
         CLI   0(RD),X'00'              TEST FOR END OF TIOT
         BE    DEBSIZE                  BRANCH IF END            S22025
*
         CLI   4(RD),X'40'              TEST FOR A BLANK DDNAME
         BE    DDNAM01                  DATA SET IS CONCATENATED X03039
*
         EJECT
DEBSIZE  EQU   *
         LA    RF,MINDEBSZ(RC)          MINIMUM DEB SIZE TO THE
*                                         NUMBER OF UXB'S
         SRL   RF,THREE                 GET NUMBR OF DOUBLEWORDS X01004
*                                         IN DEB                 X01004
         LR    RUCB,RF                  SAVE DEB SIXE            X01004
         SLL   RF,THREE                 BYTE LENGTH FOR GETMAIN  X01004
         MVI   AVTGETMN+NINE,SP230      SUBPOOL FOR DEB.         Y02027
         LA    RE,AVTGETMN+4            GET ADDRESS OF WORD TO HOLD
*                                         ADDRESS OF DEB
         BAL   RD,AVTPAR                SET UP GETMAIN PARAMETER
*                                         LIST
*
         L     RDEB,AVTGETMN+4          LOAD ADDRESS OF THE DEB
         XC    0(68,RDEB),0(RDEB)       CLEAR THE DEB
         L     RF,CVT                   LOAD THE CVT ADDRESS
         L     RF,20(RF)                LOAD THE ADDRESS OF THE IOS
*                                         APPENDAGE TABLE
         MVC   0(20,RDEB),0(RF)         MOVE THE APPENDAGE
*                                       ADDRESSES INTO THE DEB
         SRL   RC,TWO                   DIVIDE UCB SUZE BY 4 TO GET
*                                         NUMBER OF EXTENTS
         STC   RC,DEBNMEXT              STORE NUMBER OF EXTENTS IN
*                                         THE DEB
*
         MVC   DEBTCBAD+1(3),AVTTCB+1   MOVE THE TCB ADDRESS
*                                         INTO THE DEB
         ST    RDEB,DEBAPPAD            STORE THE ADDRESS OF THE
*                                         DEB APPENDAGE
         MVI   DEBEXSCL,X'02'           MOVE IN 2 FOR NON-DIRECT
*                                         ACCESS DEVICE
         L     RD,AVTTCB                LOAD THE TCB ADDRESS FROM
*                                         THE AVT
         USING IEDQTCB,RD               TCB DSECT                Y02027
         L     RE,TCBDEB                ADDR 1ST DEB IN CHAIN    Y02027
         LA    RF,DEBNMSUB              GET ADDR OF NEW DEB      Y02027
CHAINDEB ST    RE,DEBDEBAD              POINT NEW DEB TO 1ST     Y02027
         CS    RE,RF,TCBDEB             CHAIN NEW TO TCB IF      Y02027
         BNE   CHAINDEB                   TCBDEB HAS NOT CHNGED  Y02027
         MVI   DEBAMLNG,AMLNG           SET SIZE OF ACCESS METH SA65392
*                                         WORKAREA              SA62357
         ST    RF,DCBDEBAD              STORE THE DEB ADDRESS INTO
*                                         THE DCB
         ST    RDCB,DEBDCBAD            STORE THE DCB ADDRESS INTO
*                                         THE DEB
         MVC   DEBPROTG(1),TCBPKF       MOVE PROTECTION TAG FROM
*                                         THE TCB TO THE DEB
         OI    DEBPROTG,X'0F'           OR IN A DEB IDENTIFIER
         STC   RUCB,DEBLNGTH            STORE NUMBER OF DOUBLES
*                                         WORDS IN THE DEB
         DROP  RD                       DROP TCB DSECT           Y02027
         SPACE 2
         DEBCHK (RDCB),TYPE=ADD,AM=TCAM INSERT DEB IN CHAIN      Y01004
         SPACE 2
         ST    RF,DCBDEBAD              STORE DEB ADDR.          Y02027
         LA    RJ,DEBUCBAD              LOAD THE ADDRESS OF THE
*                                         FIRST DEB EXTENT
         LA    RE,TIOTENT+ONE           LOAD THE LENGTH OF TIOT  Y02027
*                                         ENTRY + 1
         EJECT
DEBMOVE  EQU   *
         SR    RF,RF                    CLEAR WORK REGISTER
         IC    RF,0(RTIOT)              GET LENGTH OF TIOT ENTRY
*
*    START OF TIOT
         SR    RF,RE                    SUBTRACT 17 FROM THE LENGTH
*                                         16 FOR ENTRY AND 1 FOR
*                                         THE EXECUTE
         EX    RF,MOVEUCB               MOVE UCB INFO FROM       Y02027
*                                         THE TIOT TO THE DEB
         MVI   ZIP(RJ),ZIP              CLEAR TIOT STATUS BYTE   Y02027
*                                       FROM UCB INFO            Y02027
         LA    RJ,1(RJ,RF)              INCREASE DEB POINTER BY
*                                         LENGTH OF UCB INFORMATION
         LA    RTIOT,17(RTIOT,RF)       BUMP POINTER TO NEXT ENTRY
*                                         IN TIOT
         BCT   RCORE,DEBMOVE            DECREASE THE NUMBER OF DD
*                                         ENTRIES BY 1
         TM    0(RPARC),OUTONLY         LINE BEING OPENED FOR    A44886
*                                         OUTPUT ONLY            A44886
         BO    DEVANLYZ                 BR, YES                  A44886
         CLC   DEBNMEXT,DCBILCT         EXTENTS RELATIVE TO THE  A44886
*                                         INVITATION LISTS       A44886
         BH    TERMERR                  BR, INSUFFICIENT         A44886
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*       THE FOLLOWING SECTION OF CODE TESTS ALL UCB DEVICE CODES  *
*       IN THIS LINE GROUP                                        *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DEVANLYZ EQU   *
         USING UCB,RD                   UCB ADDRESSABILITY       Y02027
         SR    RJ,RJ                    REGISTER
         IC    RJ,DEBNMEXT              NUMBER EXTENTS          SA62357
         SLL   RJ,TWO                   TIMES FOUR              SA62357
         LA    RTIOT,DEBUCBAD(RJ)       POINT TO ACCESS METHOD  SA62357
*                                         WORK AREA             SA62357
         SR    RJ,RJ                    CLEAR WORK REGISTER     SA62357
         LA    RE,FOUR                  SET AN INCREMENT OF 4
         LR    RF,RC                    SET THE NUMBER OF EXTENTS
         BCTR  RF,0                     REDUCE THE NUMBER OF
*                                         EXTENTS BY 1
         SLL   RF,TWO                   NULTIPLY THE NUMBER OF
*                                         EXTENTS BY 4
         SR    RC,RC                    CLEAR A REGISTER TO HOLD
*                                         THE CONTROL UNIT TYPE
DUMYTEST EQU   *
         L     RD,DEBUCBAD(RJ)          LOAD THE ADDRESS OF THE
*                                         FIRST/NEXT UCB
         LTR   RD,RD                    IS THIS A // DD DUMMY ENTRY
         BNZ   LOOPUCB                  NO, BRANCH               Y02027
         BXLE  RJ,RE,DUMYTEST           INCREMENT THE INDEX
         B     DUMMYDCB                 BRANCH THERE HAS BEEN AN
*                                         ERROR
NEXTUCB  EQU   *
         L     RCORE,DEBUCBAD(RJ)       LOAD THE ADDRESS OF THE
*                                         NEXT UCB
         LTR   RCORE,RCORE              IS THIS A // DD DUMMY ENTRY
         BZ    LOOPUCB                  YES, BRANCH              Y02027
         MVC   ZIP(FOUR,RWTGC),UCBTYP   MOVE FIRST UCB DEVICE    Y02027
*                                       CODE TO THE WORK AREA    Y02027
         XC    ZIP(FOUR,RWTGC),UCBTYP-UCBOB(RCORE) COMPARE DEVICEY02027
*                                       CODES OF FIRST/NEXT UCB  Y02027
         CLI   EIGHTEEN(RD),GRAPHIC     GRAPHICS DEVICE        @XA09807
         BNE   UCBTEST                  NO, BRANCH             @XA09807
         CLI   NINETEEN(RD),D3277       3277 DISPLAY           @XA09807
         BNE   UCBTEST                  NO, BRANCH             @XA09807
         NI    ONE(RWTGC),ALARMOFF      TURN OFF AUDIBLE ALARM @XA09807
UCBTEST  EQU   *                                               @XA09807
         NC    0(4,RWTGC),HOLDMASK      REMOVE ALL IRELEVENT BITS
*                                         FROM DEVICE CODE
         BC    4,BADEVICE               BRANCH IF NOT ZERO TO
*                                         DEVICE ERROR
         EJECT
LOOPUCB  EQU   *                                                 Y02027
         L     RCORE,UCBEXTPT           POINT TO UCB EXTENSION   Y02027
         MVC   FOUR(ONE,RTIOT),UCBETI-UCBCMEXT(RCORE) SAVE       Y02027
*                                       ERROR TAB                Y02027
         MVC   FIVE(ONE,RTIOT),UCBATI-UCBCMEXT(RCORE) SAVE       Y02027
*                                       ATTENTION TAB            Y02027
         BXLE  RJ,RE,NEXTUCB            ADD 4 TO RJ,COMPARE TO RF,
*                                         AND GO TO NEXTUCB IF
*                                         TOTAL IS LOW OR EQUAL TO
*                                         TOTAL IN RF
         TM    UCBUNTYP,TYP3270         COULD THIS BE A 3270   @SA73348
*                                        DEVICE ?              @SA73348
         BZ    TESTDVCL                 NO, BRANCH TO CONTINUE @SA73348
         TM    UCBUNTYP,TYPE3270        IS THIS A 3277,3284    @SA73348
*                                        OR 3286               @SA73348
         BZ    TESTDVCL                 NO, BRANCH TO CONTINUE @SA73348
         TM    DCBPCI,DCBPCIN1          IS PCI 'N' SPECIFIED   @SA73348
         BO    TESTDVCL                 YES, PCI OK FOR 3270   @SA73348
         MVI   DCBPCI,DCBPCIX1+DCBPCIN2  SET PCI TO 'XN'       @SA73348
TESTDVCL EQU   *
         CLI   UCBDVCLS,TPDEVCD         IS THIS TP               Y02027
*                                         EQUIPMENT
         BE    TESTADPT                 BRANCH IF YES            S22025
         CLI   UCBDVCLS,GRAPHICS        IS THE TGRAPHICS EQUIP.  Y02027
         BNE   BADVCLS                  AN ERROR, SOMETHING BESIDES
*                                         TELECOMMUNICATIONS
*                                         OR GRAPHICS EQUIPMENT WAS
*                                         SPECIFIED
         B     ENDLOAD1                 THIS IS A 2260 LOCAL, DO X01004
*                                         NOT TEST THE ADAPTER TYPE
TESTADPT EQU   *
         CLI   UCBUNTYP,AUDIOCTL        IS THIS A 7770 AUDIO UNITY02027
         BE    INSERT                   YES, BRANCH
         TM    UCBUNTYP,CNTRLUNT        COULD THIS POSSIBLY BE   Y02027
*                                         2701, 2702, OR A 2703
         BZ    BADCNTRL                 NO, BRANCH
         TM    UCBUNTYP,NOCONTRL        COULD IT BE SOMETHING    Y02027
*                                         BESIDES THE NORMAL
*                                         CONTROL UNIT
         BNZ   BADCNTRL                 YES, BRANCH
INSERT   EQU   *
         IC    RC,UCBUNTYP              PICK UP THE ADAPTER AND  Y02027
*                                         CONTROL UNIT BITS
         SRL   RC,FOUR                  CLEAR OUT LOW ORDER BITS OF
*                                         2701,2702,OR 2703
         EJECT
ENDLOAD1 EQU   *
         LA    RD,UCBTYP                INCREMENT THE UCB        Y02027
*                                       STARTING ADDRESS         Y02027
         SR    RUCB,RUCB                CLEAR INDEX REGISTER
         MVC   AVTEZERO(TWO,RWTGC),AQTAMLD2 MOVE ID TO WTG TBL @Z30X8NM
         L     RJ,FOUR(RWTGC)           LOAD ADDRESS OF THE WORK
*                                         AREA
         STM   0,15,0(RJ)               SAVE REGISTERS IN WORK AREA
*
RELOOP   EQU   *
         LA    RWTGC,EIGHT(RWTGC)       INCREMENT CURRENT WTG
*                                         REGISTER
         LA    RPARC,FOUR(RPARC)        INCREMENT CURRENT DCB
*                                         REGISTER
         CLC   0(2,RWTGC),AMIDCNST      IS THIS EXECUTOR NEEDED
*                                         AGAIN
         BE    RESTART                  YES, BRANCH FOR NEXT DCB S22025
*
         CLC   0(2,RWTGC),OPIDCNST      TEST FOR END OF WTG TABLE
         BNE   RELOOP                   GO CHECK NEXT ENTRY
*
         LR    RPARC,RPAR               RESET DCB AND
         LA    RWTGC,FIRSTID(RWTG)      WTG POINTERS
*
ZCHECK   EQU   *
         CLI   0(RWTGC),X'00'           IS THE FIRST ENTRY ZERO
         BNE   XCTLRTNE                 NO, XCTL TO NEXT LOAD  @Z30X8NM
*
         LA    RWTGC,EIGHT(RWTGC)       IF ZERO, INCREMENT POINTERS
         LA    RPARC,FOUR(RPARC)          AND RETURN TO ZCHECK
         B     ZCHECK                   XCTL TO NEXT MODULE      S22025
*
XCTLRTNE EQU   *
         LA    RJ,DXCCW12-FORCORE(RJ)   POINT TO DOUBL WORD LIST Y02027
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID TO NAME FIELD
         MVC   14(3,RWTG),2(RWTGC)      MOVE TTR TO WTG TABLE
ENDLOAD3 EQU   *                                                 X01004
         XCTL  DE=(RWTG),SF=(E,(15))
         EJECT
AVTPAR   EQU   *
         ST    RF,AVTGETMN              STORE BYTE COUNT IN AVT
*                                         PARAMETER LIST
         ST    RE,AVTGETMN+4            STORE ADDRESS OF WORD IN
*                                         SECOND WORD OF AVT
*                                         PARAMETER LIST
         LA    RF,AVTGETMN              LOAD ADDRESS OF AVT
*                                         PARAMETER LIST
         MODESET EXTKEY=DATAMGT         GET INTO KEY 5           Y02027
         SPACE 2
         GETMAIN  ,MF=(E,(1))           GETMAIN MACRO
         SPACE 2
         MODESET EXTKEY=SUPR            RETURN TO KEY 0          Y02027
         SPACE 2
         LTR   RJ,RJ                    TEST THE RETURN CODE
         BNE   GETERR                   GETMAIN FAILED
         BR    RD                       RETURN TO CALLER
         EJECT
OSMCP    EQU   *                                                 X02004
         MVI   AVTOPERT,EXERROS         SET OPEN ERROR TYPE      X02004
         MVI   AVTOPERL,EXERR06         SET MODULE LOAD ID       X02004
         B     WTGSETUP                 EXIT TO ERROR MODULE     X02004
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF ERROR CODE IS EXECUTED ONLY WHEN         *
*        WE ARE UNABLE TO GET ENOUGH MAIN STOTAGE TO BUILD        *
*                       A LINE GROUP DEB                          *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
GETERR   EQU   *
         MVI   AVTOPERT,TCAMER01        MOVE THE ERROR IDENTIFIER
*                                         FOR TCAM INTO THE AVT
         B     WTGSETUP                 SET THE WHERE-TO-GO ENTRY
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF ERROR CODE IS EXECUTED ONLY WHEN         *
*        UNLIKE DEVICES ARE FOUND TO BE IN THE SAME LINE GROUP    *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
BADEVICE EQU   *
         TM    0(RWTGC),FIFTEN          ARE THE MODEL CODES DIFFERENT
         BZ    SHIFTDEB                 NO, BRANCH
         TM    UCBUNTYP,TYPEI           IS THIS A TYPE I ADAPTER Y02027
         BZ    SHIFTDEB                 NO, BRANCH
         L     RCORE,DEBUCBAD(RJ)       LOAD THE UCB ADDRESS OF THE
*                                         SECOND DEVICE
         TM    UCBTBYT1,TST2741C        IS THIS A 2741           Y02027
*                                         CORRESPONDENCE DEVICE
         BO    TEST01                   YES, BRANCH
         TM    UCBTBYT1,TST2741E        IS THIS A 2741 EBCD DEV  Y02027
         BNO   SHIFTDEB                 NO, BRANCH
         TM    UCBTBYT1-UCBOB(RCORE),TST2741C OTHER DEV A 2741   Y02027
*                                         CORRESPONDENCE TERMINAL
         BO    LOOPUCB                  YES, IGNORE THE ERROR    Y02027
         B     SHIFTDEB                 OTHERWISE, ERROR IS SET  S22025
TEST01   EQU   *
         TM    UCBTBYT1-UCBOB(RCORE),TST2741E OTHER DEV A 2741E  Y02027
         BO    LOOPUCB                  YES, IGNORE THE ERROR    Y02027
SHIFTDEB EQU   *
         SRL   RJ,TWO                   DIVIDE THE BXLE INDEX BY 4
*                                         TO DETERMINE THE NUMBER
*                                         OF GOOD EXTENTS
         STC   RJ,DEBNMEXT              STORE THIS VALUE IN THE DEB
         MVI   AVTOPERT,TCAMER02        MOVE THE ERROR IDENTIFIER
*                                         FOR TCAM INTO THE AVT
         B     WTGSETUP                 SET THE WHERE-TO-GO ENTRY
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF ERROR CODE IS EXECUTED ONLY WHEN         *
*        AN UNACCEPTABLE DEVICE CLASS IS FOUND IN THE UCB         *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
BADVCLS  EQU   *
         MVI   AVTOPERT,TCAMER03        MOVE THE ERROR IDENTIFIER
*                                         FOR TCAM INTO THE AVT
         B     WTGSETUP                 SET THE WHERE-TO-GO ENTRY
         SPACE 3                                                 Y02027
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *Y02027
*                                                               *Y02027
*        THIS SECTION OF ERROR CODE IS EXECUTED ONLY WHEN       *Y02027
*        THE INVOKER OF THIS MODULE IS NOT RUNNING IN KEY 6     *Y02027
*                                                               *Y02027
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *Y02027
NOTKEY6  EQU   *                                                 Y02027
         MVI   AVTOPERT,UNAUTHL         INDICATE THAT INVOKER IS Y02027
*                                       NOT IN KEY 6             Y02027
         B     WTGSETUP                 SET THE WHERE-TO-GO ENTRYY02027
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF ERROR CODE IS EXECUTED ONLY WHEN         *
*        THE USER HAS FAILED TO PROVIDE ANY VALID UCB             *
*        NAMES ON HIS DD CARDS OTHER THAN 'DUMMY'                 *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DUMMYDCB EQU   *
         MVI   AVTOPERT,TCAMER11        MOVE THE ERROR IDENTIFIER
*                                         FOR TCAM INTO THE AVT
         B     WTGSETUP                 SET THE WHERE-TO-GO ENTRY
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *A44886
*                                                                A44886
*        THIS SECTION OF ERROR CODE IS EXECUTED WHEN THE NUMBER  A44886
*        OF INVITATION LISTS IS LESS THAN THE ONE PER LINE       A44886
*        REQUIREMENT FOR EACH LINE IN THE LINE GROUP             A44886
*                                                                A44886
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *A44886
*                                                                A44886
TERMERR  EQU   *                                                 A44886
         MVI   AVTOPERT,TCAMER24        SET THE ERROR INDICATOR  A44886
         B     WTGSETUP                 TAKE ERROR EXIT          A44886
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF ERROR CODE IS EXECUTED ONLY WHEN         *
*        AN UNSUPPORTED CONTROL UNIT IS FOUND IN THE UCB          *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
BADCNTRL EQU   *
         MVI   AVTOPERT,TCAMER04        MOVE THE ERROR IDENTIFIER
*                                         FOR TCAM INTO THE AVT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF CODE IS USED TO SAVE THE ID OF THE       *
*        CURRENT OPEN EXECUTOR AND TO MOVE THE ID OF THE          *
*        ERROR RECOVERY MODULE INTO THE WHERE-TO-GO PARAMETER     *
*                            LIST                                 *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
WTGSETUP EQU   *
         L     RJ,FOUR(RWTGC)           LOAD THE ADDRESS OF THE
*                                         OPEN WORK AREA
         MVC   AVTOPXCL(2),AMIDCNST     MOVE THE ID OF THE CURRENT
*                                         EXECUTOR INTO THE AVT
*                                         FOR THE RETURN TO THIS
*                                         EXECUTOR
         MVC   AVTEZERO(TWO,RWTGC),OPENERP MOVE ID FOR THE     @Z30X8NM
*                                         ERROR MODULE INTO THE
*                                         WHERE-TO-GO PARAMETER LIST
         B     XCTLRTNE                 BRANCH TO THE XCTL ROUTINE
         EJECT
MOVEUCB  MVC   ZIP(ONE,RJ),TIOTUCB(RTIOT) MOVE UCB INFO          Y02027
EXERROS  EQU   31                       CODE OF OS ASSEMBLY MCP  X02004
ALL      EQU   15                       MASK FOR ICM OF 4 BYTES  X02004
ONE      EQU   1                        NUMERIC CONSTANT         Y02027
TWO      EQU   2                        NUMERIC CONSTANT         Y02027
THREE    EQU   3                        NUMERIC CONSTANT         Y02027
FOUR     EQU   4                        NUMERIC CONSTANT         Y02027
FIVE     EQU   5                        NUMERIC CONSTANT         Y02027
EIGHT    EQU   8                        NUMERIC CONSTANT         Y02027
TIOTPTR  EQU   12                       TIOT POINTER IN TCB      Y02027
FIFTEN   EQU   15                       NUMERIC CONSTANT         Y02027
TIOTENT  EQU   16                       TIOT SIZE                Y02027
CVT      EQU   16                       CVT ADDRESS              S22025
MODLCODE EQU   16                       MODEL CODE OFFSET IN UCB S22025
TCAMER11 EQU   17                       INVALID UCB ADDRESS      S22025
TIOTUCB  EQU   16                       TIOT OFFSET OF UCB INFO  Y02027
TCAMER12 EQU   18                       INCORRECT BUFFER SIZE    S22025
TCAMER22 EQU   22                       INVALID OPTIONAL FEATURESS22025
TCAMER24 EQU   24                       TCAM ERROR CODE 24       A44886
DEBPRFIX EQU   36                       SIZE OF DEB PREFIX       S22025
MINDEBSZ EQU   104                      MINIMUM DEB SIZE         X01004
AMLNG    EQU   6                        ACCESS METHOD SECTION   SA65392
*                                         LENGTH IN THE DEB     SA65392
TCAMER01 EQU   X'01'                    NO CORE FOR DEB          S22025
AUDIOCTL EQU   X'00'                    7770 AUDIO UNIT          Y02027
TCAMER02 EQU   X'02'                    INCOMPATIBLE STATIONS    S22025
CNTRLUNT EQU   X'03'                    270X CONTROL UNIT        S22025
TCAMER03 EQU   X'03'                    NOT TP OR GRAPHICS UCB   S22025
TCAMER04 EQU   X'04'                    INVALID CONTROL UNIT     S22025
TST2741E EQU   X'06'                    2741 EBCD                S22025
TST2741C EQU   X'05'                    2741 CORRESPONDENCE      S22025
TYP3270  EQU   X'08'                    USEDTO TEST FOR        @SA63601
TYPE3270 EQU   X'03'                    3270 DEVICES           @SA63601
*                                       09=3277                @SA63601
*                                       0A=3284                @SA63601
*                                       0B=3286                @SA63601
EXERR06  EQU   X'06'                    ERROR ID                 Y02027
NOCONTRL EQU   X'0C'                    NOT CONTROL UNIT         Y02027
OUTONLY  EQU   X'0F'                    OUTPUT ONLY              Y02027
TYPEI    EQU   X'10'                    TYPE I ADAPTER           Y02027
FIRSTID  EQU   32                       OFFSET TO 1ST WTG ENTRY  Y02027
AMIDCNST DC    C'35'                    ID OF THIS OPEN MODULE   S22025
GRAPHICS EQU   X'10'                    GRAPHICS DEVICE          Y02027
TPDEVCD  EQU   X'40'                    TP DEVICE                Y02027
OPIDCNST DC    C'0S'                    END OF WTG TABLE         S22025
OPWKAD   EQU   4                        OPEN WK AREA ADDR OFFSET Y02027
KEY6     EQU   X'60'                    KEY 6.                   Y02027
UNAUTHL  EQU   32                       UNAUTH. ERROR NUMB.      Y02027
         EJECT
HOLDMASK DS    0F            BITS       FIELD
         DC    X'FF'                    IOSFLAGS AND MODEL CODE
         DC    X'FC'                    OPT FEAT. AND SAD COMMAND
         DC    X'FF'                    DEVICE CLASS
         DC    X'FC'                    ADAPTER AND CONTROL UNITSA68037
         EJECT
ZIP      EQU   0                        CONSTANT OF 0            X01004
SIX      EQU   6                        CONSTANT OF 6            X01004
NINE     EQU   9                        CONSTANT OF 9            X01004
TWOTWO   EQU   22                       CONSTANT OF 22           X01004
FOURTEEN EQU   14                       CONSTANT OF 14           X01004
EIGHTEEN EQU   18                       CONSTANT OF 18         @XA09807
NINETEEN EQU   19                       CONSTANT OF 19         @XA09807
TWOFIVE  EQU   25                       CONSTANT OF 25           X01004
SP230    EQU   230                      SUBPOOL FOR DEBS.
RLD      EQU   X'FE'                    FLAG FOR RLD PRESENT     X01004
GRAPHIC  EQU   X'10'                    GRAPHIC DEVICE         @XA09807
D3277    EQU   X'09'                    3277 DISPLAY           @XA09807
ALARMOFF EQU   X'EF'                    FLAG FOR AUDIBLE ALARM @XA09807
VSTCAM   EQU   C'V'                     VS TCAM MCP ID           Y05330
AQTAMLD2 DC    C'36'                    ID OF NEXT LOAD OF OPEN  S22025
OPENERP  DC    C'33'                    OPEN ERROR MODULE
         EJECT
         TDEBD
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
FORCORE  DSECT                                                   Y02027
         IECDSECT                                                Y02027
         EJECT
         TTCBD
         EJECT
         TAVTD
         EJECT
         DCBD  DSORG=TX
         END
