         TITLE 'IGG01940 SIXTH LOAD OF TCAM LINE OPEN'
IGG01940 CSECT
*A-000000-999999                                               @X31X8N0
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A133831-133837,578300-578600                                    S22027
*C412000*                                                       SA53611
*C088600,412000,561000                                           S22027
*A460000,546000                                                  S21101
*C453000                                                         S21101
*088000,184630,184700                                            A41032
*A002300-002900                                                  S22025
*C087000,350000,352000,360000,513000,613000,615000-616000        S22025
*A133810-133970,447200-447600,584000-585000,592000-593000        S22026
*D576100-576300                                                  S22026
*C134200-134300,134500-134700,561000                             S22026
*A001000,079000,089000,125000,131000,134800,144500,156000,165000 CLUP21
*A176000,312000,320000,336000,343000,354000,392000,405000,422000 CLUP21
*A436000,444000,450000,532000,544000,548000,565000,578000        CLUP21
*C004600-012000,063000,075000,088600,091000-101000,134000,143000 CLUP21
*C161000,164000,166000-170000,175000,390000,479000,561000        CLUP21
*D054000-055000,080000,082000,138000-142000,145000-154000        CLUP21
*D177000-303000,313000,339000-340000,369000-386000,394000        CLUP21
*D406000-418000,430000,438000,451000-478000,480000-510000        CLUP21
*D521000-525000,528000-529000,530000,533000-536000,537000-540000 CLUP21
*D540500-542000,545000-547500,549000-550000,566000-576000        CLUP21
*D579000-610000,620000,623000                                    CLUP21
*A079000,217000,222000                                          SA52971
*C088400,088600-088700                                          SA52971
*D290000-295000,512000                                          SA52971
*A002400,607000                                                  S21903
*C002400,492000,561000                                           S21903
*D337000-338000,394000-394500,396000-423000,439000-502000,514000 S21903
*D538000,546000,552000,553000-559000,565000,567000,569000,571000 S21903
*D573000,578600,578800,583000,585000,591000,593000,599000,607000 S21903
*A330000                                                        SA62408
*A115000                                                        SA65396
*A527000                                                         X01004
*C142500-144500,274000-279000,282000-287000,331000               X01004
*A551000                                                         Y01004
*C097000                                                         X02004
*C003300,022000,026000,047000-048200,088200-089000               Y02027
*C091000-099000,205000-208000,363000,426000-428000,517000-527000 Y02027
*C531000,537000,543000-545000,551500                             Y02027
*D100000,101000,515000                                           Y02027
*A081600,082200,126500,552000,552500                             Y02027
*A001010                                                       @Z30X8NM
*C003300,341000                                                @Z30X8NM
*D205600-207400,561000-564000,607000,612000-617000             @Z30X8NM
***********************************************************************
*                                                                     *
*  TITLE:  'IGG01940' -- SIXTH LOAD TCAM LINE OPEN                    *
*                                                                     *
*  MODULE NAME = IGG01940                                             *
*                                                                     *
*  DESCRIPTIVE NAME = SIXTH LOAD OF LINE OPEN                         *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS = CHANGE LEVEL 8                                     @Z30X8NM
*                                                                     *
*FUNCTION/OPERATION - THIS ROUTINE LOADS ADDITIONAL MODULES           *
*   THAT ARE REQUIRED FOR LINE OPERATION.  THESE MODULES              *
*   INCLUDE THE SEND AND RECEIVE SCHEDULERS.                     X03039
*   FOR NON-BUFFERED TERMINALS, THE QEVENT SUBTASK IS PLACED AT  CLUP21
*   THE END OF THE STCB CHAIN POINTED TO BY THE LCB. FOR ALL     CLUP21
*   TERMINALS THE ADDRESS OF THE STARTUP MESSAGE ROUTINE IS      CLUP21
*   PLACED IN THE LCB. FINAL PROCESSING PRIOR TO TRANSFERRING    CLUP21
*   CONTROL TO THE NEXT LOAD OF OPEN IS TO START I/O ON THE      CLUP21
*   LINES.                                                       CLUP21
*                                                                     *
*ENTRY POINTS - THIS ROUTINE IS ENTERED AT IGG01940 VIA AN            *
*   XCTL FROM THE PREVIOUS EXECUTOR (IGG01939).  IT MAY               *
*   ALSO BE RE-ENTERED FROM THE RELOOP PORTION OF THIS ROUTINE        *
*   IF IT IS DETERMINED THAT ANOTHER DATA CONTROL BLOCK IS TO         *
*   BE PROCESSED BY IGG01940.                                         *
*                                                                     *
*INPUT - REGISTERS 5,6,7, AND 8 ARE THE INPUT AS FOLLOWS              *
*                                                                     *
*   5 - THE ADDRESS OF THE WTG TABLE PREFIX                      Y02027
*                                                                     *
*   6 - THE ADDRESS OF THE BEGINNING OF THE WHERE-TO-GO TABLE         *
*                                                                     *
*   7 - THE ADDRESS OF THE COPY OF THE CURRENT DCB               Y02027
*                                                                     *
*   8 - THE ADDRESS OF THE CURRENT EXECUTOR'S ENTRY IN THE            *
*       WHERE-TO-GO TABLE                                             *
         EJECT
*OUTPUT - ALL FUNCTIONS NEEDED FOR LINE OPERATION WILL HAVE           *
*   BEEN LOADED AND INITIAL I/O WILL HAVE BEEN STARTED ON             *
*   EACH LINE                                                         *
*                                                                     *
*EXTERNAL ROUTINES - NONE                                             *
*                                                                     *
*EXITS - NORMAL                                                       *
*   THIS ROUTINE EXITS VIA AN XCTL TO THE MODULE IDENTIFIED BY        *
*   THE NEXT NON-ZERO ENTRY IN THE WHERE-TO-GO TABLE,                 *
*   NAMELY IGG01948.                                                  *
*                                                                     *
*      - ERROR                                                        *
*   NONE                                                              *
*                                                                     *
*TABLES/WORKAREAS - THE WHERE-TO-GO TABLE CONTAINS THE ID             *
*   AND TTR OF THE OPEN EXECUTOR TO BE GIVEN CONTROL.                 *
*                                                                Y02027
*   THE WTG TABLE PREFIX CONTAINS THE ADDRESS OF A COPY          Y02027
*   OF EACH DCB SPECIFIED IN THE OPEN MACRO INSTRUCTION          Y02027
*                                                                     *
*ATTRIBUTES - THIS ROUTINE IS EXECUTED IN THE TRANSIENT AREA          *
*   AS ENABLED, PRIVILEGED, AND REENTRANT.                            *
***********************************************************************
         EJECT
*    REGISTER USAGE
*
*
RE       EQU   0                        WORK REGISTER
RF       EQU   1                        WORK REGISTER
RDCB     EQU   2                        ADDRESS OF THE CURRENT DCB
RTIOT    EQU   3                        WORK REGISTER
RCORE    EQU   4                        BASE REGISTER FOR LCB/IOBCLUP21
RPAR     EQU   5                        ADDRESS OF FIRST ENTRY IN
*                                         DCB PARAMETER LIST
RWTG     EQU   6                        ADDRESS OF THE WHERE-TO-GO
*                                         TABLE
RPARC    EQU   7                        ADDRESS OF THE CURRENT
*                                         ENTRY IN DCB PARAMETER
*                                         LIST
RWTGC    EQU   8                        ADDRESS OF THE CURRENT
*                                         ENTRY IN THE WTG TABLE
RAVT     EQU   9                        ADDRESS OF THE AVT
RUCB     EQU   10                       WORK REGISTER
RDEB     EQU   11                       BASE REGISTER FOR DEB    CLUP21
RBASE    EQU   12                       BASE OF THIS ROUTINE
RC       EQU   13                       WORK REGISTER
RD       EQU   14                       WORK REGISTER
RJ       EQU   15                       WORK REGISTER
         SPACE 2                                                SA52971
         EJECT
         USING IEDQDEB,RDEB             DEB DSECT
         USING FORCORE,RJ               WORK AREA DSECT          Y02027
         USING IEDQTCX,RF               TCX DSECT                Y02027
         USING IEDQAVTD,RAVT            AVT DSECT
         USING IHADCB,RDCB              DCB DSECT
         USING IEDQLCB,RCORE            LCB/IOB DSECT
*
         BALR  RBASE,0                  ESTABLISH ADDRESSIBILITY S22025
         USING *,RBASE
         SPACE 2
IGG01940 IEDHJN DATE                                             Y02027
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
*        THIS SECTION OF CODE ESTABLISHES ADDRESSIBILITY ON THE  CLUP21
*        DCB, AVT, DEB, AND COMPUTES THE OFFSET INTO THE DEB OF  CLUP21
*        THE SUBROUTINE-ID SECTION.                              CLUP21
*                                                                CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
RESTART  EQU   *
         L     RJ,FOUR(RWTGC)           ADDR OF OPEN WORK AREA   Y02027
         L     RF,CVTPTR                ADDRESS OF CVT           Y02027
         L     RF,CVTAQAVT-CVTD(RF)     LOAD ADDRESS OF          Y02027
*                                       TCAM EXTENSION           Y02027
         L     RAVT,TCXAVT              GET THE AVT ADDRESS      Y02027
         L     RDCB,DXUDCBAD            GET CURRENT DCB ADDRESS  Y02027
         L     RDEB,DCBDEBAD            GET THE DEB ADDRESS      Y02027
         LA    RF,DEBNMSUB-IEDQDEB      LOAD SIZE OF DEB PREFIX  Y02027
         SR    RDEB,RF                  RESET TO BEGINNING OF DEBY02027
         SR    RTIOT,RTIOT              CLEAR A REGISTER TO CONTAIN
*                                         THE COUNT OF LOADED
*                                         SUBROUTINES
         IC    RTIOT,DEBNMSUB           GET THE NUMBER OF
*                                         SUBROUTINES LOADED BY
*                                         IGG01939
         SR    RC,RC                    CLEAR AN EXTENT REGISTER
         IC    RC,DEBNMEXT              GET THE NUMBER OF EXTENTS
*                                         IN THIS DEB
         SLL   RC,TWO                   MULTIPLY THE NUMBER OF
*                                         EXTENTS BY 4
         LA    RUCB,DEBUCBAD(RC)        GET THE ADDRESS OF THE
*                                         ACCESS METHOD DEPENDENT
*                                         SECTION
         SR    RC,RC                    CLEAR WORK REGISTER     SA65396
         IC    RC,DEBAMLNG              GET THE LENGTH OF THE ACCESS
*                                         METHOD
         AR    RUCB,RC                  GET THE ADDRESS OF THE
*                                         SUBROUTINE ID SECTION
         LR    RC,RTIOT                 COPY THE COUNT OF LOADED
*                                         SUBROUTINES
         SLL   RC,ONE                   MULTIPLY THE COUNT BY 2
         AR    RUCB,RC                  SET THE ADDRESS FOR THE
*                                         NEXT ID TO BE MOVED IN
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
*        THIS SECTION OF CODE LOADS THE BUFFERED TERMINAL        CLUP21
*        SCHEDULER AND SAVES ITS ADDRESS IN THE AVT. FOR NON-    CLUP21
*        BUFFERED TERMINALS IT LOADS THE APPROPRIATE SEND        CLUP21
*        SCHEDULER AND SAVES ITS ADDRESS IN THE AVT.             CLUP21
*                                                                CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
         L     RCORE,DCBIOBAD           GET THE IOB ADDRESS
         SR    RF,RF                    CLEAR FOR INSERT         Y02027
         IC    RF,DCBEIOBX              GET THE SIZE OF AN LCB
         LA    RD,IOBPTR                SET THS IOB OFFSET
         SR    RCORE,RD                 SET THE LCB - AN LCB ADDRESS
         AR    RCORE,RF                 SET THE ACTUAL LCB ADDRESS
         CLI   LCBRSKEY,BUFTSCHD        ARE THERE BUFFERED TERMINALS
         BNE   LOADSEND                 NO, BRANCH               CLUP21
         LA    RC,BUFTRMSH              LOAD ADDRESS OF ID FOR   CLUP21
*                                         BUFFERED TERM SCHED    CLUP21
         BAL   RD,LOADRTNE              LOAD IGG019RD            CLUP21
*                                                                CLUP21
         ST    RE,AVTHB                 STORE BUFFERED TERMINAL  CLUP21
*                                         SCHED ADDRESS IN AVT   CLUP21
         B     LOADRTN                  SKIP SEND SCHEDULERS     CLUP21
*                                                                CLUP21
LOADSEND EQU   *                                                 CLUP21
*                                                                S22026
*  DETERMINE PROPER SEND SCHEDULER(S) TO LOAD                    S22026
*                                                                S22026
         L     RC,DEBUCBAD              GET ADDRESS OF UCB       S22027
         CLI   DEVCLASS(RC),GRAPHICS    IS UCB FOR GRAPHICS      S22027
         BE    LOADNONC                 YES, BRANCH              S22027
         TM    AVTSCOPT,AVTCMBUF        IS COMMON BUFFER TRANS-  S22027
*                                         MISSION TO BE USED     S22027
         BZ    NOCMBF                   BRANCH IF NOT            S22027
         LA    RC,SENDCMBF              ADDR OF COMBUF SEND SCHEDS22027
         BAL   RD,LOADRTNE              LOAD COMBUF SEND SCHED   S22027
         ST    RE,AVTCMBSS              STORE ADDRESS IN AVT     S22027
NOCMBF   EQU   *                                                 S22027
         TM    AVTSCOPT,AVTCONCO+AVTCONC  IS CONC SEND SCHEDULER S22026
*                                          SPECIFIED             S22026
         BZ    LOADNONC                 NO, FIND PROPER NON CONC S22026
*                                         SCHEDULER TO LOAD      S22026
         LA    RC,SENDCONC              ADDR OF CONC SEND        S22026
*                                         SCHEDULER              S22026
         BAL   RD,LOADRTNE              LOAD CONC SEND SCHEDULER S22026
         ST    RE,AVTCSCH               STORE ADDRESS IN AVT     S22026
         TM    AVTSCOPT,AVTCONCO        IS ONLY CONC SEND        S22026
*                                         SCHEDULER SPECIFIED    S22026
         BO    LOADRTN                  YES, BYPASS LOADING ANY  S22026
*                                         OTHER SEND SCHEDULER   S22026
*                                                                S22026
LOADNONC EQU   *                                                 S22026
         LA    RC,SENDMINI              ADDR OF MINI SEND SCHEDULER
         TM    AVTSCOPT,AVTN2741+AVTNDIAL  IS MINI SCHEDULER     S22026
*                                            SPECIFIED           S22026
         BO    LOADSCHD                 YES, GO LOAD IGG019Q6    S22026
         LA    RC,SENDNTSO              ADDR OF NTSO SEND SCHEDULER
         TM    AVTSCOPT,AVTN2741        IS NON TSO, NON 2741     S22026
*                                         SCHEDULER SPECIFIED    S22026
         BO    LOADSCHD                 YES, GO LOAD IGG019Q7    S22026
         LA    RC,SENDSCHD              ADDR OF FULL SEND SCHEDULER
*                                                                CLUP21
LOADSCHD EQU   *
         BAL   RD,LOADRTNE              RETURN LINKAGE ESTABLISHED
*
         STCM  RE,AD,AVTHD+ONE          SAVE ADDRESS IN AVT      X01004
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
*        THIS SECTION OF CODE INSPECTS A TYPICAL UCB FOR LINES   CLUP21
*        IN THE LINE GROUP AND LOADS THE 2260 LOCAL RECEIVE      CLUP21
*        SCHEDULER FOR GRAPHICS DEVICES, THE DIAL SCHEDULER FOR  CLUP21
*        DIAL LINES, AND THE RECEIVE SCHEDULER FOR NON-BUFFERED  CLUP21
*        TERMINALS.                                              CLUP21
*                                                                CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
LOADRTN  EQU   *
         SR    RJ,RJ                    CLEAR AN INDEX REGISTER
*                                                                CLUP21
LOADRTN1 EQU   *
         L     RF,DEBUCBAD(RJ)          LOAD THE ADDRESS OF THE
*                                         FIRST/NEXT UCB ADDRESS
         LTR   RF,RF                    IS THIS A // DD DUMMY ENTRY
         BNZ   TEST2260                 NO, BRANCH               CLUP21
         LA    RJ,FOUR(RJ)              INCREMENT THE INDEX REGISTER
         B     LOADRTN1                 BRANCH
*                                                                CLUP21
TEST2260 EQU   *                                                 CLUP21
         TM    UCBDVCLS(RF),GRAPHICS    IS THIS A 2260 LOCAL DEVICE
         BZ    TESTDIAL                 NO, BRANCH               CLUP21
         LA    RC,RECV2260              2260 LOCAL RECEIVE SCHED.CLUP21
         BAL   RD,LOADRTNE              LOAD IGG019Q1            CLUP21
*                                                                CLUP21
         ST    RC,AVT2260L              STORE ADDRESS IN AVT     CLUP21
         ST    RE,AVTE7+4                                        CLUP21
         B     LOADRECV                 LOAD RECEIVE SCHED       CLUP21
*                                                                CLUP21
TESTDIAL EQU   *                                                 CLUP21
         TM    UCBDVCLS-1(RF),DIAL      IS THIS A DIAL LINE      CLUP21
         BZ    LOADRECV                 NO, BRANCH               CLUP21
         LA    RC,DIALSCHD              LOAD THE ADDRESS OF THE
*                                         DIAL SCHEDULER
         BAL   RD,LOADRTNE              RETURN LINKAGE ESTABLISHED
*
         ST    RC,AVTR1                 SAVE ADDRESS OF THE      CLUP21
*                                         DIAL SCHEDULER
*                                                                CLUP21
LOADRECV EQU   *                                                 CLUP21
         CLI   LCBRSKEY,BUFTSCHD        BUFFERED TERMINALS       CLUP21
         BE    GETADDR                  YES, BRANCH              CLUP21
         LA    RC,RECVSCHD              RECIEVE SCHEDULER ID     CLUP21
         BAL   RD,LOADRTNE              LOAD RECEIVE SCHEDULER   CLUP21
*                                                                CLUP21
         ST    RE,AVTHA                 SAVE RECEIVE SCHED. ADDR.CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
*        THIS SECTION OF CODE COMPUTES THE ADDRESS OF THE QEVENT CLUP21
*        SUBTASK CONTROL BLOCK AND FOR NON-BUFFERED TERMINALS    CLUP21
*        CHAINS IT AS THE LAST ELEMENT IN THE SUBTASK CHAIN      CLUP21
*        POINTED TO BY THE LCB. FOR ALL TERMINALS, THE ADDRESS   CLUP21
*        OF THE STARTUP MESSAGE ROUTINE (IF IT HAS BEEN LOADED)  CLUP21
*        IS PLACED IN THE LCB.                                   CLUP21
*                                                                CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
GETADDR  EQU   *
         LA    RE,TWO                                            CLUP21
         CLI   LCBRSKEY,BUFTSCHD        BUFFERED TERMINALS       CLUP21
         BNE   SETRECV                  NO, BRANCH               CLUP21
         L     RC,AVTHB                 ADDRESS OF BUFFERED SCHEDCLUP21
         SR    RC,RE                    LOCATE ORIGIN OF MODULE  CLUP21
         B     LOADCB                   INITIALIZE LCBS          CLUP21
*                                                                CLUP21
SETRECV  EQU   *                                                 CLUP21
         L     RC,AVTHA                 ADDRESS RECEIVE SCHEDULERCLUP21
         SR    RC,RE                    LOCATE BEGINING OF MODULECLUP21
         AH    RC,0(RC)                 OFFSET TO QEVENT SUBTASK CLUP21
*                                                                CLUP21
LOADCB   EQU   *                                                 CLUP21
         SR    RD,RD                    CLEAR SIZE REGISTER      CLUP21
         IC    RD,DCBEIOBX              SIZE OF AN LCB           CLUP21
         IC    RE,DEBNMEXT              NO. LINES IN THE GROUP   CLUP21
         SR    RJ,RJ                    CLEAR INDEX REGISTER     CLUP21
*                                                                CLUP21
TESTUCB  EQU   *                                                 CLUP21
         L     RF,DEBUCBAD(RJ)          ADDRESS OF FIRST/NEXT UCBCLUP21
         LTR   RF,RF                    //DD DUMMY ENTRY         CLUP21
         BNZ   LCBLOOP                  NO, BRANCH               CLUP21
         LA    RJ,FOUR(RJ)              BUMP UCB ADDRESS INDEX   CLUP21
         B     TESTUCB                  TEST NEXT UCB ENTRY      CLUP21
*                                                                CLUP21
LCBLOOP  EQU   *                                                 CLUP21
         CLI   LCBRSKEY,BUFTSCHD        BUFFERED TERMINALS       CLUP21
         BE    STARTEST                 YES, BRANCH              CLUP21
         LA    RJ,LCBRSKEY              ADDRESS OF FIRST ELEMENT CLUP21
*                                         ON THE CHAIN           CLUP21
NEXTELMT EQU   *                                                 CLUP21
         L     RF,0(RJ)                 ADDRESS OF NEXT ELEMENT  CLUP21
         CLI   FOUR(RF),ENDCHAIN        LAST ELEMENT ON CHAIN    CLUP21
         BE    SETQEVNT                 YES,BRANCH               CLUP21
         LA    RJ,FOUR(RF)              GET LINK TO NEXT ELEMENT CLUP21
         B     NEXTELMT                 TEST NEXT ELEMENT        CLUP21
*                                                                CLUP21
SETQEVNT EQU   *                                                 CLUP21
         STCM  RC,AD,ONE(RJ)            STORE ADDRESS OF QEVENT  X01004
*                                        SUBTASK IN LAST CHAIN   X01004
*                                        POINTER                 X01004
         TM    0(RPARC),OUTONLY         OPEN FOR OUTPUT ONLY     CLUP21
         BNO   STARTEST                 NO, BRANCH               CLUP21
         STCM  RC,AD,LCBRSLNK           STORE QEVENT SUBTASK ADDRX01004
*                                        INTO STCB LINK FIELD OF X01004
*                                        LCB                     X01004
*                                                                CLUP21
STARTEST EQU   *                                                 CLUP21
         AR    RCORE,RD                 BUMP TO NEXT LCB         CLUP21
         BCT   RE,LCBLOOP               DECREMENT LINE COUNT AND CLUP21
*                                         BRANCH                 CLUP21
         STC   RTIOT,DEBNMSUB           STORE THE COUNT OF LOADED
*                                         SUBROUTINES IN THE DEB
*
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        START I/O ON ALL LINES                                   *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                CLUP21
         SR    RD,RD                    CLEAR THE LINE COUNT
         IC    RD,DEBNMEXT              NUMBER OF LINES
         SR    RC,RC                    SIZE REGISTERS
         IC    RC,DCBEIOBX              SIZE OF AN LCB
         L     RCORE,DCBIOBAD           LOAD THE ADDRESS OF THE
*                                         FIRST IOB
         SR    RUCB,RUCB                CLEAR AN INDEX REGISTER
*                                                                CLUP21
NEXTLINE EQU   *
         LA    RF,0(RCORE,RC)           LOAD THE IOB ADDRESS
*                                         FOR IOS
         AR    RCORE,RC                 BUMP THE POINTER TO THE
*                                         NEXT LCB
         L     RTIOT,DEBUCBAD(RUCB)     LOAD THE ADDRESS OF THE
*                                         FIRST/NEXT UCB ADDRESS
         LTR   RTIOT,RTIOT              IS THIS A // DD DUMMY ENTRY
         BZ    BCT                      YES, BRANCH
*
         DROP  RCORE                                            SA62408
         USING LCBFLAG1,RCORE           IOB ADDRESSABILITY      SA62408
         CLI   LCBSTAT1,AVTEZERO        OPEN IDLE REQUESTED     SA62408
         BNE   EXCPX                    BRANCH NO - DO EXCP     SA62408
         SPACE 1                                                SA62408
         MVI   LCBTPCD,AVTEZERO         INDICATE OPEN COMPLETE  SA62408
         B     BCT                      CONTINUE WITH NEXT LCB  SA62408
         SPACE 1                                                SA62408
EXCPX    EQU   *                                                SA62408
         EXCPVR (1),SUBSYS                                       X01004
*
BCT      EQU   *
         LA    RUCB,FOUR(RUCB)          INCREMENT THE INDEX REGISTER
         BCT   RD,NEXTLINE              BRANCH IF THERE ARE MORE
*                                         LINES TO START
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
*        THIS SECTION OF CODE DETERMINES IF THIS EXECUTOR IS     CLUP21
*        REQUIRED AGAIN (MULTIPLE DCB'S BEING OPEN'ED) AND IF SO CLUP21
*        RESTARTS. AFTER ALL DCB'S HAVE BEEN PROCESSED,          CLUP21
*        PARAMETERS ARE SET UP FOR TRANSFER OF CONTROL TO THE    CLUP21
*        LAST LOAD OF LINE OPEN - IGG01948.                      CLUP21
*                                                                CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
         MVC   AVTEZERO(TWO,RWTGC),TCAMLD7 ID FOR              @Z30X8NM
*                                         LAST LOAD OF LINE  OPEN
         L     RJ,4(RWTGC)              ADDRESS OF OPEN WORK AREA
*                                                                CLUP21
RELOOP   EQU   *
         LA    RWTGC,8(RWTGC)           NEXT WTG ENTRY
         LA    RPARC,4(RPARC)           NEXT DCB LIST ENTRY
         CLC   0(2,RWTGC),AMIDCNST      IS THIS EXECUTOR NEEDED
*                                         AGAIN
         BE    RESTART                  BRANCH IF YES            S22025
         CLC   0(2,RWTGC),OPIDCNST      END OF WTG TABLE
         BNE   RELOOP                   BRANCH IF NO             S22025
         LR    RPARC,RPAR               REINITIALIZE WTG AND
         LA    RWTGC,32(RWTG)           PARAMETER REGISTERS
*                                                                CLUP21
ZCHECK   EQU   *
         CLI   0(RWTGC),X'00'           IS THIS ENTRY ZERO
         BNE   XCTLRTNE                 IF NOT ZERO, TRANSFER CNTRL
         LA    RWTGC,8(RWTGC)           ADDRESS OF NEXT ENTRY
         LA    RPARC,4(RPARC)           ADDRESS OF NEXT DCB
         B     ZCHECK                   CHECK NEXT ENTRY         S22025
*
XCTLRTNE EQU   *
         LA    RJ,DXCCW12               ADDRESS OF A DOUBLE WORD Y02027
*                                         SAVE AREA
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID TO NAME FIELD
         MVC   14(3,RWTG),2(RWTGC)      MOVE TTR TO WTG TABLE
         XCTL  DE=(RWTG),SF=(E,(15))
*
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        STANDARD LOAD SUBROUTINE. UPON ENTRY REGISTER 13 POINTS CLUP21
*        TO THE TWO-CHARACTER ID OF THE MODULE TO BE LOADED. THE CLUP21
*        APPROPRIATE ATTRIBUTES ARE SET AND THE LOAD SVC ISSUED. CLUP21
*        THE TWO-CHARACTER ID IS PLACED IN THE DEB AND THE COUNT CLUP21
*        OF LOADED MODULES INCREMENTED. UPON EXIT REGISTER 13    CLUP21
*        CONTAINS THE ADDRESS OF THE LOADED ROUTINE, REGISTER 0  CLUP21
*        CONTAINS ITS ADDRESS + 2.                               CLUP21
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                CLUP21
LOADRTNE EQU   *
         MVC   6(2,RWTG),0(RC)          MOVE ID TO WTG TABLE
         LR    RE,RWTG                  LOAD THE ADDRESS OF THE
*                                         PARAMETER LIST
         L     RF,CVTPTR                ADDRESS OF CVT           Y02027
         L     RF,CVTSVDCB-CVTD(RF)     GET ADDRESS OF THE       Y02027
*                                       DCB FOR SVC LIB          Y02027
         LOAD  EPLOC=(0),DCB=(1)        LOAD THE ROUTINE         S21903
         MVC   0(2,RUCB),0(RC)          MOVE SUB-ROUTINE ID TO THE
*                                         DEB
         LA    RUCB,2(0,RUCB)           BUMP POINTER TO NEXT ID
*                                         POSITION IN THE DEB
         LA    RTIOT,1(0,RTIOT)         KEEP A COUNT OF ALL LOADED
*                                         SUBROUTINES
         LR    RC,RE                    SAVE ADDRESS             CLUP21
         LA    RE,TWO(RC)               BUMP BY TWO              CLUP21
         BR    RD                       RETURN
         EJECT
OPIDCNST DC    C'0S'                    FLAG INDICATING END OF   S22025
*                                       'WHERE TO GO' TABLE      S22025
ONE      EQU   1                        NUMERIC CONSTANT
TWO      EQU   2                        NUMERIC CONSTANT         Y02027
THREE    EQU   3                        NUMERIC CONSTANT         Y02027
FOUR     EQU   4                        NUMERIC CONSTANT         Y02027
FIVE     EQU   5                        NUMERIC CONSTANT         Y02027
UCBDVCLS EQU   18                       UCB DEVICE CLASS         Y02027
AD       EQU   7                        ICM/STCM MASK            X01004
IOBPTR   EQU   32                       IOB OFFSET IN LCB        Y02027
ENDCHAIN EQU   X'00'                    LAST ELEMENT ON CHAIN    Y02027
GRAPHICS EQU   X'10'                    2260 LOCAL               Y02027
BUFTSCHD EQU   X'1A'                    BUFFERED TERMINAL        Y02027
OUTONLY  EQU   X'0F'                    OUTPUT ONLY              Y02027
DIAL     EQU   X'90'                    TO TEST FOR DIAL LINE    CLUP21
RLD      EQU   X'EF'                    TO INDICATE RLD PRESENT  CLUP21
HIGHPRI  EQU   X'FC'                    PRIORITY                 Y02027
AMIDCNST DC    C'40'                    ID FOR THIS EXECUTOR     Y02027
DEVCLASS EQU   18                       DEV CLASS OFFSET IN UCB  S22027
         EJECT
RECV2260 DC    C'Q1'                    2260L RECEIVE SCHEDULER  CLUP21
RECVSCHD DC    C'R3'                    TCAM RECEIVE SCHEDULER   CLUP21
SENDCMBF DC    C'RE'                    COMBUF SEND SCHEDULER    S22027
DIALSCHD DC    C'R1'                    ID FOR THE DIAL SCHD     S22027
SENDMINI DC    C'Q6'                    MINI SEND SCH- NON DIAL, S22026
SENDNTSO DC    C'Q7'                    SEND SCHED - N/N TSO/2741CLUP21
SENDCONC DC    C'Q9'                    CONC SEND SCHEDULER ID   S22026
SENDSCHD DC    C'R4'                    TCAM SEND SCHEDULER      CLUP21
BUFTRMSH DC    C'RD'                    BUFFERED TERMINAL SCHED  CLUP21
TCAMLD7  DC    C'48'                    LAST LOAD OF OPEN
         EJECT
         TAVTD                          AVT DSECT
         EJECT
         TTCXD                                                   Y02027
         EJECT
FORCORE DSECT
         IECDSECT                                                Y02027
         EJECT
CVTD     DSECT                                                   Y02027
         CVT                                                     Y02027
         EJECT
         TLCBD
         TDEBD
         DCBD  DSORG=TX
         END
