304G     TITLE 'IGE0304G-UNIT CHECK OTHER THAN READ,WRITE, OR POLL'
IGE0304G CSECT
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*
*A348000                                                         A50195
*C596000                                                         A50195
*D792000,836000,860000,868000,880000-884000,900000               A50195
*A752000,892000                                                 SA61781
*A100000-102000,114000,152000,210000,218000,290000,321000-322000,Y02027
*A338000,351000,922000                                           Y02027
*C068000,080000,096000-098000,112000,148000,150000,216000,300000,Y02027
*C648000,648600,648800-648900,650200                             Y02027
*C377120                                                       @OS76315
*A894000                                                       @OS76315
         SPACE 3
***********************************************************************
*                                                                     *
*MODULE-NAME = IGE0304G                                               *
*                                                                     *
*DESCRIPTIVE-NAME = ERP FOR UNIT CHECK AND UNIT EXCEPTION FOR STSP    *
*                   AND BSC NON-READ/WRITE OR POLL COMMANDS           *
*                                                                     *
*COPYRIGHTS = 'NONE'                                                  *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
*FUNCTION -- TO ATTEMPT RETRY ON INTERRUPTED CCW SEQUENCES            *
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
*EXTERNAL ROUTINE -- IGG019QE (AOS/TCAM SIO APPENDAGE) -- TO CONVERT  *
*   VIRTUAL CCW ADDRESSES TO REAL BEFORE RETRY.                       *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'ERP IN CONTROL (IOSERR)' AND 'EXCEPTIONAL   Y02027
*                CONDITION(IOSEX)'. RETURN TO IOS TO RETRY THE   Y02027
*                   ERROR.                                       Y02027
*        SVC   3                                                      *
*                                                                     *
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*        SVC   3                                                      *
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO I/O SUPERVISOR BLOCK                Y02027
*        SVC   15   'EXCEPTIONAL CONDITION(IOSEX)'. RETURN TO    Y02027
*                   LINE END APPENDAGE. ERROR PERMANENT.         Y02027
*        SVC   3                                                      *
*                                                                     *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*   TO SCHEDULE ERROR POST                                            *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TCCWD                                                             *
*   TLCBD                                                             *
*   TSCBD                                                             *
*   IECDIOSB                                                     Y02027
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
*   THIS MODULE DOES NOT SERVICE ERRORS INVOLVING THE AUDIO RESPONSE  *
*   UNIT OR 2260 LOCAL.                                               *
*                                                                     *
***********************************************************************
         EJECT
         USING *,R15
         USING LCBFLAG1,RLCB
         USING IEDQCCW,RCCW
         USING IEDQAVTD,RAVT
         USING IEDQSCB,RSCB
         USING IHADCB,RDCB
         USING IOSB,RIOSB                                        Y02027
*              REGISTERS
R0       EQU   0
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RIOB     EQU   2                        IOB REGISTER - REQUIRED BY
*                                       SIO APPENDAGE.
RUCB     EQU   3                        UCB REGISTER
RLCB     EQU   4                        LCB REG.
RSCB     EQU   5                        SCB REGISTER
RCCW     EQU   6                        CCW REGISTER
RWKA     EQU   7                        WORK REGISTER
RTST     EQU   7                        ADDR OF REQUEST QUEUE    Y02027
*                                       ELEMENT                  Y02027
RSIO     EQU   8                        SIO APPENDAGE ADDRESS.
RWKB     EQU   9                        WORK REGISTER
RDCB     EQU   10
RAVT     EQU   11                       AAVT BASE
RLINK    EQU   13                       LINKAGE FOR SUBSEQUENT LOAD
RSAVE    EQU   13                       SAVE REGISTER FOR IOSB   Y02027
RXCTL    EQU   14                       XCTL REGISTER
R15      EQU   15                       ADDRESSABILITY REGISTER
         EJECT
IGE0304G IEDHJN DATE                                             S22025
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         L     RLCB,IOBOFFST(R0,RTST)   ADDRESS OF IOB
         L     RSCB,LCBSCBA-1           SET SCB BASE
         L     RDCB,LCBDCBPT            GET DCB B ASE
         L     RCCW,LCBCSW-1            FAILING CCW+8
         SH    RCCW,H8                  ADJUST TO ENDING CCW
         TM    LCBCSW+3,UNITCHK         UNIT CHECK SET
         BZ    ITSUNEX                  NO
         TM    LCBSTAT2,LCBSYNC         BSC LINE
         BZ    NOBSC                    BRANCH NO
         SPACE
         L     RWKB,LCBSTART-1          START ADDRESS          @OS76315
         LA    RWKB,ZERO(RWKB)          CLEAR FOR SUBTRACT     @OS76315
         LA    RWKA,LCBCPA              START OF CH PGM AREA   @OS76315
         SR    RWKB,RWKA                INTERRUPT BEFORE CPA   @OS76315
         LR    RWKA,RWKB                SAVE DIFFERNCE         @OS76315
         BM    NODIALUP                 NOT IN CH PG AREA      @OS76315
         SRL   RWKB,THREE               DIVIDE BY 8 FOR INDEX  @OS76315
         LA    RWKB,LCBTPCD(RWKB)       GET OP CODE FOR COMPARE@OS76315
         CLI   ZERO(RWKB),TPWRDLET      WRITE DLE EOT?         @OS76315
         BNE   NODIALUP                 BRANCH IF NO
         SPACE
         LA    RWKA,LCBCPA+8(RWKA)      ADDRESS OF DISABLE     @OS76315
         ST    RWKA,LCBSTART-1          SET START ADDRESS
NODIALUP EQU   *
         SPACE
         TM    LCBSENS0,CMDREJ          COMMAND REJECT
         BO    CLEARIT                  BRANCH YES
NOBSC    EQU   *
         SPACE
         TM    LCBSENS0,EQUIPCHK        EQUIPMENT CHECK SET?
         BO    SNO                      YES
         SPACE
         TM    LCBSENS0,LOSTD           LOST DATA SET
         BO    TESTDIAL                 YES SEE IF DIAL CCW
         SPACE
         TM    LCBSENS0,TIMEOUT         TIME OUT SET
         BO    CHKOPCDE                 YES-TEST OP CODE
         SPACE
         TM    LCBSENS0,INTREQ          INTERVENTION REQUIRED
         BO    CHKPREP                  YES-TEST IF PREPARE OR DIAL
         SPACE
         TM    LCBSENS0,BUSOUT          BUSOUT SET
         BO    TESTDIAL                 YES-CHECK IF DIAL
         SPACE
         TM    LCBSENS0,DATACHCK        DATA CHECK
         BO    SNO                      YES
         SPACE
         TM    LCBSENS0,OVERRUN         OVERRUN SET
         BO    CHANERR                  BRANCH IF YES
         SPACE
         B     CHKRETRY                 RETRY COMMAND REJECT-NONBSC
         SPACE
CLEARIT  EQU   *
         SPACE
*        WRITE DLE ENQ ADAPTER IS HUMG IN TRANSPARENT MODE
         MVI   LCBINCAM+1,DLENQRET      SET 404H RETURN
         MVC   LCBCSWSV,LCBCSW          SAVE CURRENT STATUS
         MVC   LCBSNSV,LCBSENS0         SAVE SENSE
         L     RWKA,DCBSCTAD-1          SCT ADDRESS
         SR    RWKB,RWKB                CLEAR INDEX REGISTER
         IC    RWKB,DLEENQCH(RWKA)      GET DLE ENQ INDEX
         LA    RWKA,1(RWKB,RWKA)        POINT TO SEQUENCE
         ST    RWKA,LCBERCCW            SET IN CCW
         MVI   LCBERCCW,CCWWRITE        SET COMMAND CODE
         LA    RWKA,2                   COUNT
         ST    RWKA,LCBERCCW+4          SET NO FLAGS AND COUNT
         LA    RWKA,LCBERCCW            START ADDRESS
         ST    RWKA,LCBSTART-1          SET FOR IOS
         B     CONVERT                  BRANCH TO SET UP FOR RETRY.
         SPACE 3
         SPACE
CHANERR  EQU   *
         OI    SCBERR4,SCBCHANN         SET DHANNEL ERROR
         B     SNO1                     EXIT
         EJECT
CHKOPCDE EQU   *
         TM    CCWOPCDE,X20             DISABLE, ENABLE, OR DIAL
         BO    CHKRETRY                 YES
         SPACE
CHKPREP  EQU   *
         CLI   CCWOPCDE,CCWPREP         PREPARE
         BE    PERMEXIT                 BRANCH YES               A50195
         SPACE
TESTDIAL EQU   *
         CLI   CCWOPCDE,CCWDIAL         DIAL COMMAND
         BNE   CHANERR                  BRANCH NO, SHOULD NOT OCCUR
         SPACE 5
CHKRETRY EQU   *
         CLI   LCBINCAM,RETRY           RTRY LIMIT REACHED
         BNL   PERMEXIT                 YES
         SPACE
         IC    RWKA,LCBINCAM            RETRY COUNT
         LA    RWKA,ONE(R0,RWKA)        ADD ONE
         STC   RWKA,LCBINCAM            SAVE BACK
         MVI   IOSSNS,AVTEZERO          CLEAR SENSE FOR RETRY    Y02027
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE IS ADDED FOR AOS.                         *
*                                                                     *
CONVERT  EQU   *
         CLI   LCBECBCC,HIOCC           IOHALT ISSUED            Y02027
         BNE   SVCS                     BRANCH NO                Y02027
         SPACE 1                                                 Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CTL      Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
SVCS     EQU   *                                                 Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        LINE CONTROL BLOCK TO THE I/O SUPERVISOR BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY OPERATIONS FROM ERP          Y02027
**************************************************************** Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                       INTO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM LCB TO IOSB    Y02027
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         SR    RIOB,RIOB                CLEAR FOR ICM.
         ICM   RIOB,7,IOBPTR(RTST)      GET IOB ADDRESS FROM RQE,
*                                       FOR USE BY SIO APPENDAGE.
         SR    RSIO,RSIO                CLEAR FOR ICM.
         ICM   RSIO,7,DEBPTR(RTST)      GET DEB ADDRESS FROM RQE.
         LA    R15,DEBNMSUB-IEDQDEB     GET APDG. TABLE SIZE     Y02027
         SLR   RSIO,R15                 POINT TO APDG. TABLE     Y02027
         L     RSIO,SIOPTR(ZERO,RSIO)   GET ADDR OF SIO APDG.    X01004
*                                       FROM APDG. VECTOR TABLE.
         SR    R15,R15                  CLEAR R15 TO TELL SIO
*                                       APDG. IT'S A SUBROUTINE.
         BAL   RXCTL,SIOENTRY(ZERO,RSIO)  LNK TO SIO APDG AT     X01004
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                       OFFSET +16 TO CONVERT CCW'S
*                                       FROM VIRTUAL TO REAL.
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
         SPACE
         SVC   EREXCP
         SPACE
         SVC   RETURN                   RETURN TO IOS
         SPACE 5
ITSUNEX  EQU   *
         CLI   CCWOPCDE,CCWPREP         PREPARE COMMAND
         BE    CHKRETRY                 YES
         SPACE
         OI    SCBERR3,SCBXCEPN         SET UNIT EXCEPTION ERROR
         SPACE 3
SNO      EQU   *
         OI    SCBERR4,SCBCTLUN         CONTROL UNIT ERROR
SNO1     EQU   *
         MVI   LCBINCAM+1,POSNO         SET RETURN FOR 504G
         B     LOADSDR                  USE COMMON CODE
         SPACE
PERMEXIT EQU   *
         MVI   LCBINCAM+1,POPERM        SET RETURN FOR 504G
LOADSDR  EQU   *
         TM    CCWOPCDE,X'20'           DISABLE,ENABLE OR DIAL
         BZ    NONDIAL                  BRANCH NONE OF ABVOE
         SPACE
         OI    SCBERR4,SCBCONNN         SET CONNECT/DISCONNECT ERR
         MVI   LCBTSTSW,AVTEFF          INSURE A NEW DIAL UP FOR
*                                       NEXT MESSAGE
         NI    LCBTPCD+11,AVTEFF-DISDLEOT DON'T WRITE DICCONNECTSA61781
*                                       PRIOR TO DISABLE        SA61781
NONDIAL  EQU   *
         LH    RLINK,POSTMOD            504G ID
         L     RXCTL,CVTPTR             CVT ADDRESS
         L     RXCTL,XCTLADD(,RXCTL)
         BR    RXCTL                    LINK TO SCTL
         EJECT
*              CONSTANTS
POSTMOD  DC    AL2(5047)                IGE0504G ID              Y02027
H8       DC    H'8'                     HALFWORD = 8             Y02027
X20      EQU   X'20'                    MASK FOR DISAB, DIAL, ENAB
ONE      EQU   1
ZERO     EQU   0                        LENGTH EQUATE FOR ADDR   X01004
UNITCHK  EQU   X'02'                    MASK FOR UNIT CHECK
LOSTD    EQU   X'02'                    LOST DATA MASK
TIMEOUT  EQU   X'01'                    TIMEOUT MASK
INTREQ   EQU   X'40'                    INTERVENTION REQUIRED
BUSOUT   EQU   X'20'
CMDREJ   EQU   X'80'                    COMMAND REJECT MASK-SENSE
OVERRUN  EQU   X'04'                    OVERRUN MASK IN SENSE BIYTE
RETRY    EQU   X'02'                    RETRY LIMIT
CVTPTR   EQU   X'10'                    CVT LOCATION POINTER
XCTLADD  EQU   X'2C'                    OFFSET IN CVT-XCTL ROUTINE
DATACHCK EQU   X'08'                    DATA CHECK IN SENSE BYTE
EREXCP   EQU   15                       ERROR EXCP
RETURN   EQU   3                        ETURN SVC FOR TRANSIENT S
EQUIPCHK EQU   X'10'                    MASK FOR EQUIPMENT CHECK
IOBOFFST EQU   4                        IOB OFFSET IN RQE
DLENQRET EQU   28                       RETURN FOR 404H
DLEENQCH EQU   17                       INDEX INTO SCT FOR SEQUENCE
POSNO    EQU   16                       RETURN INDDICATOR
POPERM   EQU   20                       RETURN INDICATOR FOR 504G
IOBPTR   EQU   5                        OFFSET OF IOB ADDR. IN RQE.
DEBPTR   EQU   9                        OFFSET OF DEB ADDR. IN RQE.
SIOPTR   EQU   4                        OFFSET OF SIO APDG. ADDRESS
*                                       IN APDG. VECTOR TABLE.
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
*                                       POINT IN SIO APPENDAGE.
HIOCC    EQU   X'48'                    IOHALT COMPLETION CODE   Y02027
TWO      EQU   2                        CONSTANT OF 2            Y02027
SEVEN    EQU   7                        CONSTANT OF 7            Y02027
DISDLEOT EQU   X'01'                    TO RESET DISCONNECT     SA61781
THREE    EQU   3                        FOR DIVIDE BY EIGHT    @OS76315
         EJECT
         TDEBD                          DEB DSECT
         EJECT
         TSCBD
         TTPD
         DCBD  DSORG=TX
         TLCBD
         TCCWD
         TAVTD
         IECDIOSB                                                Y02027
         END
