19Q1     TITLE 'IGG019Q1 - LOCAL RECEIVE SCHEDULER'
IGG019Q1 CSECT
         SPACE 3
***********************************************************************
*                                                              @Y17XAIL
* MODULE NAME = IGG019Q1 (TCAM,I/O)                            @Y17XAIL
*                                                              @Y17XAIL
* DESCRIPTIVE NAME = LOCAL RECEIVE SCHEDULER                   @Y17XAIL
*                                                              @Y17XAIL
* COPYRIGHT = NONE                                             @Y17XAIL
*                                                              @Y17XAIL
* STATUS = VERSION 10.0                                        @Y17XAIL
*                                                              @Y17XAIL
* FUNCTION = TO SCHEDULE A RECEIVE OPERATION WHEN THE          @Y17XAIL
* ATTENTION ELEMENT IS POSTED AND TO SET THE LINE 'FREE'       @Y17XAIL
* AFTER A RECEIVE OPERATION.                                   @Y17XAIL
*                                                              @Y17XAIL
*                                                              @Y17XAIL
* FOR 3705, RECEIVE OPERATIONS ARE SCHEDULED AFTER             @Y17XAIL
* ATTENTION OR UPON READ REQUEST. THE CHANNEL PROGRAM TO       @Y17XAIL
* READ IS BUILT HERE AND STARTIO IS ISSUED (VIA AQCTL) HERE    @Y17XAIL
* UNLESS OUTPUT                                                @G36XRIB
* IS ON THE OUTPUT CORE QUEUE, WHICH RESULTS IN BYPASS         @Y17XAIL
* TO THE WRITE SCHEDULER TO BUILD WRITE CHANNEL                @Y17XAIL
* PROGRAMS ALSO.                                               @Y17XAIL
*                                                              @Y17XAIL
* NOTES = SEE BELOW                                            @Y17XAIL
*                                                              @Y17XAIL
*    DEPENDENCIES = NONE                                       @Y17XAIL
*                                                              @Y17XAIL
*    RESTRICTIONS = NONE                                       @Y17XAIL
*                                                              @Y17XAIL
*    REGISTER CONVENTIONS = SEE REGISTER EQUATES               @Y17XAIL
*                                                              @Y17XAIL
*    PATCH LABEL = NONE                                        @Y17XAIL
*                                                              @Y17XAIL
* MODULE TYPE = PROCEDURE                                      @Y17XAIL
*                                                              @Y17XAIL
*    PROCESSOR = XF ASSEMBLER                                  @Y17XAIL
*                                                              @Y17XAIL
*    MODULE SIZE =                                             @Y17XAIL
*                                                              @Y17XAIL
*    ATTRIBUTES = ENABLED,PROBLEM PROGRAM MODE,                @Y17XAIL
*    REUSABLE, REFRESHABLE                                     @Y17XAIL
*                                                              @Y17XAIL
* ENTRY POINT = FIRST EXECUTABLE INSTRUCTION                   @Y17XAIL
*                                                              @Y17XAIL
*    PURPOSE = SEE FUNCTION                                    @Y17XAIL
*                                                              @Y17XAIL
*    LINKAGE = L   R15,AVT2260L                                @Y17XAIL
*              BR  R15                                         @Y17XAIL
*                                                              @Y17XAIL
* INPUT = REGISTERS INPUT                                      @Y17XAIL
*    REGISTER 1  = ADDRESS OF ATTENTION ELEMENT, LCB,          @Y17XAIL
*                  OR ERB                                      @Y17XAIL
*    REGISTER 11 = DISPATCHER BASE                             @Y17XAIL
*    REGISTER 13 = ADDRESS OF AVTSAVE2                         @Y17XAIL
*    REGISTER 15 = ENTRY POINT ADDRESS                         @Y17XAIL
*                                                              @Y17XAIL
* OUTPUT = REGISTER OUPUT                                      @Y17XAIL
*    REGISTER 1  = ADDRESS OF ELEMENT TO POST                  @Y17XAIL
*    REGISTER 11 = DISPATCHER BASE                             @Y17XAIL
*    REGISTER 13 = ADDRESS OF AVTSAVE2                         @Y17XAIL
*                                                              @Y17XAIL
* EXIT-NORMAL = TCAM DISPATCHER                                @Y17XAIL
*                                                              @Y17XAIL
* EXIT-ERROR = NONE                                            @Y17XAIL
*                                                              @Y17XAIL
* EXTERNAL REFERENCES = SEE BELOW                              @Y17XAIL
*                                                              @Y17XAIL
*    ROUTINES = IEDAYZ (TSO SCHEDULER)                         @Y17XAIL
*               IEDQTNT                                        @Y17XAIL
*                                                              @Y17XAIL
*    DATA AREAS = LCB CHANNEL PROGRAM AREA                     @Y17XAIL
*                 INVITATION LIST                              @Y17XAIL
*                                                              @Y17XAIL
*    CONTROL BLOCKS = AVT                                      @Y17XAIL
*                     DCB                                      @Y17XAIL
*                     LCB                                      @Y17XAIL
*                     QCB                                      @Y17XAIL
*                     SCB                                      @Y17XAIL
*                     STCB                                     @Y17XAIL
*                     TRM                                      @Y17XAIL
*                     TSID                                     @Y17XAIL
*                                                              @Y17XAIL
* TABLES = NONE                                                @Y17XAIL
*                                                              @Y17XAIL
* MACROS = EXCPVR                                              @Y17XAIL
*          GETMAIN                                             @Y17XAIL
*          IEDHJN                                              @Y17XAIL
*                                                              @Y17XAIL
* CHANGE ACTIVITY = SEE BELOW                                  @Y17XAIL
*A-000000-999999                                               @Y16X5I0
*A248000,426000,500000,504000,668000,700000,780000               S22025
*C128000,130000,220000,224000                                    S22025
*C328000,348000,384000-388000,432000,452000,468000-476000,500000 S22025
*C532000                                                         S22025
*D284000,436000-444000,456000460000,464000-467000,508000-524000  S22025
*C128000,130000,220000,224000                                    S22025
*C396000                                                        SA54274
*D577000                                                        SA54274
*C404000                                                        SA54274
*A534600                                                        SA56231
*D426600-472000,501700-502200,A426600-466600                    SA56606
*A378400,426000,476000,529000,627530,627830,714000               S22024
*C032000,684000                                                  S22024
*D736000,744000,752000,760000,784000,792000,800000,808000        S22024
*A380000                                                        SA62315
*C500100-500200                                                 SA69626
*A504860                                                         Y06327
*D384000,385000                                                @XA05314
*A384000                                                       @XA05314
*C384300                                                       @XA07484
*A378800                                                       @ZA02641
*A535000                                                       @SA73490
*D588000-600000,616000-620000                                  @SA73490
*C426220                                                       @YA08428
*D535020-535180,A584000,A612000                                @SA75437
*A216000,280000,292000,300000,332000,368000,378000,398000      @Y17XAIL
*A416000,726000,740000,781000,796000,812000                    @Y17XAIL
*C016000-212000,296000,312000,320000,328000,368000,378400      @Y17XAIL
*C378850-379550,384200,385400,400000-408000,426050             @Y17XAIL
*C426150-426500,480000,500900,534600,670950                    @Y17XAIL
*D220000-228000,250000,264000,304000,477000-478000,            @Y17XAIL
*D504860-504920,506660-506720,528000-530000,698000,702000      @Y17XAIL
*D726000,732000                                                @Y17XAIL
*C506600                                                       @OY14092
*A272000,422840,680000                                         @G36XRIB
*C008721                                                       @G36XRIB
*D422900,776000-778000                                         @G36XRIB
*A552000,A564000,A568000                                       @OY20448
*                                                              @Y17XAIL
***********************************************************************
         EJECT
************************************************************** @Y17XAIL
*                                                            * @Y17XAIL
*        USINGS AND REGISTER EQUATES                         * @Y17XAIL
*                                                            * @Y17XAIL
************************************************************** @Y17XAIL
         SPACE 1
         USING IEDQRECB,RRECB           RECB ADDRESSIBILITY    @YM08074
         USING IEDQSTCB,RSTCB
         USING IEDQLCB,RLCB
         USING IEDQPRF,RWORK            BUFFER ADDRESSABILITY  @Y17XAIL
         USING IEDQQCB,RQCB
         USING IEDNTRM,RTRM             TTE ADDRESSABILITY     @Y17XAIL
         USING IHADCB,RDCB
         USING IEDQDISP,RSUPBASE
         USING AVTSAVE2,R13
         SPACE 2
R0       EQU   0                        REG 0                    S22024
R1       EQU   1                                               @G36XRIB
RRECB    EQU   1                        ELEMENT BASE
RA       EQU   2                        WORK REGISTER
RSTCB    EQU   3                        STCB BASE              @Y17XAIL
RLCB     EQU   4                        LCB BASE
RWORK    EQU   6                        TEMPORARY WORK REG     @Y17XAIL
RQCB     EQU   7                        ENTRY QCB ADDRESS (LCB)
RTRM     EQU   8                        TERM ENTRY BASE        @Y17XAIL
RCURRENT EQU   8                        CURRENT INVITATION     @Y17XAIL
*                                       LIST ENTRY             @Y17XAIL
RILIST   EQU   9                        INVITATION LIST ADDRESS
RDCB     EQU   10                       DCB ADDR               @Y17XAIL
RSUPBASE EQU   11                       DISPATCHER BASE REGISTER
R12      EQU   12                       WORK REGISTER          @Y17XAIL
R13      EQU   13                       SAVE AREA ADDRESS (AVTSAVE2
RRET     EQU   14                       RETURN   REGISTER      @Y17XAIL
R15      EQU   15                       ENTRY BASE REGISTER
         EJECT
************************************************************** @Y17XAIL
*                                                            * @Y17XAIL
*        ESTABLISH ADDRESSABILITY AND CHECK FOR ATTENTION    * @Y17XAIL
*        ELEMENT AS INPUT.                                   * @Y17XAIL
*                                                            * @Y17XAIL
************************************************************** @Y17XAIL
         SPACE 1                                               @Y17XAIL
         DC    AL1(DSPMCPL2)            ACTIVATION KEY OF STCB @Y17XAIL
         DC    AL1(0)                   ENTRY PT FOLLOWS STCB  @Y17XAIL
         USING *,R15                    BASE FOR IGG019Q1      @Y17XAIL
IGG019Q1 IEDHJN START                                            S22025
         LA    RLCB,AVTEZERO(,RRECB)    GET LCB FOR BASE
         CLI   RECBPRI,PRIATTN          ATTENTION ELEMENT
         BE    ATTN                     BR YES, HANDLE ATTN    @YM08074
         CLI   RECBPRI,PRIATN05         3705 ATTENTION ELEM    @YM08074
         BNE   NOTATTN                  NO, BRANCH             @Y17XAIL
         EJECT                                                 @Y17XAIL
************************************************************** @Y17XAIL
*                                                            * @Y17XAIL
*        PROCESSING FOR INPUT = ATTENTION ELEMENT            * @Y17XAIL
*                                                            * @Y17XAIL
************************************************************** @Y17XAIL
         SPACE 1                                               @Y17XAIL
         SH    RLCB,LCBBKUP5            GET 3705 LCB ADDRESS   @YM08074
         LA    RRECB,LCBATL05           3705 ATTENTION ELEMENT @YM08074
         B     GETDCB                   BR TO GET DCB          @YM08074
ATTN     EQU   *                                               @YM08074
         SH    RLCB,LCBBKUP             BACK UP TO LCB
         LA    RRECB,LCBATTEL           LOCAL ATTENTION ELE    @YM08074
GETDCB   EQU   *                                               @YM08074
         L     RDCB,LCBDCBPT            GET DCB ADDRESS             TSO
         MVI   RECBPRI,AVTEZERO         CLR ATTN ELEMENT PRI   @YM08074
         TM    DCBDSRG2,DCBDSGTR        IS THE DCB FOR 3705 ?  @Y17XAIL
         BNO   FREEATN                  BRANCH ON NO             S22024
         LR    RTRM,R15                 SAVE BASE ADDRESS      @Y17XAIL
         LH    RRECB,LCBTTCIN           GET TNT OFFSET         @Y17XAIL
         N     RRECB,AVTCLRHI           CLEAR HI ORDER BYTES   @Y17XAIL
         L     R15,AVTRNMPT             GET ADDR OF TNT LOOKUP @Y17XAIL
*                                       CODE                   @Y17XAIL
         BALR  RRET,R15                 CALL TNT LOOKUP        @Y17XAIL
         LR    R15,RTRM                 RESTORE BASE ADDRESS   @Y17XAIL
         LR    RTRM,RRECB               SET TERM ENTRY ADDR    @Y17XAIL
         SH    RTRM,PRFSZ               BACK UP TO TERM ENTRY  @Y17XAIL
*                                       PREFIX                 @Y17XAIL
         TM    TRMBYTE2,TRMRSACT+TRMINPG IS NCP ACTIVE OR IS   @Y17XAIL
*                                       ACTPU IN PROGRESS      @Y17XAIL
         BNZ   FREEATN                  YES, BRANCH            @Y17XAIL
         L     RA,LCBRECOF              GET WORD THAT CONTAINS @Y17XAIL
*                                       THE ATTENTION FLAG     @Y17XAIL
TRYAGN1  EQU   *                                               @Y17XAIL
         LR    RRET,RA                  GET COPY OF WORD       @Y17XAIL
         N     RRET,RESATTN             RESET ATTENTION FLAG   @Y17XAIL
         CS    RA,RRET,LCBRECOF         WAS WORD UPDATED       @Y17XAIL
*                                       ASYNCHRONOUSLY         @Y17XAIL
         BNE   TRYAGN1                  YES, TRY AGAIN         @Y17XAIL
         B     DSPDISP                  EXIT                   @ZA02641
         EJECT                                                 @Y17XAIL
FREEATN  EQU   *                                                 S22024
         NI    LCBTSTSW,XF0             FREE UP ATTEN ELEMENT
         TM    LCBSTAT1,LCBFREEN        LCB FREE ?             @XA05314
         BO    POSTAWAY                 BRANCH YES             @XA05314
         TM    DCBDSRG2,DCBDSGTR        3705 DCB?              @Y17XAIL
         BO    DSPDISP                  BRANCH YES             @XA07484
         MVI   LCBTSTSW,X'00'           CLR SW FOR DEFERRED    @XA05314
         B     DSPDISP                  READ                   @XA05314
POSTAWAY EQU   *                                               @XA05314
         SPACE
         TM    DCBDSRG2,DCBDSGTR        IS THE DCB FOR 3705 ?  @Y17XAIL
         BO    POST1                    YES, GO POST LCB         S22024
         SPACE 2
         TM    AVTBIT1,AVTTSON          IS TSO IN SYSTEM            TSO
         BO    TSO3                     BRANCH ON YES               TSO
         SPACE
         NI    LCBTSTSW,X'0F'           RESET FLAG, LEAVING     SA54274
*                                         POSTED FLAG UNTOUCHED SA54274
POST1    EQU   *                                               @Y17XAIL
         L     RA,LCBRECOF              GET WORD THAT CONTAINS @Y17XAIL
*                                       'RECEIVING' AND 'FREE' @Y17XAIL
*                                       FLAGS                  @Y17XAIL
TRYAGN2  EQU   *                                               @Y17XAIL
         LR    RRET,RA                  GET COPY OF THE WORD   @Y17XAIL
         O     RRET,SETRECVN            SET RECEIVING ON       @Y17XAIL
         N     RRET,RESFREE             RESET FREE             @Y17XAIL
         CS    RA,RRET,LCBRECOF         WAS WORD UPDATED       @Y17XAIL
*                                       ASYNCHRONOUSLY         @Y17XAIL
         BNE   TRYAGN2                  YES, TRY AGAIN         @Y17XAIL
         ST    RLCB,LCBQCBA-1           QCB FOR POST
         MVI   LCBPRI,PRILNFRE          SET PRIORITY           @Y17XAIL
         LR    RRECB,RLCB               DISPATCH PARAMETER
         BAL   RRET,DSPPOST             POST LCB TO ITSELF     @Y17XAIX
         EJECT                                                 @Y17XAIL
************************************************************** @Y17XAIL
*                                                            * @Y17XAIL
*        CHECK FOR LCB OR ERB AS INPUT                       * @Y17XAIL
*                                                            * @Y17XAIL
************************************************************** @Y17XAIL
         SPACE 1                                               @Y17XAIL
NOTATTN  EQU   *                                               @Y17XAIL
         CLI   RECBPRI-IEDQRECB(RLCB),PRIACTIV ERB INPUT       @Y17XAIL
         BE    ERB                      YES, BRANCH            @Y17XAIL
         SPACE 3                                               @Y17XAIL
************************************************************** @Y17XAIL
*                                                            * @Y17XAIL
*        PROCESSING FOR INPUT = LCB                          * @Y17XAIL
*                                                            * @Y17XAIL
************************************************************** @Y17XAIL
         SPACE 1                                               @Y17XAIL
         L     RDCB,LCBDCBPT            GET DCB ADDRESS
         TM    DCBDSRG2,DCBDSGTR        IS THE DCB FOR 3705    @Y17XAIL
         BNO   NO3705                   BRANCH ON NO             S22024
         B     CHKCQ                    GO CHECK QUEUE         @Y17XAIL
         EJECT                                                 @Y17XAIL
************************************************************** @Y17XAIL
*                                                            * @Y17XAIL
*        PROCESSING FOR INPUT = ERB                          * @Y17XAIL
*                                                            * @Y17XAIL
************************************************************** @Y17XAIL
         SPACE 1                                               @Y17XAIL
ERB      EQU   *                                               @Y17XAIL
         SH    RLCB,OFFERB              GET ADDR OF LCB        @Y17XAIL
         L     RWORK,LCBERBCH-1         GET 1ST UNIT ADDR      @Y17XAIL
         SLR   RILIST,RILIST            CLEAR FOR INSERT       @Y17XAIL
         IC    RILIST,PRFNBUNT          GET NO. OF UNITS       @Y17XAIL
         LA    RWORK,LCBERBCH-(PRFTIC+1-IEDQPRF) MAKE LCBERBCH @Y17XAIL
*                                       APPEAR AS DUMMY FIRST  @Y17XAIL
*                                       UNIT                   @Y17XAIL
BUILDUNT EQU   *                                               @Y17XAIL
         L     RWORK,PRFTIC             GET NEXT UNIT          @Y17XAIL
         LA    RRET,PRFSUNIT            CCW DATA ADDRESS       @Y17XAIL
         ST    RRET,PRFIOADR-1          SET DATA ADDRESS       @Y17XAIL
         MVI   PRFOPCDE,CCWREAD         SET READ COMMAND CODE  @Y17XAIL
         MVI   PRFFLAGS,CCWCC+CCWSLI    INDICATE CMD CHAINING  @Y17XAIL
*                                       AND SUPPRESS INCORRECT @Y17XAIL
*                                       LENGTH                 @Y17XAIL
         MVI   PRFFLAGS+1,AVTEZERO      CLEAR RESERVED FIELD   @Y17XAIL
         MVC   PRFCOUNT,AVTKEYLE        SET COUNT              @Y17XAIL
         MVI   PRFTICC,CCWTIC           SET TIC IN NEXT CCW    @Y17XAIL
         BCT   RILIST,BUILDUNT          CONTINUE IF MORE UNITS @Y17XAIL
         MVC   PRFTIC+1(3),LCBRDBFR     LINK LAST UNIT IN NEW  @Y17XAIL
*                                       CHAIN TO FIRST UNIT ON @Y17XAIL
*                                       READ CHAIN             @Y17XAIL
         MVC   LCBRDBFR,LCBERBCH        UPDATE POINTER TO      @YM03700
*                                       FIRST READ BUFFER      @YM03700
         XC    LCBERBCH,LCBERBCH        CLEAR ERB CHAIN        @Y17XAIL
         MVI   LCBRBCT2,AVTEZERO        CLEAR REQUEST COUNT    @YM03745
         L     RDCB,LCBDCBPT            GET DCB ADDRESS        @Y17XAIL
         L     RA,LCBRECOF              GET WORD THAT CONTAINS @Y17XAIL
*                                       THE BUFFER AVAILABLE   @Y17XAIL
*                                       FLAG                   @Y17XAIL
TRYAGN3  EQU   *                                               @Y17XAIL
         LR    RRET,RA                  GET COPY OF THE WORD   @Y17XAIL
         O     RRET,SETBFRAV            SET BFRS AVAILABLE     @Y17XAIL
         CS    RA,RRET,LCBRECOF         WAS WORD UPDATED       @Y17XAIL
*                                       ASYNCHRONOUSLY         @Y17XAIL
         BNE   TRYAGN3                  YES, TRY AGAIN         @Y17XAIL
         EJECT                                                 @Y17XAIL
CHKCQ    EQU   *                                               @Y17XAIL
         LR    RTRM,R15                 SAVE BASE REGISTER     @Y17XAIL
         LH    RRECB,LCBTTCIN           GET 3705 TNT OFFSET    @Y17XAIL
         N     RRECB,AVTCLRHI           CLEAR HI ORDER 2 BYTES @Y17XAIL
         L     R15,AVTRNMPT             GET ADDR OF TNT LOOKUP @Y17XAIL
*                                       CODE                   @Y17XAIL
         BALR  RRET,R15                 CALL TNT LOOKUP        @Y17XAIL
         LR    R15,RTRM                 RESTORE BASE ADDRESS   @Y17XAIL
         LR    RTRM,RRECB               GET TERM ENTRY ADDR    @Y17XAIL
         SH    RTRM,PRFSZ               BACK UP TO TERM ENTRY  @Y17XAIL
*                                       PREFIX                 @Y17XAIL
         SPACE 1                                               @YM02908
         TM    TRMNCP,TRMIPLDM          IPL/DUMP IN PROGRESS   @YM02908
         BO    DSPDISP                  BR YES, DON'T COMPETE  @YM02908
         SPACE 1                                               @YM02908
         CLC   LCBCQELE,AVTDELAD        IS CORE QUEUE EMPTY    @Y17XAIL
         BE    CHKBFRS                  YES, BRANCH            @Y17XAIL
         SPACE 1                                               @Y17XAIL
         TM    TRMNCP,TRMSLOWN          IS 3705 IN SLOWDOWN    @Y17XAIL
         BNO   NEXTSTCB                 NO, BYPASS TO IGG019TE @YM02908
         EJECT                                                 @Y17XAIL
CHKBFRS  EQU   *                                               @Y17XAIL
         TM    LCBSTAT2,LCBBFRAV        ARE BUFFERS AVAILABLE  @Y17XAIL
         BZ    DSPDISP                  NO, EXIT TO DISPATCHER @Y17XAIL
         SPACE 1                                               @Y17XAIL
         TM    LCBSTAT2,LCBATTN         ATTENTION RECEIVED     @Y17XAIL
         BO    CHKTSTSW                 YES, GO CHECK LCB      @YM02909
         SPACE 1                                               @YM02909
         TS    LCBTSTSW                 LCB AVAILABLE          @YM02909
         BNZ   DSPDISP                  NO, EXIT               @YM02909
         SPACE 1                                               @YM02909
         L     RA,LCBRECOF              GET WORD CONTAINING    @YM02909
*                                       LCBSTATE               @YM02909
TRYAGN4  EQU   *                                               @YM02909
         LR    RRET,RA                  COPY STATE VALUE       @YM02909
         O     RRET,SETFREE             INDICATE LCB FREE      @YM02909
         N     RRET,RESRECVN                                   @YM02909
         CS    RA,RRET,LCBRECOF         WAS LCBSTATE UPDATED   @YM02909
*                                       ASYNCHRONOUSLY         @YM02909
         BNE   TRYAGN4                  NO, TRY AGAIN          @YM02909
         SPACE 1                                               @YM02909
         MVI   LCBTSTSW,AVTEZERO        MAKE LCB AVAILABLE     @YM02909
         B     DSPDISP                  AND EXIT               @YM02909
         SPACE 1                                               @Y17XAIL
CHKTSTSW EQU   *                                               @YM02909
         TS    LCBTSTSW                 IS LCB AVAILABLE FOR   @Y17XAIL
*                                       I/O                    @Y17XAIL
         BNZ   DSPDISP                  NO, EXIT TO DISPATCHER @Y17XAIL
         L     RA,LCBRECOF              GET WORD THAT CONTAINS @Y17XAIL
*                                       BUFFER AVAILABLE       @Y17XAIL
*                                       FLAG                   @Y17XAIL
TRYAGN5  EQU   *                                               @Y17XAIL
         LR    RRET,RA                  GET COPY OF THE WORD   @Y17XAIL
         O     RRET,SETRECVN            INDICATE RECEIVING     @Y17XAIL
         N     RRET,RESBFRAV            RESET BUFFER AVAILABLE @Y17XAIL
*                                       AND LINE FREE          @Y17XAIL
         CS    RA,RRET,LCBRECOF         WAS WORD UPDATED       @Y17XAIL
*                                       ASYNCHRONOUSLY         @Y17XAIL
         BNE   TRYAGN5                  YES, TRY AGAIN         @Y17XAIL
         SPACE 1                                               @Y17XAIL
         LA    RA,LCBCCW3                                      @Y17XAIL
         STCM  RA,AD,LCBSTART                                  @Y17XAIL
         MVC   LCBCCW4+1(3),LCBRDBFR                           @Y17XAIL
         MVI   LCBSIO,SIOCODE           CODE FOR STARTIO ENTRY INTO
*                                       QEB                    @G36XRIB
         ST    RA,LCBCP                 ADDR OF START OF CHANNEL PROG
*                                                              @G36XRIB
         LA    R1,LCBQEBPL              ADDR OF PARM LIST IN R1 FOR
*                                       QEB                    @G36XRIB
*        ISSUE SVC TO DO STARTIO                               @G36XRIB
         AQCTL                                                 @G36XRIB
         B     DSPDISP                  EXIT TO DISPATCHER     @Y17XAIL
         EJECT                                                 @Y17XAIL
NO3705   EQU   *                                                 S22024
         TM    AVTBIT1,AVTCLOSN         CLOSEDOWN IN PROGRESS   SA56606
         BO    NEXTSTCB                 YES-EXIT NEXT STCB      SA56606
         TM    LCBSTAT1,LCBOCNI         STOPLINE IN PROGRESS    SA56606
         BZ    BYPASS                   NO-CONTINUE             SA56606
NEXTSTCB EQU   *                                                SA56606
         L     RSTCB,LCBRSLNK-1         GET NEXT STCB ADDR      SA56606
         LR    RRECB,RLCB               ELEMENT ADDR            SA56606
         LR    RQCB,RLCB                QCB ADDR                SA56606
         B     DSPBYPAS                 EXIT NEXT STCB          SA56606
BYPASS   EQU   *                                                 S22025
         BAL   RRET,ILISTRTN            BRANCH TO ILIST RTN    @Y17XAIL
         SPACE 2
         CLI   ACTIVE(RILIST),AVTEZERO ANY ACTIVE ENTRIES
         BE    FREETEST                 NO, BRANCH               S22025
         LA    RA,0(,RILIST)            GET INVILIST            SA69626
         LA    R0,TWO                   SET AN OFFSET VALUE      S22025
         SR    RA,R0                    LOCATE THE TNT INDEX     S22025
         LH    RRECB,0(RA)              GET THE TNT INDEX        S22025
         LR    RQCB,R15                 SAVE THE BASE REGISTER   S22025
         L     R15,AVTRNMPT             GET THE ADDRESS OF THE   S22025
*                                         IEDQTNT ROUTINE        S22025
         BALR  RRET,R15                 BRANCH TO IT           @Y17XAIL
         SPACE 2                                                 S22025
         LR    R15,RQCB                 RESTORE BASE REGISTER    S22025
         L     RQCB,TRMDESTQ-1-IEDQTRM(,RRECB)   GET THE         S22025
*                                         DESTINATION QCB ADDR   S22025
         LR    RRECB,RLCB               RESTORE THE LCB ADDRESS  S22025
*                                         FOR A POSSIBLE POST    S22025
         TM    QCBSTAT,QCBSEND          ARE WE CURRENTLY SENDING S22025
*                                         TO THIS STATION        S22025
         BO    NOACTIV                  YES, BRANCH              S22025
TSOTEST  EQU   *                                                 S22025
         TM    AVTBIT1,AVTTSON          IS TSO IN THE SYSTEM     S22025
         BZ    FREETEST                 NO, BRANCH               S22025
TSO3     EQU   *                                                 S22025
         L     RWORK,AVTTSOPT           GET THE TSO POINTER      S22025
         L     RWORK,TSISCHED-IEDQTSI(,RWORK)    GET THE ADDRESS S22025
*                                         OF THE TIME SHARING    S22025
*                                         SCHEDULER              S22025
         BAL   RSTCB,THREETWO(,RWORK)   LINK TO IT               S22025
         SPACE 2                                                 S22025
FREETEST EQU   *                                                 S22025
         LR    RRECB,RLCB               RESTORE THE ELEMENT ADDR S22025
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN SAVE THE STOPLINE BITS@OY14092
         OI    LCBSTAT1,LCBFREEN        ASSUME THE LCB IS TO BE  S22025
*                                         FREED                  S22025
         CLI   LCBTSTSW,XF0             IS THE LCB TO BE FREED   S22025
         BE    DSPDISP                  YES, BRANCH              S22025
         TM    DCBDSRG2,DCBDSGTR        IS THE DCB FOR 3705    @Y17XAIL
         BO    TSO1                     BRANCH ON YES            S22024
         XI    LCBSTAT1,LCBRECVN+LCBFREEN    JIGGLE THE FREE AND S22025
*                                         RECEIVE BITS           S22025
         BAL   RRET,ILISTRTN            BRANCH TO ILIST RTN    @Y17XAIL
         CLI   ACTIVE(RILIST),AVTEZERO  ANY ACTIVE ENTRIES      SA56231
         BE    NOACTIV                  NO, BRANCH              SA56231
         SPACE 2                                               @SA73490
         SPACE 2
         TM    AVTBIT3,AVTRECVN+AVTRFULN IS RECEIVE OPERATION
*                                        POSSIBLE
         BZ    GOTUNIT                  BRANCH IF YES
DELAY    EQU   *
         TM    LCBINSRC+TWO,X'02'       POSTED TO TIME DELAY?  @OY20448
         BO    DSPDISP                  BRIF YES- NO POST      @OY20448
         OI    LCBINSRC+TWO,X'02'       INDICATE POSTED        @OY20448
         LA    RWORK,1                  SET FOR 1 SECOND DELAY
         STH   RWORK,LCBEOLTD           TIME DELAY PARAMETER
         MVI   LCBTDL,LCBINSRC-1-IEDQLCB  SET TIME QUEUE PARAMETER
         L     RWORK,LCBSTCBA-1         STCB ADDRESS           @OY20448
         CLI   0(RWORK),X'1E'           IS THIS LAST SCHEDULER @OY20448
         BNE   ITSSCHED                 BRIF NO                @OY20448
         MVC   LCBSTCBA,LCBRSLNK        GIVE CONTROL TO QEVENT
ITSSCHED EQU   *                                               @OY20448
         LA    RWORK,AVTDELYB           TIME DELAY QUEUE
         ST    RWORK,LCBQCBA-1          FOR POST
         MVI   LCBSTAT1,LCBRECVN        SET LCB NOT FREE        SA52509
         BAL   RRET,DSPPOST             POST LCB TO TIME QUEUE @Y17XAIX
GOTUNIT  EQU   *
         IC    RWORK,ILSTSIZE(0,RILIST) GET LIST ENTRY SIZE    @SA75437
         BCTR  RWORK,R0           SUBTRACT ONE TO REACH INDEX B@SA75437
         LA    RCURRENT,ILSTPRF(0,RILIST) GET ADDR OF FIRST ENT@SA75437
         IC    RWORK,0(RCURRENT,RWORK) GET INDEX BYTE          @SA75437
         IC    R0,LCBINVPT-1
         ST    RCURRENT,LCBINVPT-1 SET AS CURRENT ENTRY
         STC   R0,LCBINVPT-1
         AR    RWORK,RWORK        DOUBLE FOR NEGATIVE INDEX    @SA75437
         SR    RILIST,RWORK             GET ERM TABLE INDEX    @SA75437
         MVC   LCBTTBIN(2),0(RILIST)    SET IN LCB
         TM    AVTBIT1,AVTTSON          IS TSO IN SYSTEM            TSO
         BZ    TSO1                     BRANCH ON NO                TSO
         L     RWORK,AVTTSOPT           ADDRESS TS SCHED            TSO
         L     RWORK,TSISCHED-IEDQTSI(,RWORK)                       TSO
         BAL   RSTCB,0(,RWORK)          LINK TO TS SCHEDULER        TSO
         NOP   TSO1                     FIRST RETURN POINT          TSO
TSO1     EQU   *                                                    TSO
         LA    RA,AVTBFREB              BUFFER REQUEST QUEUE
         ST    RA,LCBERB                POST ADDRESS
         LA    RA,LCBERB                READY FOR POST
         MVI   LCBERBPY,PRIINTRQ        INITIAL REQUEST PRIORITY
         SR    RWORK,RWORK              CLR WORK REGISTER
         IC    RWORK,DCBBUFIN           INITIAL COUNT
         SRL   RWORK,4                  SHIFT FOR HI ORDER BITS
         STC   RWORK,LCBERBCT           ESTABLISH INIT REQUEST
         LA    RRECB,LCBERB             ELEMENT FOR POST
         BAL   RRET,DSPPOST             POST ERB TO GET BUFFRS @Y17XAIX
NOACTIV  EQU   *                                                 S22025
         L     RSTCB,LCBRSLNK-1         GET THE NEXT STCB        S22025
         CLI   STCBPRI,AVTEZERO         IS THIS THE LAST STCB    S22025
         BE    DSPBYPAS                 YES, BRANCH              S22025
         B     DELAY                    FORCE A 1 SECOND DELAY   S22025
ILISTRTN EQU   *                                                 S22025
         SR    RWORK,RWORK              CLEAR A WORK REGISTER    S22025
         IC    RWORK,LCBUCBX            GET THE REL. LINE NO.    S22025
         SLL   RWORK,TWO                MULTIPLY BY FOUR         S22025
         L     RILIST,DCBINVLI(RWORK)   GET THE INVITATION LIST  S22025
*                                         ADDRESS FOR THIS LINE  S22025
         SR    RWORK,RWORK              CLEAR AGAIN              S22025
         BR    RRET                     RETURN                 @Y17XAIL
         EJECT
*        CONSTANTS AND EQUATES
*                                       OTHER EQUATES
         SPACE 1
SIOCODE  EQU   48                 STARTIO ENTRY CODE INTO QEB  @G36XRIB
ACTIVE   EQU   1                  OFFSET OF NUM OF ACTIVE ENTRIES
ILSTSIZE EQU   2                  LENGTH OF AN INVITATION LIST ENTRY
TWO      EQU   2                        TWO                      S22024
AD       EQU   7                        STCM MASK FOR ADDRESS  @Y17XAIL
THREETWO EQU   32                       THIRTY2                  S22024
ILSTPRF  EQU   8                        SIZE OF ILIST COTNROL AREA
         SPACE 1
         DS    0H                                              @Y17XAIL
OFFERB   DC    AL2(LCBERB-IEDQLCB)      OFFSET OF ERB IN LCB   @Y17XAIL
LCBBKUP  DC    AL2(LCBATTEL-IEDQLCB)    OFFSET IN LCB TO ATTN ELEMENT
LCBBKUP5 DC    AL2(LCBATL05-IEDQLCB)    OFFSET IN LCB TO 3705  @YM08074
*                                       ATTENTION ELEMENT      @YM08074
XF0      EQU   X'F0'                    MASK TO FREE ATTEN ELE
         SPACE 1                                               @Y17XAIL
         DS    0H                                              @Y17XAIL
PRFSZ    DC    AL2(TRMPRFSZ)            TERM ENTRY PREFIX SIZE @Y17XAIL
         EJECT                                                 @Y17XAIL
************************************************************** @Y17XAIL
*                                                            * @Y17XAIL
*        COMPARE AND SWAP WORDS                              * @Y17XAIL
*                                                            * @Y17XAIL
************************************************************** @Y17XAIL
         SPACE 1                                               @Y17XAIL
*        WORD FOR RESETTING LCBATTN IN WORD LCBRECOF           @Y17XAIL
RESATTN  DS    0F                                              @Y17XAIL
         DC    AL1(255)                                        @Y17XAIL
         DC    AL1(255)                                        @Y17XAIL
         DC    AL1(255)                                        @Y17XAIL
         DC    AL1(255-LCBATTN)         RESET LCBATTN          @Y17XAIL
         SPACE 1                                               @Y17XAIL
*        WORD FOR SETTING LCBRECVN IN WORD LCBRECOF            @Y17XAIL
SETRECVN DS    0F                                              @Y17XAIL
         DC    AL2(0)                                          @Y17XAIL
         DC    AL1(LCBRECVN)            SET LCBRECVN           @Y17XAIL
         DC    AL1(0)                                          @Y17XAIL
         SPACE 1                                               @Y17XAIL
*        WORD FOR RESETTING LCBFREEN IN WORD LCBRECOF          @Y17XAIL
RESFREE  DS    0F                                              @Y17XAIL
         DC    AL1(255)                                        @Y17XAIL
         DC    AL1(255)                                        @Y17XAIL
         DC    AL1(255-LCBFREEN)        RESET LCBFREEN         @Y17XAIL
         DC    AL1(255)                                        @Y17XAIL
*        WORD FOR SETTING LCBBFRAV IN WORD LCBRECOF            @Y17XAIL
SETBFRAV DS    0F                                              @Y17XAIL
         DC    AL3(0)                                          @Y17XAIL
         DC    AL1(LCBBFRAV)            SET LCBBFRAV           @Y17XAIL
         SPACE 1                                               @Y17XAIL
*        WORD FOR RESETTING LCBBFRAV IN WORD LCBRECOF          @Y17XAIL
RESBFRAV DS    0F                                              @Y17XAIL
         DC    AL1(255)                                        @Y17XAIL
         DC    AL1(255)                                        @Y17XAIL
         DC    AL1(255-LCBFREEN)        RESET LCBFREEN         @Y17XAIL
         DC    AL1(255-LCBBFRAV)        RESET LCBBFRAV         @Y17XAIL
         SPACE 1                                               @YM02908
*        WORD FOR SETTING LCBFREEN IN WORD LCBRECOF            @YM02908
SETFREE  DS    0F                                              @YM02908
         DC    AL2(0)                                          @YM02908
         DC    AL1(LCBFREEN)            SET LCBFREEN           @YM02908
         DC    AL1(0)                                          @YM02908
         SPACE 1                                               @YM02908
*        WORD FOR RESETTING LCBRECVN IN WORD LCBRECOF          @YM02908
RESRECVN DS    0F                                              @YM02908
         DC    AL1(255)                                        @YM02908
         DC    AL1(255)                                        @YM02908
         DC    AL1(255-LCBRECVN)        RESET LCBRECVN         @YM02908
         DC    AL1(255)                                        @YM02908
         EJECT
         TAVTD
         EJECT                                                 @Y17XAIL
         TCCWD                                                 @Y17XAIL
         EJECT                                                 @Y17XAIL
         DCBD  DSORG=TX
DCBTRSTA EQU   IHADCB+X'19'             STATUS BYTE              Y06327
DCBNIDLE EQU   X'02'                    IDLE=NO,SPECIFIED OR     Y06327
*                                       IMPLIED BY DEFAULT IN    Y06327
*                                       OPEN MACRO               Y06327
         EJECT                                                 @Y17XAIL
         TDISPD
         EJECT
         TLCBD
         EJECT
         TPRFD                                                 @Y17XAIL
         EJECT                                                 @Y17XAIL
         TPRIOR                                                @Y17XAIL
         EJECT                                                 @Y17XAIL
         TQCBD
         EJECT                                                 @Y17XAIL
         TRECBD
         EJECT                                                 @Y17XAIL
         TSTCBD
         EJECT                                                 @Y17XAIL
         TTRMD
         EJECT                                                 @Y17XAIL
         TTSID                                                 @Y17XAIL
         SPACE 3                                               @Y17XAIL
         END
