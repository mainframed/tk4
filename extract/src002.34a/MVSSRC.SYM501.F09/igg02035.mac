2035     TITLE 'IGG02035 FIRST LOAD OF TCAM LINE CLOSE'
IGG02035 CSECT
*A-000000-999999                                               @X31X8T0
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS:                                   S21903
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C237400-238600                                                  S22025
*D633000                                                         S22025
*C309000                                                         A49219
*A279000                                                         A52482
*A510100,685000                                                   M2315
*C238600                                                          M2315
*D510200-513100                                                   M2315
*A237000,510000,645000                                           A47150
*D513000                                                         A47150
*A237000,467000,474000,513000                                    CLUP21
*C300000,420300-420400,420900-421000,421300-421400,476100,582000 CLUP21
*D669000,681000                                                  CLUP21
*A417000,875700                                                 SA51774
*A342000,393000,399000,646000                                   SA52959
*D419070,510100,646000,685000                                   SA52959
*A003000,009000-011400                                           S21903
*C234000,630000-645000,705000-726000                             S21903
*A357000                                                        SA59508
*D369000-375000                                                 SA59503
*A446800                                                        SA61822
*A418440,418530                                                 SA61823
*A519000,688000                                                  X01004
*C507000                                                         X01004
*A273000,421300,688000                                           Y01004
*D264000-267000                                                  Y01004
*D270000-273400,275000,421300-421340,421380                      X02004
*A357000                                                        SA63987
*C4446000-446400                                                OY03847
*A459500,625380-626080,647230,647260                            OY03847
*C012000,042000,051000,084000,117000,120000,208000,220000,225000 Y02027
*C242200,246000-249000,255000,331000-411000,522000-525000,603000 Y02027
*D237400-238600,258000,261000,272400-274000,2796000-306000       Y02027
*D327000-330000,414000-419790                                    Y02027
*A260000,624040-625280,647100-647400                             Y02027
*C492000,676000                                                  Y02027
*A493000-494000                                                  Y02027
*D510800-514300                                                  Y02027
*C446900-450000,459500-467400,471000                           @SA69629
*A515000,695700,773000                                         @SA69629
*C012000,528000,738000-741000                                  @Z30X8TM
*D519700-521100                                                @Z30X8TM
*C445800                                                       @OZ17621
*A204000                                                       @OZ17621
*D270600,271800,266100                                         @OZ30749
*A248100                                                       @OZ30749
         SPACE 3
***********************************************************************
*                                                                     *
*MODULE-NAME = IGG02035                                          S21903
*                                                                S21903
*DESCRIPTIVE-NAME = TCAM LINE CLOSE - FIRST LOAD                 S21903
*                                                                S21903
*COPYRIGHTS = 'NONE'                                             S21903
*                                                                S21903
*STATUS = CHANGE LEVEL 8                                       @Z30X8TM
*                                                                     *
*FUNCTION - OPERATION                                                 *
*   IGG02035 ABENDS ALL APPLICATION PROGRAMS IF THE MESSAGE           *
*   CONTROL PROGRAM IS SCHEDULED TO BE ABENDED, AND IT                *
*   ISSUES EXCP ON THE FIRST LINE IN A LINE GROUP TO GIVE             *
*   CONTROL TO TCAM'S ERROR RECOVERY PROCEDURES FOR THE               *
*   RECORDING OF CERTAIN ERROR INFORMATION.                           *
*                                                                     *
*INPUT - THE REGISTERS 5, 6, 7, AND 8 ARE THE INPUT AS FOLLOWS        *
*   5 - THE ADDRESS OF THE BEGINNING OF THE WTG TABLE PREFIX     Y02027
*   6 -  THE ADDRESS OF THE BEGINNING OF THE WHERE-TO-GO TABLE        *
*   7 - THE ADDRESS OF THE COPY OF THE CURRENT DCB               Y02027
*   8 -  THE ADDRESS OF THE CURRENT ENTRY IN THE WHERE-TO-GO          *
*        TABLE                                                        *
*                                                                     *
*ENTRY POINTS - THIS ROUTINE IS ENTERED AT IGG02035 VIA AN            *
*   XCTL FROM AN I/O SUPPORT MODULE OR IT MAY BE RE-ENTERED           *
*   FROM THE RELOOP PORTION OF THIS EXECUOTR IF IT IS DETERMINED      *
*   THAT THERE IS ANOTHER DATA CONTROL BLOCK TO BE CLOSED             *
*   IF ENTRY IS FROM THE I/O SUPPORT MODULE, THEN THE MODULE     Y02027
*   MUST ISSUE MODESET TO GET FROM KEY OF DATA MANAGEMENT TO     Y02027
*   KEY 0.                                                       Y02027
*                                                                     *
*OUTPUT - REGISTERS 7 AND 8 WILL BE POSITIONED AT THE NEXT            *
*   ENTRIES IN THE DATA CONTROL BLOCK COPY LIST AND              Y02027
*   THE WHERE-TO-GO TABLE RESPECTIVELY.                               *
*                                                                     *
*EXTERNAL ROUTINES - NONE                                             *
*                                                                     *
*EXITS - NORMAL                                                       *
*   THIS ROUTINE EXITS VIA AN XCTL TO THE EXECUTOR IDENTIFIED         *
*   BY THE NEXT NON-ZERO ENTRY IN THE WHERE-TO-GO TABLE.              *
*                                                                     *
*TABLES - WORK AREAS - THE WHERE-TO-GO TABLE CONTAINS THE             *
*   ID AND TTR OF THE CLOSE EXECUTORS.  THE DATA CONTROL              *
*   BLOCK COPY LIST CONTAINS THE ADDRESS OF A COPY OF EACH       Y02027
*   DCB SPECIFIED IN THE CLOSE MACRO INSTRUCTION.                Y02027
*                                                                     *
*ATTRIBUTES - THIS ROUITNE IS EXECUTED IN THE TRANSIENT AREA AS       *
*   ENABLED, PRIVILEGED, AND RE-ENTRANT.                              *
***********************************************************************
         EJECT
*        REGISTER USAGE
*
RE       EQU   0                        WORK REGISTER
RF       EQU   1                        WORK REGISTER
RDCB     EQU   2                        DCB BASE REGISTER
RTIOT    EQU   3                        TCB BASE REGISTER
RCORE    EQU   4                        TERMINAL TABLE ENTRY DSECT
*                                         BASE
RPAR     EQU   5                        ADDRESS OF THE FIRST ENTRY
*                                         IN THE DCB PARAMETER LIST
RWTG     EQU   6                        ADDRESS OF THE BEGINNING OF
*                                         THE WHERE-TO-GO TABLE
RPARC    EQU   7                        POINTER TO THE CURRENT ENTRY
*                                         IN THE DCB PARAMETER LIST
RWTGC    EQU   8                        ADDRESS OF THE CURRENT ENTRY
*                                         IN THE WHERE-TO-GO TABLE
RAVT     EQU   9                        BASE REGISTER FOR THE AVT
RUCB     EQU   10                       TERMINAL NAME TABLE BASE
*                                         REGISTER
RDEB     EQU   11                       DEB BASE REGISTER
RBASE    EQU   12                       BASE REGISTER FOR IGG02035
RC       EQU   13                       PCB DSECT BASE REGISTER
RD       EQU   14                       WORK REGISTER
RJ       EQU   15                       WORK REGISTER
FIXED800 EQU   X'800'                   VALUE ADDED TO MODULES @OZ17621
*                                       OVER HEX 800 IN LENGTH @OZ17621
         SPACE 2
         USING IEDQTCX,RF               TCX DSECT                Y02027
         USING IHADCB,RDCB              DCB DSECT
         USING IEDQTCB,RTIOT            TCB DSECT
         USING IEDQLCB,RCORE            LCB DSECT
         USING IEDQAVTD,RAVT            AVT DSECT
         USING FORCORE,RUCB             OPEN WORK  AREA DSECT    Y02027
         USING IEDQDEB,RDEB             DEB DSECT
         USING IEDQRECB,RC              RESOURCE ELEM CNTRL BLOCKY02027
         USING CVT,RJ                   CVT DSECT
*
         EJECT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY S21903
         USING *,RBASE
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
*        THE FOLLOWING ROUTINE IS THE COMMON START FOR ALL DCB'S CLUP21
*        TO BE CLOSED. IT ESTABLISHES THE ADDRESS OF THE DCB,    CLUP21
*        AVT, AND DEB.                                           CLUP21
*                                                                CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
IGG02035 IEDHJN START                                            Y02027
RESTART  EQU   *
         L     RUCB,FOUR(RWTGC)         ADDR OF OPEN WORK AREA   Y02027
         L     RDCB,DXUDCBAD            CURRENT DCB ADDRESS      Y02027
         SPACE 2
         MODESET EXTKEY=SUPR            GET INTO KEY 0           Y02027
         DEBCHK (RDCB),TYPE=VERIFY,AM=TCAM  VALIDATE DEB       @OZ30749
         LR    RDEB,RF                  SAVE ADDRESS OF DEB    @OZ30749
         SPACE 2
         L     RF,CVTPTR                LOAD THE ADDRESS OF THE CVT
         L     RF,AVTCVTPT(RF)          GET THE TCX ADDRESS      Y02027
         L     RAVT,TCXAVT              GET THE AVT ADDRESS      Y02027
         EJECT
         SPACE 2
         SPACE 2
         OI    TCXFLAG1,TCXMCPCD        INDICATE THAT MCP IS     Y02027
*                                       TERMINATING              Y02027
         LA    RF,DEBPRFSZ              SIZE OF DEB PREFIX       Y01004
         SR    RDEB,RF                  SET THE DSECT BASE FOR THE
*                                         DEB
         L     RTIOT,DEBTCBAD           LOAD THE ADDRESS OF THE  A49219
*                                         TCAM MCP TCB
         TM    TCBFLGS,ABEND            IS THE MESSAGE CONTROL
*                                         PROGRAM SCHEDULED FOR AN
*                                         ABEND
         BZ    ERRORECD                 NO, BRANCH
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        PURGE I/O ON ALL LINES PRIOR TO DOING ANY OBR/SDR        *
*        RECORDING                                                *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ERROR01  EQU   *
         L     RJ,CVTPTR                LOAD THE ADDRESS OF THE CVT
         MVC   DEBAPPAD+1(THREE),CVTXAPG+1 MOVE THE IOS          CLUP21
*                                         APPENDAGE INTO THE DEB CLUP21
         L     RF,FOUR(RWTGC)           LOAD THE ADDRESS OF THE
*                                         OPEN WORK AREA
         LA    RC,DEBPRFSZ(RDEB)        GET ADDR OF DEB (POINT   Y02027
*                                         AFTER PREFIX)          Y02027
         ST    RC,0(RF)                 STORE THE DEB ADDRESS    Y02027
*                                         INTO THE PARM LIST     CLUP21
         MVI   0(RF),OPTION             PUT THE OPTION BYTE IN THE
*                                         PARAMETER LIST
         XC    FOUR(EIGHT,RF),FOUR(RF)  CLEAR THE ECB
         PURGE (1)                      ISSUE PURGE              Y02027
*
         L     RF,FOUR(RWTGC)           LOAD THE ADDRESS OF THE  Y02027
*                                       OPEN WORK AREA           Y02027
         LA    RF,FOUR(RF)              LOAD THE ADDRESS OF THE ECB
         WAIT  ECB=(RF)                 WAIT FOR COMPLETION      Y02027
*
         ST    RDEB,DEBAPPAD            RESTORE THE ADDRESS OF THE
*                                         APPENDAGE TABLE
         MVI   DEBEXSCL,TWO             INDICATE THAT THIS DEB IS
*                                         FOR A NON-DIRECT ACCESS
*                                         DEVICE
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF CODE ISSUES EXCP ON THE FIRST LINE       *
*        IN A LINE GROUP TO GIVE CONTROL TO TCAM'S ERP FOR        *
*        THE RECORDING OF CERTAIN ERROR INFORMATION               *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ERRORECD EQU   *
         ICM   RF,ALL,AVTIOT            ADDR OF I/O TRACE RTN   OY03847
         BZ    TEST3705                 BRANCH IF I/O TRACE RTN OY03847
*                                       IS NOT PRESENT          OY03847
         XC    AVTIOT,AVTIOT            CLEAR I/O TRACE POINTER OY03847
         SPACE
*****                                                          @OZ17621
*    THE FOLLOWING SECTION OF CODE REFLECTS THE                @OZ17621
*    LENGTH OF IGG019Q0                                        @OZ17621
         SPACE
         LR    RJ,RF                    COPY ADDRESS TO UNFIX  @OZ17621
         SRL   RJ,HIGHBYTE              GET NO. OF DOUBLE WORDS@OZ17621
         SLL   RJ,THREE                 CONVERT TO BYTE LENGTH @OZ17621
         LA    RJ,FIXED800(RF,RJ)       BUMP TO END ADDR + 1   @OZ17621
         PGFREE R,A=(1),EA=(15)         FREE IGG019Q0          @OZ17621
*****
TEST3705 EQU   *                                                OY03847
         TM    DCBDSORG+ONE,DCB3705     3705 DCB?              @ZM47865
         BZ    NOT3705                  NO                     @ZM47865
         SR    RF,RF                    YES CLEAR REGISTER     @ZM47865
         STH   RF,0(RWTGC)              CLEAR WTGC POINTER     @ZM47865
         B     ENDLOAD                  EXIT CLOSE PROCESS     @ZM47865
NOT3705  EQU   *                                               @ZM47865
         L     RJ,AVTRNMPT              GET TNT BASE           @SA69629
         USING IEDQTNTD,RJ                                     @SA69629
         DROP  RTIOT                                           @SA69629
         SR    RTIOT,RTIOT              CLEAR FOR INSERT       @SA69629
         ICM   RTIOT,HALF,TNTLEN        NUMBER OF TNT ENTRIES  @SA69629
TNTLOOP  EQU   *                                               @SA69629
         LR    RF,RTIOT                 COPY TNT OFFSET        @SA69629
         BALR  RD,RJ                    GET TRM ADDR           @SA69629
         CLI   TRMTEMPR-IEDQTRM(RF),AVTEZERO  ERRORS TO RECORD @SA69629
         BE    TRYNEXT                  BR IF NO               @SA69629
         ICM   RC,AD,TRMDESTQ-IEDQTRM(RF) GET QCB ADDR         @SA69629
         CLM   RDCB,AD,QCBDCBAD-IEDQQCB(RC) BELONG TO LINE GRP @SA69629
         BNE   TRYNEXT                  NO, BR                 @SA69629
         SR    RD,RD                    CLEAR FOR INSERT       @SA69629
         IC    RD,QCBRELLN-IEDQQCB(RC)  GET REL LINE NO        @SA69629
         SR    RJ,RJ                    CLEAR FOR INSERT       @SA69629
         IC    RJ,DCBEIOBX              GET SIZE OF AN LCB     @SA69629
         L     RCORE,DCBIOBAD           GET IOB ADDR FOR IOS   @SA69629
         MR    RD,RD                    GET OFFSET TO IOB      @SA69629
         LA    RE,IOBPTR                GET THE IOB OFFSET
         SR    RCORE,RE                 GET THE BEGINNING ADDRESS
*                                         FOR THE LCB FOR IOS
         AR    RCORE,RJ                 SET LCB ADDR           @SA69629
         MVC   LCBINCAM(TWO),ERPNTIND   SET THE ERP ENTRY INDICATOR
         ST    RTIOT,LCBERCCW           SET TNT OFFSET FOR ERP @SA69629
         MVI   LCBTPCD,TPOPCODE         SET THE TP OP CODE
*                                                                CLUP21
*  CLEAR TP OP CODE BYTE USER FOR TSO SPECIAL ACTION. AVOIDS PROBLEM
*  OF TSO CODE IN LINE APPENDAGE USURPING CONTROL ON FINAL ENTRY.
*                                                                CLUP21
         MVI   LCBTPCD+TEN,AVTEZERO     CLEAR ANY TSO SPECIAL    CLUP21
*                                         CODE                   CLUP21
         LA    RC,LCBCPA                GET THE ADDRESS OF THE
*                                         CHANNEL PROGRAM AREA
         ST    RC,LCBSIOCC              STORE IT IN THE LCB
         ST    RAVT,LCBCPA              STORE ANY VALID ADDRESS IN
*                                         THE FIRST CCW
         MVI   LCBCPA,TICCOMND          USE TIC COMMAND TO BUILD Y02027
*                                       INVALID CCW FORCING      Y02027
*                                       ENTRY TO LINE END        Y02027
         MVC   LCBCPA+4(FOUR),NOP       BUILD THE REST OF THE NOP
*                                         CCW
         LA    RF,LCBFLAG1              GET THE IOB ADDRESS FOR IOS
*
         MVI   AVTOSECB,CLEAR           CLEAR THE ECB PRIOR TO
*                                         ISSUING EXCP
         EXCPVR (1),SUBSYS              ISSUE EXCPVR             X01004
*
         WAIT  ECB=AVTOSECB             WAIT ON THE IOS ECB       M2315
TRYNEXT  EQU   *                                               @SA69629
         L     RJ,AVTRNMPT              GET TNT BASE           @SA69629
         BCT   RTIOT,TNTLOOP            LOOP ON NEXT ENTRY     @SA69629
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
*        THIS SECTION OF CODE DETERMINES IF THERE IS ANOTHER DCB CLUP21
*        TO BE CLOSED, AND IF SO POSITIONS TO THE NEXT DCB IN    CLUP21
*        THE LIST AND RELOOPS THROUGH THIS EXECUTOR. OTHERWISE   CLUP21
*        THE WHERE-TO-GO TABLE IS MODIFIED TO XCTL TO THE NEXT   CLUP21
*        LOAD OF TCAM CLOSE - IGG02036                           CLUP21
*                                                                CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
ENDLOAD  EQU   *
         L     RUCB,FOUR(RWTGC)         LOAD THE ADDRESS OF THE  Y02027
*                                         OPEN WORK AREA         Y02027
         MVC   0(TWO,RWTGC),CLOSE2      MOVE THE ID FOR THE    @Z30X8TM
*                                         SECOND LOAD OF CLOSE INTO
*                                         THE PARAMETER LIST
RELOOP   EQU   *
         LA    RWTGC,EIGHT(RWTGC)       BUMP CURRENT WTG POINTER
         LA    RPARC,FOUR(RPARC)        LOAD THE POINTER TO THE NEXT
*                                         ENTRY IN THE DCB
*                                         PARAMETER LIST
         CLC   0(TWO,RWTGC),AMIDCNST    IS THIS EXECUTOR REQUIRED
*                                         AGAIN
         BE RESTART                     YES, BRANCH
         CLC   0(TWO,RWTGC),CLOSEIDL    THIS CODE IS USED TO
         BL    RELOOP                   DETERMINE WHICH OF THE
         CLC   0(TWO,RWTGC),CLOSEIDH    CLOSE EXECUTORS TO GIVE
         BH    RELOOP                   CONTROL TO
         LR    RPARC,RPAR               INITIALIZE THE CURRENT
         LA    RWTGC,32(RWTG)           POINTERS
ZCHECK   EQU   *
         CLI   0(RWTGC),AVTEZERO        IS THIS ENTRY COMPLETE   CLUP21
         BNE   XCTLRTNE                 NO, THEN BRANCH
         LA    RWTGC,EIGHT(RWTGC)       BUMP POINTER TO TEST FOR
         LA    RPARC,FOUR(RPARC)          COMPLETION OF THE NEXT
*                                         ENTRY
         B     ZCHECK                   TEST THE NEXT ENTRY
XCTLRTNE EQU   *
         LA    RJ,DXCCW12               LOAD THE ADDRESS OF THE  Y02027
*                                         LAST DOUBLE WORD IN THE
*                                         OPEN WORK AREA
         MVC   SIX(TWO,RWTG),0(RWTGC)   MOVE THE ID TO THE NAME FIELD
         MVC   14(THREE,RWTG),TWO(RWTGC)  MOVE THE TTR INTO THE
*                                         PARAMETER LIST
         XCTL  DE=(RWTG),SF=(E,(15))    XCTL
         EJECT
FREEPG   EQU   *                                                OY03847
         LR    RJ,RF                    COPY ADDRESS TO UNFIX   OY03847
         SRL   RJ,HIGHBYTE              GET NO. OF DOUBLE WORDS OY03847
         SLL   RJ,THREE                 CONVERT TO BYTE LENGTH  OY03847
         LA    RJ,ZERO(RF,RJ)           BUMP TO END ADDR + 1    OY03847
         PGFREE R,A=(1),EA=(15)         ISSUE PAGE FREE         OY03847
         BR    RD                       RETURN TO CALLER        OY03847
         EJECT
         DS    0F
ABCODE   DC    X'80046000'              0C4  ABEND  CODE         S21903
CLOSEIDL DC    C'0B'                    ID OF LAST CLOSE MODULES S21903
CLOSEIDH DC    C'0G'                    ID OF LAST CLOSE MODULES S21903
NOP      DC    X'20000001'              LAST HALF OF NOP-CCW     S21903
ERPNTIND DC    X'01FF'                  ERP ENTRY INDICATOR      S21903
DISABLED DC    X'00'                    TO DISABLE ALL INT.     SA52959
ENABLED  DC    X'FF'                    TO ENABLE ALL INT.      SA52959
HIGHBYTE EQU   24                       HIGH-ORDER BYTE SHIFT   OY03847
ALL      EQU   15                       ICM/STCM MASK           OY03847
AMIDCNST DC    C'35'                    ID FOR THIS EXECUTOR     Y02027
HIBIT    DC    X'80'                    HIGH ORDER BIT           Y02027
EXCP     EQU   0                        VALUE 0                  S22024
ONE      EQU   1                        VALUE 1                  S22024
TWO      EQU   2                        VALUE 2                  S22024
THREE    EQU   3                        VALUE 3                  S22024
FOUR     EQU   4                        VALUE 4                  S22024
FIVE     EQU   5                        VALUE 5                  S22024
SIX      EQU   6                        VALUE 6                  S22024
EIGHT    EQU   8                        VALUE 8                  S22024
TEN      EQU 10                         VALUE 10                 S22024
IOBPTR   EQU   32                       VALUE 32                 S22024
DEBPRFSZ EQU   36                       LEN OF DEB APPENDAGE     S22024
CLEAR    EQU   X'00'                    VALUE ZERO               S22024
TICCOMND EQU   8                        TIC CCW                  Y02027
TPOPCODE EQU   X'22'                    FLAGS                    S22024
CUBUSY   EQU   X'60'                    CONTROL UNIT BUSY        S22024
ABEND    EQU   X'80'                    ABEND FLAG               S22024
OPTION   EQU   X'A0'                    OPTIONS                  S22024
USAGE    EQU   X'FF'                    FLAGS                    S22024
TWOK     EQU   2048                     LENGTH OF IGG02036       X01004
RECLNG   EQU   25                       LENGTH OFFSET IN BLDL    X01004
RLD      EQU   22                       RLD OFFSET IN BLDL LIST  X01004
RLDON    EQU   X'FE'                    SET RLD ON FLAG          X01004
PFLAG    EQU   X'20'                    AQCTL ACTION CODE       SA51774
DCB3705  EQU   X'04'                    DSORG = TR               S22024
ZERO     EQU   0                        OFFSET                  OY03847
HI       EQU   8                        ICM/STCM MASK           OY03847
HALF     EQU   3                        ICM/STCM MASK          @SA69629
AD       EQU   7                        ICM/STCM MASK          @SA69629
PGFREE   EQU   113                      PAGE FREE SVC           OY03847
FREEPAGE EQU   X'20'                    ACTION CODE             OY03847
         EJECT
         DS    0D
XCTLTAB  EQU   *
CLOSE2   DC    C'36'                    IGG02036                 S21903
PATCH    DC    8F'0'                    PATCH AREA               Y02027
         EJECT
         TTCXD                                                   Y02027
         EJECT
FORCORE  DSECT                                                   Y02027
         IECDSECT                                                Y02027
         EJECT
         TRECBD                                                  Y02027
*****************************************************************Y02027
*****    RCB EXTENSION FOR TCAM APPLICATION OPEN/CLOSE.          Y02027
*****************************************************************Y02027
RCBASCB  DS    F                        APPLIC. PGM ASCB ADDR.   Y02027
RCBPECB  DS    F                        APPLIC. PGM PECB ADDR.   Y02027
         EJECT
         TLCBD
         EJECT
         CVT   DSECT=YES                                       @Z30X8TM
         EJECT
         TTCBD
         EJECT
         TPCBD
         IHAPSA
         EJECT
         TPEBD                                                   Y02027
         EJECT
         TDEBD
         EJECT
         DCBD  DSORG=TX
         EJECT
         TAVTD
         EJECT
         TQCBD
         EJECT
         TTNTD                                                 @SA69629
         EJECT
         TTRMD                                                 @SA69629
         END
