404H     TITLE 'IGE0404H ERP CCW RETURN MODULE'
IGE0404H CSECT
         SPACE 3                                                 S21903
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A378000                                                         S21101
*D606000-818000                                                  S21101
*A304000,860000                                                  S99244
*C308000-310000                                                  S99244
*D236000,304000,404000,480000,492000,828000-830000,834000-836000 S99244
*D842000-860000,888000,898000-900000,910000,916000-928000        S99244
*D932000-940000,948000,962000                                    S99244
*C058000,076000,110000,126000,128000,160000,174000,182000,       S21903
*C196000,198000,220000,222000,230000,268000,270000,298000,       S21903
*C326000,328000,332000,400000,406000,446000,462000,484000,       S21903
*C504000,514000,552000,562000,566000,572000,576000,580000,       S21903
*C586000,602000,832000,833000,836000,838000,842000,878000,       S21903
*C896000,912000,944000                                           S21903
*D948000                                                         S21903
*      CLEAN-UP                                                  S21903
*C198900                                                        SA63037
*A 346020-346520,594100-594400,944100-945700                     X01004
*C 036000,198900,310000,424000,468000,820000                     X01004
*D 232000,236000-258000,312000-316000,426000-428000              X01004
*D 470000-472000,822000,824000                                   X01004
*A042000-043000,049000,068000,101000,105000,147000,166600-167200,Y02027
*A179000,492000,601000,962000                                    Y02027
*C102600,032000,040000-041000,048000,066000-067000,104000,154000,Y02027
*C186000,346000-346060,346420,346540-346600,347380,600000        Y02027
*A502000                                                       @SA69101
*A199500                                                       @YA06328
*A347000                                                       @XA08979
*D199570,199920                                                @SA74861
*A506000,588000                                                @SA74968
*A230000                                                       @ZA05036
*A402000                                                       @OS76308
*A346000                                                       @OS76482
*A183320,460000                                                @OS76482
*C264000-280000,A183680,A960000                                @OY10984
*A264800                                                       @OY12966
*C277600-278000                                                @OY12966
*C264400-278800,D183780,960600-961200                          @OY14057
*C346020                                                       @OY15006
*C308000-310400,A958000                                        @OX17153
         SPACE 3                                                 S21903
***********************************************************************
*                                                                     *
*TITLE -- 'IGE0404H', BSC INTERMEDIATE ERP CCW RETURN                 *
*                                                                     *
*  MODULE NAME = IGE0404H                                             *
*                                                                     *
*  DESCRIPTIVE NAME = BSC INTERMEDIATE ERP CCW RETURN                 *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 6                                            *
*                                                                     *
*FUNCTION -- TO SERVICE INTERRUPTS ON INTERMEDIATE ERP CCW'S.         *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
*EXTERNAL ROUTINE -- IGG019QE (AOS/TCAM SIO APPENDAGE) -- TO CONVERT  *
*   VIRTUAL CCW ADDRESSES TO REAL BEFORE RETRY.
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'ERP IN CONTROL (IOSERR)' AND 'EXCEPTIONAL   Y02027
*                CONDITION(IOSEX)'. RETURN TO IOS TO RETRY THE   Y02027
*                   ERROR.                                       Y02027
*        SVC   3                                                      *
*                                                                     *
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*        SVC   3                                                      *
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L     14,X'10'   CVT ADDRESS                           S21903*
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO I/O SUPERVISOR BLOCK                Y02027
*        SVC   15   'EXCEPTIONAL CONDITION(IOSEX)'. RETURN TO    Y02027
*                   LINE END APPENDAGE. ERROR PERMANENT.         Y02027
*        SVC   3                                                      *
*                                                                     *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L     14,X'10'   CVT ADDRESS                           S21903*
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*   TO SCHEDULE ERROR POST                                            *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TTRMD                                                             *
*   TCCWD                                                             *
*   TLCBD                                                             *
*   DCBD                                                              *
*   TSCBD                                                             *
*   IECDIOSB                                                     Y02027
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
*   IF AN INTERMEDIATE ERP SEQUENCE ENDED IN ERROR, IT IS RETRIED     *
*   USING THE ERP RETRY COUNT IN LCBERRCT.                            *
*                                                                     *
*   IF THE ERROR IS CLEARED, EXIT IS MADE TO LINE END APPENDAGE.      *
*   IF THE ERROR IS PERMANENT, EXIT IS MADE TO IGE0504H.              *
*                                                                     *
***********************************************************************
         EJECT
         USING IEDQCCW,RCCW
         USING IEDQTRM,RTERM
         USING IEDQLCB,RLCB
         USING IHADCB,RDCB
         USING IEDQAVTD,RAVT
         USING IEDQSCB,RSCB
         USING IOSB,RIOSB                                        Y02027
         USING *,R15
         SPACE 3                                                 S99244
*        R E G I S T E R S
R0       EQU   0                        WORK REGISTER
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RLCB     EQU   2                        LCB CBASE
RCCW     EQU   5                        CCW BASE
RSCB     EQU   3                        SCB BASE REGISTER        S21903
RDCB     EQU   4                        DCB BASE
RWKA     EQU   6                        WORK REGISTER
RWKB     EQU   7                        WORK REGISTER
RTST     EQU   7                        ADDR OF REQUEST QUEUE    Y02027
*                                       ELEMENT                  Y02027
RTERM    EQU   8                        TERMINAL BASE
RWKC     EQU   9                        WORK REGISTER
RWKD     EQU   10                       WORK REGISTER
RAVT     EQU   11                       AVT BASE                 S21903
RB       EQU   12                       WORK REGISTER
RLINK    EQU   13                       LINKAGE FOR SUBSEQUENT LOAD
RSAVE    EQU   13                       SAVE REGISTER FOR IOSB   Y02027
RXCTL    EQU   14                       XCTL REGISTER
R15      EQU   15                       ADDRESSABILITY REGISTER
THREE    EQU   3                        INVLIST OFFSET TO FLAGS  S99244
MASTER   EQU   X'20'                    CONTENTION FLAG IN INV   S99244
ACK0CH   EQU   4                        INDEX INTO SPECIAL CHARACTER
ACK0     EQU   X'41'                    ACK-0 FLAG FOR ACKRECEIVED
ACK1     EQU   X'42'                    ACK-0 FLAG FOR ACKRECEIVED
ACKI     EQU   X'40'                    ACK COUNTER MASK
RVICH    EQU   15                       INDEX INTO SCT SEQUENCE
NAKCH    EQU   6                        INDEX INTO SCT SEQUENCE
WACKCH   EQU   14                       INDEX INTO SCT SEQUENCE
REOT     EQU   X'04'                    MASK FOR EOT RECEIVED
RNAK     EQU   X'08'                    MASK FOR NAK RESPONSE    S21903
RWACK    EQU   X'10'                    RESPONSE RECEIVED
RRVI     EQU   X'20'                    RESPONSE RECEIVED
EOTCH    EQU   0                        INDEX TO SCT
RETRY    EQU   6                        RETRY COUNT
CVTPTR   EQU   X'10'                    POINTER TO CVT
XCTLADD  EQU   X'2C'                    OFFSET IN CVT-XCTL ROUTINE
IOBOFFST EQU   4                        IOB OFFSET IN RQE
ERPCTL   EQU   X'20'                    ERP IN CONTROL BIT       S21903
EREXCP   EQU   15                       ERROR EXCP
RETURN   EQU   3                        MASK FOR RETURN SVC
RENQ     EQU   X'80'                    ENQ WORK MASK
RSKRET   EQU   X'2C'                                           @OS76482
ENQCH    EQU   7                        OFFSET INTO SCT
XBF      EQU   X'BF'                    MASK FOR CHANNEL ERRORS  S21903
XF2      EQU   X'F2'                    MASK FOR DEVICE ERRORS
X24      EQU   X'24'                    MASK TO CLEAR ERROR FLAGS
UNEX     EQU   X'01'                    UNIT EXCEPTION MASK
POPERM   EQU   8                        504H RETURN INDICATOR    S21903
         EJECT
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         L     RLCB,IOBOFFST(,RTST)     ADDRESS OF IOB Z(LCB)
         SH    RLCB,LCBSIZE             ADJUST BASE
         L     RSCB,LCBSCBA-1           SCB BASE                 S21903
         L     RDCB,LCBDCBPT            DCB BASE
         L     RCCW,LCBCSW-1            FAILING CCW+8            S21903
         SH    RCCW,H8                  BACK UP TO FAILING CCW   S21903
         TM    LCBCSW+3,UNEX            UNIT EXCEPTION SET
         BZ    NOTUNEX                  NO
         SPACE
         CLI   CCWOPCDE,CCWWRITE        WRITE COMMAND
         BNE   NOTUNEX                  NO
         SPACE
         ST    RLCB,LCBERCCW+16         FOR READ SKIP
         MVI   LCBERCCW+16,CCWREAD      SET COMMAND CODE
         MVC   LCBERCCW+20(4),SKIP     SET FLAGS AND COUNT      SA63037
         LA    RWKA,LCBERCCW+16         FOR IOS START
         ST    RWKA,LCBSTART-1          SET PARAMETER
         BAL   RWKD,CHKCOUNT            CHECK ERROR COUNT
NOTUNEX  EQU   *
         LA    RWKA,LCBERCCW            FOR POSSIBLE RESTART
         ST    RWKA,LCBSTART-1          SET FOR IOS
         SR    RWKA,RWKA                CLEAR INDEX REGISTER
         IC    RWKA,LCBINCAM+1          RETURN INDICATOR
         B     TABLE1-4(RWKA)           USE BRANCH TABLE
TABLE1   EQU   *
         B     LOSTRET                  LOSTDTA,OVERRUN, DATA CHECK
*                                         ON RESPONSE TO TEXT
         B     WRENQRET                 UX ON WRITE ENQ
         B     UNEXRET                  UNEX WRITE RESPONSE
         B     WRRSPNQ                  UNEX WRITE RESPOSNE TO ENQ
         B     SKIPRET                  UX AN OTHER WRITES
         B     TORSPTXT                 TIME OUT RESPONSE TO TEXTS21903
         B     CMDRJRET                 CMD REJECT RESP. TO TEXT S21903
         B     TOWRITE                  TIME OUT ON WRITE
         B     BOXPRET                  BUSOUT TRANSPARENT TEXT
         B     RESETRET                 WR EOT AFTER PERM ERROR
*                                       IN READ RESPONSE CCW
         B     LDTEXT                   LOST DATA READ TEXT    @ZA05036
         EJECT
LOSTRET  EQU   *
*  THE ERP CCW SEQUENCE IS WRITE ENQ, TIC TO READ ESPONSE
         BAL   RWKD,ERRETRY             CHECK ERP CCW STATUS
         B     ERPCT                    BUILD WRITE EOT
         BAL   RWKD,CHACK               CHECK RESPONSE
         SPACE
         CLI   LCBRCB,0                 ANY VALID RESPONSE
         BE    TESTCNT                  BRANCH NO
         SPACE
         B     CLEARED                  RESET RETRY COUNT
         SPACE 10
LDTEXT   EQU   *
ERPCT    EQU   *
         MVI   LCBERRCT,0               CLEAR ERP ERROR COUNT  @OY10984
         SR    RWKA,RWKA                CLEAR FOR INSERT       @OY14057
         SR    RWKB,RWKB                CLEAR FOR INSERT       @OY14057
         L     RB,DCBSCTAD-ONE          GET SCT ADDRESS        @OY14057
         IC    RWKA,EOTCH(RB)           EOT SEQUENCE INDEX     @OY14057
         IC    RWKB,0(RWKA,RB)          GET COUNT              @OY14057
         LA    RWKA,ONE(RWKA,RB)        POINT TO SEQUENCE      @OY14057
         STM   RWKA,RWKB,LCBERCCW       SET DATA ADDR AND COUNT@OY14057
         MVI   LCBERCCW,CCWWRITE        WRITE COMMAND CODE     @OY10984
         LA    RWKA,LCBERCCW            START ADDRESS          @OY10984
         MVI   LCBINCAM+1,40            SET 404H RETURN
         B     COMEXIT                  RESTART
         SPACE 2
         SPACE 3
WRENQRET EQU   *
         BAL   RWKD,ERRETRY             CHECK ERROR RETRY COUNT
         B     PERMEXIT                 EXIT
         BAL   RWKD,CHACK               CHECK IF ENQ RECEIVED
         TM    LCBRCB,RENQ              ENQ RECEIVED             S21903
         BZ    BACKUP                   NO, RETRY WRITE ENQ
         SPACE
         TM    LCBSTAT2,LCBDIAL         DIAL LINE                S99244
         BO    BACKUP                   YES, RETRY WRITE ENQ     S99244
         SR    RWKA,RWKA                CLEAR FOR INDEXING       S99244
         IC    RWKA,LCBUCBX             GET RELATIVE LINE NO     S99244
         SLL   RWKA,2                   MULTIPLY BY FOUR         S99244
         L     RWKA,DCBINVLI(RWKA)      INVITATION LIST ADDR     S99244
         TM    THREE(RWKA),MASTER       MASTER SPECIFIED         S99244
         BO    BACKUP                   YES, BR TO TREAT AS SUCH S99244
*        CONTENTION EXISTS. THE FOLLOWING CODE WILL RESCHEDULE   S99244
*        THIS SEND OPERATION AFTER A RECEIVE OPERATION IS DONE   S99244
         MVC   LCBCSW,LCBCSWSV          RESTORE ORIGINAL CSW
         NC    LCBTTCIN,LCBTTCIN        IS DESTINATION DETERMND@OX17153
         BZ    NOLMT                    NO, NOT BUFFER BUSY    @OX17153
         LH    RWKC,LCBTTCIN            TERMINAL OFFSET        @OX17153
         LA    RWKB,AVTRNMPT            TNT CODE BASE          @OX17153
         USING IEDQTNTD,RWKB                                   @OX17153
         BAL   RWKA,TNTDCODE            FIND TERMINAL TYPE     @OX17153
         USING IEDQTRM,RTERM                                   @OX17153
         L     RTERM,TRMDESTQ-1         GET QCB ASSRESS        @OX17153
         USING IEDQQCB,RTERM                                   @OX17153
         OI    QCBELCHN+2,QCBCNTEN      SET FLAG FOR LINEEND TO@OX17153
*                                         CLEANUP LCB AND SCHED@OX17153
*                                         TO INITIATE A RECEIVE@OX17153
*                                         OPERATION            @OX17153
         DROP  RTERM,RWKB                                      @OX17153
NOLMT    EQU   *                                               @OX17153
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CNTRL    Y02027
         B     SETERR                   BRANCH TO INDICATE       X01004
*                                       SELECTION ERROR.         X01004
         SPACE 10
TORSPTXT EQU   *
         BAL   RWKD,ERRETRY             CHECK ERROR  ON ERP CCW
         B     ERPCT                    WRITE EOT
         BAL   RWKD,CHACK               CHECK RESPONSE RECEIVED  S21903
         MVC   LCBPRI,LCBMSGFM          SAVE ACK COUNTER         S21903
         XI    LCBPRI,ACKI              FLIP ACK COUNTER
         TM    LCBPRI,ACKI              TEST ACK EXPECTED        S21903
         BZ    ITSACK1                  BRANCH ACK 1 EXPECTED
         TM    LCBRCB,ACK0              SET ACK0 CONDITION CODE
TESTACK  EQU   *
         BO    CLEARED                  RESET RETRY COUNT
         BZ    TSTRVI                   CHECK OTHER POSSIBLE RESP
         SPACE
         CLI   LCBRESTR,TPRDRSPD        RESPONSE TO ADDRESSING
         BE    BACKUP                   BRANCH IF YES
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED
         BNL   ERPCT                    BRANCH IF YES
         MVI   LCBINCAM+1,0             RESET RETURN INDICATOR
         NI    IOSFLA,AVTEFF-IOSERR     POST PERMANENT ERROR     Y02027
         MVC   LCBCSW,LCBCSWSV          RESTORE ORIG CCW       @OY15006
         MVC   LCBSENS0,LCBSNSV         RESTORE ORIG SENSE     @OY15006
SETERR   EQU   *                                                 X01004
*                                       TO ABNORMAL END APPENDAG Y02027
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE IS ADDED FOR AOS.                         *
*                                                                     *
CONVERT  EQU   *                                                 X01004
         CLI   LCBECBCC,HIOCC           IOHALT ISSUED            Y02027
         BNE   SVCS                     BRANCH NO                Y02027
         SPACE 1                                                 Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CTL      Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
SVCS     EQU   *                                                 Y02027
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        LINE CONTROL BLOCK TO THE I/O SUPERVISOR BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY OPERATIONS FROM ERP          Y02027
**************************************************************** Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                       INTO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM LCB TO IOSB    Y02027
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         MVI   LCBSENS0,AVTEZERO        CLEAR SENSE BYTE       @XA08979
         MVI   IOSSNS,AVTEZERO          CLEAR IOSB SENSE       @XA08979
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         L     RLCB,IOBOFFST(,RTST)     GET IOB ADDRESS FROM RQE X01004
*                                       FOR USE BY SIO APPENDAGE X01004
         L     RTERM,DEBPTR(,RTST)      GET DEB ADDRESS FROM RQE X01004
         LA    R15,DEBNMSUB-IEDQDEB     GET APDG. TABLE SIZE     Y02027
         SLR   RTERM,R15                POINT TO APDG. TABLE     Y02027
         L     RTERM,SIOPTR(ZERO,RTERM)  GET ADDR OF SIO APDG.   X01004
*                                       FROM APDG. VECTOR TABLE. X01004
         SR    R15,R15                  CLEAR R15 TO TELL SIO    X01004
*                                       APDG. IT'S A SUBROUTINE. X01004
         BAL   RXCTL,SIOENTRY(ZERO,RTERM)  LNK TO SIO APDG AT    X01004
*                                       OFFSET +16 TO CONVERT    X01004
*                                       CCW'S TO REAL.           X01004
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
         SVC   EREXCP
         SVC   RETURN
ITSACK1  EQU   *
         TM    LCBRCB,ACK1              SET ACK1 CONDITION CODE
         B     TESTACK                  USE COMMON BRANCHES
TSTRVI   EQU   *
         TM    LCBRCB,RRVI+RNAK+RWACK+REOT VALID RESPONSE
         BNZ   ERRCLEAR                 YES
TESTCNT  EQU   *
         CLI   LCBERRCT,RETRY           ERP RETRY COUNT
         BL    BUMPIT                   TRY AGAIN
         SPACE
         B     ERPCT                    RESET THE LINE
         SPACE 10
RESETRET EQU   *
TOWRITE  EQU   *
CMDRJRET EQU   *
         NI    SCBBSCFM,AVTEFF-SCBNOEOT RESET BSC DIAL SEND PRI  S21101
*                                         FLAG WHICH INDICATES THS21101
*                                         NO EOT WAS SENT        S21101
         OI    LCBCHAIN,LCBNORTY        SET NO RETRY
         BAL   RWKD,ERRETRY             CHECK STATUS ON ERP CCW
         B     PERMEXIT                 NORMAL RETURN
         B     PERMEXIT                 EXIT
         SPACE 10
WRRSPNQ  EQU   *
UNEXRET  EQU   *
         MVI   LCBERRCT,AVTEZERO        CLEAR ERP RETRY COUNTER
SKIPRET  EQU   *
BACKUP   EQU   *
         L     RWKA,LCBCSWSV-1          ORIGINAL FAILING CCW     S21903
         SH    RWKA,H8                  BACK UP TO WRITE RESPONSE
         CLI   LCBRESTR,TPRDRSPD        RESPONSE TO ADDRESSING?@OS76308
         BNE   NOTADDR                  NO, LEAVE AS IS        @OS76308
         SH    RWKA,H8                  BACK UP TO WRITE ADDR  @OS76308
NOTADDR  EQU   *                                               @OS76308
         MVI   LCBINCAM+1,0             RESET RETURN FIELD       S21903
         MVI   LCBERRCT,0               RESET ERP RETRY COUNT
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED
         BNL   PERMEXIT                 YES
         IC    RWKB,LCBINCAM            GET CURRENT COUNT
         LA    RWKB,1(,RWKB)            BUMP
         STC   RWKB,LCBINCAM            SET BACK
COMEXIT  EQU   *
         ST    RWKA,LCBSTART-1          RESTART POINT
         B     CONVERT                  BRANCH TO SET UP RETRY.  X01004
         SPACE 10
BOXPRET  EQU   *
         MVI   LCBCSWSV+3,X'0C'         SET NORMAL ENDING
         MVC   LCBCSW,LCBCSWSV          RESET 'CURRENT' CSW
         B     ERRCLEAR                 EXIT TO APPENDAGE
         SPACE 10
ERRETRY  EQU   *
         TM    LCBCSW+4,XBF             CHANNEL ERRORS
         BNZ   CHKCOUNT                 BRANCH IF ERRORS         S21903
         SPACE
         TM    LCBCSW+3,XF2             DEVICE ERRORS
         BC    8,4(RWKD)                BRANCH YES
CHKCOUNT EQU   *
         CLI   LCBERRCT,RETRY           ERP RETRY COUNT
         BCR   11,RWKD                  YES
BUMPIT   EQU   *
         L     RWKA,LCBSTART-1          GET CCW ADDRESS        @OS76482
         TM    CCWFLAGS-IEDQCCW(RWKA),CCWSKIP IS SKIP FLAG ON  @OS76482
         BZ    OUT                      BRANCH NO              @OS76482
         MVI   LCBINCAM+1,RSKRET        ABORT AFTER READ SKIP  @OS76482
OUT      EQU   *                                               @OS76482
         IC    RWKA,LCBERRCT            ERP RETRY COUNT          S21903
         LA    RWKA,1(,RWKA)            BUMP
         STC   RWKA,LCBERRCT            SET BACK
         B     CONVERT                  BRANCH TO SET UP RETRY.  X01004
         SPACE 2
PERMEXIT EQU   *
         MVC   LCBCSW,LCBCSWSV          RESTORE ORIGINAL CSW     S21903
         MVC   LCBSENS0,LCBSNSV         ORIGINAL SENSE
         MVI   LCBINCAM+1,POPERM        504H RETURN
         LH    RLINK,POSTMOD            504H ID
         L     RXCTL,CVTPTR             PICKUP CVT ADDRESS       Y02027
         L     RXCTL,XCTLADD(,RXCTL)    XCTL ADDRESS
         BR    RXCTL                    CALL ROUTINE
         SPACE 2
         EJECT
CHACK    EQU   *
         TM    CCWOPCDE,CCWTIC          IS IT A TIC            @SA69101
         BNO   CHACK1                   NO, BRANCH             @SA69101
         SH    RCCW,H8                  BACK-UP TO FAILING CCW @SA69101
CHACK1   EQU   *                                               @SA69101
         LH    RWKA,CCWCOUNT            ORIGINAL COUNT           S21903
         SH    RWKA,LCBCSW+5            SUBTRACT RESIDUAL COUNT
         BZ    NOCHACK                  IS RESIDUAL EQ TO ORIG @SA74968
*                                       BRANCH YES             @SA74968
         BCTR  RWKA,0                   DECREMENT
         BCTR  RWKA,0                   NOW TWO
         L     RWKC,CCWADDR-1           DATA ADDRESS
         AR    RWKC,RWKA                POINT TO 2 CHAR RESPONSE S21903
         SR    RWKA,RWKA                CLEAR FOR INDEXING
         L     RWKB,DCBSCTAD-1          SCT ADDRESS
         IC    RWKA,ACK0CH(,RWKB)       INDEX TO SEQUENCE
         LA    RB,1(RWKA,RWKB)          BUMP TO SEQUENCE
         MVI   LCBRCB,ACK0              ASSUMED ACK0 RECEIVED
         CLC   0(2,RWKC),0(RB)          TEST FOR SEQUENCE
         BCR   8,RWKD                   RETURN
         SPACE
         LA    RB,3(,RB)                BUMP TO ACK-1 SEQUENCE
         MVI   LCBRCB,ACK1              ASSUME ACK-1 RECEIVED
         CLC   0(2,RWKC),0(RB)          TEST FOR ACK-1
         BCR   8,RWKD                   BRANCH YES
         IC    RWKA,ENQCH(,RWKB)        INDEX TO SEQUENCE
         LA    RB,1(RWKA,RWKB)          BUMP TO ENQ
         MVI   LCBRCB,RENQ              ASSUME ENQ RECEIVED
         CLC   1(1,RWKC),0(RB)          ENQ RECEIVED
         BCR   8,RWKD                   RETURN
         IC    RWKA,NAKCH(,RWKB)        INDEX\3 >)1+)(")
         LA    RB,1(RWKA,RWKB)          POINT TO SEQUENCE        S21903
         MVI   LCBRCB,RNAK              ASSUME NAK
         CLC   1(1,RWKC),0(RB)          NAK RECEIVED
         BCR   8,RWKD                   YES
         IC    RWKA,WACKCH(,RWKB)       INDEX TO WACK
         LA    RB,1(RWKA,RWKB)          POINT TO SEQUENCE        S21903
         MVI   LCBRCB,RWACK             SET RESPONSE
         CLC   0(2,RWKC),0(RB)          THIS THE RESPONSE        S21903
         BCR   8,RWKD                   YES
         IC    RWKA,RVICH(,RWKB)        INDEX TO RVI
         LA    RB,1(RWKA,RWKB)          POINT TO SEQUENCE        S21903
         MVI   LCBRCB,RRVI              ASSUME RVI
         CLC   0(2,RWKC),0(RB)          THIS THE RESPONSE        S21903
         BCR   8,RWKD                   YES
         IC    RWKA,EOTCH(,RWKB)        EOT INDEX                S21903
         LA    RB,1(RWKA,RWKB)          POINT TO SEQUENCE
         CLC   1(1,RWKC),0(RB)          EOT RECEIVED
         MVI   LCBRCB,REOT              ASSUME EOT RECEIVED      S21903
         BCR   8,RWKD                   BRANCH IF EOT
NOCHACK  EQU   *                                               @SA74968
         SPACE
         MVI   LCBRCB,0                 INVALID RESPONSE
         BR    RWKD                     RETURN
CLEARED  EQU   *
         MVI   LCBINCAM,0               RESET RETRY COUNT
         EJECT
ERRCLEAR EQU   *
         NI    IOSFLA,AVTEFF-(IOSERR+IOSEX) SET NORMAL ENDING    Y02027
*                                       STATUS                   Y02027
         MVI   LCBINCAM+1,0             RESET RETURN INDICATOR   S21903
         B     CONVERT                  BRANCH TO SET UP RETRY.  X01004
         EJECT
H8       DC    H'8'                     CONSTANT                 S21903
SKIP     DC    AL1(CCWSKIP),X'00FFFF'   CONSTANT                 S21903
LCBSIZE  DC    AL2(LCBFLAG1-IEDQLCB)    CONSTANT                 S21903
POSTMOD  DC    AL2(5048)                POST MODULE ID
DEBPTR   EQU   8                        OFFSET OF DEB ADDR. IN   X01004
*                                       RQE.                     X01004
SIOPTR   EQU   4                        OFFSET OF SIO APDG. ADDR X01004
*                                       IN APDG. VECTOR TABLE.   X01004
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
*                                       ENTRY POINT IN SIO APDG. X01004
ZERO     EQU   0                        LENGTH
FOUR     EQU   4                        EQUATES
SEVEN    EQU   7                        FOR
TWENTY   EQU   20                       ADDRESSING
ONE      EQU   1                         EQUATE OF 1             Y02027
TWO      EQU   2                         EQUATE OF 2             Y02027
HIOCC    EQU   X'48'                    HALT I/O COMPLETION      Y02027
         EJECT
         TDEBD                          DEB DSECT                Y02027
         EJECT
         TTRMD
         TTPD
         TCCWD
         TLCBD
         DCBD  DSORG=TX
         TSCBD
         TQCBD                                                 @OX17153
         TTNTD                                                 @OX17153
         TAVTD
         IECDIOSB                                                Y02027
   END
