         TITLE 'IGC0910D - OPCTL VARY, HOLD, MODIFY, RELEASE MSG MOD'
IGC0910D CSECT
*A000000-999999                                                  S99240
*C004000                                                       @Y17XARR
*CC941600-941700                                               @Y17XARR
*C941560                                                       @OZ27084
***********************************************************************
*                                                                     *
*MODULE NAME = IGC0910D(TCAM, OPERATOR CONTROL)               @1Y7XARR*
*                                                                     *
*DESCRIPTIVE NAME = OPERATOR CONTROL VARY, HOLD, MODIFY,      @Y17XARR*
*                  RELEASE MSG MODULE                         @Y17XARR*
*                                                                     *
*COPYRIGHT = NONE                                             @Y17XARR*
*                                                                     *
*STATUS = VERSION 10.0                                        @Y17XARR*
*                                                                     *
* FUNCTION -- THIS MODULE TAKES THE MESSAGE NUMBER PASSED IN REGISTER *
*    0 LOCATES IT IN A TABLE, AND THE MESSAGE IS MOVED TO THE RESIDENT*
*    WORKAREA AT OFFSET 4.  THE LENGTH OF THE MESSAGE IS PLACED IN    *
*    THE WORKAREA AT OFFSET 0.  THE DDNAME AT OFFSET DECIMAL 32 IN THE*
*    OPERATOR CONTROL ELEMENT (OPCE) IS INSERTEDIN THE MESSAGE.  NEXT *
*    THE COMMAND IS CHECKED TO SEE IF IT IS A VARY COMMAND.  IF IT    *
*    ISN'T THE SEQUENCE NUMBER IS INSERTED IN THE MESSAGE IF REQUIRED.*
*    IN EITHER CASE THE MESSAGE WRITER IGC0710D IS XCTLED TO UNLESS   *
*    MSG IS NOT IN THIS MODULE'S MSG TABLE THEN THIS MODULE XCTLS TO  *
*    IGC0310D.                                                @Y17XARR*
*                                                                     *
* REGISTER CONVENTIONS= OPERATOR CONTROL CONVENTIONS          @Y17XARR*
*                                                                     *
*   PATCH LABEL = NONE                                        @Y17XARR*
*                                                                     *
*  MODULE TYPE = PROCEDURE                                    @Y17XARR*
*                                                                     *
*  PROCESSOR = ASSEMBLE XF                                    @Y17XARR*
*                                                                     *
*  MODULE SIZE= SEE LISTING                                   @Y17XARR*
*                                                                     *
* ENTRY POINTS --                                                     *
*  PURPOSE = SEE FUNCTION                                     @Y17XARR*
*                                                                     *
*  LINKAGE = XCTL                                             @Y17XARR*
*                                                                     *
*    IGC0910D IS THE ONLY ENTRY POINT OF THIS MODULE.                 *
*                                                                     *
* INPUT --                                                            *
*    POINTER TO THE OPERATOR CONTROL AVT IN REGISTER 1 AND A PARAMETER*
*    IN REGISTER 0.                                                   *
*                                                                     *
* OUTPUT --                                                           *
*    REGISTER 1 CONTAINS THE ADDRESS OF THE OPCAVT AND THE RESIDENT   *
*    WORKAREA CONTAINS THE OUTPUT MESSAGE.                            *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         NONE.                                                       *
*                                                                     *
* EXITS,NORMAL --                                                     *
*         XCTL TO IGC0710D FOR OUTPUT.                                *
*                                                                     *
*         XCTL TO IGC0310D IF MSG IS NOT RECOGNIZE IN IGC0910D@Y17XARR*
*                                                                     *
* EXITS,ERROR --                                                      *
*         NONE.                                                       *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    TCAM AVT, OP CONTROL AVT, OPCE, ERRORTAB.                        *
*                                                                     *
* MACROS = IEDHJN,XCTL                                        @Y17XARR*
*                                                                     *
* ATTRIBUTES --                                                       *
*    SERIALLY REUSABLE, REFRESHABLE, ENABLED, TRANSIENT,              *
*    SUPERVISOR MODE.                                                 *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS            *
*    EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING         *
*    HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER              *
*    CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE         *
*    FOR THE NEW DEFINITION.                                          *
*                                                                     *
* NOTES --                                                            *
*    COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS          *
*    PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                      *
*                                                                     *
***********************************************************************
         EJECT
         SPACE
*                                 E Q U A T E S
         SPACE
RZERO    EQU   0                        PARAMETER REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
ROPCAVT  EQU   2                        POINTER TO OP CONTROL AVT
ROPCE    EQU   3                        POINTER TO CURRENT OPCE
RAVT     EQU   4                        TCAM AVT ADDRESS
RWORKA   EQU   5                        POINTER TO RESIDENT WORKAREA
RMSGPT   EQU   6                        POINTER TO CURRENT ERROR MSG
RTERM    EQU   6                        TERM ENTRY BASE
RTNT     EQU   6                        TERM NAME TABLE ADDR
RDCB     EQU   7                        DCB BASE
RMSGLEN  EQU   7                        LENGTH OF ERROR MESSAGE
ROFF     EQU   7                        OFFSET TO DDNAME, ADDR, RLN
RWORK    EQU   8                        WORK REGISTER
RMSGNO   EQU   9                        NUMBER OF ERROR MESSAGE
RWORK1   EQU   9                        WORK REGISTER
RPREFIX  EQU   9                        BUFFER ADDRESS
RHOLD    EQU   10                       SAVE VALUE REGISTER
RDEB     EQU   10                       DEB BASE
RQCB     EQU   11                       QCB ADDRESS
RBASE    EQU   12                       BASE FOR MODULE
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RBRNCH   EQU   15                       SUBROUTINE BRANCH ENTRY POINT
         SPACE 2
ADDROFF  EQU   2                        OFFSET IN TABLE TO MESSAGE ADDR
COMMA    EQU   X'6B'                    COMMA FOR DATA OUTPUT
DATAOFF  EQU   1                        OFFSET TO OFFSET
DDOFF    EQU   0                        OFFSET TO DDNAME OR LINE NO.
DDSIZE   EQU   8                        MAXIMUM DDNAME SIZE
EIGHT    EQU   8                        LENGTH OF EIGHT
ENTLEN   EQU   4                        LENGTH OF ERROR TABLE ENTRY
FOUR     EQU   4                        LENGTH OF FOUR
FRSTCHAR EQU   C'I'                     START OF MESSAGE CHARACTER
LASTCHAR EQU   7                        OFFSET TO LAST CONVERTED CHAR
LENOFF   EQU   0                        OFFSET TO LENGTH OF MESSAGE
MESSAGE  EQU   4                        OFFSET TO MESSAGE PAST OFFSETS
MSGLOC   EQU   4                        OFFSET TO MESSAGE IN WORKAREA
NAMELEN  EQU   2                        NO. OF NAME CHANGE CHARACTERS
NULL     EQU   0                        ZERO VALUE FOR EXECUTES
NUMOFF   EQU   0                        OFFSET IN TABLE TO MESSAGE NO.
PICKUP   EQU   5                        LOCATION OF DATA AFTER CONVERT
RLNOFF   EQU   9                        OFFSET TO RELATIVE LINE NO.
RLNSIZE  EQU   3                        SIZE OF MAX RLN
SEQOFF   EQU   2                        OFFSET TO SEQ. NO. OFFSET
SETCOMMA EQU   8                        LOCATION FOR COMMA
TERMOFF  EQU   0                        OFFSET TOE TERM NAME
NAMEOFF  EQU   2                        OFFSET IN MSG TO         S22024
*                                       LOAD MODULE NAME         S22024
VARINAME EQU   3                        OFFSET TO CHANGE LOAD NAME
VARY     EQU   X'28'                    INTERNAL OP CODE FOR VARY
ZONE     EQU   X'F0'                    CHARACTER TO COVER CONVERT SIGN
THREE    EQU   3                        THREE                    S22024
SEQCK    EQU   X'00'                    TO DETERMINE IF SEQ NO IN MSG
X40      EQU   X'40'                    TO CHECK IF START-STOP TERMINAL
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         SPACE
         BALR  RBASE,0                  SET BASE REGISTER
         USING *,RBASE                  MODULE ADDRESSABILITY
BASE     EQU   *
IGC0910D IEDHJN SKIPID
         LR    ROPCAVT,RPARM            SET UP AVT ADDRESSING
         L     RAVT,OPCAVTPT            TCAM AVT ADDRESSING
         L     ROPCE,OPCCOPCE           GET CURRENT ELEMENT POINTER
         L     RWORKA,OPCWORK           GET WORKAREA ADDRESS
         LR    RMSGNO,RZERO             PICK UP PASSED MESSAGE NUMBER
         LA    RMSGPT,ERRORTAB          PICK UP ERROR MESSAGE TABLE AD
LOCATMSG EQU   *
         CLI   NUMOFF(RMSGPT),AVTEFF    IS THIS END OF TABLE     S22024
         BE    BADMSG                   BEANCH ON YES            S22024
         CH    RMSGNO,NUMOFF(RMSGPT)    CORRECT ERROR MESSAGE?
         BE    PROCMSG                  YES, PROCESS MESSAGE
         SPACE
         LA    RMSGPT,ENTLEN(RMSGPT)    POINT TO NEXT ENTRY
         B     LOCATMSG                 CONTINUE CHECKING
BADMSG   EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),XCTL03  XCTL NAME      S22024
         B     XCTL                     GO TO NEXT MODULE        S22024
         SPACE
*              SET UP WORKAREA TO CONTAIN APPROPRIATE MESSAGE         *
PROCMSG  EQU   *
         LH    RMSGPT,ADDROFF(RMSGPT)   GET ADDRESS OF ERROR MESSAGE
         AR    RMSGPT,RBASE             ADD BASE TO OFFSET
         SR    RMSGLEN,RMSGLEN          CLEAR REGISTER FOR MESSAGE LENG
         IC    RMSGLEN,LENOFF(RMSGPT)   PICK UP MESSAGE LENGTH
         STH   RMSGLEN,LENOFF(RWORKA)   PLACE MESSAGE LENGTH IN WA
         BCTR  RMSGLEN,RZERO            DECREMENT FOR MOVE
         EX    RMSGLEN,MOVEMSG          MOVE MESSAGE INTO WORKAREA
         SPACE
         SR    ROFF,ROFF                CLEAR FOR INSERT
         LA    RWORKA,FOUR(RWORKA)      POINT TO BEGINNING OF DATA
         IC    ROFF,DATAOFF(RMSGPT)     GET OFFSET TO DATA FIELD
         LA    RWORKA,AVTEZERO(ROFF,RWORKA) POINT TO DATA PICK UP
         MVC   DDOFF(DDSIZE,RWORKA),OCBACKUP PLACE DDNAME IN MSG
         CH    RMSGNO,MSG160A           3705 LOAD CMD ?          S22024
         BE    MOVELM                   YES MOVE NAME INTO MSG   S22024
         CH    RMSGNO,MSG186A           3705 BACKUP CMD ?        S22024
         BE    MOVELM                   YES MOVE NAME INTO MSG   S22024
         CLI   OCOPTCDE,VARY            VARY COMMAND?
         BNE   WRTXCTL                  NO, MESSAGE IS CORRECT NOW
         SPACE
         TM    OCFLAG,OCLINE            LINE PROCESSED IN DATA
         BO    WRTXCTL                  YES, DATA HAS BEEN SET
         SPACE
         TM    OCSWITCH,X40             START-STOP TERMINAL
         BO    WRTXCTL                  YES
         SPACE
         MVC   RLNOFF(RLNSIZE,RWORKA),ALL SET RLN TO ALL
         SR    RWORK,RWORK              CLEAR FOR INSERT
         IC    RWORK,OCRLN              PICK UP RELATIVE LINE NO
         LTR   RWORK,RWORK              RLN SPECIFIED?
         BZ    WRTXCTL                  PUT OUT MESSAGE
         SPACE
         MVI   SETCOMMA(RWORKA),COMMA   SET COMMA IN DATA
         CVD   RWORK,OPCDOUBL           CONVERT RLN
         UNPK  OPCDOUBL(EIGHT),OPCDOUBL(EIGHT) UNPACK RLN
         OI    OPCDOUBL+LASTCHAR,ZONE   COVER SIGN
         MVC   RLNOFF(RLNSIZE,RWORKA),OPCDOUBL+PICKUP PLACE RLN
WRTXCTL  EQU   *
         CLI   SEQOFF(RMSGPT),SEQCK     CHARACTER FOR 2 (NO OFFSET)
         BZ    NOSEQ                    YES, DO NOT PLACE SEQUENCE NO.
         SPACE
         SR    ROFF,ROFF                CLEAR FOR INSERT
         IC    ROFF,SEQOFF(RMSGPT)      PICK UP SEQUENCE OFFSET
         LA    RWORKA,AVTEZERO(ROFF,RWORKA) ADD OFFSET TO START
         MVC   AVTEZERO(FOUR,RWORKA),OCMODNME PLACE SEQUENCE NO. IN MSG
NOSEQ    EQU   *
         MVC   OPCLDNME+VARINAME(NAMELEN),WRITE SET UP WRITE MOD NAME
XCTL     EQU   *                                                 S22024
         LR    RPARM,ROPCAVT            POINT TO AVT
         SPACE
         XCTL  SF=(E,OPCXCTL)           CALL NEXT MODULE
         SPACE 2
MOVEMSG  MVC   MSGLOC(NULL,RWORKA),THREE(RMSGPT)   MOVING MESSAGE
         SPACE
MOVENAME MVC   TERMOFF(AVTEZERO,RWORKA),AVTEZERO(RPARM) PLACE TERMNAME
*                                       MESSAGE TABLE FOR VARY, HOLD,
*                                       AND RELEASE COMMANDS INCLUDING
*                                       GENERAL POLL
MOVELM   EQU   *
         IC    ROFF,NAMEOFF(RMSGPT)     GET OFFSET TO DATA FIELD S22024
         LA    RWORKA,AVTEZERO(ROFF,RWORKA) POINT TO DATA OFFSET S22024
         MVC   DDOFF(DDSIZE,RWORKA),OCMODNME PICK UP LOAD MOD    S22024
         B     NOSEQ                    XCTL TO OUTPUT WRITER    S22024
***********************************************************************
*                                                                     *
*  THE FOLLOWING TABLE CONTAINES THE MESSAGE NUMBER AND THE      S22024
*   OFFSET FROM THE BEGINNING OF THIS MODULE TO THE MESSAGE.     S22024
*                                                                     *
***********************************************************************
         SPACE 2
ERRORTAB DS    0F
         DC    AL2(20),AL2(MSG20-BASE)  MESSAGE NUMBER
         DC    AL2(26),AL2(MSG26-BASE)  AND
         DC    AL2(51),AL2(MSG51-BASE)  MESSAGE
         DC    AL2(54),AL2(MSG54-BASE)  ADDRESSES
         DC    AL2(143),AL2(MSG143-BASE)
         DC    AL2(144),AL2(MSG144-BASE)
         DC    AL2(145),AL2(MSG145-BASE)
         DC    AL2(146),AL2(MSG146-BASE)
         DC    AL2(147),AL2(MSG147-BASE)
         DC    AL2(160),AL2(MSG160-BASE)                         S22024
         DC    AL2(174),AL2(MSG174-BASE)                         S22024
         DC    AL2(186),AL2(MSG186-BASE)                         S22024
         DC    AL2(194),AL2(MSG194-BASE)                         S22024
         DC    AL2(195),AL2(MSG195-BASE)                         S22024
         DC    AL2(401),AL2(MSG401-BASE)                         S22024
         DC    AL2(398),AL2(MSG398-BASE)                         S22024
         DC    X'FF'                                             S22024
***********************************************************************
*                                                                     *
*  THE FOLLOWING ARE DC'S FOR THE MESSAGES                       S22024
*                                                                     *
***********************************************************************
         SPACE 2
MSG20    DC    AL1(END20-*-3),AL1(8),AL1(0) VARY STARTED
         DC    C'IED020I              STARTED' MESSAGE
END20    EQU   *
         SPACE
MSG26    DC    AL1(END26-*-3),AL1(8),AL1(0) VARY STOPPED
         DC    C'IED026I              STOPPED' MESSAGE
END26    EQU   *
         SPACE
MSG51    DC    AL1(END51-*-3),AL1(8),AL1(31) GOOD HOLD
         DC    C'IED051I          SET FOR HOLD, SEQ-OUT=    ' MESSAGE
END51    EQU   *
         SPACE
MSG54    DC    AL1(END54-*-3),AL1(8),AL1(27) GOOD RELEASE
         DC    C'IED054I          RELEASED, SEQ-OUT=    ' MESSAGE
END54    EQU   *
         SPACE 2
MSG143   DC   AL1(END143-*-3),AL1(8),AL1(0) GEN. POLL STARTED
         DC    C'IED143I          GENERAL POLL STARTED' MESSAGE
END143   EQU   *
         SPACE
MSG144   DC   AL1(END144-*-3),AL1(8),AL1(0) GEN. POLL STOPPED
         DC    C'IED144I          GENERAL POLL STOPPED' MESSAGE
END144   EQU   *
         SPACE
MSG145   DC    AL1(END145-*-3),AL1(8),AL1(0) G.P. ALREADY STARTED
         DC    C'IED145I          GENERAL POLL ALREADY STARTED'
END145   EQU   *
         SPACE
MSG146   DC    AL1(END146-*-3),AL1(8),AL1(0) G.P. ALREADY STOPPED
         DC    C'IED146I          GENERAL POLL ALREADY STOPPED'
END146   EQU   *
         SPACE
MSG147   DC    AL1(END147-*-3),AL1(8),AL1(0) GENERAL POLL MESSAGE
         DC    C'IED147I          COMMAND INVALID FOR GENERAL POLL'
END147   EQU   *
MSG160   DC    AL1(END160-*-3),AL1(13),AL1(14)                   S22024
         DC    C'IED160I 3705          LOAD=        '            S22024
END160   EQU   *                                                 S22024
MSG174   DC    AL1(END174-*-3),AL1(13),AL1(0)  BACKUP IS INVALID S22024
         DC    C'IED174I 3705          IS AN AVAILABLE BACKUP'   S22024
END174   EQU   *                                                 S22024
MSG186   DC    AL1(END186-*-3),AL1(13),AL1(16)                   S22024
         DC    C'IED186I 3705          BACKUP=        '          S22024
END186   EQU   *                                                 S22024
MSG194   DC    AL1(END194-*-3),AL1(13),AL1(0) IPL ENDED MESSAGE  S22024
         DC    C'IED194I 3705          IPL ENDED'                S22024
END194   EQU   *                                                 S22024
MSG195   DC    AL1(END195-*-3),AL1(13),AL1(0)                    S22024
         DC    C'IED195I 3705             IPL/DUMP CANCELLED'    S22024
END195   EQU   *                                                 S22024
MSG401   DC    AL1(END401-*-3),AL1(8),AL1(0)                   @Y17XARR
         DC    C'IED401I              HAS BEEN DEACTIVATED'    @OZ27084
END401   EQU   *                                                 S22024
MSG398   EQU   *
         DC    AL1(END398-*-3),AL1(13),AL1(0)                    S22024
         DC    C'IED398I 3705          DUMP ENDED'
END398   EQU   *
***********************************************************************
         SPACE
XCTL03   DC    C'03'                    ERROR MODULE NAME
CHECKPT  DC    C'06'                    CHECKPOINT MODULE NAME
WRITE    DC    C'07'                    OUTPUT WRITER NAME
ALL      DC    C'ALL'                   RLN WAS ALL
MSG160A  DC    H'160'                   LOAD CMD MSG NUM         S22024
MSG186A  DC    H'186'                   BACKUP CMD MSG NUM       S22024
         TITLE 'IGC0910D - DSECTS REFERENCED BY THIS MODULE'
         TAVTD
         EJECT
         TOPCED
         EJECT
         TOPCAVTD
         END
