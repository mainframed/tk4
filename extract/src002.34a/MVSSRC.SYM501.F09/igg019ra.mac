         TITLE 'IGG019RA-CHECKPOINT DISK END APPENDAGE'
IGG019RA CSECT
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS:
*A 525000,735000                                                OY03902
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE:'IGG019RA' CHECKPOINT DISK END APPENDAGE                       *
*                                                                     *
*                                                                     *
*  MODULE NAME = IGG019RA                                             *
*                                                                     *
*  DESCRIPTIVE NAME = CHECKPOINT DISK END APPENDAGE                   *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
*                                                                     *
*FUNCTION:THIS MODULE WRITES THE CHECKPOINT CONTROL RECORD AFTER      *
*   THE LAST SEGMENT OF AN ENVIRONMENT CHECKPOINT RECORD IS WRITTEN   *
*                                                                     *
*ENTRY POINT:                                                         *
*                                                                     *
*        IGG019RA                                                     *
*                                                                     *
*INPUT:PARAMETERS ARE PASSED IN REGISTER BY THE I/O SUPERVISOR AS     *
*   FOLLOWS:                                                          *
*                                                                     *
*1-ADDRESS OF RQE                                                     *
*   2-ADDRESS OF IOB                                                  *
*   3-ADDRESS OF DEB                                                  *
*   4-ADDRESS OF DCB                                                  *
*   7-ADDRESS OF UCB                                                  *
*   14-RETURN POINT-IOS                                               *
*   15-ADDRESS OF APPENDAGE ENTRY POINT                               *
*                                                                     *
*OUTPUT:THE ABOVE REGISTERS REMAIN THE SAME                           *
*                                                                     *
*   THE CHECKPOINT CONTROL RECORD IS WRITTEN.                         *
*                                                                     *
*EXTERNAL ROUTINES:NONE                                               *
*                                                                     *
*EXITS-NORMAL:THIS MODULE                                             *
*                                                                     *
*   1.BRANCHES TO ADDRESS IN REGISTER 14-THIS MODULE IS NOT           *
*     WRITTING A CONTROL RECORD.                                      *
*   2.BRANCHES TO ADDRESS IN REGISTER 14 PLUS 8-THIS MODULE IS        *
*     WRITTING A CONTROL RECORD USING RETRY.                          *
*                                                                     *
*EXITS-ERROR: THIS MODULE BRANCHES TO ADDRESS IN REGISTER 14.         *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   DEB                                                               *
*   CHECKPOINT WORK AREA (INCLUDES IOB AND CONTROL RECORD)            *
*                                                                     *
*ATTRIBUTES:REENTERABLE                                               *
*                                                                     *
*NOTES:THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRE-   *
*   SENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO    *
*   THE ONE USED AT ASSEMBLY TIME. THE CODING HAS BEEN ARRANGED SO    *
*   THAT REDEFINITION OF 'CHARACTER CONSTANTS,BY REASSEMBLY,WILL      *
*   RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.               *
*                                                                     *
***********************************************************************
         SPACE 3
*REGISTER ASSIGNMENTS
         SPACE
R0       EQU   0
R1       EQU   1                        ADDR OF REQUEST QUEUE ELEMT
R2       EQU   2                        ADDR OF IOB
R3       EQU   3                        ADDR OF DEB
R4       EQU   4                        ADDR OF DCB
R5       EQU   5
R6       EQU   6
R7       EQU   7                        ADDR OF UCB
R8       EQU   8
R9       EQU   9
R10      EQU   10                       ADDR OF CKPT WORK AREA
R11      EQU   11                       ADDR OF DISK RECORD
R12      EQU   12
R13      EQU   13
R14      EQU   14                       RETURN ADDR
R15      EQU   15                       ADDR OF APPENDAGE ENTRY PT
ZERO     EQU   0                       DISPLACEMENT             OY03902
ONE      EQU   1                       LENGTH                   OY03902
SIX      EQU   6                       DISPLACEMENT             OY03902
SEV      EQU   7                       DISPLACEMENT             OY03902
         SPACE 3
         USING CKPIOB,R2                BASE FOR CHECKPOINT WK AREA
         SPACE
         L     R11,CKPRW                ADDRESS OF DISK RECORD
         USING IEDQCDRD,R11             BASE
         SPACE
         CLI   CDRKEY,CDRTOTAL          CHECK FOR TOTAL
         BCR   7,R14                    BRANCH IF NOT TOTAL-RETURN
         CLI   CKPRW,CKPWRITE           CHECK FOR WRITE DATA COMMND
         BCR   7,R14                    BRANCH IF NOT WRITE DATA
         USING IEDQIOB,R2               BASE FOR IOB
         SPACE 3
*WRITE CONTROL RECORD-USE RETRY
         SPACE
         XC    IOBFLAG1(4),IOBFLAG1     RE/INITIALIZE FLAGS
         OI    IOBFLAG1,CKPCMDCH        TURN ON COMMAND CHAINING
         XC    IOBFLAG3(8),IOBFLAG3     CLEAR FLAG AND CSW
         XC    IOBERRCT,IOBERRCT        CLEAR ERROR COUNTS
         USING IEDQDEB,R3
         MVI   IOBSEEK,0                EXTENT
         MVC   IOBSEEK+1(6),DEBBINUM    BIN,CYLINDER,TRACK
         MVI   IOBSEEK+7,1              RECORD NUMBER
         USING CKPIOB,R2                BASE FOR CHECKPOINT WK AREA
         LA    R13,CKPCNTLR             ADDR OF CONTROL RECORD
         MVI   CKPRW,0
         C     R13,CKPRW                CHECK FOR CONTROLRCD JUST      X
                                        WRITTEN
         BCR   8,R14                    BRANCH IF CONTROL RCD
         ST    R13,CKPRW                PUT ADDR IN CCW
         MVI   CKPRW,CKPWRITE           WRITE COMMAND CODE
         MVI   CKPRW+SIX,ZERO          CLEAR COUNT              OY03902
         MVC   CKPRW+SEV(ONE),CKPCRLEN CONTROL REC COUNT        OY03902
         MVI   CKPSECTR,0               SET SECTOR ID TO 1ST SECTOR0420
         B     8(R14)                   RETURN TO RETRY ENTRY
         SPACE 2
         DC    CL8'IGG019RA'
         DC    CL6'042071'
         EJECT
*DSECT FOR DEB
         SPACE
IEDQDEB  DSECT
         ORG   IEDQDEB+16
DEBNMEXT DS    X                        NO OF EXTENTS IN DATA SET
         ORG   IEDQDEB+25
DEBDCBAD DS    AL3                      ADDR OF DCB
         SPACE
*FOR EACH EXTENT THERE IS A 16-BYTE SEGMENT AS FOLLOWS.
         SPACE
         ORG   IEDQDEB+32
DEBDVMOD DS    XL1                      DEVICE MODIFIER
DEBUCBAD DS    XL3                      ADDRESS OF UCB
DEBBINUM DS    XL2                      BIN NO
DEBSTRCC DS    XL2                      CYLINDER ADDR FOR START
DEBSTRHH DS    XL2                      TRACK ADDR FOR START OF EXT
DEBENDCC DS    XL2                      CYLINDER ADDR FOR EXTNT END
DEBENDHH DS    XL2                      TRACK ADDR FOR EXTENT END
DEBNMTRK DS    XL2                      NO TRACKS IN THIS EXTENT
DEBEND  EQU   *
DEBSEGLN EQU   DEBEND-DEBDVMOD          LNGTH OF SEG FOE EACH EXTNT
         TIOBD
         TCKPD 3330
         END
