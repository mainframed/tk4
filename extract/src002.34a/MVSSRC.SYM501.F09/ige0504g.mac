504G TITLE 'IGE0504G ERROR POST AND ERP CCW RETURN'
IGE0504G CSECT
*A-000000-999999                                               @Y02X6E0
*A274000,352600,714000                                           S22025
*C195500,238000-240000,274000,275200,348900,502000,662000-668000 S22025
*C672000                                                         S22025
*D320000-332000                                                  S22025
*A194000,196000                                                  CLUP21
*A372000,380000                                                  A42420
*A206000,694000,720000                                           S21101
*C348000-366200                                                  S21101
*C630000                                                        SA58994
*A284000,288000,310000,392000,608000,673000,730000              SA65392
*C213000-216000,252000-254000,274700-274800,275600-275760       SA65392
*C316000,460000,472000,544000,586000-588000                     SA65392
*D194500,292000-300000,430000,664000,670000-672000,676000       SA65392
*D682000,702000,712000                                          SA65392
*A 042600-043200,259000,262070-263890,673300,714100-714900       X01004
*C 275680,302000-306000,630000                                   X01004
*D 275760                                                        X01004
*D646000                                                       SA66363
*C017200,198000,438000                                         @Z30AAEE
*D698000                                                       @Z30AAEE
*A392500,602000                                                @SA70291
*C392600-392950,393050-393400,393600-393900                    @SA70291
*D284300                                                       @SA70291
*A372000,734000                                                @XA03955
*A392000                                                       @XA05319
*C402000                                                       @XA05319
*C392520,392800,393250                                         @SA73187
*A644000                                                       @OY11534
*D602300-603500                                                @YA10246
*C393600                                                       @SA75445
*A630000                                                       @ZA06642
*C582000,C586000,A673400                                       @OX13491
*C582000,C608000,C642000-646000,A648000                        @OX14080
*A628000                                                       @OZ16799
*C392450,A392500,C642000-643000,C648000-649600,A728000         @OX16393
*A392500,C643200,C644000-645000,C648000,A729200                @OZ24288
*A602000                                                       @OZ30194
*C602080,602720,603200-603520                                  @OZ32827
*                                                                     *
***********************************************************************
*                                                                     *
*TITLE -- 'IGE0504G',START/STOP ERP CCW RETURN AND ERROR POST.        *
*                                                                     *
*  MODULE NAME = IGE0504G                                             *
*                                                                     *
*  DESCRIPTIVE NAME = START-STOP ERP CCW RETURN AND POST              *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS: CHANGE LEVEL 8                                      @Z30AAEE
*                                                                     *
*FUNCTION -- TO ATTEMPT RETRY AFTER INTERMEDIATE ERP ACTION AND TO    *
*   INTERFACE WITH THE OS MESSAGE WRITER AND OBR/SDR.                 *
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
*EXTERNAL ROUTINES - 'IEDQTNT' - TO OBTAIN TERMINAL ENTRY ADDRESS     *
*                  - 'IGG019QE' - TO CONVERT VIRTUAL CCW ADDRESSES TO *
*   REAL BEFORE RETRY.                                                *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'ERP IN CONTROL (IOSERR)' AND 'EXCEPTIONAL   Y02027
*                CONDITION(IOSEX)'. RETURN TO IOS TO RETRY THE   Y02027
*                   ERROR.                                       Y02027
*        SVC   3                                                      *
*                                                                     *
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*        SVC   3                                                      *
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO I/O SUPERVISOR BLOCK                Y02027
*        SVC   15   'EXCEPTIONAL CONDITION(IOSEX)'. RETURN TO    Y02027
*                   LINE END APPENDAGE. ERROR PERMANENT.         Y02027
*        SVC   15  LCBFLAG1 X'C6'                                     *
*        SVC   3                                                      *
*                                                                     *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*   TO SCHEDULE ERROR POST                                            *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TTRMD                                                             *
*   TCCWD                                                             *
*   TLCBD                                                             *
*   DCBD                                                              *
*   TSCBD                                                             *
*   IECDIOSB                                                     Y02027
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
*   IF OPERATOR CONTROL IS IN USE, THE OS MESSAGE WRITER IS BYPASSED. *
*                                                                     *
*   CERTAIN PERMANENT ERRORS ARE NOT RECORDED TO PREVENT OVERFLOW OF  *
*   SYS1.LOGREC.                                                      *
*                                                                     *
*   PARAMETERS PASSED TO OBR/SDR RECORDING MODULE:                    *
*   LCBERCCW+16 - X'00' 1 BYTE INDICATES PERMANENT ERROR              *
*   LCBERCCW+20 - 1 BYTE SIZE OF TERMINAL NAME                        *
*   LCBERCCW+21 - 3 BYTES - ADDRESS OF TERMINAL NAME                  *
*   LCBERCCW+17 - 3 BYTES - ADDRESS OF TERMINAL COUNTERS              *
*                                                                     *
***********************************************************************
         EJECT
         USING IEDQLCB,RLCB
         USING IEDQCCW,RCCW
         USING IEDQAVTD,RAVT
         USING IEDQTRM,RTERM
         USING IHADCB,RDCB
         USING IOSB,RIOSB                                        Y02027
         USING EWA,RERPWA                                        Y02027
         USING *,R15
*              REGISTERS
R0       EQU   0
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RLCB     EQU   2                        LCB CBASE
RCCW     EQU   5                        CCW BASE
RUCB     EQU   3                        UCB REGISTER
RDCB     EQU   4                        DCB BASE
RWKA     EQU   6                        WORK REGISTER
RWKB     EQU   7                        WORK REGISTER
RTST     EQU   7                        ADDR OF REQUEST QUEUE    Y02027
*                                       ELEMENT                  Y02027
RTERM    EQU   8                        TERMINAL BASE
RWKC     EQU   9                        WORK REGISTER
RWKD     EQU   10                       WORK REGISTER
RAVT     EQU   11                       AAVT BASE
RERPWA   EQU   12                       ADDR OF ERP WORK AREA    Y02027
RLINK    EQU   13                       LINKAGE FOR SUBSEQUENT LOAD
RSAVE    EQU   13                       SAVE REGISTER FOR IOSB   Y02027
RXCTL    EQU   14                       XCTL REGISTER
R15      EQU   15                       ADDRESSABILITY REGISTER
         EJECT
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         L     RERPWA,IOSERP            PICKUP ADDR OF ERP WORK  Y02027
*                                       AREA                     Y02027
         L     RUCB,IOSUCB              ADDRESS OF UCB         @Z30AAEE
         L     RLCB,IOBOFFST(R0,RTST)   ADDRESS OF IOB
         SH    RLCB,LCBSIZE             ADJUST BASE
         L     RDCB,LCBDCBPT            DCB BASE
         L     RWKB,LCBSCBA-1           CURRENT SCB
         USING IEDQSCB,RWKB                                      S21101
         L     RXCTL,CVTPTR             CVT ADDRESS             SA65392
         L     RAVT,AVTCVTPT(RXCTL)     LOAD LIST ADDRESS       SA65392
         L     RAVT,AVTEZERO(,RAVT)     AVT BASE
         SR    RWKA,RWKA                CLEAR INDEX RETISTER
         IC    RWKA,LCBINCAM+1          GET RETURN BYTE
         MVI   LCBINCAM+1,0             RESET CODE               Y02027
         B     TABLE-4(RWKA)            BRANCH TO APPROPRITE ETNRY
         SPACE 2
TABLE    EQU   *
         B     SKIPRET                  SKIP RETURN
         B     BRKRET                   BREAK RETURN
         B     UNUSRET                  UNUSUAL RETURN
         B     SNORET                   SHOULD-NOT-OCCUR RETURN  S22025
         B     PERMRET                  PERMANENT ERROR RETURN   S22025
         SPACE 2
SKIPRET  EQU   *
         SPACE
         MVC   LCBCSW,LCBCSWSV          ORIGINAL CSW
         MVC   LCBSENS0,LCBSNSV         ORIGINAL SENSE
         TM    LCBCSWSV+3,UNITCHK       UNIT CHECK ORIGINAL ERROR
         BZ    TRYRETRY                 NO, IT WAS UNIT EXCEPTION
         CLI   LCBINCAM,RETRY           TEST RTRY COUNT
         BNL   EXHAUST                  EXHAUSTED
SETERR   EQU   *
         NI    IOSFLA,AVTEFF-IOSERR     POST PERMANENT ERROR TO  Y02027
*                                       ABNORMAL END APPENDAGE   Y02027
EXIT     EQU   *
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE IS ADDED FOR AOS.                         *
*                                                                     *
         BAL   RXCTL,HIOSUB             CHECK FOR HALTIO         Y02027
         SPACE 1                                                 Y02027
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         LA    RLCB,LCBFLAG1            IOB ADDRESS TO SIO       Y02027
         SR    RTERM,RTERM              CLEAR FOR ICM.           X01004
         ICM   RTERM,7,DEBPTR(RTST)     GET DEB ADDRESS FROM RQE.X01004
         LA    R15,DEBNMSUB-IEDQDEB     GET APDG. TABLE SIZE     Y02027
         SLR   RTERM,R15                POINT TO APDG. TABLE     Y02027
         L     RTERM,SIOPTR(ZERO,RTERM) GET ADDR OF SIO APDG.    X01004
*                                       FROM APDG. VECTOR TABLE.
         SR    R15,R15                  CLEAR R15 TO TELL SIO    X01004
*                                       APDG. IT'S A SUBROUTINE.
         BAL   RXCTL,SIOENTRY(ZERO,RTERM)  LINK TO SIO APDG AT   X01004
*                                       OFFSET +16 TO CONVERT CCW'S
*                                       FROM VIRTUAL TO REAL.    X01004
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
         SVC   EREXCP
         SVC   RETURN
         SPACE 2
BRKRET   EQU   *
         MVC   LCBSENS1,LCBSENS0        SENSE BYTE FROM BREAK
         MVC   LCBCSW,LCBCSWSV          ORIGINAL CSW
         MVC   LCBSENS0,LCBSNSV         ORIGINAL SENSE
         TM    LCBTSOB,LCB2741N         2741 TERMINAL ?          S22025
         BNO   TRYRETRY                 BRANCH IF NO             S22025
         TM    LCBCSWSV+3,UNITCHK       WAS ORIGINAL ERROR A     S22025
*                                       UNIT CHK OR UNIT EXCPTN  S22025
         BZ    TRYRETRY                 BRANCH IF UNIT EXCPTN    S22025
         CLI   LCBRESTR,TPRDRESP        TEXT CCW OR RESP TO POLL S22025
         BNE   EXHAUST                  BRANCH IF TEXT           S22025
         CLI   LCBSENS1,COMREJ          TEST RETURN FROM BREAK   S22025
*                                       FOR COMMAND REJECT       S22025
         BNE   RETLEND                  BRANCH IF NOT            S22025
         TM    AVTBIT1,AVTDLAYN+AVTCLOSN  CLOSEDOWN REQUESTED ?  S22025
         BNZ   RETLEND                  BRANCH IF YES - DO NOT   S22025
*                                       RETRY READ RESPONSE      S22025
         TM    LCBSTAT1,LCBOCNI         STOPLINE PENDING ?       S22025
         BNO   RERDRSP                  BRANCH IF NO - GO RETRY  S22025
*                                       READ RESPONSE            S22025
         SPACE 1
RETLEND  EQU   *                                                 S22025
         NI    IOSFLA,AVTEFF-(IOSERR+IOSEX) INDICATE ERROR IS    Y02027
*                                       CLEARED                  Y02027
*                                       END APPENDAGE            S22025
         MVI   LCBCSW+3,NORMAL          SET ENDING STATUS FOR    S22025
*                                       ORIGINAL CCW TO LOOK     S22025
*                                       LIKE NEGATIVE RESPONSE   S22025
*                                       TO POLL                  S22025
         B     EXIT                     GO RETURN TO LINE END    S22025
         SPACE 2
TRYRETRY EQU   *
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED
         BE    EXHAUST                  YES
         SPACE
         IC    RWKA,LCBINCAM            RETRY COUNT
         LA    RWKA,1(,RWKA)            BUMP
         STC   RWKA,LCBINCAM            SAVE BACK
RERDRSP  EQU   *                                                 S22025
         MVI   IOSSNS,AVTEZERO          CLEAR SENSE FOR RETRY    Y02027
         L     RWKA,LCBCSWSV-1          ORIGINAL CCW FROM CSW
         SH    RWKA,H8                  BACK UP TO FAILING
         ST    RWKA,LCBSTART-1          SET FOR RESTART
         B     EXIT                     BRANCH TO SET UP FOR RETRY.
         SPACE
         SPACE
UNUSRET  EQU   *
SNORET   EQU   *
         OI    SCBERR4-IEDQSCB(RWKB),SCBUNDFN  UNDEFINED ERROR
         OI    LCBCHAIN,LCBNORTY        NO RETRY POSSIBLE
         SPACE
EXHAUST  EQU   *
PERMRET  EQU   *
         L     RCCW,LCBCSW-1            FAILING CW
         LA    RCCW,AVTEZERO(,RCCW)     CLEAR FOR SUBTRCT
         SH    RCCW,H8                  BACK UP TO FAILING CCW
*        THE FOLLOWING CODE TEST FOR A PERMANENT ERROR IN        S21101
* ATTEMPTING TO SELECT A BUFFERED TERMINAL FOR A RECEIVE         S21101
* OPERATION.  IF WE ARE ATTEMTPING TO RECEIVE ANOTHER PART OF    S21101
* THE MESSAGE THE HEADER BUFFER IS CONVERTED TO TEXT AND ANEOT   S21101
* IS INSERTED IN THE BUFFER.  LINE END WILL POST IT TO MH AS A   S21101
* PERMANENT TEXT ERROR WITH NO RETRY.  THIS WILL FORMALLY END    S21101
* THE MESSAGE ON THE QUEUE.                                      S21101
         CLI   LCBRSKEY,BUFF            BUFFERED LINE            S21101
         BNE   NOTBFRD                  BRANCH NO                S21101
         TM    LCBSTAT1,LCBRECVN        RECEIVE SIDE             S21101
         BZ    NOTBFRD                  NO                       S21101
         SPACE 1                                                 S21101
         CLI   LCBRESTR,TPRDSKIP        TEXT RELATED             S21101
         BNL   NOTBFRD                  BRANCH TEXT RELATED      S22025
         SPACE 1                                                 S21101
         TM    SCBQTYPE,SCBBFMM         MIDDLE OF MESSAGE        S21101
         BZ    NOTBFRD                  BRANCH NO                S21101
         SPACE 1                                                 S21101
         CLI   LCBRESTR,TPRDLC          READ LCOUT               S21101
         BE    NOTBFRD                  YES, TEXT RELATED        S21101
         SPACE 1                                                 S21101
         CLI   LCBRESTR,TPWRAKNK        WRITE RESPONSE TO TEXT   S21101
         BE    NOTBFRD                  YES, EXIT                S21101
         SPACE 1                                                 S21101
         CLI   LCBRESTR,TPRESET         ABORT SEQUENCE           S21101
         BE    NOTBFRD                  YES                      S21101
         SPACE 1                                                 S21101
         OI    LCBCHAIN,LCBNORTY        SET NO RETRY FLAG        S21101
         L     RWKA,LCBLSPCI-1          GET ADDRESS OF HEADER    S21101
         USING IEDQPRF,RWKA                                      S21101
         TM    PRFSTAT1,PRFNHDRN        TEXT BUFFER              S21101
         BO    NOTBFRD                  BRANCH YES               S21101
         SPACE 1                                                 S21101
         OI    PRFSTAT1,PRFNHDRN        SET TO TEXT              S21101
         OI    PRFTIC,PRFBFMM           TELL AA ABOUT NEW ILDES  S21101
         OI    SCBERR4,SCBTXTTN         SET TEXT ERROR           S21101
         SR    RWKC,RWKC                CLEAR FOR INDEX          S21101
         IC    RWKC,LCBISZE             GET IDLES COUNT          S21101
         LA    RWKC,PRFSHDR-PRFSTXT(RWKC) BUMP IDLES COUNT       S21101
         STC   RWKC,LCBISZE                                      S21101
         LH    RWKD,PRFSIZE             GET PRE-SET SIZE         S21101
         LA    RWKD,1(,RWKD)            BUMP FOR EOT             S21101
         STH   RWKD,PRFSIZE             SET IN PREFIX            S21101
         LA    RWKD,PRFSTXT(RWKC)       ADDRESS OF EOT CHARACTER S21101
         DROP  RWKA                                              S21101
         L     RWKA,DCBSCTAD-1          GET SCT ADDRESS          S21101
         IC    RWKC,AVTEZERO(RWKA)      GET EOT INDEX            S21101
         LA    RWKA,1(RWKC,RWKA)        ADDRESS OF EOT CHARACTER S21101
         MVC   0(1,RWKD),0(RWKA)        MOVE EOT IN BUFFER       S21101
         EJECT
NOTBFRD  EQU   *                                                 S21101
         CLI   LCBRESTR,TPTEXT          TEXT ERROR               S22025
         BNE   NOTEXT                   NO                       S22025
*                                                                S22025
         OI    SCBERR4,SCBTXTTN         SET TEXT ERROR           S22025
NOTEXT   EQU   *                                                 S22025
         TM    SCBERR4,SCBCTLUN+SCBCHANN CONTROL UNIT OR CHANERLERR
         BNZ   NOTLNTR                  BRANCH IF EITHEROR BOTH
         SPACE
         OI    SCBERR4,SCBTRMLN         MUST BE TERMINAL/LINE ERROR
         DROP  RWKB
NOTLNTR  EQU   *
         NI    IOSFLA,AVTEFF-IOSERR     POST PERMANENT ERROR TO  Y02027
*                                       ABNORMAL END APPENDAGE   Y02027
         TM    AVTBIT2,AVTTOPOL         ERROR MSG ON TIMEOUT -POLL
         BZ    GOMSG                    BRANCH IF MSG TO BE SENT
         CLI   LCBRSKEY,DSPLOCSC        LOCAL DEVICE ?         @XA03955
         BE    GOMSG                    BRANCH YES             @XA03955
         CLI   CCWOPCDE,CCWPOLL         IS THIS AUTOPOLL         A42420
         BE    CKSENS                   IF SO, CHECK SENSE       A42420
         SPACE
         CLI   LCBRESTR,TPRDRESP        TIME OUT IN POLLING RESONSE
         BNE   GOMSG                    NO, WRITE MSG
         SPACE
CKSENS   EQU   *                                                 A42420
         CLI   LCBSENS0,TIMEOUT         TIME OUT
         BE    EXIT                     YES, NO MSG OR OBR
         CLI   LCBSENS0,IR              INTERVENTION REQUIRED
         BE    EXIT                     YES, NO MSG OR OBR
         SPACE
GOMSG    EQU   *
         MVI   LCBINCAM,6               SET RETRY COUNT TO MAX
         L     RWKA,DCBDEBAD            GET DEB ADDRESS         SA65392
         USING DEBTCBAD,RWKA            DEB ADDRESSIBILITY      SA65392
         SR    RWKB,RWKB                CLEAR WORK REGISTER     SA65392
         LR    RWKC,RWKB                CLEAR WORK REGISTER     SA65392
         IC    RWKB,DEBNMEXT            GET NUMBER OF EXTENTS   SA65392
         SLL   RWKB,TWO                 SIZE OF UCB ADDRESSES   SA65392
         L     RWKB,DEBUCBAD(RWKB)      THRESHOLD AREA          SA65392
         IC    RWKC,LCBUCBX             RLN MINUS ONE           SA65392
         SLL   RWKC,THREE               THRESHOLD INDEX        @OX16393
         AR    RWKB,RWKC                THRESHOLD COUNTERS      SA65392
         SRL   RWKC,1                   ADD HALF AGAIN FOR     @OZ24288
         AR    RWKB,RWKC                ERRMSG DATA SAVE AREA  @OZ24288
         LR    RLINK,RWKB               SAVE ADDRESS OF THRESH @OX16393
         MVC   LCBRESTR+3(1),AVTHRESH+1 SAVE CORRECT CNT       @SA73187
         CLI   LCBSENS0,DCK             DATA CHECK              SA65392
         BE    SETUP                    BR YES, DO THRESHHOLD  @SA70291
         LA    RWKB,1(RWKB)             BUMP TO IR COUNTER     @SA70291
         MVC   LCBRESTR+3(1),AVTHRESH+2 SAVE CORRECT CNT       @SA73187
         CLI   LCBSENS0,IR              INTERVENTION REQUIRED   SA65392
         BE    SETUP                    BR YES, DO THRESHHOLD  @SA70291
         LA    RWKB,1(RWKB)             BUMP TO TO COUNTER     @SA70291
         MVC   LCBRESTR+3(1),AVTHRESH+3 SAVE CORRECT CNT       @SA73187
         CLI   LCBSENS0,TIMEOUT         TIME OUT                SA65392
         BNE   GOTHRESH                 BRANCH NO-WRITE MSG     SA65392
         CLI   LCBRESTR,TPRDRPEB        TEXT TIMEOUT            SA65392
         BE    GOTHRESH                 BR YES-WRITE MSG       @SA75445
SETUP    EQU   *                                               @SA70291
         IC    RWKC,ONE(RWKB)           GET OLD VALUE          @SA70291
         LA    RWKC,ONE(RWKC)           INCREMENT              @SA70291
         STC   RWKC,ONE(RWKB)           AND SAVE               @SA70291
         CLC   ONE(ONE,RWKB),LCBRESTR+3 THRESHHOLD REACHED     @SA70291
         BL    EXIT                     BR NO, EXIT            @SA70291
         MVI   ONE(RWKB),ZERO           ELSE CLEAR COUNTER     @SA70291
GOTHRESH EQU   *                                                SA65392
         MVI   LCBSENS1,0               CLEAR 2ND SENSE BYTE
         MVI   EWTCTRM,BLANK                                     Y02027
         MVC   EWTCTRM+ONE(SEVEN),EWTCTRM BLANK OUT TERMINAL     Y02027
*                                       NAME FIELD IN EWA        Y02027
         SR    RWKA,RWKA                CLEAR WORK REGISTER
         STH   RWKA,LCBRESTR+2          CLEAR DEVICE CHARACTERS
         LH    RWKC,LCBTTCIN            TNT OFFSET
         LTR   RWKC,RWKC                SOURCE SPECIFIED
         BNZ   SOURCE                   YES
         SPACE
         TM    LCBSTAT2,LCBDIAL         DIAL LINE
         BZ    USEUCB                   BRANCH NO
         SPACE
         LH    RWKC,LCBLNENT            LINE ENTRY USED
         LTR   RWKC,RWKC                LINE ENTRY?
         BNZ   SOURCE                   BRANCH YES
         SPACE
USEUCB   EQU   *
         L     RXCTL,CVTPTR             RELOAD CVT BASE          Y02027
         L     RTERM,STATTAB(,RXCTL)    STAT TABLE OFFSET FROM CVT
         L     RWKB,UCBEXTPT-UCBOB(,RUCB) UCB EXTENSION          Y02027
         IC    RWKC,UCBSTI-UCBCMEXT(,RWKB) STATAB INDEX          Y02027
         LR    RWKB,RTERM               COPY TABLE ADDRESS
LOOP1    EQU   *
         CLM   RUCB,THREE,ZERO(RWKB)    IS DEV SUPPORTED HERE  @Z30AAEE
         BC    4,LOOP2                  BRANCH YES
         SPACE
         LA    RWKB,2(,RWKB)            NEXT ENTRY
         LA    RWKC,256(,RWKC)          NEXT STAT TABLE IN
         B     LOOP1                    CHECK THIS ENTRY
         SPACE
LOOP2    EQU   *
         MH    RWKC,H10                 MULTIPLY BY 10
         AR    RTERM,RWKC               SAT TABLE ADDRESS SET
         LA    RWKC,UCBNAME-UCBOB(,RUCB) UCB NAME ADDR           Y02027
         STM   RTERM,RWKC,LCBERCCW+16   ADDR OF SDR COUNTERS    SA65392
*                                         AND ADDR OF TERMNAME  SA65392
         LA    RWKA,TWO                 SET SIZE FOR EXECUTE    Y02027
*                                       INSTR  TO MOVE UCB NAME  Y02027
         EX    RWKA,MOVENAME            MOVE UCB NAME TO ERP     Y02027
*                                       WORK AREA                Y02027
         ST    RTERM,LCBERCCW+16        ADDRESS OF SDR COUNTERS
         B     NOADDR                   COMMMON CODE AND EXIT
         SPACE
SOURCE   EQU   *
         N     RWKC,AVTCLRHI            CLEAL NEG PROPAGATION   SA65392
         L     RWKB,AVTRNMPT            ROUTINE BASE
         USING IEDQTNTD,RWKB
         BAL   RWKA,TNTDCODE            LINK TO ROUTINE
         SR    R0,R0                    CLEAR WORK REGISTER
         IC    R0,X'28'(,RWKB)          GET SIZE OF NAME
         LA    RWKC,79(RWKB,RWKC)        ADDRESS OF TERM ADDRESS
         SR    RWKC,R0                  BACK UP TO NAME ADDRESS
*    THE FOLLOWING INSTRUCTION SETS LCBERCCW=0 - INDICATES OBR
         LA    RWKA,TRMSIO              ADDRESS OF COUNTERS
         ST    RWKA,LCBERCCW+16         ADDRESS OF COUNTERS FOR OBR
         CLC   1(1,RWKA),2(RWKA)        CHECK COUNTS
         BH    NOADD                    OKAY
         MVC   1(1,RWKA),2(RWKA)        SET COUNTS
NOADD    EQU   *
         ST    RWKC,LCBERCCW+20         ADDRESS OF EBCDIC NAME
         LR    RWKA,R0                  MOVE NAME COUNT FOR EXEC Y02027
*                                       INSTRUCTION              Y02027
         BCTR  RWKA,ZERO                REDUCE NAME CONT BY ONE  Y02027
*                                       FOR EXECUTE INSTRUCTION  Y02027
         EX    RWKA,MOVENAME            MOVE THE TERMINAL NAME   Y02027
*                                       TO THE ERP WORK AREA     Y02027
         SR    RWKB,RWKB                CLEAR WORK REGISTER
         TM    TRMSTATE,TRMOPTFN        OPTION FIELDS PRESENT
         BZ    NOOPT                    BRANCH IF NO             S22025
         SPACE
         IC    RWKB,TRMOPNO             NO OF OPTION ENTRIES
         LA    RWKB,3(,RWKB)            ADJUST FOR REQUIRED
*                                         OPTION BYTES
NOOPT    EQU   *
         LH    RWKC,TRMDEVFL            DEVICE FLAGS
         LTR   RWKC,RWKC                BUFSIZE SPECIFIED
         BNM   NOBUFSZ                  NO
         SPACE
         LA    RWKB,3(,RWKB)            ADJUST FOR BUFSIZE
NOBUFSZ  EQU   *
         LA    RWKB,TRMCHCIN+1(RWKB)    DEVICE INGORMATION
         SLL   RWKC,17                  SHIFT DEVICE BITS
         LTR   RWKC,RWKC                DIAL DIGITS
         BNM   NODIGITS                 NO
         SPACE
         SR    RWKA,RWKA                CLEAR WORK REGISTER
         CLI   0(RWKB),4                AT LEAST 4 DIGITS
         BNH   PACKIT                   BRANH IF 4 OR LESS
         IC    RWKA,0(,RWKB)            GET COUNT OF DIGITS
         SH    RWKA,AVTHA4              DECREMENT BY 4          SA65392
         LA    RWKB,0(RWKA,RWKB)        ADJUST FOR PACK
PACKIT   EQU   *
         PACK  LCBERCCW+8(4),1(7,RWKB)  PACK DIAL DIGITS
         MVC   LCBRESTR+2(2),LCBERCCW+8 MOVE FOR WTO MODULE
         B     POSTEXIT                 LEAVE
         SPACE
NODIGITS EQU   *
         TM    LCBSTAT1,LCBSENDN        SEND STATE
         BO    GETCHAR                  BRANCH YES TO ADDR CHARS
         TM    LCBSTAT2,LCBMSGNN        MESSAGE GEN
         BZ    NOADDR                   BRANCH NO TO GET POLLING
GETCHAR  EQU   *
         SLL   RWKC,1                   ADDRESSING  CHARS
         LTR   RWKC,RWKC                NO ADDRESSING CHARS
         BNM   NOADDR                   NO
         SPACE
         MVC   LCBRESTR+2(2),1(RWKB)    ASSUME AT LEAST 2 CHARS.
         CLI   AVTEZERO(RWKB),1         ONLY ONE CHAR
         BE    CLEAR2                   BRANCH YES             @OX14080
         SPACE
         CLI   AVTEZERO(RWKB),FIVE      FIVE CHARACTERS        @OX13491
         BNE   POSTEXIT                 NO-LEAVE               @OX13491
         MVC   LCBRESTR+THREE(ONE),THREE(RWKB) UPDATE 2ND BYTE @OX13491
         B     POSTEXIT                 LEAVE                  @OX13491
         SPACE 2
NOADDR   EQU   *
         SR RWKA,RWKA                   CLEAR WORK REGISTER
         IC    RWKA,LCBUCBX             RLN MINUS 1
         SLL   RWKA,2                   MULTIPLY BY 4
         L     RWKB,DCBINVLI(RWKA)      ILIST FOR THIS LINE
         SR    RWKA,RWKA                CLEAR WORK             @OZ30194
         SR    R0,R0                    CLEAR INDEX REGISTER   @OZ32827
         LA    RWKC,8(,RWKB)            GET CONTROL WORD       @OZ30194
         IC    RWKA,2(,RWKB)            GET ENTRY LENGTH       @OZ30194
         BCTR  RWKA,0                   REDUCE BY 1            @OZ30194
         B     STARLOOK                 CHECK LIST             @OZ30194
LOOPLOOK EQU   *                                               @OZ30194
         AR    RWKC,RWKA                MOVE TO NEXT           @OZ30194
         LA    RWKC,1(,RWKC)            ENTRY                  @OZ30194
         SR    R0,R0                    CLEAR INDEX REGISTER   @OZ32827
STARLOOK EQU   *                                               @OZ30194
         CLI   0(RWKC),XFE              END OF LIST            @OZ30194
         BE    YESEND                   BRANCH YES             @OZ30194
         CLI   1(RWKC),XFE              END OF LIST            @OZ30194
         BE    YESEND                   BRANCH YES AGAIN       @OZ30194
         IC    R0,0(RWKA,RWKC)          GET ENTRY INDEX        @OZ32827
         AR    R0,R0                    DOUBLE FOR SUBTRACT    @OZ32827
         LNR   R0,R0                    MAKE NEGATIVE          @OZ32827
         AR    R0,RWKB                  SUBTRACT FOR TNTINDEX  @OZ32827
         CH    R0,LCBTTCIN              CORRECT ENTRY          @OZ32827
         BNE   LOOPLOOK                 BRANCH NO              @OZ30194
YESEND   EQU   *                                               @OZ30194
         MVC   LCBRESTR+2(2),AVTEZERO(RWKC) ASSUME 2 CHARACTERS
         CLI   2(RWKB),2                ONLY 1 CHARACTER
         BNH   CLEAR2                   YES BRANCH             @OX14080
         CLI   TWO(RWKB),SIX            FIVE CHARACTERS        @OX13491
         BNE   POSTEXIT                 NO-LEAVE               @OX13491
         MVC   LCBRESTR+THREE(ONE),THREE(RWKC) UPDATE 2ND BYTE @OX13491
         B     POSTEXIT                 LEAVE                  @OX13491
CLEAR2   EQU   *                                                SA65392
         SPACE
         MVI   LCBRESTR+3,AVTEZERO      CLEAR 2ND BYTE
POSTEXIT EQU   *
         LH    RWKB,AVTOPCON            OPERATOR CONTROL
         LTR   RWKB,RWKB                SPECIFIED
         BZ    SYSCON                   NO, USE SYSTEM CONSOLE
         SPACE
         N     RWKB,AVTCLRHI            CLEAR HIGH ORDER HALF WORD
         A     RWKB,AVTRNMPT            GET ADDR OF TERM NAME ENTRY
         L     RWKA,LCBERCCW+20         TERMNAME ADDR FOR LCBTTCIN
         LA    RWKA,0(,RWKA)            CLEAR HIGH ORDER BYTE
         CR    RWKA,RWKB                CHECK FOR PRIM CONTROL TERM
         BNE   OPCTL                    BRANCH IF ERROR NOT ON PRIM
SYSCON   EQU   *
         USING UCBOB,RUCB                                      @OZ16799
         MVC   EWACHA,UCBCHA            SAVE DEV ADDR FOR MSG  @OZ16799
         DROP  RUCB                                            @OZ16799
         OI    IOSFLB,IOSMSG+IOSLOG     'I/O' MSG + OBR          Y02027
         OI    EWAFLG1,EWADDMSG         ERP DEP DATA IN ERRMSG @ZA06642
         MVC   EWTCTID(TWO),LCBRESTR+TWO MOVE TERMINAL ID CHARS  Y02027
*                                       TO ERP WK AREA FOR IOS   Y02027
*                                       WTO ERROR ROUTINE        Y02027
         LA    RLINK,WTOMSG             LINK TO MESSAGE WRITE
         B     NEXTLOAD                 COMMON CODE
OPCTL    EQU   *
         OI    LCBCHAIN,LCBERMSG        WE OWE MSG TO OPERATOR CTL
         USING LCBEXTX,RLINK            ADDRESSABILITY         @OX16393
         MVC   IEALTPCD(4),LCBRESTR     SAVE TP CDS AND CHARS  @OX16393
         MVC   IEASENS0,LCBSENS0        SAVE SENSE INFORMATION @OZ24288
         MVC   IEACSW,LCBCSW+3          SAVE CSW               @OZ24288
*                                       LAID BY MSGGEN OR RECAL@OX14080
         MVC   IEACMD,CCWOPCDE          SAVE COMMAND CODE      @OZ24288
         LA    RLINK,SDRLOAD            OBR ID
NEXTLOAD EQU   *
         MVC   EWTCCSW(SEVEN),LCBCSW    MOVE CSW INTO THE ERP    Y02027
*                                       WORK AREA                Y02027
         MVC   EWTCCCW(EIGHT),ZERO(RCCW) MOVE CCW INTO THE ERP   Y02027
*                                       WORK AREA                Y02027
         IC    RWKA,LCBERMSK            ASSUME LINE ERROR MASK   Y02027
         TM    LCBERMSK,LINEMASK        IS LINE MASK PRESENT     Y02027
         BNZ   MOVEMASK                 YES. MOVE MASK.          Y02027
         IC    RWKA,TRMSENSE            PICKUP TERMINAL ERR MASK Y02027
MOVEMASK EQU   *                                                 Y02027
         STC   RWKA,EWTCMSK             MOVE MASK INTO ERP WORK  Y02027
*                                       AREA                     Y02027
         L     RTERM,LCBERCCW+SIXTEEN   PICKUP ADDR OF SDR CTRS  Y02027
         USING TRMSIO,RTERM                                      Y02027
         MVC   EWTCSIO(THREE),TRMSIO    MOVE START I/O AND TEMP  Y02027
*                                       ERROR COUNTERS TO THE    Y02027
*                                       ERP WORK AREA            Y02027
         XC    TRMSIO(THREE),TRMSIO     CLEAR ERROR COUNTERS IN  Y02027
*                                       THE TERMINAL TABLE       Y02027
         MVC   EWTCTPF(ONE),LCBRESTR    MOVE 1ST TP OP CODE TO   Y02027
*                                       OBR/SDR RECORD           Y02027
         MVC   EWTCTPL(ONE),LCBRESTR+ONE MOVE LAST TP OP CODE TO Y02027
*                                       OBR/SDR RECORD           Y02027
         MVC   EWTCSNF(ONE),LCBSNSV     MOVE 1ST BYTE OF SENSE   Y02027
*                                       DATA TO OBR/SDR RECORD   Y02027
         MVC   EWTCSNL(ONE),LCBSENS0    MOVE LAST BYTE OF SENSE  Y02027
*                                       DATA TO OBR/SDR DATA     Y02027
         LA    RWKA,EWTCSIO             INDICATE ADDR OF START   Y02027
         STCM  RWKA,SEVEN,EWADDISP      DEVICE DEPENDENT INFO    Y02027
*                                       FOR OBR/SDR              Y02027
         MVI   EWADCNT,OBRDATA          INDICATE SIZE OF DEVICE  Y02027
*                                       DEPENDENT DATA FOR OBR   Y02027
         L     RXCTL,CVTPTR             CVT ADDRESS              Y02027
         L     RXCTL,XCTLADD(,RXCTL)    XCTL ADDRESS
HIOSUB   EQU   *                                                 Y02027
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        LINE CONTROL BLOCK TO THE I/O SUPERVISOR BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY OPERATIONS FROM ERP          Y02027
**************************************************************** Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                       INTO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM LCB TO IOSB    Y02027
         CLI   LCBECBCC,HIOCC           WAS IOHALT DONE          Y02027
         BNER  RXCTL                    BRANCH NO - RETURN       Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CNTRL    Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
         BR    RXCTL                    LINK TO XCTL
         EJECT
*              EXECUTE INSTRUCTIONS
         SPACE
MOVENAME MVC   EWTCTRM(ZERO),ZERO(RWKC) MOVE TERMINAL NAME INTO  Y02027
*                                       THE ERP WORK AREA        Y02027
         SPACE
*              CONSTANTS
H8       DC    H'8'                     USED TO BACK UP TO       S22025
*                                       FAILING CCW              S22025
*                                       COUNTER                  S22025
H10      DC    H'10'                    USED TO ADJUST STATISTIC S22025
*                                       TABLE POINTER            S22025
LCBSIZE  DC    AL2(LCBFLAG1-IEDQLCB)    ADJUSTMENT TO BACK UP TO S22025
*                                       BEGINNING OF LCB         S22025
*                                       HALF OF REGISTER         S22025
SEVEN    EQU   7                                                 Y02027
EIGHT    EQU   8                                                 Y02027
TWELVE   EQU   12                                                Y02027
SIXTEEN  EQU   16                                                Y02027
BLANK    EQU   X'40'                                             Y02027
LINEMASK EQU   X'FF'                    TEST FOR LINE ERROR MASK Y02027
OBRDATA  EQU   18                       SIZE OF DEVICE DEPENDENT Y02027
*                                       DATA FOR OBR/SDR         Y02027
HIOCC    EQU   X'48'                    IOHALT COMPLETION CODE   Y02027
ZERO     EQU   0                        CONSTANT DISPLACEMENT   SA65392
ONE      EQU   1                        CONSTANT DISPLACEMENT   SA65392
TWO      EQU   2                        CONSTANT DISPLACEMENT   SA65392
THREE    EQU   3                        CONSTANT DISPLACEMENT   SA65392
FIVE     EQU   5                        CHARACTER COUNT        @OX13491
SIX      EQU   6                        CHARACTER COUNT        @OX13491
DCK      EQU   X'08'                    MASK FOR SENSE BYTE     SA65392
UCBMASK  EQU   X'17'                    OFFSET IN UCB TO SENSE BYTE
STATTAB  EQU   X'70'                    CVT OFFSET TO STATISTICS
RETRY    EQU   X'02'                    RETRY LIMIT
CVTPTR   EQU   X'10'                    CVT LOCATION POINTER
X10      EQU   X'10'                    MASK-EXTENDED ERROR MESSAGE
XCTLADD  EQU   X'2C'                    OFFSET IN CVT-XCTL ROUTINE
EREXCP   EQU   15                       ERROR EXCP
RETURN   EQU   3                        ETURN SVC FOR TRANSIENT S
TIMEOUT  EQU   X'01'                    MSK FOR SENSE BYTE
BUFF     EQU   X'1A'                    STVTO FOR BUFFERED LCB   S21101
IR       EQU   X'40'                    MSK FOR SENSE BYTE
IOBOFFST EQU   4                        IOB OFFSET IN RQE
WTOMSG   EQU   253                      LINK VALUE FOR WTO MODULE
SDRLOAD  EQU   256                      OBR/SDR MODULE ID
UNITCHK  EQU   X'02'                    MASK FOR UNIT CHECK IN CSW
IOBPTR   EQU   5                        OFFSET OF IOB ADDR. IN RQE.
DEBPTR   EQU   9                        OFFSET OF DEB ADDR. IN RQE.
SIOPTR   EQU   4                        OFFSET OF SIO APDG. ADDRESS
*                                       IN APDG. VECTOR TABLE.
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
*                                       POINT IN SIO APPENDAGE.
NORMAL   EQU   X'0D'                    NORMAL END FOR RD RESP   S22025
DB       EQU   X'DB'                    MASK FOR IOS TO RETURN   S22025
*                                       TO LINE END APPENDAGE    S22025
*                                       WITHOUT SIO              S22025
COMREJ   EQU   X'80'                    MASK TO TEST FOR         S22025
*                                       COMMAND REJECT           S22025
XFE      EQU   X'FE'                    END OF LIST ID         @OZ30194
         EJECT
         TSCBD
         TPRFD                                                   S21101
         EJECT
         TTRMD
         TTNTD
         TCCWD
         TLCBD
         EJECT                                                 @OX16393
LCBEXTX  DSECT                                                 @OX16393
THRWORD  DS    F                        FOUR BYTE THRESH INFO  @OX16393
IEALTPCD DS    CL1                      SAVED LAST TP OP CODE  @OX16393
IEAFTPCD DS    CL1                      SAVED FIRST TP OP CODE @OX16393
IEADVCHS DS    CL2                      SAVED DEVICE CHARACTERS@OX16393
IEASENS0 DS    CL1                      SAVED SENSE INFORMATION@OZ24288
IEACMD   DS    CL1                      SAVED COMMAND CODE     @OZ24288
IEACSW   DS    CL2                      SAVED CSW INFORMATION  @OZ24288
         DCBD  DSORG=TX
         TDEBD                                                  SA65392
         TAVTD
         TTPD
         EJECT
         TDISPD                                                @XA03955
         IECDIOSB                                                Y02027
         IECDERWA EWTC
         EJECT
         DSECT
         IEFUCBOB
         END
