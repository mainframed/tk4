         TITLE 'IGC0810D - ERROR MESSAGE GENERATOR'
IGC0810D CSECT
*A000000-999999                                                  S99240
*A336000,488000,452000,578000,795200                             S22024
*A076000,152000,156000,205000,800000                           @Y17XARW
*C020000-024000,032000,668000                                  @Y17XARW
*D036000-040000                                                @Y17XARW
***********************************************************************
*                                                                     *
*MODULE NAME - IGC0810D (TCAM, OP CONTROL)                     @Y17XARW
*                                                              @Y17XARW
*DESCRIPTIVE NAME - OPERATOR CONTROL ERROR MESSAGE MODULE 4.   @Y17XARW
*                                                              @Y17XARW
*COPYRIGHT - NONE                                              @Y17XARW
*                                                                     *
*STATUS - VERSION 10.0                                         @Y17XARW
*                                                                     *
*FUNCTION -                                                           *
*        THIS MODULE TAKES THE ERROR MESSAGE NUMBER PASSED IN         *
*        REGISTER ZERO, LOCATES IT IN A TABLE, AND THE                *
*        MESSAGE IS MOVED TO THE RESIDENT WORKAREA AT OFFSET 4.  THE  *
*        LENGTH OF THE MESSAGE IS PLACED IN THE WORKAREA AT OFFSET 0 *
*        THE WORKAREA.  CONTROL IS THEN PASSED TO IGC0710D FOR OUTPUT.*
*                                                                     *
*ENTRY POINTS -                                                       *
*        IGC0810D IS THE ONLY ENTRY POINT OF THIS MODULE.             *
*                                                              @Y17XARW
*        PURPOSE - SEE FUNCTION                                @Y17XARW
*                                                              @Y17XARW
*        LINKAGE - IGC0810D XCTL'S TO IGC0710D TO WRITE MSG    @Y17XARW
*                                                                     *
*INPUT -                                                              *
*        POINTER TO THE OPERATOR CONTROL AVT IN REGISTER 1 AND        *
*        AN ERROR MESSAGE NUMBER IN REGISTER 0.                       *
*                                                                     *
*OUTPUT -                                                             *
*        ERROR MESSAGES IN RESIDENT WORKAREA                          *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*        SVC 7 (XCTL)                                                 *
*                                                                     *
*EXITS,NORMAL --                                                      *
*              XCTL TO IGC0710D FOR OUTPUT.                           *
*EXITS,ERROR --                                                @Y17XARW
*              XCTL TO IGC0E10D FOR ERROR OUTPUT.              @Y17XARW
*                                                                     *
*TABLES/WORKAREAS -                                                   *
*        TCAM AVT,OP CONTROL AVT,ERRORTAB,OPCE                        *
*                                                              @Y17XARW
*MODULE TYPE - PROCEDURE                                       @Y17XARW
*                                                              @Y17XARW
*        PROCESSOR - ASSEMBLER                                 @Y17XARW
*                                                                     *
*        MODULE SIZE - THE MAXIMUM SIZE ALLOWED FOR THIS       @Y17XARW
*          MODULE IS 2048 DECIMAL BYTES                        @Y17XARW
*                                                              @Y17XARW
*ATTRIBUTES -                                                         *
*        REFRESHABLE,REUSABLE,DISABLED,TRANSIENT(TYPE 4),SUPERVISOR   *
*        MODE                                                         *
*                                                                     *
*CHARACTER CODE DEPENDENCY --                                         *
*        THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        *
*        REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH           *
*        IS EQUIVALENT TO THE ONE USED AT ASSEMBLY                    *
*        TIME.  THE CODING HAS BEEN ARRANGED SO THAT REDEFINITION     *
*        OF CHARACTER CONSTANTS, BY REASSEMBLY WILL RESULT IN A       *
*        CORRECT MODULE FOR THE NEW DEFINITION.                       *
*                                                                     *
*NOTES --                                                             *
*        COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES         *
*        IS PRIMARILY THROUGH THE OPERATOR CONTROL AVT.               *
*                                                              @Y17XARW
*        RESTRICTIONS - NONE                                   @Y17XARW
*                                                              @Y17XARW
*        REG CONVENTIONS - SEE REGISTER DECLARES               @Y17XARW
*                                                              @Y17XARW
*        PATCH LABEL - ZAPAREA                                 @Y17XARW
*                                                                     *
***********************************************************************
         EJECT
         SPACE
*                          E Q U A T E S
         SPACE
RZERO    EQU   0                        PARAMETER REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
ROPCAVT  EQU   2                        POINTER TO OP CONTROL AVT
ROPCE    EQU   3                        POINTER TO CURRENT OPCE
RAVT     EQU   4                        TCAM AVT ADDRESS
RWORKA   EQU   5                        POINTER TO RESIDENT WORKAREA
RMSGPT   EQU   6                        POINTER TO CURRENT ERROR MSG
RMSGLEN  EQU   7                        LENGTH OF ERROR MESSAGE
ROFF     EQU   8                        OFFSET TO DDNAME, ADDR, RLN
RMSGNO   EQU   9                        NUMBER OF ERROR MESSAGE
RWORK    EQU   10                       WORK REGISTER
RBASE    EQU   12                       BASE FOR MODULE
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RBRNCH   EQU   15                       SUBROUTINE BRANCH ENTRY POINT
         SPACE 2
ADDROFF  EQU   2                        OFFSET IN TABLE TO MESSAGE ADDR
ENDTABLE EQU   255                      END OF ERROR TABLE
ENTLEN   EQU   4                        LENGTH OF ERROR TABLE ENTRY
LENOFF   EQU   0                        OFFSET TO LENGTH OF MESSAGE
MESSAGE  EQU   4                        OFFSET TO MESSAGE PAST OFFSETS
MSGLOC   EQU   4                        OFFSET TO MESSAGE IN WORKAREA
NAMELEN  EQU   2                        NO. OF NAME CHANGE CHARACTERS
NULL     EQU   0                        ZERO VALUE FOR EXECUTES
NUMOFF   EQU   0                        OFFSET IN TABLE TO MESSAGE NO.
VARINAME EQU   3                        OFFSET TO CHANGE LOAD NAME
DDOFF    EQU   0                        OFFSET TO DDNAME IN MSG  S22024
DDSIZE   EQU   8                        MAX LENGTH OF DDNAME     S22024
OFFOFF   EQU   1                        OFFSET TO NAME           S22024
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         SPACE
         USING *,RBRNCH                 TEMPORARY BASE           S22024
         BAL   RBASE,SKIPID             SET BASE REGISTER        S22024
         USING *,RBASE                  MODULE ADDRESSABILITY
         DROP  RBRNCH                                            S22024
BASE     EQU   *
IGC0810D IEDHJN ,                       MODULE ID                S22024
SKIPID   EQU   *                                                 S22024
         LR    ROPCAVT,RPARM            SET UP AVT ADDRESSING
         L     RAVT,OPCAVTPT            TCAM AVT ADDRESSING
         L     ROPCE,OPCCOPCE           GET CURRENT ELEMENT POINTER
         L     RWORKA,OPCWORK           GET WORKAREA ADDRESS
         LR    RMSGNO,RZERO             PICK UP PASSED MESSAGE NUMBER
         MVC   OPCLDNME+VARINAME(NAMELEN),LOADID NEXT LOAD MODULE
         LA    RMSGPT,ERRORTAB          PICK UP ERROR MESSAGE TABLE AD
LOCATMSG EQU   *
         CLI   NUMOFF(RMSGPT),ENDTABLE  CHECK FOR END OF TABLE
         BE    XCTL                     YES, CALL NEXT LOAD OF ERRORS
         SPACE
         CH    RMSGNO,NUMOFF(RMSGPT)    CORRECT ERROR MESSAGE?
         BE    PROCMSG                  YES, PROCESS MESSAGE
         SPACE
         LA    RMSGPT,ENTLEN(RMSGPT)    POINT TO NEXT ENTRY
         B     LOCATMSG                 CONTINUE CHECKING
SETDATA  EQU   *                                                 S22024
         LA    RWORKA,MSGLOC(ROFF,RWORKA)   SET DATA POINTER     S22024
         MVC   DDOFF(DDSIZE,RWORKA),OCBACKUP  PLACE DDNAME OR    S22024
*                                       HARDWARE ADDR IN THE MSG S22024
         B     XCTL07                   XCTL TO THE MSG WRITER   S22024
         SPACE
PROCMSG  EQU   *
         LH    RMSGPT,ADDROFF(RMSGPT)   GET ADDRESS OF ERROR MESSAGE
         AR    RMSGPT,RBASE             ADD BASE TO OFFSET
         SR    RMSGLEN,RMSGLEN          CLEAR REGISTER FOR MESSAGE LENG
         IC    RMSGLEN,LENOFF(RMSGPT)   PICK UP MESSAGE LENGTH
         STH   RMSGLEN,LENOFF(RWORKA)   PLACE MESSAGE LENGTH IN WA
         BCTR  RMSGLEN,RZERO            DECREMENT FOR MOVE
         EX    RMSGLEN,MOVEMSG          MOVE MESSAGE INTO WORKAREA
         SR    ROFF,ROFF                CLEAR TO PICK UP OFFSET  S22024
         IC    ROFF,OFFOFF(RMSGPT)      PICK UP OFFSET           S22024
         LTR   ROFF,ROFF                OFFSET ?                 S22024
         BNE   SETDATA                  YES                      S22024
XCTL07   EQU   *                                                 S22024
         SPACE
         MVC   OPCLDNME+VARINAME(NAMELEN),WRITE SET UP WRITE MOD NAME
XCTL     EQU   *
         LR    RPARM,ROPCAVT            POINT TO AVT
         XCTL  SF=(E,OPCXCTL)           CALL NEXT MODULE
         SPACE 2
MOVEMSG  MVC   MSGLOC(NULL,RWORKA),ADDROFF(RMSGPT) MOVING MESSAGE
*                                                                     *
*  THE FOLLOWING TABLE CONTAINS THE MESSAGE IDENTIFIERS AND THE       *
*    OFFSET FROM THE BEGINNING OF THIS MODULE TO THE APPROPRIATE      *
*    ERROR MESSAGES.                                                  *
*                                                                     *
         SPACE 2
ERRORTAB DS    0F
         DC    AL2(11),AL2(MSG11-BASE)
         DC    AL2(12),AL2(MSG12-BASE)
         DC    AL2(13),AL2(MSG13-BASE)
         DC    AL2(45),AL2(MSG45-BASE)
         DC    AL2(47),AL2(MSG47-BASE)
         DC    AL2(55),AL2(MSG55-BASE)
         DC    AL2(102),AL2(MSG102-BASE)
         DC    AL2(103),AL2(MSG103-BASE)
         DC    AL2(104),AL2(MSG104-BASE)
         DC    AL2(106),AL2(MSG106-BASE)
         DC    AL2(107),AL2(MSG107-BASE)
         DC    AL2(109),AL2(MSG109-BASE)
         DC    AL2(124),AL2(MSG124-BASE)
         DC    AL2(157),AL2(MSG157-BASE)
         DC    AL2(164),AL2(MSG164-BASE)                         S22024
         DC    AL2(165),AL2(MSG165-BASE)                         S22024
         DC    AL2(166),AL2(MSG166-BASE)                         S22024
         DC    AL2(171),AL2(MSG171-BASE)                         S22024
         DC    AL2(184),AL2(MSG184-BASE)                         S22024
         DC    AL2(185),AL2(MSG185-BASE)                         S22024
         DC    X'FF'                    END OF TABLE
MSG11    DC    AL1(END11-*-2),AL1(0)
         DC    C'IED011I SYSTEM INTERVAL CANNOT BE ALTERED'
END11    EQU   *
         SPACE
MSG12    DC    AL1(END12-*-2),AL1(0)
         DC    C'IED012I TSO SESSION ON LINE - COMMAND REJECTED'
END12    EQU   *
         SPACE
MSG13    DC    AL1(END13-*-2),AL1(0)
         DC    C'IED013I STOP REQUEST FOR SELF-VARY COMMAND REJECTED'
END13    EQU   *
         SPACE
MSG45    DC    AL1(END45-*-2),AL1(0)
         DC    C'IED045I SYSTEM INTERVAL ALREADY ACTIVE'
*                                                                     *
END45    EQU   *
         SPACE
MSG47    DC    AL1(END47-*-2),AL1(0)
         DC    C'IED047I SYSTEM INTERVAL IS ACTIVE'
*                                                                     *
END47    EQU   *
         SPACE
MSG55    DC    AL1(END55-*-2),AL1(0)
         DC    C'IED055I TRACE CANNOT BE ALTERED'              @YM06033
*
END55    EQU   *
         SPACE
         SPACE
MSG102   DC    AL1(END102-*-2),AL1(0)
         DC    C'IED102I INVALID OPERATION'
*                                                                     *
END102   EQU   *
         SPACE
MSG103   DC    AL1(END103-*-2),AL1(0)
         DC    C'IED103I ROUTINE ALREADY ACTIVE'
*                                                                     *
END103   EQU   *
         SPACE
MSG104   DC    AL1(END104-*-2),AL1(0)
         DC    C'IED104I ROUTINE NOT ACTIVE'
*                                                                     *
END104   EQU   *
         SPACE
MSG106   DC    AL1(END106-*-2),AL1(0)
         DC    C'IED106I MULTIPLE REQUEST'
*                                                                     *
END106   EQU   *
         SPACE
MSG107   DC    AL1(END107-*-2),AL1(0)
         DC    C'IED107I COMWRITE NOT ACTIVE'
*                                                                     *
END107   EQU   *
         SPACE
MSG109   DC    AL1(END109-*-2),AL1(0)
         DC    C'IED109I ROUTINE NOT DELETED'
END109   EQU   *
         SPACE
MSG124   DC    AL1(END124-*-2),AL1(0)
         DC    C'IED124I QUEUE HAS BEEN WRAPPED'
*                                                                     *
END124   EQU   *
         SPACE 2
MSG157   DC    AL1(END157-*-2),AL1(0)
         DC    C'IED157I TCAM SYSTEM DELAY ACTIVE-HALT COMMAND REJECTEDX
               '
*                                                                     *
END157   EQU   *
MSG164   DC    AL1(END164-*-2),AL1(13)                           S22024
         DC    C'IED164I 3705          IPL/DUMP TERMINATED'      S22024
         DC    C' CORE UNAVAILABLE'                              S22024
END164   EQU   *                                                 S22024
MSG165   DC    AL1(END165-*-2),AL1(13)                           S22024
         DC    C'IED165I 3705          IPL TERMINATED BLDL FAILED'
END165   EQU   *                                                 S22024
MSG166   DC    AL1(END166-*-2),AL1(13)                           S22024
         DC    C'IED166I 3705          IPL TERMINATED OPEN FAILED'
END166   EQU   *                                                 S22024
MSG171   DC    AL1(END171-*-2),AL1(8)                            S22024
         DC    C'IED171I          IS A 3705 LINE'                S22024
END171   EQU   *                                                 S22024
MSG184   DC    AL1(END184-*-2),AL1(13)                           S22024
         DC    C'IED184I 3705          DOES NOT HAVE A VALID BACKUP'
END184   EQU   *                                                 S22024
MSG185   DC    AL1(END185-*-2),AL1(13)                           S22024
         DC    C'IED185I 3705          NOT DESIGNATED AS A BACKUP'
END185   EQU   *                                                 S22024
WRITE    DC    C'07'                    MESSAGE WRITER
LOADID   DC    C'0E'                    NEXT MODULE              S22024
ZAPAREA  DC    50X'00'                  ZAPAREA                @Y17XARW
         TITLE 'IGC0810D - DSECTS REFERENCED BY THIS MODULE'
         TAVTD
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
         END
