RK TITLE '''IGG019RK'' - DISK END APPENDAGE FOR 1 CPB'
IGG019RK CSECT
         SPACE 3                                                SA63974
***********************************************************************
*                                                                     *
* TITLE -- 'IGG019RK' - DISK END APPENDAGE FOR 1 CPB'                 *
*                                                                     *
*   MODULE NAME = IGG019RK                                            *
*                                                                     *
*   DESCRIPTIVE NAME = DISK END APPENDAGE FOR 1 CPB                   *
*                                                                     *
*   COPYRIGHT = 'NONE'                                                *
*                                                                     *
*   STATUS CHANGE LEVEL 5                                             *
*                                                                     *
* FUNCTION -- THIS APPENDAGE IS TO RECEIVE CONTROL FROM IOS AT THE    *
*    END OF A DISK I/O OPERATION.  IT REMOVES THE CPB FROM THE IOB,   *
*    PASSES IT TO THE CPB CLEANUP ROUTINE VIA AVTDKAPQ, POSTS THE     *
*    CPB CLEANUP QCB TO THE DISABLED READY QUEUE TO PROCESS THE CPB,  *
*    AND POSTS TCAM'S ECB TO ACTIVATE THE TCAM DISPATCHER.            *
*                                                                     *
*    A TEST ALSO IS MADE FOR AN ERROR IN THE I/O OPERATION. IF   99226*
*    ERROR, THE BAD CPB IS FOUND AND FLAGGED.  THE FOLLOWING CPB 99226*
*    IS EXPANDED AND RETURNED WITH A REQUEST TO RETRY.           99226*
*                                                                99226*
* ENTRY POINT -- THERE IS ONLY ONE ENTRY, CALLED BY IOS               *
*        'IGG019RK' - DISK END APPENDAGE                              *
*    CALLING SEQUENCE -                                               *
*        L     15,ADDR OF APPENDAGE FROM DEB VECTOR TABLE             *
*        BALR  14,15                                                  *
*                                                                     *
* INPUT -- REGISTER 15 HAS THE ENTRY POINT.                           *
*    REGISTER 2 HAS THE IOB ADDRESS.                                  *
*    REGISTER 14 HAS THE RETURN ADDRESS.                              *
*    REGISTER 7 HAS THE ADDRESS OF THE UCB                       99226*
*                                                                     *
* OUTPUT -- CPB CLEANUP QCB IS POSTED.  TCAM'S ECB IS POSTED.         *
*                                                                     *
*    ERRORED CPBS ARE FLAGGED, THE IOB ERRORED BITS ARE CLEARED, 99226*
*    AND THE FOLLOWING CPB IS EXPANDED.                          99226*
*                                                                99226*
* EXTERNAL ROUTINES -- THERE IS ONLY ONE EXTERNAL CALL,               *
*         'OS POST' - POST TCAM'S ECB TO ACTIVATE DISPATCHER          *
*                                                                     *
* EXITS-NORMAL -- RETURN TO IOS VIA REGISTER 14.                      *
*                                                                     *
* EXITS-ERROR -- N/A                                                  *
*                                                                     *
* TABLES/WORKAREAS -- QCB, FOR CPB CLEANUP IS PUT ON DISABLED READY Q.*
*    IOB, HAS CPB ADDRESS                                             *
*    CPB, MOVED FROM IOB TO AVTDKAPQ                                  *
*    AVT, AVTDKAPQ RECEIVES CPB, AND AVTCPBCB (CPB CLEANUP QCB) IS    *
*    POSTED.                                                          *
*                                                                     *
* ATTRIBUTES -- SERIALLY REUSABLE,REENTERANT,DISABLED,SUPERVISOR MODE *
*                                                                     *
* NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A        *
*    PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.*
*                                                                     *
*    THIS MODULE IS LOADED FROM SVCLIB WHEN THE FIRST DISK MESSAGE    *
*    QUEUE DATA SET IS OPENED, AND IF IN INTRO IS SPECIFIED CPB=1     *
*                                                                     *
* CHANGE ACTIVITY = AS FOLLOWS                                 @Y17XAQI
*A371000,493500,707000                                           A42413
*C512000                                                         A42413
*D791300-794900,833000                                           A42413
*A357000,399000,427000                                           S21101
*C462000,512000                                                  S21101
*A588000                                                         S21101
*C595000-596000                                                  S21101
*A609000                                                         S21101
*D709000                                                         S21101
*C770000,799000                                                  S21101
*A819000,840000                                                  S21101
*C511500,672000                                                  S21903
*D833000                                                         S21903
*A 742000                                                       SA63974
*D 714000-728000                                                SA63974
*C759000-763000                                                 OX01436
*D833000                                                         S21903
*A511000,593400,819000                                           X01004
*C493550,588000-588100,795000-799000,878000                      X01004
*A493980,823500                                                  X02004
*C593404,819050,819080                                           X02004
*D483000                                                         X02004
*A315000,378000,406000,878000                                  @Y17XAQI
*C623000-700000                                                @Y17XAQI
*                                                                     *
***********************************************************************
         EJECT                                                   S21903
R15      EQU   15                       ENTRY
R14      EQU   14                       RETURN
R13      EQU   13
RBASE    EQU   12                       PROGRAM BASE
R12      EQU   12                       TCB ADDR FOR OS POST
RCPB     EQU   11                       CPB DSECT BASE           S21121
R11      EQU   11                       TCAM ECB ADDRESS
RQCB     EQU   11                       CPB CLEANUP QCB
R10      EQU   10                       REGISTER 10              A42413
RAVT     EQU   10                       AVT DSECT                99226
RTCX     EQU   10                       TCX BASE REGISTER      @Y17XAQI
R9       EQU   9                        ERRORED CPB WORKAREA     99226
*                                         THEN, CLEARED FOR IOS  99226
R8       EQU   8                        SAVED FOR IOS
RUCB     EQU   7                        UCB ADDRESS              99226
R7       EQU   7                        REGS 3 THRU 7 USED IN    S21121
*                                         MOVING CCWS IN CPB     S21121
R6       EQU   6                        TEMPORARY AVT BASE
RCURR    EQU   6                        CURRENT ELEM REGISTER  @Y17XAQI
R5       EQU   5                        NOT USED
RDCB     EQU   4                        DCB ADDR (UNUSED)
R4       EQU   4                        UNUSED
RDEB     EQU   3                        DEB ADDR (UNUSED)
R3       EQU   3                        REGS 3 THRU 7 USED IN    S21121
*                                         MOVING CCWS IN CPB     S21101
RIOB     EQU   2                        IOB DSECT
R1       EQU   1                        12*
R0       EQU   0                        NOT USED
*
         USING IEDQTCB,R14
         USING IEDQCPB,RCPB                                      S21101
         USING IEDQIOB,RIOB
         USING UCB,RUCB                                          99226
         USING IEDQAVTD,RAVT
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                       LOCAL EQUATES
*
INTVREQ  EQU   X'40'                    INTERVENTION REQUIRED BIT99226
GOODCODE EQU   X'7F'                    SUCCESSFUL COMPLETION    99226
CPBER    EQU   X'01'                    FLAG TO INDICATE BAD CPB 99226
FLAGSET  EQU   X'C0'                    FLAG1 RESET CONSTANT     99226
INC      EQU   1                        INCREMENTOR TO NEXT TYPE
W2       EQU   4                        OFFSET TO 2ND WORD OF AREA
POSTED   EQU   X'40'                    BIT IS ON IN POSTED ECB
AD       EQU   7                        CONSTANT                 X01004
SM       EQU   X'31'                    CONSTANT                 X01004
NOTIC    EQU   7                        CONSTANT                 X01004
EIGHT    EQU   8                        CONSTANT                 X01004
NO       EQU   8                        CONSTANT                 X01004
ALL      EQU   15                       CONSTANT                 X01004
SXTN     EQU   16                       CONSTANT                 X01004
CCON     EQU   1                        BR ONES CONDITION CODE   X01004
         SPACE 3                                                 S21903
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                       ENTRY - DISK END APPENDAGE
*                             FOR ONE CPB
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         DC    A(END19RK-IGG019RK)      LENGTH OF APPENDAGE      X02004
         USING *,R15                    ESTABLISH ADDRESSABILITY X02004
         BAL   RBASE,TAG                SKIP CONSTANTS,SET PROG BASE
*
         DROP  R15                      FORGET TEMPORARY
         USING *,RBASE                  ESTABLISH ADDRESSABILITY
*
         DC    A(DISKSIO-IGG019RK)      OFFSET OF SIO APPENDAGE  X01004
IGG019RK IEDHJN ,                                                S21903
*
TAG      DS    0H
         USING CVT,R15
*
         L     R11,CVTPTR               GET ADDRESS OF CVT
         L     R15,AVTCVTPT(,R11)       GET ADDRESS OF DISPATCHER
         L     RAVT,0(,R15)             GET AVT ADDR FORM DISPATCHER
         STM   R14,R1,AVTSAVEX          SAVE REGISTERS           X01004
         STM   RDEB,R8,AVTSAVEX+SXTN    SAVE REGISTERS           X01004
         TM    IOBSENS0,INTVREQ         TEST INTERV REQUIRED     99226
         BZ    NOTREQ                     IF NO, CONTINUE        99226
*                                         IF YES, RETURN         99226
         BR    R14                      RETURN TO OS FOR HANDLING99226
*                                                                99226
NOTREQ   EQU   *                                                 99226
*
* CHECK FOR MULTIPROCESSOR CPU ENVIRONMENT AND STOP OTHER CPU
*
         L     R11,AVTCVTMP(,R11)       GET ADDR OF MPCVT
         LTR   R11,R11                  IS MPCVT PRESENT
         BZ    NOMPCVT                    NO, DO NOT HAVE MPCVT
*                                         YES, GOT MPCVT
         L     R14,AVTTCB               GET TCAM'S TCB ADDRESS
         OI    TCBNDSP1,TCBTPSP         MARK TCAM'S TCB AS BEING
         OI    TCBFLGS+W2,TCBPNDSP        NON-DISPATCHABLE
         L     R15,AVTMPTRR(,R11)       FROM MPCVT GET ENTRY TO
*                                         TASK REMOVAL ROUTINE
         BALR  R14,R15                  INTERRUPT OTHER CPU
*
NOMPCVT  EQU   *
*
* END OF SECTION OF MULTIPROCESSOR CPU SUPPORT.
*
         USING IEDQCCW,R11                                       X01004
         NI    IOBINCAM,X'FF'-CCWREAL   RESET TRANSLATION FLAG   X02004
         ICM   R11,AD,IOBSTART          START OF CHANNEL PROGRAM X01004
CONVRLOP EQU   *                                                 X01004
         SR    R1,R1                    CLEAR HI-ORD BYTE OF R1  X01004
         ICM   R1,AD,CCWADDR            I/O ADDRESS              X01004
         BAL   R14,VIRTUAL              CONVERT ADDRESS          X01004
         STCM  R1,AD,CCWADDR            SET VIRTUAL ADDRESS      X01004
         CLI   CCWOPCDE,SM              IS STATUS MODIF EXPECTD  X01004
         BE    SMNOTIC                  BRANCH YES               X01004
         TM    CCWOPCDE,NOTIC           IS THIS CCW A TIC        X01004
         BZ    ITSTIC                   BRANCH YES               X01004
         TM    CCWFLAGS,CCWCC+CCWCD     CHAINING SPECIFIED       X01004
         BZ    ENDOF                    BRANCH NO - END OF CCW'S X01004
*                                                                X01004
PLUS8V   EQU   *                                                 X01004
         LA    R11,CCW+EIGHT            BUMP TO NEXT CCW         X01004
         B     CONVRLOP                 CONTINUE LOOP            X01004
*                                                                X01004
SMNOTIC  EQU   *                                                 X01004
         LA    R11,CCW+EIGHT            BUMP TO TIC              X01004
         SR    R1,R1                    CLEAR HI-ORD BYTE OF R1  X01004
         ICM   R1,AD,CCWADDR            TIC TO ADDRESS           X01004
         BAL   R14,VIRTUAL              CONVERT                  X01004
         STCM  R1,AD,CCWADDR            SET VIRTUAL ADDRESS      X01004
         B     PLUS8V                   GET NEXT CCW             X01004
*                                                                X01004
ITSTIC   EQU   *                                                 X01004
         LR    R11,R1                   NEXT CCW IS TIC TO ADDR  X01004
         B     CONVRLOP                 CONTINUE LOOP            X01004
*                                                                X01004
VIRTUAL  EQU   *                                                 X01004
         L     R15,CVTPTR               ADDRESS OF CVT           X01004
         L     R15,CVTPTRV              REAL TO VIRT CONVERTER   X01004
         BR    R15                      LINK TO ROUTINE          X01004
         DROP  R11                                               X01004
         USING IEDQCPB,RCPB                                      X01004
ENDOF    EQU   *                                                 X01004
*                                                                X01004
* TEST IF THERE WAS AN ERROR IN THE I/O OPERATION                99226
*   IF SO, REGISTER 9 CONTAINS THE COMPLETION CODE               99226
*   IF NOT REGISTER 9 IS ZERO.                                   99226
*                                                                99226
         MVC   AVTDOUBX,IOBFLAG3        GET CSW                  99226
         SR    R9,R9                    CLEAR WORK REGISTER      99226
         CLI   IOBECBCC,GOODCODE        CHECK COMPLETION CODE    99226
         BNE   ERROR                      BAD, SET UP ERROR      99226
*                                         GOOD, GO ON            99226
         NC    AVTDOUBX+4(2),ERRBITS    ARE ERROR BITS SET IN CSW99226
         BZ    TESTQ                      NO, GOOD I/O           99226
*                                         YES, GET DISK ERROR    99226
ERROR    EQU   *                                                 99226
         IC    R9,IOBECBCC              SAVE COMPLETION CODE     99226
         MVI   AVTDOUBX,AVTEZERO        CLEAR HIGH BYTE OF BAD   99226
*                                         CCW ADDRESS            99226
TESTQ    EQU   *                                                 99226
         L     RCPB,IOBSTART-1          MOVE CPB TO AVT TO       S21101
         ST    RCPB,AVTDKAPQ              BECOME ONLY CPB ON     S21101
         ST    RCPB,AVTDKAPQ+W2             CLEANUP FIFO QUEUE   S21101
         LA    R15,0(,RCPB)             SET PSEUDO BASE OF CPB   S21101
*                                       AND CLEAR HIGH BYTE      S21101
*                                                                S21101
* ASSUME CPB IS ALWAYS A MEDIUM CPB (NO SEEK HEAD)               S21101
* WITH SEARCH - TIC PRECEEDED BY OPTIONAL SET SECTOR CCW.        S21101
*                                                                S21101
         CLI   CPBSEEK,CPBSETC          IS FIRST CCW SET         S21101
         BE    LESS8                      YES, MEDIUM CPB WITH SES21101
*                                           GO ADJUST FOR MISSINGS21101
*                                         SEEK                   S21101
*                                         NO, NOT SET CCW        S21101
         SH    R15,H8                   ADJUST FOR MISSING SET   S21101
*                                                                S21101
LESS8    EQU   *                                                 S21101
         SH    R15,H8                   ADJUST FOR MISSING SEEK  S21101
*                                         CPB BASE               S21101
         LM    R3,R6,CPBRDWR-IEDQCPB(R15) GET RD,RD,TIC FROM     99226
*                                         PSEUDO CPB             S21101
         TM    CPBXDWR-IEDQCPB(R15),CPBTICC IS THIS TIC OR FF    S21101
         BZ    *+8                        NEITHER, SKIP NEXT INSTS21101
*                                         YES, TIC OR FF         S21101
         SH    R15,H8                   ADDR OF DISK UNIT AT     S21101
*                                         CPBAREA, NOT CPBXREA   S21101
         L     R15,CPBXREAF-IEDQCPB(,R15) GET UNIT ADDRESS       S21101
         STM   R3,R6,CPBRDWR              RESTORE RD,RD,TIC TO   99226
*                                         PROPER POSITION        S21101
         ST    R15,CPBXREAF             SET BUFFER ADDRESS TO    S21101
*                                         PROPER POSITION        S21101
         MVI   CPBSEEK,CPBSEEKC         FLAG ADJUSTED CPB AS     S21101
*                                         LARGE CPB              S21101
         LTR   R9,R9                    ANY ERRORS               99226
         BZ    TESTIOB                    NO, NORMAL OPERATION   99226
*                                         YES, CPB BAD SOMEWHERE 99226
         OI    CPBFLAG,CPBER            FLAG THIS CPB AS BAD     99226
         MVC   CPBFLAG3(8),IOBFLAG3     SAVE CSW                 99226
         MVC   CPBFLAG1(5),IOBFLAG1     SAVE FLAG1-2,SENS0-1,    99226
*                                         IOBECBCC               99226
         MVC   CPBUCBID,UCBNAME         PASS UCB ID CHARS        99226
         MVI   IOBFLAG1,FLAGSET         RESET FLAG1              99226
         XC    IOBFLAG2(3),IOBFLAG2     CLEAR FLAG2,SENS0-1      99226
         XC    IOBFLAG3(8),IOBFLAG3     CLEAR CSW                99226
         XC    IOBERRCT(2),IOBERRCT     CLEAR ERROR COUNT        99226
TESTIOB  EQU   *                                                 99226
*                                                                S21101
         DROP  RCPB                     FINISHED WITH CPB        S21101
         USING IEDQQCB,RQCB             USE SAME REG FOR QCB BASES21101
*                                                                S21101
         LA    RQCB,AVTCPBCB            GET CPB CLEANUP QCB
         CLI   QCBPRI,AVTEZERO          IS CLEANUP QCB ALREADY   S21101
*                                         POSTED                 S21101
         BNE   ENQUED                     YES, SKIP POST         S21101
*                                         NO, POST CPB CLEANUP   S21101
*                                           QCB TO DISABLED      S21101
*                                           READY QUEUE          S21101
         MVI   QCBPRI,PRICPBCL          PUT PRIORITY INTO QCB
         L     RTCX,CVTPTR              PICK UP CVT ADDRESS    @Y17XAQI
         L     RTCX,AVTCVTPT(RTCX)      PICK UP TCX ADDRESS    @Y17XAQI
         DROP  RAVT                                            @Y17XAQI
         USING IEDQTCX,RTCX             ADDRESSIBILITY         @Y17XAQI
         L     RCURR,TCXREADY           PICK UP ADDRESS OF     @Y17XAQI
*                                       CURRENT ENTRY ON THE   @Y17XAQI
*                                       ASYNCHRONOUS READY QUE @Y17XAQI
NEWCURR  EQU   *                                               @Y17XAQI
         STCM  RCURR,AD,QCBLINK         LINK NEW ELEMENT TO    @Y17XAQI
*                                       THE CURRENT ELEMENT    @Y17XAQI
         CS    RCURR,RQCB,TCXREADY      WAS CURRENT ELEMENT    @Y17XAQI
*                                       UPDATED ASYNCHRONOUSLY @Y17XAQI
         BNE   NEWCURR                  YES, LINK NEW ELEMENT  @Y17XAQI
*                                       TO UPDATED CURRENT     @Y17XAQI
*                                       ELEMENT                @Y17XAQI
*                                       NO, ASYNCHRONOUS READY @Y17XAQI
*                                       QUEUE NOW CONTAINS     @Y17XAQI
*                                       NEW ELEMENT            @Y17XAQI
         L     RAVT,TCXAVT              RESTORE AVT ADDRESS    @Y17XAQI
         DROP  RTCX                                            @Y17XAQI
         USING IEDQAVTD,RAVT            ADDRESSIBILITY         @Y17XAQI
ENQUED   EQU   *
         L     R14,AVTTCB               GET TCAM'S TCB ADDR      A42413
         NI    TCBNDSP1,X'FF'-TCBTPSP   CLEAR BIT FROM TCB
*                                       PERMITTING TCAM TO BE
*                                       DISPATCHED               A42413
         NC    TCBNDSP1(3),TCBNDSP1     OTHER NON-DISP BITS ON?  A42413
         BNZ   BYPASS                   YES,DONT SET PRIM DISP   A42413
         NI    TCBFLGS+W2,X'FF'-TCBPNDSP PERMIT DISPATCHING      A42413
BYPASS   EQU   *                                                 A42413
         LR    R6,RAVT                  SHIFT AVT BASE
         LA    R11,AVTOSECB             PASS ECB ADDR
         TM    AVTBIT1,AVTREADN         WAS READ ISSUED BY      SA63974
*                                        CHECKPOINT QUEUE SCAN  SA63974
         BO    POSTCAM                  NO, CHECK TCAM ECB      SA63974
         LA    R11,AVTCKECA             YES, USE DIFFERENT ECB  SA63974
POSTCAM  EQU   *                                                SA63974
         TM    AVTEZERO(R11),POSTED     IS ECB ALREADY POSTED   SA63974
         BO    EXIT                     YES, SKIP POST          SA63974
         L     R15,CVTPTR               ADDR OF CVT
         L     R15,CVT0PT01             OS POST ENTRY ADDR
         L     R12,AVTTCB               TCAM'S TCB ADDR
         SR    R10,R10                  CLEAR PARM REG          OX01436
         BALR  R14,R15                  POST TCAM ECB TO ACTIVATES21101
*                                         TCAM DISPATCHER
         DROP  RAVT                     RAVT CLOBBERED BY OS POST
         USING IEDQAVTD,R6              USE ALTERNATE AVT BASE
EXIT     EQU   *
         LM    R14,R1,AVTSAVEX          RESTORE REGISTERS        X01004
         LM    RDEB,R8,AVTSAVEX+SXTN    RESTORE REGISTERS        X01004
         SR    R9,R9                    CLEAR REG 9 FOR IOS
         BR    R14                      RETURN TO IOS
*
DISKSIO  EQU   *                                                 X01004
         USING *,R15                                             X01004
         USING IEDQIOB,RIOB                                      X01004
         USING IEDQCCW,R10                                       X01004
         TM    IOBINCAM,CCWREAL         CCW'S TRANSLATED         X02004
         BCR   CCON,R14                 BRANCH YES - EXIT        X01004
*                                                                X01004
         OI    IOBINCAM,CCWREAL         SET TRANSLATION FLAG     X02004
         ICM   R10,AD,IOBSTART          CHANNEL PROGRAM START    X01004
CONVLOOP EQU   *                                                 X01004
         ICM   R11,AD,CCWADDR           I/O ADDRESS              X01004
         LRA   R13,0(,R11)              CONVERT TO REAL          X01004
         STCM  R13,AD,CCWADDR           SET REAL ADDRESS         X01004
         CLI   CCWOPCDE,SM              STATUS MODIFIER EXPECTED X01004
         BE    STATUSM                  BRAVCH YES               X01004
*                                                                X01004
         TM    CCWOPCDE,NOTIC            THIS CCW A TIC          X01004
         BZ    ISATIC                   BRANCH YES               X01004
*                                                                X01004
         TM    CCWFLAGS,CCWCC+CCWCD     CHAINING FLAGS SET       X01004
         BCR   NO,R14                   BRANCH NO - END OF CHAIN X01004
*                                       RETURN TO IOS            X01004
PLUS8    EQU   *                                                 X01004
         LA    R10,CCW+EIGHT                                     X01004
         B     CONVLOOP                 CONTINUE LOOP            X01004
*                                                                X01004
STATUSM  EQU   *                                                 X01004
         LA    R10,CCW+EIGHT            BUMP TO TIC              X01004
         ICM   R11,AD,CCWADDR           TIC TO ADDRESS           X01004
         LRA   R13,0(,R11)              CONVERT TO REAL          X01004
         STCM  R13,AD,CCWADDR           SET REAL ADDRESS         X01004
         B     PLUS8                    GET NEXT CCW             X01004
*                                                                X01004
ISATIC   EQU   *                                                 X01004
         LR    R10,R11                  NEXT CCW IS TIC TO ADDR  X01004
         B     CONVLOOP                 CONTINUE LOOP            X01004
H8       DC    H'8'                     ADJUST PSEUDO CPB REG    S21101
BADCCW   DC    A(CPBABSAD-IEDQCPB)      CPB ADDR OUT OF RANGE    99226
ERRBITS  DC    XL2'037F'                CSW ERROR BITS TO TEST   99226
         CNOP  0,8                      ALIGN TO DOUBLEWORD      X02004
END19RK  EQU   *                        END OF LOAD MODULE       X02004
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         TCPBD 3330                                              99226
*                                                                99226
* IN DISK ERROR CONDITIONS, DISK APPENDAGE PASSES ERROR STATUS   99226
* INFORMATION IN THE CPB AS FOLLOWS                              99226
*                                                                99226
         ORG   CPBSEEK                  REDEFINE FROM CPB START  99226
         DS    A                        RESERVED FOR IEDQRQ      99226
CPBFLAG3 DS    C                        FROM IOBFLAG3            99226
CPBCSW   DS    CL7                      FROM IOBCSW              99226
CPBFLAG1 DS    C                        FROM IOBFLAG1            99226
CPBFLAG2 DS    C                        FROM IOBFLAG2            99226
CPBSENS0 DS    C                        FROM IOBSENS0            99226
CPBSENS1 DS    C                        FROM IOBSENS1            99226
CPBECBCC DS    C                        FROM IOBECBCC            99226
CPBUCBID DS    CL3                      FROM UCBNAME             99226
         TTCBD
         TPRIOR
UCB      DSECT                                                   99226
         IEFUCBOB                                                99226
         TQCBD
         TIOBD
CVT      DSECT
         CVT
         TAVTD
         TCCWD                                                   X01004
         TTCXD SVS                                             @Y17XAQI
         END
