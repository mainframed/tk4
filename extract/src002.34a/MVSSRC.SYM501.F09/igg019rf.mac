RF       TITLE '''IGG019RF'' - EXCP DRIVER, SINGLE CPB'
IGG019RF CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS                                 SUPT CODE
*A156000,196600,218000,372000                                    S21101
*C194000,199600,280000,310000-326000,626000,666000,922000-924000 S21101
*D212000-216000                                                  S21101
*C114000                                                       @XA11330
*C242000,288000,814000,852000                                    S21903
*D890000                                                         S21903
*A196750                                                         X01004
*C020000,080000,086000-088000,134000,218000-219200,236000-240000,X01004
*C315400-315600,336000-338000,344000,634000-638000,774000,778000 X01004
*D373000,624000                                                  X01004
*C873200,873600,875000                                          OX02204
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE 'IGG019RF' - EXCP DRIVER, SINGLE CPB                           *
*                                                                     *
*  MODULE NAME= IGG019RF                                              *
*                                                                     *
*  DESCRIPTIVE NAME = EXCP DRIVER , SINGLE CPB                        *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS = CHANGE LEVEL 5                                            *
*                                                                     *
*FUNCTION -- THIS MODULE TAKES THE CPB BEGUN BY IEDQFA, FINISHES      *
*   BUILDING THE CCWS, ADD SEARCH-TIC CCWS, TRANSLATES ABSOLUTE       *
*   DISK ADDRESS TO MBBCCHHR, CALLS IOS TO DO EXCPVR, AND RETURNS TO  *
*   THE DISPATCHER, ENDING DISK I/O SUBTASK.                          *
*                                                                     *
*                                                                99226
*   A ROUTINE ALSO RESIDES HERE WHICH WRITES A MESSAGE TO THE    99226
*   OPERATOR IN THE EVENT OF DISK ERROR                          99226
*   A DISK WRITE ERROR REQUESTS A QUICK CLOSEDOWN                99226
*   A DISK READ ERROR ISSUES AN ABEND                            99226
*                                                                99226
*ENTRY POINTS -- ONLY ONE ENTRY POINT, CALLED BY IEDQFA               *
*        'IGG019RF' - START DISK I/O FOR ONE CPB                      *
*   CALLING SEQUENCE -                                                *
*        L     15,AVTFL GET ADDR OF ENTRY OF IGG019RF                 *
*        BR    15                                                     *
*                                                                     *
*INPUT -- THERE ARE 3 TYPES OF INPUT, REGISTERS, AVT CONSTANTS, AND   *
*   THE CPB PREPARED BY IEDQFA.                                       *
*   REGISTER INPUT --                                                 *
*   R15 HAS ADDR OF ENTRY POINT                                       *
*   R13 HAS ADDR OF AVTSAVE2                                          *
*                                                                     *
*   AVT CONSTANTS --                                                  *
*   AVTINCPQ HAS ADDR OF CPB.                                         *
*   AVTIOBR HAS ADDR OF REUSABLE DISK IOB.                            *
*   AVTIOBN HAS ADDR OF NON-REUSABLE DISK IOB.                        *
*   AVTDSKCT HAS NUMBER OF WRITING CPBS.                              *
*                                                                     *
*   CPB FIELDS EXPECTED TO BE DONE --                                 *
*   CPBRDWR HAS READ OR WRITE CCW OP CODE.                            *
*   CPBXDWR HAS A NO-OP, OR READ, OR WRITE OP CODE.                   *
*   CPBXREA HAS ADDR OF UNIT.                                         *
*   CPBNEXT IS ASSUMED ZERO.                                          *
*   CPBFLAG INDICATES REUSABLE OR NON-REUSABLE DISK MSG QUEUE.        *
*   CPBADDR HAS ABSOLUTE DISK ADDRESS.                                *
*                                                                     *
*OUTPUT -- EXCPVR IS STARTED ON THE COMPLETED CPB.                    *
*   AVTDSKCT IS BUMPED BY 1 IF CPB WAS A 'WRITE'.                     *
*                                                                     *
*EXTERNAL ROUTINES -- EXCPVR MACRO GENERATES CALL TO                  *
*        'SVC 114' - START DISK I/O                                   *
*                                                                     *
*EXITS-NORMAL -- EXITS TO DISPATCHER.                                 *
*                                                                     *
*EXITS-ERROR -- N/A                                                   *
*                                                                     *
*TABLES/WORKAREAS -- THESE MACROS GENERATE DSECTS -                   *
*   TIOBD                                                             *
*   TDEBD                                                             *
*   TCPBD                                                             *
*   TAVTD                                                             *
*   TDISPD                                                            *
*                                                                     *
*ATTRIBUTES -- RESIDENT, REUSABLE, PROBLEM PROGRAM            @XA11330*
*   MODE, LOADED BY DISK MSG Q OPEN IF ON INTRO CPB=1.                *
*                                                                     *
*NOTES -- THE OPERATION OF THIS SUBROUTINE DEPENDS UPON AN INTERNAL   *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDIFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITION.           *
*                                                                     *
*   THERE ARE THREE MAJOR STEPS - BUILD CCWS, CONVERT ABSOLUTE ADDR   *
*   TO MBBCCHHR, AND ISSUE EXCPVR.                                    *
*                                                                     *
R15      EQU   15                       ENTRY ADDR
R14      EQU   14
RIOBINDX EQU   14                       SIZE OF AN IOB
R13      EQU   13                       SAVE2
RAVT     EQU   13                       AVT DSECT BASE
R12      EQU   12
RBASE    EQU   12                       PROGRAM BASE
R11      EQU   11                       DISPATCHER ADDRESS       99226
RIOB     EQU   11                       IOB DSECT BASE
R10      EQU   10
RDUMCPB  EQU   10                       PSEUDO CPB BASE          S21101
R9       EQU   9
R8       EQU   8
R7       EQU   7
RCPB     EQU   7                        CPB DSECT BASE
R6       EQU   6
RPREFIX  EQU   6                        PREFIX DSECT BASE        99226
RDEB     EQU   5                        DEB DSECT BASE
R5       EQU   5                        WORK REGISTER            99226
R4       EQU   4
R3       EQU   3
R2       EQU   2
R1       EQU   1                        PARAMETER PASSER
R0       EQU   0
*
         USING IEDQCPB,RCPB
         USING IEDQAVTD,RAVT
         USING IEDQIOB,RIOB
         USING IEDQPRF,RPREFIX          PREFIX DSECT             99226
         USING IEDQDEB,RDEB
         USING *,R15
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                       LOCAL EQUATES
*
HISREG14 EQU   12                       OFFSET INTO SAVEAREA OF R14
HISREG15 EQU   16                       OFFSET INTO SAVEAREA OF R15
SAVECCW  EQU   12                       OFFSET INTO SAVE2 WHERE 3
*                                         CCWS ARE SAVED
THREECCW EQU   20                       LENGTH OF CCWS SAVED IN SAVE2
RPS      EQU   X'10'                    RPS DEVICE IN UCBTBYT2   S21101
FULLSHIF EQU   32                       DOUBLE REG SHIFT ENTIRE REG
BYTESHIF EQU   8                        SHIFT COUNT FOR 1 BYTE
THRESHFF EQU   X'FF'                    VALUE IN AVTHRESS - MEANS
*                                         ELE NOT YET ACTIVATED
THRESHF7 EQU   X'F7'                    VALUE IN AVTHRESS - MEANS
*                                         ELE IS REQUESTED TO BE
*                                         POSTED UPON EXIT FROM '-RF'
THRESHF0 EQU   X'F0'                    VALUE IN AVTHRESS - MEANS
*                                         ELE HAS BEEN POSTED TO READY
ABEND1   EQU   1                        ABEND SUBCODE, OUT OF
*                                         SPACE, NON-REUS DISK
ABEND5   EQU   5                        ABEND SUBCODE, DISK DCB
*                                         NOT OPEN
ROUNDER  EQU   3                        VALUE FOR RRN ADJUSTMENT S21101
LORDER   EQU   3                        OFFSET VALUE             S21101
DIV4     EQU   2                        VALUE FOR RRN ADJUSTMENT S21101
SEVEN    EQU   7                        3-BYTE ICM/STCM MASK.    X01004
ALL      EQU   15                       4-BYTE ICM/STCM MASK.    X01004
ONE      EQU   1                                                 X01004
TWO      EQU   2                        1-BYTE ICM MASK          X01004
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                       ENTRY - EXCP DRIVER
*                          FOR ONE CPB
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         BAL   RBASE,TAG                SKIP CONSTANTS AND
*                                         SET PROGRAM BASE
         DROP  R15                      FORGET TEMPORARY
         USING *,RBASE                  ESTABLISH ADDRESSABILITY
*
*                                                                99226
* THIS CONSTANT ALLOWS ANOTHER ROUTINE ACCESS TO THE DISK ERP WTO99226
*                                                                99226
         DC    A(WTORTN)                WTO ROUTINE FOR DISK ERP 99226
IGG019RF IEDHJN ,                                                S22025
*
TAG      DS    0H
         SR    RCPB,RCPB                CLEAR FOR ICM.           X01004
         ICM   RCPB,SEVEN,AVTINCPQ+ONE  FIRST CPB ON INPUT QUEUE X01004
         BZ    EXIT                     EXIT IF THE INPUT QUEUE  X01004
*                                         IS EMPTY.              X01004
*                                         NO, GOT CPB            S21101
         MVI   CPBXWFL,AVTEZERO         CLEAR FLAG BYTE 2ND CCW
         CLI   CPBXDWR,CPBNOPC          IS SECOND CCW A NO-OP
         BE    NOPCODE                    YES, IT IS A NO-OP
*                                         NO, READ/WRITE
         MVI   CPBRWFL,CPBCDC+CPBSLIC   SET 'DATA CHAIN' AND 'SLI'
         MVC   CPBCOUNT(2),AVTKEYLE     SET KEYLENGTH
         L     R15,CPBXREAF             GET UNIT ADDRESS
         LA    R15,AVTUMALN(,R15)       BUMP TO KEY PART OF BUFFER
         STCM  R15,SEVEN,CPBAREA        SET KEY BUFFER ADDRESS.  X01004
         B     DISKCPB                  BR AROUND NO-OP CODE     S21903
*
NOPCODE  EQU   *
         MVI   CPBRWFL,AVTEZERO         CLEAR CCWFLAG BYTE 1ST CCW
         LA    R15,AVTDATLN             GET DATA RECORD SIZE
         STH   R15,CPBCOUNT               TO 1ST CCW
         MVC   CPBAREA,CPBXREA          DATA FROM UNIT
*
DISKCPB  EQU   *
         LA    R15,IEDQFP               GET ADDR OF CONVERT ROUTINE
         BALR  R14,R15                  CHANGE ABSOLUTE RECORD NO.
*                                         TO MBBCCHHR
         TM    CPBRDWR,CPBWRITB         'WRITE'
         BO    SKIPBUMP                   YES, SKIP COUNTER
*                                         NO, COUNT 'READS'
         LH    R1,AVTDSKCT              GET PREVIOUS COUNT
         LA    R1,1(,R1)                BUMP IT BY 1
         STH   R1,AVTDSKCT              RESTORE NEW COUNT
SKIPBUMP EQU   *
         TM    CPBADDR+LORDER,CPBQTYPE  REUS DISK                S21101
         BO    LEUSDISK                   YES, REUS
*                                         NO, NON-REUS
         L     RIOB,AVTIOBN             GET NON-REUS FIRST IOB
         B     SKIP                     BR AROUND REUS CODE      S21903
*
LEUSDISK EQU   *
         L     RIOB,AVTIOBR             GET REUS FIRST IOB
SKIP     EQU   *
         SR    R15,R15                  CLEAR WORK REG
         IC    R15,CPBABSAD             GET M OF MBBCCHHR
         LA    RIOBINDX,IOBEND-IEDQIOB  SIZE OF IOB
         MR    R15-1,RIOBINDX           SIZE IOBS TO BE SKIPPED
         AR    RIOB,R15                 GO TO PROPER IOB
         MVC   SAVECCW(THREECCW,R13),CPBRDWR SAVE RD/WR CCWS
         LR    RDUMCPB,RCPB             SET PSEUDO CPB BASE      S21101
         SH    RDUMCPB,HALF8            ADJUST FOR MISSING       S21101
*                                         SEEK CCW               S21101
         L     R2,IOBDCBPT-1            GET DCB                  S21101
         L     R2,DCBDEBAD-IHADCB(,R2)  GET DEB                  S21101
         L     R2,DEBUCBAD-IEDQDEB-(DEBNMSUB-IEDQDEB)(,R2)       S21101
*                                         GET UCB                S21101
         TM    UCBTBYT2-UCB(R2),RPS     IS RPS SUPPORTED         S21101
         BO    BUILDRPS                   YES, GO BUILD SET      S21101
*                                         SECTOR CCW             S21101
*                                         NO, SKIP SET SECTOR    S21101
         SH    RDUMCPB,HALF8            ADJUST PSEUDO CPB BASE FOS21101
*                                         MISSING SET SECTOR CCW S21101
         B     SKIPRPS                  SKIP SET SECTOR CCW      S21101
*                                                                S21101
BUILDRPS EQU   *                        BUILD SET SECTOR CCW     S21101
* SET UP PARAMETERS TO CALL SECTOR CONVERT ROUTINE               S21101
* R0 HAS DDKR                                                    S21101
*        DD -  PHYSICAL BLOCK SIZE                               S21101
*        K  -  KEYLENGTH                                         S21101
*        R  -  RECORD NUMBER                                     S21101
* R2 HAS DAAA                                                    S21101
*        D  -  UCB DEVICE TYPE CODE FROM UCB+19                  S21101
*        AAA-  POINTER TO ONE BYTE AREA TO RECEIVE SECTOR VALUE  S21101
* R14 HAS RETURN ADDRESS                                         S21101
* R15 HAS ENTRY ADDRESS (FROM CVT+232)                           S21101
*                                                                S21101
* R2 HOW HAS ADDRESS OF UCB                                      S21101
*                                                                S21101
         IC    R2,UCBTBYT4-UCB(R2)      DEVICE TYPE CODE         S21101
         LA    R3,CPBSECTR-IEDQCPB(,RDUMCPB) SECTOR VALUE ADDRESSS21101
         ST    R3,CPBSETAF-IEDQCPB(,RDUMCPB) POINT CCW TO        S21101
*                                         SECTOR VALUE           S21101
         SLL   R3,BYTESHIF              ADDRESS TO TOP 3 BYTES   S21101
         SLDL  R2,FULLSHIF-BYTESHIF     R2 NOW HAS DAAA          S21101
*                                                                S21101
         L     R0,ZEROPARM              R0 NOW HAS DDKX          S21101
         ICM   R0,TWO,AVTKEYLE+ONE      GET K OF DDKR            X01004
         IC    R0,CPBABSAD+7            R0 NOW HAS DDKR          S21101
*                                                                S21101
         L     R15,CVTPTR               GET CVT                  S21101
         USING CVT,R15                                           S21101
         L     R15,CVT0SCR1             GET ENTRY TO SECTOR      S21101
*                                         CONVERSION ROUTINE     S21101
         STM   R0,R12,AVTSAVE4          SAVE REGS CLOBBERED      S21101
*                                         BY CONVERT ROUTINE     S21121
         BALR  R14,R15                  CALCULATE SECTOR VALUE   S21101
*                                       BUILD SET SECTOR CCW     S21101
         DROP  R15                                               S21101
         LM    R0,R12,AVTSAVE4          RESTORE REGS             S21101
         MVI   CPBSET-IEDQCPB(RDUMCPB),CPBSETC SET SECTOR OP CODES21101
         LA    R15,CPBSET1              GET COUNT OF 1           S21101
         ST    R15,CPBSETCT-2-IEDQCPB(RDUMCPB) CCW AND CLEAR     S21101
*                                         UNUSED BYTE            S21101
         MVI   CPBSETFL-IEDQCPB(RDUMCPB),CPBCCC COMMAND CHAIN    S21101
SKIPRPS  EQU   *                                                 S21101
         LA    R15,CPBABSAD+3           ADDR OF CCHHR            S21101
         ST    R15,CPBSRECF-IEDQCPB(,RDUMCPB) STORE ADDR OF CCHHRS21101
         MVI   CPBSRCH-IEDQCPB(RDUMCPB),CPBSRCHC SEARCH ID EQ    S21101
*                                         OP CODE                S21101
         MVI   CPBSRHFL-IEDQCPB(RDUMCPB),CPBCCC COMMAND CHAIN    S21101
         LA    R15,CPBSRH5              GET LENGTH OF 5          S21101
         STH   R15,CPBSRHCT-IEDQCPB(RDUMCPB) TO SEARCH CCW       S21101
         LA    R15,CPBSRCH-IEDQCPB(,RDUMCPB) GET SEARCH CCW ADDR S21101
         ST    R15,CPBTICSF-IEDQCPB(,RDUMCPB) TIC BACK TO SEARCH S21101
         MVI   CPBTIC1-IEDQCPB(RDUMCPB),CPBTICC TIC OP CODE      S21101
         MVC   CPBRDWR-IEDQCPB(THREECCW,RDUMCPB),SAVECCW(R13)    S21101
*                                         GET RD/WR CCWS         S21101
         NC    CPBADDR+1(3),CPBADDR+1   IS ADDRESS ZERO
         BNZ   OK                         NO, GO DO I/O
*                                       YES, ERROR               S22025
* THIS SHOULD NEVER OCCUR.  BUT, JUST IN CASE, BETTER KILL IT NOW
* BEFORE ALL TRACE OF WHY THE FALSE REQUEST WAS MADE IS LOST.
*
         DC    H'0'                     CPB REQUESTS DISK REC ZERO
*
* THIS IS AN INTENTIONAL PROGRAM CHECK TO TRAP A TYPE OF INTERNAL
* TCAM LOGICAL BUG THAT CAUSES RECORD ZERO TO INCORRECTLY BE REFERENCED
* IN THE CPB.  THERE IS NO SUCH RECORD.
* THE FIX LIES NOT IN IGG019RF BUT IN WHOEVER BUILT THE CPB AND GAVE
* IT TO IGG019RF FOR ITS INPUT QUEUE.  RCPB POINTS TO CPB.
* CPBFLAG X'80' BIT ON SAYS CPB BUILT BY IGG019RP, REUS/COPY.
* IF THAT NOT ON, CHECK 'READY HAD BEEN EXECUTED' BIT IN AVT.
* IF ON, PROBABLY BUILT BY IEDQFA.  IF NOT ON, WARMSTART BUILT CPB.
*
OK       DS    0H                       NON-ZERO CPBADDR
         MVC   IOBSEEK,CPBABSAD         SET MBBCCHHR TO IOB
         MVC   IOBSTART,AVTINCPQ+ONE    POINT IOB TO CPB.        X01004
         XC    AVTINCPQ+ONE(ROUNDER),AVTINCPQ+ONE  CLEAR INPUT Q X01004
         XC    IOBXECB,IOBXECB          CLEAR ECB
         LR    R1,RIOB                  PASS IOB ADDR
         EXCPVR (1),SUBSYS              DO DISK I/O.             X01004
*
EXIT     EQU   *
         L     R11,AVTEA
         CLI   AVTHRESS,THRESHF7        DOES ELE NEED POSTING
         USING IEDQDISP,R11
         BNE   DSPDISP                    NO, DO NOT POST
*                                           EXIT TO DISPATCHER
*                                         YES, POST THRESH ELE
         LA    R1,AVTHRESE              PASS ADDR OF THRESH ELE
         MVI   AVTHRESS,THRESHF0        MARK ELEMENT 'POSTED'
         BAL   R14,DSPPOST              EXIT TO DISPATCHER     @Y17XAQX
*
*                        LOCAL CONSTANTS                         S21101
         DS    0F                       R0 PARAM TO SET SECTOR   S21101
*                                         ROUTINE                S21101
ZEROPARM DC    H'6'                     DATALENGTH OF DISK RECORDS21101
HALF8    DC    H'8'                     ADJUSTS FOR MISSING CCW  S21101
         EJECT ,                                                 99226
*                                                                99226
* THE WTORTN IS A WTO MESSAGE WRITTEN AT DISK ERP TIME.          99226
*   IT IS CALLED UPON TO SEND A MESSAGE TO THE OPERATOR BY       99226
*   IEDQFQ, IGG019Q8,IGG019R6                                    99226
*                                                                99226
* A CHECK IS ALSO MADE FOR THE TYPE OF DISK ERROR.               99226
*   IF IT IS A DISK WRITE ERROR, A QUICK CLOSEDOWN IS REQUESTED. 99226
*   IF IT IS A DISK READ ERROR, AN ABEND IS ISSUED.              99226
*                                                                99226
*   ENTRY HERE IS THROUGH A BRANCH TO THE ADDRESS IN THE         99226
*          SECOND WORD OF IGG019RF                               99226
*                                                                99226
*   INPUT--R13 HAS AVTSAVE2                                      99226
*          R7 HAS CPB BASE                                       99226
*          R14 HAS RETURN ADDRESS                                99226
*          R15 HAS ENTRY POINT                                   99226
*                                                                99226
*   OUTPUT-ALL REGISTERS ARE UNCHANGED                           99226
*                                                                99226
*   EXTERNAL ROUTINES--DSPPOSTR--PUTS CLOSEDOWN ELEMENT ON       99226
*          READY QUEUE IN CASE OF DISK WRITE ERRORS              99226
*                                                                99226
*   EXITS-NORMAL - N/A                                           99226
*                                                                99226
*   EXITS-ERROR                                                  99226
*          AVTABEND--TO ABEND S045 U008 DISK READ ERROR          99226
*                                                                99226
*          RETURN TO CALLING PROGRAM TO AWAIT CLOSEDOWN ON       99226
*          DISK WRITE ERRORS                                     99226
*                                                                99226
*                                                                99226
*                                                                99226
WTORTN   DS    0H                                                99226
         STM   R14,R12,12(R13)                                   99226
         BALR  RBASE,0                  ESTABLISH                99226
         USING *,RBASE                  ADDRESSABILITY           99226
         USING IEDQCPB,RCPB                                      99226
*        USING IEDQAVTD,RAVT                                     99226
*        USING IEDQPRF,RPREFIX                                   99226
         DROP  RIOB                                              99226
         USING IEDQDISP,R11                                      99226
         UNPK  MSGECBCC(3),CPBECBCC(2)  GET COMPLETION CODE      99226
         UNPK  MSGFLAG1(9),CPBFLAG1(5)  GET FLAG1-2,SENS0-1      99226
         UNPK  MSGCSW(9),CPBFLAG3(5)    GET FLAG3 AND HALF CSW   99226
         UNPK  MSGCSW+8(9),CPBCSW+3(5)  GET LAST HALF CSW        99226
         MVC   MSGUCBID(3),CPBUCBID     GET UCB IDENTIFICATION   99226
         UNPK  MSGADDR(7),CPBADDR+1(4)  GET DISK ADDRESS         99226
         TR    MSGECBCC(46),TRTABLE     MAKE IT PRINTABLE        99226
         MVC   MSGECBCC+2(2),COMBLNK    SEPARATE WITH FIELD      99226
         MVC   MSGFLAG1+8(2),COMBLNK      SEPARATORS, A COMMA    99226
         MVC   MSGCSW+16(2),COMBLNK       AND A BLANK            99226
         MVC   MSGUCBID+3(2),COMBLNK                             99226
         MVI   MSGADDR+6,AVTEPER        PUT PERIOD AT END        99226
         TM    CPBRDWR,CPBWRITB         IS CPB FOR WRITE         99226
         BZ    BADREAD                    NO, READ               99226
*                                         YES, WRITE             99226
         MVC   MSGRDWR(3),WR            SET TO WRITE             99226
*                                                                99226
* REQUEST A CLOSEDOWN IF NOT ALREADY REQUESTED                   99226
*                                                                99226
         CLI   AVTHRESS,THRESHFF        REQUEST FOR CLOSEDOWN YET99226
         BNE   DISKERR                    YES, CONTINUE          99226
*                                         NO,REQUEST IT          99226
         LA    R1,AVTHRESE              PASS ADDRESS OF CLOSEDOWN99226
*                                         ELEMENT TO DISPATCHER  99226
*                                         TO BE POSTED TO QUICK  99226
*                                         CLOSEDOWN              99226
         MVI   AVTHRESE+CLOSETYP,QUICK  SET CLOSE AS QUICK       99226
         MVI   AVTHRESS,THRESHF0        MARK ELEMENT POSTED      99226
         L     R11,AVTEA                GET DISPATCHER           99226
         LR    R5,R7                    SAVE CPB BASE            99226
         BAL   R14,DSPPOSTR             GO TO DISPATCHER         99226
*                                                                99226
         LR    R7,R5                    RESTORE CPB BASE         99226
         B     DISKERR                  GO TO WTO                99226
*                                                                99226
BADREAD  EQU   *                                                 99226
         MVC   MSGRDWR(3),RD            SET TO READ              99226
*                                                                99226
DISKERR  EQU   *                                                 99226
         CLI   CNT,MSGCNT               TEST COUNT EXCEEDED      99226
         BNL   RETURNX                    IF YES,SKIP WTO        99226
*                                         IF NO, WRITE MSG       99226
         SR    R3,R3                    INCREMENT WTO COUNT      99226
         IC    R3,CNT                                            99226
         LA    R3,1(,R3)                                         99226
         STC   R3,CNT                                            99226
         WTO   MF=(E,MSGNAME)           DISPLAY ERROR MESSAGE    99226
*                                                                99226
         CLI   CNT,MSGCNT               TEST LAST OF COUNT       99226
         BL    RETURNX                    IF NO, SKIP LAST MSG   99226
*                                         IF LAST, WRITE SUCH MSG99226
         WTO   MF=(E,MSGNAME2)          PRINT STOPPED MSG        99226
RETURNX  DS    0H                                                99226
         TM    CPBRDWR,CPBWRITB         IS CPB FOR WRITE         99226
         BNZ   RETURN                     YES, RETURN            99226
*                                         NO, ABEND              99226
         LA    R15,ABCODE               SET ABEND CODE           99226
         BAL   R14,AVTABEND             ABEND                    99226
*                                                                99226
RETURN   RETURN (14,12)                 RETURN TO CALLER         99226
*                                                                99226
CLOSETYP EQU   8                        CLOSEDOWN TYPE           99226
QUICK    EQU   X'30'                    QUICK CLOSE IN CLOSEDOWN 99226
CNT      DC    X'00'                    COUNTER FOR DISK WTOS    99226
MSGCNT   EQU   X'05'                    DISK WTO MAX             99226
ABCODE   EQU   X'08'                    DISK READ ERROR CODE     99226
RD       DC    CL3'RD '                 READ IDENTIFIER          99226
WR       DC    CL3'WR '                 WRITE IDENTIFIER         99226
EXCLVAL  EQU   X'01'                    REVERSE BIT              99226
COMBLNK  DC    CL2', '                  FIELD SEPARATOR          99226
*                                                                99226
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
IEDQFP   DS    0H
*                                                                     *
*TITLE   'IEDQFP' - ABSOLUTE RECORD NUMBER TO MBBCCHHR CONVERTER      *
*                                                                     *
*STATUS - CHANGE LEVEL 0                                              *
*                                                                     *
*FUNCTION/OPERATION - THE PURPOSE OF THIS ROUTINE IS TO CONVERT       *
*   THE ABSOLUTE RECORD NUMBER INTO AN MBBCCHHR DISK ADDRESS FOR      *
*   A MULTIVOLUME TCAM DISK MESSAGE QUEUE.                       S22025
*                                                                     *
*   THE DISK MESSAGE QUEUE CONSISTS OF ONE OR MORE SIMILAR EXTENTS    *
*   OF ONE FIXED, UNBLOCKED DATA SET, ONE ON EACH OF ONE OR MORE      *
*   DISK DRIVES OF THE SAME TYPE.  EACH EXTENT MUST CONTAIN THE       *
*   SAME NUMBER OF CONTINUOUS CYLINDERS ON A CYLINDER BOUNDARY.       *
*                                                                     *
*   THE RECORDS ARE ASSIGNED TO THE DISKS IN THE FOLLOWING ORDER -    *
*   SEQUENTIALLY ON THE FIRST TRACK ON THE FIRST CYLINDER OF THE      *
*   FIRST EXTENT.  THEN SEQUENTIALY ON THE FIRST TRACK OF THE         *
*   FIRST CYLINDER ON THE SECOND EXTENT, AND SO ON UNTIL THE FIRST    *
*   TRACK OF ALL EXTENTS ARE ASSIGNED.  THEN CONTINUE BACK ON THE     *
*   FIRST EXTENT WITH THE SECOND TRACK OF THE FIRST CYLINDER. THEN    *
*   THE SAME FOR THE SECOND EXTENT, AND SO ON.  THIS PROCESS IS       *
*   REPEATED FOR THE REST OF THE FIRST CYLINDER OF ALL EXTENTS.       *
*   PROCEED TO THE SECOND CYLINDER, AND CONTINUE THRU THE DATA SET.   *
*   THIS ALGORITHM IS DESIGNED FOR USE WITH MULTIPLE ARM SUPPORT.     *
*                                                                     *
*ENTRY POINTS -                                                       *
*        'IEDQFP - CONVERT ABSOLUTE RECORD NUMBER TO MBBCCHHR         *
*   CALLING SEQUENCE IS                                               *
*        LA    R15,IEDQFP                                             *
*        BALR  R14,R15                                                *
*                                                                     *
*INPUT - RCPB HAS THE ADDR OF THE CPB WHICH CONTAINS IN THE CPBADDR   *
*   FIELD THE ABSOLUTE RECORD NUMBER TO BE CONVERTED.  REGISTER 13    *
*   POINTS TO AVTSAVE2. R15 HAS ENTRY ADDRESS.R14 HAS EXIT ADDRESS    *
*                                                                     *
*   THE AVT HAS TWO 6 WORD AREAS INITIALIZED TO HAVE THE FOLLOWING    *
*   FULLWORD VALUES.  A BIT IN CPBFLAG DETERMINES WHICH 6 WORD AREA   *
*   IS TO BE USED.  (ONE GROUP IS FOR REUSABLE DISK, THE OTHER FOR    *
*   NON-REUSABLE DISK QUEUES.)                                        *
*                                                                     *
*        AVTDQDEB DC F'0' ADDRESS OF DEB OF DISK QUEUE DCB            *
*        AVTNOVOL DC F'0' NUMBER OF EXTENTS                           *
*        AVTRCTRK DC F'0' NUMBER OF RECORDS PER TRACK                 *
*        AVTTRCYL DC F'0' NUMBER OF TRACKS PER CYLINDER               *
*        AVTTOTNO DC F'0' NUMBER OF RECORDS IN ENTIRE DATA SET        *
*        AVTVOLRT DC F'0' PRODUCT OF NO. OF VOLUMES * NO. RECORDS     *
*                          PER TRACK                                  *
*                                                                     *
*OUTPUT - ALL REGISTERS ARE UNCHANGED.  IN THE CPB, THE               *
*   CPBABSAD FIELD OF THE CPB NOW CONTAINS THE MBBCCHHR.  ALL OTHER   *
*   FIELDS, INCLUDING THE INPUT FIELD, CPBADDR, ARE UNCHANGED,        *
*   EXCEPT THE COMMAND CHAIN BIT IN SECOND READ/WRITE CCW IS          *
*   TURNED OFF.                                                       *
*                                                                     *
*EXTERNAL REFERENCES - THE ABSOLUTE CYLINDER ID COMES FROM DEB EXTENT *
*                                                                     *
*   THE DSECT OF THE AVT IS TO BE PROVIDED BY THE MACRO, 'AVT'.       *
*                                                                     *
*   THE ROUTINE MAKES NO CALL TO ANY EXTERNAL SUBROUTINE.  THUS IT    *
*   DOES NOT CHANGE THE VALUE IN R13, HAVING NO SAVEAREA OF ITS OWN.  *
*                                                                     *
*EXITS-NORMAL - ALL REGISTERS REMAIN UNCHANGED.  NO RETURN CODE.      *
*   CPBABSAD FIELD OF THE CPB NOW CONTAINS THE REQUIRED MBBCCHHR.     *
*   RETURN IS MADE TO THE ADDRESS IN R14                              *
*                                                                     *
*EXITS-ERROR  --  ABEND CODE S045 U001 - NON-REUS DISK WRAP           *
*   ABEND CODE S045 U005 - CPB REQUESTING I/O ON UNOPENED DATA SET    *
*                                                                     *
*TABLES/WORKAREAS - ALL LOCAL STORAGE IS IN REGISTERS. REFERENCE      *
*   IS MADE TO THE AREAS OF THE AVT DESCRIBED UNDER 'INPUT' ABOVE.    *
*   IN THE CPB, THE FIELD CPBTTR IS REFERENCED, AND THE FIELD         *
*   CPBABSAD MODIFIED.                                                *
*                                                                     *
*ATTRIBUTES - REUSABLE, REFRESHABLE                                   *
*                                                                     *
*NOTES - THE OPERATION OF THIS SUBROUTINE DOES NOT DEPEND UPON A      *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   A MESSAGE IS DISPLAYED WITH 'WTO',ROUTCDE=(2,11),DESC=6           *
*   'IED076I TCAM NON-REUSABLE DISK THRESHOLD CLOSEDOWN' - THE SYSTEM *
*   IS CLOSING DOWN DUE TO AN IMPENDING FULLNESS OF THE DISK.         *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                        REGISTER USAGE DEFINITIONS
*
*15      EQU   15                       PROGRAM BASE
*14      EQU   14                       RETURN ADDRESS
*13      EQU   13                       CALLER'S SAVEAREA
*12      EQU   12
RVOLRT   EQU   12                       PRODUCT OF NO. OF VOLUMES
*                                         TIMES NO. RECORDS PER TRK
RTOTNO   EQU   11                       TOTAL NO. OF RECORDS IN
*                                         ENTIRE DATA SET
RTRCYL   EQU   10                       NO. TRACKS PER CYLINDER
RRCTRK   EQU   9                        NO. RECORDS PER TRACK
RNOVOL   EQU   8                        NO. OF EXTENTS
RDEBX    EQU   7                        ADDR OF DEB OF DISK Q
*7       EQU   7                        7-12 LOADED FROM AVT
RCPBX    EQU   6                        BASE OF CPB DSECT
*5       EQU   5                        UNUSED IN CONVERT ROUTINE
*4       EQU   4                        UNUSED IN CONVERT ROUTINE
*3       EQU   3                        2-3 EVEN ODD PAIR FOR DIVISION
*2       EQU   2                        2-3 EVEN ODD PAIR FOR DIVISION
*1       EQU   1                        1-0 EVEN ODD PAIR FOR DIVISION
*0       EQU   0                        1-0 EVEN ODD PAIR FOR DIVISION
*
         DROP  RCPB
         DROP  RDEB
         USING *,R15                    PROGRAM BASE
         USING IEDQCPB,RCPBX            BASE OF CPB DSECT
         USING IEDQDEBX,RDEBX           BASE OF DEB EXTENT DSECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                        ENTRY POINT
*
         STM   R14,R12,HISREG14(R13)    SAVE REGISTERS
         LA    RCPBX,0(,RCPB)           CLEAR HIGH BYTE AND
*                                         SET CPB DSECT BASE
         L     R1,CPBADDR               GET ABS. REC. NO. FROM CPB
         LA    R1,ROUNDER(R1)           ADJUST THE               S21101
         SRL   R1,DIV4                    RELATIVE RECORD NO.    S21101
         TM    CPBADDR+LORDER,CPBQTYPE  IS CPB FOR REUSABLE DISK S21101
         BO    PEUSDISK                   YES, REUSABLE DISK
*                                         NO, NON-REUSABLE
         LM    R7,R12,AVTADEBN          GET VALUES FROM AVT
         CLM   R1,ALL,AVTHRESN          DOES THIS RECORD EXCEED  X01004
*                                         THE THRESHOLD VALUE    X01004
         BH    EXCEED                     YES, THRESHOLD EXCEEDED
*                                         NO, WITHIN LIMITS OK
*
* ABSOLUTE RECORD NUMBER / TOTAL NO OF RECORDS GIVES
* QUOTIENT - IGNORED
* REMAINDER- RELATIVE RECORD NUMBER
CONTINUE EQU   *
         LTR   RTOTNO,RTOTNO            HAS OPEN DONE THIS TYPE OF
*                                         DISK MSG Q
         BZ    NOTOPEN                    NO, DATA SET NOT OPEN
*                                         YES, IT IS OPEN OK
         SR    R0,R0                    CLEAR EVEN REG FOR DIVISION
         DR    R1-1,RTOTNO
         TM    CPBADDR+LORDER,CPBQTYPE  IS CPB FOR REUSABLE DISK S21101
         BO    NOTWRAP                    YES, REUSABLE DISK
*                                         NO, NON-REUSABLE DISK
         LTR   R1,R1                    HAS ENTIRE DATA BEEN USED
         BZ    NOTWRAP                    NO, NOT WRAPPED
*                                         YES, NON-REUSABLE DISK
*                                           IS TRYING TO WRAP
         LA    R15,ABEND1               PASS SUBCODE IN R15
         BAL   R14,AVTABEND             ABEND, OUT OF SPACE NON REUS
*
NOTOPEN  EQU   *
         LA    R15,ABEND5               PASS SUBCODE IN R15
         BAL   R14,AVTABEND             ABEND, DISK DCB NOT OPEN
*
NOTWRAP  EQU   *
         SRDA  R0,FULLSHIF              MOVE REMAINDER TO ODD REG
*
* RELATIVE RECORD NUMBER / PRODUCT OF NO. VOLS AND NO. REC PER TRK
* QUOTIENT - RELATIVE TRACK
* REMAINDER - RELATIVE RECORD NO. ON THIS ROW OF TRACKS
*
         DR    R1-1,RVOLRT
*
* R1 HAS RELATIVE TRACK, R0 HAS RELATIVE RECORD NO. ON THIS ROW
*
         LR    R3,R1                    GET RELATIVE TRACK
         SR    R2,R2                    CLEAR EVEN REG FOR DIVISION
*
* RELATIVE TRACK / NO. TRACKS PER CYL GIVES
* QUOTIENT - RELATIVE CYLINDER NO.
* REMAINDER- RELATIVE TRACK NO.
*
         DR    R3-1,RTRCYL
*
* R3 HAS RELATIVE CYLINDER NO, R2 HAS RELATIVE TRACK NO.
*
* RELATIVE RECORD NO. ON THIS ROW OF TRACKS / NO RECORDS PER TRACK
* QUOTIENT - RELATIVE DRIVE NUMBER
* REMAINDER- RELATIVE RECORD NUMBER
*
         SRDA  R0,FULLSHIF              CLEAR EVEN REG.
         DR    R1-1,RRCTRK
*
* R1 HAS RELATIVE DRIVE NUMBER, R0 HAS RELATIVE RECORD NUMBER
*
         XR    R1,R0                    SWAP REGISTERS R0, R1
         XR    R0,R1
         XR    R1,R0
*
* R0 HAS RELATIVE DRIVE NUMBER, R1 HAS RELATIVE RECORD NUMBER
*
         STC   R0,CPBABSAD              SET M OF MBBCCHHR
         XC    CPBABSAD+ONE(TWO),CPBABSAD+ONE  SET BB TO ZERO    X01004
         LA    R1,1(R1)                 MAKE REC NO. REL. TO 1
         STC   R1,CPBABSAD+SEVEN        SET R OF MBBCCHHR.       X01004
         STC   R2,CPBABSAD+6            GET REL TRACK NO.
         SRL   R2,BYTESHIF
         STC   R2,CPBABSAD+5
*
         LA    R1,DEBEXTLM              GET SIZE OF EACH DEB EXTENT
         MR    R1-1,R0                  MULT BY RELATIVE DRIVE NO.
         LA    RDEBX,DEBEXTPT(RDEBX,R1) BUMP DEB PT TO PROPER EXT
         AH    R3,DEBSTRCC              GET ABSOLUTE CYLINDER ADDR
         STC   R3,CPBABSAD+4              INTO MBBCCHHR
         SRL   R3,BYTESHIF
         STC   R3,CPBABSAD+3
         LM    R14,R12,HISREG14(R13)    RESTORE REGISTERS
         BR    R14                      RETURN TO MAIN ROUTINE
*
*
PEUSDISK EQU   *
         LM    R7,R12,AVTADEBR          GET VALUES FROM AVT
         B     CONTINUE                 GO TO SET UP MBBCCHHR    S21903
*
EXCEED   EQU   *
         TM    AVTBIT1,AVTREADN         IS CHECKPOINT OPEN DOING
*                                         THIS CPB (OR, HAS READY NOT
*                                         BEEN EXECUTED YET)
         BZ    CONTINUE                   YES, CKPOINT,IGNORE THRESHOLD
*                                         NO, USUAL DISK TRAFFIC
         CLI   AVTHRESS,THRESHFF        HAS CLOSEDOWN BEEN REQUESTED
         BNE   CONTINUE                   YES, IGNORE THRESHOLD
*                                         NO, REQUEST THRESHOLD
         MVI   AVTHRESS,THRESHF7        MARK IT AS REQUESTED
         LR    R3,R1                    SAVE VALUE OF ADDR
*
         WTO   MF=(E,IED076I)           THRESHOLD CLOSEDOWN MSG
*
         L     R15,HISREG15(,R13)       RELOAD PROGRAM BASE
         LR    R1,R3                    RESTORE VALUE OF ADDR
         B     CONTINUE                 GO TO SET UP MBBCCHHR    S21903
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                       WRITE - TO - OPERATOR
*
IED076I  WTO   'IED076I TCAM NON-REUSABLE DISK THRESHOLD CLOSEDOWN',   *
               ROUTCDE=(2,11),DESC=4,MF=L
         ENTRY IED076I
IED140I  WTO   'IED140I TCAM DISK ERROR 41, F1F2S0S1, F3CSWCSWCSWCSWCS,*
               UCB, RD ADDRESS',ROUTCDE=(2,10,11),DESC=4,MF=L   OX02204
MSGNAME  EQU   IED140I                                          OX02204
MSGECBCC EQU   MSGNAME+28               2 BYTE ECB CODE          99226
MSGFLAG1 EQU   MSGNAME+32               8 BYTE FLAG1-2,SENS0-1   99226
MSGCSW   EQU   MSGNAME+42               16 BYTE FLAG3 AND CSW    99226
MSGUCBID EQU   MSGNAME+60               3 BYTE UCBID             99226
MSGRDWR  EQU   MSGNAME+65               2 BYTE TYPE OPERATION    99226
MSGADDR  EQU   MSGNAME+68               6 BYTE DISK ADDR         99226
         ENTRY IED140I                                          OX02204
IED139I  WTO   'IED139I PRINTING STOPPED',                       99226 *
               ROUTCDE=(2,10,11),DESC=4,MF=L                     99226
MSGNAME2 EQU   IED139I                                           99226
         ENTRY IED139I                                           99226
TRTABLE  EQU   *-239                    TRANSLATE TABLE          99226
         DC    C' 0123456789ABCDEF'     TRANSLATE TABLE          99226
*                                                                99226
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                        DSECTS
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
IEDQDEBX DSECT
DEBEXTPT EQU   68                       POINTER TO FIRST DEB EXTENT
DEBEXTLM EQU   16                       LENGTH OF EACH EXTENT
DEBDVMO  DS    0C                       DEVICE MODIFIER
DEBUCBA  DS    A                        ADDRESS OF UCB
         DS    2C                       RESERVED
DEBSTRCC DS    CL2                      CYLINDER START ADDRESS
DEBSTRHH DS    CL2                      READ OR WRITE TRACK START ADDR
DEBENDCC DS    CL2                      CYLINDER END ADDRESS
DEBENDHH DS    CL2                      READ OR WRITE TRACK END ADDR
DEBNMTRK DS    H                        NUMBER OF TRACKS ALLOCATED
*                                         IN THIS EXTENT
*
         TCPBD 3330                                              S21101
*                                                                99226
* IN DISK ERROR CONDITIONS, DISK APPENDAGE PASSE                 99226
* ERROR STATUS INFORMATION IN THE CPB AS FOLLOWS                 99226
*                                                                99226
         ORG   CPBSEEK                  REDEFINE FROM CPB START  99226
         DS    A                        RESERVED FOR IEDQFQ      99226
CPBFLAG3 DS    C                        FROM IOBFLAG3            99226
CPBCSW   DS    CL7                      FORM IOBCSW              99226
CPBFLAG1 DS    C                        FORM IOBFLAG1            99226
CPBFLAG2 DS    C                        FORM IOBFLAG2            99226
CPBSENS0 DS    C                        FORM IOBSENS0            99226
CPBSENS1 DS    C                        FROM IOBSENS1            99226
CPBECBCC DS    C                        FORM IOBECBCC            99226
CPBUCBID DS    CL3                      FROM UCBNAME             99226
*                                                                99226
CVT      DSECT                                                   S21101
         CVT   RPS=YES                                           S21101
UCB      DSECT                                                   S21101
         IEFUCBOB                                                S21101
         DCBD  DSORG=TQ                                          S21101
         TIOBD
         TDEBD
         TDATAD                                                  99226
         TPRFD                                                   99226
         TDISPD
         TAVTD 2
         END
