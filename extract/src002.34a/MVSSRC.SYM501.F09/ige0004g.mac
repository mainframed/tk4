004G     TITLE ' IGE0004G - TCAM/EXCP START-STOP ERP CONTROL MODULE '
IGE0004G CSECT
         SPACE 3
*CHANGE ACTIVITY= AS FOLLOWS:                                    S21903
*A135000,595000                                                 SA51090
*C252000-253000,256000-257000,259000                            SA51090
*A375000,399000                                                 SA55381
*C372000                                                        SA55381
*D298000,348000-350000,376000-396000,411000,479000              SA55381
*D538000-539000,541000,543000,549000-550000,556000-557000       SA55381
*D562000,566000-569000,575000-576000,578000-581000,588000       SA55381
*C354000                                                         A42406
*A116000,137000,144000,150000,347100,453100,605000               S21903
*C378000,477000,515000                                           S21903
*D139000,298000,400000-401000,479000,518000-519000,538000-539000 S21903
*D543000,549000-550000,552000-553000,556000-557000,562000        S21903
*D566000-569000,575000-576000,578000-581000,588000,592000        S21903
*A595100-595500                                                  S22025
*C005000,232000-234000,271000,340000                             S22025
*D353140-353770,354200,354800                                    S22025
*A595100-595500                                                  S22025
*C005000,232000-234000,271000,340000                             S22025
*D353140-353770,354200,354800                                    S22025
*C465500                                                        SA58994
*C159000,165000-166000                                          SA58998
*C166200                                                        SA61774
*D159000                                                        SA61774
*A138000                                                        SA62387
*C112000                                                        SA62387
*A127000,534000,597000                                           X02004
*C120000-121000,123000-124000,131000,135000-138000,144100-144700,X02004
*C605600-605800                                                  X02004
*D117000,140000                                                  X02004
*A305500                                                        SA68236
*C144790-144811,144850                                         @ZA00227
*A334000-350000,605900                                         @ZA00227
*A1500000                                                      @SA72456
*C362000,C372000,C376000,C399400                               @OS76278
*C200000,C561000                                               @OZ14171
* A120000,124000,135000,144100,144300,144400                   @G36XREV
* C007000,144150,144350                                        @G36XREV
* D142230,144050,144250,144802                                 @G36XREV
*
         SPACE 3
***********************************************************************
*                                                                     *
*  MODULE NAME = IGE0004G                                             *
*                                                                     *
*  DESCRIPTIVE NAME = TCAM START/STOP ERP  ( FIRST LOAD )             *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*STATUS:  CHANGE LEVEL 10                                      @G36XREV
*                                                                     *
*FUNCTION -- ALL ERP MLDULES ATTEMPT TO RECOVER FROM ALL ERRORS.      *
*   CERTAIN TEXT ERRORS REQUIRE RETURN TO LINE END TO SCHEDULE A      *
*   SUBSEQUENT RECALL OPERATION.                                      *
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
*EXTERNAL ROUTINES -- 'IEDQTNT' - TO GET TERMINAL ENTRY ADDRESS       *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'ERP IN CONTROL (IOSERR)' AND 'EXCEPTIONAL   Y02027
*                CONDITION(IOSEX)'. RETURN TO IOS TO RETRY THE   Y02027
*                   ERROR.                                       Y02027
*        SVC   3                                                      *
*                                                                     *
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*        SVC   3                                                      *
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO I/O SUPERVISOR BLOCK                Y02027
*        SVC   15   'EXCEPTIONAL CONDITION(IOSEX)'. RETURN TO    Y02027
*                   LINE END APPENDAGE. ERROR PERMANENT.         Y02027
*        SVC   3                                                      *
*                                                                     *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*   TO SCHEDULE ERROR POST                                            *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TTRMD                                                             *
*   TCCWD                                                             *
*   TLCBD                                                             *
*   DCBD                                                              *
*   TSCBD                                                             *
*   IECDIOSB                                                     Y02027
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
*   EXIT FROM ERP IS MADE WHENEVER THE RETRY COUNT IS EXHAUSTED, OR   *
*   A SHOULD NOT OCCUR ERROR IS PRESETNED.  TEXT ERRORS NOT           *
*   RECOVERABLE BY ERP ARE POSTED TO LINE END APPENDAGE WITH ERROR    *
*   (VIA SVC 15).                                                     *
*                                                                     *
*   IF ANOTHER ERP LOAD IS PERFORMING INTERMITTENT ERP RECOVERY, EXIT *
*   IS MADE TO THE SPECIAL RETURN ERP LOADS.  THIS IS INDICATED BY A  *
*   NON-ZERO VALUE IN LCBINCAM+1.  RETRY COUNT IS MAINTAINED IN       *
*   LCBINCAM.                                                         *
*                                                                     *
*   ON FIRST OCCURRENCE OF AN ERROR, THE CSW, SENSE BYTE, AND TP OP   *
*   CODE ARE SAVED IN LCBCSWSV, LCBSENS1, AND LCBRESTR+1              *
*   RESPECTIVELY.  IF THE ERROR IS CLEARED THROUGH ERP ACTION, THE    *
*   TEMPORARY ERROR COUNTER IN THE TERMINAL ENTRY IS UPDATED.  FOR    *
*   2740/2, THE LEADING GRAPHIC RESPONSE IS SAVED IN LCBSENS1.        *
*                                                                     *
*   THE USER HAS THE OPTION TO RECORD THESE INTERMITTENT ERRORS VIA   *
*   THE OPERATOR CONTROL FACILITY WHCIH SETS THE MASK IN LCBERMSK     *
*   (LINE BASIS) OR TRMSENSE (TERMINAL BASIS).  LINE MASK TAKES       *
*   PRECEDENCE OVER TERMINAL MASK.  THE LOW ORDER 4 BITS INDICATE     *
*   THE NUMBER OF RECORDINGS TO BE PERFORMED FOR THIS TYPE OF ERROR.  *
*   (THE COUNT IS DECREMENTED FOR EACH RECORDING.)                    *
*                                                                     *
*   THE RECORDING MASK IS AS FOLLOWS:                                 *
*   X'10' - TIME OUT                                                  *
*   X'20' - LOST DATA                                                 *
*   X'30' - OVERRUN                                                   *
*   X'40' - DATA CHECK                                                *
*   X'50' - EQUIPMENT CHECK                                           *
*   X'60' - BUSOUT CHECK                                              *
*   X'70' - INTERVENTION REQUIRED                                     *
*   X'80' - COMMAND REJECT                                            *
*   X'90' - UNIT EXCEPTION                                            *
*   X'F0' - INTENSIVE MODE (ANY OF ABOVE)                             *
*   X'A0' - 2740/2 GRAPHIC RESPONSE                                   *
*   PARAMETERS PASSED TO OBR/SDR RECORDING MODULE:                    *
*   LCBERCCW+16 - 1 BYTE INDICATES TYPE OF RECORDING                  *
*      X'00' - OBR                                                    *
*      X'80' - RECORD INTERMITTENT ERROR                              *
*      X'40' - OVERFLOW OF SIO COUNTER OR TEMPORARY ERROR COUNTER     *
*      X'20' - CLOSE DOWN RECORDING                                   *
*   LCBERCCW+17 - 3 BYTES ADDRESS OF TERMINAL COUNTERS                *
*   LCBERCCW+20 - 1 BYTE - SIZE OF TERMINAL NAME                      *
*   LCBERCCW+21 - 3 BYTES - ADDRESS OF TERMINAL NAME                  *
*                                                                     *
*   NOTE THAT ANY CHANGES IN THE OFFSET FROM LCBFLAG1 TO LCBERCCW+16  *
*   MUST BE PRESENTED TO THE OWNERS OF OBR/SDR MODULES.               *
***********************************************************************
         EJECT
         USING IEDQCCW,RCCW
         USING IEDQLCB,RLCB                                     SA62387
         USING IHADCB,RDCB
         USING IEDQAVTD,RAVT
         USING IEDQSCB,RSCB
         USING IEDQTRM,RTERM
         USING IOSB,RIOSB                                        Y02027
         USING CVTDSECT,RCVT                                     Y02027
         USING EWA,RERPWA                                        Y02027
*              REGISTERS
R0       EQU   0
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLK    Y02027
RPARM    EQU   1                        PARAMETER REG          @G36XREV
RLCB     EQU   2                        LCB ADDRESS              X02004
RUCB     EQU   3                        UCB REGISTER
RDEB     EQU   3                        DEB ADDRESS              X02004
RDCB     EQU   4                        DCB ADDRESS              X02004
RA       EQU   5                       WORK REGISTER             X02004
R5       EQU   5                        WORK REG               @G36XREV
RCVT     EQU   5                        ADDR OF CVT              Y02027
RWKA     EQU   6                        WORK REGISTER
RWKB     EQU   7                        WORK REGISTER
RTST     EQU   7                         ADDRESS OF RQE          Y02027
RTERM    EQU   8                        TERMINAL BASE
*                                       IN CCW CONVERT SUB-ROUT  X02004
RCCW2    EQU   8                        SECOND CCW ADDRESS.      X02004
RWKC     EQU   9                        WORK REGISTER
RCCW     EQU   10                       CCQW BASE
RAVT     EQU   11                       AVT BASE
RBASE    EQU   12                       BASE REGISTER.           X02004
RLINK    EQU   13                       LINKAGE FOR SUBSEQUENT LOAD
RSAVE    EQU   13                       SAVE REGISTER FOR IOSB   Y02027
RXCTL    EQU   14                       XCTL REGISTER
RERPWA   EQU   14                       ADDR OF ERP WORK AREA    Y02027
R15      EQU   15                       ADDRESSABILITY REGISTER
RSCB     EQU   15                       SCB REGISTER             X02004
RNTRY    EQU   15                       ENTRY REG@G36XREV
         EJECT
         USING *,R15                    TEMPORARY BASE.          X02004
IGE0004G IEDHJN ID
         L     RTST,IOSUSE               PICKUP ADDR OF RQE      Y02027
         LM    RLCB,RDEB,IOBPTR(RTST)   IOB AND DEB ADDRESSES    Y02027
         L     RDCB,LCBDCBPT-LCBFLAG1(RLCB)   DCB ADDRESS        X02004
         LA    R15,ZERO(RDEB)           COPY DEB ADDRESS         X02004
         LA    RDEB,DEBNMSUB-DEBEOEA    OFFSET TO DEB START      X02004
         SR    R15,RDEB                 ADDRESS OF SIO APPENDAGE X02004
         L     R15,SIOPTR(,R15)         ADDRESS OF SIO APPENDAGE X02004
SETERP   SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=CHANNEL PROGRAM,X
               IGE0004H(SETERP),IGG019QE(SETERP),IEDQGT(SETTRAN),      X
               IEDQGA(SETASSC,SETCONC),'PCI LINEEND,AND START I/O      X
               APPENDAGES ARE LOCKED ON THIS RESOURCE THRU THE EXCP    X
               PROCESSOR ISSUING SETLOCK (ERP,BUFFER ASSOCIATION AND   X
               TRANSPARENT CCW BUILD MUST ISSUE SETLOCK'
         BAL   RBASE,INTENTRY(R15)      LINK TO SIO APPENDAGE -  X02004
*                                       SECOND ENTRY TO TRANSLATEX02004
*                                       REAL TO VIRTUAL          X02004
         SPACE 1                                                 X02004
         USING *,RBASE                  USE NEW BASE             X02004
         L     RTST,IOSUSE              PICKUP ADDRESS OF RQE    Y02027
         L     RCVT,CVTPTR              PICKUP ADDR OF CVT       Y02027
         L     RAVT,CVTAQAVT            DISPATHER WORK AREA      X02004
         L     RAVT,ZERO(,RAVT)         AVT ADDRESS              X02004
         USING IEDQSCB,RSCB                                      X02004
         SH    RLCB,LCBSIZE             ADJUST BASE             SA62387
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        I/O SUPERVISOR BLOCK TO THE LINE CONTROL BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY I/O OPERATIONS FROM ERP      Y02027
**************************************************************** Y02027
         MVC   LCBCSW(SEVEN),IOSCSW     MAP CSW FROM IOSB TO LCB Y02027
         MVC   LCBSENS0(TWO),IOSSNS     MAP SENSE INFO FROM IOSB Y02027
*                                       TO LCB                   Y02027
         MVC   LCBSIOCC(ONE),IOSCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM IOSB TO LCB    Y02027
         LR    RWKA,RBASE               SAVE BASE OVER SETLOCK @ZM46665
         LA    R0,LOAD904G              SET BRANCH REGISTER      Y02027
         CLI   LCBINCAM+1,AVTEFF        CLOSE DOWN ENTRY
         BE    NOTRACE                  BRANCH YES               Y02027
         LA    R0,HIORET                SET BRANCH REGISTER      Y02027
         CLI   LCBECBCC,HIO             WAS HIO DONE
         BE    NOTRACE                  BRANCH YES               Y02027
         LA    R0,CONTINUE              SET BRANCH REGISTER      Y02027
         TM    IOSFLA,IOSERR            ERP ALREADY IN CONTROL   Y02027
         BNO   NOTRACE                  BRANCH NOT ERP           Y02027
         LA    R0,SPECIAL               SET BRANCH REGISTER      Y02027
         SLR   RNTRY,RNTRY              CLEAR, HI BYTE 0 FOR Q0@G36XREV
         ICM   RNTRY,AD,AVTIOT+1        IS I/O TRACE ACTIVE    @G36XREV
         BZ    NOTRACE                  BRANCH NO                Y02027
         OI    LCBFLAG1,IOSERR+IOSEX    SET FOR IGG019Q0         Y02027
         LR    R5,RIOSB                 SAVE IOSB ADDR         @G36XREV
         L     RPARM,IOSUSE             RQE ADDR PARM TO Q0    @G36XREV
         LA    RPARM,0(RPARM)           CLEAR HIGH BYTE        @G36XREV
         BALR  RXCTL,RNTRY              LINK TO IGG019Q0       @G36XREV
         SPACE 1                                                 Y02027
         LR    RIOSB,R5                 RESTORE IOSB BASE      @G36XREV
         NI    LCBFLAG1,X'FF'-(IOSERR+IOSEX) RESET FLAGS         Y02027
         SPACE 1                                                 X02004
NOTRACE  EQU   *                                                 S21903
RESETERP SETLOCK RELEASE,TYPE=LOCAL,RELATED=CHANNEL PROGRAM,           X
               IGE0004G(SETERP)
         LR    RBASE,RWKA               RESTORE BASE AFTER LOCK@ZM46665
         OI    IOSFLA,IOSERR+IOSEX      INDICATE ERP IN CONTROL  Y02027
         L     RCVT,CVTPTR              PICK UP ADDR OF CVT    @ZA00227
         L     RUCB,CVTAQAVT            PICK UP TCX            @ZA00227
         USING IEDQTCX,RUCB                                    @ZA00227
         L     RAVT,TCXAVT              AVT ADDRESS            @ZA00227
         TM    LCBSIOCC,CC3             SIO COMP 3             @ZA00227
         BO    PATHAVAL                 YES, CHECK ALT. PATH   @ZA00227
NOPATH   EQU   *                                               @ZA00227
         SR    RLINK,RLINK              SET FOR INDEXING         Y02027
         L     RUCB,IOSUCB              UCB ADDRESS            @ZA00227
         LR    R15,R0                   SET BRANCH REGISTER      Y02027
         BR    R15                      TO PROPER ROUTINE        Y02027
SPECIAL  EQU   *                                                 Y02027
         SPACE
         TM    LCBINCAM+1,AVTEFF        ANY SPECIAL RETURNS
         BNZ   LOAD504G                 READ SKIP AND WRITE BREAK
         SPACE
CONTINUE EQU   *
         MVI   LCBINCAM+1,AVTEZERO      ZERO RETURN INDICATOR  @SA72456
         L     RERPWA,IOSERP            PICKUP ADDR OF ERP WORK  Y02027
*                                       AREA                     Y02027
         L     RSCB,LCBSCBA-1           SCB BASE                 S21903
         TM    LCBSIOCC,CC3             SIO COMP CODE 3
         BO    LOAD204G                 YES-CONTROL UNIT NOT UP
         SPACE
         BZ    CHECKCAT                 BRANCH CC=0 CONTINE TESTING
         SPACE
         OI    EWTCFLG,EWTCSEL          INDICATE INTIAL          Y02027
*                                       SELECTION ERROR          Y02027
         L     RWKA,LCBSTART-1          START ADDRESS
         LA    RWKA,EIGHT(R0,RWKA)      BUMP BY 8
         STCM  RWKA,SEVEN,LCBCSW         TRUE FAILING CCW + 8    Y02027
CHECKCAT EQU   *                        CATASTROPIC ERRORS
*                                         WTO MODULE
         SR    RCCW,RCCW                 CLEAR FOR ICM           Y02027
         ICM   RCCW,SEVEN,LCBCSW         ADDRESS FAILURE CCW + 8 Y02027
         LA    RWKC,TPTEXT              INDICATE TEXT CCW        S21903
         SH    RCCW,H8                 BACK UP TO FAILURE CCW   SA58998
         BM    TEXTCCW                 ZERO CSW ADDRESS         SA61774
*                                      RCCW (A) MAY GO NEG.     SA61774
*                                      AT THIS POINT WITH       SA61774
*                                      DEVICE END STATUS THIS   SA61774
*                                      IS NORMAL FOR GOOD       SA61774
*                                      COMP AFTER A RESTART     SA61774
*                                      WRITE ON 3270 LOCAL      SA61774
         LA    RWKA,LCBCPA              START OF CHAN PROGRAM AREA
         SR    RWKA,RCCW                INTERRUPT BEFORE LCBCPA
         BP    TEXTCCW                  BRANCH IF TEXT CCW
         SPACE
         SR    RWKB,RWKB                CLEAR WORK REGISTER
         IC    RWKB,DCBEIOBX            SIZE OF AN LCB
         LA    RWKB,ZERO(RLCB,RWKB)     POINT TO END OF LCBCPA
         SR    RWKB,RCCW                INTERRUPT OUT OF LCBCPA
         BNP   TEXTCCW                  BRANCH IF TEXT CCW
         SPACE
         LPR   RWKA,RWKA                MAKE DIFFERENCE POSITIVE
         SRL   RWKA,THREE               DIVIDE BY 8 FOR INDEXING
         IC    RWKC,LCBTPCD(RWKA)       CORRESPONDING TP OP CODE S21903
TEXTCCW  EQU   *
         STC   RWKC,LCBRESTR            TEST CASTROPHIC ERRORS   S21903
         CLI   LCBINCAM,AVTEZERO        RETRY COUNT EQ 0
         BNE   NOT1ST                   NO
         SPACE
         MVC   LCBSNSV,LCBSENS0         SABE SENSE BYTE
         MVC   LCBCSWSV,LCBCSW          SAVE CURRENT CSW
         MVC   LCBRESTR+1(1),LCBRESTR   SAVE FIRST TP OP CODE
         MVC   EWTCCSW(SEVEN),LCBCSW    MOVE CSW INTO THE ERP    Y02027
*                                       WORK AREA                Y02027
         MVC   EWTCCCW(EIGHT),ZERO(RCCW) MOVE CCW INTO THE ERP   Y02027
*                                       WORK AREA                Y02027
NOT1ST   EQU   *
         TM    LCBCSW+4,CONTCHK+INTCHK  SEVERE CHANNEL ERRORS
         BNZ   LOAD804G                 BRANCH IF ANY
         TM    LCBCSW+4,CHDATCHK        CHANNEL DATA CHECK
         BO    UNUSUAL                  YES
         TM    LCBCSW+3,ATSMBY          ATTENTION,STATUS MOD,  @OZ14171
*                                         CONTROL UNIT END, BUSY
         BZ    TRYNEXT                  BRANCH NONE OF THESE
         CLI   TTY3(RUCB),GRAPHICS      LOCAL DEVICE?            S99228
         BNE   UNUSUAL                  BRANCH NO                S99228
         CLI   TTY4(RUCB),LCL3277       3270 DEVICE?             S99228
         BNL   AUDTV                    BRANCH YES               S99228
UNUSUAL  EQU   *
         MVI   LCBINCAM+1,UNUSRET       ST RETURN INDICATOR
         B     LOAD504G                 NEXT LOAD
TRYNEXT  EQU   *                        TEST 2ND STATUS BYTE
         TM    LCBCSW+4,CHCKPROT        CHAINING CECK ,PROT CHECK
         BNZ   UNUSUAL                  YES
         SPACE 2
         TM    LCBCSW+4,PC              PROGRAM CHECK
         BZ    NOPC                     NO
         CLI   CCWOPCDE,CCWTIC          TIC
         BNE   UNUSUAL                  NO
         SPACE
         SH    RCCW,H8                  BACK UP TO TEST CCW
NOPC     EQU   *
         TM    LCBCSW+3,UNITCHK         UNIT CHECK
         BZ    TRYUNEX                  BRANCH NO
         BAL   RWKC,AUDTV               TEST FOR ADIO OR 2260L
         SPACE 2
         CLI   CCWOPCDE,CCWREAD         READ OR WRITE CCW
         BH    TESTPOLL                 FALL THROUGH-READ OR WRITE
         CLI   LCBRESTR,TPRDRESP        RESPONSE TO POLLING
         BNE   NOPOLRSP                 NO
         SPACE
         CLI   LCBCPA+24,CCWPOLL        THIS RESPONSE TO AUTOPOLL
         BE    LOAD404G                 YES
         SPACE
NOPOLRSP EQU   *
         TM    LCBSENS0,EQCKLD          EQUIPMENT CHECK OR       S22025
*                                       LOST DATA ?              S22025
         BNZ   LOAD104G                 BRANCH IF EITHER         S22025
         TM    LCBSENS0,TO              TIME OUT ?               S22025
         BO    LOAD204G                 BRANCH = YES             S22025
         TM    LCBSENS0,BOIR            BUS OUT OR INTERVENTION  S22025
*                                            REQUIRED ?          S22025
         BNZ   LOAD104G                 BRANCH IF EITHER         S22025
         SPACE
         LA    RWKA,LCBCPA+16           ASSUME START AT TOP
         TM    LCBSENS0,DATCHK          DATA CHECK SET
         BZ    TRYOVRN                  NO, TEST FOR OVERRUN
         SPACE
         TM    CCWOPCDE,CCWWRITE        WRITE COMMAND
         BO    PERMEXIT            BRANCH YES
         SPACE
         CLI   LCBRESTR,TPRDSKIP        TEXT RELATED ERROR
         BL    NODCTXT                  BRANCH IF NO
         SPACE
TXTERR   EQU   *
         MVI   LCBINCAM+1,TEXTRTRY      SET 104G OFFSET
         B     LOAD104G                 NEXT LOAD
         SPACE
TESTPOLL EQU   *
         CLI   CCWOPCDE,CCWINHIB        INHIBIT COMMAND
         BE    NOPOLRSP                 BRANCH YES
         SPACE
         CLI   CCWOPCDE,CCWBREAK        BREAK COMMAND
         BE    NOPOLRSP                 BRANH YES
         CLI   CCWOPCDE,CCWPOLL         POLL COMMAND
         BE    LOAD404G                 BRANCH YES NEXT LOAD
         SPACE
         B     LOAD304G                 HANDLE UNIT CHECKS FOR NON
*                                         READ, WRITE, POLL CCW'S
TRYUNEX  EQU   *
         TM    LCBCSW+3,UNEX            UNIT EXCEPTION SET
         BZ    ERRCLEAR                 NO, ERROR IS CLEARED
         CLI   TTY3(RUCB),GRAPHICS      LOCAL DEVICE?            S99228
         BNE   REGUNEX                  BRANCH NO                S99228
         CLI   TTY4(RUCB),LCL3277       3270 LOCAL DEVICE?       S99228
         BNL   AUDTV                    BRANCH YES               S99228
REGUNEX  EQU   *                                                 S99228
         LA    RWKC,ERRCLEAR            SET EXIT ADDRESS        SA51090
         CLI   CCWOPCDE,CCWREAD         IS THIS A READ COMMAND  SA51090
         BE    AUDTEST                  IF SO, IS THIS AUDIO    SA51090
         LA    RWKC,EXAUDTV             SET EXIT ADDRESS
AUDTV    EQU   *
         CLI   DEVCLASS(RUCB),X40       COMMUNICATIONS CLASS    SA51090
         BNE   LOAD604G                 NO, IT IS 2260 LOCAL    SA51090
         SPACE
AUDTEST  EQU   *                                                SA51090
         CLI   UNITYPE(RUCB),ZERO       AUDIO CONTROL UNIT      SA51090
         BE    LOAD604G                 YES
         BR    RWKC                     RETURN
EXAUDTV  EQU   *
         SPACE
         CLI   CCWOPCDE,CCWINHIB        INHIBIT COMMAND
         BE    ERRCLEAR                 YES
         SPACE
         CLI   CCWOPCDE,CCWBREAK        BRWAK COMMAND
         BE    LOAD104G                 YES
         SPACE
         CLI   CCWOPCDE,CCWWRITE        WRITE COMMAND
         BE    LOAD204G                 BRANCH = YES             S22025
         B     LOAD304G                 NON READ WRITE CCW'S
         SPACE
NODCTXT  EQU   *
         CLI   LCBRESTR,TPTWXID         TWX ID RESPONSE
         BE    TRYRETRY                 YES
         CLI   LCBRESTR,TPRDRESP        RESPONSE TO POLLING
         BE    NOTTEXT                  BRANCH NOT TEXT
         CLI   LCBRESTR,TPRDRSPD        RESPONSE TO ADDRESSING
         BE    NOTTEXT                  BRANCH NOT TEXT
         CLI   LCBRESTR,TPRDRPEB        RESPONSE TO EOB
         BE    TXTERR                   YES, USE COMMON CODE
         SPACE
SNO      EQU   *
         OI    SCBERR4,SCBCTLUN         CONTROL UNIT ERROR
         MVI   LCBINCAM+1,POSNO         RETURN INDEX FOR 504G
         B     LOAD504G                 NEXT MODULE
TRYOVRN  EQU   *
         TM    LCBSENS0,OVERRUN         OVERRUN OR COMD REJECT
         BZ    SNO                      SHOULD NOT OCCUR         S22025
         OI    SCBERR4,SCBCTLUN         SET OVERRUN             SA68236
         SPACE
         TM    CCWOPCDE,CCWWRITE        WRITE COMMAND
         BO    SNO                      BRANCH YES
         CLI   LCBRESTR,TPTWXID         TWX ID RESPONSE
         BE    TRYRETRY                 YES
         CLI   LCBRESTR,TPRDSKIP        TEXT RELATED ERROR
         BNL   TXTERR                   BRANCH IF YES
         SPACE 2
NOTTEXT  EQU   *
         CLI   LCBCPA+16,CCWDISAB       DIAL START
         BNE   TRYRETRY                 BRANCH NO
         SPACE
         LA    RWKA,16(,RWKA)           BUMP PAST DIAL CCW'S
TRYRETRY EQU   *
         CLI   LCBINCAM,RETRY           RETRY LINT REACHED
         BE    PERMEXIT                 YES
         IC    RWKB,LCBINCAM            RETRY COUNT
         LA    RWKB,1(,RWKB)            BUMP
         STC   RWKB,LCBINCAM            SAVE BACK
         ST    RWKA,LCBSTART-1          START ADDRESS
         MVI   LCBSENS0,AVTEZERO        CLEAR SENSE FIELD FOR RETRY
         B     APPRET                   RETRY THE OPERATION      Y02027
PATHAVAL EQU   *                                               @ZA00227
CC3LOOP  EQU   *                                               @ZA00227
         TS    TCXCC3TS                 RESERVE SAVE AREA IN TCXOZ00227
         BNZ   CC3LOOP                  LOOP TILL TCX AVAILABLE@ZA00227
         STM   R15,RIOSB,TCXERPSV       SAVE REGS DESTROYED    @ZA00227
         LA    RLINK,TCXESAVE           SAVE AREA FOR IECVIOPM @ZA00227
         L     RIOSB,IOSUCB             UCB ADDRESS            @ZA00227
         LA    RIOSB,ZERO(RIOSB)        CLEAR HIGH BYTE        @ZA00227
         ST    RIOSB,TCXERPPM           SET PARM LIST          @ZA00227
         LA    RIOSB,TCXERPPM           ADDRESS OF PARM LIST   @ZA00227
         LINK     EP=IECVIOPM,MF=(E,(1)) CHECK AVAIL PATHS     @ZA00227
         LTR   R15,R15                  PATH AVAILABLE         @ZA00227
         LM    R15,RIOSB,TCXERPSV       RESTORE REGS           @ZA00227
         MVI   TCXCC3TS,AVTEZERO        CLEAR TS LOCK          @ZA00227
         BNZ   NOPATH                   NO,BRANCH              @ZA00227
         NI    LCBSIOCC,AVTEFF-CC3      RESET CONDITION CODE   @ZA00227
         B     APPRET                   RETRY OTHER PATH       @ZA00227
         SPACE 5
ERRCLEAR EQU   *
         NI    IOSFLA,AVTEFF-(IOSERR+IOSEX) RESET ' ERP IN CTL'  Y02027
*                                       AND 'PERMANENT ERROR'    Y02027
*                                       INDICATORS               Y02027
         MVI   SCBERR4,0                CLEAR ERROR INDICATION
         MVI   LCBINCAM,AVTEZERO        CLEAR RETRY OCUNT
         TM    LCBCSWSV+3,UNITCHK+UNEX  UNIT CHECK OR UNIT EXCEPTION
         BNZ   TRYREC                   YES, CONTINUE CHECKING
         SPACE
         CLI   LCBRCB,X0F               2740/2 RECRORD
         BNE   APPRET                   BRANCH NO              @OS76278
         SPACE
         MVC   EWTCGRA(ONE),LCBERCCW+TWELVE SAVE MOD 2 RESPONSE  Y02027
         B     TERM                     CONTINUE PROCESSING
         SPACE
TRYREC   EQU   *
         SR    RA,RA                    CLEAR WORK REGISTER
         CH    RA,LCBTTCIN              SPECIFIC CONNECTION
         BNE   TERM                     BRANCH IF LINE ENRTY
         TM    LCBSTAT2,LCBDIAL         DIAL LINE
         BZ    APPRET                   FORGET OBR/SDR         @OS76278
         LH    RWKC,LCBLNENT            LINE ENTRY USED
         LTR   RWKC,RWKC                LINE ENTRY PRESENT
         BNZ   TERM1                    BRANCH NOT DIAL
         B     APPRET                   FORGET OBR/SDR         @OS76278
TERM     EQU   *
         LH    RWKC,LCBTTCIN            CURRENTLY CONNECTED
TERM1    EQU   *
         CLI   LCBRESTR,TPRDIDNQ        FORGET OBRSDR RECORDING SA55381
         BL    RECORDS                    FOR READ ID/ENQ, READ SA55381
         CLI   LCBRESTR,TPTWXID           ID/ACK, ABORT FOR     SA55381
         BNH   APPRET                     SEND/RECEIVE, AND    @OS76278
*                                         READ TWX-ID           SA55381
         SPACE 1                                                SA55381
RECORDS  EQU   *                                                SA55381
         L     RWKB,AVTRNMPT            ADDRESS OF CODE A BASE
         USING IEDQTNTD,RWKB
         BAL   RWKA,TNTDCODE            LINK TO IT
         LA    RTERM,TRMSIO             RESTET BASE  ADJUST FOR DIAL
         USING TRMSIO,RTERM
         SR    RA,RA                    CLEAR WORK REGISTER      Y02027
         IC    RA,TNTENLEN              SIZE OF EACH NAME        Y02027
         LA    RWKC,79(RWKB,RWKC)       ADDRESS OF TERM ADDRESS
         SR    RWKC,RA                  SUB SIZE TO GET ADDRESS  Y02027
*                                       OF NAME                  Y02027
         MVI   EWTCTRM,BLANK                                     Y02027
         MVC   EWTCTRM+ONE(SEVEN),EWTCTRM BLANK OUT TERMINAL     Y02027
*                                       NAME FIELD IN EWA        Y02027
         BCTR  RA,ZERO                  REDUCE NAME CONT BY ONE  Y02027
*                                       FOR EXECUTE INSTRUCTION  Y02027
         EX    RA,MOVENAME              MOVE THE TERMINAL NAME   Y02027
*                                       TO THE ERP WORK AREA     Y02027
         ST    RTERM,LCBERCCW+16        ADDRESS OF COUNTERS
         CLC   1(1,RTERM),2(RTERM)      CHECK COUNTS
         BH    NOADD                    OKAY
         MVC   1(1,RTERM),2(RTERM)      SET REALISTIC COUNT
NOADD    EQU   *
         SR    RA,RA                    CLEAR WORK REGISTER
         IC    RWKA,TRMTEMPR            COUNT FO TEMP ERRORS
         LA    RWKA,1(,RWKA)            BUMP COUNT
         STC   RWKA,TRMTEMPR            DAVE BACK
         IC    RA,LCBERMSK              ASSUME LINE MASK USED
         LA    RWKB,LCBERMSK            ASSUME LINE MASK
         TM    LCBERMSK,X0F             LINE MASK SET
         BNZ   UPDATE                   BRANCH YES
         SPACE
         IC    RA,TRMSENSE              MASK BYTE
         LA    RWKB,TRMSENSE            ADJUST FOR TERMINAL LMASK
UPDATE   EQU   *
         TM    0(RWKB),X0F              ANY RECORDING TO BE DONE
         BZ    NORECORD                 BRANCH NO
         STC   RA,LCBERCCW              SAVE MASK IN WORK AREA
         TM    0(RWKB),ANYERR           INTENSIVE MOSDE SET
         BZ    NORECORD                 BRANCH NO MASK SET
         SPACE
         BO    GOSDR                    BRANCH INTENSIVE
         SPACE
         NI    LCBERCCW,XF0             CLEAR COUNT FIELD
         CLI   LCBERCCW,MOD2            MODEL TWO RECORDING
         BNE   NOTMOD2                  NO
         CLI   LCBRESTR,TPRDRSPD        ADDRESSING RESPONSE
         BNE   NORECORD                 NO
         SPACE
         B     DECRCNT                  DECREMENT COUNT , GO TO /BR
         SPACE
NOTMOD2  EQU   *
         XC    EWTCGRA(ONE),EWTCGRA     CLEAR GRAPHIC RESPONSE   Y02027
*                                       CHARACTER                Y02027
         CLI   LCBERCCW,UNEXMSK         CHECK FOR INTENSIVE MODE S22025
         BH    NORECORD                 BRANCH IF YES            S22025
         SPACE
         LR    RWKA,RA                  SAVE MASK
         BL    TESTUNCK                 TEST SENSE BYTE
         SPACE
         TM    LCBCSWSV+3,UNEX          UNIT EXCEPTION ERROR
         BNZ   GOSDR                    RECORD ERROR             S21903
NORECORD EQU   *
         CLI   TRMTEMPR,AVTEFF          OVERFLOW CONDITION
         BE    OVERFL                   BRANCH IF IOHALT         Y02027
         SPACE
         CLC   TRMSIO,AVTCLRHI+2        SIO COUNTER OVERFLOW     S21903
         BNE   APPRET                   BRANCH NO
OVERFL   EQU   *
         OI    EWTCFLG,EWTCOVF          INDICATE SIO COUNTER     Y02027
*                                       OVERFLOW                 Y02027
         B     LOADSDR                  USE COMMON CODE
HIORET   EQU   *
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CNYRL    Y02027
APPRET   EQU   *
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE IS ADDED FOR AOS.                         *
*                                                                     *
         BAL   RXCTL,HIOSUB             CHECK FOR IOHALT         Y02027
         SPACE 1                                                 Y02027
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         LA    RLCB,LCBFLAG1            IOB ADDRESS TO SIO       Y02027
*                                       FOR USE BY SIO APPENDAGE.X01004
         L     RTERM,EIGHT(,RTST)       GET DEB ADDRESS FROM RQE X01004
         LA    R15,DEBNMSUB-IEDQDEB     GET APDG. TABLE SIZE     Y02027
         SLR   RTERM,R15                POINT TO APDG. TABLE     Y02027
         L     RTERM,SIOPTR(,RTERM)     GET ADDRESS OF SIO APDG. X01004
*                                       FROM APDG. VECTOR TABLE. X01004
         SR    R15,R15                  CLEAR R15 TO TELL SIO    X01004
*                                       APDG. IT'S A SUBROUTINE. X01004
         BAL   RXCTL,SIOENTRY(,RTERM)   LINK TO SIO APPENDAGE AT X01004
*                                       OFFSET +16 TO CONVERT    X01004
*                                       CCW'S TO REAL.           X01004
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
         SVC   EREXCP
         SPACE
         SVC   RETURN
         SPACE 2
TESTUNCK EQU   *
         SRL   RWKA,4                   AHKFT MASK FOR INDEX
         IC    RWKA,TABLE-1(RWKA)       GET ASSOCIATED SENSE MASK
         EX    RWKA,TMMASK              THIS THE ERROR
         BZ    NORECORD                 BRANCH NO
GOSDR    EQU   *
         MVC   LCBRESTR(1),LCBRESTR+1   TP OP CODE OF 1ST FAILURE
DECRCNT  EQU   *
         BCTR  RA,0                     DECREMENT RECORDING COUNT
         STC   RA,0(RWKB)               SAVE MASK BACK
         SPACE
         STC   RA,EWTCMSK               MOVE RECORDING MASK TO   Y02027
*                                       RECORD                   Y02027
         OI    EWTCFLG,EWTCTEM          INDICATE TEMPORARY ERROR Y02027
LOADSDR  EQU   *
         MVC   EWTCSIO(THREE),TRMSIO    MOVE START I/O AND TEMP  Y02027
*                                       ERROR COUNTERS TO THE    Y02027
*                                       ERP WORK AREA            Y02027
         XC    TRMSIO(THREE),TRMSIO     CLEAR ERROR COUNTERS IN  Y02027
*                                       THE TERMINAL TABLE       Y02027
         MVC   EWTCTPF(ONE),LCBRESTR    MOVE 1ST TP OP CODE TO   Y02027
*                                       OBR/SDR RECORD           Y02027
         MVC   EWTCTPL(ONE),LCBRESTR+ONE MOVE LAST TP OP CODE TO Y02027
*                                       OBR/SDR RECORD           Y02027
         MVC   EWTCSNF(ONE),LCBSNSV     MOVE 1ST BYTE OF SENSE   Y02027
*                                       DATA TO OBR/SDR RECORD   Y02027
         MVC   EWTCSNL(ONE),LCBSENS0    MOVE LAST BYTE OF SENSE  Y02027
*                                       DATA TO OBR/SDR DATA     Y02027
         LA    RWKA,EWTCSIO             INDICATE ADDR OF START   Y02027
         STCM  RWKA,SEVEN,EWADDISP      DEVICE DEPENDENT INFO    Y02027
*                                       FOR OBR/SDR              Y02027
         MVI   EWADCNT,OBRDATA          INDICATE SIZE OF DEVICE  Y02027
*                                       DEPENDENT DATA FOR OBR   Y02027
         OI    IOSFLB,IOSLOG            INDICATE REQUEST FOR     Y02027
*                                       OBR/SDR                  Y02027
         LA    RLINK,SDRLOAD            SDR ID
         L     RCVT,CVTPTR              CVT ADDRESS              Y02027
         L     RXCTL,CVTXTLER           XCTL ENTRY ADDRESS       Y02027
HIOSUB   EQU   *                                                 Y02027
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        LINE CONTROL BLOCK TO THE I/O SUPERVISOR BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY OPERATIONS FROM ERP          Y02027
**************************************************************** Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                       INTO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM LCB TO IOSB    Y02027
         CLI   LCBECBCC,HIOCC           IOHALT ISSUED            Y02027
         BNER  RXCTL                    BRANCH NO - RETURN       Y02027
         SPACE 1                                                 Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CTL      Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
         BR    RXCTL                    LINK/RETURN TO CALLER
         SPACE
PERMEXIT EQU   *
         MVI   LCBINCAM+1,POPERM        RETURN TO 504G
         B     LOAD504G                 NEXT MODULE
         SPACE
         EJECT
*   THE FOLLOWING CODE INITIALIZES REGISTER 13 (LINKAGE REGISTER).
* THE VALUE OF RLINK INDICATES TO IOS THE NEXT ERP MODULE TO BE
* LOADED
LOAD904G EQU   *
         LA    RLINK,D1000(R0,RLINK)    INDEX FOR NEXT LOAD
LOAD804G EQU   *
         LA    RLINK,2000(,RLINK)       INDEX - NEXT LOAD
LOAD604G EQU   *
         LA    RLINK,D1000(R0,RLINK)    INDEX-NEXT LOAD
LOAD504G EQU   *
         LA    RLINK,D1000(R0,RLINK)    INDEX-NEXT LOAD
LOAD404G EQU   *
         LA    RLINK,D1000(R0,RLINK)    INDEX-NEXT LOAD
LOAD304G EQU   *
         LA    RLINK,D1000(R0,RLINK)    INDEX-NEXT LOAD
LOAD204G EQU   *
         LA    RLINK,D1000(R0,RLINK)    INDEX-NEXT LOAD
LOAD104G EQU   *
         LA    RLINK,D1000(R0,RLINK)    INDEX-NEXT LOAD
         LA    RLINK,X4G(R0,RLINK)      4G LAST TWO CHARACTERS OF
         L     RCVT,CVTPTR              PICKUP ADDR OF CVT       Y02027
         L     RXCTL,CVTXTLER           XCTL ROUTINE ADDRESS     S21903
         BR    RXCTL                    CALL NEXT ERP LOAD
         EJECT
*              CONSTANTS
LCBSIZE  DC    AL2(LCBFLAG1-IEDQLCB)    AIZE OF LCB UP TO IOB
H8       DC    H'8'                     USED TO BACK UP CCW OR   S22025
*                                       CSW POINTER              S22025
TMMASK   EQU   *
         TM    LCBSNSV,0                TEST FR OBR,SDR RECRODING
MOVENAME MVC   EWTCTRM(ZERO),ZERO(RWKC) MOVE TERMINAL NAME INTO  Y02027
*                                       THE ERP WORK AREA        Y02027
TABLE    EQU   *
         DC    X'01'                    TIME OUT
         DC    X'02'                    LOST DATA
         DC    X'04'                    OVERRUN
         DC    X'08'                    DATA CHECK
         DC    X'10'                    EQUIPMENT CHECK
         DC    X'20'                    BUS OUT
         DC    X'40'                    INTERVENTION REQUIRED
         DC    X'80'                    COMMAND REJECT
         SPACE 3
* EQUATES.
AD       EQU   7                        MASK FOR ICM             Y02027
         SPACE
SIOPTR   EQU   4                        OFFSET TO SIO APPENDAGE  X02004
INTENTRY EQU   4                        SECOND ENTRY OFFSET IN   X02004
*                                       IGG019QE                 X02004
IOBPTR   EQU   4                        OFFSET OF IOB ADDRESS IN X02004
*                                       RQE.                     X02004
ALL      EQU   15                       ICM MASK - ALL 4 BYTES.  X02004
HIOCC    EQU   X'48'                    HALT I/O COMPLETION CODE Y02027
SDRLOAD  EQU   256                      SDR MODULE ID
OVERRUN  EQU   X'04'                    OVERRUN MASK IN SENSE BYTE
UNEXMSK  EQU   X'90'                    UNIT EXCEPTION MASK
ANYERR   EQU   X'F0'                    INTENSIVE MODE MASK
X0F      EQU   X'0F'                    WORK MASK FOR RECORDINGS
XF0      EQU   X'F0'                    UCB CONTROL UNIT MASK
RETRY    EQU   X'02'                    RETRY LIMIT
IOBOFFST EQU   4                        IOB OFFSET IN RQE
ERPCTL   EQU   X'24'                    MASK ERP IN CONTROL
CHDATCHK EQU   X'08'                    MASK-CHANNEL DATA CHECK
CONTCHK  EQU   X'04'                    MASK-CHANNEL CONTROL CHECK
INTCHK   EQU   X'02'                    MASK-INTERFACE CHECK
ATSMBY   EQU   X'D0'                    MASK-UNUSUAL STATUS    @OZ14171
UNITCHK  EQU   X'02'
UCBOFFST EQU   2                        OFFSET TO UCB ADDRESS -RQE
EIGHT    EQU   8
THREE    EQU   3
MOD2     EQU   X'A0'                    RECORD MASK FOR MODEL 2
TEXTRTRY EQU   4                        RTURN FOR 104G
CC3      EQU   X'30'                    SIO CONDITION CODE TEST
ZERO     EQU   0
ONE      EQU   1                                                 Y02027
TWO      EQU   2                                                 Y02027
TWELVE   EQU   12                                                Y02027
BLANK    EQU   X'40'                                             Y02027
OBRDATA  EQU   18                       SIZE OF DEVICE DEPENDENT Y02027
*                                       DATA FOR OBR/SDR         Y02027
SEVEN    EQU   7                                                 Y02027
DATCHK   EQU   X'08'                    DAT ACHECK IN SENDE
UNEX     EQU   X'01'                    UNIT EXCEPTION-CSW STATUS
EREXCP   EQU   15                       ERROR EXCP
RETURN   EQU   3
D1000    EQU   1000
HIO      EQU   X'48'                    ECB COMPLETION FOR HIO
X4G      EQU   47                       INDEX VALUE FOR ERP LINKAGE
CHCKPROT EQU   X'11'                    CHAING OR PROT CHECK
PC       EQU   X'20'                    PROGRAM CHECK
POSNO    EQU   16                       RETURN INDICATOR - 504G
UNUSRET  EQU   12                       INDEX FOR 504G
POPERM   EQU   20                       RETURN INDEX FOR 504G
DEBPTR   EQU   9                        OFFSET OF DEB ADDR. IN RQE.
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
*                                       POINT IN SIO APPENDAGE.
DEVCLASS EQU   18                       DEVICE CLASS OF UCB     SA51090
TTY3     EQU   X'12'                    UCBTYPE2 OFFSET          S99228
UNITYPE  EQU   19                       UNIT TYPE OF UCB        SA51090
TTY4     EQU   X'13'                    UCBTYPE3 OFFSET          S99228
X40      EQU   X'40'                                            SA51090
GRAPHICS EQU   X'10'                    GRAPHICS DEVICE          S99228
LCL3277  EQU   X'09'                    3270 LOCAL MODEL CODE    S99228
EQCKLD   EQU   X'12'                    EQUIPMENT CHECK OR LOST  S22025
*                                       DATA IN SENSE            S22025
TO       EQU   X'01'                    TIME OUT IN SENSE        S22025
BOIR     EQU   X'60'                    BUS OUT OR INTERVENTION  S22025
*                                            REQUIRED IN SENSE   S22025
         EJECT
         TTRMD
         TDEBD                                                   X02004
         EJECT
         TCCWD
         TLCBD
         TTNTD
         DCBD  DSORG=TX
         TAVTD
         TSCBD
         TTPD
         IECDIOSB                                                Y02027
         IHAPSA
         EJECT
CVTDSECT CVT   DSECT=YES                                         X02004
         IECDERWA EWTC
         TTCXD                                                 @ZA00227
         END
