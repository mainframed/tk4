19RQ     TITLE '''IGG019RQ'' - TCAM POST-PENDING ROUTINE'
IGG019RQ CSECT
         SPACE 4
***********************************************************************
*TITLE: 'IGG019RQ' POST-PENDING ROUTINE                               *
*STATUS: CHANGE LEVEL 0                                               *
*FUNCTION:  THIS ROUTINE IS ACTIVATED BY IEAQRORI WHEN THERE IS A     *
*   POST PENDING FOR A TASK CURRENTLY BEING ROLLED IN.  IT TURNS      *
*   OFF THE POST-PENDING FLAG IN THE APPLICATION PROGRAM TCB AND      *
*   SCANS THE TCB DEB CHAIN FOR A TCAM DEB FOR WHICH A POST IS        *
*   PENDING.  THE ECB ADDRESS IS FOUND AND IS PASSED TO THE POST      *
*   SVC ROUTINE.  WHEN THE DEB CHAIN IS EXHAUSTED, CONTROL IS         *
*   RETURNED TO IEAQRORI.                                             *
*ENTRY POINT:                                                         *
*        IGG019RQ                                                     *
*INPUT:                                                               *
*   3  - CVT ADDRESS                                                  *
*   4  - TCB ADDRESS                                                  *
*OUTPUT-                                                              *
*        NONE                                                         *
*EXTERNAL ROUTINES:                                                   *
*        IGC002                                                       *
*EXIT-NORMAL: RETURN VIA REG 14 TO IEAQRORI                           *
*EXIT-ERROR:  NONE                                                    *
*TABLES/WORK AREAS:                                                   *
*   CVT                                                               *
*   AVT                                                               *
*   TCB                                                               *
*   DEB                                                               *
*   PEWA                                                              *
*   PCB                                                               *
*ATTRIBUTES: REENTRANT,REFRESHABLE,SUPERVISOR MODE                    *
*                                                                     *
*NOTES: THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL         *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
*                                                                     *
***********************************************************************
         EJECT
*                                                                     *
*            S Y M B O L I C    R E G I S T E R S                     *
*                                                                     *
RZERO    EQU   0                        GENERAL WORK REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
RDEB     EQU   2                        DATA EXTENT BLOCK
*                                       BASE REGISTER
RCVT     EQU   3                        COMMUNICATIONS VECTOR
*                                       TABLE BASE REGISTER
RTCB     EQU   4                        TASK CONTROL BLOCK
*                                       BASE REGISTER
R5       EQU   5                        UNUSED
R6       EQU   6                        UNUSED
R7       EQU   7                        UNUSED
R8       EQU   8                        UNUSED
RAVT     EQU   9                        TCAM ADDRESS VECTOR TABLE
RPEWA    EQU   10                       PROCESS ENTRY WORKAREA
RTEMP    EQU   11                       GENERAL WORK REGISTER
RBASE    EQU   12                       CSECT BASE REGISTER
RWORK    EQU   13                       GENERAL WORK REGISTER
RETURN   EQU   14                       RETURN ADDRESS
RENTRY   EQU   15                       ENTRY POINT ADDRESS
         L     RENTRY,CVT               CVT ADDRESS
         L     RENTRY,AVTCVTPT(0,RENTRY)     TCAM LOAD LIST ADDRESS
         L     RAVT,0(0,RENTRY)         ADDRESS OF TCAM AVT
         USING IEDQAVTD,RAVT            TCAM ADDRESS VECTOR TABLE
         USING IEDQDEB,RDEB             TCAM APPL. PROG. DATA
*                                       EXTENT BLOCK
         USING IEDQTCB,RTCB             TASK CONTROL BLOCK
         USING IEDQPEWA,RPEWA           PROCESS ENTRY WORKAREA
         STM   RZERO,RCVT,AVTSAVEX      SAVE RORI REGISTERS(0-3)
         STM   RAVT,RETURN,AVTSAVEX+16  SAVE RORI REGISTERS(9-14)
         BALR  RBASE,RZERO              ESTABLISH
         USING *,RBASE                            ADDRESSABILITY
         EJECT
*                                                                     *
*   SCAN DEB CHAIN FOR TCAM DEB FOR WHICH A POST IS PENDING.          *
*                                                                     *
         L     RDEB,TCBDEB              ADDRESS OF FIRST DEB
LOOP     EQU   *
         LA    RDEB,0(0,RDEB)           CLEAR HIGH-ORDER BYTE
         LTR   RDEB,RDEB                DEB CHAIN EXHAUSTED
         BZ    TURNOFF                  BRANCH IF YES TO EXIT
         TM    DEBTAMID,ID              TCAM DEB
         BO    GO                       BRANCH IF YES
NEXTDEB  EQU   *
         L     RDEB,DEBDEBAD-1          ADDRESS OF NEXT DEB IN
*                                       THE TCB DEB CHAIN
         B     LOOP                     GO LOOK AT THIS ONE
         SPACE 2
GO       EQU   *
         CLI   DEBTAMPP,AVTEFF          IS POST-PENDING INDICATOR
*                                       SET FOR THIS DEB
         BNE   NEXTDEB                  IF NO, CHECK NEXT DEB
         MVI   DEBTAMPP,AVTEZERO        TURN OFF POST-PENDING
*                                       INDICATOR
*                                                                     *
*        FIND THE ECB TO BE POSTED COMPLETE                           *
*                                                                     *
         L     RPEWA,DEBQCBAD-1         ADDRESS OF READ-AHEAD QCB
*                                       IN PROCESS ENTRY WORKAREA
         LA    RWORK,PERAQCB-IEDQPEWA   DISPLACEMENT OF R-A QCB
*                                       FROM START OF PEWA
         SR    RPEWA,RWORK              ADDRESS OF PROCESS ENTRY
*                                       WORKAREA
         L     RTEMP,PEWAECBA           GET ADDRESS OF APPL PROG
*                                       ECB FROM PROCESS ENTRY
*                                       WORKAREA
         TM    0(RTEMP),CC              ECB NEED TO BE POSTED
         BNO   POST                     BRANCH IF YES
         L     RTEMP,PEPCBAD            PROCESS CONTROL BLOCK ADDR
         L     RTEMP,PCBWRKA+8-IEDQPCB(0,RTEMP) CHECKPOINT ECB ADDR
POST     EQU   *
         LA    RTEMP,0(0,RTEMP)         INSURE HIGH ORDER BYTE
*                                       CONTAINS ZERO
         LA    RPEWA,CC                 POST COMPLETION CODE
         L     RENTRY,POSTAD(0,RCVT)    GET ADDRESS OF POST BRANCH
*                                       ENTRY POINT FROM CVT
         BALR  RETURN,RENTRY            CALL POST ROUTINE
         B     LOOP                     CONTINUE TO SCAN DEB CHAIN
*                                       FOR ANOTHER PENDING POST
         EJECT
TURNOFF  EQU   *
         NI    TCBFLTRN,X'FF'-TCBTCPP   TURN OFF POST-PENDING
*                                       FLAG IN TCB
         LM    RZERO,RCVT,AVTSAVEX      RESTORE RORI REGISTERS(0-4)
         LM    RAVT,RETURN,AVTSAVEX+16  RESTORE RORI REGISTERS(9-14)
         SR    RENTRY,RENTRY            NORMAL COMPLETION CODE
         BR    RETURN                   RETURN TO RORI
         EJECT
*                                                                     *
*          C O N S T A N T S    A N D     E Q U A T E S               *
*                                                                     *
CC       EQU   X'40'                    POST COMPLETION CODE
POSTAD   EQU   152
ID       EQU   X'C0'                    TCAM DEB ID
CVT      EQU   16
         EJECT
*                                                                     *
*        D U M M Y    C O N T R O L    S E C T I O N S                *
*                                                                     *
         TAVTD
         EJECT
         TPEWAD
         EJECT
         TDEBAPD
         EJECT
         TTCBD
         EJECT
         TPCBD
         END
