504H     TITLE ' IGE0504H - BSC ERROR POST ROUTINE'
IGE0504H CSECT
*A-000000-999999                                               @Y02X6E0
*A371100                                                         S22025
*C367400,600000,849000-858000,864000                             S22025
*A297000,867000,969000                                           S21101
*C369000-399300                                                  S21101
*A 064000-065000,834100-836700,867300-867600,960600-965400       X01004
*C 792000                                                        X01004
*A729000,765000,865000                                           S99228
*C720000,759000                                                  S99228
*A298000                                                        SA60005
*C792000                                                        SA58994
*A432000,867000,984000                                          SA65392
*D816000                                                       SA66363
*C026400,321000,504000                                         @Z30AAEE
*D888000                                                       @Z30AAEE
*A793000,A434730,D433200,D432600                               @ZA07080
*A813000                                                       @OY11534
*C759000,C810000-816000,A807000,A819000,C861000-865000         @OX14080
*C555000,C720000-729000,A762000                                @OX14080
*A789000
*C432630,A432700,C807700,C810000-821400,A981000                @OX16393
*A432700,C807900,C808400-809100,C810000,A983400                @OZ24288
*A750000                                                       @OZ30194
*A567000,768000                                                @OY20488
*C750100,750900,751500-751900                                  @OZ32827
***********************************************************************
*                                                                     *
*TITLE -- 'IGE0504H', BSC ERROR POST MODULE                           *
*                                                                     *
*  MODULE NAME = IGE0504H                                             *
*                                                                     *
*  DESCRIPTIVE NAME = BI-SYNC ERROR POST MODULE                       *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS: CHANGE LEVEL 8                                      @Z30AAEE
*                                                                     *
*FUNCTION -- TO INTERFACE WITH THE OS MESSAGE WRITER AND OBR/SDR.     *
*   IF OPERATOR CONTROL IS IN USE, THE OS MESSAGE WRITER IS BYPASSED. *
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO REQUEST ELEMENT                                   *
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO REQUEST ELEMENT                               *
*                                                                     *
*EXTERNAL ROUTINES - 'IEDQTNT' - TO OBTAIN TERMINAL ENTRY ADDRESS     *
*                  - 'IGG019QE' - TO CONVERT VIRTUAL CCW ADDRESSES TO *
*   REAL BEFORE RETRY.                                                *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO REQUEST ELEMENT                         *
*        SVC   15    LCBFLAG1  X'E6'  RETRY ERROR                     *
*        SVC   3                                                      *
*                                                                     *
*        SVC   15  LCBFLAG1  X'C2'  RETURN TO LINE END ERROR CLEARED  *
*        SVC   3                                                      *
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO REQUEST ELEMENT                          *
*        SVC   15  LCBFLAG1 X'C6'                                     *
*        SVC   3                                                      *
*                                                                     *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*   TO SCHEDULE ERROR POST                                            *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TTRMD                                                             *
*   TCCWD                                                             *
*   TLCBD                                                             *
*   DCBD                                                              *
*   TSCBD                                                             *
*                                                                     *
*ATTRIBUTES -- RE-USABLE, REFRESHABLE, SUPERVISOR MODE, DISABLED      *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
*   CERTAIN PERMANENT ERRORS ARE NOT RECORDED TO PREVENT OVERFLOW OF  *
*   SYS1.LOGREC.                                                      *
*   PARAMETERS PASSED TO OBR/SDR RECORDING MODULE:                    *
*                                                                     *
*   LCBERCCW+16 - X'00' 1 BYTE INDICATES PERMANENT ERROR              *
*   LCBERCCW+17 - 3 BYTES - ADDRESS OF TERMINAL COUNTERS              *
*   LCBERCCW+20 - 1 BYTE SIZE OF TERMINAL NAME                        *
*   LCBERCCW+21 - 3 BYTES - ADDRESS OF TERMINAL NAME                  *
*                                                                     *
***********************************************************************
         EJECT
         USING IEDQCCW,RCCW
         USING IEDQLCB,RLCB
         USING IHADCB,RDCB
         USING IEDQAVTD,RAVT
         USING IEDQTRM,RTERM
         USING IOSB,RIOSB                                        Y02027
         USING EWA,RERPWA                                        Y02027
         USING *,R15
*              REGISTERS
R0       EQU   0
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RLCB     EQU   2                        LCB CBASE
RCCW     EQU   5                        CCW BASE
RUCB     EQU   3                        UCB REGISTER
RDCB     EQU   4                        DCB BASE
RWKA     EQU   6                        WORK REGISTER
RWKB     EQU   7                        WORK REGISTER
RTST     EQU   7                        ADDR OF REQUEST QUEUE    Y02027
*                                       ELEMENT                  Y02027
RTERM    EQU   8                        TERMINAL BASE
RWKC     EQU   9                        WORK REGISTER
RWKD     EQU   10                       WORK REGISTER
RAVT     EQU   11                       AAVT BASE
RERPWA   EQU   12                       ADDR OF ERP WORK AREA    Y02027
RLINK    EQU   13                       LINKAGE FOR SUBSEQUENT LOAD
RSAVE    EQU   13                       SAVE REGISTER FOR IOSB   Y02027
RXCTL    EQU   14                       XCTL REGISTER
R15      EQU   15                       ADDRESSABILITY REGISTER
         EJECT
IGE0504H  IEDHJN  ENTER
         SPACE
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         L     RERPWA,IOSERP            PICKUP ADDR OF ERP WORK  Y02027
*                                       AREA                     Y02027
         L     RLCB,IOBOFFST(,RTST)     ADDRESS OF IOB Z(LCB)
         SH    RLCB,LCBSIZE             ADJUST BASE
         L     RWKB,LCBSCBA-1           CURRENT SCB
         USING IEDQSCB,RWKB                                      S21101
         TM    SCBQTYPE,SCBCONC         IS THIS A CONCENTRATOR  SA60005
         BZ    NOCONC                   NO, SCB ADDRESS OK      SA60005
         L     RWKB,LCBSCBDA-1          GET LINE SCB ADDRESS    SA60005
NOCONC   EQU   *                                                SA60005
         L     RCCW,LCBCSW-1            FAILING CCW
         SH    RCCW,H8                  BACK UP TO FAILING CCW
         L     RDCB,LCBDCBPT            DCB BASE
         L     RAVT,CVTPTR              CVT ADDRESS
         LR    RXCTL,RAVT               COPY CVT ADDRESS
         L     RAVT,AVTCVTPT(,RAVT)     LOAD LIST ADDRESS
         L     RAVT,AVTEZERO(,RAVT)     AVT BASE
         L     RUCB,IOSUCB              ADDRESS OF UCB         @Z30AAEE
         SR    RWKA,RWKA                CLEAR FOR INDEXING
         IC    RWKA,LCBINCAM+1          GET RETURN INDICATOR
         MVI   LCBINCAM+1,0             RESET RETURN FIELD
         B     TABLE-4(RWKA)            USE BRANCH TABLE
         SPACE
TABLE    EQU   *
         B     SNORET                   'SHOULD NOT OCCUR ERROR'
         SPACE
         B     PERMRET                  RETRY EXHAUST
         SPACE
SNORET   EQU   *
         OI    SCBERR4-IEDQSCB(RWKB),SCBUNDFN  UNDEFINED ERROR
         OI    LCBCHAIN,LCBNORTY        NO RETRY POSSIBLE
         SPACE
PERMRET  EQU   *
*        THE FOLLOWING CODE TEST FOR A PERMANENT ERROR IN        S21101
* ATTEMPTING TO SELECT A BUFFERED TERMINAL FOR A RECEIVE         S21101
* OPERATION.  IF WE ARE ATTEMTPING TO RECEIVE ANOTHER PART OF    S21101
* THE MESSAGE THE HEADER BUFFER IS CONVERTED TO TEXT AND ANEOT   S21101
* IS INSERTED IN THE BUFFER.  LINE END WILL POST IT TO MH AS A   S21101
* PERMANENT TEXT ERROR WITH NO RETRY.  THIS WILL FORMALLY END    S21101
* THE MESSAGE ON THE QUEUE.                                      S21101
         CLI   LCBRSKEY,BUFF            BUFFERED LINE            S21101
         BNE   NOTBFRD                  BRANCH NO                S21101
         TM    LCBSTAT1,LCBRECVN        RECEIVE SIDE             S21101
         BZ    NOTBFRD                  NO                       S21101
         SPACE 1                                                 S21101
         CLI   LCBRESTR,TPRDSKIP        TEXT RELATED             S21101
         BNL   NOTBFRD                  BRANCH TEXT RELATED      S22025
         SPACE 1                                                 S21101
         TM    SCBQTYPE,SCBBFMM         MIDDLE OF MESSAGE        S21101
         BZ    NOTBFRD                  BRANCH NO                S21101
         SPACE 1                                                 S21101
         CLI   LCBRESTR,TPRDLC          READ LCOUT               S21101
         BE    NOTBFRD                  YES, TEXT RELATED        S21101
         SPACE 1                                                 S21101
         CLI   LCBRESTR,TPWRAKNK        WRITE RESPONSE TO TEXT   S21101
         BE    NOTBFRD                  YES, EXIT                S21101
         SPACE 1                                                 S21101
         CLI   LCBRESTR,TPRESET         ABORT SEQUENCE           S21101
         BE    NOTBFRD                  YES                      S21101
         SPACE 1                                                 S21101
         OI    LCBCHAIN,LCBNORTY        SET NO RETRY FLAG        S21101
         L     RWKA,LCBLSPCI-1          GET ADDRESS OF HEADER    S21101
         USING IEDQPRF,RWKA                                      S21101
         TM    PRFSTAT1,PRFNHDRN        TEXT BUFFER              S21101
         BO    NOTBFRD                  BRANCH YES               S21101
         SPACE 1                                                 S21101
         OI    PRFSTAT1,PRFNHDRN        SET TO TEXT              S21101
         OI    PRFTIC,PRFBFMM           TELL AA ABOUT NEW ILDES  S21101
         OI    SCBERR4,SCBTXTTN         SET TEXT ERROR           S21101
         SR    RWKC,RWKC                CLEAR FOR INDEX          S21101
         IC    RWKC,LCBISZE             GET IDLES COUNT          S21101
         LA    RWKC,PRFSHDR-PRFSTXT(RWKC) BUMP IDLES COUNT       S21101
         STC   RWKC,LCBISZE                                      S21101
         LH    RWKD,PRFSIZE             GET PRE-SET SIZE         S21101
         LA    RWKD,1(,RWKD)            BUMP FOR EOT             S21101
         STH   RWKD,PRFSIZE             SET IN PREFIX            S21101
         LA    RWKD,PRFSTXT(RWKC)       ADDRESS OF EOT CHARACTER S21101
         DROP  RWKA                                              S21101
         L     RWKA,DCBSCTAD-1          GET SCT ADDRESS          S21101
         IC    RWKC,AVTEZERO(RWKA)      GET EOT INDEX            S21101
         LA    RWKA,1(RWKC,RWKA)        ADDRESS OF EOT CHARACTER S21101
         MVC   0(1,RWKD),0(RWKA)        MOVE EOT IN BUFFER       S21101
         EJECT
NOTBFRD  EQU   *                                                 S21101
         CLI   LCBRESTR,TPTEXT          TEXT ERROR               S22025
         BNE   NOTEXT                   NO                       S22025
*                                                                S22025
         OI    SCBERR4,SCBTXTTN         SET TEXT ERROR           S22025
NOTEXT   EQU   *                                                 S22025
         TM    SCBERR4,SCBCTLUN+SCBCHANN CONTROL UNIT OR CHANERLERR
         BNZ   NOTLNTR                  BRANCH IF EITHEROR BOTH
         SPACE
         OI    SCBERR4,SCBTRMLN         MUST BE TERMINAL/LINE ERROR
         DROP  RWKB
NOTLNTR  EQU   *
         NI    IOSFLA,AVTEFF-IOSERR     POST PERMANENT ERROR TO  Y02027
*                                       ABNORMAL END APPENDAGE   Y02027
         TM    AVTBIT2,AVTTOPOL         ERROR MSG ON TIMEOUT -POLL
         BZ    GOMSG                    BRANCH IF MSG TO BE SENT
         SPACE
         CLI   LCBRESTR,TPRDRESP        TIME OUT IN POLLING RESONSE
         BNE   GOMSG                    NO, WRITE MSG
         SPACE
         CLI   LCBSENS0,TIMEOUT         TIME OUT
         BE    EXIT                     YES, NO MSG OR OBR
         SPACE
GOMSG    EQU   *
         L     RWKA,DCBDEBAD            GET DEB ADDRESS         SA65392
         USING DEBTCBAD,RWKA            DEB ADDRESSIBILITY      SA65392
         SR    RWKB,RWKB                CLEAR WORK REGISTER     SA65392
         LR    RWKC,RWKB                CLEAR WORK REGISTER     SA65392
         IC    RWKB,DEBNMEXT            GET NUMBER OF EXTENTS   SA65392
         SLL   RWKB,TWO                 SIZE OF UCB ADDRESSES   SA65392
         L     RWKB,DEBUCBAD(RWKB)      THRESHOLD AREA          SA65392
         IC    RWKC,LCBUCBX             RLN MINUS ONE           SA65392
         SLL   RWKC,THREE               THRESHOLD INDEX        @OX16393
         AR    RWKB,RWKC                THRESHOLD COUNTERS      SA65392
         SRL   RWKC,1                   ADD HALF AGAIN FOR     @OZ24288
         AR    RWKB,RWKC                ERRMSG DATA SAVE AREA  @OZ24288
         LR    RLINK,RWKB               SAVE ADDRESS OF THRESH @OX16393
         CLI   LCBSENS0,DCK             DATA CHECK              SA65392
         BNE   TESTIR                   BRANCH-NO               SA65392
         IC    RWKC,ONE(RWKB)           GET DATA CHECK COUNT    SA65392
         LA    RWKC,ONE(RWKC)           INCREMENT COUNTER       SA65392
         STC   RWKC,ONE(RWKB)           SET NEW VALUE           SA65392
         CLC   ONE(ONE,RWKB),AVTHRESH+1 MAXIMUM DATA CHECKS     SA65392
         BL    EXIT                     BRANCH-NO NO MESSAGE    SA65392
         MVI   ONE(RWKB),ZERO           CLEAR COUNTER           SA65392
TESTIR   EQU   *                                                SA65392
         CLI   LCBSENS0,IR              INTERVENTION REQUIRED   SA65392
         BNE   TESTTOT                  BRANCH-NO               SA65392
         IC    RWKC,TWO(RWKB)           GET IR COUNT            SA65392
         LA    RWKC,ONE(RWKC)           INCREMENT COUNTER       SA65392
         STC   RWKC,TWO(RWKB)           SET NEW VALUE           SA65392
         CLC   TWO(ONE,RWKB),AVTHRESH+2 MAXIMUM INTERVENTIONS   SA65392
*                                       NAME FIELD IN EWA        Y02027
         BL    EXIT                     BRANCH-NO NO MESSAGE    SA65392
         MVI   TWO(RWKB),ZERO           CLEAR COUNTER           SA65392
TESTTOT  EQU   *                                                SA65392
         CLI   LCBSENS0,TIMEOUT         TIME OUT                SA65392
         BNE   GOTHRESH                 BRANCH NO-WRITE MSG     SA65392
         CLI   LCBRESTR,TPRDRPEB        TEXT TIMEOUT            SA65392
         BE    GOTHRESH                 BRANCH-YES EXIT         SA65392
         IC    RWKC,THREE(RWKB)         GET TIMEOUT COUNT       SA65392
         LA    RWKC,ONE(RWKC)           INCREMENT COUNTER       SA65392
         STC   RWKC,THREE(RWKB)         SET NEW VALUE           SA65392
         CLC   THREE(ONE,RWKB),AVTHRESH+3 MAXIMUM TIMEOUTS      SA65392
         BL    EXIT                     BRANCH-NO NO MESSAGE    SA65392
         MVI   THREE(RWKB),ZERO         CLEAR COUNTER           SA65392
GOTHRESH EQU   *                                                SA65392
         MVI   EWTCTRM,BLANK            BLANK OUT TERM         @ZA07080
         MVC   EWTCTRM+ONE(SEVEN),EWTCTRM BLANK OUT TERM       @ZA07080
         MVI   LCBSENS1,0               CLEAR 2ND SENSE BYTE
         SR    RWKA,RWKA                CLEAR WORK REGISTER
         STH   RWKA,LCBRESTR+2          CLEAR DEVICE CHARACTERS
         MVI   LCBINCAM,RETRY           SET RETRY COUNT TO MAX
*                                         INSURE NO RECALL
         LH    RWKC,LCBTTCIN            TNT OFFSET
         LTR   RWKC,RWKC                SOURCE SPECIFIED
         BNZ   SOURCE                   YES
         SPACE
         TM    LCBSTAT2,LCBDIAL         DIAL LINE
         BZ    USEUCB                   BRANCH NO
         SPACE
         LH    RWKC,LCBLNENT            LINE ENTRY USED
         LTR   RWKC,RWKC                LINE ENTRY?
         BNZ   SOURCE                   BRANCH YES
         SPACE
USEUCB   EQU   *
         L     RTERM,STATTAB(,RXCTL)    STAT TABLE OFFSET FROM CVT
         SR    RWKC,RWKC                CLEAR FOR INDEX
         L     RWKB,UCBEXTPT-UCBOB(,RUCB) UCB EXTENSION          Y02027
         IC    RWKC,UCBSTI-UCBCMEXT(,RWKB) STATAB INDEX          Y02027
         LR    RWKB,RTERM               COPY TABLE ADDRESS
LOOP1    EQU   *
         CLM   RUCB,THREE,ZERO(RWKB)    IS DEV SUPPORTED HERE  @Z30AAEE
         BC    4,LOOP2                  BRANCH YES
         SPACE
         LA    RWKB,2(,RWKB)            NEXT ENTRY
         LA    RWKC,256(,RWKC)          NEXT STAT TABLE IN
         B     LOOP1                    CHECK THIS ENTRY
         SPACE
LOOP2    EQU   *
         MH    RWKC,H10                 MULTIPLY BY 10
         AR    RTERM,RWKC               SAT TABLE ADDRESS SET
         LA    RWKC,UCBNAME-UCBOB(,RUCB) EBCIDIC NAME            Y02027
         ST    RWKC,LCBERCCW+20         SET ADDRESS OF NAME
         LA    RWKA,TWO                 SET SIZE FOR EXECUTE    Y02027
*                                       INSTR  TO MOVE UCB NAME  Y02027
         EX    RWKA,MOVENAME            MOVE UCB NAME TO ERP     Y02027
*                                       WORK AREA                Y02027
         ST    RTERM,LCBERCCW+16        ADDRESS OF SDR COUNTERS
         B     NOADDR                   COMMMON CODE AND EXIT
         SPACE
SOURCE   EQU   *
         N     RWKC,AVTCLRHI            CLEAR NEG. PROPAGATION @OX14080
         L     RWKB,AVTRNMPT            ROUTINE BASE
         USING IEDQTNTD,RWKB
         BAL   RWKA,TNTDCODE            LINK TO ROUTINE
         SR    R0,R0                    CLEAR WORK REGISTER
         SR    RWKA,RWKA                CLEAR INDEX REGISTER   @OY20488
         IC    RWKA,TRMCHCIN            AND GET DCT INDEX      @OY20488
         BCTR  RWKA,0                   REDUCE FOR MULTIPLY    @OY20488
         MH    RWKA,AVTDCTLN            AND GET OFFSET         @OY20488
         A     RWKA,AVTCSTCS            LOCATE DCT ENTRY       @OY20488
         TM    1(RWKA),DCT3270          3270 TERMINAL          @OY20488
         BNO   NOTUBE                   BRANCH NO              @OY20488
         OI    LCBERBST,LCBLOGDV        INDICATE LOG DEVICE    @OY20488
NOTUBE   EQU   *                                               @OY20488
         IC    R0,X'28'(,RWKB)          GET SIZE OF NAME
         LA    RWKC,79(RWKB,RWKC)        ADDRESS OF TERM ADDRESS
         SR    RWKC,R0                  BACK UP TO NAME ADDRESS
         LA    RWKA,TRMSIO              ADDRESS OF COUNTERS
*    THE FOLLOWING INSTRUCTION SETS LCBERCCW=0 - INDICATES OBR
         ST    RWKA,LCBERCCW+16         ADDRESS OF COUNTERS FOR OBR
         CLC   1(1,RWKA),2(RWKA)        CHECK COUNTS
         BH    NOADD                    OKAY
         MVC   1(1,RWKA),2(RWKA)        SET COUNTS
NOADD    EQU   *
         ST    RWKC,LCBERCCW+20         ADDRESS OF EBCDIC NAME
         LR    RWKA,R0                  MOVE NAME COUNT FOR EXEC Y02027
*                                       INSTRUCTION              Y02027
         BCTR  RWKA,ZERO                REDUCE NAME CONT BY ONE  Y02027
*                                       FOR EXECUTE INSTRUCTION  Y02027
         EX    RWKA,MOVENAME            MOVE THE TERMINAL NAME   Y02027
*                                       TO THE ERP WORK AREA     Y02027
         SR    RWKB,RWKB                CLEAR WORK REGISTER
         TM    TRMSTATE,TRMOPTFN        OPTION FIELDS PRESENT
         BZ    NOOPT                    BRANCH IF NO             S22025
         SPACE
         IC    RWKB,TRMOPNO             NO OF OPTION ENTRIES
         LA    RWKB,3(,RWKB)            ADJUST FOR REQUIRED
*                                         OPTION BYTES
NOOPT    EQU   *
         LH    RWKC,TRMDEVFL            DEVICE FLAGS
         LTR   RWKC,RWKC                BUFSIZE SPECIFIED
         BNM   NOBUFSZ                  NO
         SPACE
         LA    RWKB,3(,RWKB)            ADJUST FOR BUFSIZE
NOBUFSZ  EQU   *
         LA    RWKB,TRMCHCIN+1(RWKB)    DEVICE INGORMATION
         SLL   RWKC,17                  SHIFT DEVICE BITS
         LTR   RWKC,RWKC                DIAL DIGITS
         BNM   NODIGITS                 NO
         SPACE
         CLI   0(RWKB),4                AT LEAST 4 DIGITS
         BNH   PACKIT                   BRANH IF 4 OR LESS
         SR    RWKA,RWKA                CLEAR WORK REGISTER
         IC    RWKA,0(,RWKB)            GET COUNT OF DIGITS
         SH    RWKA,H4                  DECREMENT BY 4
         LA    RWKB,0(RWKA,RWKB)        ADJUST FOR PACK
PACKIT   EQU   *
         PACK  LCBERCCW+8(4),1(7,RWKB)  PACK DIAL DIGITS
         MVC   LCBRESTR+2(2),LCBERCCW+8 MOVE FOR WTO MODULE
         B     POSTEXIT                 LEAVE
         SPACE
NODIGITS EQU   *
         TM    LCBSTAT1,LCBSENDN        SEND STATE
         BO    GETCHAR                  BRANCH YES TO ADDR CHARS
         TM    LCBSTAT2,LCBMSGNN        MESSAGE GEN
         BZ    NOADDR                   BRANCH NO TO GET POLLING
GETCHAR  EQU   *
         SLL   RWKC,1                   ADDRESSING  CHARS
         LTR   RWKC,RWKC                NO ADDRESSING CHARS
         BNM   NOADDR                   NO
         SPACE
         MVC   LCBRESTR+2(2),1(RWKB)    ASSUME AT LEAST 2 CHARS.
         CLI   AVTEZERO(RWKB),1         ONLY ONE CHAR
         BE    CLEAR2                                          @OX14080
FIVEADDR CLI   AVTEZERO(RWKB),FIVE       FIVE CHARS.             S99228
         BNE   POSTEXIT                  NO                      S99228
         MVC   LCBRESTR+THREE(ONE),THREE(RWKB)   UPDATE 2ND BYTE S99228
         B     POSTEXIT                  LEAVE                   S99228
         SPACE 2
NOADDR   EQU   *
         SR RWKA,RWKA                   CLEAR WORK REGISTER
         IC    RWKA,LCBUCBX             RLN MINUS 1
         SLL   RWKA,2                   MULTIPLY BY 4
         L     RWKB,DCBINVLI(RWKA)      ILIST FOR THIS LINE
         SR    RWKA,RWKA                CLEAR WORK             @OZ30194
         SR    R0,R0                    CLEAR INDEX REGISTER   @OZ32827
         LA    RWKC,8(,RWKB)            GET CONTROL WORD       @OZ30194
         IC    RWKA,2(,RWKB)            GET ENTRY LENGTH       @OZ30194
         BCTR  RWKA,0                   REDUCE BY 1            @OZ30194
         B     STARLOOK                 CHECK LIST             @OZ30194
LOOPLOOK EQU   *                                               @OZ30194
         AR    RWKC,RWKA                MOVE TO NEXT           @OZ30194
         LA    RWKC,1(,RWKC)            ENTRY                  @OZ30194
         SR    R0,R0                    CLEAR INDEX REGISTER   @OZ32827
STARLOOK EQU   *                                               @OZ30194
         CLI   0(RWKC),XFE              END OF LIST            @OZ30194
         BE    YESEND                   BRANCH YES             @OZ30194
         CLI   1(RWKC),XFE              END OF LIST            @OZ30194
         BE    YESEND                   BRANCH YES AGAIN       @OZ30194
         IC    R0,0(RWKA,RWKC)          GET ENTRY INDEX        @OZ32827
         AR    R0,R0                    DOUBLE FOR SUBTRACT    @OZ32827
         LNR   R0,R0                    MAKE NEGATIVE          @OZ32827
         AR    R0,RWKB                  SUBTRACT FOR TNTINDEX  @OZ32827
         CH    R0,LCBTTCIN              CORRECT ENTRY          @OZ32827
         BNE   LOOPLOOK                 BRANCH NO              @OZ30194
YESEND   EQU   *                                               @OZ30194
         MVC   LCBRESTR+2(2),AVTEZERO(RWKC) ASSUME 2 CHARACTERS
         CLI   2(RWKB),2                ONLY 1 CHARACTER
         BH    FIVEPOLL                 NO,CHECK FIVE CHARS    @OX14080
         SPACE
CLEAR2   EQU   *                                               @OX14080
         MVI   LCBRESTR+3,AVTEZERO      CLEAR 2ND BYTE
         B     POSTEXIT                  LEAVE                   S99228
FIVEPOLL CLI   TWO(RWKB),SIX             FIVE CHARS.             S99228
         BNE   POSTEXIT                  NO                      S99228
         MVC   LCBRESTR+THREE(ONE),THREE(RWKC)   UPDATE 2ND BYTE S99228
POSTEXIT EQU   *
         TM    LCBSTAT2,LCBDIAL         DIAL                   @OY20488
         BO    NORECRYP                 BRANCH YES             @OY20488
         L     RWKB,LCBDCBPT            GET DCB ADDRESS        @OY20488
         L     RWKB,DCBTRANS-1          AND TRANSLATE TABLE    @OY20488
         L     R0,0(,RWKB)              EBCDIC TABLE           @OY20488
         LTR   R0,R0                    SPECIFIED              @OY20488
         BZ    NORECRYP                 BRANCH NO              @OY20488
         LA    RWKB,4(0,RWKB)           GET INPUT TABLE        @OY20488
         TR    LCBRESTR+2(2),0(RWKB)    TRANSLATE CU AND DVC   @OY20488
NORECRYP EQU   *                                               @OY20488
         LH    RWKB,AVTOPCON            OPERATOR CONTROL
         LTR   RWKB,RWKB                SPECIFIED
         BZ    SYSCON                   NO, USE SYSTEM CONSOLE
         SPACE
         N     RWKB,AVTCLRHI            CLEAR HIGH ORDER HALF WORD
         A     RWKB,AVTRNMPT            GET ADDR OF TERM NAME ENTRY
         L     RWKA,LCBERCCW+20         TERMNAME ADDR FOR LCBTTCIN
         LA    RWKA,0(,RWKA)            CLEAR HIGH ORDER BYTE
         CR    RWKA,RWKB                CHECK FOR PRIM CONTROL TERM
         BNE   OPCTL                    BRANCH IF ERROR NOT ON PRIM
SYSCON   EQU   *
         USING UCBOB,RUCB                                      @OZ16799
         MVC   EWACHA,UCBCHA            SAVE DEV ADD FOR MSG   @OZ16799
         DROP  RUCB                                            @OZ16799
         OI    IOSFLB,IOSMSG+IOSLOG     INDICATE I/O ERR MSG AND Y02027
*                                       OBR/SDR                  Y02027
         OI    EWAFLG1,EWADDMSG         ERP DEP DATA IN ERRMSG @ZA07080
         MVC   EWTCTID(TWO),LCBRESTR+TWO MOVE TERMINAL ID CHARS  Y02027
*                                       TO ERP WK AREA FOR IOS   Y02027
*                                       WTO ERROR ROUTINE        Y02027
         LA    RLINK,WTOMSG             LINK TO MESSAGE WRITE
         B     NEXTLOAD                 COMMON CODE
OPCTL    EQU   *
         OI    LCBCHAIN,LCBERMSG
         USING LCBEXTX,RLINK            ADDRESSABILITY         @OX16393
         MVC   IEASENS0,LCBSENS0        SAVE SENSE INFORMATION @OZ24288
         MVC   IEALTPCD(4),LCBRESTR     SAVE TP CDS AND CHARS  @OX16393
         MVC   IEACSW,LCBCSW+3          SAVE CSW               @OZ24288
         MVC   IEACMD,CCWOPCDE          SAVE COMMAND CODE      @OZ24288
         LA    RLINK,SDRLOAD            OBR ID
NEXTLOAD EQU   *
         MVC   EWTCCSW(SEVEN),LCBCSW    MOVE CSW INTO THE ERP    Y02027
*                                       WORK AREA                Y02027
         MVC   EWTCCCW(EIGHT),ZERO(RCCW) MOVE CCW INTO THE ERP   Y02027
*                                       WORK AREA                Y02027
         IC    RWKA,LCBERMSK            ASSUME LINE ERROR MASK   Y02027
         TM    LCBERMSK,LINEMASK        IS LINE MASK PRESENT     Y02027
         BNZ   MOVEMASK                 YES. MOVE MASK.          Y02027
         IC    RWKA,TRMSENSE            PICKUP TERMINAL ERR MASK Y02027
MOVEMASK EQU   *                                                 Y02027
         STC   RWKA,EWTCMSK             MOVE MASK INTO ERP WORK  Y02027
*                                       AREA                     Y02027
         L     RTERM,LCBERCCW+SIXTEEN   PICKUP ADDR OF SDR CTRS  Y02027
         USING TRMSIO,RTERM                                      Y02027
         MVC   EWTCSIO(THREE),TRMSIO    MOVE START I/O AND TEMP  Y02027
*                                       ERROR COUNTERS TO THE    Y02027
*                                       ERP WORK AREA            Y02027
         XC    TRMSIO(THREE),TRMSIO     CLEAR ERROR COUNTERS IN  Y02027
*                                       THE TERMINAL TABLE       Y02027
         MVC   EWTCTPF(ONE),LCBRESTR    MOVE 1ST TP OP CODE TO   Y02027
*                                       OBR/SDR RECORD           Y02027
         MVC   EWTCTPL(ONE),LCBRESTR+ONE MOVE LAST TP OP CODE TO Y02027
*                                       OBR/SDR RECORD           Y02027
         MVC   EWTCSNF(ONE),LCBSNSV     MOVE 1ST BYTE OF SENSE   Y02027
*                                       DATA TO OBR/SDR RECORD   Y02027
         MVC   EWTCSNL(ONE),LCBSENS0    MOVE LAST BYTE OF SENSE  Y02027
*                                       DATA TO OBR/SDR DATA     Y02027
         LA    RWKA,EWTCSIO             INDICATE ADDR OF START   Y02027
         STCM  RWKA,SEVEN,EWADDISP      DEVICE DEPENDENT INFO    Y02027
*                                       FOR OBR/SDR              Y02027
         MVI   EWADCNT,OBRDATA          INDICATE SIZE OF DEVICE  Y02027
*                                       DEPENDENT DATA FOR OBR   Y02027
         L     RXCTL,XCTLADD(,RXCTL)    XCTL ADDRESS
HIOSUB   EQU   *                                                 Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                         TO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                         CODE FROM LCB TO IOSB  Y02027
         CLI   LCBECBCC,HIOCC           IOHALT ISSUED            Y02027
         BNER  RXCTL                    BRANCH NO - RETURN       Y02027
         SPACE 1                                                 Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CTL      Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
         BR    RXCTL                    LINK/RETURN TO CALLER
EXIT     EQU   *
         BAL   RXCTL,HIOSUB             CHECK FOR IOHALT         Y02027
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE IS ADDED FOR AOS.                         *
*                                                                     *
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         SR    RLCB,RLCB                CLEAR FOR ICM.
         ICM   RLCB,SEVEN,IOBPTR(RTST)  GET IOB ADDRESS FROM RQE X01004
*                                       FOR USE BY SIO APPENDAGE.
         SR    RTERM,RTERM              CLEAR FOR ICM.
         ICM   RTERM,SEVEN,DEBPTR(RTST)  GET DEB ADDR FROM RQE   X01004
         LA    R15,DEBNMSUB-IEDQDEB     GET APDG. TABLE SIZE     Y02027
         SLR   RTERM,R15                POINT TO APDG. TABLE     Y02027
         L     RTERM,SIOPTR(ZERO,RTERM)  GET ADDR FROM SIO APDG  X01004
*                                       FROM APDG. VECTOR TABLE.
         SR    R15,R15                  CLEAR R15 TO TELL SIO
*                                       APDG. IT'S A SUBROUTINE.
         BAL   RXCTL,SIOENTRY(ZERO,RTERM)  LNK TO SIO APDG AT    X01004
*                                       OFFSET +16 TO CONVERT CCW'S
*                                       FROM VIRTUAL TO REAL.
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
         SVC   EREXCP
         SVC   RETURN
         EJECT
*              EXECUTE INSTRUCTIONS
         SPACE
MOVENAME MVC   EWTCTRM(ZERO),ZERO(RWKC) MOVE TERMINAL NAME INTO  Y02027
*                                       THE ERP WORK AREA        Y02027
         SPACE
*              CONSTANTS                                         Y02027
H4       DC    H'4'                     USED TO ADJUST DIGIT     S22025
*                                       COUNTER                  S22025
H8       DC    H'8'                     USE TO BACK UP TO        S22025
*                                       FAILING CLW              S22025
H10      DC    H'10'                    USED TO ADJUST STATISTIC S22025
*                                       TABLE POINTER            S22025
LCBSIZE  DC    AL2(LCBFLAG1-IEDQLCB)    ADJUSTMENT TO BACK UP TO S22025
*                                       BEGINNING OF LCB         S22025
ONE      EQU   1                         DECIMAL ONE             S99228
TWO      EQU   2                         DECIMAL TWO             S99228
THREE    EQU   3                         DECIMAL THREE           S99228
FIVE     EQU   5                         DECIMAL FIVE            S99228
SIX      EQU   6                         DECIMAL SIX             S99228
RETRY    EQU   6                        RETRY COUNT
EIGHT    EQU   8                                                 Y02027
TWELVE   EQU   12                                                Y02027
SIXTEEN  EQU   16                                                Y02027
BLANK    EQU   X'40'                                             Y02027
LINEMASK EQU   X'FF'                    TEST FOR LINE ERROR MASK Y02027
OBRDATA  EQU   18                       SIZE OF DEVICE DEPENDENT Y02027
*                                       DATA FOR OBR/SDR         Y02027
ZERO     EQU   0                        LENGTH CONSTANT          X01004
DCK      EQU   X'08'                    MASK FOR SENSE BYTE     SA65392
IR       EQU   X'40'                    INTERVENTION REQUIRED   SA65392
SEVEN    EQU   7                        LENGTH CONSTANT          X01004
BUFF     EQU   X'1A'                    STVTO FOR BUFFERED LCB   S21101
STATTAB  EQU   112                      OFFSET INTO CVT
IOBOFFST EQU   4                        IOB OFFSET IN RQE
XCTLADD  EQU   X'2C'                    OFFSET IN CVT-XCTL ROUTINE
ERPCTL   EQU   X'20'                    ERP IN CONTRO BIT
UNITCHK  EQU   X'02'                    UNIT CHECK MASK IN CSW
EREXCP   EQU   15                       ERROR EXCP
RETURN   EQU   3                        MASK FOR RETURN SVC
INTREQ   EQU   X'40'                    MASK FOR INTERVENTION REQ
HEXUCB   EQU   4                        UCB OFFSET TO PACKED NAME
TIMEOUT  EQU   X'01'                    MSK FOR SENSE BYTE
BUSOUT   EQU   X'20'                    MASK FOR BUSOUT
EQCHK    EQU   X'10'                    EQUIPMENT CHECK MSSK
LOSTD    EQU   X'02'                    LOST DATA MASK
X24      EQU   X'24'                    MASK FOR ERP IN CONTROL
CHEND    EQU   X'80'                    MASK FOR CHANNEL END
DEVEND   EQU   X'04'                    MASK FOR DEVICE END
POPERM   EQU   12                       PERMANENT ERROR RETURN
POSNO    EQU   16                       SHOULD NOT OCCUR REUTRN
ENQCH    EQU   7                        OFFSET INTO SCT
RDENQ1   EQU   16                       RETURN FO R 404H
LDRSPETB EQU   4                        RETURN INDICATOR
SKIPRET  EQU   20                       RETURN FOR 404H
X01      EQU   X'01'                    WTO -GIVE CONTROL TO OBR
X10      EQU   X'10'                    EXTENDED MESSAGE FORMAT
WTOMSG   EQU   X'FD'                    WTO MODULE ID
SDRLOAD  EQU   256                      OBR/SDR MODULE ID
CVTPTR   EQU   16                       LOCATION OF CVT POINTER
UCBMASK  EQU   X'17'                    OFFSET IN UCB TO SENSE BYTE
IOMSG    EQU   X'04'                    'I/OMSG' FLAG
XFE      EQU   X'FE'                    END OF LIST ID         @OZ30194
IOBPTR   EQU   5                        OFFSET OF IOB ADDR. IN RQE.
DEBPTR   EQU   9                        OFFSET OF DEB ADDR. IN RQE.
SIOPTR   EQU   4                        OFFSET OF SIO APDG. ADDRESS
*                                       IN APDG. VECTOR TABLE.
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
HIOCC    EQU   X'48'                    HALT I/O COMPLETION      Y02027
*                                       POINT IN SIO APPENDAGE.
         EJECT
         TSCBD
         TDCTD                                                 @OY20488
         TPRFD                                                   S21101
         EJECT
         TTRMD
         TTNTD
         TCCWD
         TLCBD
         EJECT                                                 @OX16393
LCBEXTX  DSECT                                                 @OX16393
THRWORD  DS    F                        FOUR BYTE THRESH INFO  @OX16393
IEALTPCD DS    CL1                      SAVED LAST TP OP CODE  @OX16393
IEAFTPCD DS    CL1                      SAVED FIRST TP OP CODE @OX16393
IEADVCHS DS    CL2                      SAVED DEVICE CHARACTERS@OX16393
IEASENS0 DS    CL1                      SAVED SENSE INFORMATION@OZ24288
IEACMD   DS    CL1                      SAVED COMMAND CODE     @OZ24288
IEACSW   DS    CL2                      SAVED CSW INFORMATION  @OZ24288
         EJECT                                                 @OX16393
         DCBD  DSORG=TX
         TDEBD                                                  SA65392
         TAVTD
         TTPD
         IECDIOSB                                                Y02027
         IECDERWA EWTC
         EJECT
         DSECT
         IEFUCBOB
         END
