         TITLE 'IGG01937 THIRD LOAD OF LINE GROUP OPEN'
IGG01937 CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C002986,238000-239000                                           X01004
*D081000,091000-222000,519000-533000,553000-553500,556000,602600 X01004
*C101000-104000,107000,557000-562000,565000-566000,568000        S21903
*C569000-578000,580000,581000-586000,594000                      S21903
*C085400,443300,443600                                           S22029
*A104000,219000                                                  A42373
*C105000,220000                                                  A42373
*D221000                                                         A42373
*A265000                                                         A42381
*A173300                                                         S22025
*C085400,466000,471080-471800                                     M1178
*A365000,471000                                                  A44908
*C085000,466000-467000                                           A44908
*D444000-449000                                                  A44908
*A265210-265260                                                  A51796
*C085400,265200                                                  A51796
*A129000                                                         A48258
*C313000-317000                                                  A48258
*A010100-010600,223000-229400,265010-265070,340050-340850        S22026
*A580300-580600,614200-614600                                    S22026
*A121300-121600,166500,173050,553500,572500                      S22026
*C010000                                                         S22026
*C085200-085400,123000-126000,174000,262420,262600,262720        S22026
*C328000-334000                                                  S22026
*D223000-230000                                                  S22026
*D138000-139000,157000-165000,170000-171000,178000-187000        S22026
*D248000-249000,262060,388000-389000,554000                      S22026
*A002990                                                         S22025
*C040000,083000,416000,430000,555000,595000-596000,599000        S22025
*C601000-602000                                                  S22025
*A002000                                                         S21903
*C002010-002992,003000                                           S21903
*D198000,318000,375000,397000,564000,567000,579000,              S21903
*D582000,587000,610000                                           S21903
*A340850                                                        SA57354
*C003900,019000,022000,028000,043000,044000,045000,059000,071000 Y02027
*C080000,087000-090000,366000-367000,400000,402000,430500,435000 Y02027
*C563000                                                         Y02027
*D073000                                                         Y02027
*A050010-050040,081000,097000-147000,367500,567000,602200        Y02027
*A471880                                                       @SA70305
*C471720                                                       @SA70305
*D050010-050030                                                @ZA01156
*C485000                                                       @YA11150
*A471940                                                       @OY12667
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE:  'IGG01937'  THIRD LOAD OF LINE GROUP OPEN                    *
*                                                                     *
*  MODULE NAME = IGG01937  (TCAM,INITIALIZATION)               @G36XRNP
*                                                                     *
*  DESCRIPTIVE NAME = THIRD LOAD OF LINE GROUP OPEN                   *
*                                                                     *
*  COPYRIGHT =  'NONE'                                                *
*                                                                     *
*  STATUS = CHANGE LEVEL 10.0                                  @G36XRNP
*                                                                     *
*FUNCTION/OPERATION - THIS ROUTINE COMPUTES THE SIZE OF THE           *
*   ENTIRE LCB AREA REQUIRED FOR THIS LINE GROUP AND ISSUES           *
*   A GETMAIN MACRO INSTRUCTION FOR THE MAIN STORAGE.  THE            *
*   ENTIRE AREA IS THEN DIVIDED INTO INDIVIDUAL LCB'S FOR             *
*   EACH LINE AND THE INDIVIDUAL LCB'S ARE INITIALIZED                *
*   PRIOR TO BUILDING CHANNEL PROGRAMS FOR THE LCB'S.                 *
*   THIS ROUTINE ALSO INSERTS THE SEND SCHEDULER STCB INTO THE   S22026
*   STCB CHAIN OF THE LCB BY PRIORITY.  FOR CONCENTRATORS, THE   S22026
*   CONCENTRATOR SEND SCHEDULER STCB IS INSERTED INTO THE LCB    S22026
*   STCB CHAIN.  FOR TERMINALS ATTACHED TO CONCENTRATORS, THE    S22026
*   CONCENTRATOR SEND SCHEDULER STCB IS INSERTED INTO THE        S22026
*   ELEMENT CHAIN OF THE DATA READY QUEUE.                       S22026
*                                                                     *
*ENTRY POINTS - THIS ROUTINE IS ENTERED AT IGG01937 VIA AN            *
*   XCTL FROM THE OPEN EXECUTOR IGG01936.  IT MAY ALSO BE             *
*   RE-ENTERED FROM THE RELOOP PORTION OF THIS ROUTINE IF             *
*   IT IS DETERMINED THAT THERE IS ANOTHER DATA CONTROL               *
*   BLOCK TO BE OPENED.                                               *
*                                                                     *
*INPUT - THE REGISTERS 5, 6, 7, AND 8 ARE THE INPUT AS FOLLOWS        *
*   5 - THE ADDRESS OF THE WTG TABLE PREFIX                      Y02027
*   6 -  THE ADDRESS OF THE BEGINNING OF THE WHERE-TO-GO TABLE.       *
*   7 - THE ADDRESS OF THE COPY OF THE CURRENT DCB               Y02027
*   8 -  THE ADDRESS OF THE CURRENT ENTRY IN THE WHERE-TO-GO          *
*        TABLE .                                                      *
*                                                                     *
*OUTPUT - REGISTERS 7 AND 8 WILL BE POSITIONED AT THE NEXT            *
*   ENTRIES IN THE WHERE-TO-GO PREFIX TABLE AND                  Y02027
*   THE WHERE-TO-GO TABLE RESPECTIVELY.  LINE CONTROL BLOCKS          *
*   WILL HAVE BEEN BUILT AND INITIALIZED FOR EACH LINE IN             *
*   THE LINE GROUP                                                    *
*                                                                     *
*EXTERNAL ROUTINES - NONE                                             *
*                                                                     *
*EXITS - NORMAL                                                       *
*   THIS ROUTINE EXITS VIA AN XCTL TO THE EXECUTOR IDENTIFIED         *
*   BY THE NEXT NON-ZERO ENTRY IN THE WHERE-TO-GO TABLE               *
*   NAMELY IGG01938                                                   *
*                                                                     *
*EXITS - ERROR  NONE                                             S22025
*                                                                     *
*TABLES/WORK AREAS - THE WHERE-TO-GO TABLE CONTAINS THE ID AND        *
*   TTR OF THE OPEN EXECUTORS.  THE WHERE-TO-GO PREFIX           Y02027
*   TABLE CONTAINS THE ADDRESS OF A COPY OF EACH DCB             Y02027
*   SPECIFIED IN THE OPEN MACRO INSTRUCTION.                     Y02027
*                                                                     *
*ATTRIBUTES - THIS ROUTINE IS EXECUTED IN THE TRANSIENT AREA AS       *
*   ENABLED, PRIVILEGED, AND RE-ENTRANT.                              *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
*REGISTER USAGE
*
RE       EQU   0                        WORK REGISTER
RF       EQU   1                        WORK REGISTER
RDCB     EQU   2                        ADDRESS OF THE CURRENT DCB
RTIOT    EQU   3                        ADDRESS OF THE TIOT      Y02027
RCORE    EQU   4                        ADDRESS OF DCB WORK AREA
RPAR     EQU   5                        ADDRESS OF FIRST ENTRY IN
*                                         DCB PARAMETER LIST
RWTG     EQU   6                        ADDRESS OF WHERE-TO-GO TABLE
RPARC    EQU   7                        ADDRESS OF CURRENT ENTRY IN
*                                         DCB PARAMETER LIST
RWTGC    EQU   8                        ADDRESS OF CURRENT ENTRY IN
*                                         WHERE-TO-GO TABLE
RAVT     EQU   9                        ADDRESS OF THE AVT
RUCB     EQU   10                       ADDRESS OF CURRENT UCB
RDEB     EQU   11                       ADDRESS OF THE DEB
RBASE    EQU   12                       BASE REGISTER            Y02027
RC       EQU   13                       WORK REGISTER
RJ       EQU   15                       WORK REGISTER
*
         USING IEDQAVTD,RAVT            AVT DSECT
         USING IHADCB,RDCB              DCB DSECT
         USING IEDQTNTD,RJ              TERMINAL NAME TABLE DSECT
         USING IEDQQCB,RTIOT            QCB DSECT
         USING FORCORE,RCORE            OPEN WORK AREA DSECT     Y02027
         USING IEDQTCX,RF               TCAM CVT EXTENSION DSECT Y02027
*
         BALR  RBASE,0                  ESTABLISH BASE REGISTER  S22025
         USING *,RBASE
         SPACE 2
         B     INIT1937                 BRANCH AROUND ID         A44908
         DC    CL2'19'                  1ST PART OF MODULE NAME  S22026
AMIDCNST DC    CL2'37'                  2ND PART OF MODULE NAME  S22026
         IEDHJN
*                                                                A44908
INIT1937 EQU   *                                                 A44908
RESTART  EQU   *
         L     RCORE,FOUR(RWTGC)        GET THE ADDRESS OF THE   Y02027
*                                         OPEN WORK AREA         Y02027
         L     RDCB,DXUDCBAD            GET THE ADDRESS OF THE   Y02027
*                                         USER'S DCB             Y02027
         L     RF,CVT                   GET THE CVT ADDRESS      Y02027
         L     RF,AVTCVTPT(RF)          GET THE TCX ADDRESS      Y02027
         L     RAVT,TCXAVT              GET THE AVT ADDRESS      Y02027
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S22026
*                                                             *  S22026
*        THIS SECTION OF CODE LINKS THE STCB FOR THE SEND     *  S22026
*        SCHEDULER INTO THE STCB CHAIN OF THE LCB BY          *  S22026
*        PRIORITY.  FOR TERMINALS ATTACHED TO CONCENTRATORS,  *  S22026
*        IT LINKS THE CONCENTRATOR SEND SCHEDULER INTO THE    *  S22026
*        ELEMENT CHAIN OF THE DATA READY QUEUE.               *  S22026
*                                                             *  S22026
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S22026
         L     RJ,AVTRNMPT              ADDRESS OF THE TERMNAME
*                                         TABLE
         LH    RF,TNTLEN                THE NUMBER OF ENTRIES IN THE
*                                         TERMNAME TABLE
          LA   RDEB,TNTFIRST            ADDRESS OF FIRST ENTRY
         SR    RC,RC                    CLEAR LENGTH REGISTER
          IC   RC,TNTENLEN              LENGTH OF COMPARAND FIELD
         SR    RUCB,RUCB                CLEAR FOR INSERT         X01004
         IC    RUCB,DCBEIOBX            GET LENGTH OF LCB        X01004
TERMAD    EQU  *
         LA    RDEB,0(RC,RDEB)          TERMINAL TABLE ADDRESS
         MVC   AVTGETMN+5(THREE),0(RDEB) MOVE THE CURRENT TERMINAL
*                                          ADDRESS INTO A SCRATCH
*                                          AREA
         L     RTIOT,AVTGETMN+4         LOAD THE TERMINAL ADDRESS
         L     RTIOT,0(RTIOT)           ADDRESS OF THE DESTINATION
*                                         QCB
         L     RJ,QCBRELLN              LOAD THE ADDRESS OF THE DCB
         LA    RJ,0(RJ)                 CLEAR THE HIGH ORDER BYTE
*                                         OF THE ADDRESS
         LA    RDCB,ZERO(RDCB)          CLEAR THE HIGH ORDER BYTE
*                                         OF THE DCB ADDRESS
         CR    RJ,RDCB                  IS THIS THE CORRECT DCB
         BNE   TERMADL                  GO TO NEXT ENTRY
         L     RJ,DCBDEBAD              LOAD THE ADDRESS OF THE DEB
         CLC   EXTNTCNT(ONE,RJ),QCBRELLN    ARE THE NUMBER OF LINES
*                                           BEING OPENED LESS THAN
*                                           THE RELATIVE LINE NUMBER
*                                           OF THIS TERMINAL ENTRY
         BL    TERMADL                  YES, THEN IGNORE THIS QCB
         SR    RCORE,RCORE              CLEAR FOR THE RELATIVE LINE
*                                         NUMBER
         IC    RCORE,QCBRELLN           GET THE RELATIVE LINE
*                                         NUMBER ASSOCIATED WITH
*                                         THIS LINE
         SLL   RCORE,TWO                MULT BY 4                S22026
         L     RCORE,UCBADDR(RCORE,RJ)  GET THE UCB ADDRESS FOR
*                                         THIS LINE
         LTR   RCORE,RCORE              BRANCH IF THIS LINE      S22026
         BZ    TERMADL                  ASSOCIATED W/ A DD DUMMY S22026
*                                       ENTRY; DON'T PUT SEND    S22026
*                                         SCHEDULER IN THE STCB
*                                         CHAIN OF THIS QCB
         USING IEDQTRM,RJ               TERMINAL TABLE DSECT     S22027
         L     RJ,AVTGETMN+4            LOAD THE ADDRESS OF THE  S22027
*                                         TERMINAL ENTRY         S22027
         SR    RCORE,RCORE              CLEAR THE REGISTER       S22027
         IC    RCORE,TRMCHCIN           GET CHARACTERISTICS INDEXS22027
         BCTR  RCORE,ZERO1              DECREMENT BY ONE         S22027
         MH    RCORE,AVTDCTLN           MULTIPLY INDEX BY THE  @G36XRNP
*                                       SIZE OF A DCT ENTRY    @G36XRNP
         A     RCORE,AVTCSTCS           DCT BASE                 S22027
         DROP  RCORE                                           @G36XRNP
         USING IEDDCT,RCORE             DCT ADDRESSABILITY     @G36XRNP
         TM    DCTBYTE2,DCTBRDST        IS THIS A BROADCAST TRM@G36XRNP
         BO    TERMADL                  DO NOT PUT SEND          S22027
*                                         SCHEDULER IN THE STCB  S22027
*                                         CHAIN                  S22027
         LR    RJ,RCORE                 SAVE DCT ADDRESS       @G36XRNP
         LA    RE,QCBSTVTO              LOAD THE ADDRESS OF THE@G36XRNP
*                                         SUBTASK VECTOR INDEX
         ST    RE,AVTGETMN              STORE IT IN THE AVT    @G36XRNP
         LA    RE,IOBPTR                LOAD THE IOB OFFSET    @G36XRNP
         L     RCORE,DCBIOBAD           LOAD THE STARTING ADDRESS
*                                         OF THE IOB
         SR    RCORE,RE                 SUBTRACT THE IOB OFFSET@G36XRNP
*                                       FROM THE IOB START ADDRESS
         SR    RE,RE                    CLEAR LINE REGISTER
         IC    RE,QCBRELLN              GET RELATIVE LINE NUMBER
         DROP  RCORE                    DROP DCT ADDRESSABILITY@G36XRNP
         USING IEDQLCB,RCORE            LCB ADDRESSABILITY     @G36XRNP
LCBAD    EQU   *
         LA    RCORE,ZERO(RUCB,RCORE)   BUMP LCB POINTER
         BCT   RE,LCBAD                 DECREASE LINE NUMBER
         TM    DCTBYTE1-IEDDCT(RJ),DCTUMASK IS THIS GEN POLL   @G36XRNP
*                                       CLUSTER                @G36XRNP
         BO    SETDCBAD                 YES BYPASS STCB SETUP  @ZM46673
         CLI   QCBSTVTO,CSVTO           DOES VECTOR TABLE OFFSET S22026
*                                       POINT TO CONC SCHED      S22026
         BNE   NOCONC                   BRANCH IF NO             S22026
         CLI   QCBDSFLG,QCBDRQQ         IS THIS A DATA READY     S22026
*                                         QUEUE QCB              S22026
         BNE   NODIAL                   BRANCH IF NO             S22026
NOCONC   EQU   *                                                 S22026
         MVI   AVTOPERT,TCAMER16        SET ERROR CODE           S22027
         CLI   QCBSTPRI,NOTDIAL         DIAL TERMINAL            A42381
         BNE   DIALINE                  BRANCH YES               A51796
         TM    LCBSTAT2,LCBDIAL         DIAL LINE                A51796
         BZ    NODIAL                   BOTH AGREE               A51796
         B     GETERR                   PASS ERROR ON            A51796
DIALINE  EQU   *                                                 A51796
         TM    LCBSTAT2,LCBDIAL         DIAL LINE                A51796
         BZ    GETERR                   NO, EXIT FOR ERROR WORK  A42381
NODIAL   EQU   *                                                 A42381
         CLC   QCBSTCHN(THREE),AVTGETMN+1    IS THIS QCB ALREADY
*                                            CHAINED OFF THE LCB
         BNE   SETLINE                  YES, THEN DO NOT RECHAIN
*                                         THE QCB
         EJECT
         USING IEDQTRM,RJ               TERMINAL TABLE DSECT
*                                       RE-ASSIGN RJ AS THE BASE
*                                       FOR THIS DSECT
         L     RJ,AVTGETMN+4            LOAD THE ADDRESS OF THE
*                                         TERMINAL ENTRY
         TM    TRMDEVFL,BFRDTERM        IS THIS A BUFFERED TERMINAL
         BZ    SETDUMEL                 NO, BRANCH
         MVI   LCBRSKEY,BUFTSCHD        MOVE THE BUFFERED TERMINAL
*                                         SCHEDULER OFFSET INTO THE
*                                         LCB
         MVI   LCBRSPRI,EQULPRI         FORCE AN EQUAL PRIORITY FOR
*                                         BUFFERED TERMINALS
         B     SETUP                    BRANCH TO CHAIN THE STCB'S
SETDUMEL EQU   *
         TM    QCBDSFLG,QCBTSQ          TS TERMINAL ON LINE      S22029
         BZ    NOTTSQ                   BRANCH IF NO             S22029
         MVI   LCBRSPRI,SENDPRI         FORCE SEND PRIORITY      S22029
NOTTSQ   EQU   *                                                 S22029
         TM    LCBSTAT2,LCBDIAL         DIAL LINE                A51796
         BNZ   DIALOUT1                 BRANCH IF DIAL TO CHAIN  A48258
*        THE STCB'S FROM THE DIAL OUT CALL QUEUE.  THE SCHEDULAR A48258
*        PRIORITY HAS BEEN SET ALREADY                           A48258
         TM    0(RPARC),OUTONLY         IS THIS LINE TO BE OPENED
*                                         FOR OUTPUT ONLY
         BNO   CLEARPRI                 NO, BRANCH
         MVC   LCBSTCBA(THREE),AVTDELAD+1    MOVE THE ADDRESS OF
*                                            THE DUMMY LAST ELEMENT
*                                            INTO THE LCB
CLEARPRI EQU   *
         SR    RJ,RJ                    CLEAR A PRIORITY REGISTER
         IC    RJ,DCBCPRI               PICK UP THE DCB PRIORITY
         SLA   RJ,FIVE                  SHIFT PRIORITY TO LEFT   S22026
         BNZ   STCPRI                   BRANCH IF A PRIORITY HAS S22026
*                                         BEEN SPECIFIED IN DCB  S22026
         LA    RJ,SENDPRI               NO PRIORITY SPECIFIED,   S22026
*                                         LOAD DEFAULT VALUE     S22026
*                                                                S22026
STCPRI   EQU   *                                                 S22026
         STC   RJ,LCBRSPRI              SET THE TCAM PRIORITY IN
*                                         THE LCB
SETUP EQU *
         LA    RJ,LCBRSKEY              SET THE CHAIN POINTER
         LA    RDCB,LCBPRI              GET THE ADDRESS OF THE LINK
*                                         FIELD
*                                                                S22026
* IF TERMINAL ATTACHED TO A CONCENTRATOR, SET UP REGISTERS TO    S22026
* MOVE STCB INTO DATA READY QUEUE ELEMENT CHAIN                  S22026
*                                                                S22026
         CLI   QCBSTVTO,CSVTO           DOES VECTOR TABLE OFFSET S22026
*                                         POINT TO CONC SCHED    S22026
         BNE   NOCONCX                  BRANCH IF NO             S22026
         CLI   QCBDSFLG,QCBDRQQ         IS THIS A DATA READY     S22026
*                                         QUEUE QCB              S22026
         BE    NOCONCX                  BRANCH IF YES            S22026
         LR    RJ,RTIOT                 ADDR OF QCB              S22026
         AH    RJ,QCBEXTO               ADDR OF QCBE             S22026
         L     RJ,QCBECONC-1-IEDQQCBE(RJ)  ADDR OF CONC TERM     S22026
         L     RJ,TRMDESTQ-1-IEDQTRM(RJ)  ADDR OF DATA READY Q   S22026
         LR    RDCB,RJ                  ADDR OF DATA READY Q     S22026
         SH    RDCB,AVTHA4                MINUS FOUR             S22026
NOCONCX  EQU   *                                                 S22026
         TM    ZERO(RPARC),OUTONLY      INPUT ONLY              SA57354
         BZ    SETDCBAD                 BR YES SKIP SETTING UP  SA57354
*                                         SEND SCHED STCB       SA57354
         L     RJ,0(RJ)                 GET THE ADDRESS OF THE
*                                         FIRST ELEMENT ON THE CHAIN
COMPRITY EQU   *
         CLC   FOUR(ONE,RJ),QCBSTPRI    COMPARE THE PRIORITIES OF
*                                         THE 2 ELEMENTS
         BL    INSERT                   IF THE CURRENT ELEMENT IS
*                                         LOW, INSERT THE NEW
*                                         ELEMENT ABOVE IT ON THE
*                                         CHAIN
         LR    RDCB,RJ                  COPY THE ADDRESS OF THE
*                                         CURRENT ELEMENT
         L     RJ,FOUR(RJ)              GET THE ADDRESS OF THE NEXT
*                                         ELEMENT
         B     COMPRITY                 CHECK THE NEXT ELEMENT
INSERT   EQU   *
         MVC   FIVE(THREE,RDCB),QCBSTCHN     PUT THE NEW STCB INTO
*                                              THE CHAIN
         MVC   QCBSTCHN(THREE),QCBSLINK MOVE THE STCB LINK POINTER
*                                         INTO THE STCB POINTER IN
*                                         THE QCB
         IC    RE,QCBSTPRI              SAVE THE PRIORITY OF THE
*                                         NEW ELEMENT
         ST    RJ,QCBSTPRI              SET THE ENDING ADDRESS OF
*                                         THE CHAIN
         STC   RE,QCBSTPRI              RESTORE THE SAVED PRIORITY
SETDCBAD EQU   *                                                 A44908
         L     RDCB,FOUR(RWTGC)         GET THE ADDRESS OF THE   Y02027
*                                         OPEN WORK AREA         Y02027
         L     RDCB,DXUDCBAD-DXDSCB(RDCB)  LOAD THE ADDRESS OF   Y02027
*                                         THE CURRENT DCB        Y02027
         EJECT
SETLINE  EQU   *
         IC    RE,QCBRELLN              GET THE RELATIVE LINE NUMBER
         TM    LCBSTAT2,LCBDIAL         DIAL LINE                A51796
         BNZ   LINENT2                  YES, BRANCH
         CLI   QCBSCBOF,X'00'           IS THE SCB OFFSET ZERO
         BNE   CLEARIT                  NO, BRANCH
         NC    QCBEOLDT+1(2),QCBEOLDT+1 IS THE STATION CONTROL     0109
*                                         BLOCK ADDRESS ALREADY
*                                         ZERO
         BZ    TERMADL                  YES, BRANCH
         MVC   LCBSCBDA(3),QCBEOLDT+1   MOVE THE ADDRESS OF THE
*                                         STATION CONTROL BLOCK
*                                         DIRECTORY INTO THE LCB
         MVC   LCBSCBA(3),QCBEOLDT+1   INIT FIRST SCB POINTER
CLEARIT  EQU   *
         XC    QCBEOLDT+1(3),QCBEOLDT+1
         MVC   QCBSTAT,QCBLINK+2        SET THE QCB STATUS IN CASE
*                                         THIS IS A RESTART
TERMADL  EQU   *
          LA   RDEB,3(RDEB)             BUMP TERMNAME TABLE ADDRESS
          BCT  RF,TERMAD                DECREASE ENTRIES BY 1
*
         EJECT
         MVC   0(5,RWTGC),TCAMLD4       MOVE THE ID TTR AND LENGTH
*                                         TO THE WHERE-TO-GO TABLE
         L     RCORE,FOUR(RWTGC)        LOAD THE ADDRESS OF THE  Y02027
*                                         OPEN WORK AREA
         STM   RE,RJ,ZERO1(RCORE)       SAVE THE REGISTERS FOR   Y02027
*                                         IGG01938
*
RELOOP   EQU   *
         LA    RWTGC,8(RWTGC)           INCREMENT POINTER TO NEXT
*                                         ENTRY IN WTG TABLE
         LA    RPARC,4(RPARC)           INCREMENT POINTER TO NEXT
*                                         DCB
         CLC   0(2,RWTGC),AMIDCNST      IS IGGO1930 REQUIRED AGAIN
*
         BE    RESTART                  NO, GO CHECK FOR END OF THE
*                                         WTG TABLE
         CLC   0(2,RWTGC),OPIDCNST      TEST FOR END OF WTG TABLE
*
         BNE   RELOOP                   BRANCH IF NOT AT THE END S22025
*
         LR    RPARC,RPAR               RESET POINTER TO FIRST OF
*                                         DCB PARAMETER LIST
         LA    RWTGC,32(RWTG)           RESET POINTER TO FIRST OF
*                                         THE WTG TABLE
ZCHEK    EQU   *
         CLI   0(RWTGC),X'00'           IS THIS ENTRY'S ID ZERO
         BNE   XCTLRTN                  IF NOT, TRANSFER CONTROL
*
         LA    RWTGC,8(RWTGC)           PICK UP ADDRESS OF NEXT
*                                         ENTRY IN WTG TABLE
         LA    RPARC,4(RPARC)           BUMP POINTER TO NEXT DCB
*
         B     ZCHEK                    CHECK NEXT ENTRY ID      S22025
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF CODE IS USED TO SAVE THE ID OF THE       *
*        CURRENT OPEN EXECUTOR AND TO MOVE THE ID OF THE          *
*        ERROR RECOVERY MODULE INTO THE WHERE-TO-GO PARAMETER     *
*                            LIST                                 *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
GETERR   EQU   *
         L     RCORE,FOUR(RWTGC)        LOAD THE ADDRESS OF THE  Y02027
*                                         OPEN WORK AREA
         MVC   AVTOPXCL(TWO),AMIDCNST   MOVE THE ID OF THE CURRENT
*                                         EXECUTOR INTO THE AVT FOR
*                                         THE RETURN TO THIS EXECUTOR
         MVC   0(FIVE,RWTGC),OPENERP    MOVE THE ID AND TTR FOR THE
*                                         ERROR MODULE INTO THE
*                                         WHERE-TO-GO PARAMETER LIST
*
*   TRANSFER CONTROL TO THE OPERATING SYSTEM
*
XCTLRTN  EQU   *
         LA    RJ,DXCCW12-DXLBL(RCORE)  LOAD THE ADDRESS OF THE  Y02027
*                                         LAST DOUBLE WORD IN THE
*                                         OPEN WORK AREA
         MVC   6(2,RWTG),0(RWTGC)       MOVE EXECUTOR ID TO
*                                       PARAMETER LIST
         MVC   14(3,RWTG),2(RWTGC)      MOVE TTR TO LIST
         XCTL  DE=(RWTG),SF=(E,(15))
         EJECT
DIALOUT1 EQU   *
         TM    QCBDSFLG,QCBDISK+QCBCORE TSO ONLY QUEUEING        S22029
         BZ    SETLINE                 YES  BRANCH               S22029
         L     RJ,DCBIOBAD              LOAD THE IOS ADDRESS OF THE
*                                         IOB FOR THIS LINE
         LA    RDCB,IOBPTR              LOAD THE IOB DISPLACEMENT
         SR    RJ,RDCB                  ACCESS THE IOS ADDRESS OF
*                                         THE LCB
         AR    RJ,RUCB                  LOAD THE BEGINNING ADDRESS
*                                         OF THE FIRST LCB
         LA    RDCB,EIGHT               LOAD THE OFFSET TO THE DIAL
*                                         OUT CALL QUEUE
         SR    RJ,RDCB                  GET THE BEGINNING ADDRESS
*                                         OF THE DIAL OUT CALL QUEUE
         L     RDCB,FOUR(RJ)            GET THE ADDRESS OF THE NEXT
*                                         ELEMENT ON THE CHAIN
DIALCOMP EQU   *
         CLC   ONE(THREE,RJ),FIVE(RDCB) HAVE WE FOUND THE END OF
*                                         THE CHAIN
         BE    TESTART                  YES, TEST FOR RESTART     M1178
         L     RDCB,FOUR(RDCB)          GET THE ADDRESS OF THE NEXT
*                                         ELEMENT
         B     DIALCOMP                 TEST FOR THE END OF THE
*                                         CHAIN
TESTART  EQU   *                                                  M1178
         TM    AVTBIT2,AVTSTRTN         IS RESTART IN PROGRESS    M1178
         BNO   TESTQCB                  IF NOT, CHECK QCB         M1178
         ST    RTIOT,LCBLINK-ONE        IF SO, SAVE QCB ADDRESS   M1178
TESTQCB  EQU   *                                                  M1178
         TM    QCBSTPRI,CLOCK           CALL-UP TIME SPECIFIED    M1178
         BO    SETDCBAD                 RESTORE DCB ADDRESS    @OY12667
TESTNO   EQU   *                                               @SA70305
         NC    QCBMSGCT,QCBMSGCT        FEFO CHAIN EMPTY?      @SA70305
         BZ    SETDCBAD                 YES,RESTORE DCB ADDR   @SA70305
         TM    QCBSTPRI,INTERVAL        INTERVAL SPECIFIED     @OY12667
         BNO   INSERT                   NO, PROCEED            @OY12667
         OI    QCBLINK+2,QCBSCHDL       SCHEDULE SEND          @OY12667
         B     INSERT                   NO ,INSERT STCB        @SA70305
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF CODE COMPUTES THE RELATIVE POSITION      *
*        IN THE TERMNAME TABLE OF THE LINE ENTRY FOR ANY GIVEN    *
*                                  LINE
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
LINENT2  EQU   *
         L     RJ,AVTGETMN+4            LOAD THE TERMINAL TABLE
*                                         ADDRESS FOR THIS LINE
         TM    0(RJ),LINENTRY           IS THIS A LINE ENTRY IN THE
*                                         TERMINAL TABLE
         BZ    CLEARIT                  NO,CHECK NEXT ENTRY IN @YA11150
*                                         THE TERMNAME TABLE
         LR    RTIOT,RE                 COPY THE RELATIVE LINE
*                                         NUMBER
         L     RJ,DCBDEBAD              LOAD THE ADDRESS OF THE
*                                         CURRENT DEB
         IC    RE,EXTNTCNT(RJ)          GET THE NUMBER OF LINES
*                                         BEING OPENED BY THIS DCB
         CR    RTIOT,RE                 IS THE LINE ENTRY NUMBER
*                                         GREATER THAN THE NUMBER
*                                         OF LINES
         BH    TERMADL                  YES, BRANCH
*
*
         USING IEDQTNTD,RJ
*
*
         L     RJ,AVTRNMPT              LOAD THE ADDRESS OF THE
*                                         TERMNAME TABLE
         LH    RTIOT,TNTLEN             LOAD THE NUMBER OF ENTRIES
*                                         IN THE TERMNAME TABLE
         SR    RTIOT,RF                 SUBTRACT FROM THE TOTAL
*                                         NUMBER OF ENTRIES, THE
*                                         NUMBER OF UNCHECKED
*                                         ENTRIES IN THE TABLE
         LA    RTIOT,1(RTIOT)           ADD 1 TO THE DIFFERENCE
         STH   RTIOT,LCBLNENT           STORE THE INDEX OF THE
*                                         LINE ENTRY FOR THIS
*                                         PARTICULAR LINE IN ITS
*                                         LCB
         B     TERMADL                  CONTINUE CHECKING FOR THE
*                                         REMAINING LINES IN THIS
*                                         LINE GROUP
         EJECT
         EJECT
OPIDCNST DC    C'0S'                    FLAG ENDING WTG TABLE    S22025
ZERO1    EQU   0                        CONSTANT ZERO            S22027
ONE      EQU   1                        ONE                      S21903
TWO      EQU   2                        TWO                      S21903
THREE    EQU   3                        THREE                    S21903
FOUR     EQU   4                        FOUR                     S21903
FIVE     EQU   5                        FIVE                     S21903
EIGHT    EQU   8                        EIGHT                    S21903
EXTNTCNT EQU   16                       OFFSET TO NO. OF EXTENTS Y02027
UCBADDR  EQU   28                       UCB ADDR OFFSET          S21903
IOBPTR   EQU   32                       IOB OFFSET               S21903
CVT      EQU   16                       CVT DISPLACEMENT         Y02027
ZERO     EQU   X'00'                    HEXADECIMAL ZERO         S21903
BFRDTERM EQU   X'02'                    BUFRED TERM DEVICE FLAG  S21903
TCAMER08 EQU   X'08'                    ERROR IDENTIFIER         S21903
NODIALRS EQU   X'0C'                    NON-DIAL REC'V SCHED     S21903
OUTONLY  EQU   X'0F'                    OUTPUT ONLY LINE         S21903
GRAPHICS EQU   X'10'                    GRAPHICS DEVICE          S21903
TIMDELAY EQU   X'14'                    TIME DELAY OFFSET        S21903
DIALRECV EQU   X'18'                    DIAL REC'V SCHED         S21903
BUFTSCHD EQU   X'1A'                    BUFRD TERM SCHED         S21903
LOCALKEY EQU   X'1E'                    LOCAL REC'V SCHED        S21903
SENDPRI  EQU   X'20'                    SEND PRIORITY            S21903
CSVTO    EQU   X'20'                    CONC SEND SCHEDULER      S22026
*                                         VECTOR TABLE OFFSET    S22026
EQULPRI  EQU   X'40'                    EQUAL PRIORITY           S21903
INTERVAL EQU   X'70'                    CINTVL PRIORITY          S21903
CLOCK    EQU   X'80'                    CLOCK PRIORITY           S21903
LINENTRY EQU   X'80'                    LINE ENTRY               S21903
DIAL     EQU   X'90'                    DIAL LINE                S21903
TCAMER16 EQU   X'16'                    CHAR ERR INDICATOR       A42381
NOTDIAL  EQU   X'40'                    LEASED QCB INDICATOR     A42381
CBRDCAST EQU   X'40'                    BROADCAST TERMINAL       S22027
         EJECT
NENT     EQU   2
LENGTH   EQU   NENT*6+6
NODBWDS  EQU   (LENGTH+7)/8
ORGIDTTR EQU   1024-NODBWDS*8
END      EQU   *
         ORG   IGG01937+ORGIDTTR        ADJUST LOCATION CTR      S21903
UNUSED   EQU   *-END
TCAMLD4  DC    C'38'                    MODULE ID SUFFIX         S22025
         DC    XL4'00'                  TTR AND LENGTH           S22025
OPENERP  DC    C'33'                    OPEN ERROR HANDLER
         DC    XL4'0'                     TTR AND LENGTH
IDEND    DC    X'0000'                  END OF TTR TABLE         S22025
         CNOP  4,8
LOADID   DC    C'019'                   TRANSIENT MODULE ID      S22025
         DC    YL1(X'80'-NODBWDS)       ADJUST MODULE LENGTH TO  S22025
*                                       X'400' BYTES             S22025
         EJECT
FORCORE  DSECT                                                   Y02027
         IECDSECT                                                Y02027
         EJECT
         TTCXD                                                   Y02027
         EJECT
         TTRMD
         EJECT
         TAVTD
         EJECT
         TLCBD
         EJECT
         EJECT
         TTNTD
         EJECT
         TQCBD
         EJECT                                                   S22026
         TQCBED                                                  S22026
         EJECT                                                   S22026
         DCBD  DSORG=TX
*
         EJECT
         TDCTD                                                 @G36XRNP
         END
