19R1     TITLE ' IGG019R1 - DIAL RECEIVE SCHEDULER '
         SPACE 1
IGG019R1 CSECT
         SPACE 3
         SPACE 3
*CHANGE ACTIVITY= AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A434000                                                        SA59166
*C458040                                                         S22029
*D598000,458070                                                  S22029
*A274000,396000,458030,588000                                    S22025
*C206000,278000,293200,458060                                    S22025
*D283000-286000,414000,594500,598000-610000                      S22025
*C204000,293236.293242,451000,664000,762000,778800-780400,781800 S22025
*A232000,292000                                                 SA53611
*C012000                                                         S21903
*D612000                                                         S21903
*A534000,548000                                                 SA59006
*C072200,664000                                                  X01004
*D776000                                                         X01004
*C808000                                                         Y01948
*A184000,750000,781800                                           Y05330
*C200000-204000,246000,262000-272000,293220-293224,293390-293400,Y05330
*C368000-372000,448000-450000,740000                             Y05330
*D190000-191000,204000,250000-252000,262000-272000,293230-293242,Y05330
*D293400,368000-372000,454000-456000                             Y05330
*A 549000                                                       SA68698
*D 558000-564000                                                SA68698
*C502000                                                        SA69638
*A578000                                                       @YA08092
*A676000                                                       @SA73490
*A676600,677400                                                @SA74959
*A293450                                                       @SA75440
*D676200-677700                                                @SA75437
*C535200                                                       @YA10696
*C408000,A770000                                               @OY12954
*C438000,A774000                                               @OY13276
*C420000                                                       @OY13287
*C534200                                                       @OY13624
*A219000                                                       @OY14092
*C276000,595000-595500                                         @OY14092
*A308000,380000                                                @OY14058
*A626000,644000,652000-660000                                  @OX17150
*D438000-439000                                                @OX19964
         SPACE 3
***********************************************************************
*                                                                     *
*MODULE-NAME= IGG019R1                                           S21903
*                                                                S21903
*DESCRIPTIVE-NAME= DIAL RECEIVE SCHEDULER                        S21903
*                                                                S21903
*COPYRIGHT= 'NONE'                                               S21903
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
*FUNCTION -- TO INITIATE A RECEIVE OPERATION AND TO SCAN THE DIAL OUT *
*   CALL QUEUE WHEN A LINE IS AVAILABLE FOR OUTPUT.  TO POST A        *
*   DESTINATION QCB TO THE TIME QUEUE TO OBSERVE A SPECIFIED DELAY    *
*   (CLOCK OR INTVL OPTION ON TERMINAL MACRO).                        *
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION                         *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTS TO ELEMENT (LCB OR QCB)                               *
*   R11  DISPATCHER BASE                                              *
*   R13  ADDRESS OF AVTSAVE2                                          *
*   R15  ENTRY POINT ADDRESS                                          *
*                                                                     *
*OUTPUT --                                                            *
*   1.  LCB IS POSTED TO ITSELF TO ACTIVATE NEXT STCB                 *
*   2. ERB IS POSTED TO OBTAIN BUFFERS FOR RECEIVE OPERATION          *
*   3.  LCB IS POSTED TO TIME QUEUE WHEN CORE OR DISK QUEUE IS FULL   *
*   4.  NEXT STCB IN THE LCB'S STCB CHAIN IS TO ACTIVATED             *
*   R1  POINTS TO ELEMENT IF POSTING IS TO BE DONE                    *
*   R11 DISPATCHER BASE                                               *
*   R13  ADDRESS OF AVTSAVE2                                          *
*                                                                     *
*EXTERNAL ROUTINES --                                                 *
*   'IEDQTNT' TO CONVERT TWO BYTE TNT OFFSET TO TERMINAL ADDRESS      *
*                                                                     *
*   'TIME' MACRO - TO OBTAIN TIME OF DAY TO COMPUTE DELAY             *
*                                                                     *
*   EXCPVR - TO DISABLE AN OUTPUT ONLY LINE                           *
*                                                                     *
*   DSPUNAVR - TO ADJUST STCB CHAIN OF LCB                            *
*                                                                     *
*   DSPLIFOR - TO PLACE SEND STCB AT TOP OF LCB CHAIN                 *
*                                                                     *
*EXITS-NORMAL --                                                      *
*   R1  ADDRESS OF ELELMENT IS POSTING IS DONE                        *
*   R13  ADDRESS OF AVTSAVE2                                          *
*   R11  DISPATCHER BASE                                              *
*   RETURN IS ON R11 WITH AND OFFSET TO HAVE THE DISPATCHER PROVIDE   *
*   THE DESIRED SERVICE.                                              *
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TSCTBD                                                            *
*   TAVTD                                                             *
*   TLCBD                                                             *
*   TQCBD                                                             *
*   DCBD                                                              *
*   TTRMD                                                             *
*   TCCWD                                                             *
*   TDEBD                                                             *
*                                                                     *
*ATTRIBUTES -- ENABLED, PROBLEM PROGRAM MODE, RE-USABLE, REFRESHABLE  *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   THE DIAL OUT CALL QUEUE IS SCANNED FOR AN OUTGOING CALL WHENEVER  *
*   THE NEGATIVE RESPONSE BIT IS SET IN LCBSTAT2.  THE MOST ECONOMICAL*
*   CALL WILL BE MADE (WATS TECHNIQUE - LOWEST RLN) UNLESS MESSAGE    *
*   PRIORITY LEVELS ARE SPECIFIED.  THE HIGHEST PRIORITY MESSAGE      *
*   WILL BE SENT  USING THE LINE THAT IS CURRENTLY AVAILABLE.         *
*                                                                     *
*   FOR QCB'S WITH CLOCK OR INTVL, THIS MODULE WILL COMPUTE THE TNT   *
*   OFFSET FOR THE TERMINAL WHICH IS TO BE CALLED AND WILL POST THE   *
*   QCB TO THE TIME QUEUE AFTER THE CALL IS MADE.                     *
*                                                                     *
***********************************************************************
         EJECT
*
*        REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1                        ELEMENT REGISTER
R2       EQU   2                        WORK REGISTER
RSTCB    EQU   3                  STCB BASE
RLCB     EQU   4                        LCB BASE
RA       EQU   5                        WORK REGISTER
RWORK    EQU   6                  TEMPORARY WORK REG
RQCB     EQU   7                        ENTRY QCB ADDRESS (LCB)
RB       EQU   8                        WORK REGISTER
RDCB     EQU   10                 DCB ADDR
RSUPBASE EQU   11                       DISPATCHER BASE REGISTER
RBASE    EQU   12                 SUBTASK BASE REG
R13      EQU   13                       SAVE AREA ADDRESS (AVTSAVE2
R14      EQU   14                 RETURN ADDR REG
R15      EQU   15                       ENTRY BASE REGISTER
*
         USING *,R15                                             Y05330
         BAL   RBASE,ID                 BYPASS ID - SET NEW BASE Y05330
         DROP  R15                      DROP TEMPORARY BASE  ASE Y05330
         USING *,RBASE
IGG019R1 IEDHJN
         SPACE 1                                                 S22025
         USING IEDQSTCB,RSTCB
         USING IEDQQCB,RQCB
         USING IEDQDISP,RSUPBASE
         USING AVTSAVE2,R13
ID       DS    0H                                                Y05330
         LA    RLCB,AVTEZERO(,R1)       SET THE LCB ADDRESS      S22025
         CLI   QCBSTVTO,DSPDIASC       THIS AN LCB OR QCB
         BE    LCB                      BRANCH, IT'S AN LCB
         SH    RSTCB,QCBBKUP            BACK UP TO LCB
         LR    RLCB,RSTCB               LCB ADDRESS TO LCB REGISTER
         USING IEDQLCB,RLCB
         L     RDCB,LCBDCBPT            GET DCB BASE
         NI    LCBCHAIN,AVTEFF-LCBCTLMD CLEAR DIAL-UP REQUEST  @OY14092
         TM    QCBSTAT,QCBSCHDL         QCB COME OFF TIME Q WITH
*                                         LCB FOUND BY SEND SCHEDULER
         BZ    NOSRCH                   NO
         EJECT
TNTFIND  EQU   *
*        SOURCE MUST BE FOUND AND DIAL UP TO POLL
         LA    RQCB,AVTEZERO(,RQCB)    CLEAR HI ORDER BYTE
         NI    QCBSTAT,AVTEFF-QCBSCHDL  TURN OFF SEITCH
         L     RWORK,DCBDEBAD-IHADCB(,RDCB) DEB ADDRESS         SA53611
         TM    DEBOPATB-DEBNMSUB(RWORK),OUTPUT OUTPUT ONLY      SA53611
         BO    POSTFRE                  BRANCH IF YES           SA53611
         SR    R2,R2                    CLEAR TNT REGISTER
         LR    RB,R2                    CLEAR WORK REGISTER
         L     RA,AVTRNMPT              TNT CODE BASE
         USING IEDQTNTD,RA              TERMINAL NAME TABLE DSECT
         IC    RB,TNTENLEN              GET THE SIZE OF A NAME
*                                         TABLE ENTRY
         LA    RA,TNTFIRST-THREE        FIRST ENTRY - 3          Y05330
*                                         FIRST ENTRY IN THE TABLE
TNTLOOP  EQU   *
         LA    R2,1(,R2)                BUMP TNT OFFSET
         LA    RA,THREE(RB,RA)          POINT TO THE TERMINAL ENTRY
*                                         ADDRESS
         ICM   RWORK,AD,0(RA)           TERMINAL ENTRY ADDRESS   Y05330
         CLM   RQCB,AD,ONE(RWORK)       SAME QCB ADDRESS         Y05330
         BNE   TNTLOOP                  NO, LOOP
         OI    LCBCHAIN,LCBCTLMD        SET DIAL=UP REQUEST    @OY14092
         STH   R2,LCBTTCIN              SET UP THE SOURCE        S22025
REPOLL   EQU   *                                                 S22025
         NI    LCBSTAT2,X'FF'-LCBNEGRP  RESET NEG RESPONSE       S22025
         B     POLL                     REQUEST BUFFERS          S22025
         EJECT
NOSRCH   EQU   *
         L     RWORK,DCBDEBAD-IHADCB(,RDCB) DEB ADDRESS         SA53611
         TM    DEBOPATB-DEBNMSUB(RWORK),OUTPUT OUTPUT ONLY      SA53611
         BNO   NOUTONLY                 BRANCH IF NO            SA53611
         OI    LCBSTAT2,LCBNEGRP        SET NEG RESPONSE        SA53611
NOUTONLY EQU   *                                                SA53611
         TM    LCBSTAT2,LCBNEGRP        NEGATIVE RESP READ TO DISAB
         BZ    LCB                      BRANCH IF NO             S22025
         BAL   R14,TNTSUB               GET DESTINATION ADDRESS  Y05330
         SPACE 1                                                 Y05330
         BZ    NORETRY                  ZERO, NO COUNT TO CLEAR  S22025
         USING IEDQTRM,R1                                        S22025
         SR    R2,R2                    CLEAR REGISTER TO ZERO   S22025
         LH    R2,TRMDEVFL              GET DEVICE FLAGS         S22025
         SLL   R2,TWEN6                 SHIFT TO RETRY BIT       S22025
         LTR   R2,R2                    RETRY VALUE SPECIFIRD    S22025
         BNL   NORETRY                  BRANCH IF NOT            S22025
         TM    TRMSTATE,TRMOPTFN        OPTION FIELDS SPECIFIED  S22025
         BO    OPTFL                    BRANCH IF YES            S22025
         LA    RWORK,TRMOPNO            GET ADDR OF FIRST DEVICE S22025
*                                         DEPENDENT FIELD        S22025
         B     RSCAN                    GO FIND RETRY COUNT FIELDS22025
OPTFL    EQU   *                                                 S22025
         SR    R14,R14                  CLEAR REGISTER TO ZERO   S22025
         IC    R14,TRMOPNO              NUMBER OF OPTION FIELDS  S22025
         LA    RWORK,TRMOPT(R14)        DEVICE DEPENDENT ADDR    Y05330
*                                         FIELD                  S22025
RSCAN    EQU   *                                                 S22025
         SR    R14,R14                  CLEAR REGISTER TO ZERO   S22025
         SR    R15,R15                  CLEAR REGISTER TO ZERO   S22025
         LH    R14,TRMDEVFL             DEV DEP FIELD FLAGS      S22025
         N     R14,AVTCLRHI             CLEAR HIGH TWO BYTES   @SA75440
         SR    RB,RB                    CLEAR REGISTER TO ZERO   S22025
         SRL   R14,FIVE                 ELIMINATE FIVE BITS      S22025
SHIFT    EQU   *                                                 S22025
         SRDL  R14,ONE                  MOVE IN ONE BIT          S22025
         SRL   R15,THIRTY1              MAKE REG ONE OR ZERO     S22025
         AR    RB,R15                   BUMP D.D. COUNTER        S22025
         LTR   R14,R14                  ALL BITS TESTED          S22025
         BNZ   SHIFT                    BRANCH IF NOT            S22025
BUMP     EQU   *                                                 S22025
         CH    RB,HONE                  RETRY COUNT FOUND        S22025
         BE    CLEAR                    BRANCH IF YES            S22025
         IC    R14,0(0,RWORK)           SIZE OF D.D. FIELD       S22025
         LA    RWORK,1(R14,RWORK)       BUMP PAST THIS D.D. FIELDS22025
         BCT   RB,BUMP                  CONTINUE LOOP TO RETRY   S22025
*                                         COUNT                  S22025
CLEAR    EQU   *                                                 S22025
         MVI   2(RWORK),AVTEZERO        MAKE RETRY FIELD ZERO    S22025
NORETRY  EQU   *                                                 S22025
         LR    R2,RLCB                  SAVE LCB ADDRESS
         LR    RWORK,RQCB               SAVE QCB ADDRESS
         LR    RSTCB,RQCB               SET FOR UNAVR
         LR    RQCB,RLCB                FOR UNVAR
         BAL   R14,DSPUNAVR             REMAOVE SEND SCHEDUALER
         LR    RQCB,RWORK               RESTORE QCB BASE
         LH    RWORK,QCBINTVL           GET THE AMOUNT OF DELAY
         N     RWORK,AVTCLRHI           CLEAR HIGH ORDER HALF WORD
         BZ    NOTIMEQ                  DIAL-UP READ ONLY      @OY14058
         CLI   QCBSTPRI,CLOCK           CLOCK OR INTVL
         BNE   TIMEEXIT                 BRANCH IF INTVL
*
         AR    RWORK,RWORK              DOUBLE
         TIME  BIN                      GET TIME OF DAY
         SRDL  R0,HALF                  SHIFT TIME TO R1
         D     R0,HUND                  CONVERT TIME TO SECONDS
*                          RESULT IS IN R0 + 1
         N     R1,CLRMASK               CLEAR UNNEEDED BITS
         CLR   R1,RWORK                 COMPARE TIME OF DAY TO CLOCK
*                                       REQUES
         BL    SAMEDAY                  BRANH IF REQUESTED TIME IS
*                                       TODAY
*
         A     RWORK,TW24HRS            ADD 24 HOURS - REQUESTED
*                                       INTERVAL TIME GOES PAST
*                                       MIDNIGHT
SAMEDAY  EQU   *
         SR    RWORK,R1                 DELAY (IN SECONDS)
TIMEEXIT EQU   *
         C     RWORK,TWELVHRS           IS DELAY OVER 12 HOURS
         BNH   LESS12                   BRANCH YES
*
         S     RWORK,TWELVHRS           DECREMENT 12 HORRS
         OI    QCBSTAT,QCBTIME          INDICATE MORE THAN 12 HOURS
LESS12   EQU   *
         STH   RWORK,QCBEOLDT           SET DELAY REQUESY
         MVI   QCBLKRLN,AVTEZERO        SET THE OFFSET TO THE QCB
*                                         FOR TIME DELAY
         STCM  RQCB,AD,QCBELCHN         QCB ADDRESS FOR POST     Y05330
         MVC   QCBSTCHN,QCBSLINK        REMOVE SEND SCHEDULER
         MVI   QCBPRI,PRIDSKRQ          SET QCB PRTY HIGHER     SA63954
         LR    R1,RQCB                  TIME Q PARAMETER
         L     R15,AVTHG01              BALR ENTRY
         BALR  R14,R15                  LINK TO IT
NOTIMEQ  EQU   *                                               @OY14058
         LR    RLCB,R2                  RESTORE LCB AADDRESS
         MVI   LCBTSTSW,AVTEFF          INDICCATE NO SOURCDE
         B     POSTFRE                  POST LCB TO ITSELF
         EJECT
LCB      EQU   *
         L     RDCB,LCBDCBPT            DCB BASE
         USING IHADCB,RDCB
         L     RWORK,LCBSCBA-1          GET THE SCB ADDRESS      S22025
         USING IEDQSCB,RWORK                                     S22025
         TM    SCBQTYPE,SCBBFMM         MIDDLE OF MESSAGE        S22025
         BO    REPOLL                   YES, BRANCH              S22025
         TM    AVTBIT1,AVTCLOSN         IS CLOSE DOWN IN PROGRESS
         BO    OUTONLY                  BRANCH YES-SEARCH QUEUE
         TM    LCBSTAT1,LCBOCNI         STOP LINE IN PROGRESS
         BZ    NOSTOP                   NO
         MVC   LCBQCBA,AVTHK+1          SET QCB ADDRESS FOR POST
         L     RQCB,LCBSTCBA-1          GET STCB               @OY12954
         LA    RQCB,0(RQCB)             CLEAR HIGH-ORDER BYTE  @OY12954
         SH    RQCB,QCBBKUP             BACKUP TO QCB          @OY12954
         CLI   QCBSTVTO,DSPSNDSC        IS SEND SCHED IN CHAIN @OY12954
         BNE   POSTIT                   BRANCH-NO              @OY12954
         CLI   QCBSTPRI,CALLOPT         CLOCK OR CINTVL        @OY12954
         BL    POSTIT                   BRANCH-NO              @OY12954
         NC    QCBMSGCT,QCBMSGCT        ANY MSGS ON QUEUE      @OY12954
         BNZ   POSTIT                   BRANCH-YES             @OY12954
         OI    LCBSTAT2,LCBNEGRP        TURN ON NEGREP         @OY12954
         B     NOUTONLY                 GO REMOVE SCH AND      @OY12954
*                                       OBSERVE NEW INTVL      @OY12954
*
NOSTOP   EQU   *
         USING IEDQSCB,RWORK
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN EXTENDD LOCK
         BNZ   REPOLL                   RE-POLL                @OY13287
         DROP  RWORK
         TM    AVTBIT1,AVTTSON          IS TSO IN SYSTEM?           TSO
         BZ    TSO1                     BRANCH ON NO                TSO
         L     RWORK,AVTTSOPT           GET ADDRESS OF TSO SCHED    TSO
         L     RWORK,TSISCHED-IEDQTSI(,RWORK)                       TSO
         BAL   R2,4(,RWORK)             BRANCH TO ROUTINE           TSO
TSO1     EQU   *                                                    TSO
         L     R2,DCBDEBAD              DEB ADDRESS             SA59166
         TM    DEBOPATB-DEBNMSUB(R2),OUTPUT OUTPUT ONLY LINE    SA59166
         BNO   TSO1A                    BRANCH IF NO            SA59166
         OI    LCBSTAT2,LCBNEGRP        SET NEG RESP FLAG       SA59166
TSO1A    EQU   *                                                SA59166
         TM    LCBSTAT2,LCBNEGRP        NEGATIVE RESPONSE TO POLL
         BNO   POLL                     NO,POLL AGAIN          @OY13276
         EJECT
OUTONLY  EQU   *
         SR    R2,R2                    CLEAR WORK REGISTER
         SR    RQCB,RQCB                ASSUME ORIGIN NOT USED
         BAL   R14,TNTSUB               GET DESTINATION ADDRESS  Y05330
         SPACE 1                                                 Y05330
         BZ    NOSOURCE                 BRANCH IF NO ORIGIN      S22025
         CLI   LCBTSTSW,AVTEFF          NO LONGER HAVE CONNECTION
         BE    NOSOURCE                 BRANCH IF YES
         L     RQCB,TRMDESTQ-1-IEDQTRM(,R1) QCB ADDRESS
         TM    QCBDSFLG,QCBDISK+QCBCORE TIME-SHARING QUEUES?        TSO
         BZ    REQBUF                  YES SKIP DIAL CALL QUEUE  S22029
         TM    QCBFLAG,QCBTSSES        QCB IN TSO SESSION        S22029
         BO    REQBUF                  YES SKIP DIAL CALL QUEUE  S22029
         TM    AVTBIT1,AVTCLOSN         IS IT CLOSEDOWN          S22025
         BO    NEXSTCB                  YES, BRANCH              S22025
         CLI   QCBSTPRI,CLOCK           IS IT CLOCK
         BE    NOCALLUP                 BRANCH IF YES
         TM    QCBINSRC+2,TIMEQ         IS QCB ON TIME Q
         BZ    NOCALLUP                 BRANCH NO
         LR    R1,RQCB                  SET FOR CALLING HG
         L     R15,AVTHG02              GET ENTRY POINT OF HG
         BALR  R14,R15                  LINK TO IT
         NI    QCBSTAT,AVTEFF-QCBSCHDL  INDICATE SOURCE SET
         LA    R1,QCBSTVTO              SET SUBTASK ADDRESS
         B     MOVESTCB                 MOVE SCHEDULER
NOCALLUP EQU   *
         LA    RQCB,QCBSTVTO      GET SEND SCH STCB ADDR
NOSOURCE EQU   *
         SR    R0,R0              ZERO FLAG REG
         XC    AVTDOUBL(8),AVTDOUBL     CLEAR WORK AREA
         LA    RA,AVTDOUBL-QCBPRLVL+QCBSTVTO TO 0 PRI LEVEL AND RLN
         L     RWORK,DCBIOBAD           ADD IOB LESS LCB SIZE
         SR    RB,RB                    CLEAR WORK REGISTER
         IC    RB,DCBEIOBX              SIZE OF AN LCB
         AR    RWORK,RB                 POINT O IOB OF LCB
         SH    RWORK,DOCQ               CALL Q IS 8 BYTE PREFIX
*                                         OF 1ST LCB IN GROUP
LOOPOCQ  EQU   *
         LR    RSTCB,RWORK        SAVE PREVIOUS ITEM
         L     RWORK,STCBLINK-1   GET ADDR OF NEXT ITEM IN CHAIN
         CLI   STCBPRI-IEDQSTCB(RWORK),AVTEZERO  IS THIS THE LAST
*                                                ITEM IN THE CHAIN
         BE    ENDOCQ             YES, GO SEE IF CALL FOUND
         LA    RWORK,0(0,RWORK)   ZERO HI-ORDER BYTE
         CLR   RWORK,RQCB         ARE WE CONNECTED TO THIS TERMINAL
         BNE   TESTCALL                 NOT SAME DESTINATION
         MVI   LCBTSTSW,F0              PREVENT DIAL UP
         NI    QCBSTAT-QCBSTVTO(RQCB),AVTEFF-QCBSCHDL TTCIN SET SA69638
REMOVEOC EQU   *
         MVC   STCBLINK(3),STCBLINK-IEDQSTCB(RWORK) REMOVE FROM Q
         LR    R1,RWORK                 USE DISPATCHER CODE
MOVESTCB EQU   *
         LA    RQCB,LCBSTCBA-1    SET CHAIN AS LCB'S STCB CHAIN
         BAL   R14,DSPLIFOR       GO PLACE SEND SCHEDULER AT TOP OF CHN
         MVI   LCBPRI,PRILNFRE          POST PRIORITY
         ST    RLCB,LCBQCBA-1           POST TO ITSELF
         LR    R1,RLCB                  ELEMENT FOR POST
         L     RQCB,LCBSTCBA-1          POINT TO SEND SCHEDULER
         TM    QCBSTAT-QCBSTVTO(RQCB),QCBSCHDL CLOCK OR INTVL
         BZ    DSPPOST                  BRANCH NO
         SH    RQCB,QCBBKUP             ADJUST QCB BASE
         B     TNTFIND                  SET SOURCE FOR DIAL UP
TESTCALL EQU   *
         CLI   STCBPRI-IEDQSTCB(RWORK),CALLPRI IS THIS CALL=NONE
         BE    LOOPOCQ                  YES LOOK AT NEXT ENTRY
         TM    QCBFLAG-QCBSTVTO(RWORK),QCBTERMQ QUEUED BY TRM  @OY13624
         BZ    USERLN                   NO, BRANCH             @YA10696
         TM    QCBSTAT-QCBSTVTO(RWORK),QCBTRMHO IS QUEUE HELD   SA59006
         BO    LOOPOCQ                  LEAVE HELD QCB ON DOCQ @YA10696
USERLN   EQU   *                                               @YA10696
         CLC   QCBPRLVL-QCBSTVTO(1,RWORK),QCBPRLVL-QCBSTVTO(RA)
*                                         ARE THE HIGHEST MESSAGE
*                                         PRIORITIES THE SAME
         BL    LOOPOCQ                  ITS LOWER LOOK AT NEXT ENTRY
         BH    SAVECALL                 SAVE THIS CALL
         CLI   QCBPRLVL-QCBSTVTO(RWORK),AVTEZERO 0 PRIORITY LEVEL
         BNE   LOOPOCQ                  NO WE HAVE TRUE PRIORITY
         CLC   QCBRELLN-QCBSTVTO(1,RWORK),QCBRELLN-QCBSTVTO(RA) SA68698
*                                       IS THIS A HIGHER RLN    SA68698
         BNH   LOOPOCQ                  NO, BRANCH              SA68698
SAVECALL EQU   *                                                SA68698
         IC    R2,QCBRELLN-QCBSTVTO(0,RWORK) GET RLN FOR QCB
         BCTR  R2,R0              SUBTRACT ONE
         EX    R2,RLNCLI          CAN CALL BE DONE ON THIS LINE
         BL    LOOPOCQ                  BRANCH LCB RLN LESS THAN QCB
         LR    RA,RWORK                 SAVE A CALL
         LR    R0,RSTCB                 SAVE FOR DELINK
         B     LOOPOCQ                  LOOK AT NEXT ENTRY
         SPACE
         SPACE
         SPACE 1
ENDOCQ   EQU   *
         XC    LCBTTCIN,LCBTTCIN        CLEAR PREV SOURCE      @YA08092
         MVI   LCBTSTSW,AVTEFF          INSURE A DIAL UP
         LR    RWORK,RA                 RESTORE  THE CALL
         LTR   RSTCB,R0                 WAS A CALL FOUND
         BNZ   REMOVEOC                 YES
         MVC   LCBTTCIN,LCBLNENT        SET LINE ENTRY AS SOURCE
REQBUF   EQU   *
         TM    AVTBIT1,AVTCLOSN         IS CLOSE IN PROGRESS
         BZ    POLL                     NO, BRANCH               S22025
NEXSTCB  EQU   *                                                 S22025
         L     RSTCB,LCBRSLNK-1         GET QEVENT
         LR    R1,RLCB                  BYPASS PARAMETER
         B     DSPBYPAS                 CONTROL TO QEVENT
         EJECT
POLL     EQU   *
         MVC   LCBTTBIN,LCBTTCIN        SAVE SOURCE INFORMATION
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN RESET ALL EXCEPT OP   @OY14092
*                                       CONTROL BITS           @OY14092
*                                         PRE ENT WAIT STATE IF
*                                         CLOSE DOWN IN PROGRESS
         OI    LCBSTAT1,LCBRECVN        SET LCBSTATE
         L     RWORK,DCBDEBAD           GET DEB ADDRESS
         USING DEBNMSUB,RWORK
         TM    DEBOPATB,OUTPUT          OPENED FOR OUPUT ONLY
         BNO   TRYRCVT                  NO, POST ERB TO RECEIVE
         DROP  RWORK
         CLI   LCBCPA,CCWDISAB          DID WE ALREADY DISCONNECT
         BE    SETFREE                  BRANCH YES, SET LINE FREE
         CLI   LCBCPA+8,CCWDISAB        DID WE ALREADY DISC.   @OX17150
         BE    SETFREE                  BRIF YES. SET LINE FREE@OX17150
         SPACE
         TM    LCBSTAT2,LCBSYNC         BSC
         BZ    DISAB                    BRANCH NO
         TM    LCBSTAT2,LCBNEGRP        NEGATIVE RESPONSE SET
         BO    DISAB                    YES
         OI    LCBSTAT2,LCBNEGRP        INDICATE NEGATIVE RESPONSE
         LR    R1,RLCB                  POST PARAMETER
         B     DSPPOST                  POST LCB
DISAB    EQU   *
         LA    RA,LCBCPA+8              GET START CCW          @OX17150
         L     RWORK,LCBSCBA-1          GET THE SCB ADDRESS    @OX17150
         USING IEDQSCB,RWORK                                   @OX17150
         TM    SCBBSCFM,SCBNOEOT        EOT SENT               @OX17150
         BZ    DISABLE                  BRIF YES               @OX17150
         DROP  RWORK                                           @OX17150
         L     RWORK,DCBSCTAD-1         GET SCT ADDRESS        @OX17150
         SR    RA,RA                    CLEAR FOR INSERT       @OX17150
         IC    RA,0(RWORK)              INDEX INTO SEQUENCE    @OX17150
         LA    RWORK,0(RA,RWORK)        POINT TO COUNT         @OX17150
         IC    RA,0(RWORK)              GET SEQUENCE COUNT     @OX17150
         LA    RWORK,1(RWORK)           POINT TO EOT CHARS     @OX17150
         ST    RWORK,LCBCPA             SET CCW ADDRESS        @OX17150
         ST    RA,LCBCPA+4              SET CCW COUNT          @OX17150
         MVI   LCBCPA,CCWWRITE          SET COMMAND CODE       @OX17150
         MVI   LCBCPA+4,CCWCC+CCWSLI    SET FLAGS              @OX17150
         MVI   LCBTPCD,TPNULL           SET TP CODE            @OX17150
         LA    RA,LCBCPA                GET START CCW          @OX17150
DISABLE  EQU   *                                               @OX17150
         ST    RA,LCBSTART-1            SET START              @OX17150
         ST    RLCB,LCBQCBA-1           SET FOR RETURN FROM LINE END
         XC    LCBTTCIN,LCBTTCIN        CLEAR CURRENTLY CONNECTID
         LA    RLCB+1,1                 FLAGS AND COUNT
         STM   RLCB,RA,LCBCPA+8         SET IN CCW             @OX17150
         MVI   LCBCPA+8,CCWDISAB        SET COMMAND CODE       @OX17150
         MVI   LCBCPA+12,CCWCC+CCWSLI   SET FLAGS              @OX17150
         MVI   LCBTPCD+1,TPOPEN         SET TP CODE            @OX17150
         MVI   LCBCPA,CCWDISAB          SET COMMAND CODE
         STM   RLCB,RA,LCBCPA+16        NO-OP CCW              @OX17150
         MVI   LCBCPA+16,CCWNOP         SET NO-OP CC           @OX17150
         MVI   LCBTPCD+2,TPOPEN         SET TP CODE            @OX17150
         LA    R1,LCBFLAG1              SET FOR IOS
         EXCPVR (R1),SUBSYS                                      X01004
         B     DSPDISP                  EXIT TO DISPATCHER
SETFREE  EQU   *
         MVI   LCBTSTSW,0               NO CONNECTION
         MVI   LCBSTAT1,LCBFREEN        SET LINE 'FREE'
         B     DSPDISP                  EXIT
TRYRCVT  EQU   *
         TM    AVTBIT3,AVTRECVN+AVTRFULN IS RECEIVE OPERATION
*                                         POSSIBLE
         BZ    GOTUNIT                  BRANCH IF YES
         SPACE
         L     RWORK,LCBSTCBA-1         GET TOP STCB
         CLI   AVTEZERO(RWORK),DSPDIASC RECEVIE SCHEDULAER AT TOP
         BE    DELAYIT                  BRANCH YES TO PLACE ON TIME Q
         OI    LCBSTAT2,LCBNEGRP        SET NEGATIVE RESPONSE-TO PRE
*                                         VENT LOOPNBETWEEN SCHEDULERS
*                                         FOR DIAL UP AND NO MSGS
*                                         ON DEST. QUEUE
         SPACE
POSTFRE  EQU   *
         ST    RLCB,LCBQCBA-1           POST LCB TO ITSELF FOR SEND
POSTIT   EQU   *
         LR    R1,RLCB                  ELEMENT FOR POST
         B     DSPPOST                  POST LCB TO ITSELF
DELAYIT  EQU   *
         LA    R0,1                     SET FOR 1 SECOND DELAY
         STH   R0,LCBEOLTD              SET PARAMETER
         MVI   LCBTDL,LCBINSRC-1-IEDQLCB  SET INDEX FOR TIME DELAY
         MVC   LCBSTCBA,LCBRSLNK        REMOVE RCV SCHED AND MOVE IN
*                                         QEVENT IN ITS PLACE
         LA    R0,AVTDELYB              TIME DELAY QUEUE
         ST    R0,LCBQCBA-1             SET IN LCB
         B     POSTIT                   COMMON EXIT
GOTUNIT  EQU   *
         LA    RA,AVTBFREB              BUFFER REQUEST QUEUE
         ST    RA,LCBERB                POST ERB
         MVI   LCBERBPY,PRIINTRQ        INITIAL REQUEST
         SR    RWORK,RWORK              CLEAR WOF REGISTER
         STH   RWORK,LCBRECOF           RESET OFFSET INTO RECORD
         STCM  RWORK,AD,LCBRECAD        RESET FIRST EOB RECORD   Y05330
         IC    RWORK,DCBBUFIN           NO. OF INITIAL BUFFERS
         SRL   RWORK,4                  SHIFT TO LOW ORDER BITS
         STC   RWORK,LCBERBCT           SET COUNT
         LA    R1,LCBERB                POST ERB FOR BUFFERS
         B     DSPPOST                  GO POST ERB FOR BUFFERS
         SPACE 3                                                 Y05330
TNTSUB   DS    0H                                                Y05330
         L     R15,AVTRNMPT             ADDRESS OF TERM NAME     Y05330
         LH    R1,LCBTTCIN              DESTINATION KEY          Y05330
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF    Y05330
         BZR   R14                      RETURN IF ZERO           Y05330
         LA    R14,FOUR(,R14)           BUMP RETURN ADDRESS      Y05330
         BR    R15                      LINK TO IEDQTNT          Y05330
         SPACE 3                                                 Y05330
DOCQ     DC    AL2(LCBFLAG1-IEDQLCB+8)  OFFSET TO OBTAIN CALL Q
RLNCLI   CLI   LCBUCBX,AVTEZERO
         EJECT
TWELVHRS DC    F'43200'                 12 HOURS
TW24HRS  DC    F'86400'                 NUMBER SECONDS PER DAY
QCBBKUP  DC    AL2(QCBSTVTO-IEDQQCB)    OFFSET FROM STCB TO QCB  S22025
THREE    EQU   3
TIMEQ    EQU   X'02'                   FLAG FOR ELEMENT ON TIME Q
CALLPRI  EQU   X'50'                    SEND PRI WITH CALL=NONE
CLOCK    EQU   X'80'                    SEND SCH PRI FOR CLOCK
CALLOPT  EQU   X'70'                    MASK FOR STCB PRIORITY @OY12954
*                                       CLOCK OR INTVL         @OY12954
F0       EQU   X'F0'                    MASK FOR NO DAIL UP
OUTPUT   EQU   X'03'                    DEB MASK FOR OUTPUT ONLY
MASTER   EQU   X'20'                    MASTER/SLAVE STATUS    @OY13276
HALF     EQU   32
HUND     DC    F'100'                   USED TO CONVERT TIME TO  S22025
CLRMASK  DS    0F                       SECONDS                  S22025
         DC    X'7FFFFFFF'              MASK TO MAKE POSITIVE    S22025
THIRTY1  EQU   31                                                S22025
TWEN6    EQU   26                                                S22025
FIVE     EQU   5                                                 S22025
ONE      EQU   1                                                 S22025
HONE     DC    H'1'                     TO CHECK RETRY COUNT     S22025
AD       EQU   7                        MASK FOR ADDRESS ICM     Y05330
FOUR     EQU   4                        CONSTANT OF FOUR         Y05330
         EJECT
         TTNTD
         EJECT
         TTPD
         TPRIOR
         EJECT
         TAVTD
         EJECT
         TDISPD
         EJECT
         TLCBD
         EJECT
         TQCBD
         EJECT
         TSTCBD
         TTRMD
         TTSID                                                      TSO
         TSCBD
         DCBD  DSORG=TX
         TCCWD
         TDEBD
         END
