2041     TITLE 'IGG02041-CLOSE CHECKPOINT DATA SET'
IGG02041 CSECT
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS:                                   S21903
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C384000                                                         A49219
*D416000-424000                                                  A49219
*C324000                                                         S22025
*C332000                                                         A49219
*A004000                                                         S21903
*D772000                                                         S21903
*C020000,304000-320000,520000,664000,                            S21903
*C700000-704000,744000-764000,808000-812000,82400                S21903
*C696000                                                        SA57360
*C028000,304000,312000-320000,520000,664000,744000-752000        S22024
*C760000-764000,808000-812000,824000                             S22024
*A344000,348800,358000,541000,568000,616000                      S22024
*A384000,721000                                                  Y01004
*C028000,080000,088000,136000-140000,312000-340000,376000-386000 Y02027
*C432000,502000,504000,676000,714000                             Y02027
*A248000,252000,276000,280000,296800-299200,433000,434000,593000 Y02027
*A594000,673600-675200,688200,714200-715600                      Y02027
*A813000                                                         Y02027
*A449000,450000                                                 OY03902
*                                                              @G36XRTP
*A349600,351200                                                @G36XRTP
*C544000                                                       @G36XRTP
*D356000,360000-372000,520000,540000-542000,548000-550000      @G36XRTP
*THIS IS A DUMMY APAR TO CORRECT FICHE FLAGGING PROBLEM        @OZ19773
         SPACE 3
***********************************************************************
*                                                                     *
*MODULE-NAME = IGG02041      (TCAM,TERMINATION)                @G36XRTP
*                                                                S21903
*DESCRIPTIVE-NAME = CLOSE CHECKPOINT DATA SET                    S21903
*                                                                S21903
*COPYRIGHTS = 'NONE'                                             S21903
*                                                                     *
*STATUS = VERSION 10.0                                         @G36XRTP
*                                                                     *
*FUNCTION:THIS MODULE CLOSES THE CHECKPOINT DCB. IF THIS IS A         *
*   NORMAL CLOSEDOWN, AN INDICATION IS SET IN THE CHECKPOINT          *
*   CONTROL RECORD,AND THE RECORD IS WRITTEN ON DISK. THE CHECK-      *
*    POINT WORK AREA IS FREED VIA FREEMAIN.                           *
*                                                                     *
*ENTRY POINT:                                                         *
*                                                                     *
*        IGG02041                                                     *
*                                                                     *
*INPUT:REGISTERS 5,6,7,8 CONTAIN THE FOLLOWING VALUES:                *
*                                                                     *
*   5-ADDRESS OF THE PREFIX OF THE WHERE-TO-GO TABLE             Y02027
*   6-ADDRESS OF BEGINNING OF WHERE-TO-GO TABLE                       *
*   7-ADDRESS OF THE CURRENT ENTRY IN THE WTG TABLE PREFIX       Y02027
*   8-ADDRESS OF CURRENT ENTRY IN WHERE-TO-GO TABLE                   *
*                                                                     *
*   CHECKPOINT WORK AREA                                              *
*                                                                     *
*ENTRY POINTS - THIS ROUTINE IS ENTERED AT IGG02041 VIA AN       Y02027
*   XCTL FROM THE CLOSE EXECUTOR IGG02030 OR IT MAY BE REENTERED Y02027
*   FROM THE RELOOP PORTION OF THIS EXECUTOR IF IT IS DETERMINED Y02027
*   THAT THERE IS ANOTHER DATA CONTROL BLOCK TO BE CLOSED.       Y02027
*   IF ENTRY IS AT IGG02041, THEN A MODESET IS ISSUED FOR THE    Y02027
*   PROTECT KEY OF 0.                                            Y02027
*OUTPUT:REGISTERS 5 AND 6 ARE SAME AS INPUT. REGISTERS 7 AND 8 ARE    *
*   UPDATED TO THE NEXT ENTRIES.                                      *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*                                                                     *
*        'IECPCNVT'-CONVERTS RELATIVE TRACK ADDRESS TO ACTUAL ADDR    *
*                                                                     *
*EXITS - NORMAL                                                  Y02027
*   THIS MODULE WILL XCTL TO THE NEXT NON-ZERO ENTRY IN          Y02027
*   THE WHERE-TO-GO TABLE. THE EXIT IS MADE IN THE KEY OF        Y02027
*   DATA MANAGEMENT.                                             Y02027
*                                                                     *
*EXITS-ERROR:THIS MODULE ISSUES AN ERROR MESSAGE VIA WTO AND XCTLS    *
*   TO THE NEXT ENTRY IN THE WHERE-TO-GO TABLE. THE ONLY ERROR        *
*   RECOGNIZED IS A DISK ERROR WHILE WRITING THE CONTROL RECORD.      *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*    CVT                                                              *
*   AVT (AVTCKGET,AVTCPRCD,AVTNCKPR,AVTGETMN)                         *
*   CHECKPOINT WORK AREA                                              *
*                                                                     *
*ATTRIBUTES:REENTERABLE                                               *
*                                                                     *
*NOTES:THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRE-   *
*   SENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO    *
*   THE ONE USED AT ASSEMBLY TIME. THE CODING HAS BEEN ARRANGED SO    *
*   THAT REDEFINITION OF 'CHARACTER' CONSTANTS,BY REASSEMBLY, WILL    *
*   RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.               *
*                                                                     *
***********************************************************************
         EJECT
*REGISTER ASSIGNMENTS
         SPACE
R0       EQU   0                        REG ZERO                 S22024
R1       EQU   1                        PARM REG                 S22024
R2       EQU   2                        ADDRESS OF CHECKPT WRK AREA
RDCB     EQU   3                        THE DCB BASE REGISTER    Y02027
RCORE    EQU   4                        OPEN WORK AREA BASE REG  Y02027
R5       EQU   5                        ADDR OF 1ST DCB PARAMETER
R6       EQU   6                        BEGINNING OF WHERE-TO-GO TB
R7       EQU   7                        ADDR OF CURRENT DCB PARM
R8       EQU   8                        ADDR-CURRENT WHERETOGO ENTY
RAVT     EQU   9                        ADDRESS OF AVT
RTCB     EQU   10                       TCB BASE REGISTER        Y02027
RDEB     EQU   11                       THE DEB BASE REGISTER    Y02027
R12      EQU   12                       BASE REG
R13      EQU   13                       REG 13                   S22024
R14      EQU   14                       REG 14                   S22024
R15      EQU   15                       REG 15                   S22024
         SPACE 2
         USING IEDQCKPD,R2              THE CHECKPOINT WORK AREA Y02027
         USING IEDQTCX,R1               TCAM CVT EXTENSION DSECT Y02027
         USING IHADCB,RDCB              THE DCB DSECT            Y02027
         USING FORCORE,RCORE            THE OPEN WORK AREA DSECT Y02027
         USING IEDQAVTD,RAVT            THE AVT DSECT            Y02027
         USING DEBNMSUB,RDEB            THE DEB DSECT            Y02027
         USING IEDQTCB,RTCB             TCB DSECT                Y02027
         SPACE 3
         BALR  R12,0                    ESTABLISH ADDRESSABILITY S22024
         USING *,R12                                             S21903
         SPACE 2
IGG02041 IEDHJN START                                            Y02027
         SPACE 2
         L     RCORE,FOUR(R8)           GET THE ADDRESS OF THE   Y02027
*                                         OPEN WORK AREA         Y02027
         L     RDCB,DXUDCBAD            ADDRESS OF CURRENT DCB   Y02027
         SPACE 2
         MODESET EXTKEY=SUPR            GET INTO KEY 0           Y02027
         SPACE 2
         SPACE 2
         L     R1,CVT                   GET THE CVT ADDRESS      Y02027
         L     R1,AVTCVTPT(R1)          GET THE TCX ADDRESS      Y02027
         L     RAVT,TCXAVT              GET THE AVT ADDRESS      Y02027
         L     R2,DCBIOBAD              GET CHECKPOINT WORKAREA  S22024
*                                        ADDRESS                 S22024
         LTR   R2,R2                    IS THERE A CKPT        @G36XRTP
*                                       WORKAREA               @G36XRTP
         BZ    QL725                    NO, BRANCH             @G36XRTP
         TM    DCBOPTCD,CKP3705         3705 CHECKPOINT DCB      S22024
         BO    QL705                    YES, BRANCH              S22024
         TM    AVTCKFLG,AVTCKTAC        TCAM CKPT ACTIVE       @G36XRTP
         BNO   QL720                    NO, BRANCH TO FREE     @G36XRTP
*                                       WORKAREA, TCAM CKPT    @G36XRTP
*                                       ABENDED OR THERE WAS   @G36XRTP
*                                       OPEN ERROR             @G36XRTP
         SPACE
QL705    EQU   *                                                 S22024
         L     RDEB,DCBDEBAD            GET THE DEB ADDRESS      Y02027
         L     RTCB,DEBTCBAD            GET THE TCB ADDRESS      Y02027
         TM    TCBFLGS,ABEND            IS ABEND IN PROGRESS     Y02027
         BO    QL720                    BRANCH IF ABNORMAL TERMINT
         SPACE 3
*SET NORMAL COMPLETION BIT IN CHECKPOINT CONTROL RECORD AND WRITE
*THE RECORD
         SPACE 1
         USING DXDEB,RDEB               DEB DSECT                Y02027
         SPACE
         OI    CKPFLAGS,CKPNORML        SET NORMAL CLOSE BIT
         MVI   CKPIOM,0                 PUT EXTENT IN IOB
         MVC   CKPIOBB(SIX),DXDEBBIN    MOVE BIN, CYLINDER, AND  Y02027
*                                         TRACK ADDRESSES INTO   Y02027
*                                         CHECKPOINT WORK AREA   Y02027
         MVI   CKPIOR,1                 RECORD NO.
         LA    R13,CKPCNTLR             ADDRESS OF CONTROL RCD
         ST    R13,CKPRW                PUT ADDR INTO CCW
         MVI   CKPRW,CKPWRITE           COMMAND CODE FOR WRITE DATA
         MVI   CKPRW+SX,ZRO            CLEAR COUNT              OY03902
         MVC   CKPRW+SEV(ONE),CKPCRLEN CONTROL REC COUNT        OY03902
         MVI   CKPSECTR,AVTEZERO        SET SECTOR ID TO 0  0430 S21101
         SPACE
         EXCP  CKPIOB                   WRITE CONTROL RCD
         SPACE
         WAIT  ECB=CKPECB               WAIT FOR I/O
         SPACE
         TM    CKPIOECB,X'7F'           CHECK FOR DISK ERROR
         BO    QL720                    BRANCH IF NO ERROR
         SPACE 3
*NOTIFY CONSOLE OF DISK ERROR VIA WTO
         SPACE
         MVC   CKPMSG(QL7MSGL),QL7MSG   MOVE ERROR MESSAGE       S22024
         L     RTCB,AVTTCB              GET TCB ADDRESS          Y02027
         L     R13,TCBTIO               GET TIOT ADDRESS         Y02027
         AH    R13,DCBTIOT              GET TIOE ADDRESS         S22024
         MVC   CKPMSG+DDNAME(DDL),TIOEDDNM(R13) MOVE DDNAME TO   S22024
*                                        MESSAGE                 S22024
         LA    R1,CKPMSG                ADDR OF MSG
         WTO   MF=(E,(1)),ROUTCDE=11,DESC=4
         B     QL720                    BRANCH                   S22024
         SPACE 3
*FREE CHECKPOINT WORK AREA AND DELETE DISK END ROUTINE,LOADED BY
*CHECKPOINT OPEN.
         SPACE
QL720    EQU   *
         LH    R13,CKPWKALN             LENGTH OF CKPT WORK AREA
         SPACE
         FREEMAIN E,LV=(R13),A=DCBIOBAD,MF=(E,AVTGETMN),SP=250   S22024
QL725    EQU   *                                               @G36XRTU
         TM    DCBOPTCD,CKP3705         3705 CHECKPOINT DCB      S22024
         BO    QL730                    YES, BYPASS DELETE       S22024
         SPACE
         DELETE EP=IGG019RA
         EJECT
*SET UP FOR XCTL TO NEXT CLOSE MODULE
         SPACE 2
QL730    EQU   *
         L     R15,4(R8)                GET ADDRESS OF OPEN      Y02027
*                                       WORK AREA                Y02027
         XC    0(2,R8),0(R8)            CLEAR TO INDICATE COMPLETE
QL735    EQU   *
         LA    R8,8(R8)                 UPDATA CURRENT WHERETOGO
         LA    R7,4(0,R7)               UPDATE CURRENT DCB PARM
         CLC   ZERO(TWO,R8),AMIDCNST    IS THIS EXECUTOR NEEDED  Y02027
         BE    START                    YES, RETURN TO PROCESS ITY02027
         CLC   0(2,R8),QL70B            CHECK FOR END OF WTG TABLE
         BL    QL735                    BRANCH IF NOT END
         CLC   0(2,R8),QL70G            COMPARE FOR END OF WTG TABLE
         BH    QL735                    BRANCH IF NOT END
         LR    R7,R5                    RESET PARM LIST ADDR
         LA    R8,32(0,R6)              RESET WTG TABLE ADDR
QL740    EQU   *
         CLI   0(R8),0                  CHECK FOR 0 ENTRY
         BNE   QL750                    BRANCH IF NOT 0
         LA    R8,8(0,R8)               ADDR OF NEXT WTG ENTRY
         LA    R7,4(0,R7)               ADDR OF NEXT DCB
         B     QL740                    TEST NEXT ENTRY          S22024
         SPACE
QL750    EQU   *
         SPACE 1
         MODESET EXTKEY=DATAMGT         RETURN TO KEY 5          Y02027
         SPACE 2
XCTLRTNE EQU   *                                                 Y02027
         LA    R15,DXCCW12-FORCORE(R15) XCTL PARM LIST           Y02027
         MVC   6(2,R6),0(R8)            PUT EXECUTOR ID IN PARM
         MVC   14(3,R6),2(R8)           PUT TTR IN WTG TABLE
         XCTL  DE=(R6),SF=(E,(15))
         EJECT                                                   Y02027
*CHECKPOINT DISK ERROR WTO MESSAGE                               Y02027
         SPACE 3
QL7MSG   WTO   'IED087I CHECKPOINT DISK ERROR-CONTROL RECORD-XXXXXXXX',*
               ROUTCDE=(2,11),DESC=4,MF=L                       SA57360
QL7MSGL  EQU   *-QL7MSG                 SIZE OF MESSAGE          S22024
DDNAME   EQU   49                       OFFSET TO DDNAME IN MSG  S22024
TIOEDDNM EQU   4                        DDNAME IN TIOE           S22024
DDL      EQU   8                        LENGTH OF DDNAME         S22024
CKP3705  EQU   X'10'                    OPTCD=I 3705 CHECKPOINT  S22024
QL70B    DC    C'0B'                    LOWEST POSSIBLE ID FOR END
QL70G    DC    C'0G'                    HIGHEST POSSIBLE ID FOR END
ZRO      EQU   0                        NUMERIC ZERO            OY03902
ONE      EQU   1                       LENGTH                   OY03902
SX       EQU   6                        NUMERIC SIX             OY03902
SEV      EQU   7                       DISPLACEMENT             OY03902
FOUR     EQU   4                        CONSTANT                 Y01004
ZERO     EQU   0                        NUMERIC 0                Y02027
TWO      EQU   2                        NUMERIC 2                Y02027
FIVE     EQU   5                        OFFSET                   Y02027
SIX      EQU   6                        NUMERIC 6                Y02027
CVT      EQU   16                       CVT DISPLACEMENT         Y02027
ABEND    EQU   X'80'                    ABEND INDICATOR IN TCB   Y02027
AMIDCNST DC    C'41'                    ID FOR THIS EXECUTOR     Y02027
         SPACE 3
*XCTL TABLE-ROUNDS OUT LENGTH TO 1024 BYTES
         SPACE
NENT     EQU   1                        VALUE 1                  S22024
LENGTH   EQU   NENT*6+6                 LENGTH                   S22024
NODBWDS  EQU   (LENGTH+7)/8             LENGTH                   S22024
ORGIDTTR EQU   1024-NODBWDS*8           ADDRESS                  S22024
         ORG   IGG02041+ORGIDTTR        EXTEND TO FULL SIZE      S22024
IDEND    DC    X'0000'                  END OF NAMELIST          S22024
         CNOP  4,8                                               S21903
LOADID   DC    C'020'                   CLOSE ID                 S22024
         DC    YL1(X'80'-NODBWDS)       ADJUST MODULE LENGTH     S22024
         SPACE 3
         TAVTD
         TCKPD 3330                     MERLIN VERSION      0430 S21101
         DCBD  DSORG=TQ
         SPACE 3
         EJECT
         TDEBD                                                   Y02027
         EJECT
         TTCXD                                                   Y02027
         EJECT
FORCORE  DSECT
         IECDSECT
         EJECT
         TTCBD                                                   Y02027
         EJECT
         END
