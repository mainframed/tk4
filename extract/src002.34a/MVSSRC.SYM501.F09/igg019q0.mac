         TITLE 'IGG019Q0 TCAM CHANNEL I/O TRACE ROUTINE'
IGG019Q0 CSECT
***************************************************************@Y17XASK
*                                                                S21903
* MODULE NAME = IGG019Q0 (TCAM, SERVICE AIDS)                  @Y17XASK
*                                                                S21903
* DESCRIPTIVE NAME = TCAM CHANNEL I/0 TRACE ROUTINE            @Y17XASK
*                                                                S21903
* COPYRIGHT = NONE                                               S21903
*                                                                S21903
* STATUS = VERSION 10.0                                        @Y17XASK
*                                                                S21903
* FUNCTION = PROVIDE A TRACE OF ALL I/O ACTIVITY ON SELECTED   @Y17XASK
*            SUBCHANNELS.  UNIT ADDRESS, CSW, CCWS, PART OR    @Y17XASK
*            ALL OF THE DATA TRANSFERRED BY EACH CCW, TRACE    @Y17XASK
*            ENTRY SEQUENCE NUMBER, IOB FLAGS, DEVICE TTCIN,   @Y17XASK
*            DEVICE NAME, POINTER TO CURRENT ENTRY IN THE      @Y17XASK
*            INVITATION LIST, CCW ADDRESS, AND TP OPCODE ARE   @Y17XASK
*            TRACED AS APPROPRIATE FOR THE TYPE OF INTERRUPT   @Y17XASK
*            BEING PROCESSED.  SEE THE DSECTS AT THE END OF    @Y17XASK
*            THE MODULE FOR THE TRACE ENTRY FORMATS.  FOR ALL  @Y17XASK
*            INTERRUPTS HEADER AND INFORMATION ENTRIES ARE     @Y17XASK
*            MADE IN THE TRACE TABLE.  FOR NORMAL INTERRUPTS   @Y17XASK
*            (NOT ASYNCHRONOUS) AN ENTRY IS MADE FOR EACH CCW. @Y17XASK
*            IF THE DATA MOVED BY THE CCW IS MORE THAN 4 BYTES @Y17XASK
*            AN ADDITIONAL DATA ENTRY IS MADE.  TRACING IS     @Y17XASK
*            DONE ON SUBCHANNELS SPECIFIED BY THE OPERATOR     @Y17XASK
*            UNTIL EXPLICITLY TERMINATED.  TRACING IS DONE ON  @Y17XASK
*            ALL SUBCHANNELS WITH ERRORS IF REQUESTED BY THE   @Y17XASK
*            OPERATOR UNTIL A CHANNEL PROGRAM COMPLETES WITH   @Y17XASK
*            NORMAL ENDING STATUS AND/OR THE OPERATOR          @Y17XASK
*            TERMINATES THE TRACING OPERATION.  UP TO 50       @Y17XASK
*            SUBCHANNELS CAN BE TRACED CONCURRENTLY.           @Y17XASK
*                                                              @G36XRSV
* NOTE:      ALL APPENDAGES EXCEPT PCI NOW RUN ENABLED.        @G36XRSV
*            PCI RUNS DISABLED THROUGH THE DIE EXIT. THIS      @G36XRSV
*            MODULE WILL BE ENTERED WITH THE LOCAL LOCK FROM   @G36XRSV
*            ERP, LINEEND, AND THE ATTENTION HANDLER TO        @G36XRSV
*            SERIALIZE THOSE INTERRUPTS. THE POSSIBILITY OF    @G36XRSV
*            PCI INTERRUPTING LINEEND, ERP, OR ATTENTION       @G36XRSV
*            STILL EXISTS, THUS DESTROYING TRACE ENTRIES AS    @G36XRSV
*            THEY ARE BEING BUILT. TO PREVENT THIS OCCURRENCE, @G36XRSV
*            ERP, LINEEND, AND ATTENTION TRACE ENTRIES         @G36XRSV
*            ARE BUILT IN A SIXTEEN ENTRY TEMPORARY TABLE      @G36XRSV
*            AND THEN MOVED TO THE TRACE TABLE WHEN COMPLETED. @G36XRSV
*            IF A NON PCI INTERRUPT REQUIRES MORE THAN 16      @G36XRSV
*            ENTRIES, ONLY THE FIRST SIXTEEN ARE RECORDED.     @G36XRSV
*                                                              @OZ17621
*            IF THE LENGTH OF THIS MODULE VARIES AROUND        @OZ17621
*            HEX 800 BYTES, THEN IGG02035 SHOULD BE CHECKED    @OZ17621
*            TO MAKE SURE THE CORRECT NUMBER OF BYTES ARE      @OZ17621
*            BEING FREED.                                      @OZ17621
*                                                                S21903
* NOTES = SEE BELOW                                              S21903
*                                                                S21903
*    DEPENDENCIES = NONE                                         S21903
*                                                                S21903
*    RESTRICTIONS = NONE                                         S21903
*                                                                S21903
*    REGISTER CONVENTIONS = SEE REGISTER EQUATES                 S21903
*                                                              @Y17XASK
*    PATCH LABEL = NONE                                        @Y17XASK
*                                                                S21903
* MODULE TYPE = PROCEDURE                                        S21903
*                                                                S21903
*    PROCESSOR = ASSEMBLER                                       S21903
*                                                                S21903
*    MODULE SIZE = APPROXIMATELY 1770 BYTES                    @Y17XASK
*                                                                S21903
*    ATTRIBUTES = SERIALLY REUSABLE                            @Y17XASK
*                                                                S21903
* ENTRY POINT = IGG019Q0                                       @Y17XASK
*                                                                S21903
*    PURPOSE = SEE FUNCTION                                      S21903
*                                                                S21903
*    LINKAGE = R15 CONTAINS ENTRY POINT ADDRESS                @Y17XASK
*              FROM IGG019RN (PCI): HIGH ORDER BYTE CONTAINS   @G36XRSV
*                 X'FF'                                        @G36XRSV
*              FROM OTHERS: HIGH ORDER BYTE CONTAINS NON X'FF' @G36XRSV
*              R14 CONTAINS RETURN ADDRESS                     @Y17XASK
*              ENTERED FROM:                                   @Y17XASK
*              IGE0004G - SS ERP                               @Y17XASK
*              IGE0004H - BSC ERP                              @Y17XASK
*              IGG019RN - PCI APPENDAGE                        @Y17XASK
*              IGG019Q4 - MINI LINE END APPENDAGE              @Y17XASK
*              IGG019Q3 - SS LINE END APPENDAGE                @Y17XASK
*              IGG019Q2 - BSC LINE END APPENDAGE               @Y17XASK
*              IGG019R0 - SS/BSC LINE END APPENDAGE            @Y17XASK
*              IGG019TH - 370X CHANNEL END APPENDAGE           @Y17XASK
*              IGG019R5 - LOCAL DEVICE ATTENTION HANDLER       @Y17XASK
*              IGE0004I - 370X ERP                             @Y17XASK
*                                                              @Y17XASK
* INPUT = CHANNEL PROGRAM(IN LCB AND TCAM BUFFER UNITS)        @Y17XASK
*         CSW(IN IOB)                                          @Y17XASK
*         ENTRY FROM IGG019R5:                                 @Y17XASA
*         R1 = ZERO                                            @Y17XASA
*         R7 = UCB ADDRESS                                     @Y17XASA
*         R9 = IOB ADDRESS                                     @Y17XASA
*         ENTRY FROM ALL OTHER MODULES:                        @Y17XASA
*         R1 = RQE ADDRESS                                     @Y17XASA
*                                                                S21903
* OUTPUT = TRACE OF INTERRUPT INFORMATION IN TCAM'S LINE TRACE   S21903
*          TABLE                                               @Y17XASK
*                                                                S21903
* EXIT-NORMAL = RETURN TO CALLER                                 S21903
*                                                                S21903
* EXIT-ERROR = NONE                                            @Y17XASK
*                                                                S21903
* EXTERNAL REFERENCES = SEE BELOW                                S21903
*                                                                S21903
*    ROUTINES = IEDQFE20-TO DUMP TRACE TABLE WHEN IT IS ABOUT    S21903
*                        TO WRAP                                 S21903
*               IEAPTRV -ADDRESS CONVERSION ROUTINE            @Y17XASA
*               IEDQTNT -TERMINAL NAME TABLE ROUTINE           @Y17XASA
*                                                                S21903
*    DATA AREAS = TCAM LINE TRACE TABLE                          S21903
*                 TCAM TERMINAL NAME TABLE                     @Y17XASK
*                                                                S21903
*    CONTROL BLOCKS = OS CVT (COMMUNICATIONS VECTOR TABLE)       S21903
*                     OS RQE (REQUEST ELEMENT TABLE)             S21903
*                     TCAM AVT (ADDRESS VECTOR TABLE)            S21903
*                     TCAM LCB (LINE CONTROL BLOCK)            @Y17XASK
*                     OS UCB (UNIT CONTROL BLOCK)              @Y17XASK
*                     OS DCB (DATA CONTROL BLOCK)              @Y17XASK
*                                                              @Y17XASK
*    TABLES = Q0SAVE1 AND Q0SAVE2 - LOCAL SAVE AREAS           @Y17XASK
*             LINETABL - TRACE CONTROL TABLE (TCT)             @Y17XASK
*                                                                S21903
* MACROS = IEDHJN,TAVTD,TLCBD,TCCWD,CVT,DCBD,TTCXD,TTNTD,      @Y17XASK
*          TTHD,TTRMD,TPRFD                                    @Y17XASK
*                                                                S21903
* CHANGE ACTIVITY = SEE BELOW                                  @Y17XASK
*A000000-999999                                                  S21903
*A341000                                                        OX02185
*A199000                                                        SA64870
*C195000,236000-237000                                          SA64870
*D084000,103000                                                 SA64870
*A109000,196000                                                 OX02202
*A287000                                                         Y00487
*A109000,134000,235000,241000,274000,366000,376000,483000,488000 X02004
*A492000,494000                                                  X02004
*C084000,106000,117000,278000,280000,284000,290000,420000,483000 X02004
*C484000-487000,488000                                           X02004
*D084000,106000,240000,324000,326000,376000                      X02004
*A463000                                                       @SA69982
*C441000-442000,451000,464000                                  @SA69983
*D465000-466000                                                @SA69983
*A588000                                                       @SA70178
*C276000-277000,329000-330000,508000                           @SA70178
*C283000                                                       @SA70179
*D318000-320000,341300-341600                                  @SA70179
*A105000,222000,503000                                         @SA70180
*D270000                                                       @SA70180
*A174000                                                       @SA70190
*C162000-163000,170000,172000                                  @SA70190
*A366000                                                       @YA07104
*C394000                                                       @YA07104
*D266000                                                       @YA10963
*A158000,162000,165000,529000                                  @OX11945
*C133000,174630-174700,176000                                  @OX11945
*D164000-165000,168000-172000,174070-174210                    @OX11945
*C000003,007000,009000,013000,015000-019000,033000,035000      @Y17XASK
*C037000,041000-045000,051000,060000,065000,067000             @Y17XASK
*D000004-005000,059000                                         @Y17XASK
*A027300-027600,045500,047500,065100-065500,067500             @Y17XASK
*A068020-068600                                                @Y17XASK
*A085000,089500,103600,122100,133300,137100-137500,138500      @Y17XASA
*A140200-140700,155500-155600,156500-156600,168200             @Y17XASA
*A179050-179650,220100-220200,221500,222000-222210             @Y17XASA
*A222270-222320,222870-222875,222890,232500-232900,241200      @Y17XASA
*A261700,274950-274960,277500,287700-287815,289100-290955      @Y17XASA
*A297500,339500-339900,385050-385980,387500,413500             @Y17XASA
*A417050-417600,458500,459500,460500,465500-465600,480000      @Y17XASA
*A494700,494740,503050,523500,529100,529400,529700-529900      @Y17XASA
*A552500,577200-577700,588650-588880                           @Y17XASA
*C167000,236000,268000,296000,365000,367000,448000,488500      @Y17XASA
*C503300                                                       @Y17XASA
*D098000,103000,104000,109000-110000,114000-115000,109100      @Y17XASA
*D156000,190000,222280-222320,235100,235600,241000,243000      @Y17XASA
*D260000,262000-265000,276000,294000-295000,329000,363000      @Y17XASA
*D364000,444600-447000,478000,479000,480000,488500,489000      @Y17XASA
*D499000,502000                                                @Y17XASA
*A310000,C494600                                               @OX12574
*C508000                                                       @OY15041
*D177200,530000                                                @OY15041
*A192000                                                       @OX14797
* A019000,041000,068857,075000,081000,083000,112500,133600,    @G36XRSV
* A134000,174560,192000,207000,211000,214000,222600,401000,    @G36XRSV
* A415800,418000,419000,420000,423000,496000,497000,507100,    @G36XRSV
* A509000,543000                                               @G36XRSV
* C065400,068040,073000-074000,134000,174700,214000,           @G36XRSV
* C217000,232200,415200,419000,423000,496000,509000            @G36XRSV
* D174630,176000,215000-216000,232300-232500,424000-466000,    @G36XRSV
* D498000                                                      @G36XRSV
*A242000,246000,269000                                         @OY18045
*D259000-260000                                                @OY18045
*C529500                                                       @OX11945
*A019840                                                       @OZ17621
*D416728-416760                                                @OZ26897
*C232200                                                       @OZ26897
*C191000                                                       @OZ30223
*A196600                                                       @OZ28724
*A416326,A416333                                               @OZ30460
*A114900,415910,416656,496600,503050                           @OZ30604
*C207400,415928-416624,420500-420700,507300-507400,507700      @OZ30604
*A415921                                                       @OZ28809
*****************************************************************S21903
         EJECT
* R E G I S T E R   E Q U A T E S                                S21903
         SPACE
RWRK0    EQU   0                        WORK REGISTER 0        @G36XRSV
RWRK1    EQU   1                        WORK REGISTER 1        @G36XRSV
RCCW     EQU   2                        CURRENT CCW            @Y17XASA
RWRK2    EQU   2                        WORK REGISTER 2        @G36XRSV
RTABL    EQU   3                        TRACE CONTROL TABLE      S21903
RLCB     EQU   4                        LCB BASE REGISTER      @Y17XASA
RGTIC    EQU   5                        TIC CCW                  S21903
RWRK6    EQU   6                        WORK REGISTER 6          S21903
RWRK7    EQU   7                        WORK REGISTER 7          S21903
RUCB     EQU   7                        UCB REGISTER REG 7     @Y17XASA
RCVTD    EQU   8                        CVT BASE REGISTER        S21903
RWRK8    EQU   8                        WORK REGISTER 8        @G36XRSV
RWRK9    EQU   9                        WORK REGISTER 9          S21903
RDCB     EQU   10                       DCB BASE REGISTER        S21903
RWRKA    EQU   10                       WORK REGISTER 10       @G36XRSV
RWRKB    EQU   11                       WORK REGISTER 11       @Y17XASA
RBASE    EQU   12                       CSECT BASE REGISTER      S21903
RAVT     EQU   13                       AVT BASE REGISTER        S21903
RBALR    EQU   14                       BALR RETURN LINKAGE      S21903
RWRKF    EQU   15                       CURRENT TRACE ENTRY    @Y17XASA
RNTRY    EQU   15                       ENTRY REG 15           @Y17XASA
         SPACE
* O T H E R   E Q U A T E S                                      S21903
         SPACE
BLANK    EQU   X'40'                    BLANK                  @Y17XASA
ZERO     EQU   X'00'                    INITIALIZATION           S21903
ONE      EQU   X'01'                    CONSTANT =  1            S21903
TWO      EQU   X'02'                    CONSTANT =  2            S21903
THREE    EQU   X'03'                    CONSTANT =  3            S21903
FOUR     EQU   X'04'                    CONSTANT =  4            S21903
INVTIC   EQU   X'07'                    TEST FOR INVALID TIC     S21903
TIC      EQU   X'07'                    TEST FOR TIC CCW         S21903
SEVEN    EQU   X'07'                    MASK FOR ICM OR STCM   @Y17XASA
CSWLOC   EQU   X'40'                    LOW CORE CSW LOCATION  @YM07302
LINEID   EQU   X'F0'                    LINE ENTRY INDICATOR     S21903
FA       EQU   X'FA'                    TERMINAL NAME ENTRY    @SA70180
HIBYTE   EQU   X'08'                    MASK FOR HI BYTE OF REG@G36XRSV
*                                        'IN USE'              @G36XRSV
TWO56    EQU   256                      TWO HUNDRED FIFTY-SIX  @OZ30604
PCIOP    EQU   X'80'                    PCI RUNNING INDICATOR  @OZ28809
         EJECT
         USING IEDQRQE,RWRK1            RQE ADDRESSIBILITY     @Y17XASA
         USING IGG019Q0,RNTRY           TEMPORARY BASE           X02004
         USING IEDQLCB,RLCB             LCB DSECT ADDRESSIBILITY S21903
         USING IEDQCCW,RCCW             CCW DSECT ADDRESSIBILITY S21903
         USING LINETAB,RTABL            LINE TABL ADDRESSIBILITY S21903
         USING CVTDSECT,RCVTD           CVT DSECT ADDRESSIBILITY S21903
         USING IEDQUCB,RUCB             UCB DSECT ADDRESSIBILITY S21903
         USING IHADCB,RDCB              DCB ADDRESSIBILITY     @Y17XASA
         SPACE
*****************************************************************S21903
*                                                                S21903
* THIS SECTION OF CODE SAVE CALLER'S REGISTERS IN A LOCAL        S21903
* SAVEAREA. THE TRACE INDICATOR (LCBTRACE) IS TESTED IN THE LCB  S21903
* TO DETERMINE IF A TRACE IS DESIRED FOR THIS LINE, IF NOT,      S21903
* CONTROL IS PASSED TO THE LABEL 'RETURN'.                       S21903
*                                                                S21903
*****************************************************************S21903
         SPACE
         B     START                    BRANCH AROUND ID       @OX11945
         DC    A(LINETABL-LTSIZE)       ADCON USED BY IGCM610D @OX11945
IGG019Q0 IEDHJN ,HJN                                           @Y17XASA
START    EQU   *                                               @OX11945
         CLM   RNTRY,HIBYTE,FOXFOX      FROM PCI?              @G36XRSV
         BNE   NOPCISAV                 N-BR                   @G36XRSV
         STM   RWRK0,RNTRY,Q0SAVE1      SAVE CALLER'S REGISTERS@G36XRSV
         B     GETBASE                                         @G36XRSV
NOPCISAV DS    0H                                              @G36XRSV
         STM   RWRK0,RNTRY,Q0SAVE2      SAVE REGS FOR NON-PCI  @G36XRSV
GETBASE  DS    0H                                              @G36XRSV
         LR    RBASE,RNTRY              COPY BASE REGISTER       X02004
         DROP  RNTRY                                             X02004
         USING IGG019Q0,RBASE           CSECT ADDRESSIBILITY     X02004
         L     RCVTD,CVTPTR             CVT ADDRESS              S21903
         L     RAVT,CVTAQAVT            TCAM TCX ADDRESS         S21903
         USING TCXAVT,RAVT              TCX ADDRESSIBILITY     @Y17XASA
         L     RAVT,TCXAVT              TCAM AVT ADDRESS         S21903
         USING IEDQAVTD,RAVT            AVT DSECT ADDRESS      @Y17XASA
         LTR   RWRK1,RWRK1              ATTENTION ENTRY        @Y17XASA
         BNZ   NOTATTN1                 NO                     @Y17XASA
         LR    RLCB,RWRK9               GET IOB ADDRESS        @Y17XASA
         B     ATTNENT1                 CONTINUE               @Y17XASA
NOTATTN1 EQU   *                                               @Y17XASA
         L     RLCB,RQEIOB              IOB ADDRESS FROM RQE     S21903
ATTNENT1 EQU   *                                               @Y17XASA
         SH    RLCB,LCBBKUP             BACK UP TO LCB PROPER    S21903
         LA    RLCB,ZERO(,RLCB)         CLEAR HI-ORDER BYTE    @YM09095
         TM    LCBSTAT2,LCBTRACE        TRACING ON THIS LINE     S21903
         BO    TRACE                    YES                    @Y17XASA
         TM    AVTIOTR,AVTIOTRE         ERROR TRACE SPECIFIED  @Y17XASA
         BZ    RETURN                   NO , RETURN            @Y17XASA
         LTR   RWRK1,RWRK1              ATTENTION ENTRY        @Y17XASA
         BZ    RETURN                   YES , RETURN           @Y17XASA
         TM    LCBCSWUS,LCBUSUC         UNIT CHECK IN CSW      @Y17XASA
         BNO   RETURN                   BR NO TO RETURN          S21903
         EJECT
*****************************************************************S21903
*                                                                S21903
* THIS SECTION OF CODE SCANS A MULTIPLE-ENTRY LINE CONTROL TABLE S21903
* (LINETAB DSECT) TO DETERMINE IF AN ENTRY ALREADY EXISTS FOR    S21903
* THIS LINE. IF SO CONTROL IS PASSED TO THE LABEL 'THISNTRY'.    S21903
* OTHERWISE THE TABLE IS SEARCHED FOR AN UNUSED OR AVAILABLE     S21903
* ENTRY. IF ONE IS FOUND IT IS INITIALIZED FOR THIS LINE AND     S21903
* CONTROL IS PASSED TO THE LABEL 'THISNTRY'. OTHERWISE CONTROL   S21903
* IS PASSED TO THE LABEL 'RETURN' AND NO TRACE IS PERFORMED      S21903
* ON THIS LINE EVEN THOUGH REQUESTED.                            S21903
*                                                                S21903
*****************************************************************S21903
         SPACE
TRACE    EQU   *                                               @Y17XASA
         L     RDCB,LCBDCBPT            DCB ADDRESS FROM LCB   @Y17XASA
         LTR   RWRK1,RWRK1              ATTENTION ENTRY        @Y17XASA
         BZ    ATTNENT2                 YES                    @Y17XASA
         SLR   RUCB,RUCB                CLEAR REGISTER         @Y17XASK
         ICM   RUCB,3,RQEUCB            GET UCB ADDR FROM RQE  @YM06131
ATTNENT2 EQU   *                                               @Y17XASA
         LH    RWRK9,UCBCHA             CHANNEL/UNIT ADDRESS   @OX11945
         N     RWRK9,CLEARHI            CLEAR UNWANTED BITS    @OX11945
         LA    RTABL,LINETABL           LINE CONTROL TABLE ADDR  S21903
TABLSRCH EQU   *                                                 S21903
         CLI   LTCUU,AVTEFF             END OF TABLE             S21903
         BE    RESEARCH                 TEST IF TABLE FULL.    @SA70190
         TM    LTPCI,LTINUSE            ENTRY IN USE           @OX11945
         BZ    TBLSRCH1                 BRANCH NO              @OX11945
         CH    RWRK9,LTCUU              ENTRY FOR THIS ADDRESS   S21903
         BE    ATTNTEST                 BR TO TEST ATTN        @Y17XASA
TBLSRCH1 EQU   *                                               @SA70190
         LA    RTABL,LTSIZE(,RTABL)     POINT TO NEXT ENTRY      S21903
         B     TABLSRCH                 BR TO CONTINUE SEARCH    S21903
RESEARCH EQU   *                                               @SA70190
         LA    RTABL,LINETABL           RESET TO BEGINNING.    @SA70190
TBLSRCH2 EQU   *                                               @SA70190
         CLI   LTCUU,AVTEFF             TEST FOR END OF TABLE. @SA70190
         BE    RETURN                   YES - SKIP TRACE       @SA70190
         TM    LTPCI,LTINUSE            IS SLOT IN USE ?       @G36XRSV
         BO    TBLSRCH3                 Y-GET NEXT SLOT        @G36XRSV
         L     RWRKB,LTPCI              CONTAINS 'IN USE' BIT  @G36XRSV
         L     RWRKF,INUSEMSK           SET BIT IN REG         @G36XRSV
         CS    RWRKB,RWRKF,LTPCI        SET BIT IF NOT 'IN USE'@G36XRSV
         BZ    NEWENTRY                 NOT ALREADY 'IN USE'   @G36XRSV
TBLSRCH3 DS    0H                                              @G36XRSV
         LA    RTABL,LTSIZE(,RTABL)     BUMP BY TABLE WIDTH.   @SA70190
         B     TBLSRCH2                 LOOP THRU TABLE TILL   @SA70190
*                                       END OR EMPTY SLOT FOUND.SA70190
NEWENTRY EQU   *                                                 S21903
         STH   RWRK9,LTCUU              SET NEW CHAN/UNIT ADDR   S21903
         STH   RWRK7,LTUCBAD            SAVE UCB ADDRESS         S21903
         TM    DCBDSRG2,DCBDSGTR        DCB FOR 370X           @Y17XASA
         BZ    NOT370X1                 NO                     @Y17XASA
         OI    LTPCI,LTP370X            INDICATE 370X ENTRY    @Y17XASA
NOT370X1 EQU   *                                               @Y17XASA
         TM    LCBSTAT2,LCBTRACE        TRACE ACTIVE ON LINE   @Y17XASA
         BO    ATTNTEST                 YES                    @Y17XASA
         OI    LTPCI,LTFIRST+LTERROR    FIRST AND ERROR SWITCH @Y17XASA
         OI    LCBSTAT2,LCBTRACE        INDICATE TRACE IN LCB  @Y17XASA
ATTNTEST EQU   *                                               @Y17XASA
         LTR   RWRK1,RWRK1              ATTENTION ENTRY        @Y17XASA
         BNZ   THISNTRY                 NO                     @Y17XASA
         OI    LTPCI,LTLATTN            INDICATE ATTN ENTRY    @Y17XASA
         B     TRACEIT                  BRANCH TO TRACEIT      @Y17XASA
         EJECT
*****************************************************************S21903
*                                                                S21903
* THIS SECTION OF CODE POINTS TO THE CURRENT TRACE ENTRY AND     S21903
* BUILDS A HEADER ENTRY FOR THIS INTERRUPT. THE FORMAT OF THIS   S21903
* ENTRY IS DESCRIBED BY THE TRACEHDR DSECT.                      S21903
*                                                                S21903
*****************************************************************S21903
         SPACE
THISNTRY EQU   *                                                 S21903
         TM    LCBFLAG1,LCBERPIN        ENTRY FROM ERP         @OZ30223
         BNO   NOTERP                   BR NO NOTHING SPECIAL    S21903
         TM    LCBCSW+FOUR,LCBCSPCI     PCI INTERRUPT          @OX14797
         BO    TESTEND                  BRANCH YES             @OX14797
         OI    LTPCI,LTITSERP           INDICATE THIS ENTRY HAS  S21903
*                                         BEEN TRACED UNDER ERP  S21903
         B     TESTEND                  BR TO TRACE ENTRY       SA64870
NOTERP   EQU   *                                                 S21903
         CLI   LCBECBCC,LCBECB41        PERMANENT ERROR         OX02202
         BNE   NOTERP1                  BRANCH NO              @OZ28724
         NI    LTPCI,AVTEFF-LTPCIN-LTAUTOPL                    @OZ28724
         B     SETRET                   BRANCH IF PERMERR      @OZ28724
NOTERP1  EQU   *                                               @OZ28724
         TM    LTPCI,LTERPPCI           HAS THIS ENTRY BEEN    @Y17XASA
*                                         TRACED UNDER ERP OR PCIS21903
         BNZ   SETRET                   BR YES FORGET IT NOW     S21903
TESTEND  EQU   *                                                SA64870
         TM    LCBCSWUS,LCBUSCE+LCBUSDE CHANNEL/DEVICE END       S21903
         BNO   TRACEIT                  BR NO NOT DUPLICATE      S21903
         TM    LCBCSWCS,LCBCSPCI        PCI INTERRUPT            S21903
         BNO   TRACEIT                  BR NO NOT DUPLICATE      S21903
         OI    LTPCI,LTITSPCI           ENTRY IS FROM PCI AND    S21903
*                                         WILL BE ENTERED AGAIN  S21903
*                                         FROM LINEEND-SET SKIP  S21903
TRACEIT  EQU   *                                                 S21903
         CLM   RBASE,HIBYTE,AVTHFF      ENTRY FROM PCI?        @G36XRSV
         BE    PCITRACE                 Y-BR                   @G36XRSV
         LA    RWRKF,TABLE2             POINT TO TEMP TABLE    @G36XRSV
         XC    TABLE2(TWO56),TABLE2     CLEAR TABLE            @OZ30604
         XC    TABLE2+TWO56(TWO56),TABLE2+TWO56                @OZ30604
         XC    TABLE2+TWO*TWO56(TWO56),TABLE2+TWO*TWO56        @OZ30604
         XC    TABLE2+THREE*TWO56(TWO56),TABLE2+THREE*TWO56    @OZ30604
         B     TRACEIT1                                        @G36XRSV
PCITRACE DS    0H                                              @G36XRSV
         L     RWRKF,AVTRACE            I/O TRACE CONTROL WORDS  S21903
         USING TRACNTRL,RWRKF                                    S21903
         L     RWRKF,TRACURR            POINT TO CURRENT ENTRY   S21903
         DROP  RWRKF                                             S21903
TRACEIT1 DS    0H                                              @G36XRSV
         USING TRACEHDR,RWRKF                                    S21903
         MVC   TRACCUU,LTCUU            SET CHANNEL/UNIT ADDRESS S21903
         L     RWRK1,TRACECNT           OLD SEQUENCE NUMBER    @G36XRSV
TCOUNT   DS    0H                                              @G36XRSV
         LA    RWRK2,ONE(RWRK1)         INCREMENT BY 1         @G36XRSV
         CS    RWRK1,RWRK2,TRACECNT     AND STORE              @G36XRSV
         BNZ   TCOUNT                   BR IF ALREADY UPDATED  @G36XRSV
         STH   RWRK2,TRACSEQ            TRACE SEQUENCE NUMBER  @G36XRSV
         MVC   TRACFLG1,LCBFLAG1        TRACE IOB FLAGS          S21903
         MVC   TRACPSRT,LCBSTART        TRACE CHAN PROG START    S21903
         MVC   TRACSW,LCBCSW            TRACE CSW              @Y17XASA
         TM    LTPCI,LTLATTN            ATTENTION ENTRY        @Y17XASA
         BO    TRACECSW                 BR YES, TRACE REAL CSW @YM07302
         MVC   TRACSENS,LCBSENS0        TRACE SENSE INFO       @Y17XASA
         B     TRACEITX                                        @YM07302
TRACECSW EQU   *                                               @YM07302
         MVC   TRACSW,CSWLOC+ONE        TRACE LOW CORE CSW     @YM07757
TRACEITX EQU   *                                               @Y17XASA
         BAL   RBALR,WRAPTEST           CHECK FOR END OF TRACE   S21903
         EJECT                                                 @SA70180
****************************************************************SA70180
*                                                              @SA70180
* THIS SECTION OF CODE WILL DETERMINE THE CURRENTLY ATTACHED   @SA70180
* TERMINAL AS PER THE CONTENTS OF LCBTTCIN OR LCBLNENT, IF     @SA70180
* THESE ARE INVALID OR INDETERMINATE THE UCB NAME WILL BE      @SA70180
* INSERTED INSTEAD. POSSIBLE CAUSE OF THE ABOVE WILL BE IN THE @SA70180
* CASE OF A DIAL LINE AND/OR OPEN PROCESSING.                  @SA70180
*                                                              @SA70180
****************************************************************SA70180
         SPACE 1                                               @SA70180
         USING TRACINFO,RWRKF                                  @SA70180
         MVI   TRACIND,FA               INDICATE ADDL ENTRY.   @SA70180
         MVI   TRACNAME,BLANK           MOVE BLANK TO NAME     @Y17XASA
         MVC   TRACNAME+1(L'TRACNAME-1),TRACNAME  BLANK NAME   @Y17XASA
         TM    LTPCI,LTLATTN            ATTENTION ENTRY        @Y17XASA
         BZ    NOTATTN3                 NO                     @Y17XASA
         TM    LTPCI,LTP370X            370X ENTRY             @Y17XASA
         BNO   NOTDIAL                  NO                     @Y17XASA
NOTATTN3 EQU   *                                               @Y17XASA
         LH    RWRK6,LCBTTCIN           TEST IF A TERMINAL     @SA70180
         LTR   RWRK6,RWRK6              SAVE TTCIN AND TRACE IT@SA70180
         BNZ   TNAMETBL                 NO TTCIN- TRY LINE ENT @SA70180
         TM    LCBSTAT2,LCBDIAL         TEST FOR DIAL          @SA70180
         BZ    NOTDIAL                  NO - DEFAULT TO UCB NM.@SA70180
         LH    RWRK6,LCBLNENT           TEST IF LINE ENTRY     @SA70180
         LTR   RWRK6,RWRK6              ZERO = NO LN ENTRY.    @SA70180
         BNZ   TNAMETBL                 YES = DO NAME SEARCH.  @SA70180
NOTDIAL  EQU   *                                               @SA70180
         LH    RUCB,LTUCBAD             GET UCB ADDRESS.       @SA70180
         N     RUCB,AVTCLRHI            CLEAR SIGN PROPAGATION.@SA70180
         MVC   TRACNAME(THREE),UCBNAME  TRACE UCB NAME         @SA70180
         B     TNAMEXIT                 CONTINUE TRACEING.     @SA70180
TNAMETBL EQU   *                                               @SA70180
         N     RWRK6,AVTCLRHI           CLEAR SIGN PROPAGATION @G36XRSV
         STH   RWRK6,TRACTCIN           TRACE LNENT CONTENTS.  @SA70180
         L     RWRK7,AVTRNMPT           TERMINAL NAME TABLE ADDRSA70180
         USING IEDQTNTD,RWRK7           TEMP BASE FOR TNT.     @SA70180
         LA    RWRK1,TNTFIRST           ADDR OF FIRST ENTRY.   @SA70180
         SR    RWRKB,RWRKB              CLEAR REG FOR WORK.    @SA70180
         IC    RWRKB,TNTENLEN           LTH OF EACH ENTRY.     @SA70180
         LA    RWRK7,THREE(,RWRKB)      TERM NAME + TERM CTL.  @SA70180
         BCTR  RWRK6,RWRK0              DECREMENT INDEX        @SA70180
         MR    RWRK6,RWRK6              COMPUTE DISPLACEMENT TO@SA70180
         AR    RWRK7,RWRK1              ENTRY,ADDRESS OF ENTRY.@SA70180
         BCTR  RWRKB,RWRK0              DECREMENT SIZE FOR EX. @SA70180
         EX    RWRKB,MOVENAME           MOVE TERM NAME TO SLOT.@SA70180
TNAMEXIT EQU   *                                               @SA70180
         TM    LTPCI,LTP370X            370X ENTRY             @Y17XASA
         BO    NCP370X1                 YES                    @Y17XASA
         MVC   TRACINV,LCBINVPT         TRACE LCBINVPT         @Y17XASK
NCP370X1 EQU   *                                               @Y17XASA
         BAL   RBALR,WRAPTEST           CHECK FOR END OF TBL.  @SA70180
         DROP  RWRKF                                           @SA70180
         DROP  RWRK7                                           @SA70180
         EJECT
*****************************************************************S21903
*                                                                S21903
* THIS SECTION OF CODE DETERMINES THE APPROPRIATE CCW AT WHICH   S21903
* TO BEGIN THE TRACE. ON A PCI INTERRUPT THE CCW ADDRESS SAVED   S21903
* FROM THE LAST INTERRUPT IS USED. ON ANY INTERRUPT WITH ERP IN  S21903
* CONTROL THE START CCW ADDRESS IS TAKEN FROM LCBSTART.          S21903
*                                                                S21903
*****************************************************************S21903
         SPACE
         TM    LTPCI,LTLATTN            ATTENTION ENTRY        @Y17XASA
         BZ    NOTATTN                  NO                     @OZ26897
         NI    LTPCI,AVTEFF-LTLATTN     RESET ENTRY            @OZ26897
         B     MOVETRAC                 ATTENTION ENTRY        @OZ26897
NOTATTN  EQU   *                                               @OZ26897
         TM    LTPCI,LTPCIN             WAS LAST INTERRUPT PCI   S21903
         BNO   USESTART                 BR NO TO TEST RESTART    S21903
         L     RCCW,LTPCI               LAST PCI INTERRUPT       S21903
         LA    RCCW,ZERO(,RCCW)         CLEAR HIGH ORDER BYTE    X02004
         CLI   CCWOPCDE,ZERO            ZERO CCW OP-CODE MEANS   X02004
*                                       VIRTUAL DATA ADDRESS     X02004
         L     RWRK6,CCWADDR-ONE        GET DATA ADDRESS         X02004
         BE    CCWSTART                 BR YES,VIRTUAL DATA ADDR X02004
         B     CONVERTX                 BR CCW FOUND           @Y17XASA
USESTART EQU   *                                                 S21903
         L     RCCW,LCBSTART-ONE        CHAN PROG START CCW      S21903
         LA    RCCW,ZERO(,RCCW)         CLEAR HIGH ORDER BYTE    S21903
CONVERTX EQU   *                                               @Y17XASA
         BAL   RBALR,CONVERT            CONVERT TO VIRTUAL       X02004
CCWSTART EQU   *                                                 X02004
         SR    RGTIC,RGTIC              CLEAR TIC INDICATOR      S21903
         SR    RWRK7,RWRK7              ZERO REG 7             @OY18045
         CLI   LCBCCW4,CCWPOLL          AUTOPOLL                 S21903
         BNE   RESTART                  BR NO NOT AUTOPOLL       S21903
         OI    LTPCI,LTAUTOPL           SET AUTOPOLL INDICATOR   S21903
         B     RESTART                  START TRACING          @OY18045
         EJECT
*****************************************************************S21903
*                                                                S21903
* THIS SECTION OF CODE IS ENTERED FOR EACH TRACE ENTRY TO BE     S21903
* CONSTRUCTED. THE FORMAT OF EACH ENTRY IS DESCRIBED BY THE      S21903
* TRACELN1 DSECT. FOR CCWS WITH LESS THAN TWENTY BYTES OF DATA,  S21903
* ALL DATA IS TRACED. FOR CCWS WITH MORE THAN TWENTY BYTES OF    S21903
* DATA ONLY THE FIRST EIGHT AND LAST TEN BYTES ARE TRACED, USING S21903
* THE TRACELN1 AND TRACELN2 DSECTS AND TWO TRACE ENTRIES.        S21903
*                                                                S21903
*****************************************************************S21903
         SPACE
TRACEALL EQU   *                                                 S21903
         CLM   RCCW,SEVEN,LCBCSW        FIRST CCW THE INTRPT   @Y17XASA
         BE    PREPEXIT                 BR YES , END OF TRACE  @Y17XASA
RESTART  EQU   *                                               @OY18045
         USING TRACELN1,RWRKF                                    S21903
         ST    RCCW,TRACCWAD            TRACE CCW ADDRESS        S21903
         MVI   TRACCWAD,LINEID          INDICATE LINE ENTRY      S21903
         ST    RWRK6,TRACCW             TRACE DATA ADDRESS       X02004
         MVC   TRACCW(ONE),CCWOPCDE     TRACE CCW OP CODE        X02004
         CLI   CCWOPCDE,ZERO            ZERO OP CODE             X02004
         BNE   GOODOP                   BR NO, GOOD CCW OP CODE  X02004
         MVI   TRACCW,CCWREAD           ASSUME READ CCW          X02004
         TM    LCBSTAT1,LCBSENDN        SENDING                  X02004
         BNO   GOODOP                   BR NO, READ IS RIGHT     X02004
         MVI   TRACCW,CCWWRITE          SET WRITE CCW            X02004
GOODOP   EQU   *                                                 X02004
         MVC   TRACCW+FOUR(FOUR),CCWFLAGS TRACE LAST HALF CCW    X02004
         TM    LTPCI,LTP370X            370X ENTRY             @Y17XASA
         BO    NCP370X2                 YES                    @Y17XASA
         BAL   RBALR,TRACOPCD           TRACE TP OP CODE         S21903
NCP370X2 EQU   *                                               @Y17XASA
         TM    TRACCW,TIC               TIC CCW                  X02004
         BZ    TICCCW                   BR YES HANDLE UNIQUELY   S21903
         TM    TRACCW,CCWWRITE          WRITE CCW                X02004
         BO    NOAUTOPL                 BR YES TO TRACE DATA     S21903
         TM    CCWFLAGS,CCWSKIP         SKIP DATA                S21903
         BO    DATADONE                 NO DATA FOR SKIP.      @SA70179
         CLI   TRACCW,CCWREAD           READ CCW                 X02004
         BNE   NOAUTOPL                 BR NO NO AUTOPOLL        S21903
         NI    LTPCI,AVTEFF-LTAUTOPL    RESET AUTOPOLL INDICATOR S21903
NOAUTOPL EQU   *                                                 S21903
         CLI   TRACCW,CCWNOP            NO-OP CCW                Y00487
         BNE   NOTNOP                   NO NOT NOP             @Y17XASA
         TM    LTPCI,LTP370X            370X ENTRY             @Y17XASA
         BZ    DATADONE                 NO                     @Y17XASA
         SH    RCCW,CCWBKUP             SUBTRACT 8             @Y17XASA
         CLI   CCWOPCDE,CCWBRK          PREVIOUS CCW WR-BRK    @Y17XASA
         LA    RCCW,CCWLEN(,RCCW)       BUMP TO NEXT CCW       @Y17XASA
         BNE   DATADONE                 NO                     @Y17XASA
         BAL   RBALR,WRAPTEST           CHECK FOR END OF TRACE @Y17XASA
         LA    RCCW,CCWLEN(,RCCW)       BUMP TO NEXT CCW       @Y17XASA
         CLM   RCCW,SEVEN,LCBCSW        INTERRUPTED CCW        @Y17XASA
         BNE   TICEND                   NO                     @Y17XASA
         B     SKIPPCI                  YES                    @Y17XASA
NOTNOP   EQU   *                                               @Y17XASA
         LH    RWRK1,CCWCOUNT           GET DATA COUNT           S21903
         N     RWRK1,AVTCLRHI           CLEAR HIGH HALF WORD     S21903
         TM    LTPCI,LTP370X            370X ENTRY             @Y17XASA
         BZ    NOT370X2                 NO                     @Y17XASA
         LR    RWRK9,RWRK1              SAVE COUNT             @Y17XASA
         LH    RWRK1,LCBTTCIN           GET TNT INDEX          @Y17XASA
         LR    RWRKB,RWRKF              SAVE REG 15            @Y17XASA
         L     RNTRY,AVTRNMPT           ADDRESS OF TNT ROUTINE @Y17XASA
         BALR  RBALR,RNTRY              BALR TO TNT ROUTINE    @Y17XASA
         LR    RWRKF,RWRKB              RESTORE REG 15         @Y17XASA
         SH    RWRK1,TRMPREFX           SUBTRACT PREFIX SIZE   @Y17XASA
         USING IEDNTRM,RWRK1            TERMINAL ENTRY ADDRES  @Y17XASA
         TM    TRMNCP,TRMIPLDM          IPL/DUMP OF 370X       @Y17XASA
         DROP  RWRK1                    DROP ADDRESSIBILITY    @Y17XASA
         LR    RWRK1,RWRK9              RESTORE COUNT          @Y17XASA
         BO    NOT370X2                 YES , TRACE NORMAL     @Y17XASA
         CR    RCCW,RLCB                IS CCW BEFORE LCB?     @YM04622
         BL    NOTINLCB                 YES-TRACE DATA         @YM04622
         SLR   RWRK9,RWRK9              CLEAR REGISTER         @YM04622
         IC    RWRK9,DCBEIOBX           GET LCB SIZE           @YM04622
         LA    RWRK9,ZERO(RWRK9,RLCB)   GET END OF LCB ADDRESS @YM04622
         CR    RCCW,RWRK9               IS CCW AFTER LCB?      @YM04622
         BL    DATADONE                 NO-DON'T TRACE DATA    @YM04622
NOTINLCB EQU   *                        THERE IS DATA TO TRACE @YM04622
         CLI   TRACCW,CCWREAD           READ CCW               @Y17XASA
         BNE   NOT370X2                 NO                     @Y17XASA
         LTR   RWRK7,RWRK7              START OF PIU           @Y17XASA
         BNZ   NCP370X3                 NO                     @Y17XASA
         USING IEDQPRF,RCCW             BUFFER PREFIX ADDRESS  @Y17XASA
         IC    RWRK7,PRFPIUO            GET PIU OFFSET         @Y17XASA
         LA    RWRK6,ZERO(RWRK6,RWRK7)  ADD TO DATA ADDRESS    @Y17XASA
         SR    RWRK1,RWRK7              SUBTRACT FROM COUNT    @Y17XASA
         USING IEDTH,RWRK6              TH ADDRESSIBILITY      @Y17XASA
         LH    RWRKB,TTHDCF             GET DCF                @Y17XASA
         DROP  RWRK6                                           @Y17XASA
         USING IEDQCCW,RCCW             CCW DSECT ADDRESS      @Y17XASA
         LA    RWRK7,TTHLEN(RWRK7,RWRKB) ADD TH PAD & DCF      @Y17XASA
         LH    RWRK9,AVTKEYLE           SIZE OF UNIT           @Y17XASA
         LR    RWRK0,RWRK6              SAVE DATA ADDRESS      @Y17XASA
         SR    RWRK6,RWRK6              CLEAR REG 6            @Y17XASA
         DR    RWRK6,RWRK9              COMPUTE # OF UNITS     @Y17XASA
         STH   RWRK6,LTPIURES           SAVE RESIDUAL COUNT    @Y17XASA
         LTR   RWRK6,RWRK6              REMAINDER EQ ZERO      @Y17XASA
         LR    RWRK6,RWRK0              RESTORE DATA ADDRESS   @Y17XASA
         BZ    NCP370X3                 YES                    @Y17XASA
         LA    RWRK7,ONE(,RWRK7)        ADD ONE TO QUOTIENT    @Y17XASA
NCP370X3 EQU   *                                               @Y17XASA
         BCT   RWRK7,NOT370X2           DEC REG 7 IF NE 0 BR   @Y17XASA
         LA    RWRK0,CCWLEN(,RCCW)      NEXT CCW               @Y17XASA
         CLM   RWRK0,SEVEN,LCBCSW       CURRENT EQUAL FAILING  @Y17XASA
         BE    SUBRESCT                 YES , SUBT RESIDUAL    @Y17XASA
         LH    RWRK1,LTPIURES           GET PIU RESIDUAL COUNT @Y17XASA
         B     ALLDATA                  BRANCH TO ALLDATA      @Y17XASA
NOT370X2 EQU   *                                               @Y17XASA
         TM    LCBCSWUS,LCBUSCE+LCBUSDE CHANNEL/DEVICE END       S21903
         BZ    ALLDATA                  BR NO TRACE ALL DATA     S21903
         LA    RWRK0,CCWLEN(,RCCW)      NEXT CCW                 S21903
         CLM   RWRK0,SEVEN,LCBCSW       CURRENT EQUAL FAILING  @Y17XASA
         BNE   ALLDATA                  BR NO USE CCW COUNT      S21903
SUBRESCT EQU   *                                               @Y17XASA
         SH    RWRK1,LCBCSWRC           SUBTRACT RESIDUAL COUNT  S21903
         EJECT
*****************************************************************S21903
*                                                                S21903
* THIS SECTION OF CODE WILL TRACE FOUR OR LESS BYTES OF DATA IN  S21903
* SAME TRACE ENTRY. FOR CCWS WITH MORE THAN FOUR BYTES OF DATA,  S21903
* A SECOND ENTRY IS USED ALONG WITH THE FIRST TO TRACE TWENTY    S21903
* BYTES OF DATA. IF THERE ARE MORE THAN TWENTY BYTES OF DATA,    S21903
* ONLY THE FIRST EIGHT AND LAST TEN ARE TRACED.                  S21903
*                                                                S21903
*****************************************************************S21903
         SPACE
ALLDATA  EQU   *                                                 S21903
         TM    CCWFLAGS,CCWIDA          TEST FOR INDIRECT ADDR @OX12574
         BNO   SKIPCK                   NO, SKIP CONVERT CODE  @OX12574
         LR    RWRK9,RWRK1              SAVE DATA COUNT        @OX12574
         L     RWRK1,ZERO(RWRK6)        GET REAL IDA           @OX12574
         BAL   RBALR,CONVERT2           CONVERT DATA ADDR      @OX12574
         LR    RWRK1,RWRK9              RESTORE DATA COUNT     @OX12574
         STCM  RWRK6,7,TRACCW+1         TRACE VIRTUAL ADDRESS  @OX12574
SKIPCK   EQU   *                        DONE WITH INDIRECT     @OX12574
         CH    RWRK1,AVTHA4             FIVE OR MORE DATA BYTES  S21903
         BH    BOTHENDS                 BR YES TRACE FIRST/LAST  S21903
         BCTR  RWRK1,RWRK0              DECREMENT FOR EXECUTE    S21903
         LTR   RWRK1,RWRK1              ANY DATA TO BE MOVED     S21903
         BM    DATADONE                 BR NO TO GET NEXT CCW    S21903
         EX    RWRK1,MVCDATA1           VARIABLE MOVE OF DATA    S21903
         B     DATADONE                 BR TO GET NEXT CCW       S21903
BOTHENDS EQU   *                                                 S21903
         MVI   TRACCWAD,AVTEFF          INDICATE DOUBLE ENTRY    S21903
         MVC   TRACDAT1,ZERO(RWRK6)     TRACE FIRST FOUR BYTES   S21903
         BAL   RBALR,WRAPTEST           NEXT TRACE ENTRY         S21903
         DROP  RWRKF                                             S21903
         USING TRACELN2,RWRKF                                    S21903
         SH    RWRK1,AVTHA4             FOUR BYTES HAVE ALREADY  S21903
*                                         BEEN TRACED            S21903
         CH    RWRK1,AVTHA16            SIXTEEN OR LESS DATA     S21903
*                                         BYTES REMAINING        S21903
         BH    TWOENDS                  BR NO TRACE TWO ENDS     S21903
         BCTR  RWRK1,RWRK0              DECREMENT FOR EXECUTE    S21903
         EX    RWRK1,MVCDATA2           TRACE ALL DATA           S21903
         B     DATADONE                 FETCH NEXT CCW           S21903
TWOENDS  EQU   *                                                 S21903
         TM    LTPCI,LTP370X            370X ENTRY             @Y17XASA
         BZ    NOT370X3                 NO                     @Y17XASA
         MVC   TRACEDAT(TRACSIZE),FOUR(RWRK6) TRACE PIU        @Y17XASA
         B     DATADONE                                        @Y17XASA
NOT370X3 EQU   *                                               @Y17XASA
         MVC   TRACEDAT,FOUR(RWRK6)     TRACE FIRST EIGHT BYTES  S21903
         LA    RWRK6,FOUR(RWRK1,RWRK6)  POINT TO END OF DATA     S21903
         SH    RWRK6,DATABKUP           BACK UP TEN BYTES        S21903
         MVC   TRACEEND,ZERO(RWRK6)     TRACE LAST TEN DATA BYTESS21903
         DROP  RWRKF                                             S21903
         USING TRACELN1,RWRKF                                    S21903
DATADONE EQU   *                                                 S21903
         BAL   RBALR,WRAPTEST           CHECK FOR END OF TRACE   S21903
         EJECT
*****************************************************************S21903
*                                                                S21903
* THIS SECTION OF CODE DETERMINES IF THERE IS ANOTHER CCW TO     S21903
* BE TRACED. IF SO CONTROL IS PASSED TO THE LABEL 'TRACEALL'     S21903
* OTHERWISE CONTROL IS PASSED TO THE LABEL 'PREPEXIT'.           S21903
*                                                                S21903
*****************************************************************S21903
         SPACE
         TM    CCWFLAGS,CCWCD+CCWCC     COMMAND/DATA CHAINING    S21903
         LA    RCCW,CCWLEN(,RCCW)       POINT TO NEXT CCW        S21903
         BNZ   COMPEND                  BR YES TO GET NEXT CCW   S21903
         TM    LTPCI,LTAUTOPL           AUTOPOLL                 S21903
         BNO   PREPEXIT                 BR NO PREPARE TO RETURN  S21903
COMPEND  EQU   *                                                 S21903
         CLM   RCCW,SEVEN,LCBCSW        CHANNEL PROGRAM END    @Y17XASA
         BE    PREPEXIT                 BR YES PREPARE TO RETURN S21903
TICEND   EQU   *                                               @YA07104
         BAL   RBALR,CONVERT            CONVERT TO VIRTUAL       X02004
         B     TRACEALL                 BR TO CONTINUE TRACE   @Y17XASA
         EJECT
*****************************************************************S21903
*                                                                S21903
* THIS SECTION OF CODE HANDLES SPECIAL PROCESSING FOR TIC CCWS.  S21903
*                                                                S21903
*****************************************************************S21903
         SPACE
TICCCW   EQU   *                                                 S21903
         TM    CCWOPCDE,CCWREAL         TIC TO REAL ADDRESS      X02004
         L     RWRK1,CCWADDR-ONE        GET TIC TO ADDRESS       X02004
         BNO   GOODAD                   BR NO, ALREADY VIRTUAL   X02004
         BAL   RBALR,CONVERT1           CONVERT TO VIRTUAL       X02004
         STCM  RWRK1,SEVEN,TRACCW+ONE   TRACE TIC TO CCW ADDRESS X02004
GOODAD   EQU   *                                                 X02004
         LA    RWRK1,ZERO(,RWRK1)                                S21903
         LA    RWRK6,CCWLEN(,RWRK1)     NEXT CCW                 S21903
         CR    RGTIC,RCCW               STATUS MODIFY CCW LOOP   S21903
         LR    RGTIC,RCCW               SAVE TIC CCW ADDRESS     S21903
         BE    NEXTTIC                  BR YES STATUS MODIFIER   S21903
         CR    RCCW,RWRK6               TIC TO PREVIOUS CCW      S21903
         BE    NEXTTIC                  BR YES GET NEXT CCW      S21903
         TM    CCWADDR+TWO,INVTIC       INVALID TIC ADDRESS      S21903
         BNZ   WRAPEND                  BR YES END OF CHAN PROG  S21903
         TM    LTPCI,LTFIRST            FIRST TIME SWITCH SET  @Y17XASA
         BZ    GOODTIC                  NO                     @Y17XASA
         TM    LCBSTAT1,LCBRECVN        LINE RECEIVING         @Y17XASA
         BO    LINERECV                 YES                    @Y17XASA
         TM    LCBSTAT1,LCBSENDN        LINE SENDING           @Y17XASA
         BZ    NOPCI                    NO                     @Y17XASA
         TM    DCBPCI,DCBPCIA2+DCBPCIR2 PCI=( ,A) OR ( ,R)     @Y17XASA
         BZ    NOPCI                    NO                     @Y17XASA
         B     CHECKTIC                 YES                    @Y17XASA
LINERECV EQU   *                                               @Y17XASA
         TM    DCBPCI,DCBPCIA1+DCBPCIR1 PCI=(A, ) OR (R, )     @Y17XASA
         BZ    NOPCI                    NO                     @Y17XASA
CHECKTIC EQU   *                                               @Y17XASA
         CR    RWRK1,RLCB               TIC BELOW LCB          @Y17XASA
         BL    GETLSPCI                 YES , GET LCBLSPCI     @Y17XASA
         SR    RWRKB,RWRKB              CLEAR REG 11           @Y17XASA
         IC    RWRKB,DCBEIOBX           SIZE OF LCB            @Y17XASA
         LA    RWRKB,ZERO(RWRKB,RLCB)   END OF LCB             @Y17XASA
         CR    RWRK1,RWRKB              TIC BEYOND LCB         @Y17XASA
         BL    GOODTIC                  NO                     @Y17XASA
GETLSPCI EQU   *                                               @Y17XASA
         ICM   RCCW,SEVEN,LCBLSPCI      GET LCBLSPCI AS NEXT   @Y17XASA
         NI    LTPCI,AVTEFF-LTFIRST     RESET FIRST TIME SW    @Y17XASA
         B     WRAPIT                   BRANCH TO USE LCBLSPCI @Y17XASA
NOPCI    EQU   *
         NI    LTPCI,AVTEFF-LTFIRST     RESET FIRST TIME SW    @Y17XASA
GOODTIC  EQU   *                                               @Y17XASA
         TM    LTPCI,LTAUTOPL           AUTOPOLL CHANNEL PROG    S21903
         BO    NEXTTIC                  BR YES TRACE EVERYTHING  S21903
         LR    RCCW,RWRK1               TIC TO CCW               S21903
         B     WRAPIT                   BR TO CHECK END OF TABLE S21903
NEXTTIC  EQU   *                                                 S21903
         LA    RCCW,CCWLEN(,RCCW)       GET NEXT CCW             S21903
WRAPIT   EQU   *                                                 S21903
         BAL   RBALR,WRAPTEST           CHECK FOR END OF TABLE   S21903
         B     TICEND                   CHECK NEXT CCW         @YA07104
         EJECT
*****************************************************************S21903
*                                                                S21903
* THIS SECTION OF CODE PERFORMS TERMINATION PROCESSING.          S21903
*                                                                S21903
*****************************************************************S21903
         SPACE
ENDTABLE EQU   *                                               @G36XRSV
         CLI   ZERO(RNTRY),AVTEFF       IS LAST ENTRY HALF     @G36XRSV
*                                       OF X'FF' ENTRY?        @G36XRSV
         BNE   PREPEXIT                 NO, PREPARE TO EXIT    @G36XRSV
         MVI   ZERO(RNTRY),LINEID       NOT ENOUGH ROOM IN TBL @G36XRSV
*                                       FOR X'FF' ENTRY, MAKE  @G36XRSV
*                                       IT X'F0'               @G36XRSV
         B     PREPEXIT                 PREPARE TO EXIT        @G36XRSV
         SPACE 1                                               @G36XRSV
SETRET   EQU   *                                                 S21903
         NI    LTPCI,AVTEFF-LTERPPCI    RESET SKIP INDICATOR   @Y17XASA
         B     RETURN                   AND RETURN TO CALLER     S21903
WRAPEND  EQU   *                                                 S21903
         BAL   RBALR,WRAPTEST           CHECK FOR END OF TABLE   S21903
PREPEXIT EQU   *                                                 S21903
         TM    LCBCSWCS,LCBCSPCI        PCI INTERRUPT            S21903
         BNO   SKIPPCI                  BR NO SKIP INDICATOR     S21903
         OI    LTPCI,LTPCIN             SET PCI INDICATOR        S21903
         STCM  RCCW,SEVEN,LTPCIADD      SAVE LAST PCI ADDRESS  @Y17XASA
SKIPPCI  EQU   *                                                 S21903
         TM    LCBCSWUS,LCBUSCE+LCBUSDE CHANNEL/DEVICE END       S21903
         BZ    MOVETRAC                 BR NO LEAVE PCI SET    @G36XRSV
         NI    LTPCI,AVTEFF-(LTPCIN+LTAUTOPL+LTFIRST)            S21903
*                                       RESET PCI,AUTOPOLL,    @Y17XASA
*                                       AND FIRST TIME SWITCH  @Y17XASA
MOVETRAC DS    0H                       MOVE TRACE TO TABLE    @G36XRSV
         L     RWRKB,AVTRACE            ADDRESS OF TRACE CTL   @G36XRSV
         CLM   RBASE,HIBYTE,AVTHFF      ENTRY FROM PCI         @G36XRSV
         BE    NOTPASMD                 YES, BRANCH            @G36XRSV
*                                                              @G36XRSV
*        MOVE DATA FROM LINEEND ERP TABLE TO TRACE TABLE       @G36XRSV
*                                                              @G36XRSV
         OC    ZERO(TRACSIZE,RNTRY),ZERO(RNTRY) LAST ENTRY USED@G36XRSV
         BZ    NOTUSED                  NO, BRANCH             @G36XRSV
         LA    RNTRY,TRACSIZE(,RNTRY)   POINT TO TBL END       @G36XRSV
NOTUSED  EQU   *                                               @G36XRSV
         LA    RWRK0,TABLE2             GET TABLE ADDRESS      @G36XRSV
         SR    RNTRY,RWRK0              GET SIZE OF DATA       @G36XRSV
         STM   RNTRY,RWRK0,SAVESIZE     SAVE TABLE PTR & SIZE  @OZ30604
         SPACE 1                                               @G36XRSV
TRYAGAIN EQU   *                                               @G36XRSV
         L     RWRK6,CVTPTR             GET CVT ADDRESS        @OZ28809
         L     RWRK6,AVTCVTPT(,RWRK6)   GET TCX                @OZ28809
         USING IEDQTCX,RWRK6                                   @OZ28809
RECHPCI  EQU   *                                               @OZ28809
         TM    TCXDSAVE,PCIOP           PCI RUNNING            @OZ28809
         BO    RECHPCI                  BRANCH YES             @OZ28809
         DROP  RWRK6                                           @OZ28809
         LM    RWRK6,RWRK9,TRACURR-TRACNTRL(RWRKB) GET TRACE   @OZ30604
*                                       TABLE CONTROL INFO     @OZ30604
         LA    RWRK9,0(,RWRK9)          CLEAR HIGH ORDER BYTE  @OZ30604
         ST    RWRK6,SAVECURR           SAVE CURRENT ENTRY     @OZ30604
         ST    RWRK6,NEWCURR            SAVE CURRENT ENTRY     @OZ30604
         LM    RNTRY,RWRK0,SAVESIZE     GET TABLE PTR & SIZE   @OZ30604
         LA    RWRK8,TRACSIZE(RWRK8)    TRUE END OF TABLE      @OZ30604
         NI    TRACFLAG-TRACNTRL(RWRKB),X'FF'-TRCNFIT-TRACMIDN-TRACENDN
*                                       RESET CONTROL FLAGS    @OZ30604
         SR    RWRK1,RWRK1              CLEAR REGISTER TO ZERO @OZ30604
         CR    RWRK6,RWRK9              IS THIS MID-POINT      @OZ30604
         BL    BEFRMID                  NO, BEFORE MID-POINT   @OZ30604
         BH    AFTRMID                  NO, AFTER MID-POINT    @OZ30604
         LA    RWRK6,TRACSIZE(RWRK6)    BUMP PAST HEADER       @OZ30604
AFTRMID  EQU   *                                               @OZ30604
         TM    TRACFLAG-TRACNTRL(RWRKB),TRACENDN PAST END OF   @OZ30604
*                                       TABLE                  @OZ30604
         BNO   AFTRMIDA                 NO, BRANCH             @OZ30604
         LA    RWRK1,1(RWRK1)           COUNT OF HALF TABLES   @OZ30604
*                                       THAT WILL NOT BE       @OZ30604
*                                       WRITTEN BY COMWRITE    @OZ30604
         NI    TRACFLAG-TRACNTRL(RWRKB),X'FF'-TRACENDN RESET   @OZ30604
*                                       END OF TABLE FLAG      @OZ30604
AFTRMIDA EQU   *                                               @OZ30604
         AR    RWRK6,RNTRY              ADD LENGTH TO CURRENT  @OZ30604
         CR    RWRK6,RWRK8              WILL DATA FIT IN       @OZ30604
*                                       LAST HALF OF TABLE     @OZ30604
         LR    RWRK2,RWRK6              SET NEW CURRENT        @OZ30604
         BE    WILLFT2                  YES, WILL FIT EXACTLY  @OZ30604
         BL    WILLFT2A                 YES, FIT WITH REMNDR   @OZ30604
         SR    RWRK6,RWRK8              AMOUNT REMAINING       @OZ30604
         LR    RWRK8,RNTRY              GET TOTAL AMOUNT       @OZ30604
         SR    RWRK8,RWRK6              AMOUNT FOR LAST HALF   @OZ30604
         TM    TRACFLAG-TRACNTRL(RWRKB),TRCNFIT IS OVERFLOW SET@OZ30604
         BNO   NOVRFLWB                 NO, BRANCH             @OZ30604
         AR    RWRK0,RWRK9              UPDATE TABLE POINTER   @OZ30604
         LR    RWRK9,RWRK2              GET CURRENT ENTRY      @OZ30604
         SR    RWRK9,RNTRY              ADJUST CURRENT ENTRY   @OZ30604
         ST    RWRK9,NEWCURR            SAVE MOVE TO ADDRESS   @OZ30604
NOVRFLWB EQU   *                                               @OZ30604
         LR    RNTRY,RWRK6              SAVE AMOUNT REMAINING  @OZ30604
         ICM   RWRK6,7,TRACSTRT-TRACNTRL+ONE(RWRKB) POINTER TO @OZ30604
*                                       START OF TABLE         @OZ30604
         LA    RWRK6,TRACSIZE(RWRK6)    SKIP HEADER ENTRY      @OZ30604
         ICM   RWRK9,7,TRACMID-TRACNTRL+ONE(RWRKB) POINTER TO  @OZ30604
*                                       MIDDLE ENTRY           @OZ30604
         OI    TRACFLAG-TRACNTRL(RWRKB),TRCNFIT+TRACENDN SET   @OZ30604
*                                       OVERFLOW AND END OF    @OZ30604
*                                       TABLE                  @OZ30604
BEFRMID  EQU   *                                               @OZ30604
         TM    TRACFLAG-TRACNTRL(RWRKB),TRACMIDN PAST MIDDLE   @OZ30604
*                                       OF TABLE               @OZ30604
         BNO   BEFRMIDA                 NO, BRANCH             @OZ30604
         LA    RWRK1,1(RWRK1)           COUNT OF HALF TABLES   @OZ30604
*                                       THAT WILL NOT BE       @OZ30604
*                                       WRITTEN BY COMWRITE    @OZ30604
         NI    TRACFLAG-TRACNTRL(RWRKB),X'FF'-TRACMIDN RESET   @OZ30604
*                                       MIDDLE OF TABLE FLAG   @OZ30604
BEFRMIDA EQU   *                                               @OZ30604
         AR    RWRK6,RNTRY              ADD LENGTH TO CURRENT  @OZ30604
         CR    RWRK6,RWRK9              WILL DATA FIT IN       @OZ30604
*                                       FIRST HALF OF TABLE    @OZ30604
         LR    RWRK2,RWRK6              SET NEW CURRENT        @OZ30604
         BE    WILLFT1                  YES, WILL FIT EXACTLY  @OZ30604
         BL    WILLFT1A                 YES, FIT WITH REMNDR   @OZ30604
         SR    RWRK6,RWRK9              AMOUNT REMAINING       @OZ30604
         LR    RWRK9,RNTRY              GET TOTAL AMOUNT       @OZ30604
         SR    RWRK9,RWRK6              AMOUNT FOR FIRST HALF  @OZ30604
         TM    TRACFLAG-TRACNTRL(RWRKB),TRCNFIT IS OVERFLOW SET@OZ30604
         BNO   NOVRFLWA                 NO, BRANCH             @OZ30604
         AR    RWRK0,RWRK8              UPDATE TABLE POINTER   @OZ30604
         LR    RWRK8,RWRK2              GET CURRENT ENTRY      @OZ30604
         SR    RWRK8,RNTRY              ADJUST CURRENT ENTRY   @OZ30604
         ST    RWRK8,NEWCURR            SAVE MOVE TO ADDRESS   @OZ30604
NOVRFLWA EQU   *                                               @OZ30604
         LR    RNTRY,RWRK6              SAVE AMOUNT REMAINING  @OZ30604
         ICM   RWRK6,7,TRACMID-TRACNTRL+ONE(RWRKB) POINTER TO  @OZ30604
*                                       MIDDLE ENTRY           @OZ30604
         LA    RWRK6,TRACSIZE(RWRK6)    SKIP HEADER ENTRY      @OZ30604
         ICM   RWRK8,7,TRACLAST-TRACNTRL+ONE(RWRKB) POINTER TO @OZ30604
*                                       LAST ENTRY             @OZ30604
         LA    RWRK8,TRACSIZE(RWRK8)    TRUE END OF TABLE      @OZ30604
         OI    TRACFLAG-TRACNTRL(RWRKB),TRCNFIT+TRACMIDN SET   @OZ30604
*                                       OVERFLOW AND MIDDLE OF @OZ30604
*                                       TABLE FLAGS            @OZ30604
         B     AFTRMID                                         @OZ30604
WILLFT1  EQU   *                                               @OZ30604
         LA    RWRK2,TRACSIZE(RWRK2)    SKIP HEADER ENTRY      @OZ30604
         OI    TRACFLAG-TRACNTRL(RWRKB),TRACMIDN SET MIDDLE OF @OZ30604
*                                       TABLE FLAG             @OZ30604
         TM    TRACFLAG-TRACNTRL(RWRKB),TRACENDN PAST END OF   @OZ30604
*                                       TABLE                  @OZ30604
         BNO   WILLFT1A                 NO, BRANCH             @OZ30604
         LA    RWRK1,1(RWRK1)           COUNT OF HALF TABLES   @OZ30604
*                                       THAT WILL NOT BE       @OZ30604
*                                       WRITTEN BY COMWRITE    @OZ30604
         NI    TRACFLAG-TRACNTRL(RWRKB),X'FF'-TRACENDN RESET   @OZ30604
*                                       END OF TABLE FLAG      @OZ30604
WILLFT1A EQU   *                                               @OZ30604
         L     RWRKA,SAVECURR           GET SAVED CURRENT      @OZ30604
*                                       ENTRY POINTER          @OZ30604
         CS    RWRKA,RWRK2,TRACURR-TRACNTRL(RWRKB) ATTEMPT TO  @OZ30604
*                                       UPDATE CURRENT ENTRY   @OZ30604
         BNE   TRYAGAIN                 BRANCH IF UNSUCCESSFUL @OZ30604
         LR    RWRK9,RWRK8              GET DATA COUNT         @OZ30604
         B     TRCMOVE                  GO MOVE DATA           @OZ30604
WILLFT2  EQU   *                                               @OZ30604
         ICM   RWRK2,7,TRACSTRT-TRACNTRL+ONE(RWRKB) POINTER TO @OZ30604
*                                       START OF TABLE         @OZ30604
         LA    RWRK2,TRACSIZE(RWRK2)    SKIP HEADER ENTRY      @OZ30604
         OI    TRACFLAG-TRACNTRL(RWRKB),TRACENDN SET END OF    @OZ30604
*                                       TABLE FLAG             @OZ30604
         TM    TRACFLAG-TRACNTRL(RWRKB),TRACMIDN PAST MIDDLE   @OZ30604
*                                       OF TABLE               @OZ30604
         BNO   WILLFT2A                 NO, BRANCH             @OZ30604
         LA    RWRK1,1(RWRK1)           COUNT OF HALF TABLES   @OZ30604
*                                       THAT WILL NOT BE       @OZ30604
*                                       WRITTEN BY COMWRITE    @OZ30604
         NI    TRACFLAG-TRACNTRL(RWRKB),X'FF'-TRACMIDN RESET   @OZ30604
*                                       MIDDLE OF TABLE FLAG   @OZ30604
WILLFT2A EQU   *                                               @OZ30604
         L     RWRKA,SAVECURR           GET SAVE CURRENT ENTRY @OZ30604
*                                       POINTER                @OZ30604
         CS    RWRKA,RWRK2,TRACURR-TRACNTRL(RWRKB) ATTEMPT TO  @OZ30604
*                                       UPDATE CURRENT ENTRY   @OZ30604
         BNE   TRYAGAIN                 BRANCH IF UNSUCCESSFUL @OZ30604
TRCMOVE  EQU   *                                               @OZ30604
         LR    RWRKA,RWRK0              MOVE FROM DATA ADDRESS @OZ30604
         TM    TRACFLAG-TRACNTRL(RWRKB),TRCNFIT IS OVERFLOW SET@OZ30604
         BNO   NOVRFLWX                 NO, BRANCH             @OZ30604
         AR    RWRKA,RWRK9              UPDATE MOVE FROM ADDR  @OZ30604
NOVRFLWX EQU   *                                               @OZ30604
         SR    RWRK6,RNTRY              MOVE TO TABLE ADDRESS  @OZ30604
         LR    RWRK7,RNTRY              SAVE DATA COUNT        @OZ30604
MOV256C  EQU   *                                               @OZ30604
         CH    RNTRY,H256               MORE THAN 256 BYTES    @OZ30604
         BNH   LNGOK3                   NO, BRANCH             @OZ30604
         LA    RNTRY,TWO56              SET TO MOVE 256 BYTES  @OZ30604
LNGOK3   EQU   *                                               @OZ30604
         BCTR  RNTRY,0                  REDUCE FOR EXECUTED MVC@OZ30604
         EX    RNTRY,MVC                MOVE DATA TO TRACE TBL @OZ30604
         AH    RWRKA,H256               UPDATE MOVE FROM ADDR  @OZ30604
         AH    RWRK6,H256               UPDATE MOVE TO ADDRESS @OZ30604
         SH    RWRK7,H256               DECREMENT DATA COUNT   @OZ30604
         BNP   CHKNFIT2                 BRANCH IF MOVE COMPLETE@OZ30604
         LR    RNTRY,RWRK7              SAVE DATA COUNT        @OZ30604
         B     MOV256C                  GO MOVE MORE DATA      @OZ30604
CHKNFIT2 EQU   *                                               @OZ30604
         TM    TRACFLAG-TRACNTRL(RWRKB),TRCNFIT IS OVERFLOW SET@OZ30604
         BNO   NOTPASMD                 NO, BRANCH             @OZ30604
         L     RWRK6,NEWCURR            MOVE TO TABLE ADDRESS  @OZ30604
         LR    RWRKA,RWRK0              MOVE FROM DATA ADDRESS @OZ30604
         LR    RNTRY,RWRK9              SAVE DATA COUNT        @OZ30604
MOV256D  EQU   *                                               @OZ30604
         CH    RNTRY,H256               MORE THAN 256 BYTES    @OZ30604
         BNH   LNGOK4                   NO, BRANCH             @OZ30604
         LA    RNTRY,TWO56              SET TO MOVE 256 BYTES  @OZ30604
LNGOK4   EQU   *                                               @OZ30604
         BCTR  RNTRY,0                  REDUCE FOR EXECUTED MVC@OZ30604
         EX    RNTRY,MVC                MOVE DATA TO TRACE TBL @OZ30604
         AH    RWRKA,H256               UPDATE MOVE FROM ADDR  @OZ30604
         AH    RWRK6,H256               UPDATE MOVE TO ADDRESS @OZ30604
         SH    RWRK9,H256               DECREMENT DATA COUNT   @OZ30604
         BNP   NOTPASMD                 BRANCH IF MOVE COMPLETE@OZ30604
         LR    RNTRY,RWRK9              SAVE DATA COUNT        @OZ30604
         B     MOV256D                  GO MOVE MORE DATA      @OZ30604
NOTPASMD EQU   *                                               @G36XRSV
         TM    TRACFLAG-TRACNTRL(RWRKB),TRACMIDN+TRACENDN      @G36XRSV
*                                       END OR MIDDLE OF TABLE @G36XRSV
         BZ    NODUMP                   NO, BRANCH             @G36XRSV
*
* REGISTER 1 CONTAINS COUNT OF HALF TABLES THAT HAVE BEEN      @OZ30604
*     LOST IN THIS ROUTINE DUE TO SMALL SIZE OF TRACE TABLE.   @OZ30604
*     THIS COUNT IS PASSED AS A PARAMETER TO IEDQFE20.         @OZ30604
*
         L     RWRK9,AVTAFE20           GET ADDRESS IEDQFE20   @G36XRSV
         CLM   RWRK9,HIBYTE,AVTHFF      IS DUMP REQUESTED      @G36XRSV
         BNE   NODUMP                   NO, BRANCH             @G36XRSV
         TS    DUMPTS                   DUMP IN USE            @G36XRSV
         BNZ   NODUMP                   YES, BRANCH            @G36XRSV
         BALR  RBALR,RWRK9              GO DUMP TABLE          @G36XRSV
         MVI   DUMPTS,ZERO              CLEAR DUMP IN USE SW   @G36XRSV
         TM    LCBCSWUS,LCBUSCE+LCBUSDE CE/DE?                 @G36XRSV
         BZ    RETURN                   N-RETURN TO CALLER     @G36XRSV
NODUMP   EQU   *                                               @G36XRSV
         TM    LTPCI,LTERROR            ERROR TRACE THIS LINE  @Y17XASA
         BZ    RETURN                   NO , EXIT              @Y17XASA
         TM    LCBCSWUS,LCBUSUC         UNIT CHECK IN CSW      @Y17XASA
         BO    RETURN                   YES , RETURN           @Y17XASA
         TM    AVTIOTR,AVTIOTRH         CONTINUE AFTER ERRORS  @Y17XASA
         BO    RETURN                   YES , RETURN           @Y17XASA
         NI    LCBSTAT2,AVTEFF-LCBTRACE RESET TRACE THIS LINE  @Y17XASA
         MVI   LTPCI,LTFREE             FREE OLD ENTRY         @Y17XASA
RETURN   EQU   *                                                 S21903
         CLM   RBASE,HIBYTE,FOXFOX      FROM PCI?              @G36XRSV
         BNE   RTN1                     N-BR                   @G36XRSV
         LM    RWRK0,RNTRY,Q0SAVE1      RESTORE CALLER'S REGS  @G36XRSV
         BR    RBALR                    RETURN TO CALLER       @G36XRSV
RTN1     DS    0H                       NON-PCI INTERRUPTS     @G36XRSV
         LM    RWRK0,RNTRY,Q0SAVE2      RESTORE CALLER'S REGS  @G36XRSV
         BR    RBALR                    RETURN TO CALLER         X02004
         SPACE 1                                               @G36XRSV
*                                                              @G36XRSV
*                                       EXECUTED INSTRUCTION   @G36XRSV
*                                                              @G36XRSV
MVC      MVC   0(0,RWRK6),0(RWRKA)      EXECUTED MOVE          @OZ30604
         EJECT
* E X E C U T E D   S U B R O U T I N E S                        S21903
         SPACE 1                                               @G36XRSV
***************************************************************@G36XRSV
*                                                              @G36XRSV
* THIS ROUTINE BUMPS TO THE NEXT ENTRY IN THE TRACE TABLE OR   @G36XRSV
* TEMPORARY TABLE. IF THE TEMPORARY TABLE IS BEING USED, A     @G36XRSV
* CHECK IS MADE TO SEE IF IT IS FULL. IF SO, A BRANCH IS MADE  @G36XRSV
* TO THE ENDTABLE ROUTINE. IF THIS IS A PCI INTERRUPT AND THE  @G36XRSV
* REAL TRACE TABLE IS BEING USED, A CHECK IS MADE FOR THE      @G36XRSV
* MIDPOINT OR END OF TABLE BEING REACHED AND ,IF SO, A BIT IS  @G36XRSV
* SET.                                                         @G36XRSV
*                                                              @G36XRSV
***************************************************************@G36XRSV
         SPACE 1                                               @G36XRSV
WRAPTEST EQU   *                                               @G36XRSV
         CLM   RBASE,HIBYTE,AVTHFF      ENTRY FROM PCI         @G36XRSV
         BE    WRPTEST1                 YES, BRANCH            @G36XRSV
         LA    RWRK9,TBL2LAST           ADDRESS OF LAST ENTRY  @G36XRSV
         CR    RWRK9,RNTRY              LAST ENTRY IN TABLE    @G36XRSV
         BE    ENDTABLE                 END OF TABLE           @G36XRSV
         LA    RNTRY,TRACSIZE(,RNTRY)   GET NEXT ENTRY         @G36XRSV
         BR    RBALR                    RETURN                 @G36XRSV
         SPACE 1                                               @G36XRSV
WRPTEST1 EQU   *                                               @G36XRSV
         L     RWRK9,AVTRACE            TRACE CONTROL WORDS    @G36XRSV
         USING TRACNTRL,RWRK9                                  @G36XRSV
         LA    RNTRY,TRACSIZE(RNTRY)    GET NEXT TRACE ENTRY   @G36XRSV
         ST    RNTRY,TRACURR            UPDATE TRACE CTL WORDS @G36XRSV
         CLC   TRACURR+ONE(THREE),TRACMID+ONE MIDPOINT REACHED @G36XRSV
         BNE   TESTLAST                 BR NO TO TEST FOR LAST @G36XRSV
         OI    TRACFLAG,TRACMIDN        INDICATE MIDPOINT      @G36XRSV
         LA    RNTRY,TRACSIZE(RNTRY)    ALLOW FOR PREFIX       @G36XRSV
         ST    RNTRY,TRACURR            UPDATE TRACE CTL WORKS @G36XRSV
         B     WRAPEXIT                 RETURN                 @G36XRSV
TESTLAST EQU   *                                               @G36XRSV
         CLC   TRACURR+ONE(THREE),TRACLAST+ONE LAST ENTRY      @G36XRSV
         BNH   WRAPEXIT                 BR NO TO RETURN        @G36XRSV
SETLAST  EQU   *                                               @G36XRSV
         OI    TRACFLAG,TRACENDN        INDICATE END OF TABLE  @G36XRSV
WRAPTRAC EQU   *                                               @G36XRSV
         L     RNTRY,TRACSTRT           WRAP TRACE TABLE       @G36XRSV
         LA    RNTRY,TRACSIZE(RNTRY)    ALLOW FOR PREFIX       @G36XRSV
         ST    RNTRY,TRACURR                                   @G36XRSV
WRAPEXIT EQU   *                                               @G36XRSV
         XC    ZERO(TRACSIZE,RNTRY),ZERO(RNTRY) CLEAR ENTRY    @G36XRSV
         BR    RBALR                    RETURN                 @G36XRSV
         EJECT
*****************************************************************S21903
*                                                                S21903
* THIS ROUTINE TRACES THE TP OP-CODE FOR ALL NON-TEXT TRANSFER   S21903
* CCWS WHICH ARE LOCATED IN THE CHANNEL PROGRAM AREA OF THE      S21903
* LCB (IOB). CONTROL IS THEN RETURNED TO INLINE CODE VIA BRANCH  S21903
* ON REGISTER 14.                                                S21903
*                                                                S21903
*****************************************************************S21903
         SPACE
TRACOPCD EQU   *                                                 S21903
         USING TRACELN1,RWRKF                                  @Y17XASA
         LA    RWRK1,LCBCPA             POINT TO CHAN PROG AREA  S21903
         SR    RWRK1,RCCW               CCW IN LCB               S21903
         BPR   RBALR                    RETURN IF TEXT CCW     @Y17XASA
         LR    RWRK0,RWRK1              SAVE INDEX               X02004
         SR    RWRK1,RWRK1                                       X02004
         IC    RWRK1,DCBEIOBX           LCB SIZE                 X02004
         LA    RWRK1,ZERO(RWRK1,RLCB)   END OF LCB               X02004
         SR    RWRK1,RCCW               CCW IN LCB               X02004
         BNPR  RBALR                    RETURN IF TEXT CCW     @Y17XASA
         LPR   RWRK1,RWRK0              CCW OFFSET INTO CPA      S21903
         SRL   RWRK1,THREE              COMPUTE INDEX            S21903
         IC    RWRK1,LCBTPCD(RWRK1)     GET TP OP-CODE           S21903
         STC   RWRK1,TRACOPT            TRACE TP OP-CODE         S21903
         BR    RBALR                                             S21903
         EJECT
*****************************************************************X02004
*                                                                X02004
* THE PURPOSE OF THIS ROUTINE IS TO CONVERT REAL ADDRESSES TO    X02004
* VIRTUAL ADDRESSES. THE VIRTUAL ADDRESS IS RETURNED IN RWRK6    X02004
* (REGISTER 6).                                                  X02004
*                                                                X02004
*****************************************************************X02004
         SPACE
CONVERT  EQU   *                                                 X02004
         TM    LCBFLAG3,LCBRLAD         REAL DATA ADDRESS        X02004
         L     RWRK6,CCWADDR-ONE        GET DATA ADDRESS         X02004
         BCR   14,RBALR                 RETURN IF VIRTUAL        X02004
CONVERT1 EQU   *                                                 X02004
         L     RWRK1,CCWADDR-ONE        GET REAL DATA ADDRESS    X02004
CONVERT2 LA    RWRK1,ZERO(,RWRK1)       CLEAR HIGH ORDER BYTE  @OX12574
         LR    RWRK6,RWRKF              SAVE REGISTER 15         X02004
         L     RNTRY,CVTPTRV            CONVERT ROUTINE ADDRESS  X02004
         LR    RWRKB,RBALR              SAVE REG 14            @Y17XASA
         BALR  RBALR,RNTRY              CONVERT TO VIRTUAL       X02004
         LR    RBALR,RWRKB              RESTORE REG 14         @Y17XASA
         LR    RWRKF,RWRK6              RESTORE REGISTER 15      X02004
         LR    RWRK6,RWRK1              SAVE VIRTUAL ADDRESS     X02004
         BR    RBALR                    RETURN TO INLINE CODE    X02004
         EJECT
* D E F I N E D   C O N S T A N T S                              S21903
Q0SAVE1  DC    16F'0'                   SAVEAREA FOR PCI       @G36XRSV
Q0SAVE2  DC    16F'0'                   SAVEAREA FOR NON PCI   @G36XRSV
SAVECURR DC    A(0)                     CURRENT ENTRY SAVE AREA@G36XRSV
SAVESIZE DC    2A(0)                    TABLE POINTER & SIZE   @OZ30604
NEWCURR  DC    A(0)                     MOVE TO TABLE ADDRESS  @OZ30604
CLEARHI  DC    X'00000FFF'              MASK FOR CHAN/UNIT ADDR  S21903
TRACECNT DC    F'00'                    SEQUENCE NUMBER        @G36XRSV
INUSEMSK DS    0F                       USED FOR SETTING IN USE@G36XRSV
         DC    AL1(LTINUSE)                                    @G36XRSV
         DC    AL3(0)                                          @G36XRSV
CCWBKUP  DC    AL2(CCWLEN)              BACK UP ONE CCW          S21903
DATABKUP DC    H'10'                    LAST DATA BYTE COUNT     S21903
LCBBKUP  DC    AL2(LCBPRFSZ)            BACK UP FROM IOB TO LCB  S21903
TRMPREFX DC    AL2(TRMPRFSZ)            TERMINAL PREFIX        @Y17XASA
H256     DC    H'256'                   TWO HUNDRED FIFTY-SIX  @OZ30604
         DROP  RWRKF                                           @SA70180
         USING TRACINFO,RWRKF                                  @SA70180
MOVENAME MVC   TRACNAME(1),0(RWRK7)     MOVES TERMINAL NAME.   @Y17XASA
         DROP  RWRKF                                           @SA70180
         USING TRACELN1,RWRKF                                  @SA70180
MVCDATA1 MVC   TRACDATA(0),ZERO(RWRK6)  VARIABLE MOVE OF DATA    S21903
         DROP  RWRKF                                             S21903
         USING TRACELN2,RWRKF                                    S21903
MVCDATA2 MVC   TRACEDAT(0),FOUR(RWRK6)  VARIABLE MOVE OF DATA    S21903
         SPACE 1                                               @G36XRSV
TABLE2   DC    256F'0'                  LINEEND,ERP WORK TABLE @OZ30604
TRCSIZE  EQU   16                       SIZE OF ONE ENTRY      @G36XRSV
TBL2LAST EQU   *-TRCSIZE                ADDR LAST ENTRY        @G36XRSV
LINETABL DC    100F'0'                  LINE TRACE CTL TBL.    @OY15041
FOXFOX   DC    X'FF'                    TABLE END INDICATOR      S21903
DUMPTS   DC    X'00'                    DUMP IN USE SWITCH     @G36XRSV
         EJECT
* D S E C T S   U S E D   B Y   I G G 0 1 9 Q 0                  S21903
         SPACE
* THE FOLLOWING DSECT IS FOR THE LINE CONTROL TABLE, A TABLE     S21903
* CONTAINED WITHIN THIS MODULE WITH ENTRIES FOR EACH OF THE      S21903
* LINES BEING TRACED. THE NUMBER OF ENTRIES IN THIS TABLE        S21903
* CAN BE INCREASED BY EXPANDING THIS MODULE.                     S21903
         SPACE
LINETAB  DSECT                                                   S21903
LTCUU    DS    H                        CHANNEL/UNIT ADDRESS     S21903
LTUCBAD  DS    H                        UCB ADDRESS              S21903
LTPCI    DS    CL1                      FLAG BYTE              @Y17XASA
LTPCIN   EQU   X'80'                    PCI INTERRUPT INDICATOR  S21903
LTAUTOPL EQU   X'40'                    AUTOPOLL INT  INDICATOR  S21903
LTERPPCI EQU   X'20'                                           @Y17XASA
LTITSERP EQU   LTERPPCI                 PREVIOUS ENTRY WAS     @Y17XASA
*                                         ERP AND TRACE WILL BE  S21903
*                                         SKIPPED IN LINEEND     S21903
LTITSPCI EQU   LTERPPCI                 PREVIOUS ENTRY WAS     @Y17XASA
*                                         PCI AND TRACE WILL BE  S21903
*                                         SKIPPED IN LINEEND     S21903
LTFIRST  EQU   X'10'                    FIRST TIME SWITCH      @Y17XASA
LTP370X  EQU   X'08'                    370X ENTRY             @Y17XASA
LTLATTN  EQU   X'04'                    ATTENTION ENTRY        @Y17XASA
LTERROR  EQU   X'02'                    ERROR TRACE ENTRY      @Y17XASA
LTINUSE  EQU   X'01'                    TRACE ENTRY IN USE     @ZM47908
LTFREE   EQU   X'00'                    TRACE ENTRY FREE       @Y17XASA
LTPCIADD DS    0CL3                     LAST PCI ADDRESS       @Y17XASA
         DS    CL1                      FILLER                 @Y17XASA
LTPIURES DS    CL2                      PIU RESIDUAL COUNT     @Y17XASA
LTSIZE   EQU   *-LTCUU                  LINE TABLE ENTRY SIZE    S21903
         EJECT
* THE FOLLOWING DSECT ALLOWS ADDRESSIBILITY ON THE TRACE TABLE   S21903
* CONTROL WORDS.                                                 S21903
         SPACE
TRACNTRL DSECT                                                   S21903
TRACURR  DS    F                        CURRENT ENTRY            S21903
TRACSTRT DS    F                        FIRST   ENTRY            S21903
TRACLAST DS    F                        LAST    ENTRY            S21903
TRACMID  DS    F                        MIDDLE  ENTRY            S21903
TRACFLAG EQU   TRACMID                  FLAG BYTE                S21903
TRACMIDN EQU   X'20'                    MIDDLE OF TABLE IND      S21903
TRACENDN EQU   X'10'                    END OF TABLE IND         S21903
TRCNFIT  EQU   X'08'                    TRACE TABLE OVERFLOW   @G36XRSV
         SPACE
* THE FOLLOWING DSECT IS FOR CREATION OF THE UNIQUE HEADER ENTRY S21903
* IN THE TRACE TABLE FOR EACH INTERRUPT FIELDED.                 S21903
         SPACE
TRACEHDR DSECT                                                   S21903
TRACCUU  DS    H                        CHANNEL/UNIT ADDRESS     S21903
TRACSEQ  DS    H                        SEQUENCE NUMBER          S21903
TRACFLG1 DS    X                        IOB FLAGS                S21903
TRACPSRT DS    XL3                      CHAN PROG START ADDRESS  S21903
TRACSENS DS    XL1                      SENSE BYTE ZERO        @Y17XASA
TRACSW   DS    XL7                      CSW                    @Y17XASA
         EJECT
* THE FOLLOWING DSECT IS FOR THE CREATION OF THE UNIQUE        @SA70180
* INFORMATION ENTRY FOR EACH INTERRUPT                         @SA70180
         SPACE 1                                               @SA70180
TRACINFO DSECT                                                 @SA70180
TRACIND  DS    C                        ENTRY INDICATOR IS FA  @SA70180
         DS    C                        UNUSED                 @SA70180
TRACTCIN DS    XL2                      LCBTTCIN VALUE         @SA70180
TRACNAME DS    CL8                      TERMNAME               @SA70180
         DS    X                        RESERVED               @Y17XASK
TRACINV  DS    XL3                      LCBINVPT               @Y17XASK
         SPACE 1                                               @SA70180
* THE FOLLOWING DSECT IS FOR THE ENTRIES CREATED IN THE TRACE    S21903
* TABLE FOR EACH CCW IN THE CHANNEL PROGRAM. ONE OR MORE OF      S21903
* ENTRIES WILL OCCUR FOR EACH INTERRUPT.                         S21903
         SPACE
TRACELN1 DSECT                                                   S21903
TRACCWAD DS    F                        CCW ADDRESS              S21903
TRACCW   DS    XL8                      CCW                      S21903
TRACDATA DS    CL4                      TRANSFERRED DATA         S21903
         ORG   TRACDATA                                          S21903
TRACDAT1 DS    CL4                      DATA                     S21903
TRACSIZE EQU   *-TRACCWAD               TRACE ENTRY SIZE         S21903
         ORG   TRACCW+5                                          S21903
TRACOPT  DS    C                        TP OP-CODE               S21903
         SPACE
* THE FOLLOWING DSECT IS USED IN TRACING DATA IN THOSE INSTANCES S21903
* WHEN MORE THAN FOUR BYTES OF DATA WERE TRANSFERRED. TWO TRACE  S21903
* ENTRIES ARE USED, THE FIRST ENTRY FORMATTED BE THE TRACELN1    S21903
* DSECT AND THE SECOND BY THE TRACELN2 DSECT.                    S21903
         SPACE
TRACELN2 DSECT                                                   S21903
TRACEDAT DS    XL4                      FIRST DATA BYTES         S21903
         DS    2X                       PADDING                  S21903
TRACEEND DS    XL10                     LAST DATA BYTES          S21903
         SPACE
TRACENT  DSECT                                                 @Y17XASA
ENTRY    DS    CL16                     TRACE TABLE ENTRY      @Y17XASA
         SPACE 3                                               @Y17XASA
IEDQRQE  DSECT                          RQE DSECT              @Y17XASA
         DS    H                        NEXT RQE ADDRESS       @Y17XASK
RQEUCB   DS    H                        RQE UCB ADDRESS        @Y17XASK
RQEIOB   DS    F                        RQE IOB ADDRESS        @Y17XASA
         EJECT
         TAVTD
         EJECT
         TCCWD
         EJECT
CVTDSECT DSECT
         CVT
         EJECT
         DCBD  DSORG=TX
         EJECT
         TLCBD
         EJECT                                                 @SA70178
         TPRFD                    BUFFER PREFIX DSECT          @SA70178
         EJECT                                                 @Y17XASA
         TTCXD                    TCX DSECT                    @Y17XASA
         EJECT                                                 @Y17XASA
         TTNTD                    TERMINAL NAME TABLE DSECT    @Y17XASA
         EJECT                                                 @Y17XASA
         TTRMD                    TERMINAL TABLE DSECT         @Y17XASA
         EJECT                                                 @Y17XASA
         TTHD                     TH DSECT                     @Y17XASA
         EJECT                                                 @Y17XASA
IEDQUCB  DSECT                                                 @Y17XASA
         IEFUCBOB                 UCB DSECT                    @Y17XASA
         END
