804G     TITLE 'IGE0804G   -   CHANNEL CHECK ERP MODULE'
IGE0804G CSECT
*A-000000-999999                                               @Y02X6E0
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*
*A048000-049000,055000,074000,105000,109000,148600-149200,159000,Y02027
*A167200-167600,807000                                           Y02027
*C032000,038000,046000-047000,054000,072000-073000,108000,134000,Y02027
*C604300,604420-604480,605380                                    Y02027
*C016800,172000                                                @Z30AAEE
*D760000                                                       @Z30AAEE
*A220000                                                       @ZA02096
*A244000,246000                                                @SA74028
*                                                                     *
***********************************************************************
*                                                                     *
*MODULE-NAME = IGE0804G                                               *
*                                                                     *
*DESCRIPTIVE-NAME = ERP FOR START/STOP CHANNEL CHECK HANDLER          *
*                                                                     *
*COPYRIGHTS = 'NONE'                                                  *
*                                                                     *
*STATUS: CHANGE LEVEL 8                                        @Z30AAEE
*                                                                     *
*FUNCTION -- TO ATTEMPT RECOVERY OF CHANNEL CONTROL CHECKS AND        *
*   INTERFACE CONTROL CHECKS.                                         *
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
*EXTERNAL ROUTINE -- IGG019QE (AOS/TCAM SIO APPENDAGE) -- TO CONVERT  *
*   VIRTUAL CCW ADDRESSES TO REAL BEFORE RETRY.                       *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'ERP IN CONTROL (IOSERR)' AND 'EXCEPTIONAL   Y02027
*                CONDITION(IOSEX)'. RETURN TO IOS TO RETRY THE   Y02027
*                   ERROR.                                       Y02027
*        SVC   3                                                      *
*                                                                     *
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*        SVC   3                                                      *
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO I/O SUPERVISOR BLOCK                Y02027
*        SVC   15   'EXCEPTIONAL CONDITION(IOSEX)'. RETURN TO    Y02027
*                   LINE END APPENDAGE. ERROR PERMANENT.         Y02027
*        SVC   3                                                      *
*                                                                     *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*   TO SCHEDULE ERROR POST                                            *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TCCWD                                                             *
*   TLCBD                                                             *
*   DCBD                                                              *
*   TSCBD                                                             *
*   IECDIOSB                                                     Y02027
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
***********************************************************************
         EJECT
*
*        REGISTERS
*
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RUCB     EQU   2                        UCB REGISTER
RLCB     EQU   3                        IOB REGISTER
R4       EQU   4                        WORK REGISTER
R5       EQU   5                        WORK REGISTER
R6       EQU   6                        WORK REGISTER
RCCW     EQU   7                        CCW REGISTER
R8       EQU   8                        WORK REGISTER
RTST     EQU   9                        ADDR OF REQUEST QUEUE    Y02027
*                                       ELEMENT
R9       EQU   9                        WORK REGISTER
R10      EQU   10                       WORK REGISTER
R11      EQU   11                       WORK REGISTER
R12      EQU   12                       WORK REGISTER
RLINK    EQU   13                       LINKAGE - NEXT LOAD
RSAVE    EQU   13                       SAVE REGISTER FOR IOSB   Y02027
RXCTL    EQU   14                       XCTL REGISTER
RBASE    EQU   15                       BASE - ADDRESSABILITY
         EJECT
         USING *,RBASE
IGE0804G  IEDHJN  ENTER
*
         USING IOSB,RIOSB                                        Y02027
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         L     RLCB,IOBOFFST(,RTST)     LCB BASE
         USING LCBFLAG1,RLCB
         L     RUCB,IOSUCB              ADDRESS OF UCB         @Z30AAEE
         L     RXCTL,CVTPTR             CVT POINTER
         L     RCCW,LCBCSW-1            FAILING CCW
         USING IEDQCCW,RCCW
         SH    RCCW,H8                  BACK UP TO CCW
         L     RLINK,LCBSCBA-1          SCB ADDRESS
         L     R4,IOSERP                ADDRESS OF ERP WORKAREA  Y02027
         USING EWA,R4
         MVC   LCBRESTR(FOUR),EWARGFG1  SAVE VALIDITY, TERM. ,   Y02027
*                                         RETRY FLAGS
         XC    EWAERPIB,EWAERPIB        CLEAR ERPIB FOR SYSTEM   Y02027
         TM    LCBRESTR,NORTY           NO RETRY FLAG SET
         BO    POSTEXIT                 BRANCH NO RETRIES
         TM    LCBRESTR+1,EWACCPU       ACR CONDITION?         @ZA02096
         BO    POSTEXIT                 YES, POST PERM ERROR   @ZA02096
*
         TM    LCBRESTR+3,XC0           VALID CSW SOTRED
         BO    POSTEXIT                 YES
*
         TM    LCBRESTR,XC0             VALID CSW STORED?
         BNZ   CKRETRY                  YES CHECK RETRY CODE
*
         TM    LCBRESTR,X10             HIO CAUSE INTERRUPT
         BZ    POSTEXIT                 NO, ERRONEOUS INTERRUPT
*
         BAL   R12,CHKCCW               CHECK CCW VALIDITY
*
         ICM   RCCW,SEVEN,LCBLSPCI      GET BUFFER ADDRESS     @SA74028
         BNZ   USELSPCI                 USE LSPCI BUFFER       @SA74028
         L     RCCW,LCBSTART-1          1ST CCW
USELSPCI EQU   *                                               @SA74028
         LA    RCCW,EIGHT(,RCCW)        BUMP FOR ENDING STATUS
         STCM  RCCW,SEVEN,LCBCSW        UPDATE CSW ADDRESS       X01004
         SPACE 2
POSTEXIT EQU   *
         OI    SCBERR4-IEDQSCB(RLINK),SCBCHANN  SET CHANNEL ERROR
         MVI   LCBINCAM+1,POSNO         SET RETURN FOR ERROR POST
         LH    RLINK,POSTMOD            504G RETURN
         L     RXCTL,XCTLADD(,RXCTL)    XCTL ADDRESS
         BR    RXCTL                    NEXT MODULE
*
         SPACE
SNO      EQU   *
         OI    SCBERR4-IEDQSCB(RLINK),SCBUNDFN  SET UNDEFINED ERROR
         B     POSTEXIT                 EXIT TO POST MODULE
*
         SPACE 3
* CSW STURED BY SIO OR AN INTERRUPT
CKRETRY  EQU   *
         BAL   R12,CHKCCW               CHECK CCW ADDRESS
*
         B     POSTEXIT                 INVALID RETURN
*
         TM    LCBRESTR+2,X10           VALID RETRY FLAG
         BZ    POSTEXIT                 NO
*
         LA    R8,THREE                 CONTROL UNIT TYPE
         N     R8,CUTYP(RUCB)           REGISTER
*                                       2701  = 2 =X'0010'
*                                       2702  = - =X'0001'
*                                       2703  = 3 =X'0011'
         BCTR  R8,0                     DECREMENT
         SLL   R8,TWO                   TIMES 4
*                        R5 CONTAINS    2701 = 4
*                                       2702 = 0
*                                       2703 = 8
         LA    R11,SEVEN                INITIALIZE FOR RETRY
         N     R11,LCBRESTR             ONLY RETRY CODE REMAINS
         LA    R10,ONE                  FOR BXLE INCREMENT
         SR    R9,R9                    INITIALIZE 1ST OPERAND
         MVI   LCBRESTR+1,XC0           READY TO SET COND. CODE
         NC    LCBRESTR+1(1),LCBRESTR+3 FOR 'TERMINATION CODE'
         CLI   LCBRESTR+1,X40           TERM. CODE  COND. CODE
*                                         X'00'        4 LOW
*                                         X'01'        8 EQUAL
*                                         X'02'        2 HIGH
         SPACE 3
         BXLE  R9,R10,TS001             BRANCH RETRY CODE NOT X000
*
         B     POSTEXIT                 CODE=0
*
         SPACE
TS001    EQU   *
         BXLE  R9,R10,TS010             CODE NOT X'001'
*
         BH    RETRY                    CODE > 1                 Y02027
*
         BE    CK000A(R8)               TERM. CODE 1
*
         B     CK000B(R8)               CODE 0
*
         SPACE
TS010    EQU   *
         BXLE  R9,R10,TS011             BRANCH NOT X'010'
*
         BH    RETRY                    CODE = 2
*
         BL    CK000B(R8)               CODE = 0
*
         B     CK000C(R8)               CODE = 1
*
         SPACE
TS011    EQU   *
         BXLE  R9,R10,TS100             RETRY CODE NOT X'011'
*
         SPACE
C02C011  EQU   *
         BH    POSTEXIT                 CODE = 2
*                                       NEED SIO TO PERMIT EQ.
*                                         CHECK,THEN RETRY
         BE    LOSTD                    CODE = 1
*
         SPACE
         B     *+4(R8)                  BRANCH ON CONTROL UNIT TYPE
*
         B     LOSTD                    2702
*
         B     SNO                      2701, 2703
*
LOSTD    EQU   *
         CLI   CCWOPCDE,CCWDIAL         DIAL
         BNE   CKREAD                   NO
*
         B     FRETR                    YES
*
         SPACE
TS100    EQU   *
         BXLE  R9,R10,TS101             RETRY CODE NOT X'100'
*
         BE    SNO                      CODE =1
*
         BH    RETRY                    CODE =2
*
         B     CK000B(R8)               CODE=0
*
         SPACE
TS101    EQU   *
         BXLE  R9,R10,POSTEXIT          BRANCH RETRY CODE NOT X'101'
*
         LTR   R8,R8                    2702
         BNZ   AYB1                     NOT 2702
         SPACE
         CLI   LCBRESTR+1,X40           RESET CONDITION CODE
         B     C02C011                  USE 2702, CODE 011
*
AYB1     EQU   *
         CLI   LCBSENS0,0               NO SENE INFORMATION
         BNE   CKEQK                    BRANCH SENSE INFO.
*
         TM    LCBCSW+4,CHDATACK        CHANNEL DATA CHECK
         BO    POSTEXIT                 YES
*
         B     SNO                      SHOULD NOT OCCUR
CKEQK    EQU   *
         TM    LCBSENS0,EC              EQUIPMENT CHECK
         BO    POSTEXIT                 YES
         SPACE
         TM    LCBSENS0,LD              LOST DATA
         BO    LOSTD                    YES
*
         TM    LCBSENS0,TO+DC           TIME OUT OR DATA CHECK
         BZ    SNO                      NO
*
CKREAD   EQU   *
         CLI   CCWOPCDE,CCWREAD         -EAD COMMAND
         BE    CKTEXT                   YES
*
         CLI   CCWOPCDE,CCWPOLL         POLL COMMAND
         BNE   SNO                      NO
*
FRETR    EQU   *                        CHECK RETRY COUNT
         BAL   R12,CKCOUNT              CHECK COUNT
*
         B     POSTEXIT                 RETRY EXHAUSTED
*
         B     FIRST                    CONTINUE RETRY           Y02027
*
CKTEXT   EQU   *
         LR    R12,RCCW                 SAVE CCW ADDRESS
         LA    R10,LCBCPA               START OF CHANNEL PROGRAM
         SR    R10,R12
         BH    TEXTCCW                  BR IF TEXT               Y02027
*
         SR    R6,R6                    CLEAR WORK
         L     R5,LCBDCBPT              DCB ADDRESS
         USING IHADCB,R5
         IC    R6,DCBEIOBX              LCB SIZE
         SH    R6,LCBSIZE               CALCULATE IOB
         LA    R6,0(RLCB,R6)            END OF CHANNEL PROGRAM AREA
         BNH   NOTTEXT                  BR IF CCW NOT FOR TEXT   Y02027
         SPACE
TEXTCCW  EQU   *
         BAL   R12,CKCOUNT              CHECK COUNT
         SPACE
         B     POSTEXIT                 INVALID
*
         B     EXCP                     BRANCH TO SET UP FOR RETRY.
*
         SPACE
NOTTEXT  EQU   *
         TM    CCWOPCDE,X20             DIAL OR ENABLE
         BZ    FRETR                    NO
*
         LA    RCCW,EIGHT(,RCCW)        RESTART AFTER CONNECTION
         BAL   R12,CKCOUNT              CHECK COUNT
*
         B     POSTEXIT                 INVALID
*
EXCP     EQU   *
         CLI   LCBECBCC,HIOCC           IOHALT ISSUED            Y02027
         BNE   SVCS                     BRANCH NO                Y02027
         SPACE 1                                                 Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CTL      Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
SVCS     EQU   *                                                 Y02027
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        LINE CONTROL BLOCK TO THE I/O SUPERVISOR BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY OPERATIONS FROM ERP          Y02027
**************************************************************** Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                       INTO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM LCB TO IOSB    Y02027
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE IS ADDED FOR AOS.                         *
*                                                                     *
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         SR    RUCB,RUCB                CLEAR FOR ICM.
         ICM   RUCB,7,IOBPTR(RTST)      GET IOB ADDRESS FROM RQE,
*                                       FOR USE BY SIO APPENDAGE.
         SR    R8,R8                    CLEAR FOR ICM.
         ICM   R8,7,DEBPTR(RTST)        GET DEB ADDRESS FROM RQE.
         LA    RBASE,DEBNMSUB-IEDQDEB   GET APDG. TABLE SIZE     Y02027
         SLR   R8,RBASE                 POINT TO APDG. TABLE     Y02027
         L     R8,FOUR(ZERO,R8)         GET ADDRESS OF SIO APPENDAGE
*                                       FROM APDG. VECTOR TABLE.
         SR    RBASE,RBASE              CLEAR RBASE (15) TO TELL
*                                       SIO APDG. IT'S A SUBROUTINE.
         BAL   RXCTL,SIOENTRY(ZERO,R8)  LINK TO SIO APPENDAGE AT
*                                       OFFSET +16 TO CONVERT CCW'S
*                                       FROM VIRTUAL TO REAL.
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
         SVC   EREXCP                   ERROR EXECUTE CHANNEL PROGRAM
         SVC   RETURN                   RETURN
*
         SPACE
CK000C   EQU   *                        ENTRY
         B     RETRY                    2702
         SPACE
         B     RETRY                    2701
*
         SPACE
CK000A   EQU   *                        ENTRY BRANCH FROM
         B     SNO                      2702     CK000A
*                                       2701     CK000C
*
         B     SNO                      2701     CK000A
*                                       2701     CK000C
         SPACE
CK000B   EQU   *
         B     RETRY                    2703     CK000A
*                                       2702     CK000B
         B     SNO                      2701
         B     RETRY                    2703
*
         SPACE 3
RETRY    EQU   *
         BAL   R12,CKCOUNT              CHECK RETRY COUNT
*
         B     POSTEXIT                 EXHAUSTED
*
FIRST    EQU   *
         CLI   CCWOPCDE,CCWENAB         ENABLE,DIAL, DISABLE
         BL    EXCP                     NO, BRANCH               Y02027
         SPACE
         LA    RCCW,LCBCPA+16           ASSUME WRITE
         TM    LCBSTAT1,LCBRECVN        RECEIVE STATE
         BZ    SEND                     NO
*
         LA    RCCW,EIGHT(,RCCW)        BUMP FOR RECEIVE
SEND     EQU   *
         ST    RCCW,LCBSTART-1          START ADDRESS
         B     EXCP                     BRANCH TO SET UP FOR RETRY.
*
         SPACE 2
CHKCCW   EQU   *
         L     RCCW,LCBSTART-1          1ST CCW IN CHAIN
         TM    LCBRESTR,X80             SIO CAUSE INTERRUPT
         BO    FOUR(R12)                BRANCH YES
         SPACE
         L     RCCW,LCBCSW-1            CCW ADDRESS INCSW
         SH    RCCW,H8                  BACK UP TO FAILING
         TM    LCBRESTR,FOUR            VALID CCW
         BZ    FOUR(R12)                BRANCH YES
*
         BR    R12                      INVALID
         SPACE 2
CKCOUNT  EQU   *
         SR    R10,R10
         IC    R10,LCBINCAM             RETRY COUNT
         LA    R10,ONE(,R10)            ADD ONE
         STC   R10,LCBINCAM             STORE UPDATED COUNT
         CLI   LCBINCAM,THREE           RETRY FAILED
         BCR   11,R12                   BRANCH YES
*
         B     FOUR(R12)                NORMAL RETURN
*
         EJECT
H8       DC    H'8'                     HALFWORD = 8             Y02027
POSTMOD  DC    AL2(5047)                IGE0504G ID              Y02027
LCBSIZE  DC    AL2(LCBFLAG1-IEDQLCB)    VALUE TO BACK-UP TO BEGINNING
*                                       OF LCB                   Y02027
CHDATACK EQU   X'08'                    CHANNEL DATA CHECK
LD       EQU   X'02'                    LOST DATA
TO       EQU   X'01'                    TIME OUT
DC       EQU   X'04'                    DATA CHECK
EC       EQU   X'10'                    EQUIPMENT CHECK
CVTPTR   EQU   16                       CVT ADDRESS LOCATION
PIBPTR   EQU   116                      POINTER TO ERPIB TABLE
IOBOFFST EQU   4                        IOB OFFSET IN RQE
RETURN   EQU   3                        SVC RETURN
EREXCP   EQU   15                       SVC ERROR RETRY
XCTLADD  EQU   X'2C'                    XCTL ADDRESS OFFSET IN CVT
CUTYP    EQU   16                       CONTROL UNIT TYPE OFFSET
POSNO    EQU   16                       SHOLD NOT OCCUR ERROR
NORTY    EQU   1                        NO RETRY FLAG
ZERO     EQU   0
ONE      EQU   1
TWO      EQU   2
THREE    EQU   3
FOUR     EQU   4
SEVEN    EQU   7
EIGHT    EQU   8
X10      EQU   X'10'
X20      EQU   X'20'
X40      EQU   X'40'
X80      EQU   X'80'                    MASK SIO ISSUED (ERPIB)
XC0      EQU   X'C0'                    RESET
IOBPTR   EQU   5                        OFFSET OF IOB ADDR. IN RQE.
DEBPTR   EQU   9                        OFFSET OF DEB ADDR. IN RQE.
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
*                                       POINT IN SIO APPENDAGE.
HIOCC    EQU   X'48'                    IOHALT COMPLETION CODE   Y02027
         EJECT
         TDEBD                          DEB DSECT                Y02027
         EJECT
         TLCBD
         TSCBD
         TCCWD
         DCBD DSORG=TX
         IECDIOSB                                                Y02027
         IECDERWA EWTC
         END
