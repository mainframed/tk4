         TITLE 'IGG02030 TCAM MESSAGE QUEUES CLOSE'
IGG02030 CSECT
*A-000000-999999                                               @X31X8T0
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS:                                   S21903
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A152000,312000,696000,808000                                    X01004
*C066000-068000,174000-176000,288000-298000,382000-660000,672000-X01004
*C674000                                                         X01004
*A312200                                                         Y01004
*C186000-188000,244000                                           Y01004
*A312340,312390,321640,697400,809600                             X02004
*C153000,312090-312120,312280,312710-312970                      X02004
*A184000                                                         A52482
*A164000                                                         A49219
*C168000,190000                                                  A49219
*A004000,006000                                                  S21903
*C156000,330000,334000,338000,704000-708000                      S21903
*C712000,730000,736000-742000,760000-766000                      S21903
*A312210,312970,697000                                           Y05330
*C312730                                                         Y05330
*A312250,312290,697600                                          OY00465
*C313050-313070                                                 OY00451
*D692500                                                        OY00451
*C313056,313100                                                 OY00541
*A313074                                                        OY00541
*A313120                                                        OY00532
*C313190-313200                                                 OX02223
*A809600                                                        OX02223
*A313145                                                        OY03847
*A312430                                                        OY00512
*C008000,018000,034000,040000,056500,082000-086000,153500        Y02027
*C172000-180000,188000,244000,312180,312190,312210,312215,312250 Y02027
*C312260,390000                                                  Y02027
*D182000-184000,697300                                           Y02027
*A057000,057500,076600,077200,153700,164000,364500-365500        Y02027
*A702500,808480-808720                                           Y02027
*A313074                                                        OY00541
*A313120                                                        OY00532
*A312660,312680                                                @YA03928
*A689500                                                         X03039
*C008000,189200,244000,312730,313105                             X03039
*D312215,312971-312981,313120-313123                             X03039
*A714000                                                       @Z30X8TM
*C008000,240000-242000,313080-313090,668000,691500,716000      @Z30X8TM
*D728000,742000                                                @Z30X8TM
*A092000,154000,405000,746000,750000,752000,754000,756000      @Z40X9TG
*A808800,809200                                                @Z40X9TG
*C008000,014000-028000,090000,166000,186000,250000-260000      @Z40X9TG
*C262000-264000,272000-274000,278000-293000,300000-302000      @Z40X9TG
*C312100,312110,718000-726000                                  @Z40X9TG
*D173000,687000,690500,758000-806000                           @Z40X9TG
*                                                              @G36XRTP
*A809000                                                       @G36XRTP
*C240000,312650,312660,313030,313112-313114,313119             @G36XRTP
*D240500-242500                                                @G36XRTP
*C726400-741100                                                @OZ26164
* DUMMY APAR                                                   @OZ27328
*C153300,176000                                                @OZ30722
*D182000-185600                                                @OZ30722
*A187500                                                       @OZ30722
         SPACE 3
***********************************************************************
*                                                                     *
*MODULE-NAME = IGG02030       (TCAM,TERMINATION)               @G36XRTP
*                                                                S21903
*DESCRIPTIVE-NAME = CLOSE ROUTINE FOR TCAM MESSAGE QUEUES        S21903
*                                                                S21903
*COPYRIGHTS = 'NONE'                                             S21903
*                                                                S21903
*STATUS - VERSION 10.0                                         @G36XRTP
*                                                                     *
*FUNCTION - OPERATION                                                 *
*   FOR MESSAGE QUEUE CLOSE, THIS MODULE FREES THE IOSBE'S.    @Z40X9TG
*   FOR CHECKPOINT CLOSE, THIS MODULE XCTL'S TO IGG02041.      @Z40X9TG
*   IF THE MESSAGE QUEUE DCB IS THE LAST OPEN DCB IN THE MCP,  @Z40X9TG
*   THEN FIXED CONTROL BLOCKS (FOR EXAMPLE, THE AVT) ARE       @Z40X9TG
*   PAGE FREED.                                                @Z40X9TG
*                                                                     *
*INPUT - THE REGISTERS 5, 6, 7, AND 8 ARE THE INPUT AS FOLLOWS        *
*   5 -  THE ADDRESS OF THE BEGINNING OF THE WTG TABLE PREFIX    Y02027
*   6 -  THE ADDRESS OF THE BEGINNING OF THE WHERE-TO-GO TABLE        *
*   7 -  THE ADDRESS OF THE COPY OF THE CURRENT DCB              Y02027
*   8 -  THE ADDRESS OF THE CURRENT ENTRY IN THE WHERE-TO-GO          *
*        TABLE                                                        *
*                                                                     *
*ENTRY POINTS - THIS ROUTINE IS ENTERED AT IGG02030 VIA AN            *
*   XCTL FROM AN I/O SUPPORT MODULE OR IT MAY BE RE-ENTERED           *
*   FROM THE RELOOP PORTION OF THIS EXECUOTR IF IT IS DETERMINED      *
*   THAT THERE IS ANOTHER DATA CONTROL BLOCK TO BE CLOSED             *
*   IF ENTRY IS FROM THE I/O SUPPORT MODULE, THEN THE MODULE     Y02027
*   MUST ISSUE MODESET TO GET FROM KEY OF DATA MANAGEMENT TO     Y02027
*   KEY 0.                                                       Y02027
*                                                                     *
*OUTPUT - REGISTERS 7 AND 8 WILL BE POSITIONED AT THE NEXT            *
*   ENTRIES IN THE WTG TABLE PREFIX AND THE WHERE-TO-GO TABLE    Y02027
*   RESPECTIVELY.                                                Y0202M
*                                                                     *
*EXTERNAL ROUTINES -- AOS PAGE SUPERVISOR SERVICE INTERFACE (SVC 113) *
*   TO FREE PAGES CONTAINING CPB'S FROM REAL MEMORY.                  *
*                                                                     *
*EXITS - NORMAL                                                       *
*   THIS ROUTINE EXITS VIA AN XCTL TO THE EXECUTOR IDENTIFIED         *
*   BY THE NEXT NON-ZERO ENTRY IN THE WHERE-TO-GO TABLE.              *
*   IF THE EXIT IS TO TCAM ERROR MODULE, THEN EXIT IN KEY 0;     Y02027
*   OTHERWISE, EXIT IN DATA MANAGEMENT PROTECT KEY.              Y02027
*                                                                     *
*TABLES - WORK AREAS - THE WHERE-TO-GO TABLE CONTAINS THE             *
*   ID AND TTR OF THE CLOSE EXECUTORS.  THE WTG TABLE PREFIX     Y02027
*   CONTAINS THE ADDRESSES OF THE COPIES OF THE DCB'S            Y02027
*   CURRENTLY BEING CLOSED                                       Y02027
*                                                                     *
*ATTRIBUTES - THIS ROUTINE IS EXECUTED IN LPALIB AND RUNS      @Z40X9TG
*   ENABLED, PRIVILEGED, AND RE-ENTRANT.                              *
***********************************************************************
         EJECT
*
* REGISTER USAGE
*
RE       EQU   0                        WORK REGISTER
RF       EQU   1                        WORK REGISTER
RDCB     EQU   2                        ADDRESS OF THE CURRENT DCB
RTIOT    EQU   3                        ADDRESS OF THE TIOT
RCORE    EQU   4                        ADDRESS OF DCB WORK AREA
RPAR     EQU   5                        ADDRESS OF FIRST ENTRY IN
*                                         DCB PARAMETER LIST
RWTG     EQU   6                        ADDRESS OF WHERE-TO-GO
*                                         TABLE
RPARC    EQU   7                        ADDRESS OF CURRENT ENTRY IN
*                                         DCB PARAMETER LIST
RWTGC    EQU   8                        ADDRESS OF THE CURRENT
*                                         ENTRY IN THE WTG TABLE
RAVT     EQU   9                        ADDRESS OF THE AVT
RUCB     EQU   10                       ADDRESS OF THE CURRENT UCB
RDEB     EQU   11                       ADDRESS OF THE DEB
RBASE    EQU   12                       BASE OF THIS ROUTINE
RC       EQU   13                       WORK REGISTER
RD       EQU   14                       WORK REGISTER
RJ       EQU   15                       WORK REGISTER
*
         USING FORCORE,RCORE            DSCB DSECT
         USING IHADCB,RDCB              DCB DSECT
         USING IEDQDEB+36,RDEB          DEB DSECT
         USING IEDQAVTD,RAVT            AVT DSCECT BASE
         USING IEDQTCB,RD               TCB DSECT
         USING IEDQTNTD,RUCB                                     X02004
         USING IEDQTCX,RC               TCX DSECT              @OZ30722
         USING CVTD,RF                  CVT DSECT                Y02027
*
         EJECT                                                 @Z40X9TG
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY S21903
         USING *,RBASE
         SPACE 2
         SR    RTIOT,RTIOT              CLEAR COUNT OF PASSES THROUGH
*                                       THIS MODULE
         MODESET EXTKEY=SUPR            GET INTO KEY 0           Y02027
         SPACE 2
IGG02030 IEDHJN RESTART                 MODULE ID AND DATE       S22025
         SPACE 1                                               @Z40X9TG
         LA    RTIOT,1(,RTIOT)          ADD 1 TO COUNT OF PASSES
         L     RCORE,FOUR(RWTGC)        ADDR OF OPEN WORK AREA   Y02027
         L     RDCB,DXUDCBAD            ADDR OF THE CURRENT DCB  Y02027
         SPACE 2
         L     RF,CVTPTR                ADDRESS OF CVT           Y02027
         L     RC,CVTAQAVT              GET THE TCX ADDR       @OZ30722
         L     RAVT,TCXAVT              GET THE AVT ADDRESS      Y02027
         EJECT                                                 @Z40X9TG
         DEBCHK (RDCB),TYPE=VERIFY,AM=TCAM VALIDATE DEB          Y01004
         SPACE 2
         LR    RJ,RF                    SAVE DEB ADDRESS         Y01004
         OI    TCXFLAG1,TCXMCPCD        MCP IS TERMINATING     @OZ30722
         L     RC,AVTCOMPT              GET CONSOLE CSCB       @OZ30722
         L     RC,0(RC)                 GET CONSOLE ECB ADDRESS@OZ30722
         MVI   0(RC),AVTEZERO           CLEAR ECB TO PREVENT   @OZ30722
*                                       301 ABENDS ON REATTACH @OZ30722
         DROP  RC                                              @OZ30722
         USING IEDQTCX,RJ               TCX DSECT              @OZ30722
         L     RF,CVTPTR                ADDRESS OF CVT           Y02027
         L     RD,AVTEZERO(,RJ)         TCB ADDRESS              A49219
         L     RDEB,TCBDEB              LOAD THE ADDRESS OF THE
*                                         FIRST DEB ON THE CHAIN
         SR    RUCB,RUCB                CLEAR COUNT OF DEBS ON TCB
CLRDEBAD EQU   *
         L     RC,DEBDCBAD              LOAD THE ADDRESS OF THE DCB
*                                         FOR THIS DEB
         TM    DSORG(RC),DISKDCB        IS THIS A MESSAGE QUEUES
*                                         DCB
         BNO   LASTEST                  NO, BRANCH
         LA    RUCB,1(,RUCB)            ADD 1 TO COUNT OF DEBS IN CHAIN
LASTEST  EQU   *
         NC    5(3,RDEB),5(RDEB)        IS THIS THE LAST DEB IN THE
*                                         CHAIN
         BE    LASTMSGQ                 YES, BRANCH
         L     RDEB,4(RDEB)             NO, LOAD THE ADDRESS OF THE
*                                         NEXT DEB IN THE CHAIN
         B     CLRDEBAD                 BRANCH
LASTMSGQ EQU   *
         CR    RTIOT,RUCB               COMPARE NO.CLOSED DEBS IN
*                                       TCB CHAIN WITH TOTAL NO. OF
*                                       DEBS IN TCB CHAIN
         BE    LASTIME                  BRANCH IF ALL MSG QUEUE(OR
*                                       CKPT) DEBS ARE CLOSED
CHKPOINT EQU   *
         TM    DCBOPTCD,CKPOINT+CKP3705 IS THIS CHKPOINT DCB   @G36XRTP
         BNZ   CHKPTID                  YES, BRANCH            @ZM47734
RESTDEB  EQU   *                                               @Z30X8TM
         L     RDEB,DCBDEBAD            RESTORE ADDRESS OF THE   Y02027
*                                         CURRENT DEB
         EJECT
************************************************************** @Z40X9TG
*                                                            * @Z40X9TG
*        FREE THE IOSBE'S                                    * @Z40X9TG
*                                                            * @Z40X9TG
************************************************************** @Z40X9TG
         L     RJ,AVTIOBN               LOAD ADDR OF THE       @Z40X9TG
*                                       NON-REUS IOSBE'S       @Z40X9TG
         TM    DCBOPTCD,NONREUS         IS THIS A NON-REUSABLE DISK
*                                         DATA SET
         BO    REUS                     YES, BRANCH
         L     RJ,AVTIOBR               LOAD ADDR OF THE       @Z40X9TG
*                                       REUS IOSBE'S           @Z40X9TG
REUS     EQU   *
         SLR   RF,RF                    CLEAR FOR INSERT       @Z40X9TG
         IC    RF,DEBNMEXT              NUMBER OF EXTENTS      @Z40X9TG
         MH    RF,SZIOSBE               GET SIZE OF ALL        @Z40X9TG
*                                       IOSBE'S                @Z40X9TG
         LR    RC,RF                    SAVE SIZE              @Z40X9TG
         LR    RCORE,RJ                 SAVE IOSBE ADDRESS     @Z40X9TG
         BAL   RD,FREEPG                PAGE FREE IOSBE'S      @Z40X9TG
*                                                                X01004
         LR    RE,RC                    LENGTH TO FREE           X01004
         LR    RF,RCORE                 ADDRESS OF IOB'S TO FREE X01004
         ICM   RE,HI,SPIOSBE            SET SUBPOOL ID         @Z40X9TG
         FREEMAIN  R,LV=(0),A=(1)       FREEMAIN THE IOBS
         EJECT
ENDLOAD  EQU   *
         XC    0(4,RWTGC),0(RWTGC)      INDICATE THE COMPLETION OF
*                                         THIS EXECUTOR
         LA    RF,DCBEND                LENGTH OF DCB            X01004
         LR    RJ,RDCB                  ADDRESS OF DCB           X01004
         BAL   RD,FREEPG                UNFIX DCB                X01004
*                                                                X01004
         LA    RF,DEBPRFIX              ADDR OF APPENDAGE TABLE  X01004
         SR    RDEB,RF                  ADJUST BASE              X01004
         DROP  RDEB                                              X01004
         USING IEDQDEB,RDEB                                      X01004
         L     RJ,DEBCEA                ADDRESS OF DISK END APP. X02004
         LA    RF,APPSTART-APP          GET OFFSET TO ENTRY PT @Z40X9TG
         SR    RJ,RF                    ADJUST TO START OF     @Z40X9TG
*                                       LOAD MODULE            @Z40X9TG
         LH    RF,APPLEN-APP(,RJ)       GET LENGTH FROM START  @Z40X9TG
*                                       OF APPENDAGE           @Z40X9TG
         BAL   RD,FREEPG                UNFIX APPENDAGE          X02004
         LR    RF,RDEB                  COPY DEB ADDRESS         Y01004
         ICM   RF,EIGHT,DEBLNGTH        DOUBLE WORD LENGTH OF DEBY01004
         BAL   RD,FREEPG1               PAGE FREE THE DEB        Y01004
*                                                                X01004
ENDLOAD1 EQU   *                                                 X01004
         L     RF,CVTPTR                ADDRESS OF CVT           Y02027
         TM    CVTTCMFG,LASTDCB         LAST TCAM DCB IS CLOSED  Y02027
         BNO   ENDLOAD2                 NO, BRANCH               X01004
         L     RJ,CVTAQAVT              ADDRESS OF TCX           Y02027
         SR    RCORE,RCORE              CLEAR FOR INSERT         X02004
         IC    RCORE,TCXTOTE            NUMBER OF TOTE ENTRIES   Y02027
         LM    RE,RF,TCXBUFA            ADDR AND LENGTH OF I/O   Y02027
*                                       TRACE, BUFFERS AND CPB'S X01004
         LR    RJ,RE                    COPY ADDRESS OF BUFFERS  X01004
         BAL   RD,FREEPG                UNFIX BUFRS-TRACE & CPB  X01004
         L     RUCB,AVTRNMPT            ADDRESS OF TERMINAL NAME X02004
*                                         TABLE (TNT)            X01004
         SR    RF,RF                                             X01004
         IC    RF,TNTENLEN              LENGTH OF ENTRY          X01004
         LA    RE,THREE                                          X01004
         AR    RF,RE                    BUMP FOR TNT POINTERS    X01004
         LR    RJ,RF                    SAVE ENTRY LENGTH        X02004
         MH    RF,TNTLEN                MULTIPLY LENGTH OF ENTRY X01004
*                                         BY NUMBER OF ENTRIES   X01004
         LA    RF,TNTOFF(RF)            ADD TO LENGTH THE CORDE  X01004
*                                         BEFORE THE FIRST ENTRY X01004
         LR    RD,RCORE                 NUMBER OF TOTE ENTRIES   X02004
         MR    RD,RD                    ENTRIES TO FIX FOR TOTE  X02004
         AR    RF,RJ                    ADD TOTE TO TNT LENGTH   X02004
         LTR   RJ,RJ                    ANY TOTE ENTRIES?       OY00512
         BNZ   TOTE                     BRANCH IF YES           OY00512
         LA    RF,ONE(,RF)              BUMP LENGTH BY ONE      OY00512
TOTE     EQU   *                                                OY00512
         LR    RJ,RUCB                  COPY TNT START ADDRESS   X02004
         AR    RF,RE                    ROUND TO WORD BOUNDARY   X01004
         SRL   RF,TWO                   SHIFT OUT 2 LO-ORD BITS  X01004
         SLL   RF,TWO                   SHIFT BACK               X01004
         LA    RCORE,ZERO(RF,RJ)                                 X01004
         ICM   RCORE,ALL,ZERO(RCORE)    GET ADDRESS OF TERMINAL  X01004
*                                         TABLE                  X01004
         BAL   RD,FREEPG                UNFIX TNT                X01004
         L     RF,ZERO(RCORE)           ADDRESS OF END OF THE    X01004
*                                       TERMINAL TAB AND QCBS    X01004
         LR    RUCB,RF                  SAVE ADDRESS OF SCBS     X01004
         SR    RF,RCORE                 GET LENGTH OF TERM TABLE X01004
         LR    RJ,RCORE                 ADDR OF TERMINAL TABLE   X01004
         L     RCORE,FOUR(RCORE)        ADDRESS OF THE NUMBER OF X01004
*                                         ENTRIES IN DEVICE      X01004
*                                         CHARACTERISTICS TABLE  X01004
         BAL   RD,FREEPG                UNFIX TERMINAL TABLE AND X01004
*                                         QUEUE CONTROL BLOCKS   X01004
         LH    RF,ZERO(RCORE)           NUMBER OF ENTRIES IN     X01004
*                                         DEVICE CHARACTERISTICS X01004
*                                         TABLE                  X01004
         MH    RF,AVTDCTLN              MULTIPLY BY THE SIZE   @G36XRTP
*                                       OF A DCT ENTRY         @G36XRTP
         LTR   RF,RF                    ZERO LENGTH            @YA03928
         BZ    ZERODCT                  BRANCH IF YES          @YA03928
         L     RJ,AVTCSTCS              ADDRESS OF DCT           X01004
         BAL   RD,FREEPG                UNFIX DCT                X01004
ZERODCT  EQU   *                                               @YA03928
         LR    RJ,RUCB                  ADDRESS OF SCBS          X01004
         LH    RF,TWO(RCORE)            SCB LGTH IN DOUBLEWORDS  X01004
         BAL   RD,FREEPG2               UNFIX SCB'S              X01004
         TM    AVTSCOPT,AVTCMBUF        COMMON BUFFER USED       X02004
         BZ    CKTSO                    NO, CHECK FOR TSO        X03039
         L     RUCB,AVTCBQCB            ADDRESS OF MASTER QCB    X02004
         USING IEDCMB,RUCB                                       X02004
         L     RCORE,CMBFIRST-ONE       ADDRESS OF FIRST PREFIX  X02004
         LR    RJ,RUCB                  ADDR. OF QCB, STCB'S AND X02004
*                                       PREFIXES FOR COMMBUF     X02004
         L     RF,CMBLAST-ONE           ADDRESS OF LAST PREFIX   X02004
         LA    RF,PRELNG(,RF)           ADDR. END OF LAST PREFIX X02004
         SR    RF,RJ                    LENGTH OF QCB'S, ETC.    X02004
         BAL   RD,FREEPG                FREE COMMBUF CNTRL BLOCKSX02004
         LH    RTIOT,CMBDAREA           NUMBER OF DATA AREAS     X02004
         N     RTIOT,AVTCLRHI           FORCE POSITIVE           X02004
COMLOOP  EQU   *                                                 X02004
         LA    RE,EIGHT                 CONSTANT TO LOCATE IDA   X02004
         SR    RJ,RJ                    CLEAR FOR ICM            X02004
         ICM   RJ,AD,ONE(RCORE)         ADDRESS OF DATA AREA     X02004
         SR    RJ,RE                    ADJUST TO IDA LIST PTR   X02004
         L     RF,FOUR(RJ)              TOTAL LENGTH TO FIX      X02004
         TM    ZERO(RJ),CCWIDA          IDA TO BE USED           X02004
         BNO   COMLOOP2                 NO, FIX BUFFER           X02004
         L     RJ,ZERO(RJ)              ADDRESS OF IDA LIST      X02004
COMLOOP2 EQU   *                                                 X02004
         BAL   RD,FREEPG                FREE COMMBUF DATA AREA   X02004
         LA    RCORE,PRELNG(RCORE)      BUMP TO NEXT PREFIX      X02004
         BCT   RTIOT,COMLOOP            FREE NEXT DATA AREA      X02004
CKTSO    EQU   *                        CHECK PRESENCE OF TSO    X02004
         TM    AVTBIT1,AVTTSON          TSO OR MIXED ENVIORNMENT X01004
         BZ    FREEAVT                  NO, BRANCH               X01004
         ICM   RJ,ALL,AVTTSOPT          ADDR. OF TSINPUT QCB     X02004
         BZ    FREEAVT                  BRANCH IF IT'S ZERO      X02004
         LA    RF,TSILEN(RE)            LENGTH TO FREE         @G36XRTP
         BAL   RD,FREEPG                PAGE FREE TSINPUT QCB    X02004
         L     RJ,AVTKA02               ADDRESS OF IEDQKA02     OY00451
         LTR   RF,RJ                    IS KA IN THE SYSTEM     OY00541
         BZ    NOKA                     NO, SKIP PAGEFREE       OY00541
         SH    RF,AVTHA4                ADJUST TO LENGTH        OY00451
         L     RF,ZERO(RF)              LENGTH TO PAGE FREE     OY00451
         BAL   RD,FREEPG                UNFIX IEDQKA02          OY00451
NOKA     EQU   *                                                OY00541
         LA    RF,AYZLNG                LENGTH OF IEDAYZ       @Z30X8TM
         L     RJ,AVTTSOPT              ADDRESS OF IEDAYZ      @Z30X8TM
         ICM   RJ,ALL,TSISCHED-IEDQTSI(RJ)  ADDRESS OF IEDAYZ   OY00541
         BZ    FREEAVT                  SKIP PAGEFREE IF NO AYZ OY00541
         BAL   RD,FREEPG                PAGE FREE OEDAXZ         X01004
FREEAVT  EQU   *                                                 X01004
         ICM   RJ,ALL,AVTSAVTP          GET SECONDARY AVT ADDR  OY00532
         BZ    NOSAVT                   SKIP PAGEFREE IF NO SVT OY00532
         LA    RF,SAVTVMSZ              VM ONLY                  X03039
         TM    AVTSAVTF,AVTVTMCP        3705 IN THE SYSTEM?    @G36XRTP
         BZ    ONLYEP                   NO BRANCH TO FREE      @G36XRTP
         LA    RF,SAVTSIZE              ENTIRE LENGTH            X03039
ONLYEP   EQU   *                                               @G36XRTP
         BAL   RD,FREEPG                UNFIX SECONDARY AVT     OY00532
NOSAVT   EQU   *                                                OY00532
         ICM   RF,ALL,AVTIOT            ADDR OF I/O TRACE RTNE   X01004
         BZ    FREEAVT1                 I/O TRACE NOT PRESENT  E X01004
         XC    AVTIOT,AVTIOT            CLEAR I/O TRACE POINTER OY03847
         BAL   RD,FREEPG3               PAGE FREE I/O TRACE RTN@ZM47865
FREEAVT1 EQU   *                                                 X01004
         LR    RJ,RAVT                  ADDRESS OF AVT           X01004
         LA    RF,AVTLNG                LENGTH TO UNFIX          X01004
         BAL   RD,FREEPG                UNFIX AVT               OX02223
ENDLOAD2 EQU   *                                                OX02223
         L     RCORE,4(RWTGC)           LOAD THE ADDRESS OF THE
*                                         OPEN WORK AREA
         ST    RAVT,EIGHT(RCORE)        SAVE AVT ADDRESS FOR     S22024
*                                        CHECKPOINT CLOSE        S22024
RELOOP   EQU   *
         LA    RWTGC,8(RWTGC)           BUMP THE CURRENT WTG POINTER
         LA    RPARC,4(RPARC)           GET THE NEXT ENTRY IN THE
*                                         DCB PARAMETER LIST
         CLC   0(2,RWTGC),AMIDCNST      IS THIS EXECUTOR REQUIRED
*                                         AGAIN
         BE    RESTART                  GO TO EXECUTOR           S21903
         CLC   0(2,RWTGC),CLOSEIDL
         BL    RELOOP                   GET NEXT ENTRY           S21903
         CLC   0(2,RWTGC),CLOSEIDH
         BH    RELOOP                   GET NEXT ENTRY           S21903
*
         LR    RPARC,RPAR               INITIALIZE CURRENT DCB REG
         LA    RWTGC,32(RWTG)           AND WHERE-TO-GO REGISTERS
*
ZCHECK   EQU   *
         CLI   0(RWTGC),X'00'           IS THIS ENTRY COMPLETED
         BNE   XCTLRTNE                 IF ENTRY IS NOT COMPLETE,
*                                         TRANSFER CONTROL
         LA    RWTGC,EIGHT(RWTGC)       BUMP POINTER TO TEST FOR
         LA    RPARC,FOUR(RPARC)          COMPLETION OF THE NEXT
*                                         ENTRY
         B     ZCHECK                   TEST THE NEXT ENTRY
XCTLRTNE EQU   *
         MODESET EXTKEY=DATAMGT         RETURN TO KEY 5          Y02027
         SPACE 2
         LA    RJ,DXCCW12               LOAD THE ADDRESS OF THE
*                                         LAST DOUBLE WORD LIST
         MVC   6(TWO,RWTG),0(RWTGC)     MOVE ID TO NAME FIELD     *
         MVC   14(THREE,RWTG),TWO(RWTGC) MOVE TTR TO WTG TABLE    *
         XCTL  DE=(RWTG),SF=(E,(15))    TRANSFER CONTROL
         EJECT
LASTIME  EQU   *
         OI    CVTTCMFG,LASTDCB         SET LAST DCB TO BE CLOSD Y02027
         B     CHKPOINT                 RETURN TO IN-LINE CODE   X01004
         SPACE 3
************************************************************** @Z40X9TG
*                                                            * @Z40X9TG
*        PAGE FREE SUBROUTINE                                * @Z40X9TG
*                                                            * @Z40X9TG
************************************************************** @Z40X9TG
FREEPG1  EQU   *                                                 X01004
         LR    RJ,RF                    COPY ADDRESS TO UNFIX    X01004
         SRL   RF,HIGHBYTE              GET NUM OF DOUBLEWORDS   X01004
FREEPG2  EQU   *                                                 X01004
         SLL   RF,THREE                 CONVERT TO BYTE LENGTH   X01004
FREEPG   EQU   *                                                 X01004
         LR    RE,RJ                    SAVE START ADDRESS       Y02027
         LA    RJ,ZERO(RF,RJ)           BUMP TO END ADDR + 1     Y02027
         LR    RF,RE                    SET START ADDRESS        Y02027
         PGFREE R,A=(1),EA=(15)         ISSUE PAGE FREE          Y02027
         BR    RD                       RETURN TO CALLER         X01004
*
FREEPG3  EQU   *                                               @ZM47865
         LR    RJ,RF                    COPY ADDR TO UNFIX     @ZM47865
         SRL   RF,HIGHBYTE              GET NUM OF DOUBLE WORDS@ZM47865
         LA    RF,TWO56(RF)             ADD BACK 2K OF MOD     @ZM47865
         B     FREEPG2                  GO GET BYTE LENGTH     @ZM47865
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
CHKPTID  EQU   *
         MVC   0(TWO,RWTGC),CKPTCLOZ    SET THE ID FOR         @Z30X8TM
*                                         CHECKPOINT CLOSE
         B     ENDLOAD1                 RETURN TO END OF LOAD    X01004
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
TWO56    EQU   256                      2K IN DOUBLE WORDS     @ZM47865
TWO      EQU   2                        VALUE 2                  S22024
THREE    EQU   3                        VALUE 3                  S22024
FOUR     EQU   4                        VALUE 4                  S22024
SIX      EQU   6                        VALUE 6                  S22024
EIGHT    EQU   8                        VALUE 8                  S22024
TWELVE   EQU   12                       VALUE 12                 S22024
CVT      EQU   16                       VALUE 16                 S22024
DSORG    EQU   27                       VALUE 27                 S22024
NONREUS  EQU   X'02'                    NON REUSE                S22024
DEBPRFIX EQU   36                       LENGTH OF DEB PREFIX     X01004
AVTLNG   EQU   X'500'                   AVT LENGTH TO FREE       X01004
AYZLNG   EQU   X'AF4'                   LENGTH OF IEDAYZ       @Z30X8TU
LASTDCB  EQU   X'0F'                    LAST ENTRY SWITCH        X01004
ALL      EQU   15                       ICM/STCM MASK            X01004
SIXTEN   EQU   16                       NUMERIC CONSTANT         X01004
HIGHBYTE EQU   24                       SHIFT TO HIGH-ORDER BYTE X01004
ZERO     EQU   0                        NUMERIC CONSTANT         X01004
ONE      EQU   1                        NUMERIC CONSTANT         X01004
HI       EQU   8                        ICM/STCM MASK            X01004
CCWIDA   EQU   4                        INDIRECT ADDRESSING FLAG X02004
AD       EQU   7                        ICM/STCM MASK            X02004
PRELNG   EQU   12                       LENGTH OF COMMBUF PREFIX X02004
LCB3705  EQU   184                      PLCB SIZE                Y05330
CKP3705  EQU   X'10'                    OPTCD=I 3705 CHECKPOINT  S22024
CKPOINT  EQU   X'20'                    OPTCD=C TCAM CKPT DCB    S22024
DISKDCB  EQU   X'20'                    VALUE                    S22024
IOTRCSIZ EQU   32                       LENGTH OF IO TRACE ENTRY   0610
FIVE     EQU   5                        OFFSET                   Y02027
HWORD    DC    H'0'                     NUMERIC CONSTANT         S21903
CLOSEIDL DC    C'0B'                    ID OF LAST CLOSE MODULES S21903
CLOSEIDH DC    C'0G'                    ID OF LAST CLOSE MODULES S21903
AMIDCNST DC    C'30'                    ID OF THIS CLOSE EXECUTOR
H7       DC    H'7'                     NUMERIC CONSTANT         S21903
H20      DC    H'20'                    NUMERIC CONSTANT
CKPTCLOZ DC    C'41'                    ID FOR CHECKPOINT CLOSE
         DS    0F                                              @Z30X8TM
SZIOSBE  DC    AL2(IOSBELEN)            SIZE OF IOSBE          @Z40X9TG
SPIOSBE  DC    AL1(250)                 SUBPOOL FOR IOSBE'S    @Z40X9TG
         EJECT                                                 @Z40X9TG
*        OVERLAY FOR START OF DISK END APPENDAGE               @Z40X9TG
         SPACE 1                                               @Z40X9TG
APP      DSECT                                                 @Z40X9TG
APPLEN   DS    H                        LENGTH IN BYTES        @Z40X9TG
APPSIO   DS    H                        OFFSET TO DISKSIO APP  @Z40X9TG
APPNAME  DS    CL8                      NAME OF APPENDAGE      @Z40X9TG
APPHJN   DS    CL2                      HJN                    @Z40X9TG
APPSTART DS    H                        START OF CHE AND ABE   @Z40X9TG
         SPACE 4                                               @Z40X9TG
         EJECT
         TIOSBED                                               @OZ26164
         EJECT                                                 @Z40X9TG
         EJECT
         TTCXD                                                   Y02027
         EJECT
         TAVTD
         EJECT                                                 @Z40X9TG
FORCORE  DSECT
         IECDSECT
         EJECT                                                 @Z40X9TG
         TCPBD
         EJECT                                                 @Z40X9TG
         TDEBD
         EJECT                                                 @Z40X9TG
         TTCBD
         EJECT                                                 @Z40X9TG
         DCBD  DSORG=(TQ,DA)                                   @G36XRYP
DCBEND   EQU   X'40'                                             X01004
         EJECT                                                   Y02027
CVTD     DSECT                          CVT DSECT                Y02027
         CVT                            CVT                      Y02027
         EJECT                                                   Y02027
         TTNTD                                                   X01004
         EJECT                                                 @Z40X9TG
         TTSID                                                   X01004
         EJECT
         TTSWD
         EJECT                                                 @Z40X9TG
         IEDCMB                                                  X02004
         END
