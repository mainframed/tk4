         TITLE 'IGG01939 FIFTH LOAD OF LINE GROUP OPEN'
IGG01939 CSECT
*CHANGE ACTIVITY= AS FOLLOWS:
*A122373-122376,122388,144393,172600,436500,452230,452550,       X01004
*A489100-493900,502000                                           X01004
*C122370,279000,291000,452020-452220,488300                      X01004
*D122010-122258,122390,452240-452380,486600,488600               X01004
*A097500,398340-398500,494700-495500                             Y01004
*C097000,398300                                                  Y01004
*A122332,122382-122386,398540,530600-531200                      X02004
*C004310,122324,122380,142393,282000                             X02004
*D004610-006410,122330,285000-288000                             X02004
*C003000,022000,026000,047000-048200,083500,088240-095000        Y02027
*C338000,452170,459000-467000,468500-473500,474200-474800        Y02027
*C480000,483000,493900                                           Y02027
*D097000-097500                                                  Y02027
*A196000,436560-436920                                           Y02027
*C205000,207000,388000-390000,427000-432000,452040-452090,452130 Y02027
*C452180,522000-525000,437200                                    Y02027
*D433000,434000,452140,452150,452190,452200                      Y02027
*D468000,469500,488070,488300-489900,498000-501000               Y02027
*D518100                                                         Y02027
*D436680,436740                                                @ZA04035
*A436860,436920                                                @ZA04035
*A063000,122267-122320,126393,205000,412020-458400,488070-488210 S99228
*A498000,509260-509320,516000                                    S99228
*D371000-378000,412020-414720,477600,479000,481000,516000        S99228
*A089000,122030,122230,122390                                    A41031
*C122060,133600                                                  A41031
*D089200-089800,091000-096000,373000-375000                      S21101
*A090000,366000,412000,412200,412500                             S21101
*A001000,079000,089800,122010,122130,122220,122340,153600,179200 CLUP21
*A195000,203000,206000,309000,318000,329000,398000,406000,420000 CLUP21
*A436000,453000,462000,474800,477600,483000,488000,508000        CLUP21
*C004010-004900,063000,075000,089600,122020-122070,122150,157600 CLUP21
*C160000,168800,347000,364000-366600,403000-404000,412100-414000 CLUP21
*C418000,498000                                                  CLUP21
*D029600,054000-054600,080000,082000,085000,102000-110000,122140 CLUP21
*D122220-122300,122350-122360,122393-147000,181000-193000        CLUP21
*D207000-309000,311000,371000-384000,437000-451000,452320-452340 CLUP21
*D457000-458000,463000,465000-466000,469500,472000-473000        CLUP21
*D475400-477300,478000,481000-482000,484000-486300,487000-487500 CLUP21
*D503000-504000,509000-518000,520600-520800,528000-529000,531000 CLUP21
*D514600-514800                                                  S22026
*C122267-122275,364000,412020-412580,513800-514400,515000        S22026
*A475000-475200,503000-504000,513260-513320,515800-518600        S22026
*C087000,325000-325500,327000,335000,335500,453150,453240,453300 S22025
*C453390-453540,453780-453870,455000,502000,502500,513000        S22025
*C521000,523000,524000                                           S22025
*A001960,001980,052500,088060                                    S22025
*A001980,513600                                                  S21903
*C001988,122267,166400-179200,314000-315000,352000,354000-385000 S21903
*D122312,128393,166400-179200,314000-315000,352000,354000-385000 S21903
*D401000-414810,452740-452760,456000-458400,464000,471000,475000 S21903
*D475200,484800,489000-496000,502000-503000,506000,508000,509200 S21903
*D509320,509600,510000,510400,510800,511200,511600,512000,512400 S21903
*D512800,513200-513320,513600-518600                             S21903
***********************************************************************
*                                                                S21903
*MODULE-NAME= IGG01939                                           S21903
*                                                                S21903
*DESCRIPTIVE-NAME= TCAM LINE GROUP OPEN ROUTINE (FIFTH LOAD)     S21903
*                                                                S21903
*COPYRIGHT= 'NONE'                                               S21903
*                                                                     *
*STATUS = CHANGE LEVEL 6                                         Y02027
*                                                                     *
*FUNCTION/OPERATION - IGG01939 IS THE FIFTH LOAD OF TCAM LINE    CLUP21
*   GROUP OPEN.                                                  X02004
*                                                                CLUP21
*   IGG01939 ATTEMPTS TO OPTIMIZE SPACE IN SUBPOOL 252 BY        CLUP21
*   LOADING LARGER MODULES FIRST SO THAT SMALLER MODULES WILL    CLUP21
*   FIT ABOVE THEM IN THE SAME 2K BLOCK. THE LARGEST MODULE, THE CLUP21
*   LINE END APPENDAGE ROUTINE, IS LOADED FIRST AND ITS ADDRESS  CLUP21
*   SAVED IN THE DEB AS BOTH THE NORMAL AND ABNORMAL LINE END    CLUP21
*   APPENDAGE. IF THE DCB INDICATES THE USER HAS REQUESTED PCI,  CLUP21
*   THE PCI APPENDAGE IS LOADED AND ITS ADDRESS SAVED IN THE DEB.CLUP21
*   THE I/O TRACE ROUTINE IS LOADED IF THERE IS AN I/O TRACE     CLUP21
*   AREA, ITS ADDRESS SAVED IN THE AVT, AND IF THE USER HAS      CLUP21
*   PROVIDED AN EXIT ROUTINE THE CIRB MACRO IS ISSUED TO BUILD   CLUP21
*   AND INITIALIZE AN INTERRUPT REQUEST ELEMENT. THE LAST        CLUP21
*   ROUTINE TO BE LOADED IS THE DEVICE DEPENDENT SPECIAL         CLUP21
*   CHARACTERS TABLE. ITS ADDRESS IS SAVED IN THE DCB.           CLUP21
*                                                                     *
*ENTRY POINTS - THIS ROUTINE IS ENTERED AT IGG01939 VIA AN            *
*   XCTL FROM THE PREVIOUS EXECUTOR (IGG01938).  IT MAY               *
*   ALSO BE RE-ENTERED FROM THE RELOOP PORTION OF THIS ROUTINE        *
*   IF IT IS DETERMINED THAT ANOTHER DATA CONTROL BLOCK IS TO         *
*   BE PROCESSED BY IGG01939.                                         *
*                                                                     *
*INPUT - REGISTERS 5,6,7, AND 8 ARE THE INPUT AS FOLLOWS              *
*                                                                     *
*   5 - THE ADDRESS OF THE WTG TABLE PREFIX                      Y02027
*                                                                     *
*   6 - THE ADDRESS OF THE BEGINNING OF THE WHERE-TO-GO TABLE         *
*                                                                     *
*   7 - THE ADDRESS OF THE COPY OF THE CURRENT DCB               Y02027
*                                                                     *
*   8 - THE ADDRESS OF THE CURRENT EXECUTOR'S ENTRY IN THE            *
*       WHERE-TO-GO TABLE                                             *
*OUTPUT - THE DISPATCHER WILL HAVE BEEN LOADED ALONG WITH             *
*   THE CORRECT SCHEDULER OR SCHEDULERS FOR THIS LINE GROUP.          *
*                                                                     *
*EXTERNAL ROUTINES - NONE                                             *
*                                                                     *
*EXITS - NORMAL                                                       *
*   THIS ROUTINE EXITS VIA AN XCTL TO THE MODULE IDENTIFIED BY        *
*   THE NEXT NON-ZERO ENTRY IN THE WHERE-TO-GO TABLE,                 *
*   NAMELY, IGG01940                                                  *
*                                                                     *
*      - ERROR                                                        *
*   NONE                                                              *
*                                                                     *
*TABLES/WORKAREAS - THE WHERE-TO-GO TABLE CONTAINS THE ID             *
*   AND TTR OF THE OPEN EXECUTOR TO BE GIVEN CONTROL.                 *
*   THE WHERE-TO-GO TABLE PREFIX CONTAINS THE ADDRESS OF A       Y02027
*   COPY OF EACH DCB THAT WAS SPECIFIED IN THE OPEN MACRO        Y02027
*   INSTRUCTION                                                  Y02027
*                                                                     *
*ATTRIBUTES - THIS ROUTINE IS EXECUTED IN THE TRANSIENT AREA          *
*   AS ENABLED, PRIVILEGED, AND REENTRANT.                            *
*                                                                     *
***********************************************************************
         EJECT
*
*    REGISTER USAGE
*
*
RE       EQU   0                        WORK REGISTER
RF       EQU   1                        WORK REGISTER
RDCB     EQU   2                        ADDRESS OF THE CURRENT DCB
RTIOT    EQU   3                        WORK REGISTER
RCORE    EQU   4                        WORK REGISTER            CLUP21
R4       EQU   4                        WORK REG                 S99228
RPAR     EQU   5                        ADDRESS OF FIRST ENTRY IN
*                                         DCB PARAMETER LIST
RWTG     EQU   6                        ADDRESS OF THE WHERE-TO-GO
*                                         TABLE
RPARC    EQU   7                        ADDRESS OF THE CURRENT
*                                         ENTRY IN DCB PARAMETER
*                                         LIST
RWTGC    EQU   8                        ADDRESS OF THE CURRENT
*                                         ENTRY IN THE WTG TABLE
RAVT     EQU   9                        ADDRESS OF THE AVT
RUCB     EQU   10                       WORK REGISTER
RDEB     EQU   11                       ADDRESS OF CURRENT DEB   CLUP21
RBASE    EQU   12                       BASE OF THIS ROUTINE
RC       EQU   13                       WORK REGISTER
RD       EQU   14                       WORK REGISTER
RJ       EQU   15                       WORK REGISTER
         SPACE 2
         USING IEDQDEB,RDEB             DEB DSECT
         USING IEDQAVTD,RAVT            AVT DSECT
         USING FORCORE,RCORE            OPEN WORK AREA DSECT     Y02027
         USING IHADCB,RDCB              DCB DSECT
*
         BALR  RBASE,0                  SET UP BASE REGISTER     S22025
         USING *,RBASE
         SPACE 2
IGG01939 IEDHJN RESTART                                          S22025
         EJECT
**************************************************************** Y02027
*                                                              *
*        THIS SECTION OF CODE ESTABLISHES ADDRESSABILTY        * Y02027
*        FOR THE DEB AND COMPUTES THE OFFSET INTO THE          * Y02027
*        DEB OF THE SUBROUTINE - ID SECTION.                   * Y02027
*                                                              * Y02027
**************************************************************** Y02027
*                                                                CLUP21
         L     RCORE,FOUR(RWTGC)        LOAD THE ADDRESSES OF THEY02027
*                                       OPEN WORK AREA           Y02027
         L     RDCB,DXUDCBAD            GET THE CURRENT DCB ADDR Y02027
         L     RDEB,DCBDEBAD            GET THE DEB ADDRESS      Y02027
         LA    RF,DEBPTR                LOAD THE LENGTH OF THE DEB
*                                         PREFIX
         SR    RDEB,RF                  SET THE BEGINNING ADDRESS
*                                         AT THE DEB PREFIX
         SR    RTIOT,RTIOT              CLEAR A REGISTER FOR THE
*                                         COUNT OF LOADED
*                                         SUBROUTINES
         SR    RD,RD                    CLEAR SIZE REGISTER
         IC    RD,DEBNMEXT              GET THE NUMBER OF EXTENTS
         SLL   RD,2                     MULTIPLY EXTENTS BY 4
         LA    RUCB,DEBUCBAD(RD)        ADDRESS OF ACCESS METHOD
*                                         DEPENDENT SECTION
         SR    RD,RD                    CLEAR LENGTH RECORD
         IC    RD,DEBAMLNG              LENGTH OF ACCESS METHOD
         AR    RUCB,RD                  ADDRESS OF SUB-ROUTINE ID
*                                         SECTION
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
*        THIS SECTION OF CODE LOADS THE APPROPRIATE LINE END     CLUP21
*        APPENDAGE AND THE PCI APPENDAGE IF NEEDED.              CLUP21
*                                                                CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
GETAPNDG EQU   *                                                 CLUP21
         IC    RF,AVTBIT2               LINE CONFIGURATION BITS  S21903
         LA    RD,THREE                                          S21903
         NR    RF,RD                    CLEAR EXTRANEOUS BITS    S21903
         AR    RF,RF                    GET TWO BYTE INDEX       S21903
         LA    RC,LINEEND(RF)           ADDRESS OF APPENDAGE     S21903
*                                         TO BE LOADED           S21903
         CLC   0(TWO,RC),APNDGQ3        LOADING START/STOP       S21903
         BNE   BALOAD                   NO, BRANCH               S21903
         TM    AVTBIT1,AVTTSON          TSO OR MIXED ENVIRONMENT S99228
         BO    BALOAD                   BRANCH YES               S99228
         LA    RC,APNDGQ5               LOAD ADDR Q5 ID          S99228
BALOAD   EQU   *                                                 S99228
         BAL   RD,LOADRTNE              LOAD LINE APPENDAGE      S99228
         ST    RC,DEBCEA                STORE APPENDAGE ADDR     X02004
         ST    RC,DEBXCEA               STORE SAME ADDRESS FOR   X02004
*                                         THE ABNORMAL END APPENDAGE
         BAL   RD,FIXPAGE               FIX LINE END APPENDAGE   X01004
         LA    RC,SIOAPNDG              ADDRESS OF SIO APPENDAGE X01004
         BAL   RD,LOADRTNE              LOAD SIO APPENDAGE       X01004
         ST    RC,DEBSIOA               SAVE ADDRESS IN DEB      X02004
         OI    DEBSIOA,DEBSIOX+DEBIOVR  SET EXCPVR VALID AND SIO X02004
*                                       APG TO BE ENTERED WHEN   X02004
*                                       ERP ACTIVE               X02004
         BAL   RD,FIXPAGE               FIX START I/O APPENDAGE  X01004
         TM    DCBPCI,NOPCI             HAS USER SPECIFIED NO PCICLUP21
         BO    LOADTRAC                 YES, BRANCH              CLUP21
         LA    RC,PCIAPNDG              ADDRESS OF PCI APPENDAGE CLUP21
         BAL   RD,LOADRTNE              LOAD IGG019RN            CLUP21
*                                                                CLUP21
         ST    RC,DEBPCIA               STORE PCI ADDRESS IN DEB X02004
         BAL   RD,FIXPAGE               FIX PCI APPENDAGE        X01004
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF CODE LOADS THE I/O TRACE ROUTINE         *
*        AND ISSUES THE CIRB MACRO TO BUILD AN INTERRUPT          *
*        QUEUE ELEMENT FOR THE I/O TRACE ROUTINE                  *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                CLUP21
LOADTRAC EQU   *                                                 CLUP21
         L     RJ,AVTIOT                LOAD THE VALUE STORED AT
*                                         AVTIOT
         LTR   RJ,RJ                    HAVE WE PREVIOUSLY LOADED
*                                         THE I/O TRACE ROUTINE
         BNZ   TEST2260                 YES, BRANCH              CLUP21
         L     RJ,AVTRACE               LOAD THE VALUE AT AVTRACE
         LTR   RJ,RJ                    IS THERE ANY I/O TRACE AREA
         BZ    TEST2260                 NO, BRANCH               CLUP21
         LA    RC,IOTRACE               LOAD THE ADDRESS OF THE I/O
*                                         TRACE ROUTINE
         BAL   RD,LOADRTNE              RETURN LINKAGE ESTABLISHED
*
         ST    RE,AVTIOT                STORE THE ADDRESS OF THE
*                                         LOADED TRACE ROUTINE IN
*                                         THE AVT
         BAL   RD,FIXPAGE               FIX I/O TRACE ROUTINE    X01004
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
*        THIS SECTION OF CODE LOADS IN THE APPROPRIATE SPECIAL   CLUP21
*        CHARACTERS TABLE.                                       CLUP21
*                                                                CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
TEST2260 EQU   *
         SR    RJ,RJ                    CLEAR AN INDEX REGISTER
*                                                                CLUP21
TESTUCB1 EQU   *
         USING UCB,RF                   UCB ADDRESSABILITY       Y02027
         L     RF,DEBUCBAD(RJ)          LOAD THE ADDRESS OF THE
*                                         FIRST/NEXT UCB
         LTR   RF,RF                    IS THIS A // DD DUMMY ENTRY
         BNZ   UCBTEST                  NO, BRANCH
         LA    RJ,FOUR(RJ)              INCREMENT THE UCB ADDRESS
*                                         INDEX
         B     TESTUCB1                 BRANCH
*                                                                CLUP21
UCBTEST  EQU   *
         TM    UCBDVCLS,GRAPHICS        GRAPHICS DEVICE ?        Y02027
*                                       OR A 3270 LOCAL DEVICE   S99228
         BO    LOAD2260                 YES, BRANCH
         TM    UCBUNTYP,CNTRLUNT        IS CONTROL UNIT SPECIFIEDY02027
         BZ    AUDIOLD                  NO, AUDIO TERMINAL       CLUP21
         LA    RE,CHATRANS              STARTING OFFSET OF THE   CLUP21
*                                         SPECIAL CHARACTERS     CLUP21
*                                         TRANSLATE TABLE        CLUP21
         SR    RF,RF                    CLEAR FOR SPECIAL        CLUP21
*                                         CHARACTERS INDEX       CLUP21
         IC    RF,DCBSCTAD+2            GET TRANSLATE TABLE INDEXCLUP21
*                                         FROM THE DCB           CLUP21
         LTR   RF,RF                    IS THE INDEX ZERO        CLUP21
         BZ    LOADSPEC                 YES, BRANCH              CLUP21
         SR    RF,RE                    ADJUST INDEX TO START OF CLUP21
*                                         TRANSLATE TABLE        CLUP21
         STC   RF,AVTGETMN              STORE SPECIAL CHARS INDEXCLUP21
         TR    AVTGETMN(1),CHARTBL      TRANSLATE INDEX INTO     CLUP21
*                                         ADDRESS OFFSET TO ID   CLUP21
         IC    RF,AVTGETMN              GET OFFSET TO SCT ID     CLUP21
*                                                                CLUP21
LOADSPEC EQU   *                                                 CLUP21
         LA    RC,SPECIALC(RF)          ADDRESS OF SCT ID        CLUP21
*                                                                CLUP21
LOADIT   EQU   *                                                 CLUP21
         BAL   RD,LOADRTNE              LOAD SPECIAL CHARACTERS  CLUP21
*                                         TABLE                  CLUP21
         STCM  RE,AD,DCBSCTAD          SAVE ADDR OF SCT IN DCB   X01004
         STCM  RE,HI,DEBEOEA+THREE      SAVE LENGTH FOR CLOSE    X02004
         BAL   RD,FIXPAGE               FIX SPECIAL CHARACTERS   X01004
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
*        THIS SECTION OF CODE SAVES THE COUNT OF SUBROUTINES     CLUP21
*        LOADED IN THE DEB AND DETERMINES IF THIS EXECUTOR IS    CLUP21
*        REQUIRED AGAIN (MULTIPLE DCB'S BEING OPEN'ED). AFTER    CLUP21
*        ALL DCB'S HAVE BEEN PROCESSED, PARAMETERS ARE SET UP FORCLUP21
*        TRANSFER OF CONTROL TO THE NEXT LOAD OF OPEN - IGG01940.CLUP21
*                                                                CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
         STC   RTIOT,DEBNMSUB           STORE THE COUNT OF LOADED
*                                         SUBROUTINES IN THE DEB
         MVC   0(5,RWTGC),TCAMLD6       SET THE ID AND TTR FOR THE
*                                         LAST LOAD OF LINE  OPEN
         L     RJ,4(RWTGC)              ADDRESS OF OPEN WORK AREA
*                                                                CLUP21
RELOOP   EQU   *
         LA    RWTGC,8(RWTGC)           NEXT WTG ENTRY
         LA    RPARC,4(RPARC)           NEXT DCB LIST ENTRY
         CLC   0(2,RWTGC),AMIDCNST      IS THIS EXECUTOR NEEDED
*                                         AGAIN
         BE    RESTART                  BRANCH IF YES TO PROCESS S22025
*                                       NEXT DCB                 S22025
         CLC   0(2,RWTGC),OPIDCNST      END OF WTG TABLE
         BNE   RELOOP                   BRANCH IF NO FOR NEXT DCBS22025
         LR    RPARC,RPAR               REINITIALIZE WTG AND
         LA    RWTGC,32(RWTG)           PARAMETER REGISTERS
*                                                                CLUP21
ZCHECK   EQU   *
         CLI   0(RWTGC),X'00'           IS THIS ENTRY ZERO
         BNE   XCTLRTNE                 IF NOT ZERO, TRANSFER CNTRL
         LA    RWTGC,8(RWTGC)           ADDRESS OF NEXT ENTRY
         LA    RPARC,4(RPARC)           ADDRESS OF NEXT DCB
         B     ZCHECK                   TRANSFER CONTROL TO NEXT S22025
*                                       ENTRY                    S22025
*
XCTLRTNE EQU   *
         LA    RJ,DXCCW12-FORCORE(RJ)   ADDR OF A DOUBLE WORD    Y02027
*                                         SAVE AREA
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID TO NAME FIELD
         MVC   14(3,RWTG),2(RWTGC)      MOVE TTR TO WTG TABLE
         XCTL  DE=(RWTG),SF=(E,(15))
*
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        STANDARD LOAD SUBROUTINE. UPON ENTRY REGISTER 13 POINTS CLUP21
*        TO THE TWO-CHARACTER ID OF THE MODULE TO BE LOADED. THE CLUP21
*        APPROPRIATE ATTRIBUTES ARE SET AND THE LOAD SVC ISSUED. CLUP21
*        THE TWO-CHARACTER ID IS PLACED IN THE DEB AND THE COUNT CLUP21
*        OF LOADED MODULES INCREMENTED. UPON EXIT, REGISTERS 0   CLUP21
*        AND 13 CONTAIN THE ADDRESS OF THE LOADED ROUTINE.       CLUP21
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
LOADRTNE EQU   *
         MVC   6(2,RWTG),0(RC)          MOVE ID TO WTG TABLE
         LR    RE,RWTG                  LOAD THE ADDRESS OF THE
*                                         PARAMETER LIST
         L     RF,CVTPTR                GET ADDRESS OF CVT       Y02027
         L     RF,CVTSVDCB-CVTMAP(RF)   GET ADDRESS OF THE DCB   Y02027
*                                       FOR SVCLIB               Y02027
         LOAD  EPLOC=(0),DCB=(1)        LOAD THE ROUTINE         S21903
*
         MVC   0(2,RUCB),0(RC)          MOVE SUB-ROUTINE ID TO THE
*                                         DEB
         LA    RUCB,2(0,RUCB)           BUMP POINTER TO NEXT ID
*                                         POSITION IN THE DEB
         LA    RTIOT,1(0,RTIOT)         KEEP A COUNT OF ALL LOADED
*                                         SUBROUTINES
         ICM   RE,HI,IDEND              CLEAR HIGH ORDER BYTE    Y01004
         LR    RC,RF                    COPY LENGTH OF MODULE    Y01004
         SLL   RC,BLDOFF                SHIFT LENGTH TO HIGH     Y01004
*                                         ORDER BYTE             Y01004
         OR    RC,RE                    COPY MODULE ADDRESS      Y01004
         LR    RE,RC                    RETURN WITH MODULE ADDR  Y01004
         LA    RC,FOUR(RC)              RETURN WITH ADDR + FOUR  X02004
*                                         IN TWO REGISTERS       CLUP21
         BR    RD                       RETURN
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF CODE LOADS THE ATTENTION ROUTINE         *
*        AND THE SPECIAL CHARACTERS TABLE FOR 2260 LOCAL DEVICES.CLUP21
*        IT ALSO LOADS THE SPECIAL CHARACTERS TABLE FOR AUDIO    CLUP21
*        DEVICES.                                                CLUP21
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                CLUP21
LOAD2260 EQU   *
         LR    R4,RF                    SAVE UCB ADDR            S99228
         LA    RC,ATEN2260              LOAD THE ADDRESS OF THE
*                                         2260 LOCAL ATTENTION
*                                         ROUTINE
         BAL   RD,LOADRTNE              RETURN LINKAGE ESTABLISHED
*
         L     RC,CVTPTR                GET CVT ADDRESS          Y02027
         USING CVT,RC                   CVT ADDRESSABILITY       Y02027
         L     RJ,CVTAQAVT              GET TCX ADDRESS          Y02027
         USING IEDQTCX,RJ               TCX ADDRESSABILITY       Y02027
         ST    RE,TCXATTEN              SET ADDR OF ATTN RTN     Y02027
         OI    CVTTCMFG,LOCALBIT        SET BIT TO INDICATE      Y02027
*                                         THE PRESENCE OF A 2260
*                                         LOCAL DEVICE
         BAL   RD,FIXPAGE               FIX ATTENTION ROUTINE    X01004
         SPACE 2                                                 Y02027
         L     RJ,CVTAQAVT              GET TCX ADDRESS          Y02027
         LA    RD,IECITABT              GET TCAM ATTN INDEX      Y02027
         LH    RC,TCXASID               GET ASID OF TCAM         Y02027
         L     RJ,TCXATTEN              GET ATTN RTN ADDR        Y02027
         SR    R4,R4                    CLEAR FOR IC           @ZA04035
         IC    R4,DEBNMEXT              GET NO. OF EXTENTS     @ZA04035
UCBLOOP  EQU   *                                               @ZA04035
         SLL   R4,2                     MULT BY FOUR FOR INDEX @ZA04035
         L     RF,DEBUCBAD-4(R4)        GET UCB ADDRESS        @ZA04035
         LA    RD,IECITABT              GET TCAM ATTN INDEX    @ZA04035
         LA    RF,0(RF)                 CLEAR HI ORDER BYTE    @ZA04035
         SRL   R4,2                     RESTORE RLN            @ZA04035
         LTR   RF,RF                    DD DUMMY ENTRY         @ZA04035
         BZ    NXTUCB                   YES,SKIP               @ZA04035
*                                            ATTNTAB SETTING   @ZA04035
         LR    RE,RF                    SAVE GOOD UCB ADDR.    @ZA04035
         IOSGEN TP,VAR=ATNMOD,UCB=RF,TABLE=RD,REG=(RJ,RC)        Y02027
NXTUCB   EQU   *                                               @ZA04035
         BCT   R4,UCBLOOP               LOOP THRU ALL UCB'S    @ZA04035
         LR    R4,RE                    RESTORE LAST UCB ADDR  @ZA04035
         LA    RC,SPC2260L              ADDRESS OF 2260L SCT ID  CLUP21
         CLI   UCBUNTYP-UCBOB(R4),LOC3270 UNIT TYPE 3270L        Y02027
         BL    LOADIT                   NO-GO LOAD 2260 SCT      S99228
         LA    RC,SPC3270L              ADDR OF 3270L SCT ID     S99228
         B     LOADIT                   SET THE ATTRIBUTES       CLUP21
*                                                                CLUP21
AUDIOLD  EQU   *                                                 CLUP21
         LA    RC,AUDIOSPC              ADDRESS OF SPECIAL       CLUP21
*                                         CHARACTERS TABLE ID    CLUP21
*                                         FOR AUDIO DEVICES      CLUP21
         B     LOADIT                   LOAD AUDIO SCT           CLUP21
         EJECT
FIXPAGE  EQU   *                                                 X01004
         SLL   RF,THREE                 CONVERT TO BYTE LENGTH   X01004
         LR    RJ,RE                    GET START ADDRESS        Y02027
         LA    RJ,0(RF,RJ)              SET END ADDRESS          Y02027
         LR    RF,RE                    SET START ADDRESS        Y02027
         LA    RE,AVTDMECB              ADDRESS OF FIX ECB       X01004
         XC    AVTDMECB(FOUR),AVTDMECB  CLEAR ECB                X01004
*                                                                X01004
         PGFIX R,A=(1),EA=(15),ECB=(0)  ISSUE PAGE FIX           Y02027
*                                                                X01004
         LA    RF,AVTDMECB              ADDRESS OF FIX ECB       Y02027
         WAIT  ECB=(RF)                 WAIT FOR PAGE FIX        Y02027
*                                                                X01004
         TM    AVTDMECB+THREE,FOUR      CHECK RETURN CODE        X01004
         BCR   EIGHT,RD                 RETURN TO CALLER         X01004
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                 *
*        THIS SECTION OF CODE IS USED TO SAVE THE ID OF THE       *
*        CURRENT OPEN EXECUTOR AND TO MOVE THE ID OF THE          *
*        ERROR HANDLER INTO THE WHERE-TO-GO PARAMETER LIST        *
*                                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
WTGSETUP EQU   *
         MVI   AVTOPERT,TCAMER23        SET TCAM ERROR INDEX     X01004
         L     RCORE,FOUR(RWTGC)        LOAD THE ADDRESS OF THE
*                                         OPEN WORK AREA
         MVC   AVTOPXCL(TWO),AMIDCNST   MOVE THE ID OF THE CURRENT
*                                         EXECUTOR INTO THE AVT
*                                         FOR THE RETURN TO THIS
*                                         EXECUTOR
         MVC   0(FIVE,RWTGC),OPENERP    MOVE THE ID AND TTR FOR THE
*                                         ERROR HANDLER INTO THE
*                                         WHERE-TO-GO PARAMETER LIST
         B     XCTLRTNE                 BRANCH TO THE XCTL ROUTINE
         EJECT
CHARTBL  EQU   *                        TRANSLATE TABLE FOR      CLUP21
*                                         SPECIAL CHAR MODULES   CLUP21
         DC    X'00'                    INDEX TO 1030 CHARACTERS CLUP21
         DC    X'00'                    INDEX TO 1050 CHARACTERS CLUP21
         DC    X'00'                    INDEX TO 1050 FOLDED     S22025
         DC    X'00'                    INDEX TO 1060 CHARACTERS CLUP21
         DC    X'08'                    INDEX TO 2260R SCT       S21903
         DC    X'00'                    INDEX TO 2265 CHARACTERS S22025
         DC    X'00'                    INDEX TO 2740 CHARACTERS CLUP21
         DC    X'00'                    INDEX TO 2740 FOLDED     S22025
         DC    X'0E'                    INDEX TO WTTA SCT        S21903
         DC    X'0E'                    INDEX TO WTTA SCT        S21903
         DC    X'0A'                    INDEX TO 83B3 SCT        S21903
         DC    X'10'                    INDEX TO TWX SCT         S21903
         DC    X'00'                    RESERVED                 S22025
         DC    X'00'                    FOR                      S22025
         DC    X'00'                    FUTURE                   S22025
         DC    X'00'                    USE                      S22025
         DC    X'0A'                    INDEX TO 83B3 & 115A SCT S21903
         DC    X'10'                    INDEX TO TWX SCT         S21903
         DC    X'0C'                    INDEX TO TWX SCT         S21903
         DC    X'06'                    INDEX TO 6-BIT SCT       S21903
         DC    X'04'                    INDEX TO ASCHII SCT      S21903
         DC    X'02'                    INDEX TO EBCDIC SCT      S21903
         DC    X'00'                    INDEX TO 2741 CHARACTERS CLUP21
         DC    X'00'                    INDEX TO 2741 EBCDIC     S22025
         DC    X'00'                    INDEX TO 2741 CORRESP.   S22025
         DC    X'00'                    RESERVED FOR             S22025
         DC    X'00'                    FUTURE USE               S22025
         EJECT
AMIDCNST DC    C'39'                    ID OF THIS MODULE
OPIDCNST DC    C'0S'                    END OF WTG TABLE         S22025
TWO      EQU   2                        NUMERIC CONSTANT         Y02027
THREE    EQU   3                        NUMERIC CONSTANT         Y02027
FOUR     EQU   4                        NUMERIC CONSTANT         Y02027
FIVE     EQU   5                        NUMERIC CONSTANT         Y02027
EIGHT    EQU   8                        NUMERIC CONSTANT         Y02027
NINE     EQU   9                        NUMERIC CONSTANT         Y02027
TWELVE   EQU   12                       NUMERIC CONSTANT         Y02027
CHATRANS EQU   16                       OFFSET TO SPECIAL CHAR   Y02027
*                                       OFFSET TABLE             Y02027
SIXTEN   EQU   16                       NUMERIC CONSTANT         Y02027
TCAMER23 EQU   23                       TCAM ERROR ID            Y02027
DEBPTR   EQU   36                       LENGTH OF THE DEB PREFIX
IQE      EQU   100
IQETRCAD EQU   105
IQEIRBAD EQU   108
IQETCBAD EQU   113
CNTRLUNT EQU   X'03'                                             CLUP21
NOPCI    EQU   X'0C'                    TO TEST FOR NO PCI       CLUP21
GRAPHICS EQU   X'10'                    2260 LOCAL               Y02027
LOCALBIT EQU   X'40'                    2260 LOCAL SW. IN AVT    Y02027
PURGESIZ EQU   X'C0'                    FOR GETMAIN IN PURGE RTN CLUP21
SP252    EQU   X'FC'                    SUBPOOL 252 INDICATOR    CLUP21
LOC3270  EQU   X'09'                    09 OR 0A INDICATE LOCAL  S99228
*                                       3270 DEVICE              S99228
ONE      EQU   1                        CONSTANT OF ONE          X01004
LOW      EQU   3                        ICM/STCM MASK            X01004
AD       EQU   7                        ICM/STCM MASK            X01004
HI       EQU   8                        ICM/STCM MASK            X01004
BLDOFF   EQU   24                       SHIFT LENGTH             Y02027
         EJECT
SIOAPNDG DC    C'QE'                    ID OF SIO APPENDAGE      X01004
*                                       FOLLOWING MODULES < 1K   S22026
ATEN2260 DC    C'R5'                    2260L ATTENTION ROUTINE
IOTRACE  DC    C'Q0'                    I/O TRACE MODULE
SPC2260L DC    C'RV'                    2260L SPECIAL CHARACTERS CLUP21
SPC3270L DC    C'EM'                    3270 SPECIAL CHARS TBL   S99228
AUDIOSPC DC    C'RY'                    AUDIO SPECIAL CHARACTERS CLUP21
SPECIALC DC    C'RR'                    ID FOR SPECIAL CHARS     CLUP21
         DC    C'R7'                    EBCDIC TRANSMISSION CODE CLUP21
         DC    C'R8'                    ASCHII TRANSMISSION CODE CLUP21
         DC    C'R9'                    6 BIT  TRANSMISSION CODE CLUP21
         DC    C'RS'                    2260R  TRANSMISSION CODE CLUP21
         DC    C'RT'                    83B3 & 115A SCT ID       CLUP21
         DC    C'RU'                    TWX    TRANSMISSION CODE CLUP21
         DC    C'RW'                    WTTA   TRANSMISSION CODE CLUP21
         DC    C'RX'                    EVEN PARITY TWX TRANS C. S22025
PCIAPNDG DC    C'RN'                    ID OF PCI APPENDAGE      CLUP21
LINEEND  DC    C'R0'                    START/STOP & BSC APPNDG  S21903
         DC    C'Q2'                    BSC APPNDG               S21903
APNDGQ3  DC    C'Q3'                    START/STOP & TSO APPNDG  S21903
         DC    C'Q4'                    MINI APPNDG              S21903
APNDGQ5  DC    C'Q5'                    START/STOP APPNDG        S21903
TCAMLD6  DC    C'40'                    LAST LOAD OF OPEN
         DC    XL4'0'                   TTR AND LENGTH
OPENERP  DC    C'33'                    ID OF THE OPEN ERROR HANDLER
         DC    XL4'00'                    TTR AND LENGTH
IDEND    DC    X'0000'                  END OF TABLE             S22025
         EJECT                                                   Y02027
         TTCXD                                                   Y02027
         EJECT                                                   Y02027
UCB      DSECT                                                   Y02027
         IEFUCBOB
         EJECT                                                   Y02027
CVT      DSECT                                                   Y02027
         CVT
         EJECT                                                   Y02027
         IECDIOCM
         EJECT                                                   Y02027
         IECDATB
         EJECT
         TAVTD                          AVT DSECT
         EJECT
FORCORE  DSECT                                                   Y02027
         IECDSECT
         EJECT
         TDEBD
DEBSIOX  EQU   X'40'                    ENTER SIO APG FROM ERP   X02004
DEBIOVR  EQU   X'20'                    EXCPVR VALID FOR THIS DEBX02004
         DCBD  DSORG=TX
         END
