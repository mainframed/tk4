 TITLE 'IGG019HI -WRITE CHECK APPENDAGE'
IGG019HI CSECT
*          RELEASE OS/VS2-02 DELETIONS
*                                                               YM03876
*          RELEASE 19 DELETIONS
*          RELEASE 20 DELETIONS
*2774017400,023000,023200,023400,026200,026600,026800,028400,    S20201
*2774028600,028800,029400,029800,030800,031400,031600,031800,    S20201
*2774033000,041400,041600,041800,042200,042400,042800,043000,    S20201
*2774043200,043600,043800,045000,045200,045400,045600,045800,    S20201
*2774046800,047000,047200,047400,047600,049200,051400-051600,    S20201
*2774055000,055800,057000,057800,058000,058600,059000,059200,    S20201
*2774059600,061800,062000,063000,063800,064200,065400,065800,    S20201
*2774066000,066400,066800,067000,067900,068800,069000,069400,    S20201
*2774069800,070000,075200,076000,076400,077200,077600,078000,    S20201
*2774078200,078600,080600,083800-084000,084200-091000,091400-    S20201
*2774091600,091800-118000                                        S20201
*          RELEASE 21 DELETIONS
*                                                                S21045
*
*STATUS CHANGE LEVEL 005
*
*FUNCTION/OPERATION-
*   WRITE CHECKING IS DONE A SET AT A TIME. A SET IS DEFINED AS A     *
*   PORTION OF THE WRITE Q THAT IS COMMAND CHAINED TOGETHER.THE NUMBER*
*   OF CHANNEL PROGRAMS PER SET VARIES WITH THE TYPE OF RECORD BEING  *
*   PROCESSED. GENERALLY, THE FIRST SET IS WRITTEN AND THEN CHECKED   *
*   (READ BACK) BEFORE THE NEXT SET IS WRITTEN. IF A RECORD FAILS THE *
*   CHECK, IT IS REWRITTEN AND RECHECKED ONCE,                    17516
*   AFTER WHICH IT IS MARKED UNWRITEABLE. IF A RECORD COULD NOT BE    *
*   INITIALLY WRITTEN, NO ATTEMPT IS MADE TO REWRITE OR RECHECK IT.   *
*                                                                     *
*   WHEN A CHANNEL END COMPLETION RESULTS FROM WRITING A WRITE Q SET, *
*   THE CCW'S OF THE CHANNEL PROGRAMS ARE SET TO READ(CHECK). IF A    *
*   CHANNEL END COMPLETION RESULTS FROM THE CHECK, WRITE CHECKING FOR *
*   THIS SET WAS SUCCESSFUL.THE NEXT SET(IF ANY) IS THEN WRITTEN.     *
*                                                                     *
*   IF THE INITIAL WRITE WAS SUCCESSFUL BUT AN ABNORMAL END COMPLETION*
*   RESULTED ON THE CHECK, THAT RECORD IS REWRITTEN AND RECHECKED     *
*   ONCE.  IF IT STILL CANNOT BE WRITTEN, THE RECORD IS TAGGED    17516
*   UNWRITEABLE AND THE USER IS TOLD OF THIS WHEN HE ISSUES A GET. IF,*
*   DURING THE AFOREMENTIONED REWRITE RECHECK LOOP, ABNORMAL END      *
*   RESULTS ON THE REWRITE,THE RECORD IS MARKED,AND THE NEXT ELEMENT  *
*   OF THE SET(IF ANY) IS CHECKED (READ).IF IT WAS THE LAST ITEM OF   *
*   THE SET, THE NEXT WRITE Q SET(IF ANY) WILL BE WRITTEN.            *
*                                                                     *
*   IF ABNORMAL END COMPLETION RESULTS FROM THE ORIGINAL WRITE, THAT  *
*   PARTICULAR RECORD IS MARKED UNREACHABLE OR UNWRITEABLE(WHICHEVER  *
*   IT MAY BE). WRITING CONTINUES UNTIL IT IS END OF SET.THE          *
*   SUBSEQUENT CHECK IS DONE ONLY FOR THOSE UNMARKED RECORDS.         *
*                                                                     *
*ENTRY POINTS- THE APPENDAGE VECTOR TABLE ADDRESSES THE VARIOUS ENTRY *
*   POINTS TO MODULE 'IGG019HI'. THE SPECIFIC POINTS OF ENTRY ARE-    *
*        SISK1B1 - CHANNEL END ON A WRITE                             *
*        SISK0A3 - CHANNEL END ON A CHECK                             *
*        SISK1C2 - CHANNEL END ON A REWRITE                           *
*        SISK4G3 - CHANNEL END ON A RECHECK                           *
*                                                                     *
*        SISK2B2 - ABNORMAL END ON A WRITE                            *
*        SISK3B2 - ABNORMAL END ON A CHECK                            *
*        SISK4B2 - ABNORMAL END ON A REWRITE                          *
*        SISK3G4 - ABNORMAL END ON A RECHECK                          *
*                                                                     *
*INPUT- REGISTER 1- 12* ADDRESS                                       *
*       REGISTER 2- IOB ADDRESS                                       *
*       REGISTER 3- DEB ADDRESS                                       *
*       REGISTER 4- DCB ADDRESS                                       *
*       REGISTER 6 - BASE FOR REGISTER SAVE AREA                      *
*       REGISTER 7- UCB ADDRESS                                       *
*       REGISTER 12- WORK AREA ADDRESS                                *
*       REGISTER 14- RETURN TO IOS VIA IGG019HG                       *
*       REGISTER 15- MODULE BASE                                      *
*                                                                     *
*OUTPUT- SAME AS FOR INPUT                                            *
*                                                                     *
*EXTERNAL ROUTINES- N/A                                               *
*                                                                     *
*EXITS- NORMAL- RETURN TO IOS THROUGH IGG019HG                        *
*   ERROR-N/A                                                         *
*                                                                     *
*TABLES/WORK AREAS- DATA EXTENT BLOCK(DEB) DESCRIBED BY SISDEB DSECT  *
*   WORKAREA DESCRIBED BY SISWORK1 DSECT                              *
*   DATA CONTROL BLOCK DESCRIBED BY IHADCB DSECT                      *
*                                                                     *
*ATTRIBUTES- REENTRANT, READ ONLY                                     *
*                                                                     *
*NOTES- REGISTER USAGE IS CONSISTENT IN ALL SCAN MODE                 *
*   APPENDAGE MODULES                                                 *
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
NORMAL   EQU   0                                                  15924
ASYNCH   EQU   4                        BRANCH OFFSET TO ASYNCH  Y02072
*                                       SCHEDULING ROUTINE IN IGG019HG
EXCP     EQU   8                                                  15924
BYPASS   EQU   12                                                 15924
UNWRITE  EQU   X'04'                    UNWRITABLE RECORD        S20201
UNREACH  EQU   X'02'                    UNREACHABLE BLOCK        S20201
CCSTORE  EQU   X'01'                    REMEMBER CC BIT          S20201
K5       EQU   5                        CONSTANT                 S20201
K4       EQU   4                        CONSTANT                 S20201
K1       EQU   1                        CONSTANT                 S20201
L3       EQU   3                        LENGTH                   S20201
L1       EQU   1                        CONSTANT                 S20201
L4       EQU   4                        LENGTH                   S20201
L8       EQU   8                        LENGTH                   S20201
BADDSET  EQU   X'20'                    BAD DATA SET INDICATOR  YM03876
IOBOC    EQU   X'01'                    OUTPUT IOB COMPLETE BIT YM03876
WQERR    EQU   X'80'                    BAD WRITE BIT           YM03876
WAPBIT   EQU   X'40'                    WRITE APPEND WORK BIT   YM03876
ONES     EQU   X'FF'                    MASK OF ALL ONES        YM03876
         SPACE 4
         USING IHADCB,R4                                         S20201
         USING SISDEB,R3
         USING SISWORK1,R12
         USING SISCP22,R5                                        S20201
         USING *,R15
         EJECT
         DS    0F                                                     *
*                                     ************************
*                                     *                      *
*                                     *  CHAN END APP CODE C *
*                                     *                      *
*                                     ************************
SISCTK0  DC    A(SISK1B1-SISCTK0)      APP CODE = 8  CE WRITE
         DC    A(SISK0A3-SISCTK0)      APP CODE =C CE CHECK
         DC    A(SISK1C2-SISCTK0)      APP CODE = 10  CE REWRITE
         DC    A(SISK4G3-SISCTK0)      APP CODE = 14  CE RECHECK
         SPACE 2
         DC    A(SISK2B2-SISCTK0)      APP CODE = 8  AB END WRITE
         DC    A(SISK3B2-SISCTK0)      APP CODE = C  AB END CHECK
         DC    A(SISK4B2-SISCTK0)      APP CODE = 10 AB END REWRITE
         DC    A(SISK3G4-SISCTK0)      APP CODE = 14 AB END RECHECK
         SPACE 4
         USING *,R15
SISK1B1  B     SISK1B10                 FOR SETTING BASE
         SPACE 2
*                                  ***** CONSTANTS *****
CON3ZERO DC    X'000000'                3 BYTES OF ZERO
         DS    0H                                                S20201
CONO16   DC    AL2(CN3-CN1)             LENGTH RKP=0,UNBLOCKED   S20201
CON32    DC    AL2(CN5-CN1)             LENGTH OTHERWISE         S20201
TABOFF4  EQU   4                        OFFSET FOR EXIT EFFECTOR TABLE
SIXTEEN  EQU   16                       COMMUNICATION VECTOR TABLE
         SPACE 5
*  ENTRY POINT CHANNEL END ON CHECK
         SPACE 2
SISK0A3  TM    W1OSBIT3,X'20'          ANY ERRORS IN THIS SET
         BZ    SISK0A2                 NO
         SPACE
*                                      WHEN A SET HAS ERRORS THE CHECK
*                                      IS DONE 1 BUFFER AT A TIME
         SPACE
         L     R5,W1COMADO             COMADO MUST POINT TO CN5--C.E.
         SH    R5,CON32                BRING R5 TO CN1
         TM    CN2+K5,CC               SAVE CC BIT ON            S20201
         BZ    SISK0A2                 NO
         OI    CN4+K4,CC               SET CN4 CC BIT BACK ON    S20201
SISK0A3A L     R5,CN5                  GET NEXT CP ADDR          S20201
         B     SISK1G4                 GO TO CHECK NEXT CP
         SPACE 2
*        THE CC BIT IS SET BACK ON IF THIS CHAN PROG PASSED THE CHECK
*   AND WAS NOT THE LAST ONE OF THE SET IN ERROR.
         SPACE 3
*  END OF SET, RESET SKIP BITS
         SPACE
SISK0A2  L     R5,W1FCPS                POINT TO CP1 OF SET
SISK0B2  NI    CN3+K4,X'FF'-SKIP       SET OFF SKIP BIT IN CN3   S20201
         NI    CN4+K4,X'FF'-SKIP       SET OFF SKIP BIT IN CN4   S20201
         TM    CN4+K4,CC               LAST CP OF SET            S20201
         BZ    SISK0C2                 YES
*                                      NO
         L     R5,CN5                  GET NEXT CHAN PROG ADDR   S20201
         B     SISK0B2                 CHANGE NEXT CHANNEL PROGRAM
SISK0C2  CLC   CON3ZERO,CN5+K1          LAST SET ON WRITE Q      S20201
SISK0D2  BE    SISK0D1                  YES
         SPACE 2
*        NOT LAST SET, WRITE NEXT SET
         SPACE 2
SISK0D3  MVC   W1FCPS+K1(L3),CN5+K1    SAVE CHAIN ADD IN FCPS    S20201
SISK0D4  MVI   W1OEXTEN+1,X'08'        SET APP CODE TO WRITE
         SPACE 2
         NIL   W1OSBIT3,ONES-BADDSET,REF=W1OSBIT1, SET OFF BAD  YM03876X
               WREGS=(R9,R10,R13)       DATA SET INDICATOR      YM03876
         SPACE 2
SISK0E3  MVC   W1OCPS+1(3),CN5+1       SET OCPS TO NEXT CP       S20201
         L     R5,CN5                  LOAD CN1 OF NEXT SET      S20201
SISK0F3  MVC   W1ODAD,CN6              SET IOBO TO NEXT CP CN6   S20201
         SPACE 2
*        IS NEXT SET ON SAME DEVICE
         SPACE 2
SISK0G3  LA    R11,DEBNIEE             LOAD R11 WITH M=0 ADDRESS
         SR    R8,R8                   CLEAR WORK REG
         IC    R8,CN6                  M FROM CN6                S20201
         SLL   R8,4                    M X 16
         ALR   R11,R8                  GET ADDR OF M EXTENT
         L     R11,0(R11)               GET UCB ADDR
         LA    R11,0(R11)              CLEAR HIGH ORDER BYTE
         LA    R7,0(R7)                    OF REGS
         CR    R7,R11                  IS IT SAME DEVICE
         BE    SISK1G3                 YES, TAKE EXCP RETURN
SISSK0H3 EQU   *                                                 Y02072
         B     ASYNCH(,R14)             BRANCH TO EXIT ROUTINE   Y02072
*                                       IN IGG019HG
         SPACE 3
*        ALL BUFFERS WRITTEN AND CHECKED
         SPACE 2
SISK0D1  OIL   W1OSBIT1,IOBOC,REF=W1OSBIT1, SET ON OUTPUT IOB   YM03876X
               WREGS=(R9,R10,R13)       COMPLETION BIT          YM03876
         SPACE 2
         NIL   W1OSBIT3,ONES-BADDSET,REF=W1OSBIT1,  SET OFF BAD YM03876X
               WREGS=(R9,R10,R13)       DATA SET INDICATOR      YM03876
         SPACE 2
SISK0E1  TM    W1OSBIT2,X'40'      WRITE Q WORK BIT ON  (BIT9 OF OSB)
         BO    SISK0E1A            YES
SISK0F1  EQU   *                                                 Y02072
         LA    R10,NORMAL               LOAD IOS RET ADDR OFST   Y02072
         BR    R14                      RETURN TO IOS VIA EXIT   Y02072
*                                       ROUTINE IN IGG019HG
         SPACE 2
SISK0E1A OIL   W1OSBIT2,WQERR,REF=W1OSBIT1, SET ON BAD          YM03876X
               WREGS=(R9,R10,R13)       WRITE QUEUE BIT         YM03876
         SPACE 2
         NIL   W1OSBIT2,ONES-WAPBIT,REF=W1OSBIT1, SET OFF WRITE YM03876X
               WREGS=(R9,R10,R13)       Q WORK BIT              YM03876
         SPACE 2
         B     SISK0F1                  TAKE NORMAL RETURN TO IOS
         EJECT
SISCTK1  EQU   *
*                                     ******************************
*                                     *                            *
*                                     *  CHAN END APP CODES 8, 10  *
*                                     *                            *
*                                     ******************************
         SPACE 2
* ENTRY POINT CHANNEL END ON WRITE
         SPACE 2
SISK1B10 MVI   W1OEXTEN+1,X'0C'        SET APP. CODE TO C-CHECK
SISK1B2  MVC   W1OCPS,W1FCPS            SET CHAN PROG POINTER TO FCPS
SISK1B3  L     R5,W1FCPS               SET R5 TO FCPS
         TM    W1OSBIT3,X'20'          ANY BAD WRITES ON THIS SET
         BO    SISK1G4                 YES
SISK1B4  NI    CN1,X'FF'-MT            SET OFF MT BIT IN THIS CP S20201
SISK1C3  OI    CN4+K4,SKIP             SET ON SKIP BITS IN CN4   S20201
SISK1D3  CLI   CN3,TIC                 DOES CN3 = TIC            S20201
         BE    SISK1E3                 YES, USING CP22B
SISK1D2  OI    CN3+K4,SKIP             NO, SET SKIP IN CN3(22A)  S20201
SISK1E2  MVI   CN3,RKD                 SET CN3 TO READ KD        S20201
         B     SISK1F3
SISK1E3  MVI   CN4,RDATA               SET CN4 TO RD DATA--22B   S20201
SISK1F3  TM    CN4+K4,CC               IS CC BIT OF CN4 ON       S20201
         BZ    SISK1G3                 OFF, END OF THIS SET      S20201
*                                      ON, NOT END OF SET
SISK1F4  L     R5,CN5                   GET ADDR OF NEXT CP      S20201
         B     SISK1C3                  GO BACK FOR NEXT CP      S20201
         SPACE 3
*        SINCE AT LEAST 1 BUFFER OF THIS SET HAD AN ERROR, THE CHECKING
*        IS DONE 1 AT A TIME.  THE BUFFER IN ERROR CANT BE CHECKED
*        BECAUSE THE CN6 HAS BEEN USED TO STORE IO STATUS INFORMATION
         SPACE 2
SISK1G4  TM    CN2+K4,UNWRITE+UNREACH   WAS CP IN ERROR          S20201
         BM    SISK1G5                  YES                      S20201
*                                       NO, CHECK IT
SISK1G4A MVC   CN2+K5(L1),CN4+K4        SAVE CN4 CC BIT IN CN2   S20201
         NI    CN4+K4,X'FF'-CC          SET OFF CC BIT IN CN4    S20201
         B     SISK1B4                  GO TO SET CCWS FOR CHECKING
         SPACE 2
* BAD BUFFER
         SPACE
SISK1G5  TM    CN4+K4,CC                END OF SET               S20201
         BZ    SISK0C2                  YES, GO WRITE NEXT SET   S20201
         L     R5,CN5                   GET NEXT CP ADDR         S20201
         ST    R5,W1OCPS                SET OCPS TO NEXT CP      S20201
         MVC   W1ODAD,CN6               SET IOB SEEK TO CN6      S20201
         B     SISK1G4                  LOOP BACK TO TEST THIS CP
         SPACE 3
SISK1G3  EQU   *                                                 Y02072
         NI    W1OF1,X'FA'
         MVI   W1OF2,X'00'             RE_INITIALIZATION
         MVI   W1OCSW,X'00'            FOR EXCP RETURN
         MVC   W1OERRCT,CON3ZERO                                 S20201
         SR    R13,R13                 CLEAR WORK REGISTER
         IC    R13,W1OMBBCC            R13=000M
         SLL   R13,4                   M X 16
         LA    R13,32(R3,R13)          R13 POINTS TO EXTENT OF M
         MVC   W1OMBBCC+1(2),4(R13)    MOVE BB FROM DSB INTO IOBO
         XC    W1IF2(3),W1IF2           CLEAR FLAG2,SENSE BYTES   15924
         XC    W1ICSW(9),W1ICSW         CLEAR FLAG3,CSW,SIOCC     15924
         XC    W1IERRCT(2),W1IERRCT     CLEAR ERROR COUNT         15924
         NI    W1IF1,X'C2'              RESET FLAG1               15924
SISK1G3A EQU   *                                                 Y02072
         LA    R10,EXCP                 LOAD IOS RET ADDR OFST   Y02072
         BR    R14                      RETURN TO IOS VIA EXIT   Y02072
*                                       ROUTINE IN IGG019HG
         SPACE 2
* ENTRY POINT CHANNEL END  ON REWRITE
         SPACE 2
SISK1C2  MVI   W1OEXTEN+1,X'14'        SET APP CODE TO RECHECK
         L     R5,W1WR1ST               PT TO FIRST WRITE        S20201
         B     SISK1B4                  CHANGE CCWS FOR          S20201
*                                       RECHECKING               S20201
         EJECT
SISCTK2  EQU   *
*                                     ***************************
*                                     *                         *
*                                     *  ABNORMAL END APP CODE 8*
*                                     *                         *
*                                     ***************************
         SPACE 2
* ENTRY POINT ABNORMAL END ON WRITE
         SPACE 2
SISK2B2  TM    W1OSENSE+2,X'3F'     PERM ERROR
         BO    SISK0F1                  NO
*
*        THE FOLLOWING LOGIC ISOLATES THE CCW OF THE CP IN ERROR
*        IT TAGS THE BUFFER ACCORDINGLY.
*
SISK2C2  L     R5,W1COMADO             LOAD ERROR CCW+8 TO R5
         LA    R5,0(R5)                CLEAR HIGH ORDER BYTE    SA66577
         SH    R5,EIGHT                BRING R5 TO ERROR CCW
         L     R10,W1OCPS              ADDR FIRST CCW IN CP     SA66577
         LA    R10,0(R10)              CLEAR HIGH ORDER BYTE    SA66577
         CR    R5,R12                  ERROR CCW ADDR BELOW     SA66577
*                                      SCAN WORK AREA ADDRESS   SA66577
         BL    SISK2D2                 BR YES-ERROR IN IOS CCW  SA66577
         CR    R5,R10                  ERROR CCW FIRST IN CP    SA66577
         BNE   SISK2D2A                BRANCH IF NO             SA66577
SISK2D2  LR    R5,R10                  CHANNEL PGM START ADDR   SA66577
         CLI   CN1,SETSECT              RPS FAILURE              S20201
         BE    RPSFAIL1                 YES MARK THIS BUFFER BAD S20201
         B     SISK2H2A                ADDR CN1 IN REG5         SA66577
SISK2D2A EQU   *                       TEST FOR STOP ON CN3/4   SA66577
         CLI   0(R5),WKD               DID THE CP STOP ON A CN3  S20201
         BNE   SISK2C2A                NO
         SH    R5,CONO16               BRING R5 TO CN1
         B     SISK2H2B
SISK2C2A CLI   0(R5),WD                DID CP STOP ON A CN4      S20201
         BNE   SISK2H2A                NO
*
*        IF CP DID NOT STOP ON CN3 OR CN4, MUST BE AT CN1
*
         SH    R5,TWENTY4              SET R5 TO CN1
SISK2H2B TM    W1OSENSE,X'08'          DID CP STOP ON DATA CHECK
         BZ    SISK2H2A                NO
SISK2C2B OI    CN2+K4,UNWRITE          MARK THIS BUFFER          S20201
*                                       UNWRITABLE               S20201
*  IF CP STOPPED ON A DATA CHECK ON A WRITE COMMAND,
*   IT IS MARKED UNWRITABLE
*
         SPACE 1
SISK2E2  OIL   W1OSBIT2,WAPBIT,REF=W1OSBIT1,  SET WRITE Q WORK  YM03876X
               WREGS=(R9,R10,R13)       WORK BIT ON             YM03876
         SPACE 2
         MVC   CN5+K4(L4),W1OF1        MOVE IOB FIELDS TO CP     S20201
         MVC   CN6,W1OCSW              FOR ERROR EXIT TO USER    S20201
         SPACE 2
         OIL   W1OSBIT3,BADDSET,REF=W1OSBIT1,  SET ON BAD       YM03876X
               WREGS=(R9,R10,R13)       DATA SET INDICATOR      YM03876
         SPACE 2
SISK2F2  NI    W1OF1,X'FB'             SET OFF IOB EXCEPTION BIT
         TM    CN4+K4,CC               IS THIS LAST BUFF OF SET  S20201
         BZ    SISK1B1                 YES, GO TO CHECK BUFFERS
SISK2G3  L     R8,CN5                  GET NEXT CHAN PROG IN SET S20201
         DROP  R5                                                S20201
         USING SISCP22,R8                                        S20201
         NI    CN1,X'FF'-MT            SET OFF ITS MT BIT        S20201
         DROP  R8                                                S20201
         USING SISCP22,R5                                        S20201
         B     SISK0E3                 GO TO WRITE THE REST OF THE SET
*
*   RPS FAILURES DON'T POINT TO CP SO PT AHEAD
*
RPSFAIL1 EQU   *                        *                        S20201
         L     R5,CN2                   PT TO A BUFFER           S20201
SISK2H2A OI    CN2+K4,UNREACH          MARK BUFFER UNREACHABLE   S20201
         B     SISK2E2
         EJECT
SISCTK3  EQU   *
*                                      ********************************
*                                      *                              *
*                                      * ABNORM END APP CODES C AND 14*
*                                      *                              *
*                                      ********************************
         SPACE 2
*                                  ***** CONSTANTS *****
EIGHT    DC    H'08'                                             S20201
TWENTY4  DC    AL2(CN4-CN1)                                      S20201
         SPACE 3
* ENTRY POINT ABNORMAL END ON CHECK
         SPACE 2
SISK3B2  L     R5,W1COMADO             LOAD ERROR CCW+8 TO R5
         LA    R5,0(R5)                CLEAR HIGH ORDER BYTE    SA66577
         L     R10,W1OCPS              CHANNEL PGM START ADDR   SA66577
         LA    R10,0(R10)              CLEAR HIGH ORDER BYTE    SA66577
         CR    R5,R12                  ERROR CCW ADDR BELOW     SA66577
*                                      SCAN WORK AREA ADDRESS   SA66577
         BL    SISK3SS                 BR YES-ERROR IN IOS CCW  SA66577
         LR    R9,R5                   ADDR ERROR CCW+8         SA66577
         SH    R9,EIGHT                ADDRESS OF ERROR CCW     SA66577
*
         CR    R9,R10                  ERROR CCW FIRST IN CP    SA66577
         BNE   SISK3A1                 BR NO-TEST IF ON A TIC   SA66577
SISK3SS  LR    R5,R10                  CHANNEL PGM ADDRESS      SA66577
         CLI   CN1,SETSECT             FIRST CCW SET SECTOR     SA66577
         BNE   SISK3B3                 BRANCH IF NOT SET SECTOR SA66577
         L     R5,CN2                  ADDRESS OF FIRST BUFFER  SA66577
         B     SISK3B3                 TEST FOR PERM ERROR      SA66577
SISK3A1  CLI   0(R5),TIC               DID CP STOP ON A TIC     SA66577
         BNE   SISK3B2B                NO, MUST HAVE STOPPED AT CN4
*                                      YES
SISK3B2A LR    R5,R9                   R5 TO EITHER CN1 OR CN4  SA66577
         CLI   0(R5),SIDEQ             R5= CN1--MT BIT OFF       S20201
         BE    SISK3B3                 THEN ERROR AT CN1
         CLI   0(R5),SIDEQ+MT          R5= CN1--MT BIT ON        S20201
         BE    SISK3B3                 THEN ERROR AT CN1
*                                      STOPPED ON CN4
SISK3B2B SH    R5,TWENTY4              BRING R5 TO CN1
SISK3B3  TM    W1OSENSE+2,X'3F'        PERMANENT ERROR
         BO    SISK0F1                  NO, LET IOS RETRY
SISK3C3  TM    CN4+K4,CC               IS CC BIT OF THIS CP ON   S20201
         BZ    SISK3C3B                NO
SISK3C3C NI    CN4+K4,X'FF'-CC         IF ON , SET OFF CC OF CN4 S20201
         OI    CN2+K4,CCSTORE          IF CC ON SET ON BIT 7     S20201
         B     SISK3D3
SISK3C3B TM    CN2+K5,CC               IS SAVE CC BIT ON         S20201
         BO    SISK3C3C                YES
SISK3C3A NI    CN2+K4,X'FF'-CCSTORE    SET OFF 'REMEMER CC  BIT' S20201
SISK3D3  NI    CN1,X'FF'-MT            SET MT BIT OF THIS CP OFF S20201
         SPACE 2
*        RETRY                                                    17516
         SPACE
SISK3D4  MVC   W1ODAD,CN6              SET ODAD TO CN6 OF ERR CP S20201
         ST    R5,W1OCPS
SISK3E4  MVI   W1OEXTEN+1,X'10'         SET APP CODE TO REWRITE
         L     R5,W1OCPS               RELOAD FOR REWRITING
SISK3E3  NI    CN4+K4,X'FF'-CC-SKIP     SET CC AND SKIP BITS OFF S20201
SISK3F3  CLI   CN3,TIC                  22B                      S20201
         BNE   SISK3F2                  NO , CP22A'S ARE ON Q
SISK3G3  MVI   CN4,WD                   SET CN4=WRITE DATA(22B)  S20201
         B     SISK1G3                  TAKE EXCP RETURN (REWRITE)
SISK3F2  NI    CN3+K4,X'FF'-SKIP        SKIP IN CN3(CP22A) OFF   S20201
         MVI   CN3,WKD                  SET CN3 TO WRTE KD       S20201
         SPACE 2
SISK3H3  B     SISK1G3                  TAKE EXCP RETURN (REWRITE)
         SPACE 2
* ENTRY POINT ABNORMAL END ON RECHECK
         SPACE 2
SISK3G4  TM    W1OSENSE+2,X'3F'        PERM ERROR                 17516
         BO    SISK0F1                 NO, LET IOS RETRY          17516
         BE    SISK4C2                 YES, GO MARK BUFFER        17516
*                                          UNWRITEABLE            17516
         EJECT
SISCTK4  EQU   *
*                                ********************************
*                                *                              *
*                                * AE APP CODE 10,CE APP CODE 14*
*                                *                              *
*                                ********************************
         SPACE 2
* ENTRY POINT ABNORMAL END ON REWRITE
         SPACE 2
SISK4B2  TM    W1OSENSE+2,X'3F'       PERM ERROR
         BO    SISK0F1                 NO
SISK4C2  L     R5,W1COMADO
         LA    R5,0(R5)                CLEAR HIGH ORDER BYTE    SA66577
         SH    R5,EIGHT                BRING R5 TO ERROR CCW
*
         L     R10,W1OCPS              CHANNEL PGM START ADDR   SA66577
         LA    R10,0(R10)              CLEAR HIGH ORDER BYTE    SA66577
         CR    R5,R12                  ERROR CCW ADDR BELOW     SA66577
*                                      SCAN WORK AREA ADDRESS   SA66577
         BL    SISK4A1                 BR YES-ERROR IN IOS CCW  SA66577
         CR    R5,R10                  ERROR CCW FIRST IN CP    SA66577
         BNE   SISK4A2                 BRANCH IF NO             SA66577
SISK4A1  LR    R5,R10                  CHANNEL PGM START ADDR   SA66577
         CLI   CN1,SETSECT             FIRST CCW SET SECTOR     SA66577
         BNE   SISK4C2D                BR NO-MARK UNWRITABLE    SA66577
         L     R5,CN2                  ADDRESS OF FIRST BUFFER  SA66577
         B     SISK4C2D                MARK BUFFER UNWRITABLE   SA66577
SISK4A2  EQU   *                        FIND WHICH CCW IN ERROR SA66577
         CLI   0(R5),WKD               CP STOP ON A CN3 (WR KD)  S20201
         BNE   SISK4C2B                NO
SISK4C2C SH    R5,CONO16               BRING R5 TO CN1
         B     SISK4C2D
SISK4C2B CLI   0(R5),RKD               CP STOP ON A RD KD (CN3)  S20201
         BE    SISK4C2C                YES
SISK4C2A CLI   0(R5),WD                CP STOP ON A CN4  (WR D)  S20201
         BNE   SISK4C2E                DID NOT STOP AT WR DATA
SISK4C2F SH    R5,TWENTY4              BRING R5 TO CN1
         B     SISK4C2D
SISK4C2E CLI   0(R5),RDATA             CP STOP ON A CN4 (RD      S20201
*                                       DATA)                    S20201
         BE    SISK4C2F                YES
SISK4C2D OI    CN2+K4,UNWRITE          MARK BUFFER UNWRITABLE    S20201
         SPACE 2
         OIL   W1OSBIT2,WAPBIT,REF=W1OSBIT1,  SET ON WRITE Q    YM03876X
               WREGS=(R9,R10,R13)       WORK BIT                YM03876
         SPACE 2
         MVC   CN5+4(4),W1OF1          MOVE IOB FIELDS           S20201
         MVC   CN6,W1OCSW              INTO CHAN. PROG FOR USER  S20201
SISK4F2  NI    W1OF1,X'FB'             SET OFF IOB EXCEPTION BIT
SISK4G2  TM    CN2+K4,CCSTORE          'REMEMBER' CC BIT ON{     S20201
         BO    SISK4G2A                YES
         B     SISK0A2         GO TO WRITE NEXT SET
*              THE SETC4 SUB ROUTINE HAS PREV. INITIALIZED
*              ALL CP'S ON WRITE Q TO WRITE CCW'S
         EJECT
* ENTRY POINT CHANNEL END ON RECHECK
         SPACE 2
SISK4G3  L     R5,W1OCPS               GET ADDR OF RECHECKED CHAN PROG
         CLI   CN1,SETSECT              RPS PREFIX               S20201
         BNE   SISK4G2                  NO - PTR OK              S20201
         L     R5,CN2                   CORRECT PTR              S20201
         B     SISK4G2
SISK4G2A OI    CN4+K4,CC               IF FLAG ON, SET ON CN4 CC S20201
         MVI   W1OEXTEN+1,X'0C'         SET APP. CODE TO C (CHECK)
         TM    W1OSBIT3,X'20'          ANY WRITE ERRORS ON THIS SET
         BO    SISK0A3                 YES
         B     SISK0E3                  CONTINUE CHECKING
*
*   IF THERE WERE NO ERRORS ON THIS SET, CHECKING IS CONTINUED SINCE
*   THE CHAN. PROGS. HAVE ALREADY BEEN SET TO READ.IF ERRORS OCCURRED,
*   THE REMAINING CHAN. PROGS. MUST BE CHANGED TO READ BEFORE CHECKING
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
         EJECT
SISCP22  DSECT
         IGGCP22
         EJECT
SISDEB   IGGDEBD
         EJECT
SISWORK1 IGGSCAN
         EJECT
         DCBD  DSORG=(IS)
         END
