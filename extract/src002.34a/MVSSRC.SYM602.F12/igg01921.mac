         TITLE 'IGG01921 - LOAD OPEN,GETMAIN LOAD MODE WORK AREA'
IGG01921 CSECT                                                     DM0E
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG01921                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, INITIAL OR RESUME LOAD MODE           *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = GET CORE FOR LOAD MODE WORK AREA (WA) FROM SUBPOOL 250.  *
*            ZERO WORK AREA CORE.  INITIALIZE THE FOLLOWING:  WA ADDR *
*            IN DCBWKPT1; WA SIZE IN ISLVPTR8; BCT ADDR IN ISLVPTR3;  *
*            AREA Y ADDR IN ISLVPTR1; KEYSAVE ADDR IN ISLVPTR2; VPTRS *
*            ADDR IN DCBWKPT6; RECORD OVERHEAD IN ISLLGAP AND ISLIGAP;*
*            ECB ADDR'S IN IOBS; ECB COMPLETION CODES; FOR INITIAL    *
*            LOAD ONLY, DCBNREC, DCBST, DCBHIIOV, DCBHIRPD, DCBHIROV, *
*            AND DCBHIIOV.                                            *
*                                                                     *
* NOTES = NONE                                                        *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 944 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG01921                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0192C FOR  *
*              INITIAL OR RESUME LOAD MODE ONLY.  RECEIVES CONTROL IN *
*              STORAGE PROTECT KEY 5 AND PRIVILEGED STATE.            *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG0192D FOR INITIAL LOAD, *
*               IGG01920 FOR RESUME LOAD WITH FIXED LENGTH RECORDS    *
*               (FLR) OR IGG01950 FOR RESUME LOAD WITH VLR.           *
*                                                                     *
* EXIT-ERROR = ABEND CODES:                                           *
*              002 - BLKSIZE LESS THAN LRECL                          *
*                  - BLKSIZE NOT AN EVEN MULTIPLE OF LRECL            *
*                  - BLKSIZE TOO BIG FOR TRACK                        *
*                  - LRECL + OVERFLOW LINK TOO BIG FOR TRACK          *
*                                                                     *
*              03B - DCB NOT OPENED FOR OUTPUT                        *
*                  - LRECL = 0                                        *
*                  - KEYLENGTH NOT SPECIFIED                          *
*                  - RKP + KEYLENGTH GREATER THAN LRECL               *
*                  - FOR VLR ONLY, RKP LESS THAN 4                    *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY, DEB AND CVT                           *
*                                                                     *
* TABLES = DEVICE TABLE, AND BUFFER CONTROL TABLE                     *
*                                                                     *
* MACROS = MODESET, GETMAIN, ABEND AND XCTL.                          *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
FORCORE  DSECT
         IECDSECT
         EJECT
ISLCOMON IGGLOAD
         EJECT
IHADEB   IGGDEBD
         DCBD  DSORG=(IS)
IGG01921 CSECT                                                     DM0E
R0       EQU   0                        WORK REGISTER
R1       EQU   1                        WORK REGISTER
R4       EQU   4                        WORK REGISTER            O19113
R5       EQU   5                        WORK REGISTER            O19113
R6       EQU   6                        WORK REGISTER            O19113
R7       EQU   7                        WORK REGISTER            O19113
R8       EQU   8                        WORK REGISTER            O19113
R9       EQU   9                        WORK REGISTER            S20201
R10      EQU   10                       WORK REGISTER
RDEB     EQU   12                       BASE REG FOR USERS DEB   O19113
RE       EQU   0                        WORK REGISTER
RF       EQU   1                        WORK REGISTER
RDCB     EQU   2                        BASE REG FOR DCB
RBASE    EQU   3                        BASE REGISTER FOR THIS MODULE
RCORE    EQU   4                        BASE REG FOR OPEN WORK AREALE
RPAR     EQU   5    *                   ADDRESS OF PARAMETER TABLE
RWTG     EQU   6    *                   ADDRESS OF WHERE-TO-GO TABLE
RPARC    EQU   7    *                   CURRENT PARAMETER ENTRY
RWTGC    EQU   8    *                   CURRENT WHERE-TO-GO ENTRY
RTIOT    EQU   9                        ADDRESS OF TIOT
R11      EQU   11                       WORK REGISTER
RC       EQU   13                       WORK REGISTER
RD       EQU   14                       WORK REGISTER
RJ       EQU   15                       WORK REGISTER
CVTPTR   EQU   16                                                O19113
CVTZDTAB EQU   64                       OFFSET TO DEV CHAR TABLE S20201
UCBTYPE  EQU   19                       OFFSET FOR TYPE OF       O19113
*                                       DEVICE                   O19113
YLEN     EQU   72                       BASIC LENGTH AREA Y      S20201
K8       EQU   8                        CONSTANT                 S20201
K10      EQU   10                       CONSTANT                 S20201
TRKL     EQU   4                        TRACK LENGTH DEV TBLE    S20201
K0       EQU   0                        CONSTANT                 S20201
L5       EQU   5                        LENGTH                   S20201
ZEUSSW   EQU   9                        DEV TBLE ZEUS SW         S20201
ZEUS     EQU   X'08'                    ZEUS INDICATOR           S20201
LIDO     EQU   7                        LAST RECORD OVERHEAD     S20201
NIDO     EQU   6                        NORMAL RECORD OVERHEAD   S20201
DEVT     EQU   ISLAREAZ+86              INDEX DEVICE TYPE SA     S20201
COMPLETE EQU   X'40'                    IOB COMPLET SW           A42181
RC2C     EQU   X'2C'                    ISAM 002 ABEND RET CODE  Y02072
FIXED    EQU   X'80'                                             A34959
VIRTUAL  EQU   X'80'                    VIRTUAL INDICATOR
K1       EQU   1                        CONSTANT
*  COMMON WORK AREA EXCLUDING VARIABLE POINTERS AND  VARIABLE AREAS
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         USING IHADCB,RDCB
         USING FORCORE,RCORE
         USING IHADEB,RDEB                                       O19113
         L     RDCB,0(RPARC)            RDCB = ADR OF DCB
         L     RCORE,4(RWTGC)           RCORE = ADR OF OPEN WK AREA
         ST    RCORE,DCBRELSE           SAVE OPEN W/A ADDR       Y02072
*                                                                  DM0E
* GET CODE FOR LOAD MODE WORK AREA                                 DM0E
*                                                                  DM0E
ISL04A5  EQU   *                   GET CORE FOR LOAD MODE WORK AREA
         L     RDEB,DCBDEBAD            SET UP BASE FOR DEB       17321
         MVC   DCBWKPT2(3),ISLRETRY     RETRY COUNT FOR 3 IOBS   A42170
         TM    DEBOPATB,X'0F'           DCB OPENED FOR OUTPUT    O19113
         BNO   ABEND                    NO--ABEND                A46632
         LH    RC,DCBLRECL              GET LRECL VALUE          A46632
         LTR   RC,RC                    LRECL EQUALS ZERO ?      A46632
         BZ    ABEND                    YES - ABEND 03B          A46632
         TM    DCBRECFM,X'10'           TEST FOR BLOCKED RECORDS A38145
         BZ    BLKOK                    BR IF NOT TO CONTINUE    A38145
         SR    R10,R10                                           A38145
         LH    R11,DCBBLKSI             GET BLKSIZE VALUE        A38145
         CR    R11,RC                   BLKSIZE LT LRECL         A38145
         BL    TOOLONG                  BR IF LOW TO ABEND       Y02072
         TM    DCBRECFM,X'80'           TEST FOR FIXED           A38145
         BZ    BLKOK                    BR IF NOT TO CONTINUE    A38145
         DR    R10,RC                   TEST FOR MULTIPLE        A38145
         LTR   R10,R10                  IF REMAINDER, NOT        A38145
*                                       MULTIPLE                 A38145
         BNZ   TOOLONG                  WILL ABEND               Y02072
BLKOK    EQU   *                                                 A38145
         SR    RE,RE                                             A30791
         IC    RE,DCBKEYLE              WAS KEYLENGTH SPECIFIED  A30791
         LTR   RE,RE                                             A30791
         BZ    ABEND                    NO, ABEND                A30791
         CLC   DCBRKP,ZERO              IS RKP = 0               A34920
         BNE   TESTRKP                  NO, GO TEST IT           A34920
         TM    DCBRECFM,X'50'           NEITHER VAR NOR BLOCKED  A34920
         BZ    RKPOK                    RECFM=F,RKP=0--NO TEST   A34920
TESTRKP  AH    RE,DCBRKP                ADD RKP TO THE KEYLENGTH A34920
         CH    RE,DCBLRECL              COMPARE RKP+KEYLE TO     A30791
         BH    ABEND                    DCBLRECL AND ABEND IF HI A30791
         TM    DCBRECFM,X'80'           IS IT VLR                A30791
         BO    RKPOK                    NO, BRANCH               A30791
         NI    DCBOPTCD,X'FF'-FTIW      FTIW ILLEGAL VLR         S20201
FTIW     EQU   X'40'                    FTIW INDICATOR           S20201
         CLC   DCBRKP,FOUR              FOR VLR, RKP CAN'T BE    A30791
         BL    ABEND                    LESS THAN FOUR           A30791
RKPOK    EQU   *                                                 A30791
         LA    RE,250                   FROM SUBPOOL 250
         SLL   RE,24
         SR    R11,R11                                           S20201
         SR    R10,R10                  CLEAR REGISTER
         IC    R11,DCBKEYLE             R11 = KEYSAVE AREA       S20201
         LA    RC,YLEN(R0,R11)          RC = CORE FOR AREA Y     S20201
         IC    R10,DCBBUFNO
         SLL   R10,2                   R10=BUF CTRL TBL =8 + 4(BUFNO)
         LA    R10,K8(R0,R10)           CORE REQ'D IS WKAREA +   S20201
         LA    R9,ISLCSIZE(R0,R11)      KEYLE + AREA Y + BUFFER  S20201
         AR    R9,R10                   CONTROL TABLE
         AR    R9,RC
         OR    RE,R9                    R9 = TOTAL BYTES OBTAINED
*
         GETMAIN R,LV=(0)
*
         ST    RF,DCBWKPT1              DCBWKPT1=ADDR. OF COMON WRKAREA
         LR    RE,R9                    SAVE SIZE OF ISLCOMON
         STM   RCORE,R9,DXCCW1          SAVE REGS 4 THRU 9       Y02072
*
* CLEAR ISLCOMON WORK AREA
*  RF = ADDRESS OF AREA,  R9 = SIZE OF AREA
         SPACE 1
         MODESET  KEYADDR=DXUKEY,WORKREG=14  SET USERS KEY       Y02072
         SPACE 1
         LA    RD,255                   CLEAR 256 BYTES AT A TIME
COMPARE  BCTR  R9,0                     COUNT FOR EX MOVE
         CR    R9,RD                    TEST IF LESS THAN 256 REMAIN
         BNH   LASTIME                  BRANCH = YES, LESS THAN 256
         EX    RD,CLEER                 CLEAR 256 BYTES
         SR    R9,RD                    DECREMENT COUNTER BY 255
         LA    RF,256(RF)               ADVANCE 256 BYTES IN WK AREA
         B     COMPARE                  LOOP TO CLEAR MORE
         SPACE 2
*
*   ROUTINE TO FIND DEVICE TABLE ENTRY
*        INPUT - RC - LOW ORDER BYTE DEVICE TYPE
*        OUTPUT - R6 - ADDRESS OF ENTRY
DEVRTN   SR    R6,R6                    CLEAR WORK REGISTER      S20201
         L     R5,CVTPTR                R5= A(COMM VECTOR TABLE) S20201
         L     R5,CVTZDTAB(R0,R5)       R5= A(I/O DEVICE TABLE)  S20201
         IC    R6,0(RC,R5)              R6= OFFSET IN DEVICE     S20201
*                                       TABLE                    S20201
         LA    RC,0(R5,R6)              DEV TABLE POINTER        S20201
         BR    R9                       RETURN                   S20201
*
*   ROUTINE TO GET RECORD GAPS
*        INPUT - R6 - ADDRESS OF DEVICE TABLE ENTRY
*        OUTPUT - R8 - NIDO
*               - R7 - LIDO
*
*   CHECK FOR ZEUS DEVICE
GETGAPS  TM    ZEUSSW(RC),ZEUS          ON A ZEUS DEVICE         S20201
         BO    ZEUSDEV                  BR - IS ZEUS DEVICE      S20201
         SR    R7,R7                    CLEAR WORK REGISTER      S20201
         SR    R8,R8                    CLEAR WORK REGISTER      S20201
         IC    R7,LIDO(RC)              LIDO                     S20201
         IC    R8,NIDO(RC)              NIDO                     S20201
         BR    R9                       RETURN                   S20201
ZEUSDEV  LH    R7,NIDO(RC)              LIDO ALSO NIDO           S20201
         LR    R8,R7                    NIDO                     S20201
         BR    R9                       RETURN                   S20201
*
CLEER    XC    0(1,RF),0(RF)            CLEAR AMT IN EXECUTE REGISTER
         SPACE 2
*
LASTIME  EX    R9,CLEER                 R9 IS LESS THAN 256
*
         L     RF,DCBWKPT1              SET RF = ADR OF WORK AREA
         USING ISLCOMON,RF
         ST    RE,ISLVPTR8              SET PTR8 = SIZE OF WORK AREA
         LA    RD,ISLCSIZE(R0,RF)                                S20201
         SPACE 2
*  ISLVPTR3  =  ADR. OF BUFFER CONTROL TABLE
         ST    RD,ISLVPTR3
         AR    RD,R10
*  ISLVPTR1  =  ADR. OF AREA Y
         ST    RD,ISLVPTR1
         AR    RD,RC
*  ISLVPTR2  =  ADR. OF KEYSAVE AREA
         ST    RD,ISLVPTR2
*
*   FIND PRIME DEVICE TYPE ENTRY AND SAVE RECORD OVERHEADS IN THE
*   WORK AREA.
*
         SR    RC,RC                    CLEAR WORK REGISTER      S20201
         IC    RC,DCBDEVT               DEVICE TYPE OF PROPER    S20201
*                                       ENTRY                    S20201
         STC   RC,DEVT                  SAVE DEVICE TYPE         S20201
         BAL   R9,DEVRTN                GET ENTRY ADDRESS        S20201
         ST    RC,ISLOCNT               SAVE DEV TBL PTR         S20201
         SPACE 1
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
         SPACE 1
         ST    RC,DCBLRAN               SAVE DEV TBL PTR         Y02072
         LA    R9,ISLVPTR1              ADDR OF VARIABLE PTRS    Y02072
         ST    R9,DCBWKPT6              STORE VAR PTRS ADDR      Y02072
         SPACE 1
         MODESET KEYADDR=DXUKEY,WORKREG=9  CHANGE TO USER KEY    Y02072
         SPACE 1
         BAL   R9,GETGAPS               GET RECORD OVERHEADS     S20201
         STH   R7,ISLLGAP               SAVE LAST PRIME OVERHEAD S20201
         STH   R8,ISLIGAP               SAVE NORMAL PRIME        S20201
*                                       OVERHEAD                 S20201
         LA    R10,3                    SET UP CTR FOR 3 ECBS AND IOBS
         L     R9,DXUDCBAD              GET USERS DCB ADDRESS    Y02072
         DROP  RCORE
ISL02G21 ST    RF,8(RF)                 COMPLETE ECB POINTER
         MVI   4(RF),X'C2'
*  SET FLAGS 1 IN IOB TO DATA AND COMMAND CHAINING
         USING DXECB,RF                 ECB/IOB ADDRESSABILITY   Y02072
         ST    R9,IOBWGHT               STORE DCB ADDRESS        Y02072
         DROP  RF                       END ECB/IOB ADDR         Y02072
         USING FORCORE,RCORE            ESTABLISH ADDRESSABILITY Y02072
         LA    RF,44(RF)                SET POINTER TO NEXT ECB
         BCT   R10,ISL02G21             DECREMENT COUNTER BY 1
         L     RF,DCBWKPT1              RESTORE WA PTR           S20201
         USING ISLCOMON,RF              ESTABLISH ADDR           Y02072
         MVI   ISLECBA,COMPLETE         MARK IT COMPLETE         A42181
ISL03K1  EQU   *
*        IF DISP=OLD AND DATA SET OPENED FOR LOAD THEN USER
*        INTENDS TO REUSE PRE-ALLOCATED SPACE.
*
         MODESET EXTKEY=DATAMGT         SET DATA MGT KEY         Y02072
         SPACE 1
         TM    JFCBIND2,X'40'           TEST IF DISP=NEW/OLD     A40531
         BO    ISL0500                  B IF NOT MOD             A40531
*
***********************************************************************
         TM    DCBST,X'20'              RESUME LOAD               P4701
         BZ    ISL0500                  BR-INITIAL LOAD (2D)      P4701
         L     R9,DSCCORE               FORMAT 2 ADDRESSABILITY  A31998
         NC    X'43'(4,R9),X'43'(R9)    WERE ANY RECORDS LOADED  A31998
         BZ    ISL0500                  NO - JUST RELOAD         A31998
         LM    RCORE,R9,DXCCW1          RESTORE REGISTERS        Y02072
         MVC   0(L'LOAD20,RWTGC),LOAD20 INITIALIZE WHERE-TO-GO   Y02072
         TM    DCBRECFM,FIXED           ARE RECS FIXED LENGTH    A34959
         BO    RELOOP                   YES, BR TO LOAD 1920     A34959
         MVC   0(L'LOAD50,RWTGC),LOAD50 INITIALIZE WHERE-TO-GO   Y02072
*                                       A3495                    A40531
         B     RELOOP                                            A40531
*                                       P4701                    A40531
*
*       THIS SECTION WILL INITIALIZE HIRPD,HIROV,HIIOV
*       BEFORE GOING TO IGG0192D
*
ISL0500  EQU   *                                                 Y02072
         XC    DCBNREC(L'DCBNREC+L'DCBST),DCBNREC  CLEAR DCB    SA54486
*                                       NREC AND STATUS FIELDS  SA54486
         XC    DCBHIIOV(1),DCBHIIOV                              A40531
         L     RC,DCBLRAN               PTR TO DEV TBL ENTRY     S20201
         MODESET  KEYADDR=DXUKEY,WORKREG=6   SET USERS KEY       Y02072
*
         DROP  RCORE                    END ADDRESSABILITY       Y02072
         LH    R6,ISLIGAP               NIDO                     S20201
         LH    R7,ISLLGAP               LIDO                     S20201
ISLFC011 EQU   *                                                 A32559
         SR    R9,R9                    CLEAR WORK REGISTER      S20201
         IC    R9,DCBKEYLE              R9=KEYLENGTH             S20201
         LA    R8,K10(R9)               R8=KEYLENGTH+10          S20201
         LH    R4,TRKL(RC)              R4=TRACKLENGTH           S20201
         SR    R4,R7                    R4=TRACKLENGTH-LIDO      S20201
         AH    R9,DCBBLKSI              R9=KEYLENGTH+BLKSIZE     S20201
         LR    R5,R6                    R5=NIDO                  S20201
*
*        DCBHIIOV USED AS A SWITCH TO DETERMINE WHICH PASS THROUGH
*        ROUTINE.  WILL BE SET TO X'01' WHEN OVERFLOW CALCULATIONS
*        TO BE MADE.
*
         TM    DCBHIIOV,X'01'           HAS HIRPD PREV BEEN CALC O19113
         BO    ISLFC013                 YES, CALC HI R FOR OFLOW O19113
         LR    R7,R4                    CALC HIRPD- HI R FOR     O19113
*                                       PRIME DATA
         SR    R7,R9                    R7=TRKLNG-LIDO-KEYLENGTH O19113
*                                         -BLKSIZE
         BM    TOOLONG                  ABEND IF BLKSI TOO BIG   O19113
         CH    R7,ISLC110               WILL OFLOW LINK FIT IF   O19113
*                                       RECFM IS UNBLOCKED
         BNL   ISLFC012                 YES, CONTINUE PROCESSING O19113
         TM    DCBRECFM,X'10'           ARE RECORDS UNBLOCKED    O19113
         BZ    TOOLONG                  YES, OFLOW WON'T FIT     O19113
ISLFC012 EQU   *                                                 O19113
         TM    DCBRECFM,X'80'           IS IT VLR                M4502
         BZ    ISLF01B                  YES, BYPASS PDT LAYOUT   M4502
         SR    R6,R6                                             O19113
         MH    R9,10(RC)                R9=TOL(KEYLEN+BLKSIZE)   O19113
         SRA   R9,9                     R9=TOL(KEYLE+BLKSI)/512  O19113
         AR    R9,R5                    R9=R9+NIDO               O19113
         DR    R6,R9                    DIVIDE R7 BY R9          O19113
         LA    R7,1(R7)                 R7=HI R PRIME DATA TRK   O19113
         SPACE 1
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
         SPACE 1
         L     R6,DCBRELSE              RESTORE O/C/E WA ADDR    Y02072
         USING FORCORE,R6               ESTABLISH ADDRESSABILITY Y02072
         STC   R7,DCBHIRPD              STORE IN HIRPD           O19113
*
         MODESET  KEYADDR=DXUKEY,WORKREG=7   SET USERS KEY       Y02072
*
         DROP  R6                       END ADDRESSABILITY       Y02072
*
*        NOW CALCULATE HIROV
*
ISLFC013 LR    R7,R4                    RESET R7 TO TRKLG-LIDO   O19113
         AH    R8,DCBLRECL              R8=KEYLEN+10+LRECL       O19113
         SR    R7,R8                    R7=TRKLNG-LIDO-(KEYLE+   O19113
*                                          10+LRECL)
         SR    R6,R6                                             O19113
         LH    R11,10(RC)               R11=TOL                  O19113
         MR    R10,R8                   R11=TOL(KEYLE+10+LRECL)  O19113
         SRA   R11,9                    DIVIDE BY 512 FOR TOL    O19113
         AR    R11,R5                   R11=TOL(KEYLE+10+LRECL)  O19113
*                                          +NIDO
         DR    R6,R11                   DIVIDE R7 BY R11         O19113
         LA    R7,1(R7)                 ADD1                     O19113
         SPACE 1
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY         Y02072
         SPACE 1
         TM    DCBHIIOV,X'01'           INDEPENDENT OVERFLOW     O19113
*                                       PASS                     O19113
         BZ    ISLFC0A                                           O19113
         STC   R7,DCBHIIOV              HI R FOR IND OVFLOW      O19113
         B     ISLF01B                                           O19113
ISLFC0A  EQU   *                                                 Y02072
         STC   R7,DCBHIROV              HIROV=HI R FOR CYL OFLW  Y02072
ISLFC014 TM    DCBOPTCD,X'10'           INDENDENT OVERFLOW REQ   O19113
         BZ    ISLF01B                  NO                       O19113
*
*        MUST NOW DETERMINE IF INDEPENDENT OVERFLOW ON A
*        DIFFERENT DEVICE TYPE
*
         SR    RC,RC                                             O19113
         IC    RC,DCBOVDEV              RC= IND OFLOW DEV TYPE   O19113
         LTR   RC,RC                    OVERFLOW ON SAME DEVICE  O19113
         BNE   ISLFC015                 YES = BRANCH             O19113
         IC    RC,DCBDEVT               NO, OVFLOW DEV SAME      O19113
         STC   RC,DCBOVDEV              SAVE                     O19113
ISLFC015 OI    DCBHIIOV,X'01'           SET OFLOW PASS BIT       O19113
         CLC   DCBOVDEV,DCBDEVT         IS OFLOW ON SAME DEV     O19113
*                                       TYPE                     O19113
         BE    ISLF01A                  BR - NOT DIFFERENT       S20201
*
         L     R6,DCBRELSE              RESTORE O/C/E WA ADDR    Y02072
         USING FORCORE,R6               ESTABLISH ADDR           Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=9   SET USERS KEY       Y02072
*
         DROP  R6                       END ADDR                 Y02072
         BAL   R9,DEVRTN                PT TO DEVICE TABLE       S20201
         BAL   R9,GETGAPS               OVERFLOW OVERHEADS       S20201
         LR    R6,R8                    NIDO                     S20201
         B     ISLFC011                 NO, CALC HI OVFL         S20201
ISLF01A  STC   R7,DCBHIIOV              HI R ALREADY CALC        S20201
ISLF01B  EQU   *                                                 Y02072
         SPACE 1
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY         Y02072
         SPACE 1
         L     RCORE,DCBRELSE           RESTORE OPEN W/A ADDR    Y02072
         USING FORCORE,RCORE            ESTABLISH ADDR           Y02072
         XC    DCBRELSE,DCBRELSE        CLEAR DCB FIELD          Y02072
         LM    R4,R9,DXCCW1             RESTORE REGS             Y02072
         MVC   0(L'LOAD2D,RWTGC),LOAD2D INITIALIZE WTG           Y02072
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH=BEGINNING OF THIS MODULE
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BNE   RELOOP                   BR = NOT AT END          A34959
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BNE   TCTLRTN                  BR IF NOT ZERO           A34959
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         LA    RJ,DXCCW12
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
TOOLONG  EQU   *                                                 O19113
         LA    RJ,RC2C                  ISAM RETURN CODE         Y02072
         ABEND X'002',DUMP,,SYSTEM      SYSTEM 002 ABEND         Y02072
*
ABEND    EQU   *
         ABEND X'03B',DUMP,,SYSTEM      SYSTEM 03B ABEND         Y02072
* CONSTANTS
ZERO     DC    H'0'                                              A34920
FOUR     DC    H'4'                     MINIMUM RKP FOR VLR      A30791
         DS    0F
THISLOAD DC    C'21'                                               DM0E
OPNLOD7  DC    C'0S'
ISLC110  DC    H'10'                                             O19113
         ORG   *-1                      USE LOW ORDER BYTE OF DC A42170
ISLRETRY DC    3X'0A'                   TEN RETRIES FOR EACH IOB A42170
*
LOAD2D   DC    C'2D'                    ID OF MODULE IGG0192D    Y02072
LOAD20   DC    C'20'                    ID OF MODULE IGG01920    Y02072
LOAD50   DC    C'50'                    ID OF MODULE IGG01950    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         END
