         TITLE 'IGG019HD - SETL K(D) OR SETL KC(D) MODULE'
IGG019HD CSECT
*          RELEASE OS/VS2-02 DELETIONS                           Y02072
*  A050700                                                      ZA01384
*          RELEASE 21 DELETIONS                                       *
*                                                                S21045
*    046200-046600,047400-047600                                SA52459
*                                                                     *
*        RELEASE VS2030 PTF CHANGES
*A030500                                                       @ZA07593
*
*STATUS CHANGE LEVEL 009
*                                                                     *
*FUNCTION/OPERATION - THIS MODULE IS USED TO LOCATE THE RECORD AT     *
*   WHICH THE USER DESIRES TO BEGIN SEQUENTIAL RETRIEVAL OF HIS DATA  *
*   SET. THE USER SPECIFIES THE KEY OR KEYCLASS OF THE DESIRED RECORD.*
*   THE MODULE INITIALIZES WORK AREA FIELDS AND READS THE FIRST       *
*   RECORDS WHICH WILL BE MADE AVAILABLE TO THE USER BY SUBSEQUENT    *
*   GETS.  THIS MODULE SUPPORTS BOTH FIXED AND VARIABLE LENGTH
*   RECORD FORMATS.
*                                                                     *
*ENTRY POINTS- 'IGG019HD' (SISCTSK) IS THE ENTRY POINT FOR AN ISAM    *
*   SETL K(D) OR SETL KC(D) MACRO. THE GENERATED CALLING SEQUENCE IS- *
*                                                                     *
*   SETL DCBNAME,K(D),KEY               SETL DCBNAME,KC(D),KEYCLASS   *
*   LA   1,DCBNAME                      LA   1,DCBNAME                *
*   LA   0,KEY                          LA   0,KEYCLASS               *
*   L    15,76(0,1)                     L    15,76(0,1)               *
*   BALR 14,15                          BALR 14,15                    *
*                                                                     *
*INPUT-  REGISTER  0-BYTE 1 = TYPE OF SETL (BIT 0 =1 IF K),           *
*                             (BIT 1=1 IF C),(BIT 4=1 IF D)           *
*                    BYTES 2-4 = KEY OR KEYCLASS ADDRESS              *
*        REGISTER  1-DCB ADDRESS                                      *
*        REGISTER 13-REGISTER SAVE AREA ADDRESS                       *
*        REGISTER 14-RETURN ADDRESS                                   *
*        REGISTER 15-ENTRY POINT                                      *
*                                                                     *
*OUTPUT- NORMAL EXIT, SAME AS FOR INPUT
*   ERROR EXIT, DCBEXCD1 BIT 3(ILLEGAL REQUEST) IS SET                *
*               DCBEXCD1 BIT 2(UNREACHABLE) IS SET                    *
*               DCBEXCD1 BIT 1(KEY NOT FOUND) IS SET                  *
*   REGISTER 15- USER'S SYNCHRONOUS ERROR EXIT ROUTINE ADDRESS        *
*                                                                     *
*EXTERNAL ROUTINES- 'IGG019HB' SUBROUTINES- 'SISCTSGE'(EINFO), TO
*   INFORM THE USER OF BAD WRITES ON A RECORD OF A PREVIOUS SCAN.     *
*   'SISCTSB'(SCHEDULE), TO SET UP AND INITIATE THE READING OF DATA   *
*   RECORDS BEGINNING AT THE USER SPECIFIED KEY. 'SISCTSD'(QUEUE), TO *
*   MOVE THE SPECIFIED NUMBER OF BUFFERS FROM ONE DESIGNATED QUEUE TO *
*   ANOTHER. 'SISCTBS' (SETLB) , TO INITIATE A SCAN AT THE BEGINNING  *
*   OF THE DATA SET IF A SETL B MACRO WAS SPECIFIED                   *
*   RATHER THAN A SETL K.                                             *
*                                                                     *
*EXITS- NORMAL-'SISSLK4', RETURNS CONTROL TO THE USER TO THE NEXT     *
*   SEQUENTIAL INSTRUCTION AFTER THE SETL K OR SETL KC MACRO          *
*   EXPANSION.                                                        *
*                                                                     *
*   ERROR-  'SISSKB1C'  IS THE EXIT TO THE USERS SYNCHRONOUS ERROR    *
*   ROUTINE. 'SISSKF1A' IS THE ABEND EXIT IF NO USER'S SYNCHRONOUS    *
*   ERROR ROUTINE WAS SPECIFIED.                                      *
*                                                                     *
*TABLES/WORKAREAS- DATA EXTENT BLOCK (DEB) DESCRIBED BY SISDEB DSECT, *
*   WORKAREA DESCRIBED BY SISWORK1 DSECT, DATA CONTROL BLOCK DESCRIBED*
*   BY IHADCB DSECT.                                                  *
*                                                                     *
*ATTRIBUTES - REENTRANT, READ ONLY.                                   *
*                                                                     *
*NOTES - REGISTER USAGE IS CONSISTANT IN ALL SCAN MODE MACRO TIME     *
*   MODULES.
         EJECT
         USING IHADCB,R1
         USING SISCP236,R4
         USING SISCP22,R3
         USING SISWORK1,R2
         USING *,R15
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
DELETE   EQU   X'FF'                    DELETE MARK FOR RECS     S20201
K8       EQU   8                        CONSTANT                 S20201
K1       EQU   1                        CONSTANT                 S20201
L3       EQU   3                        LENGTH                   S20201
         SPACE 3
SISCTSK  B     SISSKA2
         SPACE
*                                  ***** CONSTANTS *****
EMTYQCON DC    F'0'
         SPACE 2
* UPON ENTRY TO THE SETL MACRO, R0 CONTAINS THE TYPE OF SETL AND
* THE DESIRED KEY ADDRESS
         SPACE 2
SISSKA2  STM   R14,R12,12(R13)          SAVE REGISTERS           M4902
         L     R2,DCBWKPT1         LOAD WORK AREA BASE
         XC    DCBEXCD1(2),DCBEXCD1    ZERO EXCD1 AND EXCD2
         OI    W1OSBIT2,X'02'      SET ON RELEASE BIT
         ST    R0,W1REGSAV         SAVE SETL TYPE AND KEY ADDRESS
SISSKA2A TM    W1REGSAV,X'80'      WHAT KIND OF A SETL WAS SPECIFIED
         BO    SISSKA2B                                          A35340
         TM    W1REGSAV,X'10'      WAS IT SETL B
         BZ    SISSKB1             NO, GO TO SYNAD FOR INVALID TYPE
         LM    R2,R12,28(R13)      YES, RESTORE REG2-12
         L     R15,DCBGET
         A     R15,GSETLB(R15)      GET SETL B MODULE
         BR    R15                 GO TO SETL B MODULE
         SPACE 2
*              TEST IF LAST WRITES OF LAST SCAN WERE SUCCESSFULLY
*              COMPLETED
SISSKA2B EQU   *                                                 A35340
         TM    W1DCBFA,X'80'            TEST IF DISP=SHR         A35340
         BZ    CNTINU                   BR IF NOT TO CONTINUE    A35340
*                                       RTN                      A35340
         SVC   54                       IF SO, ISSUE SVC 54 TO   A35340
*                                       REFRESH                  A35340
*                                       DCB FIELDS
CNTINU   EQU   *                                                 A35340
         SPACE 2
SISSKA3  TM    W1ECBO,X'40'        ECBO COMP BIT ON
         BZ    SISSKA4                  NO, WAIT
SISSKB3  CLI   W1WRITEC+1,X'00'        YES, IS WRITE Q EMPTY
         BE    SISSKB2                  YES
         B     SISSKB4                  NO
SISSKA4  LR    R5,R1
         LR    R6,R15                   SAVE R15 AND R1
         WAIT  ECB=W1ECBO
         LR    R15,R6
         LR    R1,R5                    RESTORE R15 AND R1
SISSKB4  TM    W1OSBIT2,X'80'           ANY WRITE Q ERRORS
         BZ    SISSKC5                  NO
SISSKB5  L     R11,DCBGET               YES
         A     R11,GEINFO(R11)          INFORM USER OF WRITE Q ERRORS
         BALR  R9,R11
         B     SISSKB2                  BRANCHES WHEN ALL BUFFERS HAVE
*                                    BEEN MOVED TO FREE Q IN EINFO
         SPACE
SISSKC5  LA    R3,W1FR1ST        MOVE WRITE Q BUFS TO FREE Q
         LA    R4,W1WR1ST
         NI    W1OSBIT1,X'FE'        SET OFF IOBO COMP BIT
         LH    R5,W1WRITEC
         L     R10,DCBGET               GET ADDRESS OF QUEUE ROUTINE
         A     R10,GQUEUE(R10)
         BALR  R9,R10                   BRANCH TO QUEUE
         SPACE 2
SISSKB2  L     R4,W1CP23PT
         TM    W1OSBIT1,X'80'      TEST SCAN MODE INDICATOR
         BZ    SISSKC3             OFF
*                                  IF ON NO ESETL GIVEN BETWEEN THIS
*                                  AND PREVIOUS SETL
SISSKB1  OI    DCBEXCD1,X'10'      SET ON ILLEGAL REQUEST BIT
SISSKB1A TM    DCBSYNAD+3,X'01'        WAS A SYNAD ADDRESS SPECIFIED
         BO    SISSKF1             NO
SISSKB1B EQU   *
         NI    W1OSBIT1,X'80'                                    A26332
         NI    W1OSBIT2,X'00'                                    A26332
         LH    R5,W1READC               COUNT ON READ Q.         A37178
         AH    R5,W1READR               = CNT + RESID            A37178
         STH   R5,W1READC               *                        A37178
         XC    W1READR,W1READR          RESID = 0                A37178
         LA    R3,W1FR1ST              CLEAR READ Q BUFFERS
         LA    R4,W1RD1ST              TO THE FREE Q
         L     R10,DCBGET              GET ADDRESS OF
         A     R10,GQUEUE(R10)         QUEUE ROUTINE
         BALR  R9,R10                  BRANCH TO THE QUEUE ROUTINE
         MVI   W1READR+1,X'00'         CLEAR OUT RESIDUE
         LA    R0,W1IOBI            LOAD REG 0 WITH IOB ADD    @ZA07593
         L     R15,DCBSYNAD            GET USER'S SYNAD ADDR
         LM    R2,R12,28(R13)      RESTORE REGS. 2-12
         L     R14,12(R13)              RESTORE REG 14           A34626
SISSKB1C BR    R15                 TAKE SYNAD EXIT
*                                  NO SYNAD EXIT SPECIFIED
SISSKF1  LM    R2,R12,28(R13)
         ABEND X'031',DUMP,,SYSTEM      031 ABEND, NO SYNAD      Y02072
         SPACE 3
SISSKC3  TM    W1REGSAV,X'40'      SETL KC
         BO    SISSKC4             YES
         MVC   CS22+7(1),DCBKEYLE      RESET TO KEY LENGTH SINCE MACRO
         MVC   CS29+7(1),DCBKEYLE      WAS SETL K NOT KC
SISSKD2  TM    DCBRECFM,X'10'           ARE RECORDS BLOCKED
         BO    SISSKF4                  YES, INITIALIZE CP22B IN CASE
*                                       A PREVIOUS OPERATION HAS TAKEN
*                                       PLACE
SISSKF3  TM    W1REGSAV,X'08'      WHAT TYPE OF SETL, D OR K-D
         BO    SISSKF4               DATA ONLY
         SPACE 2
*                        SETL OF KEY AND DATA TYPE HAS BEEN REQUESTED
*                        IF BUFFER SIZE IS LARGE ENOUGH TO ALLOW FOR
*                        THIS TYPE, OPEN SETS ON BIT 0 OF OSBIT3
         SPACE
         TM    W1OSBIT3,X'80'           IS BUFFER LARGE ENOUGH
         BZ    SISSKB1                  NO
*                                       YES
         SPACE
* IF UNBLOCKED KEY AND DATA RECORDS, ALL CHANNEL PROGRAMS ON THE FREE
* QUEUE ARE SET TO READ (CP22A MODE)
         SPACE
         SPACE
         L     R3,W1FR1ST               SET BASE OF CP22 DSECT
SISSKF2C L     R7,CN7+4                 BUFFER ADDRESS FOR KEY
         ST    R7,CN3
         MVC   CN4+1(3),CN7+1      RE-INITIALIZE CN4              22835
         MVI   CN3,X'0E'                READ KEY-DATA OP CODE
         MVI   CN2+4,X'20'              SET ALL FLAGS OFF BUT KD
         CLC   CN5+1(3),EMTYQCON        ARE WE AT END OF Q
         BE    SISSKG3                  YES, ALL CP'S CHANGED
         L     R3,CN5                   GET NEXT CP IN CHAIN
         B     SISSKF2C                 LOOP
         EJECT
* IF UNBLOCKED RECORDS DATA ONLY SPECIFIED, ALL CHANNEL PROGRAMS ON
*  THE FREE Q ARE SET UP FOR THIS FORM (CP22B MODE).
         SPACE 2
SISSKF4  L     R3,W1FR1ST               GET CP1 OF FREE Q
SISSKF4A LA    R7,CN4
         MVC   CN4+1(3),CN7+1          REINITIALIZE CN4
         ST    R7,CN3                   SET CN3 TO CN4 ADDRESS
         MVI   CN3,X'08'                SET CN3 TO A TIC
         MVI   CN2+4,X'00'              SET OFF ALL FLAGS
         CLC   CN5+1(3),EMTYQCON        LAST CP IN CHAIN
         BE    SISSKG3                  YES
         L     R3,CN5                   NO, GET NEXT CP
         B     SISSKF4A                 LOOP
         SPACE 3
SISSKG3  L     R4,W1CP23PT         LOAD CP23 ADDRESS TO R4
         MVC   CS2+1(3),W1REGSAV+1      MOVE KEY ADDRESS TO CCW'S
         MVC   CS1B+K1(L3),W1REGSAV+K1  *                        S20201
         MVC   CS11+1(3),W1REGSAV+1
         MVC   CS22+1(3),W1REGSAV+1
         MVC   CS29+1(3),W1REGSAV+1
         TM    W1REGSAV,X'02'           TEST FOR HI/EQ  OPTION   O19111
         BO    SISSKG3A                 YES                      O19111
         TM    DCBRECFM,X'10'           ARE RECORDS BLOCKED      O19111
         BO    SISSKG3A                 YES                      O19111
         MVI   CS22,X'29'               SRCH UNBLKED REC KEY EQ  O19111
         B     SISSKG4A                                          O19111
SISSKG3A MVI   CS22,X'69'               BLKED RECS, SRC HI/EQ    O19111
SISSKG4A LA    R3,W1RD1ST
         LA    R4,W1FR1ST               MOVE 1 CP FROM FREEQ TO
         LA    R5,1                    READ Q
         L     R10,DCBGET
         A     R10,GQUEUE(R10)
         BALR  R9,R10                   BRANCH TO QUEUE SUB AND RETURN
SISSKH4  L     R3,W1RD1ST
         MVI   CN6+7,X'00'         SET R OF CP1 READ Q TO ZERO
SISSKH3  LA    R3,CN6+3
         L     R4,W1CP23PT
         ST    R3,CS25             PUT CN6+3 ADD TO CS25
         MVI   CS25,X'12'          RESTORE OP CODE
SISSKH2  MVC   W1IDAD(7),DCBFTHI   SET IOBI+32(MBBCCHH) TO DCBFTHI
*                                       HIGHEST INDEX LEVEL ADDRESS
         MVI   W1IDAD+7,X'00'          SET R OF IOBI SEEK TO 0    17516
         MVC   CS6+7(8),W1IDAD          SET CS7 FOR 1 LEVEL      A34003
*                                       * SEARCH IN CASE CYL INDEX ON
*                                       * SAME DEVICE AS T.I.
SISSKJ2  CLI   DCBNLEV,X'00'       NLEV EQUAL ZERO
         BE    SISSKK2             YES, THEN TRACK IS HIGHEST LEV INDEX
SISSKJ3  ST    R4,W1ICPS           SET CPS TO CS1 OF CP23
SISSKK5  OI    W1OSBIT1,X'08'      INDICATE KEY FOUND
         B     SISSLA2
         SPACE 2
SISSKK2  LA    R7,CS9
         ST    R7,W1ICPS                SET CPS TO ADD OF CS9 (CP23)
         MVC   CS6+7(7),DCBLPDA         SET UP FOR APPENDAGE       054A
         OC    CS7+5(1),DCBFIRSH+3      REDUCE TO CYL BDRY         054A
         XC    CS7+5(1),DCBFIRSH+3                                 054A
         B     SISSKK5
         SPACE 4
*  KEY CLASS SPECIFIED, COMPUTE CLASS LENGTH
         SPACE 2
SISSKC4  SR    R5,R5
         IC    R5,DCBKEYLE              PUT KEY LENTH IN R5
         L     R6,W1REGSAV              KEY ADDRESS IN R6
         AR    R6,R5
SISSKC4A BCTR  R6,0                    R6 NOW CONT. LOW ORDER BYTE KC
         CLI   0(R6),X'00'              IS THIS BYTE ZERO
         BNE   SISSKD4                  NO
         BCT   R5,SISSKC4A              YES, SUB 1 FROM R5 AND LOOP
         LA    R5,K1(R5)                PREVENT ZERO CNT CCW    ZA01384
         SPACE
*        THE PREVIOUS INSTRUCTIONS COMPUTED CLASS LENGTH. IT IS
*        NOW IN R5.
         SPACE
SISSKD4  L     R4,W1CP23PT
         STC   R5,CS29+7           INSERT KEY CLASS LENGTH IN CS29
         STC   R5,FIXIND           STORE KEY CL LEN, USED AT SLH3
         OI    CS29+4,X'20'        SET ON SILI BIT IN CS29
         STC   R5,CS22+7           PUT KEY CLASS IN CS22
         OI    CS22+4,X'20'        SET ON SILI BIT IN CS22
         B     SISSKD2
         EJECT
         DS    0F
         SPACE 2
*                       *****             *****
*                       *****             *****
*                       ***** SETL K OR KC*****
*                       *****             *****
*                       *****             *****
         SPACE
TRKREC1  DC    X'00000001'              HHR OF 001
EXECUTE  CLC   0(0,R3),0(R6)            USED TO FIND RECORD WITHIN BLK
         SPACE 2
SISSLA2  MVI   W1IEXTEN+1,X'04'         SET APP CODE
         NI    W1IF1,X'7F'              SET OFF CD BIT IN IOB
         BAL   R6,SISEXCP              GO TO EXCP ROUTINE
SISSLA3  TM    W1ECBI,X'40'             IS COMP BIT SET
         BO    SISSLA4                  YES, NO WAIT NEEDED
         LR    R7,R15
         WAIT  ECB=W1ECBI
         LR    R15,R7
SISSLA4  LR    R1,R8                   RESTORE R1
         TM    W1OSBIT1,X'04'           CATASTROPHE
         BZ    SISSLB4                  NO
SISSLA5  OI    DCBEXCD1,X'02'           INDICATE UNREACHABLE
         B     SISSKB1A                GO TO SYNAD RETURN
SISSLB4  TM    W1OSBIT1,X'08'           WAS KEY FOUND
         BO    SISSLB3                 YES
         OI    DCBEXCD1,X'80'          INDICATE LOWER LIMIT NOT FOUND
         B     SISSKB1A                EXIT TO USER
SISSLB3  TM    W1OSBIT1,X'20'           IS KEY ON OVERFLOW TRACK
         BO    SISSLE3                  YES
         SPACE
*                       NO, KEY IS IN PRIME DATA RECORD
*                       COUNT OF RECORD IS READ INTO CN6 BY CP23,
*                       UNLESS RECORD IS 1ST OF A TRACK.
         SPACE
         L     R3,W1RD1ST
         CLI   CN6+7,X'00'             R IN FIRST CN6=0
         BNE   SISSLC1                  NO
*                                  YES, KEY IS THAT OF 1ST RECORD ON
*                                  A TRACK
SISSLB1  MVC   CN6+1(6),CS18            SET CN6 BBCCHH
         MVI   CN6+7,X'01'              R MUST BE 1 TO GO TO THIS PATH
         TM    CS18+7,X'08'              SHARED TRACK
         BZ    SISSLC1
         MVC   CN6+7(1),CS18+6           YES  R IS PROPER
SISSLC1  MVC   W1IDAD(7),CS17+7        MOVE MBBCCHH INTO IOB
SISSLC1A MVI   W1IDAD+6,X'00'          ZERO TRK VALUE
SISSLC1B MVI   W1IDAD+7,X'01'          R=1
         SPACE 2
*        INITIALIZING THE FIRST CP22 ON READ Q. AT THIS TIME R4
*        CONTAINS THE STARTING CCW ADDRESS OF CP23(CS1) AND R3 CONTAINS
*        THE ADDRESS OF 1ST CCW OF 1ST CP22 ON READQ
         SPACE
SISSLC2  MVC   CN6(1),CS17+7       SET M IN CN6
         NI    CN1,X'7F'           SET OFF MT BIT
         MVC   CN4+6(2),DCBBLKSI   SET COUNT TO BLKSIZE
         NI    CN4+4,X'BF'         SET CC FLAG OFF
SISSLC2A MVI   CN4,X'06'           CN4 TO READ D
         ST    R3,W1CN15           SET CN1 ADDRESS TO CN15
         SR    R6,R6
         IC    R6,CN6
         SLL   R6,4
         ST    R6,W1CN14
         L     R6,DCBDEBAD
         LA    R6,32(R6)
         A     R6,W1CN14
         MVC   CN6+1(2),4(R6)
         MVI   W1CN15,X'08'             RESET TIC CODE
         LA    R3,CN6+1
         ST    R3,W1CN14                SET SEEK ADDR IN CN14 TO DATA
*                                       RECORD ADDRESS
         MVI   W1CN14,X'1B'             RESTORE SEEK HH IN CN14
SISSLE2  LA    R6,W1CN8
         ST    R6,W1ICPS                SET CP PTR TO READ TRK INDEX CP
SISSLE1  MVI   W1IEXTEN+1,X'00'         SET IOB APP CODE TO 0
         OI    W1IF1,X'80'             SET ON DATA CHAIN BIT
         BAL   R6,SISEXCP
SISSLF1  L     R3,W1RD1ST
         TM    W1ECBI,X'40'             IS IOBI COMPLETE BIT ON
*                                       NO
         BO    SISSLG1                  YES, READ FINISHED
         LR    R7,R15
         WAIT  ECB=W1ECBI               WAIT
         LR    R15,R7
SISSLG1  LR    R1,R8                    RESTORE REGS
         TM    CN2+4,X'0A'              WAS READ SUCCESSFUL
         BNE   SISSLK3                 NO, PREPARE TO RETURN TO USER
SISSLG1A TM    DCBRECFM,X'10'           BLOCKED RECORDS
         LA    R14,SISSLH3A             SET UP NO OVERFLOW IND   M4902
         BO    SISSLG2                  YES, FIND RCD WITHIN BLOCK
*                                       NO
         L     R3,CN7                   GET BUFFER ADDRESS        18746
         TM    DCBRECFM,X'80'           IS IT FIXED              A43934
         BO    SISSLK2P                 SEE IF REC DELETED       A43934
         LA    R3,4(0,R3)               BUMP OVER BDW            A43934
         B     SISSLK2P                 SEE IF RECORD IS DELETED  18746
SISSLJ1  L     R3,W1RD1ST
         MVC   W1LPDR,CN6          SET LPDR TO CN6 OF 1ST ON READ Q
SISSLK3  OI    W1OSBIT1,X'80'      SET ON SCAN MODE INDICATOR
         MVC   W1CBF,W1EOB        TO FORCE EOB SUBR. ENTRY
         OI    W1OSBIT2,X'20'        INDIC SAME CYLINDER
         IC    R7,DCBFIRSH+3           LOAD DEVICE MASK
         IC    R8,W1LHH+1              LOAD LPDR H
*
*        CHECK FOR EOF
*
         NR    R8,R7                   AND MASK WITH LPDR H
         EX    R8,SISEXEC              CLI    DCBFIRSH+1,H OF LPDR
         BNE   SISSLK4
         CLI   DCBFIRSH+2,X'01'      R OF FIRSH EQUAL ONE
         BE    SISSLK4             YES
         OI    W1OSBIT2,X'10'        INDIC SHARED TRACK
SISSLK4  LM    R14,R12,12(R13)          RESTORE REGISTERS        M4902
         BR    R14                 RETURN TO USER
SISEXEC  CLI   DCBFIRSH+1,0            IS H OF LPDR=H OF FIRSH
SISEXCP  SR    R8,R8                   CLEAR WORK REG
         IC    R8,W1IDAD               GET M FROM IOBI
         SLL   R8,4                    M X 16
         L     R7,DCBDEBAD             GET DCB ADDR
         LA    R8,32(R8,R7)            R8 POINTS TO EXTENT OF M.
         MVC   W1IDAD+1(2),4(R8)       MOVE BB INTO IOB
         LR    R8,R1                   SAVE R1
         LR    R7,R15                  SAVE R15
         EXCP  W1IOBI                  FOR CP24-CP22
         LR    R15,R7                  RESTORE R15
         BR    R6
         SPACE 3
SISSLG2  L     R8,CN7              BUFFER ADDRESS
         TM    DCBRECFM,X'80'           IS IT FIXED               VLR
         BO    FIXEOB                   YES, BRANCH               VLR
         AH    R8,0(R8)                 VLR BLKSIZE IS VAR        VLR
         B     VAREOB                                             VLR
FIXEOB   EQU   *                                                  VLR
         AH    R8,DCBBLKSI                                        VLR
VAREOB   EQU   *                                                  VLR
         BCTR  R8,0                    R8=END OF BUFFER
         TM    W1REGSAV,X'40'      SETL KC
         BZ    SISSLH2             NO, JUST SETL K
SISSLG3  SR    R5,R5               YES
         IC    R5,FIXIND           R5=CLASS LENGTH
         SPACE
SISSLH3  L     R6,W1REGSAV         KEY ADDRESS
         L     R3,CN7              LOAD BUFFER ADDRESS TO R3
         TM    DCBRECFM,X'80'           IS IT FIXED               VLR
         BO    FIXRKP                   YES, BRANCH               VLR
         LA    R3,4(R3)                 BUMP OVER BLK LL FLD      VLR
         MVC   RCDLEN(2),0(R3)          SAVE LENGTH               VLR
FIXRKP   EQU   *                                                  VLR
         AH    R3,DCBRKP
         BCTR  R5,0                    FOR WAY CLC WORKS
         BR    R14                      BR IF OVERFLOW           M4902
SISSLH3A EX    R5,EXECUTE           CLC   0(0,R3),0(R6)
         SPACE 2
*        IN THE PREVIOUS INSTRUCTIONS,R5 CONTAINED THE KEYLENGTH
*        (FROM SLH2) OR THE KEY CLASS(FROM SLG3). THE EX INSTRUCTION
*        CAUSES A BRANCH TO EXECUTE TAG WHICH COMPARES THE RCD KEY
*        (AT THE LOC. OF R3) TO THE LOWER LIMIT KEY(AT R6)
*        THE NUMBER OF BYTES TO BE COMPARED IS DESIGNATED BY R5
         SPACE 2
         BE    SISSLK2                  IF COMPARE EQUAL,THEN KEY FOUND
         BH    SISSLJ3                  IF HI, CHECK FOR HI-EQ   O19111
*                                       OPTION
         TM    DCBRECFM,X'80'           IS IT FIXED               VLR
         BO    FIXLEN                   YES, BRANCH               VLR
         SH    R3,DCBRKP                GET NXT REC LL            VLR
         AH    R3,RCDLEN                                          VLR
         MVC   RCDLEN(2),0(R3)                                    VLR
         AH    R3,DCBRKP                                          VLR
         B     VARLEN                                             VLR
FIXLEN   EQU   *                                                  VLR
         AH    R3,W1CURLEN              R3=ADDR NEXT RCD IN BUFF  VLR
VARLEN   EQU   *                                                  VLR
         CR    R8,R3                    IS THERE A NEXT RCD IN THIS BUF
         BL    SISSLJ4                  NO,IT'S EOB AND KEY NOT FOUND
         B     SISSLH3A                 SEARCH NEXT RECORD OF THIS BUF.
         SPACE
SISSLH2  SR    R5,R5
         IC    R5,DCBKEYLE              PUT KEY LENGTH IN R5
         B     SISSLH3                  BRANCH TO H3
SISSLJ3  TM    W1REGSAV,X'02'           SETL KH OR KDH           M4581
         BZ    SISSLJ4                  NO, BR--NO REC FOUND     M4581
         SPACE
*                                       RECORD FOUND
SISSLK2  OI    W1OSBIT2,X'01'      SET ON BIT TO BE TESTED IN EOB
         SH    R3,DCBRKP     BRING R3 BACK TO DATA OF RCD FROM H3A
         ST    R3,W1KEYBLK              SAVE CORR ADDR FOR EOB RTN
SISSLK2P TM    DCBOPTCD,X'02'           DELETE OPTION SPECIFIED   18746
         BZ    SISSLJ1                  NO--TAKE RCD FOUND EXIT   18746
         TM    DCBRECFM,X'80'           IS IT FIXED               VLR
         BO    SISSLK2Q                 YES, BRANCH               VLR
         CLI   4(R3),X'FF'              VLR, CHECK STH BYTE       VLR
         B     SISSLK2R                                           VLR
SISSLK2Q EQU   *                                                  VLR
         CLI   0(R3),X'FF'              TEST FOR DELETED RCD      VLR
SISSLK2R EQU   *                                                  VLR
         BNE   SISSLJ1                  IF NOT, BRANCH            18746
         TM    W1REGSAV,X'40'           SETL KC                   20816
         BO    SISSLJ1                  YES--OK IF DELETED        20816
         SPACE
*              IF HI-EQUAL OPTION NOT SPECIFIED THEN RECORD
*              NOT FOUND
*
         TM    W1REGSAV,X'02'           SETL KH OR KDH           M4581
         BO    SISSLJ1                  YES, OK IF DELETED       M4581
SISSLJ4  OI    DCBEXCD1,X'80'           SET ON LOWER LIM NOT FOUND BIT
         B     SISSKB1A                 GO TO SYNAD
         SPACE 2
*              KEY IS IN OVERFLOW RECORD
*              LPDR IS SET IN SETL K APPENDAGE FOR OVERFLOW RCDS
         SPACE
SISSLE3  MVC   W1WOVFL(10),CS34+7       MOVE ADDR OF REC TO OVERFLOW
*                                       ENTRY OF TRACK INDEX AREA
         MVC   W1READR,W1READC          SET READ R = READ C       18746
         XC    W1READC(2),W1READC       SET COUNT TO ZERO         18746
         L     R11,DCBGET
         A     R11,GSCHED(R11)          GO TO SCHEDULE TO READ RECORD
         BALR  R9,R11
         TM    W1ECBI,X'40'             IS READING FINISHED       18746
         BO    SISSLE4                  YES--BRANCH               18746
         LR    R7,R1
         LR    R8,R15
         WAIT  ECB=W1ECBI              WAIT
         LR    R1,R7                    RESTORE R1
         LR    R15,R8                   RESTORE R15
*
*              KEY IN OVERFLOW MUST BE COMPARED TO DETERMINE AND
*              OPTION CHECKED TO DETERMINE IF RECORD GOOD.
*
SISSLE4  TM    W1REGSAV,X'02'           HI/EQ OPTION SPECIFIED   M4581
         BO    SISSLK3                  YES, BR--KEY WAS FOUND   M4581
         TM    DCBRECFM,X'10'           ARE RECORDS BLOCKED      M4581
         BO    SISSLE40                 YES,BR--KEY IS IN DATA   M4581
         NC    DCBRKP,DCBRKP            IS RKP ZERO              M4581
         BZ    SISSLE42                 YES, KEY FOUND           M4581
*
*        A SEARCH KEY EQUAL OR HIGH WAS USED IN THE CHANNEL PGM
*        AND THE SETL WAS NOT EQ/HIGH THEREFORE THE RECORD MUST
*        BE EXAMINED TO SEE IF THE KEY CONTAINED WITHIN IT IS
*        EQUAL TO THE DESIRED KEY.
SISSLE40 L     R3,W1RD1ST               SET CP22 BASE            M4581
         BAL   R14,SISSLG2              BRANCH TO PREPARE FOR    M4902
*                                       *    COMPARE.
SISSLE41 EX    R5,EXECUTE               CLC  0(0,R3),0(R6)       O19111
         BNE   SISSLJ4                  BR--NO RECORD FOUND      M4581
SISSLE42 TM    DCBOPTCD,X'02'           DELETE OPTION SPECIFIED  O19111
         BZ    SISSLK3                  NO--TAKE RCD FOUND EXIT   18746
         TM    W1REGSAV,X'40'           SETL KC                   20816
         BO    SISSLK3                  YES--OK IF DELETED        20816
         L     R3,W1RD1ST               SET CP22 BASE             18746
         L     R3,CN7                   GET BUFFER ADDRESS        18746
         TM    DCBRECFM,X'80'           IS IT FIXED               VLR
         BO    SISSLE4A                 YES, BRANCH               VLR
         CLI   K8(R3),DELETE            VLR, CHECK 8TH BYTE      S20201
         B     SISSLE4B                                           VLR
SISSLE4A EQU   *                                                  VLR
         CLI   0(R3),X'FF'              WAS THIS RECORD DELETED   18746
SISSLE4B EQU   *                                                  VLR
         BNE   SISSLK3                  BRANCH IF RECORD FOUND    18746
         B     SISSLJ4                  OTHERWISE, RECORD NOT FND 18746
         SPACE 3
*                                  OFFSETS OF VECTOR TABLE IN HB
GEINFO   EQU   12
GQUEUE   EQU   16
GSETLB   EQU   36
GSCHED   EQU   20
*
PATCH    DC    XL((*-IGG019HD)/20)'00'  ZEROED PATCH AREA        Y02072
*
         EJECT
SISWORK1 IGGSCAN
RCDLEN   EQU   W1WDCXDM+10              CURRENT RECORD LENGTH    A35340
*                                  THE NO. OF CP 22'S IS DEPENDENT ON
*                                  THE NO. OF BUFFERS %DCBBUFNO<
         EJECT
         DCBD  DSORG=(IS)
         EJECT
SISCP22  DSECT
         IGGCP22
         SPACE 5
SISCP236 DSECT
         IGGCP23
         IGGCP26
         END
