         TITLE 'IGG019G3 - APPENDAGES-PART 1,UNBL,USR WA,WITH WRT CHK'
IGG019G3 CSECT
*          RELEASE OS/VS2-02 DELETIONS
*          RELEASE 19 DELETIONS                                       *
*2682072435-072442,072526,072554,072568,072589,073790,098800,    A27357
*2682125270-125320                                               A27357
*          RELEASE 20 DELETIONS                                       *
*3361018850,018900,018950,019000,037000-037200,039600-040200,    S20201
*3361040400-040800,043600,044000,044600,045000,045200,045600,    S20201
*3361046000,046200,046600,046800,053400,053600-054600,056400,    S20201
*3361057400,057600-058000,059200,060600,066000,072000,072505,    S20201
*3361101600,114600,114800,117000,117200,122200,134600,134800-    S20201
*3361135800,136600,141600,141800,142000,142400,143400,153000-    S20201
*3361158800,159000-164800,165000-170800,171000-176800,177000-    S20201
*3361177800,178000-178800,179000-179800,180000-180800,181000-    S20201
*3361183600,189600-195400,195600-202400,202800-204600,204800-    S20201
*3361207960,208200-211000,211200-215600                          S20201
*          RELEASE 21 DELETIONS                                       *
*1201022200,023200,086400                                        S21045
*1201                                                            A33533
*1201                                                            A46955
*XXXX                                                            A49403
*1201022000                                                      A50670
*            CHANGES SINCE VS2-3.7                                    *
*A024200-024290,C024320                                        @ZA26522
*
*STATUS CHANGE LEVEL 011
*
* FUNCTION/OPERATION- APPENDAGE ROUTINES FOR BISAM
*        WHEN WRITE KN IS USED WITH OR WITHOUT READ AND UPDATE
*        WHEN THE USER HAS     SUPPLIED A READ-IN AREA,
*        WHEN WRITE VALIDITY CHECKING IS     REQUESTED,
*        AND RECORD FORMAT IS UNBLOCKED.
*        (PART 1)
*
*        CEND  - IF CHANNEL PROGRAM ENDS
*                **WITHOUT ERROR, EITHER
*                    PROCESSING COMPLETION WILL BE SCHEDULED,
*                    PROCESSING CONTINUATION WILL BE SCHEDULED,
*                    OR PROCESSING WILL CONTINUE.
*                **WITH A LENGTH CHECK, TESTS DETERMINE WHETHER
*                    OR NOT TO TREAT THE SITUATION AS A REAL
*                    ERROR.
*                **ANY OTHER ERROR, IN GENERAL, 'UNREACHABLE
*                    BLOCK' IS INDICATED IN THE DECB, AND
*                    PROCESSING COMPLETION WILL BE SCHEDULED.
*
*        ABEND - IF CHANNEL PROGRAM ENDS ABNORMALLY BECAUSE OF
*                **PERMANENT ERROR, 'UNCORRECTABLE I/O ERROR'
*                    IS INDICATED IN THE DECB, AND PROCESSING
*                    COMPLETION WILL BE SCHEDULED.
*                    USE OF WRITE VALIDITY CHECKING OPTION
*                    ALLOWS A LIMITED NUMBER OF RE-EXECUTIONS
*                    BEFORE INDICATING AN ERROR.
*                **NON-PERMANENT ERROR, ONE RE-TRY IS MADE
*                    BEFORE INDICATING A PERMANENT ERROR.
*                **FILE PROTECTION, TESTS DETERMINE WHETHER
*                    OR NOT TO TREAT THE SITUATION AS A REAL
*                    ERROR.
*                **ANY OTHER ERROR, IN GENERAL, 'UNREACHABLE
*                    BLOCK' IS INDICATED IN THE DECB, AND
*                    PROCESSING COMPLETION WILL BE SCHEDULED.
* ENTRY POINTS-
*        ENTRY POINT               FUNCTION
*        -----------               --------
*        APPCEND                   CEND
*        APPABEND                  ABEND
* INPUT - N/A
* OUTPUT - N/A
* EXTERNAL ROUTINES - N/A
*
* EXITS- 1. RETURN TO IOS VIA THE APPENDAGE RETURN VECTOR TABLE POINTED
*            TO BY REGISTER 14.  SEE THE DSECT LABELED    APPRV FOR THE
*            FORMAT OF THIS TABLE AND THE USE OF EACH EXIT.
*           USED TO CONTINUE PROCESSING.
*        2. SCHEDULE AN ASYNCHRONOUS ROUTINE - BRANCH VIA THE EXIT
*            EFFECTOR ADDRESS IN THE COMMUNICATION VECTOR TABLE.
*           USED TO SCHEDULE PROCESSING COMPLETION OR CONTINUATION.
*        3. IF THE DESIRED APPENDAGE ROUTINE IS IN PART 2, BRANCH TO
*            THAT ROUTINE VIA THE PART 2 VECTOR TABLE.  THE POINTER TO
*            THE MODULE IS IN DCBWKPT5.  SEE THE DSECT LABELED
*            IHAPART2 FOR RELATIVE ADDRESSES AND USES OF THE ROUTINES.
*
* TABLES/WORK AREAS - DECB, DCB, IOB AND EXTENTION, DCB WA (DCW), DEB,
*        CVT, PART 2 VECTOR TABLE.
*        SEE DSECTS AT END OF MODULE FOR FORMAT AND DESCRIPTIONS.
*
* ATTRIBUTES- REENTRANT. DISABLED.
* NOTES - NONE
         EJECT
* GENERAL REGISTERS ARE USED AS FOLLOWS
*
R0       EQU   0                   *    -----
R1       EQU   1                   *    12* ADDRESS
R2       EQU   2                   *    IOB
R3       EQU   3                   *    DEB
R4       EQU   4                   *    DCB
BASEXXXX EQU   5                        BASE REG FOR CLEANUP     Y02072
R6       EQU   6                   *    -----
R7       EQU   7                   *    UCB
SAVEXXXX EQU   8                        SAVE AREA FOR CLEANUP    Y02072
R9       EQU   9                   #    WORK REGISTER
R10      EQU   10                       CHANNEL PROGRAM POINTER
R11      EQU   11                       DECB
R12      EQU   12                       WORK REGISTER
R13      EQU   13                       WORK REGISTER
R14      EQU   14                       IOS RETURN VECTOR TABLE
R15      EQU   15                       BASE REGISTER
*
*        * MEANS THIS REGISTER IS SET UP UPON ENTRY FROM IOS,
*          AND ITS CONTENTS MUST BE RESTORED UPON RETURN TO IOS
*        # MEANS THIS REGISTER MAY BE CHANGED, BUT UPON RETURN TO IOS
*          MUST CONTAIN ZEROES IN THE THREE HIGH ORDER BYTES
*
PERRMASK EQU   X'20'          TO TEST FOR PERMANENT ERROR, TEST
PERRYES  EQU   8               IOBECBAD, B2. IF OFF (CONDITION CODE 8)
PERRNO   EQU   7               THERE IS A PERM ERR. ELSE B2 ON ( CD 7)
ONE      EQU   1                        MISC OFFSETS             S20201
TWO      EQU   2                        *                        S20201
THREE    EQU   3                        *                        S20201
FOUR     EQU   4                        *                        S20201
FIVE     EQU   5                        *                        S20201
SIX      EQU   6                        *                        S20201
SEVEN    EQU   7                        *                        S20201
EQEIGHT  EQU   8                        *                        S20201
SIXTEEN  EQU   16                       *                        S20201
OFLOW    EQU   X'10'                    OVERFLOW BIT IN F BYTE   S20201
DUMMY    EQU   X'20'                    DUMMY BIT IN F BYTE      S20201
CCOFF    EQU   X'BF'                    MASK FOR CC OFF
BYP      EQU   12                       RETURN TO IOS/BYPASS     S20201
EXCP     EQU   8                        RETURN TO IOS/EXCP       S20201
NORMAL   EQU   0                        RETURN TO IOS/NORMAL     S20201
ABENTRY  EQU   X'01'                    ABEND ENTRY INDICATOR    A50670
DATACHK  EQU   X'08'                    DATA CHECK IOB SENSE     A50670
REEXCP   EQU   8                        REEXCP RETURN TO IOS     Y02072
SKIPX    EQU   4                        SKIP RETURN TO IOS       Y02072
BYPASS   EQU   12                       BYPASS RETURN TO IOS     Y02072
FMVIOL   EQU   X'04'                    FILE MASK VIOLATION      Y02072
NOTSEEK  EQU   8                        CC - SEEK TEST SENSE B0  Y02072
SEEKCHK  EQU   X'01'                    SEEK CHECK INDICATOR     Y02072
*
         USING RQE,R1                   RQE ADDRESSABILITY       Y02072
         USING IHAIOB,R2                IOB POINTER              S20201
         USING IHADEB,R3                DEB POINTER
         USING IHADCB,R4                DCB POINTER
         USING IHAWKNCP,R10             CHANNEL PROGRAM POINTER
         USING IHADECB,R11              DECB POINTER
         USING IHADCW,R12               DCB WA POINTER ONLY AS WORK REG
         SPACE 4
*                             APPENDAGE VECTOR TABLE
APP      DC    A(APPCEND-APP)           CHANNEL END      USED BY WKN
         DC    A(APPABEND-APP)          ABNORMAL END  -     USED BY WKN
APPNO     EQU   *
         USING APPCEND,R15
APPCEND  NI    IOBINDCT,X'FE'          INDICATE CEND ENTRY
         B     SETUPREG                 BRANCH TO SET UP REGISTERS
APPABEND EQU   *                                                 A50670
         USING APPABEND,R15
*                                                                A50670
*        IF THIS IS A CORRECTABLE DATA CHECK FROM AN RPS         A50670
*        DEVICE THEN RETURN TO IOS FOR ERP HANDLING              A50670
*                                                                A50670
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR          A50670
         BZ    NOCDACK                  YES CONTINUE AS USUAL    A50670
         TM    IOBSENS1,FMVIOL          WAS IT A SEEK CHECK      Y02072
         BCR   NOTSEEK,R14              NO - RETURN TO ERPS      Y02072
         SPACE 3
NOCDACK  EQU   *                                                 A50670
         OI    IOBINDCT,ABENTRY         INDICATE ABEND ENTRY     A50670
SETUPREG L     R15,DEBEXPTR              GET ADDR OF DEB         S21045
*                                       EXTENSION PTR            S21045
         USING DEBEXT,R15                                        S21045
         L     R15,DEBWKPT4              R15  BASE               S21045
         DROP  R15
         USING APP,R15
*
*   SAVE IOS REGISTERS AND SET UP ADDRESSABILITY TO RETURN TO SPECIAL
*   CLEANUP ROUTINE.
*
         STM   R14,R12,0(R13)           SAVE REGISTERS           Y02072
         LR    SAVEXXXX,R13             SAVE SAVE AREA PTR       Y02072
         LA    R14,IOSRETS              RETURN TO RETURN TO IOS  Y02072
         LR    BASEXXXX,R14             BASE REG FOR RETURN      Y02072
         SPACE 2
         MODESET KEYADDR=RQEPRT,WORKREG=13 ACQUIRE USER KEY      Y02072
         SPACE 3
         L     R10,IOBCCWAD             R10  CP
         LA    R10,0(R10)               CLEAR HIGH ORDER BYTE    A49403
         MVC   IOBECBAD+1(3),IOBBCHAD+1 RESTORE ECB              A33533
         L     R11,IOBECBAD             R11  DECB
         L     R13,DEBEXPTR              GET ADDR OF DEB         S21045
*                                       EXTENSION PTR            S21045
         USING DEBEXT,R13                                        S21045
         L     R13,DEBWKPT5              R13  BASE, PART 2       S21045
         DROP  R13                                               S21045
         USING IHAPART2,R13
         L     R12,IOBCSW               R12  ADDRESS OF LAST CCW + 8
         LA    R12,0(0,R12)
         IC    R9,IOBAPP                R9   RELATIVE ADDRESS IN
         CH    R9,TWOFOUR               IS APP CODE GT 24?     @ZA26522
         BNH   CODEOK                   IF NOT BYPASS PROGCHK  @ZA26522
         DC    H'0'                     PROG CHK FOR BAD CODE  @ZA26522
TWOFOUR  DC    X'0018'                  MAX VALUE FOR APP CODE @ZA26522
CODEOK   SLL   R9,2                            APPENDAGE TABLE @ZA26522
         TM    IOBINDCT,X'01'
         BO    APPABENT(R9)            IF ABEND, BRANCH VIA ABEND TABLE
         B     APPCENDT(R9)            IF CEND, BRANCH VIA THAT TABLE
         EJECT
***********************************************************************
*****              CHANNEL END APPENDAGE VECTOR TABLE             *****
***********************************************************************
APPCENDT EQU   *              CODE      FOR COMPLETION OF
         B     APPABA2   0             CP4/CP5, READ
         B     APPABA2   1             CP4/CP5, WRITE
         B     APPADB2   2             CP7
         B     APPAAB2   3             CP1 OR CP2, READ AND UPDATE
         B     APPAFB                   CP14-PART2                17332
         B     APPACA2   5             CP6
         B     APPAHB2   6             CP4/CP5 WRITE CHECK
         B     APPM67CE  7             CP1 OR CP2, WRITE KN
         B     APPN3B21
         B     APPN7B2   9             CP10AW - TRUE INSERT
         B     APPN8A2   10            CP10BW - TRUE INSERT
         B     APPN8A2   11            CP10BW - ADD TO END
         B     APPM4A2   12            CP14W - SETUPS 1, 2, OR 5
         B     APPM5A2   13            CP14W - SETUPS 3, 4, OR 6
         B     APPM1B21
         B     APPM2B2   15            CP16 - SITUATION 2
         B     APPM3B2   16            CP16 - SITUATION 3
         B     APPN9A2   17            CP17W - TRACK INDEX ONLY
         B     APPN9A2   18            CP17W - TRACK AND HIGHER LEVELS
         B     APPN9A2   19            CP17W - HIGHER LEVELS ONLY
         B     APPT1B2   20            CP12A
         B     APPJ9C2            CP123W                          17332
         B     APPJ9C2   22             CP123W
         B     APPN7B2   23             CP10AW - ADD TO END
         B     APPT3B2   24             CP12CW
         EJECT
***********************************************************************
*****             ABNORMAL END APPENDAGE VECTOR TABLE             *****
***********************************************************************
APPABENT EQU   *              CODE      FOR COMPLETION OF
         B     APPABD2   0             CP4/CP5, READ
         B     APPABD2   1             CP4/CP5, WRITE
         B     APPAIB2   2             CP7
         B     APPAAA2   3             CP1 OR CP2, READ AND UPDATE
         B     APPAEB                   CP14 PART 2               17332
         B     APPACE2   5             CP6
         B     APPAHD2   6             CP4/CP5 WRITE CHECK
         B     APPM67AE  7             CP1 OR CP2, WRITE KN
         B     APPN3E2   8             CP8
         B     APPJ3B2   9             CP10AW - TRUE INSERT
         B     APPJ4B2   10            CP10BW - TRUE INSERT
         B     APPJ4B2   11            CP10BW - ADD TO END
         B     APPJ5B2   12            CP14W - SETUPS 1, 2, OR 5
         B     APPJ6B2   13            CP14W - SETUPS 3, 4, OR 6
         B     APPM1J2   14            CP15
         B     APPM2H2   15            CP16 - SITUATION 2
         B     APPM2H2   16            CP16 - SITUATION 3
         B     APPJ7B2   17            CP17W - TRACK INDEX ONLY
         B     APPJ7B2   18            CP17W - TRACK AND HIGHER LEVELS
         B     APPJ7B2   19            CP17W - HIGHER LEVELS ONLY
         B     APPT1A3   20            CP12A
         B     APPJ9E2            CP123W                          17332
         B     APPJ9E2   22             CP123W
         B     APPJ3B2   23             CP10AW - ADD TO END
         B     APPJBB2   24             CP12CW
         EJECT
*   SPECIAL IOS RETURN CLEANUP ROUTINE INTERCEPTS ALL RETURNS TO
*   IOS AND RESTORES REGISTERS AND SETS PROTECTION KEY TO 0.
         SPACE 2                                                 Y02072
IOSRETS  EQU   *                        RETURN POINT             Y02072
         USING *,BASEXXXX               ADDRESSABILITY           Y02072
         B     NORMRET                  NORMAL RETURN            Y02072
         B     SKIPRET                  SKIP POST RETURN         Y02072
         B     REEXCPRT                 REEXCP RETURN            Y02072
         SPACE 2
BYPASRET EQU   *                        BYPASS POST RETURN       Y02072
         BAL   R12,IOSRETX              RESTORE REGS AND KEY     Y02072
         LA    R14,BYPASS(R14)          SCHEDULE ASYNCH ROUTINE  Y02072
         L     R13,CVTPTR               CVT ADDRESSABILITY       Y02072
         USING CVT,R13                  *                        Y02072
         L     R13,CVTEXEF              ASYNCH SCHED ROUTINE     Y02072
         DROP  R13                      *                        Y02072
         BR    R13                      GO TO ROUTINE            Y02072
         SPACE 2
REEXCPRT EQU   *                        REEXCP RETURN            Y02072
         BAL   R12,IOSRETX              RESTORE REGS AND KEY     Y02072
         B     REEXCP(R14)              RETURN - REEXCP          Y02072
         SPACE 2
SKIPRET  EQU   *                        RETURN - SKIP            Y02072
         BAL   R12,IOSRETX              RESTORE REGS AND KEY     Y02072
         B     SKIPX(R14)               RETURN - SKIP            Y02072
         SPACE 2
NORMRET  EQU   *                        RETURN - NORMAL          Y02072
         BAL   R12,IOSRETX              RESTORE REGS AND KEY     Y02072
         BR    R14                      RETURN                   Y02072
         SPACE 2
IOSRETX  EQU   *                        RESTORE REGS AND KEY     Y02072
         MODESET KEYADDR=KEY0,WORKREG=13   SET KEY = 0           Y02072
         SPACE 2
         LM    R14,R11,0(SAVEXXXX)      RESTORE REGISTERS        Y02072
         BR    R12                      RETURN                   Y02072
         DROP  BASEXXXX                 RESET ADDRESSABILITY     Y02072
         SPACE 2                                                 Y02072
KEY0     DC    H'0'                     KEY 0                    Y02072
         EJECT                                                   Y02072
*              CHANNEL  END CP8,    APPENDAGE CODE  8,         CHART N3
APPN3B2  EQU   *                        *
*
         LA    R13,CB8A+EQEIGHT         DID CHANNEL PROGRAM END ON
         CLR   R12,R13                  CB8A
         BNE   APPAOS22                 NO, CONTINUE
         TM    CB8A+FIVE,CC             WAS CB8A CHAINED
         BZ    APPAOS22                 NO, CP UNCHANGED
         NI    CB8A+FIVE,CCOFF          RESET CHAIN IND.
         OI    CB8A+FOUR,CC             TURN CC BACK ON
         CLI   CB12,SEEKHH              CB12 SET FOR HEAD SEEK
         BNE   APPN3F3                  NO,  FILE PROTECT
         LA    R13,CB16                 RESET IOBSTART
         ST    R13,IOBSTART-ONE         START CP WITH SETSECT
         MVC   IOBDADAD,CB10+SEVEN      RESET IOBSEEK
         B     APPRVXCP                 SECHDULE EXCP IOS RETURN
APPAOS22 EQU   *                        *
*
         LA    R13,CB21+EQEIGHT         DID CHAN PROG STOP AT CB21
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPN3A3                  NO - CHECK FOR LENGTH ERROR
         TM    CB11+7,X'08'             TEST IF TRACK IS SHARED
         L     R12,DCBWKPT2             CREATE PROPER 'R' IN DCWHIRPS
         BO    APPN3C2A                 BO IF SHARED
         MVC   DCWHIRPS,DCBHIRPD        UNSHARED - USE DCBHIRPD
         B     APPN3C2B
APPN3C2A MVC   DCWHIRPS,DCBHIRSH        SHARED   - USE DCBHIRSH
APPN3C2B EQU   *                                                 S20201
APPN3C2D MVC   IOBDADAD+3(5),CB23+3     MOVE CB23 CCHAR TO IOB
         MVI   IOBAPP,X'14'             SET APPENDAGE CODE TO 20
         NI    IOBFLAG1,X'7F'           DONT USE DATA CHAINING
         OI    IOBFLAG1,X'40'           USE COMMAND CHAINING
         LA    R13,CD1                  START AT CD1
         ST    R13,IOBSTART-1
         MVI   IOBCOUNT,X'0A'           SET COUNTER TO 10
         L     R12,DCBMSWA              SET UP COUNT FIELD AT W
         LA    R12,2(R12)                                         17332
         MVC   0(5,R12),IOBDADAD+3      CCHHR FROM IOB
         MVC   5(1,R12),DCBKEYLE        KL    FROM DCBKEYLE
         MVC   6(2,R12),DCBBLKSI        DL    FROM DCBBLKSI
         L     R12,CB2+4                POINTER TO CEA
         LA    R13,IOBDADAD+3           PUT IOB ADDRESS AT CEA
         USING CP123,R12                ESTABLISH ADDRESSIBILITY S20201
         STH   R13,CEA+TWO              STORE ADDRESS            S20201
         SRL   R13,16
         STC   R13,CEA+ONE              STORE ADDRESS            S20201
         L     R12,CD2+4                POINTER TO CE1
         LA    R13,IOBDADAD+3           PUT IOB ADDRESS AT CE1 AND CL1
         USING CP12B,R12                                         S20201
         STH   R13,CE1+TWO              STORE ADDRESS            S20201
         SRL   R13,16
         STC   R13,CE1+ONE              STORE ADDRESS            S20201
         MVC   CL1+ONE(THREE),CE1+ONE   MOVE ADDRESS             S20201
         MVC   CL3A+1(3),CL1+1          PUT IOB ADDRESS AT CL3A
         MVC   CE4+ONE(THREE),DECBKEY+ONE PUT I ADDR IN CE4      S20201
         L     R13,DECBAREA
         LA    R13,SIXTEEN(R0,R13)      PUT AREA+16 ADDR AT CE5  S20201
         STH   R13,CE5+TWO              PUT A(AREA+16) IN CE5    S20201
         SRL   R13,16
         STC   R13,CE5+ONE              PUT A(AREA+16) IN CE5    S20201
         MVC   CL3+ONE(THREE),CE5+ONE   PUT A(AREA+16) IN CL3    S20201
         SR    R13,R13                  CLEAR REGISTER 13
         L     R12,DCBWKPT2
         USING IHADCW,R12               ESTABLISH ADDRESSABILITY S20201
         IC    R13,DCWHIRPS
         IC    R9,IOBDADAD+7
         SR    R13,R9
         LA    R13,1(R0,R13)            NUMBER OF RECORDS
         SLL   R13,3                    MULTIPLY BY 8
         LA    R12,CD2
         AR    R13,R12                  'SET OFF' ADDRESS IN REG 13
         LA    R9,8(R0,R0)              PUT 8 IN REG 9
         NI    4(R13),X'BF'             SET OFF CC FLAG
APPN3D2  SR    R13,R9
         CLR   R13,R12                  IS IT LAST TIME THROUGH LOOP
         BE    APPRVXCP                 YES - EXCP RETURN TO IOS
         OI    4(R13),X'40'             SET ON CC FLAG
         B     APPN3D2                  LOOP
APPN3B3  OI    DECBEXC1,X'04'           RECORD IS UNREACHABLE
APPN3B4  MVI   IOBASYN,X'08'            SCHEDULE PROCESSING COMPLETION
APPN3B5  B     BYP(R14)                 SCHEDULE ASYNCH ROUTINE  Y02072
APPN3B6  LA    R13,CB8A+EQEIGHT         DID CP STOP AT CB8A      S20201
         CLR   R12,R13                  COMPARE REGS             S20201
         BNE   APPN3B3                  NO, UNREACHABLE BLOCK    S20201
         TM    CB11+SEVEN,OFLOW+DUMMY   OFLO, INACTIVE, OR DUMMY S20201
         BNZ   APPN3F3                  YES, GO TO FILE PROTECT  S20201
         MVC   CB23+THREE(FOUR),CB11+TWO   SET UP CCHH           S20201
         MVI   CB23+SEVEN,ONE           SET R EQUAL TO 1         S20201
         B     APPN3C2D                 GO READ RECORD           S20201
         EJECT
*              ABNORMAL END CP8,    APPENDAGE CODE  8,         CHART N3
APPN3E2  EQU   *                                                  19374
         TM    IOBSENSE+1,X'04'         WAS ABEND DUE TO          15924
*                                       FILE PROTECT              15924
         BO    APPN3F2                  YES--BRANCH               19374
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR?          19374
         BC    PERRYES,APPN3B3          YES--BRANCH               19374
         B     APPRVNOR                 NO--RETRY CHANNEL PGM     19374
APPN3F2  LA    R13,CB12+8               DID CHAN PROG STOP AT CB12
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPN3B3                  NO - RECORD IS UNREACHABLE
APPN3F3  TM    CB11+SEVEN,OFLOW         WAS OFLO TRK IX ENTRY RD S20201
         BZ    APPN3G3                  BZ IF NO                 S20201
         TM    CB11+SEVEN,DUMMY         DUMMY OR INACTIVE        S20201
         BO    APPN3G3                  BO IF NO
         MVC   CJ11(7),IOBDADAD         SAVE TRACK INDEX MBBCCHH
         MVI   IOBAPP,X'10'             SET APPENDAGE CODE TO 16
         NI    IOBFLAG1,X'7F'           DONT USE DATA CHAINING
         OI    IOBFLAG1,X'40'           USE COMMAND CHAINING
         LA    R13,CJ1                  START AT CJ1
         ST    R13,IOBSTART-1
         MVC   IOBDADAD,CB10+7          PUT CB10 MBBCCHHR IN IOB
         LA    R13,IOBDADAD+3           PUT SEEK ADDRESS IN CJ1 AND CJ5
         IC    R9,CJ1
         ST    R13,CJ1
         STC   R9,CJ1
         MVC   CJ5+ONE(THREE),CJ1+ONE     MOVE IN ADDR OF SEEK   S20201
         MVC   CJ3+ONE(THREE),DECBKEY+ONE   MOVE IN ADDR OF KEY  S20201
         MVC   CJ7+ONE(THREE),DECBKEY+ONE   MOVE IN ADDR OF KEY  S20201
         L     R12,DCBWKPT2             SET ON FIRST TIME ONLY SWITCH
         USING IHADCW,R12               ESTABLISH ADDRESSABILITY S20201
         OI    DCWWKNI,X'40'
         LA    R12,0(R0,R0)             IS FIRST OFLO ON SAME MODULE
         IC    R12,CB10+7                      GET M OF OFLO ENTRY
         SLL   R12,4(R0)                       MULT BY 16
         L     R12,DEBUCBAD(R12)        GET UCB ADDR FROM DEB    S20201
         LA    R12,0(R0,R12)                   COMP WITH UCB AD IN 7
         CR    R12,R7
         BE    APPRVXCP                 SAME MODULE - EXCP RTRN TO IOS
         MVI   IOBASYN,X'01'            DIFFERENT MODULE - MUST EXCP
         B     APPN3B5                   IN ASYNCHRONOUS ROUTINE
APPN3A3  TM    IOBCSW+5,X'40'           DID CHAN PROG HAVE LENGTH ERROR
         BZ    APPN3B6                  GO TEST FOR SPECIAL CASE S20201
         TM    CB11+7,X'30'             WAS ENTRY INACTIVE
         BO    APPN3B3                  BO IF YES
APPN3G3  TM    CB11+7,X'20'             VERIFY ADD-TO-END SITUATION
         BZ    APPN3B3                  BZ IF DUMMY OR INACTIVE
         CLI   DCBNLEV,X'00'            ERROR IF 2 OR MORE INDEX LEVELS
         BNE   APPN3B3                  BNE IF MORE THAN 1 INDEX LEVEL
         TM    CB11+7,X'08'             TEST IF TRACK IS SHARED
         L     R12,DCBWKPT2             CREATE PROPER 'R' IN DCWHIRPS
         USING IHADCW,R12                                        S20201
         BO    APPN3G33                 BO IF SHARED
         MVC   DCWHIRPS,DCBHIRPD        UNSHARED - USE DCBHIRPD
         B     APPN3G5
APPN3G33 MVC   DCWHIRPS,DCBHIRSH        SHARED   - USE DCBHIRSH
APPN3G5  MVI   IOBAPP,X'0E'             SET APPENDAGE CODE TO 14
         MVC   IOBDADAD(3),DCBLPDA      SEEK TO DCBLETI'S CYLINDER
         MVC   IOBDADAD+3(4),DCBLETI    CCHH
         OC    IOBDADAD+6(1),DCBFIRSH+3
         XC    IOBDADAD+6(1),DCBFIRSH+3 REDUCE TO TRK BNDRY OR ZERO
         MVI   IOBDADAD+7,0
         LA    R12,CI1                  START AT CI1
         ST    R12,IOBSTART-1
         NI    IOBFLAG1,X'7F'           DON'T USE DATA CHAINING
         OI    IOBFLAG1,X'40'           USE COMMAND CHAINING
         LA    R12,IOBDADAD+3           PUT SEEK ADDRESS IN CI1
         IC    R9,CI1
         ST    R12,CI1
         STC   R9,CI1
         MVC   CI5+TWO(FIVE),DCBLETI    PUT DCBLETI CCHHR IN CI5 S20201
         MVC   CI5(TWO),DCBLPDA+ONE     MOVE IN BB FOR HEAD SEEK S20201
         B     APPRVXCP
         EJECT
*              CHANNEL  END CP12A,  APPENDAGE CODE 20,         CHART T1
APPT1B2  LA    R13,CD2+8
         CLR   R12,R13
         BNH   APPN3B3                  NO - RECORD IS UNREACHABLE
         L     R12,DECBKEY              KEY IN I
         L     R13,DCBMSWA
         LA    R13,10(R13)                                        17332
         IC    R9,DCBKEYLE
         BCTR  R9,R0                    COMPARE KEYLE BYTES
         EX    R9,APPT1D2               ARE KEYS EQUAL
         BE    APPT1E2                  YES - RECORD IS DUPLICATE
         TM    IOBCSW+4,X'01'           DID CHAN PROG END WITH EOF
         L     R12,DCBWKPT2
         USING IHADCW,R12               ESTABLISH ADDRESSABILITY S20201
         BZ    APPT1D5                  BZ IF NO
         OI    DCWWKNI,X'08'            SET ON 'CP12A ENDED W/EOF' SW
         L     R13,IOBCSW               POINT TO LAST READ CCW PLUS 8
         LA    R9,8(R0,R0)              PUT 8 IN REG 9
         SR    R13,R9                   POINT TO LAST READ - OF THE EOF
         L     R13,0(R13)               POINT TO COUNT OF EOF
         MVC   5(1,R13),DCBKEYLE        NON-EOF KL TO REPLACE EOF'S 0
         MVC   6(2,R13),DCBLRECL        NON-EOF DL TO REPLACE EOF'S 00
APPT1E3  L     R12,IOBCSW               SET REG 12 TO STOP ADDR PLUS 8
         LA    R12,0(R0,R12)
         LA    R13,CD3
         SR    R12,R13                  SET REG 12 TO TRUE NO. OF RDS,
         SLL   R12,1                     INCLUDING EOF, TIMES 16
         L     R13,CD2+4                CE4 POINTER
         USING CP12B,R13                ESTABLISH ADDRESSABILITY S20201
         LA    R13,CE3                  POINT TO CE3             S20201
         AR    R12,R13                  SET REG 12 TO 'SET OFF CC' ADDR
         NI    4(R12),X'BF'             SET OFF CC FLAG
         ST    R12,CH10+4         SAVE ADDR OF LAST CCW IN 12B    17332
         ST    R12,IOBFCHAD                                       17332
         L     R11,CB2+4          PTR TO CP123W START             17332
*
*              LINK CP 12B TO CP123W
*
         LA    R9,8(R12)                PT TO END OF CP 12B      A27357
         CR    R9,R11                  IS  EXP END OF CP12B=      17332
*                                      START OF CP123W
         BE    APPU1GA                 IF YES-BRANCH              17332
         MVC   CH15A+4(4),8(R12)        SAVE CCW WHERE TIC GOES  A27357
         MVC   9(3,R12),CB2+5       SET UP CP 12B TO TIC TO       17332
*                                 THE START OF CP123W
         MVI   8(R12),X'08'                                       17332
APPU1GA  OI    4(R12),X'40'       SET  CC BIT ON ALST CP12B CCW   17332
         L     R11,CD2+4                                          17332
         DROP  R13                      DROP REGISTER            S20201
         USING CP12B,R11                ESTABLISH ADDRESSABILITY S20201
         LA    R11,CE5                  POINT TO FIRST CE5       S20201
         CR    R11,R12                                            17332
         L     R11,IOBECBAD             RESTORE DECB REG         A27357
         USING IHADECB,R11                                       S20201
         BNE   APPT1B3                                            17332
         SH    R12,EIGHT                POINT TO WRITE KEY CCW   A27357
         ST    R12,IOBFCHAD             SAVE PTR TO LAST WRITE   A27357
*                                       KEY CCW USED IN CP 12B
         AH    R12,EIGHT                RESTORE REG.             A27357
         L     R9,DECBKEY                                         17332
         B     APPT1B4                                            17332
APPT1B3  EQU   *                                                  17332
         LR    R9,R12                                             17332
         L     R9,0(R9)                 PT TO NORMAL IX KEY      A27357
APPT1B4  EQU   *                                                  17332
         ST    R9,CH6+4                 RECORD KEY AND DATA       17332
         LA    R9,16(R0,R0)             PUT 16 IN REG 9
APPT1E32 SR    R12,R9                   REDUCE 'SET OFF/ON' ADDR BY 16
         CLR   R12,R13                  IS IT REDUCED TO CE4 YET
         BE    APPT1F3                  YES - LOOP COMPLETE
         OI    4(R12),X'40'             SET ON CC FLAG
         B     APPT1E32                 LOOP
APPT1F3  SR    R9,R9                                              17332
         B     APPJ9B2            SET UP CP 123W                  17332
APPT2B6  L     R12,DCBMSWA         USE 1ST RECORD OF WA          A27357
         LA    R12,2(R12)          TO TEST FOR DELETED RECORD    A27357
         B     APPT2B7                                           A27357
APPT2B5  EQU   *                                                  17332
         L     R12,CH6+4                PTR TO NORMAL IX KEY     A27357
         IC    R9,DCBKEYLE              PUT IL IN REG 9
         L     R13,DECBKEY         IS KEY OF NORMAL TRK INDEX    A27357
         CR    R12,R13             EQU USER KEY                  A27357
         BE    APPT2B6             IF SO, BRANCH                 A27357
         LA    R12,0(R9,R12)                                      M3275
         AH    R12,DCBBLKSI            PT TO OVFL RECORD         A27357
APPT2B7  EQU   *                                                 A27357
         LA    R12,8(R9,R12)                                     A27357
         IC    R9,DCBFIRSH+3            TRACK MASK
         SLL   R12,8                    SAVE 3 LOW ORDER BYTES OF R12
         IC    R12,IOBDADAD+6           TRACK BYTE
         NR    R9,R12                   LOWER TO CYL BNNDRY OR ZERO
         SRL   R12,8                    REINSTATE R12
         TM    DCBOPTCD,X'02'           WAS DELETE OPTION TAKEN
         BZ    APPT2H3                  BZ IF NO
         CLI   0(R12),X'FF'             WAS RCD MARKED FOR DELETION
         BNE   APPT2H3                  NO--OVERFLOW IT           12999
         EX    R9,CLILDT
         BE    APPD2G31                 YES--BRANCH               12999
         CLC   DCBLPDA(7),IOBDADAD      IS IT LAST RCD OF DATA SET
         BNE   APPT2K2                                            17332
APPD2G31 EQU   *                                                  12999
         TM    CB11+7,X'10'             CURR IX ENTRY OFLOW       12999
         BZ    APPD2G33                 NO, BRANCH--NEXT INDEX    12999
*                                       ENTRY MUST BE OVERFLOW    12999
         TM    CB11+7,X'08'             CHAINING PRESENT          12999
         BZ    APPT2H3                  NO--BUMP DELETED REC      12999
         B     APPT2K2                                            17332
APPD2G33 TM    CB26,X'08'               CHECK FOR CHAINING        17332
         BO    APPT2K2                                            17332
APPT2H3  EQU   *                                                  M3216
         L     R12,DCBWKPT2                                       M3216
         USING IHADCW,R12                                        S20201
         TM    DCWWKNI,X'08'      DID CP12A END WITH EOF          M3216
         BO    APPT2K2            IF YES BRANCH                   M3216
         L     R12,CH6+4                                          M3216
         MVC   CH7+1(3),CH6+5          ADDR OF LAST KEY ON        17332
*                                      PRIME TRACK
         NI    IOBINDCT,X'CF'                                     17332
         OI    CH8E+4,X'82'                                       17332
         L     R9,DECBKEY              HI KEY ON TRK EQ           17332
         CR    R9,R12                  KEY OF NEW RECORD          17332
         BE    APPT2H31                IF YES-BRANCH              17332
         AH    R12,DCBBLKSI            OVFL REC COUNT ADDR        17332
         SR    R9,R9                                              17332
         IC    R9,DCBKEYLE                                        17332
         AR    R12,R9                                             17332
APPT2H32 ST    R12,CH6+4                                          17332
         MVI   IOBASYN,X'09'            SCHED ASYN ROUTINE        17332
         SR    R9,R9                                              17332
         B     APPN3B5                                            17332
APPT2H31 L     R12,DCBMSWA             OVFL KEY IN WORK AREA      17332
         LA    R12,2(R12)                                         17332
         B     APPT2H32                                           17332
APPT2K2  L     R13,CD2+4               CP12B POINTER              17332
         IC    R9,IOBDADAD+7                                      17332
         BCTR  R9,R0                                              17332
         STC   R9,IOBDADAD+7                                      17332
         NI    CH8E+4,X'40'             NO RECORDS BUMPED TO OVFL 17332
APPT1K2  LR    R12,R13                  START AT CE1
         ST    R12,IOBSTART-1
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         MVI   IOBAPP,X'15'             SET APPENDAGE CODE TO 21
         B     APPRVXCP                 EXCP RETURN TO IOS
APPT1D5  NI    DCWWKNI,X'F7'            SET OFF 'CP12A ENDED W/EOF' SW
         B     APPT1E3
APPT1D2  CLC   0(0,R12),0(R13)          EXECUTED INSTRUCTION
APPT1E2  TM    DCBOPTCD,X'02'           WAS DELETE OPTION TAKEN
         BO    APPT1F2                  YES
APPT1E1  OI    DECBEXC1,X'01'           INDICATE DUPLICATE RECORD
         MVI   IOBASYN,X'08'
         B     APPN3B5                  SCHEDULE PROCESSING COMPLETION
APPT1F2  L     R12,DCBMSWA              CHECK FIRST DATA BYTE AT W+KL+8
         LA    R12,2(R12)                                         17332
         IC    R9,DCBKEYLE
         AR    R12,R9
         CLI   8(R12),X'FF'             IS RECORD MARKED FOR DELETION
         BNE   APPT1E1                  NO - IT IS A TRUE DUPLICATE
         LA    R13,CL1                  REPLACE DEL RCD - START AT CL1
         ST    R13,IOBSTART-1
         NI    IOBFLAG1,X'7F'           DO NOT USE DATA CHAINING
         OI    IOBFLAG1,X'40'           USE COMMAND CHAINING
         MVI   IOBAPP,X'18'             SET APPENDAGE CODE TO 24
         B     APPRVXCP                 EXCP RETURN TO IOS
         EJECT
*              ABNORMAL END CP12A,  APPENDAGE CODE 20,         CHART T1
APPT1A3  TM    IOBECBAD,PERRMASK        WAS ABEND DUE TO PERM ERROR
         BC    PERRNO,APPRVNOR          NO - NORMAL RETURN TO IOS
         TM    IOBSENSE,X'08'           WAS ABEND DUE TO DATA CHECK
         BZ    APPN3B3                  NO - RECORD IS UNREACHABLE
         LA    R13,CD2+8
         CLR   R12,R13
         BNH   APPN3B3                  NO - RECORD IS UNREACHABLE
         OI    DECBEXC1,X'08'           UNCORRECTABLE I/O ERROR
         B     APPN3B4                  SCHEDULE PROCESSING COMPLETION
         EJECT
APPT2C2  L     R12,IOBFCHAD             POINT TO CP12B SAVED ADDRESS
         LA    R9,8                                               M3216
         SR    R12,R9                                             M3216
         L     R13,0(R0,R12)            POINT TO LAST COUNT
         L     R12,DCBWKPT2
         USING IHADCW,R12                                        S20201
         TM    DCWWKNI,X'08'            DID CP12A END WITH EOF
         BZ    APPT2E2                  BZ IF NO
         MVC   IOBDADAD+4(4),1(R13)     MOVE CHHR OF LAST COUNT TO IOB
         OI    DCWWKNI,X'20'            SET ON SAME MODULE SW
         MVI   IOBAPP,X'09'             SET APPENDAGE CODE TO 9
         L     R15,DEBEXPTR              GET ADDR OF DEB         S21045
*                                       EXTENSION PTR            S21045
         USING DEBEXT,R15                                        S21045
         L     R15,DEBWKPT5              ADDRESS OF PART 2 IN    S21045
*                                       R15                      S21045
         DROP  R15                                               S21045
         MVC   DCBLPDA+7(1),IOBDADAD+7  UPDATE R IN LPDA          13494
         USING IHAPART2,R15
         B     APPN7B4                  BRANCH TO CP10A APPENDAGE
         USING APP,R15
APPT2E2  TM    CH8E+4,X'02'             RECORD BUMPED TO OVFL     17332
         BZ    APPT2J2                  IF NO-BRANCH              17332
         MVI   IOBAPP,X'04'             SET UP 2ND PART CP14      17332
         MVC   IOBSTART,CH21+4          TO UPDATE INDICES         17332
         MVC   IOBDADAD,CH14           RESTORE IOBSEEK            17332
         MVI   CH14,X'03'               CHANGE CH14               17332
         MVI   CH14+4,X'20'             TO ANOP                   17332
         MVI   CH14+7,X'01'                                       17332
         NI    CH8E+4,X'40'            RESET  FLAG                17332
         B     APPRVXCP                                           17332
APPT2J2  LA    R12,CK1                  START AT CK1
         ST    R12,IOBSTART-1
         MVC   IOBDADAD+5(3),CB23       PUT CB23 HHR IN IOB
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         LA    R12,IOBDADAD+3           PUT IOB ADDRESS IN CK1 AND CK4
         STH   R12,CK1+2
         SRL   R12,16
         STC   R12,CK1+1
         MVC   CK4+1(3),CK1+1
         L     R12,IOBFCHAD
         MVC   CK6+1(3),1(R12)          PUT KEY ADDRESS IN CK6
         MVI   IOBAPP,X'11'             SET APPENDAGE CODE TO 17
         MVC   CK7A+1(3),CK1+1          PUT IOB ADDRESS IN CK7A
         MVI   IOBCOUNT,X'0A'           SET COUNTER TO 10
         B     APPRVXCP                 EXCP RETURN TO IOS
CLILDT   CLI   DCBLDT+1,0
         EJECT
*              ABNORMAL END CP12B,  APPENDAGE CODE 21,         CHART T2
APPT2A3  TM    IOBECBAD,PERRMASK        WAS ABEND DUE TO PERM ERROR
         BC    PERRNO,APPRVNOR          NO - NORMAL RETURN TO IOS
         TM    IOBSENSE,X'08'           WAS ABEND DUE TO DATA CHECK
         BZ    APPN3B3                  NO - RECORD IS UNREACHABLE
         L     R13,IOBSTART-1           POINTER TO CE1
         USING CP12B,R13                                         S20201
         LA    R13,CE3                  POINTER TO CE3           S20201
         CLR   R12,R13
         BNH   APPN3B3                  NO - RECORD IS UNREACHABLE
         OI    DECBEXC1,X'08'           UNCORRECTABLE I/O ERROR
         B     APPN3B4                  SCHEDULE PROCESSING COMPLETION
         EJECT
*              CHANNEL  END CP12CW, APPENDAGE CODE 24,         CHART T3
APPT3B2  LA    R13,CL3C+8               DID CHAN PROG STOP AT CL3C
         CLR   R12,R13
         BNE   APPN3B3                  NO - RECORD IS UNREACHABLE
         B     APPN3B4                  SCHEDULE PROCESSING COMPLETION
         EJECT
*              ABNORMAL END CP12CW, APPENDAGE CODE 24,         CHART JB
APPJBB2  EQU   *
         TM    IOBECBAD,PERRMASK        WAS ABEND DUE TO PERM ERROR
         BC    PERRNO,APPRVNOR          NO-NORMAL RETURN TO IOS
         TM    IOBSENSE,X'08'           WAS ABEND DUE TO DATA CHECK
         BZ    APPN3B3                  NO- RECORD UNREACHABLE
         LA    R13,CL3C+8               DID CHAN PROG STOP AT CL3C
         CLR   R12,R13
         BE    APPJBE3                  YES- E3 I/O ERROR
         LA    R13,CL3+8                DID CHAN PROG STOP AT CL3
         CLR   R12,R13
         BNE   APPN3B3                  NO - RECORD IS UNREACHABLE
APPJBE3  OI    DECBEXC1,X'08'           UNCORRECTABLE I/O ERROR
         B     APPN3B4                  SCHEDULE PROCESSING COMPLETION
         EJECT
*              SET UP CP123W,AFTER CP12B (CHART J9)
APPJ9B2  L     R12,CB2+4                POINTER TO CEA
         LA    R13,IOBDADAD+3          ADDR OF IOB INTO R13
         USING CP123,R12                                         S20201
         ST    R13,CEA                  STORE SCH ARG IN CEA     S20201
         MVI   CEA,SIDEQ                RESTORE COMMAND CODE     S20201
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         MVI   IOBCOUNT,X'01'                                     17332
         L     R13,CD2+4                                         A27357
         OI    4(R13),X'40'                                      A27357
         L     R13,CH10+4         ADDR +8 OF LAST CKD IN CP12B    17332
         LA    R13,8(R13)                                         17332
         LA    R13,0(R0,R13)
         L     R12,CD2+4                POINTER TO CE1
         LA    R12,24(R0,R12)           POINTER TO CE5
         SR    R13,R12                  NO. OF WRITES TIMES 16
         SRL   R13,1                    NO. OF WRITES TIMES 8
         L     R12,CB2+FOUR             POINTER TO CP123         S20201
         USING CP123,R12                                         S20201
         LA    R12,CEB                  POINT TO CEB             S20201
         AR    R13,R12                  SET REG 13 TO 'SET OFF CC' ADDR
         NI    4(R13),X'BF'             SET OFF CC FLAG
         LA    R9,8(R0,R0)              PUT 8 IN REG 9
APPJ9B34 SR    R13,R9                   REDUCE REG 13
         CLR   R13,R12                  IS LOOP COMPLETE
         BE    APPJ9B4                  YES - EXECUTE CP123W
         OI    4(R13),X'40'             SET ON  CC FLAG
         B     APPJ9B34                 LOOP
APPJ9B4  B     APPT2B5                                            17332
         EJECT
*              CHANNEL  END CP123W, APPENDAGE CODE 22,         CHART J9
APPJ9C2  EQU   *                        *
*
         LA    R9,EQEIGHT               CCW LENGTH
         SR    R12,R9                   MOVE BACK TO LAST CCW
         TM    FIVE(R12),CC             WAS IT CHAINED
         BZ    APPJAC3                  NO, CP UNCHANGED
         NI    FIVE(R12),CCOFF          RESET INDICATOR
         OI    FOUR(R12),CC             RESET CC
         MVC   IOBSTART(THREE),CB2+FIVE SETUP TO EXCP CP123
         B     APPRVXCP                 EXCP CP123
APPJAC3  EQU   *                        *
         AR    R12,R9                   ADD BACK CCW LENGTH
*
         BAL   R11,APPRCP               RESTORE CP12B
         L     R11,IOBECBAD                                       17332
         SR    R12,R9                   POINT TO LAST OP CODE
         CLI   0(R12),X'1E'             DID CHAN PROG STOP AT A RD CKD
         BNE   APPN3B3                  NO - RECORD IS UNREACHABLE
         TM    4(R12),X'40'             WAS CC FLAG OFF
         BZ    APPT2C2                  YES - RETURN TO CP12B APPENDAGE
         B     APPN3B3                  NO - RECORD IS UNREACHABLE
         EJECT
*              ABNORMAL END CP123W, APPENDAGE CODE 22,         CHART J9
APPJ9E2  EQU   *
         TM    IOBECBAD,PERRMASK        WAS ABEND DUE TO PERM ERROR
         BC    PERRNO,APPRVNOR          NO-NORMAL RETURN TO IOS
         BAL   R11,APPRCP              RESTORE CP 12B             17332
         L     R11,IOBECBAD                                       17332
         TM    IOBSENSE,X'08'           WAS ABEND DUE TO DATA CHECK
         BZ    APPN3B3                  NO- RECORD UNREACHABLE
         L     R13,CB2+4                POINTER TO CEA
         USING CP123,R13                ESTABLISH ADDRSSABILITY  S20201
         LA    R13,CEB+ONE              POINTER TO CEB+1         S20201
         CLR   R12,R13                  DID CHAN PROG STOP AT A RD CCW
         BL    APPN3B3                  NO- N3B3
APPJ9H3  OI    DECBEXC1,X'08'           UNCORRECTABLE I/O ERROR
         B     APPN3B4                  SCHEDULE PROCESSING COMPLETION
APPRCP   L     R13,CH10+4                                         17332
         LA    R13,8(R13)                                         17332
         L     R9,CB2+4                                           17332
         LA    R9,0(R9)                                           17332
         CLR   R9,R13                                             17332
         BE    APPRCPR                                            17332
         MVC   0(4,R13),CH15A+4         RESTORE CCW              A27357
         L     R10,IOBCCWAD            RESTORE CP POINTER         17332
APPRCPR  LA    R9,8                                               17332
         BR    R11                                                17332
         EJECT
*              CHANNEL  END CP15,   APPENDAGE CODE 14,         CHART M1
APPM1B2  LA    R13,CI4+8                DID CHAN PROG STOP AT CI4
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPM1B3                  NO - RECORD IS UNREACHABLE
         L     R12,DCBWKPT2
         USING IHADCW,R12                                        S20201
         OI    DCWWKNI,X'10'            SET ADD-TO-END INDICATOR ON
         CLI   DCBFIRSH+TWO,ONE         TEST FOR SHARED TRACK    A46955
         BNH   APPM1B20                 NO                       A46955
         CLC   DCBFIRSH(2),DCBLPDA+5    END OF SHARED TRACK        7851
         BE    APPM1B2A                                            7851
APPM1B20 EQU   *                        *                        A46955
         MVC   DCWHIRPS(1),DCBHIRPD                                7851
         B     APPM1B2B                                            7851
APPM1B2A MVC   DCWHIRPS(1),DCBHIRSH                                7851
APPM1B2B EQU   *                                                   7851
         TM    DCBST,X'01'              IS LAST TRACK FULL
         BO    APPM1C2                  BO IF YES, TO SET UP CP16
         MVI   IOBAPP,X'17'             SET APPENDAGE CODE TO 23
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         LA    R12,CB46                 START AT CB46
         ST    R12,IOBSTART-1
         MVC   IOBDADAD,DCBLPDA         PUT DCBLPDA MBBCCHHR IN IOB
         MVC   CB49+1(3),DECBKEY+1      MOVE KEY  ADDRESS TO CB49
         L     R12,DECBAREA             MOVE DATA ADDRESS TO CB50
         LA    R12,16(R0,R12)
         ST    R12,CB50
         IC    R9,DCBLPDA+7             MOVE DCBLPDA CCHHR TO CB51
         LA    R12,1(R0,R9)              THEN INCREASE CB51 R BY 1
         STC   R12,CB51+4
         MVC   CB51(4),DCBLPDA+3
         LA    R12,IOBDADAD+3
         STH   R12,CB46+2
         SRL   R12,16
         STC   R12,CB46+1               MOVE IOB ADDRESS TO CB46
         MVC   CB50A+1(3),CB46+1        MOVE IOB ADDRESS TO CB50A
         MVI   IOBCOUNT,X'0A'           SET COUNTER TO 10
         B     APPRVXCP                 EXCP RETURN TO IOS
APPM1C2  EQU   *
         TM    CB26,X'08'               WAS ENTRY READ IN CB25 OFLO END
         BZ    APPM1D3                  YES  M1D3
         MVC   IOBDADAD,CB25            MOVE CB25 MBBCCHHR TO IOB
         MVI   IOBAPP,X'0F'             SET APPENDAGE CODE TO 15
         NI    IOBFLAG1,X'7F'           DONT USE DATA CHAINING
         OI    IOBFLAG1,X'40'           USE COMMAND CHAINING
         LA    R12,CJ1                  START AT CJ1
         ST    R12,IOBSTART-1
         LA    R12,IOBDADAD+3           MOVE IOB ADDRESS TO CJ1 AND CJ5
         ST    R12,CJ1
         MVI   CJ1,SIDEQ                RESTORE COMMAND CODE     S20201
         MVC   CJ5+ONE(THREE),CJ1+ONE   MOVE IN ADDR OF SEEK     S20201
         MVC   CJ3+ONE(THREE),DECBKEY+ONE   MOVE IN ADDR OF KEY  S20201
         MVC   CJ7+ONE(THREE),DECBKEY+ONE   MOVE IN ADDR OF KEY  S20201
         LA    R12,0(R0,R0)             CMP TRK INDEX AND OFLO RCD MODL
         IC    R12,CB25                  ARE THEY THE SAME
         SLL   R12,4(R0)
         L     R12,DEBUCBAD(R12)        GET UCB ADDR FROM DEB    S20201
         LA    R12,0(R0,R12)
         CLR   R12,R7
         BE    APPRVXCP                 YES - EXCP RETURN TO IOS
         MVI   IOBASYN,X'01'
         B     APPM1D4                  EXCP IN ASYNCHRONOUS ROUTINE
APPM1D3  MVI   IOBASYN,X'0A'            SET ASYNCHRONOUS CODE TO 10
APPM1D4  NI    IOBINDCT,X'CF'           OFLO RCD AND KEY IN AREA AND I
         B     APPM1B5                  SCHEDULE ASYNCHRONOUS ROUTINE
APPM1B3  OI    DECBEXC1,X'04'           RECORD IS UNREACHABLE
APPM1B4  MVI   IOBASYN,X'08'            SCHEDULE PROCESSING COMPLETION
APPM1B5  B     BYP(R14)                 SCHEDULE ASYNCH ROUTINE  Y02072
         EJECT
*              ABNORMAL END CP15,   APPENDAGE CODE 14,         CHART M1
APPM1J2  TM    IOBECBAD,PERRMASK        WAS ABEND DUE TO PERM ERROR
         BC    PERRNO,APPRVNOR          NO   CONTINUE, NORMAL IOS
         B     APPM1B3                  YES  M1B3
APPN3B21 LA    R13,APPN3B2
         BC    15,APPN3B22
APPM1B21 LA    R13,APPM1B2
APPN3B22 BR    R13                      NULL ROUTINE             Y02072
APPRVXCP EQU   *
         SR    R12,R12
         IC    R12,IOBDADAD             CURRENT EXTENT
         SLL   R12,4                    LENGTH OF 1 EXTENT = 16
         LA    R12,DEBUCBAD(R12)        ADDR OF CORRECT EXTENT   S20201
         MVC   IOBDADAD+1(2),4(R12)     MOVE BB TO IOB
         LA    R14,EXCP(R14)            SETUP FOR EXCP IOS RETURN 15924
         XC    IOBFLAG2(3),IOBFLAG2     CLEAR FLAG2,SENSE BYTES   15924
         XC    IOBCSW(9),IOBCSW         CLEAR FLAG3,CSW,AND SIOCC 15924
         XC    IOBERRCT(2),IOBERRCT     CLEAR ERROR COUNT         15924
         NI    IOBFLAG1,X'C2'           RESET FLAG1               15924
APPRVNOR BR    R14                      RETURN TO IOS             15924
EIGHT    DC    H'8'                                              A27357
*
MODID    DC    C'IGG019G3'              MODULE NAME            @ZA26522
DATE     DC    CL8'&SYSDATE'            FIX DATE               @ZA26522
FIX      DC    C' OZ26522'              LATEST FIX             @ZA26522
PATCH    DC    XL((*-IGG019G3)/20)'00'  ZEROED PATCH AREA        Y02072
*
         EJECT
*                        ADDRESS OF COMMUNICATION VECTOR TABLE
CVTPTR   EQU   16
*                             RETURN TO IOS VECTOR TABLE
CVT      DSECT
         DS    F         COMMUNICATION VECTOR TABLE EXIT EFFECTOR
CVTEXEF  DS    A                        *
IHAPART2 DS    0D                       PART 2 VECTOR TABLE
*        ROUTINE FOR          CODE      CEND COMPLETION OF
APPM67CE DS    CL6            7         CP1 OR CP2, WRITE KN
APPN7B2  DS    CL6            9,23      CP10AW
APPN8A2  DS    CL6            10,11     CP10BW
APPM4A2  DS    CL6            12        CP14W - SETUPS 1, 2, OR 5
APPM5A2  DS    CL6            13        CP14W - SET UPS 3, 4, OR 6
APPM2B2  DS    CL6            15        CP16 - SITUATION 2
APPM3B2  DS    CL6            16        CP16 - SITUATION 3
APPN9A2  DS    CL6            17,18,19  CP17W
APPAFB   DS    CL6                                                17332
*        ROUTINE FOR          CODE     ABEND COMPLETION OF
APPM67AE DS    CL6            7         CP1 OR CP2, WRITE KN
APPJ3B2  DS    CL6            9,23      CP10AW
APPJ4B2  DS    CL6            10,11     CP10BW
APPJ5B2  DS    CL6            12        CP14W -SETUPS 1, 2, OR 5
APPJ6B2  DS    CL6            13        CP14W -SETUPS 3, 4, OR 6
APPM2H2  DS    CL6            15,16     CP16
APPJ7B2  DS    CL6            17,18,19  CP17W
APPAEB   DS    CL6                                                17332
APPN7A3  DS    CL4            CONTINUATION OF ROUTINES BEGUN IN
APPN7B4  DS    CL4                 PART 1
*        ROUTINE FOR          CODE      CEND COMPLETION OF
APPABA2  DS    CL6            0,1       CP4/CP5
APPADB2  DS    CL6            2         CP7W
APPAAB2  DS    CL6            3         CP1 OR CP2, READ AND UPDATE
APPACA2  DS    CL6            5         CP6W
APPAHB2  DS    CL6            6         CP4/CP5W, WRITE CHECK
*        ROUTINE FOR          CODE     ABEND COMPLETION OF
APPABD2  DS    CL6            0,1       CP4/CP5W
APPAIB2  DS    CL6            2         CP7W
APPAAA2  DS    CL6            3         CP1 OR CP2, READ AND UPDATE
APPACE2  DS    CL6            5         CP6W
APPAHD2  DS    CL6            6         CP4/CP5W, WRITE CHECK
         EJECT
RQE      IECDRQE ,                      RQE                      Y02072
         EJECT
*        WRITE KEY NEW CHANNEL PROGRAM REFERENCES (WRITE CHECK)
IHAWKNCP IGGWKNCP  OPTCD=W                                       S20201
         IGGCP12C  OPTCD=W                                       S20201
         IGGCP12A
CP12B    DSECT
         IGGCP12B
CP123    DSECT
         IGGCP123
         SPACE 5
*              DATA EVENT CONTROL BLOCK
IHADECB  DSECT
         DS    0F
DECBECB  DS    CL4                      EVENT CONTROL BLOCK (ECB)
DECBTYP1 DS    BL1                      TYPE B6 - 1 IF LENGTH IS S
*                                            B7 - 1 IF AREA IS S
DECBTYP2 DS    BL1                           B0 - 1 IF READ K
*                                            B1 - 1 IF READ KX
*                                            B2 - 1 IF READ KU
*                                            B4 - 1 IF WRITE K
*                                            B5 - 1 IF WRITE KN
DECBLGTH DS    CL2                      LENGTH OF BLOCK
DECBDCBA DS    A                        POINTER TO DCB
DECBAREA DS    A                        ADDRESS OF AREA
DECBLOGR DS    A                        POINTER TO LOGICAL RECORD
DECBKEY  DS    A                        POINTER TO KEY
DECBEXC1 DS    BL1                      EXCPTN CD B0-RECORD NOT FOUND
*                                                 B1-RECORD LGTH CHK
*                                                 B2-NO SPACE
*                                                 B3-INVALID REQUEST
*                                                 B4-UNCORRECTABLE IO
*                                                 B5-UNREACHABLE BLOCK
*                                                 B6-OVERFLOW RECORD
*                                                 B7-DUPLICATE
DECBEXC2 DS    BL1                                B7-READ KU
         EJECT
         DCBD  DSORG=(IS)
         EJECT
IHAIOB   IGGIOBD
         EJECT
IHADCW   IGGBISAM
         EJECT
IHADEB   IGGDEBD
         END
