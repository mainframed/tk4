         TITLE 'IGG01950 IND OFLOW VALIDATION FOR VLR'           S20201
IGG01950 CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG01950                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, VALIDATION ROUTINE FOR VLR            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = IF DCB OPENED FOR SHR, AND M OF DFALPDA IS NOT 0, XCTL   *
*            TO NEXT EXECUTOR.  OTHERWISE, IF INDEPENDENT OVERFLOW IS *
*            SPECIFIED:  INITIALIZE AND EXECUTE CHANNEL PROGRAM       *
*            VXCCW1B TO READ EOF OR END OF DS2LOVAD TRACK FOR         *
*            INDEPENDENT OVERFLOW RECORDS; UPDATE DS2RORG2, DS2OVRCT, *
*            AND DS2LOVAD; ERASE UNUSED PORTION OF TRACK; AND UPDATE  *
*            DS2BYOVL.                                                *
*                                                                     *
*            THEN, WHETHER IND. OV. SPECIFIED OR NOT:  INITIALIZE     *
*            AND EXECUTE CP VXCCW1B AGAIN TO READ EOF OR END OF       *
*            DS2LPRAD TRACK FOR PRIME DATA RECORDS; UPDATE DS2LPRAD;  *
*            AND IF DCB OPENED FOR SHR, AND M OF DFALPDA IS 0, MERGE  *
*            F2 DSCB FIELDS INTO FIELD AREA; AND, IF ENTERED FROM     *
*            RESTART, MERGE F2 DSCB FIELDS INTO DCB.                  *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 1115 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG01950                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM RESTART MODULE IGC0W05B, OR FROM *
*              ISAM OPEN EXECUTOR IGG01921 FOR RESUME LOAD, OR        *
*              IGG0192C FOR BISAM OR SCAN MODE.  RECEIVES CONTROL IN  *
*              STORAGE PROTECT KEY 5 AND PRIVILEGED STATE.            *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
*         HOWEVER, IF ENTERED FROM RESTART MODULE IGC0W05B, REGISTERS *
*         ARE ESTABLISHED AS FOLLOWS:                                 *
*         REG 1 (RVAL) POINTS TO THE RESTART AREA                     *
*         REG 4 (RCORE) POINTS TO THE OPEN WORK AREA                  *
*         REG 7 (RPARC) IS ZERO                                       *
*         REG 8 (RWTGC) POINTS TO THE RESTART TABLE                   *
*         REG 9 (RDEB) POINTS TO THE CURRENT RESTART ENTRY            *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = IF ENTERED FROM RESTART, RETURN VIA XCTL TO RESTART   *
*               MODULE IGC0W05B.  OTHERWISE XCTL TO ISAM OPEN         *
*               EXECUTOR IGG0192W FOR BISAM MODE, IGG01928 FOR SCAN   *
*               MODE, OR IGG0196C FOR RESUME LOAD MODE.               *
*                                                                     *
* EXIT-ERROR = ABEND CODES:                                           *
*              033 - I/O ERROR TRYING TO READ EOF OR EOT              *
*                                                                     *
*              IF ENTERED FROM RESTART, DO NOT ISSUE ABEND, BUT XCTL  *
*              TO IGC0W05B WITH ABEND CODE IN REGISTER 1.             *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = RESIDENT CONVERT ROUTINE                              *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                                                                     *
*    CONTROL-BLOCKS = DCB, DCBFA, DEB, DSCB, IOB, AND CVT.            *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = ABEND, EXCP, WAIT, AND XCTL.                               *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*          RELEASE OS/VS2-02 DELETIONS                                *
*                                                               YM03922
*              OS/VS2 RELEASE 030 CHANGES
* D482000-484000,C480000,A703800                                OZ04716
*                                                                     *
*A702200,702830,702830                                         @ZA06668
*C702250                                                       @ZA06668
*              CHANGES SINCE VS2-037
*A237100-237800                                                @ZA17080
*C238420                                                       @ZA24295
*A317600                                                       @ZA30069
*C237420,A222800,A237430-237470                                @ZA31637
*C237620,A237520                                               @ZA33836
***********************************************************************
         EJECT
DS2DSECT DSECT           FORMAT 2 DSCB
         DS    0D
DS2KEY   DS    CL44
DS2FMTID DS    CL1
DS2NOLEV DS    CL1
DS2DVIND DS    CL1
DS21RCYL DS    CL3
DS2LTCYL DS    CL2
DS2CYLOV DS    CL1
DS2HIRIN DS    CL1
DS2HIRPR DS    CL1
DS2HIROV DS    CL1
DS2RSHTR DS    CL1
DS2HIRTI DS    CL2
DS2TAGDT DS    CL2
DS2RORG3 DS    CL3
DS2NOBYT DS    CL2
DS2NOTRK DS    CL1
DS2PRCTR DS    CL4
DS2STIND DS    CL1
DS2CYLAD DS    CL7
DS2ADLIN DS    CL7
DS2ADHIN DS    CL7
DS2LPRAD DS    CL8
DS2LTRAD DS    CL5
DS2LCYAD DS    CL5
DS2LMSAD DS    CL5
DS2LOVAD DS    CL8
DS2BYOVL DS    CL2
DS2RORG2 DS    CL2
DS2OVRCT DS    CL2
DS2RORG1 DS    CL2
         DCBD  DSORG=IS                                          A34959
FORCORE  DSECT
         IECDSECT
*                       CP TO READ TO EOF                        A25984
         ORG   DXCCW                                             A25984
VXCCW1   DS    BL8                 SRCH ID EQ
VXCCW2   DS    BL8                     TIC
VXCCW3   DS    BL8                 RD CKD
VXCCW4   DS    BL8                 RD CKD
VXCCW5   DS    BL8                 TIC
VXCCW6   DS    BL8                     COUNT FLD
VXCCW7   DS    BL8                 COUNT FLD
VXCCW8   DS    BL8                 NEW MBBCCHHR
SAVEREG  DS    CL8                     REG. SAVE AREA
         DS    CL4                     OLD TTR
SAVER2   DS    CL2                     NEW R VALUE
SAVER1   DS    CL2                     OLD R VALUE
         SPACE 4
IHADEB   IGGDEBD                                                 Y02072
         SPACE 4
R0       EQU   0                        WORK REG
RVAL     EQU   1                        BASE REG FOR VALIDATE CP
RDSCB    EQU   2                        BASE REG FOR DSCB FORMAT 2
RBASE    EQU   3                        BASE REG FOR THIS MODULE
RCORE    EQU   4                        BASE REG FOR OPEN WORK AREA
RPAR     EQU   5         *              ADDR OF PARAMETER TABLE
RWTG     EQU   6         *              ADDR OF WHERE-TO-GO TABLE
RPARC    EQU   7         *              CURRENT PARAMETER ENTRY
RWTGC    EQU   8         *              CURRENT WHERE-TO-GO ENTRY
RRSTC    EQU   9                        CURRENT RESTART ENTRY    A44475
RDEB     EQU   9                        DEB PTR
RA       EQU   10                       WORK REG
RB       EQU   11                       WORK REG
RC       EQU   12                       WORK REG
RD       EQU   13                       WORK REG
RE       EQU   14                       WORK REG
RF       EQU   15                       WORK REG
RJ       EQU   15
R1       EQU   1
*
*
CVTPTR   EQU   16                       LOCATION OF THE CVT PTR
SHR      EQU   X'80'                    TEST FOR DISP EQ SHR     Y02072
L3BYTE   EQU   7                        STORE 3 LOW ORDER BYTES  Y02072
LOAD     EQU   X'18'                                             A34959
DCBFA    IGGDCBFA
BLOCKED  EQU   X'10'                    DCBRECFM FOR BLOCKED   @ZA31637
         EJECT                                                   A25984
IGG01950 CSECT                                                   A25984
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         USING FORCORE,RCORE
         USING DS2DSECT,RDSCB
         USING IHADCB,RB                                         A25984
         LTR   RPARC,RPARC              CHECK FOR RESTART ENTRY  A25984
         BNZ   OPEN                     BRANCH IF NOT            A25984
         STM   RDSCB,RA,DXDSCB          SAVE REG 2-10 IN F1 DSCB A25984
         L     RB,0(RRSTC)              GET CURRENT DCB ADDR     A44475
         LA    RDSCB,DXJBF              POINT TO FORMAT 2 DSCB   A25984
         B     COMMON                                            A25984
OPEN     EQU   *                                                 A25984
         L     RB,0(RPARC)              SET DCB ADDR
         L     RCORE,4(RWTGC)           SET OPEN WK AREA ADDR
         L     RDSCB,DSCCORE            SET PTR TO F2 DSCB       A25984
         CLC   DCBKEYLE,DSCKEYL         IS KEY EQUAL ?         @ZA17080
         BNE   COMMON1                  NO ABEND               @ZA17080
         CLI   DCBRECFM,X'00'           IS RECFM 0?            @ZA17080
         BE    COMMON1                  YES, ABEND             @ZA17080
         IC    RF,DCBRECFM              SAVE RECFM             @ZA17080
         NI    DCBRECFM,X'FE'           RESET KEYL BIT         @ZA17080
         CLC   DCBRECFM,DSCRECFM        DOES DCB RECFM EQUAL   @ZA17080
*                                       DSCB1 RECFM ?          @ZA17080
         STC   RF,DCBRECFM              RESTORE DCBRECFM       @ZA17080
         BNE   COMMON1                  NO-GO ABEND            @ZA31637
         TM    DSCRECFM,BLOCKED         RECORDS BLOCKED?       @ZA31637
         BNO   COMMON                   NO-GO AROUND ABEND     @ZA31637
         CLC   DCBBLKSI,DSCBLKL         DOES DCB BLKSIZE EQUAL @ZA31637
*                                       DSCB BLKSIZE?          @ZA31637
         BE    COMMON                   YES-GO AROUND ABEND    @ZA31637
COMMON1  LR    RDSCB,RB                 DCB PTR FOR PROB DETER @ZA33836
         ABEND X'03B',DUMP,,SYSTEM      NOT EQUAL ABEND.       @ZA33836
COMMON   EQU   *                                                 A25984
         L     RDEB,DCBDEBAD            SET DEB BASE             A44475
         USING IHADEB,RDEB              DEB ADDRESSABILITY       Y02072
*
*   IF DATA SET HAS BEEN OPENED FOR SHR, DEBDCBFA WILL CONTAIN
*        A POINTER TO THE DCBFA.  IF  M IS NONZERO IN DFALPDA,
*        DO NOT VALIDATE AS IT HAS BEEN DONE.
*
         L     RA,DEBEXPTR              PTR TO DEB EXTENSION   @ZA24295
         USING DEBEXT,RA                EXTNSION ADDRSSABILITY   Y02072
         TM    DEBDCBFA,SHR             TEST FOR DISP EQ SHR     Y02072
         BZ    CNTINU                   BR TO CONTINUE RTN IF    A35340
*                                       NOT                      A35340
         L     RA,DEBDCBFA              ADDR OF FIELD AREA       Y02072
         USING DCBFA,RA                                          A37175
         CLI   DFALPDA,X'00'                                     A37175
         BNZ   NOEXEC                   BR TO END OF MODULE      A37175
CNTINU   EQU   *                                                 A35340
*
         LA    RVAL,DXCCW1              POINT TO WORKAREA CCWS   A25984
         ST    RB,IOBWGHT               SET DCB ADDR IN PHONY IOB
*INITIALIZE CHANNEL PROG TO READ EOF
*
         LM    RA,RF,VALCCW1
         LA    R0,DXDAADDR+3            POINT TO CCHHR           A25984
         ALR   RA,R0               OF DS2LOVAD                    
         ALR   RC,RVAL                  RELOCATE ADDR FLDS IN
         ALR   RE,RVAL                     CCW2 AND CCW3
         STM   RA,RF,VXCCW1             INIT.VXCCW1,2,3
         LM    RA,RD,VALCCW4
         ALR   RA,RVAL                  RELOCATE ADDR FLDS IN
         ALR   RC,RVAL                     CCW4 AND CCW5
         STM   RA,RD,VXCCW4             INIT.VXCCW4,5
*
*
         CLI   DS2LOVAD,X'00'      ANY OVERFLOWS IN IND OFL       
         BE    NOOVRFL             NO NO NEED TO UPDATE           
         MVC   DXDAADDR,DS2LOVAD        SET UP IOB SEEK ADDR
*
         BAL   RC,EOFREAD               GO READ EOF FOR          A35340
*                                       INDEPENDENT              A35340
*                                         OVERFLOW
TESTIO   EQU   *                                                 A27811
         TM    IOBSTAT0,X'01'           END OF FILE
         BO    SETLOVAD                 YES
         CLI   IOBECBPT,X'42'           END OF EXTENT            A27811
         BNE   GOABEND                  NO, BR--I/O ERROR        A27811
         SR    RA,RA                    ELSE GO TO NEXT EXTENT   A27811
         IC    RA,DXDAADDR              GET M FROM IOB           A27811
         LA    RA,1(R0,RA)              ADD ONE FOR NEXT EXTENT  A27811
         STC   RA,DXDAADDR              PUT M+1 IN IOBSEEK       A27811
         CLC   DXDAADDR(1),16(RDEB)     DEBNMEXT VS NEXT M       A27811
         BE    NOOVRFL                  BR IF NO MORE EXTENTS    A27811
         SLL   RA,4                     M TIMES 16               A27811
         LA    RA,32(RA,RDEB)           NEW EXTENT               A27811
         MVC   DXDAADDR+3(4),6(RA)      MOVE CCHH TO IOB         A27811
         MVI   DXDAADDR+7,X'00'         START AT FIRST RECORD    A27811
         BAL   RC,EOTREAD               CHECK NEXT EXTENT        A35340
*                                       A2781                    A35340
         B     TESTIO                   CHECK CP RESULT          A27811
*                                                                 
GOABEND  L     R1,ABENDCD                  PICK UP ABEND CODE     
         LTR   RPARC,RPARC              IS RESTART IN CONTROL    A25984
         BZ    RESTARTX                 DON'T ABEND IF SO        A25984
         L     RCORE,DXUDCBAD           USERS DCB ADDRESS      @ZA30069
         ABEND X'033',DUMP,,SYSTEM      SYSTEM 033 ABEND         Y02072
*
SETLOVAD LA    RB,SETLIOV2
         LA    RE,NOOVRFL
         BAL   RF,STOPCHK
*
* SET UP NEW MBBCCHHR
SETLIOV1 MVC   VXCCW8+3(5),VXCCW6       CCHHR FROM 1ST CT FLD
         B     SETLIOV3
SETLIOV2 MVC   VXCCW8+3(5),VXCCW7       CCHHR FROM 2ND CT FLD
SETLIOV3 MVC   VXCCW8(3),DS2LOVAD       MBB INIT.SAME AS DSCB
*
NEWADDR  L     RC,CVTPTR               LOAD CVT POINTER
         L     RF,32(RC)               LOAD ADDR OF CONVERT RT
         STM   RDSCB,RBASE,SAVEREG     SAVE REG.2,3
         LR    RBASE,RVAL              SAVE REG.1
         LR    R1,RDEB                  SET PTR TO DEB
         LA    RDSCB,DS2LOVAD           SET PTR TO MBBCCHHR INPUT
         BALR  RE,RF                    CONVERT MBBCCHHR TO TTR
         LR    RDEB,R1                 RESTORE DEB PTR.
         LR    RVAL,RBASE              RESTORE REG.1
         ST    R0,SAVEREG+8            SAVE OLD TTR
         LA    RDSCB,VXCCW8             SET PTR TO MBBCCHHR INPUT
         LR    R1,RDEB                 SET DEB PTR FOR CONVERT RT
         L     RC,CVTPTR               LOAD CVT PTR
         L     RF,32(RC)               LOAD ADDR OF CONVERT RT
         BALR  RE,RF                    CON VERT MBBCCHHR TO TTR
         LR    RDEB,R1                  RESET DEB POINTR          
         LR    RVAL,RBASE              RESTORE REG.1
         LM    RDSCB,RBASE,SAVEREG     RESTORE REG.2,3
         L     RA,SAVEREG+8             RA=OLD TTR. R0=NEW TTR
* UPDATE # OF UNUSED TRKS IN INDEPENDENT OVRFLO
*
         STH   R0,SAVER2                SAVE NEW R VALUE
         STH   RA,SAVER1                SAVE OLD R VALUE
         SRL   R0,16                    SHIFT TO LOW ORDER 2 BYTE
         SRL   RA,16                    SHIFT TO LOW ORDER 2 BYTE
         SLR   R0,RA                    R0=NEW TT-OLD TT
         LR    RC,R0                   LOAD RC WITH DIFF.OF TTS
         LH    RB,DS2RORG2             OLD NO.UNUSED TRKS
         CLC   SAVER2(1),SAVER1        NEW R EQUAL OLD R
         BE    UPDATE1                 YES
         CLC   SAVER2(1),DS2HIROV      NEW R EQUAL HI R FOR OVFL
         BNE   UPDATE1                 NO
         LA    RC,1(RC)                ADD1 TO DIFFERENCE
UPDATE1  SR    RB,RC                   SUBTRACT DIFF, FROM NO. OF
*                                         UNUSED TRKS
         STH   RB,DS2RORG2              UPDATE DS2RDRG2
*
* UPDATE # OF RECORDS IN INDEPENDENT OVRFLO
*
         LR    RD,R0                    RD=# TRKS DIFF.
         SR    RC,RC                    CLEAR REG
         IC    RC,DS2HIROV              RC=NO. REC/TRK
         MR    RC,RC                    RD=NO.REC.DIFFERENCE
         SR    RA,RA                    CLEAR REG
         SR    RB,RB
         IC    RA,SAVER2                PICK UP R2
         IC    RB,SAVER1                PICK UP R1
         SR    RA,RB                    RA=NO.REC.DIFFERENCE
         AR    RD,RA                    RD=TOTAL NO.REC.DIFF.
         LH    RC,DS2OVRCT
         AR    RC,RD                   ADD TO NO OF REC IN OVRFLO
         STH   RC,DS2OVRCT              UPDATE DS2OVRCT
*
         XC    VXCCW8+1(2),VXCCW8+1    ZERO BB
         MVC   DS2LOVAD,VXCCW8          UPDATE DS2LOVAD
*                                                                 
*        UPDATE DS2BYOVL NUMBER OF BYTES REMAINING ON LST TRK     
*                                                                 
UPDATE30 LA    RA,VXCCW1                                          
         ST    RA,IOBSTART-1      SET UP IOB                      
         CLC   VXCCW6+2(2),VXCCW7+2 LAST REC & EOF ON SAME TRK    
         BNE   UPDATE32           NO SRCH ON LAST OVFLOW REC      
UPDATE31 LH    RA,VXCCW8+6        ADD 1 TO R TO POINT TO EOF      
         LA    RA,1(RA)           & SEARCH ON EOF REC             
         STH   RA,VXCCW8+6                                        
UPDATE32 EQU   *                                                 A25984
         MVC   VXCCW3(16),VALERASE     MOVE IN ERASE COMMAND      
         LA    RA,VXCCW4               ERASE COUNT IS 32K       OZ04716
         STCM  RA,SEVEN,VXCCW3+1       SET UP VXCCW4 PTR        OZ04716
         MVC   DXDAADDR(8),VXCCW8  SET UP SEEK ADDR               
*                                                                 
         BAL   RC,EOFREAD         ERASER REMAINDER OF TRK        A35340
*                                        
*                                                                 
         TM    IOBSENS1,X'40'     TRK OVERRUN                     
         BNO   GOABEND                  NO, TROUBLE, ABEND       M4045
*                                                                 
*        DS2BYOVL = CCW COUNT - RESIDUAL                          
*                                                                 
UPDATE33 LH    RA,VXCCW4+6        PICK UP CCW COUNT               
         LH    RC,IOBCSW+6        PICK UP RESIDUAL                
         SR    RA,RC              GET DIFFERENCE                  
         SH    RA,DEVTABLE                                       M4045
         BNM   UPDATE                                            M4045
         SR    RA,RA                                             M4045
UPDATE   EQU   *                                                 M4045
         STH   RA,DS2BYOVL        UPDATE FMT 2 FIELD              
*                                                                 
         B     NOOVRFL
*
* DETERMINE WHICH CCW THE CHANNEL PROG STOPPED ON;THE ONLY VALID STOPS
* ARE ON CCW4 OR CCW5
*
*
STOPCHK  L     RC,IOBCOMAD              ADDR OF NXT CCW TO BE EXEC.
         LA    RC,0(RC)                 CLEAR HI ORDER BYTE
         LA    RD,VXCCW4                ADDR OF CCW4
         CR    RC,RD                    NXT CCW IS VXCCW4
         BNE   SNDSTOP                  NO
         NC    VXCCW7(8),VXCCW7         2ND COUNT FLD EMPTY
         BCR   8,RE                     YES,DSCB PTR CORRECT     A35340
FRSTOUT  BR    RB                       NO,UPDATE DSCB PTR TO 2ND CNT
SNDSTOP  LA    RD,VXCCW5                ADDR OF CCW5
         CR    RC,RD                    NXT CCW IS VXCCW5
         BCR   8,RF                     YES,UPDATE DSCB PTR TO   A35340
*                                       1ST CNT                  A35340
         B     GOABEND                  ANY OTHER STOP IS ERROR
EOFREAD  SR    RA,RA                    CLEAR WORK REG.           16068
         IC    RA,DXDAADDR              GET M FROM IDB
         SLL   RA,4                     M X 16
         LA    RA,32(RA,RDEB)           POINT TO EXTENT OF M
EOTREAD  MVC   DXDAADDR+1(2),4(RA)      MOVE BB INTO IOB          16068
         XC    VXCCW6(16),VXCCW6        INIT.VXCCW6,7 TO ZERO    A35340
*                                        1606                    A35340
         LR    RB,RVAL                  SAVE REG.1
*
         MVC   SAVEREG(4),28(RDEB)      SAVE APP VECT TBL PTR    A25984
         MVC   29(3,RDEB),DXDEBAPP+1    POINT TO DUMMY VECT TBL  A25984
         L     RVAL,IOBWGHT             ADDR OF COPY DCB         Y02072
         STCM  RVAL,L3BYTE,DEBDCBAD+1   DEB PT TO DCB COPY       Y02072
         EXCP  DXIOB                    READ TO EOF
         WAIT  1,ECB=DXECB
*
         L     RVAL,DXUDCBAD            USERS DCB ADDRESS        Y02072
         STCM  RVAL,L3BYTE,DEBDCBAD+1   DEB PT TO USER DCB       Y02072
         MVC   28(4,RDEB),SAVEREG       RESTORE VECTOR TABLE PTR A25984
         LR    RVAL,RB                  RESTORE REG.1
         BR    RC                                                A35340
**                                                                
*        UPDATE DS2LPRAD FROM LAST NORM INDEX ENTRY               
*                                                                 
NOOVRFL  MVC   VXCCW8(5),DS2LPRAD  SET UP MBBCC OF INDEX          
         MVC   VXCCW8+5(3),DS2LTRAD+2   SET   HHR                 
         MVC   DXDAADDR(8),VXCCW8  SET UP IOB ADDR                
         LA    RA,VXCCW6                                          
         ST    RA,VXCCW3                                          
         MVI   VXCCW3,X'06'        SET CCW TO READ DATA           
         MVC   VXCCW3+4(4),VALPDACT  SET UP COUNT                 
*                                                                 
         BAL   RC,EOFREAD                                        A35340
*                                        
*                                                                 
         CLI   IOBECBPT,X'7F'      ANY ERRORS                     
         BNE   GOABEND             YES                            
*                                                                 
         MVC   DS2LPRAD(8),VXCCW6  RESET LPRAD                    
*                                                                 
         L     RB,IOBWGHT               RESTORE DCB ADDRESS      A35340
         LA    RA,DXDCB                 ADDR OF PHONY DCB
         ST    RA,IOBWGHT
NOEXEC1  EQU   *                                                 A35340
         L     RA,DEBEXPTR              PTR TO DEB EXTENSION     Y02072
         USING DEBEXT,RA                EXTNSION ADDRSSABILITY   Y02072
         TM    DEBDCBFA,SHR             TEST FOR DISP EQ SHR     Y02072
*                                       VALIDATED                A35340
         BNO   NOEXEC                   BR IF NOT TO EXIT        A35340
         L     RA,DEBDCBFA              FIELD AREA ADDRESS       Y02072
         USING DCBFA,RA                                          A35340
         CLI   DFALPDA,X'00'                                     A37175
         BNZ   NOEXEC                                            A37175
         MVC   DFARORG3+L'DFARORG3-L'DS2RORG3(L'DS2RORG3),DS2RORG3
         MVC   DFANREC,DS2PRCTR                                  A35340
         MVC   DFAST,DS2STIND                                    A35340
         MVC   DFALPDA,DS2LPRAD                                  A35340
         MVC   DFANBOV,DS2BYOVL         NO. BYTES REMAINING ON  YM03922
*                                       CURRENT OVERFLOW TRACK  YM03922
         MVC   DFARORG2,DS2RORG2        FREE TRKS IN IND OVRFLW YM03922
         MVC   DFANOREC,DS2OVRCT        NO. RCDS IN OVERFLOW    YM03922
         MVC   DFALIOV,DS2LOVAD                                  A35340
         MVC   DFARORG1,DS2RORG1                                 A35340
NOEXEC   EQU   *                                                 A35340
         LTR   RPARC,RPARC              IF REGISTER IS ZERO,     A25984
         BZ    RESTART                  ENTRY WAS FROM RESTART   A25984
         TM    DCBMACRF,X'40'           TEST IF GET              A35340
         BO    SCANNEXT                                          A35340
         TM    DCBMACRF+1,LOAD          VLR RESUME LOAD          A34959
         BZ    BISAMNXT                 NO, BISAM                A34959
         MVC   0(L'LOAD6C,RWTGC),LOAD6C INITIALIZE WHERE-TO-GO   Y02072
         B     RELOOP                                            A34959
BISAMNXT EQU   *                                                 A34959
         MVC   0(L'LOAD2W,RWTGC),LOAD2W INITIALIZE WHERE-TO-GO   Y02072
         TM    DCBMACRF,SCAN            IS IT SCAN               S20201
         BZ    RELOOP                   NO, BRANCH               S20201
SCANNEXT MVC   0(L'LOAD28,RWTGC),LOAD28 INITIALIZE WHERE-TO-GO   Y02072
***********************************************************************
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH=BEGINNING OF THIS MODULE
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BC    7,RELOOP                 BRANCH=NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE REPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BC    7,TCTLRTN                BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         LA    RJ,DXCCW12
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
RESTART  SR    RVAL,RVAL                ZERO RETURN CODE         A25984
         MODESET  KEYADDR=DXUKEY,WORKREG=1 CHANGE TO USER KEY  @ZA06668
         L     RB,DXUDCBAD              GET USER DCB ADDR      @ZA06668
         MVC   DCBNBOV(2),DS2BYOVL      MERGE BYTES ON OVERFLOW  A25984
         MVC   DCBLPDA(8),DS2LPRAD      MERGE FORMAT 2 FIELDS    A25984
         MVC   DCBST(1),DS2STIND        INTO DCB.                A25984
         MVC   DCBLIOV(8),DS2LOVAD                               A25984
         MVC   DCBNOREC(2),DS2OVRCT                              A25984
         MVC   DCBRORG2(2),DS2RORG2                              A25984
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY @ZA06668
         SR    RVAL,RVAL                RESTORE REG 1          @ZA06668
RESTARTX EQU   *                                                 A42870
         LA    RWTG,DXDSCB+464          SET UP WORKAREA FOR XCTL A25984
         MVC   0(8,RWTG),IGC0W05B       RESTART MODULE NAME      A25984
         ST    RWTG,DXDSCB+16           SAVE RWTG FOR RESTORE
         LM    RDSCB,RA,DXDSCB                                   A42870
         LA    RJ,8(RWTG)               SET UP REG FOR XCTL      A25984
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) RETURN TO RESTART  Y02072
SCAN     EQU   X'40'                    SCAN DCB                 S20201
FIVE     EQU   5                        MISC OFFSET              S20201
SEVEN    EQU   7                        CONSTANT 7              OZ04716
* CONSTANTS
IGC0W05B DC    C'IGC0W05B'              ISAM RESTART MODULE      A25984
THISLOAD DC    C'50'                                              VLR
OPNLOD7  DC    C'0S'
*        THE FOLLOWING ARE CONSTANTS THAT MUST BE SUBTRACTED
*        FROM TRK CAPACITY TO ALLOW FOR POSSIBLE VARIATIONS IN
*        THE VARIOUS DEVICES
DEVTABLE DC    H'422'                                            M4045
         DS    0F                                                S20201
ABENDCD  DC    X'80033000'             ABEND 33, DUMP OPTION ON
VALERASE DC    X'11'              ERASE COMMAND                   
         DC    X'000000'                                          
         DC    X'A0000008'                                        
VALERCT2 DC    X'11000001'                                        
VALERCT  DC    X'20007FFF'                                        
*                                                                 
VALPDACT DC    X'20000008'                                        
VALCCW1  CCW   X'31',000,X'40',5        SRCH ID EQ   LPDA+3,5,CC
VALCCW2  CCW   X'08',000,X'00',0        TIC          VALCCW1
VALCCW3  CCW   X'9E',040,X'60',8       RD CKD(MT) VALCCW6,8,CC,SK
VALCCW4  CCW   X'9E',048,X'60',8       RD CKD(MT) VALCCW7,8,CC,SK
VALCCW5  CCW   X'08',016,X'00',0        TIC          VALCCW3
*
LOAD2W   DC    C'2W'                    ID OF MODULE IGG0192W    Y02072
LOAD28   DC    C'28'                    ID OF MODULE IGG01928    Y02072
LOAD6C   DC    C'6C'                    ID OF MODULE IGG0196C    Y02072
*
MODID    DC    CL8'IGG01950'            MODULE NAME            @ZA17080
DATE     DC    CL8'&SYSDATE'            DATE LAST CHANGED'     @ZA17080
FIX      DC    C'OZ33836'               LATEST FIX             @ZA30069
PATCH    DC    XL((*-IGG01950)/20)'00'  ZEROED PATCH AREA        Y02072
         END
