         TITLE 'IGG019G0 - APPENDAGES-PART1,UNBL,SYS WA,NO WRT CHK'
IGG019G0 CSECT
*          RELEASE OS/VS2-02 DELETIONS                                *
*          RELEASE 19 DELETIONS                                       *
*          RELEASE 20 DELETIONS                                       *
*2331018650-018700,018750,018800,038800-039000,039200-040000,    S20201
*2331047400-048200,050400,051400,051600-052000,054600,059800,    S20201
*2331087200-087500,117000,117200-118200,135000,135200,135400,    S20201
*2331135600-162200,167000,167200-171000,171200-175000,175200-    S20201
*2331194400                                                      S20201
*2331078200-078400                                               A38857
*2331044000,044200,062410,062420,062430,062440,062450,062460,    A31323
*2331062470,062480,062490,062500,062510,062520,062530,062600,    A31323
*2331062800,063000,065800,079600-080260,083600-085000,099200-    A31323
*2331099860                                                      A31323
*2331070200,085800-086000,086400-089000,089600,104800-105000,    A34922
*2331105400-108000                                               A34922
*2331085700,086000-086100,086500                                 A38537
*          RELEASE 21 DELETIONS                                       *
*1202104700,105000-105100,105800                                 A44495
*1202021800,022800                                               S21045
*1202                                                            A33533
*1202                                                            A46955
*XXXX                                                            A49403
*1202021600                                                      A50670
*D043980-044000,A043990-044086                                  SA53815
*           CHANGES SINCE VS2-3.7                                     *
*A023800-023890,C023920                                        @ZA26522
*                                                                     *
*STATUS CHANGE LEVEL 007
*
* FUNCTION/OPERATION- APPENDAGE ROUTINES FOR BISAM
*        WHEN WRITE KN IS USED WITH OR WITHOUT READ AND UPDATE
*        WHEN THE USER HAS NOT SUPPLIED A READ-IN AREA,
*        WHEN WRITE VALIDITY CHECKING IS NOT REQUESTED,
*        AND RECORD FORMAT IS UNBLOCKED.
*        (PART 1)
*
*
*
*        CEND  - IF CHANNEL PROGRAM ENDS
*                **WITHOUT ERROR, EITHER
*                    PROCESSING COMPLETION WILL BE SCHEDULED,
*                    PROCESSING CONTINUATION WILL BE SCHEDULED,
*                    OR PROCESSING WILL CONTINUE.
*                **WITH A LENGTH CHECK, TESTS DETERMINE WHETHER
*                    OR NOT TO TREAT THE SITUATION AS A REAL
*                    ERROR.
*                **ANY OTHER ERROR, IN GENERAL, 'UNREACHABLE
*                    BLOCK' IS INDICATED IN THE DECB, AND
*                    PROCESSING COMPLETION WILL BE SCHEDULED.
*
*        ABEND - IF CHANNEL PROGRAM ENDS ABNORMALLY BECAUSE OF
*                **PERMANENT ERROR, 'UNCORRECTABLE I/O ERROR'
*                    IS INDICATED IN THE DECB, AND PROCESSING
*                    COMPLETION WILL BE SCHEDULED.
*                **NON-PERMANENT ERROR, ONE RE-TRY IS MADE
*                    BEFORE INDICATING A PERMANENT ERROR.
*                **FILE PROTECTION, TESTS DETERMINE WHETHER
*                    OR NOT TO TREAT THE SITUATION AS A REAL
*                    ERROR.
*                **ANY OTHER ERROR, IN GENERAL, 'UNREACHABLE
*                    BLOCK' IS INDICATED IN THE DECB, AND
*                    PROCESSING COMPLETION WILL BE SCHEDULED.
* ENTRY POINTS-
*        ENTRY POINT               FUNCTION
*        -----------               --------
*        APPCEND                   CEND
*        APPABEND                  ABEND
*
* INPUT - N/A
* OUTPUT - N/A
* EXTERNAL ROUTINES - N/A
* EXITS- 1. RETURN TO IOS VIA THE APPENDAGE RETURN VECTOR TABLE POINTED
*            TO BY REGISTER 14.  SEE THE DSECT LABELED    APPRV FOR THE
*            FORMAT OF THIS TABLE AND THE USE OF EACH EXIT.
*           USED TO CONTINUE PROCESSING.
*        2. SCHEDULE AN ASYNCHRONOUS ROUTINE - BRANCH VIA THE EXIT
*            EFFECTOR ADDRESS IN THE COMMUNICATION VECTOR TABLE.
*           USED TO SCHEDULE PROCESSING COMPLETION OR CONTINUATION.
*        3. IF THE DESIRED APPENDAGE ROUTINE IS IN PART 2, BRANCH TO
*            THAT ROUTINE VIA THE PART 2 VECTOR TABLE.  THE POINTER TO
*            THE MODULE IS IN DCBWKPT5.  SEE THE DSECT LABELED
*            IHAPART2 FOR RELATIVE ADDRESSES AND USES OF THE ROUTINES.
*
* TABLES/WORK AREAS - DECB, DCB, IOB AND EXTENTION, DCB WA (DCW), DEB,
*        CVT, PART 2 VECTOR TABLE.
*        SEE DSECTS AT END OF MODULE FOR FORMAT AND DESCRIPTIONS.
*
* ATTRIBUTES- REENTRANT. DISABLED.
* NOTES - NONE
         EJECT
* GENERAL REGISTERS ARE USED AS FOLLOWS
*
R0       EQU   0                   *    -----
R1       EQU   1                   *    12* ADDRESS
R2       EQU   2                   *    IOB
R3       EQU   3                   *    DEB
R4       EQU   4                   *    DCB
BASEXXXX EQU   5                        BASE REG FOR CLEANUP     Y02072
R6       EQU   6                   *    -----
R7       EQU   7                   *    UCB
SAVEXXXX EQU   8                        SAVE AREA FOR CLEANUP    Y02072
R9       EQU   9                   #    WORK REGISTER
R10      EQU   10                       CHANNEL PROGRAM POINTER
R11      EQU   11                       DECB
R12      EQU   12                       WORK REGISTER
R13      EQU   13                       WORK REGISTER
R14      EQU   14                       IOS RETURN VECTOR TABLE
R15      EQU   15                       BASE REGISTER
*
*        * MEANS THIS REGISTER IS SET UP UPON ENTRY FROM IOS,
*          AND ITS CONTENTS MUST BE RESTORED UPON RETURN TO IOS
*        # MEANS THIS REGISTER MAY BE CHANGED, BUT UPON RETURN TO IOS
*          MUST CONTAIN ZEROES IN THE THREE HIGH ORDER BYTES
*
PERRMASK EQU   X'20'          TO TEST FOR PERMANENT ERROR, TEST
PERRYES  EQU   8               IOBECBAD, B2. IF OFF (CONDITION CODE 8)
PERRNO   EQU   7               THERE IS A PERM ERR. ELSE B2 ON ( CD 7)
ONE      EQU   1                        MISC OFFSET              S20201
TWO      EQU   2                        *                        S20201
THREE    EQU   3                        *                        S20201
FOUR     EQU   4                        *                        S20201
FIVE     EQU   5                        *                        S20201
SEVEN    EQU   7                        *                        S20201
EIGHT    EQU   8                        *                        S20201
OFLOW    EQU   X'10'                    OVERFLOW BIT IN F BYTE   S20201
DUMMY    EQU   X'20'                    DUMMY/INACTIVE BIT       S20201
BYP      EQU   12                       RETURN TO IOS/BYPASS     S20201
CCOFF    EQU   X'BF'                    MASK FOR CC OFF
*                                        1592                    S20201
EXCP     EQU   8                        RETURN TO IOS/EXCP       S20201
*                                        1592                    S20201
NORMAL   EQU   0                        RETURN TO IOS/NORMAL     S20201
*                                        1592                    S20201
ABENTRY  EQU   X'01'                    ABEND ENTRY INDICATOR    A50670
DATACHK  EQU   X'08'                    DATA CHECK IOB SENSE     A50670
FMVIOL   EQU   X'04'                    FILE MASK VIOLATION      Y02072
REEXCP   EQU   8                        REEXCP RETURN TO IOS     Y02072
SKIPX    EQU   4                        SKIP RETURN TO IOS       Y02072
BYPASS   EQU   12                       BYPASS RETURN TO IOS     Y02072
NOTSEEK  EQU   8                        CC - SEEK TEST SENSE B0  Y02072
SEEKCHK  EQU   X'01'                    SEEK CHECK INDICATOR     Y02072
*
         USING RQE,R1                   RQE ADDRESSABILITY       Y02072
         USING IHAIOB,R2                IOB POINTER              S20201
         USING IHADEB,R3                DEB POINTER
         USING IHADCB,R4                DCB POINTER
         USING IHAWKNCP,R10             CHANNEL PROGRAM POINTER
         USING IHADECB,R11              DECB POINTER
         USING IHADCW,R12               DCB WA POINTER ONLY AS WORK REG
         SPACE 5
*                             APPENDAGE VECTOR TABLE
APP      DC    A(APPCEND-APP)           CHANNEL END       USED BY WKN
         DC    A(APPABEND-APP)          ABNORMAL END  -     USED BY WKN
         USING APPCEND,R15
APPCEND  NI    IOBINDCT,X'FE'          INDICATE CEND ENTRY
         B     SETUPREG                 BRANCH TO SET UP REGISTERS
APPABEND EQU   *
         USING APPABEND,R15
*                                                                A50670
*        IF THIS IS A CORRECTABLE DATA CHECK FROM AN RPS         A50670
*        DEVICE THEN RETURN TO IOS FOR ERP HANDLING              A50670
*                                                                A50670
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR          A50670
         BZ    NOCDACK                  YES CONTINUE AS USUAL    A50670
         TM    IOBSENS1,FMVIOL          WAS IT A SEEK CHECK      Y02072
         BCR   NOTSEEK,R14              NO - RETURN TO ERPS      Y02072
         SPACE 3
NOCDACK  EQU   *
         OI    IOBINDCT,ABENTRY         INDICATE ABEND ENTRY     A50670
SETUPREG L     R15,DEBEXPTR              GET ADDR OF DEB         S21045
*                                       EXTENSION PTR            S21045
         USING DEBEXT,R15                                        S21045
         L     R15,DEBWKPT4              R15  BASE               S21045
         DROP  R15
         USING APP,R15
*
*   SAVE IOS REGISTERS AND SET UP ADDRESSABILITY TO RETURN TO SPECIAL
*   CLEANUP ROUTINE.
*
         STM   R14,R12,0(R13)           SAVE REGISTERS           Y02072
         LR    SAVEXXXX,R13             SAVE SAVE AREA PTR       Y02072
         LA    R14,IOSRETS              RETURN TO RETURN TO IOS  Y02072
         LR    BASEXXXX,R14             BASE REG FOR RETURN      Y02072
         SPACE 2
         MODESET KEYADDR=RQEPRT,WORKREG=13 ACQUIRE USER KEY      Y02072
         SPACE 2
         L     R10,IOBCCWAD             R10  CP
         LA    R10,0(R10)               CLEAR HIGH ORDER BYTE    A49403
         MVC   IOBECBAD+1(3),IOBBCHAD+1 RESTORE ECB              A33533
         L     R11,IOBECBAD             R11  DECB
         L     R13,DEBEXPTR              GET ADDR OF DEB         S21045
*                                       EXTENSION PTR            S21045
         USING DEBEXT,R13                                        S21045
         L     R13,DEBWKPT5              R13  BASE, PART 2       S21045
         DROP  R13                                               S21045
         L     R12,IOBCSW               CSW= STOP ADDRESS PLUS 8
         LA    R12,0(R12)               CLEAR HIGH ORDER ZERO
         USING IHAPART2,R13
         IC    R9,IOBAPP                R9   RELATIVE ADDRESS IN
         CH    R9,TWOTHREE              IS APP CODE GT 23?     @ZA26522
         BNH   CODEOK                   IF NOT BYPASS PROGCHK  @ZA26522
         DC    H'0'                     PROG CHK ON BAD CODE   @ZA26522
TWOTHREE DC    X'0017'                  MAX VALUE FOR APPCODE  @ZA26522
CODEOK   SLL   R9,2                            APPENDAGE TABLE @ZA26522
         TM    IOBINDCT,X'01'
         BO    APPABENT(R9)            IF ABEND, BRANCH VIA ABEND TABLE
         B     APPCENDT(R9)            IF CEND, BRANCH VIA THAT TABLE
         EJECT
***********************************************************************
*****              CHANNEL END APPENDAGE VECTOR TABLE             *****
***********************************************************************
APPCENDT EQU   *              CODE      FOR COMPLETION OF
         B     APPABA2   0             CP4/CP5, READ
         B     APPABA2   1             CP4/CP5, WRITE
         B     APPADB2   2             CP7
         B     APPAAB2   3             CP1 OR CP2, READ AND UPDATE
         B     APPRVNOR  4             NOT USED
         B     APPACA2   5             CP6
         B     APPRVNOR  6             NOT USED
         B     APPM67CE  7             CP1 OR CP2, WRITE KN
         B     APPN3B21
         B     APPN7B2   9             CP10A  - TRUE INSERT
         B     APPN8A2   10            CP10B  - TRUE INSERT
         B     APPN8A2   11            CP10B  - ADD TO END
         B     APPM4A2   12            CP14  - SETUPS 1, 2, OR 5
         B     APPM5A2   13            CP14  - SETUPS 3, 4, OR 6
         B     APPM1B21
         B     APPM2B2   15            CP16 - SITUATION 2
         B     APPM3B2   16            CP16 - SITUATION 3
         B     APPN9A2   17            CP17  - TRACK INDEX ONLY
         B     APPN9A2   18            CP17  - TRACK AND HIGHER LEVELS
         B     APPN9A2   19            CP17  - HIGHER LEVELS ONLY
         B     APPN4B2   20            CP9A
         B     APPN5B2   21            CP9B
         B     APPN6B2   22            CP9C
         B     APPN7B2   23             CP10A  - ADD TO END
         EJECT
***********************************************************************
*****             ABNORMAL END APPENDAGE VECTOR TABLE             *****
***********************************************************************
APPABENT EQU   *              CODE      FOR COMPLETION OF
         B     APPABD2   0             CP4/CP5, READ
         B     APPABD2   1             CP4/CP5, WRITE
         B     APPADE2   2             CP7
         B     APPAAA2   3             CP1 OR CP2, READ AND UPDATE
         B     APPRVNOR  4             NOT USED
         B     APPACE2   5             CP6
         B     APPRVNOR  6             NOT USED
         B     APPM67AE  7             CP1 OR CP2, WRITE KN
         B     APPN3E2   8             CP8
         B     APPN7J2   9             CP10A  - TRUE INSERT
         B     APPN8G2   10            CP10B  - TRUE INSERT
         B     APPN8G2   11            CP10B  - ADD TO END
         B     APPM4E2   12            CP14  - SETUPS 1, 2, OR 5
         B     APPM5E2   13            CP14  - SETUPS 3, 4, OR 6
         B     APPM1J2   14            CP15
         B     APPM2H2   15            CP16 - SITUATION 2
         B     APPM2H2   16            CP16 - SITUATION 3
         B     APPN9G4   17            CP17  - TRACK INDEX ONLY
         B     APPN9G4   18            CP17  - TRACK AND HIGHER LEVELS
         B     APPN9G4   19            CP17  - HIGHER LEVELS ONLY
         B     APPN4G2   20            CP9A
         B     APPN5G2   21            CP9B
         B     APPN6H2   22            CP9C
         B     APPN7J2   23             CP10A  - ADD TO END
         DROP  R13
         USING CVT,R13                  COMMUNICATION VECTOR TABLE
         EJECT
*   SPECIAL IOS RETURN CLEANUP ROUTINE INTERCEPTS ALL RETURNS TO
*   IOS AND RESTORES REGISTERS AND SETS PROTECTION KEY TO 0.
         SPACE 2                                                 Y02072
IOSRETS  EQU   *                        RETURN POINT             Y02072
         USING *,BASEXXXX               ADDRESSABILITY           Y02072
         B     NORMRET                  NORMAL RETURN            Y02072
         B     SKIPRET                  SKIP POST RETURN         Y02072
         B     REEXCPRT                 REEXCP RETURN            Y02072
         SPACE 2
BYPASRET EQU   *                        BYPASS POST RETURN       Y02072
         BAL   R12,IOSRETX              RESTORE REGS AND KEY     Y02072
         LA    R14,BYPASS(R14)          SCHEDULE ASYNCH ROUTINE  Y02072
         L     R13,CVTPTR               CVT ADDRESSABILITY       Y02072
         L     R13,CVTEXEF              ASYNCH SCHED ROUTINE     Y02072
         BR    R13                      GO TO ROUTINE            Y02072
         SPACE 2
REEXCPRT EQU   *                        REEXCP RETURN            Y02072
         BAL   R12,IOSRETX              RESTORE REGS AND KEY     Y02072
         B     REEXCP(R14)              RETURN - REEXCP          Y02072
         SPACE 2
SKIPRET  EQU   *                        RETURN - SKIP            Y02072
         BAL   R12,IOSRETX              RESTORE REGS AND KEY     Y02072
         B     SKIPX(R14)               RETURN - SKIP            Y02072
         SPACE 2
NORMRET  EQU   *                        RETURN - NORMAL          Y02072
         BAL   R12,IOSRETX              RESTORE REGS AND KEY     Y02072
         BR    R14                      RETURN                   Y02072
         SPACE 2
IOSRETX  EQU   *                        RESTORE REGS AND KEY     Y02072
         MODESET KEYADDR=KEY0,WORKREG=13   SET KEY = 0           Y02072
         SPACE 2
         LM    R14,R11,0(SAVEXXXX)      RESTORE REGISTERS        Y02072
         BR    R12                      RETURN                   Y02072
         DROP  BASEXXXX                 RESET ADDRESSABILITY     Y02072
         SPACE 2                                                 Y02072
KEY0     DC    H'0'                     KEY 0                    Y02072
         EJECT                                                   Y02072
*              CHANNEL  END CP8,    APPENDAGE CODE  8,         CHART N3
APPN3B2  EQU   *                        *
*
         LA    R13,CB8A+EIGHT           DID CHANNEL PROGRAM END ON
         CLR   R12,R13                  CB8A
         BNE   APPAOS22                 NO, CONTINUE
         TM    CB8A+FIVE,CC             WAS CB8A CHAINED
         BZ    APPAOS22                 NO, CP UNCHANGED
         NI    CB8A+FIVE,CCOFF          RESET CHAIN IND.
         OI    CB8A+FOUR,CC             TURN CC BACK ON
         CLI   CB12,SEEKHH              CB12 SET FOR HEAD SEEK
         BNE   APPN3F3                  NO,  FILE PROTECT
         LA    R13,CB16                 RESET IOBSTART
         ST    R13,IOBSTART-ONE         START CP WITH SETSECT
         MVC   IOBDADAD,CB10+SEVEN      RESET IOBSEEK
         B     APPRVXCP                 SECHDULE EXCP IOS RETURN
APPAOS22 EQU   *                        *
*
         LA    R13,CB21+EIGHT           DID CHAN PROG STOP AT CB21
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPN3A3                  NO - CHECK FOR LENGTH ERROR
         TM    CB11+7,X'08'             TEST IF TRACK IS SHARED
         L     R12,DCBWKPT2             CREATE PROPER 'R' IN DCWHIRPS
         BO    APPN3C2A                 BO IF SHARED
         MVC   DCWHIRPS,DCBHIRPD        UNSHARED - USE DCBHIRPD
         B     APPN3C2B
APPN3C2A MVC   DCWHIRPS,DCBHIRSH        SHARED   - USE DCBHIRSH
APPN3C2B EQU   *                                                 S20201
APPN3C2D MVC   IOBDADAD+3(5),CB23+3     MOVE CB23 CCHHR TO IOB
         MVI   IOBAPP,X'14'             SET APPENDAGE CODE TO 20
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         LA    R13,CB30                 START AT CB30
         ST    R13,IOBSTART-1
         LA    R13,IOBDADAD+3           PUT SEEK ADDRESS IN CB30,
         IC    R9,CB30                   CB34, AND CB40
         ST    R13,CB30
         STC   R9,CB30
         MVC   CB34+1(3),CB30+1
         MVC   CB40+1(3),CB30+1
         MVC   CB36+1(3),DECBKEY+1      PUT I IN CB36 AND CB38
         MVC   CB38+1(3),DECBKEY+1
         L     R13,DECBAREA             PUT AREA + 16 IN CB37 AND CB39
         LA    R13,16(R0,R13)
         IC    R9,CB37
         ST    R13,CB37
         STC   R9,CB37
         MVC   CB39+1(3),CB37+1
         NI    IOBINDCT,X'FF'-LASTIND   FLAG NORMAL USE OF CP9A  A31323
*
*        TEST FOR OUT OF SPACE CONDITION
*
         TM    DCBOPTCD,X'08'          CYL OVFL OPTION           A31323
         BZ    APPNSC                  IF NO-BRANCH              A31323
         CLI   CB22+5,X'00'            CYL OVFL FULL             A31323
         BNE   APPRVXCP                IF NO-BRANCH              A31323
APPNSC   TM    DCBOPTCD,X'10'          INDEP OVFL OPTION         A31323
         BZ    APPNSE                  IF NO-BRANCH              A31323
*      ADJUST DCBRORG2 TO ZERO IF IND OVFL IS FULL              SA53815
         LH    R13,DCBRORG2             LAST UNFILLED IOV TRK?  SA53815
*
         LTR   R13,R13                  ANY IND OVRFLOW TRKS    SA65051
         BZ    APPNSD                   BRANCH IF NO            SA65051
         BCT   R13,APPRVXCP             NO - BR TO READ BLOCK   SA53815
*                                       YES - CONTINUE          SA53815
APPNSD   IC    R13,DCBLIOV+7            GET LIOV (REC NO.)      SA65051
         LA    R13,1(R13)               BUMP REC NO.            SA53815
         SR    R12,R12                  CLEAR REG               SA53815
         IC    R12,DCBHIIOV             GET HIGHEST REC. NO.    SA53815
         CLR   R13,R12                  LIOV+EOF FILLS LAST TRK?SA53815
         BNE   APPRVXCP                 NO - BR TO READ BLOCK   SA53815
*                                       YES - CONTINUE          SA53815
         SR    R13,R13                  REDUCE NUMBER OF -      SA53815
         STH   R13,DCBRORG2             IND OVFL TRKS TO ZERO   SA53815
APPNSE   CLC   IOBDADAD(7),DCBLPDA      ON LAST TRK OF DATA SET  A31323
         BNE   APPNSG                   NO, BRANCH               A31323
         TM    DCBST,X'01'              IS LAST TRACK FULL       A31323
         BNO   APPRVXCP                 NO, BR--SPACE IS AVAIL   A31323
APPNSG   TM    DCBOPTCD,X'02'           WAS DELETE OPTION TAKEN  A31323
         BO    APPNSI                   YES, BR--RD LAST BLK TO  A31323
*
*        CHECK FOR DELETED RECORDS.  OTHERWISE, SET NO SPACE FND
*
         OI    DECBEXC1,X'20'           THEN READ DESIRED BLK    A31323
         B     APPRVXCP                 TO CHECK FOR DUP RECS.   A31323
APPNSI   OI    IOBINDCT,LASTIND         WILL CHECK LAST RECORD   A31323
         L     R12,DCBWKPT2             RESTORE WORK AREA BASE   A31323
         USING IHADCW,R12                                        A31323
         MVC   IOBDADAD+7(1),DCWHIRPS   SET R TO LAST ON TRK     A31323
         B     APPRVXCP                 CHK RECORD FOR DELETION  A31323
APPN3B3  OI    DECBEXC1,X'04'           RECORD IS UNREACHABLE    A31323
APPN3B4  MVI   IOBASYN,X'08'            SCHEDULE PROCESSING COMPLETION
APPN3B5  B     BYP(R14)                 SCHEDULE ASYNCH ROUTINE  Y02072
APPN3B6  LA    R13,CB8A+EIGHT           DID CP STOP AT CB8A      S20201
         CLR   R12,R13                  COMPARE ADDRESSES        S20201
         BNE   APPN3B3                  NO, UNREACHABLE BLOCK    S20201
         TM    CB11+SEVEN,OFLOW+DUMMY   OFLO, INACTIVE, OR DUMMY S20201
         BNZ   APPN3F3                  YES, GO TO FILE PROTECT  S20201
         MVC   CB23+THREE(FOUR),CB11+TWO    SET UP CCHH          S20201
         MVI   CB23+SEVEN,ONE           SET R EQUAL TO 1         S20201
         B     APPN3C2D                 GO READ RECORD           S20201
         EJECT
*              ABNORMAL END CP8,    APPENDAGE CODE  8,         CHART N3
APPN3E2  EQU   *                                                  19374
         TM    IOBSENSE+1,X'04'         WAS ABEND DUE TO FILE     15924
*                                       PROTECT                   15924
         BO    APPN3F2                  YES--BRANCH               19374
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR?          19374
         BC    PERRYES,APPN3B3          YES--BRANCH               19374
         B     APPRVNOR                 NO--RETRY CHANNEL PGM     19374
APPN3F2  LA    R13,CB12+8               DID CHAN PROG STOP AT CB12
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPN3B3                  NO - RECORD IS UNREACHABLE
APPN3F3  TM    CB11+SEVEN,OFLOW         WAS OFLO TRK IX ENTRY RD S20201
         BZ    APPN3G3                  BZ IF NO
         TM    CB11+7,X'20'
         BO    APPN3G3                  BO IF NO
         MVC   CJ11(7),IOBDADAD         SAVE TRK INDEX MBBCCHH
         MVI   IOBAPP,X'10'             SET APPENDAGE CODE TO 16
         NI    IOBFLAG1,X'7F'           DONT USE DATA CHAINING
         OI    IOBFLAG1,X'40'           USE COMMAND CHAINING
         LA    R13,CJ1                  START AT CJ1
         ST    R13,IOBSTART-1
         MVC   IOBDADAD,CB10+7          PUT CB10 MBBCCHHR IN IOB
         LA    R13,IOBDADAD+3           PUT SEEK ADDRESS IN CJ1 AND CJ5
         IC    R9,CJ1
         ST    R13,CJ1
         STC   R9,CJ1
         MVC   CJ5+ONE(THREE),CJ1+ONE   MOVE IN ADDR OF SEEK     S20201
         MVC   CJ3+ONE(THREE),DECBKEY+ONE   MOVE IN ADDR OF KEY  S20201
         MVC   CJ7+ONE(THREE),DECBKEY+ONE   *                    S20201
         L     R12,DCBWKPT2             SET ON FIRST TIME ONLY SWITCH
         USING IHADCW,R12                                        S20201
         OI    DCWWKNI,X'40'
         LA    R12,0(R0,R0)             IS FIRST OFLO ON SAME MODULE
         IC    R12,CB10+7                      GET M OF OFLO ENTRY
         SLL   R12,4(R0)                       MULT BY 16
         L     R12,32(R12,R3)                  LOAD UCB ADDR FROM DEB
         LA    R12,0(R0,R12)                   COMP WITH UCB AD IN 7
         CR    R12,R7
         BE    APPRVXCP                 SAME MODULE - EXCP RTRN TO IOS
         MVI   IOBASYN,X'01'            DIFFERENT MODULE - MUST EXCP
         B     APPN3B5                   IN ASYNCHRONOUS ROUTINE
APPN3A3  TM    IOBCSW+5,X'40'           DID CHAN PROG HAVE LENGTH ERROR
         BZ    APPN3B6                  GO TEST FOR SPECIAL CASE S20201
         TM    CB11+7,X'30'             WAS ENTRY INACTIVE
         BO    APPN3B3                  BO IF YES
APPN3G3  TM    CB11+7,X'20'             VERIFY ADD-TO-END SITUATION
         BZ    APPN3B3                  BZ IF DUMMY OR INACTIVE
         CLI   DCBNLEV,X'00'            ERROR IF 2 OR MORE INDEX LEVELS
         BNE   APPN3B3                  BNE IF MORE THAN 1 INDEX LEVEL
         TM    CB11+7,X'08'             TEST IF TRACK IS SHARED
         L     R12,DCBWKPT2             CREATE PROPER 'R' IN DCWHIRPS
         USING IHADCW,R12                                        S20201
         BO    APPN3G33                 BO IF SHARED
         MVC   DCWHIRPS,DCBHIRPD        UNSHARED - USE DCBHIRPD
         B     APPN3G5
APPN3G33 MVC   DCWHIRPS,DCBHIRSH        SHARED   - USE DCBHIRSH
APPN3G5  MVI   IOBAPP,X'0E'             SET APPENDAGE CODE TO 14
         MVC   IOBDADAD(3),DCBLPDA      SEEK TO DCBLETI'S CYLINDER
         MVC   IOBDADAD+3(4),DCBLETI    CCHH
         OC    IOBDADAD+6(1),DCBFIRSH+3
         XC    IOBDADAD+6(1),DCBFIRSH+3 REDUCE TO CYL BOUNDRY OR ZERO
         MVI   IOBDADAD+7,0
         LA    R12,CI1                  START AT CI1
         ST    R12,IOBSTART-1
         MVI   IOBFLAG1,X'42'
         LA    R12,IOBDADAD+3           PUT SEEK ADDRESS IN CI1
         IC    R9,CI1
         ST    R12,CI1
         STC   R9,CI1
         MVC   CI5+TWO(FIVE),DCBLETI    PUT DCBLETI CCHHR IN CI5 S20201
         MVC   CI5(TWO),DCBLPDA+ONE     MOVE IN BB FOR HEAD SEEK S20201
         B     APPRVXCP
         EJECT
*              CHANNEL  END CP9A,   APPENDAGE CODE 20,         CHART N4
APPN4B2  LA    R13,CB33+8               DID CHAN PROG STOP AT CB33
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPN3B3                  NO - RECORD IS UNREACHABLE
         L     R12,CB32                 KEY OF RECORD JUST READ
         LA    R12,0(R0,R12)
         L     R13,DECBKEY              KEY OF NEW RECORD
         IC    R9,DCBKEYLE
         BCTR  R9,R0
         EX    R9,APPN4C2A              ARE KEYS EQUAL
         BE    APPN4C3                  YES  C3
         TM    DECBEXC1,X'20'           WAS NO SPACE FOUND       A31323
         BO    APPN3B4                  YES, POST REQ IN ERROR   A31323
         NI    CB35+4,X'00'             NO - SET OFF REPL-OF-DEL A31323
*                                       FLAG                     A31323
         L     R12,DCBWKPT2             SET WORK AREA BASE       A31323
         USING IHADCW,R12                                        A31323
         TM    IOBINDCT,LASTIND         WAS LAST BLK READ        A31323
         BZ    APPN4B7                  NO, BR TO CONTINUE       A31323
         NI    IOBINDCT,X'FF'-LASTIND   TURN OFF BIT             A31323
         L     R13,CB33                 POINT TO DATA FIELD      A31323
         CLI   0(R13),X'FF'             WAS RECORD DELETED       A31323
         BNE   APPN4B50                 NO, BR--SPACE NOT AVAIL  A31323
         BAL   R13,COMPLDT              YES, SPACE MAY BE AVAIL  A31323
*
*        RETURN HERE IF RECORD MUST BE BUMPED.  RETURN HERE + 4
*        (AFTER THE NEXT INSTRUCTION) IF RECORD NEED NOT GO INTO
*        OVERFLOW.
*
APPN4B50 OI    DECBEXC1,X'20'           SPACE IS NOT AVAILABLE   A31323
APPN4B5  CLC   DCWHIRPS(1),CB23+7       IS THIS THE DESIRED BLK  A31323
         BE    APPN4E4                  YES, BRANCH              A31323
         MVC   IOBDADAD+7(1),CB23+7     NO, SEEK TO CORRECT BLK  A31323
         B     APPRVXCP                 READ IT IN               A31323
APPN4B7  CLC   IOBDADAD+7(1),DCWHIRPS   WAS RCD LAST ON TRK      A31323
         BE    APPN4E4                  YES  E4
         MVI   CB37+4,X'40'             SET ON  CB37  CC FLAG
APPN4F4  MVI   IOBAPP,X'15'             SET APPENDAGE CODE TO 21
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         LA    R12,CB34                 START AT CB34
         ST    R12,IOBSTART-1
         B     APPRVXCP
APPN4C3  TM    DCBOPTCD,X'02'           WAS DELETE OPTION TAKEN
         BZ    APPN4B4                  NO   B4
         L     R12,CB33                 IS RECORD MARKED FOR DELETION
         CLI   0(R12),X'FF'
         BNE   APPN4B4                  NO   B4
         OI    CB35+4,X'80'             SET ON REPL-OF-DEL FLAG
APPN4E4  TM    DECBEXC1,X'20'           WAS NO SPACE AVAIL       A31323
         BO    APPN3B4                  YES, POST REQ IN ERROR   A31323
         MVI   CB37+4,X'00'             SET OFF CB37  CC FLAG    A31323
         B     APPN4F4
APPN4B4  OI    DECBEXC1,X'01'           INDICATE DUPLICATE RECORD
         MVI   IOBASYN,X'08'
         B     APPN3B5                  SCHEDULE PROCESSING COMPLETION
         EJECT
*              ABNORMAL END CP9A,   APPENDAGE CODE 20,         CHART N4
APPN4G2  TM    IOBECBAD,PERRMASK        WAS ABEND DUE TO PERM ERROR
         BC    PERRNO,APPRVNOR          NO   CONTINUE, NORMAL IOS
         TM    IOBSENSE,X'08'           WAS ABEND DUE TO DATA CHECK
         BZ    APPN3B3                  NO   N3B3
         LA    R13,CB32+8               DID CHAN PROG STOP AT CB32
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BE    APPN4K2                  YES - UNCORRECTABLE I/O ERROR
         LA    R13,CB33+8               DID CHAN PROG STOP AT CB33
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPN3B3                  NO - RECORD IS UNREACHABLE
APPN4K2  OI    DECBEXC1,X'08'           UNCORRECTABLE I/O ERROR
         B     APPN3B4
APPN4C2A CLC   0(0,R13),0(R12)          EXECUTED INSTRUCTION
         EJECT
*              CHANNEL  END CP9B,   APPENDAGE CODE 21,         CHART N5
APPN5B2  MVI   CB36,WKD                 RESTORE COMMAND CODE     A34922
         NI    CB36+4,X'FF'-SKIP        RESTORE FLAGS            A34922
         NI    CB37+4,X'FF'-SKIP        RESTORE FLAGS            A34922
         LA    R13,CB39+8               CHAN PGM STOP AT CB39    A34922
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPN5B3                  NO - TRY CB38
         IC    R9,IOBDADAD+7            INCREASE R IN IOB BY 1
         LA    R13,1(R0,R9)
         STC   R13,IOBDADAD+7
         L     R12,DCBWKPT2             WAS RCD JUST READ LAST ON TRACK
         USING IHADCW,R12                                        S20201
         CLC   IOBDADAD+7(1),DCWHIRPS
         BE    APPN5D1                  YES  D1
         MVI   CB43+4,X'40'             SET ON  CB43  CC FLAG
APPN5E1  MVI   IOBAPP,X'16'             SET APPENDAGE CODE TO 22
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         LA    R12,CB40                 START AT CB40
         ST    R12,IOBSTART-1
         B     APPRVXCP                 EXCP RETURN TO IOS
APPN5D1  MVI   CB43+4,X'00'             SET OFF CB43  CC FLAG
         B     APPN5E1
APPN5B3  LA    R13,CB38+8               DID CHAN PROG STOP AT CB38
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPN5B4                  NO - TRY CB37
         TM    IOBCSW+4,X'01'           DID CHAN PROG END WITH EOF
         BZ    APPN3B3                  NO   N3B3
         MVI   IOBAPP,X'09'             SET APPENDAGE CODE TO 9
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         LA    R12,CB46                 START AT CB46
         ST    R12,IOBSTART-1
         MVC   CB51(4),IOBDADAD+3       MOVE IOB CCHHR (PLUS 1) TO CB51
         IC    R9,IOBDADAD+7
         LA    R12,1(R0,R9)
         STC   R12,CB51+4
         MVC   CB49+1(3),CB32+1         MOVE KEY  ADDRESS TO CB49
         MVC   CB50+1(3),CB33+1         MOVE DATA ADDRESS TO CB50
         LA    R12,IOBDADAD+3
         IC    R9,CB46                  SAVE
         ST    R12,CB46                 PUT IOB ADDRESS IN CB46
         STC   R9,CB46                  RESTORE
         B     APPRVXCP                 EXCP RETURN TO IOS
APPN5B4  LA    R13,CB37+8               DID CHAN PROG STOP AT CB37
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPN3B3                  NO - RECORD IS UNREACHABLE
         TM    CB35+4,X'80'             IS REPL-OF-DEL FLAG ON   A38857
         BO    APPN3B4                  BR IF YES                A38857
         L     R12,DCBWKPT2             GET WKAREA BASE          A34922
         CLC   DCWHIRPS(1),IOBDADAD+7   END OF TRACK STOP        A34922
         BE    APPN5B5                  YES, BRANCH              A34922
         OI    CB37+4,CC+SKIP           NO, MUST BE ERROR STOP   A34922
         OI    CB36+4,SKIP              NO OP WRITE COMMAND      A34922
         B     APPRVXCP                 RESTART CHANNEL PROG     A34922
APPN5B5  EQU   *                        PROCESS END OF TRACK     A34922
         TM    DCBOPTCD,X'02'           WAS DELETE OPTION TAKEN
         BZ    APPN5F4                  NO   F4
         L     R12,CB33                 IS RECORD MARKED FOR DELETION
         CLI   0(R12),X'FF'
         BNE   APPN5F4                  NO   F4
         BAL   R13,COMPLDT              SEE IF MUST BUMP RECORD  A31323
         B     APPN5F4                  YES, RETURN HERE         A31323
*
*                                       NO, DON'T BUMP
         MVC   IOBDADAD+5(3),CB23       MOVE CB23 CCHHR TO IOB
         MVI   IOBAPP,X'11'             SET APPENDAGE CODE TO 17
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         LA    R12,CK1                  START AT CK1
         ST    R12,IOBSTART-1
         LA    R12,IOBDADAD+3           PUT IOB ADDRESS IN CK1 AND CK4
         ST    R12,CK1
         MVI   CK1,X'31'
         ST    R12,CK4
         MVI   CK4,X'31'
         MVC   CK6+1(3),DECBKEY+1       PUT I ADDRESS IN CK6
         B     APPRVXCP                 EXCP RETURN TO IOS
APPN5F4  OI    IOBINDCT,X'20'           INDICATE OFLO RECORD IN W
         MVI   IOBASYN,X'09'            SET ASYNCHRONOUS CODE TO 9
         MVC   CH7+1(3),DECBKEY+1       PUT I ADDRESS IN CH7
         B     APPN3B5                  SCHEDULE ASYNCHRONOUS ROUTINE
COMPLDT  IC    R9,DCBHMASK              GET TRACK MASK           A31323
         SLL   R13,8                    MOVE OVER RETURN ADDR    A31323
         IC    R13,IOBDADAD+6           PICK UP TRACK BYTE       A31323
         NR    R9,R13                   MASK OFF TRACK           A31323
         SRL   R13,8                    RESTORE RETURN ADDR      A31323
         EX    R9,CLILDT                SEE IF LAST TRK OF CYL   A31323
         BE    COMPLDT1                 BRANCH IF ON LAST TRACK  A31323
*
*        IF NOT LAST TRACK, CHECK TO SEE IF LAST TRK OF PRIME.
*
         CLC   IOBDADAD(7),DCBLPDA      LAST PRIME DATA TRK      A31323
         BNE   4(R13)                   NO, DON'T BUMP RECORD    A31323
COMPLDT1 TM    CB26,X'08'               DOES OVERFLOW IX ENTRY   A31323
*
*        INDICATE THE EXISTENCE OF A CHAIN.
*
         BO    4(R13)                   YES, NO NEED TO BUMP     A31323
         BR    R13                      MUST BUMP--NO CHAIN YET  A31323
CLILDT   CLI   DCBLDT+1,0               REC ON LAST TRK OF CYL
         EJECT
*              ABNORMAL END CP9B,   APPENDAGE CODE 21,         CHART N5
APPN5G2  EQU   *                                                 A38537
         TM    IOBECBAD,PERRMASK        TEST FOR PERM ERROR      A38537
         BC    PERRNO,APPN5G3           BIF FIRST ENTRY          A38537
         MVI   CB36,WKD                 RESTORE COMMAND CODE     A38537
         NI    CB36+4,X'FF'-SKIP        RESTORE FLAGS            A34922
         NI    CB37+4,X'FF'-SKIP        RESTORE FLAGS            A34922
         TM    IOBSENSE,X'08'           WAS ABEND DUE TO DATA CHECK
         BZ    APPN3B3                  NO, UNREACHABLE BLOCK    A34922
         B     APPN4K2                  YES, UNCORR I/O ERROR    A34922
APPN5G3  EQU   *                                                 A38537
         TM    IOBSENSE,TCC             IS IT ALT.TRK            A38537
TCC      EQU   X'02'                    TRACK COND CHECK         A38537
         BO    APPRVNOR                 NO ACTION FOR ALT. TRK   A38537
         LA    R13,CB38+8               POINT TO NXT REC CCW     A38537
         CLR   R12,R13                  DID CP GET THIS FAR      A34922
         BL    APPN5G4                  NO, RETRY CHAN PGM       A34922
         MVI   CB36,RKD                 YES, CONVERT TO RD KD    A34922
         OI    CB36+4,SKIP              SUPPRESS DATA XFER       A34922
         OI    CB37+4,SKIP              SUPPRESS DATA XFER       A34922
         B     APPRVNOR                 RETRY CHAN PGM           A34922
APPN5G4  NI    CB37+4,X'FF'-CC          TURN OFF COMMAND CHAIN   A34922
         MVI   CB36,WKD                 RESTORE COMMAND          A38537
         NI    CB36+4,X'FF'-SKIP        RESTORE FLAGS            A38537
         NI    CB37+4,X'FF'-SKIP        RESTORE FLAGS            A38537
         B     APPRVNOR                 RETRY CHANNEL PGM        A34922
         EJECT
*              CHANNEL  END CP9C,   APPENDAGE CODE 22,         CHART N6
APPN6B2  MVI   CB42,WKD                 RESTORE COMMAND CODE     A34922
         NI    CB42+4,X'FF'-SKIP        RESTORE FLAGS            A34922
         NI    CB43+4,X'FF'-SKIP        RESTORE FLAGS            A34922
         LA    R13,CB45+8               CHAN PGM STOP AT CB45    A34922
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPN6B3                  NO - TRY CB44
         IC    R9,IOBDADAD+7            INCREASE R IN IOB BY 1
         LA    R13,1(R0,R9)
         STC   R13,IOBDADAD+7
         L     R12,DCBWKPT2             WAS RCD JUST READ LAST ON TRACK
         USING IHADCW,R12                                        S20201
         CLC   IOBDADAD+7(1),DCWHIRPS
         BNE   APPN6E2                  NO   E2
         MVI   CB37+4,X'00'             SET OFF CB37  CC FLAG
         NI    CB35+4,X'7F'             SET OFF REPL-OF-DEL FLAG
APPN6F1  MVI   IOBAPP,X'15'             SET APPENDAGE CODE TO 21
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         LA    R12,CB34                 START AT CB34
         ST    R12,IOBSTART-1
         B     APPRVXCP                 EXCP RETURN TO IOS
APPN6E2  MVI   CB37+4,X'40'             SET ON  CB37  CC FLAG
         B     APPN6F1
APPN6B3  LA    R13,CB44+8               DID CHAN PROG STOP AT CB44
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPN6B4                  NO - TRY CB43
         TM    IOBCSW+4,X'01'           DID CHAN PROG END WITH EOF
         BZ    APPN3B3                  NO   N3B3
         MVI   IOBAPP,X'09'             SET APPENDAGE CODE TO 9
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         LA    R12,CB46                 START AT CB46
         ST    R12,IOBSTART-1
         LA    R12,IOBDADAD+3           PUT IOB ADDRESS IN CB46
         ST    R12,CB46
         MVI   CB46,X'31'
         MVC   CB49+1(3),DECBKEY+1      MOVE KEY  ADDRESS TO CB49
         L     R12,DECBAREA
         LA    R12,16(R0,R12)
         ST    R12,CB50                 MOVE DATA ADDRESS TO CB50
         MVC   CB51(4),IOBDADAD+3
         IC    R9,IOBDADAD+7
         LA    R12,1(R0,R9)
         STC   R12,CB51+4               MOVE IOB CCHHR (PLUS 1) TO CB51
         B     APPRVXCP                 EXCP RETURN TO IOS
APPN6B4  LA    R13,CB43+8               DID CHAN PROG STOP AT CB43
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPN3B3                  NO - RECORD IS UNREACHABLE
         L     R12,DCBWKPT2             GET WKAREA BASE          A34922
         CLC   DCWHIRPS(1),IOBDADAD+7   END OF TRACK STOP        A34922
         BE    APPN6B5                  YES, BRANCH              A34922
         OI    CB43+4,CC+SKIP           NO, MUST BE ERROR STOP   A34922
         OI    CB42+4,SKIP              NO OP WRITE COMMAND      A34922
         B     APPRVXCP                 RESTART CHANNEL PROG     A34922
APPN6B5  EQU   *                        PROCESS END OF TRACK     A34922
         TM    DCBOPTCD,X'02'           WAS DELETE OPTION TAKEN
         BZ    APPN6E4                  NO   E4
         L     R12,DECBAREA             IS RECORD MARKED FOR DELETION
         LA    R12,16(R0,R12)
         CLI   0(R12),X'FF'
         BNE   APPN6E4                  NO   E4
         BAL   R13,COMPLDT              SEE IF MUST BUMP RECORD  A31323
         B     APPN6E4                  YES, RETURN HERE         A31323
*                                       NO, DON'T BUMP
         MVC   IOBDADAD+5(3),CB23       MOVE CB23 CCHHR TO IOB
         MVI   IOBAPP,X'11'             SET APPENDAGE CODE TO 17
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         LA    R12,CK1                  START AT CK1
         ST    R12,IOBSTART-1
         LA    R12,IOBDADAD+3           PUT IOB ADDRESS IN CK1 AND CK4
         ST    R12,CK1
         MVI   CK1,X'31'
         ST    R12,CK4
         MVI   CK4,X'31'
         L     R12,CB42
         LA    R12,0(R0,R12)
         ST    R12,CK6                  MOVE KEY ADDRESS TO CK6
         MVI   CK6,X'0D'
         B     APPRVXCP                 EXCP RETURN TO IOS
APPN6E4  NI    IOBINDCT,X'DF'           INDICATE OFLO RECORD IN AREA
         MVI   IOBASYN,X'09'            SET ASYNCHRONOUS CODE TO 9
         L     R12,CB42                 MOVE KEY ADDRESS TO CH7
         LA    R12,0(R0,R12)
         ST    R12,CH7
         MVI   CH7,X'0D'
         B     APPN3B5                  SCHEDULE ASYNCHRONOUS ROUTINE
         EJECT
*              ABNORMAL END CP9C,   APPENDAGE CODE 22,         CHART N6
APPN6H2  EQU   *                                                 A44495
         TM    IOBECBAD,PERRMASK        TEST FOR PERMANENT ERROR A44495
         BC    PERRNO,APPN6H3           BIF FIRST ENTRY          A44495
         MVI   CB42,WKD                 RESTORE COMMAND CODE     A44495
         NI    CB42+4,X'FF'-SKIP        RESTORE FLAGS            A34922
         NI    CB43+4,X'FF'-SKIP        RESTORE FLAGS            A34922
         TM    IOBSENSE,X'08'           WAS ABEND DUE TO DATA CHECK
         BZ    APPN3B3                  NO, UNREACHABLE BLOCK    A34922
         B     APPN4K2                  YES, UNCORR I/O ERROR    A34922
APPN6H3  EQU   *                                                 A44495
         TM    IOBSENSE,TCC             TRK COND CHK             A44495
         BO    APPRVNOR                 LET 9C COMPLETE          A44495
         LA    R13,CB44+8               POINT TO NEXT REC CCW    A44495
         CLR   R12,R13                  DID CP GET THIS FAR      A34922
         BL    APPN6H4                  NO, RETRY CHAN PGM       A34922
         MVI   CB42,RKD                 YES, CONVERT TO RD KD    A34922
         OI    CB42+4,SKIP              SUPPRESS DATA XFER       A34922
         OI    CB43+4,SKIP              SUPPRESS DATA XFER       A34922
         B     APPRVNOR                 RETRY CHAN PGM           A34922
APPN6H4  NI    CB43+4,X'FF'-CC          NO COMMAND CHAIN         A34922
         MVI   CB42,WKD                 RESTORE COMMAND CODE     A44495
         NI    CB42+4,X'FF'-SKIP        RESTORE FLAGS            A44495
         NI    CB43+4,X'FF'-SKIP        RESTORE FLAGS            A44495
         B     APPRVNOR                 RETRY CHANNEL PROGRAM    A34922
         EJECT
*              CHANNEL  END CP15,   APPENDAGE CODE 14,         CHART M1
APPM1B2  LA    R13,CI4+8                DID CHAN PROG STOP AT CI4
         CLR   R12,R13                  BE MEANS YES, BNE MEANS NO
         BNE   APPM1B3                  NO - RECORD IS UNREACHABLE
         L     R12,DCBWKPT2
         USING IHADCW,R12                                        S20201
         OI    DCWWKNI,X'10'            SET ADD-TO-END INDICATOR ON
         CLI   DCBFIRSH+TWO,ONE         TEST FOR SHARED TRACK    A46955
         BNH   APPM1B20                 NO                       A46955
         CLC   DCBFIRSH(2),DCBLPDA+5    END OF SHARED TRACK        7851
         BE    APPM1B2A                                            7851
APPM1B20 EQU   *                        *                        A46955
         MVC   DCWHIRPS(1),DCBHIRPD                                7851
         B     APPM1B2B                                            7851
APPM1B2A MVC   DCWHIRPS(1),DCBHIRSH                                7851
APPM1B2B EQU   *                                                   7851
         TM    DCBST,X'01'              IS LAST TRACK FULL
         BO    APPM1C2                  BO IF YES, TO SET UP CP16
         MVI   IOBAPP,X'17'             SET APPENDAGE CODE TO 23
         OI    IOBFLAG1,X'C0'           USE DATA AND COMMAND CHAINING
         LA    R12,CB46                 START AT CB46
         ST    R12,IOBSTART-1
         MVC   IOBDADAD,DCBLPDA         PUT DCBLPDA MBBCCHHR IN IOB
         MVC   CB49+1(3),DECBKEY+1      MOVE KEY  ADDRESS TO CB49
         L     R12,DECBAREA             MOVE DATA ADDRESS TO CB50
         LA    R12,16(R0,R12)
         ST    R12,CB50
         IC    R9,DCBLPDA+7             MOVE DCBLPDA CCHHR TO CB51
         LA    R12,1(R0,R9)              THEN INCREASE CB51 R BY 1
         STC   R12,CB51+4
         MVC   CB51(4),DCBLPDA+3
         LA    R12,IOBDADAD+3
         STH   R12,CB46+2
         SRL   R12,16
         STC   R12,CB46+1               MOVE IOB ADDRESS TO CB46
         B     APPRVXCP                 EXCP RETURN TO IOS
APPM1C2  EQU   *
         TM    CB26,X'08'               WAS ENTRY READ IN CB25 OFLO END
         BZ    APPM1D3                  YES  M1D3
         MVC   IOBDADAD,CB25            MOVE CB25 MBBCCHHR TO IOB
         MVI   IOBAPP,X'0F'             SET APPENDAGE CODE TO 15
         NI    IOBFLAG1,X'7F'           DONT USE DATA CHAINING
         OI    IOBFLAG1,X'40'           USE COMMAND CHAINING
         LA    R12,CJ1                  START AT CJ1
         ST    R12,IOBSTART-1
         LA    R12,IOBDADAD+3           MOVE IOB ADDRESS TO CJ1 AND CJ5
         ST    R12,CJ1
         MVI   CJ1,SIDEQ                RESTORE COMMAND CODE     S20201
         MVC   CJ5+ONE(THREE),CJ1+ONE      MOVE IN ADDR OF SEEK  S20201
         MVC   CJ3+ONE(THREE),DECBKEY+ONE  MOVE IN ADDR OF KEY   S20201
         MVC   CJ7+ONE(THREE),DECBKEY+ONE                        S20201
         LA    R12,0(R0,R0)             CMP TRK INDEX AND OFLO RCD MODL
         IC    R12,CB25                  ARE THEY THE SAME
         SLL   R12,4(R0)
         L     R12,32(R3,R12)
         LA    R12,0(R0,R12)
         CLR   R12,R7
         BE    APPRVXCP                 YES - EXCP RETURN TO IOS
         MVI   IOBASYN,X'01'
         B     APPM1D4                  EXCP IN ASYNCHRONOUS ROUTINE
APPM1D3  MVI   IOBASYN,X'0A'            SET ASYNCHRONOUS CODE TO 10
APPM1D4  NI    IOBINDCT,X'DF'           INDICATE OFLO RECORD IN AREA
         B     APPM1B5                  SCHEDULE ASYNCHRONOUS ROUTINE
APPM1B3  OI    DECBEXC1,X'04'           RECORD IS UNREACHABLE
APPM1B4  MVI   IOBASYN,X'08'            SCHEDULE PROCESSING COMPLETION
APPM1B5  B     BYP(R14)                 SCHEDULE ASYNCH ROUTINE  Y02072
         EJECT
*              ABNORMAL END CP15,   APPENDAGE CODE 14,         CHART M1
APPM1J2  TM    IOBECBAD,PERRMASK        WAS ABEND DUE TO PERM ERROR
         BC    PERRNO,APPRVNOR          NO   CONTINUE, NORMAL IOS
         B     APPM1B3                  YES  M1B3
APPN3B21 LA    R13,APPN3B2
         BC    15,APPN3B22
APPM1B21 LA    R13,APPM1B2
APPN3B22 BR    R13                      NULL ROUTINE             Y02072
APPRVXCP EQU   *
         SR    R12,R12
         IC    R12,IOBDADAD             EXTENT
         SLL   R12,4                    LENGTH OF 1 EXTENT=16
         LA    R12,32(R12,R3)           ADDR OF CORRECT EXTENT
         MVC   IOBDADAD+1(2),4(R12)     MOVE BB
         LA    R14,EXCP(R14)            SETUP EXCP RETURN TO IOS  15924
         XC    IOBFLAG2(3),IOBFLAG2     CLEAR FLAG2,SENSE BYTES   15924
         XC    IOBCSW(9),IOBCSW         CLEAR FLAG3,CSW,AND SIOCC 15924
         XC    IOBERRCT(2),IOBERRCT     CLEAR ERROR COUNT         15924
         NI    IOBFLAG1,X'C2'           RESET FLAG1               15924
APPRVNOR BR    R14                      RETURN TO IOS             15924
         EJECT
*                        ADDRESS OF COMMUNICATION VECTOR TABLE
CVTPTR   EQU   16
LASTIND  EQU   X'02'       LAST BLK READ TO CHK FOR DELETES      A31323
*                             RETURN TO IOS VECTOR TABLE
MODID    DC    C'IGG019G0'              MODULE NAME            @ZA26522
DATE     DC    CL8'&SYSDATE'            FIX DATE               @ZA26522
FIX      DC    C' OZ26522'              LATEST FIX             @ZA26522
PATCH    DC    XL((*-IGG019G0)/20)'00'  ZEROED PATCH AREA        Y02072
*
CVT      DSECT
         DS    F         COMMUNICATION VECTOR TABLE EXIT EFFECTOR
CVTEXEF  DS    A                        *                        Y02072
         SPACE 2
RQE      IECDRQE ,                      RQE                      Y02072
         EJECT
IHAPART2 DS    0D                       PART 2 VECTOR TABLE
*        ROUTINE FOR          CODE      CEND COMPLETION OF
APPM67CE DS    CL6            7         CP1 OR CP2, WRITE KN
APPN7B2  DS    CL6            9,23      CP10A
APPN8A2  DS    CL6            10,11     CP10B
APPM4A2  DS    CL6            12        CP14  - SETUPS 1, 2, OR 5
APPM5A2  DS    CL6            13        CP14  - SET UPS 3, 4, OR 6
APPM2B2  DS    CL6            15        CP16 - SITUATION 2
APPM3B2  DS    CL6            16        CP16 - SITUATION 3
APPN9A2  DS    CL6            17,18,19  CP17
         DS    CL6                                                17332
*        ROUTINE FOR          CODE     ABEND COMPLETION OF
APPM67AE DS    CL6            7         CP1 OR CP2, WRITE KN
APPN7J2  DS    CL6            9,23      CP10A
APPN8G2  DS    CL6            10,11     CP10B
APPM4E2  DS    CL6            12        CP14  -SETUPS 1, 2, OR 5
APPM5E2  DS    CL6            13        CP14  -SETUPS 3, 4, OR 6
APPM2H2  DS    CL6            15,16     CP16
APPN9G4  DS    CL6            17,18,19  CP17
         DS    CL6                                                17332
APPN7A3  DS    CL4            CONTINUATION OF ROUTINES BEGUN IN
APPN7B4  DS    CL4                 PART 1
*        ROUTINE FOR          CODE      CEND COMPLETION OF
APPABA2  DS    CL6            0,1       CP4/CP5
APPADB2  DS    CL6            2         CP7
APPAAB2  DS    CL6            3         CP1 OR CP2, READ AND UPDATE
APPACA2  DS    CL6            5         CP6
*        ROUTINE FOR          CODE     ABEND COMPLETION OF
APPABD2  DS    CL6            0,1       CP4/CP5
APPADE2  DS    CL6            2         CP7
APPAAA2  DS    CL6            3         CP1 OR CP2, READ AND UPDATE
APPACE2  DS    CL6            5         CP6
IHAWKNCP IGGWKNCP
         IGGCP9A
         IGGCP9B
         IGGCP9C
         EJECT
*              DATA EVENT CONTROL BLOCK
IHADECB  DSECT
         DS    0F
DECBECB  DS    CL4                      EVENT CONTROL BLOCK (ECB)
DECBTYP1 DS    BL1                      TYPE B6 - 1 IF LENGTH IS S
*                                            B7 - 1 IF AREA IS S
DECBTYP2 DS    BL1                           B0 - 1 IF READ K
*                                            B1 - 1 IF READ KX
*                                            B2 - 1 IF READ KU
*                                            B4 - 1 IF WRITE K
*                                            B5 - 1 IF WRITE KN
DECBLGTH DS    CL2                      LENGTH OF BLOCK
DECBDCBA DS    A                        POINTER TO DCB
DECBAREA DS    A                        ADDRESS OF AREA
DECBLOGR DS    A                        POINTER TO LOGICAL RECORD
DECBKEY  DS    A                        POINTER TO KEY
DECBEXC1 DS    BL1                      EXCPTN CD B0-RECORD NOT FOUND
*                                                 B1-RECORD LGTH CHK
*                                                 B2-NO SPACE
*                                                 B3-INVALID REQUEST
*                                                 B4-UNCORRECTABLE IO
*                                                 B5-UNREACHABLE BLOCK
         DCBD  DSORG=(IS)                                        S20201
IHAIOB   IGGIOBD
IHADCW   IGGBISAM
IHADEB   IGGDEBD
         END
