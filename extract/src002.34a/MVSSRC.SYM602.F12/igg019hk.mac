         TITLE  'IGG019HK - SETL K OR KC APPENDAGE'
IGG019HK CSECT
*          RELEASE OS/VS2-02 DELETIONS                                *
*C024350                       EQUIVALENT TO APAR OX04644       YA03706
*          RELEASE 19 DELETIONS                                       *
*0875023200-023400,049500-050600                                 O19111
*0875                                                            M4902
*0875039060                                                      M4902
*0875                                                            M4581
*          RELEASE 20 DELETIONS                                       *
*1180                                                            M6118
*1180027000                                                      M5706
*1180                                                            A32508
*1180052400-052600,052800-077400,078000,078060-085800            S20201
*          RELEASE 21 DELETIONS                                       *
*                                                                S21045
*1192031000-031100                                               A42867
*1192019800-020200                                              SA52459
*                                                                     *
*STATUS CHANGE LEVEL 008
*                                                                     *
*FUNCTION/OPERATION- THIS APPENDAGE DETERMINES WHY THE SETL K CHANNEL *
*   PROGRAM STOPPED AT A PARTICULAR CCW. IT WILL INDICATE WHETHER     *
*   THERE WAS AN UNREACHABLE BLOCK OR IF THE SEARCHED KEY COULD NOT   *
*   BE FOUND AND TAKE A NORMAL RETURN TO IOS. BY INTERROGATING THE    *
*   SETTING OF CERTAIN CCWS AND WHERE THE CHANNEL PROGRAM STOPPED,    *
*   THE APPENDAGE ASCERTAINS WHICH IS THE HIGHEST LEVEL INDEX AND WHAT*
*   KIND OF RECORDS ARE BEING SEARCHED, AND STARTS THE CHANNEL        *
*   PROGRAM UP AGAIN,ACCORDINGLY.                                     *
*                                                                     *
*ENTRY POINTS- THE APPENDAGE VECTOR TABLE ADDRESSES THE ENTRY POINTS  *
*   TO MODULE 'IGG019HK'.                                             *
*        SISSMB2 - CHANNEL END                                        *
*        SISSNB4 - ABNORMAL END                                       *
*                                                                     *
*INPUT- REGISTER 1- 12* ADDRESS                                       *
*       REGISTER 2- IOB ADDRESS                                       *
*       REGISTER 3- DEB ADDRESS                                       *
*       REGISTER 4- DCB ADDRESS                                       *
*       REGISTER 7- UCB ADDRESS                                       *
*       REGISTER 12- WORK AREA ADDRESS                                *
*       REGISTER 14- RETURN TO IOS VIA IGG019HG                       *
*       REGISTER 15- MODULE BASE                                      *
*                                                                     *
*OUTPUT- SAME AS FOR INPUT                                            *
*                                                                     *
*EXTERNAL ROUTINES- N/A                                               *
*                                                                     *
*EXITS- NORMAL- RETURN TO IOS THROUGH REGISTER 14                     *
*   ERROR-N/A                                                         *
*                                                                     *
*TABLES/WORK AREAS- DATA EXTENT BLOCK(DEB) DESCRIBED BY SISDEB DSECT  *
*   WORKAREA DESCRIBED BY SISWORK1 DSECT                              *
*   DATA CONTROL BLOCK DESCRIBED BY IHADCB DSECT                      *
*   CHANNEL PROGRAMS 23 AND 26 DESCRIBED BY SISCP236 DSECT            *
*                                                                     *
*ATTRIBUTES- REENTRANT, READ ONLY                                     *
*                                                                     *
*NOTES- REGISTER USAGE IS CONSISTENT IN ALL SCAN MODE.                *
*   APPENDAGE MODULES                                                 *
         EJECT
*                                *******************************
*                                *                             *
*                                * CHANNEL END APP SETL K OR KC*
*                                *                             *
*                                *******************************
         USING SISWORK1,R12
         USING IHADCB,R4
         USING SISCP236,R11
         USING *,R15
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
NORMAL   EQU   0                                                  15924
ASYNCH   EQU   4                        BRANCH OFFSET TO ASYNCH  Y02072
*                                       SCHEDULING ROUTINE IN
*                                       IGG019HG
EXCP     EQU   8                                                  15924
BYPASS   EQU   12                                                 15924
CYLINDEX EQU   X'01'                       F BYTE - NORMAL CYL INDEX
K7       EQU   7                        CONSTANT
K6       EQU   6                        CONSTANT
         SPACE 2
SISCTSM  DC    A(SISSMB2-SISCTSM)
         DC    A(SISSNB4-SISCTSM)
         USING *,R15
SISSMB2  L     R5,W1COMAD               LOAD IOBI CSW ADDR, ERR CCW +8
         LA    R5,0(R5)                CLEAR HI ORDER BYTE
         SH    R5,BYTE8A                BRING R5 TO CCW IN ERROR
         L     R11,W1CP23PT        SET BASE FOR DSECT
*
*   DID WE STOP AT CS15 - AOS FORCED SPLIT.
*
         LA    R8,CS15                     PTR TO CS15
         CR    R5,R8                       STOP THERE.
         BE    AOSCS15                     YES - RESTART
*
*   DID WE STOP AT CS32 - AOS FORCED SPLIT.
*
         LA    R8,CS32                     PT TO STOP
         CR    R5,R8                       STOP AT CS32
         BE    AOSCS32                     YES - RESTART CP.
         LA    R8,CS24             SET R8=ADDR OF CS24
         CR    R5,R8               DID CP END AT CS24
         BE    SISSMD3             YES
SISSMB1  LA    R8,CS4              SET R8=ADDR OF CS4
         CR    R5,R8               DID CP STOP AT CS4
         BNE   SISSMA1             NO, GO TO A1
SISSMC1  TM    CS7+7,X'20'         DUMMY OR INACTIVE ENTRY
         BZ    SISSMD1             NO, THEN MUST BE NORMAL OR OVFLO
*                                  YES.  IS IT
SISSMC2  TM    CS7+7,X'08'         DUMMY ENTRY CHAINED
         BO    SISSME1A            YES
SISSMC3  NI    W1OSBIT1,X'F7'       INDIC KEY NOT FOUND
         SPACE 2
SISSMD3  NI    W1IF1,X'FB'         RESET IOBI EXCEPT BIT
SISSMD31 EQU   *                                                 Y02072
         LA    R10,NORMAL               LOAD IOS RET ADDR OFST   Y02072
         BR    R14                      RETURN TO IOS VIA EXIT   Y02072
*                                       ROUTINE IN IGG019HG
         SPACE 3
SISSMD1  EQU  *                         *
SISSME1A MVI   CS7+K6,0                 SET R OF SEEK TO 0
         MVC   W1IDAD,CS6+K7            MOVE SEEK FIELD INTO IOB.
*
*   TEST FOR BROKEN CP23.
*
         CLI   CS7+K7,CYLINDEX             CYLINDER INDEX       SA52459
         BE    SPLTSTOP                    YES - RESTART AT CS9 SA52459
         ST    R11,W1ICPS               START AT CS1
SISSMF1  EQU   *                                                 Y02072
         SR    R13,R13                 CLEAR WORK REGISTER.
         IC    R13,W1IMBBCC            R13=000M
         SLL   R13,4                   M X 16
         LA    R13,32(R3,R13)          R13 POINTS TO EXTENT OF M
         MVC   W1IMBBCC+1(2),4(R13)    MOVE DEB BB INTO IOBI
         XC    W1IF2(3),W1IF2           CLEAR FLAG2,SENSE BYTES   15924
         XC    W1ICSW(9),W1ICSW         CLEAR FLAG3,CSW,SIOCC     15924
         XC    W1IERRCT(2),W1IERRCT     CLEAR ERROR COUNT         15924
         NI    W1IF1,X'C2'              RESET FLAG1               15924
         LA    R10,EXCP                 LOAD IOS RET ADDR OFST   Y02072
         BR    R14                      RETURN TO IOS VIA EXIT   Y02072
*                                       ROUTINE IN IGG019HG
SISSMA1  LA    R8,CS31
         CR    R5,R8               DID CP STOP AT CS31
         BE    SISSMA4             YES
         TM    DCBRECFM,X'10'           IS FORMAT UNBLOCKED      O19111
         BZ    SISSMC3                  YES, INDICATE KEY NOT    O19111
*                                       FOUND                    O19111
SISSMA3  OI    W1OSBIT1,X'04'      INDICATE UNREACHABLE
         B     SISSMD3
SISSMA4  OI    W1OSBIT1,X'20'      SET ON OVERFLOW INDICATOR
         B     SISSMD3
AOSCS15  EQU   *                           *
*
*   CP STOPPED AT AOS FORCED STOP ON CS15 - RESTART
*
         CLI   CS19,SEEKHH                 OVERFLOW NEXT
         BNE   SISSND2                     YES - START CP26
*                                          NO - RESTART CP23
         MVC   W1IDAD,CS17+K7              SRCH ADDRESS
         LA    R8,CS20                     START ADDRESS
         ST    R8,W1ICPS                   *
         B     SISSMF1                     RESTART CP
AOSCS32  EQU   *                           *
         MVC   W1ICPS,W1CP26PT          START CP 26
         MVC   W1IDAD,CS34+K7              SET SEEK ADDRESS FOR RESTART
         CLI   CS36,SEEK                   FULL SEEK
         BNE   SISSMF1                     NO - REEXCP CP26
         B     SISSNG3                 SCHEDULE ASYNCHRONOUSLY  YA03706
         EJECT
SISCTSN  DS    0F
*                           *********************************
*                           *                               *
*                           * ABNORMAL END APP SETL K OR KC *
*                           *                               *
*                           *********************************
         SPACE 4
*                                  ***** CONSTANTS *****
SIXTEEN  EQU   16                  OFFSET FOR COMMUN. VECTOR TABLE
BYTE8A   DC    HL2'08'
ENTRY2   EQU   4                   USED TO SCHEDULE ASYCH. ROUTINE
         SPACE 4
SISSNB4  EQU   *                                                 M5706
*
*   REMOVE RPS PREFIX IF ATTACHED
*
         L     R11,W1ICPS               CHANNEL PROGRAM START    M5706
         CLI   CS1,SETSECT              IS THE PREFIX ATTACHED   M5706
         BNE   SISSNB41                 NO - CONTINUE            M5706
*                                       YES - -REMOVE PREFIX
         MVC   W1ICPS+1(3),W1ICPEXT+9   CP STARTING ADDRESS      M5706
         SPACE 5                                                 M5706
SISSNB41 EQU   *                                                 M5706
         L     R5,W1COMAD               IOB CSW ADDRESS          M6118
         LA    R5,0(R5)                CLEAR HI ORDER BYTE
         SH    R5,BYTE8A           BRING R5 TO ERROR CCW
         L     R11,W1CP23PT
         TM    W1ISENSE+1,X'04'         STOPPED ON PROTECTION CHK 13888
         BO    SISSNC1                  YES--BRANCH               13888
         TM    W1IECBAD,X'20'          PERMANENT ERROR            17516
         BZ    SISSNC4                 YES                        17516
SETCTR1  L     R8,CS25                 NO                         17516
         MVI   4(R8),X'00'             ZERO R IN D.A. DEVICE ADDR 17516
*                                                      (CN6)      17516
         B     SISSMD31                GO TO ERPS                 17516
*                                                                 17516
         SPACE 2
SISSNC1  LA    R8,CS8                   DID CP STOP AT CS8
         CR    R5,R8
         BNE   SISSNC2             NO
*                                  YES
SISSND1  TM    CS7+7,X'20'         DUMMY OR INACTIVE ENTRY
         BO    SISSMC2             YES, SEE IF CHAINED
SISSNE1  EQU   *                        *                        A42867
         MVI   CS7+6,X'00'              SET R TO 0               A42867
         MVC   W1IDAD,CS6+7             SET SEEK TO ENTRY FOUND  A42867
         CLI   CS7+7,X'01'             CLY IND
         BNE   SISSNG1A                 NO
SPLTSTOP EQU   *                           *                    SA52459
         LA    R8,CS9                  SET CP POINTER TO CS9 IF NEXT
*                                       IS TRACK INDEX
         ST    R8,W1ICPS
SISSNG1  CLI   CS8,X'07'
*
*              THE PREVIOUS INSTRUCTION TESTS THE P IN CS8 FOR A
*              SEEK BBCCHH (07). IF YES, THEN TRACK INDEX ON DIF. MOD.
*
         BNE   SISSMF1             SAME MODULE
         B     SISSNG2
SISSNG1A SR    R10,R10                  CLEAR WORK REG
         IC    R10,W1IMBBCC            R10=000M
         SLL   R10,4                   M X 16
         L     R13,32(R3,R10)          R13 POINTS TO EXTENT OF M
         SR    R10,R10                   CLEAR IN CASE M GT 16   A32508
         IC    R10,CS6+7               R10=000M
         SLL   R10,4                   M X 16
         L     R10,32(R3,R10)          R10 POINTS TO EXTENT OF M
         CR    R13,R10                 TRK  IX ON SAME DEVICE
         BE    SISSMF1                  YES, BRANCH.
         SPACE 2
*        SCHEDULE ASYNCHRONOUS ROUTINE
         SPACE 2
SISSNG2  EQU   *                                                 Y02072
         B     ASYNCH(,R14)             BRANCH TO EXIT ROUTINE   Y02072
*                                       IN IGG019HG
         SPACE 2
SISSNC2  LA    R8,CS19
         CR    R5,R8               DID CP STOP AT CS19
         BNE   SISSNH3             NO
SISSND2  TM    CS18+7,X'20'        DUMMY OR INACTIVE ENTRY
         BO    SISSMC3             YES, TAKE NORMAL RETURN
*                                  NO, OVERFLOW RECORD
*        SET LPDR TO THE DATA TRACK ADDRESS WHICH THE OVERFLOW
*        CHAIN SEQUENTIALLY FOLLOWS
*
*        TEST FOR SPECIAL CASE WHERE OVERFLOW CHAIN IS AT END OF DATA
*        SET OR AT END OF CYLINDER
*        FOR OVERFLOW, SEARCH KEY EQUAL OR HIGH UNLESS A SETL K
*        OR SETL KC IS BEING PERFORMED AND THE KEY IS NOT WITHIN
*        THE DATA (I.E. RKP=0,RECFM=F)
*
         MVI   CS29,X'69'               SET SEARCH TO HIGH OR =  M4581
         NC    DCBRKP,DCBRKP            IS RKP EQUAL TO 0        M4581
         BNZ   SISSND20                 NO, BRANCH               M4581
         TM    DCBRECFM,X'10'           ARE PRIME RECS BLOCKED   M4581
         BO    SISSND20                 YES--BRANCH              M4581
         TM    W1REGSAV,X'02'           EVEN IF KEY NOT IN DATA  M4581
         BO    SISSND20                 USE EQ OR HI IF SETL KH  M4581
         MVI   CS29,X'29'               OTHERWISE, SEARCH KEY EQ M4581
SISSND20 EQU   *                                                 M4581
*                                       *    VALUE
         SPACE 3
         TM    DCBRECFM,X'80'           IS IT VLR                 VLR
         BO    SISSND21                 NO, ITS FIXED             VLR
         MVC   W1WDCXDM+7(1),DCBHIRPD   SET R                     VLR
SISSND21 EQU   *                                                  VLR
         TM    W1WDNXDM+8,X'20'         END OF CYLINDER
         BO    SISSND22                 YES
*                                       NO, NORMAL CASE.  ONLY PART OF
*                                       LPDR WHICH MATTERS IS R
         TM    DCBRECFM,X'80'           IS IT FIXED               VLR
         BO    SISSFIX                  YES, BRANCH               VLR
         SR    R8,R8                                              VLR
         IC    R8,W1WDNXDM+5                                      VLR
         SLL   R8,8                                               VLR
         IC    R8,W1WDNXDM+6                                      VLR
         BCTR  R8,0                     POINT BACK TO PRIME TRK   VLR
         STC   R8,W1LPDR+6              ASSOCIATED WITH OVFLOW    VLR
         SRL   R8,8                                               VLR
         STC   R8,W1LPDR+5                                        VLR
         B     SISSND30                                           VLR
SISSFIX  EQU   *                                                  VLR
         OI    W1OSBIT2,X'10'          INDICATE SHARED TRACK      0756
         MVC   W1LR,DCBHIRSH                 USE HIRSH FOR SHARED
         B     SISSND3
SISSND22 CLC   DCBLPDA(1),CS6+7        COMP M
         BNE   SISSNC33                NO, BRANCH
         IC    R8,DCBLPDA+6            SAVE H
         OC    DCBLPDA+6(1),DCBFIRSH+3 REDUCE TO A
         XC    DCBLPDA+6(1),DCBFIRSH+3  CYL BOUNDRY
         CLC   CS7+2(4),DCBLPDA+3      IS THE CYL LAST OF DATA SET
         STC   R8,DCBLPDA+6            RESTORE SAVED H
         BNE   SISSNC33                 NO
SISSNC34 MVC   W1LPDR,DCBLPDA           YES, SET FOR END OF DATA SET
         B     SISSND3
         SPACE 2
*        LAST OVERFLOW CHAIN OF CYLINDER BUT NOT OF DATA SET
         SPACE
SISSNC33 MVC   W1LPDR(7),CS6+7         MOVE MBBCCHH INTO LPDR
         OC    W1LPDR+6(1),DCBLDT+1    OR IN LAST PRIME DATA TRACK
SISSND30 MVC   W1LR,DCBHIRPD            R=HI R PRIME DATA
         SPACE 3
SISSND3  MVC   PRIMEIND(1),W1IDAD  SAVE M (IOBI+32) IN PRIMEIND
SISSNE2  MVC   W1IDAD,CS17+7       SET IOB SEEK TO START OF OVERFLOW
*                                  CHAIN
SISSNF2  MVC   CS34+7(10),CS17+7
*
*              PREV INST SET CS34 AND CS35 (CP26) TO SEEK ADD. IN
*              CS17 AND CS18 (CP23)
*
         MVC   W1ICPS,W1CP26PT   SET CP PNTR TO CS27 (1ST CCW OF CP26)
         B     SISSNG1A
         SPACE 2
SISSNC4  TM    W1ISENSE+1,X'08'        NO RECORD FOUND            17516
         BZ    SISSMA3                 NO,INDICATE UNREACHABLE    17516
         LA    R8,CS25                                            17516
         CR    R5,R8                   DID CP STOP ON CS25        17516
         BNE   SISSMA3                 NO, INDICATE UNREACHABLE   17516
*                                                                 17516
         TM    DCBRECFM,X'10'           IS FORMAT UNBLOCKED      O19111
         BZ    SISSMC3                  YES, INDICATE KEY NOT    O19111
*                                       FOUND                    O19111
         B     SISSMA3             BLOCKED, GO TO SMA3
         SPACE 2
SISSNH3  LA    R8,CS36
         CR    R5,R8               DID CP STOP AT CS36
         BNE   SISSMA3             NO, GO TO SMA3
SISSNG3        EQU        *
         CLI   CS35+7,X'10'             END OF OVERFLOW CHAIN     13888
         BE    SISSMC3                  YES, KEY NOT FOUND        13888
SISSNH2  MVC   W1IDAD,CS34+7       RESET IOB+32
         B     SISSNG2
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
         EJECT
SISWORK1 IGGSCAN                        SCAN MODE WORK AREA      S20201
         EJECT
SISCP236 DSECT
         IGGCP23
         IGGCP26
         SPACE 3
         SPACE 5
         DCBD  DSORG=(IS)
         END
