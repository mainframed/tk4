         TITLE 'IGG0192N - OPEN,BISAM WRITE KN,SET UP CHANNEL PROGRAMS'
IGG0192N CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192N                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, BISAM MODE, WKN AND ISAM-PROVIDED     *
*                    WORK AREA                                        *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = WHEN USER DOES NOT SUPPLY WORK AREA, COMPLETE INITIALI-  *
*            ZATION UNIQUE TO WKN, WHETHER OR NOT READ IS PRESENT.    *
*            FOR UNBLOCKED RECORDS, MOVE AND INITIALIZE CP9A, CP9B    *
*            (OR CP9BW FOR WRT CHK), AND CP9C (OR CP9CW FOR WRT CHK). *
*            FOR BLOCKED RECORDS, MOVE AND INITIALIZE CP11A AND CP11B *
*            (CP11BW FOR WRT CHK).  THEN, DELETE ORIGINAL WKN CHANNEL *
*            PROGRAM MODULE.                                          *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 700 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192N                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0192Q      *
*              IF USER DOES NOT SUPPLY WORK AREA.  RECEIVES CONTROL   *
*              IN STORAGE PROTECT KEY 5 AND PRIVILEGED STATE.         *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG0192J IN STORAGE        *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGBISAM - BISAM WORK AREA                          *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY                                        *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, DELETE, AND XCTL                                  *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
RE       EQU   00                       WORK REG
RF       EQU   01                       WORK REG
RDCB     EQU   02                       USER'S DCB
RBASE    EQU   03                       BASE REG
RG       EQU   04                       WORK REG
RPAR     EQU   05                  *    PARAMETER LIST ADDRESS
RWTG     EQU   06                  *    WHERE TO GO TABLE BEGINNING
RPARC    EQU   07                  *    CURRENT ENTRY,PARAMETER LIST
RWTGC    EQU   08                  *    CURRENT ENTRY, WTG TABLE
RTIOT    EQU   09                       TIOT ADDRESS
RUCB     EQU   10                       UCB ADDRESS
RA       EQU   11                       WORK REG
RB       EQU   12                       WORK REG
RC       EQU   13                       WORK REG
RD       EQU   14                       WORK REG
R15      EQU   15                       WORK REG
RCORE    EQU   RG                       DCB WA ADDRESS
*                   *     MEANS SET BEFORE ENTRY AND BEFORE EXIT
WRCHK    EQU   X'80'                    WRITE VALIDITY CHECK     Y02072
         BALR  RBASE,0                  ESTABLISH  ADDRESSABILITY
         USING *,RBASE
         L     RDCB,0(RPARC)            RDCB  ADDRESS OF DCB
         USING IHADCB,RDCB
         L     RCORE,DCBWKPT2           RCORE ADDRESS OF DCB WA
         USING IHADCW,RCORE
         L     RD,DCBWKPT3              RD    ADDRESS OF WKN CP
         USING WTGDSECT,RWTGC           WHERE TO GO TABLE        Y02072
         L     RC,OPENWA                OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,RC               WA ADDRESSABILITY        Y02072
         L     RA,DXCCW9                ADDR OF CURRENT CP       Y02072
         L     RF,DXCCW10               CURR CP ADDR IN MODULE   Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=10  CHANGE TO USER KEY  Y02072
*
         DROP  RC                       END USING ON WORK AREA   Y02072
*
         XC    DCWFIOBU,DCWFIOBU        ZERO AREAS USED TO SAVE
         XC    12(4,RD),12(RD)           ADDRESS OF CURRENT CP
         TM    DCBRECFM,X'10'
         BO    OPNO7D5I                 BRANCH IF BLOCKED
         USING CP9A,RA                  INITIALIZE CP'S FOR UNBLOCKED
         MVC   0(SZ9A,RA),0(RF)         MOVE CP9A BASIC          S20201
*                                       STRUCTURE                S20201
         L     RC,CB31
         ALR   RC,RD                    BEGIN INITIALIZING CP9A
         ST    RC,CB31
         MVC   CB32+1(3),DCBMSWA+1      WORK AREA ADDR TO CB32
         SR    RC,RC
         IC    RC,DCBKEYLE
         STH   RC,CB32+6                KEY LGTH TO CB32
         AL    RC,DCBMSWA
         LA    RC,10(RC)
         ST    RC,CB33                  KEY LGTH&WA&10 TO CB33
         MVC   CB33+6(2),DCBBLKSI       BLOCKSIZE TO CB33
         LA    RA,SZ9A(RA)                                       S20201
         LA    RF,SZ9A(RF)                                       S20201
         USING CP9B,RA                  MOVE CP9B
         MVC   0(SZ9B,RA),0(RF)         MOVE CP9B BASIC          S20201
*                                       STRUCTURE                S20201
         L     RC,CB35
         ALR   RC,RD
         ST    RC,CB35
         MVC   CB36+7(1),DCBKEYLE       INITIALIZE CB35,CB36 AND CB37
         MVC   CB37+6(2),DCBBLKSI
         LA    RA,SZ9B-SIXTEEN(RA)      *                        S20201
         LA    RF,SZ9B-SIXTEEN(RF)      *                        S20201
         TM    DCBOPTCD,X'80'
         BZ    OPNO7D5G                 BRANCH NOT WC
         USING CB370,RA                                          S20201
         MVC   SIXTEEN(SZ9BWC,RA),SIXTEEN(RF) INSERT WC COMMANDS S20201
         L     RC,CB37B
         ALR   RC,RD
         ST    RC,CB37B                 INSERT AND INITIALIZE
         SR    RC,RC
         IC    RC,DCBKEYLE
         AH    RC,DCBBLKSI
         STH   RC,CB37C+6                CP9BW COMMANDS
         LA    RA,SZ9BWC(RA)            *                        S20201
         LA    RF,SZ9BWC(RF)            *                        S20201
         USING CB38,RA                  *                        S20201
OPNO7D5G MVC   CB38+7(1),DCBKEYLE       COMPLETE CP9B INITIALIZATION
         MVC   CB39+6(2),DCBBLKSI
         LA    RA,SIXTEEN(RA)           *                        S20201
         LA    RF,SIXTEEN(RF)           *                        S20201
         USING CP9C,RA                  CP9C, CP9CW
         MVC   0(SZ9C,RA),0(RF)         MOVE CP9C BASIC          S20201
*                                       STRUCTURE                S20201
         L     RC,CB41
         ALR   RC,RD
         ST    RC,CB41
         MVC   CB42+1(3),DCBMSWA+1
         SR    RC,RC                    AND MOVE IN WA,
         IC    RC,DCBKEYLE
         STH   RC,CB42+6
         AL    RC,DCBMSWA
         LA    RC,10(RC)                KEY LGTH AND BLOCKSIZE
         ST    RC,CB43
         MVC   CB43+6(2),DCBBLKSI
         LA    RA,SZ9C-SIXTEEN(RA)      *                        S20201
         LA    RF,SZ9C-SIXTEEN(RF)      *                        S20201
         TM    DCBOPTCD,X'80'
         BZ    OPNO7D5H                 BRANCH NOT WC
         USING CB430,RA                                          S20201
         MVC   CCW2(SZ9CWC,RA),CCW2(RF) INSERT WC COMMANDS       S20201
         L     RC,CB43B
         ALR   RC,RD
         ST    RC,CB43B
         SR    RC,RC
         IC    RC,DCBKEYLE              AND INITIALIZE THEM
         AH    RC,DCBBLKSI
         STH   RC,CB43C+6
         LA    RA,SZ9CWC(RA)                                     S20201
         LA    RF,SZ9CWC(RF)                                     S20201
         USING CB44,RA                  COMPLETE CP9C AND CP9CW  S20201
OPNO7D5H MVC   CB44+1(3),DCBMSWA+1
         SR    RC,RC
         IC    RC,DCBKEYLE
         STH   RC,CB44+6
         AL    RC,DCBMSWA               INITIALIZE CB44 AND CB45
         LA    RC,10(RC)
         ST    RC,CB45
         MVC   CB45+6(2),DCBBLKSI
         LA    RA,SIXTEEN(RA)           *                        S20201
         LA    RE,OPNMCPKN              BASIC MODULE ADDR FOR DELETION
         B     OPNO7E5D
         USING CP11A,RA                 INITIALIZE BLOCKED CP'S
OPNO7D5I MVC   0(SZ11A,RA),0(RF)       MOVE CP11A BASIC          S20201
*                                       STRUCTURE                S20201
         L     RC,CC2
         ALR   RC,RD
         ST    RC,CC2                   RELOCATE ADDR IN CC2
         MVC   CC2A+1(3),DCBMSWA+1
         SR    RC,RC
         IC    RC,DCBKEYLE
         STH   RC,CC2A+6                KEY LGTH AND WA TO CC2A
         AH    RC,DCBLRECL
         AL    RC,DCBMSWA
         STH   RC,CC3+2
         SRL   RC,16
         STC   RC,CC3+1                 W&IL&RL AND AL TO CC3
         MVC   CC3+6(2),DCBBLKSI
         LA    RA,SZ11A(RA)             *                        S20201
         LA    RF,SZ11A(RF)             *                        S20201
         USING CP11B,RA                 CP11B, CP11BW
         MVC   0(SZ11B,RA),0(RF)       MOVE CP11B BASIC          S20201
*                                       STRUCTURE                S20201
         L     RC,CC5
         ALR   RC,RD
         ST    RC,CC5
         MVC   CC6+1(3),DCBMSWA+1
         SR    RC,RC                    INITIALIZE CC5 AND CC6
         IC    RC,DCBKEYLE
         AH    RC,DCBBLKSI
         STH   RC,CC6+6
         TM    DCBOPTCD,X'80'
         BZ    OPNO7D5J                 BRANCH NOT WC
         USING CP11BW,RA
         MVC   SZ11B(SZ11BWC,RA),SZ11B(RF) MOVE WC COMMANDS      S20201
         STH   RC,CC6C+6
         L     RC,CC6B                  ADD THE WC COMMANDS
         ALR   RC,RD
         ST    RC,CC6B
         LA    RA,SZ11BWC(RA)                                    S20201
OPNO7D5J LA    RA,SZ11B(RA)                                      S20201
         LA    RE,OPNMCPKN+8            BASIC MODULE ADDR FOR DELETION
*
OPNO7E5D MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
         TM    DCBOPTCD,WRCHK           TEST FOR WRITE CHECK     Y02072
         BZ    OPNO7E5E                 BRANCH NOT WC
         LA    RF,16
         AR    RE,RF
*
OPNO7E5E DELETE  EPLOC=(0)              DELETE ORIGINAL CP MODULE
OPNEND2N MVC   0(L'LOAD2J,RWTGC),LOAD2J ID OF IGG0192J           Y02072
         L     RCORE,4(RWTGC)
         USING FORCORE,RCORE            WORK AREA ADDR           Y02072
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT NAME ON THE WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)          AND DCB ON THE PARAM TABLE
         CLC   0(2,RWTGC),OPNEXNAM      IF NAME ON TABLE SAME ID AS
         BCR   8,RBASE                   CURRENT LOAD, BRANCH TO REPEAT
         CLC   0(2,RWTGC),OPIDCNST     IF NOT END OF WTG TABLE BRANCH
         BNE   RELOOP                    TO TRY NEXT NAME
         LR    RPARC,RPAR               OTHERWISE, RETURN TO START OF
         LA    RWTGC,WAOFF(0,RWTG)       BOTH TABLES AND SEARCH FOR
ZCHECK   CLI   0(RWTGC),ZCNST            FIRST NON-ZERO ID
         BNE   TCTLRTN
         LA    RWTGC,WGOFF(0,RWTGC)
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       WHEN FOUND, SET UP PARAMETERS
*                                        TO CALL THAT MODULE
         LA    R15,DXCCW12                                       Y02072
TRCTL    XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) GO TO NEXT EXEC    Y02072
WGOFF    EQU   8                        SIZE OF ONE ENTRY ON WTG TABLE
PLOFF    EQU   4                        SIZE OF ONE ENTRY ON PARAM TAB
OPNEXNAM DC    C'2N'                    ID OF THIS OPEN EXECUTOR
ZCNST    EQU   X'00'                    ZERO - SIGNALS INACTIVE ENTRY
SIXTEEN  EQU   16                       OFFSET                   S20201
CCW2     EQU   SIXTEEN                  LENGTH OF 2 CCW'S        S20201
WAOFF    EQU   32                       SIZE OF WA AT START OF WTG TAB
OPIDCNST DC    C'0S'                    ID OF OPEN LOAD #7
*                                       FIRST HALF OF
*                            TABLE OF WRITE KN CP MODULES
*                             WC * FU/FB * WA * REL ADDR
*                             ***************************
         DS    0D
OPNMCPKN DC    C'IGG019JN'    N  *  FU   * N  *    0
         DC    C'IGG019JO'    N  *  FB   * N  *    8
         DC    C'IGG019JP'    Y  *  FU   * N  *   16
         DC    C'IGG019JQ'    Y  *  FB   * N  *   24
*
LOAD2J   DC    C'2J'                    ID OF MODULE IGG0192J    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         SPACE 4
         DCBD  DSORG=(IS)
IHADCW   IGGBISAM OPTCD=W                                        S20201
IHAWKNCP DSECT      WRITE KN CHANNEL PROGRAM REFERENCES
         IGGCP8
CP9A     DS    0D                                                S20201
         IGGCP9A
CP9B     DS    0D                                                S20201
CP9BW    DS    0D                                                S20201
         IGGCP9B OPTCD=W                                         S20201
CP9C     DS    0D                                                S20201
CP9CW    DS    0D                                                S20201
         IGGCP9C OPTCD=W                                         S20201
CP11A    DS    0D                                                S20201
         IGGCP11A
CP11B    DS    0D                                                S20201
CP11BW   DS    0D                                                S20201
         IGGCP11B OPTCD=W                                        S20201
SZ9BWC   EQU   SZ9BW-SZ9B               SIZE CP9B WC CCW'S       S20201
SZ9CWC   EQU   SZ9CW-SZ9C               SIZE CP9C WC CCW'S       S20201
SZ11BWC  EQU   SZ11BW-SZ11B             SIZE CP11B WC CCW'S      S20201
WTGDSECT DSECT                                                   Y02072
IDTTR    DS    F                        IDTTR OF MODULE          Y02072
OPENWA   DS    A                        OPEN WORK AREA ADDRESS   Y02072
FORCORE  DSECT                                                   Y02072
         IECDSECT                                                Y02072
         END
