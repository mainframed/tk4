         TITLE 'IGG0192U - OPEN,QISAM LOAD MODE,SET UP CP18 W/WRT CHK'
IGG0192U CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192U                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, LOAD MODE WITH WRITE CHECK            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 01                                *
*                                                                     *
* FUNCTION = LOAD PUT APPENDAGE, AND CHANNEL PROGRAM MODULES WHEN     *
*            WRITE CHECK IS SPECIFIED.  FOR FIXED LENGTH  RECORDS     *
*            (FLR), LOAD IGG019GB, IGG019GD, AND IGG019GE.  FOR VLR,  *
*            LOAD IGG019IB, IGG019GD, AND IGG019IF.  FOR FLR WITH     *
*            FULL TRACK INDEX WRITE (FTIW), LOAD IGG019I2 INSTEAD OF  *
*            IGG019GB.  ALSO GETMAIN FOR CHANNEL PROGRAMS AND         *
*            INITIALIZE CP18.                                         *
*                                                                     *
* NOTES = FOR FTIW, THE LENGTH OF CP20 IS LEFT IN THE CALCULATION FOR *
*         THE CHANNEL PROGRAM GETMAIN TO ALLOW ENOUGH SPACE FOR CP91  *
*         TO OVERLAY CP19.                                            *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 1016 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192U                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0195U IF   *
*              FTIW SPECIFIED, OTHERWISE FROM IGG0192G IF INITIAL     *
*              LOAD, OR IGG0195D IF RESUME LOAD WITH HIGH-LEVEL       *
*              INDICES, OR IGG0196G IF RESUME LOAD WITHOUT HIGH-LEVEL *
*              INDICES.  RECEIVES CONTROL IN STORAGE PROTECT KEY 5    *
*              AND PRIVILEGED STATE.                                  *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG0192S IN STORAGE        *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY, DEB, AND IOB.                         *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, LOAD, GETMAIN, AND XCTL.                          *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*          RELEASE OS/VS2-02 DELETIONS                                *
*                                                               YM01159
*                                                                     *
***********************************************************************
         EJECT
********************
* DCB REFERENCE    *
********************
         DCBD  DSORG=(IS)
         USING IHADCB,R1
         EJECT
********************
* DEB REFERENCE    *
********************
*
IHADEB   IGGDEBD
         USING IHADEB,R8                ADDRESSABILITY
         EJECT
ISLCOMON IGGLOAD
         USING ISLCOMON,R12                                      S20201
         EJECT
* IOBBCT REFERENCE         C(ISLVPTRS+8)=A(IOBBCT)
*
IOBBCT   DSECT
         USING IOBBCT,R11
         DS    0D
IOBFLAGS DS    0CL1                     FLAGS
IOBPTRA  DS    A                        PTR A
IOBB     DS    0CL1                     B
IOBPTRB  DS    A                        PTR B
IOBS     DS    0CL1                     S - STATUS FIELD FOR BUF NO 1
IOBABUF  DS    A                        A(BUF NO 1) - ADR OF BUF NO 1
         SPACE 4
********************
* IOB REFERENCE    *
********************
*
IHAIOB   DSECT
         USING IHAIOB,R2
         DS    0D
IOBFLG1  DS    CL1                      FLAGS 1
IOBFLG2  DS    CL1                      FLAGS 2
AIOBSENS DS    CL2                      SENSE BYTES              Y01021
IOBECBAD DS    A                        ECB POINTER
AIOBCSW  DS    CL8                      CHANNEL STATUS WORD      Y01021
AIOBSIOC DS    0CL1                     SIO CC                   Y01021
IOBCPSAD DS    A                        CHANNEL PROGRAM START ADR
IOBWT    DS    0CL1                     WEIGHT
IOBDCBAD DS    A                        DCB POINTER
IOBCPRAD DS    A                        CHANNEL PROGRAM RESTART ADR
IOBBCTI  DS    CL2                      BLK CTR INCR
IOBECT   DS    CL2                      ERROR CTR
IOBDADAD DS    CL8                      DIR ACESS DEV ADR MBBCCHHR
         EJECT
****************************
* OPEN WORK AREA REFERENCE *
****************************
FORCORE  DSECT                          OPEN/CLOSE/EOV WORK AREA Y01021
         IECDSECT                                                Y01021
         EJECT
* * * * * * * * * *  LOAD CHANNEL PROGRAM SOURCE SKELETON * * * * * * *
LOADCPS  DSECT
         IGGLDCP OPTCD=W                                         S20201
         SPACE 4
* * * * * * * * * *  SKELETON FOR RECEIVING CHANNEL PROGRAMS  * * * * *
CPSTO    DSECT
CPS1     DS    D                        ADDRESS                  S20201
CPS2     DS    D                        ADDRESS                  S20201
CPS3     DS    D                        ADDRESS                  S20201
CPS4     DS    D                        ADDRESS                  S20201
CPS5     DS    D                        ADDRESS                  S20201
         SPACE 4
         CVT   DSECT=YES                                        YM01159
         EJECT
**********************************************************************
* ISL PUT OPEN #2                                                     *
**********************************************************************
*
*
IGG0192U CSECT
TSTF800  BALR  R15,0
         USING *,R15
BASETAG  L     R1,0(RPARC)
         L     RCORE,4(RWTGC)
         L     R12,DCBWKPT1
         LR    RBASE,R15
         USING FORCORE,RCORE                                     Y02072
         STM   R0,R15,DXCCW1            SAVE REGISTERS           Y02072
         LR    R13,RCORE                SAVE OPEN W/A ADDR       Y02072
         DROP  RCORE                                             Y02072
         USING IHADEB,R8                                         S21045
         USING FORCORE,R13              OPEN WA ADDRESSABILITY   Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=11  SET USERS KEY       Y02072
         SPACE 2
         L     R11,ISLVPTR3             BUFFER CONTROL TABLE
         L     R8,DCBDEBAD               DEB ADDRESSABILITY      S21045
*
* EQUATE SYMBOLIC REGISTERS
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RBASE    EQU   3
RCORE    EQU   4
RPAR     EQU   5
RWTG     EQU   6
RPARC    EQU   7
RWTGC    EQU   8
RJ       EQU   15
K0       EQU   0                        CONSTANT                 S20201
K4       EQU   4                        CONSTANT                 S20201
K6       EQU   6                        CONSTANT                 S20201
K7       EQU   7                        CONSTANT                 S20201
K8       EQU   8                        CONSTANT                 S20201
K16      EQU   16                       CONSTANT                 S20201
K20      EQU   20                       CONSTANT                 S20201
K24      EQU   24                       CONSTANT                 S20201
K28      EQU   28                       CONSTANT                 S20201
K35      EQU   35                       CONSTANT                 S20201
K64      EQU   64                       CONSTANT                 S20201
L1       EQU   1                        LENGTH                   S20201
L2       EQU   2                        LENGTH                   S20201
L8       EQU   8                        LENGTH                   S20201
L24      EQU   24                       CONSTANT                 S20201
L32      EQU   32                       CONSTANT                 S20201
K12      EQU   12                       CONSTANT                 S20201
K32      EQU   32                       CONSTANT                 S20201
*
VIRTUAL  EQU   X'80'                    VIRTUAL=VIRTUAL
CEDISP   EQU   4                        CE DISPLACEMENT
AEDISP   EQU   12                       AE DISPLACEMENT
ID       EQU   6                        DISPLACEMENT TO MODULE ID
         EJECT
*
TSTHSK   LA    R2,ISLIOBA               C(R2)=A(IOBA)
*
* LOAD PUT, APP, AND CP MODULES (WR CHK)
*
         L     R9,CVTPTR                ADDRESS OF CVT          YM01159
         USING CVT,R9                   CVT ADDRESSABILITY      YM01159
         L     R9,CVTLINK               ADDRESS OF LINKLIB DCB  YM01159
         DROP  R9                       END CVT ADDRESSABILITY  YM01159
         LR    R4,R1                    SAVE R1
         USING IHADCB,R4                ADDRESSABILITY ON DCB
         DROP  R1                       *
         USING BASETAG,R3               ADDRESSABILITY
         DROP  R15                      *
*
*   R7 WILL BE USED AS AN INDEX FOR FINDING THE CORRECT CHANNEL
*   PROGRAM AND PROCESSING MODULES TO LOAD.
*
         LA    R7,CPVLR-CP              ASSUME VLR
         TM    DCBRECFM,X'80'           IS IT FIXED               VLR
         BNO   LOADPUT                  NO - VLR CORRECT
         SR    R7,R7                    ASSUME FIXED - NOT FTIW
         TM    ISLVPTRA,FTIW            IS IT FTIW
FTIW     EQU   X'C0'                    FTIW INDICATOR
         BNO   LOADPUT                  NO - LOAD PUT MODULE     O19110
         LA    R7,CPFTIW-CP             IT IS FIXED - FTIW
LOADPUT  EQU   *                                                 O19110
*
*   LOAD PROCESSING TIME MODULE - IGG019GB, IGG019IB OR IGG019I2.
*
         LH    R5,PROC(R7)              LAST TWO BYTES OF NAME
         STH   R5,ISLVPTR5              SAVE ID FOR DEB
         SPACE 2
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
         SPACE 2
         BAL   R10,LOAD                 LOAD PROCESSING MODULE
         IC    R5,DCBPUT                SAVE O FLAGS
         ST    R0,DCBPUT                ADDRESS OF PUT MODULE
         STC   R5,DCBPUT                RESTORE O FLAGS
*
*   SAVE PROCESSING MODULE ADDRESS FOR CLOSE - IGG0202I.
*
         L     R5,DEBEXPTR              ADDRESSABILITY ISAM EXTENSION
         USING DEBEXT,R5                *
         ST    R0,DEBPUT                SAVE PUT MODULE ADDRESS
         DROP  R5
*
*   LOAD CHANNEL END AND ABNORMAL END APPENDAGE MODULE - IGG019GD.
*
         LH    R5,APP                   LAST TWO BYTES OF NAME
         BAL   R10,LOAD                 LOAD APPENDAGE MODULE
         ST    R0,DCBWKPT5              ADDRESS OF APPENDAGE
         MVI   DCBWKPT5,K0              RESET SW                 Y02072
*
*   LOAD CHANNEL PROGRAM MODULE.
*
         LH    R5,CP(R7)                LAST TWO BYTES OF NAME.
LOAD     EQU   *                        LOAD ANY MODULE
         STH   R5,ID(RWTG)              FINISH MODULE NAME
*
         LOAD  EPLOC=(RWTG),DCB=(R9)    LOAD CPS
*
*   SUBROUTINE RETURNS TO LAST BAL OR BALR ISSUED ON R10, THIS
*   INCLUDES THE BALR INSTRUCTION THAT FOLLOWS.
*
         BALR  R10,R10                  RETURN TO LOAD NEXT
*
         L     R10,DCBWKPT6             ADDRESSABILITY ISLVPTRS
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=15  SET USERS KEY       Y02072
         SPACE 2
         ST    R0,CL1AD                 PTR TO CP MODULE
         LR    R15,R3                   RESTORE BASE
         LR    R1,R4                    RESTORE R1
         USING BASETAG,R15              REVERSE ADDRESSABILITY
         DROP  R3                       *
         USING IHADCB,R1                ADDRESSABILITY ON DCB
         DROP  R4                       *
         L     R6,CL1AD                 C(R6)=ADDR CP18 SKLTN
         LA    R6,CP18LEN(R6)           ADD SIZE OF CP18         S20201
*                                        VLR                     S20201
         ST    R6,CM1AD                 STORE ADDR CP19 SKLTN     VLR
         LA    R6,CP19LEN(R6)           ADD SIZE OF CP19         S20201
*                                        VLR                     S20201
         ST    R6,CQ1AD                 STORE ADDR CP20 SKLTN     VLR
         LA    R6,CP20LEN(R6)           ADD SIZE OF CP20         S20201
*                                        VLR                     S20201
         ST    R6,CQT1AD                STORE ADDR CP20 WR CHK    VLR
         LA    R6,CP20WRCK(R6)          ADD SIZE OF CP20 WR CHK  S20201
*                                        VLR                     S20201
         ST    R6,CQ40AD                ST ADDR CP21 SKLTN        VLR
         LA    R6,CP21LEN(R6)           ADD SIZE OF CP21         S20201
*                                        VLR                     S20201
         ST    R6,CQ45AD                STORE ADDR CP21 WR CHK
*
* SET UP APPENDAGE VECTOR TABLE IN 5 WORDS ON TOP DEB
*
         L     R8,DCBDEBAD              C(R8)=A(DEB)
         USING IHADEB,R8                R8 = DEB ADDRESS         Y01021
         L     R7,DEBAPPAD              C(R7)=A(VECTOR)
         USING DEBAVT,R7                ADDRESSABILITY ON AVT
         L     R3,DCBWKPT5              APPENDAGE ADDRESS
         SPACE 2
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
         SPACE 2
         ST    R3,DEBEOEA               END OF EXTENT PTR
         LA    R3,0(R3)                 *
         TM    DEBSIOA,VIRTUAL          VIRTUAL = REAL
         BO    TSTHSIO                  NO - DON'T OVERLAY SIO PTR
         ST    R3,DEBSIOA               YES - OVERLAY SIO
TSTHSIO  EQU   *                        *
         LR    R4,R3                    PCI ADDRESS
         LA    R5,CEDISP(R3)            CE ADDRESS
         LA    R6,AEDISP(R3)            AE ADDRESS
         STM   R4,R6,DEBPCIA            STORE APPENDAGE ADDRESS
         DROP  R7
*
* STORE PUT AND APPENDAGE MODULE IDS BEYOND LAST EXTENT IN DEB
*
         SR    R4,R4
         IC    R4,DEBNMEXT              NUMBER OF EXTENTS        S20201
*                                       *  ISAM DEPENDANT SECTION
         SLL   R4,4                     C(R4)=NO. OF EXTENTS X 16
         LA    R5,LOADEXT               GET DEB EXTENSION LENGTH S21045
         STC   R5,DEBAMLNG              DEB EXTENSION=ACS METH   S21045
*                                       SECTION                  S21045
         AR    R4,R5                    ADD EXTENSION LENGTH     S21045
         LA    R5,DEBNIEE(R4)           PT TO ID LIST
*
*   MOVE SAVED ID INTO DEB.
*
         MODESET  KEYADDR=DXUKEY,WORKREG=3   SET USERS KEY       Y02072
         SPACE 1
         LH    R3,ISLVPTR5              LOAD ID                  Y02072
         SPACE 1
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
         SPACE 1
         STH   R3,0(,R5)                STORE ID IN DEB          Y02072
*
*   MOVE APPENDAGE ID INTO DEB.
*
         MVC   L2(L'APP,R5),APP         MOVE IN APP ID
*
* CALCULATE AMT OF STOREGE NEEDED FOR CHANNEL PROGRAMS (WR CHK)
*
         LA    R3,K24                   BASIC CP LENGTH FOR EACH S20201
*                                       *  BUFFER
         TM    DCBRECFM,X'10'           TEST FOR BLOCKED (BIT-3) * CP18
         BC    1,TSTHK081               B IF BLOCKED             ******
         NC    DCBRKP,DCBRKP            DOES RKP = 0             S20201
         BZ    TSTHK082                 YES - USE BASIC LENGTH   S20201
*                                       NO - EXTEND BASIC LENGTH
TSTHK081 LA    R3,K16(R3)               INCREASE LENGTH          S20201
TSTHK082 SR    R4,R4
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=5   SET USERS KEY       Y02072
         SPACE 2
         SR    R5,R5
         IC    R5,ISLBUFNO              C(R4+R5) = BUFNO
         MR    R4,R3                    C(R5) = ZXBUFNO
         LA    R5,K64(R5)               *C(R5)=48+(ZXBUFNO)      S20201
*                                       13334                     VLR
         LR    R3,R5                    C(R3) = SIZE OF CP18
*                                                                * CP19
         SR    R4,R4                                             ******
         CLI   DCBHIRSH,X'00'           TEST HIRSH VS 0          O19110
         BNE   TSTHK083                 B NOT 0 = SHARED
         TM    DCBOPTCD,X'08'           TEST OPTCD BIT 4, CYL OVFL
         BC    8,TSTHK084               B NOT ON = NO CP19 NEEDED
*
*   ADD LENGTH OF CP 18 AND CP19
*
TSTHK083 LA    R5,CP19LEN(R5)                                    S20201
         LA    R4,CP19LEN(R4)           C(R4)=SIZE CP19          S20201
*                                       O1911                    S20201
*
*   ADD LENGTH OF CP20 W/WRITE CHECK AND CP21 W/WRITE CHECK
*
TSTHK084 EQU   *                                                 O19110
         LA    R5,CP20LEN+CP20WRCK+CP21LEN+CP21WRCK(R5)          S20201
*
* GET MAIN FOR CHANNEL PROGRAMS
*
         ST    R5,32(R10)               STORE SIZE OF CPS FOR DELETE
         LA    R0,250                   SUBPOOL NO. 250
         SLL   R0,24                    C(R0) = P000, P = SUBPOOL NO.
         OR    R0,R5                    C(R0) = PNNN, NNN = POOL SIZE
         LR    R6,R1                    SAVE R1
         LR    R7,R15                   SAVE R15, BASE
*
         GETMAIN R,LV=(0)
*
         LR    R15,R7                   RESTORE R15
         LR    R7,R1                    C(R7)=A(SUBPOOL)
         LR    R1,R6                    RESTORE R1
         LR    R6,R7                    SAVE DESTINATION         S20201
         LA    R7,K8(R7)                ALLOW FOR RPS            S20201
*                                       INSTRUCTION              S20201
*
*
* SET UP ISLVPTRS FOR CHANNEL PROGRAM ADDRESSES
*
         ST    R7,12(R10)               A(CP18)
         AR    R7,R3                    ADD SIZE OF CP18
         ST    R7,16(R10)               A(CP19)
         AR    R7,R4                    ADD SIZE OF CP19 (MAY BE 0)
         TM    36(R10),X'C0'            FTIW-SUCCESSFUL GETMAIN  O19110
         BO    SKPCP20                  YES-DON'T STORE CP20     O19110
*                                       ADDR                     O19110
         ST    R7,20(R10)               A(CP20)
SKPCP20  EQU   *                                                 O19110
*   ADD SIZE OF CP20 W/WRITE CHECK
         LA    R7,CP20LEN+CP20WRCK(R7)                           S20201
         ST    R7,24(R10)               A(CP21)
*
         SPACE 4
*
* INITIALIZE CP18
*
         LR    R10,R6                   NEXT DESTINATION         S20201
         USING CPSTO,R10                                         S20201
         L     R9,CL1AD                 C(R9)=A(CP18 SKLTN, CL1)
         USING LOADCPS,R9                                        S20201
         SR    R3,R3
         IC    R3,ISLBUFNO              C(R3)=NO OF BUFFRS
         MVC   CPSTO(L32),0(R9)         MOVE CLO,CL1,CL2,CL3     S20201
* * CL0 INITIALIZATION
         LA    R4,ISLRPSSS              SET SECTOR               S20201
         O     R4,CL0                                            S20201
         ST    R4,CPS1                                           S20201
* * CL1 INITIALIZATION
         LA    R4,K35(R2)               SEARCH ID EQUAL          S20201
         O     R4,CL1                   *  I0B+35                S20201
         ST    R4,CPS2                  *                        S20201
* * CL2 INITIALIZATION
         LA    R4,CPS2                  TIC TO CL1               S20201
         O     R4,CL2                   *                        S20201
         ST    R4,CPS3                  *                        S20201
* * CL3 INITIALIZATION
         LA    R4,CPS5                  TIC TO CL4               S20201
         O     R4,CPS4                  TIC TO CL4               S20201
         ST    R4,CPS4                  *                        S20201
         ST    R4,CPS4+K4               SAVE ADDRESS             S20201
         LA    R10,CPS5                 NEXT DESTINATION         S20201
*
*   ASSIGN BUFFERS TO CHANNEL PROGRAMS
*
         SR    R8,R8                    CLEAR REGISTER 8         O19110
         SPACE 1
         L     R7,0(R11)                1ST AVAILABLE BUFFER
         MVI   ISLOFFST+3,32            THREE CCWS PER BUFFER    M0170
         LA    R7,0(R7)                 CLEAR HIGH BYTE
         TM    DCBRECFM,X'10'           TEST RECFM BIT 3 FOR BLOCKED
         BC    1,TSTHK15                B IF 1 = BLOCKED
*
*                                       UNBLOCKED
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
         SPACE 1
         NC    DCBRKP,DCBRKP            TEST RKP VS 0            S20201
         SPACE 1
         MODESET  KEYADDR=DXUKEY,WORKREG=4   SET USERS KEY       Y02072
         SPACE 1
         BNZ   TSTHK20                  BR IF RKP NOT 0          S20201
*                                       O1911                    S20201
*                                       UNBLOCKED, RKP = 0
         MVI   ISLOFFST+3,16            IF RECFM=F, RKP=0 ONE    M0170
         OI    0(R11),X'01'             SET FLAGS BIT 7 = 1 IN BCT
         IC    R8,DCBKEYLE              C(R8)=KEYLE, 000N = IL
         AH    R8,DCBLRECL              C(R8)=AL+IL (AL=LRECL)   O19110
         LA    R8,K8(R8)                C(R8)=AL+IL+8, 00NN      S20201
*
*
*   FIXED LENGTH RECORDS, RKP = 0 - SET UP 1 COPY OF CL4 FOR
*   EACH BUFFER.
*
TSTHK09  MVC   CPS1,CL4                 MOVE CL4                 S20201
         STH   R8,CPS1+K6               LEN FOR WRITE            S20201
         L     R4,0(R7)                 C(R4)=C(SLOT S)=A(BUF B)
         LA    R4,0(R4)
         O     R4,CL4                   *COMBINE OP CODE         S20201
*                                        1333                    S20201
         ST    R4,CPS1                  STORE COUNT ADR IN CL4   S20201
         LR    R5,R10                   *R5 POINTS TO CURRENT CCW 13334
         DROP  R10                                               S20201
         USING CPSTO,R5                                          S20201
         LA    R10,CPS3                 NEXT DESTINATION         S20201
         ST    R10,CPS2                 BUILD A NEW TIC WITH     S20201
         ST    R10,CPS2+K4              *  DUPLICATE ADDRESS FOR S20201
         OI    CPS2,TIC                 *  RESTORATION           S20201
         LA    R7,4(R7)                 C(R7)=A(NXT SLOT)         VLR
         BCT   R3,TSTHK09               LOOP
*                                       BLOCKED                  S20201
         DROP  R5                                                S20201
         USING CPSTO,R10                                         S20201
         B     TSTHK30                  B TO MOVE CL5
*
*                                       UNBLOCKED, RKP NOT 0
*
*                                       BLOCKED
TSTHK15  EQU   *                                                 O19110
         LH    R8,DCBBLKSI              C(R8)=BLKSI              O19110
         SH    R8,DCBLRECL              C(R8)=BLKSI-LRECL        O19110
*
*                                       COMMON RT FOR CL6, CL7, +CL8
TSTHK20  EQU   *                                                 O19110
         AH    R8,DCBRKP                C(R8)=REL KEY ADDR       O19110
*
TSTHK26  MVC   CPS1(L24),CL6            MOVE IN CL6,CL7,CL8      S20201
         MVC   CPS2+K7(L1),DCBKEYLE     LEN FOR WR KEY           S20201
         MVC   CPS3+K6(L2),DCBBLKSI     LEN FOR WR DATA          S20201
         L     R4,0(R7)                 ADDRESS OF BUFFER        S20201
* * CL6 INITIALIZATION
         IC    R5,CL6                   WRITE COUNT              S20201
         ST    R4,CPS1                  *                        S20201
         STC   R5,CPS1                  *                        S20201
* * CL7 INITIALIZATION
         LA    R4,K8(R4)                PT TO DATA               S20201
         ST    R4,CPS3                                           S20201
* * CL8 INITIALIZATION
         AR    R4,R8                    PT TO HIGH KEY IN BLOCK  S20201
         ST    R4,CPS2                  WRITE KEY                S20201
         LR    R5,R10                   CURRENT DESTINATION      S20201
         LA    R10,CPS5                 NEXT DESTINATION         S20201
         DROP  R10                                               S20201
         USING CPSTO,R5                                          S20201
         ST    R10,CPS4                 TIC CCW ADDRESS          S20201
         ST    R10,CPS4+K4              DUPLICATE FOR            S20201
*                                       RESTORATION              S20201
         OI    CPS4,TIC                 RESTORE OP CODE          S20201
         LA    R7,4(R7)                 C(R7)=A(NXT DEST)         VLR
*                                       13334                     VLR
         BCT   R3,TSTHK26               *LOOP                     13334
         LA    R5,K16(R5)               FAKE UNBLOCKED RKP=0
*                                                                 13334
*                                                                 13334
TSTHK30  EQU   *                        *C(R9)=A(CP18,CL1)       S20201
         IC    R3,DCBKEYLE              *C(R3)=KEY LENGTH         13334
         LA    R3,K8(R3)                *C(R3)=KEY LENGTH+8      S20201
         AH    R3,DCBBLKSI              *C(R3)=KEY LENGTH+8+DATALN13334
         L     R4,DCBWKPT6              VPTRS                    S20201
         L     R4,K12(R4)               CP18 ADDRESS             S20201
         MVC   CPS2,K16(R4)             MOVE IN WRAP AROUND TIC
         LR    R5,R10                   NEXT DESTINATION         S20201
         MVC   CPS1(L32),0(R6)          MOVE IN                  S20201
*                                       CLO*,CL1*,CL2*,CL3*      S20201
* * CL0* SAME AS CL0 EXCEPT NOP
         MVI   CPS1,NOP                 MAKE CL0* NOP            S20201
*
* * CL1* INITIALIZATION
         LA    R4,CPS2                  TIC TO CL1*              S20201
         ST    R4,CPS3                                           S20201
         OI    CPS3,TIC                 RESTORE OP CODE          S20201
         ST    R5,K20(R6)               SAVE ADDRESS OF SCH CCW  S20201
         LA    R5,K32(R5)               NEXT DESTINATION         S20201
         SR    R4,R4                                             S20201
         IC    R4,ISLBUFNO              NUMBER OF WRITES TO      S20201
*                                       CHECK                    S20201
* * CL9 INITIALIZATION
AGN      MVC   CPS1,CL9                 READ CKD - SUPPRESS DATA S20201
         STH   R3,CPS1+K6               *  TRANSFER              S20201
         TM    DCBRECFM,VLR             IS IT VLR                S20201
VLR      EQU   X'40'                                             S20201
         BNO   VLR8                     BIF NOT VLR.             S20201
         OI    CPS1+K4,SILI             SET SILI FOR VLR.        S20201
VLR8     EQU   *                        *                        S20201
         LA    R5,8(R5)                 *NEXT DESTINATION         13334
         BCT   R4,AGN                   *LOOP                     13334
         ST    R5,K28(R6)               *SET UP 2ND TIC ADDR.    S20201
*                                       FLD.                     S20201
         SH    R5,TST8                  *GET LAST READ CKD CCW   O19110
         NI    CPS1+K4,X'FF'-CC         TURN CC FLAG OFF         S20201
         EJECT
*
* EXIT ********** EXIT ***************** EXIT *********** EXIT ********
*
         LM    R0,R15,DXCCW1            RESTORE REGISTERS        Y02072
         DROP  R13                                               Y02072
         SPACE 1
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
         SPACE 1
         USING BASETAG,RBASE
         MVC   0(L'LOAD2S,RWTGC),LOAD2S ID OF MODULE IGG0192S    Y02072
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH=BEGINNING OF THIS MODULE
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BC    7,RELOOP                 BRANCH=NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BC    7,TCTLRTN                BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         USING FORCORE,RCORE                                     Y01021
         LA    RJ,DXCCW12                                        Y01021
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
*
         DS    0D
*   LAST TWO BYTES OF MODULE NAMES - USED TO CONSTRUCT NAMES.
         SPACE 5
*   CHANNEL END AND ABNORMAL END APPENDAGE MODULE.
APP      DC    C'GD'                    IGG019GD - LOAD APPENDAGE
*   CHANNEL PROGRAM MODULES.
CP       DC    C'GF'                    IGG019GF - FIXED NON FTIW
CPVLR    DC    C'IF'                    IGG019IF - VLR
CPFTIW   DC    C'GF'                    IGG019GF - FIXED FTIW
*   PROCESSING MODULES( MACRO TIME ).
PROC     DC    C'GB'                    IGG019GB - FIXED NON FTIW
         DC    C'IB'                    IGG019IB - VLR
         DC    C'I2'                    IGG019I2 - FIXED FTIW
*
TST8     DC    H'0008'                                           O19110
*                                       13334                     VLR
THISLOAD DC    C'2U'
OPNLOD7  DC    C'0S'
LOAD2S   DC    C'2S'                    IS OF MODULE IGG0192S    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         END
