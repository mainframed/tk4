         TITLE 'IGG0192J - OPEN,BISAM,LOAD APPENDAGE,LOAD ASYNC'
IGG0192J CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192J                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, BISAM MODE                            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 02                                *
*                                                                     *
* FUNCTION = USING CODE DEVELOPED IN IGG0192I, LOAD APPENDAGE MODULES,*
*            ASYNCHRONOUS MODULE, AND RPS SIO MODULE, IF REQUIRED.    *
*            INITIALIZE DEB WITH ADDRESSES AND ID'S OF LOADED MODULES.*
*            INITIALIZE IRB.  UPDATE USER'S DCB FROM COPY DCB.        *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 714 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192J                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = WHEN USER PROVIDES WORK AREA, RECEIVES CONTROL FROM    *
*              ISAM OPEN EXECUTOR IGG0192Z FOR VLR, IGG0192O FOR FLR. *
*              WHEN ISAM PROVIDES WORK AREA, FROM IGG0192N.  RECEIVES *
*              CONTROL IN STORAGE PROTECT KEY 5 AND PRIVILEGED STATE. *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO COMMON OPEN IGG0190S.                         *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGBISAM - BISAM WORK AREA                          *
*                                                                     *
*    CONTROL-BLOCKS = DCB, DCB COPY, IRB, AND DEB.                    *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = LOAD, CIRB, MODESET, AND XCTL                              *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*          RELEASE OS/VS2-02 DELETIONS                                *
*                                                               YM01159
*                                                               YM01437
*                                                                     *
***********************************************************************
         EJECT
RE       EQU   00                       WORK REG
RF       EQU   01                       WORK REG
RDCB     EQU   02                       USER'S DCB
RBASE    EQU   03                       BASE REG
RG       EQU   04                       WORK REG
RPAR     EQU   05                  *    PARAMETER LIST ADDRESS
RWTG     EQU   06                  *    WHERE TO GO TABLE BIGINNING
RPARC    EQU   07                  *    CURRENT ENTRY,PARAMETER LIST
RWTGC    EQU   08                  *    CURRENT ENTRY, WTG TABLE
RTIOT    EQU   09                       CVT DCB ADDRESS
RUCB     EQU   10                       UCB ADDRESS
RA       EQU   11                       WORK REG
RB       EQU   12                       WORK REG
RC       EQU   13                       WORK REG
RD       EQU   14                       WORK REG
R15      EQU   15                       WORK REG
RCORE    EQU   RG                       DCB WA ADDRESS
IDOFF    EQU   6                        OFFSET INTO WTG TAB
*                   *     MEANS SET BEFORE ENTRY AND BEFORE EXIT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         L     RDCB,0(RPARC)
         USING IHADCB,RDCB
         TM    DCBRECFM,X'80'            TEST IF VLR              VLR
         BO    OPNFIX2L                  B=FIXED                  VLR
         SR    RA,RA                     CLEAR REG A              VLR
         IC    RA,DCBWKPT6+2             INSERT 1 BYTE CODE FROM  VLR
*                                       2H                        VLR
         B     OPN2LXC                                            VLR
OPNFIX2L L     RA,DCBWKPT6               RA=CODE DEVELOPED IN 2H  VLR
OPN2LXC  XC    DCBWKPT6,DCBWKPT6         ZERO DCBWKPT6            VLR
         L     RB,DCBDEBAD             RB   DEB ADDRESS
         USING IHADEB,RB
         SR    RC,RC                   RC   ADDRESS OF DEB ID'S
         IC    RC,DEBNMEXT                   RELATIVE TO BEGINNING OF
         SLL   RC,4                          DEB ASSUMING ZERO EXTENTS
         LA    RE,BISAMEXT              GET ACS METH SECT LENGTH S21045
         STC   RE,DEBAMLNG              INTIIALIZE DEB WITH IT   S21045
         AR    RC,RE                    ADD TO SUBRTN ID SECT    S21045
*                                       OFFSET                   S21045
         AR    RC,RB
         L     RTIOT,CVTPTR             RTIOT  ADDRESS OF CVT   YM01159
         USING CVT,RTIOT                CVT ADDRESSABILITY      YM01159
         L     RTIOT,CVTLINK            ADDRESS OF LINKLIB DCB  YM01159
         DROP  RTIOT                    END CVT ADDRESSABILITY  YM01159
*
         TM    DEBRPSID,HEXE0           TEST FOR RPS DEVICE      S20201
         BZ    OPNSLRA                  BRANCH = NO              S20201
         MVC   IDOFF(2,RWTG),OPNAPPIO   SIO APP ADDRESS
         LOAD  EPLOC=(RWTG),DCB=(RTIOT) LOAD SIO APPENDAGE
         L     RD,DEBEXPTR              GET ADDR OF DEB          S21045
*                                       EXTENSION PTR            S21045
         L     RF,DEBAPPAD              SIO APPENDAGE TABLE ADDR S20201
         USING DEBAVT,RF                APPENDAGE VECTOR TABLE  YM01437
         TM    DEBSIOA,VIRTUAL          VIRTUAL ENVIRONMENT     YM01437
         BO    OPNSIO                   BR IF VIRTUAL ENVIRON.  YM01437
         DROP  RF                       END USING ON DEB AVT    YM01437
*
         USING DEBEXT,RD                                         S21045
         TM    DEBRPSID,HEX10           TEST IF PREVIOUSLY       S20201
*                                       LOADED                   S20201
         BO    OPNSIO                   BRANCH = YES             S20201
         ST    RE,FOUR(RF)                                       S20201
         B     OPNSTID                  BRANCH TO STORE DEBID    S20201
OPNSIO   ST    RE,DEBRPSIO              STORE SIO APPENDAGE
         DROP  RD                       *
OPNSTID  SR    RE,RE                                             S20201
         IC    RE,DEBNMSUB              TAKE NO. OF SUBROUTINES  S20201
         BCTR  RE,ZERO                  DECREMENT BY 1           S20201
         SLL   RE,1                     MULTIPLY RESULTS BY 2    S20201
         DROP  RB                       DROP BASE REG            S20201
         AR    RC,RE                    ADDRESSIBILITY           S20201
         USING IHADEB,RC                NEW BASE REG             S20201
         MVC   DEBID1,OPNAPPIO          SIO APPENDAGE ID TO
*                                       DEBID                    S20201
         DROP  RC                       DROP REGISTER            S20201
         SR    RC,RE                    RESET RC                 S20201
         USING IHADEB,RB                                         S20201
OPNSLRA  SLL   RA,ONE                   USE RA TO FIND NEEDED    S20201
         TM    DCBRECFM,X'80'            TEST IF VLR ?            VLR
         BO    OPNFIXAP                  B=FIXED                  VLR
         LA    RA,OPNVAPP(RA)            SELECT VARIABLE          VLR
*                                       APPANDAGES                VLR
         B     OPNWKNT                                            VLR
OPNFIXAP LA    RA,OPNAPP(RA)              APPANDAGE MODULE        VLR
OPNWKNT  TM    DCBMAC,X'02'              IF WKN,2 APPENDAGE       VLR
         BZ    OPNO5K3B                 MODULES ARE USED
         SR    RD,RD                    CLEAR
         IC    RD,WKNAPP(RA)            DISPLACEMENT TO NAME OF MODULE
         LH    RD,MODULES(RD)           ID PART TWO APP
         STH   RD,IDOFF(RWTG)           CONSTRUCT NAME TO LOAD
         LOAD  EPLOC=(RWTG),DCB=(RTIOT)
         DROP  RB
         USING IHADEB,RC
         STH   RD,DEBID2B               PUT ID IN DEB
         DROP  RC
         USING IHADEB,RB
         L     RD,DEBEXPTR               GET ADDR OF DEB         S21045
*                                       EXTENSION PTR            S21045
         USING DEBEXT,RD                                         S21045
         ST    RE,DEBWKPT5               SET UP ADDRESS OF APP   S21045
*                                       MODULE                   S21045
         DROP  RD                                                S21045
         DROP  RB                      SAVE ITS ID IN THE DEB
         USING IHADEB,RC
OPNO5K3B EQU   *                        LOAD PART I APPENDAGE
         SR    RD,RD                    CLEAR
         IC    RD,0(RA)                 DISPLACEMENT TO NAME OF MODULE
         LH    RD,MODULES(RD)           ID OF PART ONE APP
         STH   RD,IDOFF(RWTG)           CONSTRUCT MODULE NAME
         LOAD  EPLOC=(RWTG),DCB=(RTIOT) LOAD 1ST (OR ONLY) MODULE
         STH   RD,DEBID2                PUT ID IN DEB
         DROP  RC
         USING IHADEB,RB
         LR    RF,RE                    RF  POINTS TO THE MODULE
         L     RD,DEBEXPTR               GET ADDR OF DEB         S21045
*                                       EXTENSION PTR            S21045
         USING DEBEXT,RD                                         S21045
         ST    RF,DEBWKPT4               SET UP ADDRESS OF APP   S21045
*                                       MODULE                   S21045
         DROP  RD                                                S21045
         LA    RD,4                     SET UP APPENDAGE LIST
         L     RA,DEBAPPAD
OPNO5K3A L     RE,0(RD,RF)              REL ADDRESSES IN BEGINNING
         AR    RE,RF                     OF MODULE.  STORE
         ST    RE,12(RD,RA)              ACTUAL ADDRESSES INN LIST
         SH    RD,OPNO5K3C
         BNL   OPNO5K3A
         TM    DCBRECFM,X'80'            TEST IF VLR ?            VLR
         BO    OPNO5K4                   B=FIXED                  VLR
         LA    RA,OPNVASY                SELECT VARIABLE          VLR
*                                       ASYNCHRONOUS              VLR
         B     OPNWKNT3                                           VLR
OPNO5K4  LA    RA,OPNASY+2              FIND NAME OF ASYN.MODULE
OPNWKNT3 TM    DCBMAC,X'02'              MODULES DEPEND ON READ   VLR
*                                       AND                       VLR
         BZ    OPNO5J5B                 UPDATE,WKN,OR BOTH,
         LA    RA,2(RA)                 AND,IF WKN, WC OR NOT.
         TM    DCBMACRF,X'20'
         BO    OPNO5J5A
         TM    DCBMAC,X'04'
         BZ    OPNO5JJ6
OPNO5J5A LA    RA,2(RA)
OPNO5JJ6 EQU   *
         TM    DCBRECFM,X'80'            TEST FOR VLR             VLR
         BZ    OPNO5J5B                 B=VARIABLE                VLR
         TM    DCBOPTCD,X'80'
         BZ    OPNO5J5B
         LA    RA,4(RA)
*   SET UP MODULE NAME AND LOAD ASYNCHRONOUS MODULE.
OPNO5J5B EQU   *                        *
         MVC   IDOFF(2,RWTG),0(RA)      MOVE IN ID FOR LOAD
         LOAD  EPLOC=(RWTG),DCB=(RTIOT) LOAD THE MODULE
         DROP  RB
         USING IHADEB,RC
         MVC   DEBID2A,0(RA)            SAVE THE ID IN THE DEB
         DROP  RC
         USING IHADEB,RB
         CIRB  EP=(0),KEY=PP,MODE=SUPR,STAB=RE    GENERATE IRB
OPNO5K5A STH   RF,DEBIRBAD+2            SAVE ITS ADDRESS IN THE DEB
         SRL   RF,16
         STC   RF,DEBIRBAD+1
         SR    RF,RF                    PUT THE CORRECT BB BYTES INTO
         IC    RF,DCBLPDA                LPDA
         SLL   RF,4
         LA    RF,32(RF,RB)
         MVC   DCBLPDA+1(2),4(RF)
         MVC   0(5,RWTGC),OPNNXTEX    MOVE ZERO TO WTG,OPEN 7 NEXT
         L     RCORE,4(RWTGC)
         USING FORCORE,RCORE            OPEN WA ADDRESSABILITY   Y02072
*
*    SET USERS DCB TO SAME AS THE DCB COPY.
*
         L     RA,DXUDCBAD              ADDR OF USERS DCB        Y02072
         LA    RA,DCBKEYLE-IHADCB(RA)   BYPASS DCB PREFIX        Y02072
         LA    RF,DCBKEYLE              BYPASS COPY DCB PREFIX   Y02072
         LH    RB,DXUDCBML              NO. OF BYTES TO MOVE     Y02072
         BCTR  RB,0                     COUNT LESS ONE FOR MOVE  Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=15  CHANGE TO USER KEY  Y02072
*
         EX    RB,COPYDCB               UPDATE USERS DCB         Y02072
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT NAME ON THE WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)        AND DCB ON PARAM TABLE
         CLC   0(2,RWTGC),OPNEXNAM      IF NAME ON TABLE SAME ID AS
         BCR   8,RBASE                   CURRENT LOAD, BRANCH TO REPEAT
         CLC   0(2,RWTGC),OPIDCNST    IF NOT END OF WTG TABLE,BRANCH
         BNE   RELOOP                    TO TRY NEXT NAME
         LR    RPARC,RPAR               OTHERWISE, RETURN TO START OF
         LA    RWTGC,WAOFF(0,RWTG)       BOTH TABLES AND SEARCH FOR
ZCHECK   CLI   0(RWTGC),ZCNST            FIRST NON-ZERO ID
         BNE   TCTLRTN
         LA    RWTGC,WGOFF(0,RWTGC)
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       WHEN FOUND, SET UP PARAMETERS
*
         LA    R15,DXCCW12              WA ADDRESS               Y01021
TRCTL    XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
*
COPYDCB  MVC   0(0,RA),0(RF)            UPDATE USERS DCB         Y02072
*                                       FROM COPY DCB            Y02072
WGOFF    EQU   8
PLOFF    EQU   4                        SIZE OF ONE ENTRY ON PARAM TAB
OPNEXNAM DC    C'2J'                    ID OF THIS OPEN EXECUTOR
         DS    0F                       *
PAGEMASK DC    X'000007FF'              PAGE MASK
OPIDCNST DC    C'0S'                    ID OF OPEN LOAD #7
ZCNST    EQU   X'00'                    ZERO - SIGNALS INACTIVE ENTRY
HEXE0    EQU   X'E0'                     RPS TEST                S20201
HEX10    EQU   X'10'                    LOADED TEST              S20201
WAOFF    EQU   32                       SIZE OF WA AT START OF WTG TAB
ZERO     EQU   0                        *                        S20201
FOUR     EQU   4                        *                        S20201
SIX      EQU   6                        *                        S20201
ONE      EQU   1                        1
THREE    EQU   3                        3
EIGHT    EQU   8                        8
ELEVEN   EQU   11                       11
VIRTUAL  EQU   X'80'                    VIRTUAL MASK
DYMBUF   EQU   X'04'                    DYNAMIC BUFFERING MASK
WKNAPP   EQU   1                        DISPACEMENT TO PART 2 APP
PAGE     EQU   2048                     MODULO 2048
*   MODULE NAME LIST
MODULES  EQU   *                        BASE FOR MODULE LIST
IGG019G0 DC    C'G0'                    APPPENDAGE
IGG019G1 DC    C'G1'                    APPPENDAGE
IGG019G2 DC    C'G2'                    APPPENDAGE
IGG019G3 DC    C'G3'                    APPPENDAGE
IGG019G4 DC    C'G4'                    APPPENDAGE
IGG019G5 DC    C'G5'                    APPPENDAGE
IGG019G6 DC    C'G6'                    APPPENDAGE
IGG019G7 DC    C'G7'                    APPPENDAGE
IGG019G8 DC    C'G8'                    APPPENDAGE
IGG019G9 DC    C'G9'                    APPPENDAGE
IGG019GL DC    C'GN'                    APPPENDAGE GN FOR GL     Y02072
IGG019GM DC    C'GO'                    APPPENDAGE GO FOR GM     Y02072
IGG019GN DC    C'GN'                    APPPENDAGE
IGG019GO DC    C'GO'                    APPPENDAGE
IGG019IM DC    C'IN'                    APPPENDAGE IN FOR IM     Y02072
IGG019IN DC    C'IN'                    APPPENDAGE
IGG019IO DC    C'IO'                    APPPENDAGE
IGG019I9 DC    C'I9'                    APPPENDAGE
*
*   MODULE DISPLACEMENTS
*
G0       EQU   IGG019G0-MODULES         DISPLACEMENT TO MODULE ID
G1       EQU   IGG019G1-MODULES         DISPLACEMENT TO MODULE ID
G2       EQU   IGG019G2-MODULES         DISPLACEMENT TO MODULE ID
G3       EQU   IGG019G3-MODULES         DISPLACEMENT TO MODULE ID
G4       EQU   IGG019G4-MODULES         DISPLACEMENT TO MODULE ID
G5       EQU   IGG019G5-MODULES         DISPLACEMENT TO MODULE ID
G6       EQU   IGG019G6-MODULES         DISPLACEMENT TO MODULE ID
G7       EQU   IGG019G7-MODULES         DISPLACEMENT TO MODULE ID
G8       EQU   IGG019G8-MODULES         DISPLACEMENT TO MODULE ID
G9       EQU   IGG019G9-MODULES         DISPLACEMENT TO MODULE ID
GL       EQU   IGG019GL-MODULES         DISPLACEMENT TO MODULE ID
GM       EQU   IGG019GM-MODULES         DISPLACEMENT TO MODULE ID
GN       EQU   IGG019GN-MODULES         DISPLACEMENT TO MODULE ID
GO       EQU   IGG019GO-MODULES         DISPLACEMENT TO MODULE ID
IM       EQU   IGG019IM-MODULES         DISPLACEMENT TO MODULE ID
IN       EQU   IGG019IN-MODULES         DISPLACEMENT TO MODULE ID
IO       EQU   IGG019IO-MODULES         DISPLACEMENT TO MODULE ID
I9       EQU   IGG019I9-MODULES         DISPLACEMENT TO MODULE ID
OPNAPPIO DC    C'JH'                    RPS SIO APPENDAGE        S20201
         EJECT
*    TABLE OF APPENDAGE MODULES
*                                     CODE * R+U/WKN * FB/FU * WA * WC
*                                     ********************************
OPNAPP   DC    AL1(G0)                  0  *   WKN   *  FU   * N  * N
         DC    AL1(GN)                  GN REPLACES GL           Y02072
         DC    AL1(G1)                  1  *   WKN   *  FU   * N  * Y
         DC    AL1(GO)                  GO REPLACES GM           Y02072
         DC    AL1(G2)                  2  *   WKN   *  FU   * Y  * N
         DC    AL1(GN)                  GN REPLACES GL           Y02072
         DC    AL1(G3)                  3  *   WKN   *  FU   * Y  * Y
         DC    AL1(GO)                  GO REPLACES GM           Y02072
         DC    AL1(G4)                  4  *   WKN   *  FB   * N  * N
         DC    AL1(GN)                  GN REPLACES GL           Y02072
         DC    AL1(G5)                  5  *   WKN   *  FB   * N  * Y
         DC    AL1(GO)                  GO REPLACES GM           Y02072
         DC    AL1(G6)                  6  *   WKN   *  FB   * Y  * N
         DC    AL1(GN)                  GN REPLACES GL           Y02072
         DC    AL1(G7)                  7  *   WKN   *  FB   * Y  * Y
         DC    AL1(GO)                  GO REPLACES GM           Y02072
         DC    AL1(G0)                  8  *   BOTH  *  FU   * N  * N
         DC    AL1(GN)
         DC    AL1(G1)                  9  *   BOTH  *  FU   * N  * Y
         DC    AL1(GO)
         DC    AL1(G2)                  10  *   BOTH  *  FU   * Y  * N
         DC    AL1(GN)
         DC    AL1(G3)                  11  *   BOTH  *  FU   * Y  * Y
         DC    AL1(GO)
         DC    AL1(G4)                  12  *   BOTH  *  FB   * N  * N
         DC    AL1(GN)
         DC    AL1(G5)                  13  *   BOTH  *  FB   * N  * Y
         DC    AL1(GO)
         DC    AL1(G6)                  14  *   BOTH  *  FB   * Y  * N
         DC    AL1(GN)
         DC    AL1(G7)                  15  *   BOTH  *  FB   * Y  * Y
         DC    AL1(GO)
         DC    AL1(G8)                  16  *   R+U   *  ANY  * -  * N
         DC    AL1(0)                  FILLER
         DC    AL1(G9)                  17  *   R+U   *  ANY  * -  * Y
OPNVAPP  DC    AL1(IO)                  0  *  WKN    *  ANY * Y  * ANY
         DC    AL1(IN)                  IN REPLACES IM           Y02072
         DC    AL1(IO)                  1  *  BOTH   *  ANY * Y  * ANY
         DC    AL1(IN)
         DC    AL1(I9)                  2  *  R+U   *  ANY  * -  * ANY
OPNO5K3C DC    H'4'
         ORG   *-2            ASYNCHRONOUS MODULES
OPNASY   DS    CL2            ADJUST LABEL FOR ADDRESSING
         DC    C'GZ'     READ AND UPDATE ALONE GZ REPLACES GX    Y02072
         DC    C'GZ'     NO WC    WRITE KN ALONE GZ REPLACES GY  Y02072
         DC    C'GZ'               COMBINED
         DC    C'GW'     WC       WRITE KN ALONE GW REPLACES GV  Y02072
         DC    C'GW'              COMBINED
OPNVASY  DC    C'IZ'  READ AND UPDATE ALONE VLR IZ REPLACES IX   Y02072
         DC    C'IZ'  WC + NO WC  WKN ALONE VLR IZ REPLACES IY   Y02072
         DC    C'IZ'              WC + NO WC  COMBINED      VLR
OPNNXTEX DC    X'000000000000'
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
FORCORE  DSECT                          OPEN/CLOSE/EOV WORK AREA Y01021
         IECDSECT                                                Y01021
         EJECT
         DCBD  DSORG=(IS)
         EJECT
IHADCW  IGGBISAM OPTCD=V                                         S20201
         EJECT
IHADEB   IGGDEBD
*
         ORG   IHADEB+32
*        THE FOLLOWING ADDRESSES ARE MODIFIED BY NMEXT X 16
DEBID1   DS    CL2                      MODULE ID 1
DEBID2   DS    CL2                      MODULE ID 2
DEBID2A  DS    CL2                      MODULE ID 2A
DEBID2B  DS    CL2                     MODULE ID 2B
DEBID3   DS    CL2                      MODULE ID 3
DEBID4   DS    CL2                      MODULE ID 4
DEBID5   DS    CL2                      MODULE ID 5
         SPACE 4
         CVT   DSECT=YES                                        YM01159
         END
