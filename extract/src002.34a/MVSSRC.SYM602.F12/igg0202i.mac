         TITLE 'IGG0202I - QISAM LOAD MODE CLOSE- FLUSH BFRS,INDICIES'
IGG0202I CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0202I                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM CLOSE, LOAD MODE WITH FIXED LENGTH RECORDS  *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = FOR A NULL RESUME LOAD, XCTL TO NEXT ISAM CLOSE EXECUTOR,*
*            OTHERWISE, ESTABLISH COMMUNICATION WITH PUT AND          *
*            APPENDAGE ROUTINES, STORING APPENDAGE ROUTINE ADDRESS IN *
*            DCBRELSE OF THE COPY DCB.  AWAIT COMPLETION OF ALL PRIOR *
*            I/O.  IF PUT LOCATE, CHECK SEQUENCE OF LAST PUT.  IF IN  *
*            ERROR, SYNCH TO PROCESSING ROUTINE FOR SYNAD EXIT.  IF   *
*            CURRENT BUFFER IS NOT FULL, SYNCH TO PROCESSING ROUTINE  *
*            TO PAD OUT BUFFER AND SCHEDULE WRITE.  IF CURRENT BUFFER *
*            IS FULL, BUT NOT WRITTEN, SYNCH TO PROCESSING ROUTINE TO *
*            SCHEDULE WRITE.                                          *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = CLOSE EXECUTOR                                        *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 932 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0202I                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM IOS CLOSE WHEN FIXED LENGTH      *
*              RECORDS (FLR) SPECIFIED.  RECEIVES CONTROL IN STORAGE  *
*              PROTECT KEY 5 AND PRIVILEGED STATE.                    *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL IN STORAGE PROTECT KEY 5 TO ISAM CLOSE EXECUTOR  *
*               IGG0202J FOR A NULL RESUME LOAD, OTHERWISE TO IGG0202N*
*                                                                     *
* EXIT-ERROR = SYNCH TO PROCESSING ROUTINE TO TAKE SYNAD EXIT IF A    *
*              SEQUENCE ERROR OCCURS.                                 *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = ISAM LOAD MODE PROCESSING AND APPENDAGE ROUTINES      *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - CLOSE WORK AREA                           *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                                                                     *
*    CONTROL-BLOCKS = DCB, DCB COPY, IOB, CVT, TCB, AND SVRB          *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, SYNCH, WAIT, AND XCTL                             *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
********************
* DCB REFERENCE    *
********************
         DCBD  DSORG=(IS)
         EJECT
*****************************
* CLOSE WORK AREA REFERENCE *
******************************
FORCORE  DSECT                          OPEN/CLOSE/EOV WORK AREA Y01021
         IECDSECT                                                Y01021
         EJECT
********************
* DEB REFERENCE    *
********************
*
IHADEB   IGGDEBD
         EJECT
***********************************************************************
* ISLCOMON REFERENCE     C(DCBWKPT1)=A(ISLCOMMON)                     *
***********************************************************************
ISLCOMON IGGLOAD
         USING ISLCOMON,R12                                      S21045
         EJECT
*
* IOBBCT REFERENCE         C(ISLVPTRS+8)=A(IOBBCT)
*
IOBBCT   DSECT
         USING IOBBCT,R11
         DS    0D
IOBFLAGS DS    0CL1                     FLAGS
IOBPTRA  DS    A                        PTR A
IOBB     DS    0CL1                     B
IOBPTRB  DS    A                        PTR B
IOBS     DS    0CL1                     S - STATUS FIELD FOR BUF NO 1
IOBABUF  DS    A                        A(BUF NO 1) - ADR OF BUF NO 1
*
********************
* IOB REFERENCE    *
********************
*
IHAIOB   DSECT
         DS    0D
IOBFLG1  DS    CL1                      FLAGS 1
IOBFLG2  DS    CL1                      FLAGS 2
AIOBSENS DS    CL2                      SENSE BYTES              Y01021
IOBECBAD DS    A                        ECB POINTER
AIOBCSW  DS    CL8                      CHANNEL STATUS WORD      Y01021
AIOBSIOC DS    0CL1                     SIO CC                   Y01021
IOBCPSAD DS    A                        CHANNEL PROGRAM START ADR
IOBWT    DS    0CL1                     WEIGHT
IOBDCBAD DS    A                        DCB POINTER
IOBCPRAD DS    A                        CHANNEL PROGRAM RESTART ADR
IOBBCTI  DS    CL2                      BLK CTR INCR
IOBECT   DS    CL2                      ERROR CTR
IOBDADAD DS    CL8                      DIR ACESS DEV ADR MBBCCHHR
*
********************
* IXLT REFERENCE   *
********************
*
IXLT     DSECT
         USING IXLT,R7
         DS    0D
IXLTIND  DS    CL1                      INDICATOR                  LEV1
IXLBEG   DS    CL8                      BEGINING COUNT MBBCCHHR
IXLSTP   DS    CL8                      STEPPING COUNT MBBCCHHR
IXLEND   DS    CL8                      ENDING COUNT   MBBCCHHR
         DS    CL1
         DS    CL26                                                LEV2
         DS    CL26                                                LEV3
         DS    CL26                                                LEV4
         EJECT
CVT      DSECT                                                   Y02072
         CVT                                                     Y02072
         SPACE 2
TCB      IKJTCB                                                  Y02072
         EJECT
         IKJRB                                                   Y02072
         EJECT
IGG0202I CSECT
ISLG500  BALR  R15,0
         USING *,R15
BASETAG  L     R1,0(RPARC)
         L     RCORE,4(RWTGC)
         USING FORCORE,RCORE            WA ADDRESSABILITY        Y02072
         USING IHADCB,R1                COPY DCB ADDRESSABILITY  Y02072
         L     R12,DCBWKPT1
         LR    RBASE,R15
         STM   R0,RJ,DXCCW1             SAVE REGISTERS           Y02072
*
* EQUATE SYMBOLIC REGISTERS
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
RCOPY    EQU   10                       DCB COPY BASE REG        Y02072
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   9
RBASE    EQU   3
RCORE    EQU   4
RPAR     EQU   5
RWTG     EQU   6
RPARC    EQU   7
RWTGC    EQU   8
RA       EQU   11
RB       EQU   12
RC       EQU   13
RD       EQU   14
RJ       EQU   15
L5       EQU   5                        LENGTH OF ID-TTR
K0       EQU   0                        CONSTANT                 S20201
COMPLETE EQU   X'40'                    IOB COMPLETE SW          S20201
EOT      EQU   X'08'                    BUFFER AT END OF TRACK   S20201
EOTSW    EQU   X'01'                    DATA SET ENDS ON TRACK   S20201
*                                       * BOUNDARY. (DCBST)
ERRORX   EQU   X'DE'                    DCB MARKED IN ERROR      S20201
L3       EQU   3                        LENGTH                   S20201
K1       EQU   1                        CONSTANT                 S20201
K13      EQU   13                       CONSTANT                 S20201
K10      EQU   10                       CONSTANT                 S20201
K8       EQU   8                        CONSTANT                 S20201
CLOSE    EQU   X'40'                    CLOSE SW                 S20201
K26      EQU   26                       CONSTANT                 S20201
L7       EQU   7                        LENGTH                   S20201
CLOSE2   EQU   X'04'                    CLOSE SW                 S20201
K20      EQU   20                       CONSTANT                 S20201
WRCHK    EQU   X'80'                    WRITE CHECK SW DCB       S20201
RSLOAD   EQU   X'20'                    RESUME LOAD IND(DCBST)   XM6075
ISLKEYVL EQU   X'04'                    KEY SEQUENCE VALID       XM6075
CP20LAST EQU   X'03'                    CP20(FTIW) WAS SCHEDULED
*
         EJECT
***********************************************************************
* CHART G5 - CLOSE (FINISH PUT) HOUSEKEEPING                          *
***********************************************************************
*
         TM    DCBST,X'20'              IS THIS A NULL RESUME LD M0170
         BO    ISLG820                  YES, BRANCH              M0170
         L     R2,DCBDEBAD              DEB ADDR FROM COPY DCB   Y02072
         USING IHADEB,R2                DEB ADDRESSABILITY       Y02072
         DROP  RCORE                    DROP USING ON WORK AREA  Y02072
         LR    R8,RCORE                 CLOSE WORK AREA ADDR     Y02072
         USING FORCORE,R8               WORK AREA ADDRSSABILITY  Y02072
         LR    RCOPY,R1                 DCB COPY ADDRESS         Y02072
         L     R1,DXUDCBAD              ADDRESS OF USER DCB      Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=4  CHANGE TO USER KEY   Y02072
*
         L     R11,ISLVPTR3             C(R11)=A(BCT)            XM6075
         CLC   ISLVPTRB,DCBSYNAD        TEST VPTER 11 VS A(SYNAD)XM6075
         BNE   ISLG5012                 B IF NOT EQUAL
         OI    DCBEXCD2,X'20'           SET EXCD2 BIT 2 ON = NO SYNAD
ISLG5012 EQU   *                                                 Y02072
         OI    IOBFLAGS,X'10'           SET FLAGS BIT-3 ON (CLOSE)
         NI    IOBFLAGS,X'BD'           TURN OFF BITS 1 & 6       VLR
         NI    DCBST,X'FC'              CLEAR LAST BLK AND TRK    23769
*                                       BITS IN CASE RESUME LOAD
*                                       WAS USED
*
* SET UP LINES OF COMMUNICATION TO PUT AND APP MODULES
*
         L     R4,DEBEXPTR              DEB EXTENSION ADDRESS    S21045
         USING DEBEXT,R4                                         S21045
         L     R4,DEBPUT                PUT VECTOR               S21045
         DROP  R4                                                S21045
         LA    R4,6(R4)                 ISLF800+6 = PUT BASE
         ST    R4,ISLF8AD               STORE ADDR OF PUT BASE
         LA    R4,4(R4)                                          O19110
         ST    R4,ISLFXAD               STORE ADDR OF B TO ISLFX20
         LA    R4,4(R4)                                          O19110
         ST    R4,ISLFYAD               STORE ADDR OF B TO ISLFY01
         LA    R4,4(R4)                                          O19110
         ST    R4,ISLFZAD               STORE ADDR OF B TO ISLFZ01
         LA    R4,4(R4)                                          O19110
         ST    R4,ISLPAAD               STORE ADDR OF B TO ISLPA01
*
         L     R4,DEBAPPAD              AVT ADDRESS              S21045
         DROP  R2                       END USING ON DEB         Y02072
         USING DEBAVT,R4                                         S21045
         L     R4,DEBCEA                CHAN END ADDRESS         S21045
         DROP  R4                                                S21045
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         USING IHADCB,RCOPY             COPY DCB ADDR            Y02072
         ST    R4,DCBRELSE              CHAN END APP ADDR IN     Y02072
*                                       COPY DCB
         DROP  RCOPY                    END USING ON COPY        Y02072
         USING IHADCB,R1                USER DCB ADDR            Y02072
*
         MODESET KEYADDR=DXUKEY,WORKREG=2  CHANGE TO USER KEY    Y02072
*
*
* AWAIT COMPLETION OF ALL PRIOR I/O ACTIVITY
*
         LA    R2,ISLIOBB               C(R2)=A(IOBB)
         USING IHAIOB,R2                IOB ADDRESSABILITY       Y02072
         BAL   R14,ISLG502              B TO COMMON WAIT
         LA    R2,ISLIOBC               C(R2)=A(IOBC)
         BAL   R14,ISLG502              B TO COMMON WAIT
         BAL   R14,ISLG502A             B TO COMMON WAIT         XM6075
*
* TEST FOR MOVE OR LOCATE PUT
*
ISLG503  TM    DCBMACRF+1,X'10'         TEST MACRF BIT 11
         L     R7,DCBNREC               NUMBER OF RECORDS        A33664
         BC    1,ISLG508                B IF ON = MOVE PUT
*
* LOCATE PUT - CHECK SEQUENCE OF LAST PUT
*
*                                       HOUSEKEEPING
ISLG504  OI    DCBMACRF+1,X'10'         SET MACRF BIT 11 ON, SIM MOVE
         LH    R4,DCBRKP                C(R4)= RKP (00NN)
         A     R4,ISLCBF                C(R4)=RKP+CBF = A(KEY IN RCD)
         IC    R3,DCBKEYLE              C(R3)=KEYLEN
         BCTR  R3,0                     C(R3)=KEYLEN-1,FOR EXECUTE
         L     R5,ISLVPTR2              C(R5)=A(KEYSAVE)         XM6075
*
         TM    DCBEXCD1,X'FE'
         BC    7,ISLG505
         TM    DCBEXCD2,X'C0'                PREVIOUS ERROR
         BNE   ISLG505                  YES,BRANCH
         LA    R6,R2                    NREC 1 OR LESS           A33664
         CR    R7,R6                    *                        A33664
         BL    ISLG508                  B IF LOW, SEQ ERR IMPOSSIBLE
*
         EX    R3,ISLG504A              C(CBF+RKP) VS C(KEYSAVE)
         BH    ISLG508                  B IF NEW KEY HIGH, SEQUENCE OK
*
*                                       SEQUENCE NOT OK, TAKE SYNAD
*                                       ***************************
         NI    DCBMACRF+1,X'EF'         TURN BIT 11 BACK OFF (LOCATE)
         L     R13,ISLF8AD              ENTRY PT FOR SYNCH       Y02072
         BAL   R4,SYNCHRTN              SYNCH FOR SYNAD          Y02072
*
         TM    ISLIXLT,ISLKEYVL         DATA ADDED               XM6075
         BO    ISLG504                  YES - NOT NULL RS LOAD   XM6075
         OI    DCBST,RSLOAD             NO - MARK AS NULL RS LOADXM6075
         B     ISLG504                  GO TO HOUSEKEEPING AGAIN
*
*                                       PREV SEQ ERR, DROP LAST RCD
*
ISLG502A EQU   *                        *                        XM6075
         LA    R2,ISLIOBA               WAIT ON IOBA             XM6075
ISLG502  L     R4,IOBECBAD              C(R4)=A(ECB)             S20201
         DROP  R2                       END USING ON IOB         Y02072
         TM    0(R4),COMPLETE           TEST ECB BIT 1 (C-BIT)   S20201
         BCR   1,R14                    RETURN, I/O              S20201
*                                       COMPLETE-DONT WAIT       S20201
         LA    R13,ISLVRSAV             C(R13)=A(VRSAV)          S20201
         LR    R6,R14                   SAVE R14                 S20201
         LR    R3,R1                    SAVE R1                  S20201
         LR    R1,R4                    C(R1)=A(ECB)             S20201
         WAIT  ECB=(1)                  WAIT                     S20201
         LR    R1,R3                    RESTORE R1               S20201
         LR    R14,R6                   RESTORE R14              S20201
         BR    R14                      RETURN                   S20201
*
*                                       ***************************
ISLG505  EX    R3,ISLG505A              MOVE KEY FROM KEYSAVE TO BUFFR
ISLG505B EQU   *                        *                        A33664
         NI    DCBEXCD2,X'3F'           TURN EXCD2 BITS 0 AND 1 OFF
         BCTR  R7,0                     NREC-U                   A33664
         ST    R7,DCBNREC               STORE IT                 A33664
         L     R3,IOBPTRB               C(R3)=C(PTRB) = A(CURR SLOT)
         LA    R3,0(R3)
         L     R4,0(R3)                 C(R4)=A(CURR BUFFR START)
         LA    R4,8(R4)                 C(R4)=A(1ST RCD IN CURR BUFFR)
         C     R4,ISLCBF                TEST A(1ST RCD) VS C(CBF)
         BNE   ISLG5085                 B NOT EQUAL TO BACK UP CBF
*
*                                       CBF POINTS TO 1ST RCD IN BUFFR
         NI    0(R3),X'80'              TURN OFF ALL STATUS BITS
         LA    R4,IOBABUF               C(R4)=A(SLOT 1)
         CR    R3,R4                    TEST A(CURR SLOT) VS A(SLOT 1)
         BE    ISLG506                  B IF CURR SLOT = SLOT 1
         SH    R3,ISL4                  C(R3)=A(PREV SLOT)       O19110
         B     ISLG507
ISLG506  L     R3,ISLBUFN               C(R3)=A(SLOT N) = A(PREV SLOT)
ISLG507  IC    R5,IOBB                  SAVE B
         ST    R3,IOBPTRB               STORE A(PREV SLOT) IN PTRB
         STC   R5,IOBB                  RESTORE B
         L     R4,0(R3)                 C(R4)=A(PREV BUF)
         LA    R4,0(R4)
         MVC   DCBLPDA(8),DCBFTMI3      LPDA = PREVIOUS LPDA
         MVC   0(5,R4),DCBLPDA+3        PREV BUF CCHHR = LPDA CCHHR
         B     ISLG509
*
* BUMP CBF
*
ISLG508  EQU   *                                                 A33664
         TM    DCBEXCD1,X'20'           PREVIOUS SPACE PROBLEMS  A33664
         BO    ISLG505B                 YES CORRECT BUFFER PTRS  A33664
         L     R3,ISLCBF                C(R3)=CBF                A33664
         A     R3,ISLBMPR               C(R3)=CBF+BMPR
         ST    R3,ISLCBF                C(CBF)=CBF+BMPR
         NC    DCBNREC,DCBNREC          WAS DATA CREATED         A33664
         BE    ISLG509                  NO BRANCH
*
* TEST IF CURR BUFFR FULL
*
ISLG5085 CLC   ISLCBF(4),ISLEOB         TEST CBF VS EOB
         BL    ISLG601                  B IF CURR BUFFR NOT FULL
*
*                                       CURR BUFFR IS FULL
*
* TEST FOR LOCATE PUT, IF LOCATE (NOT SPECIAL) FULL BUF NOT YET WRITTEN
*
         TM    DCBMACRF+1,X'08'         TEST MACRF BIT 12 (LOCATE)
         BC    8,ISLG509                B IF 0, MOVE PUT
*
*                                       LOCATE PUT (NOT SPECIAL)
         L     R3,IOBPTRB               C(R3)=A(CURR SLOT)
         NI    0(R3),X'DF'              SET STATUS BITS 1 AND 2 = 10
         OI    0(R3),X'40'
         IC    R3,IOBB                  BUMP B, B=B+1
         LA    R3,1(R3)
         STC   R3,IOBB
*
ISLG509  OI    DCBST,X'02'              TURN ON DCBST BIT 6
*
* TEST IF CURR BUFFR HAS BEEN WRITTEN
*
         L     R3,IOBPTRB               C(R3)=A(CURR SLOT)
         TM    0(R3),X'60'              TEST STATUS BITS 1 AND 2
         BNE   ISLG701                  B IF CURR BUFFR FULL,    S20201
*                                       * BUT NOT WRITTEN
         LA    R4,ISLG810               RETURN ADDRESS           S20201
         SPACE 5                                                 S20201
TESTEOT  EQU   *                        *                        S20201
         L     R3,IOBPTRB               BUFFER PTR               S20201
         TM    0(R3),EOT                D.S. END ON EOT          S20201
         BZ    EOT1                     NO - MARK IT ANY WAY     S20201
         OI    DCBST,EOTSW              DATA SET ENDS ON EOT     S20201
EOT1     OI    0(R3),EOT                FORCE EOT                S20201
         BR    R4                       RETURN                   S20201
         EJECT
***********************************************************************
* CHART G6 - CLOSE (PAD OUT BUFFER)                                   *
***********************************************************************
*
* LOCATE LAST WR CKD TO BE EXECUTED IN CP18
*
ISLG601  L     R3,IOBPTRB               C(R3)=A(CURR SLOT) = X
         LA    R3,0(R3)
         LA    R4,IOBABUF               C(R4)=A(1ST SLOT)
         SR    R3,R4                    C(R3)=NO OF BYTES X IS FROM 1
         SRA   R3,2                     DIV BY 4, C(R3)=DIFF IN SLOTS
         L     R5,ISLOFFST              C(R4+R5)=OFFST
         MR    R4,R3                    X X OFFST, C(R5) = THE NO OF
*                                         BYTES WR CKD X IS FROM THE
*                                         1ST BYTE OF WR CKD 1
         L     R4,ISLVPTR4              C(R4)=A(CP18)            XM6075
         LA    R4,24(R4,R5)             C(R4)=A(WR CKD 1)        XM6075
*
* BACK UP CBF, BUFFER NOT FULL
*
         L     R3,ISLCBF                C(R3)=CBF
         S     R3,ISLBMPR               C(R3)=CBF-BMPR
*
* SET KEY ADDR IN LAST WR CKD TO REFERENCE LAST REAL RCD
*
         LH    R5,DCBRKP                C(R5)= RKP (00NN)
         AR    R5,R3                    C(R5)=CBF+RKP = A(LAST KEY)
         ST    R5,8(R4)                 STORE KEY ADDR WITH OP = 0
*
         A     R3,ISLBMPR               C(R3)=NEW CBF
         BCTR  R3,0                     C(R3)=CBF-1
         LR    R0,R3                    C(R0)=CBF-1
         MVC   TSTWK2C(1),0(R3)         C(WK2C)=C(CBF-1),SAVE    O19110
*                                       BYT                      O19110
         MVI   0(R3),X'FF'              C(CBF-1)=BINARY IS = PADDING
         NI    ISLECBA,X'BF'            INDICATE ECB BUSY SO PROCESSING
*                                       MODULE WON'T WRITE OUT BUFFER
*                                       BEFORE LAST BYTE OF RECORD
*                                       IS RESTORED             OX00614
ISLG602  L     R13,ISLFXAD              C(R13)=A(ISLFX20)
         BAL   R4,SYNCHRTN              SYNCH TO MOVE PADDING    Y02072
*
* BUMP CBF
*
         L     R3,ISLCBF                C(R3)=CBF
         A     R3,ISLBMPR               C(R3)=CBF+BMPR
         ST    R3,ISLCBF                C(CBF)=CBF+BMPR
*
* TEST FOR EOB, HAS RCD JUST MOVED FILLED A BUFFER
*
         C     R3,ISLEOB                TEST CBF VS EOB
         BL    ISLG602                  B IF NOT EOB
*
* BUFFER FILLED, EOB
*
         LR    R3,R0                    C(R3)=A(BORROWED BYTE)
         MVC   0(1,R3),TSTWK2C          RESTORE BYTE             O19110
         OI    ISLECBA,X'40'            INDICATE ECB NOT BUSY SO
*                                       PROCESSING MODULE WILL
*                                       WRITE OUT THIS BUFFER   OX00614
         SPACE 4
***********************************************************************
* CHART G7 - CLOSE (WR BUFFERS)                                       *
***********************************************************************
*
* TEST B VS FBW
*
ISLG701  BAL   R4,TESTEOT               CHECK FOR END OF TRACK   S20201
*
         B     ISLG704                  ENSURE ALL I/0 COMPLETE  A50362
*
ISLG701A EQU   *                        SCHEDULE LOOP            S20201
*                                       * FORCE INDEX
         TM    DCBEXCD1,ERRORX          UNCORRECTABLE ERRORS     S20201
         BM    ISLG810                  YES TAKE BRANCH           13662
         CLC   IOBB(1),ISLFBW+3         TEST B VS FBW
         BH    ISLG702                  B IF B GR THAN FBW
         IC    R3,IOBB                  C(R3)=B
         STC   R3,ISLFBW+3              C(FBW)=B
*
ISLG702  L     R13,ISLFYAD              C(R13)=A(ISLFY01)
         BAL   R4,SYNCHRTN              SYNCH TO WRITE BUFFERS   Y02072
*
ISLG704  EQU   *                        WAIT ON IOBA             A50362
         BAL   R14,ISLG502A             B TO COMMON WAIT         XM6075
*
* TEST B VS 0
*
         CLI   IOBB,X'00'               TEST B VS 0              O19110
         BH    ISLG701A                 BR TO SCHEDULE REST      S20201
*
         EJECT
ISLG810  LM    R0,RJ,DXCCW1             RESTORE ALL REGISTERS    Y02072
         DROP  R8                       END USING ON WORK AREA   Y02072
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         USING BASETAG,RBASE
         MVC   0(L'LOAD2N,RWTGC),LOAD2N NEXT LOAD ID - IGG0202N  Y02072
         B     RELOOP                   GO SEARCH WTG TABLE
ISLG820  MVC   0(L'LOAD2J,RWTGC),LOAD2J GO TO 202J.  REGS STILL  Y02072
*                                       PROPERLY INITIALIZED.
*
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH=BEGINNING OF THIS MODULE
         CLI   0(RWTGC),X'F0'           TEST FOR END OF WTG TABLE  MC1A
         BC    7,RELOOP                 BRANCH=NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BC    7,TCTLRTN                BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         USING FORCORE,RCORE                                     Y01021
         LA    RJ,DXCCW12               WA ADDRESS               Y01021
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
         EJECT
***********************************************
* SYNCH SUBROUTINE - GO TO PROCESSING ROUTINE *
***********************************************
*
         USING FORCORE,R8               CLOSE WA ADDRESSABILITY  Y02072
*
SYNCHRTN MODESET EXTKEY=DATAMGT         CHANGE TO DATA MGT KEY   Y02072
*
         STM   R0,R5,DXCCW9             SAVE REGISTERS 0-5       Y02072
         L     RJ,CVTPTR                ADDR OF CVT              Y02072
         USING CVT,RJ                   CVT ADDRESSABILITY       Y02072
         L     RJ,CVTTCBP               ADDR OF TCB PTRS         Y02072
         L     RJ,0(RJ)                 TCB ADDR                 Y02072
         USING TCB,RJ                   TCB ADDRESSABILITY       Y02072
         L     RJ,TCBRBP                SVRB ADDRESS             Y02072
         USING RBBASIC,RJ               RB ADDRESSABILITY        Y02072
*
         MODESET KEYADDR=KEY0,WORKREG=4  CHANGE TO KEY ZERO      Y02072
*
         STM   R6,RJ,RBEXSAVE           SAVE REGISTERS 6-15      Y02072
         DROP  RJ                       END USING ON RB          Y02072
         L     R15,ISLF8AD              SET BASE ADDR FOR RTN    Y02072
         LR    RJ,R13                   ENTRY POINT TO REG 15    Y02072
*
         SYNCH (15)                     GO TO PROCESSING RTN     Y02072
*
         L     RJ,CVTPTR                CVT ADDRESS              Y02072
         USING CVT,RJ                   CVT ADDRESSABILITY       Y02072
         L     RJ,CVTTCBP               ADDR OF CVT PTRS         Y02072
         L     RJ,0(RJ)                 ADDRESS OF TCB           Y02072
         USING TCB,RJ                   TCB ADDRESSABILITY       Y02072
         L     RJ,TCBRBP                SVRB ADDRESS             Y02072
         USING RBBASIC,RJ               RB ADDRESSABILITY        Y02072
         LM    R6,RJ,RBEXSAVE           RESTORE REGS 6-15        Y02072
         DROP  RJ                       END USING ON SVRB        Y02072
*
         MODESET KEYADDR=DXUKEY,WORKREG=5 CHANGE TO USER KEY     Y02072
*
         LM    R0,R5,DXCCW9             RESTORE REGS 0-5         Y02072
         BR    R4                       RETURN                   Y02072
         EJECT
*
* CONSTANTS
*
ISL4     DC    H'0004'                                           O19110
KEY0     EQU   ISL4                     STORAGE PROTECT KEY ZERO Y02072
THISLOAD DC    C'2I'                                             O19110
*
ISLG504A CLC   0(1,R4),0(R5)            KEY COMP TO BE EXECUTED (L)
ISLG505A MVC   0(1,R4),0(R5)            KEY MOVE TO BE EXECUTED (L)
*
LOAD2J   DC    C'2J'                    ID OF MODULE IGG0202J    Y02072
LOAD2N   DC    C'2N'                    ID OF MODULE IGG0202N    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         SPACE 1
         END
