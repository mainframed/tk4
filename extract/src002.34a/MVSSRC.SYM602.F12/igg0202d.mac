         TITLE 'IGG0202D - COMMON ISAM CLOSE'
IGG0202D CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0202D                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM CLOSE, COMMON TO ALL MODES                  *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = XCTL TO NEXT CLOSE EXECUTOR IF DCB OPENED FOR GET        *
*            WITHOUT PUTX, OR READ WITHOUT WRITE; OR IF ABEND ISSUED  *
*            CLOSE FOR A LOAD MODE DATA SET.  OTHERWISE, GET CORE     *
*            FROM SUBPOOL 230, AND EXECUTE CP CLOSECCW1 TO READ F2    *
*            DSCB INTO CORE.  FOR LOAD MODE, FREE CHANNEL PROGRAM     *
*            CORE, WORK AREA CORE, AND TRACK INDEX SAVE AREA (TISA)   *
*            IF FULL TRACK INDEX WRITE (FTIW) SPECIFIED.  FOR INITIAL *
*            LOAD, SET BIT 2 OF DCBST.  FOR BISAM, SCAN, AND LOAD     *
*            MODE--IF NOT A NULL RESUME LOAD--ACQUIRE LOCAL LOCK,     *
*            VALIDATE DCB ADDRESS, CHANGE TO KEY 0, AND MERGE         *
*            APPROPRIATE DCB FIELDS INTO THE F2 DSCB.  THEN RELEASE   *
*            LOCK, RETURN TO DATA MANAGEMENT KEY, AND EXECUTE CP      *
*            CLOSECCW2 TO WRITE F2 DSCB BACK INTO VTOC.  FINALLY,     *
*            FREE CORE GOTTEN FOR F2 DSCB.                            *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = CLOSE EXECUTOR                                        *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 990 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0202D                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM IFG0200W IF ABEND IS IN CONTROL  *
*              FOR A LOAD MODE DATA SET, OTHERWISE FROM AN ISAM CLOSE *
*              EXECUTOR:  IGG0202M FOR LOAD MODE, IGG02029 FOR SCAN,  *
*              OR IGG0202A FOR BISAM.  RECEIVES CONTROL IN STORAGE    *
*              PROTECT KEY 5 AND PRIVILEGED STATE.                    *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL IN STORAGE PROTECT KEY 5 TO COMMON CLOSE.        *
*                                                                     *
* EXIT-ERROR = ABEND CODES:                                           *
*              03A - I/O ERROR WHILE READING OR WRITING F2 DSCB       *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = DCB VALIDITY-CHECK ROUTINE                            *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - CLOSE WORK AREA                           *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                 IHAPSA - LOW CORE ADDRESSABILITY                    *
*                                                                     *
*    CONTROL-BLOCKS = DCB, DCB COPY, DEB, DSCB, CVT, TCB, AND IOB     *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, GETMAIN, FREEMAIN, EXCP, WAIT, SETLOCK, ABEND,    *
*          AND XCTL                                                   *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*         RELEASE OS/VS2-02 DELETIONS                                 *
*                                                               YM01385
*         CHANGES SINCE VS2-3.7
*A017100,A018894-018944                                        @ZA31639
*                                                                     *
***********************************************************************
         EJECT
ISLCOMON IGGLOAD                                                 Y02072
         SPACE 4
CVT      DSECT                                                   Y02072
         CVT                                                     Y02072
F2DSECT  DSECT
         DS    0D
F2K1     DS    CL1                      02
F2K2     DS    CL7                      DCBFTMI2
F2K3     DS    CL5                      DCBLEMI2
F2K4     DS    CL7                      DCBFTMI3
F2K5     DS    CL5                      DCBLEMI3
F2D0A    DS    CL1                      DCBOVDEV                 O19113
         DS    CL10                                              O19113
F2D0     DS    CL8                      DCBMSWA                   P4701
F2D1     DS    CL1                      ID
F2D2     DS    CL1                      DCBNLEV
F2D3     DS    CL1                      DCBNTM
F2D4     DS    CL3                      DCBFIRSH
F2D5     DS    CL2                      DCBLDT
F2D6     DS    CL1                      DCBCYLOF
F2D7     DS    CL1                      DCBHIRCM
F2D8     DS    CL1                      DCBHIRPD
F2D9     DS    CL1                      DCBHIROV
F2D10    DS    CL1                      DCBHIRSH
F2D11    DS    CL1                      DCBHIRTI                 O19113
F2D11A   DS    CL1                      DCBHIIOV                 O19113
F2D12    DS    CL2                      DCBTDC
F2D13    DS    CL3                      DCBRORG3
F2D14    DS    CL2                      DCBNCRHI
F2D15    DS    CL1                      DCBNTHI
F2D16    DS    CL4                      DCBNREC
F2D17    DS    CL1                      DCBST
F2D18    DS    CL7                      DCBFTCI
F2D19    DS    CL7                      DCBFTMI1
F2D20    DS    CL7                      DCBFTHI
F2D21    DS    CL8                      DCBLPDA
F2D22    DS    CL5                      DCBLETI
F2D23    DS    CL5                      DCBLECI
F2D24    DS    CL5                      DCBLEMI1
F2D25    DS    CL8                      DCBLIOV
F2D26    DS    CL2                      DCBNBOV
F2D27    DS    CL2                      DCBRORG2
F2D28    DS    CL2                      DCBNOREC
F2D29    DS    CL2                      DCBRORG1
F2K6     DS    CL3                      ISLNIRT                   P4701
FORCORE  DSECT
         IECDSECT
         DCBD  DSORG=(IS)
IHATCB   DSECT                                                    20852
         USING IHATCB,R10                                         20852
         ORG   IHATCB+29                                          20852
TCBFLAGS DS    CL1                      FLAG BYTE 2               20852
         SPACE 4
         IHAPSA                                                  Y02072
         EJECT
IGG0202D CSECT
RE       EQU   0                        WORK REGISTER
RF       EQU   1                        WORK REGISTER
RDCB     EQU   2                        DCB BASE REGISTER
RBASE    EQU   3                        BASE REGISTER
RCORE    EQU   4                        IECDSECT BASE REGISTER
RPAR     EQU   5                        PARAMETER LIST REGISTER
RWTG     EQU   6                        ADDRESS OF WHERE TO GO TABLE
RPARC    EQU   7                        CURRENT ENTRY IN PAR TBL
RWTGC    EQU   8                        CURRENT ENTRY IN WTG TABLE
R9       EQU   9                        WORK REGISTER
R10      EQU   10                       WORK REGISTER
RA       EQU   11                       WORK REGISTER
RB       EQU   12                       WORK REGISTER
RC       EQU   13                       WORK REGISTER
RD       EQU   14                       WORK REGISTER
R15      EQU   15                       WORK REGISTER
RJ       EQU   15                       WORK REGISTER
RDSCB2   EQU   9                        PTR TO DSCB FORMAT 2
SP230    EQU   230                      SUBPOOL TO READ F2 INTO  Y02072
DCWOPCLS EQU   40                       OFFSET TO DCWOPCLS IN BISAM WA
HGH3BYT  EQU   X'0E'                    ST 3 HIGH ORDER BYTES    Y02072
OPNCMPLT EQU   X'08'                                           @ZA31639
*
*   DCBST - BIT LOCATIONS.
*
EOCST    EQU   X'10'                    DATA SET ENDS ON THE     A39157
*                                       * LAST RECORD OF A CYL.
EOTST    EQU   X'02'                    D.S. END AT END OF TRACK A39157
EOBST    EQU   X'01'                    D.S. END AT END OF BLOCK A39157
*
         EJECT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         USING FORCORE,RCORE
         L     RCORE,4(RWTGC)           INITIALIZE BASE OF WORK AREA
         LA    RCORE,0(RCORE)
         USING IHADCB,RDCB
         L     RDCB,0(RPARC)            INITIALIZE BASE OF DCB    22544
         L     R10,CVTPTR               C(R10) = A(CVT)           20852
         L     R10,0(R10)               C(R10) = A(TCB POINTER)   20852
         L     R10,4(R10)               C(R10) = A(TCB)           20852
*
* IF THIS DCB IS FOR A GET WITHOUT PUTX OR A READ WITHOUT WRITE, THEN
* THE FORMAT 2 DSCB SHOULD NOT BE UPDATED AS NONE OF THE ATTRIBUTES
* OF THE DATA SET WILL HAVE CHANGED.
*
         TM    DCBMACRF,X'60'           IS IT FOR READ OR GET    A31990
         BZ    CONTINUE                 NO - FORMAT 2 MUST BE    A31990
*                                       *    UPDATED.
         TM    DCBMACRF+1,X'60'         IS IS A PUT OR WRITE     A31990
         BZ    EXIT                     NO - FORMAT2 SHOULD NOT  A31990
*                                       *    BE UPDATED.
CONTINUE EQU   *                        YES - FORMAT 2 MUST BE   A31990
*                                       *     UPDATED
         TM    TCBFLAGS,X'80'           DID ABEND ISSUE CLOSE     22544
         BZ    RDFMT2                   NO, READ FORMAT 2 DSCB    22544
         TM    DCBMACRF+1,X'18'         LOAD MODE                 22544
         BNZ   EXIT                     YES, DON'T CLOSE LOAD     22544
RDFMT2   EQU   *                                                  22544
         TM    DCBMACRF+1,X'18'         LOAD MODE?             @ZA31639
         BNZ   GETSPC                   YES-GO AROUND          @ZA31639
         TM    DCBST,OPNCMPLT           WAS OPEN COMPLETE?     @ZA31639
         BNO   EXIT                     NO-BR TO EXIT MODULE   @ZA31639
         NI    DCBST,X'F7'              TURN OFF OPEN COMPLETE @ZA31639
GETSPC   EQU   *                                               @ZA31639
         GETMAIN R,LV=DSCBSIZE,SP=SP230 GET FORMAT 2 SPACE       Y02072
DSCBSIZE EQU   140                      FORMAT 2 SIZE
*
*   GET CORE FOR DSCB FORMAT 2
*   INITIALIZE CHANNEL PROGRAM TO READ DSCB FORMAT 2
*
         LA    RDSCB2,0(RF)             RDSCB2 = ADDRESS OF FORMAT 2
         LM    RA,RD,CLOSECCW           FILL IN CCWS 1 AND 2
         ALR   RA,RCORE                 COMPLETE ADDRESS
         ALR   RC,RCORE                 COMPLETE ADDRESS
         STM   RA,RD,DXCCW1             INITIALIZE DXCCWS 1 AND 2
         LM    RA,RB,CLOSECCW+16        FILL IN CCW 3
         ALR   RA,RDSCB2                ADD BASE OF DSCB FORMAT 2
         STM   RA,RB,DXCCW3             SET UP CCW IN OPEN WK AREA
         MVI   DXDAADDR,0               ZERO M OF MBBCCHHR
         MVC   DXDEBUCB,DXUCBADR        INSERT UCB ADDRESS      YM01385
         MVC   DXDAADDR+3(L'DSCBNEXT),DSCBNEXT   F2 CCHHR        Y02072
         SR    RE,RE                    COMPLETE DEB EXTENT
         STH   RE,DXDEBSCC              SET START CCHH = 0000
         STH   RE,DXDEBSHH
         BCTR  RE,0                     RE = F'S
         STH   RE,DXDEBECC              SET END CCHH = FFFF
         STH   RE,DXDEBEHH
         L     RF,DCBDEBAD
         LA    RE,36                    GET ADDR OF DEB PREFIX
         SR    RF,RE
         MVC   DXDEBUCB+4(2),21(RF)     MOVE BB TO PHONY DEB
         MVC   DXDAADDR+1(2),21(RF)      AND TO IOBSEEK
*
         EXCP  DXIOB                    EXECUTE READ KEY AND DATA
         WAIT  1,ECB=DXECB
         TM    IOBECBPT,X'20'           TEST FOR PERMANENT I/O ERROR
         BO    TESTMODE                 BRANCH = NO ERROR
ABEND3A  EQU   *                        PERMANENT ERROR WHILE UPDATING
*                                       THE FORMAT 2 DSCB = 03A
         ABEND X'03A',DUMP,,SYSTEM      SYSTEM 03A ABEND         Y02072
*
*
TESTMODE EQU   *
         USING F2DSECT,RDSCB2
         CLI   F2D1,X'F2'               WAS DSCB READ A FMT.2      MC1A
         BNE   ABEND3A                  IF NOT, ABEND              MC1A
         TM    DCBMACRF+1,X'18'         TEST IF QISAM LOAD MODE
         BZ    TESTSCAN                 BRANCH = NOT  LOAD MODE
*
*    IT IS LOAD MODE
*   FREE CORE USED BY LOAD MODE FOR ITS CHANNEL PROGRAMS
*
         L     RB,DCBDEBAD              GET PTR TO DEB           A33513
         TM    8(RB),X'80'              TEST DEBOFLGS FOR DISP   A33513
         BO    MODNEW                   BR IF NEW/MOD            A33513
         XC    1(43,RDSCB2),1(RDSCB2)   ZERO ALL FORMAT 2 FIELDS A33513
         XC    45(90,RDSCB2),45(RDSCB2) EXCEPT HEX CODE,         A33513
*                                       DS2FMTID, AND DS2PTRDS
MODNEW   EQU   *                                                 A33513
*
         MODESET  KEYADDR=DXUKEY,WORKREG=12  CHANGE TO USER KEY  Y02072
*
         L     RB,DCBWKPT1              RB = ADR OF LOAD WORK AREA
         USING ISLCOMON,RB              LOAD MODE WORK AREA      Y02072
         IC    RA,ISLHIRT               HIGHEST R IN TRK INDEX   Y02072
         L     RD,ISLNIRT               HHR OF DUMMY TI ENTRY    Y02072
         L     RF,ISLVPTR4              LOAD CHANNEL PGM ADDR    Y02072
         L     RE,ISLVPTR9              SIZE OF CHANNEL PGM      Y02072
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         STC   RA,F2D11                 TRK INDEX HIGH R         Y02072
         STCM  RD,HGH3BYT,F2K6          DUMMY TRK INDEX HHR      Y02072
         SPACE 2                                                 M0947
*   FREE LOAD MODE CHANNEL PROGRAM AREA, ALLOW FOR RPS PREFIX CP18.
         SPACE 1                                                 M0947
         LA    RA,250                   USE SUBPOOL 250
         SLL   RA,24
         SH    RF,HWEIGHT               ALLOW FOR RPS PREFIX     A52413
         OR    RE,RA
         FREEMAIN R,LV=(0),A=(1)
*
         MODESET  KEYADDR=DXUKEY,WORKREG=1   CHANGE TO USER KEY  Y02072
*
         TM    680(RB),X'C0'            SUCCESSFUL GETMAIN FOR   O19110
*                                       FULL TRACK INDEX WRITE
         BNO   FREECORE                 NO BR TO CONTINUE        O19110
*
*              FREE CORE USED BY FULL TRACK INDEX WRITE OPTION
*
         L     RF,680(RB)               C(RF)=A(TISA)            O19110
         LA    RF,0(RF)                 ZERO HIGH ORDER BYTE     O19110
         L     RE,16(RF)                C(RE)=SIZE OF TISA       O19110
         OR    RE,RA                    COMBINE SIZE & SUBPOOL   O19110
         FREEMAIN R,LV=(0),A=(1)                                 O19110
*
*
*   FREE CORE USED BY LOAD MODE AS A WORK AREA
FREECORE EQU   *                                                 O19110
         L     RE,672(RB)               C(RE)= WORK AREA SIZE     20852
         OR    RE,RA
         LR    RF,RB                    RF = ADR OF LOAD WORK AREA
         FREEMAIN R,LV=(0),A=(1)
         L     RDCB,DXUDCBAD            USERS DCB ADDRESS        Y02072
         NC    DCBNREC,DCBNREC          TEST FOR NULL DATA SET   M0170
         BZ    MERGELD                  BRANCH IF IT IS          M0170
         CLC   DCBNREC,F2D16            WAS IT NULL RESUME LOAD  M0170
         BE    FREEDSCB                 YES, BRANCH              M0170
MERGELD  EQU   *                                                 M0170
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         BAL   R15,GETLOCK              ACQUIRE LOCK AND         Y02072
*                                       VALIDATE ADDRESS OF USERS DCB
         MVC   F2K2(24),DCBFTMI2        MOVE SPECIFIED LOAD MODE FIELDS
         MVC   F2D2,DCBNLEV              FROM DCB TO DSCB FORMAT 2
         MVC   F2D3,DCBNTM
         MVC   F2D4,DCBFIRSH
         MVC   F2D5,DCBLDT
         MVC   F2D6,DCBCYLOF
         MVC   F2D7(4),DCBHIRCM
         MVC   F2D14,DCBNCRHI
         MVC   F2D18,DCBFTCI
         MVC   F2D19,DCBFTMI1
         MVC   F2D20,DCBFTHI
         MVC   F2D0,DCBMSWA             SAVE LAST PRIME DATA TRK  P4701
LIKELOAD EQU   *                        MOVE FIELDS COMMON TO LOAD AND
         MVC   F2D16,DCBNREC             BISAM FROM DCB TO DSCB FORMAT2
         MVC   F2D21(13),DCBLPDA
         MVC   F2D23,DCBLECI
         MVC   F2D24,DCBLEMI1
         MVC   F2D25,DCBLIOV
         MVC   F2D26,DCBNBOV
         MVC   F2D27,DCBRORG2
         MVC   F2D11A,DCBHIIOV                                   O19113
         MVC   F2D0A(1),DCBOVDEV                                 O19113
*   CORRECT DCBST SWITCHES THAT MAY HAVE CHANGED
         NI    F2D17,X'FF'-EOTST-EOBST  RESET SWITCHES           A39157
         OI    DCBST,X'20'              SET STATUS TO RESUME LOAD P4701
         OC    F2D17(1),DCBST                                     P4701
         NI    F2D17,X'FF'-EOCST        RESET EOC SW FOR RSLOAD  A39157
         B     RITEBACK
TESTSCAN EQU   *
         L     RDCB,DXUDCBAD            USERS DCB ADDRESS        Y02072
         BAL   R15,GETLOCK              ACQUIRE LOCK AND         Y02072
*                                       VALIDATE USER DCB ADDRESS
         TM    DCBMACRF,X'40'           TEST IF SCAN
         BO    ITISSCAN                 BRANCH = SCAM
* IT IS BISAM
         MVC   F2K3,DCBLEMI2            MOVE FIELDS UNIQUE TO BISAM
         MVC   F2K5,DCBLEMI3             FROM DCB TO DSCB FORMAT 2
         MVC   F2D12,DCBTDC
         MVC   F2D13(3),DCBRORG3+1
         MVC   F2D28,DCBNOREC
         MVC   F2D29,DCBRORG1
         B     LIKELOAD
ITISSCAN EQU   *
         MVC   F2D12,DCBTDC             MOVE FIELD FOR SCAN FROM DCB
*                                        TO DSCB FORMAT 2
         B     RITEBACK                 WRITE DSCB               Y02072
*
*
GETLOCK  EQU   *                        ROUTINE ACQUIRES LOCAL   Y02072
*                                       LOCK, FREED AT RITEBACK
*                                       OR VLDCKERR
         STM   RDCB,R15,DXCCW5          SAVE REGISTERS 2-15      Y02072
*
         MODESET KEYADDR=KEY0,WORKREG=13  CHANGE TO KEY ZERO     Y02072
*
*    ACQUIRE THE LOCAL LOCK.  IT WILL BE RELEASED FOLLOWING THE
*    MERGING OF THE FIELDS FROM THE DCB TO THE DSCB IF THE DCB
*    ADDRESS IS VALID, OR FOLLOWING THE RETURN FROM THE VALIDATE
*    ROUTINE IF THE ADDRESS IS INVALID.
*
GLCL     SETLOCK  OBTAIN,TYPE=LOCAL,MODE=UNCOND,   OBTAIN THE    Y02072*
               RELATED=(DCB,IGG0202D)   LOCAL LOCK               Y02072
*
         LA    RF,0(,RDCB)              VALID CHK BEGIN ADDR     Y02072
         LA    RDCB,DCBWKPT6-IHADCB(RDCB)  END OF ADDR RANGE     Y02072
         LR    RPAR,RCORE               SAVE CLOSE WA ADDRESS    Y02072
         SR    RCORE,RCORE              USE CURR TCB FOR TEST    Y02072
         L     R15,CVTPTR               ADDRESS OF CVT           Y02072
         USING CVT,R15                  CVT ADDRESSABILITY       Y02072
         L     R15,CVT0VL01             ADDR OF VALID CHK RTN    Y02072
         DROP  R15                      END USING ON CVT         Y02072
*
         BALR  RD,R15                   BR TO VALIDATE ROUTINE   Y02072
*
         BNZ   VLDCKERR                 BR IF TEST NOT SUCC      Y02072
         LR    RCORE,RPAR               RESTORE WA ADDRESS       Y02072
         LM    RDCB,R15,DXCCW5          RESTORE REGISTERS 2-15   Y02072
         BR    R15                      RETURN                   Y02072
*
VLDCKERR EQU   *                        FREES LOCK WHEN VALID    Y02072
*                                       CHK NOT SUCCESSFUL
*
*    RELEASE THE LOCAL LOCK ACQUIRED AT STATEMENT GLCL.
*
FLCL1    SETLOCK  RELEASE,TYPE=LOCAL,   FREE THE                 Y02072*
               RELATED=(DCB,IGG0202D)   LOCAL LOCK               Y02072
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         LR    RCORE,RPAR               RESTORE WA ADDRESS       Y02072
         LM    RDCB,R15,DXCCW5          RESTORE REGS 2-15        Y02072
         B     FREEDSCB                 FREE DSCB CORE           Y02072
*
RITEBACK EQU   *
*
*    RELEASE THE LOCAL LOCK ACQUIRED AT STATEMENT GLCL.
*
         SETLOCK  RELEASE,TYPE=LOCAL,   RELEASE LOCAL LOCK       Y02072*
               RELATED=(DCB,IGG0202D)   ACQUIRED AT GETLOCK      Y02072
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         LM    RDCB,R15,DXCCW5          RESTORE REGS 2 THRU 15   Y02072
         LM    RA,RB,DXCCW3             MOVE CCW3 (READ KD) TO
         STM   RA,RB,DXCCW6              CCS6 BUT SUPPRESS DATA TRANS
         MVI   DXCCW6+4,X'10'           CCW6 = READ KEY AND DATA
         LM    RA,RB,CLOSECCW+24        FILL IN CCW 3
         ALR   RA,RDSCB2                ADD BASE OF DSCB
         STM   RA,RB,DXCCW3             CCW3 = WRITE KEY AND DATA
         LM    RA,RD,DXCCW1             USING SEARCH = ID AND TIC OF
         SR    R10,R10                   CCW1 AND 2 SET UP CCW4 AND 5
         LA    R10,24                    TO SEARCH = ID AND TIC
         ALR   RC,R10
         STM   RA,RD,DXCCW4
         EXCP  DXIOB
         WAIT  1,ECB=DXECB
         TM    IOBECBPT,X'20'           TEST FOR PERMANENT I/O ERROR
         BC    12,ABEND3A               BRANCH = PERMANENT ERROR
*   FREE AREA USED FOR DSCB FORMAT 2
FREEDSCB EQU   *                                                 M0170
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY         Y02072
*
         LA    RF,0(RDSCB2)             RF = ADDRESS OF DSCB
         FREEMAIN R,LV=DSCBSIZE,SP=SP230,A=(1) FREE FMT 2 SPACE  Y02072
*
EXIT     EQU   *                                                  20852
*
         MODESET  KEYADDR=DXUKEY,WORKREG=1   CHANGE TO USER KEY  Y02072
*
* THE FOLLOWING TWO INSTRUCTIONS RESTORE DCBESETL, DCBLRAN, DCBLWKN,
* DCBRELSE, DCBPUTX, DCBRELX AND DCBFREED TO THEIR PRE-OPEN CONDITION
*
         L     RDCB,DXUDCBAD            ENSURE USER DCB ADDR     Y02072
         MVC   DCBESETL,ONE             PUT CONSTANT ONE IN FIRST
*                                       ADDRESS FIELD            A51472
         MVC   DCBLRAN(24),DCBESETL     PROPAGATE TO REST        A51472
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         MVC   0(L'CLOSE,RWTGC),CLOSE   RETURN TO COMMON CLOSE   Y02072
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH=BEGINNING OF THIS MODULE
         CLC   0(1,RWTGC),ENDCLOSE     TEST FOR END OF WTG TABLE
         BC    7,RELOOP                 BRANCH=NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BC    7,TCTLRTN                BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
*
         LA    RJ,DXCCW12
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO CLOSE      Y02072
         EJECT
CLOSECCW DS    0D
* CCW1 - SEARCH EQUAL ID
         DC    X'31'                    SEARCH
         DC    AL3(3+DXDAADDR-DXLBL)    ADDRESS OF IOB + 35
         DC    X'6000'                  CC
         DC    X'0005'                  LENGTH
* CCW2 - TIC TO CCW1
         DC    X'08'                    TIC
         DC    AL3(DXCCW1-DXLBL)        ADDRESS OF DXCCW1
         DC    F'0'
*CCW3  - READ KEY AND DATA
         DC    X'0E'
         DC    AL3(DXDSCB-DXLBL)        ADDRESS
         DC    X'0000'                  NO CC
         DC    X'008C'                  LENGTH = 140
* CCW3A - WRITE KEY AND DATA
         DC    X'0D'
         DC    AL3(DXDSCB-DXLBL)        ADDRESS
         DC    X'4000'                  CC
         DC    X'008C'                  LENGTH = 140
* CCW4 - SEARCH EQUAL ID,  USE DXCCW1
* CCW5 - TIC TO CCW4,  USE DXCCW2
* CCW6 - READ KEY AND DATA AND SUPPRESS DATA TRANSFER, USE CCW3
*
HWEIGHT  DC    H'8'                     HALFWORD CONSTANT        M0947
KEY0     EQU   HWEIGHT                  STORAGE PROTECT KEY ZERO Y02072
THISLOAD DC    C'2D'
ENDCLOSE DC    C'00'
CLOSE    DC    X'0000'                  ZERO CURR ENTRY IN WTG   Y02072
ONE      DC    F'1'
*
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
*
         END
