 TITLE 'IGG01924 - THIRD AND LAST SCAN MODE OPEN MODULE'         A35340
IGG01924 CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG01924                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, SCAN MODE                             *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 01                                *
*                                                                     *
* FUNCTION = MERGE F2 DSCB FIELDS INTO DCB.  CALCULATE W1ICNOT (USED  *
*            FOR SCHEDULING I/O).  IF ANY PART OF THE DATA SET        *
*            EXISTS ON AN RPS DEVICE, OR IF IN A VIRTUAL ENVIRONMENT, *
*            LOAD RPS SIO MODULE, IGG019HA.  FIGURE LIMITS FOR OVER-  *
*            FLOW RECORDS.  CREATE IRB FOR ASYNCHRONOUS MODULE, USING *
*            ADDRESS IN DXCCW7, SAVED BY IGG01928.  FINISH WA         *
*            INITIALIZATION.  REFRESH USER DCB FROM COPY DCB.         *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 826 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG01924                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG01929 IN   *
*              STORAGE PROTECT KEY 5 AND PRIVILEGED STATE.            *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = RETURN TO COMMON OPEN, LOAD 7, VIA XCTL IN STORAGE    *
*               PROTECT KEY 5.  THIS IS THE LAST OF THE ISAM OPEN     *
*               EXECUTORS FOR SCAN MODE.                              *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGSCAN - ISAM SCAN MODE WORK AREA                  *
*                                                                     *
*    CONTROL-BLOCKS = DCB, DCB COPY, DEB, DSCB, AND CVT               *
*                                                                     *
* TABLES = DEVICE TABLE                                               *
*                                                                     *
* MACROS = MODESET, LOAD, CIRB, AND XCTL                              *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*          RELEASE OS/VS2-02 DELETIONS                                *
*D053400                                                        YM01159
*D                                                              YM02867
*          RELEASE VS2-030 CHANGES                             @ZA12254
*C077500                                                       @ZA12254
*                                                              @ZA12254
*                                                                     *
***********************************************************************
         EJECT
         USING IHADCB,RDCB
         USING SISWORK1,RUCB
         BALR  RBASE,0
         USING *,RBASE
R15      EQU   15
RE       EQU   0
RF       EQU   1
RDCB     EQU   2
RBASE    EQU   3
RG       EQU   4
RPAR     EQU   5
RWTG     EQU   6
RPARC    EQU   7
RWTGC    EQU   8
RTIOT    EQU   9
RUCB     EQU   10
RA       EQU   11
RB       EQU   12
RC       EQU   13
RD       EQU   14
RJ       EQU   15
TWO      EQU   2                        LEN OF EXCEPTION FIELD   A35399
K1       EQU   1                        CONSTANT                 S20201
RPS      EQU   X'E0'                    RPS IDENTIFIER           S20201
K2       EQU   2                        CONSTANT                 S20201
L2       EQU   2                        CONSTANT                 S20201
COMP     EQU   X'40'                    IOB COMPLETE INDICATOR   S20201
DEBSIOAD EQU   4                        DEB SIO APPENDAGE ENTRY  A35340
IGG019HA EQU   C'A'                     IGG019HA - RPS SIO MODULE
VIRTUAL  EQU   X'80'                    VIRTUAL NE REAL INDICATOR
THIRTY   EQU   30                       LIMIT FOR BUFFERS USED
         SPACE 2
*        INITIALIZE REGISTERS
SIS05A2  L     RDCB,0(RPARC)
         SPACE 2
         L     RUCB,DCBWKPT1
         L     RG,4(RWTGC)              ADDRESSABILITY ON OPEN   A35340
         USING FORCORE,RG               *    WORK AREA           A35340
*        LOCATE DSCB
         L     RA,DSCCORE                                        A35340
         USING DSCBFRM2,RA                                       A35340
         STM   RPAR,RWTGC,DXCCW1        SAVE REGS 5-8            Y02072
         DROP  RG                       END OPEN WA ADDR         Y02072
         LR    RPAR,RG                  GET OPEN WA ADDR         Y02072
         USING FORCORE,RPAR             EST OPEN WA ADDR         Y02072
         SPACE 3                                                 A35340
*        WHEN  DSCB IS LOCATED MOVE DSCB FIELDS TO DCB
         SPACE 2                                                 A35340
         MVC   DCBNREC(4),D2NREC        NUMBER OF PRIME RECORDS  A35340
         MVC   DCBNOREC,D2NOREC         NUMBER OF OFL RECORDS   OZ05938
*                                       A2735                    A35340
         SR    RB,RB                    STATUS BITS              A35340
         IC    RB,D2ST                                           A35340
         SRL   RB,6                                              A35340
         STC   RB,DCBST                                          A35340
         MVC   DCBLPDA,D2LPDA           LAST PRIME DATA RCD OF   A35340
*                                       SET                      A35340
         MVC   DCBNOREC,D2NOREC         NUMBER OF OVERFLOW RECORDS
         MVC   DCBLIOV,D2LIOV           LAST IND OVFL RECORD
         XC    DCBEXCD1(TWO),DCBEXCD1   ZERO EXCEPTION FLAGS     A35399
         L     RG,DCBDEBAD              ADDRESSABILITY ON DEB    A35340
         USING SISDEB,RG                                         A35340
* SET UP BB FOR 2321 IN LPDA
         SR    R15,R15                                           A36373
         IC    R15,DCBLPDA              GET EXTENT NUMBER        A36373
         SLL   R15,4                    TIMES 16                 A36373
         LA    R15,DEBUCBAD(R15)        GET EXTENT ADDRESS       A36373
         MVC   DCBLPDA+1(2),4(R15)      SET UP BB                A36373
         SPACE 3                                                 A35340
*        FIND ICNOT = SMALLEST INTEGER THAT WILL CONTAIN BUFNO/2
         MODESET  KEYADDR=DXUKEY,WORKREG=14  SET USERS KEY       Y02072
         SPACE 2
         LH    RD,W1FREEC               BUFFER NUMBER
         SRL   RD,1                     DIVIDE BY 2
         TM    W1FREEC+1,X'01'          IS BUFFER NUMBER ODD
         BZ    SIS05A3                  NO
         LA    RD,1(RD)                 IF ODD, ADD 1            A35340
SIS05A3  STC   RD,W1ICNOT
         CLC   W1ICNOT,DCBHIRPD         FOR FIXED ICNOT MUST NOT
         BNH   SIS05A4                  EXCEED HI R OF PRIME DATA
         MVC   W1ICNOT,DCBHIRPD         SET ICNOT
SIS05A4  EQU   *                                                 A35340
*   LIMIT NUMBER OF BUFFERS TO STOP OVERSIZED CHANNEL PROGRAMS.
         CLI   W1ICNOT,THIRTY           TOO MANY BUFFERS POSS
         BNH   NOTOOBIG                 NO - O.K.
         MVI   W1ICNOT,THIRTY           YES - SET TOO LIMIT
NOTOOBIG EQU   *                        *
         EJECT
*
*   DETERMINE IF ANY OF THE EXTENTS ARE ON AN RPS DEVICE, IF SO
*   LOAD A START I/O MODULE FOR RPS.  THE SIO MODULE MUST ALSO BE
*   IF IN VIRTUAL ENVIRONMENT FOR SPLITTING SELF-MODIFYING CHANNEL
*   PROGRAMS (WHETHER OR NOT ANY RPS DEVICES USED).
*
         L     RA,DEBAPPAD              DEB APPENDAGE ADDRESS   YM02867
         USING DEBAVT,RA                DEB AVT ADDRESSABILITY  YM02867
*
         TM   DEBRPSID,RPS              RPS DEVICE PRESENT       S20201
         BNZ  LOADSIO                   YES - LOAD SIO MODULE  @ZA12254
*                                       NO - TEST FOR VIRTUAL ENV.
         TM    DEBSIOA,VIRTUAL          IS IT VIRTUAL ENVIRON.   Y02072
         BZ    NORPSDEV                 BRANCH IF REAL           Y02072
*
LOADSIO  L     RTIOT,CVTPTR             ADDRESS OF CVT          YM01159
         USING CVT,RTIOT                CVT ADDRESSABILITY      YM01159
         L     RTIOT,CVTLINK            ADDRESS OF LINKLIB DCB  YM01159
         DROP  RTIOT                    END CVT ADDRESSABILITY  YM01159
*
*   LOAD THE START I/O MODULE IGG019HA.
*
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         MVC   DXCCW6,IGG019            MAKE SIO MODULE NAME     Y02072
         LOAD  EPLOC=DXCCW6,DCB=(RTIOT)                          Y02072
*
         L     RB,DXCCW5                PTR TO NEXT SPOT IN DEB  Y02072
         TM    DEBSIOA,VIRTUAL          DOES VIRTUAL = REAL
         BO    NOTREAL1                 NO - VIRTUAL
         ST    RE,DEBSIOA               SAVE SIO ADDRESS IN AVT
         B     NOTVIRT1                 DONE
NOTREAL1 EQU   *                                                 Y02072
         ST    RE,DEBSIOA               SAVE SIO ADDRESS IN AVT  Y02072
         OI    DEBSIOA,VIRTUAL          RESET VIRTUAL INDICATOR  Y02072
         L     RA,DEBEXPTR              ADDRESSABILITY ON ISAM SECTION
         USING DEBEXT,RA                *
         ST    RE,DEBRPSS               SAVE RPS SIO PTR
NOTVIRT1 EQU   *                        *
*
*   PLACE ID FOR SIO MODULE ON THE SUBROUTINE NAME LIST
*
         MVC   0(L'RPSSIOID,RB),RPSSIOID MODULE ID TO DEB        Y02072
*
* SET UP THE SIO PREFIXES.
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11  SET USERS KEY       Y02072
*
*    TEST IF RPS DEVICE USED, IF NOT SKIP RPS INITIALIZTAION CODE.
*
         TM    DEBRPSID,RPS             RPS DEVICE PRESENT       Y02072
         BZ    NORPSDEV                 BR IF NO RPS DEVICES     Y02072
*
         MVC   W1ICPEXT(48),SIOCCWS     MOVE IN SKELETON         M5092
         LA    RA,W1ISECT               PT TO SECTOR             S20201
         A     RA,W1ICPEXT              AD OP CODE               S20201
         ST    RA,W1ICPEXT              INPUT EXTENSION          S20201
         LA    RA,K1(RA)                PT TO SECTOR             S20201
         ST    RA,W1OCPEXT              OUTPUT EXTENSION         S20201
         MVI   W1OCPEXT,SETSECT         OPCODE = SETSECTOR       S20201
         LA    RA,W1ISECT               READ SECTOR OF OVERFLOW  M5092
         A     RA,W1RDSECT              * RECORDS FOR PUTX.      M5092
         ST    RA,W1RDSECT              *                        M5092
*   FIND MINIMUM OVERFLOW LENGTH AND RECORD LENGTH
         LH    RB,DCBBLKSI              BLOCKSIZE FIXED          M5092
         LH    RA,DCBLRECL              RECORD LENGTH            M5092
FIXED    EQU   X'80'                    FIXED LENGTH SW          M5092
         SR    RC,RC                                             M5092
         IC    RC,DCBKEYLE              KEY LENGTH               M5092
         TM    DCBRECFM,FIXED           FLR                      M5092
         BO    RPS001                   LENGTH O.K.              M5092
         LA    RA,4(RC)                 4+KEYLENGTH              M5092
         AH    RA,DCBRKP                +RKP                     M5092
         SH    RB,DCBLRECL              BLKSIZE-LRECL            M5092
         CR    RA,RB                    BLKSI-LRECL BIGGER       M5092
         BNH   RPS001                   YES - USE IT             M5092
         LR    RB,RA                    NO - USE 4+KL+RKP        M5092
RPS001   EQU   *                        SAVE LENGTHS             M5092
         LA    RA,10(RA)                ALLOW FOR OVERFLOW CHAIN M5092
         STH   RA,W1OVLEN               MINIMUM OVERFLOW LENGTH  M5092
         AR    RB,RC                    LENGTH EACH PRIME        M5092
         STH   RB,W1RECLEN              SAVE IT                  M5092
*   FIND LIMITS FOR ESTIMATING OVERFLOW SECTORS.
         IC    RC,DCBDEVT               FIND LIMITS FOR CYL OVFL M5092
         BAL   RF,RPS002                LINK TO SUBRTN           M5092
         MVC   W1RPSC1(2),W1RPSI1       SAVE LIMITS              M5092
*
         IC    RC,DCBOVDEV              FIND LIMITS FOR IND OVFL M5092
*
RPS002   EQU   *                        ROUTINE TO CALC LIMITS   M5092
         L     RB,CVTPTR                CVT ADDRESS              M5092
         L     RB,DEVTBL(RB)            PTR TO DEV TABLE         M5092
DEVTBL   EQU   64                       DEV TABLE                M5092
         SR    RA,RA                                             M5092
         IC    RA,0(RC,RB)              GET DISPLACEMENT         M5092
         LA    RA,0(RB,RA)              DEV TBL ADDRESS          M5092
         LH    RG,W1OVLEN               OVERFLOW LENGTH          M5092
         LH    RB,TRKL(RA)              TRACK LENGTH             M5092
TRKL     EQU   4                        DISP TO TRK LEN          M5092
         SR    RC,RC                                             M5092
         IC    RC,DCBKEYLE              KEY LENGTH               M5092
         AR    RG,RC                    ENTRY + KEYLEN           M5092
         IC    RC,6(RA)                 OVERHEAD NONZEUS         M5092
         TM    9(RA),X'08'              ZEUS                     M5092
         BZ    RPS003                   NO - OVERHEAD O.K.       M5092
         LH    RC,6(RA)                 OVERHEAD - ZEUS          M5092
RPS003   EQU   *                        *                        M5092
         AR    RG,RC                    TOTAL LENGTH RECORD      M5092
         SRDL  RB,33                    TRK/2                    M5092
         DR    RB,RG                    TRK/2/LENGTH             M5092
         STC   RC,W1RPSI2               SAVE LIMIT               M5092
         IC    RC,DCBKEYLE              KEY LEN                  M5092
         LH    RG,DCBLRECL              RECORD LENGTH MINUS      M5092
         SR    RG,RC                    MIN RECORD LENGTH EQUALS M5092
         SH    RG,DCBRKP                DEVIATION = DELTA        M5092
         MVI   W1RPSI1,X'FF'            SET IN CASE DEV=0        M5092
         LTR   RG,RG                    DEVIATION=0              M5092
         BZ    RPS0031                  YES THEN LIMIT=MAX       M5092
         LH    RB,TRKL(RA)              TRK LENGTH/2             M5092
         SRDL  RB,33                    *                        M5092
         DR    RB,RG                    TRK / DELTA / 2          M5092
         CH    RC,HEXFFHW               IS R TO HIGH             M1825
         BNL   RPS0031                  YES - THEN X'FF' IS GOOD M1825
         STC   RC,W1RPSI1               LOWER LIMIT              M5092
RPS0031  EQU   *                        *                        M5092
         SR    RC,RC                    CLEAR WORK REG           M1825
         BALR  RF,RF                    DO INDEPENDANT IF NOT    M5092
*                                       DONE ALREADY
*   CALCULATE RUNNING TRACK TOTAL TO FIRST SHARED
         IC    RC,DCBKEYLE              KEY LENGTH               M5092
         LA    RG,10(RC)                +10 = SHARED LENGTH      M5092
         IC    RC,DCBFIRSH+2            S-1                      M5092
         BCTR  RC,0                     *                        M5092
         MR    RB,RG                    (S-1)* LENGTH            M5092
         STH   RC,W1FSTSH               TOTAL TO FIRST SHARED    M5092
NORPSDEV EQU   *                        *                        M5092
         EJECT
*
*   FINISH INITIALIZING THE WORK AREA
*
SIS05J4  OI    W1ECBI,X'40'             SET ECB COMPLETE BITS ON A35340
         OI    W1ECBO,X'40'
         OI    W1IF1,X'42'              SET CC BIT IN FLAG 1 AND ISAM
         OI    W1OF1,X'C2'               O/P SET ON DC BIT          BIT
         LA    RC,W1ECBI                SET ECB POINTERS
         ST    RC,W1IECBAD
         LA    RC,W1ECBO
         ST    RC,W1OECBAD
         L     RC,DXUDCBAD              SET DCB POINTERS IN      Y02072
         ST    RC,W1IDCBAD              INPUT IOB AND            Y02072
         ST    RC,W1ODCBAD              OUTPUT IOB               Y02072
         LH    RC,DCBLRECL              INITIALIZE W1CURLEN
         STH   RC,W1CURLEN
         SPACE 4
         L     RC,DXCCW7                ASYCHRONOUS ROUTINE      Y02072
         CIRB  EP=(RC),KEY=PP,MODE=SUPR,STAB=RE   CREATE REUSABLE IRB
         SPACE 2
*        REFRESH USERS DCB                                       Y02072
         SPACE 2
         L     RPARC,DXUDCBAD           USERS DCB ADDR           Y02072
         LH    RWTG,DXUDCBML            NO OF BYTES TO MOVE      Y02072
         BCTR  RWTG,0                   LENGTH MINUS 1 FOR MOVE  Y02072
         EX    RWTG,COPYDCB             UPDATE USERS DCB         Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         L     RG,DCBDEBAD             SET DEB BASE
         IC    RD,DEBOFLGS                   PUT IRB ADDRESS IN DEB
         ST    RF,DEBIRBAD
         STC   RD,DEBOFLGS
         SPACE 5
*                   FINISHED WITH THIS DCB.
*
*                   SET NAME PORTION OF WTG ENTRY TO ZERO
*                                       SINCE NEXT LOAD IS OPEN LOAD 7
         LR    RG,RPAR                  GET OPEN WA ADDR         Y02072
         DROP  RPAR                     END OPEN WA ADDR         Y02072
         USING FORCORE,RG               EST OPEN WA ADDR         Y02072
         LM    RPAR,RWTGC,DXCCW1        RESTORE REGISTERS 5-8    Y02072
SIS05END XC    0(5,RWTGC),0(RWTGC)                               A35340
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURRENT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURRENT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH = BEGINNING OF THIS MOD
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BNE   RELOOP                   BRANCH NOT =, NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BNE   TCTLRTN                  BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         LA    RJ,DXCCW12               WA ADDRESS               Y01021
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
         SPACE 2
*        UPDATE USERS DCB                                        Y02072
         SPACE 1
COPYDCB  MVC   DCBKEYLE-IHADCB(0,RPARC),DCBKEYLE  UPDATE         Y02072
         SPACE 2
*        CONSTANTS
         SPACE 1
HEXFFHW  DC    H'255'                   MAXIMUM RECORD VALUE     M1825
THISLOAD DC    C'24'                                              
OPNLOD7  DC    C'0S'
*   START I/O MODULE IDENTIFIER FOR LOAD INSTRUCTION
IGG019   DC    C'IGG019'                MODULE ID FOR RPS SIO    Y02072
RPSSIOID DC    C'HA'                    MODULE IGG019HA          Y02072
*   SIO PREFIX CCWS
SIOCCWS  CCW   SETSECT,0,CC+SILI,1      OUTPUT EXTENSION         S20201
         CCW   TIC,0,0,0                TIC TO CP                S20201
         CCW   SETSECT,0,CC+SILI,1      OUTPUT EXTENSION         S20201
         CCW   TIC,0,0,0                TIC TO CP                S20201
         CCW   RDCNT,0,CC+SKIP+SILI,8   POSITION TO NEXT RECORD  M5092
         CCW   RDSECT,0,0,1             READ SECTOR OF NEXT REC  M5092
         SPACE 3
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
SISDEB   IGGDEBD
         EJECT
SISWORK1 IGGSCAN                SCAN MODE WORK AREA              A35340
         EJECT
         DCBD  DSORG=IS
         EJECT
FORCORE  DSECT
         IECDSECT
         SPACE 4
         CVT   DSECT=YES                                        YM01159
         EJECT
*
*   FORMAT 2 DATA SET CONTROL BLOCK ( DSCB )
*
DSCBFRM2 DSECT
         DS    0D                                                A35340
DSC2KEY  DS    CL1                                               A35340
D2FTMI2  DS    CL7                      ADR OF 2ND LEVEL MASTER  A35340
*                                       INDEX                    A35340
D2LEMI2  DS    CL5                      ADR OF LAST 2ND LEVEL    A35340
*                                       MASTER                   A35340
*                                                      INDEX ENTRY
D2FTMI3  DS    CL7                      ADR OF 3RD LEVEL MASTER  A35340
*                                       INDEX                    A35340
D2LEMI3  DS    CL5                      ADR OF LAST 3RD LEVEL    A35340
*                                       MASTER                   A35340
*                                                      INDEX ENTRY
         DS    CL19                     SPARE                    A35340
* DATA PORTION
D2FMTID  DS    CL1                      FORMAT ID                A35340
D2NLEV   DS    CL1                      NUMBER OF LEVELS OF      A35340
*                                       MASTER IND               A35340
D2NTM    DS    CL1                      HIGH LEVEL INDX          A35340
*                                       DEVELOPMNT IND           A35340
D2FIRSH  DS    CL3                      FIRST DATA RECORD        A35340
D2LDT    DS    CL2                      LAST DATA RECORD IN      A35340
*                                       CYLINDER                 A35340
D2CYLOF  DS    CL1                      NUMBER OF TRACKS FOR CYL A35340
*                                       OVFLO                    A35340
D2HIRCM  DS    CL1                      HIGHEST R ON INDEX       A35340
*                                       ENTRIES                  A35340
D2HIRPD  DS    CL1                      HIGHEST R ON PRIME       A35340
D2HIROV  DS    CL1                      HIGHEST R ON OVFLO       A35340
D2HIRSH  DS    CL1                      R OF LAST DATA RCD ON    A35340
*                                       SHARED                   A35340
         DS    CL2                      RESERVED                 A35340
D2TDC    DS    CL2                      TAG DELETION COUNT       A35340
D2RORG3  DS    CL3                      NON-FIRST OVFLO REF.     A35340
*                                       COUNT                    A35340
D2NCRHI  DS    CL2                      NUM. BYTES FOR HIGHEST   S20201
*                                       LEV IND                  S20201
D2NTHI   DS    CL1                      NUM. TRKS FOR HIGHEST    S20201
*                                       LEV INDX                 S20201
D2NREC   DS    CL4                      PRIME RECORD COUNT       S20201
D2ST     DS    CL1                      STATUS INDICATORS        S20201
D2FTCI   DS    CL7                      ADR OF CYLINDER INDEX    S20201
D2FTMI1  DS    CL7                      ADR OF LOWEST LEV MASTER S20201
*                                       INDEX                    S20201
D2FTHI   DS    CL7                      ADR OF HIGHEST LEV       S20201
*                                       MASTER INDE              S20201
D2LPDA   DS    CL8                      LAST PRIME DATA RECORD   S20201
*                                       ADR                      S20201
D2LETI   DS    CL5                      LAST TRACK INDEX ENTRY   S20201
*                                       ADR                      S20201
D2LECI   DS    CL5                      LAST CYL. INDEX ENTRY    S20201
*                                       ADR.                     S20201
D2       DS    CL5                      LAST MSTR INDEX ENTRY    S20201
*                                       ADR.                     S20201
D2LIOV   DS    CL8                      LAST INDP. OVFLO RCD     S20201
*                                       ADR.                     S20201
D2NBOV   DS    CL2                      BYTES LEFT ON OVFLO      S20201
*                                       TRACK                    S20201
D2RORG2  DS    CL2                      NUM. OF INDP. OVFLO      S20201
*                                       TRACKS                   S20201
D2NOREC  DS    CL2                      OVERFLO RECORD COUNT     S20201
D2RORG1  DS    CL2                      CYL. OVERFLO AREA COUNT  S20201
         DS    CL3                      SPARE                    S20201
D2NEXT   DS    CL5                      PTS TO NEXT DSCB         S20201
D2CORE   DS    CL4                      CHAINED CORE ADR         S20201
         END
D2NCRHI  DS    CL2                      NUM. BYTES FOR HIGHEST   A35340
*                                       LEV IND                  A35340
D2NTHI   DS    CL1                      NUM. TRKS FOR HIGHEST    A35340
*                                       LEV INDX                 A35340
D2NREC   DS    CL4                      PRIME RECORD COUNT       A35340
D2ST     DS    CL1                      STATUS INDICATORS        A35340
D2FTCI   DS    CL7                      ADR OF CYLINDER INDEX    A35340
D2FTMI1  DS    CL7                      ADR OF LOWEST LEV MASTER A35340
*                                       INDEX                    A35340
D2FTHI   DS    CL7                      ADR OF HIGHEST LEV       A35340
*                                       MASTER INDE              A35340
D2LPDA   DS    CL8                      LAST PRIME DATA RECORD   A35340
*                                       ADR                      A35340
D2LETI   DS    CL5                      LAST TRACK INDEX ENTRY   A35340
*                                       ADR                      A35340
D2LECI   DS    CL5                      LAST CYL. INDEX ENTRY    A35340
*                                       ADR.                     A35340
D2       DS    CL5                      LAST MSTR INDEX ENTRY    A35340
*                                       ADR.                     A35340
D2LIOV   DS    CL8                      LAST INDP. OVFLO RCD     A35340
*                                       ADR.                     A35340
D2NBOV   DS    CL2                      BYTES LEFT ON OVFLO      A35340
*                                       TRACK                    A35340
D2RORG2  DS    CL2                      NUM. OF INDP. OVFLO      A35340
*                                       TRACKS                   A35340
D2NOREC  DS    CL2                      OVERFLO RECORD COUNT     A35340
D2RORG1  DS    CL2                      CYL. OVERFLO AREA COUNT  A35340
         DS    CL3                      SPARE                    A35340
D2NEXT   DS    CL5                      PTS TO NEXT DSCB         A35340
D2CORE   DS    CL4                      CHAINED CORE ADR         A35340
         END
