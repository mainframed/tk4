         TITLE 'IGG0202K - QISAM LOAD MODE CLOSE- CALCULATE PADDING'
IGG0202K CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0202K                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM CLOSE, LOAD MODE                            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = MOVE CP91 SKELETON FROM THIS MODULE TO OVERLAY CP19, AND *
*            INITIALIZE CP91 TO FILL UNUSED INDEX ENTRIES WITH        *
*            INACTIVE AND DUMMY ENTRIES.  ISSUE WAIT FOR EOF EXCP     *
*            SCHEDULED BY IGG0202J.                                   *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = CLOSE EXECUTOR                                        *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 894 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0202K                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL IN STORAGE PROTECT KEY 5 AND          *
*              PRIVILEGED STATE FROM ISAM CLOSE EXECUTOR IGG0202J.    *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM CLOSE EXECUTOR IGG0202L IN STORAGE       *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - CLOSE WORK AREA                           *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                                                                     *
*    CONTROL-BLOCKS = DCB, DCB COPY, AND IOB                          *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, WAIT, AND XCTL                                    *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
********************
* DCB REFERENCE    *
********************
         DCBD  DSORG=(IS)
         EJECT
***********************************************************************
* ISLCOMON REFERENCE     C(DCBWKPT1)=A(ISLCOMMON)                     *
***********************************************************************
ISLCOMON IGGLOAD
         USING ISLCOMON,R12             ADDRESSABILITY           S20201
*
         EJECT
********************
* IOB REFERENCE    *
********************
*
IHAIOB   DSECT
         USING IHAIOB,R2
         DS    0D
IOBFLG1  DS    CL1                      FLAGS 1
IOBFLG2  DS    CL1                      FLAGS 2
         DS    CL1
IOBSENS  DS    CL1                      SENSE
IOBECBAD DS    A                        ECB POINTER
IOBCSWD  DS    CL8                      CHANNEL STATUS WORD
IOBSIO   DS    0CL1                     SIO CC
IOBCPSAD DS    A                        CHANNEL PROGRAM START ADR
IOBWT    DS    0CL1                     WEIGHT
IOBDCBAD DS    A                        DCB POINTER
IOBCPRAD DS    A                        CHANNEL PROGRAM RESTART ADR
IOBBCTI  DS    CL2                      BLK CTR INCR
IOBECT   DS    CL2                      ERROR CTR
IOBDADAD DS    CL8                      DIR ACESS DEV ADR MBBCCHHR
*
********************
* IXLT REFERENCE   *
********************
*
IXLT     DSECT
         USING IXLT,R7
         DS    0D
IXLTIND  DS    CL1                      INDICATOR                  LEV1
IXLBEG   DS    CL8                      BEGINING COUNT MBBCCHHR
IXLSTP   DS    CL8                      STEPPING COUNT MBBCCHHR
IXLEND   DS    CL8                      ENDING COUNT   MBBCCHHR
         DS    CL1
         DS    CL26                                                LEV2
         DS    CL26                                                LEV3
         DS    CL26                                                LEV4
         EJECT
FORCORE  DSECT                                                   Y02072
         IECDSECT                                                Y02072
         EJECT
IGG0202K CSECT
ISLC000  BALR  R15,0
         USING *,R15
BASETAG  L     RDCB,0(RPARC)            DCB COPY ADDRESS         Y02072
         USING IHADCB,RDCB              DCB ADDRESSABILITY       Y02072
         USING FORCORE,RCORE            WA ADDRESSABILITY        Y02072
         L     RCORE,4(RWTGC)
         L     R12,DCBWKPT1
         LR    RBASE,R15
         STM   RSAVE,RJ,DXCCW1          SAVE REGISTERS 0-15      Y02072
         L     RDCB,DXUDCBAD            USERS DCB ADDRESS        Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=10  CHANGE TO USER KEY  Y02072
*
         DROP  RCORE                    END CLOSE WA USING       Y02072
         LR    RWKAREA,RCORE            SAVE CLOSE WA ADDRESS    Y02072
         USING FORCORE,RWKAREA          OPEN WA ADDRESSABILITY   Y02072
         L     R10,DCBWKPT6
*
* EQUATE SYMBOLIC REGISTERS
*
RSAVE    EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R9       EQU   8
R10      EQU   10
RWKAREA  EQU   11                       CLOSE WORK AREA ADDRESS  Y02072
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   9
RDCB     EQU   1                        DCB BASE REGISTER        Y02072
RBASE    EQU   3
RCORE    EQU   4
RPAR     EQU   5
RWTG     EQU   6
RPARC    EQU   7
RWTGC    EQU   8
RJ       EQU   15
K0       EQU   0                        CONSTANT                 S20201
L8       EQU   8                        CONSTANT                 S20201
K1       EQU   1                        CONSTANT                 S20201
K8       EQU   8                        CONSTANT                 S20201
K35      EQU   35                       CONSTANT                 S20201
K10      EQU   10                       CONSTANT                 S20201
K9       EQU   9                        CONSTANT                 S20201
K16      EQU   16                       CONSTANT                 S20201
K6       EQU   6                        CONSTANT                 S20201
L1       EQU   1                        LENGTH                   S20201
K7       EQU   7                        CONSTANT                 S20201
FIXED    EQU   X'80'                    FLR INDICATOR            S20201
EOT      EQU   X'01'                    EOT INDICATOR            S20201
*
         EJECT
***********************************************************************
* CHART C0 - PAD TRK IX HSK                                           *
***********************************************************************
*  TEST FOR END OF TRACK AND SET DCBST EOT SW IF NECESSARY.
***********************************************************************
         TM    DCBRECFM,FIXED           FIXED LENGTH RECORDS     S20201
         BZ    NOTEOT                   NO - VLR - EOT           S20201
*                                       IRREVELANT               S20201
* ASSUME ON NOT SHARED TRACK
         LA    R3,DCBHIRPD              HIGH R NON-SHARED        S20201
         CLC   DCBLPDA+K6(L1),DCBFIRSH+K1 SHARED TRACK           S20201
         BNE   NOTSHRD                  NO - ASSUMPTION CORRECT  S20201
         CLI   DCBHIRSH,0               IS THERE A SHARED TRACK  S20201
         BE    NOTSHRD                  NO - ASSUMPTION CORRECT  Y02072
*                                       YES-MUST BE SHARED TRACK
         LA    R3,DCBHIRSH              HIGH R SHARED            S20201
NOTSHRD  EQU   *                        CHECK FOR EOT            S20201
         CLC   DCBLPDA+K7(L1),0(R3)     LAST ON TRACK            S20201
         BNE   NOTEOT                   NO - NOT EOT             S20201
         OI    DCBST,EOT                YES - MARK EOT           S20201
NOTEOT   EQU   *                        *                        S20201
         LA    R2,ISLIOBC               C(R2)=A(IOBC)            S20201
*
*        SET UP TO PAD INDEXES FROM AREA Y
*
         L     R9,0(R10)                C(R9)=A(AREA Y)
         SR    R3,R3                    CLEAR REGISTER            23446
         IC    R3,DCBKEYLE              C(R3) = KEY LENGTH        23446
         LA    R5,62(R9)                POINT TO KEY OF 1'S       23446
         AR    R5,R3                    C(R5)=A(BUF 1+8 +IL)
         XC    K0(L8,R5),K0(R5)         MOVE ZEROS TO BUF 1+8    S20201
*                                       +IL                      S20201
*
* F=30 FOR INACTIVE IX ENTRY,LEVEL 0, P=07
*
         MVI   8(R5),X'30'              F=30
         MVI   9(R5),X'07'              P=07
*
*
* MOVE CP91 TO A(CP19), MAY OVERLAY CP20
*
         LA    R9,ISLAREAZ              C(R9)=A(AREA Z)
         L     R10,16(R10)              C(R10)=A(CP19 OR CP91 DEST)
         MVC   K0(CP91LEN,R10),CM1      MOVE CP91 FROM SKLTN     S20201
         USING CM1,R10                  ADDRESSABILITY ON CP91   M3602
         ST    R5,CM27+4                STORE ADDRESS-MBBCCHHRFP M3602
         DROP  R10                                               M3602
*
* TEST FOR CYLINDER OVERFLOW AREA
*
         TM    DCBOPTCD,X'08'           TEST OPTCD BIT-4 FOR CYL OVFL
         BC    1,ISLC002                B IF ON
*
* NO CYLINDER OVERFLOW AREA, SET CM1 TO TIC TO CM40
*
         MVC   K0(L8,R10),CM27          MOVE CM27 TO CM1         S20201
         LA    R4,CM40-CM1(R10)         ADDR OF CM40             S20201
         IC    R5,0(R10)
         ST    R4,0(R10)                     STORE ADR CM40      S20201
         STC   R5,0(R10)
         B     ISLC003
*
* CYLINDER OVERFLOW AREA, SET UP HHRYYT IN AREA Z
*
ISLC002  LH    R3,DCBLDT                C(R3)=LDT
         LA    R3,K1(R3)                C(R3)=LDT+1              S20201
         SLL   R3,16                    C(R3)=HH00
         ST    R3,0(R9)                 C(Z)=HH00 = HHRY
         STC   R3,4(R9)                 C(Z)=HH000=HHRYY
         MVC   5(1,R9),DCBCYLOF         C(Z)=HH000N = HHRYYT
         MVC   0(1,R9),DCBLPDA+5        MOVE BIN NUMBER
*
* INITIALIZE CP91 AND AREA Z
*
         LA    R4,35(R2)                CM1
         IC    R5,0(R10)
         ST    R4,0(R10)                     STORE ADR IOBC+35
         STC   R5,0(R10)
         LA    R4,0(R10)                CM2
         IC    R5,8(R10)
         ST    R4,8(R10)                     STORE ADR CM1
         STC   R5,8(R10)
         IC    R5,16(R10)               CM3
         ST    R9,16(R10)                    STORE ADR AREA Z
         STC   R5,16(R10)
*
         LA    R4,33(R2)                CM4
         IC    R5,24(R10)
         ST    R4,24(R10)                    STORE ADR IOBC+33
         STC   R5,24(R10)
ISLC003  EQU   *                                                 S20201
         LA    R4,ISLRPSSS+K1           SET SECTOR               S20201
         O     R4,CM40-CM1(R10)         CM40                     S20201
         ST    R4,CM40-CM1(R10)         *                        S20201
         LA    R10,K8(R10)              CORRECT BASE REG         S20201
*
         LA    R4,K35(R2)               CM5                      S20201
         IC    R5,32(R10)
         ST    R4,32(R10)                    STORE ADR IOBC+35
         STC   R5,32(R10)
         LA    R4,32(R10)               CM6
         IC    R5,40(R10)
         ST    R4,40(R10)                    STORE ADR CM5
         STC   R5,40(R10)
*
*                                       SET UP R6 WITH CC,IL+10
         SR    R6,R6
         IC    R6,DCBKEYLE              C(R6)=IL
         LA    R6,K10(R6)               C(R6)=IL+10              S20201
         IC    R5,60(R10)               CM8  SAVE FLAG
         ST    R6,60(R10)                    STORE IL+10 AT CM8+4
         STC   R5,60(R10)                    RESTORE FLAG
         L     R6,60(R10)               C(R6)=CC,IL+10
*
         L     R7,DCBWKPT6              PICK UP ADDRESS OF        23446
         L     R7,0(R7)                 AREA Y                    23446
         LA    R7,62(R7)                POINT TO KEY OF 1'S       23446
         ST    R7,56(R10)               CM8  STORE ADR BUF 1 +8 AT CM8
*
*                                       SET UP R5 WITH OP,Z+6
         LA    R4,6(R9)                 CM7  C(R4)=Z+6
         IC    R5,48(R10)
         ST    R4,48(R10)                    STORE ADR Z+6 AT CM7
         STC   R5,48(R10)
         L     R5,48(R10)               C(R5)=OP,Z+6
*
         MVC   224(8,R10),48(R10)       CM29 = CM7
*
         MVC   216(8,R10),56(R10)       CM28 = CM8
         MVI   216(R10),X'0D'                  OP = 0D (WR KD)
         MVI   220(R10),X'00'                  CC = 00
*
*                                       SET UP R4 WITH A(1ST KDD IN Z)
         LA    R4,11(R9)                C(R4)=A(KDD)
         MVC   0(3,R4),ISLNCNT+5        MOVE 1ST KDD FROM NCNT
*
*                                       INITIALIZE CM9 - CM26 AND Z
         LA    R3,K9                    C(R3)= CNTR = 9          S20201
ISLC010  A     R4,ISL8                  STEP ALONG Z FOR KDD
         A     R5,ISL8                  STEP ALONG Z FOR A(COUNT)
         LA    R10,K16(R10)             STEP ALONG CP91          S20201
         MVC   0(3,R4),ISLNCNT+5        MOVE KDD TO Z
         ST    R5,48(R10)               STORE OP,A(Z)
         ST    R7,56(R10)               STORE A(BUF 1 +8)
         ST    R6,60(R10)               STORE CC,IL+10
         BCT   R3,ISLC010               LOOP
         MVC   ISLF9WK1(3),DCBFIRSH     SAVE FIRSH
         MVC   ISLF9WK1+3(1),DCBOPTCD   SAVE OPTCD
*
* AWAIT COMPLETION OF IOBB (EOF)
*
         LA    R2,ISLIOBB               C(R2)=A(IOBB)
         L     R4,IOBECBAD              C(R4)=A(ECB)
         TM    0(R4),X'40'              TEST ECB BIT 1 (C-BIT)
         BC    1,ISLC020                B IF ON, I/O COMPLETE-DONT WAIT
         LA    R13,ISLVRSAV             C(R13)=A(VRSAV)
         LR    R3,R1                    SAVE R1
         LR    R1,R4                    C(R1)=A(ECB)
         WAIT  ECB=(1)                  WAIT
         LR    R1,R3                    RESTORE R1
*
*
* EXIT TO C1
*
ISLC020  LM    RSAVE,RJ,DXCCW1          RESTORE  REGISTERS 0-15  Y02072
         DROP  RWKAREA                  END CLOSE WA USING       Y02072
         USING FORCORE,RCORE            CLOSE WA ADDRESSABILITY  Y02072
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         USING BASETAG,RBASE
         MVC   0(L'LOAD2L,RWTGC),LOAD2L NEXT LOAD ID - IGG0202L  Y02072
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH=BEGINNING OF THIS MODULE
         CLC   0(1,RWTGC),ENDCLOSE      TEST FOR END OF WTG TABLE
         BC    7,RELOOP                 BRANCH=NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BC    7,TCTLRTN                BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
*
         LA    RJ,DXCCW12               PARAM ADDRESS            Y02072
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO IGG0202L   Y02072
         EJECT
* CONSTANTS
*
ISL8     DC    F'0008'
KEY0     EQU   ISL8                     STORAGE PROTECT KEY ZERO Y02072
*
ISLC001A MVC   0(1,R5),62(R9)           MOVE KEY OF /S TO BE EXECUTED
*
* CP91 - PAD OUT TRACK  AND HIGH LEVEL INDEX AREAS
**************************************************************
*
CM1      CCW   X'31',0,X'40',5          SRCH ID EQ          CYL OVFL
CM2      CCW   X'08',0,X'00',0          TIC
CM3      CCW   X'05',0,X'60',6          WR D      Z
*
CM4      CCW   X'1B',0,X'40',6          SEEK HH
*
*
CM40     CCW   X'03',0,X'60',1          NOP/SET SECTOR           S20201
CM5      CCW   X'31',0,X'40',5          SRCH ID EQ          PREFORMAT
CM6      CCW   X'08',0,X'00',0
*
CM7      CCW   X'1D',0,X'80',8          WR C      Z+6
CM8      CCW   X'00',0,X'40',0             KD
CM9      CCW   X'1D',0,X'80',8          WR C      Z+14
CM10     CCW   X'00',0,X'40',0             KD
CM11     CCW   X'1D',0,X'80',8          WR C      Z+22
CM12     CCW   X'00',0,X'40',0             KD
CM13     CCW   X'1D',0,X'80',8          WR C      Z+30
CM14     CCW   X'00',0,X'40',0             KD
CM15     CCW   X'1D',0,X'80',8          WR C      Z+38
CM16     CCW   X'00',0,X'40',0             KD
CM17     CCW   X'1D',0,X'80',8          WR C      Z+46
CM18     CCW   X'00',0,X'40',0             KD
CM19     CCW   X'1D',0,X'80',8          WR C      Z+54
CM20     CCW   X'00',0,X'40',0             KD
CM21     CCW   X'1D',0,X'80',8          WR C      Z+62
CM22     CCW   X'00',0,X'40',0             KD
CM23     CCW   X'1D',0,X'80',8          WR C      Z+70
CM24     CCW   X'00',0,X'40',0             KD
CM25     CCW   X'1D',0,X'80',8          WR C      Z+78
CM26     CCW   X'00',0,X'00',0             KD
*
CM27     CCW   X'08',0,X'00',0          TIC
*
CM28     CCW   X'0D',0,X'00',0          WR KD
CM29     CCW   X'1D',0,X'80',8          WR C      Z+6
*
CP91LEN  EQU   *-CM1                    LENGTH CP 91             S20201
         EJECT
CONSTIGG DC    C'IGG020'
THISLOAD DC    C'2K'
ENDCLOSE DC    C'00'
*
LOAD2L   DC    C'2L'                    ID OF MODULE IGG0202L    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
         END
