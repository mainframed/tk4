 TITLE 'IGG01929 - SECOND SCAN MODE OPEN MODULE'                 A35340
IGG01929 CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG01929                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, SCAN MODE                             *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = INITIALIZE SCAN MODE CHANNEL PROGRAMS:                   *
*            CP24 - READ TRACK INDEX ENTRIES INTO W1WAREA             *
*            CP22 (ONE FOR EACH BUFFER) - READ DATA RECORD            *
*            CP25 - READ TRACK INDEX ENTRIES FOR SETL BY ID           *
*            CP23 - SEARCH HI-LEVEL INDEX, TRACK INDEX, OR DATA TRACK *
*                   FOR SETL BY ID                                    *
*            CP26 - EXTENSION OF CP23 TO READ OVERFLOW CHAINS         *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 1008 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG01929                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG01928 IN   *
*              STORAGE PROTECT KEY 5 AND PRIVILEGED STATE.            *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG01924 IN STORAGE        *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGSCAN - ISAM SCAN MODE WORK AREA                  *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY AND DEB                                *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET AND XCTL                                           *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         USING IHADCB,RDCB
         USING SISWORK1,RUCB
         BALR  RBASE,0
         USING *,RBASE
*
RE       EQU   0
RF       EQU   1
RDCB     EQU   2
RBASE    EQU   3
RG       EQU   4
RPAR     EQU   5
RWTG     EQU   6
RPARC    EQU   7
RWTGC    EQU   8
RTIOT    EQU   9
RUCB     EQU   10
RA       EQU   11
RB       EQU   12
RC       EQU   13
RD       EQU   14
RJ       EQU   15
FIXED    EQU   X'80'                    RECFM - FIXED INDICATOR  S20201
L4       EQU   4                        CONSTANT                 S20201
K7       EQU   7                        CONSTANT                 S20201
K4       EQU   4                        CONSTANT                 S20201
         EJECT
*        INITIALIZE REGISTERS
SIS05A2  L     RDCB,0(RPARC)
         L     RD,K4(,RWTGC)            GET OPEN WA ADDR         Y02072
         USING FORCORE,RD               EST OPEN WA ADDR         Y02072
         SPACE 1
         USING SISDEB,RG
         MODESET  KEYADDR=DXUKEY,WORKREG=10  SET USERS KEY       Y02072
         SPACE 1
         L     RUCB,DCBWKPT1
         SPACE 2
*        INITIALIZE CP 24
         SPACE 2
SIS05A4  LA    RC,W1WAREA               THESE INSTRUCTIONS PUT
         A     RC,W1CN8                 INTO RELOCATED FORM
         ST    RC,W1CN8                 THE CCW'S OF CP24--THE
         LA    RC,W1WOVFL               CHANNEL PROGRAM WHICH
         A     RC,W1CN11                READ TRACK INDECES       A35340
         ST    RC,W1CN11                INTO THE 'W' AREA        A35340
         LA    RC,W1WDCXDM              CURRENT (MBBCCHHRFP)     A35340
         A     RC,W1CN10                                         A35340
         ST    RC,W1CN10                                         A35340
         NI    W1CN10+4,X'EF'                                    A35340
         TM    DCBRECFM,X'80'           TEST RECORD FORMAT       A35340
         BZ    SISVAR1                  VARIABLE - DATA          A35340
*                                       *    TRANSFERRED
         OI    W1CN10+4,X'10'           FIXED - SUPPRESS         A35340
*                                       TRANSFER                 A35340
SISVAR1  EQU   *                                                 A35340
         LA    RC,W1WCNXDM
         A     RC,W1CN12                COUNT OF NEXT ENTRY - W1WCNXDM
         ST    RC,W1CN12
         LA    RC,W1WDNXDM              DATA (MBBCCHHRFP) OF OVERFLOW
         A     RC,W1CN13                ENTRY FOR CURRENT TRACK
         ST    RC,W1CN13                W1WOVFL
         LA    RC,W1CN8                                          S20201
         A     RC,W1CN14A                                        S20201
         ST    RC,W1CN14A                                        S20201
         LA    RC,W1CN8                                          A35340
         L     RA,W1FR1ST               ADDRESSABILITY CP22      A35340
         USING SISCP22,RA                                        A35340
         LA    RC,CN6+1                 READ INTO W1WOVFL        A35340
         ST    RC,W1CN14                NORMAL OR DUMMY ENTRY
         LA    RC,CN1+1                 READ INTO W1WDNXDM       A35340
         ST    RC,W1CN15
         MVI   W1CN15,TIC               RESTORE COMMAND CODE     A35340
         LA    RC,W1CN8
         A     RC,W1CN9
         ST    RC,W1CN9
         L     RB,DCBBUFCB              BUFCB POINTS TO 1ST BUFFER
         SPACE 2
*                             ALL COPIES OF CP22 ARE IN WORKAREA
         SPACE
*                             NOW INITIALIZE THEM
         SPACE 2
         SPACE
         L     RB,0(RB)                 ADDR OF 1ST BUFFER
*              W1TEMPSA CONTAINS OFFSET OF DATA AREA FROM 1ST CHAR
*              OF BUFFER (G)
         SPACE 2
         LR    RC,RA                    THIS SECTION OF CODING
SIS05E30 A     RC,CN1             ADD REINITIALIZES ALL COPIES
         ST    RC,CN1                   OF CP22 B'S. THIS FORM IS USED
         LR    RC,RA                    FOR BLOCKED RECORDS OR
         A     RC,CN2                   UNBLOCKED WITH DATA ONLY.
         ST    RC,CN2                   IT RELOCATES ADDRESSES,
         ST    RB,CN7+4                 ASSIGNS A BUFFER TO
         A     RB,W1TEMPSA              EACH CHANNEL PROGRAM,
         ST    RB,CN7                   SETS THE COUNT FIELDS
         A     RB,CN4                   OF CN3 AND CN4 TO KEYLENGTH
         ST    RB,CN4                   AND BLOCKSIZE, RESPECTIVELY
         LH    RC,DCBBLKSI              LOOPING TILL EACH COPY
         STH   RC,CN4+6                 IS SET UP.
         IC    RC,DCBKEYLE
         STC   RC,CN3+7
         LA    RC,CN4
         ST    RC,CN3
         MVI   CN3,X'08'
         L     RB,CN7+4                 RESET RB FOR NEXT BUFFER
         L     RB,0(0,RB)
         SPACE 5
*                        CP IS SET UP
*                        IS IT THE LAST ONE
         SPACE 2
SIS05E32 C     RA,W1FRLAST
         BE    SIS05G2
         LR    RC,RA
         LA    RC,CP22LEN(RC)           PT TO NEXT CP22          A35340
         ST    RC,CN5
         MVI   CN5,X'08'                RESTORE COMMAND
         LR    RA,RC                    RESET RA FOR BASE OF NEXT CP
         B     SIS05E30                 LOOP TO SET UP NEXT COPY
SIS05G2  L     RA,DCBBUFCB              RESET BUFCB TO POINT TO NEXT
*                                       AVAILABLE BUFFER
*
         ST    RB,0(RA)
         SPACE 3
*                                       IS CP25 LOADED
SIS05G3  L     RB,W1CP25PT
         LTR   RB,RB                    IS CP25 LOADED           A35340
         BZ    SIS05H3                  NO - DOESN'T NEED TO     A35340
*                                       *    BE INITIALIZED
         SPACE
*********************************************************************
         SPACE
         USING SISCP25,RB
         SPACE
*********************************************************************
         SPACE
         L     RA,W1FR1ST               THIS SECTION PUTS INTO
         LA    RC,W1IMBBCC+3            RELOCATED FORMAT THE CCW'S
         A     RC,CN20                  OF CP25. IT SETS THE COUNT
         ST    RC,CN20                  FIELDS OF CN22 AND CN25 TO
         ST    RB,CN21                  KEY LENGTH AND PUTS VARIOUS
         MVI   CN21,X'08'               WORKAREA ADDRESSES WHERE
         MVC   CN22+1(3),CN7+5          REQUIRED.
         LR    RC,RB
         A     RC,CN23                   CP25 IS USED WHEN A SETL I(D)
         ST    RC,CN23                  MACRO IS SPECIFIED.
         LR    RC,RB                                             S20201
         A     RC,CN23A                                          S20201
         ST    RC,CN23A                                          S20201
         LR    RC,RB
         A     RC,CN24
         ST    RC,CN24
         MVC   CN25+1(3),CN7+5
         LR    RC,RB
         A     RC,CN26
         ST    RC,CN26
         LA    RC,W1WOVFL
         TM    DCBRECFM,X'80'           TEST RECORD FORMAT       A35340
         BO    SISFIX3                  FIXED - SUPPRESS         A35340
*                                       TRANSFER                 A35340
         NI    CN27+4,X'EF'             VARIABLE - TRANSFER      A35340
SISFIX3  EQU   *                                                 A35340
         A     RC,CN28                                           A35340
         ST    RC,CN28                                           A35340
         LA    RC,W1WDCXDM              CURRENT (MBBCCHHRFP)     A35340
         A     RC,CN27                                           A35340
         ST    RC,CN27                                           A35340
         LA    RC,W1WDNXDM                                       A35340
         ST    RC,CN30                                           A35340
         OI    CN30,X'06'                                        A35340
         LA    RC,W1WCNXDM
         A     RC,CN29
         ST    RC,CN29
         IC    RC,DCBKEYLE
         STC   RC,CN22+7
         STC   RC,CN25+7
         B     SIS05J4
         SPACE 5
SIS05H3  L     RB,W1CP23PT
         LTR   RB,RB                    IS CP23 LOADED           A35340
         BZ    SIS05J4                  NO - DON'T INITIALIZE IT A35340
*                                       YES - INITIALIZE IT
         SPACE
*********************************************************************
         SPACE
         USING SISCP236,RB
         SPACE
*********************************************************************
         SPACE
         TM    DCBRECFM,FIXED           TEST RECORD FORMAT       S20201
         BO    SISFIX4                   FIXED - CHANNEL PROGRAM S20201
*                                        *    OK - BRANCH
         MVC   CS21(L4),SIS5CS20        VARIABLE - CHANGE        S20201
*                                       CHANNEL                  S20201
*                                        *    OPERATION TO TIC
         MVI   CS20,RHA                 POSITION TO START        S20201
         OI    CS20+K4,SKIP+SILI        SUPPRESS DATA TRANSFER   S20201
SISFIX4  EQU   *                                                 S20201
         LR    RC,RB                    CS26 = TIC CS22          S20201
         A     RC,CS26                  *                        S20201
         ST    RC,CS26                  *                        S20201
         L     RA,W1FR1ST               THIS SECTION RELOCATES THE
         LA    RC,W1IMBBCC+3           ADDRESSES IN THE CCW'S OF  17516
         A     RC,CS1                  CP23.                      17516
         ST    RC,CS1                                             17516
         LR    RC,RB                                              17516
         A     RC,CS1A                                            17516
         ST    RC,CS1A                                            17516
         LR    RC,RB                                             S20201
         A     RC,CS1C                                           S20201
         ST    RC,CS1C                                           S20201
         LR    RC,RB                                             S20201
         A     RC,CS1D                                           S20201
         ST    RC,CS1D                                           S20201
         LR    RC,RB                    CP23 IS USED WHEN THE
         A     RC,CS3                   SETL K(D) MACRO IS
         ST    RC,CS3                   SPECIFIED. IT IS USED TO SEARCH
         LR    RC,RB                    THE INDECIES TO FIND
         A     RC,CS4                   THE RECORD WITH THE
         ST    RC,CS4                   EY SPECIFIED BY THE
         LR    RC,RB                    MACRO STATEMENT.
         A     RC,CS5
         ST    RC,CS5
         LR    RC,RB
         A     RC,CS8
         ST    RC,CS8
         LR    RC,RB                                             S20201
         A     RC,CS80                                           S20201
         ST    RC,CS80                                           S20201
         LA    RC,CS7+2                 CS9 = SRCH ID EQ (CS7+2) A34003
         A     RC,CS9                                             17516
         ST    RC,CS9                                             17516
         LR    RC,RB                                              17516
         A     RC,CS9A                                            17516
         ST    RC,CS9A                                            17516
         LR    RC,RB
         A     RC,CS12
         ST    RC,CS12
         LR    RC,RB
         A     RC,CS13
         ST    RC,CS13
         LR    RC,RB
         A     RC,CS16
         ST    RC,CS16
         LR    RC,RB
         A     RC,CS19
         ST    RC,CS19
         LR    RC,RB
         A     RC,CS19A
         ST    RC,CS19A
         LR    RC,RB
         A     RC,CS20
         ST    RC,CS20
         LR    RC,RB
         A     RC,CS21
         ST    RC,CS21
         LR    RC,RB
         A     RC,CS23
         ST    RC,CS23
         LA    RC,W1ISECT               ADDRESS TO PUT SECTOR IN S20201
         A     RC,CS24                                           S20201
         ST    RC,CS24                                           S20201
         LA    RC,W1WCOUNT              THE 'W' AREA IS USED
         A     RC,CS10                  FOR THE READING OF
         ST    RC,CS10                  THE TRACK INDEX.
         LA    RC,W1WCNXDM
         A     RC,CS14
         ST    RC,CS14
         LA    RC,W1WDNXDM
         A     RC,CS15
         ST    RC,CS15
         LA    RC,CN6+3                 THE COUNT FIELD IS READ
         A     RC,CS25                  INTO THE CN6 OF THE
         ST    RC,CS25                  FIRST CP22.
         TM    DCBRECFM,X'10'            BLOCKED RECORDS
         BZ    SIS05G4                    NO
         MVI   CS22,X'69'               SET TO SEARCH HI-EQ FOR BLK
         SPACE 3
SIS05G4  LA    RC,W1IMBBCC+3            ICP26 ALWAYS ACCOMPANIES
         A     RC,CS27                  CP23 AND IS INITIALIZED
         ST    RC,CS27                  IN THE SAME MANNER.
         LR    RC,RB
         A     RC,CS28
         ST    RC,CS28
         LR    RC,RB
         A     RC,CS30
         ST    RC,CS30
         LR    RC,RB
         A     RC,CS32
         ST    RC,CS32
         LR    RC,RB
         A     RC,CS33
         ST    RC,CS33
         LR    RC,RB
         A     RC,CS36
         ST    RC,CS36
         LR    RC,RB
         A     RC,CS37
         ST    RC,CS37
         LR    RC,RB
         A     RC,CS38
         ST    RC,CS38
         LR    RC,RB
         A     RC,CS39
         ST    RC,CS39
         IC    RC,DCBKEYLE              KEY LENGTH TO CHANGE     A35340
*                                       *    COUNT FIELDS OF
*                                       *    CHANNEL PROGRAMS
         STC   RC,CS1B+K7               CS1B TO KEY LENGTH       S20201
         STC   RC,CS29+7                CS29 TO KEY LENGTH       A35340
         STC   RC,CS2+7                 CS2 TO KEY LENGTH        A35340
         STC   RC,CS11+7                CS11 TO KEY LENGTH       A35340
         SPACE 5
*        ALL CHANNEL PROGRAMS ARE INITIALIZED
SIS05J4  EQU   *                                                 A35340
         SPACE 5
*                   FINISHED WITH THIS DCB.
*
*                   SET NAME PORTION OF WTG ENTRY TO ZERO
*                                       SINCE NEXT LOAD IS OPEN LOAD 7
SIS05END EQU   *                                                 Y02072
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
         SPACE 1
         MVC   0(L'LOAD24,RWTGC),LOAD24 ID OF IGG01924           Y02072
         B     RELOOP
         SPACE 4
         DS    0F
SIS5CS20 DC    X'08'                     TIC - CHANNEL PROGRAM   A35340
*                                       OP                       A35340
         DC    AL3(CS22-CS1)             TO CCW CS22             A35340
         SPACE 4
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURRENT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURRENT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH = BEGINNING OF THIS MOD
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BNE   RELOOP                   BRANCH NOT =, NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BNE   TCTLRTN                  BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         LA    RJ,DXCCW12
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
         SPACE 1
THISLOAD DC    C'29'
OPNLOD7  DC    C'0S'
         SPACE 2
*  TTR TABLE
LOAD24   DC    C'24'                    ID OF MODULE IGG01924    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
SISDEB   IGGDEBD                        ISAM DEB                 A35340
         EJECT
SISWORK1 IGGSCAN                        SCAN WORK AREA           A35340
         EJECT
SISCP22  DSECT
         IGGCP22
         SPACE 2
SISCP25  DSECT
         IGGCP25
         SPACE 2
SISCP236 DSECT
         IGGCP23
         IGGCP26
         EJECT
         DCBD  DSORG=IS
         EJECT
FORCORE  DSECT                                                   Y02072
         IECDSECT                                                Y02072
         END
