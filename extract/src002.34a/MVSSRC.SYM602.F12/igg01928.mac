 TITLE 'IGG01928- FIRST SCAN MODE OPEN MODULE'
IGG01928 CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG01928                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, SCAN MODE                             *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 01                                *
*                                                                     *
* FUNCTION = CALCULATE SIZE OF SCAN MODE WORK AREA PLUS CHANNEL       *
*            PROGRAM SPACE, AND GET CORE FROM SUBPOOL 250.  SAVE WA   *
*            ADDRESS IN DCBWKPT1 AND LENGTH IN DCBWKPT6.  LOAD        *
*            APPENDAGE CONTROL RTN AND READ APPENDAGE MODULE,         *
*            IGG019HG.  SAVE ASYNCHRONOUS ROUTINE ENTRY ADDRESS IN    *
*            DXCCW7 FOR IGG01924.  INITIALIZE DEBAVT ADDRESSES.  IF   *
*            PUTX, WITH WRITE CHECK, LOAD IGG019HH; OR IF PUTX,       *
*            WITHOUT WRITE CHECK, LOAD IGG019HI.  LOAD PROCESSING     *
*            MODULE IGG019HB FOR FIXED LENGTH RECORDS (FLR), OR       *
*            IGG019HN FOR VLR.  IF SETL BY KEY, LOAD SETL MODULE      *
*            IGG019HD AND APPENDAGE IGG019HK.  IF SETL BY ID, LOAD    *
*            SETL MODULE IGG019HF AND APPENDAGE IGG019HJ.  SET        *
*            DCBSETL ADDRESS.  LOAD CHANNEL PROGRAM MODULE IGG019HL.  *
*            MOVE CP24 SKELETON INTO WA; ALSO MOVE ONE COPY OF CP22   *
*            FOR EACH BUFFER.  IF SETL BY KEY, MOVE CP23 & CP26.  IF  *
*            SETL BY ID, MOVE CP25. DELETE CP MODULE IGG019HL.        *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 896 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG01928                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG01928 FOR  *
*              FLR, OR IGG01950 FOR VLR.  RECEIVES CONTROL IN         *
*              STORAGE PROTECT KEY 5 AND PRIVILEGED STATE.            *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG01929 IN STORAGE        *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGSCAN - ISAM SCAN MODE WORK AREA                  *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY, DEB, AND CVT                          *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = GETMAIN, MODESET, LOAD, DELETE, AND XCTL                   *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*          RELEASE OS/VS2-02 DELETIONS                                *
*                                                               YM01159
*                                                               YM02867
*                                                                     *
***********************************************************************
         EJECT
         USING IHADCB,RDCB
         USING SISWORK1,RUCB
         BALR  RBASE,0
         USING *,RBASE
         USING FORCORE,RG
R15      EQU   15
RE       EQU   0
RF       EQU   1
RDCB     EQU   2
RBASE    EQU   3
RG       EQU   4
RPAR     EQU   5
RWTG     EQU   6
RPARC    EQU   7
RWTGC    EQU   8
RTIOT    EQU   9
RUCB     EQU   10
RA       EQU   11
RB       EQU   12
RC       EQU   13
RD       EQU   14
RJ       EQU   15
RPSID    EQU   X'E0'                    RPS IDENTIFIER           S20201
K4       EQU   4                        CONSTANT                 S20201
K2       EQU   2                        CONSTANT                 S20201
PUTX     EQU   X'04'                    PUTX ID                  S20201
K7       EQU   7                        CONSTANT                 S20201
K10      EQU   10                       CONSTANT                 S20201
VIRTUAL  EQU   X'80'                    VIRTUAL INDICATOR
WRTCHK   EQU   X'80'                    WRITE CHECK INDICATOR    S21045
LO3BYTE  EQU   7                        STORE 3 LOW ORDER BYTES YM02867
ID       EQU   6                        DISP TO ID               S21045
ONE      EQU   1                        CONSTANT                 S21045
K8       EQU   8                        CONSTANT                 S21045
HB       EQU   C'B'                     IGG019HB                 S21045
HN       EQU   C'N'                     IGG019HN                 S21045
HD       EQU   C'D'                     IGG019HD                 S21045
HF       EQU   C'F'                     IGG019HF                 S21045
HG       EQU   C'G'                     IGG019HG                 S21045
HH       EQU   C'H'                     IGG019HH                 S21045
HI       EQU   C'I'                     IGG019HI                 S21045
HJ       EQU   C'J'                     IGG019HJ                 S21045
HK       EQU   C'K'                     IGG019HK                 S21045
HL       EQU   C'L'                     IGG019HL                 S21045
         SPACE 2
*        INITIALIZE REGISTERS
SIS04A2  L     RDCB,0(RPARC)
         L     RG,4(RWTGC)
         L     RTIOT,CVTPTR             ADDRESS OF CVT          YM01159
         USING CVT,RTIOT                CVT ADDRESSABILITY      YM01159
         L     RTIOT,CVTLINK            ADDRESS OF LINKLIB DCB  YM01159
         DROP  RTIOT                    END CVT ADDRESSABILITY  YM01159
         USING FORCORE,RG               EST OPEN WA ADDR         Y02072
         STM   RPAR,RWTGC,DXCCW1        SAVE REGISTERS 5-8       Y02072
         LR    RPAR,RG                  SAVE OPEN WA ADDR        Y02072
         DROP  RG                       END OPEN WA ADDR         Y02072
         USING FORCORE,RPAR             EST OPEN WA ADDR         Y02072
         SPACE 4
*        CALCULATE AMOUNT OF SPACE NEEDED FOR WORK AREA
         SPACE 3
SIS04B2  SR    RD,RD                    GET NUMBER OF BUFFERS
         IC    RD,DCBBUFNO
         LR    RC,RD                    RC=RD=BUF NUM, MULTIPLY BY LEN
         M     RB,SIS4CN22              CP22, RC=BYTES FOR ALL CP22'S
         TM    DCBMACRF+1,X'03'         IS SETL K OR I SPECIFIED
         BZ    SIS04C2                  EITHER NO SETL OR SETL B
         LA    RC,CP25LEN(RC)           ASSUME SELI CP           S21045
         TM    DCBMACRF+1,X'01'         WHICH SETL IS IT
         BO    SIS04B21                 SETL ID
         LA    RC,CP23LEN+CP26LEN-CP25LEN(RC) SETL K CP'S        S21045
SIS04B21 EQU   *                        *                        S21045
*
*   TEST TO SEE WHAT SIZE THE WORK AREA WILL BE, AND ADD
*   CORRECT LENGTH TO TOTAL.
*
SIS04C2  EQU   *                                                 Y01021
         L     RG,DCBDEBAD              DEB ADDRESSABILITY       Y01021
         USING SISDEB,RG                                         A35340
         L     RA,DEBAPPAD              PTR TO THE AVT           S21045
         USING DEBAVT,RA                ADDRESSABILITY           S21045
*
*   FIND THE LENGTH OF THE WORK AREA WITHOUT CHANNEL PROGRAMS.
*
         LA    RB,W1VLRLN               BASIC LENGTH             S21045
*   CHECK FOR RPS LENGTH.
         TM    DEBRPSID,RPSID           IS THIS AN RPS DATA SET  S21045
         BZ    SISNORPS                 NO - BASIC LENGTH O.K.   S21045
         LA    RB,W1RPSLN               USE RPS LENGTH           S21045
SISNORPS EQU   *
*   TEMPORARILY SAVE WORK AREA LENGTH.
         ST    RB,DEBEOEA               *                        S21045
         DROP  RA                                                S21045
         AR    RC,RB                    ADD WORK AREA LENGTH     S21045
SIS04C21 EQU   *                        TO CHANNEL PGM LENGTH    Y02072
         GETMAIN R,LV=(RC),SP=250       GET SPACE FOR WORK AREA
SIS04D2  ST    RF,DCBWKPT1              LOCATION RETURNED IN RF
         LR    RUCB,RF                 SET BASE FOR DSECT
         ST    RC,DXCCW6                SAVE LENGTH OF WORK AREA Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11 SET USERS KEY        Y02072
*
*                                       SET WORK AREA TO ZERO    Y02072
         LR    RA,RC                    LENGTH TO PAD TO ZERO    Y02072
         SR    RC,RC                    PAD TO ZERO              Y02072
         MVCL  RUCB,RB                  ZERO WORK AREA,          Y02072
*                                       C(RUCB) = ADDRESS OF WA  Y02072
*                                       C(RA) = LENGTH OF WA     Y02072
*                                       C(RB) = UNUSED           Y02072
*                                       C(RC) = PAD CHAR, ZERO   Y02072
*
         LR    RUCB,RF                  RESTORE WORK AREA ADDR   Y02072
         L     RC,DXCCW6                RESTORE LENGTH OF WA     Y02072
         MVC   W1DCBFA(4),DCBWKPT5      SET UP ADDR OF DCBFA     M6131
         STH   RC,W1WPLEN               SAVE WKPT LEN FOR CLOSE
         STH   RD,W1FREEC              SAVE BUFNO FOR CONVENIENCE
         L     RA,DEBAPPAD              AVT ADDRESSABILITY       S21045
         USING DEBAVT,RA                ADDRESSABILITY           S21045
         L     RB,DEBEOEA               WORK AREA LENGTH         S21045
         LA    RB,0(RB,RF)              END OF WORKAREA          S21045
         ST    RB,W1FR1ST               1ST CP 22                S21045
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         SR    RA,RA                    CLEAR REGISTER           Y02072
         ST    RA,DCBWKPT5              ZERO WKPT FOR A35340     Y02072
         SPACE 3
         DROP  RA                                                S21045
*   FIND DEB SUB ID FOR LOAD ROUTINE.
         IC    RA,DEBNMEXT              LENGHT OF EXTENTS =      S21045
         SLL   RA,K4                    16 * NUMBER              S21045
         LA    RB,SCANEXT               GET ACS METH LENGTH      S21045
         STC   RB,DEBAMLNG              INIT DEB                 S21045
         AR    RA,RB                    ADD AM LEN               S21045
         LA    RA,DEBMODID(RA)          PTR TO START OF ID LIST  S21045
         ST    RA,DXCCW5                SAVE IT                  Y02072
         SPACE 3
*        FIND WHICH MODULES ARE NEEDED AND LOAD THEM
         SPACE 3
*                             LOAD MODULE CONTAING APPENDAGE CONTROL
*                             RTN AND READ APPENDAGES
         MVC   DXCCW6,IGG019H           PREFORMAT NAME FOR LOAD  Y02072
         BAL   RA,LOADRTN               LOAD READ APPENDAGE      S21045
         LR   RD,RE                     SAVE MODULE PTR           M4361
         L     R15,DEBEXPTR             DEB EXTENSION            S21045
*                                       ADDRESSABILITY           S21045
         USING DEBEXT,R15                                        S21045
*                             SET RD TO BASE OF MODULE
         SPACE
         SPACE
*                             AT THE BEGINNING OF THIS MODULE IS A
*                             VECTOR TABLE CONTAINING THE UNRELOCATED
*                             ADDRESSES OF VARIOUS ENTRY POINTS.
         L     RA,0(RD)                 CH END READ ENTRY
         AR    RA,RE                    ADD RELOCATION FACTOR
         ST    RA,W1CEREAD              MOVE TO CE VECTOR TBL
         L     RA,4(RD)                 AB END ENTRY READ
         AR    RA,RE
         ST    RA,W1ABREAD              MOVE TO AB VECTOR TBL
         L     RA,8(RD)                 ASYNCHRONOUS RTN ENTRY
         AR    RA,RE
         ST    RA,DXCCW7                SAVE FOR IRB IN 1924     Y02072
         L     RA,12(RD)                IMMEDIATE RETURN
         AR    RA,RE
         L     R15,DEBAPPAD             AVT ADDRESSABILITY       S21045
         USING DEBAVT,R15                                        S21045
*
*   FILL IN APPENDAGE VECTOR TABLE.
         ST    RA,DEBEOEA               END OF EXTENT - BR 14    S21045
*
         STCM  RA,LO3BYTE,DEBSIOA+1     SIO - BR 14             YM02867
         ST    RA,DEBPCIA               PCI - UNUSED             S21045
         DROP  R15                      *                        S21045
         L     RA,16(RD)                APPEN CTL ENTRY FOR CH END
         AR    RA,RE
         ST    RA,12(R15)               TO APP VECTOR TBL
         L     RA,20(RD)                APPEN CTL ENTRY FOR AB END
         AR    RA,RE
         ST    RA,16(R15)               TO APP VECTOR TBL
         SPACE 3
*                                       IS PUTX SPECIFIED
         SPACE
         TM    DCBMACRF+1,PUTX          *                        S20201
         BZ    SIS04G23                NO FORGET WRITE APPENDAGES
         MVI   DXCCW6+L'IGG019H,HH     PUTX APPENDAGE NO W/C     Y02072
SIS04G21 TM    DCBOPTCD,X'80'           YES, IS WRITE CHECK SPEC.
         BZ    SIS04G22                 N0
         MVI   DXCCW6+L'IGG019H,HI      PUTX APPENDAGE W/C       Y02072
SIS04G22 EQU   *                        *                        S21045
*
*   LOAD PUTX APPENDAGE
*
         BAL   RA,LOADRTN               LOAD PUTX APPENDAGE
*
         LR    R15,RE                   SET R15 TO MODULE BASE
         L     RB,DEBEXPTR              DEB EXTENSION ADDR       S21045
         USING DEBEXT,RB                                         S21045
*   SET UP C.E. VECTORS.
         LA    RB,W1CEWRIT              CE VECTORS               S21045
         DROP  RB                                                S21045
         BAL   RC,PUTXRTN               LINK                     S21045
*   THEN SETUP A.E. VECTORS.
         LA    R15,0(R15,RD)            PT TO NEXT DISPLACEMENTS S21045
         L     RB,DEBEXPTR              DEB EXTENSION ADDR       S21045
         USING DEBEXT,RB                                         S21045
         LA    RB,W1ABWRIT              AB VECTORS               S21045
         DROP  RB                                                S21045
PUTXRTN  EQU   *                        *                        S21045
         SPACE 2
         SR    RD,RD                    PUT ADDRS OF WRITE CHECKING
SIS04GA1 L     RA,0(RD,R15)              APPENDAGE ENTRY POINTS IN
         AR    RA,RE                     THE VECTOR TABLES ALSO
         ST    RA,0(RB,RD)              SAVE VECTOR              S21045
         LA    RD,K4(RD)                POINT TO NEXT            S21045
*                                       DISPLACEMENT             S21045
         TM    DCBOPTCD,WRTCHK          WRITE CHECK              S21045
         BZ    PUTXOUT                  NO - ONLY ONE VECTOR     S21045
         C     RD,SIS4CN16              TABLE OF UNRELOCATED ENTRY
         BL    SIS04GA1                 POINTS ARE AT THE BEGINNING OF
PUTXOUT  BALR  RC,RC                    GET OUT IF DONE          S21045
         SPACE 3
SIS04G23 EQU   *                        *                        S21045
         MVI   DXCCW6+L'IGG019H,HB      FIXED PROCESSING MODULE  Y02072
         TM    DCBRECFM,X'80'           TEST RECORD FORMAT       A35340
         BO    SISLDGET                 FIXED - BRANCH           A35340
*                                       VARIABLE - LOAD VARIABLE
         MVI   DXCCW6+L'IGG019H,HN      VARIABLE PROCESSING      Y02072
*                                       MODULE                   S21045
SISLDGET EQU   *                                                 A35340
         BAL   RA,LOADRTN               LOAD PROCESSING MODULE   S21045
         IC    RA,DCBOFLGS
         ST    RE,DCBGET                STORE ENTRY POINT
         STC   RA,DCBOFLGS
         L     RB,DEBEXPTR              DEB EXTENSION ADDR       S21045
         USING DEBEXT,RB                                         S21045
         ST    RE,DEBGET                SAVE GET VECTOR IN DEB   S21045
         DROP  RB                                                S21045
         LR    R15,RE                   SET R15 WITH MODULE BASE
         L     RA,40(R15)               CONSTRUCT ESETL ADDRESS AND
         AR    RA,R15                   PLACE IT IN DCB
         ST    RA,DCBESETL
         SPACE 5
*                        IS A SETL MODULE TO BE LOADED
         MVI   DXCCW6+L'IGG019H,HD      SETL K MODULE            Y02072
         LA    RC,HK                    SETL K APPENDAGE         S21045
         TM    DCBMACRF+1,X'03'
         BZ    SIS04G27                 NO, SETLB ALREADY IN GET MOD.
         TM    DCBMACRF+1,X'02'         KEY TYPE OF SETL
         BO    SIS04G25                 YES
         MVI   DXCCW6+L'IGG019H,HF      SETL I MODULE            Y02072
         LA    RC,HJ                    SETL I APPENDAGE         S21045
SIS04G25 EQU   *                        *                        S21045
         SPACE 2
         BAL   RA,LOADRTN               LOAD SETL MODULE         S21045
         ST    RE,DCBSETL               SETL ENTRY PT.           S21045
         STC   RC,DXCCW6+K7             SETL APP ID              Y02072
*
*   LOAD SETL APPENDAGE
*
         BAL   RA,LOADRTN               LOAD SETL APPENDAGE
*
         LR    R15,RE                   POINTER TO SETL MOD      S21045
         LM    RA,RB,0(R15)             ENTRY POINTS
         AR    RA,RE                    ADD RELOCATION FACTOR
         AR    RB,RE                     TO APPENDAGE ENTRY POINTS
         L     RG,DEBEXPTR              DEB EXTENSION            S21045
*                                       ADDRESSABILITY           S21045
         USING DEBEXT,RG                                         S21045
*
         ST    RA,W1CESETL              STORE IN WORKAREA APPENDAGE
         ST    RB,W1ABSETL              VECTOR TABLE
         B     SIS04A3
*
SIS04G27 L     RA,36(R15)               PUT ADDR OF SETL B IN DCB
         AR    RA,RE                    SINCE NO SPECIAL SETL WAS
         ST    RA,DCBSETL               LOADED
SIS04A3  MVI   DXCCW6+L'IGG019H,HL      CHANNEL PROGRAM MODULE   Y02072
*
         LOAD  EPLOC=DXCCW6,DCB=(RTIOT) LOAD IT                  S21045
*
         MODESET  KEYADDR=DXUKEY,WORKREG=4  SET USERS KEY        Y02072
*
         LR    RG,RE                    SET BASE FOR TABLE OF CP ADDRS
         USING FAKEHL,RG                                         S21045
         MVC   W1CP24,C24               MOVE CP 24 SKELETON      S21045
         L     RB,DCBBUFCB
         TM    DCBRECFM,X'10'           BLOCKED
         BO    SIS04C34                 YES
         SR    RC,RC                    NO, IS THERE ROOM IN BUFFER TO
         IC    RC,DCBKEYLE              HAVE KEY AND DATA
*   KEYLENGTH + 10 ROUNDED UP TO NEXT DOUBLE WORD
         LA    RC,K10+K7(RC)            *                        S21045
         SRL   RC,3                     DIVIDE BY 8              A35340
         SLL   RC,3                     MULTIPLY BY 8            A35340
         ST    RC,W1TEMPSA              SAVE G                   A35340
         AH    RC,DCBBLKSI              RC = G + BLKSI           A35340
         CH    RC,6(RB)                 TRUE BUF LEN IS IN BUF CTL BLK
         BH    SIS04C34                 NOT ROOM FOR KEY AND DATA
         OI    W1OSBIT3,X'80'           K-D TYPE OF CP WILL BE OK
         B     SIS04C32
SIS04C34 MVC   W1TEMPSA,SIS4CN16        USED FOR 'G' FACTOR IN CN7
         SPACE 3
SIS04C32 LH    RC,W1FREEC               ONE COPY OF CP22 FOR EACH BUF
*   FIND FIRST SPACE AFTER WORK AREA TO PUT CHANNEL PROGRAMS
*   IN.
         L     RTIOT,DCBDEBAD           DEB ADDRESABILITY        S20201
         USING SISDEB,RTIOT                                      S20201
         L     RD,W1FR1ST               1ST CP 22                S21045
SIS04C33 MVC   0(CP22LEN,RD),C22        MOVE IN CP22 SKELETON    S21045
         ST    RD,W1FRLAST              IN CASE THIS IS LAST ONE
         LA    RD,CP22LEN(RD)           UPDATE TO ADDR FOR NEXT  A35340
*                                       MOVE                     A35340
         BCT   RC,SIS04C33              LOOP TILL SUFFICIENT COPIES IN
         SPACE 2
SIS04G3  TM    DCBMACRF+1,X'03'         WAS SETL I OR K SPECIFIED
         BZ    SIS04J3                  NO
         TM    DCBMACRF+1,X'02'         YES, WHICH TYPE
         BO    SIS04G4                  KEY
         ST    RD,W1CP25PT              SET POINTER IN WORKAREA
         MVC   0(CP25LEN,RD),C25        MOVE CP 25 SKELETON      S21045
         SPACE 2
         B     SIS04J3
         SPACE 5
*                                  MOVE CP23 AND CP26 TO WORKAREA
SIS04G4  ST    RD,W1CP23PT
         MVC   0(HALF236,RD),C23        MOVE IN CP 23 AND        S21045
         MVC   HALF236(HALF236,RD),C23+HALF236 CP 26 SKELETONS   S21045
*   BREAK CP23.
         USING CS1,RD                   ADDRESSABILITY ON CP 23 SA52459
         NI    CS4+K4,X'FF'-CC          BREAK UP CHANNEL PROGRAMSM4505
         DROP  RD                                               SA52459
         SPACE 3                                                 S21045
         LA    RD,CP23LEN(RD)                                    A35340
         ST    RD,W1CP26PT              SET POINTER
         SPACE 3
SIS04J3  DELETE EPLOC=DXCCW6             DELETE CP MODULE        Y02072
*                                       NEEDED HAVE BEEN MOVED TO THE
*                                       WORKAREA
         SPACE 3
*                   FINISHED WITH THIS DCB.
*
*                   SET WTG ENTRY WITH NAME OF NEXT LOAD
         LR    RG,RPAR                  SET OPEN WA BASE         Y02072
         DROP  RPAR                     END OPEN WA ADDR         Y02072
         USING FORCORE,RG               EST OPEN WA ADDR         Y02072
         LM    RPAR,RWTGC,DXCCW1        RESTORE REGISTERS 5-8    Y02072
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         MVC   0(L'LOAD29,RWTGC),LOAD29 ID OF NEXT LOAD IGG01929 Y02072
         SPACE 3
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURRENT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURRENT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH = BEGINNING OF THIS MOD
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BNE   RELOOP                   BRANCH NOT =, NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BNE   TCTLRTN                  BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         LA    RJ,DXCCW12
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
         EJECT
         DROP  RG                       END OPEN WA ADDR         Y02072
         DROP  RTIOT                                             S21045
         USING FORCORE,RPAR             EST OPEN WA ADDR         Y02072
*
LOADRTN  EQU   *                        *                        S21045
*
*   SUBROUTINE TO LOAD A MODULE AND PUT ITS ID IN THE DEB.
*         REG 0 PTS TO NAME OF MODULE.
*
         LOAD  EPLOC=DXCCW6,DCB=(RTIOT)    LOAD MODULE           Y02072
         L     RB,DXCCW5                PTR TO DEB NAME SECTION  Y02072
         MVC   0(K2,RB),DXCCW6+ID       MOVE IN ID               Y02072
         LA    RB,K2(RB)                PT TO NEXT SPOT          S21045
         ST    RB,DXCCW5                SAVE PTR                 Y02072
         BR    RA                       RETURN                   S21045
         SPACE 2
*        CONSTANTS
SIS4CN16 DC    F'16'
SIS4CN22 DC    F'56'                    LENGTH OF A CP22
*
* SCAN MODE MODULES
*
IGG019H  DC    CL7'IGG019H'             READ APPEND. AND APPEND. CTL
         DC    C'G'                     IGG019HG
OPNLOD7  DC    C'0S'
THISLOAD DC    C'28'
         SPACE 3
*  TTR TABLE
LOAD29   DC    C'29'                    ID OF MODULE IGG01929    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
SCANCPS  DSECT
         IGGCP22
         IGGCP25
         IGGCP23
         IGGCP26
*
         EJECT
FORCORE  DSECT
         IECDSECT
         EJECT
SISDEB   IGGDEBD
DEBMODID EQU   DEBUCBAD                 RELATIVE LOCATION OF     A35340
*                                       *    SUBROUTINE NAME
*                                       *    LIST LESS EXTENTS
         EJECT
SISWORK1 IGGSCAN
         EJECT
FAKEHL   DSECT
*   PSUEDO CP MODULE IGG019HL.
         DS    5F                                                S21045
C22      DS    0D                       CP 22                    S21045
         ORG   *+CP22LEN                                         S21045
C24      DS    0D                       CP 24                    S21045
         ORG   *+CP24LEN                                         S21045
C23      DS    0D                       CP 23                    S21045
         ORG   *+CP23LEN                                         S21045
C26      DS    0D                       CP 26                    S21045
         ORG   *+CP26LEN                                         S21045
C25      DS    0D                       CP 25                    S21045
*
HALF236  EQU   (CP23LEN+CP26LEN)/2      HALF MOVE FOR CP         S21045
*                                       SKELETON                 S21045
         EJECT
         DCBD  DSORG=IS
         SPACE 4
         CVT   DSECT=YES                                        YM01159
         END
