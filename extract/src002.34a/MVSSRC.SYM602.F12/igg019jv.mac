         TITLE 'IGG019JV - NON PRIV MACRO TIME READ K,KU,WRITE K'
IGG019JV CSECT
*          RELEASE OS/VS2-02 DELETIONS                                *
*                                                               YM01327
*          RELEASE 19 DELETIONS                                       *
*          RELEASE 20 DELETIONS                                       *
*0875                                                            A33995
*0875013000                                                      A35340
*          RELEASE 21 DELETIONS                                       *
*2782                                                           SA52386
*                                                                     *
*STATUS CHANGE LEVEL 005
* FUNCTION/OPERATION' NON-PRIVILEGED MACRO-TIME ROUTINE FOR BISAM READ
*        AND UPDATE.  TESTS DECB FOR INVALID LENGTH, AND IF VALID,
*        GOES TO PRIVILEGED MACRO-TIME ROUTINE VIA QING SVC (SVC 54).
*        UPON RETURN FROM PRIVILEGED ROUTINE, WILL EXCP IF AN IOB HAS
*        BEEN SCHEDULED.
* ENTRY POINT' REL POS 0
* INPUT - N/A
* OUTPUT - N/A
* EXTERNAL ROUTINES - N/A
* EXITS' RETURN TO USER VIA REGISTER 14
* TABLES/WORK AREAS' DECB, DCB, IOB AND EXTENSION - SEE DSECTS AT END
*        OF MODULE FOR LAYOUTS AND DESCRIPTIONS.
* ATTRIBUTES' RE-ENTRANT, NON-PRIVELEGED
* NOTES - NONE
* GENERAL REGISTERS ARE USED AS FOLLOWS
R0       EQU   0                        WORK REGISTER
R1       EQU   1                        DECB ON ENTRY, IOB ON EXIT
R2       EQU   2                        DECB
R3       EQU   3                        WORK REGISTER
R4       EQU   4                        DCB
R5       EQU   5                        WORK REGISTER
R6       EQU   6                        WORK REGISTER
R7       EQU   7                        WORK REGISTER
R8       EQU   8                        WORK REGISTER
R9       EQU   9                        WORK REGISTER
R10      EQU   10                       WORK REGISTER
R11      EQU   11                       WORK REGISTER
R12      EQU   12                       WORK REGISTER
R13      EQU   13                       USER'S REGISTER SAVE AREA
R14      EQU   14                       RETURN ADDRESS
R15      EQU   15                       BASE REGISTER
*
WRITEK   EQU   X'08'                    WRITE K REQUEST
*
*              CHART RW   READ/WRITE MACTOTIME
         USING IHADCB,R4
         USING RWMACRO,R15
         USING IHADECB,R2
RWMACRO  SAVE  (14,12)                  SAVE REGISTERS
         LA    R2,0(R1)                 ISOLATE DECB ADDRESS      21006
         XC    DECBECB,DECBECB          CLEAR ECB
         NI    DECBEXC1,X'02'           RESET EXCEPTION FLAGS EXCEPT
         NI    DECBEXC2,X'01'            SAVE UPDATE FLAG IF WRITE K
         TM    DECBTYP2,X'08'            AND OVERFLOW FLAG IF WRITE K
         BO    RWMRWA2                                            13929
RWMRWA1  NI    DECBEXC1,X'FD'
         NI    DECBEXC2,X'FE'
RWMRWA2  L     R4,DECBDCBA              R4  DCB ADDR
         TM    DCBMACRF,X'04'           DCB SPECIFY DYNAMIC BUFFERING
         BO    RWMRWB2                  YES
         TM    DECBTYP1,X'01'           NO, DECB SPECIFY DYNAMIC BFR
         BO    RWMRWH2                  YES, GO SET ERROR CODE
*
RWMRWB2  TM    DECBTYP1,X'02'           TEST FOR OVERRIDING LENGTH
         BO    RWMRWCK                  BRANCH IF NO            SA52386
         CLC   DCBBLKSI(2),DECBLGTH     NEW LENGTH MUST BE LESS THAN
         BL    RWMRWH1                   DCB LENGTH
         TM    DCBRECFM,X'10'            ALSO, NO OVERRIDE IF
         BZ    RWMRWCV                  BLOCKED                   VLR
RWMRWH1  OI    DECBEXC1,X'40'           IF RULES VIOLATED
         B     RWMRWH3                  POST AS INCORRECT LENGTH
RWMRWCV  TM    DCBRECFM,X'80'           TEST FOR FIXED            VLR
         BO    RWMRWC3                  B=FIXED                   VLR
         TM    DECBTYP2,WRITEK          TEST FOR WRITE K          VLR
         BO    RWMRWH1                  BRANCH IF YES - ERROR     VLR
*
RWMRWC3  LR    R1,R4                                             A35340
         SVC   QING                                              A35340
QING     EQU   54
         USING IHAIOB,R1
         TM    DECBEXC1,X'90'           IF REQUEST INVALID, OR RECORD
         BNE   RWMRWH3                  NOT FOUND, POST AND RETURN
         TM    DECBECB,COMPLETE         IS THIS REQUEST DONE     A33995
         BO    RWMRWJ3                  YES--DON'T EXCP          A33995
         LTR   R1,R1                    IOB ADDR ZERO IF VLR    YM01327
         BZ    RWMRWJ3                  AND  IOB PLACED ON UN-  YM01327
*                                       SCHEDULED QUEUE BY 19IZ YM01327
         TM    IOBINDCT,X'40'           IF IOB SCHEDULED FOR EXCP,
         BNE   RWMRWJ3
         SR    R6,R6                    MOVE IN BB
         L     R7,DCBDEBAD              FROM CORRECT DEB
         IC    R6,IOBDADAD              EXTENT
         SLL   R6,4
         LA    R6,32(R6,R7)
         MVC   IOBDADAD+1(2),4(R6)
         LR    R8,R15                   SAVE R15
         TM    DECBEXC2,X'02'           HAS  IZ   SCHED THIS JOB  VLR
         BO    RWMRWJ3                  PRIOR TO OUR TEST ; B=YES VLR
         EXCP  (1)                      EXCP
         LR    R15,R8                   RESTORE R15
         B     RWMRWJ3                  AND RETURN
*
RWMRWH2  OI    DECBEXC1,X'10'           SET INVALID REQUEST CODE
RWMRWH3  LR    R1,R2                    AND POST
         POST  (1)
RWMRWJ3  NI    DECBEXC2,X'FD'           TURN  OFF SWITCH          VLR
         RETURN (14,12)                 RESTORE REGS.AND RETURN   VLR
RWMRWCK  EQU   *                        *                       SA52386
         TM    DCBRECFM,FIXED           IS IT FIXED             SA52386
         BO    RWMRWC3                  YES, DON'T VALIDATE     SA52386
         TM    DECBTYP2,ANYREAD         READ?                   SA52386
         BC    RKRKU,RWMRWC3            YES, DON'T VALIDATE     SA52386
         L     R6,DECBAREA              ADDR OF BLOCK           SA52386
         TM    DECBEXC1,OVFLO           WK TO OVERFLOW RECORD   SA52386
         BO    RWMUNBLK                 YES, USE UNBLOCKED LOGICSA52386
         TM    DCBRECFM,BLOCKED         BLOCKED?                SA52386
         BO    RWMBLK                   YES                     SA52386
RWMUNBLK EQU   *                        *                       SA52386
         SR    R10,R10                  CLEAR                   SA52386
         IC    R10,DCBKEYLE             DCB KEY LENGTH          SA52386
         LA    R10,FIVE(R10)            L'RDW+1+KEYLE  MIN LRECLSA52386
         CH    R10,SIXTEEN(R6)          RDW < L'RDW +KEYLE +1   SA52386
         BH    RWMRWH1                  YES, LENGTH ERROR       SA52386
         LH    R10,SIXTEEN(R6)          RDW                     SA52386
         LA    R10,FOUR(R10)            RDW+L'BDW               SA52386
         CH    R10,DCBBLKSI             TOTAL > LRECL           SA52386
         BH    RWMRWH1                  YES, LENGTH ERROR       SA52386
         CH    R10,TWELVE(R6)           TOTAL = BDW             SA52386
         BNE   RWMRWH1                  NO - LENGTH ERROR       SA52386
         B     RWMRWC3                  RDW O.K. FOR UNBLOCKED  SA52386
RWMBLK   EQU   *                        *                       SA52386
         LH    R10,SIXTEEN(R6)          GET RDW                 SA52386
         LA    R10,FOUR(R10)            L'BDW+RDW               SA52386
RWMLOOP  EQU   *                                                SA52386
         CH    R10,TWELVE(R6)           IS BLOCK  > BDW         SA52386
         BH    RWMRWH1                  YES, LENGTH ERROR       SA52386
         BE    RWMRWC3                  IF EQ THEN BLOCK OK     SA52386
         LA    R7,TWELVE(R10,R6)        MOVE TO NEXT RDW        SA52386
         CLC   0(TWO,R7),ZERO           RDW=0                   SA52386
         BE    RWMRWH1                  YES, LENGTH ERROR       SA52386
         AH    R10,0(R7)                ADD RDW TO ACCUMULATOR  SA52386
         B     RWMLOOP                  TEST FOR END            SA52386
COMPLETE EQU   X'40'                    ECB COMPLETION BIT       A33995
ANYREAD  EQU   X'A0'                    MASK BITS FOR ANY READ  SA52386
ZERO     DC    H'0'                     HALFWORD OF 0           SA52386
FIXED    EQU   X'80'                    FIXED MASK              SA52386
BLOCKED  EQU   X'10'                    BLOCKED MASK            SA52386
RKRKU    EQU   5                        BRANCH ONNES OR MIXED   SA52386
SIXTEEN  EQU   16                       BISAM DECBAREA WA       SA52386
TWELVE   EQU   12                       WA LEN FOR VLR          SA52386
TWO      EQU   2                        MISC OFFSET             SA52386
OVFLO    EQU   TWO                      OVERFLOW MASK BIT       SA52386
FOUR     EQU   4                        MISC OFFSET             SA52386
FIVE     EQU   5                        MISC  OFFSET            SA52386
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
*              DATA EVENT CONTROL BLOCK
IHADECB  DSECT
         DS    0F
DECBECB  DS    CL4                      EVENT CONTROL BLOCK (ECB)
DECBTYP1 DS    BL1                      TYPE B6 - 1 IF LENGTH IS S
*                                            B7 - 1 IF AREA IS S
DECBTYP2 DS    BL1                           B0 - 1 IF READ K
*                                            B1 - 1 IF READ KX
*                                            B2 - 1 IF READ KU
*                                            B4 - 1 IF WRITE K
*                                            B5 - 1 IF WRITE KN
DECBLGTH DS    CL2                      LENGTH OF BLOCK
DECBDCBA DS    A                        POINTER TO DCB
DECBAREA DS    A                        ADDRESS OF AREA
DECBLOGR DS    A                        POINTER TO LOGICAL RECORD
DECBKEY  DS    A                        POINTER TO KEY
DECBEXC1 DS    BL1                      EXCPTN CD B0-RECORD NOT FOUND
*                                                 B1-RECORD LGTH CHK
*                                                 B3-INVALID REQUEST
*                                                 B4-UNCORRECTABLE IO
*                                                 B5-UNREACHABLE BLOCK
*                                                 B6-OVERFLOW RECORD
*                                                 B7-DUPLICATE RECORD
DECBEXC2 DS    BL1                                B7-SET BY READ KU
         DCBD  DSORG=(IS)
IHAIOB   DSECT
         DS    0F
IOBFLAG1 DS    CL1            IOB
IOBFLAG2 DS    CL1
IOBSENSE DS    CL2
IOBECBAD DS    A
IOBCSW   DS    CL8
IOBSIOCC DS    CL1
IOBSTART DS    CL3
IOBWGHT  DS    CL1
IOBDCBAD DS    CL3
IOBRESTA DS    A
IOBBCTIN DS    CL2
IOBERRCT DS    CL2
IOBDADAD DS    CL8
*                             IOB EXTENSION
IOBCCWAD DS    A                        ADDRESS FIRST CHAN PROGRAM
IOBINDCT DS    CL1                      INDICATORS B0  REMOVE CP FROM Q
*                                                  B1  UNSCHEDULED BIT
IOBUNSQR DS    CL1                      UNSCHED REASON B0 CP1 OR 2 BUSY
*                                                      B1 NO CP4-5-6
*                                                      B2 NO CP7
*                                                      B3 WKN AWAIT WKN
*                                                      B4 R+U AWAIT WKN
IOBAPP   DS    CL1                      APPENDAGE CODE, SEE APP RTN
IOBASYN  DS    CL1                      ASYNCHRONOUS CODE, SEE ASYN RTN
IOBCOUNT DS    0CL1                     WRITE CHECK ERROR COUNTER
IOBFCHAD DS    A                        FORWARD CHAIN ADDRESS
IOBBCHAD DS    A                        BACKWARD CHAIN ADDRESS
IHADCW   DSECT                    DCB WORK AREA
         DS    0F
DCWFCP4  DS    A                   ADDRESS FIRST AVAILABLE CP4-5-6
DCWFCP7  DS    A                   ADDRESS FIRST AVAILABLE CP7
DCWNUCPS DS    CL1                 NO.IOBS ON UNSCHED Q FOR HIGH-LVL CP
DCWNUCP4 DS    CL1                 NO,IOBS ON UNSCHED Q FOR CP4-5-6
DCWNUCP7 DS    CL1                 NO,IOBS ON UNSCHED Q FOR CP7
DCWNLSD  DS    CL1                 NO, HIGH LEVEL INDICES SRCHED ON DEV
DCWFIOBU DS    A                   ADDRFIRST UNSCHED IOB - AWAIT CP
DCWLIOBU DS    A                   ADDR LAST UNSCHED IOB - AWAIT CP
DCWFUPDI DS    A                   ADDRFIRST IOB ON UPDATE Q
DCWLUPDI DS    A                   ADDR LAST IOB ON UPDATE Q
DCWHIAV  DS    CL1                 B0 ON MEANS HI LVL IND CP AVAILABLE
*                                  B1 ON MEANS HI LVL IND IN CORE
DCWWKNI  DS    BL1                      B0 ON MEANS WRITE KN IN PROCESS
*                                       B1 FIRST TIME SWITCH
*                                       B2 SAME MODULE SWITCH
DCWNLEVC DS    CL1                      COUNTER USED BY WRITE KN APP
DCWNUWKN DS    CL1                      NO. OF IOBS AWAITING W KN CP
DCWMSHIL DS    A                        LAST ACTIVE ENTRY HI IND W KN
DCWHIRPS DS    CL1                      DCBHIRPD OR DCBHIRPS
DCWNACT  DS    CL1                      NO. NON-W KN MACROS BEFORE W KN
DCWSIZE  DS    H                   SIZE WA'S + CP'S IN DBL WRDS   P4700
DCWOPCLS DS    D                   INFO SAVED BY OPEN FOR CLOSE   P4700
DCWFIOBE DS    A                   ADDR FIRST IOB ON ERROR QUEUE  P4700
DCWLIOBE DS    A                   ADDR LAST IOB ON ERROR QUEUE   P4700
IHADEB   DSECT
DEBNMSUB DS    0BL1           # SUBROUTINES LOADED AT OPEN
DEBTCBAD DS    A              TCB POINTER
DEBAMLNG DS    0BL1           LENGTH OF ACCESS METHOD SECTION
DEBDEBAD DS    A              POINTER TO NEXT DEB IN TASK
DEBOFLGS DS    0BL1           DATA SET STATUS FLAGS
DEBIRBAD DS    A              POINTER TO IRB FOR ERROR EXIT
DEBOPATB DS    0BL1           FILE TYPE
DEBSYSPG DS    A              SYSTEM PURGE CHAIN
DEBNMEXT DS    0BL1           NUMBER OF EXTENTS CONSTRUCTED
DEBUSRPG DS    A              USER   PURGE CHAIN
DEBPRIOR DS    0BL1           DISPATCHING PRIORITY
DEBECBAD DS    A              POINTER TO ECB
DEBPROTG DS    0BL1           TASK PROTECTION FLAG
DEBDEBID DS    0BL1           F TO IDENTIFY AS A DEB
DEBDCBAD DS    A              POINTER TO CORRESPONDING DCB
DEBEXSCL DS    0BL1           SCALE TO DETERMINE SIZE OF DEVICE SECTION
DEBAPPAD DS    A              POINTER TO APPENDAGE BLOCK
DEBNIEE  DS    0BL1           # INDEX EXTENTS
DEBFIEAD DS    A              ADDR OF 1ST INDEX EXTENT
DEBNPEE  DS    0BL1           # PRIME DATA EXTENTS
DEBFPEAD DS    A              ADDR OF 1ST PRIME DATA EXTENT
DEBNOEE  DS    0BL1           # OVERFLOW EXTENTS
DEBFOEAD DS    A              ADDR OF 1ST OVERFLOW EXTENT
DEBDISAD DS    A              POINTER TO DISABLED MODULE
*                             ***********************************
         ORG   IHADEB+32
*              ADDRESS THE FOLLOWING BY ADDING M X 16
*                                     EXTENT ENTRY
DEBDVMOD DS    0BL1           DEVICE MODIFIER
DEBUCBAD DS    A              POINTER TO UCB
DEBBINUM DS    BL2            BB OF EXTENT
DEBSTRCC DS    BL2            CYLINDER ADDR FOR START OF EXTENT
DEBSTRHH DS    BL2            HEAD NUMBER FOR START OF EXTENT
DEBENDCC DS    BL2            CYLINDER ADDR FOR END OF EXTENT
DEBENDHH DS    BL2            HEAD NUMBER FOR END OF EXTENT
DEBNMTRK DS    BL2            NUMBER OF TRACKS IN EXTENT
         END
