         TITLE 'IGG019HH - WRITE APPENDAGE'
IGG019HH CSECT
*          RELEASE OS/VS2-02 DELETIONS
*                                                               YM03876
*          RELEASE 19 DELETIONS
*          RELEASE 20 DELETIONS
*1709016800,018200,019000,022600,022800,026800,028000,029800,    S20201
*1709030200,030800,034800,035000-036000,036400-036600,036800-    S20201
*1709043600,044000-044200,044400-069000                          S20201
*          RELEASE 21 DELETIONS
*                                                                S21045
*
*STATUS CHANGE LEVEL 005
*FUNCTION/OPERATION- A CHANNEL END COMPLETION WILL RESULT IN EITHER   *
*   A NORMAL RETURN TO IOS (IF ALL THE CHANNEL PROGRAMS ON THE QUEUE  *
*   WERE WRITTEN) OR AN EXCP RETURN TO IOS (IF THERE ARE MORE WRITE Q *
*   SETS TO BE WRITTEN).                                              *
*                                                                     *
*   AN ABNORMAL END COMPLETION RESULTS IN TAGGING THE BUFFER IN ERROR,*
*   SETTING ON A WRITE ERROR INDICATOR,(THE USER WILL BE INFORMED OF  *
*   THE ERROR ON A SUBSEQUENT GET) AND DOING AN EXCP OR NORMAL RETURN *
*   TO IOS. THE TYPE OF RETURN IS DEPENDENT UPON IF THERE ARE MORE    *
*   BUFFERS TO BE WRITTEN.                                            *
*                                                                     *
*ENTRY POINTS- THE APPENDAGE VECTOR TABLE ADDRESSES THE ENTRY POINTS  *
*   TO MODULE 'IGG019HH'.  RECIEVES CONTROL FROM IGG019HG.            *
*        SISWQA2 - CHANNEL END COMPLETION ON A WRITE                  *
*        SISWAB2 - ABNORMAL END COMPLETION ON A WRITE                 *
*                                                                     *
*INPUT- REGISTER 1- 12* ADDRESS                                       *
*       REGISTER 2- IOB ADDRESS                                       *
*       REGISTER 3- DEB ADDRESS                                       *
*       REGISTER 4- DCB ADDRESS                                       *
*       REGISTER 6- BASE REG FOR REGISTER SAVE AREA                   *
*       REGISTER 7- UCB ADDRESS                                       *
*       REGISTER 12- WORK AREA ADDRESS                                *
*       REGISTER 14- RETURN TO IOS VIA APPENDAGE IGG019HG             *
*       REGISTER 15- MODULE BASE                                      *
*                                                                     *
*OUTPUT- SAME AS FOR INPUT EXCEPT FOR FOLLOWING                       *
*       REGISTER 11- OFFSET TO USE IN RETURN TO IOS IN IGG019HG       *
*                                                                     *
*EXTERNAL ROUTINES- N/A                                               *
*                                                                     *
*EXITS- NORMAL- RETURN TO IOS THROUGH REGISTER 14                     *
*   ERROR-N/A                                                         *
*                                                                     *
*TABLES/WORK AREAS- DATA EXTENT BLOCK(DEB) DESCRIBED BY SISDEB DSECT  *
*   WORKAREA DESCRIBED BY SISWORK1 DSECT                              *
*   DATA CONTROL BLOCK DESCRIBED BY IHADCB DSECT                      *
*                                                                     *
*ATTRIBUTES- REENTRANT, READ ONLY                                     *
*                                                                     *
*NOTES- REGISTER USAGE IS CONSISTENT IN ALL SCAN MODE                 *
*   APPENDAGE MODULES                                                 *
         SPACE 5
*                                     *****************************
*                                     *                           *
*                                     * ABNORMAL END APP. WRITE Q *
*                                     *                           *
*                                     *****************************
         SPACE 2
         USING SISDEB,R3
         USING IHADCB,R4
         USING SISCP22,R11
         USING SISWORK1,R12
         USING *,R15
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
NORMAL   EQU   0                                                  15924
ASYNCH   EQU   4                        BRANCH OFFSET TO ASYNCH  Y02072
*                                       SCHEDULING ROUTINE IN
*                                       IGG019HG
EXCP     EQU   8                                                  15924
BYPASS   EQU   12                                                 15924
K1       EQU   1                        CONSTANT                 S20201
L3       EQU   3                        LENGTH                   S20201
IOBOC    EQU   X'01'                    OUTPUT IOB COMPLETE BIT YM03876
ONES     EQU   X'FF'                    MASK OF ALL ONES        YM03876
WAPBIT   EQU   X'40'                    WRITE APPEND WORK BIT   YM03876
WQERR    EQU   X'80'                    WRITE QUEUE ERROR       YM03876
         EJECT
SISCTWA  DC    A(SISWQA2-SISCTWA)  CHANNEL END ENTRY POINT
         DC    A(SISWAB2-SISCTWA)  ABNORMAL END ENTRY POINT
         USING *,R15
SISWQA2  B     SISWQA20                 FOR SETTING BASE
         SPACE 2
*                                  ***** CONSTANTS *****
         DS    0F
CONOF8   DC    HL2'08'
CONOF24  DC    AL2(CN4-CN1)             CONSTANT                 S20201
ZERO     DC    X'000000'       3 BYTES OF ZERO
         SPACE 2
SISWAB2  TM    W1OSENSE+2,X'3F'       PERM ERROR
         BO    SISWAB3                 NO
         L     R11,W1COMADO             CCW ADDR FROM CSW       SA66577
         LA    R11,0(R11)               CLEAR HIGH ORDER BYTE   SA66577
         SH    R11,CONOF8               SET R11 TO ERROR CCW    SA66577
*
         L     R10,W1OCPS               CHANNEL PGM START ADDR  SA66577
         LA    R10,0(R10)               CLEAR HIGH ORDER BYTE   SA66577
*
         CR    R11,R12                  ERROR CCW ADDR BELOW    SA66577
*                                       SCAN WORK AREA ADDRESS  SA66577
         BL    SISWAC1                  BR YES-ERROR IN IOS CCW SA66577
         CR    R11,R10                  ERROR CCW FIRST IN CP   SA66577
         BNE   SISWAC2                  BR IF NO                SA66577
*
SISWAC1  LR    R11,R10                  CHANNEL PGM START ADDR  SA66577
         CLI   CN1,SETSECT              FIRST CCW SET SECTOR    SA66577
         BNE   SISWAC3                  BR NO-MARK UNREACHABLE  SA66577
         L     R11,CN2                  ADDR OF FIRST BUFFER    SA66577
         B     SISWAC3                  GO MARK UNREACHABLE     SA66577
*
SISWAC2  CLI   0(R11),WKD               STOP ON A CN3 (WRT KD)  SA66577
         BNE   SISWAC2A                NO
         SH    R11,CONO16               BRING R11 TO CN1
         B     SISWAC2B
SISWAC2A CLI   0(R11),WD                STOP ON A CN4 (WRT DATA) S20201
         BNE   SISWAC3                  GO TO MARK UNREACH.
*
*     IF THIS IS TAKEN ,THEN R11 IS AT CN1 OF 1ST UNREACH CP
*
         SH    R11,CONOF24              BRING R11 TO CN1
SISWAC2B TM    W1OSENSE,X'08'           DID CP STOP ON A DATA CHECK
         BZ    SISWAC3                 NO
         OI    CN2+4,X'04'             MARK BUFFER UNWRITABLE
         MVC   CN5+4(4),W1OF1          MOVE IOB FIELDS INTO
         MVC   CN6,W1OCSW              CHAN. PROG FOR USER
         SR    R8,R8
         IC    R8,W1OCSW
         SLL   R8,4
         LA    R8,32(R3,R8)
         MVC   CN6+1(2),4(R8)
         SPACE 2
         OIL   W1OSBIT2,WAPBIT,REF=W1OSBIT1,   SET WRITE Q      YM03876X
               WREGS=(R9,R10,R13)      WORK BIT ON              YM03876
         SPACE 2
SISWAF2  NI    W1OF1,X'FB'             SET OFF IOB IN ERROR BIT
         TM    CN4+4,X'40'             END OF SET
         BO    SISWAD3                 NO
SISWAG2  CLC   CN5+1(3),ZERO        IS THIS END OF WRITE Q
         BE    SISWQC1                 YES
         L     R5,CN5                  GET NEXT CHANNEL PROGRAM
         B     SISWQE3B
SISWAD3  L     R5,CN5                   GET NEXT CP IN SET
         DROP  R11                                               S20201
         USING SISCP22,R5                                        S20201
         NI    CN1,X'FF'-MT             SET OFF MT BIT           S20201
         DROP  R5                                                S20201
         USING SISCP22,R11                                       S20201
         B     SISWQE3B                 TAKE EXCP RETURN         S20201
SISWAB3  EQU   *                                                 Y02072
         LA    R10,NORMAL               LOAD IOS RET ADDR OFST   Y02072
         BR    R14                      RETURN TO IOS VIA EXIT   Y02072
*                                       ROUTINE IN IGG019HG      Y02072
         SPACE 2
SISWAC3  OIL   W1OSBIT2,WAPBIT,REF=W1OSBIT1,    SET ON WRITE Q  YM03876X
               WREGS=(R9,R10,R13)       WORK BIT                YM03876
         SPACE 2
         NI    W1OF1,X'FB'             SET OFF IOB IN ERROR BIT
SISWAC3A OI    CN2+4,X'02'             MARK BUFFER UNREACHABLE
         TM    CN4+4,X'40'             END OF SET
         BZ    SISWAG2                 YES
         L     R11,CN5                 GET NEXT CP IN SET
         B     SISWAC3A                GO MARK NEXT BUFFER UNREACHABLE
         DROP  R11                                               S20201
         EJECT
SISCTWQ  EQU   *
*                                     ***************************
*                                     *                         *
*                                     * CHANNEL END APP WRITE Q *
*                                     *                         *
*                                     ***************************
         SPACE 2
*                                  ***** CONSTANTS *****
CONO16   DC    AL2(CN3-CN1)             CONSTANT                 S20201
SIXTEEN  EQU   16                       FOR COMMUNICATION VECTOR TABLE
TABOFF4  EQU   4                        FOR EXIT EFFECTOR TABLE
         SPACE 2
SISWQA20 L     R8,W1OCPS                LOAD IOBO CPS TO R8
SISWQB2  L     R5,W1COMADO            COMADO MUST POINT TO A CN5 ADDR
         USING CN5,R5                                            S20201
SISWQC2  CLC   CN5+K1(L3),ZERO          END OF QUEUE             S20201
         BNE   SISWQC3                  NO
         SPACE 2
SISWQC1  OIL   W1OSBIT1,IOBOC,REF=W1OSBIT1,    SET ON IOBO      YM03876X
               WREGS=(R9,R10,R13)       COMPLETION BIT          YM03876
         SPACE 2
SISWQD1  TM    W1OSBIT2,X'40'          IS WRITE Q WORK BIT ON
         BO    SISWQD1A                 YES
         SPACE 2
         NIL   W1OSBIT2,ONES-WQERR,REF=W1OSBIT1, IF WORK BIT    YM03876X
               WREGS=(R9,R10,R13)       OFF, SET OFF OSBIT      YM03876
         SPACE 2
SISWQE1  B     SISWAB3                  NORMAL RETURN TO IOS
         SPACE 2
SISWQD1A OIL   W1OSBIT2,WQERR,REF=W1OSBIT1,   SET ON WRITE Q    YM03876X
               WREGS=(R9,R10,R13)       ERROR BIT               YM03876
         SPACE 2
         B     SISWAB3                 NORMAL RETURN TO IOS
SISWQC3  L     R5,CN5                  SET R5 TO CN1 OF NEXT CP  S20201
         DROP  R5                                                S20201
         USING SISCP22,R5                                        S20201
*
SISWQE3B MVC   W1ODAD,CN6              SEEK TO CN6 OF NEXT CP    S20201
         ST    R5,W1OCPS               SET OCPS TO CN1 OF NEXT CP
         SR    R13,R13
         IC    R13,CN6                                           S20201
         SLL    R13,4                        MX16
         LA    R11,32(R3,R13)                R11 POINTS TO EXTENT
         MVC   W1OMBBCC+1(2),4(R11)    MOVE BB INTO IOBO
         L     R11,0(R11)              GET UCB ADDR
         LA    R11,0(R11)              CLEAR HIGH ORDER BYTE
         LA    R7,0(R7)                  OF REGS
         CR    R7,R11                  SAME DEVICE
         BNE   SISWQF3                 NO, SCHED ASYCH ROUTINE
         XC    W1IF2(3),W1IF2           CLEAR FLAG2,SENSE BYTES   15924
         XC    W1ICSW(9),W1ICSW         CLEAR FLAG3,CSW,SIOCC     15924
         XC    W1IERRCT(2),W1IERRCT     CLEAR ERROR COUNT         15924
         NI    W1IF1,X'C2'              RESET FLAG1               15924
         LA    R10,EXCP                 LOAD IOS RET ADDR OFST   Y02072
         BR    R14                      RETURN TO IOS VIA EXIT   Y02072
*                                       ROUTINE IN IGG019HG
         SPACE 2
*  SCHEDULE ASYNCHRONOUS ROUTINE
SISWQF3  EQU   *                                                 Y02072
         B     ASYNCH(,R14)             BRANCH TO EXIT ROUTINE   Y02072
*                                       IN IGG019HG
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
         EJECT
SISCP22  DSECT
         IGGCP22
         SPACE 5
SISDEB   IGGDEBD
         EJECT
SISWORK1 IGGSCAN
         EJECT
         SPACE 5
         DCBD  DSORG=(IS)
         END
