         TITLE 'IGG0202N - QISAM LOAD MODE CLOSE- FLUSH INDICIES'
IGG0202N CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0202N                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM CLOSE, LOAD MODE WITH FIXED LENGTH RECORDS  *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = FOR A NULL RESUME LOAD, XCTL TO NEXT ISAM CLOSE EXECUTOR;*
*            OTHERWISE, IF LAST TRACK INDEX NEEDS TO BE WRITTEN,      *
*            SYNCH TO PROCESSING ROUTINE AND DO IT.  IF CYLINDER      *
*            INDEX NEEDS TO BE WRITTEN, DO THE SAME.                  *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = CLOSE EXECUTOR                                        *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 481 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0202N                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM CLOSE EXECUTOR IGG0202I FOR *
*              FIXED LENGTH RECORDS (FLR) ONLY.  RECEIVES CONTROL IN  *
*              IN STORAGE PROTECT KEY 5 AND PRIVILEGED STATE.         *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM CLOSE EXECUTOR IGG0202J IN STORAGE       *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = ISAM LOAD MODE PROCESSING AND APPENDAGE ROUTINES      *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - CLOSE WORK AREA                           *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                                                                     *
*    CONTROL-BLOCKS = DCB, DCB COPY, IOB, CVT, TCB, AND SVRB          *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, SYNCH, AND XCTL                                   *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
*
* EQUATE SYMBOLIC REGISTERS
*
R0       EQU   0
R3       EQU   3                        NOT USED
R4       EQU   4                        WORK REGISTER
R6       EQU   6                        CHANNEL PGM BASE
R7       EQU   7                        WORK REGISTER
R8       EQU   8                        AREA Y BASE REG
R13      EQU   13                       WORK REGISTER
R14      EQU   14                       WORK REGISTER
R15      EQU   15                       WORK REGISTER
RDCB     EQU   1                        DCB BASE REGISTER
RIOB     EQU   2                        IOB BASE REGISTER
RBASE    EQU   9                        MODULE BASE REGISTER
RCORE    EQU   4                        BASE FOR OPEN WORK AREA
RPAR     EQU   5                        POINTER TO PARAM. LIST
RWTG     EQU   6                        POINTER TO WTG TABLE
RPARC    EQU   7                        CURRENT PARAM. ENTRY
RWTGC    EQU   8                        CURRENT WTG ENTRY
RCOPY    EQU   10                       DCB COPY BASE REG        Y02072
RBCT     EQU   11                        BUFFER CONTROL TABLE REG
RISLMWA  EQU   12                        LOAD MODE WORK AREA REG
RJ       EQU   15
*
DCBOFF   EQU   4                        VALUE FOR INCR. TO NEXT DCB
IDLEN    EQU   2                        LENGTH OF MODULE ID
IDOFF    EQU   6                        OFFSET FOR XCTL ID
RESETOFF EQU   32                       OFFSET TO REPOSITION TO
*                                       START OF LIST
TTRLEN   EQU   3                        LENGTH OF TTR
TTROFF   EQU   14                       OFFSET FOR XCTL TTR
WAOFF    EQU   4                        OFFSET IN WTG TBL OF OPEN WA
WTGEND   EQU   X'F0'                    MASK FOR END OF WTG TABLE
WTGOFF   EQU   8                        VALUE FOR INCR WTG ENTRY
K0       EQU   0                        CONSTANT                 S20201
EOT      EQU   X'08'                    BUFFER AT END OF TRACK   S20201
L3       EQU   3                        LENGTH                   S20201
K1       EQU   1                        CONSTANT                 S20201
K8       EQU   8                        CONSTANT                 S20201
CLOSE    EQU   X'40'                    CLOSE SW                 S20201
K26      EQU   26                       CONSTANT                 S20201
L7       EQU   7                        LENGTH                   S20201
CLOSE2   EQU   X'04'                    CLOSE SW                 S20201
WRCHK    EQU   X'80'                    WRITE CHECK SW DCB       S20201
ONE      EQU   1                        CONSTANT
PERMERR  EQU   X'0C'                    PERMANENT IO ERROR
FTIW     EQU   X'40'                    FULL TRACK INDES WRITE
FTIWGM   EQU   X'C0'                    SUCCESSFUL FTIW GETMN
TRKFULL  EQU   X'01'                    DCBST LAST TRK FULL IND.
ZERO     EQU   X'00'                    TEST FOR ZERO
CLOSESW  EQU   X'02'                    HIGH LEVEL INDX CLOSE IND.
INDX     EQU   X'08'                    TRK INDX WRTTEN BUT NOT CYL
CP20LAST EQU   X'03'                    CP20(FTIW) WAS SCHEDULED
*
         EJECT
ISLG500  BALR  RBASE,0                  ESTABLISH BASE ADDRESS
         USING *,RBASE                  MODULE ADDRESSABILITY
*
BASETAG   L    RDCB,0(RPARC)            DCB ADDRESS
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         L     RCORE,WAOFF(RWTGC)       ADDR OF OPEN WORK AREA
         L     RISLMWA,DCBWKPT1         LOAD MODE WORK AREA ADDRESS
         USING ISLCOMON,RISLMWA         WORK AREA ADDRESSABILITY
*
         LR    R3,RCORE                 CLOSE WORK AREA ADDR     Y02072
         USING FORCORE,R3               WK AREA ADDRESSABILITY   Y02072
         STM   R0,R15,DXCCW1            SAVE ALL REGISTERS       Y02072
         LR    RCOPY,RDCB               SAVE DCB COPY ADDR       Y02072
         L     RDCB,DXUDCBAD            ADDR OF USERS DCB        Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=8  CHANGE TO USER KEY   Y02072
*
         DROP  R3                       END OPEN WA USING        Y02072
         L     RBCT,ISLVPTR3            BUFFER CONTROL TABLE ADDRESS
         USING IOBBCT,RBCT              BUFFER CNTRL TBL ADDRESSABILITY
         LA    RIOB,ISLIOBA             CP18 IOB
         USING DXIOB,RIOB               IOB ADDRESSABILITY
         EJECT
***********************************************************************
* CHART G8 - CLOSE (FORCE IX)                                         *
***********************************************************************
*
* WAS LAST TRK IX WRITTEN
*
ISLG801  L     R8,ISLVPTR1              C(R9)=A(AREA Y)
         NC    DCBNREC,DCBNREC          WAS DATA CREATED         A33664
         BE    ISLG802                  NO, BRANCH
         MVC   IOBPTRA1,IOBPTRB1        SET PTR A = PTR B = CURR BUFFR
         MVI   ISLFBW+L3,ONE            SET FBW = 1
         MVI   IOBB,ONE                 SET B = 1
         L     R4,IOBPTRB               SIMULATE FULL BUFFER     S20201
         OI    K0(R4),EOT               AT TRACK END             S20201
*
*              IF FULL TRACK INDEX WRITE, TURN CLOSE BIT ON IN
*              THE TRACK INDEX SAVE AREA FLAG FIELD.
*
         TM    DCBEXCD1,PERMERR         PERMANENT IO ERROR?      A40960
         BZ    NOERRORS                 BRANCH IF NONE           A40960
         TM    DCBOPTCD,FTIW            FTIW OPTION?             A40960
         BO    ISLG803                  CAN'T WRITE TRACK INDEX  A40960
         B     ISLG8011                 OK TO WRITE TRK INDEX    A40960
NOERRORS EQU   *                                                 A40960
         TM    ISLVPTRA,FTIWGM          SUCCESSFUL GETMAIN FOR   O19110
*                                       FULL TRACK INDEX WRITE
         BNO   ISLG8011                 NO - BR TO CONTINUE      O19110
         L     R4,ISLVPTRA              C(R4)=A(TISA)            O19110
         USING TISA,R4                  TISA ADDRESSABILITY
         TM    FLAGS,CP20LAST           HAS CP20 BEEN SCHEDULED
         BO    ISLG803                  YES - DONE
         CLC   ISLVPTR4+K1(L3),IOBSTART CP18 EXECUTED LAST ?     O19110
         BE    ISLG8023                 NO-TI DOES NOT NEED      S20201
*                                       O1911                    S20201
         L     R14,IOBSIOCC             PTR TO CP 18 SS          S20201
         LA    R14,K8(R14)              +8                       S20201
         ST    R14,IOBSIOCC             CORRECT IT               S20201
         CLC   ISLVPTR4+K1(L3),IOBSTART CP18 EXECUTED LAST       S20201
         BNE   ISLG803                  NO-TI DOES NOT NEED      S20201
ISLG8023 EQU   *                        *                        S20201
*                                       WRITG                    O19110
         OI    FLAGS,CLOSE              RN CLOSE BIT ON          S20201
         CLC   K26(L7,R8),DCBLPDA       TEST LAST NDAT VS LPDA   S20201
         BNE   ISLG8014                 LAST TRK NOT FULL        S20201
         OI    FLAGS,CLOSE2             NO NEW ENTRIES NEEDED    S20201
         B     ISLG8014                 BR TO RETURN TO PUT      S20201
*
*              FORCE LAST TRACK INDEX TO BE WRITTEN
*
ISLG8011 EQU   *                                                 S20201
         CLC   K26(L7,R8),DCBLPDA       TEST LAST NDAT VS LPDA   S20201
         BE    ISLG803                  BR IF LAST TRK IX        S20201
*                                       WRITTEN                  O19110
         L     R6,ISLVPTR6              C(R6)=A(CP20)            S20201
         USING CQ1,R6                   CP20 ADDRESSABILITY
         TM    DCBOPTCD,WRCHK           TEST OPTCD BIT 0 (WR     S20201
*                                       CHK)                     S20201
         BO    ISLG8012                 B IF WR CHK
*
         MVI   CQ14,NOP                 NO WR CHK, SET CQ14 = NOP
         B     ISLG8014
*
ISLG8012 MVI   CQT8,NOP                 CQT8 = NOP               A37537
*
ISLG8014 L     R13,ISLFYAD              C(R13)=A(ISLFY01)
         BAL   R14,SYNCHRTN             SYNCH TO PROCESSING RTN  Y02072
*
         B     ISLG803
*
*                                       LAST TRK IX WAS WRITTEN, SO
*                                       LAST TRK WAS FULL -
ISLG802  OI    DCBST,TRKFULL             TURN ON ST BIT-7
*
ISLG803  CLI   DCBNLEV,ZERO             TEST NLEV VS 0
         BE    ISLG810                  B IF 0, TRK IX ONLY
*
* NLEV NOT 0, WAS LAST CYL IX WRITTEN
*
         OI    IOBFLAGS,CLOSESW         SET FLAGS BIT 6 ON FOR    21347
*                                       CLOSE                     21347
         TM    ISLIXLT,INDX             TEST IXLT LEV1 BIT-4
         BZ    ISLG804                  B IF 0 = LAST CYL IX WRITTEN
*
* LAST CYL IX NOT WRITTEN, FORCE LAST CYL IX
*
         L     R7,IOBPTRB               C(R7)=A(CURR SLOT)
         LA    R7,0(R7)                                           15361
         DROP  RIOB                     END USING ON IOB         Y02072
*
*
         L     R13,ISLFZAD              C(R13)=A(ISLFZ01)
         BAL   R14,SYNCHRTN             SYNCH TO PROCESSING RTN  Y02072
*
ISLG804  EQU   *                        *                        S20201
         USING FORCORE,R3               OPEN WA ADDRESSABILITY   Y02072
ISLG810  LM    R0,R15,DXCCW1            RESTORE REGISTERS        Y02072
         DROP  R3                       END USING ON WORK AREA   Y02072
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         USING FORCORE,RCORE            WA ADDRESSABILITY        Y02072
         MVC   0(L'LOAD2J,RWTGC),LOAD2J NEXT LOAD IGG0202J       Y02072
RELOOP   LA    RWTGC,WTGOFF(RWTGC)      INCREMENT WTG CURNT ENTRY
         LA    RPARC,DCBOFF(RPARC)      INCREMENT DCB CURNT ENTRY
         CLC   0(L'THISLOAD,RWTGC),THISLOAD IGG0202N INDICATED
         BE    BASETAG                  BRANCH=BEGINNING OF THIS MODULE
         CLI   0(RWTGC),WTGEND          TEST FOR END OF WTG TABLE  MC1A
         BNE   RELOOP                   BRANCH=NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,RESETOFF(RWTG)     INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),ZERO            TEST FOR ENTRY = 0
         BNE   TCTLRTN                  BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,WTGOFF(RWTGC)      GET NEXT WTG ENTRY
         LA    RPARC,DCBOFF(RPARC)      GET NEXT DCB
         B     ZCHECK                   GO TEST ENTRY
TCTLRTN  EQU   *
         MVC   IDOFF(IDLEN,RWTG),0(RWTGC) MOVE ID
         LA    RJ,DXCCW12               PARAMETER ADDRESS        Y02072
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15))  XCTL TO IGG0202J  Y02072
         DROP  RCORE                    END CLOSE WA USING       Y02072
         EJECT
         USING FORCORE,R3               WA ADDRESSABILITY        Y02072
*
SYNCHRTN MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MST KEY   Y02072
*
         STM   RCOPY,R15,DXCCW9         SAVE REGS 10 THRU 15     Y02072
         L     R15,CVTPTR               CVT ADDRESS              Y02072
         USING CVT,R15                  CVT ADDRESSABILITY       Y02072
         L     R15,CVTTCBP              TCB POINTERS             Y02072
         L     R15,0(R15)               TCB ADDRESS              Y02072
         USING TCB,R15                  TCB ADDRESSABILITY       Y02072
         L     R15,TCBRBP               SVRB ADDRESS             Y02072
         USING RBBASIC,R15              ADDRESSABILITY           Y02072
*
         MODESET  KEYADDR=KEY0,WORKREG=10 CHANGE TO KEY ZERO     Y02072
*
         STM   R0,RBASE,RBEXSAVE        SAVE REGS 0 THRU 9       Y02072
         DROP  R15                      END USING ON RB          Y02072
         L     RBASE,ISLF8AD            BASE FOR PROCESSING RTN  Y02072
         LR    R15,R13                  ENTRY POINT ADDR         Y02072
*
         SYNCH (15)                     GO TO PROCESSING RTN     Y02072
*
         L     R15,CVTPTR               ADDRESS OF CVT           Y02072
         USING CVT,R15                  CVT ADDRESSABILITY       Y02072
         L     R15,CVTTCBP              ADDR OF TCB PTRS         Y02072
         L     R15,0(R15)               TCB ADDRESS              Y02072
         USING TCB,R15                  TCB ADDRESSABILITY       Y02072
         L     R15,TCBRBP               ADDRESS OF SVRB          Y02072
         USING RBBASIC,R15              SVRB ADDRESSABILITY      Y02072
         LM    R0,RBASE,RBEXSAVE        RESTORE REGS 0 THRU 9    Y02072
         DROP  R15                      END USING ON SVRB        Y02072
*
         MODESET KEYADDR=DXUKEY,WORKREG=15 CHANGE TO USER KEY    Y02072
*
         LM    RCOPY,R15,DXCCW9         RESTORE REGS 10 THRU 15  Y02072
         BR    R14                      RETURN
         SPACE 5
*
* CONSTANTS
*
THISLOAD DC    C'2N'                                             O19110
KEY0     DC    X'00'                    STORAGE PROTECT KEY ZERO Y02072
*
*
LOAD2J   DC    C'2J'                    MODULE ID                Y02072
*
PATCH    DC    XL50'00'                 PATCH AREA               Y02072
         EJECT
***********************************************************************
*    DCB REFERENCE                                                    *
***********************************************************************
         DCBD  DSORG=(IS)
         EJECT
***********************************************************************
*    DEB REFERENCE                                                    *
***********************************************************************
IHADEB   IGGDEBD
         EJECT
***********************************************************************
*    LOAD MODE WORK AREA - ISLCOMMON - REFERENCE                      *
***********************************************************************
ISLCOMON IGGLOAD
         EJECT
*
*    IOBBCT REFERENCE          C(ISLVPTRS+8) = A(IOBBCT)
*
IOBBCT   DSECT
         DS    0D
IOBFLAGS DS    CL1
IOBPTRA  EQU   IOBFLAGS
IOBPTRA1 DS    AL3
IOBB     DS   CL1
IOBPTRB  EQU   IOBB
IOBPTRB1 DS    AL3
IOBS     DS    0CL1
IOBABUF  DS    A
         SPACE 4
***********************************************************************
*    FULL TRACK INDEX WRITE SAVE AREA                                 *
***********************************************************************
*
         DS    0D
TISA     DSECT                          TRACK INDEX SAVE AREA
FTWIOB   DS    2F                       IOB FOR FULL TRK INDEX WRITE
SIZE     DS    AL2                      LENGTH OF TRK INDEX ENTRY
FLAGS    DS    AL1                      FLAGS FOR FTIW
HIGHR    DS    AL1                      HIGHEST R ON CURRENT TRK
CURRR    DS    0CL1                     CURRENT RECORD ON THIS TRK
NEXTTI   DS    A                        ADDRESS OF NEXT TI ENTRY
TISASIZE DS    A                        SIZE OF TISA
         EJECT
***********************************************************************
*    LOAD MODE CHANNEL PROGRAMS                                       *
***********************************************************************
*
CPSX     DSECT
         IGGLDCP OPTCD=W
         EJECT
CVT      DSECT                                                   Y02072
         CVT                                                     Y02072
         SPACE 2
TCB      IKJTCB                                                  Y02072
         EJECT
         IKJRB                                                   Y02072
         EJECT
FORCORE  DSECT
         IECDSECT
         END
