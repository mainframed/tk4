         TITLE 'IGG019JX - PRIV MACRO TIME, WRITE KN'
IGG019JX CSECT
*        RELEASE OS/VS2-02 DELETIONS                             Y02072
*                                                               YM01327
*                                                               YM03934
*        RELEASE 19 DELETIONS
*1295013084,013161                                               A26738
*        RELEASE 20 DELETIONS
*0712013175,027100,027150,027200,027250-027300,033380,033800,    S20201
*0712034000,034600,034800,035000,035200,035400,037400,037600-    S20201
*0712039400,039600-043400,043600-049400,049600-054400,054600-    S20201
*0712059400,059600-064400,064600-080200                          S20201
*        RELEASE 21 DELETIONS
*3513                                                            A33533
*
*STATUS CHANGE LEVEL 008
*
* FUNCTION/OPERATION- PRIVILEGED MACRO-TIME ROUTINE FOR BISAM WHEN
*        WRITE KN IS USED WITHOUT READ AND UPDATE.
*        1.  GETMAIN FOR AN IOB AND EXTENSION AND INITIALIZE THE IOB.
*        2. DETERMINE IF ANY OTHER WRITE KN MACRO IS
*            BEING PROCESSED FOR THIS DATA SET.  IF SO, ADD THE IOB TO
*            THE UNSCHEDULED QUEUE AND SET THE UNSCHEDULED SWITCH ON.
*            IF NOT, SEARCH THE HIGHEST LEVEL INDEX IN CORE IF THIS
*            IS NECESSARY, INITIALIZE THE FIRST WKN CHANNEL PROGRAM,
*            AND SET THE START ADDRESS AND SEEK-SEARCH ADDRESS IN THE
*            IOB.
*
* ENTRY POINTS-
*        ENTRY PNT     CALLING ROUTINE                FUNCTION
*        ---------     ---------------                --------
*        REL ADDR 0 NON-PRIVILEGED MACRO-TIME   ALL OF THE FUNCTIONS
*                    ROUTINE VIA QING SVC        DESCRIBED ABOVE
*                    (SVC 54) ROOT
*        REL ADDR 4 ASYNCHRONOUS ROUTINE        SET UP FIRST WKN CP
* INPUT - N/A
* OUTPUT - N/A
* EXTERNAL ROUTINES - N/A
*
* EXITS - WKNN2J2-RETURN TO THE NON-PRIVILEGED MACRO-TIME ROUTINE
*       - WKNS5G2-RETURN TO THE ASYNCHRONOUS ROUTINE
*
* TABLES/WORK AREAS- DECB, DCB, IOB AND EXTENSION, DCB WA (DCW),
*        SEE DSECTS AT END OF MODULE FOR FORMAT AND DESCRIPTIONS
*
* ATTRIBUTES- RE-ENTRANT. DISABLED UPON ENTRY AND EXIT. ENABLED WHILE
*        CHANNEL PROGRAMS ARE BEING INITIALIZED.
* NOTES - NONE
         EJECT
* GENERAL REGISTERS ARE USED AS FOLLOWS
*
R0       EQU   0                        WORK REGISTER
R1       EQU   1                   *    IOB
R2       EQU   2                 # *    DECB
R3       EQU   3                   *    -----
R4       EQU   4                 # *    DCB
R5       EQU   5                        CHANNEL PROGRAM POINTER
R6       EQU   6                        WORK REGISTER
R7       EQU   7                        WORK REGISTER
R8       EQU   8                   *    DCB WA (DCW)
R9       EQU   9                        WORK REGISTER
R10      EQU   10                       WORK REGISTER
R11      EQU   11                  *    RETURN ADDRESS, SUB-ROUTINES
R12      EQU   12                # *    BASE REGISTER
R13      EQU   13                # *    RETURN ADDRESS
R14      EQU   14                  #*  ADDR OF SAVE AREA         Y02072
R15      EQU   15                       WORK REGISTER
*
*        # MEANS THAT THESE REGISTERS ARE SET UP BEFORE ENTRY
*            FROM SVC ROOT, AND MAY NOT BE DISTURBED
*        * MEANS THAT THESE REGISTERS ARE SET UP BEFORE ENTRY FROM
*           ASYNCHRONOUS ROUTINE, AND MAY NOT BE DISTURBED
         EJECT
         USING *,R12                    BASE IS IN REG 12
         USING IHADECB,R2               DECB IS IS REG 2
         USING IHADCB,R4                DCB IS IN REG 4           P4700
         USING IHADCW,R8                DCB WORK AREA IN REG 8    P4700
         USING IHAIOB,R1                IOB IS IN REG 1           P4700
         USING IGGSAVE,R14              SAVEAREA ADDRESSABILITY  Y02072
         SPACE 2
         B     QHNQXB30                                           P4700
PADDING  DC    CL12'IGG019JX3271'       PADDING TO PROVIDE      YM03934
*                                       INTERFACE WITH ASYNC RT YM03934
         B     WKNS5B1                  CHART S5
QHNQXB30 L     R4,DECBDCBA              USE DECB POINTER TO DCB   P4700
         L     R8,DCBWKPT2              USE DCB PTR TO WORK AREA  P4700
         L     R1,DCWFIOBE              ADDRESS OF FIRST IOB      P4700
         LA    R1,0(R1)                 CLEAR HIGH ORDER BYTE    A26738
QHNQXB6  LTR   R1,R1                    IS THIS A VALID ADDRESS   P4700
         BZ    QHNQHB30                 NO - ISSUE GETMAIN        P4700
         L     R6,IOBECBAD                                        P4700
         LA    R6,0(R6)                 ADDRESS OF DECB           P4700
         CLR   R2,R6                    DECB SOUGHT               P4700
         BE    QHNQXB7                  BRANCH IF DECB FOUND      P4700
         L     R1,IOBFCHAD              FORWARD CHAIN IOB         P4700
         B     QHNQXB6                  TEST NEXT IOB             P4700
QHNQXB7  IC    R9,DCWFIOBE              SAVE ERROR QUEUE COUNT   A26738
         LA    R9,1(R9)                 ROOM FOR ONE MORE        A26738
         STC   R9,DCWFIOBE              STORE NEW VALUE          A26738
         L     R6,IOBFCHAD              LAST IOB ON QUEUE        A26738
         LTR   R6,R6                    (ADDRESS = 0)             P4700
         BZ    QHNQXB2                  BRANCH IF YES             P4700
         MVC   IOBBCHAD-IHAIOB(4,R6),IOBBCHAD  NOT LAST IOB       P4700
         B     QHNQXB1                  BRANCH TO SEE IF FIRST IOBP4700
QHNQXB2  MVC   DCWLIOBE,IOBBCHAD        BACKWARD CHAIN - LAST IOB P4700
QHNQXB1  L     R6,IOBBCHAD              IS THIS FIRST IOB         P4700
         LTR   R6,R6                    (ADDRESS = 0)             P4700
         BZ    QHNQXB14                 BRANCH IF YES             P4700
         MVC   IOBFCHAD-IHAIOB(4,R6),IOBFCHAD  NOT FIRST IOB      P4700
         B     QHNQHB8                  BRANCH TO CLEAR IOB       P4700
QHNQXB14 MVC   DCWFIOBE+1(3),IOBFCHAD+1 FORWARD CHAIN FIRST IOB  A26738
         B     QHNQHB8                  BRANCH TO CLEAR IOB       P4700
QHNQHB30 EQU   *                        *
         L     R1,IGGPDEB               VALIDATED DEB ADDR       Y02072
         LA    R0,SP250LN               ALLOW FOR BASIC IOB      Y02072
         USING IHADEB,R1                                         S20201
         TM    DEBRPSID,RPS             TEST IF RPS DEV PRESENT  S20201
         BZ    QHNQHB31                 IF NOT, BRANCH           S20201
         AH    R0,RPSCCW                ELSE ALLOW FOR RPS CCWS  S20201
QHNQHB31 EQU   *                        *                        S20201
         SPACE 2
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
         SPACE 2
         STM   R2,R15,IGGREGS           SAVE REGISTERS 2-15      Y02072
         IC    R2,IGGUKEY               USERS PROTECT KEY        Y02072
         DROP  R14                      END SAVE AREA USING      Y02072
         LR    R9,R14                   SAVE SAVE AREA ADDR      Y02072
         L     R4,DEBTCBAD              TCB ADDRESS              Y02072
         USING PSA,R0                   LOW CORE ADDRESSABILITY  Y02072
         L     R7,PSAAOLD               ASCB ADDRESS             Y02072
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=1  CHANGE TO KEY ZERO     Y02072
         SPACE 2
         GETMAIN  RU,LV=(0),SP=SP250,KEY=(R2),BRANCH=YES   IOB   Y02072
         SPACE 2
         USING IGGSAVE,R9               SAVEAREA ADDRESSABILITY  Y02072
         MODESET  KEYADDR=IGGUKEY,WORKREG=2 CHANGE TO USER KEY   Y02072
         SPACE 2
         LM    R2,R15,IGGREGS           RESTORE REGISTERS 2-15   Y02072
         DROP  R9                       END SAVE AREA USING      Y02072
         USING IGGSAVE,R14              SAVEAREA ADDRESSABILITY  Y02072
         USING IHAIOB,R1                IOB ADDRESSABILITY       Y02072
QHNQHB8  XC    IOBFLAG1(56),IOBFLAG1    CLEAR TO ZEROES           P4700
         USING IHAWKNCP,R5              CHANNEL PROGRAM WILL BE IN REG5
         ST    R2,IOBECBAD              INITIALIZE ECB POINTER
         ST    R2,IOBBCHAD              SAVE ECB                 A33533
         ST    R4,IOBDCBAD-1            INITIALIZE DCB PTR
         TM    DCWWKNI,X'80'            WKNI ON IF WKN IS IN PROCESS
         BO    WKNN2E2                  POSTPONE PROCESSING
         OI    DCWWKNI,X'80'            OK TO INITIATE THIS WKN
         NI    IOBINDCT,X'BF'           SET UNSCHEDULED BIT OFF
         ST    R2,IOBBCHAD              SAVE ECB                 A33533
         BAL   R11,WKNS5B1              SET UP FIRST CHANNEL PROGRAM
         B     WKNN2J2                  RETURN TO SVC ROOT
WKNN2E2  L     R6,DCWLIOBU              CHECK LAST IOB ON UNSCHED Q
         LTR   R6,R6                    ZERO IF NONE ON Q YET
         BZ    WKNN2E22                 THIS WILL BE THE FIRST
         ST    R1,IOBFCHAD-IHAIOB(R0,R6) SET UP FWD CHAIN IN PREV IOB
         B     WKNN2E24                 FIRST IOB ON Q IS STILL FIRST
WKNN2E22 ST    R1,DCWFIOBU              THIS IOB IS FIRST ON Q
WKNN2E24 ST    R6,IOBBCHAD              PREV IOB IS BACKWARD CHAIN ADDR
         ST    R1,DCWLIOBU              THIS IOB IS LAST ON Q
         XC    IOBFCHAD,IOBFCHAD        ZERO OUT FWD CHAIN ADDR
         IC    R6,DCWNUWKN              ONE MORE IOB ON Q
         LA    R6,1(R0,R6)
         STC   R6,DCWNUWKN              ADD 1 TO DCWNUWKN
         MVI   IOBUNSQR,X'10'           UNSCHED REASON - WKN IN EFFECT
         OI    IOBINDCT,X'40'           SET ON UNSCHED BIT IN IOB
         TM    DCBRECFM,DCBRECF         FIXED LENGTH FORMAT     YM01327
         BO    WKNN2J2                  BR IF FIXED RECORDS     YM01327
         SR    R1,R1                    VLR - SET IOB REG TO    YM01327
*                                       ZERO, IND. IOB UNSCHED  YM01327
*                                       FLAG ON                 YM01327
WKNN2J2  BR    R13                      RETURN TO SVC RT (SIMULATED)
         EJECT
*                             SET UP FIRST CHANNEL PROGRAM
WKNS5B1  EQU   *        *
         NI    DCWWKNI,X'EF'            SET ADD-TO-END INDICATOR OFF
         TM    DCWHIAV,X'40'            ON IF HI LEVEL INDEX IN CORE
         BO    WKNS5B3                  SEARCH HI LEVEL INDEX IN CORE
         MVC   IOBDADAD(7),DCBFTHI      SET SEEK ADDRESS TO MBBCCHHX
         MVI   IOBDADAD+7,X'00'         R IS ZERO
WKNS5D1  CLI   DCWNLSD,X'00'            ZERO IF TRACK INDEX IS HIGHEST
         BE    WKNS5E1                  SET UP CP 8
         MVI   IOBAPP,X'07'             SET APPENDAGE CODE TO 7
         L     R5,DCBWKPT1              PUT CP1/CP2 ADDRESS IN REG 5
         ST    R5,IOBCCWAD              CCW 0 RELATIVE ADDRESS
         LA    R6,C01                   START OF CP1              17516
         ST    R6,IOBSTART-1
         MVI   IOBFLAG1,X'42'
         LA    R6,IOBDADAD+3                                      17516
         STH   R6,C01+2                                           17516
         SRL   R6,16                                              17516
         STC   R6,C01+1                                           17516
         MVC   C1+1(3),DECBKEY+1        ADDR OF KEY TO CP1 AND CP2,C30
         MVC   C4+1(3),DECBKEY+1        ADDR OF KEY TO CP1 AND CP2,C33
         CLI   DCWNLSD,X'02'            ONE IF NO MASTER INDEX
         BL    WKNS5G2                  NO MASTER - USE CP2
         MVC   C11+1(3),DECBKEY+1       ADDR OF KEY TO CP1
         MVC   C14+1(3),DECBKEY+1       ADDR OF KEY TO CP1
         BE    WKNS5F2                  TWO IF ONLY ONE MASTER
         NI    C6+4,X'BF'               TWO OR MORE MASTERS - NO CHAIN
         B     WKNS5G2                  TO EXIT
WKNS5F2  OI    C6+4,X'40'               ONLY ONE MASTER - CHAIN
WKNS5G2  EQU   *        *
         BR    R11                      RETURN FROM SUBROUTINE
WKNS5E1  MVI   IOBAPP,X'08'             SET APPENDAGE CODE TO 8
         L     R5,DCBWKPT3              PUT CP8 ADDRESS IN REG 5
         ST    R5,IOBCCWAD              CCW 0 RELATIVE ADDRESS
         LA    R6,CB1                   START OF CP8
         ST    R6,IOBSTART-1
         MVI   IOBFLAG1,X'42'
         LA    R6,IOBDADAD+3
         STH   R6,CB1+2
         SRL   R6,16
         STC   R6,CB1+1                 SEEK-SEARCH ADDRESS AT CB1
         MVC   CB5+1(3),DECBKEY+1       ADDR OF KEY TO CP8
         MVC   CB19+ONE(THREE),DECBKEY+ONE                       S20201
         TM    DCBRECFM,X'80'           IS IT VLR                 VLR
         BZ    WKNS5G2                  BRANCH IF IT IS          S20201
         MVC   CB23+THREE(FIVE),IOBDADAD+THREE CCHH OF FIRST     S20201
*                                       TRACK, R=                S20201
         OC    CB23+FIVE(THREE),DCBFIRSH SET HHR TO FIRSH        S20201
         B     WKNS5G2
WKNS5B3  SR    R6,R6                    CLEAR WORK REGISTER
         IC    R6,DCBKEYLE              KEY LENGTH IN LOW ORDER BYTE
         BCTR  R6,R0                    COMPARE KEY LENGTH BYTES
         L     R7,DECBKEY               POINTER TO STORAGE ARGUMENT
         L     R10,DCBMSHI              POINTER TO INDEX ARGUMENT
WKNS5B31 EX    R6,WKNS5B34              MODIFY COMPARE LENGTH
         BNL   WKNS5C3                  FIRST HI OR EQUAL
         AR    R10,R6                   INCREMENT IS KEYLE-1
WKNS5B32 LA    R10,11(R0,R10)           PLUS 11 MORE (= KEYLE+10)
         B     WKNS5B31                 TRY AGAIN
WKNS5B34 CLC   0(0,R10),0(R7)           LENGTH, INDEX ARG, STORAGE ARG
WKNS5C3  AR    R10,R6                   NOW POINT TO FLAG BYTE MINUS 9
         TM    9(R10),X'28'             TEST IF DUMMY CHAINED
         BO    WKNS5B32                 SKIP ENTRY - TRY AGAIN
         TM    9(R10),X'20'             TEST IF INACTIVE OR DUMMY END
         BO    WKNS5C5                  ADD TO END OF DATA SET
         MVC   IOBDADAD(7),1(R10)       SET SEEK ADDRESS TO MBBCCHH
         MVI   IOBDADAD+7,X'00'         R IS ZERO
         B     WKNS5D1                  CONTINUE
*
*              SET UP CP15
*
WKNS5C5  EQU   *
         MVC   IOBDADAD(3),DCBLPDA      MBB
         MVC   IOBDADAD+3(4),DCBLETI    CCHH
         OC    IOBDADAD+6(1),DCBFIRSH+3 TRACK MASK
         XC    IOBDADAD+6(1),DCBFIRSH+3 REDUCE TO CYL BNDRY OR ZERO
         MVI   IOBDADAD+7,0
         MVI   IOBAPP,X'0E'             SET APPENDAGE CODE TO 14
         L     R5,DCBWKPT3              PUT CP8 ADDRESS IN REG 5
         ST    R5,IOBCCWAD              CCW 0 RELATIVE ADDRESS
         TM    DCBRECFM,X'80'           TEST VLR                  VLR
         BO    WKNSVAR                  B=FIXED                   VLR
         LA    R6,CI1-LCP10B+CP14EOFX   OMIT 10B, ADD 14 EOFX    S20201
         B     WKNSVAR1                                           VLR
WKNSVAR  TM    DCBOPTCD,X'80'                                     VLR
         BZ    *+8                      BZ IF NO, TO BYPASS INCREMENT
         LA    R5,WC(R0,R5)             ALLOW FOR WRITE CHECK    S20201
*                                       CCWS                     S20201
         LA    R6,CI1-WC                START OF CP15            S20201
WKNSVAR1 EQU   *                                                 S20201
         ST    R6,IOBSTART-1
         USING CP15,R6                                           S20201
         MVI   IOBFLAG1,X'42'
         MVC   CI5+TWO(FIVE),DCBLETI    COUNT ADDR               S20201
         MVC   CI5(TWO),DCBLPDA+ONE     SET UP BB FOR HEAD SEEK  S20201
         LA    R5,IOBDADAD+THREE        IOBSEEK                  S20201
         STH   R5,CI1+TWO                                        S20201
         SRL   R5,SIXTEEN                                        S20201
         STC   R5,CI1+ONE               PUT SEEK ADDR IN CI1     S20201
         B     WKNS5G2                  EXIT
*
ONE      EQU   1                        1                        S20201
TWO      EQU   2                        2                        S20201
THREE    EQU   3                        3                        S20201
FIVE     EQU   5                        5                        S20201
RPS      EQU   X'E0'                    RPS MASK--BIT0=P,1=I,2=O S20201
SIXTEEN  EQU   16                       USED IN SHIFTING         S20201
RPSCCW   DC    H'16'                    LENGTH OF RPS CCWS       S20201
SP250LN  EQU   56                       IOB IS 56 BYTES IN SP250 Y02072
SP250    EQU   250                      SUBPOOL 250 FOR GETMAIN  Y02072
KEY0     DC    X'00'                    ZERO PROTECT KEY         Y02072
*
         DS    0F                       ALIGNMENT                S20201
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
         EJECT
*              MODULE WORK AREAS
*              DCB
         DCBD  DSORG=(IS)
         EJECT
*              DCW
IHADCW    IGGBISAM
         EJECT
IHAIOB    IGGIOBD
         EJECT
IHAWKNCP IGGWKNCP  OPTCD=W                                       S20201
WC       EQU   LCH3C+LCH8C+LCH13C+LCH22C+LCB54C+LCH18C WRITE CHK S20201
LCP10B   EQU   CB55+8-CP10B             LENGTH OF CP10B          S20201
CP14EOFX EQU   72                       CP14 VLR EOF EXTENSION   S20201
         EJECT
IHADEB   IGGDEBD
         EJECT
*              DATA EVENT CONTROL BLOCK
IHADECB  DSECT
         DS    0F
DECBECB  DS    CL4                      EVENT CONTROL BLOCK (ECB)
DECBTYP1 DS    BL1                      TYPE B6 - 1 IF LENGTH IS S
*                                            B7 - 1 IF AREA IS S
DECBTYP2 DS    BL1                           B0 - 1 IF READ K
*                                            B1 - 1 IF READ KX
*                                            B2 - 1 IF READ KU
*                                            B4 - 1 IF WRITE K
*                                            B5 - 1 IF WRITE KN
DECBLGTH DS    CL2                      LENGTH OF BLOCK
DECBDCBA DS    A                        POINTER TO DCB
DECBAREA DS    A                        ADDRESS OF AREA
DECBLOGR DS    A                        POINTER TO LOGICAL RECORD
DECBKEY  DS    A                        POINTER TO KEY
DECBEXC1 DS    BL1                      EXCPTN CD B0-RECORD NOT FOUND
*                                                 B1-RECORD LGTH CHK
*                                                 B2-NO SPACE
*                                                 B3-INVALID REQUEST
*                                                 B4-UNCORRECTABLE IO
*                                                 B5-UNREACHABLE BLOCK
*                                                 B6-OVERFLOW RECORD
*                                                 B7-DUPLICATE
DECBEXC2 DS    BL1                                B7-READ KU
         SPACE 2
*        SAVE AREA FOR ASYNCHRONOUS AND PRIV. ROUTINES
IGGSAVE  DSECT
IGGRETRN DS    A                        RETURN ADDRESS
IGGPDEB  DS    A                        VALIDATED DEB ADDR
IGGUKEY  DS    X                        USER PROTECT KEY
IGGRESRV DS    XL3                      RESERVED
IGGREGS  DS    16F                      REGISTER SAVE AREA
IGGSIZE  EQU   *-IGGSAVE                LENGTH OF AREA
         EJECT
CVT      DSECT
         CVT
         EJECT
         IHAPSA
         END
