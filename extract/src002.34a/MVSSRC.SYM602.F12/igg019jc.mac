         TITLE 'IGG019JC-BISAM CHECK MODULE'
IGG019JC CSECT
*
*          RELEASE OS/VS2-02 DELETIONS
*          RELEASE 20 DELETIONS/CHANGES
*          RELEASE 19 DELETIONS/CHANGES
* 522000-542000                                                  I19116
* 420000                                                         A26741
*                                                                A26738
*
*STATUS CHANGE LEVEL 002
*
*
*MODULE TYPE-BISAM SUBROUTINE LOADED BY BISAM OPEN EXECUTOR IGG0192I
*        PROBLEM PROGRAM MODE, REENTRANT
*
*FUNCTION-TO TEST PREVIOUS EXCP STATUS FOR:
*        1)NORMAL COMPLETION RETURNS TO THE PROBLEM PROGRAM.
*        2)IF ERROR CONTROL PASSES TO DCB-SYNAD
*
*ENTRY POINT - IGG019JC - CHECK MACRO EXPANSION
*
*ENTRY BALR 14,15
*        REGISTER 1   ADDRESS OF DECB
*        REGISTER 13  USER SAVE AREA ADDRESS
*        REGISTER 14  RETURN ADDRESS WITHIN BISAM ROUTINE
*        REGISTER 15   ADDRESS OF IGG019JC
*
*EXITS
*        SYNAD BY BR 15
*
*ATTRIBUTES-REENTRANT,REUSABLE
*
R0       EQU   0                        CONTAINS IOB+2 ON EXIT
PARR     EQU   1                        CONTAINS DECB ADDR. FOR CHECK
DCBR     EQU   2                        DCB ADDRESS
TESTR    EQU   3                    CONTAINS DECB ADDRRESS        7M172
BASR     EQU   5                        BASE FOR CHECK
R6       EQU   6                        WORK REGISTER
R7       EQU   7                        WORK REGISTER
R8       EQU   8                        ADDRESS OF WORK AREA
SAVR     EQU   13                       ADDRESS OF USER SAVE AREA
RETR     EQU   14                       RETURN ADDR. OF CALLING ROUTINE
ENTR     EQU   15                       ENTRY POINT-LOADED FROM DCB+76
         EJECT
         USING IGG019JC,BASR
         USING IHADCB,DCBR
         USING IHAIOB,R7
         USING IHADECB,PARR
*
***********************************************************************
*
         STM   DCBR,12,28(SAVR)         SAVE REGISTERS 2 - 12
         LR    BASR,ENTR                ESTABLISH BASE REG
         L     DCBR,8(PARR)             ADDRESS OF DCB
         L     R8,232(DCBR)             ADDRESS OF WORK AREA (QUEUES)
         USING IHADCW,R8
         LR    TESTR,PARR                                         7M172
STATUS    TM   0(PARR),X'40'            TEST ECB STATUS
         BO    CHECK                     BRANCH WITH COMPLETE
*
*   ECB IS NOT COMPLETE -- ISSUE THE WAIT MACRO AND RETEST
*
         WAIT  ECB=(1)                  WAIT ON ECB
         LR    PARR,TESTR                                         7M172
         B     STATUS                   RE-TEST STATUS INDICATORS
CHECK    TM    DECBEXC1,X'FD'            TEST  ERROR BITS
         BZ    RETURN    NORMAL RETURN TO USER
*
***********************************************************************
*
         L     R7,DCWFIOBE              FIRST IOB ON ERROR QUEUE
         LA    R7,0(R0,R7)              CLEAR HI BYTE (SLOT NO)  A26738
CON0     LTR   R7,R7                                              MC1F
         BZ    CON2                     BR IF IOB NOT ON ERROR Q A26741
         L     R6,IOBECBAD                                        MC1F
         LA    R6,0(R6)                 ADDRESS OF DECB
         CLR   PARR,R6                  DECB SOUGHT
         BE    CON1                     BRANCH IF DECB FOUND
         L     R7,IOBFCHAD              FORWARD CHAIN IOB
         B     CON0                     TEST NEXT IOB
CON2     SR    R0,R0                    CLEAR IOB REGISTER       A26741
         B     CON3                     GO TO SYNAD              A26741
CON1     LA    R0,2(R7)                 IOB ADDRESS PLUS TWO
CON3     EQU   *                                                 A26741
         TM    DCBSYNAD+3,X'01'           TEST FOR SYNAD RTN
         BO    ABENDC            GO TO SYNAD RTN
         L     ENTR,DCBSYNAD
         LM    DCBR,12,28(SAVR)         RESTORE REGISTERS 2 - 12
         BR    ENTR                     BRANCH TO SYNAD ROUTINE
*
***********************************************************************
*
RETURN   LM    DCBR,12,28(SAVR)         RESTORE REGISTERS 2 - 12
         BR    RETR                     RETURN TO USER
ABENDC   EQU   *                                                 I19116
         L     R0,ABCODE                                         I19116
         LR    PARR,DCBR                                         I19116
         SVC   55                                                I19116
         DS    0F                                                I19116
ABCODE   DC    X'00001000'                                       I19116
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
         EJECT
*
***********************************************************************
*
*  DSECTS AND WORK AREAS
*
***********************************************************************
*
         CNOP  0,8
         DCBD  DSORG=IS,DEVD=DA
         EJECT
IHADCW   DSECT                          DCB WORK AREA
         DS    0F
DCWFCP4  DS    A
DCWFCP7  DS    A
DCWNUCPS DS    CL1
DCWNUCP4 DS    CL1
DCWNUCP7 DS    CL1
DCWNLSD  DS    CL1
DCWFIOBU DS    A
DCWLIOBU DS    A
DCWFUPDI DS    A
DCWLUPDI DS    A
DCWHIAV  DS    CL1
DCWWKNI  DS    BL1
DCWNLEVC DS    CL1
DCWNUWKN DS    CL1
DCWMSHIL DS    A
DCWNACT  DS    CL1
DCWSIZE  DS    H
DCWOPCLS DS    D
DCWFIOBE DS    A                        ADDR FIRST IOB ON ERROR QUEUE
DCWLIOBE DS    A                        ADDR LAST  IOB ON ERROR QUEUE
         SPACE
         SPACE
IHADECB  DSECT
         DS    0F
DECBECB  DS    CL4
DECBTYP1 DS    BL1
DECBTYP2 DS    BL1
DECBLGTH DS    CL2
DECBDCBA DS    A
DECBAREA DS    A
DECBLOGR DS    A
DECBKEY  DS    A
DECBEXC1 DS    BL1
DECBEXC2 DS    BL1
         EJECT
IHAIOB   DSECT
         DS    0F
IOBFLAG1 DS    CL1
IOBFLAG2 DS    CL1
IOBSENSE DS    CL2
IOBECBAD DS    A                        ADDRESS OF DECB
IOBCSW   DS    CL8
IOBSIOCC DS    CL1
IOBSTART DS    CL3
IOBWGHT  DS    CL1
IOBDCBAD DS    CL3
IOBRESTA DS    A
IOBBCTIN DS    CL2
IOBERRCT DS    CL2
IOBDADAD DS    CL8
IOBCCWAD DS    A
IOBINDCT DS    CL1
IOBUNSQR DS    CL1
IOBAPP   DS    CL1
IOBASYN  DS    CL1
IOBCOUNT DS    0CL1
IOBFCHAD DS    A                        FORWARD CHAIN IOB
IOBBCHAD DS    A                        BACKWARD CHAIN IOB
         END
