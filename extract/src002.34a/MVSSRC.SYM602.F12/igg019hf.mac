         TITLE 'IGG019HF - SETL I(D) MODULE'
IGG019HF CSECT
*        RELEASE OS/VS2-02 DELETIONS                             Y02072
*        RELEASE 20 DELETIONS                                    Y02072
*2081017000,056600,067400,067600,067800,068000,068200,068400,    A35340
*2081068600,068800,069000,069200,069400,069600,069800,070000,    A35340
*2081070200,070400,070600,070800,071000,071200,071400,071600,    A35340
*2081071800,072000,072200,072400,072600,072800,073000,073200,    A35340
*2081073400,073600,073800,074000,074200,074400,074600,074800,    A35340
*2081075000,075200,075400,075600,075800,076000,076200,076400,    A35340
*2081076600,076800,077000,077200,077400,077600,077800,078000,    A35340
*2081078200,078400,078600,078800,079000,079200,079400,079600,    A35340
*2081079800,080000,080200,080400,080600,080800,081000,081200,    A35340
*2081081400,081600,081800,082000,082200,082400,082600,082800,    A35340
*2081083000,083200,083400,083600,083800,084000,084200,084400,    A35340
*2081084600,084800,085000,085200,085400,085600,085800,086000,    A35340
*2081086200,086400,086600,086800,087000,087200,087400,087600,    A35340
*2081087800,088000,088200,088400,088600,088800,089000,089200,    A35340
*2081089400,089600,089800,090000,090200,090400,090600,090800,    A35340
*2081091000,091200,091400,091600,091800                          A35340
*2081056600,058000-058200,058400-065200                          S20201
*                                                                S21045
*        VS2  RELEASE 030 PTF
*A053900                                                       @ZA07593
*
*                                                                     *
*STATUS CHANGE LEVEL 003
*                                                                     *
*FUNCTION/OPERATION- THIS MODULE IS USED TO LOCATE THE RECORD AT WHICH*
*   THE USER DESIRES TO BEGIN SEQUENTIAL RETRIEVAL OF HIS DATA SET,   *
*   WHEN HE KNOWS THE 8 CHARACTER DIRECT ACCESS DEVICE ADDRESS -      *
*   (MBBCCHHR). IT INITIALIZES WORK AREA FIELDS AND READS THE FIRST   *
*   RECORDS WHICH WILL BE MADE AVAILABLE TO THE USER BY SUBSEQUENT    *
*   GETS.                                                             *
*ENTRY POINTS- 'IGG019HI' (SISSIA2) IS THE ENTRY POINT FOR AN ISAM    *
*   SETL I(D) MACRO. THE GENERATED CALLING SEQUENCE IS-               *
*   SETL DCBNAME,I,LIMIT           OR   SETL DCBNAME,ID,LIMIT         *
*   LA   1,DCBNAME                      LA   1,DCBNAME                *
*   LA   0,LIMIT                        LA   0,LIMIT                  *
*   L    15,DCBSETL(0,1) DCB+76         L    15,DCBSETL(0,1)          *
*   BALR 14,15                          BALR 14,15                    *
*                                                                     *
*INPUT- REGISTER 0, BYTE 0, TYPE OF SETL- 28=ID, 20=I, BYTES 1-3,     *
*   ADDRESS OF FIELD CONTAINING 8 BYTE DEVICE ADDRESS, REGISTER 1,    *
*   DCB ADDRESS, REGISTER 14, RETURN ADDRESS, REGISTER 15, ENTRY POINT*
*   OF SETL I MODULE, REGISTER 13, SAVE AREA ADDRESS.                 *
*                                                                     *
*OUTPUT- SAME AS INPUT FOR NORMAL EXIT. OUTPUT ON ERROR EXIT, PROPER  *
*   DCBEXCD BIT SET, REGISTER 15, USERS SYNAD ROUTINE ADDRESS.        *
*                                                                     *
*EXTERNAL ROUTINES- 'IGG019HB' SUBROUTINES.'SISCTSGE'(EINFO) TO INFORM*
*   USER OF BAD WRITE ON RECORD OF A PREVIOUS SCAN.                   *
*                                                                     *
*   'SISCTSB'(SCHEDULE) TO SET UP AND INITIATE THE READING OF DATA    *
*   RECORDS STARTING AT THE USER SPECIFIED 8 BYTE DEVICE ADDRESS.     *
*                                                                     *
*EXITS-NORMAL-'SISSIEX' RETURNS TO THE USER TO THE NEXT SEQUENTIAL    *
*   INSTRUCTION AFTER THE SETL MACRO EXPANSION. RECORDS BEGINNING AT  *
*   THE USER SPECIFIED 8 BYTE DEVICE ADDRESS HAVE BEEN READ.          *
*                                                                     *
*   ERROR-'SISSIBAD' IS THE EXIT TO THE USERS SYNCHRONOUS ERROR       *
*   ROUTINE IF THE REQUEST WAS INVALID.                               *
*   'SISSIC11' IS THE ABEND EXIT IF NO SYNCHRONOUS ERROR ROUTINE WAS  *
*   SPECIFIED BY THE USER AND THE REQUEST WAS INVALID.                *
*                                                                     *
*TABLES AND WORKAREAS- DATA EXTENT BLOCK(DEB) DESCRIBED BY SISDEB     *
*   DSECT, WORKAREA DESCRIBED BY SISWORK1 DSECT, DATA CONTROL BLOCK   *
*   DESCRIBED BY IHADCB DSECT.                                        *
*                                                                     *
*ATTRIBUTES- REENTRANT, READ ONLY.                                    *
*                                                                     *
*NOTES-REGISTER USAGE IS CONSISTENT IN ALL SCAN MODE MACRO TIME       *
*   MODULES.                                                          *
         EJECT
         USING SISDEB,R8
         USING SISCP22,R3
         USING IHADCB,R1
         USING SISWORK1,R2
         USING *,R15
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
SISCTSI  EQU   *
SISSIA2  STM   R2,R12,28(R13)           SAVE REGISTERS
         L     R2,DCBWKPT1              WORKPOINT AREA USES R2
         ST    R0,W1REGSAV
         OI    W1OSBIT2,X'02'           RELSE INDICATOR
         TM    W1REGSAV,X'20'           IS THIS AN ID TYPE OF SETL
         BO    SISSIA3                  YES
         TM    W1REGSAV,X'10'           NO, IS IT A B TYPE
         BZ    SISSIB1                  NO TAKE SYNAD EXIT
         L     R15,DCBGET               YES GET ADR OF SETL B RTN
         A     R15,GSETLB(R15)
         LM    R2,R12,28(R13)
         BR    R15                      GO TO SETL B
SISSIA3  EQU   *                                                 A35340
         TM    W1DCBFA,X'80'            TEST FOR DISP=SHR        A35340
         BZ    CNTINU                   BR IF NOT TO CONTINUE    A35340
*                                       RTN                      A35340
         SVC   54                       IF SO, ISSUE SVC TO      A35340
*                                       REFRESH                  A35340
*                                          DCB FIELDS AND RETURN
CNTINU   EQU   *                                                 A35340
         TM    W1ECBO,X'40'             ANY WRITES IN PROGRESS   A35340
         BO    SISSIB3                  NO
         LR    R6,R1                    YES, WAIT FOR COMPLETION
         LA    R1,W1ECBO
         LR    R3,R15
         WAIT  ECB=(1)
         LR    R15,R3
         LR    R1,R6                    RESTORE R1
SISSIB4  TM    W1OSBIT2,X'80'           ANY UNWRITEABLE RCDS
         BZ    SISSIC4                  NO
*                                       YES
         L     R11,DCBGET               FIND ADDR OF EINFO SUBRTN FROM
         A     R11,GEINFO(R11)       FROM VECTOR TABLE IN GET
         BALR  R9,R11                                                 *
         B     SISSIB2                                                *
SISSIC4  LH    R5,W1WRITEC              MOVE WRITE Q ENTRIES TO FREE Q
         LA    R4,W1WR1ST
         LA    R3,W1FR1ST
         L     R10,DCBGET               FIND ADDR OF QUEUE RTN FROM GET
         A     R10,GQUEUE(R10)       GET Q ADDR FROM VECTOR TABL
         BALR  R9,R10                                                 *
         B     SISSIB2
SISSIB3  CLI   W1WRITEC+1,X'00'        WRITE Q EMPTY?
         BNE   SISSIB4                 NO,CHECK FOR ERRORS
SISSIB2  NI    W1OSBIT1,X'FE'           TO BE SURE IOBO COMPLETE OFF
         TM    W1OSBIT1,X'80'
         BO    SISSIB1                  SCAN MODE IND ALREADY ON
*
*        CHECK  THAT LOWER IS CORRECT. LOWER, THE PHYSICAL LOCATION AT
*        WHICH THE USER HAS CHOSEN TO BEGIN, IS POINTED TO BY R0
*
         L     R6,W1REGSAV              SET R6 WITH ADDRESS OF LOWER
         SR    R7,R7                   CLEAR WORK REG
         IC    R7,0(R6)                GET LOWER M
         SLL   R7,4                    M X 16
         L     R8,DCBDEBAD             GET DEB ADDR
         LA    R7,32(R7,R8)            R7 IS POINTER TO EXTENT OF M
         MVC   1(2,R6),4(R7)           MOVE BB FROM DEB TO LOWER
         CLC   DCBLPDA(8),0(R6)         1) MUST NOT BE GREATER THN LPDA
         BL    SISSID2
         IC    R9,6(R6)                SAVE H FROM LOWER
         NC    6(1,R6),DCBFIRSH+3      MASK OUT ALL BUT TRK VALUE
         CLC   DCBLDT+1(1),6(R6)       IS TRK LESS THAN LDT
         STC   R9,6(R6)                RESTORE SAVED H
         BL    SISSID2
         CLI   0(R6),X'00'             IS M=0
         BE    SISSID2                 YES, BRANCH, INVALID, TOO LOW
         CLC   0(1,R6),DEBNPEE         IS M HIGHER THAN NUM OF PRIM EX
         BH    SISSID2                 YES, BRANCH, INVALID, TOO HI
         SPACE 2
*              M IS OK
*                                      IS START CCHH HIGHER THAN CCHH
         CLC   6(4,R7),3(R6)
         BH    SISSID2                  YES, INVALID LOWER ADDRESS
*                                       NO
*                                      IS FIRSH HR HIGHER THAN HR
         NC    6(1,R6),DCBFIRSH+3      MASK OUT ALL BUT TRK VALUE
         CLC   DCBFIRSH+1(2),6(R6)     FIRSH HR HIGHER THAN LOWER HR
         STC   R9,6(R6)                RESTORE H SAVED IN ABOVE ROUTINE
         BH    SISSID2                  YES, INVALID LOWER ADDRESS
*                                       NO
*                                      IS END CCHH LOWER THAN CCHH
         CLC   10(4,R7),3(R6)
         BL    SISSID2                  YES, INVALID LOWER ADDRESS
*                                       NO
         SPACE 3
*        LOWER IS OK
         SPACE 3
*        WHICH  CHANNEL PGM TYPE DO WE WANT
*
SISSIE5  LA    R8,SISSIE5B              PRESET R8 FOR ROUTINE TO SET
*                                       CP22S FOR 22B TYPE - DATA ONLY
         TM    DCBRECFM,X'10'           BLOCKED RECS
         BO    SISSIE3                  YES, BLOCKED ARE ALWAYS 22B
*                                        AND ARE SET THAT WAY IN OPEN
         SPACE
*                            UNBLOCKED - KEY AND DATA OR DATA ONLY
         TM    W1REGSAV,X'08'
         BO    SISSIE52                 DATA ONLY, USE 22B
*                                       KEY AND DATA DESIRED - 22A
*                             DOES BUFFER LENGTH ALLOW FOR THIS TYPE
         TM    W1OSBIT3,X'80'           THIS BIT IS SET ON BY OPEN
*                                        IF THERE IS ENOUGH ROOM IN
*                                        BUFFER FOR KEY AND DATA
         BZ    SISSIB1                  NOT ENOUGH ROOM, INVAL. REQUEST
         LA    R8,SISSIE5A              SET R8 WITH 22A RTN ADDR
         SPACE
*                                       ALL BUFFERS ARE ON FREE Q, SET
*                                       THEM TO PROPER FORM
SISSIE52 L     R3,W1FR1ST               SET R3 WITH ADDR OF 1ST CP
*                                       R3 IS BASE FOR CP22
         BR    R8                       R8 CONTAINS THE ADDRESS OF THE
*                                       ROUTINE TO INITIALIZE CP 22 IN
*                                       EITHER A OR B FORM
* CP22A RTN
         SPACE
SISSIE5A MVC   CN3(4),CN7+4             SET FOR 22A
         MVI   CN3,X'0E'                SET CODE TO READ-KEY-DATA
         MVI   CN2+4,X'20'              SET ON BIT 2 IN FLAG
         B     SISSIE53
* CP22B RTN
         SPACE
SISSIE5B LA    R7,CN4                  SET FOR 22B
         ST    R7,CN3                  MAKE A TIC FROM CN2
         MVI   CN3,X'08'               FOR CN3
         NI    CN2+4,X'00'              SET OFF ALL FLAG BITS
         SPACE 2
* COMMON ROUTINE
         SPACE
SISSIE53 CLC   CN5+1(3),SISSICNZ        ARE WE AT END OF Q
         BE    SISSIE3                  YES
         L     R3,CN5                   NO, SET BASE WITH NEXT CP ON Q
         BR    R8                       LOOP
*                                       SET UP CP25 AND IOBI
SISSIE3  L     R7,W1CP25PT
         MVC   W1IDAD(8),0(R6)         MBBCCHHR FROM LOWER TO IOB
         MVC   CN31(7,R7),0(R6)        MBBCCHH    FROM LOWER
         OC    CN31+6(1,R7),DCBFIRSH+3    AND OUT
         XC    CN31+6(1,R7),DCBFIRSH+3    TRACK VALUE
         MVI   CN31+7(R7),X'01'        R=1
         MVC   W1ICPS+1(3),W1CP25PT+1   SET POINTER TO CP25
         LR    R7,R1                    SAVE R1
         MVI   W1IEXTEN+1,X'04'         SET APP CODE FOR SETL
         NI    W1IF1,X'7F'              SET OFF DATA CHAIN BIT IN IOB
         LR    R3,R15                   SAVE 15
         SPACE 2
* THIS EXCP EXECUTES CP25 WHICH FINDS THE KEY OF THE REQUESTED RECORD
* AND READS THE CORRESPONDING TRACK INDEX ENTRIES INTO THE W AREA
         SPACE 2
SISSIG4  EXCP  W1IOBI
         LR    R15,R3
         TM    W1ECBI,X'40'             COMPLETE
         BO    SISSIG31                 YES
SISSIG3  WAIT  ECB=W1ECBI               NO, WAIT FOR COMPLETION
         LR    R15,R3                   RESTORE REGISTERS
SISSIG31 LR    R1,R7
SISSIH3  TM    W1OSBIT1,X'04'           WAS SEARCH SUCCESSFUL
         BZ    SISSIH2                  YES
         OI    DCBEXCD1,X'02'           NO, INDICATE IN DCB
         B     SISSIC1
SISSIH2  OI    W1OSBIT1,X'80'           SET ON SCAN MODE BIT
*                                       TO INDICATE THAT WORKAREA HAS
*                                       BEEN INITIALIZED BY THE SETL
         MVC   W1LPDR,0(R6)             SET LPDR=LOWER
         LH    R11,W1LPDR+6             SUBTRACT 1 FROM LPDR
         BCTR  R11,0
         STH   R11,W1LPDR+6
*                                       LPDR IS USED BY SCHEDULE AND
*                                       WILL READ THE 1ST RECORD
*                                       FOLLOWING LPDR
         OI    W1OSBIT2,X'20'           SET ON CYLINDER IND FOR SCHED
         IC    R9,W1LHH+1              SAVE H
         NC    W1LHH+1(1),DCBFIRSH+3   KEEP TRK VALUE ONLY
         CLC   W1LHH+1(1),DCBFIRSH+1   IS PRESENT TRACK SHARED
         STC   R9,W1LHH+1              STORE SAVED H
         BNE   SISSIH21                 TRACK
         CLI   DCBFIRSH+2,X'01'
         BE    SISSIH21                 NO
         OI    W1OSBIT2,X'10'           YES,SET INDICATOR
SISSIH21 L     R11,DCBGET               FIND ADDR OF SCHED SUBRTN
         A     R11,GSCHED(R11)
         BALR  R9,R11                   SCHEDULE READS
         LR    R7,R1
         LA    R1,W1ECBI                LOAD R1 WITH ECBI FOR WAIT
         LR    R3,R15                   SAVE REGISTERS
         WAIT  ECB=(1)
         LR    R15,R3
         LR    R1,R7                    RESTORE REGISTERS
         SPACE 2
*        SET CBF AND EOB TO FORCE ENTRY TO END OF BUFFER RTN
*        ON FIRST GET FOR CORRECTLY SETTING CBF/EOB FOR V,U,FB,FV RECS
         SPACE 2
         MVC   W1CBF,W1EOB
         LM    R2,R12,28(R13)           RESTORE REGS
SISSIEX  BR    R14                      EXIT TO USER
         SPACE 4
*        ERROR PROCESSING
SISSID2  OI    DCBEXCD1,X'40'           INVALID LOWER
         B     SISSIC1
         SPACE
SISSIB1  OI    DCBEXCD1,X'10'           SCAN MODE ALREADY INITIATED
*                                       OR WRONG TYPE OF SETL
*                            OR K-D REQUEST WITH INSUFFICIENT BUF LEN
SISSIC1  TM    DCBSYNAD+3,X'01'         WAS A SYNAD EXIT SPECIFIED
         BO    SISSIC11                 NO
         LA    R0,W1IOBI                LOAD REG 0             @ZA07593
         L     R15,DCBSYNAD
         LM    R2,R12,28(R13)
SISSIBAD BR    R15                      GO TO SYNAD RTN
SISSIC11 EQU   *                        ISSUE 031 ABEND, NO      Y02072
         ABEND X'031',DUMP,,SYSTEM      SYNAD RTN GIVEN          Y02072
         SPACE 5
         DS    0F
*                                  ***** CONSTANTS *****
SISSICNZ DC    X'00000000'
SISSICN2 DC    H'24'
SISSICN3 DC    X'0001'
         SPACE 3
CN31     EQU   96                       LABEL FOR A CP25 CCW     S20201
         SPACE 3
GQUEUE   EQU   16                       OFFSETS OF VECTOR TABLE IN HB
GSCHED   EQU   20
GEINFO   EQU   12
GSETLB   EQU   36
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
         EJECT
SISDEB   IGGDEBD
         SPACE 5
SISCP22  DSECT
CN1      DS    BL8                      CP22, CN1-CN7
CN2      DS    BL8
CN3      DS    BL8
CN4      DS    BL8
CN5      DS    BL8
CN6      DS    BL8
CN7      DS    BL8
         EJECT
SISWORK1 IGGSCAN              SCAN MODE WORK AREA                A35340
*                                  ****END OF FIXED LENGTH FIELDS ****
*                                  THE NO. OF CP 22'S IS DEPENDENT ON
*                                  THE NO. OF BUFFERS (DCBBUFNO)
         EJECT
         DCBD  DSORG=(IS)
         END
