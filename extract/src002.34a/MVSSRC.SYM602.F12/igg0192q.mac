         TITLE 'IGG0192Q - INITIALIZE WRITE KN CHANNEL PROGS'
IGG0192Q CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192Q                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, BISAM MODE WITH WKN                   *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = CONTINUE WKN INITIALIZATION WHETHER READ IS PRESENT OR   *
*            NOT, BY INITIALIZING CP15, CP16, CP17 (CP17W FOR WRITE   *
*            CHECK), AND FOR FIXED LENGTH RECORDS (FLR) CP10A (CP10AW *
*            FOR WRITE CHECK).                                        *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 680 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192Q                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0192M FOR  *
*              FLR, OR IGG0192X FOR VLR. RECEIVES CONTROL IN STORAGE  *
*              PROTECT KEY 5 AND PRIVILEGED STATE.                    *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = IF USER SUPPLIES WORK AREA, XCTL TO ISAM OPEN         *
*               EXECUTOR IGG0192O FOR FLR, OR IGG0192Z FOR VLR.       *
*               IF ISAM PROVIDES WORK AREA, XCTL TO IGG0192N.  XCTL   *
*               IN STORAGE PROTECT KEY 5.                             *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGBISAM - BISAM WORK AREA                          *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY.                                       *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET AND XCTL                                           *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
RE       EQU   00                       WORK REG
RF       EQU   01                       WORK REG
RDCB     EQU   02                       USER'S DCB
RBASE    EQU   03                       BASE REG
RG       EQU   04                       WORK REG
RPAR     EQU   05                  *    PARAMETER LIST ADDRESS
RWTG     EQU   06                  *    WHERE TO GO TABLE BEGINNING
RPARC    EQU   07                  *    CURRENT ENTRY,PARAMETER LIST
RWTGC    EQU   08                  *    CURRENT ENTRY, WTG TABLE
RTIOT    EQU   09                       TIOT ADDRESS
RUCB     EQU   10                       UCB ADDRESS
RA       EQU   11                       WORK REG
RB       EQU   12                       WORK REG
RC       EQU   13                       WORK REG
RD       EQU   14                       WORK REG
R15      EQU   15                       WORK REG
RCORE    EQU   RG                       DCB WA ADDRESS
*                   *     MEANS SET BEFORE ENTRY AND BEFORE EXIT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         L     RDCB,0(RPARC)            RDCB  ADDRESS OF DCB
         USING IHADCB,RDCB
         L     RCORE,DCBWKPT2           RCORE ADDRESS OF DCB WA
         USING IHADCW,RCORE
         L     RD,DCBWKPT3              RD    ADDRESS OF WRITE KN CP
         USING WTGDSECT,RWTGC           WTG ADDRESSABILITY       Y02072
         L     RC,OPENWA                OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,RC               WA ADDRESSABILITY        Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11  CHANGE TO USER KEY  Y02072
*
         L     RA,DXCCW9                CP CURRENTLY SET UP      Y02072
         L     RF,DXCCW10               CP ADDR IN MODULE        Y02072
         DROP  RC                       DROP REGISTER            Y02072
         USING CP15,RA                  CP15
         MVC   0(SZ15,RA),0(RF)         MOVE CP15 BASIC          S20201
*                                       STRUCTURE                S20201
OPNO7D5B L     RC,CI1A
         ALR   RC,RD                    MOVE AND INITIALIZE CP'S
         ST    RC,CI1A
         L     RC,CI1B
         ALR   RC,RD
         ST    RC,CI1B
         L     RC,CI1C                  BY RELOCATING ALL
         ALR   RC,RD
         ST    RC,CI1C
         L     RC,CI1E                  *                        S20201
         ALR   RC,RD                    *                        S20201
         ST    RC,CI1E                  *                        S20201
         L     RC,CI2
         ALR   RC,RD
         ST    RC,CI2                   REFERENCES TO ADDRESSES
         L     RC,CI3
         ALR   RC,RD
         ST    RC,CI3
         L     RC,CI4                   WITHIN EACH CHANNEL PROGRAM
         ALR   RC,RD
         ST    RC,CI4                   *                        S20201
         LA    RA,SZ15(RA)              *                        S20201
         LA    RF,SZ15(RF)              *                        S20201
         USING CP16,RA                  CP16
         MVC   0(SZ16,RA),0(RF)         MOVE CP16 BASIC          S20201
*                                       STRUCTURE                S20201
         L     RC,CJ2
         ALR   RC,RD
         ST    RC,CJ2                   MOVE BASIC STRUCTURE
         L     RC,CJ4                   *                        S20201
         ALR   RC,RD                    *                        S20201
         ST    RC,CJ4                   *                        S20201
         L     RC,CJ6                   *                        S20201
         ALR   RC,RD                    *                        S20201
         ST    RC,CJ6                   *                        S20201
         MVC   CJ9+ONE(THREE),DCBMSWA+ONE   *                    S20201
         SR    RC,RC                    ADDRESSES BY ADDING
         IC    RC,DCBKEYLE
         STH   RC,CJ3+6
         STH   RC,CJ7+SIX               *                        S20201
         LH    RC,DCBLRECL              BASE REGISTER RD          VLR
         TM    DCBRECFM,X'80'           TEST IF FIXED             VLR
         BO    OPN2QFIX                 B=FIXED                   VLR
         LA    RC,EIGHTEEN(RC)          LINK + BDW + RDW         S20201
         STH   RC,CJ10+6                                          VLR
         L     RC,DCBMSWA               FOR VLR CJ10 READS INTO   VLR
*                                       MSWA+20                   VLR
         LA    RC,20(RC)                                          VLR
         STH   RC,CJ10+2                                          VLR
         SRL   RC,16                                              VLR
         STC   RC,CJ10+1                                          VLR
         B     OPN2QALL                                           VLR
OPN2QFIX LA    RC,TEN(RC)               ADD TEN                  S20201
         STH   RC,CJ10+SIX              STORE LENGTH (LRECL+10)  S20201
         MVC   CJ10+ONE(THREE),DCBMSWA+ONE  MOVE IN ADDR         S20201
OPN2QALL LA    RF,SZ16(RF)              *                        S20201
         LA    RA,SZ16(RA)              *                        S20201
         USING CP17,RA                  CP17 OR BEGINNING OF CP17W
         MVC   0(SZ17,RA),0(RF)         MOVE CP17 BASIC          S20201
*                                       STRUCTURE                S20201
         L     RC,CK2
         ALR   RC,RD
         ST    RC,CK2
         L     RC,CK3                   MOVE AND RELOCATE
         ALR   RC,RD
         ST    RC,CK3
         L     RC,CK5
         ALR   RC,RD                    COMMANDS CONTAINING
         ST    RC,CK5
         L     RC,CK7
         ALR   RC,RD
         ST    RC,CK7                   ADDRESSES OF OTHER COMMANDS
         MVC   CK6+7(1),DCBKEYLE
         TM    DCBOPTCD,X'80'
         BO    OPNO7D5C                 BRANCH IF WC
         TM    DCBRECFM,X'80'           TEST IF FIXED             VLR
         BZ    OPN2QV1                  B=VARIABLE                VLR
         LA    RA,SZ17(RA)                                       S20201
         LA    RF,SZ17(RF)                                       S20201
         B     OPNO7D5D
OPN2QV1  MVI   CK7+4,X'00'              CC OFF IN CK7             VLR
         USING CP17W,RA                 CP17W
OPNO7D5C MVC   SZ17(SZ17WC,RA),SZ17(RF) INSERT WC COMMANDS       S20201
         L     RC,CK7B
         ALR   RC,RD
         ST    RC,CK7B                  INSERT WC COMMANDS,
         SR    RC,RC                     AND RELOCATE CK7B
         IC    RC,DCBKEYLE
         LA    RC,10(RC)                KEY LGTH&10 TO CK7C
         STH   RC,CK7C+6
         LA    RA,SZ17W(RA)                                      S20201
         LA    RF,SZ17W(RF)                                      S20201
         TM    DCBRECFM,X'80'           TEST IF FIXED             VLR
         BO    OPNO7D5D                 B=FIXED                   VLR
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         MVC   0(L'LOAD2Z,RWTGC),LOAD2Z IF VLR -2Z IS NEXT       Y02072
         B     OPNEND2M                 NO CP10 - GO TO  NEXT     VLR
         USING CP10A,RA                 CP10A,CP10AW
OPNO7D5D MVC   0(SZ10A,RA),0(RF)        MOVE CP10A BSIC          S20201
*                                       STRUCTURE                S20201
         L     RC,CB47
         ALR   RC,RD
         ST    RC,CB47
         L     RC,CB48                  MOVE MODULE  , RELOCATE
         ALR   RC,RD
         ST    RC,CB48
         MVC   CB49+7(1),DCBKEYLE       INITIALIZE CB49 & CB50
         MVC   CB50+6(2),DCBBLKSI
         LA    RA,SZ10A-EIGHT(RA)       *                        S20201
         LA    RF,SZ10A-EIGHT(RF)       *                        S20201
         TM    DCBOPTCD,X'80'
         BZ    OPNO7D5E                 BRANCH NOT WC
         MVC   EIGHT(SZ10AWC,RA),EIGHT(RF)   INSERT WC CCW'S     S20201
         USING CB500,RA                                          S20201
         L     RC,CB50B
         ALR   RC,RD
         ST    RC,CB50B                 MOVE AND INITIALIZE
         SR    RC,RC
         IC    RC,DCBKEYLE
         AH    RC,DCBBLKSI
         LA    RC,8(RC)
         STH   RC,CB50C+6               ADDITIONAL COMMANDS FOR WC
         LA    RA,SZ10AWC(RA)                                    S20201
         LA    RF,SZ10AWC(RF)                                    S20201
         USING CB51,RA                                           S20201
OPNO7D5E MVC   CB51+5(1),DCBKEYLE       COMPLETE CP10A
         MVC   CB51+6(2),DCBBLKSI
         LA    RA,EIGHT(RA)             *                        S20201
         LA    RF,EIGHT(RF)             *                        S20201
         LH    RC,DCBSMSW               TEST USER-SUPPLIED WA
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         LTR   RC,RC
         BNE   OPNO7D5K                 BRANCH IF YES
         MVC   0(L'LOAD2N,RWTGC),LOAD2N IGG0192N IS NEXT EXEC.   Y02072
         B     OPNEND2M
OPNO7D5K MVC   0(L'LOAD2O,RWTGC),LOAD2O IGG0192O IS NEXT EXEC.   Y02072
OPNEND2M L     RC,OPENWA                OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,RC               WA ADDRESSABILITY        Y02072
         ST    RA,DXCCW9                SAVE ADDR OF NEXT CP     Y02072
         ST    RF,DXCCW10               ADDR OF CP IN MODULE     Y02072
         DROP  RC                       END WA USING             Y02072
         LR    RCORE,RC                 OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,RCORE            WORK AREA ADDR           Y02072
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT NAME ON THE WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)          AND DCB ON THE PARAM TABLE
         CLC   0(2,RWTGC),OPNEXNAM      IF NAME ON TABLE SAME ID AS
         BCR   8,RBASE                   CURRENT LOAD, BRANCH TO REPEAT
         CLC   0(2,RWTGC),OPIDCNST     IF NOT END OF WTG TABLE BRANCH
         BNE   RELOOP                    TO TRY NEXT NAME
         LR    RPARC,RPAR               OTHERWISE, RETURN TO START OF
         LA    RWTGC,WAOFF(0,RWTG)       BOTH TABLES AND SEARCH FOR
ZCHECK   CLI   0(RWTGC),ZCNST            FIRST NON-ZERO ID
         BNE   TCTLRTN
         LA    RWTGC,WGOFF(0,RWTGC)
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       WHEN FOUND, SET UP PARAMETERS
*                                        TO CALL THAT MODULE
         LA    R15,DXCCW12
TRCTL    XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) GO TO NEXT EXEC    Y02072
WGOFF    EQU   8                        SIZE OF ONE ENTRY ON WTG TABLE
PLOFF    EQU   4                        SIZE OF ONE ENTRY ON PARAM TAB
OPNEXNAM DC    C'2Q'                    ID OF THIS OPEN EXECUTOR
OPIDCNST DC    C'0S'                    ID OF OPEN LOAD #7
ZCNST    EQU   X'00'                    ZERO - SIGNALS INACTIVE ENTRY
ONE      EQU   1                        MISC OFFSETS             S20201
THREE    EQU   3                        *                        S20201
SIX      EQU   6                        *                        S20201
EIGHT    EQU   8                        *                        S20201
TEN      EQU   10                       *                        S20201
EIGHTEEN EQU   18                       *                        S20201
WAOFF    EQU   32                       SIZE OF WA AT START OF WTG TAB
*
LOAD2N   DC    C'2N'                    ID OF MODULE IGG0192N    Y02072
LOAD2O   DC    C'2O'                    ID OF MODULE IGG0192O    Y02072
LOAD2Z   DC    C'2Z'                    ID OF MODULE IGG0192Z    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
         DCBD  DSORG=(IS)
IHADCW   IGGBISAM OPTCD=V                                        S20201
IHAWKNCP IGGWKNCP OPTCD=W                                        S20201
CP17W    EQU   CP17                     WRITE CHECK              S20201
CP10AW   EQU   CP10A                    WRITE CHECK              S20201
SZ17WC   EQU   SZ17W-SZ17               WRITE CHECK CCW'S        S20201
SZ10AWC  EQU   SZ10AW-SZ10A             WRITE CHECK CCW'S        S20201
WTGDSECT DSECT                                                   Y02072
IDTTR    DS    F                        IDTTR OF MODULE          Y02072
OPENWA   DS    A                        OPEN WORK AREA ADDRESS   Y02072
FORCORE  DSECT                                                   Y02072
         IECDSECT                                                Y02072
         END
