         TITLE 'IGG019JW - NON PRIV MACRO TIME, WRITE KN'
IGG019JW CSECT
*          RELEASE OS/VS2-02 DELETIONS
*          CHANGES SINCE VS2-3.7
*C010680,A010200,A010320-010440,A014925-014935                 @ZA28629
*A008550,A008580,A009484-009568,A014600                        @ZA30001
*          RELEASE 19 DELETIONS
*          RELEASE 20 DELETIONS
*2785                                                            M0088
*0875                                                            A33995
*0875011200                                                      A35340
*          RELEASE 21 DELETIONS
*2785                                                           SA52386
*
*STATUS CHANGE LEVEL 004
*
* FUNCTION/OPERATION- NON-PRIVILEGED MACRO-TIME ROUTINE FOR BISAM
*        WRITE KN.  TESTS DECB FOR OVER-RIDING LENGTH AND IF NONE,
*        GOES TO PRIVILEGED MACRO-TIME ROUTINE VIA QING SVC (SVC 54).
*        UPON RETURN FROM PRIVILEGED ROUTINE, WILL EXCP IF AN IOB HAS
*        BEEN SCHEDULED.
*
* ENTRY POINT- REL POS 0
* INPUT - N/A
* OUTPUT - N/A
* EXTERNAL ROUTINES - N/A
*
* EXITS- RETURN TO USER VIA REGISTER 14
*
* TABLES/WORK AREAS- DECB,      IOB AND EXTENSION - SEE DSECTS AT END
*        OF MODULE FOR LAYOUTS AND DESCRIPTIONS.
*
* ATTRIBUTES- RE-ENTRANT, NON-PRIVILEGED
*
* NOTES - NONE
* GENERAL REGISTERS ARE USED AS FOLLOWS
*
R0       EQU   0                        WORK REGISTER
R1       EQU   1                        DECB ON ENTRY, IOB ON EXIT
R2       EQU   2                        DECB
R3       EQU   3                        WORK REGISTER
R4       EQU   4                        WORK REGISTER
R5       EQU   5                        WORK REGISTER
R6       EQU   6                        WORK REGISTER
R7       EQU   7                        WORK REGISTER
R8       EQU   8                        WORK REGISTER
R9       EQU   9                        WORK REGISTER
R10      EQU   10                       WORK REGISTER
R11      EQU   11                       WORK REGISTER
R12      EQU   12                       WORK REGISTER
R13      EQU   13                       USER'S REGISTER SAVE AREA
R14      EQU   14                       RETURN ADDRESS
R15      EQU   15                       BASE REGISTER
DCBDEBAD EQU   44
DCBRECFM EQU   36                       VLR                       VLR
DCBLRECL EQU   82                                                 VLR
DCBKEYLE EQU   16                       KEY LENGTH              SA52386
DCBRKP   EQU   60                       RKP                    @ZA30001
MINLEN   EQU   4                        MIN REC LENGTH          SA52386
SIXTEEN  EQU   DCBKEYLE                 DECB WA LENGTH          SA52386
INVALID  EQU   X'10'                    INVALID REQUEST        @ZA30001
         EJECT
         USING *,R15                    BASE IS IN REG 15
         USING IHADECB,R1               DECB IS IS REG 1
WKNN1A2  SAVE  (14,12)                  SAVE REGISTERS 14-12
         LA    R2,0(R1)                 ISOLATE DECB ADDRESS      21006
         L     R5,DECBDCBA                                        VLR
***********************************************************************
*  COMPARE THAT DECBKEY AND RECORD KEY ARE EQUAL FOR ALL BUT
*    FIXED UNBLOCKED RECORDS WITH RKP=0
***********************************************************************
         L     R6,DECBAREA              PICK UP PTR TO RECORD     VLR
         SR    R10,R10                  CLEAR REG 10            SA52386
         CH    R10,DCBRKP(R5)           RKP=0                  @ZA30001
         BNE   WKNRKPN0                 NO-REC OK              @ZA30001
         TM    DCBRECFM(R5),X'80'       TEST FOR FIXED         @ZA30001
         BZ    WKNRKPN0                 NO, VLR                @ZA30001
         TM    DCBRECFM(R5),X'10'       IS IT FIXED BLOCKED    @ZA30001
         BZ    WKNRKP0                  FIXED,UNBLK,RKP=0      @ZA30001
WKNRKPN0 EQU   *                                               @ZA30001
         IC    R10,DCBKEYLE(R5)         GET KEY LENGTH         @ZA30001
         BCTR  R10,0                    KEYLENGTH-1            @ZA30001
         LA    R6,SIXTEEN(R6)           RECORD                 @ZA30001
         AH    R6,DCBRKP(R5)            RECORD + RKP           @ZA30001
         L     R8,DECBKEY               KEY OF DECB            @ZA30001
         EX    R10,COMPARE              COMPARE DECBKEY TO     @ZA30001
*                                       RECORD KEY             @ZA30001
         BNE   INVALIDK                 INVALID-KEYS NOT MATCH @ZA30001
WKNRKP0  EQU   *                                               @ZA30001
         TM    DCBRECFM(R5),X'80'       TEST IF FIXED          @ZA30001
         BO    WKNFIX                   YES-SKIP VLR           @ZA30001
***********************************************************************
*                   FOR VLR, VALIDATE RDW
***********************************************************************
         L     R6,DECBAREA              RESET R6 TO RECORD     @ZA30001
         IC    R10,DCBKEYLE(R5)         GET KEY LENGTH          SA52386
         LA    R10,MINLEN(R10)          MIN REC LENGTH          SA52386
         CH    R10,SIXTEEN(R6)          IS RDW < MIN LEN        SA52386
         BH    WKNN1C0                  YES, REC LENTH ERROR    SA52386
         CLC   16(2,R6),DCBLRECL(R5)    IS REC LNTH GTR THAN MAX  VLR
         BH    WKNN1C0                  YES, REC LENGTH ERROR     VLR
*
         CLC   DECBLGTH,WKNMYO          TEST IF WKN               VLR
         BE    WKNFIX                   DECBLGTH=0 MEANS WKN      VLR
*                                       WRITE KEY REPLACE
         NI    DECBEXC1,X'02'           RESET ALL BUT UPDATE AND  VLR
         NI    DECBEXC2,X'01'           OVERFLOW  BITS            VLR
         B     WKNFIX2                                            VLR
WKNFIX   XC    DECBEXC1(2),DECBEXC1     ZERO DECB EXCEPTION CODES VLR
WKNFIX2  XC    DECBECB,DECBECB          ZERO ECB                  VLR
         XC    DECBLOGR,DECBLOGR        ZERO LOGICAL RECORD POINTER
         B     WKNN1C3                  BRANCH AROUND          @ZA28629
*
INVALIDK EQU   *                                               @ZA28629
         OI    DECBEXC1,INVALID         MARK INVALID REQUEST   @ZA28629
         B     POSTDECB                 POST AND RETURN        @ZA28629
WKNN1C0  EQU   *                                                  VLR
         LA    R1,0(R1)                 CLEAR HIGH ORDER BYTE    M0088
         OI    DECBEXC1,X'40'           SET ON RECORD LENGTH INDICATOR
POSTDECB EQU   *                                               @ZA28629
         POST  (1)                      POST COMPLETION IN DECB
WKNN1C1  RETURN (14,12)                 RESTORE REGISTERS, RETURN
WKNN1C3  LR    R1,R5                                             A35340
         SVC   QING                     QING - PRIVILEGED MACRO  A35340
*                                       TIME                     A35340
*              RETURN FROM SVC RT
         DROP  R1
         USING IHAIOB,R1                IOB IS NOW IN REG 1
         USING IHADECB,R2
         TM    DECBECB,COMPLETE         IS THIS REQUEST DONE     A33995
         BO    WKNN1C4                  YES--DON'T EXCP          A33995
         LTR   R1,R1                    IOB ADDR ZERO IF VLR    YM01327
         BZ    WKNN1C4                  AND IOB PLACED ON UN-   YM01327
*                                       SCHEDULED QUEUE         YM01327
         TM    IOBINDCT,X'40'           CHECK UNSCHEDULED BIT
         BO    WKNN1C4                  IF ON RETURN              VLR
         LR    R10,R15                  SAVE BASE ADDRESS
CELLTEST SR    R6,R6
         IC    R6,IOBDADAD             GO TO CURRENT EXTENT
         SLL   R6,4                    LENGTH OF 1 EXTENT
         L     R5,DECBDCBA              ADDRESS OF DCB
         L     R5,DCBDEBAD(R5)          ADDRESS OF DEB FROM DCB
         LA    R5,32(R6,R5)            GO TO CORRECT EXTENT
         MVC   IOBDADAD+1(2),4(R5)     MOVE BB FROM DEB EXTENT
         TM    DECBEXC2,X'02'           HAS IZ SCHED THIS JOB     VLR
         BO    WKNN1C4                  PRIOR TO TEST; B=YES      VLR
         EXCP  IOBFLAG1                 EXCP USING IOB IN REG 1
         LR    R15,R10                  RESTORE BASE ADDRESS
WKNN1C4  NI    DECBEXC2,X'FD'           TURN OFF  SWITCH &  EXIT  VLR
         B     WKNN1C1                  RESTORE AND RETURN        VLR
*
COMPARE  CLC   0(0,R6),0(R8)            EXECUTED INSTRUCTION   @ZA30001
*
WKNMYO   DC    X'0000'                                            VLR
QING     EQU   54
COMPLETE EQU   X'40'                    ECB COMPLETION BIT       A33995
*
MODID    DC    C'IGG019JW'              MODULE NAME            @OZ28629
DATE     DC    CL8'&SYSDATE'            COMPILATION DATE       @OZ28629
FIX      DC    C'OZ28629'               LATEST FIX             @OZ28629
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
         EJECT
*              DATA EVENT CONTROL BLOCK
IHADECB  DSECT
         DS    0F
DECBECB  DS    CL4                      EVENT CONTROL BLOCK (ECB)
DECBTYP1 DS    BL1                      TYPE B6 - 1 IF LENGTH IS S
*                                            B7 - 1 IF AREA IS S
DECBTYP2 DS    BL1                           B0 - 1 IF READ K
*                                            B1 - 1 IF READ KX
*                                            B2 - 1 IF READ KU
*                                            B4 - 1 IF WRITE K
*                                            B5 - 1 IF WRITE KN
DECBLGTH DS    CL2                      LENGTH OF BLOCK
DECBDCBA DS    A                        POINTER TO DCB
DECBAREA DS    A                        ADDRESS OF AREA
DECBLOGR DS    A                        POINTER TO LOGICAL RECORD
DECBKEY  DS    A                        POINTER TO KEY
DECBEXC1 DS    BL1                      EXCPTN CD B0-RECORD NOT FOUND
*                                                 B1-RECORD LGTH CHK
*                                                 B2-NO SPACE
*                                                 B3-INVALID REQUEST
*                                                 B4-UNCORRECTABLE IO
*                                                 B5-UNREACHABLE BLOCK
*                                                 B6-OVERFLOW RECORD
*                                                 B7-DUPLICATE
DECBEXC2 DS    BL1                                B7-READ KU
         EJECT
*              IOB
IHAIOB   DSECT
         DS    0F
IOBFLAG1 DS    CL1            IOB
IOBFLAG2 DS    CL1
IOBSENSE DS    CL2
IOBECBAD DS    A
IOBCSW   DS    CL8
IOBSIOCC DS    CL1
IOBSTART DS    CL3
IOBWGHT  DS    CL1
IOBDCBAD DS    CL3
IOBRESTA DS    A
IOBBCTIN DS    CL2
IOBERRCT DS    CL2
IOBDADAD DS    CL8
*                             IOB EXTENSION
IOBCCWAD DS    A                        ADDRESS FIRST CHAN PROGRAM
IOBINDCT DS    CL1                      INDICATORS B0  REMOVE CP FROM Q
*                                                  B1  UNSCHEDULED BIT
*                                                  B2 OFLO RCD IN W
IOBUNSQR DS    CL1                      UNSCHED REASON B0 CP1 OR 2 BUSY
*                                                      B1 NO CP4-5-6
*                                                      B2 NO CP7
*                                                      B3 WKN IN EFFECT
IOBAPP   DS    CL1                      APPENDAGE CODE  0 READ CP4-5
*                                                       1 WRITE CP4-5
*                                                       2 CP7
*                                                       3 CP1 OR CP2
*                                                       5 CP6
*                                                       6 WRITE CHECK
*                                                 WKN   7 CP1 OR CP2
*                                                       8 CP8
*                                                       9 CP10A
*                                                      10 CP10B
*                                                      11 CP10B
*                                                      12 CP14
*                                                      13 CP14
*                                                      14 CP15
*                                                      15 CP16
*                                                      16 CP16
*                                                      17 CP17
*                                                      18 CP17
*                                                      19 CP17
*                                                      20 CP9/12/11/13A
*                                                      21 CP9/12/11/13B
*                                                      22 CP9C/CP123W
*                                                      23 CP10A
*                                                      24 X/CP12C/X/13C
IOBASYN  DS    CL1                      ASYNCHRONOUS    0 END CP456 OK
*                                                       1 DO AN EXCP
*                                                       2 END CP7 OK
*                                                       3 END CP1-2 OK
*                                                       4 END CP456 ERR
*                                                       6 END CP7 ERR
*                                                       7 END CP1-2 ERR
*                                                 WKN   8 COMPLETION
*                                                       9 CP14, SETUP 1
*                                                      10 CP14, SETUP 2
*                                                      11 CP14, SETUP 3
*                                                      12 CP14, SETUP 4
*                                                      13 CP14, SETUP 5
*                                                      14 CP14, SETUP 6
IOBCOUNT DS    0CL1                     WRITE CHECK ERROR COUNTER
IOBFCHAD DS    A                        FORWARD CHAIN ADDRESS
IOBBCHAD DS    A                        BACKWARD CHAIN ADDRESS
         EJECT
         END
