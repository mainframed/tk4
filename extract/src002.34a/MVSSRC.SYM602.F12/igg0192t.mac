         TITLE 'IGG0192T - OPEN,QISAM LOAD MODE,SET UP CP 20,21,NO WC'
IGG0192T CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192T                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, LOAD MODE WITHOUT WRITE CHECK         *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = WHEN WRITE CHECK NOT SPECIFIED, INITIALIZE CP20 AND CP21.*
*            IF FULL TRACK INDEX WRITE (FTIW) SPECIFIED, INITIALIZE   *
*            CP21 ONLY.  DELETE CHANNEL PROGRAM MODULE IGG019GE FOR   *
*            FIXED LENGTH RECORDS (FLR), OR IGG019IE FOR VLR.         *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 838 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192T                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0192S      *
*              WHEN WRITE CHECK HAS NOT BEEN SPECIFIED.  RECEIVES     *
*              CONTROL IN STORAGE PROTECT KEY 5 AND PRIVILEGED STATE. *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO COMMON OPEN EXECUTOR IGG0190S IN STORAGE      *
*               PROTECT KEY 5.  THIS CONCLUDES ISAM OPEN OPERATION.   *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY, DEB, AND IOB                          *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, DELETE, AND XCTL                                  *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
********************
* DCB REFERENCE    *
********************
         DCBD  DSORG=(IS)
         USING IHADCB,R1
         EJECT
********************
* DEB REFERENCE    *
********************
*
IHADEB   DSECT
         USING IHADEB,R8
         DS    0D
DEBNMSUB DS    0CL1
DEBTCBAD DS    A
DEBAMLNG DS    0CL1
DEBDEBAD DS    A
DEBOFLGS DS    0CL1
DEBIRBAD DS    A
DEBOPATB DS    0CL1
DEBSYSPG DS    A
DEBNMEXT DS    0CL1
DEBUSRPG DS    A
DEBPRIOR DS    0CL1
DEBECBAD DS    A
DEBPROTG DS    0BL1
DEBDEBID DS    0BL1
DEBDCBAD DS    A
DEBEXSCL DS    0CL1
DEBAPPAD DS    A
DEBNIEE  DS    0CL1
DEBFIEAD DS    A
DEBNPEE  DS    0CL1
DEBFPEAD DS    A
DEBNOEE  DS    0CL1
DEBFOEAD DS    A
         DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    A
DEBBINUM DS    CL2
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
         EJECT
ISLCOMON IGGLOAD
         USING ISLCOMON,R12                                      S20201
         SPACE 2
* IOBBCT REFERENCE         C(ISLVPTRS+8)=A(IOBBCT)
*
IOBBCT   DSECT
         USING IOBBCT,R11
         DS    0D
IOBFLAGS DS    0CL1                     FLAGS
IOBPTRA  DS    A                        PTR A
IOBB     DS    0CL1                     B
IOBPTRB  DS    A                        PTR B
IOBS     DS    0CL1                     S - STATUS FIELD FOR BUF NO 1
IOBABUF  DS    A                        A(BUF NO 1) - ADR OF BUF NO 1
         EJECT
LOADCPS  DSECT
         IGGLDCP RECFM=V                LOAD CHANNEL PROGRAM     S20201
*                                       MODULE                   S20201
         EJECT
****************************
* OPEN WORK AREA REFERENCE *
****************************
*
FORCORE  DSECT                          OPEN/CLOSE/EOV WORK AREA Y07072
         IECDSECT                                                Y07072
         EJECT
**********************************************************************
* ISL PUT OPEN #4
**********************************************************************
*
*
IGG0192T CSECT
TSTF800  BALR  R15,0
         USING *,R15
BASETAG  L     R1,0(RPARC)
         L     RCORE,4(RWTGC)
         L     R12,DCBWKPT1
         LR    RBASE,R15
         USING FORCORE,RCORE                                     Y07072
         STM   R0,R15,DXCCW1            SAVE REGISTERS           Y07072
         LR    R0,RCORE                 SAVE OPEN W/A ADDR       Y07072
         L     R10,DCBWKPT6
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11  SET USERS KEY       Y07072
*
         L     R11,8(R10)
         DROP  RCORE                    DROP ADDRESSABILITY      Y07072
*
* EQUATE SYMBOLIC REGISTERS
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RBASE    EQU   3
RCORE    EQU   4
RPAR     EQU   5
RWTG     EQU   6
RPARC    EQU   7
RWTGC    EQU   8
RD       EQU   14
RJ       EQU   15
BOBSW    EQU   X'08'                                             A34959
RESLD    EQU   X'20'                                             A34959
REMCAP   EQU   66                                                A34959
*
K1       EQU   1                        CONSTANT                 S20201
K2       EQU   2                        CONSTANT                 S20201
K3       EQU   3                        CONSTANT                 S20201
K4       EQU   4                        CONSTANT                 S20201
K6       EQU   6                        CONSTANT                 S20201
K7       EQU   7                        CONSTANT                 S20201
K8       EQU   8                        CONSTANT                 S20201
K10      EQU   10                       CONSTANT                 S20201
K18      EQU   18                       CONSTANT                 S20201
K26      EQU   26                       CONSTANT                 S20201
K35      EQU   35                       CONSTANT                 S20201
K36      EQU   36                       CONSTANT                 S20201
K44      EQU   44                       CONSTANT                 S20201
K5       EQU   5                        CONSTANT                 MXXXX
K54      EQU   54                       CONSTANT                 S20201
K62      EQU   62                       CONSTANT                 S20201
K100     EQU   100                      CONSTANT                 S20201
L1       EQU   1                        CONSTANT                 S20201
L2       EQU   2                        CONSTANT                 S20201
L100     EQU   100                      LENGTH                   S20201
         EJECT
*
* SKIP CP 20 INITIALIZATION IF FULL TRACK OPTION WRITE
*
TSTHSK   XC    TSTWK1C(8),TSTWK1C       CLEAR WORK FIELDS        S20201
         TM    36(R10),X'C0'            FTIW-SUCCESSFUL GETMAIN  S20201
         BNO   INITCP20                 NO - INIT CP20           S20201
*                                                                S20201
         L     R3,12(R10)               C(R3)=A(CP18)            S20201
         L     R4,20(R10)               C(R4)=A(CP20A)           S20201
         LTR   R4,R4                    IS THERE A CP20A         S20201
         BE    CP20B                    NO - CHECK FOR CP20B     S20201
*                                                                S20201
         L     R4,12(R4)                C(R4)=A(TIC TO CP18)     S20201
         ST    R3,0(R4)                 A(CP18) STORED IN TIC    S20201
         MVI   0(R4),X'08'              RESTORE COMMAND CODE     S20201
*                                                                S20201
CP20B    L     R4,40(R10)               C(R4)=A(CP20B)           S20201
         LTR   R4,R4                    IS THERE A CP20B         S20201
         BE    INITCP21                 NO - INITIALIZE CP21     S20201
*                                                                S20201
         L     R4,12(R4)                C(R4)=A(TIC TO CP18)     S20201
         ST    R3,0(R4)                 A(CP18) STORED IN TIC    S20201
         MVI   0(R4),X'08'              RESTORE COMMAND CODE     S20201
         B     INITCP21                 BR TO INITIALIZE CP21    S20201
*
* INITIALIZE CP20
*
INITCP20 EQU   *                                                 O19110
         LA    R2,ISLIOBA               C(R2)=A(IOBA)            O19110
         L     R9,0(R10)                C(R9)=A(AREA Y)
         L     R8,CQ1AD                 C(R8)=A(CP20 SKLTN, CQ1)
         L     R10,20(R10)              C(R10)=A(CP20 DEST, CQ1)
         SH    R10,TST8                 ALLOW FOR CQ0            S20201
         USING CQ0,R10                                           S20201
         MVC   CQ0(L100),0(R8)           MOVE CP20 SKELETON IN.  S20201
         MVC   CQ0+K100(CP20LEN-L100),K100(R8) *                 S20201
         TM    DCBRECFM,X'80'           IS IT VLR                 VLR
         BZ    TSTHSKV                  YES, BRANCH               VLR
*
         LA    R4,35(R2)                CQ1
         A     R4,CQ1                   ADR OF IOB+35            S20201
         ST    R4,CQ1                                            S20201
         LA    R4,CQ1                   CQ2                      S20201
         A     R4,CQ2                   ADR OF CQ1               S20201
         ST    R4,CQ2                                            S20201
         LA    R4,26(R9)                CQ4
         ST    R4,CQ4                   STORE ADR Y+26           S20201
*
         LA    R4,K36(R9)               CQ5                      S20201
         O     R4,CQ5                   CQ5                      S20201
         ST    R4,CQ5                                            S20201
         LA    R4,CQ5                   CQ6                      S20201
         O     R4,CQ6                                            S20201
         ST    R4,CQ6                   STORE ADR CQ5            S20201
         LA    R4,K44(R9)               CQ8                      S20201
         ST    R4,CQ8                   *     STORE ADR Y+44     S20201
*                                       *
         LA    R4,K54(R9)               CQ10                     S20201
         O     R4,CQ10                  *                        S20201
         ST    R4,CQ10                  *                        S20201
         LA    R4,CQ10                  CQ11                     S20201
         O     R4,CQ11                  *                        S20201
         ST    R4,CQ11                  *                        S20201
         LA    R4,K62(R9)               CQ12                     S20201
         O     R4,CQ12                  *                        S20201
         ST    R4,CQ12                  *                        S20201
*
*   PUT KEYLENGTH BASED CONSTANTS INTO THE CHANNEL PROGRAMS
*
         SR    R4,R4                    *                        S20201
         IC    R4,DCBKEYLE              COUNT = KEY LENGTH       S20201
         STC   R4,CQ3+K7                CQ3                      S20201
         STC   R4,CQ7+K7                CQ7                      S20201
         LA    R4,K10(R4)               COUNT = KEY LENGTH + 10  S20201
         STH   R4,CQ12+K6               CQ12                     S20201
*
         LA    R4,ISLRPSSS+K2           CQ0                      S20201
TSTCQ13  EQU   *                        *                        MXXXX
         O     R4,CQ0                   *     SET SECTOR         S20201
         ST    R4,CQ0                   *                        S20201
         LA    R4,ISLRPSSS+K2           CQ14B,CQT0               S20201
         O     R4,CQ14B                 *    SET SECTOR          S20201
         ST    R4,CQ14B                 *                        S20201
STCQ13  EQU   *                         *                        S20201
         LA    R4,CQ14A+K1              CQ13                     S20201
         A     R4,CQ13                  *                        S20201
         ST    R4,CQ13                  *                        S20201
         LA    R4,CQ1                   *                        S20201
         TM    DCBRECFM,X'80'           IS IT VLR                 VLR
         BZ    TSTCQ14V                 YES, BRANCH               VLR
         L     R4,DCBWKPT6              CQ14
         L     R4,12(R4)
TSTCQ14V EQU   *                                                  VLR
         A     R4,CQ14                  CQ14                     S20201
         ST    R4,CQ14                  *                        S20201
*
         LA    R4,K35(R2)               CQ15                     S20201
         O     R4,CQ15                  *                        S20201
         ST    R4,CQ15                  *    STORE ADR IOBA+35   S20201
         LA    R4,CQ15                  CQ16                     S20201
         O     R4,CQ16                  *                        S20201
         ST    R4,CQ16                  *    STORE ADR CQ15      S20201
         LA    R4,K18(R9)               CQ17                     S20201
         O     R4,CQ17                  *                        S20201
         ST    R4,CQ17                       STORE ADR Y+18      S20201
         LA    R4,K26(R9)               CQ19                     S20201
         ST    R4,CQ19                       STORE ADR Y+26      S20201
         LA    R4,K36(R9)               CQ21                     S20201
         O     R4,CQ21                                           S20201
         ST    R4,CQ21                       STORE ADR Y+36      S20201
         LA    R4,K44(R9)               CQ23                     S20201
         ST    R4,CQ23                       STORE ADR Y+44      S20201
*
         LA    R4,K54(R9)               CQ25                     S20201
         O     R4,CQ25                  *                        S20201
         ST    R4,CQ25                  *    STORE ADR Y+54      S20201
         SR    R4,R4                    *                        S20201
         IC    R4,DCBKEYLE              COUNT=KEYLENGTH          S20201
         STC   R4,CQ18+K7               CQ18                     S20201
         STC   R4,CQ22+K7               CQ22                     S20201
         LA    R4,K18(R4)               COUNT=KEYLENGTH+18       S20201
         STH   R4,CQ25+K6               CQ25                     S20201
         LA    R4,CQ13                  CQ26                     S20201
         O     R4,CQ26                  *                        S20201
         ST    R4,CQ26                       STORE ADR CQ13      S20201
         LA    R4,CQ30+K3               CQ27                     S20201
         O     R4,CQ27                  *                        S20201
         ST    R4,CQ27                  *    STORE ADR CQ30+1    S20201
         LA    R4,CQ27                  CQ28                     S20201
         O     R4,CQ28                  *                        S20201
         ST    R4,CQ28                  *    STORE ADR CQ27      S20201
         EJECT
*
* INITIALIZE CP21
*
INITCP21 EQU   *                                                 O19110
         LA    R2,ISLIOBB               C(R2)=A(IOBB)            O19110
         USING DXIOB,R2                 IOB B ADDRESSABILITY     Y07072
         L     R10,DCBWKPT6             C(R10)=A(VPTRS)
         L     R9,0(R10)                C(R9)=A(AREA Y)
         L     R8,CQ40AD                C(R8)=A(CP21 SKLTN, CQ40)
         L     R10,24(R10)              C(R10)=A(CP21 DEST, CQ40)
         USING CQ39A,R10                                         S20201
         IC    R5,IOBSIOCC              SAVE SIOCC               S20201
         ST    R10,IOBSIOCC             STORE A(CP21) IN IOBB CP Y07072
*                                       START                    S20201
         STC   R5,IOBSIOCC              RESTORE SIOCC            S20201
*
         DROP  R2                       DROP ADDRESSABILITY      Y07072
*
         SH    R10,TST8                 PT TO START OF MODULE    S20201
         MVC   CQ39A(CP21LEN),0(R8)     MOVE CP21 FROM SKLTN     S20201
*
         LA    R4,ISLRPSSS+K3            CQ39A                   S20201
         O     R4,CQ39A                 *                        S20201
         ST    R4,CQ39A                      STORE SECTOR        S20201
*                                       ADDRESS                  S20201
         LA    R4,K35(R2)               CQ40                     S20201
         O     R4,CQ40                  *                        S20201
         ST    R4,CQ40                  STORE IOB + 30 ADDRESS   S20201
         LA    R4,CQ40                  CQ41                     S20201
         O     R4,CQ41                  *                        S20201
         ST    R4,CQ41                  *    STORE ADR CQ40      S20201
         LA    R4,0(R9)                 CQ42                     S20201
         O     R4,CQ42                                           S20201
         ST    R4,CQ42                       STORE ADR Y         S20201
         LA    R4,K8(R9)                CQ44                     S20201
         ST    R4,CQ44                     STORE ADR Y+8         S20201
         MVC   CQ43+K7(L1),DCBKEYLE     CQ43 LENGTH = KEYLENGTH  S20201
*
         EJECT
*
* EXIT ********** EXIT ***************** EXIT *********** EXIT ********
*
         LR    RCORE,R0                 RESTORE OPEN W/A ADDR    Y07072
         USING FORCORE,RCORE            W/A ADDRESSABILITY       Y07072
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY         Y02072
*
         LA    R6,TSTMOD3               DELETE MODULE 'IGG019GE'  VLR
         TM    DCBRECFM,X'80'           IS IT FIXED               VLR
         BO    TSTEXIT                  YES, BRANCH               VLR
         LA    R6,TSTMOD3V              DELETE MODULE 'IGG019IE'  VLR
TSTEXIT  DELETE EPLOC=(R6)              DELETE CPS                VLR
         LM    R0,R15,DXCCW1            RESTORE REGISTERS        Y07072
         USING BASETAG,RBASE
         XC    0(5,RWTGC),0(RWTGC)      SET TO ZERO              O19110
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH=BEGINNING OF THIS MODULE
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BC    7,RELOOP                 BRANCH=NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BC    7,TCTLRTN                BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         LA    RJ,DXCCW12               INITIALIZE REG 15        Y07072
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
         EJECT
*     VLR CHANGES TO INITIALIZATION OF CP20
*
         USING CQ0,R10                                           S20201
TSTHSKV  EQU   *                                                  VLR
         LA    R4,CQ7                   CQ1                      S20201
         O     R4,CQ1                   SRCH ID EQ TO CCHHR IN   S20201
         ST    R4,CQ1                   * CQ7.                   S20201
*
         LA    R4,CQ1                   CQ2                      S20201
         A     R4,CQ2                   TIC TO                   S20201
         ST    R4,CQ2                   * CQ1.                   S20201
*
         LA    R4,CQ7                   CQ3                      S20201
         A     R4,CQ3                   WRITE TRACK CAPACITY     S20201
         ST    R4,CQ3                   * RECORD FROM CQ7.       S20201
*
         MVC   CQ5(L2),DCBLRECL         SET CP20 TO MAX LRECL    S20201
         TM    DCBST,RESLD              TEST FOR RESUME LOAD     A34959
         BNO   INITLOAD                 BR IF NOT                A34959
         TM    IOBFLAGS,BOBSW           IS BOB SW ON             A34959
         BO    INITLOAD                 BR IF ON, TRK CAP REQ'D  A34959
         MVC   REMCAP(2,R10),ISLWRSAV   REM CAP SAVED FROM 195G  A34959
         B     TSTCQ130                                          MXXXX
INITLOAD EQU   *                                                 A34959
         L     R4,DCBLRAN               A(DEV TBL)                VLR
         MVC   CQ9+K2(L2),K4(R4)        SAVE CAL OF TRK          S20201
TSTCQ130 EQU   *                                                 MXXXX
         LA    R4,CQ0+K5                CQ0 FOR SET SECTOR       MXXXX
         B     TSTCQ13                                            VLR
         SPACE 2
TST8     DC    H'8'                     CONSTANT                 S20201
         DS    0D
TSTMOD3  DC    C'IGG019GE'              CPS
TSTMOD3V DC    C'IGG019IE'                                        VLR
*
THISLOAD DC    C'2T'
OPNLOD7  DC    C'0S'                    ID OF MODULE IGG0190S    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         END
