         TITLE ' IGG019JG - BISAM SIO APPENDAGE FOR SELF-MODIFYING CHAN*
               PGMS '
IGG019JG CSECT
**********************************************************************
*                                                                    *
*              RELEASE OS/VS2-02 DELETIONS                       Y02072
*D                                                               Y02072
*                                                                    *
* STATUS - CHANGE LEVEL 1                                            *
*                                                                    *
* FUNCTION/OPERATION - THIS MODULE DISPLACES THE BISAM START I/O     *
*    MODULE IN AN VIRTUAL ENVIRONMENT IF VIRTUAL = REAL IS NOT       *
*    SPECIFIED. IT IS ENTERED AT START I/O TIME TO LINK TO IGG019JI  *
*    IF DYNAMIC BUFFERING IS REQUESTED OR TO IGG019JH FOR RPS DATA   *
*    SETS AFTER SPLITTING SELF-MODIFYING CHANNEL PROGRAMS AS NEEDED. *
*    IF NEITHER IS SPECIFIED, CONTROL IS RETURNED TO IOS.            *
*                                                                    *
* ENTRY POINTS -                                                     *
*         IGG019JG - START I/O TIME.                                 *
*         IGG019JG+4 - PAGEFIX, UNUSED.                              *
*                                                                    *
* INPUT -                                                            *
*                                                                    *
*    START I/O TIME -                                                *
*        MODULE RECEIVES CONTROL IN PROTECT KEY ZERO AND HOLDS       *
*        THE LOCAL LOCK.  REGISTER INPUT IS -                        *
*       REG1 - PTS TO RQE                                            *
*       REG2 - PTS TO IOB                                            *
*       REG3 - PTS TO DEB                                            *
*       REG4 - PTS TO DCB                                            *
*       REG7 - PTS TO UCB                                            *
*       REG13- PTS TO SAVE AREA                                      *
*       REG14- RETURN ADDRESS                                        *
*       REG15- BASE REGISTER                                         *
*                                                                    *
*    REGISTERS 10,11 & 13 AVAILABLE AS WORK REGS.                    *
*                                                                    *
* OUTPUT - SAME AS INPUT                                             *
*                                                                    *
* EXTERNAL ROUTINES - 'IGG019JH' ENTERED AT SIO TIME FOR RPS DATA    *
*     SETS. 'IGG019JI' ENTEREDED IF DYNAMIC BUFFERING SPECIFIED.     *
*                                                                    *
* EXITS,NORMAL - CONTROL IS ALWAYS RETURNED TO IOS TO                *
*     CONTINUE THE EXCP.                                             *
*                                                                    *
* EXITS,ERROR - NONE                                                 *
*                                                                    *
* TABLES/WORKAREAS- DCB, SCAN WORK AREA, SAVE AREA,DEB.              *
*                                                                    *
* ATTRIBUTES - REENTRANT,READ ONLY.                                  *
*                                                                    *
* NOTES - NONE.                                                      *
*                                                                    *
**********************************************************************
**********************************************************************
         SPACE 5
*
***********************************************************************
*                        GENERAL REGISTERS                            *
***********************************************************************
*
R0       EQU   00                       * WORK REG
R1       EQU   01                       * 12* ADDRESS
R2       EQU   02                       * IOB
R3       EQU   03                       * DEB
R4       EQU   04                       * DCB
RSAVE    EQU   05                       * SAVE AREA ADDRESS      Y02072
R6       EQU   06                       * -----
R7       EQU   07                       * UCB
R8       EQU   08                       * ----
R9       EQU   09                       # WORK REGISTER
R10      EQU   10                       PGFX LIST AND CP'S
R11      EQU   11                       WORK REGISTER
R12      EQU   12                       WORK REGISTER
R13      EQU   13                       WORK REGISTER
R14      EQU   14                       * IOS RETURN VECTOR TABLE
R15      EQU   15                       BASE REGISTER
*
**********************************************************************
*       * MEANS THIS REGISTER IS SET UP UPON ENTRY FROM IOS,         *
*         AND ITS CONTENTS MUST BE RESTORED UPON RETURN TO IOS.      *
*       # MEANS THIS REGISTER MAY BE CHANGED, BUT UPON RETURN TO IOS *
*         IT MUST CONTAIN ZEROES IN THE THREE HIGH ORDER BYTES.      *
**********************************************************************
*
         SPACE 2
         USING *,R15                    ADDRESSIBILITY
         B     SIOENTRY                 START I/O ENTRY          Y02072
         BR    R14                      PAGE FIX ENTRY, UNUSED,  Y02072
*                                       RETURN TO IOS            Y02072
         EJECT
*********************************************************************
*        THIS SECTION IS ENTERED AT SIO TIME. ITS PURPOSE IS TO     *
*        DETERMINE WHICH CHANNEL PROGRAM HAS TO BE FIXED            *
*        AND BRANCH TO THAT ROUTINE.  AFTER THE CHANNEL PROGRAM IS  *
*        FIXED CONTROL IS PASSED TO THE SIO MODULE OR BACK TO IOS.  *
*********************************************************************
         SPACE 1
         USING RQE,R1                   RQE ADDRESSABILITY       Y02072
         USING IHAIOB,R2                IOB ADDRESSABILITY
         USING IHADEB,R3                DEB ADDRESSABILITY
         USING IHADCB,R4                DCB ADDRESSABILITY
         USING IHADCW,13                WORK AREA ADDRESSABILITY
         SPACE 2
SIOENTRY ST    RSAVE,0(R13)             SAVE REGISTER            Y02072
         LR    RSAVE,R13                SAVE SAVE AREA ADDR      Y02072
         SPACE 2
         MODESET  KEYADDR=RQEPRT,WORKREG=13  CHANGE TO USER KEY  Y02072
         SPACE 2
         LA    R2,L0(R2)                CLEAR HI ORDER BYTE
         L     R13,DCBWKPT1             ADDR OF FIRST CP
         LA    R13,L0(R13)              CLEAR HI ORDER BYTE
         CR    R2,R13                   IS THIS CP87
         BE    IOSRETRN                 YES, RETURN TO IOS       Y02072
         L     R13,DCBWKPT2             ADDR OF WORK AREA
         L     R11,IOBSTART-L1          START ADDR OF CP
         CLI   L0(R11),SETSECT          CPSTART POINT TO PREFIX
         BNE   PGFXCPST                 NO
         L     R11,L8(R11)              START OF CP FROM PREFIX
PGFXCPST EQU   *                        *
         L     R10,IOBCCWAD             ADDR FIRST CCW IN CP
         LA    R10,L0(R10)              CLEAR HI ORDER BYTE
         LA    R11,L0(R11)              CLEAR HI ORDER BYTE
         CLI   IOBAPP,L0                APP 0
         BE    PGFXCP4                  YES - CP4
         CLI   IOBAPP,L1                APP 1
         BE    PGFXCP4                  YES - CP4
         CLI   IOBAPP,L3                APP 3
         BE    PGFXCP1                  YES - CP1 OR CP2
         CLI   IOBAPP,L5                APP 5
         BE    PGFXCP6                  YES - CP6
         CLI   IOBAPP,L7                APP 7
         BE    PGFXCP1                  YES - CP1 OR CP2
         CLI   IOBAPP,L8                APP 8
         BE    PGFXCP8                  YES - CP8
         CLI   IOBAPP,L21               APP 21
         BE    PGFXCP12                 YES - CP123
         CLI   IOBAPP,L22               APP 22
         BE    PGFXCP12                 YES - CP123
         B     PGFXSIOT                 IF THE IOB APPENDAGE CODE IS
*                                       NOT 0,1,2,3,5,7,8,21 OR 22 THEN
*                                       THE CHANNEL PROGRAM DOES NOT
*                                       HAVE TO BE SPLIT
         EJECT
*
*        CHANNEL PROGRAM ONE.
*
         USING CP1,R10
PGFXCP1  EQU   *                        *
         CR    R11,R10                  START AT CO1
         BNE   PGFXSIOT                 NO, CP O.K.
         CLI   DCWNLSD,L1               NLSD GREATER THAN ONE
         BNH   PGFXSIOT                 NO, CP2 BEING USED
         TM    C6+L4,CC                 C6 COMMAND CHAINED
         BZ    PGFXSIOT                 NO, CP ALL SET
         OI    C6+L5,CC                 INDICATE TO APP C6 WAS CHAINED
         NI    C6+L4,CCOFF              TURN OFF COMAND CHAIN
         B     PGFXSIOT                 GO TEST FOR SIO
         SPACE 4
*
*        CHANNEL PROGRAM FOUR
*
         USING CP4,R10
PGFXCP4  EQU   *                        *
         CR    R11,R10                  START AT CA01
         BNE   PGFXSIOT                 NO, CP ALL SET
         TM    CA10+L4,CC               IS CA10 COMMAND CHAINED
         BZ    PGFXSIOT                 NO, GO TEST FOR SIO
         OI    CA10+L5,CC               INDICATE CA10 WAS CHAINED
         NI    CA10+L4,CCOFF            TURN OFF COMMAND CHAIN IN CA10
         B     PGFXSIOT                 GO TEST FOR SIO
         EJECT
*
*        CHANNEL PROGRAM SIX
*
         USING CP6,R10
PGFXCP6  EQU   *                        *
         LA    R10,SZ4+SZ5(R10)         ADDRESSIBILITY ON CP6 HAS
         TM    DCBRECFM,VLR             VARIABLE LENGTH RECORDS
         BO    PGFXVLR                  YES
         TM    DCBOPTCD,WRTCHK          TEST FOR WC
         BZ    PGFXCP6A                 BZ MEANS NO
PGFXVLR  EQU   *                        *
         LA    R10,SZ5W-SZ5(R10)        ADD LENGTH OF CP5W
PGFXCP6A EQU   *                        *
         CR    R11,R10                  START AT CA26
         BNE   PGFXSIOT                 NO, CP O.K.
         CLI   CA28,SKEQHI              SEARCH KEY EQ OR HIGH
         BNE   PGFXSKEQ                 NO, SET SEEK TO SEEKCC
         MVI   CA36,SEEKHH              SET CA36 TO HEAD SEEK
         CLI   DCBLIOV+L7,L0            TEST FOR INDP OVERFLOW
         BE    PGFXSIOT                 NO, GO TEST FOR SIO
PGFXSKEQ EQU   *                        *
         MVI   CA36,SEEKCC              SET CA36 TO CYLINDER SEEK
         B     PGFXSIOT                 GO TEST FOR SIO
         SPACE 4
*
*        CHANNEL PROGRAM EIGHT
*
         USING CP8,R10
PGFXCP8  EQU   *                        *
         CR    R11,R10                  START AT CB1
         BNE   PGFXSIOT                 NO, CP O.K.
         TM    CB8A+L4,CC               CB8A COMMAND CHAINED
         BZ    PGFXSIOT                 NO, CP8 O.K.
         OI    CB8A+L5,CC               INDICATE CB8A WAS CHAIN
         NI    CB8A+L4,CCOFF            TURN OFF COMMAND CHAIN
         B     PGFXSIOT                 GO TEST FOR SIO
         EJECT
*
*        CHANNEL PROGRAM 123
*
         SPACE 1
PGFXCP12 EQU   *                        *
         TM    DCBRECFM,VLR             VLR DATA SET
         BO    PGFXSIOT                 YES, NO SPLIT NEEDED
         CLC   DCBSMSW(2),CON1          USER WORKAREA
         BNH   PGFXSIOT                 NO, CP NOT CP123
         TM    DCBOPTCD,WRTCHK          WRITE CHECK
         BZ    PGFXSIOT                 NO, CP123 FOR WC ONLY
         TM    DCBRECFM,BLKED           IS IT BLOCKED
         BO    PGFXSIOT                 YES, NO SPLIT NEEDED
         L     R10,CB2+L4               ADDR OF CP123
         LA    R10,L0(R10)              CLEAR HI ORDER BYTE
         CR    R10,R11                  START AT CP123
         BE    PGFXSIOT                 YES, CP ALL SET
         L     R11,IOBFCHAD             ADDR OF LAST CKD
         LA    R11,L0(R11)              CLEAR HI ORDER BYTE
         LTR   R11,R11                  CP9C
         BZ    PGFXSIOT                 YES, CP ALL SET
PGFXLOOP EQU   *                        *
         CLI   L8(R11),TIC              TIC TO CP123
         BE    PGFXOFF                  YES, SET OFF CC
         CR    R11,R10                  ARE WE AT CP123
         LA    R11,L8(R11)              MOVE TO NEXT CCW
         BL    PGFXLOOP                 NO, TRY NEXT CCW
         LR    R11,R10                  R11 = ADDR OF CP123
         SH    R11,CON8                 R11 = ADDR OF LAST CKD
PGFXOFF  EQU   *                        *
         TM    L4(R11),CC               IS LAST CCW COMAND CHAIN
         BZ    PGFXSIOT                 NO, CP ALL SET
         OI    L5(R11),CC               INDICATE UNCHAINED
         NI    L4(R11),CCOFF            UNCHAIN LAST CCW
         EJECT
**********************************************************************
*        THE FOLLOWING ROUTINE TESTS  TO SEE IF A SIO MODULE         *
*        HAS BEEN LOADED AND BRANCHES TO THAT MODULE.                *
*        OTHERWISE CONTROL IS RETURNED TO IOS VIA BR 14.             *
**********************************************************************
         SPACE 1
PGFXSIOT EQU   *                        *
         L     R11,DEBEXPTR             ADDRESS ISAM DEP SECTION
         USING DEBEXT,R11               *
         BAL   R10,RESETKEY             RESTORE SAVE AREA ADDR   Y02072
*                                       AND PROTECT KEY 0        Y02072
         L     R15,DEBSIOA2             ADDR OF DYM. BUF. SIO MOD
         LTR   R15,R15                  DYM. BUF. SIO MODULE LOADED
         BCR   YES,R15                  YES, BRANCH TO DYM. BUF. MOD
         L     R15,DEBRPSIO             ADDR OF RPS SIO MOD
         LTR   R15,R15                  RPS SIO MOD LOADED
         BCR   YES,R15                  YES, BRANCH TO RPS SIO MOD
RETURN   BR    R14                      RETURN TO IOS            Y02072
         SPACE 2
*        RE-ESTABLISH REGISTER SAVE AREA ADDRESS IN REGISTER 13 AND
*        CHANGE TO PROTECT KEY ZERO.
*
IOSRETRN LA    R10,RETURN               RETURN TO IOS            Y02072
RESETKEY LR    R13,RSAVE                SAVE AREA ADDR           Y02072
         SPACE 2
         MODESET  KEYADDR=KEYZERO,WORKREG=5  CHANGE TO KEY ZERO  Y02072
         SPACE 2
         L     RSAVE,0(R13)             RESTORE REGISTER 5       Y02072
         BR    R10                      EXIT                     Y02072
         EJECT
**********************************************************************
*                        EQUATES AND CONSTANTS                       *
**********************************************************************
         SPACE 1
CON1     DC    H'1'                     CONSTANT 1
CON8     DC    H'8'                     CONSTANT 8
KEYZERO  DC    X'00'                    STORAGE PROTECT KEY 0    Y02072
         SPACE 1
L0       EQU   0                        0
L1       EQU   1                        1
L2       EQU   2                        2
L3       EQU   3                        3
L4       EQU   4                        4
L5       EQU   5                        5
L6       EQU   6                        6
L7       EQU   7                        7
L8       EQU   8                        8
L9       EQU   9                        9
L10      EQU   10                       10
DYMBUF   EQU   L1                       MASK FOR DYM. BUF. TEST
YES      EQU   L2                       BC MASK
EQ       EQU   L8                       BE MASK
NLSD     EQU   11                       OFFSET OF NLSD IN WORK AREA
RPS      EQU   17                       DEVICE OFFSET
RPSTST   EQU   X'10'                    MASK BIT FOR RPS TEST
BLKED    EQU   X'10'                    BLOCKED MASK
L21      EQU   21                       21
L22      EQU   22                       22
AREA     EQU   X'20'                    MASK FOR REC IN DECBAREA
VLR      EQU   X'40'                    MASK FOR VLR RECORDS
CCOFF    EQU   X'BF'                    MASK TO TURN OFF CC
WRTCHK   EQU   X'80'                    MASK FOR WRITE CHECK
WRITEKN  EQU   X'04'                    WRITE KN REUEST BIT
HIDXCORE EQU   X'40'                    HI LEVEL IDX IN CORE BIT
BYP      EQU   4                        BYPASS RETURN FOR EXTENDED LIST
THIRTY2  EQU   32                       LGTH OF WA+DCB+IOB+DECB LIMITS
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
         EJECT
IHADCB   DCBD  DSORG=(IS)
DCBLEN   EQU   *-IHADCB                 DCB LENGTH
         EJECT
IHADCW   IGGBISAM  OPTCD=V
         EJECT
IHAIOB   IGGIOBD
IOBLEN   EQU   IOBCCW1-IHAIOB           IOB REGULAR LENGTH
IOBLEN1  EQU   IOBCCW2+8-IHAIOB         IOB LENGHT FOR RPS
         EJECT
IHADEB   IGGDEBD                        DEB
         EJECT
IGGWKNCP DSECT
CP1      DS    0D
         IGGCP1
         SPACE 2
CP4      DS    0D
         IGGCP4
         SPACE 2
         IGGCP5 OPTCD=W
         SPACE 2
CP6      DS    0D
         IGGCP6
         SPACE 2
CP8      DS    0D
         IGGCP8
         EJECT
         IECDRQE                                                 Y02072
         END
