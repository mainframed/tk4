         TITLE 'IGG019JI - BISAM DYNAMIC BUFFERING'
IGG019JI CSECT
*        RELEASE OS/VS2-02 DELETIONS
*                                                               YM01380
*                                                               YM01146
*                                                               YM01165
*          RELEASE 21 DELETIONS
*3512022850                                                      S21045
*3512016200,020800,032200,032800                                 A33533
*3512014600                                                       M0186
*3512                                                            M1792
*          RELEASE 19 DELETIONS
*1324010600,011200,011600,017100,024440,024500-024520,024780,    A26738
*1324024880-024903,024906-024978,033400-033600                   A26738
*          RELEASE 20 DELETIONS
*0751026000                                                      A36336
*0751022800,024984,024987-025000,032100,034200,041400,041600,    S20201
*0751041800-047000,047400,049008-049184,049450,049500-052000,    S20201
*0751052400,052600-053200,053400-053600,053800-054000            S20201
*
*STATUS CHANGE LEVEL 010
* FUNCTION/OPERATION' DYNAMIC BUFFERING ROUTINE FOR READ AND UPDATE.
*        1. WHEN A RECORD IS ABOUT TO BE READ, IF THE USER REQUESTS
*            DYNAMIC BUFFERING, A TEST IS MADE TO DETERMINE IF A BUFFER
*            IS AVAILABLE.  IF SO, POINTERS ARE SET TO THE BUFFER
*            AND PROCESSING OF THE MACRO CONTINUES.  IF NOT, THE IOB
*            IS PLACED ON A QUEUE OF IOB'S AWAITING BUFFERS AND
*            PROCESSING OF THIS IOB IS TEMPORARILY HALTED.
*        2. AFTER THE RECORD IS READ, THE USER MUST EITHER WRITE IT
*            BACK OR ISSUE A FREEDBUF MACRO.  WHEN THE WRITE HAS BEEN
*            COMPLETED OR WHEN THE FREEDBUF MACRO ISSUED, THIS ROUTINE
*            WILL TEST TO SEE IF ANY IOB IS AWAITING A BUFFER.  IF SO,
*            THE BUFFER IS ASSIGNED TO THE NEXT IOB AND PROCESSING OF
*            THAT IOB IS RESUMED.  IF NOT, THE BUFFER IS PLACED ON A
*            QUEUE OF AVAILABLE BUFFERS.
* ENTRY POINTS'
*        ENTRY PNT     CALLING ROUTINE                FUNCTION
*        ---------     ---------------                --------
*        BUFFREED   FREEDBUF MACRO              DESCRIBED IN 2. ABOVE
*        BUFSIO     SIO ENTRY FROM APPENDAGE    DESCRIBED IN 1. ABOVE
*                    ROUTINE
*        BUFASYN    ASYNCHRONOUS ROUTINE        DESCRIBED IN 2. ABOVE
* INPUT - N/A
* OUTPUT - N/A
* EXTERNAL ROUTINES - N/A
* EXITS' FROM BUFFREED RETURN TO FREEDBUF SVC ROOT VIA REGISTER 12
*        FROM BUFSIO   RETURN TO IO SUPERVISOR - NORMAL RETURN IF A
*             BUFFER HAS BEEN OBTAINED OR THE SKIP POSTING RETURN IF
*             IT HAS NOT.  SEE THE DSECT LABELED IHAAPPRV FOR THE
*             FORMAT AND USE OF THE RETURN VECTOR TABLE.
*        FROM BUFASYN  RETURN TO THE ASYNCHRONOUS ROUTINE VIA REGISTER
*             12.
* TABLES/WORK AREAS'  DECB, DCB, IOB AND EXTENTION, DCB WA (DCW), DEB,
*        BCB - SEE DSECTS AT END OF MODULE FOR FORMAT AND DESCRIPTIONS.
* ATTRIBUTES' REENTRANT. DISABLED.
* NOTES - NONE
* GENERAL REGISTERS ARE USED AS FOLLOWS
*                          FUNCTION  1                FUNCTION  2
R0       EQU   0    * -----                     WORK REGISTER
R1       EQU   1    * 12 STAR                 * COMPLETED IOB ON ENTRY
*                                                FROM ASYNCH ROUTINE,
*                                                ADDR OF NEXT IOB WILL
*                                                REPLACE IT TEMPORARILY
R2       EQU   2    * IOB                     * COMPLETED DECB
R3       EQU   3    * DEB                     * -----
R4       EQU   4    * DCB                     * DCB
R5       EQU   5    * -----                   * -----
R6       EQU   6    * -----                     WORK REGISTER
R7       EQU   7    * UCB                       WORK REGISTER
R8       EQU   8    * -----                     BASE REGISTER    A26738
R9       EQU   9    # WORK REGISTER           * -----
R10      EQU   10     WORK REGISTER             WORK REGISTER
R11      EQU   11     WORK REGISTER             WORK REGISTER    A26738
R12      EQU   12   * -----                     RETURN ADDRESS
R13      EQU   13     WORK REGISTER             WORK REGISTER    A26738
R14      EQU   14   * IOS VECTOR RETURN TABLE * ON FREEDBUF ENTRY -
*                                                RETURN ADDRESS
R15      EQU   15   * BASE REGISTER             BASE ADDRESS UPON ENTRY
READ     EQU   X'00'                    READ REQUEST             Y02072
*        * MEANS THIS REGISTER IS SET UP UPON ENTRY AND MAY NOT BE
*            DESTROYED BECAUSE OF THE NEEDS OF THE CALLING ROUTINE
*        # MEANS THIS REGISTER MAY BE CHANGED, BUT UPON RETURN MUST
*            BE IN THE FORM 000X BECAUSE OF THE NEEDS OF IOS
         USING IGG019JI,R15
         USING IHAIOB,R2
         USING IHAAPPRV,R14
         USING IHADCB,R4
         USING IHADEB,R3                                         S20201
*
*
BUFFREED B     ASYATA3                  BRANCH TO FREEDBUF MACRO
         USING *,R15                                              M0186
SIOAPP   SH    R15,FOUR                 ADJUST BASE TO COMMON ADR M0186
         USING IGG019JI,R15                                       M0186
BUFSIO   B     APPAFB2                  BRANCH TO APPENDAGE ROUTINE
BUFASYN  B     ASYNCH                   ASYNCHRONOUS RTN ENTRY   Y02072
FOUR     DC    H'4'                     G.P. CONSTANT             M0186
         EJECT
*********************************************************************
*        SIO DYNAMIC BUFFERING - AN APPENDAGE ROUTINE.  CALLED
*        FROM IGG019JG IN PROTECT KEY ZERO, HOLDING THE LOCAL LOCK
*        AND REGISTER 13 POINTING TO A SAVE AREA.
*********************************************************************
*
APPAFB2  EQU   *                        APPENDAGE ROUTINE        Y02072
         USING RQE,R1                   RQE ADDRESSABILITY       Y02072
         SPACE 2
         MODESET  KEYADDR=RQEPRT,WORKREG=10  CHANGE TO USER KEY  Y02072
         SPACE 2
         CLI   IOBAPP,READ              THIS REQUEST A READ      Y02072
         BNE   APPRVNOR
         L     R10,IOBBCHAD             ADDR OF DECB             A33533
         USING IHADECB,R10
         TM    DECBTYP1,X'01'           DYN BUFF WANTED           VLR2
         BZ    APPRVNOR                 NORMAL RETURN TO IOS      VLR2
         L     R10,DECBAREA             IF AREA HAS BEEN OBTAINED
         LA    R10,0(R10)                                         VLR2
         DROP  R10                                                VLR2
         L     R9,DCBBUFCB               PICK UP ADDR OF  BCB     VLR2
         USING IHABCB,R9                                          VLR2
         LA    R4,0(R9)                                           VLR2
         CR    R10,R4                                             VLR2
         BL    APPAFB3              IF LOW,  PICK UP BUFFER       VLR2
         A     R4,BCBSIZE                                         VLR2
         CR    R10,R4                                             VLR2
         BNL   APPAFB3                  IF NOT LOW, BR TO GET    A26738
*                                       BUF                      A26738
         L     R4,IOBDCBAD-1           PICK UP DCB ADDR.          VLR2
         SR    R9,R9                                              VLR2
         SR    R10,R10                                            VLR2
         B     APPRVNOR               NORMAL RETURN TO IOS        VLR2
APPAFB3  EQU   *                                                  VLR2
         L     R4,IOBDCBAD-1           PICK UP DCB ADDR.          VLR2
         SR    R9,R9                                              VLR2
         SR    R10,R10                                            VLR2
         L     R10,DCBBUFCB                                       VLR2
         USING IHABCB,R10
         L     R9,BCBNAVB               R9   FIRST AVAILABLE BUFFER
         LTR   R9,R9                    IS THERE AN AVAILABLE BUFFER{
         BNE   APPAFE2
         L     R9,BCBLIOB               NONE.
         LTR   R9,R9                    R9   LAST IOB AWAITING BUFFER
         BNE   APPAFD31                 PLACE NEW IOB ON Q AWAITING BFR
         ST    R2,BCBFIOB
         B     APPAFD32
APPAFD31 ST    R2,IOBFCHAD-IHAIOB(R9)
APPAFD32 ST    R2,BCBLIOB
         XC    IOBFCHAD,IOBFCHAD
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=9  SET PROTECTION KEY 0   Y02072
         SPACE 2
         SR    R9,R9                    CLEAR R9 FOR IOS
         B     APPRVSKP                 SKIP POST IOS RETURN
         SPACE 2
APPAFE2  MVC   BCBNAVB,0(R9)            BUFFER AVAILABLE, REMOVE FROM Q
         DROP  R10
         L     R10,IOBBCHAD             ADDR OF DECB             A33533
         USING IHADECB,R10
         ST    R9,DECBAREA              INSERT ADDRESS OF AREA IN DECB
         DROP  R10
         L     R10,IOBCCWAD             R10  ADDRESS OF CHANNEL PROGRAM
         USING CP4,R10
         TM    DCBRECFM,X'80'           TEST FOR  FIXED           VLR
         BO    APPAFIX                  BRANCH FOR FIXED          VLR
         LA    R9,12(R9)                VLR- READ TO AREA+12      VLR
         B     APPAFIX2                                           VLR
APPAFIX  LA    R9,16(R9)                FIX-READ TO AREA+16       VLR
APPAFIX2 EQU   *                                        +16       VLR
         STH   R9,CA23+2                SET ADDRESS IN CHANNEL PROGRAM
         SRL   R9,16
         STC   R9,CA23+1
APPRVNOR TM    DEBRPSID,RPSFLAGS        IS RPS PRESENT           S20201
         BZ    IOSRETRN                 NO, RETURN TO IOS        Y02072
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=10  CHANGE TO KEYZERO     Y02072
         SPACE 2
         L     R15,DEBEXPTR              GET ADDR OF DEB         S21045
*                                       EXTENSION PTR            S21045
         USING DEBEXT,R15                                        S21045
         L     R15,DEBRPSIO              ADDR OF 2ND SIO APPEND  S21045
         BR    R15                      GO TO RPS APPENDAGE      S20201
         USING IGG019JI,R15             BASE ADDRESSABILITY      Y02072
         SPACE 2
IOSRETRN MODESET  KEYADDR=KEY0,WORKREG=10  CHANGE TO KEYZERO     Y02072
         SPACE 2
         BR    R14                      RETURN TO IOS            Y02072
RPSFLAGS EQU   X'E0'                                             S20201
         DROP  R2,R4,R10,R14                                     Y02072
         EJECT
**********************************************************************
*        ENTRY FROM FREEDBUF.   ENTERED IN STORAGE PROTECT KEY ZERO,
*        A VALIDATED DEB ADDRESS IS IN REGISTER 3, REGISTER 5 POINTS
*        TO THE SVRB, REGISTER 4 POINTS TO THE DCB, REGISTER 2 POINTS
*        TO THE DECB, AND THE USER'S PROTECT KEY HAS BEEN PLACED IN
*        THE SECOND BYTE OF THE EXTENDED SVRB REGISTER SAVE AREA.
**********************************************************************
         SPACE 2
         USING RBBASIC,R5               SVRB ADDRESSABILITY      Y02072
         USING IHADECB,R2
         USING IHADCB,R4
         USING IHABCB,R10
         USING IHAIOB,R1
         USING IHADCW,R7                                          VLR2
         SPACE 2
         USING IGG019JI,R15                                      S21045
ASYATA3  STM   R11,R15,RBEXSV3          SAVE REGS ACROSS LCK RTN Y02072
         SPACE 2
**********************************************************************
*    OBTAIN THE LOCAL LOCK.  WILL BE RELEASED PRIOR TO RETURNING TO
*    FREEDBUF ROUTINE (IGC0005G - SVC 57).
**********************************************************************
         SPACE 2
GLCL1    SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,  OBTAIN THE      Y02072*
               RELATED=(IOB,IGG019JI)            LOCAL LOCK      Y02072
         SPACE 2
         LM    R11,R15,RBEXSV3          RESTORE REGS 11-15       Y02072
         LR    R12,R14                  EXIT ADDR IN REG 12      Y02072
         SPACE 2
         MODESET  KEYADDR=RBEXUKEY,WORKREG=13 SET USER'S KEY     Y02072
         SPACE 2
         LR    R13,R3                   VALIDATED DEB ADDR,      Y02072
*                                       NONZERO R13 INDICATES    Y02072
*                                       FREEDBUF ENTRY           Y02072
*
         L     R7,232(0,R1)             WKAREA PTR IN REG 7       13264
         TM    DECBTYP2,X'20'           READ KEY FOR UPDATE       13264
         BZ    ASYATB9                                            VLR2
         LA    R11,DCWFUPDI             POINT TO UPDATE QUEUE    A26738
         BAL   R10,SCHQUEUE             SEE IF IOB IS HERE       A26738
         B     ASYATB9                  NO, TRY ERROR QUEUE      A26738
         B     ASYATB2                  YES, GO FREE BUFFER      A26738
*
*
*
SCHQUEUE L     R1,0(R11)                GET FIRST IOB            A26738
         LA    R1,0(R1)                 CLEAR HIGH ORDER BYTE    A26738
ASYATA31 LTR   R1,R1                    ANY IOB                  A26738
         BCR   8,R10                    BR, NO IOB FOUND         A26738
         L     R6,IOBECBAD                                        13264
         LA    R6,0(0,R6)               ZERO HIGH ORDER BYTE      13264
         LA    R2,0(0,R2)               ZERO HIGH ORDER BYTE      13264
         CLR   R2,R6                                              13264
         BE    ASYATA32                                           13264
         L     R1,IOBFCHAD                                        13264
         B     ASYATA31                                           13264
ASYATA32 L     R6,IOBBCHAD                                        13264
         LTR   R6,R6                                              13264
         BZ    ASYATA33                                           13264
         MVC   IOBFCHAD-IHAIOB(4,R6),IOBFCHAD                     13264
         B     ASYATA34                                           13264
ASYATA33 MVC   1(3,R11),IOBFCHAD+1      SET 1ST IOB PTR          A26738
ASYATA34 L     R6,IOBFCHAD                                        13264
         LTR   R6,R6                                              13264
         BZ    ASYATA35                                           13264
         MVC   IOBBCHAD-IHAIOB(4,R6),IOBBCHAD                     13264
         B     4(R10)                   FOUND IOB RETURN         A26738
ASYATA35 MVC   5(3,R11),IOBBCHAD+1      SET LAST IOB PTR         A26738
         B     4(R10)                   FOUND IOB RETURN         A26738
*
*
*
ASYATB9  LA    R11,DCWFIOBE             POINT TO ERROR QUEUE     A26738
         IC    R8,DCWFIOBE              SAVE ERROR QUEUE CONNT   A26738
         BAL   R10,SCHQUEUE             SEE IF IOB IS HERE       A26738
         B     ASYATB2                  NO, FREE BUFFER          A26738
         LA    R8,1(R8)                 ROOM FOR ONE MORE IOB    A26738
         STC   R8,DCWFIOBE              SAVE NEW COUNT           A26738
         O     R9,F8                    MAKE REGISTER NEGATIVE   A26738
         B     ASYATB5                                            VLR2
         DROP  R3,R7,R15                                         S20201
         EJECT
**********************************************************************
*        ENTRY FROM ASYNCHRONOUS ROUTINE.  ENTERED WITH LOCAL LOCK
*        HELD, IN USER PROTECT KEY, AND REGISTER 14 POINTER TO
*        A SAVE AREA (IGGBISAV).  REGISTER 13 SET TO ZERO TO INDICATE
*        ASYNCHRONOUS ENTRY.
**********************************************************************
         SPACE 2
         USING IGGSAVE,R14              SAVEAREA ADDRESSABILITY  Y02072
ASYNCH   SR    R13,R13                  INDICATE ENTRY WAS FROM  Y02072
*                                       ASYNCHRONOUS ROUTINE     Y02072
ASYATB2  BALR  R8,0               R8  BASE REGISTER.R15 NOT USED
         USING *,R8                BECAUSE OF EXCP
         N     R9,F0                   HIGH ORDER BIT ZERO        VLR2
ASYATB5  BALR  R8,0          R8 BASE REG,R15 NOT USED BECAUSE OF  8M603
*                                       EXCP                      8M603
         USING *,R8                                               8M603
         L     R7,DECBAREA                                        8M603
         LTR   R7,R7                    WAS A BFR ASSIGNED TO THIS DECB
         BZ    ASYATC4                  NO, BRANCH               A36336
         TM    DECBTYP1,X'01'           DYNAMIC BUFFERING         21810
         BZ    ASYATC4                  NO--DON'T PUT IN POOL     21810
*
         L     R10,DCBBUFCB            R10 BUFFER CONTROL BLOCK
*                                                                 19953
*              IF BUFFER IS ALLREADY ON THE                       19953
*              FREE QUEUE, DO NOT FREE AGAIN                      19953
*                                                                 19953
         L     R6,BCBNAVB               FIRST FREE BUFFER         19953
         LA    R7,0(0,R7)               ZERO HIGH ORDER BYTE      19953
ASYATB3  LA    R6,0(0,R6)               ZERO HIGH ORDER BYTE      19953
         LTR   R6,R6                    LAST BUFFER ON QUEUE      19953
         BC    8,ASYATB4                BRANCH TO FREE BUF IF YES 19953
         CR    R6,R7                    IS BUFFER OF FREE QUEUE   19953
         BC    8,ASYATC4                BRANCH IF YES             19953
         L     R6,0(0,R6)               NEXT FREE BUFFER          19953
         B     ASYATB3                                            19953
ASYATB4  L     R6,BCBFIOB               R6 ADDRESS OF NEXT IOB    19953
         LTR   R6,R6                    IF ZERO, NO IOBS AWAITING
         BC    7,ASYATC2                 A BUFFER. THEREFORE, PUT
*                                        BUFFER ON AVAILABLE QUEUE.
         MVC   0(4,R7),BCBNAVB
         ST    R7,BCBNAVB
         XC    DECBAREA(4),DECBAREA     CLEAR BUFF PTR            VLR
         B     ASYATC4                                             7829
ASYATC2  LR    R11,R1                   IF IOB AWAITING BUFFER,
         LR    R1,R6                    R11  ADDR OLD IOB
         MVC   BCBFIOB(4),IOBFCHAD      R1   ADDR NEW IOB
         CL    R1,BCBLIOB               REMOVE NEW IOB FROM BUFFER Q
         BNE   ASYATC3
         MVC   BCBLIOB(4),BCBFIOB       ZERO LIOB IF NO MORE IOBS ON Q
ASYATC3  L     R7,IOBECBAD              R7   ADDRESS OF NEXT DECB
         MVC   DECBAREA-IHADECB(4,R7),DECBAREA  ADDR OF BFR TO NEW DECB
         XC    DECBAREA,DECBAREA                                  VLR2
         USING IHADECB,R7
         L     R6,IOBCCWAD              R6   ADDRESS OF NEXT CHAN PROG
         USING CP4,R6
         L     R10,DECBAREA             SET ADDR OF BUFFER IN CP
         TM    DCBRECFM,X'80'           TEST FOR FIXED            VLR
         BO    APPAFIX3                 BRANCH FOR FIXED          VLR
         LA    R10,12(R10)                                        VLR
         B     APPAFIX4                                           VLR
APPAFIX3 LA    R10,16(R10)              FIX - READ TO AREA+16     VLR
APPAFIX4 EQU  *                                                   VLR
         STH   R10,CA23+2
         SRL   R10,16
         STC   R10,CA23+1
*
*        DEB ADDRESS IN REGISTER 13 IF ENTRY FROM FREEDBUF       Y02072
*
         LTR   R10,R13                  DEB ADDR IN REG          Y02072
         BNZ   CONTINU                  BR YES                   Y02072
         L     R10,IGGPDEB              VALIDATED DEB ADDR       Y02072
CONTINU  EQU   *                                                 Y02072
         USING IHADEB,R10                                        S20201
         SR    R6,R6
         IC    R6,IOBDADAD             GET BB AND MOVE INTO IOB
         SLL   R6,4                     PRIOR TO EXCP
         LA    R6,DEBUCBAD(R6)                                   S20201
         DROP  R10                                               S20201
         MVC   IOBDADAD+1(2),4(R6)
         MVC   IOBBCHAD+1(3),IOBECBAD+1 SAVE ECB                 A33533
         LA    R6,IOBCSW                IOS WILL ZERO OUT CSW    A33533
         ST    R6,IOBECBAD              INSTEAD OF ECB           A33533
         LTR   R13,R13                  ASYNCH ROUTINE ENTRY     Y02072
         BZ    SAVEREGS                 BR IF YES                Y02072
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=6  CHANGE TO KEYZERO      Y02072
         SPACE 2
         STM   R9,R15,RBEXSV3           SAVE REGISTERS 9-15      Y02072
         LA    R9,RBEXSV3               ADDR TO RESTORE REGS     Y02072
         LA    R10,RBEXUKEY             ADDR OF USER KEY FIELD   Y02072
*                                       SET UP IN IGC005G-SVC57  Y02072
         B     FREELOCK                 RELEASE LOCAL LOCK       Y02072
         SPACE 2
SAVEREGS EQU   *                        SAVE REGS IN KEY 5 CORE  Y02072
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY (5)     Y02072
         SPACE 2
         STM   R9,R15,IGGREGS           SAVE REGISTERS 9-15      Y02072
         LA    R9,IGGREGS               ADDR TO RESTORE REGS     Y02072
         LA    R10,IGGUKEY              ADDR OF USER KEY FIELD   Y02072
         USING KEYADDR,R10              USER KEY ADDRESSABILITY  Y02072
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=6  CHANGE TO KEYZERO      Y02072
         SPACE 2
FREELOCK EQU   *                        RELEASE LOCAL LOCK       Y02072
         SPACE 2
**********************************************************************
*        RELEASE LOCAL LOCK.  WAS OBTAINED BY IGG019JI IF FREEDBUF
*        ENTRY OR BY ASYNCHRONOUS ROUTINE.  LOCK WILL BE RE-ACQUIRED
*        FOLLOWING EXCP SVC.
**********************************************************************
         SPACE 2
FLCL1    SETLOCK  RELEASE,TYPE=LOCAL,   FREE LOCAL LOCK          Y02072*
               RELATED=('HELD ON ENTRY, OBTAINED AFTER EXCP')    Y02072
         SPACE 2
         MODESET  KEYADDR=USERKEY,WORKREG=11 CHANGE TO USER KEY  Y02072
         SPACE 2
         LR    R6,R1                    SAVE IOB ADDRESS         M1792
         EXCP  (1)
         LR    R1,R6                    RESTORE IOB ADDRESS      M1792
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=11  CHANGE TO KEYZERO     Y02072
         SPACE 2
**********************************************************************
*        OBTAIN LOCAL LOCK.  WAS HELD ON ENTRY TO ROUTINE AND FREED
*        AT STATEMENT FLCL.
**********************************************************************
         SPACE 2
GLCL2    SETLOCK  OBTAIN,TYPE=LOCAL,MODE=UNCOND,  OBTAIN LOCAL   Y02072*
               RELATED=('REOBTAIN LOCK FOLLOWING EXCP')  LOCK    Y02072
         SPACE 2
         MODESET  KEYADDR=USERKEY,WORKREG=11 CHANGE TO USER KEY  Y02072
         SPACE 2
         LM    R9,R15,0(R9)             RESTORE REGISTERS        Y02072
         DROP  R10                      END USING FOR USER KEY   Y02072
         SPACE 2
         MVC   IOBECBAD+1(3),IOBBCHAD+1 RESTORE ECB              A33533
         LR    R1,R11                   RESTORE OLD IOB ADDRESS IN R1
ASYATC4  EQU   *                                                   7829
         LTR   R1,R1        IS THERE AN IOB                        7829
         BNZ   DEBTEST                  BR YES                   Y02072
         LTR   R13,R13                  ENTERED FROM FREEDBUF    Y02072
         BNZ   FREEDBUF                 BRANCH IF YES            Y02072
         BR    R12                      RETURN TO ASYN ROUTINE   Y02072
*
*        REGISTER 13 HAS DEB ADDRESS IF ENTRY FROM FREEDBUF.
*
DEBTEST  LTR   R10,R13                  DEB ADDR IN REG          Y02072
         BNZ   CONTINU2                 BR YES                   Y02072
         L     R10,IGGPDEB              VALIDATED DEB ADDR       Y02072
CONTINU2 EQU   *                        FREE IOB                 Y02072
         LA    R0,SP250IOB              GET IOB LENGTH           Y02072
         USING IHADEB,R10                                        S20201
         TM    DEBRPSID,RPSFLAGS        IS RPS PRESENT           S20201
         BZ    ASYATC41                 NO, BRANCH               S20201
         AH    R0,RPSCCW                YES, ALLOW FOR RPS CCWS  S20201
ASYATC41 EQU   *                        *                        S20201
         DROP  R10                      END DEB ADDRESSABILITY  YM01146
         SR    R10,R10                  CLEAR REGISTER           Y02072
         LTR   R13,R13                  DETERMINE TYPE OF ENTRY YM01165
         BZ    ASYNENTY                 BR ZERO, ASYNCH ENTRY    Y02072
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=10 CHANGE TO KEY ZERO    YM01380
         SPACE 2
         IC    R10,RBEXUKEY             USERS PROTECT KEY        Y02072
         STM   R14,R7,RBEXSV3           SAVE REGISTERS 14-7      Y02072
         LR    R4,R13                   VALIDATED DEB ADDRESS   YM01165
         LA    R5,RBEXSV3               ADDR OF REG SAVE AREA    Y02072
         B     INIT                     CHANGE TO KEY ZERO       Y02072
*
ASYNENTY EQU   *                        SAVE REGS IN KEY 5 CORE YM01380
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY        YM01380
         SPACE 2
         IC    R10,IGGUKEY              USERS PROTECT KEY        Y02072
         STM   R14,R7,IGGREGS           SAVE REGISTERS 14-7      Y02072
         L     R4,IGGPDEB               VALIDATED DEB ADDR       Y02072
         LA    R5,IGGREGS               ADDR OF REG SAVE AREA    Y02072
INIT     EQU   *                        INIT REGS FOR FREEMAIN   Y02072
         LR    R2,R10                   USERS PROTECT KEY        Y02072
         USING IHADEB,R4                DEB ADDRESSABILITY       Y02072
         L     R4,DEBTCBAD              TCB ADDRESS              Y02072
         USING PSA,R0                   LOW CORE ADDRESSABILITY  Y02072
         L     R7,PSAAOLD               ASCB ADDRESS             Y02072
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=3 CHANGE TO KEYZERO       Y02072
         SPACE 2
         FREEMAIN RU,LV=(0),A=(1),SP=SP250,KEY=(10),BRANCH=YES   Y02072
         SPACE 2
         MODESET  KEYADDR=(2)           CHANGE TO USER KEY       Y02072
         SPACE 2
         LM    R14,R7,0(R5)             RESTORE REGS 14 THRU 7   Y02072
         USING IHADCB,R4                DCB ADDRESSABILITY       Y02072
         LTR   R9,R9                                              VLR2
         BM    ASYATP1                                            VLR2
         L     R6,DCBWKPT2       GET WORK AREA                     7830
         IC    R11,37(R6)              REDUCE                      7830
         BCTR  R11,0                   DCWNACT                     7830
         STC   R11,37(R6)              BY ONE                      7830
ASYATP1  N     R9,F0             RESET HIGH ORDER BIT ZERO        VLR2
         LTR   R13,R13                  ENTERED FROM FREEDBUF    Y02072
         BNZ   FREEDBUF                 BR IF YES                Y02072
         BR    R12                                                VLR2
         SPACE 2
FREEDBUF EQU   *                        EXIT ROUTINE TO FREEDBUF Y02072
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=15  SET PROTECTION KEY 0  Y02072
         SPACE 2
**********************************************************************
*    RELEASE THE LOCAL LOCK.  WAS OBTAINED UPON ENTRY FROM
*    FREEDBUF (IGC005G - SVC 57).
**********************************************************************
*
         STM   R11,R15,RBEXSV3          SAVE REGS ACROSS LCK RTN Y02072
         SPACE 2
FLCL2    SETLOCK RELEASE,TYPE=LOCAL,    RELEASE THE LOCAL LOCK   Y02072*
               RELATED=(IOB,IGG019JI)   AND EXIT TO FREEDBUF     Y02072
         SPACE 2
         LM    R11,R15,RBEXSV3          RESTORE REGISTERS        Y02072
         BR    R12                      RETURN TO FREEDBUF       Y02072
         SPACE 5
RPSCCW   DC    H'16'                    LENGTH OF RPS CCWS       S20201
         DS    0F                                                 VLR2
F8       DC    X'80000000'                                        VLR2
F0       DC    X'00FFFFFF'                                        VLR2
SP250IOB EQU   56                       56 BYTES FOR IOB         Y02072
SP250    EQU   250                      SUBPOOL ID FOR IOB       Y02072
KEY0     DC    X'00'                    STORAGE PROTECT KEY 0    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
         EJECT
*              DATA EVENT CONTROL BLOCK
IHADECB  DSECT
         DS    0F
DECBECB  DS    CL4                      EVENT CONTROL BLOCK (ECB)
DECBTYP1 DS    BL1                      TYPE B6 - 1 IF LENGTH IS S
*                                            B7 - 1 IF AREA IS S
DECBTYP2 DS    BL1                           B0 - 1 IF READ K
*                                            B1 - 1 IF READ KX
*                                            B2 - 1 IF READ KU
*                                            B4 - 1 IF WRITE K
*                                            B5 - 1 IF WRITE KN
DECBLGTH DS    CL2                      LENGTH OF BLOCK
DECBDCBA DS    A                        POINTER TO DCB
DECBAREA DS    A                        ADDRESS OF AREA
DECBLOGR DS    A                        POINTER TO LOGICAL RECORD
DECBKEY  DS    A                        POINTER TO KEY
DECBEXC1 DS    BL1                      EXCPTN CD B0-RECORD NOT FOUND
*                                                 B1-RECORD LGTH CHK
*                                                 B3-INVALID REQUEST
*                                                 B4-UNCORRECTABLE IO
*                                                 B5-UNREACHABLE BLOCK
*                                                 B6-OVERFLOW RECORD
*                                                 B7-DUPLICATE RECORD
DECBEXC2 DS    BL1                                B7-SET BY READ KU
         SPACE 4
IGGSAVE  IGGBISAV
         SPACE 2
KEYADDR  DSECT                          DSECT FOR USER KEY
USERKEY  DS    X                        ADDRESSABILITY
         EJECT
         DCBD  DSORG=(IS)
         EJECT
IHAIOB   IGGIOBD
         SPACE 5
IHADCW   IGGBISAM
         SPACE 5
IHADEB   IGGDEBD
         SPACE 5
IHAAPPRV DSECT
APPNORM  DS    CL4                      NORMAL                   S20201
APPRVSKP DS    CL4       SKIP POST
APPRVXCP DS    CL4       EXCP
APPRVBYP DS    CL4       BYPASS
IHABCB   DSECT                    BUFFER CONTROL BLOCK
BCBFIOB  DS    A                   FIRST IOB AWAITING BUFFER
BCBLIOB  DS    A                   LASTT IOB AWAITING BUFFER
BCBNAVB  DS    A                   NEXT AVAILABLE BUFFER
BCBSIZE  DS    A                   SIZE OF BCB AND BUFFERS
IHACP47  DSECT                      CHANNEL PROGRAMS
CP4      DS    0D                       CP4
         IGGCP4
CP5      DS    0D                       CP5 AND CP5W
         IGGCP5     OPTCD=W                                      S20201
****     CCWS AFTER CA24 FOR CP5 OR CP5W
****     DIFFER BUT ARE NOT USED BY THIS MODULE
         SPACE 4
         IECDRQE                                                 Y02072
         EJECT
CVT      DSECT                                                   Y02072
         CVT                                                     Y02072
         SPACE 5
         IKJTCB                                                  Y02072
         EJECT
         IKJRB                                                   Y02072
RBEXUKEY EQU   RBEXSAVE+1               USER'S PROTECT KEY       Y02072
RBEXSV3  EQU   RBEXSAVE+8               THIRD WORD IN EXTENDED   Y02072
*                                       REGISTER SAVE AREA
         EJECT
         IHAPSA                                                  Y02072
         EJECT
         IHASCVT                                                 Y02072
         END
