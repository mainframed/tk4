         TITLE 'OPEN EXECUTOR IGG0192Z - VARIABLE VERSION OF 2O'
IGG0192Z CSECT                                                 
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192Z                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, BISAM MODE, WKN WITH VLR, AND USER-   *
*                    SUPPLIED WORK AREA                               *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = COMPLETE WRITE KN INITIALIZATION.  MOVE AND INITIALIZE   *
*            CP12A, CP12B, AND CP123W.  DELETE WKN CP MODULE IGG019HP.*
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 644 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192Z                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0192Q FOR  *
*              WKN WITH VLR, WHEN USER PROVIDES WORK AREA.  RECEIVES  *
*              CONTROL IN STORAGE PROTECT KEY 5 AND PRIVILEGED STATE. *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG0192J IN STORAGE        *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGBISAM - BISAM WORK AREA                          *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY                                        *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, DELETE, AND XCTL.                                 *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
R0       EQU   00
RE       EQU   00                       WORK REG
RF       EQU   01                       WORK REG
RDCB     EQU   02                       USER'S DCB
RBASE    EQU   03                       BASE REG
RG       EQU   04                       WORK REG
RPAR     EQU   05                  *    PARAMETER LIST ADDRESS
RWTG     EQU   06                  *    WHERE TO GO TABLE BEGINNING
RPARC    EQU   07                  *    CURRENT ENTRY,PARAMETER LIST
RWTGC    EQU   08                  *    CURRENT ENTRY, WTG TABLE
RTIOT    EQU   09                       TIOT ADDRESS
RUCB     EQU   10                       UCB ADDRESS
RA       EQU   11                       WORK REG
RB       EQU   12                       WORK REG
RC       EQU   13                       WORK REG
RD       EQU   14                       WORK REG
R15      EQU   15                       WORK REG
RCORE    EQU   RG                       DCB WA ADDRESS
*                   *     MEANS SET BEFORE ENTRY AND BEFORE EXIT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         L     RDCB,0(RPARC)            RDCB  ADDRESS OF DCB
         USING IHADCB,RDCB
         L     RCORE,DCBWKPT2           RCORE ADDRESS OF DCB WA
         USING IHADCW,RCORE
         L     RD,DCBWKPT3              RD    ADDRESS OF WKN CP
         USING IHAWKNCP,RD                                       S20201
         USING WTGDSECT,RWTGC           WHERE TO GO TABLE        Y02072
         L     RC,OPENWA                OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,RC               WA ADDRESSABILITY        Y02072
         L     RA,DXCCW9                ADDR OF CURRENT CP       Y02072
         L     RF,DXCCW10               CURR CP ADDR IN MODULE   Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=12  CHANGE TO USER KEY  Y02072
*
         DROP  RC                       DROP REGISTER            Y02072
         SR    RE,RE                    ZERO AREAS USED TO SAVE ADDR
         ST    RE,12(RD)                 OF CURRENT CP
         ST    RE,DCWFIOBU
         MVC   0(SZ12AVW,RA),0(RF)      MOVE IN CP12AV SKELETON  S20201
         LR    RG,RA                   SAVE START OF CP12AV
         L     RC,CD0A1                                             
         ALR   RC,RD                                                
         ST    RC,CD0A1                                             
         L     RC,CD0A2                                             
         ALR   RC,RD                                                
         ST    RC,CD0A2                                             
         L     RC,CD0B
         ALR   RC,RD
         ST    RC,CD0B
         L     RC,CD2
         ALR   RC,RD
         ST    RC,CD2
         LH    RE,DCBWKPT6             RE=Q+2
         LTR   RE,RE                    TEST FOR ZERO            A36362
         BNZ   *+8                      OK IF NOT ZERO           A36362
         LA    RE,1                     SET MINIMUM NUMBER  OF   A36362
*                                       BLOCKS                   A36362
         STH   RE,CD0C+6               STORE Q+2=N=NO BLKS READ ON ONE
*                                                     PASS OF CP12AV
         SR    RB,RB
         IC    RB,DCBKEYLE
         STH   RB,CD2B+6
         L     RC,DCBMSWA
         LA    RC,0(RC)
         AR    RB,RC                   RB=A(MSWA +KL)
         O     RC,CD2B
         ST    RC,CD2B
         LA    RB,10(RB)
         AH    RB,DCBLRECL
         SR    RC,RC
         IC    RC,DCBHIRPD
         SLL   RC,3
         AR    RB,RC                   RB=A(MSWA+KL+8H1RPD+10+LRECL)=
*                                         ADDRESS X
         ST    RB,CD0A+4               SAVE ADDRESS X
         MVC   CB18A+KK1(THREE),CD0A+FIVE MOVE ADDRESS X TO CP8  S20201
         LH    RC,DCBBLKSI
         STH   RC,CB18A+SIX             STORE BLKSIZE IN CP8     S20201
         O     RB,CD3
*                                SET UP Q+2=N COPIES OF CD3
         USING CP12AV,RA                                         S20201
OPNOVAR  STM   RB,RC,CD3
         MVI   CD3+4,X'20'             S1L1 ON
         LA    RA,8(RA)
         AR    RB,RC
         BCT   RE,OPNOVAR
         DROP  RA                                                S20201
         LA    RA,SZ12AVW-EIGHT(RA)                              S20201
         LA    RF,SZ12AVW(RF)                                    S20201
         ST    RA,CD2+FOUR              SAVE ADDR OF CP12BV      S20201
         USING CP12BV,RA
         MVC   0(SZ12BVW,RA),0(RF)      MOVE CP12BV SKELETON     S20201
         L     RC,CE0
         ALR   RC,RD
         ST    RC,CE0
         L     RC,CE0A
         ALR   RC,RA
         ST    RC,CE0A
         L     RC,CE0B
         ALR   RC,RD
         ST    RC,CE0B
         L     RC,CE2
         ALR   RC,RA
         ST    RC,CE2
         L     RC,CE3
         ALR   RC,RA
         ST    RC,CE3
         L     RC,CE3A
         ALR   RC,RD
         ST    RC,CE3A
         L     RC,CE3B
         ALR   RC,RA
         ST    RC,CE3B
         L     RC,CE3C
         ALR   RC,RD
         ST    RC,CE3C
         IC    RE,DCBHIRPD
         SR    RC,RC
         IC    RC,DCBKEYLE
         STH   RC,CE5+6
         L     RB,DCBMSWA
         LA    RB,0(RB)
         AR    RB,RC                   RB=A(MSWA+KL)
         O     RB,CE4
         ST    RB,CE4
         LR    RC,RE                   RC=HIRPD
         AH    RE,ONE                   RE = HIRPD + 1           A34921
         SLL   RC,3
         AR    RC,RB
         LA    RC,6(RC)                RC=A(MSWA+KL+8HIRPD+6)
         ST    RC,CE6
         L     RC,CE4+4
MOVE1    MVC   CE5+24,CE5               SET UP HIRPD+2 CE4-CE6   A34921
*        THE PRECEDING ALLOWS FOR 2 BLOCKS WORTH OF OVERFLOW REC
         MVC   CE6+24,CE6
         AH    RB,OPNOV8
         STM   RB,RC,CE4+24
         LA    RA,24(RA)
         BCT   RE,MOVE1
         LA    RA,SZ12BVW(RA)                                    S20201
         LA    RF,SZ12BVW(RF)                                    S20201
         USING CP123WV,RA
         ST    RA,12(RD)               STORE ADDR OF CP123WV IN CB2
         MVC   0(SZ123V,RA),0(RF)      MOVE IN CP123WV SKELETON  S20201
         L     RC,CEA00                                          S20201
         ALR   RC,RA                                             S20201
         ST    RC,CEA00                                          S20201
         L     RC,CEA0                                              
         ALR   RC,RD                                             S20201
         ST    RC,CEA0                                              
         L     RC,CEA05                                             
         ALR   RC,RA                                                
         ST    RC,CEA05                                             
         L     RC,CEB
         ALR   RC,RA
         ST    RC,CEB
         CLI   DCBHIRPD,X'01'
         BE    MOVE3
         IC    RE,DCBHIRPD
         BCTR  RE,R0
MOVE2    MVC   CEF+8,CEF               SET UP HIRPD COPIES OF CEF
         LA    RA,8(RA)
         BCT   RE,MOVE2
MOVE3    LA    RA,SZ123V(RA)                                     S20201
         LA    RF,SZ123V(RF)                                     S20201
         LA    RE,OPNOCPV
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         DELETE  EPLOC=(0)              DELETE CP MODULE         Y02072
*
OPNEND2O MVC   0(L'LOAD2J,RWTGC),LOAD2J ID OF NXT LOAD-IGG0192J  Y02072
         L     RCORE,4(RWTGC)
         USING FORCORE,RCORE            WORK AREA ADDR           Y02072
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT NAME ON THE WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)          AND DCB ON THE PARAM TABLE
         CLC   0(2,RWTGC),OPNEXNAM      IF NAME ON TABLE SAME ID AS
         BCR   8,RBASE                   CURRENT LOAD, BRANCH TO REPEAT
         CLC   0(2,RWTGC),OPIDCNST    IF NOT END OF WTG TABLE BRANCH
         BNE   RELOOP                    TO TRY NEXT NAME
         LR    RPARC,RPAR               OTHERWISE, RETURN TO START OF
         LA    RWTGC,WAOFF(0,RWTG)       BOTH TABLES AND SEARCH FOR
ZCHECK   CLI   0(RWTGC),ZCNST            FIRST NON-ZERO ID
         BNE   TCTLRTN
         LA    RWTGC,WGOFF(0,RWTGC)
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHECK
OPNOV8   DC    X'0008'
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       WHEN FOUND, SET UP PARAMETERS
*                                       TO CALL THAT MODULE
         LA    R15,DXCCW12                                       Y02072
TRCTL    XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) GO TO NEXT EXEC    Y02072
         SPACE 2
ONE      DC    H'1'                                              A34921
WGOFF    EQU   8                        SIZE OF ONE ENTRY ON WTG TABLE
PLOFF    EQU   4                        SIZE OF ONE ENTRY ON PARAM TAB
OPNEXNAM DC    C'2Z'                   ID OF THID EXECUTOR
OPIDCNST DC    C'0S'                    ID OF OPEN LOAD B7
ZCNST    EQU   X'00'                    ZERO - SIGNALS INACTIVE ENTRY
WAOFF    EQU   32                       SIZE OF WA AT START OF WTG TAB
         DS    0D
OPNOCPV  DC    C'IGG019HP'
         SPACE 1
LOAD2J   DC    C'2J'                    ID OF MODULE IGG0192J    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
         DCBD  DSORG=(IS)
         EJECT
IHADCW   IGGBISAM                       OPTCD=V                  S20201
         SPACE 4
IHADEB   IGGDEBD
         ORG   IHADEB+32
*        THE FOLLOWING ADDRESSES ARE MODIFIED BY NMEXT X 16
DEBID1   DS    CL2                      MODULE ID 1
DEBID2   DS    CL2                      MODULE ID 2
DEBID2A  DS    CL2                      MODULE ID 2A
DEBID3   DS    CL2                      MODULE ID 3
DEBID4   DS    CL2                      MODULE ID 4
DEBID5   DS    CL2                      MODULE ID 5
IHAWKNCP IGGWKNCP  OPTCD=VW                                      S20201
CP12AV   DS    0D                                                S20201
         IGGCP12A  OPTCD=V                                       S20201
CP12BV   DSECT
         IGGCP12B  OPTCD=V                                       S20201
CP123WV  DSECT
         IGGCP123  OPTCD=V                                       S20201
KK1      EQU   X'01'                    1                        S20201
THREE    EQU   X'03'                    3                        S20201
FOUR     EQU   X'04'                    4                        S20201
FIVE     EQU   X'05'                    5                        S20201
SIX      EQU   X'06'                    6                        S20201
EIGHT    EQU   X'08'                    8                        S20201
WTGDSECT DSECT                                                   Y02072
IDTTR    DS    F                        MODULE IDTTR             Y02072
OPENWA   DS    A                        OPEN WORK AREA ADDRESS   Y02072
         EJECT
FORCORE  DSECT                                                   Y02072
         IECDSECT                                                Y02072
         END
