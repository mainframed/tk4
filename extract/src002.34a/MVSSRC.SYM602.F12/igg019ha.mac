         TITLE 'IGG019HA * * * * * * * * RPS START I/O MODULE FOR SCAN X
               MODE * * * * * * * *'
IGG019HA CSECT
*          RELEASE OS/VS2-02 DELETIONS
*D                                                              YM05356
*D153755-153763                                                 YM08290
*          RELEASE 21 DELETIONS
*0765                                                            A44446
*0765900000-905000                                               A44462
*                                                                S21045
*0765161000,162000,242000,249000,251000,296000,356000,462000,    M0910
*0765490000,491000,492000,493000,494000,550000                   M0910
*0765                                                            M1781
*0765                                                            A53164
*0765385500                                                      SM4373
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*0000178000,197000-201000,264000,275000-276000,277000-280000,    M5092
*0000287000-294000,334000-335000,357000,363000,365000,366000-    M5092
*0000374000,375000,379000-445000,501000                          M5092
*0000319200-319600                                               M5297
* D385500                                                       SAXXXXX
*A326320                                                        SA61305
*                                                                     *
*                                                                     *
*  FUNCTION/OPERATION - THIS MODULE IS LOADED WHEN PART OF THE DATA   *
*        SET IS ON AN RPS DEVICE. IT SETS UP THE CHANNEL PROGRAMS UP  *
*        FOR RPS OR NON-RPS AS NEEDED.                                *
*                                                                     *
*  ENTRY POINTS - 'IGG019HA' - ENTRY POINT FOR START I/O APPENDAGE.   *
*                                                                     *
*  INPUT - REG - 0,5,6,8,12 - IOS INFORMATION MUST BE SAVED.          *
*          REG - 1 - POINTER TO RQE.                                  *
*          REG - 2 - POINTER TO IOB.                                  *
*          REG - 3 - POINTER TO DEB.                                  *
*          REG - 4 - POINTER TO DCB.                                  *
*          REG - 9 - HIGH ORDER 3 BYTES 0.                            *
*          REG -14 - RETURN ADDRESS.                                  *
*          REG -15 - ADDRESSABILITY ON MODULE                         *
*                                                                     *
*  OUTPUT - UPDATED CHANNEL PROGRAMS, REGISTERS SAME AS INPUT.        *
*                                                                     *
*  EXTERNAL ROUTINES - NONE                                           *
*                                                                     *
*  EXIT - RETURN TO IOS.                                              *
*                                                                     *
*  TABLES AND WORK AREAS -                                            *
*        DCB - COMMUNICATIONS WITH USER.                              *
*        DEB - DESCRIPTION OF DATA SET ALLOCATION.                    *
*        SCANWA - COMMUNICATIONS WITHIN SCAN MODE.                    *
*        W1IOBI - COMMUNICATIONS WITH IOS FOR INPUT OPERATIONS.       *
*        W1IOBO - COMMUNICATIONS WITH IOS FOR OUTPUT OPERATIONS.      *
*                                                                     *
*  ATTRIBUTES - READ ONLY,REENTRANT,PRIVELEGED,ENABLED,REUSABLE.      *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  CONSTANTS USED TO MEET IBM STANDARDS                               *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
UCBTYP1  EQU   17                       DISPLACEMENT TO RPS INDICATOR
*                                       * IN THE UCB.
FIXED    EQU   X'80'                    MASK FOR CHECKING RECORD FORMAT
ONEBYTE  EQU   8                        SHIFT ONE BYTE
SECTSUB  EQU   232                      DISPLACEMENT INTO CVT FOR
*                                       * SECTOR ROUTINE.
CVTADDR  EQU   16                       ADDRESS OF CVT TABLE
UCBTYPE3 EQU   19                       DEVICE TYPE BYTE IN UCB
RPSDEV   EQU   X'10'                    MASK USED TO CHECK FOR RPS
ZERO     EQU   0                        CONSTANT
ONE      EQU   1                        CONSTANT
TWO      EQU   2                        CONSTANT
THREE    EQU   3                        CONSTANT
FOUR     EQU   4                        CONSTANT
FIVE     EQU   5                        CONSTANT
SIX      EQU   6                        CONSTANT
SEVEN    EQU   7                        CONSTANT
EIGHT    EQU   8                        CONSTANT
NINE     EQU   9                        CONSTANT
TEN      EQU   10                       CONSTANT
K18      EQU   18                       CONSTANT
ONZERO   EQU   8                        ZERO CONDITION FOR BRANCH
IFNOTRPS EQU   14                       NOT RPS CONDITION
NOTEQUAL EQU   7                        NOT EQUAL CONDITION FOR BRANCH
EQUAL    EQU   8                        EQUAL CONDITION FOR COMPARE
OVFLREC  EQU   X'20'                    MASK TO CHECK FOR OVERFLOW
SW22     EQU   X'10'                    CP22 INDICATOR           M5092
SW24     EQU   X'20'                    CP24 INDICATOR           M5092
NOSECT   EQU   X'FF'                    NOP SECTOR               M5092
OFLOW    EQU   X'40'                    OVERFLOW CP22            M5092
CAW      EQU   X'48'                    POINTER TO CP START      A44446
CCWLEN   EQU   8                        LENGTH CCW               A44446
SETLCODE EQU   4                        SETL APPENDAGE CODE      Y02072
SETLKEY  EQU   X'02'                    SETL KEY INDICATOR       Y02072
ONES     EQU   X'FF'                    MASK OF ALL ONES         Y02072
RPSDS    EQU   X'E0'                    BIT SETTINGS PRIME,INDEX Y02072
*                                       OVRFLOW ON RPS DEVICE    Y02072
CYLOFL   EQU   X'08'                    CYLOFL CAN EXIST         Y02072
INDOFL   EQU   X'10'                    IND OVFL CAN EXIST      YM05356
         EJECT
         IECDRQE
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  DATA CONTROL BLOCK                                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
         DCBD  DSORG=IS
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  DATA EXTENT BLOCK                                                  *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
IHADEB   IGGDEBD
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
SCANWA   IGGSCAN
         ORG   W1ICPEXT+NINE            *
INCPSAD  DS    AL3                      ADDRESS OF FIRST CCW ON CHAIN
         ORG   W1OCPEXT+NINE            *
OUTCPSAD DS    AL3                      ADDRESS OF FIRST CCW ON CHAIN
         EJECT
********************
* IOB REFERENCE    *
********************
*
IHAIOB   DSECT
         USING IHAIOB,R2
         DS    0D
IOBFLAG1 DS    CL1                      FLAGS 1
IOBFLAG2 DS    CL1                      FLAGS 2
         DS    CL1                      *
IOBSENSE DS    CL1                      SENSE
IOBECBAD DS    A                        ECB POINTER
IOBCSW   DS    CL8                      CHANNEL STATUS WORD
IOBSIOCC DS    0CL1                     SIO CC
IOBCPSAD DS    A                        CHANNEL PROGRAM START ADR
IOBWT    DS    0CL1                     WEIGHT
IOBDCBAD DS    A                        DCB POINTER
IOBCPRAD DS    A                        CHANNEL PROGRAM RESTART ADR
IOBBCTI  DS    CL2                      BLK CTR INCR
IOBERRCT DS    CL2                      ERROR COUNT               15924
IOBDADAD DS    CL8                      DIR ACESS DEV ADR MBBCCHHR
IOBAPP1  DS    XL1                      APPENDAGE CODE           Y02072
IOBAPP2  DS    XL1                      APPENDAGE CODE           Y02072
         ORG   IOBCPSAD+ONE             *
IOBSTART DS    AL3                      ADDRESS OF CP REQUESTED
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  SCAN MODE CHANNEL PROGRAMS                                         *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
C22      DSECT
         IGGCP22
         SPACE 1
C23      DSECT
         IGGCP23
         IGGCP26
         SPACE 1
C25      DSECT
         IGGCP25
         EJECT
TCB      IKJTCB
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  REGISTER USAGE                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
R0       EQU   0                        USED BY IOS
R1       EQU   1                        PTR TO RQE
R2       EQU   2                        PTR TO IOB  **
R3       EQU   3                        PTR TO DEB  **
R4       EQU   4                        PTR TO DCB  **
WABASE   EQU   5                        BASE FOR SCAN WORK AREA  M0910
RSAVE    EQU   6                        IOS SAVE AREA ADDR       Y02072
R7       EQU   7                        PTR TO UCB  **
R9       EQU   9                        ZERO        **
R10      EQU   10                       WORK AREA
R11      EQU   11                       WORK AREA
R13      EQU   13                       WORK AREA
R14      EQU   14                       RETURN ADDRESS
R15      EQU   15                       BASE REGISTER
*
*  NOTE - ** - REGISTER CAN BE USED BUT MUST BE RESTORED.
*
         EJECT
IGG019HA CSECT
         USING *,R15                    ADDRESSABILITY ON THIS MODULE
         USING RQE,R1                   RQE ADDRESSABILITY       Y02072
         USING IHADEB,R3                DEB ADDRESSABILITY       Y02072
*
         B     SIOENTRY                 START I/O ENTRY          M5092
*
         BR    R14                      PG FIX ENTRY, RETURN     Y02072
         SPACE 2
SIOENTRY STM   R0,R15,0(R13)            SAVE REGISTERS           Y02072
         SPACE 2
         MODESET KEYADDR=RQEPRT,WORKREG=11  CHANGE TO USER KEY   Y02072
         SPACE 2
         USING IHADCB,R4                DCB ADDRESSABILITY       Y02072
         L     R11,DCBWKPT1             SCAN WORK AREA ADDR      Y02072
         USING SCANWA,R11               WA ADDRESSABILITY        Y02072
         LR    RSAVE,R13                SAVE SAVE AREA ADDRESS   Y02072
         SPACE 2
         L     R10,DEBTCBAD             TCB ADDRESS              Y02072
         USING TCB,R10                  TCB ADDRESSABILITY       Y02072
         TM    TCBFLGS6,TCBRV           IS IT VIRTUAL = REAL     Y02072
         BO    RPSSIO                   BR YES - NO CHANNEL PGM  Y02072
*                                       SPLITTING                Y02072
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*    CORRECT CP23/26 IF NECESSARY.                                    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
         CLI   IOBAPP2,SETLCODE         IS THIS A SETL CP        Y02072
         BNE   RPSSIO                   BR NO - CP OK            Y02072
         TM    DCBMACF2,SETLKEY         IS IT A SETL KEY         Y02072
         BZ    RPSSIO                   NO, BR - CP OK, SETLID   Y02072
         L     R10,W1CP23PT             YES, CORRECT CP23/26     Y02072
         USING CS1,R10                  CP23/26 ADDRESSABILITY   Y02072
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*    STOP AT CP 23 IF STARTED AT CS1.                                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         NI    CS4+FOUR,ONES-CC         UNCHAIN CS4              Y02072
*
*    ALLOW PRIME SEARCH IF NO OVERFLOW.  BREAK CP23 AT CS15           *
*
         NI    CS15+FOUR,ONES-CC        CP OVERFLOW BREAK        Y02072
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*    MODIFY CP26 - BREAK LOOP IF INDEPENDENT OVERFLOW EXISTS.         *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
         MVI   CS36,SEEKHH              ASSUME NO IND. OVERFLOW  Y02072
         OI    CS32+FOUR,CC             ALLOW LOOP               Y02072
         TM    DCBOPTCD,INDOFL          IS IND. OVFL POSSIBLE    Y02072
         BZ    RPSSIO                   NO - CP26 IS CORRECT     Y02072
         NI    CS32+FOUR,ONES-CC        YES - BREAK TRANSLATION  Y02072
*
RPSSIO   TM    DEBRPSID,RPSDS           THIS AN RPS DATA SET     Y02072
         BZ    IOSRET                   NO - RETURN TO IOS       Y02072
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  CHECK TO SEE IF REQUEST IS ON AN RPS DEVICE.                       *
*        IF NOT RESET CPS 23,24,25 TO A NON-RPS STATE.                *
*        IF SO SET CPS 22,23,24,25 TO AN RPS STATE AS NEEDED.         *
*  NOTE - CP22 & CP24 HANDLED BY SUBROUTINES.                         *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*   CHECK FOR REQUEST ALREADY SET UP.                                *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
         L     R10,IOBSTART-ONE         PT TO START ADDRESS
         USING CN1,R10                  ADDRESSABILITY
         CLI   CN1,SETSECT              IS THIS THE PREFIX
         BE    IOSRET                   YES-RETURN TO IOS       Y02072
         DROP  R10
         DROP  R11                      *                        M0910
         LR    WABASE,R11               WORKAREA ADDRESSABILITY  M0910
         USING SCANWA,WABASE            *                        M0910
         LA    R9,NOP                   SET UP OPCODES FOR REPLACEMENT
         LR    R10,R9                   * IN CPS 23,24,25. NON-RPS.
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  IS THIS REQUEST FOR A WRITE OPERATION?                             *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
         USING IHAIOB,R2                ADDRESSABILITY ON IOB
         LA    R2,0(R2)                 TEST FOR WRITE OPERATION M5092
         LA    R13,W1IOBO               OUTPUT IOB               M5092
         CR    R2,R13                   SAME.                    M5092
         BE    SETUP22W                 ON Q - SET UP WRITE QUEUE RPS
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  IS THIS REQUEST ON AN RPS DEVICE.                                  *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TM    UCBTYP1(R7),RPSDEV       TEST FOR RPS
         BZ    RESETCPS                 NO - BR TO RESET CPS TO NON-RPS
*                                       YES - NARROW CP REQUESTED DOWN
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  LINK INPUT RPS CHANNEL PROGRAM PREFIX TO THIS REQUEST.             *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         MVC   INCPSAD,IOBSTART         LINK PREFIX TO THE CP
         LA    R10,W1ICPEXT             LINK IOB TO RPS
         IC    R9,IOBSIOCC              * PREFIX.
         ST    R10,IOBSIOCC             *
         STC   R9,IOBSIOCC              *
         SPACE 2
         MVI   W1ICPEXT+EIGHT,TIC+SW24  ASSUME CP24              M5092
         SPACE 3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  IS THIS REQUEST FOR CP24?                                          *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LA    R10,W1CP24               ADDRESS OF CP24
         L     R13,W1ICPEXT+EIGHT       NORMALIZED ADDRESS OF
         LA    R13,ZERO(R13)            * REQUESTED CP.
         CR    R10,R13                  ARE THEY EQUAL
         BE    SETUP24                  YES - CP24 REQUESTED
         MVI   W1ICPEXT+EIGHT,TIC+SW22  ASSUME CP22              M5092
*   CHECK FOR CP22 BY ELIMINATING CP23,CP25.
         L     R10,W1CP23PT             IS CP23 SETUP            M5092
         LA    R10,0(R10)               *                        M5092
         LTR   R10,R10                  *                        M5092
         BNZ   CP22TEST                 YES - CHECK FOR 23       M5092
         L     R10,W1CP25PT             NO - IS CP25 SET UP      M5092
         LA    R10,0(R10)               *                        M5092
         LTR   R10,R10                  *                        M5092
         BZ    SETUP22R                 NO - MUST BE CP22        M5092
CP22TEST EQU   *                        CHECK FOR CP22.          M5092
         CR    R10,R13                  IS IT CP23 OR CP 25      M5092
         BH    SETUP22R                 NO - MUST BE CP II       M5092
         MVI   W1ICPEXT+EIGHT,TIC       MARK NOT CP22 OR CP24.   M5092
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  MUST BE A REQUEST FOR CP23, CP25 OR CP26 SO SET THEM UP.           *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         MVI   W1ISECT,ZERO             ALL THESE CPS START AT HOME
*                                       * ADDRESS SO SECTOR MUST BE 0.
*  SET UP OPCODES FOR INITIALIZATION TO RPS STATE.
         LA    R9,RDSECT                READ SECTOR
         LA    R10,SETSECT              SET SECTOR
         XC    W1TOTAL,W1TOTAL          RESET FOR NEW SETL       M5092
         SPACE 2
RESETCPS EQU   *                        *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  COMMON SECTION TO RESET CPS 23, 24 AND 25 TO CORRECT STATE.        *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
*  RESET CP24
         STC   R10,CN14A                CORRECT OPCODE
         SPACE 2
*  TEST IF CP23 OR CP25 LOADED.
         L     R13,W1CP23PT             ADDRESS OF CP23 IF LOADED
         LA    R13,ZERO(R13)            *
         LTR   R13,R13                  IS THE ADDRESS 0
         BZ    RESET25                  YES - CP23 NOT LOADED SO
*                                             INITLZ CP25.
         SPACE 2
*  RESET CP23
         USING C23,R13                  ADDRESSABILITY ON CP23
         STC   R10,CS1D                 SET OPCODE FOR CCW CS1D
         STC   R10,CS80                 SET OPCODE FOR CCW CS80
         STC   R10,CS19A                SET OPCODE FOR CCW CS19A
         STC   R9,CS24                  SET OPCODE FOR CCW CS25
         B     FINISHD                  RETURN TO IOS            M0910
         SPACE 2
RESET25  EQU   *                        *
*  RESET CP25
         L     R13,W1CP25PT             ADDRESSABILITY ON CP25
         USING C25,R13                  *
         LTR   R13,R13                  DOES CP25 EXIST
         BZ    FINISHD                  NO - RETURN TO IOS       M0910
         STC   R10,CN23A                SET OPCODE FOR CCW CN23A
         B     FINISHD                  RETURN TO IOS            M0910
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  SECTION TO SET UP READ REQUESTS(CP'S 22&24) FOR RPS.               *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
SETUP24  EQU   *                        ENTRY POIN FOR SETTING UP CP24.
         MVI   CN14A,SETSECT            SET CP 24 TO RPS FORMAT
*
SETUP22R EQU   *                        ENTRY POINT FOR SETTING UP
*                                       * QUEUE.
         L     R3,W1RD1ST               PT TO FIRST CP ON READ QUEUE
         USING C22,R3
*
*  CHECK FOR OVERFLOW
*
         TM    CN2+4,OFLOW              OVERFLOW RECORD          M5092
         BO    OVERFLW                  YES - BR TO HANDLE
*
         IC    R9,W1ISECT               LAST SECTOR READ IN
*
*  CHANGE LAST READ TO READ SECTOR.
*
LAST22LP EQU   *                        *
         TM    CN4+FOUR,CC              IS THIS CP CHAINED TO ANOUTHER
         BZ    LAST22                   NO - THIS IS LAST ONE BRANCH
         L     R3,CN5                   POINT TO NEXT CP22
         B     LAST22LP                 CONTINUE CHECKING
LAST22   EQU   *                        *
*
*   LINK LAST CP22 TO READ SECTOR SUFFIX UNLESS END OF TRACK.
*
         MVC   W1CN5SAV,CN5             SAVE NEXT COPY CP22      A44462
         LA    R13,W1WDCXDM+7           ASSUME VLR               M5092
         TM    DCBRECFM,FIXED           FIXED                    M5092
         BZ    TESTHIR                  NO - VLR - CHECK         M5092
         LA    R13,DCBHIRPD             ASSUME FIXED NON-SHARED  M5092
*                                       TRACK                    M5092
         CLI   DCBHIRSH,ZERO           IS THERE A SHARED TRACK   M5092
         BZ    TESTHIR                  NO - NOT SHARED - CHECK  M5092
         CLC   DCBFIRSH+1(1),CN6+6      ON SHARED TRACK          M5092
         BNE   TESTHIR                  NO - NOT SHARED - CHECK  M5092
         LA    R13,DCBHIRSH             SHARED TRACK.            M5092
TESTHIR  EQU   *                        CHECK FOR END OF TRACK   M5092
         CLC   CN6+7(1),ZERO(R13)       LAST ON TRACK            M5092
         BE    NOSUFFIX                 YES - NO SUFFIX          M5092
*
*   NOT LAST ON TRACK SO HOOK SUFFIX TO READ SECTOR OF NEXT RECORD.
*
         LA    R13,W1RDCNT              TIC TO READ COUNT AND    M5092
         ST    R13,CN5                  * READ SECTOR            M5092
         MVI   CN5,TIC+MT               *                        M5092
         OI    CN4+4,CC                 CHAIN                    M5092
NOSUFFIX EQU   *                        *                        M5092
         L     R3,W1RD1ST               PT TO FIRST CP22
         CLI   CN6+SEVEN,ONE            RECORD = 1
         BNE   CKFSTSH                  NO - CHECK IF FIRST SHARED
         SR    R9,R9                    YES - SET SECTOR TO 0
         B     NOTFSTSH                 DON'T TEST FOR FIRST SHARED
CKFSTSH  EQU   *                        CHECK FOR FIRST SHARED
         CLC   DCBFIRSH,CN6+FIVE        IS IT FIRST SHARED       M5092
         BE    FSTSHARD                 YES - THEN IT IS FIRST SHARED
NOTFSTSH EQU   *                        NOT FIRST SHARED
         STC   R9,W1ISECT               SAVE SECTOR FOUND
*
*  CHECK TO SEE IF CP24 IS CHAINED TO THE READ QUEUE
*
         CLC   INCPSAD,W1RD1ST+ONE      DOES IOB PT TO CP24
         BE    FINISHD                  YES - CP24 NOT CHAINED SO
*                                       * RETURN TO IOS.
         STC   R9,CN14A+FIVE            SECTOR FOR READ QUEUE
         MVI   W1ISECT,ZERO             SECTOR FOR CP24
         CLI   W1WCOUNT+SEVEN,ONE       RECORD ONE
         BE    FINISHD                  SECTOR CORRECT SO RETURN TO
*                                       * IOS.
         SR    R10,R10
         IC    R10,W1WCOUNT+SEVEN       RECORD NUMBER
         LA    R11,W1ISECT              PLACE TO PUT SECTOR
FINSHARE EQU   *                        *
         LA    R9,TEN                   RECORD LENGTH
         B     FIXSECT                  BRANCH TO GET SECTOR
*
*  READ Q STARTS AT FIRST SHARED SO SET IT UP.
*
FSTSHARD EQU   *                        *
         SR    R10,R10                  RECORD COUNT             M5297
         IC    R10,CN6+7                *                        M5297
         MVI   W1ISECT,ZERO             SECTOR FOR CP24
         LA    R11,CN14A+FIVE           LOCATION FOR SECTOR CALCULATED
         B     FINSHARE                 BR TO COMPLETE
OVERFLW  EQU   *                        *
*
*  PROCESS READ QUEUE FOR AN OVERFLOW RECORD.
*
         L     R3,INCPSAD-1             GET CORRECT CP           M5092
         LA    R10,CN2+5                READ SECTOR INTO         M5092
         MVC   W1CN5SAV,CN5             * THIS CP FOR PUTX       M5092
         XC    CN5,CN5                  CLEAR READ SECTOR CCW  SA61305
         ST    R10,CN5                  * AND SAVE ADDRESS OF    M5092
         MVI   CN5,RDSECT               * NEXT CP IN CHAIN       M5092
         OI    CN4+4,CC                 CONNECT IT               M5092
         MVI   CN5+SEVEN,ONE            LENGTH = 1               M5092
         CLI   CN6+SEVEN,ONE            FIRST RECORD ON TRACK
         BE    REC1OVFL                 YES BR TO SET UP
         LA    R11,W1ISECT              LOCATION FOR SECTOR
OVFLCMMN EQU   *                        COMMON SECTION FOR HANDLING
*                                       * FLOW ON THE READ OR WRITE Q'S
         SR    R10,R10                  *
         IC    R10,CN6+SEVEN            RECORD NUMBER
         TM    DCBRECFM,FIXED           FIXED                    M5092
         BZ    VAROVFL                  NO - HANDLE SEPARATELY   M5092
         LH    R9,CN4+SIX               DATA LENGTH FROM READ
         B     FIXSECT                  BRANCH TO SUBROUTINE TO GET
*                                       * SECTOR
VAROVFL  EQU   *                        *
         LA    R9,W1RPSC1               PT TO TOLERANCES CYL     M5092
*                                       OVFL                     M5092
         CLC   CN6,DCBLPDA              INDEPENDANT OVERFLOW     M5092
         BNH   SECTORC                  NO - CYLINDER            M5092
         LA    R9,W1RPSI1               PT TO TOLERANCES IND     M5092
*                                       OVFL                     M5092
SECTORC  EQU   *                        CHECK TOLERANCES         M5092
         MVI   W1ISECT,NOSECT           SET SECTOR NOP           M5092
         CLC   CN6+7(1),ZERO(R9)        BELOW LOW TOLERANCE      M5092
         BNH   SECTOK1                  YES - USE GUESS          M5092
         CLC   CN6+7(1),ONE(R9)         ABOVE HIGH TOLERANCE     M5092
         BL    FINISHD                  NO - USE NOP SECTOR      M5092
SECTOK1  EQU   *                        SECTOR GUESS WON'T BE    M5092
*                                       TOO BAD                  M5092
         SR    R9,R9                    *                        M5092
         IC    R9,DCBKEYLE              MINIMUM LENGTH = KEYLEN + 18
         LA    R9,K18(R9)               *
         AH    R9,DCBRKP                + RKP
         B     FIXSECT                  BRANCH TO SUBROUTINE TO GET
*                                       * SECTOR.
REC1OVFL EQU   *                        1ST RECORD ON TRACK
         MVI   W1ISECT,ZERO             SECTOR = 0
         B     FINISHD                  RETURN TO IOS
         EJECT
SETUP22W EQU   *                        *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  SET UP WRITE QUEUE FOR RPS.                                        *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         DROP  R3
         TM    UCBTYP1(R7),RPSDEV       IS IT AN RPS REQUEST
         BNO   FINISHD                  NO - RETURN              M0910
         L     R10,IOBSTART-ONE         ADDRESS OF CP            M5092
         USING C22,R10
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  LINK OUTPUT RPS CHANNEL PROGRAM PREFIX TO THIS REQUEST.            *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         MVC   OUTCPSAD,IOBSTART        LINK PREFIX TO THE CP
         LA    R3,W1OCPEXT              LINK IOB TO RPS          M5092
         IC    R9,IOBSIOCC              * PREFIX.
         ST    R3,IOBSIOCC              *                        M5092
         MVC   W1OSECT,CN2+5            OVERFLOW SECTOR          M5092
*
*
*  CHECK FOR OVERFLOW
*
*   INDEPENDANT OVERFLOW
*
         CLC   DCBLPDA,CN6              IS IT AN IND OVFL EXT   SAXXXXX
         BL    FINISHD                  YES - SECTOR CORRECT     A53164
*
*   CYLINDER OVERFLOW AREA                                       A53164
*
         CLC   DCBLDT,W1OHHR            IS IT CYL OVERFLOW AREA  A53164
         BL    FINISHD                  YES - SECTOR CORRECT
*
*   PRIME AREA SECTOR CALCULATION FROM RUNNING TRACK CAPACITY IN
*   THE CHANNEL PROGRAM.
*
         LH    R9,CN2+SIX               RUNNING BALANCE          M5092
         SR    R3,R3                    RECORD FOR CONVERT       M5092
*                                       ROUTINE                  M5092
         IC    R3,CN6+7                 *                        M5092
         LR    R10,R3                   *                        M5092
         LA    R11,W1OSECT              LOCATION FOR SECTOR      M5092
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  SUBROUTINE TO GET SECTOR, AND PUT IT WHERE REQUIRED AND THEN TO    *
*        INPUT -                                                      *
*              R9 - FIXED - RECORD LENGTH                             *
*                 - VARIABLE - TOTAL LENGTH OF RECORDS ON TRACK TO    *
*                              SECTOR REQUIRED                        *
*              R10 - # OF RECORD TO WHICH SECTOR IS REQUIRED.         *
*              R11 - POINTER TO WHERE SECTOR IS REQUIRED              *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
VARSECT  EQU   *                        ENTRY POINT IF RECORDS AREN'T
*                                       * CONSTANT LENGTH.
         LR    R13,R0                   SAVE R0 FOR IOS
         LR    R0,R9                    00BB - TOTAL LENGTH
         O     R0,VAR                   MARK REQUEST VARIABLE    M1781
         SLL   R0,ONEBYTE               0BB0
         AH    R0,KEYMARK               0BB1 - INDICATOR FOR KEY M0910
         IC    R2,UCBTYPE3(R7)          DEVICE TYPE
         B     COMSECT                  FINISH AT COMMON SECTION
*
FIXSECT  EQU   *                        ENTRY POINT IF RECORDS ARE OF
*                                       * CONSTANT LENGTH.
         LR    R13,R0                   SAVE R0 FOR IOS
         LR    R0,R9                    00DD - LENGTH EACH RECORD
         SLL   R0,ONEBYTE               0DD0
         IC    R0,DCBKEYLE              0DDK - KEY LENGTH
         IC    R2,UCBTYPE3(R7)          DEVICE TYPE
COMSECT  EQU   *                        *
         SLL   R0,ONEBYTE               DDK0
         SLL   R2,THREE*ONEBYTE         PUT DEVICE TYPE IN HIGH ORDER
*                                       * BYTE.
         OR    R0,R10                   DDKR - ADD RECORD NUMBER
         OR    R2,R11                   ADD SECTOR RECEIVING ADDRESS
*
*  BRANCH TO SECTOR CALCULATION ROUTINE.
*
         LR    R3,R15                   SAVE ADDRESSABILITY
         L     R15,CVTADDR              PTR TO CVT
         L     R15,SECTSUB(R15)         PTR TO SUBROUTINE
         LR    R4,R14                   SAVE IOS RETURN POINTER
         BALR  R14,R15                  BRANCH TO SECTOR ROUTINE
*
*  RESTORE REGISTERS AND RETURN TO IOS.
*
         LR    R15,R3                   RESTORE ENTRY POINT
         LR    R14,R4                   RESTORE RETURN
         LR    R0,R13                   RESTORE FOR IOS
FINISHD  EQU   *                        *
         SPACE 2
IOSRET   MODESET KEYADDR=KEY0,WORKREG=9  CHANGE TO PROTECT KEY 0 Y02072
         SPACE 2
         LM    R0,R15,0(RSAVE)          RESTORE REGISTERS        Y02072
         SR    R9,R9                    RESTORE REGISTER 9
         BR    R14                      RETURN TO IOS
         SPACE 5
*   CONSTANTS
*
         DS    F                        ALIGNMENT                M1781
VAR      DC    X'00008000'              MARKER FOR VARIABLE REQUESTS
KEYMARK  DC    H'1'                     MARKER FOR KEY           M0910
KEY0     DC    X'00'                    STORAGE PROTECT KEY 0    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         END
