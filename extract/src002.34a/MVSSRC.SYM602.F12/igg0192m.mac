         TITLE 'IGG0192M - OPEN,BISAM WRITE KN,SET UP CHANNEL PROGRAMS'
IGG0192M CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192M                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, BISAM MODE - FIXED LENGTH RECORDS     *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = CONTINUE WKN INITIALIZATION BY INITIALIZING CP14/CP14W.  *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 712 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192M                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0192L IN   *
*               STORAGE PROTECT KEY 5 AND PRIVILEGED STATE.           *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG0192Q IN STORAGE        *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGBISAM - BISAM WORK AREA                          *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY                                        *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET AND XCTL                                           *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
RE       EQU   00                       WORK REG
RF       EQU   01                       WORK REG
RDCB     EQU   02                       USER'S DCB
RBASE    EQU   03                       BASE REG
RG       EQU   04                       WORK REG
RPAR     EQU   05                  *    PARAMETER LIST ADDRESS
RWTG     EQU   06                  *    WHERE TO GO TABLE BEGINNING
RPARC    EQU   07                  *    CURRENT ENTRY,PARAMETER LIST
RWTGC    EQU   08                  *    CURRENT ENTRY, WTG TABLE
RTIOT    EQU   09                       TIOT ADDRESS
RUCB     EQU   10                       UCB ADDRESS
RA       EQU   11                       WORK REG
RB       EQU   12                       WORK REG
RC       EQU   13                       WORK REG
RD       EQU   14                       WORK REG
R15      EQU   15                       WORK REG
RCORE    EQU   RG                       DCB WA ADDRESS
*                   *     MEANS SET BEFORE ENTRY AND BEFORE EXIT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         L     RDCB,0(RPARC)            RDCB  ADDRESS OF DCB
         USING IHADCB,RDCB
         L     RCORE,DCBWKPT2           RCORE ADDRESS OF DCB WA
         USING IHADCW,RCORE
         USING WTGDSECT,RWTGC           WHERE TO GO TABLE        Y02072
         L     RC,OPENWA                OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,RC               WA ADDRESSABILITY        Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=14  CHANGE TO USER KEY  Y02072
*
         L     RD,DCBWKPT3              RD    ADDRESS OF WRITE KN CP
         L     RA,DXCCW9                CP CURRENTLY SET UP      Y02072
         L     RF,DXCCW10               CP ADDR IN MODULE        Y02072
         DROP  RC                       FREE REGISTER            Y02072
         USING CP14,RA                  CP14, CP14W
         MVC   0(LCP141,RA),0(RF)       MOVE FIRST PART OF CP14  S20201
         MVC   LCP141(LCP142,RA),LCP141(RF)  MOVE THE REST       S20201
         L     RC,CH2
         ALR   RC,RD                    MOVE AND INITIALIZE
         ST    RC,CH2
         L     RC,CH3
         ALR   RC,RD                     CP14 AND CP14W
         ST    RC,CH3
         TM    DCBOPTCD,X'80'
         BZ    OPNO7D51                 BRANCH NOT WC
         MVC   LCP14(WC,RA),LCP14(RF)   INSERT THESE COMMANDS    S20201
         LA    RF,WC(RF)                *                        S20201
         L     RC,CH3A1                 *                        S20201
         ALR   RC,RD                    *                        S20201
         ST    RC,CH3A1                 *                        S20201
         L     RC,CH3B
         ALR   RC,RD
         ST    RC,CH3B                   FOR WC ONLY
         LA    RA,LCH3C(RA)                                      S20201
OPNO7D51 LA    RA,LCH3(RA)              ALLOW FOR COMMON SEGMENT S20201
         USING CH4,RA                   NEW ADDRESSING POINT     S20201
         L     RC,CH55                  CONTINUE INITIALIZING    S20201
         ALR   RC,RD
         ST    RC,CH55                  *                        S20201
         L     RC,CH6                    FOR CP14 AND CP14W
         ALR   RC,RD
         ST    RC,CH6
         MVC   CH7+SEVEN(ONE),DCBKEYLE  MOVE KEY LENGTH TO CH7   S20201
         L     RC,CH8
         ALR   RC,RD
         ST    RC,CH8
         TM    DCBOPTCD,X'80'
         BZ    OPNO7D52                 BRANCH NOT WC
         L     RC,CH8A
         ALR   RC,RD
         ST    RC,CH8A                  FOR WC ONLY
         L     RC,CH8B
         ALR   RC,RD
         ST    RC,CH8B
         SR    RC,RC
         IC    RC,DCBKEYLE              MOVE KEY LENGTH TO CH8C
         LA    RC,10(RC)
         STH   RC,CH8C+6
         LA    RA,LCH8C(RA)             *                        S20201
OPNO7D52 LA    RA,LCH8(RA)              ADD NON-WRITE CHK LENGTH S20201
         USING CH8D,RA                  *                        S20201
         L     RC,CH8D                                           S20201
         ALR   RC,RD                    CONTINUE INCREMENTING
         ST    RC,CH8D
         L     RC,CH8E
         ALR   RC,RD
         ST    RC,CH8E
         LH    RC,DCBSMSW               TEST FOR USER WA         S20201
         LTR   RC,RC                    IS FIELD ZERO            S20201
         BZ    SYSWA                    YES, BRANCH              S20201
         OI    CH8E+4,USERWA            NO, SET USER WA FLAG     S20201
SYSWA    EQU   *                        CONTINUE WITH CP14       S20201
         L     RC,CH8F                   ALL REFERENCES TO ADDRESSES
         ALR   RC,RD
         ST    RC,CH8F
         L     RC,CH8G
         ALR   RC,RD
         ST    RC,CH8G                   WITHIN CP14 OR CP14W
         L     RC,CH95                                           S20201
         ALR   RC,RD
         ST    RC,CH95                                           S20201
         L     RC,CH10
         ALR   RC,RD                     BY BASE ADDRESS
         ST    RC,CH10
         MVC   CH12+7(1),DCBKEYLE
         L     RC,CH13
         ALR   RC,RD                     WHICH IS IN RD
         ST    RC,CH13
         TM    DCBOPTCD,X'80'
         BZ    OPNO7D53                 BRANCH NOT WC
         L     RC,CH13A
         ALR   RC,RD
         ST    RC,CH13A                 INITIALIZE FOR WC ONLY
         L     RC,CH13B
         ALR   RC,RD
         ST    RC,CH13B
         SR    RC,RC
         IC    RC,DCBKEYLE              KEY LENGTH TO CH13C
         LA    RC,10(RC)
         STH   RC,CH13C+6
         LA    RA,LCH13C(RA)            *                        S20201
OPNO7D53 LA    RA,LCH13(RA)             *                        S20201
         USING CH14,RA                  *                        S20201
         L     RC,CH14                  *                        S20201
         ALR   RC,RD                    CONTINUE INITIALIZING
         ST    RC,CH14
         L     RC,CH15
         ALR   RC,RD
         ST    RC,CH15
         L     RC,CH15A                  BOTH CP14 AND CP14W
         ALR   RC,RD
         ST    RC,CH15A
         L     RC,CH16
         ALR   RC,RD
         ST    RC,CH16
         MVC   CH17+7(1),DCBKEYLE       MOVE KEY LENGTH TO CH17
         LH    RC,DCBLRECL
         LA    RC,10(RC)                 AND RCD LGTH PLUS 10 TO CH18
         STH   RC,CH18+6
         TM    DCBOPTCD,X'80'
         BZ    OPNO7D54                 BRANCH NOT WC
         L     RC,CH18A
         ALR   RC,RD
         ST    RC,CH18A
         L     RC,CH18B                 INITIALIZE CP14W COMMANDS
         ALR   RC,RD
         ST    RC,CH18B
         SR    RC,RC
         IC    RC,DCBKEYLE
         AH    RC,DCBLRECL               CH18A, CH18B AND CH18C
         LA    RC,18(RC)
         STH   RC,CH18C+6
         LA    RA,LCH18C(RA)            *                        S20201
OPNO7D54 LA    RA,LCH18(RA)             *                        S20201
         USING CH19,RA                  *                        S20201
         L     RC,CH19                  *                        S20201
         ALR   RC,RD                    CONTINUE FOR CP14
         ST    RC,CH19
         L     RC,CH20
         ALR   RC,RD
         ST    RC,CH20
         L     RC,CH21                   AND CP14W
         ALR   RC,RD
         ST    RC,CH21
         LH    RC,DCBLRECL              RCD LGTH PLUS 10 TO CH22
         LA    RC,10(RC)
         STH   RC,CH22+6
         TM    DCBOPTCD,X'80'
         BZ    OPNO7D55                 BRANCH NOT WC
         STH   RC,CH22C+6               RCD LGTH PLUS 10 TO CH22C
         L     RC,CH22A
         ALR   RC,RD
         ST    RC,CH22A
         L     RC,CH22B
         ALR   RC,RD                    INITIALIZE CH22A AND CH22B
         ST    RC,CH22B
         LA    RA,LCH22C(RA)            *                        S20201
OPNO7D55 LA    RA,LCH22(RA)             *                        S20201
         USING CH23,RA                  *                        S20201
         MVC   CH24+FIVE(ONE),DCBKEYLE  CP14 AND CP14W           S20201
         LH    RC,DCBLRECL
         LA    RC,10(0,RC)
         STH   RC,CH24+6
         LA    RA,LCH24(RA)                                      S20201
         LA    RF,LCP14(RF)             INITIALIZE CH24          S20201
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         MVC   0(L'LOAD2Q,RWTGC),LOAD2Q ID OF MODULE IGG0192Q    Y02072
         L     RCORE,OPENWA             OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,RCORE            WA ADDRESSABILITY        Y02072
         ST    RA,DXCCW9                CP CURRENTLY SET UP      Y02072
         ST    RF,DXCCW10               CP ADDR IN MODULE        Y02072
*
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT NAME ON THE WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)          AND DCB ON THE PARAM TABLE
         CLC   0(2,RWTGC),OPNEXNAM      IF NAME ON TABLE SAME ID AS
         BCR   8,RBASE                   CURRENT LOAD, BRANCH TO REPEAT
         CLC   0(2,RWTGC),OPIDCNST     IF NOT END OF WTG TABLE BRANCH
         BNE   RELOOP                    TO TRY NEXT NAME
         LR    RPARC,RPAR               OTHERWISE, RETURN TO START OF
         LA    RWTGC,WAOFF(0,RWTG)       BOTH TABLES AND SEARCH FOR
ZCHECK   CLI   0(RWTGC),ZCNST            FIRST NON-ZERO ID
         BNE   TCTLRTN
         LA    RWTGC,WGOFF(0,RWTGC)
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       WHEN FOUND, SET UP PARAMETERS
*                                       TO CALL THAT MODULE
         LA    R15,DXCCW12              ADDR FOR XCTL PARM LIST  Y02072
TRCTL    XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) TO NXT OPEN EXEC   Y02072
WGOFF    EQU   8                        SIZE OF ONE ENTRY ON WTG TABLE
PLOFF    EQU   4                        SIZE OF ONE ENTRY ON PARAM TAB
ONE      EQU   1                        MISC OFFSETS             S20201
FOUR     EQU   4                        *                        S20201
FIVE     EQU   5                        *                        S20201
SEVEN    EQU   7                        *                        S20201
USERWA   EQU   X'40'                    USER WORKAREA FLAG       S20201
OPNEXNAM DC    C'2M'                    ID OF THIS OPEN EXECUTOR
OPIDCNST DC    C'0S'                    ID OF OPEN LOAD #7
ZCNST    EQU   X'00'                    ZERO - SIGNALS INACTIVE ENTRY
WAOFF    EQU   32                       SIZE OF WA AT START OF WTG TAB
         SPACE 2
LOAD2Q   DC    C'2Q'                    ID OF MODULE IGG0192Q    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
         DCBD  DSORG=(IS)
WTGDSECT DSECT                                                   Y02072
IDTTR    DS    F                        IDTTR OF MODULE          Y02072
OPENWA   DS    A                        OPEN WORK AREA ADDRESS   Y02072
IHADCW   IGGBISAM                 DCB WORK AREA                  S20201
CB1      EQU   0                                                 S20201
CB10     EQU   0                                                 S20201
CB22     EQU   0                                                 S20201
CB23     EQU   0                                                 S20201
CB24     EQU   0                                                 S20201
CB25     EQU   0                                                 S20201
CJ11     EQU   0                                                 S20201
CP14     DS    0D
         IGGCP14  OPTCD=W                                        S20201
WC       EQU   LCH3C+LCH8C+LCH13C+LCH18C+LCH22C     WRITE CHK    S20201
*                                       CCWS                     S20201
LCP141   EQU   LCH3+LCH8+LCH13          NON-WRITE CHK            S20201
LCP142   EQU   LCH18+LCH22+LCH24                                 S20201
LCP14    EQU   LCP141+LCP142                                     S20201
FORCORE  DSECT                                                   Y02072
         IECDSECT                                                Y02072
         END
