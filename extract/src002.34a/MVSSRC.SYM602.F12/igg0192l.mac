         TITLE 'IGG0192L - OPEN,BISAM WRITE KN,LOAD NON PRIV,CHAN PGMS'
IGG0192L CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192L                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, BISAM MODE FOR WKN                    *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 01                                *
*                                                                     *
* FUNCTION = BEGIN INITIALIZATION FOR WKN.  LOAD WKN NON-PRIVILEGED   *
*            MODULE, IGG019JW, AND SAVE ADDRESS IN DCBLWKN.  LOAD     *
*            APPROPRIATE CP MODULE.  MOVE AND INITIALIZE CP8 AND      *
*            CP10B OR CP10BW.  SAVE REGISTERS FOR NEXT LOAD.          *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 646 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192L                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0192I IF   *
*              WKN ONLY; IF WKN AND ANY OTHER OPTION, FROM IGG0192K.  *
*              RECEIVES CONTROL IN STORAGE PROTECT KEY 5, AND         *
*              PRIVILEGED STATE.                                      *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL IN STORAGE PROTECT KEY 5 TO ISAM OPEN EXECUTOR   *
*               IGG0192M FOR FIXEC LENGTH RECORDS, OR IGG0192X FOR    *
*               VARIABLE LENGTH RECORDS.                              *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGBISAM - BISAM WORK AREA                          *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY, AND DEB                               *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, LOAD, AND XCTL.                                   *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*          RELEASE OF/VS2-02 DELETIONS                                *
*                                                               YM01159
*                                                                     *
***********************************************************************
         EJECT
RE       EQU   00                       WORK REG
RF       EQU   01                       WORK REG
RDCB     EQU   02                       USER'S DCB
RBASE    EQU   03                       BASE REG
RG       EQU   04                       WORK REG
RPAR     EQU   05                  *    PARAMETER LIST ADDRESS
RWTG     EQU   06                  *    WHERE TO GO TABLE BEGINNING
RPARC    EQU   07                  *    CURRENT ENTRY,PARAMETER LIST
RWTGC    EQU   08                  *    CURRENT ENTRY, WTG TABLE
RTIOT    EQU   09                       DCB ADDRESS FOR LOAD     Y02072
RWKA     EQU   10                       OPEN WORK AREA ADDR      Y02072
RA       EQU   11                       WORK REG
RB       EQU   12                       WORK REG
RC       EQU   13                       WORK REG
RD       EQU   14                       WORK REG
R15      EQU   15                       WORK REG
RCORE    EQU   RG                       DCB WA ADDRESS
IDOFF    EQU   6                        OFFSET INTO WTG TAB
*                   *     MEANS SET BEFORE ENTRY AND BEFORE EXIT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         L     RDCB,0(RPARC)            ADDRESS OF DCB COPY      Y02072
         USING IHADCB,RDCB
         L     RCORE,DCBWKPT2           RCORE ADDRESS OF DCB WA
         USING IHADCW,RCORE
         L      RA,DCBWKPT3             RA ADDR CURRENT WKN CH PG
         L     RTIOT,CVTPTR             RTIOT=ADDRESS OF CVT    YM01159
         USING CVT,RTIOT                CVT ADDRESSABILITY      YM01159
         L     RTIOT,CVTLINK            ADDRESS OF LINKLIB DCB  YM01159
         DROP  RTIOT                    END CVT ADDRESSABILITY  YM01159
*
         MVC   IDOFF(2,RWTG),OPNMKNNP    WKN NON PRIV
         LOAD  EPLOC=(RWTG),DCB=(RTIOT)  LOAD NON-PRIVILEGED MOD
         LR    1,0
         STH   RF,DCBLWKN+2              STORE ITS ADDRESS IN THE DCB
         SRL   RF,16
         STC   RF,DCBLWKN+1
         L     RC,DCBDEBAD                   DEB POINTER
         USING IHADEB,RC                     DEB IS IN RC
         SR    RB,RB                         ZERO REGISTER
         IC    RB,DEBNMEXT                   GET END OF DEB
         SLL   RB,4                          MPY BY 16
         LA    RB,BISAMEXT(RE,RB)       ADD ACS METH LEN         M0060
         AR    RC,RB                         VBL NO. OF EXTENTS
         MVC   DEBID3,OPNMKNNP           2 LOW ORDER DIGITS
         DROP  RC
         TM    DCBRECFM,X'80'            IS IT FIXED?             VLR
         BO    OPNOFIX1                   YES                     VLR
         MVC   IDOFF(2,RWTG),OPNMCPV    IGG019HP
         B     OPNO7BV                                            VLR
OPNOFIX1 EQU   *                                                  VLR
         SR    RB,RB                    SELECT PROPER MODULE NAME
         TM    DCBOPTCD,X'80'
         BZ    OPNO7B5A                 SEE TABLE OPNMCPKN
         LA    RB,4(RB)                 YES WC
OPNO7B5A TM    DCBRECFM,X'10'
         BZ    OPNO7B5B                 BRANCH UNBLOCKED
         LA    RB,2(RB)                 FB
OPNO7B5B LH    RC,DCBSMSW
         LTR   RC,RC
         BZ    OPNO7B5C                 BRANCH IF SYSTEM WA
         LA    RB,8(RB)                 YES WA
OPNO7B5C EQU   *
         LA    RE,OPNMCPKN(RB)          GET ADDRESS OF MOD
         LR    RF,RE                    PUT OFFSET INTO REGISTER ONE
         MVC   IDOFF(2,RWTG),0(RF)      MOVE IN ID OF MOD
OPNO7BV  EQU   *
         LOAD  EPLOC=(RWTG),DCB=(RTIOT)
         LR    1,0
         USING CP8,RA
         USING WTGTBL,RWTGC             WHERE TO GO TABLE        Y02072
         L     RWKA,OPENWA              ADDR OF OPEN ORK AREA    Y02072
         USING FORCORE,RWKA             OPEN WORK AREA ADDR      Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=13  CHANGE TO USER KEY  Y02072
*
         LR    RD,RA                    RA ADDR CURRENT WKN CH PG
         MVC   0(LCP8,RA),0(RF)         MOVE IN CP8              S20201
         L     RC,CB2                   MOVE CP8 BASIC
         ALR   RC,RD
         ST    RC,CB2
         L     RC,CB3
         ALR   RC,RD                    STRUCTURE, AND
         ST    RC,CB3
         L     RC,CB4
         ALR   RC,RD
         ST    RC,CB4
         L     RC,CB6                   INITIALIZE COMMANDS
         ALR   RC,RD
         ST    RC,CB6
         L     RC,CB7
         ALR   RC,RD
         ST    RC,CB7                   BY ADDING BASE ADDR
         L     RC,CB8
         ALR   RC,RD
         ST    RC,CB8
         L     RC,CB8A
         ALR   RC,RD                    OF CP8 TO ALL
         ST    RC,CB8A
         L     RC,CB9
         ALR   RC,RD
         ST    RC,CB9
         L     RC,CB12                  REFERENCES TO ADDRESSES
         ALR   RC,RD
         ST    RC,CB12
         L     RC,CB16                  *                        S20201
         ALR   RC,RD                    WITHIN CP8               S20201
         ST    RC,CB16                  *                        S20201
         L     RC,CB18
         ALR   RC,RD
         ST    RC,CB18
         L     RC,CB18B                 *                        S20201
         ALR   RC,RD                    *                        S20201
         ST    RC,CB18B                 *                        S20201
         L     RC,CB20
         ALR   RC,RD                    RD IS BASE ADDRESS
         ST    RC,CB20
         SR    RC,RC                    STORE KEY LENGTH WHERE
         IC    RC,DCBKEYLE
         STH   RC,CB5+6
         STH   RC,CB19+6                REQUIRED IN CP8
         TM    DCBRECFM,FIXED           IS IT VLR                S20201
         BZ    CP8THRU                  YES--BRANCH              S20201
         CLI   DCBFIRSH+TWO,ONE         IS SHARED TRACK PRESENT  S20201
         BNE   NOTSPCL                  YES--BRANCH              S20201
         CLI   DCBHIRPD,ONE             ONE RECORD PER TRACK     S20201
         BNE   NOTSPCL                  NO--BRANCH               S20201
         NI    CB8A+FOUR,X'FF'-CC       DISCONNECT LATTER PART   S20201
*                                       OF CP8--SPECIAL CASE.
         MVI   DCWHIRPS,ONE             SET FLD FOR SPECIAL CASE S20201
NOTSPCL  EQU   *                        *                        S20201
         L     RC,CB17                  *                        S20201
         ALR   RC,RD                    *                        S20201
         ST    RC,CB17                  *                        S20201
         L     RC,CB18A                 *                        S20201
         ALR   RC,RD                    *                        S20201
         ST    RC,CB18A                 *                        S20201
CP8THRU  EQU   *                        *                        S20201
         LA    RA,LCP8(RA)              POINT TO NEXT CP         S20201
         LA    RF,LCP8(RF)                                       S20201
         TM    DCBRECFM,X'80'            IS IT VLR?               VLR
         BZ    OPNOVAR5                   YES                     VLR
         MVC   0(LCP10B,RA),0(RF)                                S20201
         USING CP10B,RA                 MOVE CP10B OR CP10BW BEGINNING
         L     RC,CB53
         ALR   RC,RD
         ST    RC,CB53
         L     RC,CB54                  AND INITIALIZE
         ALR   RC,RD
         ST    RC,CB54
         TM    DCBOPTCD,X'80'                WC?
         BZ    OPNEND2L                 BRANCH NOT WC
         USING CP10BW,RA                CP10BW, END
         MVC   LCP10B(LCP10BW,RA),LCP10B(RF)                     S20201
         L     RC,CB54B
         ALR   RC,RD
         ST    RC,CB54B                 MOVE AND INITIALIZE
         LA    RA,LCP10BW(RA)                                    S20201
         LA    RF,LCP10BW(RF)                                    S20201
OPNEND2L LA    RA,LCP10B(RA)                                     S20201
         LA    RF,LCP10B(RF)            SAVE REGISTERS FOR       S20201
OPNOVAR5 EQU   *                                                  VLR
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         ST    RA,DXCCW9                SAVE ADDR OF             Y02072
         ST    RF,DXCCW10                                        Y02072
         MVC   0(L'LOAD2M,RWTGC),LOAD2M ID OF MODULE IGG0192M    Y02072
         TM    DCBRECFM,X'80'            IS IT FIXED              VLR
         BO    OPNOVAR6                                           VLR
         MVC   0(L'LOAD2X,RWTGC),LOAD2X  VLR GOES TO IGG0192X    Y02072
OPNOVAR6 EQU   *                                                  VLR
         DROP  RWKA                     END USING ON OPEN WA     Y02072
         LR    RCORE,RWKA               OPEN WORK AREA ADDRESS   Y02072
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT NAME ON THE WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)         AND DCB ON THE PARAM TABLE
         CLC   0(2,RWTGC),OPNEXNAM      IF NAME ON TABLE SAME ID AS
         BCR   8,RBASE                   CURRENT LOAD, BRANCH TO REPEAT
         CLC   0(2,RWTGC),OPIDCNST      IF NOT END OF WTG TABLE BRANCH
         BNE   RELOOP                    TO TRY NEXT NAME
         LR    RPARC,RPAR               OTHERWISE, RETURN TO START OF
         LA    RWTGC,WAOFF(0,RWTG)       BOTH TABLES AND SEARCH FOR
ZCHECK   CLI   0(RWTGC),ZCNST            FIRST NON-ZERO ID
         BNE   TCTLRTN
         LA    RWTGC,WGOFF(0,RWTGC)
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       WHEN FOUND, SET UP PARAMETERS
*
         USING FORCORE,RCORE                                     Y01021
         LA    R15,DXCCW12              WA ADDRESS               Y01021
TRCTL    XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
WGOFF    EQU   8                        SIZE OF ONE ENTRY ON WTG TABLE
PLOFF    EQU   4                        SIZE OF ONE ENTRY ON PARAM TAB
OPNEXNAM DC    C'2L'                    ID OF THIS OPEN EXECUTOR
OPIDCNST DC    C'0S'                    ID OF OPEN LOAD #7
ZCNST    EQU   X'00'                    ZERO - SIGNALS INACTIVE ENTRY
ONE      EQU   1                        MISC OFFSET              S20201
TWO      EQU   2                        *                        S20201
FOUR     EQU   4                        *                        S20201
SIX      EQU   6                        *                        S20201
FIXED    EQU   X'80'                    FIXED RECORD FORMAT      S20201
WAOFF    EQU   32                       SIZE OF WA AT START OF WTG TAB
*                                  TABLE OF WRITE KN CP MODULES
*                                   WC * FU/FB * WA * REL ADDR
*                                   ***************************
OPNMCPKN DC    C'JN'                    N  *  FU   * N  *   00
         DC    C'JO'                    N  *  FB   * N  *   02
         DC    C'JP'                    Y  *  FU   * N  *   04
         DC    C'JQ'                    Y  *  FB   * N  *   06
         DC    C'JR'                    N  *  FU   * Y  *   08
         DC    C'JS'                    N  *  FB   * Y  *   10
         DC    C'JT'                    Y  *  FU   * Y  *   12
         DC    C'JU'                    Y  *  FB   * Y  *   14
OPNMKNNP DC    C'JW'    WRITE KN NON-PRIVILEGED MODULE
OPNMCPV  DC    C'HP'
LOAD2M   DC    C'2M'                    ID OF MODULE IGG0192M    Y02072
LOAD2X   DC    C'2X'                    ID OF MODULE IGG0192X    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
FORCORE  DSECT                          O/C/EOV WORK AREA DSECT  Y01021
         IECDSECT                                                Y01021
         EJECT
         DCBD  DSORG=(IS)
IHADCW   IGGBISAM
IHADEB   IGGDEBD
         ORG   IHADEB+32
*        THE FOLLOWING ADDRESSES ARE MODIFIED BY NMEXT X 16
DEBID1   DS    CL2                      MODULE ID 1
DEBID2   DS    CL2                      MODULE ID 2
DEBID2A  DS    CL2                      MODULE ID 2A
DEBID2B  DS    CL2                     MODULE ID 2B
DEBID3   DS    CL2                      MODULE ID 3
DEBID4   DS    CL2                      MODULE ID 4
DEBID5   DS    CL2                      MODULE ID 5
IHAWKNCP DSECT      WRITE KN CHANNEL PROGRAM REFERENCES
CP8      DS    0D                                                S20201
         IGGCP8
*        FOR VLR CCWS CB17 TO CB20 ARE AS FOLLOWS--
*CB17    RR0,CD0,CC+SILI,11             GET TRKBAL
*CB18    TIC,CB18B,0,0                  SKIP FIRST READ DATA
*CB18A   RD,WA+KL,CC+SILI,0             READ BLK PRIOR TO INSERT
*CB18B   RC,CB23+3,CC+SILI,5            GET COUNT OF PROBABLE INS
*CB19    SKEQHI,DECBKEY,CC,KL           SEARCH TRACK FOR INSERT
*CB20    TIC,CB18A,0,0                  BLOCK
CP10B    DS    0D
CP10BW   DS    0D
         IGGCP10B  OPTCD=W                                       S20201
LCP8     EQU   CB26+8-CB1                                        S20201
LCP10BW  EQU   CB54C+8-CB540            LENGTHS OF CHAN PGMS     S20201
LCP10B   EQU   CB55+8-CB52-LCP10BW                               S20201
WTGTBL   DSECT
IDTTR    DS    F                        MODULE ID AND TTR
OPENWA   DS    A                        ADDR OF OPEN WORK AREA
         SPACE 2
         CVT   DSECT=YES                                        YM01159
         END
