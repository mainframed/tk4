     TITLE   'IGG0195D - OPEN, RESUME LOAD'
IGG0195D CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0195D                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, RESUME LOAD MODE WITH HI-LEVEL INDEX  *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = PERFORM RESUME LOAD INITIALIZATION OF INDEX LOCATION     *
*            TABLE, ISLIXLT.                                          *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 676 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0195D                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0196G WHEN *
*              HIGH-LEVEL INDEXES EXIST.  RECEIVES CONTROL IN STORAGE *
*              PROTECT KEY 5 AND PRIVILEGED STATE.                    *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL IN STORAGE PROTECT KEY 5 TO ISAM OPEN EXECUTOR   *
*               IGG0195T IF FULL TRACK INDEX WRITE IS SPECIFIED, OR   *
*               IGG0192U IF WRITE CHECK IS SPECIFIED, OR TO           *
*               IGG0192R WITHOUT WRITE CHECK.                         *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY, DEB AND FORMAT 2 DSCB                 *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET AND XCTL.                                          *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*A062810-062960,088600-148600              XA15623,OY14419     @ZA13472
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
*                                                                     *
FORCORE  DSECT
         IECDSECT
*
ISLCOMON DSECT
         DS    0D
ISLECBA  DS    A                        FOR CP18 AND CP20
ISLIOBA  DS    CL40
ISLECBB  DS    A                        FOR CP21
ISLIOBB  DS    CL40
ISLECBC  DS    A                        FOR CP19
ISLIOBC  DS    CL40
ISLAREAZ DS    CL88                     FOR CP19
INDXTYPE EQU   ISLAREAZ+86              HI LEVEL INDEX DEV TYPE   7S017
ISLIXLT  DS    CL104                    INDEX LOCATION TABLE
ISLNIRT  DS    CL3                      HHR OF DUMMY TRACK INDEX
ISLHIRT  DS    CL1                      NO INDX ENT ON PRIM DATA TRACK
ISLCBF   DS    F                        BUR CTRL PTR
ISLBMPR  DS    CL1                      USED TO BUMP CBF TO NEXT RCD
ISLFBW   DS    CL1                      NUM BUFS SCHED TO WRITE
ISLQC    DS    H                        NUM PRIME CYLINDERS
ISLM     DS    H                        NUM ENTRIES ON LAST TRK INDEX
ISLP     DS    H                        NUM TRACKS PER CYLINDER
ISLQ     DS    H                        NUM FULL TRKS TO BE INDEXED
ISLQ1    DS    H                        NUM TOTAL TRKS TO BE INDEXED
ISLQ2    DS    H                        NUM TRACKS IN CURNT INDEX
ISLQ3    DS    H                        NUM ENTRIES IN CURNT INDEX
ISLQ4    DS    H                        NUM CYLNDRS OF MASTER INDEX
ISLR2    DS    F                        ADR OF PRIME DEV TBL ENTRY
ISLDORE  DS    CL1                      SKIP OPEN 2D INDICATOR
ISLURSAV DS    16F                      USERS REGISTERS
ISLVRSAV DS    16F
         DS    CL92
TSTWK1C  DS    F
TSTWK2C  DS    F
         DS    CL104
ISLFXWK1 DS    F
ISLFXWK2 DS    F
IHADEB   DSECT
         DS    0D
DEBNMSUB DS    0CL1                     NUM SUBROUTINES LOADED AT OPEN
DEBTCBAD DS    A                        TCB POINTER
DEBAMLNG DS    0CL1                     LENGTH OF ACCESS METHOD SECTION
DEBDEBAD DS    A                        POINTER TO NEXT DEB IN TASK
DEBOFLGS DS    0CL1                     DATA SET STATUS FLAGS
DEBIRBAD DS    A                        POINTER TO IRB FOR ERROR EXIT
DEBOPATB DS    0CL1                     FILE TYPE
DEBSYSPG DS    A                        SYSTEM PURGE CHAIN
DEBNMEXT DS    0CL1                     NUMBER OF EXTENTS CONSTRUCTED
DEBUSRPG DS    A                        USER PURGE CHAIN
DEBPRIOR DS    0CL1                     DISPATCHING PRIORITY
DEBECBAD DS    A                        POINTER TO ECB
DEBPROTG DS    0BL1                     TASK PROTECTION FLAG
DEBDEBID DS    0BL1                     F TO IDENTIFY AS A DEB
DEBDCBAD DS    A                        POINTER TO CORRESPONDING DCB
DEBEXSCL DS    0CL1                     SCALE FOR SIZE OF DEV SECT
DEBAPPAD DS    A                        POINTER TO APPENDAGE SECTION
DEBNIEE  DS    0CL1                     NUMBER OF INDEX EXTENTS
DEBFIEAD DS    A                        ADR OF FIRST INDEX EXTENT
DEBNPEE  DS    0CL1                     NUMBER OF PRIME DATA EXTENTS
DEBFPEAD DS    A                        ADR OF FIRST PRIME DATA EXTENT
DEBNOEE  DS    0CL1                     NUMBER OF OVERFLOW EXTENTS
DEBFOEAD DS    A                        ADR OF FIRST OVFLO EXTENT
         DS    CL4                      DISABLED MODULE
DEBDVMOD DS    0CL1                     DEVICE MODIFIER
DEBUCBAD DS    A                        POINTER TO UCB
DEBBINUM DS    CL2                      BB OF EXTENT
DEBSTRCC DS    CL2                      STARTING CC
DEBSTRHH DS    CL2                      STARTING HH
DEBENDCC DS    CL2                      ENDING CC
DEBENDHH DS    CL2                      ENDING HH
DEBNMTRK DS    CL2                      NUMBER OF TRACKS IN EXTENT
         DCBD  DSORG=(IS)
DSCBF2   DSECT
         DS    0D
         DS    0CL140
         DC    X'02'
DS22MIND DS    BL7                      FTMI2
DS2L2MEN DS    BL5                      LEMI2
DS23MIND DS    BL7                      FTMI3
DS2L3MIN DS    BL5                      LEMI3
         DS    CL11
DS2LPDT  DS    CL8
DS2FMTID DS    CL1
DS2NOLEV DS    AL1                      NLEV
DS2DVIND DS    AL1                      NTM
DS21RCYL DS    AL3                      FIRSH
DS2LTCYL DS    AL2                      LDT
DS2CYLOV DS    AL1                      CYLOF
DS2HIRIN DS    AL1                      HIRCM
DS2HIRPR DS    AL1                      HIRPD
DS2HIROV DS    AL1                      HIROV
DS2RSHTR DS    AL1                      HIRSH
DS2HIRTI DS    AL1
         DS    AL1
DS2TAGDT DS    AL2                      TDC
DS2RORG3 DS    AL3                      RORG3
DS2NOBYT DS    AL2                      NCRHI
DS2NOTRK DS    AL1                      NTHI
DS2PRCTR DS    CL4                      NREC
DS2STIND DS    BL1                      ST
DS2CYLAD DS    BL7                      FTCI
DS2ADLIN DS    BL7                      FTMI1
DS2ADHIN DS    BL7                      FTHI
DS2LPRAD DS    BL8                      LPDA
DS2LTRAD DS    BL5                      LETI
DS2LCYAD DS    BL5                      LECI
DS2LMSAD DS    BL5                      LEMI1
DS2LOVAD DS    BL8                      LIOV
DS2BYOVL DS    AL2                      NBOV
DS2RORG2 DS    AL2                      RORG2
DS2OVRCT DS    AL2                      NOREC
DS2RORG1 DS    AL2                      RORG1
DS2NIRT  DS    CL3
DS2PTRDS DS    BL5
         EJECT
***********************************************************************
*                                                                     *
*                        RESUME LOAD - OPEN                           *
*                                                                     *
***********************************************************************
IGG0195D CSECT
BEGIN    BALR  R9,0
         USING *,R9
R0       EQU   0                        WORK REGISTER
R1       EQU   1                        BASE REGISTER FOR DCB
R2       EQU   2                        DEVICE TABLE ENTRY ADDRESS
R3       EQU   3                        WORK REGISTER
R4       EQU   4                        WORK REGISTER
R5       EQU   5                        WORK REGISTER
R6       EQU   6                        WORK REGISTER
R7       EQU   7                        WORK REGISTER
R8       EQU   8                        WORK REGISTER
R9       EQU   9                        WORK REGISTER
R10      EQU   10                       WORK REGISTER
R11      EQU   11                       WORK REGISTER
R12      EQU   12                       BASE REGISTER FOR USERS DEB
R13      EQU   13                       BASE REG FOR COMON WORK AREA
R14      EQU   14                       WORK REGISTER
R15      EQU   15                       WORK REGISTER
RCORE    EQU   4                        BASE FOR OPEN WORK AREA
RPAR     EQU   5                        ADDR OF PARAMETER TABLE
RWTG     EQU   6                        ADDR OF WHERE-TO-GO TABL
RPARC    EQU   7                        CURRENT PARAMETER ENTRY
RWTGC    EQU   8                        CURRENT WHERE-TO-GO ENTR
CVTPTR   EQU   16
UCBTYPE  EQU   19                       LOCATION OF UCB DEVTYPE   7S017
         SPACE 3
BASETAG  L     R1,0(RPARC)              R1 = ADDR OF DCB
         USING IHADCB,R1
         L     R12,DCBDEBAD             R12 = DEB ADDRESS
         USING IHADEB,R12
         L     R13,DCBWKPT1             R13=ADDR COMON WORK AREA
         USING ISLCOMON,R13
         L     RCORE,4(RWTGC)           RCORE=ADR OPEN WORK AREA
         USING FORCORE,RCORE
         STM   R0,R15,DXCCW1            SAVE REGISTERS           Y02072
         L     R8,DSCCORE               A(FORMAT 2 DSCB)          M3233
         USING DSCBF2,R8                                          M3233
         LR    R14,RCORE                SAVE WA ADDR             Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=3  SET USERS KEY        Y02072
*
         DROP  RCORE                    END OPEN WA ADDR         Y02072
         USING FORCORE,R14              EST OPEN WA ADDR         Y02072
         SPACE 3
***********************************************************************
*                                                                     *
*              THIS ROUTINE WILL INITIALIZE THE BEGINNING AND         *
*              STEPPING ADDRESSES FOR THE ISLIXLT TABLE FOR           *
*              RESUME LOADING IF CYLINDER OR MASTER INDEXES           *
*              ARE REQUIRED.                                          *
*                                                                     *
***********************************************************************
IXLTABLE LA    R3,ISLIXLT               ADDRESS OF IXLT TABLE
         XC    TSTWK1C(8),TSTWK1C
         XC    ISLFXWK2(8),ISLFXWK2
         LA    R7,32(R12)               DEB + 32                  23786
***********************************************************************
*                                                                     *
*                             CYLINDER INDEX                          *
*                                                                     *
***********************************************************************
BEGINCI  MVC   TSTWK1C(7),DCBFTCI       CI STARTING ADDR-MBBCCHH
         MVC   ISLFXWK1(5),DCBLECI      CI CURRENT ADDR-CCHHR
         MVI   ISLFXWK2+2,X'01'         SET NUMBER OF LEVELS-1
         MVI   0(R3),X'20'              SET CI AS CURRENT LEVEL
         CLC   DCBLPDA+6(1),DCBLDT+1    LAST TRACK OF PD CYL      7S017
         BNE   OVERLAY                  NO-OVERLAY CI
         TM    DCBST,X'03'              IS LAST TRK AND BLK FULL
         BO    SETIXLT                  YES-DO NOT OVERLAY CI
***********************************************************************
*                                                                     *
*              DECREMENT R TO OVERLAY THE INDEX ENTRY.                *
*                                                                     *
***********************************************************************
OVERLAY  MVI   ISLFXWK2+3,X'01'         TURN THE OVERLAY SW ON
         IC    R5,ISLFXWK2              R
         BCTR  R5,0                     R - 1
         STC   R5,ISLFXWK2              SET R TO PREVIOUS ENTRY
***********************************************************************
*                                                                     *
*              MOVE ADDRESSES INTO IXLT TABLE                         *
*                                                                     *
***********************************************************************
SETIXLT  MVC   1(7,R3),TSTWK1C          MBBCCHH FOR BEGIN ADDR
         MVI   8(R3),X'01'              R=1 FOR BEGIN ADDR
         MVC   12(5,R3),ISLFXWK1        CCHHR FOR STEPPING ADDR
         MVC   9(3,R3),1(R3)            MBB FROM BEGIN TO STEP
         CLI   2(R3),X'00'              IS B ZERO (M FOR DCBLE)
         BE    SAMEXTNT                 YES - SAME EXTENT
         MVC   9(1,R3),2(R3)            1ST B-BEGIN TO M OF STEP
         XC    2(1,R3),10(R3)           ZERO HIGH ORDER BIN
***********************************************************************
*                                                                     *
*                             MASTER 1 INDEX                          *
*                                                                     *
***********************************************************************
SAMEXTNT CLI   ISLFXWK2+2,X'01'         ARE CI AND M1 COMPLETE
         BH    IXLTM3                   YES-GO TO M2
         CLI   DCBNLEV,X'01'            IS CI THE LAST LEVEL
         BE    SETINDC                  YES-SET CI AS LAST LEVEL
         MVC   TSTWK1C(7),DCBFTMI1      M1 STARTING ADDR
         MVC   ISLFXWK1(5),DCBLEMI1     M1 CURRENT ADDR
         MVI   ISLFXWK2+2,X'02'         SET NUMBER OF LEVELS-2
         LA    R3,26(0,R3)              ADD 26 TO BASE FOR M1
         CLC   DCBLECI+4(1),DCBHIRCM    IS CI TRACK FULL
         BNE   OVERLAY                  NO-BR TO OVERLAY
PREOVRLY CLI   ISLFXWK2+3,X'01'         ANY PREVIOUS OVERLAY
         BE    OVERLAY                  YES-CONTINUE OVERLAY
         B     SETIXLT                  YES-DO NOT OVERLAY INDEX
***********************************************************************
*                                                                     *
*                             MASTER 2 INDEX                          *
*                                                                     *
***********************************************************************
IXLTM3   CLI   ISLFXWK2+2,X'02'         ARE CI,M1,M2 COMPLETE
         BH    IXLTM4                   YES-GO TO M3
         CLI   DCBNLEV,X'02'            IS M1 THE LAST LEVEL
         BE    SETINDC                  YES-SET M1 AS LAST LEVEL
         MVC   TSTWK1C(7),DCBFTMI2      M2 STARTING ADDRESS
         MVC   ISLFXWK1(5),DCBLEMI2     M2 CURRENT ADDRESS
         MVI   ISLFXWK2+2,X'03'         SET NUMBER OF LEVELS-3
         LA    R3,26(0,R3)              INCREMENT BASE BY 26-M2
         CLC   DCBLEMI1+4(1),DCBHIRCM   IS M1 TRACK FULL
         BNE   OVERLAY                  NO-BRANCH TO OVERLAY
         B     PREOVRLY                 YES-CHECK PREVIOUS OVRLY
***********************************************************************
*                                                                     *
*                             MASTER 3 INDEX                          *
*                                                                     *
***********************************************************************
IXLTM4   CLI   ISLFXWK2+2,X'03'         ARE CI,M1,M2,M3 COMPLETE
         BH    SETINDC                  YES-SET M3 AS LAST LEVEL
         CLI   DCBNLEV,X'03'            IS M2 THE LAST LEVEL
         BE    SETINDC                  YES-SET M2 AS LAST LEVEL
         MVC   TSTWK1C(7),DS23MIND      M3 STARTING ADDRESS       M3233
         MVC   ISLFXWK1(5),DS2L3MIN     M3 CURRENT ADDRESS        M3233
         MVI   ISLFXWK2+2,X'04'         SET NUMBER OF LEVELS-4
         LA    R3,26(R3)                INCREMENT BASE BY 26-M3
         CLC   DCBLEMI2+4(1),DCBHIRCM   IS M2 TRACK FULL
         BNE   OVERLAY                  NO - BRANCH TO OVERLAY
         B     PREOVRLY                 YES-DO NOT OVERLAY INDEX
*
SETINDC  MVI   0(R3),X'80'              SET LAST LEVEL INDICATOR
         EJECT
***********************************************************************
*                                                                     *
*              SET ENDING ADDRESS IN ISLIXLT TABLE                    *
*                                                                     *
***********************************************************************
ENDIXLT  SR    R6,R6
         LA    R3,ISLIXLT
         L     R5,CVTPTR               C(R5)-A(CVT)
         L     R5,64(0,R5)             C(R5)-A(I/O DEVICE TBL)
         IC    R6,INDXTYPE              C(R6)-DEVICE TYPE CODE    7S017
         IC    R6,0(R6,R5)             C(R6)-DEV TBL OFFSET
         IC    R6,3(R6,R5)             C(R6)-TRKS/CYL THIS DEV
         BCTR  R6,0
         SR    R5,R5
         SR    R4,R4
         IC    R4,DCBNLEV
         MVC   17(7,R3),9(R3)
ISLB     STC   R6,23(0,R3)
ISLH     MVC   24(1,R3),DCBHIRCM
         CLI   DCBNLEV,X'01'            ONLY ONE LEVEL?        @ZA13472
         BNE   ISLC                     NO BRANCH              @ZA13472
         CLC   DCBHIRCM,D16(R3)         TEST FOR END OF TRACK  @ZA13472
         BNE   ISLC                     NO BRANCH              @ZA13472
         MVI   D16(R3),ZERO             SET RECORD  R=0        @ZA13472
         LH    R5,D14(R3)               SET HEAD=HEAD+1        @ZA13472
         LA    R5,D1(R5)                *                      @ZA13472
         STH   R5,D14(R3)               *                      @ZA13472
         STC   R6,TSTWK1C               HIGH HEAD              @ZA13472
         CLC   D15(D1,R3),TSTWK1C       LAST TRACK OF CYL      @ZA13472
         BNH   ISLC                     NO BRANCH              @ZA13472
         XC    D14(D2,R3),D14(R3)       HEAD=0                 @ZA13472
         LH    R5,D12(R3)               CYL=CYL+1              @ZA13472
         LA    R5,D1(R5)                *                      @ZA13472
         STH   R5,D12(R3)               *                      @ZA13472
ISLC     EQU   *                                               @ZA13472
         B     ISLHIRN
*
SETEND   CLC  DCBHIRCM,D16(R3)          TEST LEVEL ON EOT        M5305
         BNE   SETEND1                  NO STEPPING OK           M5305
         MVI   D16(R3),ZERO             YES SET R=0              M5305
         LH    R5,D14(R3)               SET HEAD = HEAD +1       M5305
         LA    R5,D1(R5)                *                        M5305
         STH   R5,D14(R3)               *                        M5305
         STC   R6,TSTWK1C               HIGH H                   M5305
         CLC   D15(D1,R3),TSTWK1C       LAST TRACK OF CYLINDER   M5305
         BNH   SETEND1                  STEPPING ENTRY CORRECT   M5305
* NEXT ENTRY ON NEXT CYLINDER
         XC    D14(D2,R3),D14(R3)       H=0                      M5305
         LH    R5,D12(R3)               CYL=CYL+1                M5305
         LA    R5,D1(R5)                *                        M5305
         STH   R5,D12(R3)               *                        M5305
         B     SETEND1                  CONTINUE                 M5305
*
ISLA     IC    R5,7(0,R3)
         BCTR  R5,0
         STC   R5,49(0,R3)
         MVC   43(6,R3),1(R3)
         B     ISLHIRM
*
SETEND1  CLC   4(3,R3),38(R3)           INDEX ON SAME CYL AS PREV 7S028
         BE    ISLA                     YES, BRANCH TO REDUCE TRK 7S028
         MVC   43(6,R3),35(R3)          END MBBCCH=CURR MBBCCH    7S028
         STC   R6,49(R3)                END TRACK = DEVICE MAX    7S028
ISLHIRM  MVC   50(1,R3),DCBHIRCM
         LA    R3,26(0,R3)
ISLHIRN  BCT   R4,SETEND
         SPACE 3
*                                                                     *
* EXIT ********** EXIT ********** EXIT ********** EXIT ****************
*                                                                     *
EXIT     LM    R0,R15,DXCCW1            RESTORE REGISTERS        Y02072
         DROP  R14                      END OPEN WA ADDR         Y02072
         USING FORCORE,RCORE            EST OPEN WA ADDR         Y02072
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         USING BASETAG,R9
         TM    DCBOPTCD,X'40'           FULL TRACK INDEX WRITE   O19110
         BO    EXIT5T                   YES                      O19110
         TM    DCBOPTCD,X'80'           TEST FOR WRITE CHECK      24503
         BZ    TSTEXIT                  BRANCH IF NO              24503
         MVC   0(L'LOAD2U,RWTGC),LOAD2U ID OF IGG0192U           Y02072
         B     RELOOP                                             24503
EXIT5T   EQU   *                                                 O19110
         MVC   0(L'LOAD5T,RWTGC),LOAD5T ID FOR IGG0195T          Y02072
         B     RELOOP                                            O19110
TSTEXIT  MVC   0(L'LOAD2R,RWTGC),LOAD2R ID FOR IGG0192R          Y02072
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,R9                     BR-BEGIN OF THIS MODULE
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BC    7,RELOOP                 BRANCH=NOT AT END
         LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BC    7,TCTLRTN                BRANCH = NOT ZERO
         LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         LA    R15,DXCCW12
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
         DS    0D
D16      EQU   16                       16                       M5305
ZERO     EQU   0                        0                        M5305
D14      EQU   14                       14                       M5305
D1       EQU   1                        1                        M5305
D15      EQU   15                       15                       M5305
D2       EQU   2                        2                        M5305
D12      EQU   12                       12                       M5305
OPNLOD7  DC    C'0S'
THISLOAD DC    C'5D'
*
LOAD2R   DC    C'2R'                    ID OF MODULE IGG0192R    Y02072
LOAD2U   DC    C'2U'                    ID OF MODULE IGG0192U    Y02072
LOAD5T   DC    C'5T'                    ID OF MODULE IGG0195T    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         DC    C'IGG0195D'              MODULE ID              @ZA13472
         DC    C'&SYSDATE'              DATE OF FIX            @ZA13472
         DC    C' ZA13472'              FLAG OF FIX            @ZA13472
         END
