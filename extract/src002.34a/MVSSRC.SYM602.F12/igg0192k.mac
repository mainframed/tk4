         TITLE 'IGG0192K - OPEN,BISAM,LOAD NON PRIV,DYN BUFF,CP4-7'
IGG0192K CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192K                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, BISAM MODE, WKN AND/OR ANY OTHER      *
*                    OPTIONS.                                         *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 01                                *
*                                                                     *
* FUNCTION = LOAD IGG019JM IF WRITE CHECK OR VLR, AND INITIALIZE      *
*            CHANNEL PROGRAMS (CP'S).  OTHERWISE, LOAD IGG0192L AND   *
*            INITIALIZE CP'S.  THEN DELETE CP MODULE.  IF DYNAMIC     *
*            BUFFERING REQUESTED, LOAD IGG019JI.  LOAD NON-PRIVILEGED *
*            MODULE IGG019JV.                                         *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 838 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192K                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0192I, IF  *
*              READ K, READ KU, OR WRITE K IS SPECIFIED.  RECEIVES    *
*              CONTROL IN STORAGE PROTECT KEY 5 AND PRIVILEGED STATE. *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL IN STORAGE PROTECT KEY 5 TO ISAM OPEN EXECUTOR   *
*               IGG0192L IF WKN IS SPECIFIED, OTHERWISE TO  IGG0192J. *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGBISAM - BISAM WORK AREA                          *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY AND DEB                                *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, LOAD, DELETE, AND XCTL                            *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*         RELEASE OS/VS2-02 DELETIONS                                 *
*                                                               YM01159
*                                                                     *
***********************************************************************
         EJECT
RE       EQU   00                       WORK REG
RF       EQU   01                       WORK REG
RDCB     EQU   02                       USER'S DCB
RBASE    EQU   03                       BASE REG
RG       EQU   04                       WORK REG
RCORE    EQU   04
RPAR     EQU   05                  *    PARAMETER LIST ADDRESS
RWTG     EQU   06                  *    WHERE TO GO TABLE BIGINNING
RPARC    EQU   07                  *    CURRENT ENTRY,PARAMETER LIST
RWTGC    EQU   08                  *    CURRENT ENTRY, WTG TABLE
RTIOT    EQU   09                       CVT DCB ADDRESS
RUCB     EQU   10                       UCB ADDRESS
RA       EQU   11                       WORK REG
RB       EQU   12                       WORK REG
RC       EQU   13                       WORK REG
RD       EQU   14                       WORK REG
R15      EQU   15                       WORK REG
IDOFF    EQU   6                        OFFSET FOR ID IN WTG TAB
WRTCHK   EQU   X'80'                    TEST FOR WR VALID CHK    Y02072
*                   *     MEANS SET BEFORE ENTRY AND BEFORE EXIT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         L     RDCB,0(RPARC)            RDCB POINTS TO DCB
         USING IHADCB,RDCB
         L     RG,DCBWKPT2              RG   POINTS TO DCB WA
         USING IHADCW,RG
         USING WTGDSECT,RWTGC           WHERE TO GO TABLE        Y02072
         L     RB,OPENWA                OPEN WORK AREA ADDR      Y02072
         USING FORCORE,RB               OPEN WA ADDRESSABILITY   Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11  CHANGE TO USER KEY  Y02072
*
         L     RD,DCWFCP4               RD    ADDRESS WHERE CP4 WILL BE
         DROP  RG
         LR    RA,RD                    RA    ADDRESS CURRENT CP
         USING CP4,RA
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         TM    DCBOPTCD,X'80'           LOAD READ AND UPDATE CP
         BO    OPNO7B3A
         TM    DCBRECFM,X'80'            TEST IF VARIABLE         VLR
         BZ    OPNO7B3A                  B=VARIABLE               VLR
*                                       ONLY WC VERSIONS OF CP ARE USED
*                                       IN VARIABLE- INITIALIZED DIFFER
*                                      -ENTLY  IF NO  WC.
*
         MVC   IDOFF(2,RWTG),OPNMRUNW   CP-4-5-6, CP7 W/O WCK
         B     OPNO7B3B
OPNO7B3A EQU   *
         MVC   IDOFF(2,RWTG),OPNMRUWC   CP4,5,6, CP7 W WRITE CHK
OPNO7B3B L     RTIOT,CVTPTR             RTIOT=ADDRESS OF CVT    YM01159
         USING CVT,RTIOT                CVT ADDRESSABILITY      YM01159
         L     RTIOT,CVTLINK            ADDRESS OF LINKLIB DCB  YM01159
         DROP  RTIOT                    END CVT ADDRESSABILITY  YM01159
*
         LOAD  EPLOC=(RWTG),DCB=(RTIOT) LOAD MODULE
         LR    RF,RE                    RF ADDR OF MODULE
         SR    RE,RE
         IC    RE,DCBNCP                RE  NO. OF CP4-5-6 TO BE LOADED
*
         MODESET  KEYADDR=DXUKEY,WORKREG=14  CHANGE TO USER KEY  Y02072
*
         DROP  RB                       DROP REGISTER            Y02072
         LR    RD,RA                                             A34478
OPNO7D3  MVC   0(256,RA),0(RF)          MOVE CP4-5-6 TO CP AREA
         TM    DCBRECFM,X'80'            TEST IF VARIABLE         VLR
         BZ    OPNO7C3                   B=VARIABLE               VLR
         TM    DCBOPTCD,X'80'
         BO    OPNO7D3A
         MVC   CP4+256(OPNSIZE1),256(RF)
         B     OPNO7D3B
OPNO7C3  MVI   CA16B,TIC                 SET CA16B TO TIC TO     S20201
*                                       CA20                     S20201
         MVI   CA16B+THREE,CA20-CA01                             S20201
         MVI   CA16A,RHA                REPOSITION TO START      S20201
         OI    CA16A+FOUR,SKIP          OF THE TRACK             S20201
OPNO7D3A MVC   CP4+256(OPNSIZE2),256(RF)
OPNO7D3B LA    RB,OPNTAB1               SET UP CP4 AND CP5 OR THE
         BAL   R15,OPNSETUP              BEGINNING OF CP5W
         MVC   CA5+7(1),DCBKEYLE
         MVC   CA8+7(1),DCBKEYLE
         MVC   CA21+7(1),DCBKEYLE
         TM    DCBOPTCD,X'80'
         BO    OPNO7D3D
         TM    DCBRECFM,X'80'            TEST IF VARIABLE         VLR
         BZ    OPNO7D3C                  B=VARIABLE               VLR
         LA    RB,OPNTAB4              SET UP CP6
         BAL   R15,OPNSETUP
         MVC   CA28+7(1),DCBKEYLE
         B     OPNO7D3E
OPNO7D3C MVI   CA43+CA43CC,NOCC          TURN OFF COMMAND       SA61527
*                                       CHAIN-CA43                VLR
OPNO7D3D LA    RB,OPNTAB2              IF WC   SET UP THE END OF CP5W
         BAL   R15,OPNSETUP
         LA    RA,OPNSIZE3(RA)
         LA    RB,OPNTAB3              SET UP CP6W
         BAL   R15,OPNSETUP
         MVC   CA28+7(1),DCBKEYLE
         LA    RA,OPNSIZE4(RA)
OPNO7D3E LA    RA,OPNSIZE1+256(RA)     RA SET TO ADDR NXT SET OF CP'S
         BCT   RE,OPNO7D4                TO BE SET UP, SET THE LINK
         LR    RD,RA                    IN REL CCW2
         B     OPNO7F3
OPNO7D4  ST    RA,12(RD)                 AND RETURN TO SET UP NEXT CP
         LR    RD,RA
         B     OPNO7D3
         USING CP7,RA
OPNO7F3  L     RG,DCBWKPT2             SET ADDRESS OF FIRST CP7 OR 7W
         USING IHADCW,RG
         ST    RA,DCWFCP7
         DROP  RG
         IC    RE,DCBNCP                INITIALIZE COUNTER
         TM    DCBOPTCD,X'80'          GET CP7 OR 7W IN CP MODULE
         BO    OPNO7G31
         TM    DCBRECFM,X'80'            TEST IF VARIABLE         VLR
         BZ    OPNO7G31                  B=VARIABLE               VLR
         LA    RF,OPNSIZE1+256(RF)
         B     OPNO7G3A
OPNO7G31 LA    RF,OPNSIZE2+256(RF)
OPNO7G3A MVC   CP7(OPNSIZE5),0(RF)      MOVE AND INITIALIZE CP7 OR CP7W
         TM    DCBOPTCD,X'80'
         BO    OPNO7G32
         TM    DCBRECFM,X'80'            TEST IF VARIABLE         VLR
         BZ    OPNO7G30                  B=VARIABLE               VLR
         LA    RB,OPNTAB6
         BAL   R15,OPNSETUP
         B     OPNO7G3B
OPNO7G30 MVI   CA46+4,X'00'              TURN OFF COMMAND-CHAIN-  VLR
*                                       CA46                      VLR
OPNO7G32 MVC   CP7+OPNSIZE5(OPNSIZE6),OPNSIZE5(RF)
         LA    RB,OPNTAB5
         BAL   R15,OPNSETUP
         LA    RA,OPNSIZE6(RA)
OPNO7G3B LA    RA,OPNSIZE5(RA)          IF THIS IS NOT THE LAST CP7 TO
         BCT   RE,OPNO7H2                BE SET UP, SET THE LINK IN REL
         LR    RD,RA
         B     OPNO7J3                   CCW2
OPNO7H2  ST    RA,12(RD)
         LR    RD,RA                     AND RETURN TO SET UP NEXT CP
         B     OPNO7G3A
OPNO7J3  EQU   *                                                 Y02072
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         TM    DCBOPTCD,WRTCHK          WRITE VALIDITY CHECK     Y02072
         BO    OPNO7J3A
         TM    DCBRECFM,X'80'            TEST IF VARIABLE         VLR
         BZ    OPNO7J3A                  B=VARIABLE               VLR
         MVC   IDOFF(2,RWTG),OPNMRUNW   READ,UPDATE, NO WR CH
         B     OPNO7J3B
OPNO7G3C MVC   0(0,RA),0(RF)
OPNO7J3A MVC   IDOFF(2,RWTG),OPNMRUWC   READ,UPDATE WITH WR CHK
*
OPNO7J3B DELETE  EPLOC=(RWTG)
         MVC   IDOFF(2,RWTG),OPNMRUNP   READ,UPDATE NON PRIV
         LOAD  EPLOC=(RWTG),DCB=(RTIOT) LOAD NON-PRIV MODULE
         STH   RE,DCBLRAN+2              AND STORE ITS ADDRESS
         SRL   RE,16
         STC   RE,DCBLRAN+1
         L     RB,DCBDEBAD              SAVE ID IN DEB
         USING IHADEB,RB                RB = DEB ADDRESS         Y01021
         SR    RC,RC
         IC    RC,DEBNMEXT
         SLL   RC,4
         LA    RC,BISAMEXT(RC)          ADD DEB EXTENSION LENGTH S21045
         AR    RC,RB
         DROP  RB
         USING IHADEB,RC                RC   DEB EXT ADDR
         TM    DCBMAC,X'02'            IF WKN, LEAVE ROOM FOR 2 EXTRA
         BZ    OPNO7F4                  ID'S.
         LA    RC,4(0,RC)
OPNO7F4  MVC   DEBID2B,OPNMRUNP         MOVE ID TO DEB
         TM    DCBMACRF,X'04'           IF DYNAMIC BUFFERING REQUIRED,
         BZ    OPNO7A5                  LOAD THE MODULE AND STORE ITS
*                                       ADDRESS
         MVC   IDOFF(2,RWTG),OPNMDYNB    DYNAMIC BUFFERING
         LOAD  EPLOC=(RWTG),DCB=(RTIOT)
         ST    RE,DCBFREED               IN THE DCB
         DROP  RC                                                S21045
         USING IHADEB,RB                RB   DEB ADDR
         L     R15,DEBEXPTR             GET ADDR OF DEB
*                                       EXTENSION PTR            S21045
         USING DEBEXT,R15               *
         ST    RE,DEBFREED              STORE DYN BUF MODULE    S21045
         DROP  RB
         USING IHADEB,RC                                         S21045
         L     RD,DEBAPPAD-IHADEB(0,RB) AND IN THE APPENDAGE LIST
         USING DEBAVT,RD
         TM    DEBSIOA,VIRTUAL          VIRTUAL = REAL
         BZ    OPNAOS                   BZ MEANS YES
         LR    RF,RE                    SIO OFFSET IN DYM. BUF.
         LA    RF,FOUR(RF)              MODULE IS + 4
         ST    RF,DEBSIOA2              SAVE ADDR OF DYM BUF MOD
OPNAOS   EQU   *                        *
         DROP  R15                      *
         OI    DEBRPSID-IHADEB(RB),SIOLOAD SIO APPEND LOADED     S20201
         TM    DEBSIOA,VIRTUAL          VIRTUAL = REAL           M4257
         BO    OPNAOS1                  BO MEANS NO              M4257
         DROP  RD                                                M4257
         LR    RF,RE
         LA    RF,4(0,RF)                (APPENDAGE ENTRY POINT IS +4)
         ST    RF,4(0,RD)
OPNAOS1  EQU   *                        *
         MVC   DEBID3,OPNMDYNB          SAVE ID IN DEB
OPNO7A5  MVC   0(L'LOAD2J,RWTGC),LOAD2J MOD ID OF IGG0192J       Y02072
         TM    DCBMAC,X'02'             IF WKN, CALL FIRST WKN EXTOR.
         BZ    ENDOPENX                  OTHERWISE, CALL LAST OPEN
         MVC   0(L'LOAD2L,RWTGC),LOAD2L MODULE ID OF IGG0192L    Y02072
         ST    RA,DCBWKPT3              IF WKN, SAVE ADDR FIRST CP
ENDOPENX EQU   *                        ID CORRECT
         L     RCORE,4(RWTGC)
RELOOP   LA    RWTGC,WGOFF(00,RWTGC)    GET NEXT NAME ON THE WTG TABLE
         LA    RPARC,PLOFF(00,RPARC)       AND DCB ON THE PARAM TABLE
         CLC   0(2,RWTGC),OPNEXNAM      IF NAME ON TABLE SAME ID AS
         BCR   8,RBASE                   CURRENT LOAD, BRANCH TO REPEAT
         CLC   0(2,RWTGC),OPIDCNST      IF NOT END OF WTG TABLE BRANCH
         BNE   RELOOP                    TO TRY NEXT NAME
         LR    RPARC,RPAR               OTHERWISE, RETURN TO START OF
         LA    RWTGC,WAOFF(00,RWTG)      BOTH TABLES AND SEARCH FOR
ZCHECK   CLI   0(RWTGC),ZCNST           FIRST NON-ZERO ID
         BNE   TCTLRTN
         LA    RWTGC,WGOFF(00,RWTGC)
         LA    RPARC,PLOFF(00,RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       WHEN FOUND, SET UP PARAMETERS
*                                       TO CALL THAT MODULE
         USING FORCORE,RCORE                                     Y01021
         LA    R15,DXCCW12              WA ADDRESS               Y01021
TRCTL    XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
WGOFF    EQU   8                        SIZE OF ONE ENTRY ON WTG TABLE
PLOFF    EQU   4                        SIZE OF ONE ENTRY ON PARAM TAB
OPNEXNAM DC    C'2K'                    ID OF THIS OPEN EXECUTOR
OPIDCNST DC    C'0S'                    ID OF OPEN LOAD #7
ZCNST    EQU   X'00'                    ZERO - SIGNALS INACTIVE ENTRY
NOCC     EQU   ZCNST                    TURN OFF CC             SA61527
THREE    EQU   3                        MISC OFFSET              S20201
FOUR     EQU   4                        *                        S20201
WAOFF    EQU   32                       SIZE OF WA AT START OF WTG TAB
SIOLOAD  EQU   X'10'                    SIO APPEND LOADED        S20201
VIRTUAL  EQU   X'80'                    VIRTUAL MASK
*
*   SETUP CCW'S POINTED TO BY TABLE USING RD AS A BASE AND DISPLACEMENT
*   FROM THE CCW. CCW FOUND BY USING DISPLACEMENT FORM TABLE X 8 +RG.
*
OPNSETUP SR    RG,RG                    CLEAR
         IC    RG,0(RB)                 NUMBER OF CCW IN CP
         SLL   RG,DW                    DISPLACEMENT INTO CP
DW       EQU   3                        8 = 2 ** 3
         L     RC,0(RG,RA)              LOAD THE CCW, WORD 1
         ALR   RC,RD                    ADJUST ITS ADDRESS
         ST    RC,0(RG,RA)              RESTORE THE CCW
         LA    RB,1(RB)                 GET NEXT ENTRY IN TABLE
         CLI   0(RB),X'FF'              X'FF' SIGNALS TABLE END
         BNE   OPNSETUP                 LOOP IF NOT END
         BR    R15                      RETURN VIA R15 IF END
CCWLEN   EQU   8                        LENGTH OF ONE CCW
OPNTAB1  DC    AL1((CA02-CP4)/CCWLEN)    *
         DC    AL1((CA03-CP4)/CCWLEN)   *
         DC    AL1((CA2-CP4)/CCWLEN)    *
         DC    AL1((CA4-CP4)/CCWLEN)    CP5 OR THE BEGINNING OF CP5W
         DC    AL1((CA6-CP4)/CCWLEN)    *
         DC    AL1((CA6B-CP4)/CCWLEN)   *
         DC    AL1((CA7-CP4)/CCWLEN)    *
         DC    AL1((CA9-CP4)/CCWLEN)    *
         DC    AL1((CA10-CP4)/CCWLEN)   *
         DC    AL1((CA11-CP4)/CCWLEN)   *
         DC    AL1((CA14-CP4)/CCWLEN)   *
         DC    AL1((CA15-CP4)/CCWLEN)   *
         DC    AL1((CA16A-CP4)/CCWLEN)  *
         DC    AL1((CA16B-CP4)/CCWLEN)  *
         DC    AL1((CA16C-CP4)/CCWLEN)  *
         DC    AL1((CA20-CP4)/CCWLEN)   *
         DC    AL1((CA22-CP4)/CCWLEN)   *
         DC    X'FF'
OPNTAB2  DC    AL1((CA24A-CP4)/CCWLEN)  ADJUST CCW'S IN CP5W,
*                                       END
         DC    AL1((CA24B-CP4)/CCWLEN)  *
         DC    AL1((CA24E-CP4)/CCWLEN)  *
         DC    AL1((CA24G-CP4)/CCWLEN)  *
         DC    X'FF'
OPNTAB3  DC    AL1((CA430-CP4)/CCWLEN)  ADJUST CCW'S IN CP6W, END
         DC    AL1((CA43A-CP4)/CCWLEN)  *
         DC    AL1((CA43B-CP4)/CCWLEN)  *
OPNTAB4  DC    AL1((CA27-CP4)/CCWLEN)   ADJUST THESE CCW'S IN CP6
         DC    AL1((CA29-CP4)/CCWLEN)   OR IN CP6W, BEGINNING
         DC    AL1((CA30-CP4)/CCWLEN)   *
         DC    AL1((CA32-CP4)/CCWLEN)   *
         DC    AL1((CA33-CP4)/CCWLEN)   *
         DC    AL1((CA36-CP4)/CCWLEN)   *
         DC    AL1((CA37-CP4)/CCWLEN)   *
         DC    AL1((CA38-CP4)/CCWLEN)   *
         DC    AL1((CA39-CP4)/CCWLEN)   *
         DC    AL1((CA40A-CP4)/CCWLEN)  *
         DC    AL1((CA41-CP4)/CCWLEN)   *
         DC    AL1((CA42-CP4)/CCWLEN)   *
         DC    X'FF'
OPNTAB5  DC    AL1((CA46B-CP7)/CCWLEN)  ADJUST THESE CCW'SINCP7W
OPNTAB6  DC    AL1((CA45-CP7)/CCWLEN)   ADJUST THESE CCW'S IN CP7
         DC    X'FF'
OPNMRUNW DC    C'JL'    READ AND UPDATE CP'S, NO WR CHK, MODULE
OPNMRUWC DC    C'JM'    READ AND UPDATE CP'S, WITH WR CHK, MOD
OPNMRUNP DC    C'JV'    READ AND UPDATE NON-PRIVILEGED MODULE
OPNMDYNB DC    C'JI'    DYNAMIC BUFFERING MODULE
*
*        XCTL TABLE
*
LOAD2J   DC    C'2J'                    ID OF MODULE IGG0192J    Y02072
LOAD2L   DC    C'2L'                    ID OF MODULE IGG0192L    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
FORCORE  DSECT                          OPEN/CLOSE/EOV WORK AREA Y01021
         IECDSECT                                                Y01021
         EJECT
         DCBD  DSORG=(IS)
         EJECT
IHADCW   IGGBISAM                 DCB WORK AREA                  S20201
WTGDSECT DSECT                                                   Y02072
IDTTR    DS    F                        MODULE ID/TTR            Y02072
OPENWA   DS    A                        ADDR OF OPEN WORK AREA   Y02072
         EJECT
IHADEB   IGGDEBD
         ORG   IHADEB+32
*        THE FOLLOWING ADDRESSES ARE MODIFIED BY NMEXT X 16
DEBID1   DS    CL2                      MODULE ID 1
DEBID2   DS    CL2                      MODULE ID 2
DEBID2A  DS    CL2                     MODULE ID 2A
DEBID2B  DS    CL2                     MODULE ID 2B
DEBID3   DS    CL2                      MODULE ID 3
DEBID4   DS    CL2                      MODULE ID 4
DEBID5   DS    CL2                      MODULE ID 5
CP4567   DSECT                          CHANNEL PROGRAMS
****
CP4      DS    0D                       CP4
         IGGCP4
CP5      DS    0D                       CP5 AND CP5W
         IGGCP5 OPTCD=W                                          S20201
CP5END   EQU   CA24+16                  CP 5 END WITHOUT WRITE   S20201
*                                       CHECK                    S20201
         ORG   CP5END                   ORG                      S20201
CP6      DS    0D                       CP6 AND CP6W
         IGGCP6 OPTCD=W                                          S20201
CP7      DS    0D                       CP7 AND CP7W
         IGGCP7 OPTCD=W                                          S20201
OPNSIZE1 EQU   SZ4+SZ5+SZ6-256         SIZE CP4, 5, 6 - 256      S20201
OPNSIZE2 EQU   SZ4+SZ5W+SZ6W-256       SIZE CP4, 5W, 6W - 256    S20201
OPNSIZE3 EQU   SZ5W-SZ5                SIZE CP5W                 S20201
OPNSIZE4 EQU   SZ6W-SZ6                SIZE CP6W                 S20201
OPNSIZE5 EQU   SZ7                     SIZE CP7                  S20201
OPNSIZE6 EQU   SZ7W-SZ7                SIZE CP7W                 S20201
CA43CC   EQU   OPNSIZE3+4               OFFSET OF CC IN CA43    SA61527
         SPACE 4
         CVT   DSECT=YES                                        YM01159
         END
