        TITLE 'IGG0192V - OPEN,QISAM LOAD MODE,SET UP CP20,21,W/WC'
IGG0192V CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192V                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, LOAD MODE WITH WRITE CHECK            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = WHEN WRITE CHECK IS SPECIFIED, INITIALIZE CP20 AND CP21. *
*            IF FULL TRACK INDEX WRITE (FTIW) IS SPECIFIED,           *
*            INITIALIZE CP21 ONLY.  DELETE CHANNEL PROGRAM MODULE     *
*            IGG019GF FOR FIXED LENGTH RECORDS (FLR), OR IGG019IF FOR *
*            VLR.                                                     *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 1032 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192V                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0192S WHEN *
*              WRITE CHECK IS SPECIFIED.  RECEIVES CONTROL IN STORAGE *
*              PROTECT KEY 5 AND PRIVILEGED STATE.                    *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO COMMON OPEN EXECUTOR IGG0190S IN STORAGE      *
*               PROTECT KEY 5.  THIS CONCLUDES ISAM OPEN OPERATION.   *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY, DEB AND IOB.                          *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, DELETE, AND XCTL.                                 *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
********************
* DCB REFERENCE    *
********************
         DCBD  DSORG=(IS)
         USING IHADCB,R1
         EJECT
********************
* DEB REFERENCE    *
********************
*
IHADEB   DSECT
         USING IHADEB,R8
         DS    0D
DEBNMSUB DS    0CL1
DEBTCBAD DS    A
DEBAMLNG DS    0CL1
DEBDEBAD DS    A
DEBOFLGS DS    0CL1
DEBIRBAD DS    A
DEBOPATB DS    0CL1
DEBSYSPG DS    A
DEBNMEXT DS    0CL1
DEBUSRPG DS    A
DEBPRIOR DS    0CL1
DEBECBAD DS    A
DEBPROTG DS    0BL1
DEBDEBID DS    0BL1
DEBDCBAD DS    A
DEBEXSCL DS    0CL1
DEBAPPAD DS    A
DEBNIEE  DS    0CL1
DEBFIEAD DS    A
DEBNPEE  DS    0CL1
DEBFPEAD DS    A
DEBNOEE  DS    0CL1
DEBFOEAD DS    A
         DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    A
DEBBINUM DS    CL2
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
         EJECT
ISLCOMON IGGLOAD
         USING ISLCOMON,R12                                      S20201
*
* IOBBCT REFERENCE         C(ISLVPTRS+8)=A(IOBBCT)
*
IOBBCT   DSECT
         USING IOBBCT,R11
         DS    0D
IOBFLAGS DS    0CL1                     FLAGS
IOBPTRA  DS    A                        PTR A
IOBB     DS    0CL1                     B
IOBPTRB  DS    A                        PTR B
IOBS     DS    0CL1                     S - STATUS FIELD FOR BUF NO 1
IOBABUF  DS    A                        A(BUF NO 1) - ADR OF BUF NO 1
         EJECT
LOADCPS  DSECT
         IGGLDCP OPTCD=W,RECFM=V                                 S20201
         EJECT
FORCORE  DSECT                                                   Y02072
         IECDSECT                                                Y02072
         EJECT
**********************************************************************
* ISL PUT OPEN #4
**********************************************************************
*
*
IGG0192V CSECT
TSTF800  BALR  R15,0
         USING *,R15
BASETAG  L     R1,0(RPARC)
         L     RCORE,4(RWTGC)
         L     R12,DCBWKPT1
         LR    RBASE,R15
         USING FORCORE,RCORE            OPEN WA ADDR             Y02072
         STM   R0,R15,DXCCW1            SAVE REGISTERS           Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=11  SET USERS KEY       Y02072
         SPACE 2
         L     R10,DCBWKPT6
         L     R11,8(R10)
         LR    RD,RCORE                 SAVE OPEN WA ADDR        Y02072
         DROP  RCORE                    DROP OPEN WA ADDR        Y02072
*
* EQUATE SYMBOLIC REGISTERS
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RBASE    EQU   3
RCORE    EQU   4
RPAR     EQU   5
RWTG     EQU   6
RPARC    EQU   7
RWTGC    EQU   8
RD       EQU   14
RJ       EQU   15
K4       EQU   4                        CONSTANT                 S20201
BOBSW    EQU   X'08'                                             A34959
RESLD    EQU   X'20'                                             A34959
REMCAP   EQU   66                                                A34959
*
K100     EQU   100                      CONSTANT                 S20201
L100     EQU   K100                     LENGTH                   S20201
K0       EQU   0                        CONSTANT                 S20201
K1       EQU   1                        CONSTANT                 S20201
K2       EQU   2                        CONSTANT                 S20201
L2       EQU   2                        CONSTANT                 S20201
K3       EQU   3                        CONSTANT                 S20201
L3       EQU   3                        LENGTH                   S20201
K5       EQU   5                        CONSTANT                 S20201
K6       EQU   6                        CONSTANT                 S20201
K7       EQU   7                        CONSTANT                 S20201
K10      EQU   10                       CONSTANT                 S20201
K18      EQU   18                       CONSTANT                 S20201
K35      EQU   35                       CONSTANT                 S20201
         EJECT
*
* SKIP CP20 INITIALIZATION IF FULL TRACK INDEX WRITE.
*
TSTHSK   XC    TSTWK1C(8),TSTWK1C       CLEAR WORK FIELDS        O19110
         TM    36(R10),X'C0'            FTIW-SUCCESSFUL GETMAIN  O19110
         BNO   INITCP20                 NO - INIT CP20           O19110
         L     R3,12(R10)               C(R3)=A(CP18)            O19110
         TM    DCBOPTCD,X'80'           WRITE CHECK              O19110
         BO    CP20C                    YES                      O19110
         L     R4,20(R10)               C(R4)=A(CP20A)           O19110
         LTR   R4,R4                    IS THERE A CP20A         O19110
         BE    CP20B                    NO - CHECK FOR CP20B     O19110
         L     R4,12(R4)                C(R4)=A(TIC TO CP18)     O19110
         ST    R3,0(R4)                 A(CP18) STORED IN TIC    O19110
         MVI   0(R4),X'08'              RESTORE COMMAND CODE     O19110
CP20B    L     R4,40(R10)               C(R4)=A(CP20B)           O19110
         LTR   R4,R4                    IS THERE A CP20B         O19110
         BE    INITCP21                 NO - INITIALIZE CP21     O19110
         B     STORETIC                 STORE TIC TO CP18        O19110
CP20C    EQU   *                                                 O19110
         L     R4,44(R10)               C(R4)=A(CP20C)           O19110
STORETIC EQU   *                                                 O19110
         L     R4,12(R4)                C(R4)=A(TIC TO CP18)     O19110
         ST    R3,0(R4)                 A(CP18) STORED IN TIC    O19110
         MVI   0(R4),X'08'              RESTORE COMMAND CODE     O19110
         B     INITCP21                 BR TO INITIALIZE CP21    O19110
*
* INITIALIZE CP20
*
INITCP20 EQU   *                                                 O19110
         LA    R2,ISLIOBA               C(R2)=A(IOBA)            O19110
         L     R9,0(R10)                C(R9)=A(AREA Y)
         L     R8,CQ1AD                 C(R8)=A(CP20 SKLTN, CQ1)
         L     R10,20(R10)              C(R10)=A(CP20 DEST, CQ1)
         SH    R10,TST8                 ALLOW FOR CQ0            S20201
         USING CQ0,R10                                           S20201
         MVC   CQ0(L100),0(R8)          MOVE CP20                S20201
         MVC   CQ0+K100(CP20LEN-L100),K100(R8) *  SKELETON       S20201
*
         L     R6,CQT1AD                C(R6)=A(CP20 WR CK SKLTN, CQT1)
         MVC   CQT0(CP20WRCK),0(R6)     MOVE CP20 WR CHK FROM    S20201
*                                       SKLTN                    S20201
         TM    DCBRECFM,X'80'           IS IT VLR                 VLR
         BZ    TSTHSKV                  YES, BRANCH               VLR
*
         LA    R4,ISLRPSSS+K2           SECTOR ADDR FOR CP20     A53207
         BAL   R3,TSTIOB                INITIALIZE CQ1 & CQ2     O19110
         LA    R4,26(R9)                CQ4
         ST    R4,CQ4                   STORE ADR Y+26           S20201
*
         LA    R4,36(R9)                CQ5
         O     R4,CQ5                   CQ5                      S20201
         ST    R4,CQ5                                            S20201
         LA    R4,CQ5                   CQ6                      S20201
         O     R4,CQ6                                            S20201
         ST    R4,CQ6                        STORE ADR CQ5       S20201
         LA    R4,44(R9)                CQ8
         ST    R4,CQ8                        STORE ADR Y+44      S20201
*
         LA    R4,54(R9)                CQ10
         O     R4,CQ10                                           S20201
         ST    R4,CQ10                                           S20201
         LA    R4,CQ10                  CQ11                     S20201
         O     R4,CQ11                                           S20201
         ST    R4,CQ11                                           S20201
         LA    R4,62(R9)                CQ12
         O     R4,CQ12                                           S20201
         ST    R4,CQ12                                           S20201
*
*   PUT KEYLENGTH BASED CONSTANTS INTO THE CHANNEL PROGRAMS
*
         SR    R4,R4                                             S20201
         IC    R4,DCBKEYLE              COUNT = KEY LENGTH       S20201
         STC   R4,CQ3+K7                CQ3                      S20201
         STC   R4,CQ7+K7                CQ7                      S20201
         LA    R4,K10(R4)               COUNT = KEY LENGTH + 10  S20201
         STH   R4,CQ12+K6               CQ12                     S20201
*
         LA    R4,ISLRPSSS+K2           CQ0                      S20201
TSTCQ13  EQU   *                                                  VLR
         O     R4,CQ0                        SET SECTOR          S20201
         ST    R4,CQ0                                            S20201
         LA    R4,ISLRPSSS+K2           CQ14B,CQT0               S20201
         O     R4,CQ14B                      SET SECTOR          S20201
         ST    R4,CQ14B                                          S20201
         ST    R4,CQT0                                           S20201
         LA    R4,33(R2)                CQ13
         O     R4,CQ13                                           S20201
         ST    R4,CQ13                                           S20201
         IC    R5,CQ14                  CQ14                     S20201
         LA    R4,CQT1                  TIC TO CQT1              S20201
         ST    R4,CQ14                  *                        S20201
         STC   R5,CQ14                                           S20201
*
         LA    R4,35(R2)                CQ15
         O     R4,CQ15                                           S20201
         ST    R4,CQ15                       STORE ADR IOBA+35   S20201
         LA    R4,CQ15                  CQ16                     S20201
         O     R4,CQ16                                           S20201
         ST    R4,CQ16                       STORE ADR CQ15      S20201
         LA    R4,18(R9)                CQ17
         O     R4,CQ17                                           S20201
         ST    R4,CQ17                       STORE ADR Y+18      S20201
         LA    R4,26(R9)                CQ19
         ST    R4,CQ19                       STORE ADR Y+26      S20201
         LA    R4,36(R9)               CQ21
         O     R4,CQ21                                           S20201
         ST    R4,CQ21                       STORE ADR Y+36      S20201
         LA    R4,44(R9)                CQ23
         ST    R4,CQ23                       STORE ADR Y+44      S20201
*
         LA    R4,54(R9)                CQ25
         O     R4,CQ25                                           S20201
         ST    R4,CQ25                       STORE ADR Y+54      S20201
         SR    R4,R4                                             S20201
         IC    R4,DCBKEYLE              COUNT=KEYLENGTH          S20201
         STC   R4,CQ18+K7               CQ18                     S20201
         STC   R4,CQ22+K7               CQ22                     S20201
         LA    R4,K18(R4)               COUNT=KEYLENGTH+18       S20201
         STH   R4,CQ25+K6               CQ25                     S20201
         LA    R4,CQ13                  CQ26                     S20201
         O     R4,CQ26                                           S20201
         ST    R4,CQ26                       STORE ADR CQ13      S20201
         LA    R4,CQ30+K3               CQ27                     S20201
         O     R4,CQ27                                           S20201
         ST    R4,CQ27                       STORE ADR CQ30+1    S20201
         LA    R4,CQ27                  CQ28                     S20201
         O     R4,CQ28                                           S20201
         ST    R4,CQ28                       STORE ADR CQ27      S20201
*
         LA    R3,TSTFIX3                                        O19110
         LA    R4,Y18(R9)               CQT1-SRCH ID Y+18        A37537
         LA    R7,CQT1                  INITIALIZE CQT1 AND CQT2 A37537
         USING CQT1,R7                  ADDRESSABILITY           A37537
*
*              FOR CP21 - THE ADDRESS OF IOBB+35 WILL BE STORED
*              IN CQ45, AND THE ADDRESS OF CQ45 WILL BE STORED
*              IN CQ46.
*
TSTWRCHK EQU   *                                                 O19110
         O     R4,CQT1                                           S20201
         ST    R4,CQT1                       STORE ADR IOBA+35   S20201
*                                        VLR                     S20201
*
TSTFIX1  LA    R4,CQT1                  CQT2                     S20201
*                                        VLR                     S20201
         O     R4,CQT2                                           S20201
         ST    R4,CQT2                       STORE ADR CQT1      S20201
         SR    R4,R4                    COUNT =                  S20201
         IC    R4,DCBKEYLE              * KEYLENGTH              S20201
         BR    R3                       RETURN TO INIT CQ47 FOR  O19110
         DROP  R7                       *                        A37537
*                                       CP21 OR CONTINUE W/ CP20
TSTFIX3  EQU   *                                                 O19110
         LA    R4,10(R4)                KEYLENGTH + 10           A37537
         STH   R4,CQT3+K6               COUNT = KEYLEN+10        A37537
         STH   R4,CQT5+K6               COUNT = KEYLEN+10        S20201
         STH   R4,CQT6+K6               COUNT = KEYLEN+10        S20201
*
*   CQT5A = TIC TO CQT7 IF INACTIVE ENTRY NOT WRITTEN.
*         = NOP IF INACTIVE ENTRY WRITTEN
*
         LA    R4,CQT7                  *                        A37537
         ST    R4,CQT5A+4               SAVE TIC ADDRESS         M5864
         LA    R4,CQT5B                 *                        M5864
         ST    R4,CQT4A+4               SAVE TIC ADDRESS         M5864
         LA    R4,CQ14A+K1              CQT7                     S20201
         O     R4,CQT7                                           S20201
         ST    R4,CQT7                       STORE ADR CQ14A+1   S20201
*
         LR    R4,R10                                             VLR
         AH    R4,TST8                  PT TO SCH ID CP20        S20201
         TM    DCBRECFM,X'80'           IS IT VLR                 VLR
         BZ    TSTCQT8V                 YES, BRANCH               VLR
         L     R4,DCBWKPT6              CQT8
         L     R4,12(R4)
TSTCQT8V EQU   *                                                  VLR
         O     R4,CQT8                                           S20201
         ST    R4,CQT8                       STORE ADR CL1       S20201
*
*   CQT4 + SRCH ID AREA Y + 36, CQT4A = TIC TO CQT4.
*
         LA    R4,Y36(R9)               *                        A37537
         LA    R7,CQT4                  *                        A37537
         BAL   R3,TSTWRCHK              *                        A37537
*
*   CQT5B = SRCH ID AREA Y +54, CQT5C = TIC TO CQT5B.
*
         LA    R4,Y54(R9)               *                        A37537
         LA    R7,CQT5B                 *                        A37537
         BAL   R3,TSTWRCHK              *                        A37537
*
Y18      EQU   18                       AREA Y - NORMAL ENTRY    A37537
Y36      EQU   36                       AREA Y - OVERFLOW ENTRY  A37537
Y54      EQU   54                       AREA Y - INACTIVE ENTRY  A37537
*
         EJECT
*
* INITIALIZE CP21
*
INITCP21 EQU   *                                                 O19110
         LA    R2,ISLIOBB               C(R2)=A(IOBB)            O19110
         L     R10,DCBWKPT6             C(R10)=A(VPTRS)
         L     R9,0(R10)                C(R9)=A(AREA Y)
         L     R8,CQ40AD                C(R8)=A(CP21 SKLTN, CQ40)
         L     R10,24(R10)              C(R10)=A(CP21 DEST, CQ40)
         USING CQ39A,R10                                         S20201
         USING DXIOB,R2                 IOB B ADDRESSABILITY     Y02072
         IC    R5,IOBSIOCC              SAVE SIOCC
         ST    R10,IOBSIOCC             STORE A(CP21) IN IOBB    Y02072
         STC   R5,IOBSIOCC              RESTORE SIOCC
         DROP  R2                       END USING ON IOBB        Y02072
         SH    R10,TST8                 PT TO START OF MODULE    S20201
         MVC   CQ39A(CP21LEN),0(R8)     MOVE CP21 FROM SKLTN     S20201
*
         L     R6,CQ45AD                C(R6)=A(CP21 WR CK SKLTN, CQ45)
         MVC   CQ44A(CP21WRCK),0(R6)    MOVE CP21 WR CHK FROM    S20201
*                                       SKLTN                    S20201
*
         LA    R3,TSTCQ42                                        O19110
         LA    R4,ISLRPSSS+K3           SECTOR ADDR FOR CP21     A53207
*
*              FOR CP20 - STORE THE ADDRESS OF IOBA+35 IN CQ1
*                         AND STORE AND ADDRESS OF CQ1 IN CQ2
*
TSTIOB   EQU   *                                                 O19110
         O     R4,CQ39A                                          S20201
         ST    R4,CQ39A                      STORE SECTOR        S20201
*                                       ADDRESS                  S20201
         LA    R4,K35(R2)               CQ40                     S20201
         O     R4,CQ40                                           S20201
         ST    R4,CQ40                  STORE IOB + 30 ADDRESS   S20201
         LA    R4,CQ40                  CQ41                     S20201
         O     R4,CQ41                                           S20201
         ST    R4,CQ41                       STORE ADR CQ40      S20201
         BR    R3                       RETURN TO INIT CQ3 FOR   O19110
*                                       CP20 OR CONTINUE W/ CP21
TSTCQ42  EQU   *                                                 O19110
         MVC   CQ44A+K1(L3),CQ39A+K1    SET UP CQ44A ADDRESS     S20201
         LA    R4,0(R9)                 CQ42                     S20201
         O     R4,CQ42                                           S20201
         ST    R4,CQ42                       STORE ADR Y         S20201
         LA    R4,8(R9)                 CQ44
         ST    R4,CQ44                       STORE ADR Y+8       S20201
*
         EJECT
*   CQ45 = SRCH ID IOBB+35,CQ46 = TIC TO CQ45.
*
         LA    R7,CQ45                  *                        A37537
         LA    R4,35(R2)                *                        A37537
         BAL   R3,TSTWRCHK              BR TO INIT CQ45 & 46     O19110
*                                       COUNT = KEYLENGTH
         STC   R4,CQ43+K7               *  CQ43                  S20201
         LA    R4,K18(R4)               COUNT = KEYLENGTH + 180  S20201
         STH   R4,CQ47+K6              *  CQ47                   S20201
*
         EJECT
*
* EXIT ********** EXIT ***************** EXIT *********** EXIT ********
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY         Y02072
         SPACE 1
         LA    R6,TSTMOD3               DELETE IGG019GF           VLR
         TM    DCBRECFM,X'80'           IS IT FIXED               VLR
         BO    TSTEXIT                  YES, BRANCH               VLR
         LA    R6,TSTMOD3V              DELETE IGG019IF           VLR
TSTEXIT  DELETE EPLOC=(R6)              DELETE CPS                VLR
         USING FORCORE,RD               OPEN WA ADDRESSABILITY   Y02072
         LM    R0,R15,DXCCW1            RESTORE REGISTERS        Y02072
*
         DROP  RD                       DROP OPEN WA ADDR        Y02072
         USING FORCORE,RCORE            EST OPEN WA ADDR         Y02072
         USING BASETAG,RBASE
*
         XC    0(5,RWTGC),0(RWTGC)      SET TO ZERO              O19110
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH=BEGINNING OF THIS MODULE
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BC    7,RELOOP                 BRANCH=NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BC    7,TCTLRTN                BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         LA    RJ,DXCCW12               GET WA ADDR              Y02072
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
         EJECT
* VLR INITIALIZATION OF CP20
*
         USING CQ0,R10                                           S20201
*
TSTHSKV  EQU   *                                                  VLR
         LA    R4,CQ0+K5                CQ0                      S20201
         O     R4,CQ0                                            S20201
         ST    R4,CQ0                   SET SECTOR TO 0          S20201
         LA    R4,CQ7                   CQ1                      S20201
         O     R4,CQ1                                            S20201
         ST    R4,CQ1                                            S20201
*                                        VLR                     S20201
         LA    R4,CQ1                   A(CQ1)                   S20201
         O     R4,CQ2                                            S20201
         ST    R4,CQ2                   CQ2                      S20201
*                                        VLR                     S20201
         LA    R4,CQ7                   A(CQ7)=YYR OF CAP REC    S20201
         O     R4,CQ3                   *                        S20201
         ST    R4,CQ3                   A(CQ7)                   S20201
         MVC   CQ5(L2),DCBLRECL         SET CP20 TO MAX LRECL    S20201
         TM    DCBST,RESLD              TEST FOR RESUME LOAD     A34959
         BNO   INITLOAD                 BR IF NOT                A34959
*
         TM    IOBFLAGS,BOBSW           IS BOB SW ON             A34959
         BO    INITLOAD                 BR IF ON, TRK CAP REQ'D  A34959
         MVC   REMCAP(2,R10),ISLWRSAV   REM CAP SAVED FROM 195G  A34959
         B     TSTCQ130                                          MXXXX
INITLOAD EQU   *                                                 A34959
         L     R4,DCBLRAN               LOAD DEV PTR TABLE ENTRY  VLR
         MVC   CQ9+K2(L2),K4(R4)        MOVE IN TRK CAP          S20201
TSTCQ130 EQU   *                                                 MXXXX
         LA    R4,CQ0+K5                CQ0 FOR SET SECTOR       S20201
         B     TSTCQ13                                            VLR
         SPACE 2
         DS    0D
TSTMOD3  DC    C'IGG019GF'              CPS
TSTMOD3V DC    C'IGG019IF'                                        VLR
TST8     DC    H'8'                     LENGTH OF A CCW          S20201
*
THISLOAD DC    C'2V'
*
OPNLOD7  DC    C'0S'                   ID OF MODULE IGG0190S     Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         END
