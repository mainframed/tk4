         TITLE 'IGG0192S - OPEN,QISAM LOAD MODE,SET UP CP 19,PRE FMT'
IGG0192S CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192S                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, LOAD MODE                             *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 01                                *
*                                                                     *
* FUNCTION = LOAD SIO MODULE IF EXECUTING IN VIRTUAL ENVIRONMENT, OR  *
*            ANY PORTION OF ISAM DATA SET RESIDES ON AN RPS DEVICE.   *
*            INITIALIZE CP 19 IF SHARED TRACKS, OR CYLINDER OVERFLOW  *
*            CONTROL RECORD (COCR) IS NEEDED.  EXECUTE CP19 TO PRE-   *
*            FORMAT FIRST PRIME CYLINDER.  IF NEITHER SHARED TRACKS   *
*            NOR COCR REQUIRED, EXIT TO NEXT OPEN LOAD WITHOUT        *
*            INITIALIZING CP19.                                       *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 998 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192S                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0192U IF   *
*              WRITE-CHECK SPECIFIED, OTHERWISE FROM IGG0192R.        *
*              RECEIVES CONTROL IN STORAGE PROTECT KEY 5 AND          *
*              PRIVILEGED STATE.                                      *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO IGG0192V IF WRITE-CHECK SPECIFIED, OTHERWISE  *
*               TO IGG0192T IN STORAGE PROTECT KEY 5.                 *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                                                                     *
*    CONTROL-BLOCKS = DCB, DCB COPY, DEB, AND IOB                     *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, LOAD, EXCP, AND XCTL                              *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*          RELEASE OS/VS2-02 DELETIONS                                *
*                                                               YM01159
*                                                                     *
***********************************************************************
         EJECT
********************
* DCB REFERENCE    *
********************
         DCBD  DSORG=(IS)
         USING IHADCB,R1
         EJECT
********************
* DEB REFERENCE    *
********************
*
IHADEB   IGGDEBD
         USING IHADEB,R8                DEB ADDRESSABILITY       S20201
         EJECT
ISLCOMON IGGLOAD
         USING ISLCOMON,R12                                      S20201
*
*
* IOBBCT REFERENCE         C(ISLVPTRS+8)=A(IOBBCT)
*
IOBBCT   DSECT
         USING IOBBCT,R11
         DS    0D
IOBFLAGS DS    0CL1                     FLAGS
IOBPTRA  DS    A                        PTR A
IOBB     DS    0CL1                     B
IOBPTRB  DS    A                        PTR B
IOBS     DS    0CL1                     S - STATUS FIELD FOR BUF NO 1
IOBABUF  DS    A                        A(BUF NO 1) - ADR OF BUF NO 1
         EJECT
********************
* IOB REFERENCE    *
********************
*
IHAIOB   DSECT
         USING IHAIOB,R2
         DS    0D
IOBFLG1  DS    CL1                      FLAGS 1
IOBFLG2  DS    CL1                      FLAGS 2
AIOBSENS DS    CL2                      SENSE BYTES              Y01021
IOBECBAD DS    A                        ECB POINTER
AIOBCSW  DS    CL8                      CHANNEL STATUS WORD      Y01021
AIOBSIOC DS    0CL1                     SIO CC                   Y01021
IOBCPSAD DS    A                        CHANNEL PROGRAM START ADR
IOBWT    DS    0CL1                     WEIGHT
IOBDCBAD DS    A                        DCB POINTER
IOBCPRAD DS    A                        CHANNEL PROGRAM RESTART ADR
IOBBCTI  DS    CL2                      BLK CTR INCR
IOBECT   DS    CL2                      ERROR CTR
IOBDADAD DS    CL8                      DIR ACESS DEV ADR MBBCCHHR
*
         EJECT
LOADCPS  DSECT
         IGGLDCP
         EJECT
FORCORE  DSECT                          OPEN/CLOSE/EOV WORK AREA Y01021
         IECDSECT                                                Y01021
         SPACE 4
         CVT   DSECT=YES                                        YM01159
         EJECT
**********************************************************************
* ISL PUT OPEN #3                                                     *
**********************************************************************
*
*
IGG0192S CSECT
TSTF800  BALR  R15,0
         USING *,R15
BASETAG  L     R1,0(RPARC)
         L     RCORE,4(RWTGC)
         L     R12,DCBWKPT1
         LR    RBASE,R15
         USING FORCORE,RCORE            OPEN W/A ADDRESSABILITY  Y02072
         STM   R0,R15,DXCCW1            SAVE REGISTERS           Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11  SET USERS KEY       Y02072
*
         L     R11,ISLVPTR3             BUFFER CONTROL TABLE
         LR    RD,RCORE                 SAVE OPEN W/A ADDR       Y02072
*
* EQUATE SYMBOLIC REGISTERS
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RBASE    EQU   3
RCORE    EQU   4
RPAR     EQU   5
RWTG     EQU   6
RPARC    EQU   7
RWTGC    EQU   8
RD       EQU   14
RJ       EQU   15
*
* EQUATE SYMBOLIC LOCATIONS
*
L7       EQU   7                        LENGTH                   S20201
K32      EQU   32                       CONSTANT                 S20201
DEBSIO   EQU   4                        START I/O APPENDAGE      S20201
RPSID    EQU   X'E0'                    DATA ON RPS DEVICE       S20201
K2       EQU   2                        CONSTANT                 S20201
K5       EQU   5                        CONSTANT                 S20201
K1       EQU   1                        CONSTANT                 S20201
K6       EQU   6                        CONSTANT                 S20201
ALL      EQU   X'FF'                    ALL SW SET               S20201
K4       EQU   4                        CONSTANT                 S20201
RESLD    EQU   X'20'                                             A34959
*
TEN      EQU   10                       CONSTANT
J38      EQU   38                       CONSTANT
J33      EQU   33                       CONSTANT
ZERO     EQU   0                        CONSTANT
VIRTUAL  EQU   X'80'                    VIRTUAL INDICATOR
TISASIZE EQU   8                        TISA SIZE DISPLACEMENT
FTIW     EQU   X'80'                    FTIW INDICATOR
IDOFF    EQU   6                        OFFSET INTO WTG TAB
         EJECT
*
*   LOAD START I/O MODULE INDEX OR PRIME ON RPS DEVICE OR IF
*        EXECUTING IN VIRTUAL ENVIRONMENT
*
         L     R8,DCBDEBAD              LOAD DEB ADDRESS         S20201
         DROP  RCORE                                             Y01021
         L     R5,DEBAPPAD              ADDRESSABILITY ON AVT
         USING DEBAVT,R5                *
*
         TM    DEBSIOA,VIRTUAL          IS IT VIRTUAL ENVIRON.   Y02072
         BO    LOADSIO                  YES - LOAD SIO APPEND.   Y02072
*                                       NO - TEST FOR RPS DEV.   Y02072
         TM    DEBRPSID,RPSID           RPS DEVICE USED          S20201
         BZ    TSTHSK                   NO - BR. NO SIO MODULE   S20201
*                                       YES - LOAD SIO MODULE
LOADSIO  LR    R2,R1                    SAVE DCB ADDRESS         Y02072
         LR    R4,R15                   SAVE ADDRESSABILITY      S20201
         L     R3,CVTPTR                ADDRESS OF CVT          YM01159
         USING CVT,R3                   CVT ADDRESSABILITY      YM01159
         L     R3,CVTLINK               ADDRESS OF LINKLIB DCB  YM01159
         DROP  R3                       END CVT ADDRESSABILITY  YM01159
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         MVC   IDOFF(2,RWTG),SIOID      IGG019GG
         LOAD  EPLOC=(RWTG),DCB=(R3)    LOAD SIO MODULE
         LR    R15,R4                   RESTOR ADDRESSABILITY
         SPACE 2
* SAVE ADDRESS OF RPS SIO MODULE
*   PUT RPS SIO ADDRESS IN THE AVT AND DEB EXTENSION.
*
         IC    R1,DEBSIOA               SAVE VIRTUAL INDICATOR   Y02072
         ST    R0,DEBSIOA               SAVE LINKAGE TO RPS SIO
         STC   R1,DEBSIOA               RESET VIRTUAL INDICATOR  Y02072
         L     R3,DEBEXPTR              ADDRESSABILITY ISAM SECTION DEB
         USING DEBEXT,R3                DEB EXTENSION ADDRESS.   Y02072
         ST    R0,DEBRPSL               SAVE FOR LINKAGE TO RPS SIO
         DROP  R3                       *
         LR    R1,R2                    DCB ADDRESSABILITY
*
*   LOCATE FIRST FREE SPOT ON DEB SUBROUTINE LOAD LIST.
*
         SR    R3,R3                                             S20201
         IC    R3,DEBNMEXT              NUMBER OF EXTENTS        S20201
         SLL   R3,4                     TOTAL LENGTH OF EXTENTS  S20201
         LA    R4,DEBNIEE+LOADEXT-K2(R3) ADDRESS OF SUBROUTINE   M1819
*                                       *    NAME SECTION
         SR    R3,R3                    *                        M6372
         IC    R3,DEBNMSUB              NUMBER OF SUBROUTINES    S20201
*                                       *    LOADED
         AR    R4,R3                    FIRST FREE SPOT ON       S20201
         AR    R4,R3                    *    NAME LIST           S20201
*
*   PLACE ID FOR SIO MODULE ON SUBROUTINE NAME LIST
*
         MVC   0(K2,R4),SIOID           *                        S20201
*
         USING FORCORE,RD               OPEN WA ADDRESSABILITY   Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=3   SET USERS KEY       Y02072
*
         DROP  RD                       END USING ON OPEN WA     Y02072
*
TSTHSK   EQU   *                        *
*
         DROP  R5
         LA    R2,ISLIOBA               PT TO IOBA
* VARIABLE LENGTH RECORDS
         TM    DCBRECFM,X'80'           IS IT FIXED?              VLR
         BO    TSTHSK30                 BRANCH IF FIXED           VLR
BOBOFF   EQU   X'F7'                                             M0651
         TM    DCBST,RESLD              TEST IF RESUME LOAD      A34959
         BO    TSTHSK30                 BR IF RESUME LOAD        A34959
         L     R5,ISLVPTRS+8            A(BCT)=A(PTRA)            VLR
         NI    0(R5),BOBOFF             BOB SW OFF FOR VLR       M0651
         L     R5,0(R5)                 PTRA=A(SLOT1)             VLR
         L     R3,0(R5)                 C(R3)=A(BUFF1)            VLR
         LA    R3,12(R3)                C(R3)=A(BUFF1)+12         VLR
         ST    R3,ISLCBF                SAVE ADDR                 VLR
TSTHSK01 L     R4,0(R5)                 C(SLOT1)=A(BUFF1)         VLR
         MVC   8(4,R4),TST4             SET UP BLK FIELD          22619
         TM    0(R5),X'80'              LAST BUFFER?              VLR
         LA    R5,4(R5)                 PICK UP ADDR NXT BUFF     VLR
         BZ    TSTHSK01                 NO MORE BUFFS             VLR
TSTHSK30 EQU   *                                                 A34959
         SR    R5,R5                    CLEAR REGISTER           A34959
*
* INITIALIZE CP19
*
         CLI   DCBHIRSH,X'00'           TEST HIRSH VS 0 FOR SHARED TRKS
         BNE   TSTHK41                  B NOT 0 = SHARED TRKS
*
         TM    DCBOPTCD,X'08'           TEST OPTCD BIT-4 FOR CYL OVFL
         BC    8,TSTHK80                B NOT ON = NO PREFORMAT NEEDED
         B     TSTHK51                  B IF CYL OVFL-NO SHARED
*
TSTHK41  TM    DCBOPTCD,X'08'           TEST OPTCD BIT-4 FOR CYL OVFL
         BC    8,TSTHK61                B NOT ON = NO CYL OVFL-SHARED
         B     TSTHK71                  B IF CYL OVFL-SHARED
*
*
*    A.* CP19 COMMON SETUP INITIAL - CYL OVFL AND/OR SHARED TRACKS
*
TSTHK42  LA    R2,ISLIOBC               C(R2)=A(IOBC)
         L     R8,ISLFXAD               C(R8)=A(CP19 SKLTN)      S20201
         L     R10,ISLVPTR5             CP 19 ADDRESS
         ST    R10,IOBCPSAD             PT IOB TO CM1            S20201
         S     R10,TST8                 ALLOW FOR CM0            S20201
         USING CM0,R10                  ADDRESSABILITY ON CP19   S20201
         MVC   CM0(CP19LEN),0(R8)       MOVE CHANNEL PROGRAM     S20201
         LA    R9,ISLAREAZ              C(R9)=A(AREA Z)
*
         LA    R4,CM0+K5                CM0                      S20201
         ST    R4,CM0                   ADDR OF SECTOR           S20201
         MVI   CM0,SETSECT              RESTORE OPCODE           S20201
*
         LA    R4,ISLRPSSS+K1           CM40                     S20201
         ST    R4,CM40                  ADDRESS OF SECTOR        S20201
         MVI   CM40,NOP                 OPCODE NOP               S20201
*
         BR    R13                      RETURN
*
*
*    B.* CP19 COMMON SETUP INITIAL - CYL OVFL /AND SHARED TRACKS
*
TSTHK43  EQU   *                                                 S20201
*
*                                       INITIALIZE DATA OF CYL OVFL
*                                       CTRL RCD AT Z (R9)
*
         LH    R3,DCBLDT                C(R3)=LDT                 22619
         LA    R3,1(R3)                 C(R3)=LDT+1 = CYL OVFL AREA
         SLL   R3,16                    C(R3)=HH00
         ST    R3,0(R9)                 C(Z)=HH00 = HHRY
         MVI   4(R9),X'00'              C(Z)=HH000= HHRYY
         MVC   5(1,R9),DCBCYLOF         C(Z)=HH000N = HHRYYT
*                                       INITIAL WRITE CYL OVFL CTRL RCD
*                                       IN CP19
*
         TM    DCBRECFM,X'80'           IS IT FIXED?              VLR
         BO    TSTHK43A                 YES, BRANCH               VLR
         L     R4,DCBLRAN               DEV TBL POINTER           VLR
         MVC   3(2,R9),4(R4)            MOVE IN YY TK CAP         VLR
TSTHK43A EQU   *                                                  VLR
         LA    R4,35(R2)                CM1
         A     R4,CM1                                            S20201
         ST    R4,CM1                   ADDRESS OF IOBC+35       S20201
*
         LA    R4,CM1                   CM2                      S20201
         A     R4,CM2                                            S20201
         ST    R4,CM2                   ADDRESS OF CM1           S20201
*
         LR    R4,R9                    CM3                      S20201
         A     R4,CM3                                            S20201
         ST    R4,CM3                   ADDRESS OF AREA Z        S20201
*
         BR    R13                      RETURN
*
*
*    C.* CP19 COMMON SETUP INITIAL - SHARED TRACKS /AND CYL OVFL
*
*                                       SET UP R6 WITH CC,IL+10
TSTHK44  SR    R6,R6
         IC    R6,DCBKEYLE              C(R6)=IL
         LA    R6,10(R6)                C(R6)= IL+10
         STH   R6,CM8+K6                CM8 LENGTH = KL+10       S20201
*
*                                       SET UP R7 WITH BUF ADDR
         L     R7,ISLCBF                C(R7)=A(BUF1+8) FOR GARBAGE ADR
         ST    R7,CM8                   STORE A(BUF) AT CM8      S20201
*
*                                       SET UP R5 WITH OP,Z+6
         LA    R5,K6(R9)                C(R4)=Z+6                S20201
         A     R5,CM7                   CM7                      S20201
         ST    R5,CM7                                            S20201
         A     R5,TST8                  C(R5)=OP,Z+14
*
*                                       INITIAL CM9 THRU CM26
*                                          C(R5)=OP,Z+6+8 OR Z+14
*                                          C(R7)=A(BUF)
*                                          C(R6)=CC,IL+10
*
         LA    R4,9                     SET UP LOOP COUNTER       22619
TSTHK445 ST    R5,CM9                   START AT CM9 AND CM10    S20201
*                                        2261                    S20201
         ST    R7,CM10                  AND INITLZ ALL           S20201
         STH   R6,CM10+K6               CP'S                     S20201
         A     R5,TST8
         LA    R10,16(0,R10)            GO TO NEXT CCW PAIR       22619
         BCT   R4,TSTHK445                                        22619
         L     R10,ISLVPTR5             CP 19 ADDRESS
         S     R10,TST8                 ALLOW FOR CM0            S20201
         NI    CM26+K4,ALL-CC           TURN OFF TENTH CC FLAG   S20201
*
*
*                                       SET CM2+4 TO POINT TO ADDR OF
*                                       CC FLAG THAT WILL BE TURNED OFF
*                                       AND ON IF MORE THAN TEN ENTRIES
*                                       MUST BE WRITTEN.
*                                       IF LESS THAN TEN ENTRIES, TURN
*                                       CC FLAG OFF NOW.
*
         SR    R4,R4
         SR    R5,R5
         IC    R5,DCBFIRSH+2            C(R5)=R OF FIRSH
         BCTR  R5,0                     SUBTRACT 1 FROM DIVIDEND  22619
         D     R4,TST10                 DIVISOR  = 000A
*                                       C(R4) = REMAINDER R = 000R
*                                       C(R5) = QUOTIENT  Q
         LTR   R4,R4                    R VS 0                    VLR
         BNE   TSTHK45                  B IF R NOT 0
*
*                                       R=0, TENTH CC FLAG OFF AT CM26
         L     R4,TST10                 C(R4) = 000A
         BCTR  R5,0                     ADJUST QUOTIENT DOWNWARD  22619
*
TSTHK45  SLL   R4,4                     C(R4) = 00R0, MULTIPLY BY 16
         LA    R6,CM6+K4                C(R6) = A(CM6+4)         S20201
         AR    R6,R4                    C(R6) = A(CM6+4+00R0) = A(FLAG)
         ST    R6,CM2+K4                C(CM2+4) = A(CC FLAG)    S20201
*
*                                       TEST IF MORE THAN TEN ENTRIES
         LTR   R5,R5                                              VLR
         BNE   TSTHK46                  B IF MORE THAN TEN ENTRIES
*
*                                       TEN OR LESS ENTRIES
         NI    0(R6),X'BF'              TURN CC FLAG (BIT-4) OFF
         SR    R4,R4
         ST    R4,CM2+K4                C(CM2+4) = 0000 = TEN OR S20201
*                                       LESS                     S20201
*
TSTHK46  LA    R4,CM5                   CM6                      S20201
         A     R4,CM6                                            S20201
         ST    R4,CM6                   STORE ADDR OF CM5        S20201
*
*                                       SET UP COUNTS IN AREA Z
         MVC   6(4,R9),ISLIOBA+35       CCHH FROM IOBA+35 TO Z+6  P4701
         MVI   10(R9),X'01'                  R=1
         MVC   11(3,R9),ISLNCNT+5            KDD FROM NCNT
*
         MVC   14(72,R9),6(R9)               Z+6 TO Z+14 THRU Z+78
*
         LA    R3,10                    C(R3)= 10 = COUNT
         LR    R4,R9                    PT TO AREA Z
         LA    R5,1                     C(R5)= 1 = R1
TSTHK47  EQU   *                        *
         STC   R5,TEN(R4)               C(R5) = 1 = R
         A     R4,TST8                            STEP Z
         LA    R5,1(R5)                 STEP R
         BCT   R3,TSTHK47                         LOOP 9 MORE TIMES
*
         BR    R13                      RETURN
*
*
*    D.* CP19 COMMON SETUP END - CYL OVFL AND/OR SHARED TRACKS
*
*      *
*      * EXECUTE CP19
*      *
TSTHK49  EQU   *
         TM    DCBST,X'20'              TEST FRO RESUME LOAD      P4701
         BO    TSTHK80                  BR - IF RL               S20201
*                                        P470                    S20201
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         NI    DCBST,X'EF'              RESET NEW CYL SW          P4701
*
         USING FORCORE,RD               OPEN WA ADDRESSABILITY   Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=3   SET USERS KEY       Y02072
*
         L     R3,DXUDCBAD              GET REAL DCB ADDR        Y02072
         LH    R4,DXUDCBML              GET MOVE LENGTH          Y02072
         BCTR  R4,0                     DECREMENT LENGTH BY 1    Y02072
         EX    R4,MOVEDCB               MV FRM COPY TO REAL DCB  Y02072
         LR    R3,R15                   SAVE R15, BASE            P4701
         LR    R4,R1                    SAVE R1
         EXCP  IHAIOB                   EXECUTE CHANNEL PROGRAM
         LR    R15,R3                   RESTORE R15, BASE
         LR    R1,R4                    RESTORE R1
*      *
*      * TURN PF-BIT (STATUS BIT-6) ON IN 1ST BCT SLOT
*      *
         L     R3,IOBPTRB               C(R3)=PTR B = A(1ST SLOT) OR-
         LA    R3,0(R3)                 C(R3)=A(STATUS BITS)
         OI    0(R3),X'02'              TURN PF-BIT ON (STATUS BIT-6)
         B     TSTHK80A                 TRANSFER TO NEXT MOD     Y02072
*
* EXIT ********** EXIT ***************** EXIT *********** EXIT ********
*
TSTHK80  EQU   *                        REFRESH USERS DCB        Y02072
         L     R3,DXUDCBAD              GET REAL DCB ADDR        Y02072
         LH    R4,DXUDCBML              GET MOVE LENGTH          Y02072
         BCTR  R4,0                     DECREMENT LENGTH BY 1    Y02072
         EX    R4,MOVEDCB               MV FRM COPY TO REAL DCB  Y02072
*
TSTHK80A EQU   *                        XFER CONTROL TO NEXT MOD Y02072
         LM    R0,R15,DXCCW1            RESTORE REGISTERS        Y02072
         DROP  RD                       DROP OPEN W/A ADDR       Y02072
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         TM    DCBOPTCD,X'80'           TEST OPTCD BIT 0 (WR CHK)
         MVC   0(L'LOAD2V,RWTGC),LOAD2V ID FOR MODULE IGG0192V   Y02072
         BO    RELOOP                   WRITE CHECKING
         USING BASETAG,RBASE
         MVC   0(L'LOAD2T,RWTGC),LOAD2T ID FOR MODULE IGG0192T   Y02072
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH=BEGINNING OF THIS MODULE
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BC    7,RELOOP                 BRANCH=NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BC    7,TCTLRTN                BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         USING FORCORE,RCORE                                     Y01021
         LA    RJ,DXCCW12               WA ADDRESS               Y01021
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
*
MOVEDCB  MVC   DCBKEYLE-IHADCB(0,R3),DCBKEYLE   MOVE FROM COPY   Y02072
*                                               DCB TO REAL DCB
         DROP  RBASE
         EJECT
*
*    1.* CYL OVFL-NO SHARED TRACKS
*
TSTHK51  BAL   R13,TSTHK42              B TO COMMON SETUP A.
         BAL   R13,TSTHK43              B TO COMMON SETUP B.
*
*                                       SET IOBC+32 TO TRACK 00, RCD 0
*   SET UP IOBC SEEK ADDRESS
         BAL   R13,MULPDA               H=0,R=0
*
*
         NI    CM3+K4,ALL-CC            TURN CC FLAG (BIT-4) OFF S20201
*                                       AT CM                    S20201
*
         B     TSTHK49                  B TO COMMON SETUP END
*
*
*    2.* SHARED TRACKS-NO CYL OVFL
*
TSTHK61  BAL   R13,TSTHK42              B TO COMMON SETUP A.
         BAL   R13,TSTHK44              B TO COMMON SETUP C.
*
         LA    R4,CM5                   C(R4)=A(CM5)             S20201
         ST    R4,IOBCPSAD              STORE A(CM5) IN IOBC CP START
*
         LA    R4,35(R2)                CM5
         A     R4,CM5                                            S20201
         ST    R4,CM5                   STORE ADDRESS OF IOBC+35 S20201
*
*                                       SET IOBC+32 TO FIRSH WITH R=0
         LA    R3,32(R2)                POINT TO IOBSEEK          22619
         BAL   R13,MULPDA               SET UP FIRST SHARED
         OC    J38(K1,R2),DCBFIRSH+K1   *
*
         B     TSTHK49                  B TO COMMON SETUP END
*
*
*    3.* SHARED TRACKS-CYL OVFL
*
TSTHK71  BAL   R13,TSTHK42              B TO COMMON SETUP A.
         BAL   R13,TSTHK43              B TO COMMON SETUP B.
         BAL   R13,TSTHK44              B TO COMMON SETUP C.
*
*                                       SET IOBC+32 TO TRACK 00, RCD 0
         BAL   R13,MULPDA               H=0,R=0
*
         EJECT
*
*   SET IOBC SEEK FIELD, CHECK FOR MORE THAN ONE TRACK
*
         LA    R4,J33(R2)               CM4&5 REFERENCE IOBC
*
*                                       TEST IF CYL OVFL RCD ON SHARED
*                                       TRACK
         CLC   DCBFIRSH(2),TST10        TEST FIRSH HH=0           22619
         BE    TSTHK72                  ON SAME TRACK
         MVC   CM27,ISLIOBC+K32         SEEK TO FIRST SHARED
         OC    CM27+K6(K1),DCBFIRSH+K1  TRACK
         LA    R4,CM27+K1               CM4&5 REFERENCE CM27
TSTHK72  EQU   *                        *
*
*   CM4 = SEEK TO SHARED TRACK, R=0
*
         ST    R4,CM4                   *
         MVI   CM4,SEEKHH               *
*
*   CM5=SEARCH ID TO R0 OF SHARED TRACK
*
         LA    R4,K2(R4)                *
         ST    R4,CM5                   *
         MVI   CM5,SIDEQ                *
*
         B     TSTHK49                  B TO COMMON END
*
         SPACE 3
MULPDA   EQU   *                        *
*
*   SUBROUTINE TO SET IOBC SEEK TO FIRST RECORD ON CYLINDER
*   REFERENCED BY IOBA SEEK.(HEAD=0,RECORD=0)
*
         MVC   K32(L7,R2),ISLIOBA+K32   SET UP MBBCCHH
         MVI   39(R2),X'00'             SET R = 0
         MVI   J38(R2),ZERO             NO SET H=0
         BR    R13                      RETURN
         EJECT
*
TST8     DC    F'0008'                  CONSTANT                 S20201
TST10    DC    F'0010'
TST4     DC    H'4'                     CONSTANT                 S20201
SIOID    DC    C'GG'                    NAME OF LOAD MODE SIO
THISLOAD DC    C'2S'
OPNLOD7  DC    C'0S'
*
*        XCTL TABLE
LOAD2T   DC    C'2T'                    ID OF MODULE IGG0192T    Y02072
LOAD2V   DC    C'2V'                    ID OF MODULE IGG0192V    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
*
         END
