 TITLE 'IGG0195U - OPEN, FULL TRACK INDEX WRITE WITH RESUME LOAD'
IGG0195U CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0195U                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, FULL TRACK INDEX WRITE                *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = FINISH FULL TRACK INDEX WRITE (FTIW) INITIALIZATION      *
*            FOR RESUME LOAD BY CALCULATING STARTING POINT FOR        *
*            RESUME LOAD, AND INITIALIZING THE TRACK INDEX SAVE       *
*            AREA (TISA) AND CHANNEL PROGRAM 20.  IF NOT RESUME LOAD, *
*            EXIT TO NEXT EXECUTOR.                                   *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 602 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0195U                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0195T IN   *
*              STORAGE PROTECT KEY 5 AND PRIVILEGED STATE.            *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG0192U IF WRITE CHECK    *
*               IS SPECIFIED, OTHERWISE TO IGG0192R, IN STORAGE       *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY AND DEB                                *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET AND XCTL                                           *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
********************
* DCB REFERENCE    *
********************
         DCBD  DSORG=(IS)
         USING IHADCB,R1
         EJECT
********************
* DEB REFERENCE    *
********************
*
IHADEB   DSECT
         USING IHADEB,R8
         DS    0D
DEBNMSUB DS    0CL1
DEBTCBAD DS    A
DEBAMLNG DS    0CL1
DEBDEBAD DS    A
DEBOFLGS DS    0CL1
DEBIRBAD DS    A
DEBOPATB DS    0CL1
DEBSYSPG DS    A
DEBNMEXT DS    0CL1
DEBUSRPG DS    A
DEBPRIOR DS    0CL1
DEBECBAD DS    A
DEBPROTG DS    0BL1
DEBDEBID DS    0BL1
DEBDCBAD DS    A
DEBEXSCL DS    0CL1
DEBAPPAD DS    A
DEBNIEE  DS    0CL1
DEBFIEAD DS    A
DEBNPEE  DS    0CL1
DEBFPEAD DS    A
DEBNOEE  DS    0CL1
DEBFOEAD DS    A
         DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    A
DEBBINUM DS    CL2
DEBSTRCC DS    CL2
DEBSTRHH DS    CL2
DEBENDCC DS    CL2
DEBENDHH DS    CL2
DEBNMTRK DS    CL2
         EJECT
***********************************************************************
* ISLCOMON REFERENCE     C(DCBWKPT1)=A(ISLCOMMON)                     *
***********************************************************************
*
ISLCOMON DSECT
         USING ISLCOMON,R12
         DS    0D
ISLECBA  DS    A                        FOR CP18 AND CP20
ISLIOBA  DS    CL40
ISLECBB  DS    A                        FOR CP21
ISLIOBB  DS    CL40
ISLECBC  DS    A                        FOR CP19
ISLIOBC  DS    CL40
ISLAREAZ DS    CL88                     FOR CP19
ISLIXLT  DS    CL104                    INDEX LOCATION TABLE
ISLNIRT  DS    CL3                      HHR OF DUMMY TRACK INDEX
ISLHIRT  DS    CL1                      NO INDX ENT ON PRIM DATA TRACK
ISLCBF   DS    F                        BUF CTRL PTR (RCD WITHIN BUF)
ISLBMPR  DS    F                        USED TO BUMP CBF TO NEXT RCD
ISLFBW   DS    F                        NO OF BUFS SCHED TO WRITE
ISLEOB   DS    F                        END OF BUFFR ADR
ISLNCNT  DS    CL8                      NORMAL IX COUNT, CCHHRKDD
ISLOCNT  DS    CL8                      OVFLOL IX COUNT, CCHHRKDD
ISLDCNT  DS    CL8                      DUMMY IX COUNT,  CCHHRKDD
ISLNDAT  DS    CL10                     NORMAL IX DATA, MBBCCHHRFP
         DS    CL2
ISLODAT  DS    CL10                     OVFLOW IX DATA, MBBCCHHRFP
         DS    CL1
ISLBUFNO DS    CL1                      DCBBUFNO OR 2
ISLBUFN  DS    F                        ADDR OF SLOT N IN BCT
ISLMVC   DS    F                        COUNT OF EXECUTED MOVE (N-1)
ISLMVCT  DS    F                        NOMBR OF 255 BYTE MOVES (N+1)
ISLVRSAV DS    18F                      ISL SAVE AREA
ISLAPSAV DS    10F                      APPENDAGE SAVE AREA
ISLWRSAV DS    16F                      ISL SAVE AREA FOR CLOSE
*
TSTWK1C  DS    F                        WORK AREA FOR FORMAT 000N
TSTWK2C  DS    F                        WORK AREA FOR FORMAT 00NN
TSTWK3C  DS    F
ISLNOENT DS    F                        NO ENTRIES REMAINING ON TRACK
ISLOFFST DS    F                        SIZE OF WR CKD INSTR IN BYTES
ISLD     DS    F                        DISCPLACEMENT FROM CP18 START
ISLFSTBF DS    F                        ADDR OF 1ST SCHED BUF (REL)
ISLLSTBF DS    F                        ADDR OF LAST SCHED BUF (REL)
ISLCCFAD DS    F                        ADDR OF CC FLAG IN CP18
ISLKEYAD DS    F                        ADDR OF KEY OF LAST RCD ON TRK
*
CL1AD    DS    F                        ADDR CP18 SKLTN
CM1AD    DS    F                        ADDR CP19 SKLTN
CQ1AD    DS    F                        ADDR CP20 SKLTN
CQT1AD   DS    F                        ADDR CP20 WR CHK EXTN
CQ40AD   DS    F                        ADDR CP21 SKLTN
CQ45AD   DS    F                        ADDR CP21 WR CHK EXTN
*
         SPACE 3
*
* ISLVPTRS REFERENCE       C(DCBWKPT6)=A(ISLVPTRS)
*
ISLVPTRS DS    A                        A(AREA Y)
         DS    A                        A(KEYSAVE)
         DS    A                        A(IOBBCT)  BUF CTRL TBL ADR
         DS    A                        A(CP18)
         DS    A                        A(CP19)
         DS    A                        A(CP20)
         DS    A                        A(CP21)
         DS    A                        SIZE OF DCB WORK AREA
         DS    A                        SIZE OF CHANNEL PGM AREA
         DS    A                        A(TRACK INDEX SAVE AREA)
*                                       BIT 0-FULL TRK INDX WRITE
*                                       BIT 1-SUCCESSFUL GETMAIN
         DS    A                        A(CP20B)-FULL TRK INDX WR
         DS    A                        A(CP20C)-FULL TRK INDX WR
ISLFXWK1 DS    F                        WORK AREA                 13711
ISLFXWK2 DS    F                        WORK AREA                 13711
ISLF9WK1 DS    F                        WORK AREA                 13711
*
         EJECT
******************************
* OPEN WORK AREA REFERENCE   *
******************************
*
FORCORE  DSECT
         IECDSECT
         EJECT
*
*              FULL TRACK INDEX WRITE - TRACK INDEX SAVE AREA
*
         DS    0D
TISA     DSECT
         USING TISA,R6
FTIWIOB  DS    2F                       IOB FOR FULL TRACK INDEX WR
SIZE     DS    AL2                      LENGTH OF TI ENTRY
FLAGS    DS    AL1                      FLAGS
HIGHR    DS    AL1                      HIGHEST R ON CURRENT TRK
CURRR    DS    0CL1                     CURRENT RECORD ON THIS TRK
NEXTTI   DS    AL3                      A(NEXT TI ENTRY)
TISASIZE DS    A                        SIZE OF TI
         EJECT
*
*
IGG0195U CSECT
         BALR  R9,0
         USING *,R9
BASETAG  L     R1,0(RPARC)
         L     RCORE,4(RWTGC)
         USING FORCORE,RCORE            OPEN WORK AREA ADDR      Y02072
         L     R12,DCBWKPT1
         STM   R0,R15,DXCCW1            SAVE REGISTERS           Y02072
*
* EQUATE SYMBOLIC REGISTERS
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RBASE    EQU   3
RCORE    EQU   4
RPAR     EQU   5
RWTG     EQU   6
RPARC    EQU   7
RWTGC    EQU   8
RJ       EQU   15
K36      EQU   36                       CONSTANT                 S20201
RSLOAD   EQU   X'20'                    RESUME LOAD SW           S20201
EOC      EQU   X'10'                    END OF CYL SW            S20201
K0       EQU   0                        CONSTANT                 S20201
K1       EQU   1                        CONSTANT                 S20201
*
         EJECT
*
*
***********************************************************************
*
*              INITIALIZE CHANNEL PROGRAM 20 WHEN USING FULL
*              TRACK INDEX WRITE WITH RESUME LOAD
*
***********************************************************************
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=10  SET USERS KEY       Y02072
         SPACE 1
         LR    R14,RCORE                SAVE OPEN WORK AREA ADDE Y02072
         DROP  RCORE                    END OPEN WORK AREA USING Y02072
         USING FORCORE,R14              OPEN WA ADDRESSABILITY   Y02072
         L     R10,DCBWKPT6
         L     R6,36(R10)               C(R6)=A(TISA)
SUCSSFUL EQU   X'C0'                    FTIW SUCESSFUL           S20201
         TM    K36(R10),SUCSSFUL        WAS GETMAIN SUCESSFUL    S20201
         BNO   EXIT                     NO EXIT MODULE           S20201
         TM    DCBST,RSLOAD             IS IT RESUME LOAD        S20201
         BZ    EXIT                     NO - FINISHED - EXIT     S20201
         LA    R6,0(R6)                 CLEAR HIGH ORDER BYTE
*
*              SET FLAG BIT FOR RESUME LOAD
*
         OI    FLAGS,X'80'              SET RL FLAG IN TISA
*
         NI    FLAGS,X'CF'              TURN EOC BIT OFF
*
         SR    R3,R3                    CLEAR REGISTER 3
         SR    R4,R4                    CLEAR REGISTER 4
***********************************************************************
*
*              RESUME LOAD ON A NEW CYLINDER
*
***********************************************************************
         TM    DCBST,EOC                RESUME LOAD ON EOC       S20201
         BNO   ISL01                    NO                       S20201
         OI    FLAGS,EOC                TURN ON EOC INDICATOR    S20201
         MVI   TSTWK2C,K0               BEGGINNING H = 0         S20201
         B     ISL04                    BR TO SET START R = 1    S20201
ISL01    EQU   *
         MVC   TSTWK2C(1),DCBLETI+3     SAVE H FOR COMPARE
         CLC   ISLNCNT+4(1),DCBLETI+4   WILL ENTRY BE OVERLAYED
         BE    ISL02                    NO
         EJECT
***********************************************************************
*
*              NEXT NORMAL TI ENTRY WILL BE OVERLAYED
*
***********************************************************************
         IC    R3,DCBLETI+4             STARTING R
         B     ISL05                    BR TO COMPUTE CCW LENGTH
ISL02    EQU   *
         CLC   ISLNCNT+4(1),ISLHIRT     IS N ENTRY LAST ON TRK
         BNE   ISL03                    NO
***********************************************************************
*
*              NEXT NORMAL ENTRY WILL BE RECORD 2
*
***********************************************************************
         IC    R4,ISLNCNT+3             H
         LA    R4,1(R4)                 H + 1
         STC   R4,TSTWK2C               SAVE H FOR COMPARE
         LA    R3,2                     SET STARTING R=2
         B     ISL05
ISL03    EQU   *
         IC    R3,ISLNCNT+4             R
         LA    R3,2(R3)                 STARTING R = R+2
         STC   R3,TSTWK2C+1             SAVE R+2 FOR COMPARE
         CLC   TSTWK2C+1(1),ISLHIRT     IS RL ON NEXT TRK
         BNH   ISL05                    NO
***********************************************************************
*
*              NEXT NORMAL ENTRY WILL BE RECORD 1
*
***********************************************************************
         OI    FLAGS,X'20'              TURN EOT BIT ON
         MVC   ISLNCNT+3(1),DCBLETI+3   SET H TO PREVIOUS TRK
ISL04    EQU   *
         LA    R3,1                     SET STARTING R=1
ISL05    EQU   *
         LR    R8,R3                    STARTING R
         SLL   R8,3                     8(R) FOR CCW LENGTH
         CLC   TSTWK2C(1),ISLNIRT+1     RL ON LAST TI TRACK
         BNE   CP20A                    NO
         CLI   DCBHIRSH,X'00'           IS THERE SHR TRK
         BNE   CP20B                    YES
         EJECT
***********************************************************************
*
*              INITIALIZE CHANNEL PROGRAM 20A FOR RESUME LOAD.
*
***********************************************************************
*
*              END OF CYLINDER - UNSHARED TRACK
*
         L     R4,20(R10)               A(CP20A)
         L     R5,20(R4)                A(1ST CCW)
         MVC   HIGHR(1),ISLNIRT+2       SET HIGH R
         B     END20A
CP20A    EQU   *
         L     R4,20(R10)               A(CP20A)
         LA    R5,24(R4)                1ST WRITE CCW
END20A   EQU   *
         ST    R4,TSTWK3C               SAVE CPSAD FOR PUT MODULE
         S     R8,ISL8                  BACK UP 1 CCW
         AR    R8,R5                    A(1ST RL CCW)
         ST    R8,16(R4)                SET TIC
         MVI   16(R4),X'08'
         L     R7,0(R8)                 ENTRY IN TISA
         ST    R7,NEXTTI
         BCTR  R3,0                     R - 1
         STC   R3,CURRR                 SET CURRENT R
         STC   R3,TSTWK3C               SAVE R FOR SRCH INIT IN PUT
         B     CP20C                    BR TO CHECK FOR WR CHECK
         EJECT
***********************************************************************
*
*              INITIALIZE CHANNEL PROGRAM 20B FOR RESUME LOAD.
*
***********************************************************************
CP20B    EQU   *
         L     R4,40(R10)               A(CP20B)
         ST    R4,TSTWK3C               SAVE CPSAD FOR PUT MODULE
         STC   R3,TSTWK3C               SAVE R FOR SRCH INIT IN  S20201
*                                       PUT                      S20201
         CH    R3,ILS001                R=1                      S20201
         BNE   RL20B                    NO                       S20201
RL20BX   EQU   *                        *                        S20201
*              INITIALIZE TISA FOR R = 1 ON A SHARED TRACK
         L     R5,24(R4)                1ST ENTRY IN TISA
         LA    R5,0(R5)
         S     R5,ISL8                  BACK UP TO COUNT
         ST    R5,NEXTTI                SET NEXT TI + CURR R=0
         MVC   HIGHR(1),ISLNIRT+2       SET HIGH R
         B     CP20C
RL20B    EQU   *
         MH    R8,ISL3                  3(8(R))
         AR    R8,R4                    CP20B
         ST    R8,16(R4)                SET TIC
         MVI   16(R4),X'08'
         L     R5,0(R8)                 1ST ENTRY IN TISA
         S     R5,ISL8                  BACK UP TO COUNT
         ST    R5,NEXTTI                SET NEXT TI ENTRY
         BCTR  R3,0                     R-1 FOR CURRR
         STC   R3,CURRR                 SET CURRENT R
         EJECT
***********************************************************************
*
*              INITIALIZE CHANNEL PROGRAM 20C FOR RESUME LOAD.
*
***********************************************************************
CP20C    EQU   *
         TM    DCBOPTCD,X'80'           WRITE CHECK
         BZ    EXIT                     NO WRITE CHECKING
         L     R4,44(R10)               A(CP20C)
         CLC   ISLNCNT+3(1),ISLNIRT+1   RL ON LAST TI TRK
         BE    LAST                     YES
         LA    R5,24(R4)                A(1ST CCW)
         B     CALC20C
LAST     EQU   *
         L     R5,20(R4)                A(1ST CCW FOR SHR TRK OR
*                                       LAST TRK )
CALC20C  EQU   *
         LA    R3,1(R3)                 STARTING R
         LR    R8,R3                    STARTING R
         MH    R8,ISL3                  MULTIPLY BY 3 FOR SRCH TIC
         SLL   R8,3                     MULTIPLY BY 8 FOR CCW LENGTH
         S     R8,ISL24                 BACK UP 3 CCW'S
         AR    R8,R5                    ADD TO 1ST CCW
         ST    R8,16(R4)                SET TIC
         MVI   16(R4),X'08'             RESTORE COMMAND CODE
         L     R5,0(R8)                 1ST ENTRY IN TISA
         S     R5,ISL8                  BACK UP TO COUNT
         ST    R5,0(R4)                 SET SEARCH
         MVI   0(R4),X'31'              RESTORE COMMAND CODE
         EJECT
* EXIT ********** EXIT ***************** EXIT *********** EXIT ********
*
EXIT     EQU   *
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
         LM    R0,R15,DXCCW1            RESTORE REGISTERS        Y02072
         DROP  R14                      END OPEN WA USING        Y02072
         USING FORCORE,RCORE            WA ADDRESSABILITY        Y02072
         USING BASETAG,RBASE            ESTABLISH BASE
         TM    DCBOPTCD,X'80'           TEST FOR WRITE CHECK      24503
         MVC   0(L'LOAD2R,RWTGC),LOAD2R XCTL TO 2R IF NOT        Y02072
         BZ    RELOOP                   BRANCH IF NOT            M4874
         MVC   0(L'LOAD2U,RWTGC),LOAD2U XCTL TO 2U IF WRITE CK   Y02072
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BE    BASETAG                  BR-BEGIN THIS MODULE
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BNE   RELOOP                   BRANCH IF NOT AT END     M4874
         LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BNE   TCTLRTN                  BRANCH IF NOT ZERO       M4874
         LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         LA    RJ,DXCCW12                                        Y02072
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
*
ILS001   DC    H'1'                     CONSTANT FOR TESTING     S20201
ISL3     DC    H'0003'
ISL8     DC    F'0008'
ISL24    DC    F'0024'
*
THISLOAD DC    C'5U'                    CURRENT MODULE ID
OPNLOD7  DC    C'0S'
LOAD2R   DC    C'2R'                    ID OF MODULE IGG0192R    Y02072
LOAD2U   DC    C'2U'                    ID OF MODULE IGG0192U    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         END
