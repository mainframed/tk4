         TITLE 'IGG02029 - SCAN MODE CLOSE MODULE'
IGG02029 CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG02029                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM CLOSE, SCAN MODE                            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 01                                *
*                                                                     *
* FUNCTION = IF ABEND IS NOT IN CONTROL, AND A READ IS IN PROGRESS,   *
*            WAIT FOR THE READ TO COMPLETE.  IF ANY WRITE ERRORS HAVE *
*            OCCURRED, SYNCH TO EINFO ROUTINE OF GET MODULE.  IF AN   *
*            UNWRITABLE RECORD IS ENCOUNTERED, THE EINFO ROUTINE SETS *
*            UNCORRECTABLE FLAG IN DCBEXCD1, AND RETURNS TO THE CLOSE *
*            ROUTINE, WHICH THEN SYNCHES TO USER'S SYNAD ROUTINE.     *
*            AFTER ALL WRITE ERRORS ARE CLEARED, IF ANY EXISTED, AND  *
*            IF CLOSE HAS NOT GONE TO THE ESETL ROUTINE OF THE GET    *
*            MODULE ALREADY, SYNCH TO ESETL.  UPON RETURN, WAIT ON    *
*            OUTSTANDING WRITES, IF ANY.  IF WRITE ERRORS OCCUR,      *
*            SYNCH TO EINFO ROUTINE AGAIN.  UPON RETURN, RESTRUCTURE  *
*            THE BUFFER POOL.  THEN, IF DCB WAS OPENED FOR SHR,       *
*            ACQUIRE LOCAL AND CMS LOCKS; REFRESH DCB FROM THE FIELD  *
*            AREA; AND DECREMENT THE FIELD AREA, AND ASID USE COUNTS  *
*            AS REQUIRED.  FREE FIELD AREA OR FIELD AREA EXTENSION    *
*            CORE, IF REQUIRED.  FINALLY, FREE SCAN MODE WORK AREA.   *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = CLOSE EXECUTOR                                        *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 1341 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG02029                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM IOS CLOSE FOR SCAN MODE ONLY.    *
*              RECEIVES CONTROL IN STORAGE PROTECT KEY 5 AND          *
*              PRIVILEGED STATE.                                      *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM CLOSE EXECUTOR IGG0202D IN STORAGE       *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = SYNCH TO USER'S SYNAD ROUTINE.  THE USER MUST, HOWEVER,*
*              RETURN TO THIS CLOSE MODULE.                           *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = EINFO ROUTINE OF THE GET MODULE                       *
*               ESETL ROUTINE OF THE GET MODULE                       *
*               USER'S SYNAD ROUTINE                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - CLOSE WORK AREA                           *
*                 IGGSCAN - ISAM SCAN MODE WORK AREA                  *
*                 IHAPSA - LOW CORE ADDRESSABILITY                    *
*                                                                     *
*    CONTROL-BLOCKS = DCB, DCB COPY, DEB, TCB, CVT, SVRB, DCBFA AND   *
*                     ASCB                                            *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, WAIT, SYNCH, SETLOCK, FREEMAIN, AND XCTL          *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*          RELEASE OS/VS2-02 DELETIONS                                *
*                                                               YM01420
*                                                               YM05395
*                                                               YM06516
*                                                               YM07002
*                                                                     *
***********************************************************************
         EJECT
         USING IHADCB,R2
         USING SISWORK1,R15
         BALR  R3,0
         USING *,R3
RE       EQU   0
RF       EQU   1
RDCB     EQU   2
RBASE    EQU   3
RG       EQU   4
RPAR     EQU   5                        CONTAIN PARAMETER LIST ADDRESS
RWTG     EQU   6                        ADDR OF BEGIN OF WTG TABLE
RPARC    EQU   7                        CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                        CURRENT ENTRY IN WTG TABLE
RTIOT    EQU   9
RUCB     EQU   10
RA       EQU   11
RB       EQU   12
RC       EQU   13
RD       EQU   14
RJ       EQU   15
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
FOUR     EQU   4                        CONSTANT OF 4            Y01021
SHR      EQU   X'80'                   INDICATOR FOR DISP=SHR    A47331
CLOSE    EQU   X'20'                    IND CLOSE IN PROCESS     Y02072
SP241    EQU   241                      SUBPOOL FOR FIELD AREA   Y02072
ESETL    EQU   X'40'                     CLOSE-ESETL INDICATOR
ABENDBIT EQU   X'80'                     ABEND INDICATOR IN TCB
WAITBIT  EQU   X'40'                     WAIT BIT IN ECB
DMCODE   EQU   250                      INTERNAL PROB DET CODE  YM05395
         SPACE 2
SISC4A1  L     R2,0(0,RPARC)            SET BASE FOR DCB
         L     R15,DCBWKPT1             SET BASE FOR WORK AREA
         USING WTGTBL,RWTGC             WHERE TO GO TABLE        Y02072
         L     R9,OPENWA                CLOSE WORK AREA ADDR     Y02072
         USING FORCORE,R9               CLOSE WA ADDRESSABILITY  Y02072
         LR    RG,R2                    COPY DCB ADDR            Y02072
         L     R1,DCBDEBAD              PROTECTED DEB ADDR       Y02072
         L     R2,DXUDCBAD              USERS DCB ADDR           Y02072
         L     R1,0(R1)                 GET TCB ADDR            SA56350
         USING TCB,R1
         TM    TCBFLGS,ABENDBIT         IS ABEND BIT ON?        SA56350
         BO    SISABEND                 YES, BR TO FREE DCBFA   SA56350
         DROP  R1                       END TCB ADDRESSABILITY  YM06516
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=11  CHANGE TO USER KEY  Y02072
         SPACE 2
         TM    W1ECBI,WAITBIT          IS A READ IN PROGRESS       7490
         BO    SISC4A2                 NO                          7490
         LR    R10,R1                   NO, LET US WAIT            7490
         LA    R1,W1ECBI               ADDRESS OF ECB              7490
*
         WAIT  ECB=(1)                 WAIT MAN WAIT               7490
*
         LR    R1,R10                  RESTORE R1                  7490
         L     R15,DCBWKPT1            REINSTATE R15               7490
SISC4A2  OI    DCBEXCD2,X'20'   INDICATE TO USER THAT CLOSE IN PROC7490
         SPACE 2
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
         SPACE 2
         STM   RG,R8,DXCCW1             SAVE REGISTERS 4-8       Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=4   CHANGE TO USER KEY  Y02072
         SPACE 2
         DROP  R9                       END CLOSE WA USING       Y02072
         LR    RWTG,R9                  CLOSE WORK AREA ADDR     Y02072
         USING FORCORE,RWTG             CLOSE WA ADDRESSABILITY  Y02072
         LA    R4,XXXC4C2               SYNCH RETURN - NON-ERROR
         STM   R3,R4,W1CN11             SAVE BASE AND RETURNS   YM07002
         DROP  R2                       END USING ON USER DCB    Y02072
         L     R10,DXCCW1               COPY DCB ADDR            Y02072
         USING IHADCB,R10               COPY ADDRESSABILITY      Y02072
         L     R10,DCBDEBAD             PROTECTED DEB ADDR       Y02072
         USING IHADCB,R2                USER DCB ADDRESSABILITY  Y02072
         USING IHADEB,R10                                        S21045
         L     R10,DEBEXPTR              GET ADDR OF DEB         S21045
*                                       EXTENSION PTR            S21045
         USING DEBEXT,R10                                        S21045
         TM    W1OSBIT2,X'80'           ANY WRITE ERRORS
         BZ    SISC4F1                  NO,GO TO ESETL
*                                       YES
SISC4B1  EQU   *                                                 S21045
         L     R11,DEBGET                LOAD ADDR OF GET MODULE S21045
         A     R11,12(0,R11)            R11 CONTAINS EINFO ADDRESS
         LR    R1,R2                    SET R1 TO DCB ADDRESS
         L     R2,DCBWKPT1              SET WORKAREA BASE FOR EINFO
         LA    R14,XXXC4D1              SYNCH RETURN - ERROR    YM07002
         ST    R14,W1CN12               SAVE RETURN FOR EINFO   YM07002
         LA    R15,SISEINFO             ADDRESS FOR SYNCH        Y02072
         BAL   R14,SYNCHRTN             SYNCH TO EINFO           Y02072
         CLI   W1CN8,C4D1               ANY BAD BUFFERS
         BE    SISC4D1                  YES - ERROR ROUTINE
         B     SISC4C2                  NO - NORMAL ROUTINE
C4D1     EQU   1                        RETURN INDICATOR - ERROR
NOTC4D1  EQU   0                        RETURN INDICATOR - O.K.
XXXC4C2  EQU   *                        NON-ERROR RETURN
         MVI   W1CN8,NOTC4D1            SET  NO ERROR INDICATOR
         BR    R14                      RETURN
XXXC4D1  EQU   *                        ERROR RETURN
         MVI   W1CN8,C4D1               SET ERROR INDICATOR
         BR    R14                      RETURN
SISEINFO EQU   *                        EINFO LINK ROUTINE
         BALR  R9,R11                   GO TO EINFO TO SET REGISTERS
*                                       FOR ERROR EXIT
         B     XXXC4D1                  ERROR RETURN
         SPACE 4
SYNCHRTN EQU   *                        SYNCH TO PROCESSING RTNS Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY (5)     Y02072
         SPACE 2
         ST    R14,DXCCW6               SAVE RETURN ADDRESS     YM07002
         USING PSA,R0                   LOW CORE ADDRESSABILITY  Y02072
         L     R14,PSATOLD              ADDR OF TCB              Y02072
         USING TCB,R14                  TCB ADDRESSABILITY       Y02072
         L     R14,TCBRBP               ADDR OF SVRB             Y02072
         USING RBBASIC,R14              SVRB ADDRESSABILITY      Y02072
         SPACE 1
         MODESET  KEYADDR=KEY0,WORKREG=5  SET PROTECTION KEY 0   Y02072
         SPACE 1
         STM   R2,R12,RBEXSAVE          SAVE REGS 2-12           Y02072
         DROP  R14                      END USING ON SVRB        Y02072
         SPACE 2
         SYNCH (15)                     SYNCH TO PROCESS RTN     Y02072
         SPACE 2
         USING PSA,R0                   LOW CORE ADDRESSABILITY  Y02072
         L     R14,PSATOLD              ADDRESS OF TCB           Y02072
         USING TCB,R14                  TCB ADDRESSABILITY       Y02072
         L     R14,TCBRBP               ADDRESS OF SVRB          Y02072
         USING RBBASIC,R14              SVRB ADDRESSABILITY      Y02072
         LM    R2,R12,RBEXSAVE          RESTORE REGS 2-12        Y02072
         DROP  R14                      END USING ON SVRB        Y02072
         SPACE 1
         MODESET  KEYADDR=DXUKEY,WORKREG=14  SET USER'S KEY      Y02072
         SPACE 1
         L     R14,DXCCW6               RESTORE RETURN ADDRESS  YM07002
         L     R2,DXUDCBAD              RESTORE USER DCB ADDRESS Y02072
         L     R15,DCBWKPT1             RESTORE WORK AREA ADDR   Y02072
         BR    R14                      RETURN                   Y02072
         SPACE 4
*        EINFO RETURNS HERE IF A BAD BUFFER EXISTS
SISC4D1  EQU   *                        GET SVRB ADDRESSABILITY  Y02072
         USING PSA,R0                   LOW CORE ADDRESSABILITY  Y02072
         L     R5,PSATOLD               TCB ADDRESS              Y02072
         L     R2,DCBDEBAD              DEB ADDRESS             YM06516
         LA    R2,0(R2)                 CLEAR HIGH ORDER BYTE   YM06516
         SH    R2,ISL8                  PT TO DEB EXTENSION PTR YM06516
         L     R2,0(R2)                 ADDRESS DEB EXTENSION   YM06516
         USING DEBXTN,R2                DEB EXT. ADDRESSABILITY YM06516
         TM    DEBXFLG1,DEBXTSKC        TASK CLOSE CLOSING DCB  YM06516
         BZ    SISSYNAD                 BR NO - TAKE SYNAD EXIT YM05395
         DROP  R2                       END DEB EXTENSION ADDR. YM06516
         SPACE 2
*        USER DID NOT ISSUE CLOSE - WAS ISSUED BY TASK TERMINATION,
*        ISSUE MESSAGE IEC203I AND TERMINATE TASK WITH 031 ABEND.
         SPACE 2
         MODESET EXTKEY=DATAMGT         SET DATA MGT KEY        YM05395
         SPACE 2
         DROP  RWTG                     END ADDR ON CLOSE WA    YM07002
         LR    R14,RWTG                 CLOSE WORK AREA ADDRESS YM07002
         USING FORCORE,R14              CLOSE WA ADDRESSABILITY YM07002
         LM    RG,R8,DXCCW1             RESTORE CLOSE REGISTERS YM07002
         LR    RG,R14                   CLOSE WA FOR PROB DET.  YM07002
         SPACE 1
         DMABCOND DMCODE,PDLOAD,RETURN=NONE,RES=NO,REGSAVE=YES  YM05395
         SPACE 1
         LR    R1,R14                   CLOSE WA ADDR FOR XCTL  YM07002
         DROP  R14                      END CLOSE WA USING      YM07002
         B     TCTLRTN                  XCTL TO PROB DET MODULE YM05395
         USING FORCORE,RWTG             CLOSE WA ADDRESSABILITY YM07002
         SPACE 2
SISSYNAD EQU   *                        TAKE SYNAD EXIT         YM05395
         L     R5,0(R5)                 SVRB ADDRESS
         SPACE
*   THE WORKPOINT BASE HAS ALREADY BEEN LOADED BY EINFO
         SPACE
         L     R2,DXUDCBAD              RESTORE USER DCB ADDR    Y02072
         USING IHADCB,R2                DCB ADDRESSABILITY      YM06516
         L     R15,DCBSYNAD             GO TO ERROR ROUTINE
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=14 CHANGE TO USER KEY     Y02072
         SPACE 2
         STM   R2,R12,96(R5)           SAVE SCAN REGS
         LM    R2,R12,40(R5)           RESTORE USERS REG
         SYNCH (15)                    TAKE SYNAD EXIT
         SPACE
*   THE USER MUST RETURN BY R14 WHICH CONTAINS THE ADDR. OF N.S.I.
         SPACE
SISC4C1  L     R5,PSATOLD              TCB ADDRESS               Y02072
         L     R5,0(R5)
         LM    R2,R12,96(R5)           RESTORE SCAN REGS
* UPON RETURN,R2 WILL HAVE DCB ADDR.
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=15  CHANGE TO USER KEY  Y02072
         SPACE 2
         L     R15,DCBWKPT1             SET R15 TO WORK POINT
         B     SISC4B1                  CHECK FOR MORE WRITE ERRORS
         SPACE 3
* EINFO WILL RETURN HERE WHEN ALL WRITE ERRORS HAVE BEEN TAKEN CARE OF
* AND THE WRITE QUEUE IS EMPTY
         SPACE
SISC4C2  EQU   *
         DROP  R2                       END USING ON USER DCB    Y02072
         L     R10,DXCCW1               COPY DCB ADDR            Y02072
         USING IHADCB,R10               COPY DCB ADDRESSABILITY  Y02072
         L     R10,DCBDEBAD             ADDR OF DEB FROM DCB    SA56388
         DROP  R10                      END USING ON COPY DCB    Y02072
         USING IHADCB,R2                USER DCB ADDRESSABILITY  Y02072
         USING IHADEB,R10                                       SA56388
         L     R10,DEBEXPTR             ADDR OF DEB EXTENSION   SA56388
         USING DEBEXT,R10                                       SA56388
         TM    W1OSBIT3,ESETL           HAS CLOSE GONE TO ESETL SA56388
         BO    SISC4J1                  YES, GO TO FREE WORK AREA
         SPACE 2
* GO TO ESETL
SISC4F1  LR    R1,R2                   SET R1 TO DCB ADDR FOR ESETL
         LA    R13,W1CN8+FOUR           POINT TO SAVE AREA       Y01021
         L     R15,DEBGET                LOAD ADDR OF GET MODULE S21045
         DROP  R10                      END USING ON DEB EXT.    Y02072
         A     R15,40(0,R15)
         SPACE
*        R13 CONTAINS THE ADDRESS OF A REGISTER SAVE AREA
*        THAT WILL BE USED BY ESETL
         SPACE
         BAL   R14,SYNCHRTN             GO TO ESETL
         SPACE
         TM    W1ECBO,X'40'             HAVE WRITES COMPLETED
         BO    SISC4G1                  YES
         LR    R11,R15
         LA    R1,W1ECBO                WAIT FOR WRITES TO COMPLETE
         WAIT  ECB=(1)
         LR    R15,R11
SISC4G1  TM    W1OSBIT2,X'80'     ANY WRITE ERRORS FROM ESETL WRITES
         BO    SISC4B1                  YES, GO TO EINFO
*                                       NO, FREE BUFFERS AND WORKAREA
         SPACE 2
***********************************************************************
* RESTRUCTURE BUFFER POOL
***********************************************************************
         SPACE 2
* OBTAIN BUFFER ADDRESSES FROM CHANNEL PROGRAMS (ALL ON EITHER WRITE OR
* FREE QUEUES) AND CHAIN BUFFERS TOGETHER WITH 1ST SCAN MODE BUFFER
* POINTING TO BUFFER PREVIOUSLY POINTED TO BY BUFCB AND BUFCB CONTENTS
* POINTING TO LAST SCAN MODE BUFFER AND INTERMEDIATE  BUFFERS CHAINED
* TOGETHER.
         SPACE 2
SISC4J1  L     R5,DCBBUFCB
         L     R5,0(R5)                 R5 POINTS TO 1ST BUFFER
         CLC   W1WRITEC(2),LAST         IS WRITE QUEUE EMPTY
         BE    SISC4J2                  YES
         L     R4,W1WR1ST               NO, GET CP ADDRESS
SISC4J11 L     R1,52(R4)                BUFFER ADDRESS
         ST    R5,0(R1)                 CHAIN TO LAST BUFFER
         LR    R5,R1                    R5 NOW POINTS TO NEW BUFFER
         CLC   33(3,R4),LAST            END OF Q
         BE    SISC4J2                  YES
         L     R4,32(R4)                NO, GET ADDRESS OF NEXT CP
         B     SISC4J11                 LOOP
SISC4J2  CLC   W1FREEC(2),LAST          IS FREE QUEUE EMPTY
         BE    SISC4J3                  YES
         L     R4,W1FR1ST               NO, GET ADDRESS 1ST FREE Q C P
SISC4J22 L     R1,52(R4)                BUFFER ADDRESS
         ST    R5,0(R1)                 CHAIN TO LAST BUFFER
         LR    R5,R1                    R5 NOW POINTS TO NEW BUFFER
         CLC   33(3,R4),LAST            END OF Q
         BE    SISC4J3                  YES
         L     R4,32(R4)                NO, GET ADDRESS OF NEXT CP
         B     SISC4J22                 LOOP
*
SISC4J3  L     R1,DCBBUFCB              GET BUFCB ADDRESS
         ST    R5,0(R1)                 BUFCB POINTS TO NEW 1ST BUFFER
         B     SISABND2                 SKIP REGISTER SAVE
         DROP  RWTG                     END CLOSE WA USING       Y02072
         SPACE 2
SISABEND  EQU  *                                                 SM4372
         USING FORCORE,R9               CLOSE WA ADDRESSABILITY  Y02072
         STM   RG,R8,DXCCW1             SAVE REGISTERS 4-8       Y02072
         DROP  R9                       END USING ON CLOSE WA    Y02072
         LR    RWTG,R9                  CLOSE WORK AREA ADDR     Y02072
         USING FORCORE,RWTG             CLOSE WA ADDRESSABILITY  Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=5   CHANGE TO USER KEY  Y02072
         SPACE 2
SISABND2 EQU   *                        CONTINUE
         DROP  R2                       END USING ON USER DCB    Y02072
         L     R5,DXCCW1                ADDR OF COPY DCB         Y02072
         USING IHADCB,R5                USE COPY DCB             Y02072
         L     R5,DCBDEBAD              PROTECTED DEB ADDR       Y02072
         USING IHADCB,R2                USER DCB ADDRESSABILITY  Y02072
         USING IHADEB,R5                DEB ADDRESSABILITY       Y02072
         L     R5,DEBEXPTR              PTR TO DEB EXTENSION     Y02072
         USING DEBEXT,R5                DEB EXTENSION            Y02072
         TM    DEBDCBFA,SHR             OPENED DISP EQ SHR       Y02072
         BZ    FREEWA                   BR NO TO FREE WA         Y02072
         XC    W1DCBFA,W1DCBFA          CLEAR FA PTR IN WORKAREA Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
         SPACE 2
         L     RF,DEBDCBFA              ADDR OF FIELD AREA       Y02072
         LA    RF,0(,RF)                CLEAR HIGH ORDER BYTE    Y02072
         SR    R15,R15                  SET REG TO ZERO          Y02072
         ST    R15,DEBDCBFA             CLEAR FIELD AREA PTR     Y02072
         ST    R15,DXCCW9               IND. NO DCBFA TO FREE    Y02072
         DROP  R5                       END USING ON DEB         Y02072
         LR    R5,RF                    FIELD AREA ADDR          Y02072
         SPACE 2
***********************************************************************
*        ACQUIRE LOCAL AND CMS LOCKS, BOTH FREED IN THIS ROUTINE.
***********************************************************************
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=11 CHANGE TO KEY ZERO     Y02072
         SPACE 2
GLCL1    SETLOCK  OBTAIN,TYPE=LOCAL,MODE=UNCOND,  OBTAIN THE     Y02072*
               RELATED=(DCBFA,IGG02029(FLCL1))  LOCAL LOCK       Y02072
         SPACE 2
GCMS1    SETLOCK  OBTAIN,TYPE=CMS,MODE=UNCOND,  OBTAIN CROSS     Y02072*
               RELATED=(DCBFA,IGG02029(FCMS1))  MEMORY SERVICES  Y02072
*                                               LOCK             Y02072
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=15 SET USER'S KEY       Y02072
         SPACE 2
         LR    R15,R5                                            A42900
         USING DCBFA,R15                                         A35340
         CLC   DFAID,DCBWKPT3       ?   TEST IF CORRECT ID       A47331
         BNE   NOREFRSH             ?   B IF NOT TO SKIP REFRSH  A47331
*
*       REFRESH DCB FROM DCBFA TO ENSURE CORRECT DSCB MERGE
         SPACE 2
         MVC   DCBRORG3(9),DFARORG3     RORG3,NREC,ST            A36467
         MVC   DCBLPDA,DFALPDA          LP                       A36467
         MVC   DCBNBOV,DFANBOV          NBOV                     A36467
         MVC   DCBRORG2,DFARORG2        RORG2                    A36467
         MVC   DCBNOREC(12),DFANOREC    NOREC,LIOV,ROR71         A36467
         SPACE 2
***********************************************************************
NOREFRSH EQU   *
         SPACE 2
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
         SPACE 2
         LH    R5,DFAUSE                GET USE COUNT            A35340
         BCT   R5,NOFREED               DECREMENT, FREE  AREA IF A35340
*                                       ZERO                     A35340
         L     RF,CVTPTR                GET CVT ADDR
         USING CVT,RF
         L     RF,CVTEXT1               GET CVT EXTENSION ADDR   A47331
         USING CVTXTNT1,RF
         L     R5,CVTFACHN              GET ADDR OF FIRST FA     A47331
*                                       0                        A35340
         LA    R5,0(R5)                 CLEAR HIGH BYTE          A47331
         DROP  R15
         USING DCBFA,R5
         LTR   R5,R5                    IS THERE A FA CHAIN      A47331
         BZ    ERR                      B IF NOT - ERROR         A47331
         LA    RB,CVTFACHN              INITLZ FOR CHAIN RESET   A47331
         LR    R0,RB                    SAVE ADDR OF CVT FA PTR  Y02072
         SPACE 2
***********************************************************************
*             SEARCH CHAIN FOR FIELD AREA TO FREE
***********************************************************************
         SPACE 2
CHNSRCH  CR    R5,R15                   IS IT THE SAME FA ADDR   A47331
         BNE   NEXTFA                   B IF NOT TO GET NEXT IN  A47331
*                                       CHAIN
         SPACE 2
         MODESET  KEYADDR=DXUKEY,WORKREG=9  SET USERS KEY       YM01420
         SPACE 2
         CLC   DFAID,DCBWKPT3           CHECK IF SAME ID         A47331
         BE    FOUNDFA                  B IF SO TO FREE         YM01420
         SPACE 2
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY (5)    YM01420
         SPACE 2
NEXTFA   LA    RB,DFACHAIN              NEXT CHAIN ADDR          A47331
         CLC   DFACHAIN+1(EOCLN),EOC    END OF CHAIN             A47331
         BE    ERR                      IF SO, NO FA - ERROR     A47331
         L     R5,DFACHAIN              GET NEXT FA IN CHAIN     A47331
         B     CHNSRCH                  B TO CONTINUE SEARCH     A47331
         SPACE 2
***********************************************************************
*             FOUND FIELD AREA, MUST UNCHAIN AND FREE AREA
***********************************************************************
         SPACE 2
FOUNDFA  EQU   *                        FIND FA IN FA CHAIN     YM01420
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY (5)    YM01420
         SPACE 2
FNDFA    CLC   DFACHAIN+1(EOCLN),EOC    LAST IN CHAIN            A47331
         BNE   UNCHAIN                  IF NOT, MOVE CHAINS      A47331
         USING CHAIN,RB                                          A47331
         CLC   LSTCHAIN,CVTFACHN        FIRST AND LAST IN CHAIN  A47331
         BNE   UNCHAIN                  B IF NOT, MOVE CHAINS    A47331
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=14 SET PROTECT KEY ZERO   Y02072
         SPACE 2
         XC    CVTFACHN,CVTFACHN        CLEAR CVT PT IF NO CHAIN A47331
         SPACE 2
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY (5)     Y02072
         SPACE 2
         B     FREEFA                   B TO FREE AREA           A47331
*
UNCHAIN  CR    R0,RB                    DELETING FIRST DCBFA     Y02072
         BNE   UNCHNFA                  BR IF NOT FIRST IN CHAIN Y02072
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=14 SET PROTECT KEY ZERO   Y02072
         SPACE 2
UNCHNFA  MVC   LSTCHAIN,DFACHAIN        MOVE CHAIN PTR           A47331
         SPACE 2
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY (5)     Y02072
         SPACE 2
FREEFA   XC    DCBFA(DFASIZE),DCBFA     CLEAR FIELD AREA         A47331
         LA    R0,SP241                 DCB FIELD AREA SUBPOOL   Y02072
         SLL   R0,24                                             A35340
         LA    R5,DFASIZE               SIZE OF WORK AREA        A35340
         OR    R0,R5                    SIZE AND SUBPOOL IN REG0 A35340
         LR    RF,R15                   ADDR OF CORE TO BE FREED Y02072
         STM   R0,RF,DXCCW9             SAVE SIZE, SUBPOOL AND   Y02072
*                                       SAVE CORE ADDRESS        Y02072
         B     FREELOCK                 RELEASE LOCAL,CMS LOCKS  Y02072
         DROP  R5
         USING DCBFA,R15
NOFREED  STH   R5,DFAUSE                STORE NEW USE COUNT      Y02072
         BAL   R14,ASIDRTN              FIND ASID USE COUNT      Y02072
ERR      EQU   *                                                 A47331
         SPACE 2
FREELOCK MODESET  KEYADDR=KEY0,WORKREG=11 CHANGE TO KEY ZERO     Y02072
         SPACE 2
***********************************************************************
*        RELEASE CMS AND LOCAL LOCKS, ACQUIRED AT GCMS1 AND GLCL1.
***********************************************************************
         SPACE 2
FCMS1    SETLOCK  RELEASE,TYPE=CMS,   RELEASE CROSS MEMORY       Y02072*
               RELATED=(DCBFA,IGG02029(GCMS1))  SERVICES LOCK    Y02072
         SPACE 2
FLCL1    SETLOCK  RELEASE,TYPE=LOCAL,  FREE THE                  Y02072*
               RELATED=(DCBFA,IGG02029(GLCL1))  LOCAL LOCK       Y02072
         SPACE 2
         MODESET EXTKEY=DATAMGT         SET DATA MGT KEY (5)     Y02072
         SPACE 2
         LM    R0,R1,DXCCW9             TEST FOR FIELD AREA TO   Y02072
         LTR   R0,R0                    BE FREED                 Y02072
         BZ    FREESWA                  BR NO FA OR FA EXT TO    Y02072
*                                       BE FREED                 Y02072
         FREEMAIN R,LV=(0),A=(1)        FREE DCBFA OR FA EXT.    Y02072
         SPACE 2
FREESWA  MODESET  KEYADDR=DXUKEY,WORKREG=15  CHANGE TO USER KEY  Y02072
         SPACE 2
FREEWA   EQU   *                        FREE SCAN WORK AREA      Y02072
         L     R15,DCBWKPT1             RESTORE PTR TO WORKAREA  A35340
         USING SISWORK1,R15                                      A35340
*              FREE QISAM SCAN WORK AREA
*
         LH    R0,W1WPLEN
         LA    R1,250
         SLL   R1,24
         OR    R0,R1                   R0 NOW HAS SUBPOOL AND LENGTH
         L     R1,DCBWKPT1                                       A42900
         XC    DCBWKPT1,DCBWKPT1                                 A42900
         FREEMAIN R,LV=(0),A=(1)                                 A42900
         SPACE 2
*   FINISHED WITH THIS DCB
         SPACE 2
         LM    RG,R8,DXCCW1             RESTORE REGISTERS 4-8    Y02072
         DROP  RWTG                     END USING ON CLOSE WA    Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
         SPACE 2
         MVC   0(L'LOAD2D,RWTGC),LOAD2D ID OF NEXT LOAD-IGG0202D Y02072
         L     R1,4(RWTGC)             SET BASE FOR WTG TABLE
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURR ENTRY
         LA    RPARC,4(RPARC)
         CLC   0(2,RWTGC),THISLOD
         BCR   8,RBASE                  BRANCH EQ TO BEGINNING THIS MOD
         CLC   0(1,RWTGC),OPLOD7        IS IT END OF WTG TABLE
         BNE   RELOOP                   NO
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC AND RWTGC
         LA    RWTGC,32(RWTG)
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = ZERO
         BNE   TCTLRTN                  ENTRY NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       MOVE ID
*
         USING FORCORE,R1               R1 = CLOSE WORKAREA ADDR Y01021
         LA    RJ,DXXCTL                WA ADDRESS               Y01021
         XC    DXXCTL,DXXCTL            CLEAR PARM LIST, NO DCB YM07002
*                                       FOR LPALIB              YM07002
         XCTL  EPLOC=(RWTG),SF=(E,(15)) XCTL TO IGG0202D         Y02072
         DROP  R1                       END USING ON CLOSE WA    Y02072
         DS    0F
ISL8     DC    H'8'                     DECR DEB PTR TO EXT PTR YM06516
PDLOAD   DC    C'6M'                    MODULE IGG0206M         YM05395
LAST     DC    F'0'
KEY0     EQU   LAST                     PROTECT KEY ZERO         Y02072
OPLOD7   DC    C'0'
THISLOD  DC    C'29'
EOC      DC    XL3'FFFFFF'              END OF CHAIN INDICATOR   A47331
EOCLN    EQU   3                        LENGTH OF EOC FIELD
*
         EJECT
**********************************************************************
*    TOTAL USE COUNT DID NOT GO TO ZERO.  FIND THE ADDRESS SPACE USE *
*    COUNT (ASID) AND DECREMENT IT BY ONE.  IF IT GOES TO ZERO, MOVE *
*    THE LAST ASID USE COUNT ENTRY TO REPLACE THE ENTRY WHICH USE    *
*    COUNT WENT TO ZERO.  THEN SET THE LAST ASID USE COUNT TO ZERO.  *
**********************************************************************
         SPACE 2
         USING FORCORE,RWTG             CLOSE WA ADDRESSABILITY  Y02072
*
ASIDRTN  STM   RPARC,RD,DXCCW4          SAVE REGISTERS 7-14      Y02072
*
         USING PSA,R0                   LOW CORE ADDRESSABILITY  Y02072
         L     R11,PSAAOLD              ADDRESS OF ASCB          Y02072
         USING ASCB,R11                 ASCB ADDRESSABILITY      Y02072
         LH    R11,ASCBASID             ADDRESS SPACE ID         Y02072
         DROP  R11                      END USING ON ASCB        Y02072
*
         USING DCBFA,R15                FIELD AREA ADDR          Y02072
         LA    RB,DFAASID1              ADDR FIRST ASID FIELD    Y02072
         USING DFAASID,RB               USER ASID FIELDS         Y02072
         SR    R9,R9                    IND SEARCH FIELD AREA    Y02072
*
NOASIDS  LA    RC,DFANOIDS              NO. ASIDS IN FIELD AREA  Y02072
         B     ASIDSRCH                 SEARCH FA FOR THIS ASID  Y02072
*
NEXTASID LA    RB,DFANXPTR(RB)          PT TO NEXT ASID          Y02072
ASIDSRCH LH    RD,DFAASIDX              USER ASID                Y02072
         CR    R11,RD                   ASID EQ THIS ADDR SPACE  Y02072
         BE    FNDASID                  BR IF THIS ADDR SPACE    Y02072
         LTR   RD,RD                    IS THERE AN ASID         Y02072
         BZ    ENDSRCH                  BR IF NO ASID            Y02072
         BCT   RC,NEXTASID              BR IF MORE ASIDS         Y02072
*
         LA    R8,DFAPTR                SAVE ADDR OF EXT PTR     Y02072
         L     RB,DFAPTR                PTR TO EXTENSION         Y02072
         LTR   R9,RB                    ANOTHER EXTENSION        Y02072
         BZ    ENDSRCH                  ASID NOT FOUND           Y02072
         B     NOASIDS                  SEARCH EXTENSION         Y02072
*
*        DECREMENT ASID USE COUNT
*
FNDASID  LH    RPARC,DFAUSEX            ASID USE COUNT           Y02072
         BCT   RPARC,STORECNT           SUBTRACT ONE, BR NOT 0   Y02072
*
*        FIND LAST ASID ENTRY IN FIELD AREA OR EXTENSION
*
         LR    RPARC,RB                 ASID FIELD PTR           Y02072
         LR    R11,RB                   ADDR LAST ASID ENTRY     Y02072
*
         DROP  RB                       END USER ASID ADDR       Y02072
         USING DFAASID,RPARC            EST USER ASID ADDR       Y02072
NXTFLD   BCT   RC,NXTASID               BR IF MORE ASIDS         Y02072
         LA    R8,DFAPTR                SAVE ADDR OF EXT PTR     Y02072
         L     RPARC,DFAPTR             PTR TO EXTENSION         Y02072
         LTR   RPARC,RPARC              ANOTHER EXTENSION        Y02072
         BNZ   NUMIDS                   BR IF ANOTHER EXTENSION  Y02072
         CR    R11,RB                   ASID LAST IN EXTENSION   Y02072
         BE    CLEAR                    CLEAR LAST ASID          Y02072
         B     RESET                    MOVE LAST ASID TO FILL   Y02072
*                                       FIELD WITH ZERO COUNT    Y02072
*
NXTASID  LA    RPARC,DFANXPTR(RPARC)    PT TO NEXT ASID          Y02072
         B     GETID                    TEST NEXT ASID           Y02072
NUMIDS   LA    RC,DFANOIDS              NO. ASIDS IN EXTENSION   Y02072
         LR    R11,RPARC                ASID PTR                 Y02072
         LR    R9,RPARC                 ADDR OF EXTENSION        Y02072
GETID    LH    RD,DFAASIDX              ASID                     Y02072
         LTR   RD,RD                    IS THERE AN ASID         Y02072
         BZ    RESET                    BR NO ASID               Y02072
         LR    R11,RPARC                ADDR LAST ASID FOUND     Y02072
         B     NXTFLD                   TEST NEXT ASID           Y02072
*
         DROP  RPARC                    END USER ASID ADDR       Y02072
         USING DFAASID,RB               EST USER ASID ADDR       Y02072
STORECNT EQU   *                        STORE NEW ADDRESS        Y02072
         STH   RPARC,DFAUSEX            SPACE USE COUNT          Y02072
         B     ENDSRCH                  RETURN                   Y02072
*
RESET    L     RPARC,0(R11)             LAST ASID ENTRY TO FILL  Y02072
         ST    RPARC,0(RB)              * FIELD WITH ZERO COUNT  Y02072
CLEAR    SR    RPARC,RPARC              SET REG TO ZERO          Y02072
         ST    RPARC,0(R11)             CLEAR LAST ASID          Y02072
         CR    R9,R11                   LAST ASID ONLY ENTRY     Y02072
         BNE   ENDSRCH                  FREE LOCKS               Y02072
*
         SR    R11,R11                  ZERO REG                 Y02072
         ST    R11,0(,R8)               ZERO EXTENSION PTR       Y02072
         LA    R8,SP241                 EXTENSION SUBPOOL        Y02072
         SLL   R8,24                    SUBPOOL TO HIGH BYTE     Y02072
         LA    R11,DFEXSIZE             SIZE OF EXTENSION        Y02072
         LA    R11,0(R11)               CLEAR HIGH BYTE          Y02072
         OR    R8,R11                   SIZE AND SUBPOOL         Y02072
         STM   R8,R9,DXCCW9             STORE SIZE, SUBPOOL, AND Y02072
*                                       ADDRESS FOR FREEMAIN     Y02072
*
ENDSRCH  LM    RPARC,RD,DXCCW4          RESTORE REGS 7-14        Y02072
         BR    R14                      RETURN                   Y02072
         SPACE 3
*
*  TTR TABLE
*
LOAD2D   DC    C'2D'                    ID OF MODULE IGG0202D    Y02072
*
PATCH    DC    XL((*-IGG02029)/20)'00'  ZEROED PATCH AREA        Y02072
         EJECT
         IECDSECS MAIN,EXPAND=YES                               YM05395
         SPACE 5
         DCBD  DSORG=(IS)
IHADEB   IGGDEBD
DEBXTN   DSECT
DEBXLNGH  DS   H                        LENGTH DEB EXTENSION
DEBXFLG1  DS   B                        FLAG BYTE
DEBXTSKC EQU   X'40'                    TASK CLOSE CLOSING DCB
         EJECT
SISWORK1 IGGSCAN              SCAN MODE WORK AREA                A35340
*                                  ****END OF FIXED LENGTH FIELDS ****
         EJECT
DCBFA    IGGDCBFA
CHAIN    DSECT
LSTCHAIN DS    F
WTGTBL   DSECT
IDTTR    DS    F                        MODULE ID AND TTR
OPENWA   DS    A                        ADDRESS OF WORK AREA
         CVT   DSECT=YES
         SPACE 2
TCB      IKJTCB
         SPACE 4
         IKJRB                                                   Y02072
         EJECT
         IHAASCB
         EJECT
         IHAPSA                                                  Y02072
         END
