         TITLE 'IGG019GO - APPENDAGES-PART 2,COMBINED,WITH WRT CHK'
IGG019GO CSECT
*
*          RELEASE OS/VS2-02 DELETIONS/CHANGES
*          RELEASE 21 DELETIONS/CHANGES
*          RELEASE 20 DELETIONS                                       *
*0568090200,105900-106000                                        A34932
*0568200200-200800                                               A34619
*0568090320                                                      M3299
*                                                                S20201
*
* THIS MODULE WAS REWRITTEN IN RELEASE 20.2 TO
* UTILIZE CHANNEL PROGRAM AND WORK AREA
* MACRO EXPANSIONS
*
*STATUS CHANGE LEVEL 011
* FUNCTION/OPERATION' APPENDAGE ROUTINES FOR BISAM
*        WHEN READ AND UPDATE IS USED WITH WRITE KN,
*        WHEN WRITE VALIDITY CHECKING IS     REQUESTED,
*        (PART 2)
*        CEND  - IF CHANNEL PROGRAM ENDS
*                **WITHOUT ERROR, EITHER
*                    PROCESSING COMPLETION WILL BE SCHEDULED,
*                    PROCESSING CONTINUATION WILL BE SCHEDULED,
*                    OR PROCESSING WILL CONTINUE.
*                **WITH A LENGTH CHECK, TESTS DETERMINE WHETHER
*                    OR NOT TO TREAT THE SITUATION AS A REAL
*                    ERROR.
*                **ANY OTHER ERROR, IN GENERAL, 'UNREACHABLE
*                    BLOCK' IS INDICATED IN THE DECB, AND
*                    PROCESSING COMPLETION WILL BE SCHEDULED.
*
*        ABEND - IF CHANNEL PROGRAM ENDS ABNORMALLY BECAUSE OF
*                **PERMANENT ERROR, 'UNCORRECTABLE I/O ERROR'
*                    IS INDICATED IN THE DECB, AND PROCESSING
*                    COMPLETION WILL BE SCHEDULED.
*                    USE OF WRITE VALIDITY CHECKING OPTION
*                    ALLOWS A LIMITED NUMBER OF RE-EXECUTIONS
*                    BEFORE INDICATING AN ERROR.
*                **NON-PERMANENT ERROR, ONE RE-TRY IS MADE
*                    BEFORE INDICATING A PERMANENT ERROR.
*                **FILE PROTECTION, TESTS DETERMINE WHETHER
*                    OR NOT TO TREAT THE SITUATION AS A REAL
*                    ERROR.
*                **ANY OTHER ERROR, IN GENERAL, 'UNREACHABLE
*                    BLOCK' IS INDICATED IN THE DECB, AND
*                    PROCESSING COMPLETION WILL BE SCHEDULED.
*
* ENTRY POINTS-
*        ENTRY POINT               FUNCTION
*        ANY POINT IN THE VECTOR TABLE AT THE BEGINNING OF THE MODULE.
* INPUT - N/A
* OUTPUT - N/A
* EXTERNAL ROUTINES - N/A
* EXITS  1. RETURN TO IOS VIA THE APPENDAGE RETURN VECTOR TABLE POINTED
*            TO BY REGISTER 14.  SEE THE DSECT LABELED    APPRV FOR THE
*            FORMAT OF THIS TABLE AND THE USE OF EACH EXIT.
*        2. SCHEDULE AN ASYNCHRONOUS ROUTINE - BRANCH VIA THE EXIT
*            EFFECTOR ADDRESS IN THE COMMUNICATION VECTOR TABLE.
* TABLES/WORK AREAS ' DECB, DCB, IOB AND EXTENTION, DCB WA (DCW), DEB,
*        COMMUNICATION VECTOR TABLE.
*        SEE DSECTS AT END OF MODULE FOR FORMAT AND DESCRIPTIONS.
* ATTRIBUTES- REENTRANT. DISABLED.
* NOTES - NONE
* GENERAL REGISTERS ARE USED AS FOLLOWS
R0       EQU   0                   *    -----
R1       EQU   1                   *    12* ADDRESS
R2       EQU   2                   *    IOB
R3       EQU   3                   *    DEB
R4       EQU   4                   *    DCB
BASEXXXX EQU   5                        BASE REGISTER            Y02072
R6       EQU   6                   *    -----
R7       EQU   7                   *    UCB
SAVEXXXX EQU   8                        SAVE AREA                Y02072
R9       EQU   9                   #    WORK REGISTER
R10      EQU   10                       CHANNEL PROGRAM POINTER
R11      EQU   11                       DECB
R12      EQU   12                       WORK REGISTER
R13      EQU   13                       WORK REGISTER
R14      EQU   14                       IOS RETURN VECTOR TABLE
R15      EQU   15                       BASE REGISTER
*        * MEANS THIS REGISTER IS SET UP UPON ENTRY FROM IOS,
*          AND ITS CONTENTS MUST BE RESTORED UPON RETURN TO IOS
*        # MEANS THIS REGISTER MAY BE CHANGED, BUT UPON RETURN TO IOS
*          MUST CONTAIN ZEROES IN THE THREE HIGH ORDER BYTES
PERRMASK EQU   X'20'          TO TEST FOR PERMANENT ERROR, TEST
PERRYES  EQU   8               IOBECBAD, B2. IF OFF (CONDITION CODE 8)
PERRNO   EQU   7               THERE IS A PERM ERR. ELSE B2 ON ( CD 7)
NORECFND EQU   X'08'                    NO RECORD FOUND SENSE BIT 19323
BYP      EQU   12                       RETURN TO IOS/BYPASS      15924
EXCP     EQU   8                        RETURN TO IOS/EXCP        15924
NORMAL   EQU   0                        RETURN TO IOS/NORMAL      15924
K0       EQU   0                                                 S20201
K1       EQU   1                                                 S20201
K2       EQU   2                                                 S20201
K3       EQU   3                                                 S20201
K4       EQU   4                                                 S20201
K5       EQU   5                                                 S20201
K6       EQU   6                                                 S20201
K7       EQU   7                                                 S20201
K10      EQU   10                                                S20201
NOT      EQU   255                                               S20201
OFLORCD  EQU   2                                                 S20201
DATACHK  EQU   8                                                 S20201
TWOBYTES EQU   16                                                S20201
BLOCKED  EQU   X'10'                                             S20201
NRF      EQU   8                                                 S20201
MIS      EQU   PERRMASK                 MASTER IDX SRCH MASK
CCOFF    EQU   X'BF'                    TURN OFF CC MASK
READKU   EQU   X'20'                    READ KEY FOR UPDATE     SA65011
         USING IHAIOB,R2                                         S20201
         USING IHADEB,R3                                         S20201
         USING IHADCB,R4
         USING IHAWKNCP,R10
         USING IHADECB,R11
         USING IHADCW,R12
         USING IGG019GO,R15             BASE
         USING IHACVTEX,R13             CVT
*         VECTOR TABLE.  BRANCH TO THE ROUTINE SELECTED IN THE
*           APPENDAGE ROUTINE, PART 1.
*        ROUTINE FOR          CODE      CEND COMPLETION OF
         LR    R15,R13
         B     APPM67CE       7         CP1 OR CP2 FOR WRITE KN
         LR    R15,R13
         B     APPN7B2        9,23      CP10AW
         LR    R15,R13
         B     APPN8A2        10,11     CP10BW
         LR    R15,R13
         B     APPM4A2        12        CP14W - SETUPS 1, 2, OR 5
         LR    R15,R13
         B     APPM5A2        13        CP14W - SETUPS 3, 4, OR 6
         LR    R15,R13
         B     APPM2B2        15        CP16 - SITUATION 2
         LR    R15,R13
         B     APPM3B2        16        CP16 - SITUATION 3
         LR    R15,R13
         B     APPN9A2        17,18,19  CP17W
         LR    R15,R13                                            17332
         B     APPAFB                   CP14 PART2                17332
*        ROUTINE FOR          CODE      ABEND COMPLETION OF
         LR    R15,R13
         B     APPM67AE       7         CP1 OR CP2, WRITE KN
         LR    R15,R13
         B     APPJ3B2        9,23      CP10AW
         LR    R15,R13
         B     APPJ4B2        10,11     CP10BW
         LR    R15,R13
         B     APPJ5B2        12        CP14W - SETUPS 1, 2, OR 5
         LR    R15,R13
         B     APPJ6B2        13        CP14W - SETUPS 3, 4, OR 6
         LR    R15,R13
         B     APPM2H2        15,16     CP16
         LR    R15,R13
         B     APPJ7B2        17,18,19  CP17W
         LR    R15,R13                                            17332
         B     APPAEB                   CP14 PART2                17332
         B     APPN7A3                  CONTINUE ROUTINES BEGUN IN
         B     APPN7B4                   PART 1
*        ROUTINE FOR          CODE      CEND COMPLETION OF
         LR    R15,R13
         B     APPABA2        0,1       CP4/CP5W
         LR    R15,R13
         B     APPADB2        2         CP7W
         LR    R15,R13
         B     APPAAB2        3         CP1 OR CP2, READ AND UPDATE
         LR    R15,R13
         B     APPACA2        5         CP6W
         DC    3H'0'                    INVALID  CODE 6          S20201
*        ROUTINE FOR          CODE      ABEND COMPLETION OF
         LR    R15,R13
         B     APPABD2        0,1       CP4/CP5W
         LR    R15,R13
         B     APPAIB2        2         CP7W
         LR    R15,R13
         B     APPAAA2        3         CP1 OR CP2, READ AND UPDATE
         LR    R15,R13
         B     APPACE2        5         CP6W
         DC    3H'0'                    INVALID CODE 6           S20201
*              BRANCH TO M6 OR M7
APPM67CE L     R13,DCBWKPT2             CHAN END OF CP1 OR CP2
         CLI   11(R13),X'01'            NLSD=1 MEANS NO MAST IX
         BE    APPM6B2                       CYLINDER - M6 (CP2)
         B     APPM7A2                       MASTER - M7 (CP1)
APPM67AE L     R13,DCBWKPT2             ABNORMAL END OF CP1 OR CP2
         CLI   11(R13),X'01'            NLSD=1 MEANS NO MAST IX
         BE    APPM6J4                       CYLINDER - M6 (CP2)
         B     APPM7J2                       MASTER - M7 (CP1)
         EJECT
*              CHART M7  CP1 FOR WRITE KN, CODE 7
*              CHANNEL END
APPM7A2  EQU   *
         LA    R13,C6+8
         CLR   R12,R13                  TEST STOP AT C6 - END OF SEARCH
         BE    APPM7B2                   OF MASTER INDEX. BRANCH IF YES
         LA    R13,C16+8
         CLR   R12,R13                  TEST STOP AT C16 - END OF SRCH
         BNE   APPN3B3                   OF CYLINDER INDEX. BRANCH NO
         TM    C18,X'20'                END OF CYLINDER SEARCH.
         BZ    APPM7C4                  TEST FOR DUMMY OR INACTIVE
         TM    C18,X'08'
         BZ    APPM7B3                  BRANCH IF INACTIVE OR DUMMY-END
         MVC   IOBDADAD,C17             DUMMY CHAINED.
         LA    R12,C10A                 RESTART CP TO SEARCH CYL S20201
         STH   R12,IOBSTART+1            INDEX ON CHAIN.
         SRL   R12,16
         STC   R12,IOBSTART
         B     APPRVXCP                 RETURN TO IOS TO EXCP
APPM7C4  MVC   IOBDADAD(7),C17          ACTIVE.
APPM7C4A MVI   IOBDADAD+7,X'00'         SET UP IOB FOR CP8
         MVI   IOBAPP,CODE8             M--R IS ADDR FOUND, R=0  S20201
         L     R10,DCBWKPT3             GET STARTING ADDR OF CP8 S20201
         ST    R10,CPSTART              STORE IN IOB AS CP START S20201
         ST    R10,IOBCCWAD             AND FOR USE AS BASE REG  S20201
         MVC   CB23+K3(K5),IOBDADAD+K3  CCHH OF FIRST TRACK, R=0 S20201
         OC    CB23+K5(K3),DCBFIRSH     SET HHR TO FIRSH         S20201
         MVI   IOBFLAG1,X'42'           APP CODE 8
         LA    R12,IOBDADAD+3           INITIALIZE CP8
         STH   R12,CB1+2                 CB1 SET TO SEEK CCHHR OF IOB
         SRL   R12,16
         STC   R12,CB1+1
         MVC   CB5+1(3),DECBKEY+1        CB5, CB15, CB19 ALL SET TO
         MVC   CB19+1(3),DECBKEY+1
APPM7G4  SR    R12,R12                  CHECK WITH DEB TO SEE IF NEXT
         IC    R12,IOBDADAD              ADDRESS ON SAME MODULE
         SLL   R12,4
         L     R12,32(R3,R12)
         LA    R12,0(R12)
         CLR   R12,R7
         BE    APPRVXCP                 IF SO, EXECUTE CP RETURN TO IOS
         MVI   IOBASYN,X'01'            IF NOT, SCHEDULE ASYNCHRONOUS
         B     APPN3B5                  ROUTINE TO EXCP
APPM7B2  EQU   *                        *
*
         TM    C6+K5,CC                 WAS C6 CHAINED
         BZ    APPAOS1                  NO, CONTINUE
         NI    C6+K5,CCOFF              TURN OFF INDICATOR
         OI    C6+K4,CC                 RECHAIN C6
         B     APPAOSJ3                 RESCHEDULE CP1
APPAOS1  EQU   *                        *
*
         TM    C9+K7,MIS                END MASTER INDEX SEARCH.
         BZ    APPM7C2                  TEST FOR DUMMY OR INACTIVE
APPM7B3  TM    C9+7,X'08'
         BO    APPM7E2                  BRANCH IF INACTIVE OR DUMMY-END
         IC    R12,C9+7                 DUMMY CHAINED.
         SLL   R12,29                   IF LEVEL INDEX NOT HIGHEST LVL,
         IC    R13,DCBNLEV               ERROR OF SOME SORT
         SLL   R13,29
         CLR   R12,R13
         BNE   APPN3B3
*                                       ALWAYS DO CP15
APPM7E3  MVI   IOBAPP,CODE14            TRK FULL--CP15 CODE      S20201
         MVC   IOBDADAD(3),DCBLPDA      SET UP IOB FOR CP15
         MVC   IOBDADAD+3(4),DCBLETI    APP CODE 14,MOVE FR LETI CCHH
         MVI   IOBDADAD+7,X'00'         ZERO R
         OC    IOBDADAD+6(1),DCBFIRSH+3
         XC    IOBDADAD+6(1),DCBFIRSH+3  INCREMENT TO A CYL BOUNDRY
         L     R10,DCBWKPT3                   HHR ZERO
         ST    R10,IOBCCWAD              START ADDR CI1
         LA    R12,CI1                   REL CCW 0  CP8
         ST    R12,IOBSTART-1           FLAGS INDICATE CC, NO DC
         MVI   IOBFLAG1,X'42'
         LA    R12,IOBDADAD+3           INITIALIZE CP15
         STH   R12,CI1+2                 CI1 POINTS TO IOB CCHHR
         SRL   R12,16
         STC   R12,CI1+1
         MVC   CI5+K2(K5),DCBLETI       COUNT ADDR               S20201
         MVC   CI5(K2),DCBLPDA+K1       MOVE IN BB FOR HEAD SEEK S20201
         B     APPM7G4                  BRANCH TO EXCP
APPM7C2  TM    C9+7,X'04'               ACTIVE ENTRY.
         BO    APPM7E2                  IF LVL 2 MAST IND JUST SRCHED
         OI    C6+4,X'40'                CC ON TO CHN FROM LVL 1 TO CYL
APPM7E2  MVC   IOBDADAD,C8+7            SEEK ADDR IN IOB IS FND ADDR
         NI    IOBFLAG1,X'7F'                B0 OFF NO DC
         OI    IOBFLAG1,X'40'                B1 ON YES DC
         B     APPRVXCP                 RETURN TO IOS TO EXCP
         EJECT
*              ABNORMAL END
APPM7J2  EQU   *                                                  19374
         TM    IOBSENSE+1,X'04'         FILE PROTECT?             19374
         BO    APPM7J3                  YES--BRANCH               19374
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR?          19374
         BC    PERRYES,APPN3B3          YES--BRANCH               19374
         BR    R14                      NO--RETRY CHANNEL        S20201
*                                       PROGRAM 1937             S20201
APPM7J3  LA    R13,C10+8                FILE PROTECT
         CLR   R12,R13
         BNE   APPN3B3                  BRANCH IF STOP NOT AT C10
*
APPAOSJ3 EQU   *                        *
*
         MVI   IOBFLAG1,X'42'           RESTORE IOBFLAGS & ERR CTR
         MVI   IOBFLAG2,X'00'             UNRELATED FLAG ON
         MVI   IOBCSW,X'00'               OTHERS OFF
         XC    IOBERRCT,IOBERRCT
         TM    C9+7,X'20'
         BO    APPM7B3                  BRANCH IF DUMMY OR INACTIVE
         LA    R12,C10A                 SET UP IOB TO RESTART    S20201
         ST    R12,IOBSTART-1
         B     APPM7E2                  BRANCH
         EJECT
*              M6 - CP2 FOR WRITE KN, CODE 7
*              CHANNEL END
APPM6B2  LA    R13,C35+8                B2   STOP AT C350
         CLR   R12,R13
         BNE   APPN3B3                       NO
         TM    C37,X'20'             C2      DUMMY OR INACTIVE{
         BZ    APPM6D2                       NO D2
         TM    C37,X'28'             C3      DUMMY CHAINED{
         BO    APPM6D3                       YES D3
         TM    DCBNLEV,X'01'         C4      NLEV=F{ F MUST BE 1
         BZ    APPN3B3                       NO
         B     APPM7E3
APPM6D2  MVC   IOBDADAD(7),C36       D2      C36 MBBCCHH
         B     APPM7C4A
APPM6D3  MVC   IOBDADAD,C36          D3      C36 MBBCCHHR
         B     APPRVXCP              E3      EXCP RETURN TO IOS
         SPACE 10
*              ABNORMAL END
APPM6J4  TM    IOBECBAD,PERRMASK     J4      PERMANENT ERROR TEST
         BC    PERRYES,APPN3B3          YES  B3
         BR    R14                      NORMAL RETURN TO IOS     S20201
         EJECT
*                        CHANNEL END APP CODES 9&23 (CP10A) CHART N7
APPN7B2  LA    R13,CB50C+8              TEST STOP AT CB50C
         CLR   R12,R13
         BNE   APPM1B3                  NO   M1B3
APPN7A3  L     R12,DCBWKPT2             YES  SET SAME MODULE SWITCH ON
         OI    DCWWKNI,X'20'
         IC    R9,IOBDADAD+7         B3      ADD 1 TO R IN IOB
         LA    R12,1(R0,R9)
         STC   R12,IOBDADAD+7
         STC   R12,DCBLPDA+7            PUT NEW R IN LPDA         11081
APPN7B4  CLI   DCBHIRSH,X'00'          ARE ALL TRACKS UNSHARED    11081
         BE    APPN7C4                  YES  C4
         IC    R9,DCBLPDA+6             IS THIS TRK SHARED
         IC    R12,DCBFIRSH+3           AND H  AND MASK TO GET
         NR    R9,R12                    REAL TRK NO.
         EX    R9,CLIFIRSH              EXECUTE THE COMPARE
         BNE   APPN7C4                  NO   C4
         CLC   DCBHIRSH,DCBLPDA+7       YES  HIRSH R = LPDA R {
         BNE   APPN7C2                  NO   C2
APPN7D3  IC    R9,IOBDADAD+6            IS THIS LAST
         IC    R12,DCBFIRSH+3
         NR    R12,R9                   DATA TRACK OF THIS
         EX    R12,CLILDT               CYLINDER
         BE    APPN7D4                  YES, GO FIND NEXT CYL
         LA    R9,1(0,R9)               NO,
         STC   R9,IOBDADAD+6            ADD 1 TO TRACK
         MVI   IOBDADAD+7,X'00'      E2 NO   SET IOB R TO ZERO
APPN7G2  TM    DCBST,X'02'           G2      LAST BLOCK FULL{
         BZ    APPN7C2                  NO   C2
         OI    DCBST,X'01'           G3 YES  SET LAST TRACK FULL SW ON
         B     APPN7C2
APPN7D4  SR    R12,R12
         IC    R12,IOBDADAD             FIND CURRENT
         SLL   R12,4                          EXTENT
         AR    R12,R3
         OC    IOBDADAD+6(1),DCBFIRSH+3      SET TRACK TO MAX
         CLC   IOBDADAD+6(1),DEBENDHH+1-IHADEB(R12)   LAST TRACK IN EXT
         BNL   APPCCH                         YES
         IC    R9,IOBDADAD+6
         LA    R9,1(0,R9)               ONLY THE 2301 WILL GO THIS PATH
         STC   R9,IOBDADAD+6              TO ADD 1  TO CLYINDER
         B     APPN7G4
APPCCH   CLC   IOBDADAD+3(3),DEBENDCC-IHADEB(R12) LAST CYL OF
         BL    APPN7E4A                      EXTENT (CCH)
         IC    R9,IOBDADAD
         LA    R9,1(0,R9)               PUT NEW M IO IOB
         STC   R9,IOBDADAD
         LA    R12,16(0,R12)            ACCESS NEXT EXTENT INTRY IN DEB
         MVC   IOBDADAD+1(6),DEBBINUM-IHADEB(R12)   MOVE NEW BBCCHH
         L     R13,DEBUCBAD-IHADEB(R0,R12) IS NEW CYL ON SAME MODULE
         LA    R13,0(R0,R13)
         CLR   R13,R7
         BE    APPN7G4                  YES  G4
         L     R12,DCBWKPT2          F5 NO   SET SAME MOD SW OFF
         NI    DCWWKNI,X'DF'
         B     APPN7G4                       G4
APPN7E4A MVI   IOBDADAD+K6,K0           SET TRACK TO ZERO        S20201
         IC    R13,IOBDADAD+K3          PICK UP C1 OF CC         S20201
         SLL   R13,8                    SHIFT                    S20201
         IC    R13,IOBDADAD+K4          PICK UP C2 OF CC         S20201
         LA    R13,K1(R0,R13)           ADD ONE TO CYL VALUE     S20201
         STC   R13,IOBDADAD+K4          RESTORE C2               S20201
         SRL   R13,8                    SHIFT                    S20201
         STC   R13,IOBDADAD+K3          RESTORE C1               S20201
APPN7G4  OC    IOBDADAD+6(1),DCBFIRSH+1
         IC    R9,DCBFIRSH+2
         BCTR  R9,0
         STC   R9,IOBDADAD+7
         B     APPN7G2
CLILDT   CLI   DCBLDT+1,0
CLIFIRSH CLI   DCBFIRSH+1,0
APPN7C4  CLC   DCBHIRPD,DCBLPDA+7    C4      HIRPD R = LPDA R {
         BE    APPN7D3                  YES  D3
APPN7C2  MVC   CB55(4),IOBDADAD+3    C2 NO
         IC    R9,IOBDADAD+7
         LA    R12,1(R0,R9)
         STC   R12,CB55+4
         MVI   IOBFLAG1,X'42'                SET B1 ON (CC)
         LA    R12,CB52                      SET IOB START TO CP10B
         ST    R12,IOBSTART-1
         LA    R13,IOBDADAD+3
         STH   R13,CB52+2
         SRL   R13,16
         STC   R13,CB52+1
         CLI   IOBAPP,CODE9             IS APPENDAGE CODE 9      S20201
         BE    APPN7E1                  YES  E1
         MVI   IOBAPP,CODE11            SET APPENDAGE CODE TO 11 S20201
APPN7F1  L     R12,DCBWKPT2          F1      SAME MODULE SW ON{
         TM    DCWWKNI,X'20'
         MVC   CB54A+1(3),CB52+1             WRITE CHECK SEARCH
         MVI   IOBCOUNT,X'0A'                SET COUNTER TO 10
         BO    APPRVXCP                 YES  EXCP RETURN TO IOS
         MVI   IOBASYN,X'01'            NO   SET ASYNC CODE 1
         B     APPM1B5                       SCHED ASYNC ROUTINE
APPN7E1  MVI   IOBAPP,CODE10            SET APPENDAGE CODE TO 10 S20201
         B     APPN7F1                       F1
         EJECT
*                        CHART J3, ABNORMAL END APP CODE9,23(CP10AW)
APPJ3B2  EQU   *
         TM    IOBECBAD,PERRMASK       PERMANENT ERROR
         BCR   PERRNO,R14                    NO,NORM IOS         S20201
         TM    IOBSENSE,X'08'      YES DATA CHECK
         BZ    APPM1B3
         LA    R13,CB50C+8              TEST STOP AT CB50C
         CLR   R12,R13
         BE    APPJ3E3             YES E3
         LA    R13,CB48+8                    STOP AT CB48?
         CLR   R12,R13
         BE    APPJ3E3                  YES  E3
         LA    R13,CB49+8                    STOP AT CB49?
         CLR   R12,R13
         BE    APPJ3E3                  YES  E3
         LA    R13,CB50+8                    STOP AT CB50?
         CLR   R12,R13
         BNE   APPM1B3                  NO   N3B3
APPJ3E3  OI    DECBEXC1,X'08'        E3 YES  IND UNCOR I/O ERROR
         B     APPM1B4                       N3B4
         EJECT
*                             CHANNEL END APP CODES 10&11(CP10B)CHARTN8
APPN8A2  LA    R13,CB54C+8              TEST STOP AT CB54C
         CLR   R12,R13
         BNE   APPM1B3                  NO   M1B3
         TM    CB53+4,X'20'             EOF FOR CP14?             13270
         BO    APPM426                                            17332
         L     R12,DCBNREC
         LA    R12,1(R0,R12)
         ST    R12,DCBNREC                   ADD 1 TO PRIME COUNT
         CLI   IOBAPP,CODE10            IS APPENDAGE CODE 10     S20201
         BE    APPM1B4                  YES  M1B4
         MVI   IOBAPP,CODE18            NO, SET APP CODE TO 18   S20201
         OI    IOBFLAG1,X'C0'                SET B0&B1 ON (CC&DC)
         LA    R12,CK1                       SET IOB START TO CP17
         ST    R12,IOBSTART-1
         MVC   IOBDADAD+3(5),DCBLETI         SET SEEK ADDR IN IOB
         MVC   IOBDADAD(3),DCBLPDA
         LA    R12,IOBDADAD+3        D2      INITIALIZE CP17
         ST    R12,CK1
         MVI   CK1,X'31'
         ST    R12,CK4
         MVI   CK4,X'31'
         ST    R12,CK7A                 PUT IOB ADDR IN CK7A
         MVI   CK7A,X'31'
         MVC   CK6+1(3),DECBKEY+1
         B     APPM7G4
         EJECT
*                        CHART J4, ABNORMAL END APP CODE 10,11(CP10BW)
APPJ4B2  EQU   *
         TM    IOBECBAD,PERRMASK       PERMANENT ERROR
         BCR   PERRNO,R14               NO,NORMAL IOS RETURN     S20201
         TM    IOBSENSE,X'08'      YES DATA CHECK
         BZ    APPM1B3
         LA    R13,CB54C+8              TEST STOP AT CB54C
         CLR   R12,R13
         BE    APPJ4E3             YES E3
         LA    R13,CB54+8                    STOP AT CB54?
         CLR   R12,R13
         BNE   APPM1B3                  NO   N3B3
APPJ4E3  OI    DECBEXC1,X'08'        E3 YES  IND UNCOR I/O ERROR
         B     APPM1B4                       N3B4
         EJECT
*                        CHANNEL END APP CODES 17,18,19 (CP17) CHART N9
APPN9A2  LA    R13,CK1                  CP START MAY HAVE BEEN   A34932
*                                       CHANGED                  A34932
         ST    R13,IOBSTART-1              FOR RETRY ON ERROR.   A34932
         LA    R13,CK7C+8           A2  STOP AT CK70             M3299
         CLR   R12,R13
         BNE   APPM1B3                  NO   M1B3
         CLI   CK9,X'00'             A3      TEST F IN CK9
         BE    APPN9B3                       NORMAL TRACK INDEX
         CLI   CK9,X'08'
         BE    APPN9B3                       SHARED
APPN9B2  EQU   *
         L     R12,DCBWKPT2
         CLI   IOBAPP,CODE17            IS APPENDAGE CODE 17     S20201
         BE    APPM1B4                  YES  M1B4
         CLI   IOBAPP,CODE18            IS APPENDAGE CODE 18     S20201
         BE    APPN9C2                  YES  C2
         CLC   DCWNLEVC,DCBNLEV      E2 NO   (19)NLEV CTR = DCBNLEV×
         BE    APPN9F1                  YES
         IC    R13,DCWNLEVC          G2 NO   ADD 1 TO NLEV COUNTER
         LA    R13,1(R0,R13)
         STC   R13,DCWNLEVC
         CLI   DCWNLEVC,X'02'        H2      NLEV COUNTER = 2{
         BE    APPN9E3                  YES  E3
         CLI   DCWNLEVC,X'03'           NO   NLEV COUNTER = 3{
         BE    APPN9F3                  YES  F3
         MVC   IOBDADAD+3(5),DCBLEMI3    G3  SET SEEK ADDR TO LEMI3
         CLI   DCBFTMI3+1,X'00'
         BE    *+14
         MVC   IOBDADAD(1),DCBFTMI3+1
         B     APPN9C5
         MVC   IOBDADAD(1),DCBFTMI3
         B     APPN9C5                       C5
APPN9E3  MVC   IOBDADAD+3(5),DCBLEMI1   E3  SET SEEK ADDR TO LEMI1
         CLI   DCBFTMI1+1,X'00'
         BE    *+14
         MVC   IOBDADAD(1),DCBFTMI1+1
         B     APPN9C5
         MVC   IOBDADAD(1),DCBFTMI1
         B     APPN9C5                       C5
APPN9F3  MVC   IOBDADAD+3(5),DCBLEMI2   F3  SET SEEK ADDR TO LEMI2
         CLI   DCBFTMI2+1,X'00'
         BE    *+14
         MVC   IOBDADAD(1),DCBFTMI2+1
         B     *+10
         MVC   IOBDADAD(1),DCBFTMI2
         XC    IOBDADAD+1(2),IOBDADAD+1
         B     APPN9C5                       C5
APPN9B3  CLI   CB26,X'10'            B3      TEST F IN CB26
         BNE   APPN9B2                       NOT OVERFLOW END
         MVC   IOBDADAD+3(5),CB24    C3      OVERFLOW ENTRY
         MVI   IOBCOUNT,X'0A'                SET COUNTER TO 10
         B     APPRVXCP              C4      EXCP RETURN TO IOS
APPN9F1  TM    DCWHIAV,X'40'                 HIGH IND IN CORE?
         BZ    APPN9H1                  NO   H1
         L     R12,DCWMSHIL          G1 YES  CHNG KEY OF LST CORE INDEX
         L     R11,DECBKEY
         IC    R9,DCBKEYLE
         LA    R13,0(R0,R9)
         BCTR  R13,R0
         EX    R13,APPN9G1A
APPN9H1  MVI   IOBASYN,X'08'         H1      SET ASYNC CODE 8
         B     APPM1B5                       SCHED ASYNC ROUTINE
APPN9C2  CLI   DCBNLEV,X'00'         C2      DCBNLEV = 0 {
         BE    APPM1B4                  YES  M1B4
         L     R12,DCBWKPT2             R12  ADDRESS DCB WA
         MVI   DCWNLEVC,X'01'        D2 NO   SET NLEV COUNTER TO 1
         MVC   IOBDADAD+3(5),DCBLECI     D3  SET SEEK ADDR TO LECI
         CLI   DCBFTCI+1,X'00'
         BE    *+14
         MVC   IOBDADAD(1),DCBFTCI+1
         B     APPMVI
         MVC   IOBDADAD(1),DCBFTCI
APPMVI   MVI   IOBAPP,CODE19            SET APPENDAGE CODE TO 19 S20201
         OI    IOBFLAG1,X'C0'                SET B0&B1 ON (CC&DC)
         LA    R12,CK1                       SET IOBSTART TO CK1
         ST    R12,IOBSTART-1
APPN9C5  MVI   IOBASYN,X'01'         C5      SET ASYNC CODE 1
         MVI   IOBCOUNT,X'0A'                SET COUNTER TO 10
         B     APPM1B5                       SCHED ASYNC ROUTINE
         EJECT
*                        CHART J7, ABNORMAL END APP CODE17,18,19(CP17W)
APPJ7B2  EQU   *
         TM    IOBECBAD,PERRMASK       PERMANENT ERROR
         BC    PERRYES,APPJ7B7                                   A34932
* THIS IS A FIRST ENTRY ON AN ERROR.
         LA    R11,CK4                  TEST FOR STOP PRIOR TO   A34932
*                                       CK4                      A34932
         CLR   R12,R11                  DID IT STOP BEFORE CK4?  A34932
         BNH   APPRVNOR                 BIF YES. RETRY FROM CK1. A34932
         ST    R11,IOBSTART-1           RETRY FROM CK4           A34932
         B     APPRVNOR                 RETURN TO IOS.           A34932
APPJ7B7  TM    IOBSENSE,X'08'           UNCORRECTABLE I/O ERROR  A34932
         BZ    APPM1B3
         LA    R13,CK7C+8
         CLR   R12,R13
         BNE   APPM1B3
APPJ7E3  OI    DECBEXC1,X'08'        E3 YES  IND UNCOR I/O ERROR
         B     APPM1B4                       N3B4
         SPACE 4
APPN9G1A MVC   0(0,R12),0(R11)
         SPACE 6
*        COMMON ROUTINE TO SCHEDULE ASYNCHRONOUS ROUTINE
APPM1B3  OI    DECBEXC1,X'04'        B3      SET DECB EXCD TO UNRCHABLE
APPM1B4  MVI   IOBASYN,X'08'         B4      SET ASYNC CODE TO 8
APPM1B5  B     BYP(R14)                 SCHEDULE ASYNCH ROUTINE  Y02072
         EJECT
*                        CHANNEL END APP CODE 15 (CP16) CHART M2
APPM2B2  LA    R13,CJ10+8               TEST STOP AT CJ10
         CLR   R12,R13
         BNE   APPM1B3                  NO   M1B3
         L     R12,DCBMSWA           C2 YES  OFLO END ENTRY?
         TM    8(R12),X'08'
         BZ    APPM2D3                  YES  D3
         MVC   IOBDADAD,0(R12)       E2 NO   SET SEEK ADDR FROM LINK
         B     APPM7G4
APPM2D3  MVC   CJ11,IOBDADAD         D3      PUT SEEK ADDR IN CJ11
         MVI   IOBASYN,X'0B'                 SET ASYNC CODE 11
         NI    IOBINDCT,X'DF'                SET B2-OFLO RCD FR AREA
         B     APPM1B5                       SCHED ASYNC ROUTINE
         SPACE 10
*                        ABNORMAL END APP CODES 15,16,(CP16) CHART M2
APPM2H2  TM    IOBECBAD,PERRMASK     H2      PERMANENT ERROR TEST
         BCR   PERRNO,R14              NO   CONTINUE, NORMAL IOS S20201
         B     APPM1B3                  YES  M1B3
         EJECT
*                        CHANNEL END APP CODE 16 (CP16) CHART M3
APPM3B2  LA    R13,CJ10+8               TEST STOP AT CJ10
         CLR   R12,R13
         BNE   APPM3B3                  NO - TRY CJ9             S20201
         MVC   CJ11,IOBDADAD                 SAVE SEEK ADDR IN CJ11
         L     R12,DCBMSWA           D2 YES  SET IOB SEEK FROM LINK
         MVC   IOBDADAD,0(R12)
         L     R12,DCBWKPT2          E2      SET FIRST TIME ONLY SW OFF
         NI    DCWWKNI,X'BF'
         B     APPM7G4                  GO TO TEST DEB
APPM3B3  LA    R13,CJ9+L'CJ9             STOP AT CJ9
         CLR   R12,R13                                           S20201
         BNE   APPM3B4                  NO - TRY CJ8             S20201
         MVC   CJ11,IOBDADAD                 SAVE SEEK ADDR IN CJ11
         TM    DCBOPTCD,X'02'        C3 YES  DELETE OPTION{
         BZ    APPM3C4                  NO   C4
         L     R12,DCBMSWA           D3 YES  RCD MARKED FOR DELETION{
         LA    R13,10(R0,R12)
         CLI   0(R13),X'FF'
         BNE   APPM3C4                  NO   C4
         MVI   IOBASYN,X'0E'         E3 YES  SET ASYNC CODE 14
APPM3E4  NI    IOBINDCT,X'DF'        E4      SET B2-OFLO RCD FR AREA
         B     APPM1B5               E5      SCHED ASYNC ROUTINE
APPM3C4  OI    DECBEXC1,X'01'        C4      SET EXCEP CODE TO IND DUPL
         MVI   IOBASYN,X'08'         D4      SET ASYNC CODE 8
         B     APPM1B5                       SCHED ASYNC ROUTINE
APPM3B4  LA    R13,CJ8+L'CJ8            SEE IF STOPPED AT CJ8
         CLR   R12,R13                                           S20201
         BNE   APPM1B3                  IF NOT, UNREACHABLE BLK  S20201
         L     R12,DCBWKPT2          C5 YES  FIRST TIME ONLY SW ON{
         TM    DCWWKNI,X'40'
         BO    APPM3B5                  YES  B5
         MVI   IOBASYN,X'0C'         D5 NO   SET ASYNC CODE 12
         B     APPM3E4                       E4
APPM3B5  MVI   IOBASYN,X'0D'         B5      SET ASYNC CODE 13
         MVC   IOBDADAD(7),CJ11         RESTORE TRK INDEX MBBCCHH
         B     APPM3E4
         EJECT
*                             CHANNEL END APP CODE 12 (CP14) CHART M4
APPM4A2  LA    R13,CH18C+8              DID CHAN PROG STOP AT CH18C
APPM4A21 EQU   *                                                  13270
         TM    CB53+4,X'20'             EOF WRITTEN               13270
         BZ    APPM4A22                 BRANCH IF NO              13270
         MVC   IOBDADAD,DCBLIOV         INITIALIZE IOBDADAD SEEK  13270
         MVC   DCBLIOV(3),CH23          RESET LIOV FROM           13270
         MVC   DCBLIOV+3(5),CH24        CP14                      13270
         CLR   R12,R13                                            13270
         BNE   APPM1B3                  B = UNREACHABLE           13270
         MVI   IOBFLAG1,X'42'          CC SET ON                  13270
         MVI   IOBAPP,CODE10            SET APPENDAGE CODE 10    S20201
         LA    R12,CB52                                           13270
         ST    R12,IOBSTART-1                                     13270
         LA    R13,IOBDADAD+3                                     13270
         STH   R13,CB52+2                                         13270
         SRL   R13,16                                             13270
         STC   R13,CB52+1                                         13270
         MVC   CB54A+1(3),CB52+1        WRITE CHECK SEARCH        13270
         SR    R13,R13                                            16384
         IC    R13,IOBDADAD             GET M                     16384
         SLL   R13,4                    TIMES 16                  16384
         LA    R13,32(R3,R13)           ALLOW FOR BASIC DEB TOO   16384
         L     R12,0(0,R13)             GET UCB POINTER           16384
         LA    R12,0(0,R12)             ZERO HIGH ORDER BYTE      16384
         CR    R7,R12                   SAME UNIT                 16384
         BE    APPRVXCP                 YES--EXCP RETURN          16384
         MVI   IOBASYN,X'01'            NO--SET ASYNCH CODE = 1   16384
         B     APPM1B5                  SCHEDULE ASYNCHRONOUS RTN 16384
APPM426  CLI   CK9+7,12                INSERT TO MIDDLE  OVFL CH  17332
         MVI   CB53+4,X'00'            RESET EOF FLAG             17332
         BE    APPM1B4                 IF YES POST COMPLETION     17332
APPM4A22 EQU   *                                                  13270
         CLR   R12,R13                                            13270
         BNE   APPM1B3                  B = UNREACHABLE           13270
         TM    CH8E+4,X'40'            TEST FOR UWA               17332
         BZ    APPM425                 IF NO-BRANCH               17332
         NI    CH8E+4,X'42'            RESET  FLAG                17332
         CLI   CK9+7,14                 ASYN CODE=14              17332
         BE    APPM1B4                  IF YES-POST COMPLETION    17332
         CLI   CK9+7,9                  ASYN CODE=9               17332
         BE    APPM4A23                 IF YES BRANCH             17332
         MVI   IOBAPP,X'04'             SET UP CP14 PART2         17332
         MVC   IOBSTART,CH21+4          TO UPDATE TRACK           17332
         MVC   IOBDADAD,CH14           RESTORE IOBSEEK            17332
         MVI   CH14,X'03'               SET NOP IN CH14           17332
         MVI   CH14+4,X'20'                                       17332
         MVI   CH14+7,X'01'                                       17332
         B     APPN9C5                                            17332
APPM4A23 MVI   IOBCOUNT,X'01'           ERROR COUNT=1             17332
         MVC   IOBDADAD(3),CB10+7       IOBSEEK ADDR              17332
         MVC   IOBDADAD+3(5),CB23+3                               17332
         IC    R12,IOBDADAD+7           REDUCE R BY 1             17332
         BCTR  R12,R0                                             17332
         STC   R12,IOBDADAD+7                                     17332
         TM    DCBRECFM,X'10'          TEST FOR BLOCKED           17332
         BO    APPM424                 IF YES-BRANCH              17332
         L     R12,CH6+4               RESTORE 10 BYTES           17332
         L     R13,DECBAREA            USED FOR LINK FIELD IN     17332
         MVC   0(10,R12),0(R13)        OVFL RECORD                17332
APPM424  MVC   IOBSTART,CD2+5           START OF CP12B            17332
         MVI   IOBAPP,X'15'             APP CODE 15               17332
         OI    IOBFLAG1,X'C0'                                     17332
         B     APPN9C5                                            17332
APPM425  L     R12,DCBWKPT2                                       17332
         TM    DCWWKNI,X'10'            IS  ADD-TO-END INDICATOR ON
         BZ    APPM1B4                  NO - SCHEDULE COMPLETION
APPM4C2  MVC   CK6+1(3),DECBKEY+1  IF SO, SET ADDR OF CK6 TO DECB KEY
         LA    R12,IOBDADAD+3           SET UP CP17
         STH   R12,CK1+2
         SRL   R12,16
         STC   R12,CK1+1
         MVC   CK4+1(3),CK1+1
         MVC   CK7A+1(3),CK1+1
         B     APPN9C2             BRANCH
         EJECT
*                        CHART J5, ABNORMAL END APP CODE 12 (CP14W)
APPJ5B2  EQU   *                                                  19374
         TM    IOBSENSE+1,X'04'         FILE PROTECT              15924
         BO    APPJ5I2                  YES, BRANCH               15924
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR?          19374
         BCR   PERRNO,R14               NO--RETRY CHAN PGM       S20201
*                                        1937                    S20201
         TM    IOBSENSE,X'08'      YES DATA CHECK
         BZ    APPM1B3
         LA    R13,CH3C+8               TEST STOP AT CH3C
         CLR   R12,R13
         BE    APPJ5D2                  YES  D2
         LA    R13,CH8C+8                    STOP AT CH8C?
         CLR   R12,R13
         BE    APPJ5D2                  YES  D2
         LA    R13,CH13C+8                   STOP AT CH13C?
         CLR   R12,R13
         BE    APPJ5D2                  YES  D2
         LA    R13,CH18C+8                   STOP AT CH18C?
         CLR   R12,R13
         BE    APPJ5D2             YES D2
         LA    R13,CH16+8                    STOP AT CH16?
         CLR   R12,R13
         BE    APPJ5E3                  YES  E3
         LA    R13,CH17+8                    STOP AT CH17?
         CLR   R12,R13
         BE    APPJ5E3                  YES  E3
         LA    R13,CH18+8                    STOP AT CH18?
         CLR   R12,R13
         BNE   APPM1B3                  NO   N3B3
APPJ5D2  EQU   *
APPJ5E3  OI    DECBEXC1,X'08'        E3 YES  IND UNCOR I/O ERROR
         B     APPM1B4                       N3B4
APPJ5I2  LA    R13,CH14+8               DID CHAN PROG STOP AT CH14
         L     R12,IOBCSW               CSW = STOP ADDRESS PLUS 8
         LA    R12,0(R0,R12)            CLEAR HIGH ORDER ZERO
          CLR   R12,R13                 BE MEANS YES, BNE MEANS NO
         BNE   APPM1B3                  NO - RECORD IS UNREACHABLE
         MVC   IOBDADAD,CH23            MOVE CH23 MBBCCHHR TO IOB
         MVC   IOBSTART,IOBCSW+1        RESUME PROCESSING AT CH15
         MVI   IOBASYN,X'01'
         B     APPM1B5                  EXCP IN ASYNCHRONOUS ROUTINE
         EJECT
*              CHART M5  COMPLETION OF CP14, CODE 13
*              CHANNEL END
APPM5A2  LA    R13,CH22C+8              DID CHAN PROG STOP AT CH22C
         B     APPM4A21
         EJECT
*                        CHART J6, ABNORMAL END APP CODE 13 (CP14W)
APPJ6B2  EQU   *                                                  19374
         TM    IOBSENSE+1,X'04'         FILE PROTECT?             15924
         BO    APPJ6I2                  YES, BRANCH               15924
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR?          19374
         BCR   PERRNO,R14               NO--RETRY CHAN PGM       S20201
*                                        1937                    S20201
         TM    IOBSENSE,X'08'      YES DATA CHECK
         BZ    APPM1B3
         LA    R13,CH3C+8               TEST STOP AT CH3C
         CLR   R12,R13
         BE    APPJ6D2                  YES  D2
         LA    R13,CH8C+8                    STOP AT CH8C?
         CLR   R12,R13
         BE    APPJ6D2                  YES  D2
         LA    R13,CH13C+8                   STOP AT CH13C?
         CLR   R12,R13
         BE    APPJ6D2                  YES  D2
         LA    R13,CH18C+8                   STOP AT CH18C?
         CLR   R12,R13
         BE    APPJ6D2                  YES  D2
         LA    R13,CH22C+8                   STOP AT CH22C?
         CLR   R12,R13
         BE    APPJ6D2             YES D2
         LA    R13,CH16+8                    STOP AT CH16?
         CLR   R12,R13
         BE    APPJ6E3                  YES  E3
         LA    R13,CH17+8                    STOP AT CH17?
         CLR   R12,R13
         BE    APPJ6E3                  YES  E3
         LA    R13,CH18+8                    STOP AT CH18?
         CLR   R12,R13
         BE    APPJ6E3                  YES  E3
         LA    R13,CH22+8                    STOP AT CH22?
         CLR   R12,R13
         BNE   APPM1B3                  NO   N3B3
APPJ6D2  EQU   *
APPJ6E3  OI    DECBEXC1,X'08'        E3 YES  IND UNCOR I/O ERROR
         B     APPM1B4                       N3B4
APPJ6I2  LA    R13,CH19+8               DID CHAN PROG STOP AT CH19
         L     R12,IOBCSW               CSW = STOP ADDRESS PLUS 8
         LA    R12,0(R0,R12)            CLEAR HIGH ORDER ZERO
          CLR   R12,R13                 BE MEANS YES, BNE MEANS NO
         BNE   APPJ5I2                  NO - BRANCH TO TEST FOR CH14
         MVC   IOBDADAD,CJ11            MOVE CJ11 MBBCCHHR TO IOB
         MVC   IOBSTART,IOBCSW+1        RESUME PROCESSING AT CH20
         MVI   IOBASYN,X'01'
         B     APPM1B5                  EXCP IN ASYNCHRONOUS ROUTINE
         EJECT
*           READ AND UPDATE APPENDAGE MODULES         *********
APPHCA14 DC    A(CA14-IHACP47+8)
APPHCA20 DC    A(CA20-IHACP47+8)        CA20 OFFSET              S20201
APPHCA23 DC    A(CA23-IHACP47+8)
APPHCA24 DC    A(CA24-IHACP47+8)
APPHCA31 DC    A(CA31-IHACP47+8)
APPCA31B DC    A(CA31B-IHACP47+8)       CA31B OFFSET             S20201
APPHCA36 DC    A(CA36-IHACP47+8)
APPHCA43 DC    A(CA43-IHACP47+8)
APHCA43C DC    A(CA43C-IHACP47+8)
APPHCA10 DC    A(CA10-IHACP47+8)        CA10 OFFSET
*
*        CHANNEL END APP CODE 3 (CP2) CHART AA
         USING CP2,R10
APPAAB2  L     R13,DCBWKPT2             R13  ADDR DCB WA
         USING IHADCW,R13
         CLI   DCWNLSD,X'01'            BRANCH IF NLSD NOT 1, I.E.
         BH    APPAEB2                   IF CP1 IS BEING USED
         LA    R13,C35+8                TEST STOP AT C35
         CLR   R12,R13
         BNE   APPAAB3                  NO   B3
         TM    C37,X'20'                YES  DUMMY OR INACTIVE TEST
         BO    APPAAC3                  YES  C3
         MVC   IOBDADAD,C36             NO   C36 TO IOBDADAD
         MVI   IOBASYN,X'03'         E2      ASYNC CD SET=3 (G CP1,2)
         B     APPABSAR
APPAAC3  TM    C37,X'08'                TEST DUMMY CHAINED
         BO    APPAAD3                  YES  D3
         IC    R12,C37                  NO   TEST NLEV PER F = DCBNLEV
         NI    C37,X'07'
         CLC   DCBNLEV,C37
         STC   R12,C37
         BNE   APPAAB3                  NO   B3
         OI    DECBEXC1,X'80'        C5 YES  DECB EXCEP SET=NO RCD FND
APPAAD4  MVI   IOBASYN,X'07'         D4      ASYNC CD SET=7 (NG CP1,2)
         B     APPABSAR                      SAR
APPAAD3  MVC   IOBDADAD,C36             C36 TO IOBDADAD
         B     APPRVXCP                      EXCP RETURN TO IOS
APPAAB3  OI    DECBEXC1,X'04'        B3      DECB EXCEP SET=UNREACHABLE
         B     APPAAD4                       D4
*
*        ABNORMAL END APP CODE 3 (CP2) CHART AA
*
APPAAA2  L     R13,DCBWKPT2            R13  ADDR DCB WA
         CLI   DCWNLSD,X'01'            BRANCH IF NLSD NOT 1, I.E.
         BNE   APPAEJ2                    IF CP1 IS BEING USED
APPAAA3  TM    IOBECBAD,PERRMASK        TEST PERM ERR
         BCR   1,R14                    NO, NORM RETURN TO IOS   S20201
*                                        1592                    S20201
         B     APPAAB3                  YES BRANCH TO INDICATE UNRCHBLE
         DROP  R13
*
*              CHART AE                 END CP1
*              CHANNEL END
         USING CP1,R10
APPAEB2  L     R12,IOBCSW               R12  ADDRESS OF FINAL CCW+8
         LA    R12,0(R12)                     WITH HIGH ORDER BYTE ZERO
         LA    R13,C6+8                 TEST FINAL CCW EQUAL C6
         CLR   R12,R13
         BE    APPAEC2                  BRANCH IF YES
         LA    R13,C16+8                TEST EQUAL C16
         CLR   R12,R13
         BE    APPAEC4                  BRANCH IF YES
         B     APPAAB3                  ERROR IF NO
*              INTERRUPT AT C6
APPAEC2  EQU   *                        *
*
         TM    C6+K5,CC                 WAS C6 CHAINED
         BZ    APPAOS8                  NO, CONTINUE
         NI    C6+K5,CCOFF              TURN OFF INDICATOR
         OI    C6+K4,CC                 RECHAIN C6
         CLI   C10,SEEKHH               C10 SET TO DO SEEK HEAD
         BNE   APPAOSJ4                 NO, FILE PROTECT
         LA    R13,C10A                 RESET IOBSTART TO
         ST    R13,IOBSTART-K1          RESTART AT C10A
         B     APPAEG2                  RESCHEDULE CP1
APPAOS8  EQU   *                        *
*
         TM    C9+K7,MIS                END MASTER INDEX SEARCH.
         BO    APPAEC3                  BRANCH IF YES
         TM    C9+7,X'04'               IF LEVEL OF INDEX IS 3
         BO    APPAEG2                   SET CC IN CP1 TO CHAIN TO
         OI    C6+4,X'40'                SEARCH CYLINDER INDEX
APPAEG2  MVC   IOBDADAD,C8+7            ADDR NEXT SRCH ADDR IN IOB
         B     APPRVXCP                 RETURN TO EXCP
*
*              DUMMY OR INACTIVE
*
APPAEC3  TM    C9+7,X'08'               TEST DUMMY ENTRY CHAINED
         BO    APPAEG2                  BRANCH IF YES
         IC    R12,C9+7                 COMPARE LEVEL OF INDEX READ TO
         SLL   R12,29                    DCB INDEX LEVEL
         IC    R13,DCBNLEV
         SLL   R13,29
         CLR   R12,R13
         BNE   APPAAB3                  BRANCH IF NOT EQUAL
         OI    DECBEXC1,X'80'           SET NO REC FND IN EXCEPTION CD
         B     APPAAD4
*
*              END OF CYLINDER SEARCH
*
APPAEC4  TM    C18,X'20'                TEST DUMMY OR INACTIVE
         BO    APPAEC5             BRANCH IF YES
         MVC   IOBDADAD,C17                  ADDR NEXT SRCH IN IOB
         MVI   IOBASYN,X'03'       SET ASYNC CODE
         B     APPABSAR                 SCHEDULE ASYNCH RTN
APPAEC5  TM    C18,X'08'                IF DUMMY ENTRY NOT CHAINED,
         BZ    APPAAB3                   BRANCH TO INDICATE UNRCHBLE
         MVC   IOBDADAD,C17             OTHERWISE, RESTART CHAN PROG
         LA    R12,C11                   WITH NEW SRCH ADDRESS
         STH   R12,IOBSTART+1
         SRL   R12,16
         STC   R12,IOBSTART
         B     APPRVXCP                 RETURN TO IOS
*
*              ABNORMAL END
*
APPAEJ2  TM    IOBSENSE+1,X'04'        FILE PROTECT               25000
         BZ    APPAAA3                  BRANCH IF NO.
         LA    R12,C10+8                TEST STOP AT C10
         L     R13,IOBCSW
         LA    R13,0(R13)
         CLR   R12,R13                  BRANCH TO INDICATE UNREACHABLE
         BNE   APPAAB3                   IF NO.
*
APPAOSJ4 EQU   *                        *
*
         TM    C9+7,X'20'              IF DUMMY OR INACTIVE       25000
*                                       RECORD                    25000
         BO    APPAEC3                   BRANCH TO SAME PROCEDURE AS
         LA    R12,C11                   CHANNEL END
         STH   R12,IOBSTART+1           OTHERWISE, RESTART CHAN PROG
         SRL   R12,16
         STC   R12,IOBSTART
         B     APPAEG2
*
*        CHANNEL END APP CODE 0,1 (CP4,5) CHART AB
         USING CP4,R10
APPABA2  EQU   *                                                 S20201
*
         L     R12,IOBCSW               CP STOP ADDR
         LA    R12,0(R12)               CLEAR HI BYTE
         SR    R12,R10                  OFFSET OF CCW
*
         CL    R12,APPHCA23             STOP AT CCW CA23        SA65011
         BNE   NOTCA23                  BRANCH IF NO            SA65011
         TM    DECBTYP2,READKU          READ K FOR UPDATE       SA65011
         BZ    NOTCA23                  BRANCH IF NO            SA65011
         CLI   CA24,RDSECT              CA24 READ SECTOR CCW    SA65011
         BE    APPABF3                  BR IF YES - SET UNREACH SA65011
*                                       BLK, ASYN CODE OF 4     SA65011
NOTCA23  EQU   *                        CONTINUE - CP OK        SA65011
         CL    R12,APPHCA10             STOP AT CA10
         BNE   APPAOSA2                 NO
         TM    CA10+K5,CC               WAS CA10 COMMAND CHAINED
         BZ    APPAOSA2                 NO
         OI    CA10+K4,CC               TURN CC BACK ON
         NI    CA10+K5,CCOFF            TURN INDICATOR OFF
         CLI   CA14,SEEKHH              CA14 SEEK HEAD
         BNE   APPAOS21                 NO, FILE PROTECT
         MVC   IOBDADAD,CA12+K7         RESET IOBSEEK
         LA    R12,CA16A                RESET IOBSTART TO
         ST    R12,IOBSTART-K1          START AT CA16A
         B     APPRVXCP                 GO DO AN EXCP
APPAOSA2 EQU   *                        *
*
         NI    DECBEXC1,NOT-OFLORCD     DECB EXECP CD SET NOT    S20201
*                                       OFLO                     S20201
         MVC   IOBDADAD+K5(K3),CA25+K5                           S20201
APPABB3  MVI   IOBASYN,X'00'         B3      ASYNC CD SET=0 (G CP4,5,6)
APPABSAR B     BYP(R14)                 SCHEDULE ASYNCH ROUTINE  Y02072
         SPACE 2
*        ABNORMAL END APP CODE 0,1 (CP4,5) CHART AB
*
APPABD2  EQU   *                                                  19374
         TM    IOBSENSE+1,X'04'         FILE PROTECT?             19374
         BO    APPABD21                 YES--BRANCH               19374
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR?          19374
         BC    PERRYES,APPABD3          YES--INDICATE PERMANENT   19374
         TM    IOBSENSE,DATACHK         WAS ABEND DUE TO DATA    S20201
*                                       CHECK                    S20201
         BCR   ZERO,R14                 NO-NORMAL RETURN TO IOS  S20201
ZERO     EQU   8                        TM COND CODE FOR ==ZERO  S20201
         LA    R13,CA24C+L'CA24C        TEST STOP AT 24C
         CLR   R12,R13                                           S20201
         BCR   NOTEQ,R14                 NO-NORMAL RETURN TO IOS S20201
NOTEQ    EQU   7                        COMPARE COND CODE FOR    S20201
*                                       NOT EQUA                 S20201
         LA    R13,CA24D                 START ADDRESS = CA24D   S20201
         STH   R13,IOBSTART+K1                                   S20201
         SRL   R13,TWOBYTES                                      S20201
         STC   R13,IOBSTART                                      S20201
         MVC   IOBDADAD+3(5),CA25+3      RESET IOBSEEK          SA62417
         BR    R14                      NORMAL RETURN TO IOS     S20201
APPABD21 EQU   *                                                 S20201
*                                        1937                    S20201
         SR    R12,R10                  TEST STOP AT CA14
         CL    R12,APPHCA14
         BNE   APPABF3                  NO   F3
*
APPAOS21 EQU   *                        *
*
         TM    CA13+7,X'20'        F2 YES  DUMMY OR INAC TEST
         BO    APPABF1                  YES  F1
         MVC   IOBDADAD,CA12+7     G2 NO   CA12+7 TO IOBDADAD
         MVC   CA34+7(8),CA12+7         CA12+7 TO CA34+7
         LA    R12,IOBDADAD+3      G1   ADIOBDADAD(CCHH)TO CA26
         ST    R12,CA26
         MVI   CA26,X'31'
         MVC   CA28+1(3),DECBKEY+1 H1   DECBKEY+1 TO CA28 AD
         CLI   IOBAPP,CODE0             TEST FOR A READ          S20201
         BE    APPABJ2                  READ J2
         LA    R12,CA40            K1 WRITE CA30 TIC TO CA40
         STH   R12,CA30+2
         SRL   R12,16
         STC   R12,CA30+1
         L     R12,DECBAREA        K2   AREA+6 TO CA40 AD
         LA    R12,6(R12)
         ST    R12,CA40
         MVI   CA40,X'06'
         MVC   CA43+1(3),CA40+1    K3   AREA+6 TO CA43 AD
         LA    R13,CA43+6               LENGTH+10 TO CA43 CNT
APPABK2  MVI   CA28,SKEQ                SCH EQ FOR WRITE         S20201
APPABK4  TM    DECBTYP1,X'02'                DECBLGTH SPECIFIED{
         BZ    APPABK4A                 YES  K4A
         LH    R12,DCBLRECL             NO   USE DCBLRECL
         B     APPABK4B
APPABK4A LH    R12,DECBLGTH             DECB LENGTH OVERRIDING
APPABK4B LA    R12,10(R12)
         STH   R12,0(R13)
         MVC   CA43C+6(2),CA43+6        SET COUNT IN CA43C
APPABK5  MVI   IOBAPP,CODE5             SET CODE 5 FOR CP6       S20201
         LA    R12,CA26            J5   AD CA26 TO IOBSTART
         IC    R13,IOBSIOCC
         ST    R12,IOBSIOCC
         STC   R13,IOBSIOCC
         MVI   IOBFLAG1,X'42'        H5 RESTORE IOB FLAGS AND ERR CTR
         MVI   IOBFLAG2,X'00'            CC AND UNRELATED FLAGS ON
         MVI   IOBCSW,X'00'              OTHERS OFF
         XC    IOBERRCT,IOBERRCT
         MVI   IOBCOUNT,X'0A'           INITIALIZE ERR CTR FOR 10 TRIES
         SR    R12,R12             G5   TEST OFLO SAME VOLUMN
         IC    R12,IOBDADAD
         SLL   R12,4
         L     R12,DEBUCBAD(R12)
         LA    R7,0(R7)
         LA    R12,0(R12)
         CLR   R7,R12
         BE    APPRVXCP                 IF SAME, EXCP RET TO IOS
         MVI   IOBASYN,X'01'         G4 NO   ASYNC CD SET=1 (EXCP)
         B     APPABSAR                      SAR
APPABJ2  LA    R12,CA31              J2 CA30 TIC TO CA31
         STH   R12,CA30+2
         SRL   R12,16
         STC   R12,CA30+1
         L     R12,DECBAREA                  AREA+6 TO CA31 ADDRESS
         LA    R12,6(R12)
         ST    R12,CA31
         MVI   CA31,X'06'
         LA    R13,CA31+6                    LENGTH+10 TO CA31 COUNT
         SR    R12,R12                                           S20201
         AH    R12,DCBRKP               TEST IF RKP=0            S20201
         BNZ   APPABJ4                  BIF RKP NOT =0           S20201
         TM    DCBRECFM,BLOCKED         RECFM=BLOCKED            S20201
         BZ    APPABK2                  BIF NOT BLOCKED          S20201
APPABJ4  MVI   CA28,SKEQHI              SCH EQ+HI FOR (READ AND  S20201
*                                       RKP NE                   S20201
*                                       0) OR (READ AND RKP=0 AND BLKD)
         B     APPABK4                       K4
APPABF1  CLI   DCBNLEV,X'00'         F1      DCBNLEV=0{
         BE    APPABD5                  YES  D5
APPABF3  OI    DECBEXC1,X'04'        F3 NO   DECB EXCEP SET=UNREACHABLE
APPABG3  MVI   IOBASYN,X'04'         G3      ASYNC CD SET=4(NG CP4,5,6)
         B     APPABSAR                      SAR
APPABD3  EQU   *
         L     R12,IOBCSW                                        S20201
         LA    R12,0(R12)
         SR    R12,R10
         TM    SENSE1,NRF               NO RECORD FOUND          S20201
*                                        1607                    S20201
         BZ    APPABB1                  NO--BRANCH               S20201
*                                        1607                    S20201
         CL    R12,APPHCA20             STOP AT CA20 TEST        S20201
         BNE   APPAHH2                  NO   H2                  S20201
         CLI   CA21,X'69'          D4,C4 READ FORMAT BLOCKED TEST
         BE    APPABF3                  YES  F3
APPABD5  OI    DECBEXC1,X'80'        D5 NO   DECB EXCEP SET=NO RCD FND
         B     APPABG3                       G3
APPABB1  CL    R12,APPHCA23             B1   STOP AT CA23        S20201
         BNE   APPAHH2                  NO   H2                  S20201
APPABD1  OI    DECBEXC1,X'08'        D1 YES  DECB EXCEP SET=UNCOR IO ER
         B     APPABG3                       G3
*
*        CHANNEL END APP CODE 5 (CP6) CHART AC
         USING CP4,R10
APPACA2  SR    R12,R10                  TEST STOP AT CA31
         CL    R12,APPHCA31             NON-RPS HALT.            S20201
         BE    APPACA6                  BIF STOP ON CA31         S20201
         CL    R12,APPCA31B             RPS HALT.                S20201
         BNE   APPACA3                  BIF NO HALT HERE.        S20201
         B     APPACA6A                 COMPARE KEYS            SA65011
*
APPACA6  TM    DECBTYP2,READKU          READ K FOR UPDATE       SA65011
         BZ    APPACA6A                 BRANCH IF NO            SA65011
         TM    CA31+4,CC                CA31 COMMAND CHAINED    SA65011
         BO    APPABF3                  BR IF YES - SET UNREACH SA65011
*                                       BLK, ASYN CODE OF 4     SA65011
APPACA6A CLI   CA28,SKEQHI              SEARCH HI USED.         SA65011
         BNE   APPACA8                  BIF NOT SCH HI           S20201
*
*        COMPARE KEYS. IF THE RECORD'S KEY IS HIGHER, TERMINATE THE
*        SEARCH OF THE OVFLO CHAIN.
*
         IC    R9,CA28+K7                GET KEY LENGTH          S20201
         SH    R9,HWDONE                EXECUTED LENGTH          S20201
         L     R12,CA28                 GET KEY ADDR             S20201
         L     R13,CA31                 GET DATA ADDR            S20201
         AH    R13,DCBRKP               FIND THE KEY IN          S20201
         LA    R13,K10(R13)             THE RECORD               S20201
         EX    R9,COMPARE               CHECK FOR MATCH          S20201
         BNE   APPACG3                  BIF NO MATCH             S20201
APPACA8  EQU   *                                                 S20201
         CLC   CA34+7(8),CA12+7         FIRST RECORD IN CHAIN   OY00310
         BE    APPACB3                  YES  B3
         L     R12,DCBRORG3             NO   ADD 1 TO DCBRORG3
         LA    R12,1(R12)
         ST    R12,DCBRORG3
         TM    DECBTYP2,X'20'           B2  TEST READ FOR UPDATE
         BZ    APPACB3                  NO   B3
         MVC   IOBDADAD,CA34+7          YES  IOBDADAD SET FOR WRITEBACK
APPACB3  OI    DECBEXC1,X'02'        B3      DECB EXCEP SET=OFLO RCD
         MVI   IOBASYN,X'00'         B4      ASYNC CD SET=0 (G CP4,5,6)
         B     APPABSAR                      SAR
APPACA3  CL    R12,APHCA43C          A3 TEST - STOP AT CA43C
         BNE   APPACF4                  NO   F4
         B     APPACB3                  YES  B3
*
*        ABNORMAL END APP CODE 5 (CP6) CHART AC
*
APPACE2  EQU   *                                                  19374
         TM    IOBSENSE+1,X'04'         FILE PROTECT?             19374
         BO    APPACE21                 YES--BRANCH               19374
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR?          19374
         BC    PERRYES,APPAGH3          YES--BRANCH               19374
         B     APPAGG2                  GO SEE IF DATA CHECK      24075
APPACE21 EQU   *                                                  19374
         SR    R12,R10                  TEST STOP AT CA36
         CL    R12,APPHCA36
         BNE   APPACF4                  NO   F4
*
         CLI   CA36,SEEKHH              CA36 SET TO SEEK HEAD
         BNE   APPAOSE2                 NO
         MVC   IOBDADAD,CA34+K7         RESET IOBSEEK
         B     APPRVXCP                 GO DO AN EXCP
APPAOSE2 EQU   *                        *
*
         TM    CA35+7,X'08'        G2 YES  END OR CHAINED TEST
         BZ    APPACG3                  END  G3
         MVI   IOBASYN,X'01'         J2      ASYNC CD SET=1 (EXCP)
         MVI   IOBFLAG1,X'42'        J3 RESTORE IOB FLAGS AND ERR CTR
         MVI   IOBFLAG2,X'00'            CC AND UNRELATED FLAGS ON
         MVI   IOBCSW,X'00'              OTHERS OFF
         XC    IOBERRCT,IOBERRCT
         MVC   IOBDADAD,CA34+7          SET SEEK SEARCH ADDRESS
         B     APPABSAR                      SAR
APPACE3  EQU   *
         L     R12,IOBCSW          E4 YES  TEST STOP AT CA31 OR CA43
         LA    R12,0(R12)
         SR    R12,R10
         CL    R12,APPHCA31
         BE    APPACE5                  YES  E5
         CL    R12,APPHCA43
         BE    APPACE5                  YES  E5
APPACF4  OI    DECBEXC1,X'04'        F4 NO   DECB EXCEP SET=UNREACHABLE
APPACG4  MVI   IOBASYN,X'04'         G4      ASYNC CD SET=4(NG CP4,5,6)
         B     APPABSAR                      SAR
APPACG3  OI    DECBEXC1,X'80'        G3      DECB EXCEP SET=NO RCD FND
         B     APPACG4                       G4
APPACE5  TM    IOBSENSE,X'08'        E5      DATA CHK{
         BZ    APPACF4                  NO   F4
APPACF5  OI    DECBEXC1,X'08'        F5 YES  DECB EXCP SET UNCOR IO ERR
         B     APPACG4                       G4
*
*              CHART AD       CHANNEL END + NORMAL END CP7
*              CHANNEL END
*
         USING CP7,R10
APPADB2  LA    R13,CA46C+8              TEST STOP AT CA46C
         CLR   R12,R13                  TEST STOP AT CA46C
         BNE   APPADB3
         MVI   IOBASYN,X'02'            IF SO, SET ASYN CODE AND BRANCH
         B     APPABSAR                 SCHEDULE ASYN ROUTINE
APPADB3  OI    DECBEXC1,X'04'           IF NOT, INDICATE UNREACHABLE IN
APPADB4  MVI   IOBASYN,X'06'             DECB AND SET ASYNCH CODE
         B     APPABSAR                 SCHEDULE ASYNCH ROUTINE
*
*    CHART AI ABNORMAL END, CODE 2  , CP7 , WRITE CHECK
*
APPAIB2  EQU   *
         TM    IOBECBAD,PERRMASK        WAS ABEND DUE TO PERM ERROR
         BCR   1,R14                    NO, NORM RETURN TO IOS   S20201
*                                        1592                    S20201
         L     R13,IOBCSW               ADDRESS+8 OF CCW WHERE    19323
         LA    R13,0(R13)                 CP STOPPED              19323
         TM    IOBSENSE,X'08'           WAS ABEND DUE TO DATA CHECK
         BZ    APPADG4                  NO- TEST FOR RETRY        19323
         LA    R12,CA46+8               IF STOP AT CA46 AND
         CLR   R13,R12                   GO TO APPADB3
         BE    APPADG3                  YES - I/O ERROR
         LA    R12,CA46C+8
         CLR   R13,R12                  CHECK STOP AT 46C
         BNE   APPADB3                  NO-INDICATE UNREACHABLE
APPADG3  OI    DECBEXC1,X'08'           DATA CHECK, SET IO UNCORRTABLE
         B     APPADB4                   ERROR BIT IN DECB
APPADG4  EQU   *                                                  19323
         TM    IOBSENSE+1,NORECFND      NO RECORD FOUND           19323
         BZ    APPADB3                 FALSE-INDICATE UNREACHABLE 19323
         LA    R12,CA46A+8                                        19323
         CLR   R13,R12                  STOP AT CA46A             19323
         BNE   APPADB3                  NO- INDICATE UNREACHABLE  19323
         CLI   IOBCOUNT,X'0A'           FIRST TIME THROUGH        19323
         BL    APPADB3                  NO- INDICATE UNREACHABLE  19323
         MVI   IOBCOUNT,X'00'           INDICATE RETRY            19323
         B     APPRVXCP                 RETRY CHANNEL PROGRAM     19323
*
*              CHART AG  ABNORMAL END, NOT FILE PROT CHK
*
         USING  CP4,R10                                          S20201
APPAGG2  EQU   *
         TM    IOBSENSE,X'08'           WAS ABEND DUE TO DATA CHECK
         BCR   8,R14                    NO - NORMAL RETURN TO    S20201
*                                       IOS                      S20201
         LA    R12,CA43C+8
         L     R13,IOBCSW
         LA    R13,0(R13)
         CLR   R12,R13
         BCR   7,R14                    NO-NORMAL RETURN TO IOS  S20201
         MVC   IOBDADAD,CA34+7
         LA    R12,CA41
         STH   R12,IOBSTART+1
         SRL   R12,16
         STC   R12,IOBSTART
         BR    R14                      TAKE NORMAL RETURN TO    S20201
*                                       IOS                      S20201
APPAGH3  EQU   *
         TM    IOBSENSE,X'08'           WAS ABEND DUE TO DATA CHECK
         BZ    APPACE3                  NO-BRANCH
         LA    R13,CA43C+8              TEST STOP AT 43C
         CLR   R12,R13
         BNE   APPACE3                  BRANCH IF NO
         B     APPACF5                   UNCORRECTABLE I/O ERROR
APPAHH2  EQU   *
         AR    R12,R10                  GET STOPPING ADDR        S20201
         TM    IOBSENSE,X'08'           WAS ABEND DUE TO DATA CHECK
         BZ    APPABF3                  NO-BRANCH
         LA    R13,CA24C+8              TEST STOP AT 24C
         CLR   R12,R13
         BE    APPABD1                  YES- I/O ERROR
         LA    R13,CA24F+8              IF PERMANENT, WAS STOP AT
         CLR   R12,R13                   CA24F
         BNE   APPABF3                  BRANCH IF NO
         B     APPABD1                   UNCORRECTABLE I/O ERROR
APPRVXCP SR    R12,R12
         IC    R12,IOBDADAD             GET BB
         SLL   R12,4
         LA    R12,32(R12,R3)
         MVC   IOBDADAD+1(2),4(R12)
         LA    R14,EXCP(R14)            SETUP FOR EXCP IOS RETURN 15924
         XC    IOBFLAG2(3),IOBFLAG2     CLEAR FLAG2,SENSE BYTES   15924
         XC    IOBCSW(9),IOBCSW         CLEAR FLAG3,CSW,AND SIOCC 15924
         XC    IOBERRCT(2),IOBERRCT     CLEAR ERROR COUNT         15924
         NI    IOBFLAG1,X'C2'           RESET FLAG1               15924
APPRVNOR BR    R14                      RETURN TO IOS             15924
*
         EJECT                                                  , 17332
*              NORMAL END APPENDAGE CODE 4
*              UPDATE TRACK INDICES WITH 2ND PART CP14
*
*
         USING IHAWKNCP,R10                                      S20201
APPAFB   LA    R13,CH14+8               DID CP END AT CH14        17332
         CLR   R12,R13                                            17332
         BNE   APPN3B3                  NO-RECORD IS UNREACHABLE  17332
         MVC   IOBASYN,CK9+7                                      17332
         CLI   IOBASYN,10          ADD TO END                     17332
         BE    APPM4C2             IF YES-GO UPDATE MSTR INDICES  17332
         CLI   IOBASYN,11          ADD TO END                     17332
         BE    APPM4C2             IF YES-GO UPDATE MSTR INDICES  17332
         B     APPM1B4             POST COMPLETION                17332
         SPACE 10
*
*              ABNORMAL END  APPENDAGE CODE 4
*              UPDATE  TRACK INDICES
*
APPAEB   TM    IOBECBAD,PERRMASK        PERM ERROR                17332
         BCR   PERRNO,R14               IF NO-NORMAL RET TO IOS  S20201
*                                        1733                    S20201
         B     APPM1B3                  POST UNREACHABLE BLOCK    17332
COMPARE  CLC   K0(K1,R12),K0(R13)       EXECUTED INSTRUCTION     S20201
         EJECT
HWDONE   DC    H'1'                     CONSTANT FOR EXECUTE     S20201
APPN3B3  EQU   APPM1B3        LABELS USED IN COMMON ROUTINE TO
APPN3B5  EQU   APPM1B5         SCHEDULE ASYNCHRONOUS ROUTINE
CODE0    EQU   0                        CHAN PGM 4-5 APPEND CODE S20201
CODE5    EQU   5                        CHAN PGM 6 APPEND CODE   S20201
CODE6    EQU   6                        CP5W APPEND CODE FOR WC  S20201
CODE8    EQU   8                        CHAN PGM 8 APP CODE      S20201
CODE9    EQU   9                        CHAN PGM 10A APP CODE    S20201
CODE10   EQU   10                       CHAN PGM 10B APP CODE    S20201
CODE11   EQU   11                       CHAN PGM 10B APP CODE    S20201
CODE14   EQU   14                       CHAN PGM 15 APP CODE     S20201
CODE17   EQU   17                       CHAN PGM 17 APP CODE     S20201
CODE18   EQU   18                       CHAN PGM 17 APP CODE     S20201
CODE19   EQU   19                       CHAN PGM 17 APP CODE     S20201
*
PATCH    DC    XL((*-IGG019GO)/20)'00'  ZEROED PATCH AREA        Y02072
*
IHACVTEX DSECT   COMMUNICATION VECTOR TABLE
         DS    A
CVTEXEF  DS    A                        USED WITH R13 TO EFFECT EXIT
*                        ADDRESS OF COMMUNICATION VECTOR TABLE
CVTPTR   EQU   16
         EJECT
*        READ K, READ KU, AND WRITE K CHANNEL PGMS(WRITE CHK)
IHACP47  IGGCP47 OPTCD=W                                         S20201
         SPACE 2
*        WRITE KEY NEW CHANNEL PROGRAM REFERENCES (WRITE CHECK)
IHAWKNCP IGGWKNCP  OPTCD=W                                       S20201
         SPACE 2
         IGGCP12C  OPTCD=W                                       S20201
         SPACE 2
         IGGCP12A
         EJECT
*              DATA EVENT CONTROL BLOCK
IHADECB  DSECT
         DS    0F
DECBECB  DS    CL4                      EVENT CONTROL BLOCK (ECB)
DECBTYP1 DS    BL1                      TYPE B6 - 1 IF LENGTH IS S
*                                            B7 - 1 IF AREA IS S
DECBTYP2 DS    BL1                           B0 - 1 IF READ K
*                                            B1 - 1 IF READ KX
*                                            B2 - 1 IF READ KU
*                                            B4 - 1 IF WRITE K
*                                            B5 - 1 IF WRITE KN
DECBLGTH DS    CL2                      LENGTH OF BLOCK
DECBDCBA DS    A                        POINTER TO DCB
DECBAREA DS    A                        ADDRESS OF AREA
DECBLOGR DS    A                        POINTER TO LOGICAL RECORD
DECBKEY  DS    A                        POINTER TO KEY
DECBEXC1 DS    BL1                      EXCPTN CD B0-RECORD NOT FOUND
*                                                 B1-RECORD LGTH CHK
*                                                 B2-NO SPACE
*                                                 B3-INVALID REQUEST
*                                                 B4-UNCORRECTABLE IO
*                                                 B5-UNREACHABLE BLOCK
*                                                 B6-OVERFLOW RECORD
*                                                 B7-DUPLICATE
DECBEXC2 DS    BL1                                B7-READ KU
         EJECT
         DCBD  DSORG=(IS)
         EJECT
IHAIOB   IGGIOBD
CPSTART  EQU   IOBSTART-1                                        S20201
SENSE1   EQU   IOBSENSE+1                                        S20201
         SPACE 3
IHADCW   IGGBISAM
         SPACE 2
IHADEB   IGGDEBD
         END
