         TITLE 'IGG019J0 - BISAM COMBINED PRIV MACRO TIME, NLSD=0'
IGG019J0 CSECT
*        RELEASE OS/VS2-02 DELETIONS                             Y02072
*        RELEASE 19 DELETIONS
*1078061200,139100                                               A25517
*1078022004-022088,022100-022136,022137,022140-022188,022300-    A26738
*1078022320,023200-023800,024600-026400,028000,029000,029200,    A26738
*1078029400                                                      A26738
*        RELEASE 20 DELETIONS
*0712022200,055600,058400,058600,058800,059000,059200,059400,    S20201
*0712059600-060600,068600,069400,088200,088400,088600,088800,    S20201
*0712089000,090400,091000-094800,095000-098800,099000-115200,    S20201
*0712120600,120800,121000-121800,122000-122800,123000-130800,    S20201
*0712131000-134800,135000-140800,141000-146600                   S20201
*0712022340,022350,022441                                        A33986
*        RELEASE 21 DELETIONS
*1024                                                            A33533
*                                                               SA67174
*
*STATUS CHANGE LEVEL 009
* FUNCTION/OPERATION' PRIVILEGED MACRO-TIME ROUTINE FOR BISAM WHEN
*        READ AND UPDATE IS USED WITH    WRITE KN AND WHEN
*        ONLY THE TRACK INDEX IS SEARCHED ON DEVICE.
*        1. IF THE MACRO IS NOT A WRITE K FOLLOWING FROM A READ KU,
*            GETMAIN FOR AN IOB AND EXTENTION AND INITIALIZE THE IOB.
*            IF  A  WRITE K FOLLOWING FROM READ KU, FIND THE IOB
*            ON THE UPDATE QUEUE AND REMOVE IF FROM THE QUEUE.
*        2. IF THE MACRO IS WRITE KN, DETERMINE IF ANY OTHER MACRO IS
*            BEING PROCESSED FOR THIS DATA SET.  IF SO, ADD THE IOB TO
*            THE UNSCHEDULED QUEUE AND SET THE UNSCHEDULED SWITCH ON.
*            IF NOT, SEARCH THE HIGHEST LEVEL INDEX IN CORE IF THIS
*            IS NECESSARY, INITIALIZE THE FIRST WKN CHANNEL PROGRAM,
*            AND SET THE START ADDRESS AND SEEK-SEARCH ADDRESS IN THE
*            IOB.
*        3. IF THE MACRO IS READ AND UPDATE MACRO, THEN
*            A. IF IT IS A WRITE K FOLLOWING FROM A READ KU OR IF
*                WRITE KN IS NOT IN PROCESS OR PENDING, THEN
*           DETERMINE IF THE FIRST CHANNEL PROGRAM NEEDED FOR THIS
*            MACRO IS AVAILABLE. IF SO, REMOVE THE CHANNEL PROGRAM FROM
*            ITS QUEUE, SEARCH THE HIGHEST LEVEL INDEX IN CORE
*            IF THIS IS NECESSARY,     INITIALIZE THE CHANNEL PROGRAM,
*            AND SET THE START ADDRESS AND SEEK-SEARCH ADDRESS IN THE
*            IOB.  IF NOT, ADD THE IOB TO THE UNSCHEDULED QUEUE AND SET
*            THE UNSCHEDULED SWITCH ON.
*            B. IF IT IS NOT A WRITE K FOLLWOING FROM READ KU AND
*                WRITE KN IS PENDING, THEN ADD THE IOB TO THE UNSCHED-
*                ULED QUEUE AND SET THE UNSCHEDULED SWITCH ON.
* ENTRY POINTS
*        ENTRY PNT     CALLING ROUTINE                FUNCTION
*        ---------     ---------------                --------
*        DISQHN     NON-PRIVILEGED MACRO-TIME   ALL OF THE FUNCTIONS
*                    ROUTINE VIA QING SVC        DESCRIBED ABOVE
*                    (SVC 54) ROOT
*
*        DISCP45    ASYNCHRONOUS ROUTINE        SET UP CP4/CP5 AND
*                                                REMOVE THEM FROM THEIR
*                                                QUEUE WHERE NECESSARY
*
*        DISCP7     ASYNCHRONOUS ROUTINE        SET UP CP7
*
*        DISCPWKN   ASYNCHRONOUS ROUTINE        SET UP FIRST WKN CP
*
*        DISPRIV    ASYNCHRONOUS ROUTINE        THE FUNCTIONS DESCRIBED
*                                                IN 3.A. ABOVE
*
* INPUT - N/A
* OUTPUT - N/A
* EXTERNAL ROUTINES - N/A
* EXITS  FROM DISQHN  RETURN TO QING SVC ROOT        VIA REGISTER 13
*        FROM DISCP45 RETURN TO ASYNCHRONOUS ROUTINE VIA REGISTER 11
*        FROM DISCP7  RETURN TO ASYNCHRONOUS ROUTINE VIA REGISTER 11
*        FROM DISCPWKN RETURN TO ASYNCHRONOUS ROUTINE VIA REGISTER 11
*        FROM DISPRIV RETURN TO ASYNCHRONOUS ROUTINE VIA REGISTER 13
* TABLES/WORK AREAS' DECB, DCB, IOB AND EXTENSION, DCB WA (DCW), DEB
*        SEE DSECTS AT END OF MODULE FOR FORMAT AND DESCRIPTIONS
* ATTRIBUTES' RE-ENTRANT. DISABLED UPON ENTRY AND EXIT. ENABLED WHILE
*        CHANNEL PROGRAMS ARE BEING INITIALIZED.
* NOTES - NONE
* GENERAL REGISTERS USED AS FOLLOWS
R0       EQU   0                        WORK REGISTER
R1       EQU   1                   *    IOB
R2       EQU   2                 # *    DECB
R3       EQU   3                   *    -----
R4       EQU   4                 # *    DCB
R5       EQU   5                   **   CHANNEL PROGRAM
R6       EQU   6                        WORK REGISTER
R7       EQU   7                        WORK REGISTER
R8       EQU   8                   *    DCB WA (DCW)
R9       EQU   9                        WORK REGISTER
R10      EQU   10                       WORK REGISTER
R11      EQU   11                  ***  RETURN ADDRESS, SUBROUTINES
R12      EQU   12                # *    BASE REGISTER
R13      EQU   13                # *    RETURN ADDRESS
R14      EQU   14                       SAVE AREA ADDRESS        Y02072
R15      EQU   15                       WORK REGISTER
*        # MEANS THAT THESE REGISTERS ARE SET UP BEFORE ENTRY
*            FROM SVC ROOT, AND MAY NOT BE DISTURBED
*        * MEANS THAT THESE REGISTERS ARE SET UP BEFORE
*            ANY ENTRY FROM THE
*           ASYNCHRONOUS ROUTINE, AND MAY NOT BE DISTURBED
*       ** MEANS THAT THESE REGISTERS ARE SET UP BEFORE
*            THE DISCP45 AND DISCP7 ENTRIES FROM THE
*            ASYNCHRONOUS ROUTINE, AND MAY NOT BE DISTURBED.
*      *** MEANS THAT THESE REGISTERS ARE SET UP BEFORE
*            THE DISCP45, DISCP7, AND DISCPWKN ENTRIES FROM THE
*            ASYNCHRONOUS ROUTINE, AND MAY NOT BE DISTURBED.
         USING IGG019J0,R12
         USING IHADCW,R8
         USING IHAIOB,R1
         USING IHADCB,R4
         USING IHADECB,R2
         USING IGGSAVE,R14              SAVEAREA ADDRESSABILITY  Y02072
         SPACE 2
DISQHN   B     QHNQAB1                  ADDRESS MACRO-TIME Q HANDLING
DISCP45  B     BMTS3B2                  ADDRESS CP4-CP5 SUB-ROUTINE
DISCP7   B     BMTS4A2                  ADDRESS CP7 SUB-ROUTINE
DISCPS   DS    A                        NOT USED THIS MODULE
DISCPWKN B     WKNS5B1                  ASYNCH ENTRY TO SET UP WKN CP
DISPRIV  B     QHNQAG1                  ASYNCH ENTRY TO SCHED R+U CP
*              CHART QA
QHNQAB1  L     R3,IGGPDEB               PROTECTED DEB ADDR       Y02072
         L     R8,DCBWKPT2              R8   DCB WK AREA
         TM    DECBTYP2,X'08'           TEST WRITE KU W  UPDATE FLAG ON
         BZ    QHNQXB30                 BRANCH TO TEST ON ERROR Q P4700
         TM    DECBEXC2,X'01'
         BO    QHNQAB5
QHNQXB30 LA    R5,DCWFIOBE              POINT TO ERROR QUEUE     A26738
         BAL   R15,SCHQUEUE             SEE IF DECB'S IOB HERE   A26738
         B     QHNQHB30                 IF NOT, TRY UPDATE QUEUE A26738
         IC    R9,DCWFIOBE              SAVE ERROR QUEUE COUNT   A26738
         BAL   R15,QHNQAC4              REMOVE FROM QUEUE        A26738
         LA    R9,1(R9)                 ROOM FOR ONE MORE        A26738
         STC   R9,DCWFIOBE              STORE NEW VALUE          A26738
         B     QHNQHB8                  GO CLEAR IOB             A26738
QHNQHB30 EQU   *                                                  MC1F
         LA    R5,DCWFUPDI              POINT TO UPDATE QUEUE    A26738
         BAL   R15,SCHQUEUE             SEE IF DECB'S IOB HERE   A26738
         B     QHNQAC3                  IF NOT, DO GETMAIN       A26738
*        IF SO, REDUCE DCWNACT AND REUSE THIS IOB.
         IC    R6,DCWNACT               DECREASE NUMBER OF NON-  A26738
         BCTR  R6,R0                    WRITE KN MACRO EVENTS.    7S029
         STC   R6,DCWNACT                                         7S029
         BAL   R15,QHNQAC4              REMOVE FROM QUEUE        A26738
         B     QHNQHB8                  GO CLEAR IT              A26738
QHNQAC3  EQU   *                        *                        Y01021
         L     R1,IGGPDEB               PROTECTED DEB ADDR       Y02072
         LA    R0,SP250LN               SIZE OF BASIC IOB        Y02072
         USING IHADEB,R1                                         S20201
         TM    DEBRPSID,RPS             TEST IF RPS DEVICE USED  S20201
         BZ    QHNQAC31                 BIF NOT RPS              S20201
         AH    R0,RPSCCW                ADD LENGTH OF RPS CCWS   S20201
QHNQAC31 EQU   *                        *                        S20201
         SPACE 2
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
         SPACE 2
         STM   R2,R15,IGGREGS           SAVE REGISTERS 2-15      Y02072
         IC    R2,IGGUKEY               USERS PROTECT KEY        Y02072
         LR    R9,R14                   SAVE SAVE AREA ADDR      Y02072
         DROP  R14                      END SAVE AREA USING      Y02072
         L     R4,DEBTCBAD              TCB ADDRESS              Y02072
         USING PSA,R0                   LOW CORE ADDRESSABILITY  Y02072
         L     R7,PSAAOLD               ASCB ADDRESS             Y02072
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=1  CHANGE TO KEY ZERO     Y02072
         SPACE 2
         GETMAIN  RU,LV=(0),SP=SP250,KEY=(R2),BRANCH=YES   IOB   Y02072
         SPACE 2
         USING IGGSAVE,R9               SAVEAREA ADDRESSABILITY  Y02072
         MODESET  KEYADDR=IGGUKEY,WORKREG=2 SET TO USER KEY      Y02072
         SPACE 2
         LM    R2,R15,IGGREGS           RESTORE REGS 2-15        Y02072
         DROP  R9                       END SAVE AREA USING      Y02072
         USING IGGSAVE,R14              SAVEAREA ADDRESSABILITY  Y02072
         USING IHAIOB,R1                                         S20201
         B     QHNQHB85                 CLEAR IOB                A26738
QHNQHB8  TM    DECBTYP2,X'A0'           READ DECB                A26738
         BZ    QHNQHB81                 NO, BRANCH               A26738
         TM    DECBTYP1,X'01'           DYNAMIC BUFFERING        A26738
         BZ    QHNQHB81                 NO, BRANCH               A26738
         MVC   DECBAREA(4),IOBCCWAD     REUSE BUFFER IF PRESENT  A26738
         B     QHNQHB9                  CLEAR IOB                A26738
QHNQHB81 EQU   *                                                 A26738
         LA    R15,QHNQHB9                                       A33986
         L     R6,IOBCCWAD              DID IT HAVE A DYN BUFF    MC1F
QHNQHB83 LTR   R6,R6                                             A33986
         BCR   8,R15                   NO CLEAR IOB              A33986
         DROP  R1                       YES ,FREE DYN BUFF        MC1F
         LR    R0,R1                    SAVE IOB ADDRESS          MC1F
         L     R1,DCBBUFCB              SET UP BUFFER CNTL BLK    MC1F
         USING IHABCB,R1                                          MC1F
         MVC   0(4,R6),BCBNAVB          SET POINTER TO THIS       MC1F
         ST    R6,BCBNAVB               BUFFER &SET UP NXT AVAL   MC1F
*                                       BUFF                      MC1F
         DROP  R1                                                 MC1F
         LR    R1,R0                    RESET                     MC1F
         BR    R15                      CLEAR IOB                A33986
QHNQHB85 TM    DECBTYP2,X'A0'           IF READ AND DYN BUFF,    A26738
         BZ    QHNQHB9                  ZERO DECBAREA.           A26738
         TM    DECBTYP1,X'01'                                    A26738
         BZ    QHNQHB9                                           A26738
         XC    DECBAREA(4),DECBAREA                              A26738
         USING IHAIOB,R1                                          MC1F
QHNQHB9  XC    IOBFLAG1(56),IOBFLAG1    CLEAR IOB                 MC1F
         MVI   IOBFLAG1,X'02'            GET CORE AND INITIALIZE
         ST    R2,IOBECBAD
         ST    R2,IOBBCHAD              SAVE ECB                 A33533
         ST    R4,IOBDCBAD-1
         TM    DECBTYP2,X'04'            IF WKN, BRANCH           P4700
         BO    QHNQCB2                                            P4700
         B     QHNQAE3
QHNQAB5  MVI   DECBEXC2,X'00'           RESET UPDATE FLAG
         LA     R5,DCWFUPDI             POINT TO UPDATE QUEUE    A26738
         BAL   R15,SCHQUEUE             SEE IF DECB'S IOB HERE   A26738
         B     QHNQAD5                  BR IF NOT THERE          A26738
         BAL   R15,QHNQAC4              REMOVE FROM QUEUE        A26738
         B     QHNQBD1                  SET UP CP 7              A26738
QHNQAD5  OI    DECBEXC1,X'10'           IF NOT THERE, TURN ON INVALID
QHNQAE5  BR    R13                       REQUEST BIT AND RETURN VIA SVC
         SPACE 2
*        THIS ROUTINE SEARCHES THE IOB QUEUE POINTED TO BY REG 5
*        FOR A MATCH WITH THE DECB IN REG 2.  IF AN IOB IS FOUND
*        THE ROUTINE RETURNS AT REG 15 + 4. IF THE IOB IS ABSENT,
*        A RETURN IS MADE AT REG 15 + 0.
         SPACE 2
SCHQUEUE L     R1,0(R5)                 GET FIRST IOB            A26738
         LA    R1,0(R1)                 CLEAR HIGH ORDER BYTE    A26738
SCHQ2    LTR   R1,R1                    IS THIS AN IOB           A26738
         BZ    0(R15)                   RETURN IF NOT            A26738
         L     R6,IOBECBAD              IF IT IS, GET ITS DECB   A26738
         LA    R6,0(R6)                 CLEAR HIGH BYTE          A26738
         CLR   R2,R6                    IS THIS THE DECB SOUGHT  A26738
         BE    4(R15)                   BRANCH IF IT IS          A26738
         L     R1,IOBFCHAD              OTHERWISE, TRY THE NEXT  A26738
         B     SCHQ2                    GO CHECK IT              A26738
*
QHNQAC4  L     R6,IOBBCHAD              IF FOUND, REMOVE IT FROM Q
         LTR   R6,R6
         BZ    QHNQAC41
         MVC   IOBFCHAD-IHAIOB(4,R6),IOBFCHAD
         B     QHNQAC42
QHNQAC41 MVC   1(3,R5),IOBFCHAD+1       SET NEW 1ST IOB PTR      A26738
QHNQAC42 L     R6,IOBFCHAD
         LTR   R6,R6
         BZ    QHNQAC43
         MVC   IOBBCHAD-IHAIOB(4,R6),IOBBCHAD
         BR    R15                      RETURN                   A26738
QHNQAC43 MVC   5(3,R5),IOBBCHAD+1       SET NEW LAST IOB PTR     A26738
         BR    R15                      RETURN                   A26738
QHNQAE3  TM    DCWWKNI,X'80'            AFTER IOB SET UP, TEST TO SEE
         BO    QHNQAE4                   IF WRITE KN WAITING OR IN
         CLI   DCWNUWKN,X'00'            PROCESS.
         BE    QHNQAG1                  BRANCH IF NOT
QHNQAE4  MVI   IOBUNSQR,X'08'            IF SO, INDICATE AWAITING W KN,
         B     QHNQBH2                  BRANCH TO PLACE ON UNSCHED Q
         SPACE 2
*   ENTRY FROM ASYNCHRONOUS ROUTINE
         SPACE 2
QHNQAG1  IC    R6,DCWNACT               INCREASE NACT - WRITE KN NOT
         LA    R6,1(R6)                  ALLOWED
         STC   R6,DCWNACT
         TM    DECBTYP2,X'20'           IF READ KU, SET UPDATE
         BZ    QHNQAH2                   FLAG ON
         OI    DECBEXC2,X'01'
         B     QHNQBB2                   AND BRANCH
QHNQAH2  TM    DECBTYP2,X'08'           IF WRITING A BLOCKED RECORD TO
         BZ    QHNQBB2                   A PRIME DATA TRACK, THE KEY
         TM    DCBRECFM,X'10'            OF THE LAST RECORD NOT A
         BZ    QHNQBB2                   PADDING RECORD MUST REPLACE
         TM    DECBEXC1,X'02'            THE DECB KEY
         BO    QHNQBB2
         SR    R9,R9                    CLEAR REGISTER            10245
         L     R5,DECBAREA                R5  ADDRESS OF REC
         LA    R5,16(R5)                  R6  ADD FOR BXLE
         AH    R5,DCBBLKSI                R7  COMPARAND FOR BXLE
         LA    R6,1(0,0)
QHNQAK3  LR    R7,R5
         BCTR  R7,0
         SH    R5,DCBLRECL
         AH    R5,DCBRKP
         IC    R9,DCBKEYLE
QHNQAK32 CLI   0(R5),X'FF'
         BNE   QHNQAJ3
         LA    R5,1(R5)
         BCT   R9,QHNQAK32
         SH    R5,DCBRKP
         IC    R9,DCBKEYLE
         SR    R5,R9
QHNQAK31 CLI   0(R5),X'FF'
         BNE   QHNQAJ3
         BXLE  R5,R6,QHNQAK31
         SH    R5,DCBLRECL
         B     QHNQAK3
QHNQAJ3  SH    R7,DCBLRECL               RECORD FOUND
         LA    R7,1(R7)                   R7  ADDRESS OF KEY IN REC
         AH    R7,DCBRKP
         IC    R6,DCBKEYLE                R6  KEY LENGTH - 1
         BCTR  R6,0
         L     R5,DECBKEY                 R5  ADDRESS WHERE KEY SAVED
         EX    R6,QHNQAJ31
         B     QHNQBB2
QHNQAJ31 MVC   0(0,R5),0(R7)
*              CHART QB
QHNQBC2  L     R5,DCWFCP4               TRACK AND DATA SRCH CP.
QHNQBB2  EQU   QHNQBC2
         LTR   R5,R5                    BRANCH IF NONE AVAILABLE
         BZ    QHNQBE2
         OI    IOBINDCT,X'80'           INDICATE REMOVE CP FROM Q
         BAL   R11,DISCP45              BRANCH TO SET UP CP ROUTINE
         B     QHNQBG3                  ERROR RETURN
         B     QHNQBG4                  OK RETURN
QHNQBG3  IC    R9,DCWNACT               DECREASE NO. OF NON-WRITE KN
         BCTR  R9,0                      MACRO EVENTS
         STC   R9,DCWNACT
         B     QHNQBH4                  BRANCH
QHNQBD1  L     R5,DCWFCP7               WRITE UPDATE CP
         LTR   R5,R5                    BRANCH IF NONE AVAILABLE
         BZ    QHNQBE1
         OI    IOBINDCT,X'80'           INDICATE REMOVE CP FROM Q
         BAL   R11,DISCP7               BRANCH TO SET UP CP ROUTINE
QHNQBG4  NI    IOBINDCT,X'BF'           SET OFF UNSCHEDULED BIT
         ST    R2,IOBBCHAD              SAVE ECB                 A33533
QHNQBH4  BR    R13                      RET TO ASYNCH RTN OR SVC ROOT
QHNQBE2  IC    R6,DCWNUCP4              NO CP4-5-6.
         LA    R6,1(R6)                 ADD 1 TO NUMBER UNSCHED IOBS
         STC   R6,DCWNUCP4               AWAITING CP4-5-6.
         MVI   IOBUNSQR,X'40'           INDICATE REASON ON Q
         B     QHNQBH2                  BRANCH
QHNQBE1  IC    R6,DCWNUCP7              NO CP7.
         LA    R6,1(R6)                 ADD 1 TO NUMBER OF UNSCHED IOBS
         STC   R6,DCWNUCP7               AWAITING CP7
         MVI   IOBUNSQR,X'20'           INDICATE REASON ON Q
QHNQBH2  L    R6,DCWLIOBU
         LTR   R6,R6
         BZ    QHNQBH21
         ST   R1,IOBFCHAD-IHAIOB(R6)
         B     QHNQBH22
QHNQBH21 ST    R1,DCWFIOBU
QHNQBH22 ST    R6,IOBBCHAD
         ST   R1,DCWLIOBU
         XC    IOBFCHAD,IOBFCHAD
         OI    IOBINDCT,X'40'           SET UNSCHEDULED FLAG ON
         BR    R13                      EXIT   TO SVC ROOT OR ASYNCH
*
*              CHART QC   WRITE KN MACRO-TIME
*
         USING IHAWKNCP,5
QHNQCB2  CLI   DCWNACT,X'00'            OTHER MACROS IN PROCESS   P4700
         BNE   QHNQCF2
         TM    DCWWKNI,X'80'
         BO    QHNQCF2
         OI    DCWWKNI,X'80'            IF NOT, INDICATE WKN IN PROCESS
         NI    IOBINDCT,X'BF'            UNSCHEDULED BIT OFF
         ST    R2,IOBBCHAD              SAVE ECB                 A33533
         BAL   R11,WKNS5B1               SET UP W KN CP
         BR    R13                       RETURN BY SVC ROOT
QHNQCF2  IC    R6,DCWNUWKN              IS SO, ADD 1 TO NUMBER OF IOBS
         LA    R6,1(R6)                  AWAITING WKN MODULE
         STC   R6,DCWNUWKN
         MVI   IOBUNSQR,X'10'            INDICATE REASON ON Q
         B     QHNQBH2                   BRANCH
         SPACE 2
*              CHART S2  HIGHEST INDEX SEARCHED IN CORE
         SPACE 2
BMTS2B2  SR    R7,R7                    R7   KEY LENGTH - 1
         IC    R7,DCBKEYLE
         BCTR  R7,0
         L     R6,DECBKEY               R6   ADDRESS OF KEY SOUGHT
         L     R5,DCBMSHI               R5   ADDRESS OF KEY ON TABLE
BMTS2B21 EX    R7,BMTS2B22              COMPARE KEY ON TABLE TO SOUGHT
         BNL   BMTS2C2                  IF HI OR EQ, BRANCH
         AR    R5,R7
BMTS2E2  LA    R5,11(R5)                ELSE ADD KEY LGTH + INDEX LGTH
         B     BMTS2B21                  TO R5 FOR NXT KEY AND BRANCH
BMTS2C2  AR    R5,R7                    HIGH FOUND
         TM    9(R5),X'20'              IF DUMMY-END OR INACTIVE, NO
         BZ    BMTS2D3                   REC FND. IF DUMMY-CHAINED,
         TM    9(R5),X'08'               SKIP THIS KEY
         BO    BMTS2E2
         OI    DECBEXC1,X'80'           NO REC FND. SET EXCEP CODE
         TM    DECBTYP1,X'01'           DYNAMIC BUFFERING        A33986
         BZ    BMTS2C4                  NO, BRANCH               A33986
         L     R6,DECBAREA                                       A33986
         LR    R7,R15                   SAVE REG 15             SA64081
         BAL   R15,QHNQHB83                                      A33986
         LR    R15,R7                   RESTORE REG 15          SA64081
BMTS2C4  LR    R0,R1                    R0= IOB ADDR             S20201
         L     R1,IGGPDEB               PROTECTED DEB ADDR       Y02072
         USING IHADEB,R1                                         S20201
         TM    DEBRPSID,RPS             TEST IF RPS DEVICE USED  S20201
         LR    R1,R0                    R1=IOB ADDR              S20201
         USING IHAIOB,R1                                         S20201
         LA    R0,SP250LN               GET SIZE OF BASIC IOB    Y02072
         BZ    BMTS2C41                 BIF NOT RPS              S20201
         AH    R0,RPSCCW                ADD LENGTH OF RPS CCWS   S20201
BMTS2C41 EQU   *                        *                        S20201
         L     R5,IOBCCWAD              RESTORE POINTER TO CP   SA67174
         SPACE 2
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
         SPACE 2
         STM   R2,R15,IGGREGS           SAVE REGISTERS 2-15      Y02072
         IC    R5,IGGUKEY               USERS PROTECT KEY        Y02072
         L     R4,IGGPDEB               PROTECTED DEB ADDR       Y02072
         DROP  R14                      END SAVE AREA USING      Y02072
         LR    R9,R14                   SAVE SAVE AREA ADDR      Y02072
         USING IHADEB,R4                DEB ADDRESSABILITY       Y02072
         L     R4,DEBTCBAD              TCB ADDRESS              Y02072
         DROP  R4                       END USING ON DEB         Y02072
         USING PSA,R0                   LOW CORE ADDRESSABILITY  Y02072
         L     R7,PSAAOLD               ASCB ADDRESS             Y02072
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=3  CHANGE TO KEY ZERO     Y02072
         SPACE 2
*                                       FREE IOB CORE            Y02072
         FREEMAIN  RU,LV=(0),A=(1),SP=SP250,KEY=(R5),BRANCH=YES  Y02072
         SPACE 2
         DROP  R1                                                S20201
         USING IHADCB,R4                DCB ADDRESSABILITY       Y02072
         SPACE 2
         SR    R10,R10                  POST COMPLETION CODE     Y02072
         LA    R11,DECBECB              ECB ADDRESS              Y02072
         LA    R11,0(R11)               CLEAR HIGH ORDER BYTE    Y02072
         L     R15,CVTPTR               CVT ADDRESS              Y02072
         USING CVT,R15                  CVT ADDRESSABILITY       Y02072
         L     R15,CVT0PT01             POST ENTRY POINT         Y02072
         DROP  R15                      END USING ON CVT         Y02072
*
         BALR  R14,R15                  GO POST COMPLETION       Y02072
*
         USING IGGSAVE,R9               SAVEAREA ADDRESSABILITY  Y02072
         SPACE 2
         MODESET  KEYADDR=IGGUKEY,WORKREG=11 CHANGE TO USER KEY  Y02072
         SPACE 2
         LM    R2,R15,IGGREGS           RESTORE REGISTERS 2-15   Y02072
         DROP  R9                       END SAVE AREA USING      Y02072
         USING IGGSAVE,R14              SAVEAREA ADDRESSABILITY  Y02072
         BR    R11                      ERROR RETURN
         USING  IHAIOB,R1                                        S20201
BMTS2D3  MVC   IOBDADAD,1(R5)           IF KEY HI OR EQ + NOT DUMMY,
         L     R5,IOBCCWAD              RESTORE POINTER TO CP
         BR    R10                       MOVE ADDR TO IOB & OK RETURN
BMTS2B22 CLC   0(0,R5),0(R6)
*
*        CHART S3 SET UP CP4 AND CP5
*
         USING CP4,R5
BMTS3B2  ST    R5,IOBCCWAD              SET POINTER TO CP IN IOB EXT
         CLI   DCWNLSD,X'00'            IF NLSD=0, TRACK IS HIGHEST LVL
         BNE   BMTS3D4                   INDEX.
         TM    DCWHIAV,X'40'            UNLESS HIGH LVL IN CORE
         BZ    BMTS3C3                  BRANCH IF IT IS NOT      S20201
         BAL   R10,BMTS2B2              IF HI LVL IN CORE, SCH   S20201
         B     BMTS3D4                  GO TEST FOR SHARED TRACK S20201
BMTS3C3  EQU   *                        TRK IX IS HIGHEST LEVEL  S20201
         MVC   IOBDADAD(SEVEN),DCBFTHI  IF TRK HIGH,SET IOB ADDR S20201
BMTS3D4  EQU   *                        TEST FOR SHARED TRACK    S20201
         SR    R6,R6                    MBBCCHH  FROM HI INDEX   S20201
         AH    R6,DCBFIRSH              R MUST BE HIGHER THAN    S20201
         BNZ   BMTS3D5                  R OF LAST INDEX REC.     S20201
         MVC   IOBDADAD+SEVEN(ONE),DCBFIRSH+TWO THUS, R = FIRST  S20201
*                                       REC OF                   S20201
         B     BMTS3C2                  SHARED TRACK, IF SHARED. S20201
*                                       R = 255 OTHERWISE.
BMTS3D5  MVI   IOBDADAD+SEVEN,HEXFF     CHANGE R TO 255          S20201
BMTS3C2  TM    IOBINDCT,X'80'           IS CP4-5-6 ON AVAILABLE Q{
         BZ    BMTS3D1                  BRANCH IF NO
         MVC   DCWFCP4,CP45PTR  REMOVE CP FROM AVAILABLE Q       A25517
BMTS3D1  EQU   *                        *
         MVC   IOBSTART,IOBCCWAD+1      INITIALIZE IOB
         NI    IOBFLAG1,X'7F'
         OI    IOBFLAG1,X'40'
         SR    R6,R6                                             S20201
         AH    R6,DCBFIRSH              IS 1ST TRK SHARED        S20201
         BZ    BMTS3F1                  BIF YES                  S20201
         LA    R6,CA5                   TIC CA5 IF NOT SHARED    S20201
         B     BMTS3F2                                           S20201
BMTS3F1  LA    R6,CA1                   TIC CA1 IF SHARED        S20201
BMTS3F2  EQU   *                        *                        S20201
         ST    R6,CA03                  STORE TIC ADDR           S20201
         MVI   CA03,TIC                 RESTORE OP CODE          S20201
         LA    R6,IOBDADAD+3            SET UP CHANNEL PROGRAM
         STH   R6,CA1+2                 KEY AND ID ADDRESSES
         STH   R6,CA01+2                                          17516
         SRL   R6,16                                              17516
         STC   R6,CA01+1                                          17516
         STC   R6,CA1+1                                           17516
         MVC   CA5+1(3),DECBKEY+1
         MVC   CA8+1(3),DECBKEY+1
         MVC   CA21+1(3),DECBKEY+1
         TM    DECBTYP1,X'02'           LENGTH - FROM DCB IF B6 OF TYPE
         BZ    BMTS3K1                           FROM DECB OTHERWISE
         MVC   CA23+6(2),DCBBLKSI
         B     BMTS3J2
BMTS3K1  MVC   CA23+6(2),DECBLGTH
BMTS3J2  L     R6,DECBAREA              DATA ADDRESS TO AREA+16
         LA    R6,16(R6)
         STH   R6,CA23+2
         SRL   R6,16
         STC   R6,CA23+1
         NI    CA24+FOUR,CCOFF          DISCONNECT WRT CHECK CP  S20201
         TM    DECBTYP2,X'E0'           WRITE OR READ{
         BZ    BMTS3G3
         MVI   CA23,X'06'               READ
         MVI   IOBAPP,X'00'             SET APPENDAGE CODE TO ZERO
         TM    DCBRECFM,X'10'
         BZ    BMTS3J4                  SET CA21 TO SRCH HI/EQ IF BLKED
         MVI   CA21,X'69'
         B     BMTS3J4
BMTS3G3  MVI   CA23,X'05'               WRITE
         MVI   IOBAPP,X'01'
         TM    DCBRECFM,X'10'           SET CA21 TO SRCH = IF BLKED
         BZ    BMTS3J4
         MVI   CA21,X'29'
BMTS3J4  TM    DCBOPTCD,X'80'           IF WRITE CHECK DESIRED,
         BZ    BMTS5E2                   SET UP ADDITIONAL COMMANDS
ANYREAD  EQU   X'E0'                                             S20201
ONES     EQU   1                                                 S20201
MIXED    EQU   4                                                 S20201
         TM    DECBTYP2,ANYREAD         NO WRT CHK IF READ       S20201
         BNZ   BMTS3J8                  BIF READ                 S20201
         OI    CA24+FOUR,CC             CONNECT WRT CHECK CP     S20201
         MVC   CA24D+1(3),CA1+1         CA24D ADDRESS FROM CA1
         MVC   CA24C+6(2),CA23+6        CA24C COUNT FROM CA23
         MVC   CA24F+1(7),CA23+1        CA24F ADDR AND COUNT FROM CA23
BMTS3J8  MVI   CA25+SEVEN,ZERO          CLEAR R                  S20201
         MVC   CA25+THREE(FOUR),IOBSEEK+THREE INIT CCHH          S20201
         OC    CA25+FIVE(THREE),DCBFIRSH INIT HHR                S20201
         B     BMTS5E4                                           S20201
BMTS5E2  EQU   *                                                 S20201
         USING CP4+(SZ5W-SZ5),R5                                 S20201
         MVI   CA25+SEVEN,ZERO          CLEAR R                  S20201
         MVC   CA25+THREE(FOUR),IOBSEEK+THREE INIT CCHH          S20201
         OC    CA25+FIVE(THREE),DCBFIRSH INIT HHR                S20201
BMTS5E4  EQU   *                        *
         B     4(R11)                   RETURN, NO ERROR
*        CHART S4   SET UP CP7
         USING CP7,R5
BMTS4A2  TM    IOBINDCT,X'80'           REMOVE FROM Q IF COMING FROM
         BZ    BMTS4B2                  MACRO
         MVC   DCWFCP7,CA45+4
BMTS4B2  ST    R5,IOBCCWAD              INITIALIZE IOB
         MVC   IOBSTART(3),IOBCCWAD+1
         NI    IOBFLAG1,X'7F'
         OI    IOBFLAG1,X'40'
         MVI   IOBAPP,X'02'              SET APPENDAGE CODE CODE TO 2
         LA    R6,IOBDADAD+3            SET UP CP7 - ADDRESS OF ID
         STH   R6,CA44+2
         SRL   R6,16
         STC   R6,CA44+1
         L     R6,DECBAREA              ADDRESS OF AREA IN R6
         SR    R7,R7                    CLEAR R7 FOR DATA LENGTH
         TM    DECBEXC1,X'02'
         BZ    BMTS4F3                  TEST FOR OVERFLOW REC
         LA    R6,6(R6)                 OVRFLOW - ADD 6 TO AREA ADDRESS
         LA    R7,10(R7)                ADD 10 TO AREA LENGTH
         TM    DECBTYP1,X'02'           IF NO OVERRIDE ON LENGTH, ADD
         BZ    BMTS4G2                   DCB RECORD SIZE TO LENGTH
         AH    R7,DCBLRECL
         B     BMTS4J2
BMTS4F3  LA    R6,16(R6)                PRIME REC-ADD 16 TO AREA ADDR
         TM    DECBTYP1,X'02'           IF NO OVERRIDE ON LENGTH, TAKE
         BZ    BMTS4G2                   FROM DCB BLOCK SIZE
         AH    R7,DCBBLKSI
         B     BMTS4J2
BMTS4G2  AH    R7,DECBLGTH              LNGTH OVRRIDE,ADD DECB LENGTH
BMTS4J2  STH   R6,CA46+2                IN ANY CASE, MOVE COMPUTED
         SRL   R6,16                     LENGTH AND ADDRESS TO CP7
         STC   R6,CA46+1                 CCW CA46
         STH   R7,CA46+6
         TM    DCBOPTCD,X'80'           WRITE CHECK DESIRED{
         BZ    BMTS4K4                  BRANCH IF NO
         MVI   IOBCOUNT,10              SET ERROR COUNTER TO 10
         MVC   CA46A+1(3),CA44+1        CA46A ADDRESS FROM CA44
         MVC   CA46C+6(2),CA46+6        CA46C COUNT FROM CA46
BMTS4K4  BR    R11                      RETURN
*
*                             SET UP FIRST CHANNEL PROGRAM
*
WKNS5B1  EQU   *                        *
         NI    DCWWKNI,X'EF'            SET ADD-TO-END INDICATOR OFF
         TM    DCWHIAV,X'40'            ON IF HI LEVEL INDEX IN CORE
         BO    WKNS5B3                  SEARCH HI LEVEL INDEX IN CORE
         MVC   IOBDADAD(7),DCBFTHI      SET SEEK ADDRESS TO MBBCCHHX
         MVI   IOBDADAD+7,X'00'         R IS ZERO
WKNS5E1  MVI   IOBAPP,X'08'             SET APPENDAGE CODE TO 8
WKNS5D1  EQU   WKNS5E1
         L     R5,DCBWKPT3              R5   POINTER TO CP8
         USING IHAWKNCP,R5
         ST    R5,IOBCCWAD              CCW 0 RELATIVE ADDRESS
         LA    R6,CB1                   START OF CP8
         ST    R6,IOBSTART-1
         MVI   IOBFLAG1,X'42'           B1 ON- YES CC
         LA    R6,IOBDADAD+3
         STH   R6,CB1+2
         SRL   R6,16
         STC   R6,CB1+1                 SEEK-SEARCH ADDRESS AT CB1
         MVC   CB5+1(3),DECBKEY+1       ADDR OF KEY TO CP8
         MVC   CB19+1(3),DECBKEY+1
         MVC   CB23+THREE(FIVE),IOBDADAD+THREE CCHH OF FIRST     S20201
*                                       TRACK, R=                S20201
         OC    CB23+FIVE(THREE),DCBFIRSH SET HHR TO FIRSH        S20201
         B     WKNS5G2
WKNS5B3  SR    R6,R6                    CLEAR WORK REGISTER
         IC    R6,DCBKEYLE              KEY LENGTH IN LOW ORDER BYTE
         BCTR  R6,0                     COMPARE KEY LENGTH BYTES
         L     R7,DECBKEY               POINTER TO STORAGE ARGUMENT
         L     R10,DCBMSHI              POINTER TO INDEX ARGUMENT
WKNS5B31 EX    R6,WKNS5B34              MODIFY COMPARE LENGTH
         BNL   WKNS5C3                  FIRST HI OR EQUAL
         AR    R10,R6                   INCREMENT IS KEYLE-1
WKNS5B32 LA    R10,11(0,R10)            PLUS 11 MORE (= KEYLE+10)
         B     WKNS5B31                 TRY AGAIN
WKNS5B34 CLC   0(0,R10),0(R7)           LENGTH, INDEX ARG, STORAGE ARG
WKNS5C3  AR    R10,R6                   NOW POINT TO FLAG BYTE MINUS 9
         TM    9(R10),X'28'             TEST IF DUMMY CHAINED
         BO    WKNS5B32                 SKIP ENTRY - TRY AGAIN
         TM    9(R10),X'20'             TEST IF INACTIVE OR DUMMY END
         BO    WKNS5C5                       ADD TO END OF DATA SET
         MVC   IOBDADAD(7),1(R10)       SET SEEK ADDRESS TO MBBCCHHX
         MVI   IOBDADAD+7,X'00'         R IS ZERO
         B     WKNS5D1                  CONTINUE
*
*              SET UP CP15
*
WKNS5C5  MVC   IOBDADAD(3),DCBLPDA      MBB FROM LPDA
         MVC   IOBDADAD+3(4),DCBLETI    CCHH FROM LETI
         OC    IOBDADAD+6(1),DCBFIRSH+3
         XC    IOBDADAD+6(1),DCBFIRSH+3
         MVI   IOBDADAD+7,X'00'
         MVI   IOBAPP,X'0E'             SET APPENDAGE CODE TO 14
         L     R5,DCBWKPT3              PUT CP8 ADDRESS IN REG 5
         ST    R5,IOBCCWAD              CCW 0 RELATIVE ADDRESS
         TM    DCBOPTCD,X'80'                WRITE CHECK?
         BZ    WKNS5C51                      NO
         LA    R5,WC(R0,R5)             ALLOW FOR WRITE CHECK    S20201
*                                       CCWS                     S20201
WKNS5C51 LA    R5,CI1-WC                START OF CP15            S20201
         USING CP15,R5                                           S20201
         ST    R5,IOBSTART-ONE                                   S20201
         MVI   IOBFLAG1,FLAGS           B1 ON- YES CC            S20201
         MVC   CI5+TWO(FIVE),DCBLETI    COUNT ADDR               S20201
         MVC   CI5(TWO),DCBLPDA+ONE     SET UP BB FOR HEAD SEEK  S20201
         LA    R6,IOBDADAD+3
         STH   R6,CI1+2
         SRL   R6,16
         STC   R6,CI1+1                 SEEK-SEARCH ADDRESS AT CI1
WKNS5G2  EQU   *                        *
         BR    R11
*
ZERO     EQU   0                        0                        S20201
ONE      EQU                            1                        S20201
TWO      EQU   2                        2                        S20201
THREE    EQU   3                        3                        S20201
FOUR     EQU   4                        4                        S20201
FIVE     EQU   5                        5                        S20201
SEVEN    EQU   7                        7                        S20201
FLAGS    EQU   X'42'                    IOB FLAGS                S20201
CCOFF    EQU   X'BF'                    TURN OFF COMAND CHAIN    S20201
HEXFF    EQU   X'FF'                    255                      S20201
RPS      EQU   X'E0'                    RPS MASK-BIT 0=P,1=I,2=O S20201
RPSCCW   DC    H'16'                    LENGTH OF RPSCCW'S       S20201
*
SP250LN  EQU   56                       BASIC IOB = 56 BYTES IN  Y02072
SP250    EQU   250                      SP 250                   Y02072
KEY0     DC    X'00'                    ZERO PROTECT KEY         Y02072
         DS    0F                       ALIGN FOR PATCH AREA     Y02072
*
PATCH    DC    XL((*-IGG019J0)/20)'00'  ZEROED PATCH AREA        Y02072
*
         EJECT
*              MODULE WORK AREAS
WKNS5MSK DC    X'FF'                    TO SET ON SYSTEM MASK
IHABCB   DSECT                                                  , MC1F
BCBFIOB  DS    A                                                  MC1F
BCBLIOB  DS    A                                                  MC1F
BCBNAVB  DS    A                                                  MC1F
BCBSIZE  DS    A                                                  MC1F
IHAWKNCP IGGWKNCP   OPTCD=W                                      S20201
WC       EQU   LCH3C+LCH8C+LCH13C+LCH18C+LCH22C+LCB54C WRITE CHK S20201
*                                       CCWS                     S20201
         EJECT
*
*              DATA EVENT CONTROL BLOCK
IHADECB  DSECT
         DS    0F
DECBECB  DS    CL4                      EVENT CONTROL BLOCK (ECB)
DECBTYP1 DS    BL1                      TYPE B6 - 1 IF LENGTH IS S
*                                            B7 - 1 IF AREA IS S
DECBTYP2 DS    BL1                           B0 - 1 IF READ K
*                                            B1 - 1 IF READ KX
*                                            B2 - 1 IF READ KU
*                                            B4 - 1 IF WRITE K
*                                            B5 - 1 IF WRITE KN
DECBLGTH DS    CL2                      LENGTH OF BLOCK
DECBDCBA DS    A                        POINTER TO DCB
DECBAREA DS    A                        ADDRESS OF AREA
DECBLOGR DS    A                        POINTER TO LOGICAL RECORD
DECBKEY  DS    A                        POINTER TO KEY
DECBEXC1 DS    BL1                      EXCPTN CD B0-RECORD NOT FOUND
*                                                 B1-RECORD LGTH CHK
*                                                 B3-INVALID REQUEST
*                                                 B4-UNCORRECTABLE IO
*                                                 B5-UNREACHABLE BLOCK
*                                                 B6-OVERFLOW RECORD
*                                                 B7-DUPLICATE REC
DECBEXC2 DS    BL1                                B7-READ KU
*        SAVE AREA FOR PRIVILEGES AND ASYNCHRONOUS ROUTINES      Y02072
IGGSAVE  DSECT
IGGRETRN DS    A                        RETURN ADDRESS
IGGPDEB  DS    A                        VALIDATED DEB ADDR
IGGUKEY  DS    X                        USER PROTECT KEY
IGGRESRV DS    XL3                      RESERVED
IGGREGS  DS    16F                      REGISTER SAVE AREA
IGGSIZE  EQU   *-IGGSAVE                LENGTH OF SAVE AREA
         EJECT
         DCBD  DSORG=(IS)
IHAIOB   IGGIOBD
IOBSEEK  EQU   IOBDADAD                                          S20201
         SPACE 5
IHADCW   IGGBISAM
         SPACE 5
IHADEB   IGGDEBD
*
         SPACE 5
IHACP47  IGGCP47   OPTCD=W                                       S20201
CP45PTR  EQU   CA02+4                                            S20201
         EJECT
CVT      DSECT                                                   Y02072
         CVT                                                     Y02072
         SPACE 4
         IHASCVT                                                 Y02072
         SPACE 4
         IHAPSA                                                  Y02072
         END
