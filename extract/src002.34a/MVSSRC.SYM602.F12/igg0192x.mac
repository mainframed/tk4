         TITLE 'OPEN EXECUTOR IGG0192X-VARIABLE VERSION OF 2M'
IGG0192X CSECT                                                 
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192X                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, BISAM MODE - WKN WITH VLR             *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = CONTINUE INITIALIZATION UNIQUE TO WRITE KN, WHETHER OR   *
*            NOT READ IS PRESENT.  MOVE AND INITIALIZE CP14W (WITH OR *
*            WITHOUT WRITE CHECK).                                    *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 758 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192X                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0192L FOR  *
*              WKN WITH VLR.  RECEIVES CONTROL IN STORAGE PROTECT KEY *
*              5 AND PRIVILEGED STATE.                                *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG0192Q IN STORAGE        *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGBISAM - BISAM WORK AREA                          *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY AND CVT.                               *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET AND XCTL.                                          *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
RE       EQU   00                       WORK REG
RF       EQU   01                       WORK REG
RDCB     EQU   02                       USER'S DCB
RBASE    EQU   03                       BASE REG
RG       EQU   04                       WORK REG
RPAR     EQU   05                  *    PARAMETER LIST ADDRESS
RWTG     EQU   06                  *    WHERE TO GO TABLE BEGINNING
RPARC    EQU   07                  *    CURRENT ENTRY,PARAMETER LIST
RWTGC    EQU   08                  *    CURRENT ENTRY, WTG TABLE
RTIOT    EQU   09                       TIOT ADDRESS
RUCB     EQU   10                       UCB ADDRESS
RA       EQU   11                       WORK REG
RB       EQU   12                       WORK REG
RC       EQU   13                       WORK REG
RD       EQU   14                       WORK REG
R15      EQU   15                       WORK REG
RCORE    EQU   RG                       DCB WA ADDRESS
CVTPTR   EQU   16                                                O19113
UCBTYPE  EQU   19                                                O19113
*                   *     MEANS SET BEFORE ENTRY AND BEFORE EXIT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         L     RDCB,0(RPARC)            RDCB  ADDRESS OF DCB
         USING IHADCB,RDCB
         L     RCORE,DCBWKPT2           RCORE ADDRESS OF DCB WA
         USING IHADCW,RCORE
         L     RB,FOUR(RWTGC)           LOAD OPEN WORK AREA ADDR Y01021
         USING FORCORE,RB               RB = OPEN WORK AREA ADDR Y01021
*
         MODESET  KEYADDR=DXUKEY,WORKREG=13  CHANGE TO USER KEY  Y02072
*
         L     RD,DCBWKPT3              RD    ADDRESS OF WRITE KN CP
         L     RA,DXCCW9                ADDR CP CRNTLY SET UP    Y02072
         L     RF,DXCCW10               ADDR OF CP IN MODULE     Y02072
         USING CP14,RA                  CP14, CP14W
         MVC   0(LCP141,RA),0(RF)                                S20201
         MVC   LCP141(LCP142,RA),LCP141(RF)                      S20201
         L     RC,CH2
         ALR   RC,RD                    MOVE AND INITIALIZE
         ST    RC,CH2
         L     RC,CH3
         ALR   RC,RD                     CP14 AND CP14W
         ST    RC,CH3
         MVC   LCP14(WC,RA),LCP14(RF)   INSERT THESE COMMANDS    S20201
         L     RC,CH3A1                                          S20201
         ALR   RC,RD                                             S20201
         ST    RC,CH3A1                                          S20201
         L     RC,CH3B
         ALR   RC,RD
         ST    RC,CH3B                   FOR WC ONLY
OPNO7D51 L     RC,CH55                  CONTINUE INITIALIZING    S20201
         ALR   RC,RD
         ST    RC,CH55                                           S20201
         L     RC,CH6                    FOR CP14 AND CP14W
         ALR   RC,RD
         ST    RC,CH6
         MVC   CH7+7(1),DCBKEYLE        MOVE KEY LENGTH TO CH7
         L     RC,CH8
         ALR   RC,RD
         ST    RC,CH8
         L     RC,CH8A                                           S20201
         ALR   RC,RD
         ST    RC,CH8A                  FOR WC ONLY
         L     RC,CH8B
         ALR   RC,RD
         ST    RC,CH8B
*
*        THIS SECTION GETS THE DEVICE TABLE ENTRY POINTER FOR
*        INDEPENDENT OVERFLOW AND SAVES IT IN CH8B+4 FOR
*        ASYNCHRONOUS ROUTINES
*
         SR    RC,RC                    CLEAR REGISTER           O19113
         IC    RC,DCBOVDEV              RC=OVERFLOW DEVTYPE      O19113
         DROP  RB                                                Y02072
         LR    R15,RB                   OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,R15              OPEN WORK AREA BASE      Y02072
         L     RB,CVTPTR                RB=PTR TO CVT            O19113
         L     RB,64(RB)                RB=PTR TO DEVICE TABLE   O19113
         IC    RC,0(RC,RB)              RE=OFFSET VALUE          O19113
         AR    RB,RC                    RB=DEVICE TABLE ENTRY    O19113
         ST    RB,CH8B+4                SAVE IN CH8B             O19113
         IC    RC,DCBKEYLE              MOVE KEY LENGTH TO CH8C
         LA    RC,10(RC)
         STH   RC,CH8C+6
         MVC   CH8F+4(4),DCWFUPDI      MOVE DEVICE PTR
         XC    DCWFUPDI(4),DCWFUPDI
         L     RC,CH8G5                                          S20201
         ALR   RC,RD
         ST    RC,CH8G5                  WITHIN CP14 OR CP14W    S20201
         L     RC,CH9
         ALR   RC,RD
         ST    RC,CH9
         MVC   CH8D(FOUR),CH9           CH8D & CH9 TIC TO CH8G5  S20201
         MVC   CH12+7(1),DCBKEYLE
         L     RC,CH13
         ALR   RC,RD                     WHICH IS IN RD
         ST    RC,CH13
         TM    DCBOPTCD,X'80'
         BO    OPNO7D53                BRANCH IF WC
         MVC   CH80(FOUR),CH8D         CH80 TICS TO CH8G5        S20201
         LA    RC,CH14
         ST    RC,CH130                CAUSE CH130 TO T1C TO     S20201
*                                       CH14                     S20201
         MVI   CH130,TIC                                         S20201
         B     OPNO7D44
OPNO7D53 L     RC,CH13B
         ALR   RC,RD
         ST    RC,CH13B
         L     RC,CH13A                                          S20201
         ALR   RC,RD
         ST    RC,CH13A
OPNO7D44 SR    RC,RC                                             S20201
         IC    RC,DCBKEYLE              KEY LENGTH TO CH13C
         LA    RC,10(RC)
         STH   RC,CH13C+6
         L     RC,CH14
         ALR   RC,RD                    CONTINUE INITIALIZING
         ST    RC,CH14
         L     RC,CH15
         ALR   RC,RD
         ST    RC,CH15
         L     RC,CH15A                  BOTH CP14 AND CP14W
         ALR   RC,RD
         ST    RC,CH15A
         L     RC,CH16
         ALR   RC,RD
         ST    RC,CH16
         MVC   CH17+7(1),DCBKEYLE       MOVE KEY LENGTH TO CH17
         LH    RC,DCBLRECL
         LA    RC,14(RC)               RCD LGTH + 14 TO CH18
         STH   RC,CH18+6
         TM    DCBOPTCD,X'80'
         BO    OPNO7D54                BRANCH IF WC
         LA    RC,CH19
         ST    RC,CH180                CAUSE CH180 TO T1C TO     S20201
*                                       CH19                     S20201
         MVI   CH180,TIC                                         S20201
         B     OPNO7D56
OPNO7D54 L     RC,CH18B
         ALR   RC,RD
         ST    RC,CH18B
         L     RC,CH18A                                          S20201
         ALR   RC,RD
         ST    RC,CH18A
         SR    RC,RC
         IC    RC,DCBKEYLE
         AH    RC,DCBLRECL               CH18A, CH18B AND CH18C
         LA    RC,22(RC)
         STH   RC,CH18C+6
****************************************************************
OPNO7D56 L     RC,CH25                  INITILIZE CP14 EOF EXT   S20201
         ALR   RC,RD               FOR EOF CCW'S
         ST    RC,CH25
         L     RC,CH26
         ALR   RC,RD
         ST    RC,CH26
         L     RC,CH27
         ALR   RC,RD
         ST    RC,CH27
         TM    DCBOPTCD,WRTCHK          WRITE CHECK              S20201
         BO    OPNO7D57                 YES                      S20201
         NI    CH27+FOUR,CCOFF         IF NOT TURN OFF CC FLG    S20201
         B     OPNO7D58                CONTINUE CP14 INIT        S20201
OPNO7D57 EQU   *                                                 S20201
         L     RC,CH28
         ALR   RC,RD
         ST    RC,CH28
         L     RC,CH29
         ALR   RC,RD
         ST    RC,CH29
         L     RC,CH30
         ALR   RC,RD
         ST    RC,CH30
OPNO7D58 EQU   *                                                 S20201
         L     RC,CH19
         ALR   RC,RD                    CONTINUE FOR CP14
         ST    RC,CH19
         L     RC,CH20
         ALR   RC,RD
         ST    RC,CH20
         L     RC,CH21                   AND CP14W
         ALR   RC,RD
         ST    RC,CH21
         LH    RC,DCBLRECL              RCD LGTH PLUS 10 TO CH22
         LA    RC,14(RC)
         STH   RC,CH22+6
         TM    DCBOPTCD,X'80'
         BO    OPNO7D55                BRANCH IF WC
         MVI   CH22+4,X'00'            CC OFF ON CH22
OPNO7D55 STH   RC,CH22C+6              LRECL + 10 TO CH22C
         L     RC,CH22A
         ALR   RC,RD
         ST    RC,CH22A
         L     RC,CH22B
         ALR   RC,RD                    INITIALIZE CH22A AND CH22B
         ST    RC,CH22B
         MVC   CH24+5(1),DCBKEYLE
         LH    RC,DCBLRECL
         LA    RC,14(RC)
         STH   RC,CH24+6
         LA    RA,LCP14+WC(RA)                                   S20201
         LA    RF,LCP14+WC(RF)                                   S20201
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         MVC   0(L'LOAD2Q,RWTGC),LOAD2Q ID OF EXEC IGG0192Q      Y02072
         ST    RA,DXCCW9                SAVE CURR CP ADDR        Y02072
         ST    RF,DXCCW10               ADDR OF CP IN MODULE     Y02072
         DROP  R15                                               Y02072
         L     RCORE,4(RWTGC)
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT NAME ON THE WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)          AND DCB ON THE PARAM TABLE
         CLC   0(2,RWTGC),OPNEXNAM      IF NAME ON TABLE SAME ID AS
         BCR   8,RBASE                   CURRENT LOAD, BRANCH TO REPEAT
         CLC   0(2,RWTGC),OPIDCNST     IF NOT END OF WTG TABLE BRANCH
         BNE   RELOOP                    TO TRY NEXT NAME
         LR    RPARC,RPAR               OTHERWISE, RETURN TO START OF
         LA    RWTGC,WAOFF(0,RWTG)       BOTH TABLES AND SEARCH FOR
ZCHECK   CLI   0(RWTGC),ZCNST            FIRST NON-ZERO ID
         BNE   TCTLRTN
         LA    RWTGC,WGOFF(0,RWTGC)
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       WHEN FOUND, SET UP PARAMETERS
*                                       TO CALL THAT MODULE
         USING FORCORE,RCORE                                     Y01021
         LA    R15,DXCCW12              WA ADDRESS               Y01021
TRCTL    XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) GO TO NEXT EXECTR  Y02072
WGOFF    EQU   8                        SIZE OF ONE ENTRY ON WTG TABLE
PLOFF    EQU   4                        SIZE OF ONE ENTRY ON PARAM TAB
OPNEXNAM DC    C'2X'                   ID OF THIS EXECUTOR
OPIDCNST DC    C'0S'                    ID OF OPEN LOAD B7
ZCNST    EQU   X'00'                    ZERO - SIGNALS INACTIVE ENTRY
WAOFF    EQU   32                       SIZE OF WA AT START OF WTG TAB
*
LOAD2Q   DC    C'2Q'                    ID OF MODULE IGG0192Q    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
FORCORE  DSECT                          OPEN/CLOSE/EOV WORK AREA Y01021
         IECDSECT                                                Y01021
         EJECT
         DCBD  DSORG=(IS)
IHADCW   IGGBISAM  OPTCD=V         DCB WORK AREA                 S20201
IHAWKNCP DSECT      WRITE KN CHANNEL PROGRAM REFERENCES
CB1      EQU   0                                                 S20201
CB10     EQU   0                                                 S20201
CB22     EQU   0                                                 S20201
CB23     EQU   0                                                 S20201
CB24     EQU   0                                                 S20201
CB25     EQU   0                                                 S20201
CJ11     EQU   0                                                 S20201
CP14     DS    0D
         IGGCP14  OPTCD=VW                                       S20201
LCP141   EQU   LCH3+LCH8+LCH13+LCH18                             S20201
LCP142   EQU   LCH22+LCH27+CH32+8-CH31                           S20201
LCP14    EQU   LCP141+LCP142                                     S20201
WC       EQU   LCH3C+LCH8C+LCH13C+LCH18C+LCH22C+LCH30            S20201
FOUR     EQU   X'04'                    4                        S20201
WRTCHK   EQU   X'80'                    WRITE CHECK MASK         S20201
CCOFF    EQU   X'BF'                    TURN OFF CMD CHAIN       S20201
         END
