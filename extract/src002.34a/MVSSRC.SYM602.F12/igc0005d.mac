         TITLE 'IGC0005D - SVC 54  DISABLE'
IGC0005D CSECT
*        RELEASE OS/VS2-02 DELETIONS                             Y02072
*D                                                              YM01299
*              VS2 RELEASE 040 CHANGES
* A010270,A010466-010468,A013820                                OZ03173
* D009041-009131,D009151                                        OZ05936
*C010670,A010685                                               @ZA19113
*                   RELEASE 21 DELETIONS
*                                                                A51488
*                                                                A53751
* 009140-009500,009100                                           A46391
*                                                               SA53798
*****007300,007400,009560,009580,010700,010800                    M4355
*
*STATUS CHANGE LEVEL 002
*                                                                     *
* FUNCTION/OPERATION -
*     1. RESET THE PSW TO USE THE SAME PROTECTION KEY AS THE CALLING
*        ROUTINE.
*     2. PICK UP THE ADDRESS OF THE PRIVILEGED ROUTINE FROM THE DEB
*        AND TRANSFER CONTROL.
*     3. UPON RETURN, RESTORE THE BASE ADDRESS OF THE CALLING ROUTINE.
*     4. EXIT
* ENTRY POINT - RELATIVE ADDRESS ZERO
* INPUT - N/A
* OUTPUT - N/A
* CALLING ROUTINE - NON-PRIVILEGED MACRO TIME ROUTINE
* EXTERNAL ROUTINES - PRIVILIGED MACRO-TIME ROUTINE
* EXITS - RETURN ON REGISTER 14
* TABLES/WORK AREAS- N/A
* ATTRIBUTES - TYPE II SVC
* NOTES - NONE
* GENERAL REGISTERS ARE USED AS FOLLOWS
R0       EQU   0                        REG 0
R1       EQU   1                        DCB ADDR                 A35340
R2       EQU   2                        DECB ADDRESS
R3       EQU   3                        WORK REGISTER
R4       EQU   4                        TCB ADDRESS              Y02072
R5       EQU   5                        ADDRESS OF REG SAVE AREA
R6       EQU   6                        DCBFA ADDRESS            A35340
R7       EQU   7                        TEMP WORK AREA ADDR      A47331
RBASE    EQU   8                        BASE REGISTER            Y02072
REXIT    EQU   9                        RETURN ADDR TO FLIH      Y02072
RWORK    EQU   10                       WORK REGISTER            Y02072
R11      EQU   11                       WORK REGISTER
R12      EQU   12                       BASE ADDR, PRIV MACRO TIME RTN
R13      EQU   13                       RETURN ADDR, PRIV MACRO-TIME RT
R14      EQU   14                       WORK REGISTER
R15      EQU   15                       BASE ADDR, NON-PRIV MACRO-TIME
* TABLES/WORK AREAS - DEB, DCB, DECB, TCB, REGISTER SAVE AREA
*        RELATIVE ADDRESSES WITHIN THESE AREAS ARE AS FOLLOWS
REGSAVER EQU   32                       DISPLACEMENT TO REG SAVE AREA
SHARE    EQU   X'80'                                             A35340
SCAN     EQU   X'40'                                             A35340
IDLEN    EQU   8                        LENGTH OF DFAID          A47331
BASEREG  EQU   92                                                A35340
*
*
WKNACT   EQU   X'80'                    WRITE KN IN PROG        SA53798
         USING IHADCB,R1                ADDRESSABILITY ON DCB
         USING IHADECB,R7               ADDRESSABILITY ON DECB   Y02072
         USING TCB,R4                   TCB ADDRESSABILITY       Y02072
         USING RBBASIC,R5               SVRB ADDRESSABILITY      Y02072
*
         BALR  RBASE,0                  ESTABLISH ADDRSSABILITY  Y02072
         USING *,RBASE                                           Y02072
*
         MODESET EXTKEY=RBT234,WORKREG=2  CHANGE TO USER KEY     Y02072
*
*              PROTECT KEY OF CALLER NOW IN REGISTER 2
*
*
*
*              IF DISP=SHR THEN REFRESH THE DCB FIELDS WITH THE
*              DCBFA AND EXIT IF SCAN, OTHERWISE CONTINUE ROUTINE
*
         LR    REXIT,R14                SAVE RETURN ADDRESS      Y02072
         TM    DCBMACRF,SCAN            TEST IF SCAN             A35340
         BZ    BISAM                    IF NOT, BR TO GET BISAM  A35340
*                                       PT                       A35340
         L     R6,DCBWKPT1              PT TO QISAM WORK AREA    A35340
         USING SCANWA,R6                ADDRESSABILITY SCAN WA
         L     R6,W1DCBFA               PT TO DCBFA              A35340
         USING DCBFA,R6                 ADDRESSABILITY ON FIELD AREA
         CLC   DFAID,DCBWKPT3           CHECK CORRECT ID         A47331
         BNE   NOREFRSH                 DO NOT REFRESH IF NOT    A47331
         B     COMMON                   BR TO MOVE FIELDS        A35340
BISAM    L     R6,DCBWKPT2              PT TO BISAM WORK AREA    A35340
         USING BISAMWA,R6               ADDRESSABILITY BISAM WA
         L     R7,RBGRS2                DECB ADDRESS             Y02072
         TM    DCWDCBFA,SHARE           TEST IF DISP=SHR         A35340
         BZ    CNTINU                   BR IF NOT TO COMPLETE    A35340
*                                       SVC RTN                  A35340
         SPACE 3
*   IF A WRITE KN IS IN PROGRESS ON THIS DCB, THE FIELD AREA WILL BE
*   UPDATED BY THE ASYNCHRONOUS ROUTINE TO RFLECT THE DCB SO A MERGE
*   IS UNNECESSARY AND DANGEROUS.
*
         TM    DCWWKNI,WKNACT           IS A WRITE KN ACTIVE    SA53798
         BO    CNTINU                   YES - SKIP MERGE        SA53798
         SPACE 2
         LR    R11,R6                   ADDR FOR COMPARE         Y02072
         L     R6,DCWDCBFA              ADDR OF DCBFA            A35340
         USING BISAMWA,R11              ADDRESSABILITY BISAM WA  Y02072
         USING DCBFA,R6                 ADDRESSABILITY FIELD AREA
         CLC   DFAID,DCWOPCLS           CHECK FOR CORRECT ID     A47331
         BNE   NOREFRSH                 NO REFRESH IF NOT        A47331
         DROP  R11                      END USING ON BISAM WA    Y02072
COMMON   EQU   *                           MOVE                  A51488
         DROP  R7                       END DECB ADDRESSABILITY  Y02072
*
         MODESET KEYADDR=KEY0,WORKREG=11     CHANGE TO KEY ZERO  Y02072
         SPACE 2
**********************************************************************
*    OBTAIN THE CMS LOCK, TO BE RELEASED AT STATEMENT FCMS.
**********************************************************************
         SPACE 2
GCMS     SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,  OBTAIN CROSS      Y02072*
               RELATED=(DCBFA,IGC054(FCMS)) MEMORY SERVICES LCK  Y02072
         SPACE 2
         MODESET KEYADDR=(2),WORKREG=11 CHANGE TO USER KEY       Y02072
         SPACE 2
         MVC   DCBRORG3(L'DCBRORG3+L'DCBNREC+L'DCBST),DFARORG3   Y02072
         MVC   DCBLPDA,DFALPDA             FIELDS                A35340
         MVC   DCBNBOV,DFANBOV             TO                    A35340
         MVC   DCBRORG2,DFARORG2           DCB                   A35340
         MVC   DCBNOREC(L'DCBNOREC+L'DCBLIOV+L'DCBRORG1),DFANOREC
         SPACE 2
         MODESET KEYADDR=KEY0,WORKREG=11  CHANGE TO KEY ZERO     Y02072
         SPACE 2
**********************************************************************
*    RELEASE CMS LOCK OBTAINED AT STATEMENT GCMS.                    *
**********************************************************************
         SPACE 2
FCMS     SETLOCK RELEASE,TYPE=CMS,      RELEASE CROSS MEMORY     Y02072*
               RELATED=(DCBFA,IGC054(GCMS))   SERVICES LOCK      Y02072
         SPACE 2
         MODESET  KEYADDR=(2),WORKREG=11 CHANGE TO TASK KEY      Y02072
         SPACE 2
         LR    R14,REXIT                RESTORE RETURN ADDRESS   Y02072
         DROP  R6
NOREFRSH EQU   *                                                 A47331
         TM    DCBMACRF,SCAN            TEST IF SCAN ISSUED SVC  A35340
         BZ    CNTINU                   IF NOT, EXECUTE ROUTINE  A38514
         SPACE 2
         MODESET  KEYADDR=KEY0,WORKREG=15  SET PROTECTION KEY 0  Y02072
         SPACE 2
         L     R15,BASEREG(R5)          RESTORE BASE REG         A35340
         BR    R14                      EXIT                     Y02072
*
*    BISAM REQUEST.  GO TO PRIVILEGED ROUTINE.
*
CNTINU   EQU   *                                                 A35340
         SPACE 2
**********************************************************************
*    USE BRANCH ENTRY TO DEBCHECK TO VALIDATE ADDRESS OF DEB.  INPUT *
*    TO DEBCHCK IS (1) DEB ADDRESS IN REG 1, (2) TCB ADDR IN REG 10, *
*    (3) RETURN ADDR IN REG 14, AND (4) ENTRY POINT IN REG 15.  ON   *
*    RETURN REGS 0-9 AND 12-14 REMAIN UNCHANGED.                     *
**********************************************************************
         SPACE 2
         DROP  R1                       END USING ON DCB         Y02072
         LR    R12,R1                   DCB ADDRESS              Y02072
         USING IHADCB,R12               DCB ADDRESSABILITY       Y02072
         SPACE
         MODESET KEYADDR=KEY0,WORKREG=1 CHANGE TO KEY ZERO       Y02072
         SPACE
BRDEBCHK L     R1,DCBDEBAD              DEB ADDR TO BE VALIDATED Y02072
         DROP  R12                      END USING ON DCB         Y02072
         LA    1,0(,1)                  CLEAR HI ORDER BYTE      Y02072
         LR    RWORK,R4                 TCB ADDR FOR DEBCHECK    Y02072
         L     R15,CVTPTR               CVT ADDRESS              Y02072
         USING CVT,R15                  CVT ADDRESSABILITY       Y02072
         L     R15,CVTEXT2              ADDR OF CVT EXTENSION    Y02072
         USING CVTXTNT2,R15             CVT EXT. ADDRESSABILITY  Y02072
         L     R15,CVTDEBVR             ENTRY POINT FOR DEBCHECK Y02072
         DROP  R15                      END USING ON CVT EXT.    Y02072
         SPACE 1
         BALR  R14,R15                  VALIDATE DEB ADDRESS     Y02072
         SPACE 1
         B     DEBVALID                 RETURNS HERE IF VALID    Y02072
*
*    DEBCHECK RETURNS TO THE FOLLOWING INSTRUCTION IF INVALID DEB.
*    RELEASE THE LOCAL LOCK AND ISSUE DEBCHK SVC.
*
NOTISDEB EQU   *                                                OZ03173
         LR    RWORK,R12                SAVE DCB ADDRESS         Y02072
FLCL1    SETLOCK RELEASE,TYPE=LOCAL,    RELEASE THE LOCAL LOCK   Y02072*
               RELATED=('HELD ON ENTRY VIA SVC TABLE')           Y02072
         SPACE 1
         LR    R1,RWORK                 DCB ADDR FOR DEBCHK SVC  Y02072
         DEBCHK (1),TYPE=VERIFY,AM=ISAM VERIFY DCB DEB POINTER   Y02072
         SPACE 1
*
*    OBTAIN THE LOCAL LOCK.  RETURNS ONLY IF DEB ADDRESS IS VALID.
*
GLCL1    SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,  GET LOCAL LOCK  Y02072*
               RELATED=('RETURN TO SUPVSR WITH LOCK HELD')       Y02072
         LR    R12,RWORK                RESTORE DCB ADDRESS      Y02072
         B     BRDEBCHK                 TAKE BR ENTRY TO DEBCHK  Y02072
         SPACE 2
**********************************************************************
*    USE BRANCH ENTRY TO GETMAIN TO GET PROTECTED SAVE AREA.         *
**********************************************************************
         SPACE 1
DEBVALID LR    RWORK,R1                 VALIDATED DEB ADDR       Y02072
         S     R1,FWTHREE               PTR TO DEB OWNER ID     OZ03173
         CLI   0(R1),X'80'              IS IT ISAM DEB?         OZ03173
         BNE   NOTISDEB                 NO-BR DEBCHK SVC        OZ03173
         USING PSA,R0                   LOW CORE ADDRESSABILITY  Y02072
         L     R7,PSAAOLD               ASCB ADDRESS             Y02072
         USING IHADEB,RWORK             DEB ADDRESSABILITY       Y02072
         L     R4,DEBTCBAD              TCB ADDRESS              Y02072
         LA    R0,IGGSIZE               LENGTH OF SAVEAREA       Y02072
         SPACE 2
*        GET SAVE AREA CORE
         GETMAIN RU,LV=(0),SP=SP230,KEY=05,BRANCH=YES            Y02072
         SPACE 2
         MODESET  EXTKEY=DATAMGT        SET DATA MGT KEY (5)     Y02072
         SPACE 2
         USING IGGSAVE,R1               SAVE AREA ADDRESSABILITY Y02072
         XC    IGGSAVE(IGGSIZE),IGGSAVE CLEAR SAVE AREA CORE     Y02072
         STM   REXIT,RWORK,IGGRETRN     SAVE RETURN ADDRESS,     Y02072
*                                       AND PROTECTED DEB ADDR   Y02072
         IC    R7,DEBPROTG              TASK PROTECTION KEY      Y02072
         STC   R7,IGGUKEY               TO PROTECTED SAVE AREA   Y02072
         DROP  R1                       END SAVE AREA USING      Y02072
         LR    R14,R1                   SAVE AREA ADDRESS        Y02072
         USING IGGSAVE,R14              SAVE AREA ADDRESSABILITY Y02072
         SPACE 2
         MODESET KEYADDR=IGGUKEY,WORKREG=11 CHANGE TO USER KEY   Y02072
         SPACE 2
L4       EQU   4                        LENGTH OF REGISTER
         USING IHADCB,R4                ADDRESSABILITY ON DCB
         SLR   R4,R4                    GET ADDR OF DCB        @ZA19113
         ICM   R4,7,DEBDCBAD+1          FROM DEB               @ZA19113
         L     R12,DEBEXPTR             DEB EXTENSION ADDR        M4355
         USING DEBEXT,R12               ISAM DEPENDANT SECTION    M4355
         L     R12,DEBDISAD             GET ADDR DISABLED RTN     M4355
         LM    R6,R11,RBGRS6            RESTORE REGISTERS        Y02072
         L     R2,RBGRS2                DECB ADDR FOR PRIV RTN   Y02072
         USING IHADECB,R2               DECB ADDRESSABILITY      Y02072
         DROP  RWORK                    END DEB ADDRESSABILITY
         SPACE 2
         BALR  R13,R12                  BRANCH TO DISABLED (PRIV) RTN
         SPACE 2
*    UPON RETURN, RESTORE BASE ADDRESS OF NON-PRIVILEGED MACRO-
*    TIME ROUTINE AND RETURN TO NON-PRIVILEGED ROUTINE.
         SPACE 2
         USING *,R13                    NEW BASE ADDRESS         Y02072
         LR    REXIT,R1                 SAVE IOB ADDRESSABILITY YM01299
         LR    R1,R14                   ADDR OF CORE TO FREE     Y02072
         L     RBASE,IGGRETRN           SAVE RETURN ADDR         Y02072
         L     R6,IGGPDEB               VALIDATED DEB ADDR       Y02072
         USING IHADEB,R6                DEB ADDRESSABILITY       Y02072
         L     R4,DEBTCBAD              TCB ADDRESS              Y02072
         USING PSA,R0                   LOW CORE ADDRESSABILITY  Y02072
         L     R7,PSAAOLD               ASCB ADDRESS             Y02072
         LA    R0,IGGSIZE               LENGTH OF CORE TO FREE   Y02072
         SPACE 2
         MODESET KEYADDR=KEY0,WORKREG=11  CHANGE TO KEY ZERO     Y02072
         SPACE 2
         FREEMAIN RU,LV=(0),A=(1),SP=SP230,KEY=05,BRANCH=YES     Y02072
*
         LR    R1,REXIT                 RESTORE IOB ADDRESS     YM01299
         L     R4,DEBDCBAD              RESTORE DCB ADDRESS      Y02072
         LR    R14,RBASE                RETURN ADDRESS           Y02072
         TM    DECBTYP2,DECBWKN         WAS IT A WRITE KN        Y02072
         BNE   A                        BR NOT WKN               Y02072
         L     R15,DCBLRAN
         BR    R14                      RETURN                   Y02072
A        L     R15,DCBLWKN
         BR    R14                      RETURN                   Y02072
*
SP230    EQU   230                      SAVE AREA SUBPOOL 230    Y02072
FWTHREE  DC    F'3'                     FW CONSTANT '3'         OZ03173
KEY0     DC    X'00'                    PROTECT KEY ZERO         Y02072
         DS    0H                       PATCH AREA ALIGNMENT     Y02072
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
IHADEB   IGGDEBD                        DEB DSECTS                M4355
         EJECT
*              DATA EVENT CONTROL BLOCK
IHADECB  DSECT
         DS    0F
DECBCODE DS    BL1                      WAIT BIT AND POST BIT
DECBECB  DS    CL3                      EVENT CONTROL BLOCK (ECB)
DECBTYP1 DS    BL1                      TYPE B6 - 1 IF LENGTH IS S
*                                            B7 - 1 IF AREA IS S
DECBTYP2 DS    BL1                           B0 - 1 IF READ K
*                                            B1 - 1 IF READ KX
*                                            B2 - 1 IF READ KU
*                                            B4 - 1 IF WRITE K
DECBWKN  EQU   X'04'                         B5 - 1 IF WRITE KN
DECBLGTH DS    CL2                      LENGTH OF BLOCK
DECBDCBA DS    A                        POINTER TO DCB
DECBAREA DS    A                        ADDRESS OF AREA
DECBLOGR DS    A                        POINTER TO LOGICAL RECORD
DECBKEY  DS    A                        POINTER TO KEY
DECBEXC1 DS    BL1                      EXCPTN CD B0-RECORD NOT FOUND
*                                                 B1-RECORD LGTH CHK
*                                                 B2-NO SPACE
*                                                 B3-INVALID REQUEST
*                                                 B4-UNCORRECTABLE IO
*                                                 B5-UNREACHABLE BLOCK
*                                                 B6-OVERFLOW RECORD
*                                                 B7-DUPLICATE
DECBEXC2 DS    BL1                                B7-READ KU
         SPACE 2
IGGSAVE  IGGBISAV
         EJECT
IHADCB   DCBD  DSORG=IS                 DCB
         EJECT
DCBFA    IGGDCBFA
         EJECT
BISAMWA  IGGBISAM
         EJECT
SCANWA   IGGSCAN
TCB      IKJTCB
         EJECT
         IKJRB
         EJECT
         IHAPSA
         EJECT
CVT      DSECT                                                   Y02072
         CVT                                                     Y02072
         EJECT
         IHASCVT                                                 Y02072
         END
