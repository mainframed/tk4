         TITLE 'IGG0192O - OPEN,BISAM WRITE KN,SET UP CHANNEL PROGRAMS'
IGG0192O CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192O                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, BISAM MODE FOR FIXED-LENGTH RECORDS   *
*                    WHEN USER PROVIDES WORK AREA                     *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = WHEN USER SUPPLIES WORK AREA, AND FIXED LENGTH RECORDS   *
*            ARE BEING PROCESSED, COMPLETE INITIALIZATION UNIQUE TO   *
*            WKN, WHETHER OR NOT READ IS PRESENT.  FOR UNBLOCKED      *
*            RECORDS, MOVE AND INITIALIZE CP12C (CP12CW FOR WRITE     *
*            CHECK), CP12A, AND CP12B.  FOR BLOCKED RECORDS, MOVE AND *
*            INITIALIZE CP13C (CP13CW FOR WRITE CHECK), CP13A, AND    *
*            CP13B.  IF WRITE CHECK, MOVE AND INITIALIZE CP123W.      *
*            DELETE ORIGINAL WKN CHANNEL PROGRAM MODULE.              *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 1028 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192O                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG0192Q      *
*              IF USER SUPPLIES WORK AREA, AND FIXED LENGTH RECORDS   *
*              ARE BEING PROCESSED.  RECEIVES CONTROL IN STORAGE      *
*              PROTECT KEY 5 AND PRIVILEGED STATE.                    *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG0192J IN STORAGE        *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGBISAM - BISAM WORK AREA                          *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY                                        *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, DELETE, AND XCTL                                 *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
RE       EQU   00                       WORK REG
RF       EQU   01                       WORK REG
RDCB     EQU   02                       USER'S DCB
RBASE    EQU   03                       BASE REG
RG       EQU   04                       WORK REG
RPAR     EQU   05                  *    PARAMETER LIST ADDRESS
RWTG     EQU   06                  *    WHERE TO GO TABLE BEGINNING
RPARC    EQU   07                  *    CURRENT ENTRY,PARAMETER LIST
RWTGC    EQU   08                  *    CURRENT ENTRY, WTG TABLE
RTIOT    EQU   09                       TIOT ADDRESS
RUCB     EQU   10                       UCB ADDRESS
RA       EQU   11                       WORK REG
RB       EQU   12                       WORK REG
RC       EQU   13                       WORK REG
RD       EQU   14                       WORK REG
R15      EQU   15                       WORK REG
RCORE    EQU   RG                       DCB WA ADDRESS
*                   *     MEANS SET BEFORE ENTRY AND BEFORE EXIT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         L     RDCB,0(RPARC)            RDCB  ADDRESS OF DCB
         USING IHADCB,RDCB
         L     RCORE,DCBWKPT2           RCORE ADDRESS OF DCB WA
         USING IHADCW,RCORE
         USING WTGDSECT,RWTGC           WHERE TO GO TABLE        Y02072
         L     RC,OPENWA                OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,RC               WA ADDRESSABILITY        Y02072
         L     RA,DXCCW9                ADDR OF CURR CP          Y02072
         L     RF,DXCCW10               CURR CP ADDR IN MODULE   Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=14  CHANGE TO USER KEY  Y02072
*
         L     RD,DCBWKPT3              ADDR OF WKN CP           Y02072
         DROP  RC                       DROP REGISTER            Y02072
         SR    RE,RE                    ZERO AREAS USED TO SAVE ADDR
         ST    RE,12(RD)                 OF CURRENT CP
         ST    RE,DCWFIOBU
         TM    DCBRECFM,X'10'
         BO    OPNO7D5Q                 BRANCH IF BLOCKED
         USING CP12C,RA                 INITIALIZE CP'S FOR UNBLOCKED
         MVC   0(SZ12C,RA),0(RF)       MOVE CP12C BASIC          S20201
*                                       STRUCTURE                S20201
         L     RC,CL2
         ALR   RC,RD
         ST    RC,CL2                   BEGIN INITIALIZING
         MVC   CL3+6(2),DCBBLKSI
         TM    DCBOPTCD,X'80'
         BZ    OPNO7D5K                 BRANCH NOT WC
         USING CP12CW,RA
         MVC   SZ12C(SZ12CWC,RA),SZ12C(RF)                       S20201
         L     RC,CL3B
         ALR   RC,RD
         ST    RC,CL3B                  INSERT WC COMMANDS
         MVC   CL3C+6(2),DCBBLKSI
         LA    RA,SZ12CWC(RA)           *                        S20201
         LA    RF,SZ12CWC(RF)           *                        S20201
OPNO7D5K LA    RA,SZ12C(RA)             *                        S20201
         LA    RF,SZ12C(RF)             MOVE AND INITIALIZE      S20201
         USING CP12A,RA                 CP12A
         MVC   0(SZ12A,RA),0(RF)        *                        S20201
         L     RG,DCBMSWA                                         17332
         LA    RG,2(RG)                                           17332
         STH   RG,CD1+2            STORE MSWA ADDR+2 IN CD1       17332
         SRL   RG,16                                              17332
         STC   RG,CD1+1                                           17332
         L     RC,CD2
         ALR   RC,RD
         ST    RC,CD2                   RELOCATE ADDR IN CD2
         SR    RC,RC
         IC    RC,DCBHIRPD
         SLL   RC,3                     AND CALCULATE ADDR
         LA    RC,16(RC)
         ALR   RC,RA
         ST    RC,CD2+4
         L     RB,DCBMSWA               OF CP12B,STORE IN CD2
         LA    RB,10(RB)                                          17332
         O     RB,CD3
         SR    RC,RC                    W&8,IL&AL TO CD3
         IC    RC,DCBKEYLE
         AH    RC,DCBBLKSI
         STM   RB,RC,CD3
         ALR   RB,RC                     CD4, ETC. REPEAT, ADDING COUNT
         LA    RE,1                       FIELD TO AREA ADDRESS, AS
         SLL   RE,28                      MANY TIMES AS RECORDS IN A
         OR    RB,RE                      PRIME DATA TRACK (DCBHIRPD).
         LA    RC,8(RC)
         SR    RE,RE
         IC    RE,DCBHIRPD
         B     OPNO7D5M
OPNO7D5L STM   RB,RC,CD3+8              RELOCATE REQD NO OF COMMANDS
         ALR   RB,RC
         LA    RA,8(RA)
OPNO7D5M BCT   RE,OPNO7D5L
         LA    RA,SZ12A(RA)             *                        S20201
         LA    RF,SZ12A(RF)             MOVE AND INITIALIZE      S20201
         USING CP12B,RA                 CP12B
         MVC   0(SZ12B,RA),0(RF)        *                        S20201
         L     RC,CE2                   RELOCATE ADDR IN CE2
         ALR   RC,RA
         ST    RC,CE2
         L     RG,DCBMSWA                                         17332
         LA    RG,2(RG)           STORE ADDR OF MSWA+2 IN CE4     M3225
         STH   RG,CE3+TWO               *                        S20201
         SRL   RG,16                                              17332
         STC   RG,CE3+ONE               *                        S20201
         MVC   CE4+SEVEN(ONE),DCBKEYLE  CE5                      S20201
         MVC   CE5+SIX(TWO),DCBBLKSI    CE6,CE7 AND AS MANY      S20201
         SR    RE,RE
         IC    RE,DCBHIRPD
         SR    RC,RC                    COMMANDS AS REQD,
         IC    RC,DCBKEYLE
         AH    RC,DCBBLKSI
         L     RB,DCBMSWA               AS FOR CP12A
         LA    RB,2(RB)                                           17332
         B     OPNO7D5P
OPNO7D5N DC    X'80000008'              CONSTANT FOR SETTING UP CCW S
OPNO7D5O LA    RB,8(RB)
         LA    RA,16(RA)
         STM   RB,RC,CE5                *                        S20201
         AR    RB,RC                    FORM AS MANY WRITE CKD'S
         ST    RB,CE4                   *                        S20201
         MVI   CE4,WCKD                                          S20201
         MVC   CE4+FOUR(FOUR),OPNO7D5N  *                        S20201
OPNO7D5P BCT   RE,OPNO7D5O              AS REQD BY DCBHIRPD
         LA    RA,SZ12B(RA)                                      S20201
         LA    RF,SZ12B(RF)             *                        S20201
         B     OPNO7D5T
         USING CP13C,RA                 INITIALIZE CP'S FOR BLOCKED
OPNO7D5Q MVC   0(SZ13C,RA),0(RF)        MOVE CP13C STRUCTURE     S20201
         L     RC,CL6
         ALR   RC,RD
         ST    RC,CL6                   RELOCATE ADDR IN CL6
         SR    RB,RB
         IC    RB,DCBHIRPD
         SLL   RB,3
         AL    RB,DCBMSWA               CL7 POINTS TO WA PLUS LGTH OF
         STH   RB,CL7+2
         SRL   RB,16                    ALL COUNT FIELDS
         STC   RB,CL7+1
         MVC   CL7+6(2),DCBBLKSI
         TM    DCBOPTCD,X'80'
         BZ    OPNO7D5V                 BRANCH NOT WC
         USING CP13CW,RA
         MVC   SZ13C(SZ13CWC,RA),SZ13C(RF)     MOVE WC COMMANDS  S20201
         L     RC,CL7B
         ALR   RC,RD
         ST    RC,CL7B
         MVC   CL7C+6(2),DCBBLKSI       AND INITIALIZE
         LA    RA,SZ13CWC(RA)           *                        S20201
         LA    RF,SZ13CWC(RF)           *                        S20201
OPNO7D5V LA    RA,SZ13C(RA)             *                        S20201
         LA    RF,SZ13C(RF)             MOVE AND INITIALIZE      S20201
         USING CP13A,RA                 CP13A
         MVC   0(SZ13A,RA),0(RF)        *                        S20201
         MVC   CF1+1(3),DCBMSWA+1        CF1
         L     RC,CF2                   RELOCATE CF2
         ALR   RC,RD
         ST    RC,CF2
         SR    RC,RC
         IC    RC,DCBHIRPD
         SLL   RC,4                     AND POINT IT TO CP13B
         LA    RC,8(RC)
         ALR   RC,RA
         ST    RC,CF2+4
         SR    RC,RC                    ADDR OF WA PLUS LGTH OF
         IC    RC,DCBHIRPD
         SLL   RC,3
         AL    RC,DCBMSWA               COUNT FIELD IS Z
         AH    RC,DCBLRECL
         LA    RC,0(RC)
         O     RC,CF3
         ST    RC,CF3
         MVC   CF3+6(2),DCBBLKSI        Z&RL,AND AL TO CF3
         L     RB,DCBMSWA
         LA    RB,8(RB)
         O     RB,CF4                   ADDR OF COUNT FIELD Y TO CF4
         SR    RE,RE
         IC    RE,DCBHIRPD
         B     OPNO7D5S
OPNO7D5R MVC   CF4+4(4),28(RF)
         ST    RB,CF4                   SET UP CF4 AND CF5
         AH    RC,DCBBLKSI              AND SUBSEQUENT PAIRS OF
         ST    RC,CF5
         MVC   CF5+4(4),CF3+4
         AH    RB,OPNO7D5N+2            READ C READ D ACCORDING
         LA    RA,SZ13A1(RA)                                     S20201
OPNO7D5S BCT   RE,OPNO7D5R
         LA    RA,SZ13A2(RA)            TO DCBHIRPD              S20201
         LA    RF,SZ13A(RF)             *                        S20201
         USING CP13B,RA                 CP13B
         MVC   0(SZ13B1,RA),0(RF)       MOVE AND INITIALIZE      S20201
         L     RC,CG2
         ALR   RC,RA
         ST    RC,CG2                   RELOCATE CG2
         SR    RE,RE
         IC    RE,DCBHIRPD
         L     RB,DCBMSWA
         LA    RB,0(RB)                 Y IS ADDR OF MSWA
         O     RB,16(RF)
         LR    RC,RE                    Z IS COUNT FIELDS PAST Y
         SLL   RC,3
         LA    RC,0(RC,RB)              M IS NO OF RCDS IN BLOCK
         LR    RG,RC                    MOVE Y TO CG4
         AH    RC,DCBBLKSI
         AH    RC,DCBRKP                Z&(M-1)RL&RKP TO CG5
         SH    RC,DCBLRECL
OPNO7D5W MVC   SZ13B1(SZ13B2,RA),SZ13B1(RF)                      S20201
         ST    RB,CG3                   Z TO CG6                 S20201
         ST    RC,CG4                   *                        S20201
         ST    RG,CG5                   *                        S20201
         MVC   CG4+SEVEN(ONE),DCBKEYLE  KEY LGTH TO CG5          S20201
         MVC   CG5+SIX(TWO),DCBBLKSI    BLKSIZE TO CG6           S20201
         AH    RB,OPNO7D5N+2
         AH    RC,DCBBLKSI              INCREMENT FOR NEXT
         AH    RG,DCBBLKSI              WRITE CKD TRIPLE
         LA    RA,SZ13B2(RA)            *                        S20201
         BCT   RE,OPNO7D5W              GENERALLY Y&(N-1)
         LA    RA,SZ13B1(RA)                                     S20201
         LA    RF,SZ13B(RF)             Z&(N-1)AL&(M-1)RL&RKP    S20201
OPNO7D5T TM    DCBOPTCD,X'80'           AND Z&(N-1)AL ARE MOVED
         BZ    OPNO7E5B                 BRANCH NOT WC
         USING CP123W,RA                MOVE AND INITIALIZE CP123W
         MVC   0(SZ123,RA),0(RF)        *                        S20201
         ST    RA,12(RD)                STORE ADDR FOR NEXT CP
         L     RC,CEB
         ALR   RC,RA                    RELOCATE ADDR IN CEB
         ST    RC,CEB
         SR    RE,RE
         IC    RE,DCBHIRPD              SET UP A READ CKD
         SR    RC,RC
         IC    RC,DCBKEYLE              COMMAND DCBHIRPD TIMES
         AH    RC,DCBBLKSI
         LA    RC,8(RC)
OPNO7D5U MVC   CEE(EIGHT),CEE-CEA00(RF) MOVE BASIC COMMAND       S20201
         STH   RC,CEE+6
         LA    RA,8(RA)                 MOVE IN 8&IL&AL
         BCT   RE,OPNO7D5U
         LA    RA,SZCKD(RA)             *                        S20201
         TM    DCBRECFM,X'10'
         BO    OPNO7E5A                 BRANCH FOR BLOCKED
         LA    RE,OPNMCPKN+48           ADDRESS FOR DELETION WC UNBL
         B     OPNO7E5
OPNO7E5A LA    RE,OPNMCPKN+56           ADDRESS FOR DELETION WC BL
         B     OPNO7E5
OPNO7E5B TM    DCBRECFM,X'10'           NOT WC  ADDRESS FOR DELETION
         BO    OPNO7E5C
         LA    RE,OPNMCPKN+32                UNBLOCKED
         B     OPNO7E5
OPNO7E5C LA    RE,OPNMCPKN+40                BLOCKED
*
OPNO7E5  MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         DELETE  EPLOC=(0)              DELETE CP MODULE         Y02072
*
OPNEND2O MVC   0(L'LOAD2J,RWTGC),LOAD2J ID OF EXEC IGG0192J      Y02072
         L     RCORE,4(RWTGC)
         USING FORCORE,RCORE            WORK AREA ADDR           Y02072
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT NAME ON THE WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)          AND DCB ON THE PARAM TABLE
         CLC   0(2,RWTGC),OPNEXNAM      IF NAME ON TABLE SAME ID AS
         BCR   8,RBASE                   CURRENT LOAD, BRANCH TO REPEAT
         CLC   0(2,RWTGC),OPIDCNST    IF NOT END OF WTG TABLE BRANCH
         BNE   RELOOP                    TO TRY NEXT NAME
         LR    RPARC,RPAR               OTHERWISE, RETURN TO START OF
         LA    RWTGC,WAOFF(0,RWTG)       BOTH TABLES AND SEARCH FOR
ZCHECK   CLI   0(RWTGC),ZCNST            FIRST NON-ZERO ID
         BNE   TCTLRTN
         LA    RWTGC,WGOFF(0,RWTGC)
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       WHEN FOUND, SET UP PARAMETERS
*                                        TO CALL THAT MODULE
         LA    R15,DXCCW12
TRCTL    XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) GO TO NEXT EXEC    Y02072
WGOFF    EQU   8                        SIZE OF ONE ENTRY ON WTG TABLE
PLOFF    EQU   4                        SIZE OF ONE ENTRY ON PARAM TAB
OPNEXNAM DC    C'2O'                    ID OF THIS OPEN EXECUTOR
OPIDCNST DC    C'0S'                    ID OF OPEN LOAD #7
ZCNST    EQU   X'00'                    ZERO - SIGNALS INACTIVE ENTRY
ONE      EQU   1                        MISC OFFSETS             S20201
TWO      EQU   2                        *                        S20201
FOUR     EQU   4                        *                        S20201
SIX      EQU   6                        *                        S20201
SEVEN    EQU   7                        *                        S20201
EIGHT    EQU   8                        *                        S20201
WAOFF    EQU   32                       SIZE OF WA AT START OF WTG TAB
         DS    0D
OPNMCPKN EQU   *-32                     LAST HALF OF
*                            TABLE OF WRITE KN CP MODULES
*                             WC * FU/FB * WA * REL ADDR
*                             ***************************
         DC    C'IGG019JR'    N  *  FU   * Y  *   32
         DC    C'IGG019JS'    N  *  FB   * Y  *   40
         DC    C'IGG019JT'    Y  *  FU   * Y  *   48
         DC    C'IGG019JU'    Y  *  FB   * Y  *   56
*
LOAD2J   DC    C'2J'                    ID OF MODULE IGG0192J    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         SPACE 4
         DCBD  DSORG=(IS)
IHADCW   IGGBISAM
IHAWKNCP DSECT      WRITE KN CHANNEL PROGRAM REFERENCES
         IGGCP8
CP12C    DS    0D
CP12CW   DS    0D
         IGGCP12C OPTCD=W                                        S20201
CP12A    DS    0D
         IGGCP12A
         ORG   CD1
CP12B    DS    0D
         IGGCP12B
CP13C    DS    0D
CP13CW   DS    0D
         IGGCP13C OPTCD=W                                        S20201
CP13A    DS    0D
         IGGCP13A
         ORG   CD1
CP13B    DS    0D
         IGGCP13B
CP123W   DS    0D
         IGGCP123 OPTCD=W                                        S20201
SZ12CWC  EQU   SZ12CW-SZ12C                                      S20201
SZ13CWC  EQU   SZ13CW-SZ13C                                      S20201
SZ13A2   EQU   CF5+8-CF3                                         S20201
SZ13B1   EQU   CG4+8-CG3                                         S20201
SZ13B2   EQU   CG5+8-CG3                                         S20201
SZ13A1   EQU   CF5+8-CF4                                         S20201
SZCKD    EQU   SZ123+16                                          S20201
WTGDSECT DSECT                                                   Y02072
IDTTR    DS    F                        MODULE IDTTR             Y02072
OPENWA   DS    A                        ADDR OF OPEN WORK AREA   Y02072
FORCORE  DSECT                                                   Y02072
         IECDSECT                                                Y02072
         END
