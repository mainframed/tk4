         TITLE 'IGG019GN - APPENDAGES-PART 2,COMBINED,NO WRT CHK'
IGG019GN CSECT
*          RELEASE OS/VS2-02 DELETIONS                                *
*          RELEASE 19 DELETIONS                                       *
*2180150800                                                      A29939
*          RELEASE 20 DELETIONS                                       *
*0568087600,103200-103400                                        A34932
*0568182400-183000                                               A34619
*0568014200,031400,032400,032600,032800,033000,038600,041600,    S20201
*0568046400,050800,052000,053000,053400-053800,054000-056000,    S20201
*0568068900,069500,075000,075400,076600,082200,082600,089400,    S20201
*0568089800,101400,105800,109600,110000,111400,111600-111800,    S20201
*0568114600,114800-115000,117400,130600,151800,152000-152800,    S20201
*0568153000-155600,160400,164200,170300,172600,176800,177000,    S20201
*0568200600-202400,202600-206400,206600-210400,210600-219400,    S20201
*0568221200-224000,224200-228000,228200-233000,233200-237000,    S20201
*0568237200-241000,241200-243000,243200-255200                   S20201
*          RELEASE 21 DELETIONS                                       *
*
*STATUS CHANGE LEVEL 009
* FUNCTION/OPERATION' APPENDAGE ROUTINES FOR BISAM WHEN READ AND UPDATE
*        IS USED WITH    WRITE KN,    WHEN WRITE VALIDITY CHECKING IS
*        NOT REQUESTED.
*        (PART 2)
*        CEND  - IF CHANNEL PROGRAM ENDS
*                **WITHOUT ERROR, EITHER
*                    PROCESSING COMPLETION WILL BE SCHEDULED,
*                    PROCESSING CONTINUATION WILL BE SCHEDULED,
*                    OR PROCESSING WILL CONTINUE.
*                **WITH A LENGTH CHECK, TESTS DETERMINE WHETHER
*                    OR NOT TO TREAT THE SITUATION AS A REAL
*                    ERROR.
*                **ANY OTHER ERROR, IN GENERAL, 'UNREACHABLE
*                    BLOCK' IS INDICATED IN THE DECB, AND
*                    PROCESSING COMPLETION WILL BE SCHEDULED.
*
*        ABEND - IF CHANNEL PROGRAM ENDS ABNORMALLY BECAUSE OF
*                **PERMANENT ERROR, 'UNCORRECTABLE I/O ERROR'
*                    IS INDICATED IN THE DECB, AND PROCESSING
*                    COMPLETION WILL BE SCHEDULED.
*                **NON-PERMANENT ERROR, ONE RE-TRY IS MADE
*                **FILE PROTECTION, TESTS DETERMINE WHETHER
*                    OR NOT TO TREAT THE SITUATION AS A REAL
*                    ERROR.
*                **ANY OTHER ERROR, IN GENERAL, 'UNREACHABLE
*                    BLOCK' IS INDICATED IN THE DECB, AND
*                    PROCESSING COMPLETION WILL BE SCHEDULED.
*
* ENTRY POINTS-
*        ANY POINT IN THE VECTOR TABLE AT THE BEGINNING OF THE MODULE.
* INPUT - N/A
* OUTPUT - N/A
* EXTERNAL ROUTINES - N/A
* EXITS  1. RETURN TO IOS VIA THE APPENDAGE RETURN VECTOR TABLE POINTED
*            TO BY REGISTER 14.  SEE THE DSECT LABELED IHAAPPRV FOR THE
*            FORMAT OF THIS TABLE AND THE USE OF EACH EXIT.
*        2. SCHEDULE AN ASYNCHRONOUS ROUTINE - BRANCH VIA THE EXIT
*            EFFECTOR ADDRESS IN THE COMMUNICATION VECTOR TABLE.
* TABLES/WORK AREAS ' DECB, DCB, IOB AND EXTENTION, DCB WA (DCW), DEB,
*        COMMUNICATION VECTOR TABLE.
*        SEE DSECTS AT END OF MODULE FOR FORMAT AND DESCRIPTIONS.
* ATTRIBUTES' REENTRANT. DISABLED.
* NOTES - NONE
* GENERAL REGISTERS ARE USED AS FOLLOWS BY THE CEND AND ABEND ROUTINES
R0       EQU   0                   *    -----
R1       EQU   1                   *    12* ADDRESS
R2       EQU   2                   *    IOB
R3       EQU   3                   *    DEB
R4       EQU   4                   *    DCB
R5       EQU   5                   *    -----
R6       EQU   6                   *    -----
R7       EQU   7                   *    UCB
R9       EQU   9                   #    WORK REGISTER
R10      EQU   10                       CHANNEL PROGRAM
R11      EQU   11                       DECB
R12      EQU   12                       WORK REGISTER
R13      EQU   13                       WORK REGISTER
R14      EQU   14                       IOS RETURN VECTOR TABLE
R15      EQU   15                       BASE REGISTER
*        * MEANS THIS REGISTER IS SET UP UPON ENTRY AND MAY NOT BE
*            DESTROYED BECAUSE OF THE NEEDS OF IOS
*        #     MEANS THIS REGISTER MAY BE CHANGED, BUT UPON RETURN MUST
*            BE IN THE FORM 000X BECAUSE OF THE NEEDS OF IOS
PERRMASK EQU   X'20'          TO TEST FOR PERMANENT ERROR, TEST
PERRYES  EQU   8               IOBECBAD, B2. IF OFF (CONDITION CODE 8)
PERRNO   EQU   7               THERE IS A PERM ERR. ELSE B2 ON ( CD 7)
BYP      EQU   12                       RETURN TO IOS/BYPASS      15924
EXCP     EQU   8                        RETURN TO IOS/EXCP        15924
NORMAL   EQU   0                        RETURN TO IOS/NORMAL      15924
K0       EQU  0                         MISC OFFSETS             S20201
K1       EQU  1                         *                        S20201
K2       EQU  2                         *                        S20201
K3       EQU  3                         *                        S20201
K4       EQU  4                         *                        S20201
K5       EQU  5                         *                        S20201
K6       EQU   6                        *                        S20201
K7       EQU   7                        *                        S20201
K10      EQU  10                        *                        S20201
NOT      EQU  255                       MASK OF ALL BITS ON      S20201
OFLORCD  EQU  2                                                  S20201
BLOCKED  EQU  X'10'                                              S20201
READKU   EQU   X'20'                    READ KEY FOR UPDATE     SA65011
MIS      EQU   PERRMASK                 MASTER IDX SRCH MASK
CCOFF    EQU   X'BF'                    TURN OFF CC MASK
         SPACE 2
         USING IHAIOB,R2                                         S20201
         USING IHADEB,R3
         USING IHADCB,R4
         USING IHAWKNCP,R10
         USING IHADECB,R11
         USING IHADCW,R12
         USING IGG019GN,R15
         USING IHACVTEX,R13             CVT
         SPACE 2
*         VECTOR TABLE.  BRANCH TO THE ROUTINE SELECTED IN THE
*           APPENDAGE ROUTINE, PART 1.
*        ROUTINE FOR          CODE      CEND COMPLETION OF
         LR    R15,R13
         B     APPM67CE       7         CP1 OR CP2 FOR WRITE KN
         LR    R15,R13
         B     APPN7B2        9,23      CP10A
         LR    R15,R13
         B     APPN8A2        10,11     CP10B
         LR    R15,R13
         B     APPM4A2        12        CP14 - SETUPS 1, 2, OR 5
         LR    R15,R13
         B     APPM5A2        13        CP14 - SETUPS 3, 4, OR 6
         LR    R15,R13
         B     APPM2B2        15        CP16 - SITUATION 2
         LR    R15,R13
         B     APPM3B2        16        CP16 - SITUATION 3
         LR    R15,R13
         B     APPN9A2        17,18,19  CP17
         LR    R15,R13                                            17332
         B     APPAFB                   CP14 PART2                17332
*        ROUTINE FOR          CODE      ABEND COMPLETION OF
         LR    R15,R13
         B     APPM67AE       7         CP1 OR CP2, WRITE KN
         LR    R15,R13
         B     APPN7J2        9,23      CP10A
         LR    R15,R13
         B     APPN8G2        10,11     CP10B
         LR    R15,R13
         B     APPM4E2        12        CP14 - SETUPS 1, 2, OR 5
         LR    R15,R13
         B     APPM5E2        13        CP14 -SETUPS 3, 4, OR 6
         LR    R15,R13
         B     APPM2H2        15,16     CP16
         LR    R15,R13
         B     APPN9G4        17,18,19  CP17
         LR    R15,R13                                            17332
         B     APPAEB                   CP14 PART2                17332
         B     APPN7A3                  CONTINUE ROUTINES BEGUN IN
         B     APPN7B4                   PART 1
*        ROUTINE FOR          CODE      CEND COMPLETION OF
         LR    R15,R13
         B     APPABA2        0,1       CP4/CP5
         LR    R15,R13
         B     APPADB2        2         CP7
         LR    R15,R13
         B     APPAAB2        3         CP1 OR CP2, READ AND UPDATE
         LR    R15,R13
         B     APPACA2        5         CP6
*        ROUTINE FOR          CODE      ABEND COMPLETION OF
         LR    R15,R13
         B     APPABD2        0,1       CP4/CP5
         LR    R15,R13
         B     APPADE2        2         CP7
         LR    R15,R13
         B     APPAAA2        3         CP1 OR CP2, READ AND UPDATE
         LR    R15,R13
         B     APPACE2        5         CP6
*              BRANCH TO M6 OR M7
APPM67CE L     R13,DCBWKPT2             CHAN END OF CP1 OR CP2
         CLI   11(R13),X'01'            NLSD=1 MEANS NO MAST IX
         BE    APPM6B2                       CYLINDER - M6 (CP2)
         B     APPM7A2                       MASTER - M7 (CP1)
APPM67AE L     R13,DCBWKPT2             ABNORMAL END OF CP1 OR CP2
         CLI   11(R13),X'01'            NLSD=1 MEANS NO MAST IX
         BE    APPM6J4                       CYLINDER - M6 (CP2)
         B     APPM7J2                       MASTER - M7 (CP1)
         EJECT
*              CHART M7  CP1 FOR WRITE KN, CODE 7
*              CHANNEL END
APPM7A2  LA    R13,C6+8
         CLR   R12,R13                  TEST STOP AT C6 - END OF SEARCH
         BE    APPM7B2                   OF MASTER INDEX. BRANCH IF YES
         LA    R13,C16+8
         CLR   R12,R13                  TEST STOP AT C16 - END OF SRCH
         BNE   APPN3B3                   OF CYLINDER INDEX. BRANCH NO
         TM    C18,X'20'                END OF CYLINDER SEARCH.
         BZ    APPM7C4                  TEST FOR DUMMY OR INACTIVE
         TM    C18,X'08'
         BZ    APPM7B3                  BRANCH IF INACTIVE OR DUMMY-END
         MVC   IOBDADAD,C17             DUMMY CHAINED.
         LA    R12,C10A                 RESTART CP TO SEARCH CYL S20201
         ST    R12,IOBSTART-1           INDEX ON CHAIN.
         B     APPRVXCP                 RETURN TO IOS TO EXCP
APPM7C4  MVC   IOBDADAD(7),C17          ACTIVE.
APPM7C45 EQU   *                                                 S20201
         MVI   IOBDADAD+7,X'00'         SET UP IOB FOR CP8
         MVI   IOBAPP,CODE8             M--R IS ADDR FOUND, R=0  S20201
         L     R10,DCBWKPT3             GET STARTING ADDR OF CP8 S20201
         ST    R10,CPSTART              STORE IN IOB AS CP START S20201
         ST    R10,IOBCCWAD             AND FOR USE AS BASE REG  S20201
         MVC   CB23+K3(K5),IOBDADAD+K3  CCHH OF FIRST TRACK, R=0 S20201
         OC    CB23+K5(K3),DCBFIRSH     SET HHR TO FIRSH         S20201
         MVI   IOBFLAG1,X'42'           APP CODE 8
         LA    R12,IOBDADAD+3           INITIALIZE CP8
         STH   R12,CB1+2                 CB1 SET TO SEEK CCHHR OF IOB
         SRL   R12,16
         STC   R12,CB1+1
         MVC   CB5+1(3),DECBKEY+1        CB5, CB15, CB19 ALL SET TO
         MVC   CB19+1(3),DECBKEY+1
APPM7G4  SR    R12,R12                  CHECK WITH DEB TO SEE IF NEXT
         IC    R12,IOBDADAD              ADDRESS ON SAME MODULE
         SLL   R12,4
         L     R12,32(R3,R12)
         LA    R12,0(R12)
         CLR   R12,R7
         BE    APPRVXCP                 IF SO, EXECUTE CP RETURN TO IOS
         MVI   IOBASYN,X'01'            IF NOT, SCHEDULE ASYNCHRONOUS
         B     APPN3B5                  ROUTINE TO EXCP
APPM7B2  EQU   *                        *
*
         TM    C6+K5,CC                 WAS C6 CHAINED
         BZ    APPAOS1                  NO, CONTINUE
         NI    C6+K5,CCOFF              TURN OFF INDICATOR
         OI    C6+K4,CC                 RECHAIN C6
         B     APPAOSJ3                 RESCHEDULE CP1
APPAOS1  EQU   *                        *
*
         TM    C9+K7,MIS                END MASTER INDEX SEARCH.
         BZ    APPM7C2                  TEST FOR DUMMY OR INACTIVE
APPM7B3  TM    C9+7,X'08'
         BO    APPM7E2                  BRANCH IF INACTIVE OR DUMMY-END
         IC    R12,C9+7                 DUMMY CHAINED.
         SLL   R12,29                   IF LEVEL INDEX NOT HIGHEST LVL,
         IC    R13,DCBNLEV               ERROR OF SOME SORT
         SLL   R13,29
         CLR   R12,R13
         BNE   APPN3B3
*                                       ALWAYS DO CP15
APPM7E3  MVI   IOBAPP,CODE14            TRK FULL--CP15 CODE      S20201
         MVC   IOBDADAD(3),DCBLPDA      SET UP IOB FOR CP15
         MVI   IOBDADAD+7,X'00'         INSERT ZERO INTO R
         MVC   IOBDADAD+3(4),DCBLETI    PUT DCBLETI CCHH INTO IOB
         OC    IOBDADAD+6(1),DCBFIRSH+3
         XC    IOBDADAD+6(1),DCBFIRSH+3  INCR TO NEXT CYL
         L     R10,DCBWKPT3                   HHR ZERO
         ST    R10,IOBCCWAD              START ADDR CI1
         LA    R12,CI1                   REL CCW 0  CP8
         ST    R12,IOBSTART-1           FLAGS INDICATE CC, NO DC
         MVI   IOBFLAG1,X'42'
         LA    R12,IOBDADAD+3           INITIALIZE CP15
         STH   R12,CI1+2                 CI1 POINTS TO IOB CCHHR
         SRL   R12,16
         STC   R12,CI1+1
         MVC   CI5+K2(K5),DCBLETI       GET CCHHR OF INDEX ENTRY S20201
         MVC   CI5(K2),DCBLPDA+K1       MOVE IN BB FOR HEAD SEEK S20201
         B     APPM7G4                  BRANCH TO EXCP
APPM7C2  TM    C9+7,X'04'               ACTIVE ENTRY.
         BO    APPM7E2                  IF LVL 2 MAST IND JUST SRCHED
         OI    C6+4,X'40'                CC ON TO CHN FROM LVL 1 TO CYL
APPM7E2  MVC   IOBDADAD,C8+7            SEEK ADDR IN IOB IS FND ADDR
         MVI   IOBFLAG1,X'42'
         B     APPRVXCP                 RETURN TO IOS TO EXCP
         EJECT
*              ABNORMAL END
APPM7J2  EQU   *                                                  19374
         TM    IOBSENSE+1,X'04'         FILE PROTECT?             19374
         BO    APPM7J3                  YES--BRANCH               19374
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR?          19374
         BC    PERRYES,APPN3B3          YES--BRANCH               19374
         B     APPRVNOR                 NO--RETRY CHANNEL PROGRAM 19374
APPM7J3  LA    R13,C10+8                FILE PROTECT
         CLR   R12,R13
         BNE   APPN3B3                  BRANCH IF STOP NOT AT C10
*
APPAOSJ3 EQU   *                        *
*
         MVI   IOBFLAG1,X'42'           RESTORE IOB FLGS & ERR CTR
         MVI   IOBFLAG2,X'00'             UNRELATED FLAG ON
         MVI   IOBCSW,X'00'               OTHERS OFF
         XC    IOBERRCT,IOBERRCT
         TM    C9+7,X'20'
         BO    APPM7B3                  BRANCH IF DUMMY OR INACTIVE
         LA    R12,C10A                 SET UP IOB TO RESTART    S20201
         ST    R12,IOBSTART-1
         NI    IOBFLAG1,X'FB'           RESET EXCEPTION FLAG
         B     APPM7E2                  BRANCH
         EJECT
*              M6 - CP2 FOR WRITE KN, CODE 7
*              CHANNEL END
APPM6B2  LA    R13,C35+8                B2   STOP AT C350
         CLR   R12,R13
         BNE   APPN3B3                       NO
         TM    C37,X'20'             C2      DUMMY OR INACTIVE{
         BZ    APPM6D2                       NO D2
         TM    C37,X'28'             C3      DUMMY CHAINED{
         BO    APPM6D3                       YES D3
         TM    DCBNLEV,X'01'         C4      NLEV=F{ F MUST BE 1
         BZ    APPN3B3                       NO
*                                       ALWAYS DO CP15
         MVI   IOBDADAD+7,X'00'         ZERO R  SET UP CP15
         MVC   IOBDADAD(3),DCBLPDA           MBB FROM LPDA
         MVC   IOBDADAD+3(4),DCBLETI    CCHH FROM LETI
         OC    IOBDADAD+6(1),DCBFIRSH+3
         XC    IOBDADAD+6(1),DCBFIRSH+3  INCREMENT TO A CYL BOUNDRY
         MVI   IOBAPP,CODE14            SET CODE 14 FOR CP15     S20201
         L     R10,DCBWKPT3          E5      CP8 ADDR
         LA    R12,CI1                       CP15 START
         ST    R12,IOBSTART-1                CHAN PROG START
         ST    R10,IOBCCWAD                  CCW O
         MVI   IOBFLAG1,X'42'           USE COMMAND CHAINING
         MVC   CI5+K2(K5),DCBLETI            COUNT ADDR          S20201
         MVC   CI5(K2),DCBLPDA+K1         MOVE IN BB FOR HEAD    S20201
*                                       SEEK                     S20201
         IC    R9,CI1                        SAVE
         LA    R12,IOBDADAD+3
         ST    R12,CI1                       IDENT ADDR
         STC   R9,CI1                        SAVE
         B     APPM7G4                  DO AN EXCP               S20201
APPM6D2  MVC   IOBDADAD(7),C36       D2      C36 MBBCCHH
         B     APPM7C45                 SET UP CP8               S20201
APPM6D3  MVC   IOBDADAD,C36          D3      C36 MBBCCHHR
         B     APPRVXCP              E3      EXCP RETURN TO IOS
         EJECT
*              ABNORMAL END
APPM6J4  TM    IOBECBAD,PERRMASK     J4      PERMANENT ERROR TEST
         BC    PERRYES,APPN3B3          YES  B3
         B     APPRVNOR              J5      NORMAL RETURN TO IOS
         EJECT
*                        CHANNEL END APP CODES 9&23 (CP10A) CHART N7
APPN7B2  LA    R13,CB50+8               B2  STOP AT CB500
         CLR   R12,R13
         BNE   APPM1B3                  NO   M1B3
APPN7A3  L     R12,DCBWKPT2             YES  SET SAME MODULE SWITCH ON
         OI    DCWWKNI,X'20'
         IC    R9,IOBDADAD+7         B3      ADD 1 TO R IN IOB
         LA    R12,1(R0,R9)
         STC   R12,IOBDADAD+7
         STC   R12,DCBLPDA+7            PUT NEW R IN LPDA         11081
APPN7B4  CLI   DCBHIRSH,X'00'          ARE ALL TRACKS UNSHARED    11081
         BE    APPN7C4                  YES  C4
         IC    R9,IOBDADAD+6            TRK SHARED
         IC    R12,DCBFIRSH+3           AND H AND MASK TO GET
         NR    R9,R12                   REAL TRK NO.
         EX    R9,CLIFIRSH
         BNE   APPN7C4                  NO   C4
         CLC   DCBHIRSH,DCBLPDA+7       YES  HIRSH R = LPDA R {
         BNE   APPN7C2                  NO   C2
APPN7D3  IC    R9,IOBDADAD+6            IS THIS LAST
         IC    R12,DCBFIRSH+3
         NR    R12,R9                   DATA TRACK OF THIS
         EX    R12,CLILDT               CYLINDER
         BE    APPN7D4                  YES, GO FIND NEXT CYL
         LA    R9,1(0,R9)               NO,
         STC   R9,IOBDADAD+6            ADD 1 TO TRACK
         MVI   IOBDADAD+7,X'00'      E2 NO   SET IOB R TO ZERO
APPN7G2  TM    DCBST,X'02'           G2      LAST BLOCK FULL{
         BZ    APPN7C2                  NO   C2
         OI    DCBST,X'01'           G3 YES  SET LAST TRACK FULL SW ON
         B     APPN7C2
APPN7D4  SR    R12,R12
         IC    R12,IOBDADAD             FIND CURRENT
         SLL   R12,4                          EXTENT
         AR    R12,R3
         OC    IOBDADAD+6(1),DCBFIRSH+3      SET TRACK TO MAX
         CLC   IOBDADAD+6(1),DEBENDHH+1-IHADEB(R12)   LAST TRACK IN EXT
         BNL   APPCCH                         YES
         IC    R9,IOBDADAD+6            ONLY 2301 WILL GO THIS PATH
         LA    R9,1(0,R9)               TO ADD 1 TO CYLINDER
         STC   R9,IOBDADAD+6
         B     APPN7G4
APPCCH   CLC   IOBDADAD+3(3),DEBENDCC-IHADEB(R12) LAST CYL OF
         BL    APPN7E4A                      EXTENT (CCH)
         IC    R9,IOBDADAD
         LA    R9,1(0,R9)               PUT NEW M IO IOB
         STC   R9,IOBDADAD
         LA    R12,16(0,R12)            ACCESS NEXT EXTENT INTRY IN DEB
         MVC   IOBDADAD+1(6),DEBBINUM-IHADEB(R12)   MOVE NEW BBCCHH
         L     R13,DEBUCBAD-IHADEB(R0,R12) IS NEW CYL ON SAME MODULE
         LA    R13,0(R0,R13)
         CLR   R13,R7
         BE    APPN7G4                  YES  G4
         L     R12,DCBWKPT2          F5 NO   SET SAME MOD SW OFF
         NI    DCWWKNI,X'DF'
         B     APPN7G4                       G4
APPN7E4A MVI   IOBDADAD+K6,K0           SET TRACK TO ZERO        S20201
         IC    R13,IOBDADAD+K3          PICK UP C1 OF CC         S20201
         SLL   R13,8                    SHIFT                    S20201
         IC    R13,IOBDADAD+K4          PICK UP C2 OF CC         S20201
         LA    R13,K1(R0,R13)           ADD ONE TO CYL VALUE     S20201
         STC   R13,IOBDADAD+K4          RESTORE C2               S20201
         SRL   R13,8                    SHIFT                    S20201
         STC   R13,IOBDADAD+K3          RESTORE C1               S20201
APPN7G4  OC    IOBDADAD+6(1),DCBFIRSH+1
         IC    R9,DCBFIRSH+2
         BCTR  R9,0
         STC   R9,IOBDADAD+7
         B     APPN7G2
CLILDT   CLI   DCBLDT+1,0               OBJECT OF EXECUTE
CLIFIRSH CLI   DCBFIRSH+1,0             OBJECT OF EXECUTE
APPN7C4  CLC   DCBHIRPD,DCBLPDA+7    C4      HIRPD R = LPDA R {
         BE    APPN7D3                  YES  D3
APPN7C2  MVC   CB55(4),IOBDADAD+3    C2 NO
         IC    R9,IOBDADAD+7
         LA    R12,1(R0,R9)
         STC   R12,CB55+4
         MVI   IOBFLAG1,X'42'           SET B1 ON (CC)
         LA    R12,CB52                      SET IOB START TO CP10B
         ST    R12,IOBSTART-1
         LA    R13,IOBDADAD+3
         STH   R13,CB52+2
         SRL   R13,16
         STC   R13,CB52+1
         CLI   IOBAPP,CODE9             IS APPENDAGE CODE 9      S20201
         BE    APPN7E1                  YES  E1
         MVI   IOBAPP,CODE11            SET APPENDAGE CODE TO 11 S20201
APPN7F1  L     R12,DCBWKPT2          F1      SAME MODULE SW ON{
         TM    DCWWKNI,X'20'
         BO    APPRVXCP                 YES  EXCP RETURN TO IOS
         MVI   IOBASYN,X'01'            NO   SET ASYNC CODE 1
         B     APPM1B5                       SCHED ASYNC ROUTINE
APPN7E1  MVI   IOBAPP,CODE10            SET APPENDAGE CODE TO 10 S20201
         B     APPN7F1                       F1
         EJECT
*                             ABNORMAL END APP CODE 9&23 (CP10A)CHARTN7
APPN7J2  TM    IOBECBAD,PERRMASK     J2      PERMANENT ERROR TEST
         BC    PERRNO,APPRVNOR          NO   CONTINUE, NORMAL IOS
         TM    IOBSENSE,X'08'        J3 YES  CAUSE DATA CHECK{
         BZ    APPM1B3                  NO   M1B3
         LA    R13,CB48+8
         CLR   R12,R13
         BE    APPN7J5                  YES  N7J5
         LA    R13,CB49+8
         CLR   R12,R13
         BE    APPN7J5                  YES  N7J5
         LA    R13,CB50+8
         CLR   R12,R13
         BE    APPN7J5                  YES  N7J5
         B     APPM1B3                  NO   M1B3
APPN7J5  OI    DECBEXC1,X'08'        J5     IND UNCOR I/O ERR
         B     APPM1B4
         EJECT
*                             CHANNEL END APP CODES 10&11(CP10B)CHARTN8
APPN8A2  LA    R13,CB54+8
         CLR   R12,R13
         BNE   APPM1B3                  NO   M1B3
         TM    CB53+4,X'20'             EOF FOR CP14?             13270
         BO    APPM426                                            17332
         L     R12,DCBNREC
         LA    R12,1(R0,R12)
         ST    R12,DCBNREC                   ADD 1 TO PRIME COUNT
         CLI   IOBAPP,CODE10            IS APPENDAGE CODE 10     S20201
         BE    APPM1B4                  YES  M1B4
         MVI   IOBAPP,CODE18            NO, SET APP CODE TO 18   S20201
         OI    IOBFLAG1,X'C0'                SET B0&B1 ON (CC&DC)
         LA    R12,CK1                       SET IOB START TO CP17
         ST    R12,IOBSTART-1
         MVC   IOBDADAD+3(5),DCBLETI         SET SEEK ADDR IN IOB
         MVC   IOBDADAD(3),DCBLPDA
         LA    R12,IOBDADAD+3        D2      INITIALIZE CP17
         ST    R12,CK1
         MVI   CK1,X'31'
         ST    R12,CK4
         MVI   CK4,X'31'
         MVC   CK6+1(3),DECBKEY+1
         B     APPM7G4
         EJECT
*                        ABNORMAL END APP CODES 10&11 (CP10B) CHART N8
APPN8G2  TM    IOBECBAD,PERRMASK     G2      PERMANENT ERROR TEST
         BC    PERRNO,APPRVNOR          NO   CONTINUE, NORMAL IOS
         TM    IOBSENSE,X'08'        G3 YES  CAUSE DATA CHECK{
         BZ    APPM1B3                  NO   M1B3
         LA    R13,CB55+8
         CLR   R12,R13
         BE    APPN7J5                  YES  N7J5
         B     APPM1B3                  NO   M1B3
         EJECT
*                        CHANNEL END APP CODES 17,18,19 (CP17) CHART N9
APPN9A2  LA    R13,CK1                  CP START MAY HAVE BEEN   A34932
*                                       CHANGED                  A34932
         ST    R13,IOBSTART-1              FOR RETRY ON ERROR.   A34932
         LA    R13,CK7+8             A2   STOP AT CK70           A34932
         CLR   R12,R13
         BNE   APPM1B3                  NO   M1B3
         CLI   CK9,X'00'             A3      TEST F IN CK9
         BE    APPN9B3                       NORMAL TRACK INDEX
         CLI   CK9,X'08'
         BE    APPN9B3                       SHARED
APPN9B2  EQU   *
         L     R12,DCBWKPT2
         CLI   IOBAPP,CODE17            IS APPENDAGE CODE 17     S20201
         BE    APPM1B4                  YES  M1B4
         CLI   IOBAPP,CODE18            IS APPENDAGE CODE 18     S20201
         BE    APPN9C2                  YES  C2
         CLC   DCWNLEVC,DCBNLEV      E2 NO   (19)NLEV CTR = DCBNLEV×
         BE    APPN9F1                  YES
         IC    R13,DCWNLEVC          G2 NO   ADD 1 TO NLEV COUNTER
         LA    R13,1(R0,R13)
         STC   R13,DCWNLEVC
         CLI   DCWNLEVC,X'02'           TEST NLEV COUNTER = 2
         BE    APPN9E3                   BRANCH IF YES
         CLI   DCWNLEVC,X'03'           TEST NLEV COUNTER = 3
         BE    APPN9F3                   BRANCH IF YES, ELSE = 4
         MVC   IOBDADAD+3(5),DCBLEMI3    G3  SET SEEK ADDR TO LEMI3
         CLI   DCBFTMI3+1,X'00'
         BE    *+14
         MVC   IOBDADAD(1),DCBFTMI3+1
         B     APPN9C5
         MVC   IOBDADAD(1),DCBFTMI3
         B     APPN9C5
APPN9E3  MVC   IOBDADAD+3(5),DCBLEMI1   E3  SET SEEK ADDR TO LEMI1
         CLI   DCBFTMI1+1,X'00'
         BE    *+14
         MVC   IOBDADAD(1),DCBFTMI1+1
         B     APPN9C5
         MVC   IOBDADAD(1),DCBFTMI1
         B     APPN9C5
APPN9F3  MVC   IOBDADAD+3(5),DCBLEMI2   F3  SET SEEK ADDR TO LEMI2
         CLI   DCBFTMI2+1,X'00'
         BE    *+14
         MVC   IOBDADAD(1),DCBFTMI2+1
         B     *+10
         MVC   IOBDADAD(1),DCBFTMI2
         XC    IOBDADAD+1(2),IOBDADAD+1
         B     APPN9C5                       C5
APPN9B3  CLI   CB26,X'10'            B3      TEST F IN CB26
         BNE   APPN9B2                       NOT OVERFLOW END
         MVC   IOBDADAD+3(5),CB24    C3      OVERFLOW ENTRY
         B     APPRVXCP              C4      EXCP RETURN TO IOS
APPN9F1  TM    DCWHIAV,X'40'                 HIGH IND IN CORE?
         BZ    APPN9H1                  NO   H1
         L     R12,DCWMSHIL          G1 YES  CHNG KEY OF LST CORE INDEX
         L     R11,DECBKEY
         IC    R9,DCBKEYLE
         LA    R13,0(R0,R9)
         BCTR  R13,R0
         EX    R13,APPN9G1A
APPN9H1  MVI   IOBASYN,X'08'         H1      SET ASYNC CODE 8
         B     APPM1B5                       SCHED ASYNC ROUTINE
APPN9C2  CLI   DCBNLEV,X'00'         C2      DCBNLEV = 0 {
         BE    APPM1B4                  YES  M1B4
         L     R12,DCBWKPT2             R12  ADDRESS DCB WA
         MVI   DCWNLEVC,X'01'        D2 NO   SET NLEV COUNTER TO 1
         MVC   IOBDADAD+3(5),DCBLECI     D3  SET SEEK ADDR TO LECI
         CLI   DCBFTCI+1,X'00'
         BE    *+14
         MVC   IOBDADAD(1),DCBFTCI+1
         B     APPMVI
         MVC   IOBDADAD(1),DCBFTCI
         XC    IOBDADAD+1(2),IOBDADAD+1
APPMVI   MVI   IOBAPP,CODE19            SET APPENDAGE CODE TO 19 S20201
         OI    IOBFLAG1,X'C0'                SET B0&B1 ON (CC&DC)
         LA    R12,CK1                       SET IOBSTART TO CK1
         ST    R12,IOBSTART-1
APPN9C5  MVI   IOBASYN,X'01'         C5      SET ASYNC CODE 1
         B     APPM1B5                       SCHED ASYNC ROUTINE
         EJECT
*                        ABNORMAL END APP CODES 17,18,19 (CP17)CHART N9
APPN9G4  TM    IOBECBAD,PERRMASK     G4      PERMANENT ERROR TEST
         BC    PERRYES,APPM1B3        YES   M1B3                 A34932
* THIS IS A FIRST ENTRY ON AN ERROR.
         LA    R11,CK4                  TEST FOR STOP PRIOR TO   A34932
*                                       CK4                      A34932
         CLR   R12,R11                  DID IT STOP BEFORE CK4?  A34932
         BNH   APPRVNOR                 BIF YES. RETRY FROM CK1. A34932
         ST    R11,IOBSTART-1           RETRY FROM CK4           A34932
         B     APPRVNOR                 RETURN TO IOS.           A34932
APPN9G1A MVC   0(0,R12),0(R11)
         EJECT
*        COMMON ROUTINE TO SCHEDULE ASYNCHRONOUS ROUTINE
APPM1B3  OI    DECBEXC1,X'04'        B3      SET DECB EXCD TO UNRCHABLE
APPM1B4  MVI   IOBASYN,X'08'         B4      SET ASYNC CODE TO 8
APPM1B5  B     BYP(R14)                 SCHEDULE ASYNCH ROUTINE  Y02072
         EJECT
*                        CHANNEL END APP CODE 15 (CP16) CHART M2
APPM2B2  LA    R13,CJ10+L'CJ10          B2   STOP AT CJ100
         CLR   R12,R13
         BNE   APPM1B3                  NO   M1B3
         L     R12,DCBMSWA           C2 YES  OFLO END ENTRY?
         TM    8(R12),X'08'
         BZ    APPM2D3                  YES  D3
         MVC   IOBDADAD,0(R12)       E2 NO   SET SEEK ADDR FROM LINK
         B     APPM7G4
APPM2D3  MVC   CJ11,IOBDADAD         D3      PUT SEEK ADDR IN CJ11
         MVI   IOBASYN,X'0B'                 SET ASYNC CODE 11
         NI    IOBINDCT,X'DF'                SET B2-OFLO RCD FR AREA
         B     APPM1B5                       SCHED ASYNC ROUTINE
         EJECT
*                        ABNORMAL END APP CODES 15,16,(CP16) CHART M2
APPM2H2  TM    IOBECBAD,PERRMASK     H2      PERMANENT ERROR TEST
         BC    PERRNO,APPRVNOR          NO   CONTINUE, NORMAL IOS
         B     APPM1B3                  YES  M1B3
         EJECT
*                        CHANNEL END APP CODE 16 (CP16) CHART M3
APPM3B2  LA    R13,CJ10+L'CJ10          B2   STOP AT CJ10
         CLR   R12,R13
         BNE   APPM3B3                  NO - TRY CJ9             S20201
         MVC   CJ11,IOBDADAD                 SAVE SEEK ADDR IN CJ11
         L     R12,DCBMSWA           D2 YES  SET IOB SEEK FROM LINK
         MVC   IOBDADAD,0(R12)
         L     R12,DCBWKPT2          E2      SET FIRST TIME ONLY SW OFF
         NI    DCWWKNI,X'BF'
         B     APPM7G4
APPM3B3  LA    R13,CJ9+L'CJ9           STOP AT CJ9
         CLR   R12,R13                                           S20201
         BNE   APPM3B4                  NO - TRY CJ8             S20201
         MVC   CJ11,IOBDADAD                 SAVE SEEK ADDR IN CJ11
         TM    DCBOPTCD,X'02'        C3 YES  DELETE OPTION{
         BZ    APPM3C4                  NO   C4
         L     R12,DCBMSWA           D3 YES  RCD MARKED FOR DELETION{
         LA    R13,10(R0,R12)
         CLI   0(R13),X'FF'
         BNE   APPM3C4                  NO   C4
         MVI   IOBASYN,X'0E'         E3 YES  SET ASYNC CODE 14
APPM3E4  NI    IOBINDCT,X'DF'        E4      SET B2-OFLO RCD FR AREA
         B     APPM1B5               E5      SCHED ASYNC ROUTINE
APPM3C4  OI    DECBEXC1,X'01'        C4      SET EXCEP CODE TO IND DUPL
         MVI   IOBASYN,X'08'         D4      SET ASYNC CODE 8
         B     APPM1B5                       SCHED ASYNC ROUTINE
APPM3B4  LA    R13,CJ8+L'CJ8            SEE IF STOPPED AT CJ8
         CLR   R12,R13                                           S20201
         BNE   APPM1B3                  IF NOT, UNREACHABLE BLK  S20201
         L     R12,DCBWKPT2          C5 YES  FIRST TIME ONLY SW ON{
         TM    DCWWKNI,X'40'
         BO    APPM3B5                  YES  B5
         MVI   IOBASYN,X'0C'         D5 NO   SET ASYNC CODE 12
         B     APPM3E4                       E4
APPM3B5  MVI   IOBASYN,X'0D'         B5      SET ASYNC CODE 13
         MVC   IOBDADAD(7),CJ11         RESTORE TRK INDEX MBBCCHH
         B     APPM3E4
         EJECT
*                             CHANNEL END APP CODE 12 (CP14) CHART M4
APPM4A2  LA    R13,CH18+8               DID CHAN PROG STOP AT CH18
APPM4A21 EQU   *                                                  13270
         TM    CB53+4,X'20'             EOF WRITTEN               13270
         BZ    APPM4A22                 BRANCH IF NO              13270
         MVC   IOBDADAD,DCBLIOV         INITIALIZE IOBDADAD SEEK  13270
         MVC   DCBLIOV(3),CH23          RESET LIOV FROM           13270
         MVC   DCBLIOV+3(5),CH24        CP14                      13270
         CLR   R12,R13                                            13270
         BNE   APPM1B3                  B = UNREACHABLE           13270
         MVI   IOBFLAG1,X'42'          CC SET ON                  13270
         MVI   IOBAPP,CODE10            SET APPENDAGE CODE 10    S20201
         LA    R12,CB52                                           13270
         ST    R12,IOBSTART-1                                     13270
         LA    R13,IOBDADAD+3                                     13270
         STH   R13,CB52+2                                         13270
         SRL   R13,16                                             13270
         STC   R13,CB52+1                                         13270
         SR    R13,R13                                            16384
         IC    R13,IOBDADAD             GET M                     16384
         SLL   R13,4                    TIMES 16                  16384
         LA    R13,32(R3,R13)           ALLOW FOR BASIC DEB TOO   16384
         L     R12,0(0,R13)             GET UCB POINTER           16384
         LA    R12,0(0,R12)             ZERO HIGH ORDER BYTE      16384
         CR    R7,R12                   SAME UNIT                 16384
         BE    APPRVXCP                 YES--EXCP RETURN          16384
         MVI   IOBASYN,X'01'            NO--SET ASYNCH CODE = 1   16384
         B     APPM1B5                  SCHEDULE ASYNCHRONOUS RTN 16384
APPM426  CLI   CK9+7,12                INSERT TO MIDDLE  OVFL CH  17332
         MVI   CB53+4,X'00'            RESET EOF FLAG             17332
         BE    APPM1B4                 IF YES POST COMPLETION     17332
APPM4A22 EQU   *                                                  13270
         CLR   R12,R13                                            13270
         BNE   APPM1B3                  B = UNREACHABLE           13270
         TM    CH8E+4,X'40'            TEST FOR UWA               17332
         BZ    APPM425                 IF NO-BRANCH               17332
         NI    CH8E+4,X'42'            RESET  FLAG                17332
         CLI   CK9+7,14                 ASYN CODE=14              17332
         BE    APPM1B4                  IF YES-POST COMPLETION    17332
         CLI   CK9+7,9                  ASYN CODE=9               17332
         BE    APPM4A23                 IF YES BRANCH             17332
         MVI   IOBAPP,X'04'             SET UP CP14 PART2         17332
         MVC   IOBSTART,CH21+4          TO UPDATE TRACK           17332
         MVC   IOBDADAD,CH14           RESTORE IOBSEEK            17332
         MVI   CH14,X'03'               SET NOP IN CH14           17332
         MVI   CH14+4,X'20'                                       17332
         MVI   CH14+7,X'01'                                       17332
         B     APPN9C5                                            17332
APPM4A23 MVI   IOBCOUNT,X'01'           ERROR COUNT=1             17332
         MVC   IOBDADAD(3),CB10+7       IOBSEEK ADDR              17332
         MVC   IOBDADAD+3(5),CB23+3                               17332
         IC    R12,IOBDADAD+7           REDUCE R BY 1             17332
         BCTR  R12,R0                                             17332
         STC   R12,IOBDADAD+7                                     17332
         TM    DCBRECFM,X'10'          TEST FOR BLOCKED           17332
         BO    APPM424                 IF YES-BRANCH              17332
         L     R12,CH6+4               RESTORE 10 BYTES           17332
         L     R13,DECBAREA            USED FOR LINK FIELD IN     17332
         MVC   0(10,R12),0(R13)        OVFL RECORD                17332
APPM424  MVC   IOBSTART,CD2+5           START OF CP12B            17332
         MVI   IOBAPP,X'15'             APP CODE 15               17332
         OI    IOBFLAG1,X'C0'                                     17332
         B     APPN9C5                                            17332
APPM425  L     R12,DCBWKPT2                                       17332
         TM    DCWWKNI,X'10'            IS  ADD-TO-END INDICATOR ON
         BZ    APPM1B4                  NO - SCHEDULE COMPLETION
APPM4C2  MVC   CK6+1(3),DECBKEY+1  IF SO, SET ADDR OF CK6 TO DECB KEY
         LA    R12,IOBDADAD+3           SET UP CP17
         STH   R12,CK1+2
         SRL   R12,16
         STC   R12,CK1+1
         MVC   CK4+1(3),CK1+1
         B     APPN9C2             BRANCH
         EJECT
*                             ABNORMAL END APP CODE 12 (CP14) CHART M4
APPM4E2  EQU   *                                                  19374
         TM    IOBSENSE+1,X'04'         FILE PROTECT?             15924
         BO    APPM4I2                  YES BRANCH                15924
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR?          19374
         BC    PERRNO,APPRVNOR          NO--RETRY CHANNEL PROG    19374
         TM    IOBSENSE,X'08'
         BZ    APPM1B3             BRANCH IF CAUSE NOT DATA CHECK
APPM4G2  LA    R13,CH16+8               IF STOP NOT AT CH16, CH17, OR
         CLR   R12,R13                   CH18, BRANCH
         BE    APPM4H2
         LA    R13,CH17+8
         CLR   R12,R13
         BE    APPM4H2
         LA    R13,CH18+8
         CLR   R12,R13
         BNE   APPM1B3
APPM4H2  OI    DECBEXC1,X'08'           IF SO, SET UNCORRECTABLE I/O
         B     APPM1B4                   ERROR IN EXCEP CODE IN DECB
APPM4I2  LA    R13,CH14+8               DID CHAN PROG STOP AT CH14
          CLR   R12,R13                 BE MEANS YES, BNE MEANS NO
         BNE   APPM1B3                  NO - RECORD IS UNREACHABLE
         MVC   IOBDADAD,CH23            MOVE CH23 MBBCCHHR TO IOB
         MVC   IOBSTART,IOBCSW+1        RESUME PROCESSING AT CH15
         MVI   IOBASYN,X'01'
         B     APPM1B5                  EXCP IN ASYNCHRONOUS ROUTINE
         EJECT
*              CHART M5  COMPLETION OF CP14, CODE 13
*              CHANNEL END
APPM5A2  LA    R13,CH22+8               DID CHAN PROG STOP AT CH22
         B     APPM4A21
         EJECT
*              ABNORMAL END
APPM5E2  EQU   *                                                  19374
         TM    IOBSENSE+1,X'04'         FILE PROTECT?             15924
         BO    APPM5I2                  YES BRANCH                15924
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR?          19374
         BC    PERRNO,APPRVNOR          NO--RETRY CHAN PROG       19374
         TM    IOBSENSE,X'08'      IF YES,
         BZ    APPM1B3              BRANCH IF CAUSE NOT DATA CHECK
         LA    R13,CH22+8
         CLR   R12,R13
         BE    APPM4H2              BRANCH IF STOP AT CH22
         B     APPM4G2              BRANCH OTHERWISE.
APPM5I2  LA    R13,CH19+8               DID CHAN PROG STOP AT CH19
          CLR   R12,R13                 BE MEANS YES, BNE MEANS NO
         BNE   APPM4I2                  NO - BRANCH TO TEST FOR CH14
         MVC   IOBDADAD,CJ11            MOVE CJ11 MBBCCHHR TO IOB
         MVC   IOBSTART,IOBCSW+1        RESUME PROCESSING AT CH20
         MVI   IOBASYN,X'01'
         B     APPM1B5                  EXCP IN ASYNCHRONOUS ROUTINE
         EJECT                                                  , 17332
*              NORMAL END APPENDAGE CODE 4
*              UPDATE TRACK INDICES WITH 2ND PART CP14
*
*
APPAFB   LA    R13,CH14+8               DID CP END AT CH14        17332
         CLR   R12,R13                                            17332
         BNE   APPN3B3                  NO-RECORD IS UNREACHABLE  17332
         MVC   IOBASYN,CK9+7                                      17332
         CLI   IOBASYN,10          ADD TO END                     17332
         BE    APPM4C2             IF YES-GO UPDATE MSTR INDICES  17332
         CLI   IOBASYN,11          ADD TO END                     17332
         BE    APPM4C2             IF YES-GO UPDATE MSTR INDICES  17332
         B     APPM1B4             POST COMPLETION                17332
         EJECT                                                  , 17332
*
*              ABNORMAL END  APPENDAGE CODE 4
*              UPDATE  TRACK INDICES
*
APPAEB   TM    IOBECBAD,PERRMASK        PERM ERROR                17332
         BC    PERRNO,APPRVNOR          IF NO-NORMAL RET TO IOS   17332
         B     APPM1B3                  POST UNREACHABLE BLOCK    17332
         EJECT
*           READ AND UPDATE APPENDAGE MODULES         *********
*
APPHCA14 DC    A(CA14-IHACP47+8)
APPHCA20 DC    A(CA20-IHACP47+8)        CA20 OFFSET              S20201
APPHCA23 DC    A(CA23-IHACP47+8)
APPHCA24 DC    A(CA24-IHACP47+8)
APPHCA31 DC    A(CA31-IHACP47+8)
APPCA31B DC    A(CA31B-IHACP47+8)       CA31B OFFSET             S20201
APPHCA36 DC    A(CA36-IHACP47+8)
APPHCA43 DC    A(CA43-IHACP47+8)
APPHCA10 DC    A(CA10-IHACP47+8)        CA10 OFFSET
*
*        CHANNEL END APP CODE 3 (CP2) CHART AA
         USING CP2,R10
APPAAB2  L     R13,DCBWKPT2             R13  ADDR DCB WA
         USING IHADCW,R13
         CLI   DCWNLSD,X'01'            BRANCH IF NLSD NOT 1, I.E.
         BH    APPAEB2                   IF CP1 IS BEING USED
         LA    R13,C35+8
         CLR   R12,R13
         BNE   APPAAB3                  NO   B3
         TM    C37,X'20'                YES  DUMMY OR INACTIVE TEST
         BO    APPAAC3                  YES  C3
         MVC   IOBDADAD,C36             NO   C36 TO IOBDADAD
         MVI   IOBASYN,X'03'         E2      ASYNC CD SET=3 (G CP1,2)
         B     APPABSAR
APPAAC3  TM    C37,X'08'                TEST DUMMY CHAINED
         BO    APPAAD3                  YES  D3
         IC    R12,C37                  NO   TEST NLEV PER F = DCBNLEV
         NI    C37,X'07'
         CLC   DCBNLEV,C37
         STC   R12,C37
         BNE   APPAAB3                  NO   B3
         OI    DECBEXC1,X'80'        C5 YES  DECB EXCEP SET=NO RCD FND
APPAAD4  MVI   IOBASYN,X'07'         D4      ASYNC CD SET=7 (NG CP1,2)
         B     APPABSAR                      SAR
APPAAD3  MVC   IOBDADAD,C36             C36 TO IOBDADAD
         B     APPRVXCP                      EXCP RETURN TO IOS
APPAAB3  OI    DECBEXC1,X'04'        B3      DECB EXCEP SET=UNREACHABLE
         B     APPAAD4                       D4
*
*        ABNORMAL END APP CODE 3 (CP2) CHART AA
*
APPAAA2  L     R13,DCBWKPT2            R13  ADDR DCB WA
         CLI   DCWNLSD,X'01'            BRANCH IF NLSD NOT 1, I.E.
         BNE   APPAEJ2                    IF CP1 IS BEING USED
APPAAA3  TM    IOBECBAD,PERRMASK        TEST PERM ERR
         BO    APPRVNOR                 NO, NORMAL IOS RETURN     15924
         B     APPAAB3                  YES BRANCH TO INDICATE UNRCHBLE
         DROP  R13
*
*              CHART AE                 END CP1
*              CHANNEL END
         USING CP1,R10
APPAEB2  LA    R13,C6+8                 TEST FINAL CCW EQUAL C6
         CLR   R12,R13
         BE    APPAEC2                  BRANCH IF YES
         LA    R13,C16+8                TEST EQUAL C16
         CLR   R12,R13
         BE    APPAEC4                  BRANCH IF YES
         B     APPAAB3                  ERROR IF NO
*              INTERRUPT AT C6
APPAEC2  EQU   *                        *
*
         TM    C6+K5,CC                 WAS C6 CHAINED
         BZ    APPAOS8                  NO, CONTINUE
         NI    C6+K5,CCOFF              TURN OFF INDICATOR
         OI    C6+K4,CC                 RECHAIN C6
         CLI   C10,SEEKHH               C10 SET TO DO SEEK HEAD
         BNE   APPAOSJ4                 NO, FILE PROTECT
         LA    R13,C10A                 RESET IOBSTART TO
         ST    R13,IOBSTART-K1          RESTART AT C10A
         B     APPAEG2                  RESCHEDULE CP1
APPAOS8  EQU   *                        *
*
         TM    C9+K7,MIS                END MASTER INDEX SEARCH.
         BO    APPAEC3                  BRANCH IF YES
         TM    C9+7,X'04'               IF LEVEL OF INDEX IS 3
         BO    APPAEG2                   SET CC IN CP1 TO CHAIN TO
         OI    C6+4,X'40'                SEARCH CYLINDER INDEX
APPAEG2  MVC   IOBDADAD,C8+7            ADDR NEXT SRCH ADDR IN IOB
         B     APPRVXCP                 RETURN TO EXCP
*
*              DUMMY OR INACTIVE
*
APPAEC3  TM    C9+7,X'08'               TEST DUMMY ENTRY CHAINED
         BO    APPAEG2                  BRANCH IF YES
         IC    R12,C9+7                 COMPARE LEVEL OF INDEX READ TO
         SLL   R12,29                    DCB INDEX LEVEL
         IC    R13,DCBNLEV
         SLL   R13,29
         CLR   R12,R13
         BNE   APPAAB3                  BRANCH IF NOT EQUAL
         OI    DECBEXC1,X'80'           SET NO REC FND IN EXCEPTION CD
         B     APPAAD4
*
*              END OF CYLINDER SEARCH
*
APPAEC4  TM    C18,X'20'                TEST DUMMY OR INACTIVE
         BO    APPAEC5             BRANCH IF YES
         MVC   IOBDADAD,C17                  ADDR NEXT SRCH IN IOB
         MVI   IOBASYN,X'03'       SET ASYNC CODE
         B     APPABSAR                 SCHEDULE ASYNCH RTN
APPAEC5  TM    C18,X'08'                IF DUMMY ENTRY NOT CHAINED,
         BZ    APPAAB3                   BRANCH TO INDICATE UNRCHBLE
         MVC   IOBDADAD,C17             OTHERWISE, RESTART CHAN PROG
         LA    R12,C11                   WITH NEW SRCH ADDRESS
         STH   R12,IOBSTART+1
         SRL   R12,16
         STC   R12,IOBSTART
         B     APPRVXCP                 RETURN TO IOS
*
*              ABNORMAL END
*
APPAEJ2  TM    IOBSENSE+1,X'04'        FILE PROTECT               25000
         BZ    APPAAA3                  BRANCH IF NO.
         LA    R12,C10+8                TEST STOP AT C10
         L     R13,IOBCSW
         LA    R13,0(R13)
         CLR   R12,R13                  BRANCH TO INDICATE UNREACHABLE
         BNE   APPAAB3                   IF NO.
*
APPAOSJ4 EQU   *                        *
*
         TM    C9+7,X'20'              IF DUMMY OR INACTIVE       25000
*                                       RECORD                    25000
         BO    APPAEC3                   BRANCH TO SAME PROCEDURE AS
         LA    R12,C11                   CHANNEL END
         STH   R12,IOBSTART+1           OTHERWISE, RESTART CHAN PROG
         SRL   R12,16
         STC   R12,IOBSTART
         B     APPAEG2
*
*        CHANNEL END APP CODE O,1 (CP4,5) CHART AB
         USING CP4,R10
APPABA2  EQU   *                                                 S20201
*
         L     R12,IOBCSW               CP STOP ADDR
         LA    R12,0(R12)               CLEAR HI BYTE
         SR    R12,R10                  OFFSET OF CCW
*
         CL    R12,APPHCA23             STOP AT CCW CA23        SA65011
         BNE   NOTCA23                  BRANCH IF NO            SA65011
         TM    DECBTYP2,READKU          READ K FOR UPDATE       SA65011
         BZ    NOTCA23                  BRANCH IF NO            SA65011
         CLI   CA24,RDSECT              CA24 READ SECTOR CCW    SA65011
         BE    APPABF3                  BR IF YES - SET UNREACH SA65011
*                                       BLK, ASYN CODE OF 4     SA65011
NOTCA23  EQU   *                        CONTINUE - CP OK        SA65011
         CL    R12,APPHCA10             STOP AT CA10
         BNE   APPAOSA2                 NO
         TM    CA10+K5,CC               WAS CA10 COMMAND CHAINED
         BZ    APPAOSA2                 NO
         OI    CA10+K4,CC               TURN CC BACK ON
         NI    CA10+K5,CCOFF            TURN INDICATOR OFF
         CLI   CA14,SEEKHH              CA14 SEEK HEAD
         BNE   APPAOS21                 NO, FILE PROTECT
         MVC   IOBDADAD,CA12+K7         RESET IOBSEEK
         LA    R12,CA16A                RESET IOBSTART TO
         ST    R12,IOBSTART-K1          START AT CA16A
         B     APPRVXCP                 GO DO AN EXCP
APPAOSA2 EQU   *                        *
*
         NI    DECBEXC1,NOT-OFLORCD     DECB EXCEP SET=NOT       S20201
*                                       OFLORCD                  S20201
         MVC   IOBDADAD+K5(K3),CA25+K5                           S20201
APPABB3  MVI   IOBASYN,X'00'         B3      ASYNC CD SET=0 (G CP4,5,6)
APPABSAR B     BYP(R14)                 SCHEDULE ASYNCH ROUTINE  Y02072
         SPACE 3
*        ABNORMAL END APP CODE 0,1 (CP4,5) CHART AB
*
APPABD2  EQU   *                                                  19374
         TM    IOBSENSE+1,X'04'         FILE PROTECT?             19374
         BO    APPABD21                 BRANCH IF SO              19374
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR?          19374
         BC    PERRYES,APPABD3          YES--BRANCH               19374
         B     APPRVNOR                 NO--RETRY CHANNEL PROGRAM 19374
APPABD21 EQU   *                                                  19374
         L     R12,IOBCSW          E2 YES  STOP AT CA14 TEST
         LA    R12,0(R12)
         SR    R12,R10
         CL    R12,APPHCA14
         BNE   APPABF3                  NO   F3
*
APPAOS21 EQU   *                        *
*
         TM    CA13+7,X'20'        F2 YES  DUMMY OR INAC TEST
         BO    APPABF1                  YES  F1
         MVC   IOBDADAD,CA12+7     G2 NO   CA12+7 TO IOBDADAD
         MVC   CA34+7(8),CA12+7         CA12+7 TO CA34+7
         LA    R12,IOBDADAD+3      G1   ADIOBDADAD(CCHH)TO CA26
         ST    R12,CA26
         MVI   CA26,X'31'
         MVC   CA28+1(3),DECBKEY+1 H1   DECBKEY+1 TO CA28 AD
         CLI   IOBAPP,CODE0             TEST FOR A READ          S20201
         BE    APPABJ2                  READ J2
         LA    R12,CA40            K1 WRITE CA30 TIC TO CA40
         STH   R12,CA30+2
         SRL   R12,16
         STC   R12,CA30+1
         L     R12,DECBAREA        K2   AREA+6 TO CA40 AD
         LA    R12,6(R12)
         ST    R12,CA40
         MVI   CA40,X'06'
         MVC   CA43+1(3),CA40+1    K3   AREA+6 TO CA43 AD
         LA    R13,CA43+6               LENGTH+10 TO CA43 CNT
APPABK2  MVI   CA28,SKEQ                SCH EQ FOR WRITE         S20201
APPABK4  TM    DECBTYP1,X'02'                DECBLGTH SPECIFIED{
         BZ    APPABK4A                 YES  K4A
         LH    R12,DCBLRECL             NO   USE DCBLRECL
         B     APPABK4B
APPABK4A LH    R12,DECBLGTH             DECB LENGTH OVERRIDING
APPABK4B LA    R12,10(R12)
         STH   R12,0(R13)
APPABK5  MVI   IOBAPP,CODE5             SET CODE 5 FOR CP6       S20201
         LA    R12,CA26            J5   AD CA26 TO IOBSTART
         IC    R13,IOBSIOCC
         ST    R12,IOBSIOCC
         STC   R13,IOBSIOCC
         MVI   IOBFLAG1,X'42'        H5 RESTORE IOB FLAGS AND ERR CTR
         MVI   IOBFLAG2,X'00'            CC AND UNRELATED FLAGS ON
         MVI   IOBCSW,X'00'              OTHERS OFF
         XC    IOBERRCT,IOBERRCT
         SR    R12,R12             G5   TEST OFLO SAME VOLUMN
         IC    R12,IOBDADAD
         SLL   R12,4
         L     R12,DEBUCBAD(R12)
         LA    R7,0(R7)
         LA    R12,0(R12)
         CLR   R7,R12
         BE    APPRVXCP                 IF SAME, EXCP RET TO IOS
         MVI   IOBASYN,X'01'         G4 NO   ASYNC CD SET=1 (EXCP)
         B     APPABSAR                      SAR
APPABJ2  LA    R12,CA31              J2 CA30 TIC TO CA31
         STH   R12,CA30+2
         SRL   R12,16
         STC   R12,CA30+1
         L     R12,DECBAREA                  AREA+6 TO CA31 ADDRESS
         LA    R12,6(R12)
         ST    R12,CA31
         MVI   CA31,X'06'
         LA    R13,CA31+6                    LENGTH+10 TO CA31 COUNT
         SR    R12,R12                                           S20201
         AH    R12,DCBRKP               RKP=0                    S20201
         BNZ   APPABJ4                  BIF NOT = 0              S20201
         TM    DCBRECFM,BLOCKED         RECFM=BLOCKED            S20201
         BZ    APPABK2                  BIF NOT BLOCKED          S20201
APPABJ4  MVI   CA28,SKEQHI             SCH EQHI FOR (READ AND    S20201
*                                       RKP NE 0                 S20201
*                                       OR (READ AND RKP=0 AND BLKD)
         B     APPABK4                       K4
APPABF1  CLI   DCBNLEV,X'00'         F1      DCBNLEV=0{
         BE    APPABD5                  YES  D5
APPABF3  OI    DECBEXC1,X'04'        F3 NO   DECB EXCEP SET=UNREACHABLE
APPABG3  MVI   IOBASYN,X'04'         G3      ASYNC CD SET=4(NG CP4,5,6)
         B     APPABSAR                      SAR
APPABD3  EQU   *
         TM    IOBSENSE+1,X'08'         NO RECORD FOUND           16072
         BZ    APPABB1                  NO--BRANCH                16072
APPABE4  L     R12,IOBCSW          E4   STOP AT CA21 TEST
         LA    R12,0(R12)
         SR    R12,R10
         CL    R12,APPHCA20                                      S20201
         BNE   APPABF3                  NO   F3
         CLI   CA21,X'69'          D4,C4 READ FORMAT BLOCKED TEST
         BE    APPABF3                  YES  F3
APPABD5  OI    DECBEXC1,X'80'        D5 NO   DECB EXCEP SET=NO RCD FND
         B     APPABG3                       G3
APPABB1  L     R12,IOBCSW          B1   STOP AT CA23
         LA    R12,0(R12)
         SR    R12,R10
         CL    R12,APPHCA23
         BNE   APPABF3                  NO   F3
APPABD1  OI    DECBEXC1,X'08'        D1 YES  DECB EXCEP SET=UNCOR IO ER
         B     APPABG3                       G3
*
*        CHANNEL END APP CODE 5 (CP6) CHART AC
         USING CP4,R10
APPACA2  L     R12,IOBCSW          A2   TEST STOP AT CA31
         LA    R12,0(R12)
         SR    R12,R10
         CL    R12,APPHCA31             NON-RPS HALT.            S20201
         BE    APPACA6                  BIF HALT ON CA31         S20201
         CL    R12,APPCA31B             RPS HALT.                S20201
         BNE   APPACA3                  BIF NO HALT HERE.        S20201
         B     APPACA6A                 COMPARE KEYS            SA65011
*
APPACA6  TM    DECBTYP2,READKU          READ K FOR UPDATE       SA65011
         BZ    APPACA6A                 BRANCH IF NO            SA65011
         TM    CA31+4,CC                CA31 COMMAND CHAINED    SA65011
         BO    APPABF3                  BR IF YES - SET UNREACH SA65011
*                                       BLK, ASYN CODE OF 4     SA65011
APPACA6A CLI   CA28,SKEQHI              SEARCH HI USED.         SA65011
         BNE   APPACA8                  BIF NOT                  S20201
*
*        COMPARE KEYS. IF THE RECORD'S KEY IS HIGHER, TERMINATE
*        THE SEARCH OF THE OVERFLOW CHAIN.
*
         IC    R9,CA28+K7                GET KEY LENGTH.         S20201
         BCTR  R9,R0                    EXECUTED LENGTH          S20201
         L     R12,CA28                 GET KEY ADDR.            S20201
         L     R13,CA31                 GET DATA ADDR.           S20201
         AH    R13,DCBRKP               FIND THE KEY IN THE      S20201
*                                       RECORD.                  S20201
         LA    R13,K10(R13)             KEY FOUND.               S20201
         EX    R9,COMPARE               CHECK FOR MATCH.         S20201
         BNE   APPACG3                  BIF NO MATCH.            S20201
APPACA8  EQU   *                                                 S20201
         CLC   CA34+7(8),CA12+7         FIRST RECORD IN CHAIN   OY00310
         BE    APPACB3                  YES  B3
         L     R12,DCBRORG3             NO   ADD 1 TO DCBRORG3
         LA    R12,1(R12)
         ST    R12,DCBRORG3
         TM    DECBTYP2,X'20'           B2  TEST READ FOR UPDATE
         BZ    APPACB3                  NO   B3
         MVC   IOBDADAD,CA34+7          YES  IOBDADAD SET FOR WRITEBACK
APPACB3  OI    DECBEXC1,X'02'        B3      DECB EXCEP SET=OFLO RCD
         MVI   IOBASYN,X'00'         B4      ASYNC CD SET=0 (G CP4,5,6)
         B     APPABSAR                      SAR
APPACA3  CL    R12,APPHCA43        A3   STOP AT CA43 TEST
         BNE   APPACF4                  NO   F4
         B     APPACB3                  YES  B3
*
*        ABNORMAL END APP CODE 5 (CP6) CHART AC
*
APPACE2  EQU   *                                                  19374
         TM    IOBSENSE+1,X'04'         FILE PROTECT?             19374
         BO    APPACE21                 YES--BRANCH               19374
         TM    IOBECBAD,PERRMASK        PERMANENT ERROR?          19374
         BC    PERRYES,APPACE3          YES--BRANCH               19374
         B     APPRVNOR                 NO--RETRY CHANNEL PROGRAM 19374
APPACE21 EQU   *                                                  19374
         L     R12,IOBCSW          F2 YES  STOP AT CA36 TEST
         LA    R12,0(R12)
         SR    R12,R10
         CL    R12,APPHCA36
         BNE   APPACF4                  NO   F4
*
         CLI   CA36,SEEKHH              CA36 SET TO SEEK HEAD
         BNE   APPAOSE2                 NO
         MVC   IOBDADAD,CA34+K7         RESET IOBSEEK
         B     APPRVXCP                 GO DO AN EXCP
APPAOSE2 EQU   *                        *
*
         TM    CA35+7,X'08'        G2 YES  END OR CHAINED TEST
         BZ    APPACG3                  END  G3
         MVI   IOBASYN,X'01'         J2      ASYNC CD SET=1 (EXCP)
         MVI   IOBFLAG1,X'42'        J3 RESTORE IOB FLAGS AND ERR CTR
         MVI   IOBFLAG2,X'00'            CC AND UNRELATED FLAGS ON
         MVI   IOBCSW,X'00'              OTHERS OFF
         XC    IOBERRCT,IOBERRCT
         MVC   IOBDADAD,CA34+7          SET SEEK SEARCH ADDRESS
         B     APPABSAR                      SAR
APPACE3  EQU   *
         L     R12,IOBCSW          E4 YES  TEST STOP AT CA31 OR CA43
         LA    R12,0(R12)
         SR    R12,R10
         CL    R12,APPHCA31
         BE    APPACE5                  YES  E5
         CL    R12,APPHCA43
         BE    APPACE5                  YES  E5
APPACF4  OI    DECBEXC1,X'04'        F4 NO   DECB EXCEP SET=UNREACHABLE
APPACG4  MVI   IOBASYN,X'04'         G4      ASYNC CD SET=4(NG CP4,5,6)
         B     APPABSAR                      SAR
APPACG3  OI    DECBEXC1,X'80'        G3      DECB EXCEP SET=NO RCD FND
         B     APPACG4                       G4
APPACE5  TM    IOBSENSE,X'08'        E5      DATA CHK{
         BZ    APPACF4                  NO   F4
         OI    DECBEXC1,X'08'        F5 YES  DECB EXCEP SET=UNCOR IO ER
         B     APPACG4                       G4
*              CHART AD       CHANNEL END + NORMAL END CP7
*              CHANNEL END
         USING CP7,R10
APPADB2  LA    R13,CA46+8               TEST STOP AT CA46
         L     R12,IOBCSW
         LA    R12,0(R12)
         CLR   R12,R13
         BNE   APPADB3
         MVI   IOBASYN,X'02'            IF SO, SET ASYN CODE AND BRANCH
         B     APPABSAR                 SCHEDULE ASYN ROUTINE
APPADB3  OI    DECBEXC1,X'04'           IF NOT, INDICATE UNREACHABLE IN
APPADB4  MVI   IOBASYN,X'06'             DECB AND SET ASYNCH CODE
         B     APPABSAR                 SCHEDULE ASYNCH ROUTINE
*
*              ABNORMAL END
*
APPADE2  TM    IOBECBAD,PERRMASK        IF NOT PERM ERR,
         BO    APPRVNOR                 NO, NORM IOS RETURN       15924
         LA    R12,CA46+8               IF STOP AT CA46 AND
         L     R13,IOBCSW                CAUSE IN NOT DATA CHECK,
         LA    R13,0(R13)                OR IF STOP NOT AT CA46
         CLR   R13,R12                   GO TO APPADB3
         BNE   APPADB3
         TM    IOBCSW+5,X'08'
         BZ    APPADB3
         OI    DECBEXC1,X'08'            DATA CHECK, SET IO UNCORRTABLE
         B     APPADB4                   ERROR BIT IN DECB
APPRVXCP SR    R12,R12
         IC    R12,IOBDADAD             GET BB
         SLL   R12,4
         LA    R12,32(R12,R3)
         MVC   IOBDADAD+1(2),4(R12)
         LA    R14,EXCP(R14)            SETUP FOR EXCP IOS RETURN 15924
         XC    IOBFLAG2(3),IOBFLAG2     CLEAR FLAG2,SENSE BYTES   15924
         XC    IOBCSW(9),IOBCSW         CLEAR FLAG3,CSW,AND SIOCC 15924
         XC    IOBERRCT(2),IOBERRCT     CLEAR ERROR COUNT         15924
         NI    IOBFLAG1,X'C2'           RESET FLAG1               15924
APPRVNOR BR    R14                      RETURN TO IOS             15924
COMPARE  CLC   K0(K1,R12),K0(R13)       EXECUTED INSTRUCTION     S20201
         EJECT
APPN3B3  EQU   APPM1B3        LABELS USED IN COMMON ROUTINE TO
APPN3B5  EQU   APPM1B5         SCHEDULE ASYNCHRONOUS ROUTINE
CVTPTR   EQU   16                       ADDR COMMUNICATION VECTOR TABLE
CODE0    EQU   0                        CHAN PGM 4-5 APPEND CODE S20201
CODE5    EQU   5                        CHAN PGM 6 APPEND CODE   S20201
CODE8    EQU   8                        CHAN PGM 8 APP CODE      S20201
CODE9    EQU   9                        CHAN PGM 10A APP CODE    S20201
CODE10   EQU   10                       CHAN PGM 10B APP CODE    S20201
CODE11   EQU   11                       CHAN PGM 10B APP CODE    S20201
CODE14   EQU   14                       CHAN PGM 15 APP CODE     S20201
CODE17   EQU   17                       CHAN PGM 17 APP CODE     S20201
CODE18   EQU   18                       CHAN PGM 17 APP CODE     S20201
CODE19   EQU   19                       CHAN PGM 17 APP CODE     S20201
*
PATCH    DC    XL((*-IGG019GN)/20)'00'  ZEROED PATCH AREA        Y02072
*
         SPACE 4
*
*              DATA EVENT CONTROL BLOCK
IHADECB  DSECT
         DS    0F
DECBECB  DS    CL4                      EVENT CONTROL BLOCK (ECB)
DECBTYP1 DS    BL1                      TYPE B6 - 1 IF LENGTH IS S
*                                            B7 - 1 IF AREA IS S
DECBTYP2 DS    BL1                           B0 - 1 IF READ K
*                                            B1 - 1 IF READ KX
*                                            B2 - 1 IF READ KU
*                                            B4 - 1 IF WRITE K
*                                            B5 - 1 IF WRITE KN
DECBLGTH DS    CL2                      LENGTH OF BLOCK
DECBDCBA DS    A                        POINTER TO DCB
DECBAREA DS    A                        ADDRESS OF AREA
DECBLOGR DS    A                        POINTER TO LOGICAL RECORD
DECBKEY  DS    A                        POINTER TO KEY
DECBEXC1 DS    BL1                      EXCPTN CD B0-RECORD NOT FOUND
*                                                 B1-RECORD LGTH CHK
*                                                 B3-INVALID REQUEST
*                                                 B4-UNCORRECTABLE IO
*                                                 B5-UNREACHABLE BLOCK
*                                                 B6-OVERFLOW RECORD
*                                                 B7-DUPLICATE REC
DECBEXC2 DS    BL1                                B7-READ KU
         EJECT
         DCBD  DSORG=(IS)
         EJECT
IHAIOB   IGGIOBD
CPSTART  EQU   IOBSTART-1                                        S20201
         EJECT
IHADCW   IGGBISAM
         EJECT
IHADEB   IGGDEBD
         EJECT
IHACVTEX DSECT                COMMUNICATION VECTOR TABLE
         DS    A
*        READ K, READ KU, AND WRITE K CHANNEL PGMS(NO WRITE CHK)
IHACP47  IGGCP47
         SPACE 2
*        WRITE KEY NEW CHANNEL PROGRAM REFERENCES (NO WRITE CHECK)
IHAWKNCP IGGWKNCP
         SPACE 2
         IGGCP12C
         SPACE 2
         IGGCP12A
         END
