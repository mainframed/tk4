         TITLE 'IGG019IN - VLR APPEND-PART 2, COMBINED'
IGG019IN CSECT                                                      
*          RELEASE OS/VS2-02 DELETIONS/CHANGES
*                                                               YM03826
*                                                               YM05342
*          RELEASE 20 DELETIONS/CHANGES
*0911                                                            M5590
*
*         RELEASE VS2030 CHANGES/DELETIONS                     @ZA11146
*C675200,887200                                                @ZA11146
*
*STATUS CHANGE LEVEL 004
* THIS MODULE WAS REWRITTEN IN RELEASE 20.2 TO
* UTILIZE CHANNEL PROGRAM AND WORK AREA
* MACRO EXPANSIONS
*
* FUNCTION/OPERATION' APPENDAGE ROUTINES FOR BISAM
*        WHEN READ AND UPDATE IS USED WITH WRITE KN,
*        WRITE CHECK OR NO WRITE CHECK
*        (PART 2)
*        CEND  - IF CHANNEL PROGRAM ENDS
*                **WITHOUT ERROR, EITHER
*                    PROCESSING COMPLETION WILL BE SCHEDULED,
*                    PROCESSING CONTINUATION WILL BE SCHEDULED,
*                    OR PROCESSING WILL CONTINUE.
*                **WITH A LENGTH CHECK, TESTS DETERMINE WHETHER
*                    OR NOT TO TREAT THE SITUATION AS A REAL
*                    ERROR.
*                **ANY OTHER ERROR, IN GENERAL, 'UNREACHABLE
*                    BLOCK' IS INDICATED IN THE DECB, AND
*                    PROCESSING COMPLETION WILL BE SCHEDULED.
*
*        ABEND - IF CHANNEL PROGRAM ENDS ABNORMALLY BECAUSE OF
*                **PERMANENT ERROR, 'UNCORRECTABLE I/O ERROR'
*                    IS INDICATED IN THE DECB, AND PROCESSING
*                    COMPLETION WILL BE SCHEDULED.
*                    USE OF WRITE VALIDITY CHECKING OPTION
*                    ALLOWS A LIMITED NUMBER OF RE-EXECUTIONS
*                    BEFORE INDICATING AN ERROR.
*                **NON-PERMANENT ERROR, ONE RE-TRY IS MADE
*                    BEFORE INDICATING A PERMANENT ERROR.
*                **FILE PROTECTION, TESTS DETERMINE WHETHER
*                    OR NOT TO TREAT THE SITUATION AS A REAL
*                    ERROR.
*                **ANY OTHER ERROR, IN GENERAL, 'UNREACHABLE
*                    BLOCK' IS INDICATED IN THE DECB, AND
*                    PROCESSING COMPLETION WILL BE SCHEDULED.
*
* ENTRY POINTS-
*        ENTRY POINT               FUNCTION
*        ANY POINT IN THE VECTOR TABLE AT THE BEGINNING OF THE MODULE.
* INPUT - N/A
* OUTPUT - N/A
* EXTERNAL ROUTINES - N/A
* EXITS  1. RETURN TO IOS VIA THE APPENDAGE RETURN VECTOR TABLE POINTED
*            TO BY REGISTER 14.  SEE THE DSECT LABELED    APPRV FOR THE
*            FORMAT OF THIS TABLE AND THE USE OF EACH EXIT.
*        2. SCHEDULE AN ASYNCHRONOUS ROUTINE - BRANCH VIA THE EXIT
*            EFFECTOR ADDRESS IN THE COMMUNICATION VECTOR TABLE.
* TABLES/WORK AREAS ' DECB, DCB, IOB AND EXTENTION, DCB WA (DCW), DEB,
*        COMMUNICATION VECTOR TABLE.
*        SEE DSECTS AT END OF MODULE FOR FORMAT AND DESCRIPTIONS.
* ATTRIBUTES- REENTRANT. DISABLED.
* NOTES - NONE
* GENERAL REGISTERS ARE USED AS FOLLOWS
R0       EQU   0                   *    -----
R1       EQU   1                   *    12* ADDRESS
R2       EQU   2                   *    IOB
R3       EQU   3                   *    DEB
R4       EQU   4                   *    DCB
R5       EQU   5                   *    -----
R6       EQU   6                   *    -----
R7       EQU   7                   *    UCB
R8       EQU   8                   *    -----
R9       EQU   9                   #    WORK REGISTER
R10      EQU   10                       CHANNEL PROGRAM POINTER
R11      EQU   11                       DECB
R12      EQU   12                       WORK REGISTER
R13      EQU   13                       WORK REGISTER
R14      EQU   14                       IOS RETURN VECTOR TABLE
R15      EQU   15                       BASE REGISTER
*        * MEANS THIS REGISTER IS SET UP UPON ENTRY FROM IOS,
*          AND ITS CONTENTS MUST BE RESTORED UPON RETURN TO IOS
*        # MEANS THIS REGISTER MAY BE CHANGED, BUT UPON RETURN TO IOS
*          MUST CONTAIN ZEROES IN THE THREE HIGH ORDER BYTES
PERRMASK EQU   X'20'          TO TEST FOR PERMANENT ERROR, TEST
PERRYES  EQU   8               IOBECBAD, B2. IF OFF (CONDITION CODE 8)
PERRNO   EQU   7               THERE IS A PERM ERR. ELSE B2 ON ( CD 7)
K0       EQU   0                                                 S20201
K1       EQU   1                                                 S20201
K2       EQU   2                                                 S20201
K3       EQU   3                                                 S20201
K4       EQU   4                                                 S20201
K5       EQU   5                                                 S20201
K6       EQU   6                                                 S20201
K7       EQU   7                                                 S20201
K10      EQU   10                                                S20201
K14      EQU   14                                                S20201
CODE13   EQU   13                       ASY CODE 13              XM1679
NOT      EQU   255                                               S20201
OVFLORCD EQU   2                                                 S20201
BLOCKED  EQU   X'10'                                             S20201
NRF      EQU   8                                                 S20201
DATACHK  EQU   8                                                 S20201
MIS      EQU   PERRMASK                 MASTER IDX SRCH MASK
CCOFF    EQU   X'BF'                    TURN OFF CC MASK
READKU   EQU   X'20'                    READ KEY FOR UPDATE     SA65011
         USING IHAIOB,R2                                         S20201
         USING IHADEB,R3
         USING IHADCB,R4
         USING IHAWKNCP,R10
         USING IHADECB,R11
         USING IHADCW,R12
         USING IHAAPPRV,R14
         USING IGG019IN,R15             BASE
         USING IHACVTEX,R13             CVT
*         VECTOR TABLE.  BRANCH TO THE ROUTINE SELECTED IN THE
*           APPENDAGE ROUTINE, PART 1.
*        ROUTINE FOR          CODE      CEND COMPLETION OF
         LR    R15,R13
         B     APPM67CE       7         CP1 OR CP2 FOR WRITE KN
         LR    R15,R13
         B     APPN7B2         9       CP14 EOF WXTENSION         
         LR    R15,R13
         B     APPN8B2                 CP 14 PART 2
         LR    R15,R13
         B     APPM4A2        12        CP14W - SETUPS 1, 2, OR 5
         LR    R15,R13
         B     APPM5A2        13        CP14W - SETUPS 3, 4, OR 6
         LR    R15,R13
         B     APPM2B2        15        CP16 - SITUATION 2
         LR    R15,R13
         B     APPM3B2        16        CP16 - SITUATION 3
         LR    R15,R13                                              
         B     APPM17         17        CP14W- PRIME ENTRY ONLY     
         LR    R15,R13
         B     APPN9A2        19        CP17W                       
*        ROUTINE FOR          CODE      ABEND COMPLETION OF
         LR    R15,R13
         B     APPM67AE       7         CP1 OR CP2, WRITE KN
         LR    R15,R13
         B     APPJ3B2         9       CP14 EOF WXTENSION         
         LR    R15,R13
         B     APPN3C2                 CP 14 PART 2
         LR    R15,R13
         B     APPJ5B2        12        CP14W - SETUPS 1, 2, OR 5
         LR    R15,R13
         B     APPJ6B2        13        CP14W - SETUPS 3, 4, OR 6
         LR    R15,R13
         B     APPM2H2        15,16     CP16
         LR    R15,R13                                              
         B     APPAB17        17        CP14W- PRIME ENTRY ONLY     
         LR    R15,R13
         B     APPJ7B2        19        CP17W                       
         B     APPRVNOR                NOT USED                     
         B     APPRVNOR                NOT USED                     
*        ROUTINE FOR          CODE      CEND COMPLETION OF
         LR    R15,R13
         B     APPABA2        0,1       CP4/CP5W
         LR    R15,R13
         B     APPADB2        2         CP7W
         LR    R15,R13
         B     APPAAB2        3         CP1 OR CP2, READ AND UPDATE
         LR    R15,R13
         B     APPACA2        5         CP6W
         LR    R15,R13
         B     APPRVNOR                 INVALID CODE 6           S20201
*        ROUTINE FOR          CODE      ABEND COMPLETION OF
         LR    R15,R13
         B     APPABD2        0,1       CP4/CP5W
         LR    R15,R13
         B     APPAIB2        2         CP7W
         LR    R15,R13
         B     APPAAA2        3         CP1 OR CP2, READ AND UPDATE
         LR    R15,R13
         B     APPACE2        5         CP6W
         LR    R15,R13
         B     APPRVNOR                 INVALID  CODE 6          S20201
*              BRANCH TO M6 OR M7
APPM67CE L     R13,DCBWKPT2             CHAN END OF CP1 OR CP2
         CLI   11(R13),X'01'            NLSD=1 MEANS NO MAST IX
         BE    APPM6B2                       CYLINDER - M6 (CP2)
         B     APPM7A2                       MASTER - M7 (CP1)
APPM67AE L     R13,DCBWKPT2             ABNORMAL END OF CP1 OR CP2
         CLI   11(R13),X'01'            NLSD=1 MEANS NO MAST IX
         BE    APPM6J4                       CYLINDER - M6 (CP2)
         B     APPM7J2                       MASTER - M7 (CP1)
         EJECT
*              CHART M7  CP1 FOR WRITE KN, CODE 7
*              CHANNEL END
APPM7A2  EQU   *
         LA    R13,C6+8
         CLR   R12,R13                  TEST STOP AT C6 - END OF SEARCH
         BE    APPM7B2                   OF MASTER INDEX. BRANCH IF YES
         LA    R13,C16+8
         CLR   R12,R13                  TEST STOP AT C16 - END OF SRCH
         BNE   APPN3B3                   OF CYLINDER INDEX. BRANCH NO
         TM    C18,X'20'                END OF CYLINDER SEARCH.
         BZ    APPM7C4                  TEST FOR DUMMY OR INACTIVE
         TM    C18,X'08'
         BZ    APPM7B3                  BRANCH IF INACTIVE OR DUMMY-END
         MVC   IOBDADAD,C17             DUMMY CHAINED.
         LA    R12,C10A                 RESTART CP TO SEARCH CYL S20201
         STH   R12,IOBSTART+1            INDEX ON CHAIN.
         SRL   R12,16
         STC   R12,IOBSTART
         B     APPRVXCP                 RETURN TO IOS TO EXCP
APPM7C4  MVC   IOBDADAD(7),C17          ACTIVE.
APPM7C4A MVI   IOBDADAD+7,X'00'         SET UP IOB FOR CP8
         MVI   IOBAPP,X'08'              MBBCCHHR IS  ADDRESS FOUND,R=0
         MVC   IOBSTART,DCBWKPT3+1       START ADDRESS AND CCW 0 ADDR
         MVC   IOBCCWAD,DCBWKPT3          FROM DCB POINTER
         L     R10,DCBWKPT3
         MVI   IOBFLAG1,X'42'           APP CODE 8
         LA    R12,IOBDADAD+3           INITIALIZE CP8
         STH   R12,CB1+2                 CB1 SET TO SEEK CCHHR OF IOB
         SRL   R12,16
         STC   R12,CB1+1
         MVC   CB5+1(3),DECBKEY+1        CB5, CB15, CB19 ALL SET TO
         MVC   CB19+1(3),DECBKEY+1
APPM7G4  SR    R12,R12                  CHECK WITH DEB TO SEE IF NEXT
         IC    R12,IOBDADAD              ADDRESS ON SAME MODULE
         SLL   R12,4
         L     R12,32(R3,R12)
         LA    R12,0(R12)
         CLR   R12,R7
         BE    APPRVXCP                 IF SO, EXECUTE CP RETURN TO IOS
         MVI   IOBASYN,X'01'            IF NOT, SCHEDULE ASYNCHRONOUS
         B     APPN3B5                  ROUTINE TO EXCP
APPM7B2  EQU   *                        *
*
         TM    C6+K5,CC                 WAS C6 CHAINED
         BZ    APPAOS1                  NO, CONTINUE
         NI    C6+K5,CCOFF              TURN OFF INDICATOR
         OI    C6+K4,CC                 RECHAIN C6
         B     APPAOSJ3                 RESCHEDULE CP1
APPAOS1  EQU   *                        *
*
         TM    C9+K7,MIS                END MASTER INDEX SEARCH.
         BZ    APPM7C2                  TEST FOR DUMMY OR INACTIVE
APPM7B3  TM    C9+7,X'08'
         BO    APPM7E2                  BRANCH IF INACTIVE OR DUMMY-END
         IC    R12,C9+7                 DUMMY CHAINED.
         SLL   R12,29                   IF LEVEL INDEX NOT HIGHEST LVL,
         IC    R13,DCBNLEV               ERROR OF SOME SORT
         SLL   R13,29
         CLR   R12,R13
         BNE   APPN3B3
*                                       ALWAYS DO CP15
APPM7E3  MVI   IOBAPP,14                LAST TRACK FULL.
         MVC   IOBDADAD(3),DCBLPDA      SET UP IOB FOR CP15
         MVC   IOBDADAD+3(4),DCBLETI    APP CODE 14,MOVE FR LETI CCHH
         MVI   IOBDADAD+7,X'00'         ZERO R
         OC    IOBDADAD+6(1),DCBFIRSH+3
         XC    IOBDADAD+6(1),DCBFIRSH+3  INCREMENT TO A CYL BOUNDRY
         L     R10,DCBWKPT3                   HHR ZERO
         ST    R10,IOBCCWAD              START ADDR CI1
         LA    R12,CI1                   REL CCW 0  CP8
         ST    R12,IOBSTART-1           FLAGS INDICATE CC, NO DC
         MVI   IOBFLAG1,X'42'
         LA    R12,IOBDADAD+3           INITIALIZE CP15
         STH   R12,CI1+2                 CI1 POINTS TO IOB CCHHR
         SRL   R12,16
         STC   R12,CI1+1
         MVC   CI5+K2(K5),DCBLETI       COUNT ADDR               S20201
         MVC   CI5(K2),DCBLPDA+K1       MOVE IN BB FOR HEAD SEEK S20201
         B     APPM7G4                  BRANCH TO EXCP
APPM7C2  TM    C9+7,X'04'               ACTIVE ENTRY.
         BO    APPM7E2                  IF LVL 2 MAST IND JUST SRCHED
         OI    C6+4,X'40'                CC ON TO CHN FROM LVL 1 TO CYL
APPM7E2  MVC   IOBDADAD,C8+7            SEEK ADDR IN IOB IS FND ADDR
         NI    IOBFLAG1,X'7F'                B0 OFF NO DC
         OI    IOBFLAG1,X'40'
         B     APPRVXCP                 RETURN TO IOS TO EXCP
         EJECT
*              ABNORMAL END
APPM7J2  TM    IOBSENSE+1,X'04'          ABNORMAL END
         BO    APPM7J3                  BRANCH IF CAUSE IS FILE PROTECT
         TM    IOBECBAD,PERRMASK             PERMANENT ERROR TEST
         BC    PERRYES,APPN3B3       YES     B3
         B     APPRVNOR                 NORMAL RETURN TO IOS
APPM7J3  LA    R13,C10+8                FILE PROTECT
         CLR   R12,R13
         BNE   APPN3B3                  BRANCH IF STOP NOT AT C10
*
APPAOSJ3 EQU   *                        *
*
         MVI   IOBFLAG1,X'42'           RESTORE IOBFLAGS & ERR CTR
         MVI   IOBFLAG2,X'00'             UNRELATED FLAG ON
         MVI   IOBCSW,X'00'               OTHERS OFF
         XC    IOBERRCT,IOBERRCT
         TM    C9+7,X'20'
         BO    APPM7B3                  BRANCH IF DUMMY OR INACTIVE
         LA    R12,C10A                 SET UP IOB TO RESTART    S20201
         ST    R12,IOBSTART-1
         B     APPM7E2                  BRANCH
         EJECT
*              M6 - CP2 FOR WRITE KN, CODE 7
*              CHANNEL END
APPM6B2  LA    R13,C35+8                B2   STOP AT C350
         CLR   R12,R13
         BNE   APPN3B3                       NO
         TM    C37,X'20'             C2      DUMMY OR INACTIVE{
         BZ    APPM6D2                       NO D2
         TM    C37,X'28'             C3      DUMMY CHAINED{
         BO    APPM6D3                       YES D3
         TM    DCBNLEV,X'01'         C4      NLEV=F{ F MUST BE 1
         BZ    APPN3B3                       NO
         B     APPM7E3
APPM6D2  MVC   IOBDADAD(7),C36       D2      C36 MBBCCHH
         B     APPM7C4A
APPM6D3  MVC   IOBDADAD,C36          D3      C36 MBBCCHHR
         B     APPRVXCP              E3      EXCP RETURN TO IOS
         EJECT
*              ABNORMAL END
APPM6J4  TM    IOBECBAD,PERRMASK     J4      PERMANENT ERROR TEST
         BC    PERRYES,APPN3B3          YES  B3
         B     APPRVNOR              J5      NORMAL RETURN TO IOS
*              APP CODE 9 NORMAL END OF CP14 EOF EXT              
APPN7B2  B     APPM1B5                 EXEC ASYN RTN 16 2ND TIME
*                                                                 
*              APP CODE 9 ABMORMAL END CP14 EOF EXT               
APPJ3B2  TM    IOBECBAD,PERRMASK       PERM ERROR
         BC    PERRNO,APPRVNOR         NO-NORMAL RETURN TO IOS
         B     APPM1B5                 YES-CONTINUE
*                                                                 
         EJECT
APPM17   EQU   *                       APP 17 - CHANNEL END ON      
*                                       CP14W- PRIME ENTRY ONLY     
*                                       VARIABLE ONLY CODE          
         TM    DCBOPTCD,X'80'          TEST IF WRITE CHECK
         BO    APPVV3                  BRANCH=YES
         LA    R13,CH8+8
         OI    CH8+4,X'40'             CC ON IN CH8
         B     APPVV4
APPVV3   LA    R13,CH8C+8              CC ON IN CH8C
         OI    CH8C+4,X'40'           *CC BIT BACK ON AT CH8C
APPVV4   CR    R12,R13                 TEST STOP WORD
         BE    APPL12
         TM    DCBOPTCD,X'80'
         BO    APPVV13X
         OI    CH13+4,X'40'
         LA    R13,CH13+8
         B     APPVVVV
APPVV13X OI    CH13C+4,X'40'
         LA    R13,CH13C+8
APPVVVV  CR    R12,R13
         BNE   APPM1B3
APPL12   L     R12,DCBWKPT2
         TM    DCWWKNI,X'10'           TEST IF ADD TO END SWITCH ON
         BZ    APPM1B4                 IF NO GO TO ASYNC=8
         NI    DCWWKNI,X'EF'           TURN ADD TO END SWITCH OFF
         L     R12,DCBNREC              GET NO OF PRIME RECS     M5590
         LA    R12,K1(R0,R12)           ADD ONE                  M5590
         ST    R12,DCBNREC              STORE NEW VALUE          M5590
         MVC   CK6+1(3),CH7+1          MOVE A(LAST KEY) TO CK6
         LA    R12,IOBDADAD+3
         STH   R12,CK1+2
         SRL   R12,16
         STC   R12,CK1+1               STORE A(IOBDADAD+3) IN CK1
         MVC   CK4+1(3),CK1+1                  DITTO          CK4
         MVC   CK7A+1(3),CK1+1        *        DITTO          CK7A
         MVC   DCBLPDA+7(1),CB11+6     HI R OF PRIME TRACK TO R OF LPDA
         B     APPN9C2
*
*              ABNORMAL END CP14W-PRIME ENTRY ONLY-APPENDAGE 17     
*                           VARIABLE ONLY CODE                      
*
APPAB17  TM    IOBSENSE,X'08'          WAS ERROR DUE TO DATA CHECK
         BZ     APPPERT                B=NO
         TM    DCBOPTCD,X'80'          TEST IF WRITE CHECK
         BZ    APPPERT                 BRANCH=NO
         LA    R13,CH8C+8             *TEST STOP A CH8C
         CLR   R12,R13
         BE    APPCT
         LA    R13,CH13C+8
         CR    R12,R13
         BNE   APPPERT
APPCT    IC    R9,IOBCOUNT
         BCTR  R9,R0
         LTR   R9,R9
         BZ    APPUNCOR                B=YES
         STC   R9,IOBCOUNT
         NI    IOBFLAG1,X'FB'          RESET EXCEPTION FLAG
         B     APPRVXCP                RETRY
APPUNCOR OI    DECBEXC1,X'08'          IND UNCORRECTABLE I/O ERROR
         B     APPM1B4
APPPERT  TM    IOBECBAD,PERRMASK
         BC    PERRNO,APPRVNOR
         B     APPM1B3                 UNREACHABLE
*
*              CHANNEL END ON CP17W  APPENDAGE CODE 19              
*
APPN9A2  TM    DCBOPTCD,X'80'          TEST IF WRITE CHECK          
         BO    APPVV6                  B=YES                        
         LA    R13,CK7+8                                            
         B     APPVV7                                               
APPVV6   LA    R13,CK7C+8                                           
APPVV7   CLR   R12,R13                 TEST PROPER CHAN END         
         BNE   APPM1B3                 NO-RECORD IS UNREACHABLE     
APPN9B2  EQU   *
         L     R12,DCBWKPT2
         CLC   DCWNLEVC,DCBNLEV      E2 NO   (19)NLEV CTR = DCBNLEV×
         BE    APPN9F1                  YES
         IC    R13,DCWNLEVC          G2 NO   ADD 1 TO NLEV COUNTER
         LA    R13,1(R0,R13)
         STC   R13,DCWNLEVC
         CLI   DCWNLEVC,X'02'        H2      NLEV COUNTER = 2{
         BE    APPN9E3                  YES  E3
         CLI   DCWNLEVC,X'03'           NO   NLEV COUNTER = 3{
         BE    APPN9F3                  YES  F3
         MVC   IOBDADAD+3(5),DCBLEMI3    G3  SET SEEK ADDR TO LEMI3
         CLI   DCBFTMI3+1,X'00'
         BE    *+14
         MVC   IOBDADAD(1),DCBFTMI3+1
         B     APPN9C5
         MVC   IOBDADAD(1),DCBFTMI3
         B     APPN9C5                       C5
APPN9E3  MVC   IOBDADAD+3(5),DCBLEMI1   E3  SET SEEK ADDR TO LEMI1
         CLI   DCBFTMI1+1,X'00'
         BE    *+14
         MVC   IOBDADAD(1),DCBFTMI1+1
         B     APPN9C5
         MVC   IOBDADAD(1),DCBFTMI1
         B     APPN9C5                       C5
APPN9F3  MVC   IOBDADAD+3(5),DCBLEMI2   F3  SET SEEK ADDR TO LEMI2
         CLI   DCBFTMI2+1,X'00'
         BE    *+14
         MVC   IOBDADAD(1),DCBFTMI2+1
         B     *+10
         MVC   IOBDADAD(1),DCBFTMI2
         XC    IOBDADAD+1(2),IOBDADAD+1
         B     APPN9C5                       C5
APPN9F1  TM    DCWHIAV,X'40'                 HIGH IND IN CORE?
         BZ    APPN9H1                  NO   H1
         L     R12,DCWMSHIL          G1 YES  CHNG KEY OF LST CORE INDEX
         L     R11,DECBKEY
         IC    R9,DCBKEYLE
         LA    R13,0(R0,R9)
         BCTR  R13,R0
         EX    R13,APPN9G1A
APPN9H1  MVI   IOBASYN,X'08'         H1      SET ASYNC CODE 8
         B     APPM1B5                       SCHED ASYNC ROUTINE
APPN9C2  CLI   DCBNLEV,X'00'         C2      DCBNLEV = 0 {
         BE    APPM1B4                  YES  M1B4
         L     R12,DCBWKPT2             R12  ADDRESS DCB WA
         MVI   DCWNLEVC,X'01'        D2 NO   SET NLEV COUNTER TO 1
         MVC   IOBDADAD+3(5),DCBLECI     D3  SET SEEK ADDR TO LECI
         CLI   DCBFTCI+1,X'00'
         BE    *+14
         MVC   IOBDADAD(1),DCBFTCI+1
         B     APPMVI
         MVC   IOBDADAD(1),DCBFTCI
APPMVI   MVI   IOBAPP,X'13'             SET APP CODE TO 19
         OI    IOBFLAG1,X'C0'                SET B0&B1 ON (CC&DC)
         LA    R12,CK1                       SET IOBSTART TO CK1
         ST    R12,IOBSTART-1
APPN9C5  MVI   IOBASYN,X'01'         C5      SET ASYNC CODE 1
         MVI   IOBCOUNT,X'0A'                SET COUNTER TO 10
         B     APPM1B5                       SCHED ASYNC ROUTINE
         EJECT
*              ABNORMAL END CP17W  APPENDAGE 19                     
APPJ7B2  TM    DCBOPTCD,X'80'          TEST IF WRITE CHECK          
         BZ    APPPERT                 B=NO                         
         TM    IOBECBAD,PERRMASK        PERMINANT ERROR           
         BC    PERRNO,APPRVNOR          NO RETRY                  
         TM    IOBSENSE,X'08'          TEST IF DATA CHECK           
         BZ    APPJ7B3                  NO   B3
         LA    R13,CK7C+8
         CLR   R12,R13
         BNE   APPJ7B3                  NO   B3
         IC    R9,IOBCOUNT           D2 YES  SUBT 1 FROM WC COUNTER
         BCTR  R9,R0                         COUNTER ZERO?
         LTR   R9,R9
         BZ    APPJ7E3                  YES  E3
         STC   R9,IOBCOUNT           F2 NO
         NI    IOBFLAG1,X'FB'                RESET EXCEPTION FLAG
         B     APPRVXCP                      EXCP RETURN TO IOS
APPJ7B3  B     APPM1B3
APPJ7E3  OI    DECBEXC1,X'08'        E3 YES  IND UNCOR I/O ERROR
         B     APPM1B4                       N3B4
         SPACE 2
APPN9G1A MVC   0(0,R12),0(R11)
         SPACE 4
*        COMMON ROUTINE TO SCHEDULE ASYNCHRONOUS ROUTINE
APPM1B3  OI    DECBEXC1,X'04'        B3      SET DECB EXCD TO UNRCHABLE
APPM1B4  MVI   IOBASYN,X'08'         B4      SET ASYNC CODE TO 8
APPM1B5  B     APPRVBYP                 SCHEDULE ASYNCH ROUTINE  Y02072
         SPACE 4
*                        CHANNEL END APP CODE 15 (CP16) CHART M2
APPM2B2  LA    R13,CJ10+L'CJ10          STOP AT CJ10
         CLR   R12,R13
         BNE   APPM1B3                  NO   UNREACHABLE BLK     S20201
         L     R12,DCBMSWA           C2 YES  OFLO END ENTRY?
         TM    28(R12),X'08'                                        
         BZ    APPM2D3                  YES  D3
         MVC   IOBDADAD,20(R12)        NO-SET SEEK ADDR FROM LINK   
         B     APPM7G4
APPM2D3  MVC   CJ11,IOBDADAD         D3      PUT SEEK ADDR IN CJ11
         MVI   IOBASYN,X'0B'                 SET ASYNC CODE 11
         NI    IOBINDCT,X'DF'                SET B2-OFLO RCD FR AREA
         B     APPM1B5                       SCHED ASYNC ROUTINE
         EJECT
*                        ABNORMAL END APP CODES 15,16,(CP16) CHART M2
APPM2H2  TM    IOBECBAD,PERRMASK     H2      PERMANENT ERROR TEST
         BC    PERRNO,APPRVNOR          NO   CONTINUE, NORMAL IOS
         B     APPM1B3                  YES  M1B3
         EJECT
*                        CHANNEL END APP CODE 16 (CP16) CHART M3
APPM3B2  LA    R13,CJ10+L'CJ10          STOP AT CJ10
         CLR   R12,R13
         BNE   APPM3B3                  NO - TRY CJ9             S20201
         MVC   CJ11,IOBDADAD                 SAVE SEEK ADDR IN CJ11
         L     R12,DCBMSWA           D2 YES  SET IOB SEEK FROM LINK
         MVC   IOBDADAD,20(R12)                                     
         L     R12,DCBWKPT2          E2      SET FIRST TIME ONLY SW OFF
         NI    DCWWKNI,X'BF'
         B     APPM7G4                  GO TO TEST DEB
APPM3B3  LA    R13,CJ9+L'CJ9            STOP AT CJ9
         CLR   R12,R13                                           S20201
         BNE   APPM3B4                  NO - TRY CJ8             S20201
*              VARIABLE ONLY CODE THRU M3E4
         L     R12,DCBMSWA
         LA    R13,0(R0,R12)           R13=A(WORK AREA)
         CLC   DECBLGTH,APPMY0         LGTH NOT ZERO MEANS WKR
         BNE   APPMVAR1
         TM    DCBOPTCD,X'02'          IF NO DELETES INSERT IS DUPLICAT
         BZ    APPM3C4
         CLI   18(R13),X'FF'           TEST IF REC DELETED
         BE    APPMVAR2                B=RECORD WITH EQUAL KEY A DELETE
         B     APPM3C4                 INSERT IS DUPLICATE
APPMVAR1 TM    DCBOPTCD,X'02'          TEST IF DELETE OPTION
         BZ    APPMVAR2                B=NO    WKR IS OK
         CLI   18(R13),X'FF'
         BNE   APPMVAR2
         OI    DECBEXC1,X'80'          DELETED RECORD ON WRITE KEY
         MVI   IOBASYN,X'08'           REPLACE MEANS NO RECORD FOUND
         B     APPM1B5
APPMVAR3 L     R12,DCBWKPT2
         TM    DCWWKNI,X'40'           TEST IF 1ST TIME
         BZ    APPMVARN                B=NO
*                                      LINK OF EQUAL REC BECOMES DATA
         MVC   CB10+7(9),0(R13)        OF OFLOW TRACK INDEX
         B     APPM3B5
APPMVARN TM    8(R13),X'08'
         BZ    APPMOV8
         MVC   28(2,R13),8(R13)
APPMOV8  MVC   20(8,R13),0(R13)
         B     APPMVAR4                OF PREV REC
APPMY0   DC    X'0000'
APPMVAR2 L     R12,DECBAREA
         CLC   14(2,R13),16(R12)       COMPARE LENGTH
         BNE  APPMVAR3                 B=NEW REC DIFFERENT THAN OLD
         MVC   CJ11,IOBDADAD           OLD SPOT BIG ENOUGH FOR NEW REC
         MVI   IOBASYN,X'0E'           SCHEDULE ASYNC=14
APPM3E4  NI    IOBINDCT,X'DF'        E4      SET B2-OFLO RCD FR AREA
         B     APPM1B5               E5      SCHED ASYNC ROUTINE
APPM3C4  OI    DECBEXC1,X'01'        C4      SET EXCEP CODE TO IND DUPL
         MVI   IOBASYN,X'08'         D4      SET ASYNC CODE 8
         B     APPM1B5                       SCHED ASYNC ROUTINE
APPM3B4  LA    R13,CJ8+L'CJ8            SEE IF STOPPED AT CJ8
         CLR   R12,R13                                           S20201
         BNE   APPM1B3                  IF NOT, UNREACHABLE BLK  S20201
         L     R12,DCBWKPT2          C5 YES  FIRST TIME ONLY SW ON{
         TM    DCWWKNI,X'40'
         BO    APPM3B5                  YES  B5
APPMVAR4 MVI   IOBASYN,X'0C'           SET ASYNC CODE TO 12         
         B     APPM3E4                       E4
APPM3B5  MVI   IOBASYN,X'0D'         B5      SET ASYNC CODE 13
         MVC   IOBDADAD(7),CJ11         RESTORE TRK INDEX MBBCCHH
         B     APPM3E4
         EJECT
*                             CHANNEL END APP CODE 12 (CP14) CHART M4
APPM4A2  TM    DCBOPTCD,X'80'          TEST IF WRITE CHECK          
         BO    APPVV10                 B=YES                        
         LA    R13,CH18+8                                           
         B     APPM4A21                                             
APPVV10  LA    R13,CH18C+8                                          
APPM4A21 CLR   R12,R13
         BNE   APPMAB3                 NO-RECORD IS UNREACHABLE
         MVC   IOBASYN,CK9+7           RESTORE ASYNC CODE           
         LA    R13,CH31                PRECEDING THE LAST
         L     R12,CB26+4              RESTORE 14 BYTES
         LTR   R12,R12                 AN OVFLOW RCD TO WRITE   YM05342
         BZ    APPM4B0                 BR NO RCD TO WRITE       YM05342
         MVC   0(14,R12),0(R13)        OVERFLOW RECORD
APPM4B0  CLI   IOBASYN,14              TEST  ASYNC CODE= 14     YM05342
         BE    APPM1B4                  IF YES POST COMPLETION
APPM4B3  CLI   IOBASYN,15              TEST-IS ASYNC CODE=15        
         BE    APPM1B5                 B=YES                        
         B     APPN5C2
APPM4C2  MVC   CK6+1(3),DECBKEY+1  IF SO, SET ADDR OF CK6 TO DECB KEY
         LA    R12,IOBDADAD+3           SET UP CP17
         STH   R12,CK1+2
         SRL   R12,16
         STC   R12,CK1+1
         MVC   CK4+1(3),CK1+1
         MVC   CK7A+1(3),CK1+1
         B     APPN9C2             BRANCH
APPN5C2  CLI   CH29+7,X'01'             INDEP OVERFLOW
         BNE   APPN5C3                  NO
APPN5CA  MVI   IOBASYN,X'10'            YES-SCHED ASYN RTN 16
         B     APPM1B5                  TO WRITE EOF IN INDEP OV
APPN5C3  CLI   IOBASYN,9                ASYN CODE=9
         BE    APPN6C3                  IF YES-SCHEDULE CP12B
         MVI   IOBAPP,X'0A'             IF NO SCHED CP14
         MVC   IOBSTART(3),CH13B+5      TO UPDATE TRACK INDICES
         MVC   IOBDADAD(8),CH14
         MVI   CH14,X'03'           SET CCW CH14  TO  NOP
         MVI   CH14+4,X'20'            SET SILI BIT
         MVI   CH14+7,X'01'             COUNT=1
         B     APPN9C5
APPN6C3  MVI   IOBCOUNT,X'01'           ERROR COUNT = 1
         MVC   IOBDADAD,CH14         MBBCCHHR OF PRIME TRACK
         OI    IOBFLAG1,X'C0'           DC ON CC ON
         MVI   IOBAPP,X'15'             APP CODE =21
         MVC   IOBSTART,CD2+5           CP12B STARTS CE0
         MVC   IOBDADAD+5(2),CD0+2     MOVE HH INTO IOBSEEK
         MVC   IOBDADAD+7(1),CB23+7    SEARCH R FOR CP12B
         CLC   IOBDADAD(7),DCBLPDA     LAST TRACK
         BNE   APPN9C5                  BRANCH IF NO
         MVC   DCBLPDA+7(1),CD0A+2
         B     APPN9C5
         EJECT
*                        CHART J5, ABNORMAL END APP CODE 12 (CP14W)
APPJ5B2  TM    IOBECBAD,PERRMASK       PERM ERROR
         BC    PERRNO,APPRVNOR         IF NO-BRANCH BACK TO IOS
         TM    DCBOPTCD,X'80'          TEST FOR WRITE CHECK
         BZ    APPJ5B3                 IF NO-BRANCH
         TM    IOBSENSE,X'08'          TEST FOR DATA CHECK
         BZ    APPMAB3
         LA    R13,CH18C+8                   STOP AT CH18C?
         CLR   R12,R13
         BNE   APPJ5B3                  NO   B3
APPJ5D2  IC    R9,IOBCOUNT           D2 YES  SUBT 1 FROM WC COUNTER
         BCTR  R9,R0                         COUNTER ZERO?
         LTR   R9,R9
         BZ    APPJ5E3                  YES  E3
         STC   R9,IOBCOUNT           F2 NO
         NI    IOBFLAG1,X'FB'                RESET EXCEPTION FLAG
         B     APPRVXCP                      EXCP RETURN TO IOS
APPJ5B3  TM    IOBSENSE,X'08'          TEST FOR DATA CHECK
         BZ    APPMAB3                 BRANCH IF ONT DATA CHECK
         L     R12,IOBCSW            D3 YES
         LA    R12,0(R0,R12)
         LA    R13,CH16+8                    STOP AT CH16?
         CLR   R12,R13
         BE    APPJ5E3                  YES  E3
         LA    R13,CH17+8                    STOP AT CH17?
         CLR   R12,R13
         BE    APPJ5E3                  YES  E3
         LA    R13,CH18+8                    STOP AT CH18?
         CLR   R12,R13
         BNE   APPMAB3                 NO-RECORD IS UNREACHABLE
APPJ5E3  OI    DECBEXC1,X'08'        E3 YES  IND UNCOR I/O ERROR
         B     APPMAB4
APPMAB3  OI    DCBEXCD1,X'04'
APPMAB4  CLI   CH29+7,X'01'            INDEP OVERFLOW
         BNE   APPM1B4                 IF NO SCHEDULE COMPLETION
         B     APPN5CA                 IFYES-SCHEDULE ASYN RTN 1
         EJECT
*              CHART M5  COMPLETION OF CP14, CODE 13
*              CHANNEL END
APPM5A2  TM    DCBOPTCD,X'80'          TEST IF WRITE CHECK          
         BO    APPVV12                 B=YES                        
         LA    R13,CH22+8                                           
         B     APPVV13                                              
APPVV12  LA    R13,CH22C+8                                          
APPVV13  B     APPM4A21
         EJECT
*                        CHART J6, ABNORMAL END APP CODE 13 (CP14W)
APPJ6B2  TM    IOBSENSE+1,X'04'         WAS ABEND DUE TO FILE PROTECT
         BO    APPJ6I2                  YES
         TM    DCBOPTCD,X'80'          TEST IF WRITE CHECK          
         BZ    APPJ6B3                 B=NO                         
         TM    IOBECBAD,PERRMASK        PERMINANT ERROR           
         BC    PERRNO,APPRVNOR          NO RETRY                  
         TM    IOBSENSE,X'08'           WAS ABEND DUE TO DATA CHECK
         LA    R13,CH18C+8                   STOP AT CH18C?
         CLR   R12,R13
         BE    APPJ6D2                  YES  D2
         LA    R13,CH22C+8                   STOP AT CH22C?
         CLR   R12,R13
         BNE   APPJ6B3                  NO   B3
APPJ6D2  IC    R9,IOBCOUNT           D2 YES  SUBT 1 FROM WC COUNTER
         BCTR  R9,R0                         COUNTER ZERO?
         LTR   R9,R9
         BZ    APPJ6E3                  YES  E3
         STC   R9,IOBCOUNT           F2 NO
         NI    IOBFLAG1,X'FB'                RESET EXCEPTION FLAG
         B     APPRVXCP                      EXCP RETURN TO IOS
APPJ6B3  TM    IOBECBAD,PERRMASK     B3    PERMANENT ERROR
         BC    PERRNO,APPRVNOR             YES CONT.NO NORM IOS
         TM    IOBSENSE,X'08'        C3 YES  DATA CHECK?
         BZ    APPM1B3                  NO   N3B3
         L     R12,IOBCSW            D3 YES
         LA    R12,0(R0,R12)
         LA    R13,CH16+8                    STOP AT CH16?
         CLR   R12,R13
         BE    APPJ6E3                  YES  E3
         LA    R13,CH17+8                    STOP AT CH17?
         CLR   R12,R13
         BE    APPJ6E3                  YES  E3
         LA    R13,CH18+8                    STOP AT CH18?
         CLR   R12,R13
         BE    APPJ6E3                  YES  E3
         LA    R13,CH22+8                    STOP AT CH22?
         CLR   R12,R13
         BNE   APPM1B3                  NO   N3B3
APPJ6E3  OI    DECBEXC1,X'08'        E3 YES  IND UNCOR I/O ERROR
         B     APPM1B4                       N3B4
APPJ6I2  LA    R13,CH19+8               DID CHAN PROG STOP AT CH19
         L     R12,IOBCSW               CSW = STOP ADDRESS PLUS 8
         LA    R12,0(R0,R12)            CLEAR HIGH ORDER ZERO
          CLR   R12,R13                 BE MEANS YES, BNE MEANS NO
         BNE   APPM1B3                 NO-UNRTEACHABLE BLOCK
         MVC   IOBDADAD,CJ11            MOVE CJ11 MBBCCHHR TO IOB
         MVC   IOBSTART,IOBCSW+1        RESUME PROCESSING AT CH20
         MVI   IOBASYN,X'01'
         B     APPM1B5                  EXCP IN ASYNCHRONOUS ROUTINE
         EJECT
*
*              NORMAL END APP CODE 10
*              UPDATE TRACK INDICES WITH 2ND PART CP14
*
APPN8B2  LA    R13,CH14+8               DID CP14 END AT CH14
         CLR   R12,R13
         BNE   APPN3B3                  NO-RECORD IS UNREACHABLE
         MVC   IOBASYN,CK9+7           ADD TO END
         CLI   IOBASYN,10              BRANCH IF YES
         BE    APPM4C2                 UPDATE  HI LEVEL INDICES
         CLI   IOBASYN,11
         BE    APPM4C2                 UPDATE  HI LEVEL INDICES
         CLI   IOBASYN,9                                         A27575
         BNE   APPM1B4                  POST COMPLETION          A27575
         L     R12,DCBWKPT2                                      A27575
         TM    DCWWKNI,X'10'            TEST IF ADD TO END       A27575
         BZ    APPM1B4                  NO, POST COMPLETION      A30816
         L     R12,DCBNREC              YES, ADD ONE TO NREC     A30816
         LA    R12,1(R0,R12)            AND UPDATE HIGH LEVEL    A30816
         ST    R12,DCBNREC              INDEXES                  A30816
         B     APPM4C2                                           A30816
         EJECT
*
*              ABNORMAL END APP CODE 10
*              UPDATE TRACK INDICES USING CP14
*
APPN3C2  TM    IOBECBAD,PERRMASK        ABEND DUE TO PERM ERROR
         BC    PERRNO,APPRVNOR
         TM    DCBOPTCD,X'80'           TEST IF WRITE CHECK
         BZ    APPM1B3                  IF NO-BRANCH
         LA    R13,CH3C+8               DID CHAN PROG STOP AT CH
         CLR   R12,R13
         BZ    APPJ5D2                 YES TRY AGAIN
         LA    R13,CH8C+8               DID CHAN PROG STOP AT CH
         CLR   R12,R13
         BE    APPJ5D2                 YES-TRY AGAIN
         LA    R13,CH13C+8              DID CHAN PROG STOP AT CH
         CLR   R12,R13
         BE    APPJ5D2                 YES-TRY AGAIN
         B     APPM1B3                  NO-POST RECORD UNREACHAB
         EJECT
*           READ AND UPDATE APPENDAGE MODULES         *********
APPHCA14 DC    A(CA14-IHACP47+8)
APPHCA20 DC    A(CA20-IHACP47+8)             CA20 OFFSET         S20201
APPHCA23 DC    A(CA23-IHACP47+8)
APPHCA24 DC    A(CA24-IHACP47+8)
APPHCA31 DC    A(CA31-IHACP47+8)
APPCA31B DC    A(CA31B-IHACP47+8)        CA31B OFFSET            S20201
APPHCA36 DC    A(CA36-IHACP47+8)
APPHCA43 DC    A(CA43-IHACP47+8)
APHCA43C DC    A(CA43C-IHACP47+8)
*
APPHCA10 DC    A(CA10-IHACP47+8)        CA10 OFFSET
*
*        CHANNEL END APP CODE 3 (CP2) CHART AA
         USING CP2,R10
APPAAB2  L     R13,DCBWKPT2             R13  ADDR DCB WA
         USING IHADCW,R13
         CLI   DCWNLSD,X'01'            BRANCH IF NLSD NOT 1, I.E.
         BH    APPAEB2                   IF CP1 IS BEING USED
         LA    R13,C35+8                TEST STOP AT C35
         CLR   R12,R13
         BNE   APPAAB3                  NO   B3
         TM    C37,X'20'                YES  DUMMY OR INACTIVE TEST
         BO    APPAAC3                  YES  C3
         MVC   IOBDADAD,C36             NO   C36 TO IOBDADAD
         MVI   IOBASYN,X'03'         E2      ASYNC CD SET=3 (G CP1,2)
         B     APPABSAR
APPAAC3  TM    C37,X'08'                TEST DUMMY CHAINED
         BO    APPAAD3                  YES  D3
         IC    R12,C37                  NO   TEST NLEV PER F = DCBNLEV
         NI    C37,X'07'
         CLC   DCBNLEV,C37
         STC   R12,C37
         BNE   APPAAB3                  NO   B3
         OI    DECBEXC1,X'80'        C5 YES  DECB EXCEP SET=NO RCD FND
APPAAD4  MVI   IOBASYN,X'07'         D4      ASYNC CD SET=7 (NG CP1,2)
         B     APPABSAR                      SAR
APPAAD3  MVC   IOBDADAD,C36             C36 TO IOBDADAD
         B     APPRVXCP                      EXCP RETURN TO IOS
APPAAB3  OI    DECBEXC1,X'04'        B3      DECB EXCEP SET=UNREACHABLE
         B     APPAAD4                       D4
*        ABNORMAL END APP CODE 3 (CP2) CHART AA
*
APPAAA2  L     R13,DCBWKPT2            R13  ADDR DCB WA
         CLI   DCWNLSD,X'01'            BRANCH IF NLSD NOT 1, I.E.
         BNE   APPAEJ2                    IF CP1 IS BEING USED
APPAAA3  TM    IOBECBAD,PERRMASK        TEST PERM ERR
         BNE   APPRVNOR                 NO  NORMAL RET TO IOS
         B     APPAAB3                  YES BRANCH TO INDICATE UNRCHBLE
         DROP  R13
*
*              CHART AE                 END CP1
*              CHANNEL END
         USING CP1,R10
APPAEB2  L     R12,IOBCSW               R12  ADDRESS OF FINAL CCW+8
         LA    R12,0(R12)                     WITH HIGH ORDER BYTE ZERO
         LA    R13,C6+8                 TEST FINAL CCW EQUAL C6
         CLR   R12,R13
         BE    APPAEC2                  BRANCH IF YES
         LA    R13,C16+8                TEST EQUAL C16
         CLR   R12,R13
         BE    APPAEC4                  BRANCH IF YES
         B     APPAAB3                  ERROR IF NO
*              INTERRUPT AT C6
APPAEC2  EQU   *                        *
*
         TM    C6+K5,CC                 WAS C6 CHAINED
         BZ    APPAOS8                  NO, CONTINUE
         NI    C6+K5,CCOFF              TURN OFF INDICATOR
         OI    C6+K4,CC                 RECHAIN C6
         CLI   C10,SEEKHH               C10 SET TO DO SEEK HEAD
         BNE   APPAOSJ4                 NO, FILE PROTECT
         LA    R13,C10A                 RESET IOBSTART TO
         ST    R13,IOBSTART-K1          RESTART AT C10A
         B     APPAEG2                  RESCHEDULE CP1
APPAOS8  EQU   *                        *
*
         TM    C9+K7,MIS                END MASTER INDEX SEARCH.
         BO    APPAEC3                  BRANCH IF YES
         TM    C9+7,X'04'               IF LEVEL OF INDEX IS 3
         BO    APPAEG2                   SET CC IN CP1 TO CHAIN TO
         OI    C6+4,X'40'                SEARCH CYLINDER INDEX
APPAEG2  MVC   IOBDADAD,C8+7            ADDR NEXT SRCH ADDR IN IOB
         B     APPRVXCP                 RETURN TO EXCP
*              DUMMY OR INACTIVE
APPAEC3  TM    C9+7,X'08'               TEST DUMMY ENTRY CHAINED
         BO    APPAEG2                  BRANCH IF YES
         IC    R12,C9+7                 COMPARE LEVEL OF INDEX READ TO
         SLL   R12,29                    DCB INDEX LEVEL
         IC    R13,DCBNLEV
         SLL   R13,29
         CLR   R12,R13
         BNE   APPAAB3                  BRANCH IF NOT EQUAL
         OI    DECBEXC1,X'80'           SET NO REC FND IN EXCEPTION CD
         B     APPAAD4
*              END OF CYLINDER SEARCH
APPAEC4  TM    C18,X'20'                TEST DUMMY OR INACTIVE
         BO    APPAEC5             BRANCH IF YES
         MVC   IOBDADAD,C17                  ADDR NEXT SRCH IN IOB
         MVI   IOBASYN,X'03'       SET ASYNC CODE
         B     APPABSAR                 SCHEDULE ASYNCH RTN
APPAEC5  TM    C18,X'08'                IF DUMMY ENTRY NOT CHAINED,
         BZ    APPAAB3                   BRANCH TO INDICATE UNRCHBLE
         MVC   IOBDADAD,C17             OTHERWISE, RESTART CHAN PROG
         LA    R12,C11                   WITH NEW SRCH ADDRESS
         STH   R12,IOBSTART+1
         SRL   R12,16
         STC   R12,IOBSTART
         B     APPRVXCP                 RETURN TO IOS
*              ABNORMAL END
APPAEJ2  TM    IOBSENSE+1,X'04'        FILE PROTECT               8M642
         BZ    APPAAA3                  BRANCH IF NO.
         LA    R12,C10+8                TEST STOP AT C10
         L     R13,IOBCSW
         LA    R13,0(R13)
         CLR   R12,R13                  BRANCH TO INDICATE UNREACHABLE
         BNE   APPAAB3                   IF NO.
*
APPAOSJ4 EQU   *                        *
*
         TM    C9+7,X'20'              IF DUMMY OR INACTIVE RECORD8M642
         BO    APPAEC3                   BRANCH TO SAME PROCEDURE AS
         LA    R12,C11                   CHANNEL END
         STH   R12,IOBSTART+1           OTHERWISE, RESTART CHAN PROG
         SRL   R12,16
         STC   R12,IOBSTART
         NI    IOBFLAG1,X'06'                                  @ZA11146
         B     APPAEG2
*
*        CHANNEL END APP CODE O,1 (CP4,5) CHART AB
         USING CP4,R10
APPABA2  EQU   *                                                 S20201
*
         L     R12,IOBCSW               CP STOP ADDR
         LA    R12,0(R12)               CLEAR HI BYTE
         SR    R12,R10                  OFFSET OF CCW
*
         CL    R12,APPHCA23             STOP AT CCW CA23        SA65011
         BNE   NOTCA23                  BRANCH IF NO            SA65011
         TM    DECBTYP2,READKU          READ K FOR UPDATE       SA65011
         BZ    NOTCA23                  BRANCH IF NO            SA65011
         CLI   CA24,RDSECT              CA24 READ SECTOR CCW    SA65011
         BE    APPABF3                  BR IF YES - SET UNREACH SA65011
*                                       BLK, ASYN CODE OF 4     SA65011
NOTCA23  EQU   *                        CONTINUE - CP OK        SA65011
         CL    R12,APPHCA10             STOP AT CA10
         BNE   APPAOSA2                 NO
         TM    CA10+K5,CC               WAS CA10 COMMAND CHAINED
         BZ    APPAOSA2                 NO
         OI    CA10+K4,CC               TURN CC BACK ON
         NI    CA10+K5,CCOFF            TURN INDICATOR OFF
         CLI   CA14,SEEKHH              CA14 SEEK HEAD
         BNE   APPAOS21                 NO, FILE PROTECT
         MVC   IOBDADAD,CA12+K7         RESET IOBSEEK
         LA    R12,CA16A                RESET IOBSTART TO
         ST    R12,IOBSTART-K1          START AT CA16A
         B     APPRVXCP                 GO DO AN EXCP
APPAOSA2 EQU   *                        *
*
         NI    DECBEXC1,NOT-OVFLORCD        DECB EXCEP CD SET    S20201
*                                       NOT OFLO                 S20201
         MVC   IOBDADAD+K5(K3),CA25+K5                           S20201
APPABB3  MVI   IOBASYN,X'00'         B3      ASYNC CD SET=0 (G CP4,5,6)
         SPACE 2
APPABSAR B     APPRVBYP                 SCHEDULE ASYNCH ROUTINE  Y02072
         SPACE 2
*        ABNORMAL END APP CODE 0,1 (CP4,5) CHART AB
*
APPABD2  TM    IOBSENSE+1,X'04'      D2      FILE PROT CHK{
         BO    APPABD21                 YES, BRANCH              S20201
         TM    IOBECBAD,PERRMASK        PERMANENT I/O ERROR      S20201
         BZ    APPABD3                  YES, BRANCH              S20201
         B     APPAHD2                  ELSE, RETRY CHANNEL PGM  S20201
APPABD21 SR    R12,R10                  TEST STOP AT CA14        S20201
         CL    R12,APPHCA14
         BNE   APPABF3                  NO   F3
*
APPAOS21 EQU   *                        *
*
         TM    CA13+7,X'20'        F2 YES  DUMMY OR INAC TEST
         BO    APPABF1                  YES  F1
         MVC   IOBDADAD,CA12+7     G2 NO   CA12+7 TO IOBDADAD
         MVC   CA34+7(8),CA12+7         CA12+7 TO CA34+7
         LA    R12,IOBDADAD+3      G1   ADIOBDADAD(CCHH)TO CA26
         ST    R12,CA26
         MVI   CA26,X'31'
         MVC   CA28+1(3),DECBKEY+1 H1   DECBKEY+1 TO CA28 AD
         CLI   IOBAPP,X'00'          J1      READ OR WRITE{
         BE    APPABJ2                  READ J2
         LA    R12,CA40            K1 WRITE CA30 TIC TO CA40
         STH   R12,CA30+2
         SRL   R12,16
         STC   R12,CA30+1
         L     R12,DECBAREA            AREA + 2 TO CA40 ADDR        
         LA    R12,2(R12)                                           
         ST    R12,CA40
         NI    CA31+4,X'DF'            TURN SILI BIT OFF            
         MVI   CA40,X'06'
         MVC   CA43+1(3),CA40+1        AREA + 2 TO CA43 ADDR        
         LA    R13,CA43+6               LENGTH+10 TO CA43 CNT
         LH    R12,10(R12)             KNOWN BLKL FROM BUFFER       
APPABK2  MVI   CA28,SKEQ                SCH EQ FOR WRITE         S20201
APPABK4B LA    R12,10(R12)
         STH   R12,0(R13)
         MVC   CA43C+6(2),CA43+6        SET COUNT IN CA43C
APPABK5  MVI   IOBAPP,X'05'          K5      APPEN CD SET=5
         LA    R12,CA26            J5   AD CA26 TO IOBSTART
         IC    R13,IOBSIOCC
         ST    R12,IOBSIOCC
         STC   R13,IOBSIOCC
         MVI   IOBFLAG1,X'42'        H5 RESTORE IOB FLAGS AND ERR CTR
         MVI   IOBFLAG2,X'00'            CC AND UNRELATED FLAGS ON
         MVI   IOBCSW,X'00'              OTHERS OFF
         XC    IOBERRCT,IOBERRCT
         MVI   IOBCOUNT,X'0A'           INITIALIZE ERR CTR FOR 10 TRIES
         SR    R12,R12             G5   TEST OFLO SAME VOLUMN
         IC    R12,IOBDADAD
         SLL   R12,4
         L     R12,DEBUCBAD(R12)
         LA    R7,0(R7)
         LA    R12,0(R12)
         CLR   R7,R12
         BE    APPRVXCP                 IF SAME, EXCP RET TO IOS
         MVI   IOBASYN,X'01'         G4 NO   ASYNC CD SET=1 (EXCP)
         B     APPABSAR                      SAR
APPABJ2  LA    R12,CA31              J2 CA30 TIC TO CA31
         STH   R12,CA30+2
         SRL   R12,16
         STC   R12,CA30+1
         OI    CA31+4,X'20'            SILI ON                      
         L     R12,DECBAREA            AREA + 2 TO CA31 ADDR        
         LA    R12,2(R12)                                           
         ST    R12,CA31
         MVI   CA31,X'06'
         LA    R13,CA31+6                    LENGTH+10 TO CA31 COUNT
         SR    R12,R12                                           S20201
         AH    R12,DCBRKP               RKP=0                    S20201
         LH    R12,DCBLRECL                                         
         LA    R12,4(R12)               ADD 4 FOR OFLOW BLKL FLD
         BNZ   APPABJ4                  BIF NOT = 0              S20201
         TM    DCBRECFM,BLOCKED         RECFM=BLOCKED            S20201
         BZ    APPABK2                  BIF NOT BLOCKED          S20201
APPABJ4  MVI   CA28,SKEQHI             SCH EQHI FOR (READ AND    S20201
*                                       RKP NE 0                 S20201
*                                       OR (READ AND RKP=0 AND BLKD)
         B     APPABK4B                                             
APPABF1  CLI   DCBNLEV,X'00'         F1      DCBNLEV=0{
         BE    APPABD5                  YES  D5
APPABF3  OI    DECBEXC1,X'04'        F3 NO   DECB EXCEP SET=UNREACHABLE
APPABG3  MVI   IOBASYN,X'04'         G3      ASYNC CD SET=4(NG CP4,5,6)
         B     APPABSAR                      SAR
APPABD3  SR    R12,R10                  DETERMINE ENDING OFFSET  S20201
         TM    SENSE1,NRF               NO RCD FOUND             S20201
         BZ    APPABB1                  NO, BRANCH               S20201
APPABE4  CL    R12,APPHCA20                                      S20201
         BNE   APPAHH2                  NO                       S20201
         CLI   CA21,X'69'          D4,C4 READ FORMAT BLOCKED TEST
         BE    APPABF3                  YES  F3
APPABD5  OI    DECBEXC1,X'80'        D5 NO   DECB EXCEP SET=NO RCD FND
         B     APPABG3                       G3
APPABB1  CL    R12,APPHCA23                                      S20201
         BNE   APPABF3                  NO   F3
         TM    IOBSENSE,X'08'        C1 YES  DATA CHK{
         BZ    APPAHH2                  NO, BRANCH               S20201
APPABD1  OI    DECBEXC1,X'08'        D1 YES  DECB EXCEP SET=UNCOR IO ER
         B     APPABG3                       G3
*
*        CHANNEL END APP CODE 5 (CP6) CHART AC
         USING CP4,R10
APPACA2  SR    R12,R10                  TEST STOP AT CA31
         CL    R12,APPHCA31             NON-RPS HALT.            S20201
         BE    APPACA6                  BIF STOP AT CA31         S20201
         CL    R12,APPCA31B             RPS HALT.                S20201
         BNE   APPACA3                  BIF NO HALT HERE.        S20201
         B     APPACA6A                 COMPARE KEYS            SA65011
*
APPACA6  TM    DECBTYP2,READKU          READ K FOR UPDATE       SA65011
         BZ    APPACA6A                 BRANCH IF NO            SA65011
         TM    CA31+4,CC                CA31 COMMAND CHAINED    SA65011
         BO    APPABF3                  BR IF YES - SET UNREACH SA65011
*                                       BLK, ASYN CODE OF 4     SA65011
APPACA6A CLI   CA28,SKEQHI              SEARCH HI USED.         SA65011
         BNE   APPACA8                  BIF NOT                  S20201
*        COMPARE KEYS. IF THE RECORD'S KEY IS HIGHER, TERMINATE
*        THE SEARCH OF THE OVERFLOW CHAIN.
         IC    R9,CA28+7                GET KEY LENGTH.          S20201
         BCTR  R9,R0                    EXECUTED LENGTH          S20201
         L     R12,CA28                 GET KEY ADDR.            S20201
         L     R13,CA31                 GET DATA ADDR.           S20201
         AH    R13,DCBRKP               FIND THE KEY IN THE      S20201
*                                       RECORD.                  S20201
         LA    R13,K14(R13)             KEY FOUND.               S20201
         EX    R9,COMPARE               CHECK FOR MATCH.         S20201
         BNE   APPACG3                  BIF NO MATCH.            S20201
APPACA8  EQU   *                                                 S20201
         CLC   CA34+7(8),CA12+7         FIRST RECORD IN CHAIN   OY00310
         BE    APPACB3                  YES  B3
         L     R12,DCBRORG3             NO   ADD 1 TO DCBRORG3
         LA    R12,1(R12)
         ST    R12,DCBRORG3
         TM    DECBTYP2,X'20'           B2  TEST READ FOR UPDATE
         BZ    APPACB3                  NO   B3
         MVC   IOBDADAD,CA34+7          YES  IOBDADAD SET FOR WRITEBACK
APPACB3  OI    DECBEXC1,X'02'        B3      DECB EXCEP SET=OFLO RCD
         MVI   IOBASYN,X'00'         B4      ASYNC CD SET=0 (G CP4,5,6)
         B     APPABSAR                      SAR
APPACA3  TM    DCBOPTCD,X'80'          TEST IF WRITE CHECK          
         BO    APPAV1                  B=YES                        
         CL    R12,APPHCA43                                         
         B     APPACOM1                                             
APPAV1   CL    R12,APHCA43C                                         
APPACOM1 BNE   APPACF4                                              
         B     APPACB3                  YES  B3
*
*        ABNORMAL END APP CODE 5 (CP6) CHART AC
*
APPACE2  TM    IOBSENSE+1,X'04'      E2      FILE PROT CHK{
         BZ    APPAGG2                  NO   AG-G2
         SR    R12,R10                  TEST STOP AT CA36
         CL    R12,APPHCA36
         BNE   APPACF4                  NO   F4
*
         CLI   CA36,SEEKHH              CA36 SET TO SEEK HEAD
         BNE   APPAOSE2                 NO
         MVC   IOBDADAD,CA34+K7         RESET IOBSEEK
         B     APPRVXCP                 GO DO AN EXCP
APPAOSE2 EQU   *                        *
*
         TM    CA35+7,X'08'        G2 YES  END OR CHAINED TEST
         BZ    APPACG3                  END  G3
         MVI   IOBASYN,X'01'         J2      ASYNC CD SET=1 (EXCP)
         MVI   IOBFLAG1,X'42'        J3 RESTORE IOB FLAGS AND ERR CTR
         MVI   IOBFLAG2,X'00'            CC AND UNRELATED FLAGS ON
         MVI   IOBCSW,X'00'              OTHERS OFF
         XC    IOBERRCT,IOBERRCT
         MVC   IOBDADAD,CA34+7          SET SEEK SEARCH ADDRESS
         B     APPABSAR                      SAR
APPACE3  TM    IOBECBAD,PERRMASK        TEST PERM ERR
         BNE   APPRVNOR                  NO - NORMAL RET TO IOS
         L     R12,IOBCSW          E4 YES  TEST STOP AT CA31 OR CA43
         LA    R12,0(R12)
         SR    R12,R10
         CL    R12,APPHCA31
         BE    APPACE5                  YES  E5
         CL    R12,APPHCA43
         BE    APPACE5                  YES  E5
APPACF4  OI    DECBEXC1,X'04'        F4 NO   DECB EXCEP SET=UNREACHABLE
APPACG4  MVI   IOBASYN,X'04'         G4      ASYNC CD SET=4(NG CP4,5,6)
         B     APPABSAR                      SAR
APPACG3  OI    DECBEXC1,X'80'        G3      DECB EXCEP SET=NO RCD FND
         B     APPACG4                       G4
APPACE5  TM    IOBSENSE,X'08'        E5      DATA CHK{
         BZ    APPACF4                  NO   F4
APPACF5  OI    DECBEXC1,X'08'        F5 YES  DECB EXCP SET UNCOR IO ERR
         B     APPACG4                       G4
*              CHART AD       CHANNEL END + NORMAL END CP7
*              CHANNEL END
         USING CP7,R10
APPADB2  EQU   *                                                 S20201
         MVI   IOBASYN,K2               SET ASYN CODE AND BRANCH S20201
         B     APPABSAR                 SCHEDULE ASYN ROUTINE
APPADB3  OI    DECBEXC1,X'04'           IF NOT, INDICATE UNREACHABLE IN
APPADB4  MVI   IOBASYN,X'06'             DECB AND SET ASYNCH CODE
         B     APPABSAR                 SCHEDULE ASYNCH ROUTINE
*              ABNORMAL END
APPADE2  TM    IOBECBAD,PERRMASK        IF NOT PERM ERR,
         BNE   APPRVNOR                  NORMAL RET TO IOS
         LA    R12,CA46+8               IF STOP AT CA46 AND
         L     R13,IOBCSW                CAUSE IN NOT DATA CHECK,
         LA    R13,0(R13)                OR IF STOP NO AT CA46
         CLR   R13,R12                   GO TO APPADB3
         BNE   APPADB3
         TM    IOBSENSE,DATACHK                                  S20201
         BZ    APPADB3
APPADG3  OI    DECBEXC1,X'08'           DATA CHECK, SET IO UNCORRTABLE
         B     APPADB4                   ERROR BIT IN DECB
*              CHART AG  ABNORMAL END, NOT FILE PROT CHK
         USING CP4,R10                                           S20201
APPAGG2  TM    IOBSENSE,X'08'          WAS CAUSE OF INTERRUPT A DATA
         BZ    APPACE3                    CHECK AT CA43C
         LA    R12,CA43C+8
         L     R13,IOBCSW
         LA    R13,0(R13)
         CLR   R12,R13
         BNE   APPACE3                  BRANCH IF NO
         SR    R13,R13                  SUBTRACT ONE FROM ERROR COUNTER
         IC    R13,IOBCOUNT
         BCT   R13,APPAGH3              IF ZERO, BRANCH TO INDICATE
         B     APPACF5                   UNCORRECTABLE IO ERROR
APPAGH3  STC   R13,IOBCOUNT             IF NINE, FIRST TIME THRU SO
         CLI   IOBCOUNT,X'09'            MUST SET UP IOB
         BNE   APPAGK4
         MVC   IOBDADAD,CA34+7
         LA    R12,CA41
         STH   R12,IOBSTART+1
         SRL   R12,16
         STC   R12,IOBSTART
APPAGK4  NI    IOBFLAG1,X'FB'           RESET EXCEPTION FLAG
         B     APPRVXCP                 RETURN TO EXCP
*              ABNORMAL END
APPAHD2  LA    R13,CA24C+8              TEST STOP AT CA24C
         CLR   R12,R13
         BNE   APPRVNOR                 NORMAL RETURN            S20201
         TM    IOBSENSE,X'08'
         BZ    APPRVNOR                 BRANCH IF NO             S20201
         LA    R13,CA24D                 START ADDRESS = CA24D
         STH   R13,IOBSTART+1
         SRL   R13,16
         STC   R13,IOBSTART
         MVC   IOBDADAD+3(5),CA25+3     RESET IOBSEEK           SA62417
         B     APPRVXCP                 RETURN TO EXCP
APPAHH2  EQU   *                                                 S20201
         AR    R12,R10                  CORRECT BASE REG         S20201
         TM    IOBSENSE,DATACHK         WAS IT A DATA CHECK      S20201
         BZ    APPABF3                  NO, UNREACHABLE BLOCK    S20201
         LA    R13,CA24C+L'CA24C        PERM ERROR AND DATA CHK
         CLR   R12,R13                  STOP AT CA24C            S20201
         BE    APPABD1                  YES, UNCORR I/O ERROR    S20201
         LA    R13,CA24F+8              IF PERMANENT, WAS STOP AT
         CLR   R12,R13                   CA24F
         BNE   APPABF3                  BRANCH IF NO
         B     APPABD1                  BRANCH IF YES            S20201
*              CHART AI  ABNORMAL END, CODE 2, CP7, WRITE CHECK
         USING CP7,R10
APPAIB2  TM    IOBSENSE,X'08'           CAUSE A DATA CHECK AT
         BZ    APPADE2                   CA46C TEST
         LA    R13,CA46C+8
         CLR   R12,R13
         BNE   APPADE2                  BRANCH IF NO
         SR    R13,R13                  SUBTRACT 1 FROM ERR CTR
         IC    R13,IOBCOUNT
         BCT   R13,APPAIF2
         B     APPADG3                  IF ZERO, BRANCH
APPAIF2  STC   R13,IOBCOUNT
         NI    IOBFLAG1,X'FE'           RESET EXCEPTION FLAG   @ZA11146
         B     APPRVXCP                 RETURN TO IOS EXCP
APPRVXCP SR    R12,R12
         IC    R12,IOBDADAD             GET BB
         SLL   R12,4
         LA    R12,32(R12,R3)
         MVC   IOBDADAD+1(2),4(R12)
         XC    IOBFLAG2(3),IOBFLAG2                               15924
         XC    IOBCSW(9),IOBCSW                                   15924
         XC    IOBERRCT(2),IOBERRCT                               15924
         NI    IOBFLAG1,X'C2'                                     15924
         B     EXCP
COMPARE  CLC   K0(K1,R12),K0(R13)                                S20201
*
PATCH    DC    XL((*-IGG019IN)/20)'00'  ZEROED PATCH AREA        Y02072
*
         EJECT
APPN3B3  EQU   APPM1B3        LABELS USED IN COMMON ROUTINE TO
APPN3B5  EQU   APPM1B5         SCHEDULE ASYNCHRONOUS ROUTINE
IHAAPPRV DSECT
APPRVNOR DS    A                      NORMAL
APPRVSKP DS    A                      SKIP POST
EXCP     DS    A                        EXCP
APPRVBYP DS    A                      BYPASS
IHACVTEX DSECT   COMMUNICATION VECTOR TABLE
*        READ K, READ KU, AND WRITE K CHANNEL PROGRAMS(WRITE CHECK)
IHACP47  IGGCP47   OPTCD=W                                       S20201
*                        ADDRESS OF COMMUNICATION VECTOR TABLE
CVTPTR   EQU   16
         EJECT
         ORG   CP4
*        WRITE KEY NEW CHANNEL PROGRAM REFERENCES (WRITE CHECK)
IHAWKNCP IGGWKNCP  OPTCD=VW                                      S20201
CP12A    DS    0D                                                S20201
         IGGCP12A  OPTCD=V                                       S20201
CP12B    DSECT
         IGGCP12B  OPTCD=V                                       S20201
CP123    DSECT
         IGGCP123  OPTCD=V                                       S20201
         EJECT
*              DATA EVENT CONTROL BLOCK
IHADECB  DSECT
         DS    0F
DECBECB  DS    CL4                      EVENT CONTROL BLOCK (ECB)
DECBTYP1 DS    BL1                      TYPE B6 - 1 IF LENGTH IS S
*                                            B7 - 1 IF AREA IS S
DECBTYP2 DS    BL1                           B0 - 1 IF READ K
*                                            B1 - 1 IF READ KX
*                                            B2 - 1 IF READ KU
*                                            B4 - 1 IF WRITE K
*                                            B5 - 1 IF WRITE KN
DECBLGTH DS    CL2                      LENGTH OF BLOCK
DECBDCBA DS    A                        POINTER TO DCB
DECBAREA DS    A                        ADDRESS OF AREA
DECBLOGR DS    A                        POINTER TO LOGICAL RECORD
DECBKEY  DS    A                        POINTER TO KEY
DECBEXC1 DS    BL1                      EXCPTN CD B0-RECORD NOT FOUND
*                                                 B1-RECORD LGTH CHK
*                                                 B2-NO SPACE
*                                                 B3-INVALID REQUEST
*                                                 B4-UNCORRECTABLE IO
*                                                 B5-UNREACHABLE BLOCK
*                                                 B6-OVERFLOW RECORD
*                                                 B7-DUPLICATE
DECBEXC2 DS    BL1                                B7-READ KU
         EJECT
         DCBD  DSORG=(IS)
         EJECT
IHAIOB   IGGIOBD
SENSE1   EQU   IOBSENSE+1                                        S20201
CPSTART  EQU   IOBSTART-1                                        S20201
         EJECT
IHADCW   IGGBISAM  OPTCD=V                                       S20201
         EJECT
IHADEB   IGGDEBD
         END
