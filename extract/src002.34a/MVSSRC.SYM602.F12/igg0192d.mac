         TITLE 'IGG0192D - OPEN, QISAM LOAD MODE'
IGG0192D CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192D                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, QISAM LOAD MODE--INITIAL LOAD OR      *
*                    RE-LOAD                                          *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = CALCULATE THE VARIOUS DCB FIELDS NEEDED TO ALLOCATE      *
*            TRACK AND MASTER INDICES.  DETERMINE IF HIGH LEVEL       *
*            MASTER INDICES ARE TO BE DEVELOPED AND WHERE TO LOCATE   *
*            THEM.                                                    *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES                      *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT                              *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 992 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = REENTRANT, PRIVILEGED                               *
*                                                                     *
* ENTRY-POINT = IGG0192D                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG01921      *
*              IN STORAGE PROTECT KEY 5 AND PRIVILEGED STATE.         *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED ON ENTRY ARE                          *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = TRANSFERS CONTROL IN STORAGE PROTECT KEY 5 TO ISAM    *
*               OPEN EXECUTOR IGG0192E IF HIGH LEVEL INDICES ARE TO   *
*               BE DEVELOPED, OTHERWISE TO IGG0192F.                  *
*                                                                     *
* EXIT-ERROR = ABEND CODE:                                            *
*              002 - CYLINDER OVERFLOW INCORRECTLY SPECIFIED OR       *
*                    NO PRIME DATA TRACKS AVAILABLE (ALL SPACE USED   *
*                    BY TRACK INDEX AND CYLINDER OVERFLOW)            *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                                                                     *
*    CONTROL-BLOCK = CVT, DCB COPY, DEB, AND UCB                      *
*                                                                     *
* TABLES = INDEX LOCATION TABLE                                       *
*          I/O DEVICE CHARACTERISTICS TABLE                           *
*                                                                     *
* MACROS = ABEND, MODESET, AND XCTL                                   *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                  CHANGES SINCE VS2-3.7                              *
*C044144,A024780,A044054-044134                              @ZA30666 *
*                                                                     *
***********************************************************************
         EJECT
FORCORE  DSECT
         IECDSECT
*
ISLCOMON DSECT
         DS    0D
ISLECBA  DS    A                        FOR CP18 AND CP20
ISLIOBA  DS    CL40
ISLECBB  DS    A                        FOR CP21
ISLIOBB  DS    CL40
ISLECBC  DS    A                        FOR CP19
ISLIOBC  DS    CL40
ISLAREAZ DS    CL88                     FOR CP19
ISLIXLT  DS    CL104                    INDEX LOCATION TABLE
ISLNIRT  DS    CL3                      HHR OF DUMMY TRACK INDEX
ISLHIRT  DS    CL1                      NO INDX ENT ON PRIM DATA TRACK
ISLCBF   DS    F                        BUR CTRL PTR
ISLBMPR  DS    CL1                      USED TO BUMP CBF TO NEXT RCD
ISLFBW   DS    CL1                      NUM BUFS SCHED TO WRITE
ISLQC    DS    H                        NUM PRIME CYLINDERS
ISLM     DS    H                        NUM ENTRIES ON LAST TRK INDEX
ISLP     DS    H                        NUM TRACKS PER CYLINDER
ISLQ     DS    H                        NUM FULL TRKS TO BE INDEXED
ISLQ1    DS    H                        NUM TOTAL TRKS TO BE INDEXED
ISLQ2    DS    H                        NUM TRACKS IN CURNT INDEX
ISLQ3    DS    H                        NUM ENTRIES IN CURNT INDEX
ISLQ4    DS    H                        NUM CYLNDRS OF MASTER INDEX
ISLR2    DS    F                        ADR OF PRIME DEV TBL ENTRY
ISLDORE  DS    CL1                      SKIP OPEN 2D INDICATOR
ISLURSAV DS    16F                      USERS REGISTERS
ISLVRSAV DS    16F
ISLIGAP  EQU   ISLCOMON+704             INTER RECORD OVERHEAD    S20201
ISLLGAP  EQU   ISLCOMON+706             LAST RECORD OVERHEAD     S20201
IHADEB   DSECT
         DS    0D
DEBNMSUB DS    0CL1                     NUM SUBROUTINES LOADED AT OPEN
DEBTCBAD DS    A                        TCB POINTER
DEBAMLNG DS    0CL1                     LENGTH OF ACCESS METHOD SECTION
DEBDEBAD DS    A                        POINTER TO NEXT DEB IN TASK
DEBOFLGS DS    0CL1                     DATA SET STATUS FLAGS
DEBIRBAD DS    A                        POINTER TO IRB FOR ERROR EXIT
DEBOPATB DS    0CL1                     FILE TYPE
DEBSYSPG DS    A                        SYSTEM PURGE CHAIN
DEBNMEXT DS    0CL1                     NUMBER OF EXTENTS CONSTRUCTED
DEBUSRPG DS    A                        USER PURGE CHAIN
DEBPRIOR DS    0CL1                     DISPATCHING PRIORITY
DEBECBAD DS    A                        POINTER TO ECB
DEBPROTG DS    0BL1                     TASK PROTECTION FLAG
DEBDEBID DS    0BL1                     F TO IDENTIFY AS A DEB
DEBDCBAD DS    A                        POINTER TO CORRESPONDING DCB
DEBEXSCL DS    0CL1                     SCALE FOR SIZE OF DEV SECT
DEBAPPAD DS    A                        POINTER TO APPENDAGE SECTION
DEBNIEE  DS    0CL1                     NUMBER OF INDEX EXTENTS
DEBFIEAD DS    A                        ADR OF FIRST INDEX EXTENT
DEBNPEE  DS    0CL1                     NUMBER OF PRIME DATA EXTENTS
DEBFPEAD DS    A                        ADR OF FIRST PRIME DATA EXTENT
DEBNOEE  DS    0CL1                     NUMBER OF OVERFLOW EXTENTS
DEBFOEAD DS    A                        ADR OF FIRST OVFLO EXTENT
         DS    CL4                      DISABLED MODULE
DEBDVMOD DS    0CL1                     DEVICE MODIFIER
DEBUCBAD DS    A                        POINTER TO UCB
DEBBINUM DS    CL2                      BB OF EXTENT
DEBSTRCC DS    CL2                      STARTING CC
DEBSTRHH DS    CL2                      STARTING HH
DEBENDCC DS    CL2                      ENDING CC
DEBENDHH DS    CL2                      ENDING HH
DEBNMTRK DS    CL2                      NUMBER OF TRACKS IN EXTENT
         DCBD  DSORG=(IS)
IGG0192D CSECT
START    BALR  R15,0                    ESTABLISH ADDRESSABILITY
         USING *,R15
R0       EQU   0                        WORK REGISTER
R1       EQU   1                        BASE REGISTER FOR DCB
R2       EQU   2                        DEVICE TABLE ENTRY ADDRESS
R3       EQU   3                        WORK REGISTER
R4       EQU   4                        WORK REGISTER
R5       EQU   5                        WORK REGISTER
R6       EQU   6                        WORK REGISTER
R7       EQU   7                        WORK REGISTER
R8       EQU   8                        WORK REGISTER
R9       EQU   9                        WORK REGISTER
R10      EQU   10                       WORK REGISTER
R11      EQU   11                       WORK REGISTER
R12      EQU   12                       BASE REGISTER FOR USERS DEB
R13      EQU   13                       BASE REG FOR COMON WORK AREA
R14      EQU   14                       WORK REGISTER
R15      EQU   15                       WORK REGISTER
RBASE    EQU   3                        BASE REG FOR THIS MODULE
RPAR     EQU   5                        ADDRESS OF PARAMETER TABLE
RWTG     EQU   6                        ADDRESS OF WHERE-TO-GO TABLE
RPARC    EQU   7                        CURRENT PARAMETER ENTRY
RWTGC    EQU   8                        CURRENT WHERE-TO-GO ENTRY
RCORE    EQU   4                        BASE REG FOR OPEN WORK AREA
RJ       EQU   15                       WORK REGISTER
UCBTYPE  EQU   19                       OFFSET FOR TYPE OF DEVICE
CVTPTR   EQU   16                       OFFSET FOR COMMUN VECTOR TABLE
LIDO     EQU   7                        LAST RECORD OVERHEAD     S20201
NIDO     EQU   6                        NORMAL RECORD OVERHEAD   S20201
SW       EQU   9                        DEV TBLE SW              S20201
ZEUS     EQU   X'08'                    ZEUS INDICATOR           S20201
RC2C     EQU   X'2C'                    ISAM 002 ABEND RET CODE  Y02072
HLINDOF  EQU   X'04'                    HI LEV INDEX GT 65535  @ZA30666
***********************************************************************
*                ENTRY POINT FOR QISAM - LOAD MODE                    *
*                     ENTERED FROM OPEN MACRO                         *
***********************************************************************
ISLFA01  EQU   *
BASETAG  L     R1,0(RPARC)              R1 = ADDRESS OF DCB
         USING IHADEB,R12
         USING IHADCB,R1
         L     R13,DCBWKPT1
         USING ISLCOMON,R13             R13 = ADDRESS OF COMON WORK ARE
         LR    RBASE,R15                CHANGE BASE OF THIS MODULE
         L     RCORE,4(RWTGC)           RCORE = ADR OF OPEN WORK AREA
         USING FORCORE,RCORE
         STM   R0,R15,DXCCW1            SAVE REGISTER CONTENTS   Y02072
         LR    R0,RCORE                 SAVE OPEN W/A ADDR       Y02072
         SR    R8,R8                    CLEAR REGISTER 8
         STH   R8,DCBEXCD1              SET EXCD1 AND 2 TO ZERO
         L     R12,DCBDEBAD             R12=DEB ADDRESS
         L     R2,DCBLRAN                DEVICE TABLE ENTRY      S20201
         IC    R8,DCBKEYLE              CALCULATE DCBHIRCM
         LA    R8,10(R8)                R8=DCBKEYLE+10
         LH    R3,4(R2)                 R3=TRACKLENGTH
*
         MODESET  KEYADDR=DXUKEY,WORKREG=4  SET USERS KEY        Y02072
*
         DROP  RCORE                    END OPEN WA USING        Y02072
         LH    R4,ISLLGAP               LIDO                     S20201
         LH    R6,ISLIGAP               NIDO                     S20201
         SR    R3,R4                    R3 = TRLNG - LIDO        S20201
         LR    R5,R8                    R5 = KEY + 10
         MH    R5,10(R2)                R5=(KEYLE+10)TOL
         SRA   R5,9                     R5 DIVIDED BY 512
         AR    R5,R6                    R5 = TOL(KEYLE+10)+NIDO  S20201
         LR    R7,R3                    R7=TRLNG-LIDO
         SR    R7,R8                    R7=TRLNG-(KEYLE+10)-LIDO
         SR    R6,R6                    CLEAR R6
         SR    R4,R4                    CLEAR R4                 S20201
         DR    R6,R5                    DIVIDE R7 BY R5
         LA    R9,1(R7)                 R9=NUMB INDEX ENTRIES PER TRACK
         STC   R9,ISLHIRT               SET HIRT = HIRCM         Y02072
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         STC   R9,DCBHIRCM              SET HIRCM = R9
         STC   R9,DCBHIRTI              SAVE HIRTI FOR RL        O19113
         IC    R4,DCBCYLOF              R4=NUMB TRACKS OF TRACK OVERFLO
         SR    R7,R7                    CLEAR WORK REGISTER      Y02072
         IC    R7,3(R2)                 NO OF TRKS PER CYL       A34907
         SR    R7,R4                    1ST TRACK OF CYL OVFL    A43218
*   CHECK IF FIRST TRACK OF CYLINDER OVERFLOW IS VALID. ABEND IF NOT.
         BNP   TOOLONG                  INVALID - BR - ABEND     A43218
         BCTR  R7,0                     R7=HH OF LAST PRIME DATA TRACK
         STH   R7,DCBLDT                LDT = 0H
*
*        ALLOCATE TRACK INDEX      CHART FB
*
ISLFB01  LA    R7,1(R7)                 R7=DCBLDT+1=N
         SLA   R7,1                     R7= 2(N)
         LA    R7,1(R7)                 R7= 2N+1
         LA    R9,2(R9)                 R9= DCBHIRCM+2  =C+2
         SR    R6,R6                    CLEAR R6
         SR    R10,R10                  R10=SKIP
         DR    R6,R9                    R7=Q = NO. OF TRACKS
         IC    R9,DCBKEYLE              SET R9 = KEYLE
         AH    R9,DCBBLKSI              R9= KEYLE+BLKSI
         LTR   R6,R6                    TEST R
         BC    8,ISLFB02                BRANCH IF R = 0
         TM    DCBRECFM,X'80'           IS IT VLR?                VLR
         BZ    ISLFB01A                 YES, BYPASS SHRD TRK.     VLR
         MR    R4,R6                    R5= R(TOL(KEYLE+10)+NIDO)
         LR    R11,R3                   R11=TRLNG-LIDO
         SR    R11,R5                   R11=REMAINDER OF TRACK
* COMPARE BLKSI+KEYLE TO TRLNG-LIDO-R(TOL(KEYLE+10)+NIDO)
         CR    R9,R11                   COMPARE K+BLK TO REMAINDER TRK
         BNH   ISLFB05                  BRANCH=TRACK IS SHAREABLE
ISLFB01A EQU   *                                                  VLR
         SH    R6,ISLC12                R6=R-2 TRACK CONTAINS 2 LESS
         BC    2,ISLFB03                BRANCH=TRACK USED       ENTRIES
         LA    R10,1(R10)               SKIP=1
ISLFB02  BCTR  R7,0                     R7= Q-1
         IC    R4,DCBHIRCM              SET R4 = HIRCM
         AR    R6,R4                    R6= R+HIRCM
ISLFB03  MVI   DCBHIRSH,X'00'           DCBHIRSH=0
ISLFB06  EQU   *
         LR    R5,R0                    OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,R5               OPEN WA ADDRESSABILITY   Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=5  SET USERS KEY        Y02072
*
         DROP  R5                       END OPEN WORK AREA USING Y02072
         STC   R6,ISLNIRT+2            STORE R IN WORK AREA NIRT
         STH   R7,ISLNIRT              STORE Q IN WORK AREA NIRT
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         SR    R5,R5                    CALCULATE DCBNCRHI=CORE FOR INX
         IC    R5,DCBHIRCM              R5=NO OF INDICES PER TRACK
         MR    R4,R7                    R5=HIRCM(Q)
         AR    R5,R6                    R5=R+Q(HIRCM)
         MR    R4,R8                    R5=(KEYLE+10)(R+Q(HIRCM))
         STH   R5,DCBNCRHI
***********************************************************************
* TEST THE CALCULATED VALUE FOR HIGH-LEVEL INDEX IN STORAGE. IF THE   *
* AMOUNT IS GREATER THAN 65535, INDICATE THIS CONDITION BY TURNING    *
* ON X'04' IN DSCOPTCD. THIS BIT WILL BE TESTED DURING BISAM OPEN,    *
* AND IF ON, THE INDEX WILL NOT BE READ INTO STORAGE.                 *
***********************************************************************
         LTR   R4,R4                    PRODUCT GT 32 BITS?    @ZA30666
         BP    GT65535                  YES-TURN ON INDICATOR  @ZA30666
         SRL   R5,16                    SHIFT OUT 65535        @ZA30666
         LTR   R5,R5                    SIZE GT 65535?         @ZA30666
         BZ    LT65536                  NO-BYPASS              @ZA30666
GT65535  LR    RCORE,R0                 O/C/EOV WORK AREA      @ZA30666
         USING FORCORE,RCORE            OPEN WA ADDRESSABILITY @ZA30666
         OI    DSCOPTCD,HLINDOF         INDICATE TO USER       @ZA30666
         DROP  RCORE                    END OPEN WA USING      @ZA30666
LT65536  CLI   DCBHIRSH,X'00'           TEST IF SHARED         @ZA30666
         BC    8,ISLFB04                BRANCH=NOT SHARED
         LA    R6,1(R6)                 CALCULATE FIRSH HHR OF 1ST DATA
         STC   R6,DCBFIRSH+2                             RCD ON CYL
         STH   R7,DCBFIRSH
         B     ISLFC01
ISLFB04  MVI   DCBFIRSH+2,X'01'         CALC FIRSH - TRACK NOT SHARED
         LA    R7,1(R7)                 R=01
         AR    R7,R10                   ADD R10 IF SKIPPED TRACK
         STH   R7,DCBFIRSH              INSERT FIRSH HH
         B     ISLFC01
*                                       CALC HIRSH - SHAREABLE TRACK
ISLFB05  SR    R11,R9                   R11=TRACK BALANCE
         LR    R5,R9                    R5= BLKSI+KEYLE
         MH    R5,10(R2)                R5= TOL(BLKSI+KEYLE)
         SRA   R5,9                     DIVIDE BY 512 FOR TOLERANCE
         LR    R10,R0                   OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,R10              OPEN WORK AREA ADDR      Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=10  SET USERS KEY       Y02072
*
         DROP  R10                      END OPEN WORK AREA USING Y02072
         AH    R5,ISLIGAP               R5=TOL(BLKSI+KEY)+NIDO   S20201
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         SR    R10,R10                  CLEAR WORK REGISTER
         DR    R10,R5                   REMAINDER/DATA RCD
         LA    R11,1(R11)                   + 1
         AR    R11,R6                       + REMAINDER
         STC   R11,DCBHIRSH                 = HIRSH
         SR    R10,R10                  CLEAR WORK REGISTER
         B     ISLFB06
*
*        CHART FC
ISLFC01  EQU   *                        *                        A43218
*   TEST FOR BAD ALLOCATION (NO ROOM FOR PRIME DATA).
         CLC   DCBLDT,DCBFIRSH          IS PRIME IN OVERFLOW     A43218
         BL    TOOLONG                  YES - ALLOCATION INVALID A43218
         MVI   DCBNLEV,X'00'            SET NLEV TO ZERO         A43218
         IC    R10,DEBNPEE              NO OF PRIME DATA EXTENT ENTRIES
         L     R4,DEBFPEAD              ADDR OF 1ST PRIME EXTENT ENTRY
         SR    R5,R5                    CLEAR WORK REGISTER
         STH   R5,DCBRORG2              SET RORG2 = ZERO
         LR    R3,R0                    OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,R3               WORK AREA ADDRESSABILITY Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=3  SET USERS KEY        Y02072
*
         DROP  R3                       END OPEN WORK AREA USING Y02072
         MVC   ISLQC(2),DCBNBOV         ISLQC=NO. OF PRIME CYLS
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         STH   R5,DCBNBOV               CLEAR NBOV
ISLFC02  AH    R5,10(R4)                ADD ENDCC
         SH    R5,6(R4)                 SUBTRACT STARTCC
         LA    R5,1(R5)                      +1
         LA    R4,16(R4)                NEXT EXTENT ENTRY
         BCT   R10,ISLFC02
         LR    R3,R0                    OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,R3               WORK AREA ADDRESSABILITY Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=3  SET USERS KEY        Y02072
*
         DROP  R3                       END WORK AREA USING      Y02072
         STH   R5,ISLQC                 STORE NO OF PRIME CYLS
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         CLC   DEBSTRCC(3),DEBENDCC     TEST STRCCH VS ENDCCH     13539
         BNE   ISLFC03                  BRANCH IF GTR THAN 1 CYL  13539
         CLI   DEBNPEE,X'01'            TEST GTR 1 EXTENT
         BC    2,ISLFC03                BRANCH = GTR 1 EXTENT
         XC    DCBFTHI+1(2),DCBFTHI+1   SET BB TO ZERO           A34907
         MVC   DCBFTHI+3(4),DEBSTRCC    STORE DCBFTHI CCHH        13539
         MVI   DCBFTHI,X'01'            DCBFTHI M = 1
         LR    R3,R0                    OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,R3               WORK AREA ADDRESSABILITY Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=3  SET USERS KEY        Y02072
*
         DROP  R3                       END OPEN WA USING        Y02072
         MVI   ISLDORE,X'E5'            E5 INDICATES ONLY 1 CYL
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         LR    RCORE,R0                 GET OPEN W/A ADDR        Y02072
         USING FORCORE,RCORE            OPEN WA ADDRESSABILITY   Y02072
         LM    R0,R15,DXCCW1            RESTORE REGISTERS        Y02072
         DROP  RCORE                    END OPEN WORK AREA USING Y02072
         MVC   0(L'LOAD2F,RWTGC),LOAD2F INITIALIZE WHERE-TO-GO   Y02072
         B     ISLFC04F
ISLFC03  EQU   *                        MORE THAN 1 CYL IN DATA SET
         MVI   DCBNLEV,X'01'            SET NLEV TO INDICATE A CLY INDX
         LR    R3,R0                    OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,R3               OPEN WA ADDRESSABILITY   Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=3  SET USERS KEY        Y02072
*
         DROP  R3                       END OPEN WORK AREA USING Y02072
         LA    R5,ISLIXLT               R5 POINTS TO INDEX LOCATION TBL
         MVC   ISLQ3(2),ISLQC           SET Q3 TO C = NO PRIME DATA CYL
         SR    R3,R3                    CLEAR WORK REGISTER
         L     R4,DEBFIEAD              TEST IF SEPARATE INDEX AREA
         CR    R3,R4
         BNE   ISLFEH1                  BRANCH = SEPARATE INDEX AREA
         L    R4,DEBFOEAD
         CR    R3,R4
         BNE   ISLFEH1                  BRANCH = SEP OVFLO AREA SA69202
         MVI   ISLDORE,X'E1'            E1=ONLY PRIME EXTENTS IN
         B     ISLFC04                    DATA SET
***                                                                 ***
* CHART FE-H1-4  THIS SECTION ENTERED WHEN INDICES NOT IN PRIME AREA  *
***                                                                 ***
ISLFEH1  L     R8,DEBFPEAD              R8=ADR OF 1ST PRIME EXTENT
         L     R6,0(R8)                 R6=ADR OF PRIME UCB
*                                       R4=ADR OF 1ST INDEX EXTENT
         L     R7,0(R4)                 R7=ADR OF INDEX UCB
         CLC   19(1,R6),19(R7)
         BNE   ISLFEH1A                 BRANCH-INDEX AND PRIME NOT
*                                           ON SAME DEVICE
         CLC   4(2,R8),4(R4)            PRIME BB VS INDEX BB
         BE    ISLFEH11                 BRANCH-INDEX AND PRIME
*                                           ON SAME DEVICE
ISLFEH1A SR    R2,R2                    CLEAR REGISTER           A34907
         IC    R2,UCBTYPE(R7)           GET DEVICE TYPE FOR      A34907
         STC   R2,ISLAREAZ+86           SAVE DEVICE TYPE
         L     R4,CVTPTR                R4 = COMMUNICATION VECTOR TBL
         L     R6,64(R4)                R6= PTR TO I/O DEVIDE TABLE
         IC    R3,0(R2,R6)              R2=DEVICE TBL ENTRY FOR INDEX
         LA    R2,0(R6,R3)
         SR    R3,R3                    CLEAR WORK REGISTER
         IC    R3,DCBKEYLE              R3 = KEYLE
         LA    R3,10(R3)                R3=KEYLE + 10
         LH    R7,4(R2)                 R7=TRLNG
         SR    R4,R4                    CLEAR REG                S20201
         SR    R6,R6                    CLEAR REG                S20201
         IC    R4,LIDO(R2)              LIDO - NON ZEUS          S20201
         IC    R6,NIDO(R2)              NIDO - NON ZEUS          S20201
         TM    SW(R2),ZEUS              IF ZEUS CHANGE OVERHEADS S20201
         BZ    NOTZEUSZ                 NOT ZEUS - BR            S20201
         LH    R4,NIDO(R2)              LIDO AND NIDO            S20201
         LR    R6,R4                    *                        S20201
NOTZEUSZ EQU   *                        *                        S20201
         SR    R7,R4                    *                        S20201
         SR    R7,R3                    R7=TRLNG-LIDO-(KEYLE+10)
         MH    R3,10(R2)                R3=TOL(KEYLE+10)
         SRA   R3,9                     DIVIDE BY 512 FOR TOLERANCE
         AR    R3,R6                    R3 = TOL(KEYLE+10)+NIDO  S20201
         SR    R6,R6                    CLEAR R6
         DR    R6,R3                    DIVIDE R7 BY R3
         LA    R7,1(R7)                 R7= DCBHIRCM
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         STC   R7,DCBHIRCM
         LR    R7,R0                    OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,R7               OPEN WA ADDRESSABILITY   Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=7  SET USERS KEY        Y02072
*
         DROP  R7                       END OPEN WORK AREA USING Y02072
ISLFEH11 SR    R7,R7                    CLEAR WORK REGISTER       13539
         IC    R7,DEBNIEE               R7=NO OF INDEX EXTENTS
         SR    R9,R9                    CLEAR WORK REGISTER       13539
         LTR   R7,R7                    INDEX EXTENT ALLOC      SA69202
         BNZ   ISLFEH3                  YES                     SA69202
         IC    R7,DEBNOEE               R7=NO OF OVFLO EXTENTS  SA69202
ISLFEH3  EQU   *                        SET ILT END M = NO OF EXTENTS
         IC    R9,DEBNPEE               R9=NUM PRIME EXTENTS
         AR    R7,R9                    R7=NUM PRIME+NUM INDEX = M OF
         STC    R7,17(R5)                 FIRST INDEX LOC TBL ENTRY
         BCTR  R7,0
         SLL   R7,4                     SETUP MBBCCHH OF END ADR IN ILT
         LA    R8,DEBUCBAD              R9=ADR OF 1ST ENTENT ENTRY
         AR    R8,R7                    R8=ADR OF LAST EXTENT ENTRY
         XC    18(2,R5),18(R5)          SET ILT END BB=0
         MVC   20(4,R5),10(R8)          SET ILT END CCHH
ISLFEH4  LH    R7,ISLQC                 C = NUM PRIME DATA CYLINDERS
         LA    R7,1(R7)                 SET Q3 = C +1
         STH   R7,ISLQ3                                            FEJ2
         SR    R7,R7                    CLEAR WORK REGISTER      Y02072
ISLFEH5  IC    R7,3(R2)                 R7 = NO OF TRKS PER CYL  A27803
ISLFEH55 STC   R7,ISLP+1                ISLP = TRACKS PER CYL    A27803
         MVI   ISLDORE,X'13'            13 = INDICES NOT IN PRIME AREA
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         ST    R8,DCBWKPT5              SAVE PTR TO EXTENT
         LR    R3,R0                    OPEN WORK AREA ADDRESS   Y02072
         USING FORCORE,R3               OPEN WA ADDRESSABILITY   Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=3  SET USERS KEY        Y02072
*
         DROP  R3                       END OPEN WA USING        Y02072
         B     ISLFC04
ISLFEH2  LR    R7,R3                    INDICES WILL BE IN OVFLO   FEH4
         IC    R7,DEBNOEE               R7=NO OF OVFLO EXTENTS
         B     ISLFEH3
ISLFC04  EQU   *
         ST    R2,ISLQ                  SAVE PTR TO IO DEVICE TABLE
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         LR    RCORE,R0                 RESTORE OPEN W/A ADDR    Y02072
         USING FORCORE,RCORE            OPEN WA ADDRESSABILITY   Y02072
         LM    R0,R15,DXCCW1            RESTORE REGISTERS        Y02072
         USING BASETAG,RBASE
         MVC   0(L'LOAD2E,RWTGC),LOAD2E INITIALIZE WHERE-TO-GO   Y02072
ISLFC04F EQU   *
*
         MODESET  KEYADDR=DXUKEY,WORKREG=10  SET USERS KEY       Y02072
*
         MVI   ISLAREAZ+87,X'FF'        ASSUME NOT A 2301        A34907
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH=BEGINNING OF THIS MODULE
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BC    7,RELOOP                 BRANCH=NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE RPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BC    7,TCTLRTN                BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         LA    RJ,DXCCW12
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
TOOLONG  EQU   *                                                  13539
         LA    R15,RC2C                 ISAM RETURN CODE         Y02072
         ABEND X'002',DUMP,,SYSTEM      SYSTEM 002 ABEND         Y02072
         DS    0F
CONF8    DC    X'FFFFFFF8'
ISLC12   DC    H'2'                                              M4536
OPNLOD7  DC    C'0S'                                              VLR
THISLOAD DC    C'2D'                                              VLR
*
LOAD2E   DC    C'2E'                    ID OF MODULE IGG0192E    Y02072
LOAD2F   DC    C'2F'                    ID OF MODULE IGG0192F    Y02072
*
MODID    DC    C'IGG0192D'              MODULE NAME            @ZA30666
DATE     DC    CL8'&SYSDATE'            COMPILATION DATE       @ZA30666
FIX      DC    C'OZ30666'               LATEST FIX             @ZA30666
PATCH    DC    50X'00'                  ZEROED PATCH AREA        Y02072
         END
