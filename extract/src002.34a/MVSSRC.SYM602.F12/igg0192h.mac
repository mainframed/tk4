         TITLE 'IGG0192H - OPEN,BISAM,MOVE FROM DSCB,GET WORK AREA'
IGG0192H CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192H                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, BISAM MODE, FIXED LENGTH RECORDS      *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = MOVE REQUIRED FIELDS FROM F2 DSCB TO DCB.  DETERMINE     *
*            WHICH COMBINATION OF CHANNEL PROGRAMS TO USE.  VERIFY    *
*            SIZE OF USER-SUPPLIED WORK AREA, IF ANY.  IF HIGH-LEVEL  *
*            INDEX IN CORE, VERIFY SIZE OF USER-SUPPLIED CORE.  GET   *
*            WORK AREA CORE FROM SUBPOOL 250.                         *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 962 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192H                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG01922, IN  *
*              STORAGE PROTECT KEY 5 AND PRIVILEGED STATE.  FIXED     *
*              LENGTH RECORDS ONLY.                                   *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG0192P IF HIGH-LEVEL     *
*               INDEX SEARCH IN STORAGE; OTHERWISE, XCTL TO IGG0192I. *
*                                                                     *
* EXIT-ERROR = ABEND CODES:                                           *
*              032 - NEITHER READ NOR WRITE SPECIFIED.                *
*              034 - USER-SUPPLIED INDEX CORE INSUFFICIENT.           *
*              035 - USER-SUPPLIED WKN WORK AREA CORE INSUFFICIENT.   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = GETMAIN TO OBTAIN WORK AREA CORE FROM SUBPOOL 250     *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGBISAM - BISAM WORK AREA                          *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY, DEB, AND DSCB                         *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = ABEND, MODESET, GETMAIN, AND XCTL                          *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
RE       EQU   00                       WORK REG                 S20201
RF       EQU   01                       WORK REG                 S20201
RDCB     EQU   02                       USER'S DCB               S20201
RBASE    EQU   03                       BASE REG                 S20201
RCORE    EQU   04                       OPEN WORK AREA BASE REG  Y02072
RPAR     EQU   05                       PARAMETER LIST ADDRESS   S20201
RWTG     EQU   06                       WTG TABLE BEGINNING      S20201
RPARC    EQU   07                       CURRENT ENTRY,PARAMETER  S20201
*                                       LIST                     S20201
RWTGC    EQU   08                       CURRENT ENTRY, WTG TABLE S20201
RTIOT    EQU   09                       TIOT ADDRESS             S20201
RDSCB    EQU   10                       DSCB ADDRESS             Y02072
RA       EQU   11                       WORK REG                 S20201
RB       EQU   12                       WORK REG                 S20201
RC       EQU   13                       WORK REG                 S20201
RD       EQU   14                       WORK REG                 S20201
R15      EQU   15                       WORK REG                 S20201
*
         BALR  RBASE,ZERO               ESTABLISH ADDRESSABILITY S20201
         USING *,RBASE                                           S20201
         L     RDCB,0(RPARC)            RDCB=ADDR OF DCB         S20201
         USING IHADCB,RDCB                                       S20201
         L     RCORE,FOUR(,RWTGC)       LOAD OPEN WORK AREA ADDR Y01021
         USING FORCORE,RCORE                                     Y01021
         L     RDSCB,DSCCORE            LOAD ADDRESS OF DSCB     Y02072
         USING IHADSCB,RDSCB                                     Y02072
         MVC   DCBNREC,DSCBD16          MERGE NO OF PRIME REC    S20201
         IC    RB,DCBST                 BITS 5,6,& 7 OF DCBST    S20201
         SRL   RB,THREE                 IGNORE BITS FROM DCB     S20201
         IC    RC,DSCBD17               GET STATUS FROM F2       S20201
         SLL   RC,TWENTY9               POSITION                 S20201
         SLDL  RB,THREE                 COMBINE FIELDS           S20201
         STC   RB,DCBST                 STORE IN DCB             S20201
         MVC   DCBLPDA,DSCBD21          * DSCBD21 AND DSCBD22
*                                       * TO DCBLPDA AND DCBLETI.
         MVC   DCBLIOV,DSCBD25          LAST INDEPENDANT OVERFLOW
         MVC   DCBNBOV,DSCBD26          *                        S20201
         MVC   DCBRORG2,DSCBD27         *                        S20201
         MVC   DCBNOREC,DSCBD28         *                        S20201
OPNO5B2  SR    RA,RA                    RA = CODE FOR ANY OF 54  S20201
         TM    DCBMAC,WRITEKN           POSSIBLE CHAN PROG.      S20201
         BO    OPNO5B1                  COMBOS DEPENDING ON      S20201
         TM    DCBMACRF,READ            1. WRITE KN AND/OR READ, S20201
         BO    OPNO5B2A                 2.NLSD=0, =1, OR GT 1    S20201
         TM    DCBMAC,WRITEKU           3.WRITECHECK OR NOT      S20201
         BZ    ABENDT32                 AND, IF WRITE KN         S20201
OPNO5B2A LA    RA,SIXTEEN(RA)           4.USER SUPPLIED WA OR    S20201
*                                       NOT                      S20201
         B     OPNO5D3                  5.BLOCKED OR UNBLOCKED   S20201
OPNO5B1  LH    RE,DCBSMSW               GET SIZE OF USER         S20201
*                                       WORKAREA                 S20201
         N     RE,OPNOMSK2              CLEAR HI ORDER.          S20201
         LTR   RE,RE                    TEST FOR USER WORKAREA   S20201
         BZ    OPNO5E1                  BRANCH IF NOT SUPPLIED   S20201
         SR    RB,RB                    IF WRITE KN,CHECK SIZE   S20201
*                                       OF                       S20201
         IC    RB,DCBHIRPD              USER-SUPPLIED AREA. IF   S20201
*                                       NOT                      S20201
         LH    RC,DCBBLKSI              ZERO, MUST BE AT LEAST   S20201
         LA    RC,EIGHT(RC)                                      S20201
         TM    DCBRECFM,BLOCKED         BLOCKED  N(8+AL)+ RL     S20201
         BO    OPNO5B11                 UNBLKED  N(8+IL+RL)      S20201
         SR    RD,RD                                             S20201
         IC    RD,DCBKEYLE                WHERE N = DCBHIRPD     S20201
         AR    RC,RD                            AL = DCBBLKSI    S20201
         MR    RB,RB                            RL= DCBLRECL     S20201
         LA    RC,TWO(RC)                                        S20201
         B     OPNO5B12                         IL = DCBKEYLE    S20201
OPNO5B11 MR    RB,RB                                             S20201
         AH    RC,DCBLRECL                                       S20201
OPNO5B12 CLR   RE,RC                                             S20201
         BL    ABENDT35                 ABEND IF NOT ENOUGH ROOM S20201
         LA    RA,TWO(RA)               COMPLETE CP-COMB CODE    S20201
OPNO5E1  TM    DCBRECFM,BLOCKED         TEST FOR BLOCKED RECORDS S20201
         BZ    OPNO5E11                 BRANCH IF NOT            S20201
         LA    RA,FOUR(RA)              ADD TO CODE              S20201
OPNO5E11 TM    DCBMACRF,READ            TEST FOR READ            S20201
         BO    OPNO5DD3                 BRANCH IF SO             S20201
         TM    DCBMAC,WRITEKU           WRITE FOR UPDATE SPEC'D  S20201
         BZ    OPNO5D3                  NO, BRANCH               S20201
OPNO5DD3 LA    RA,EIGHT(RA)             ADD TO CODE              S20201
OPNO5D3  TM    DCBOPTCD,WRTCHK          WAS WRITE CHK SPEC'D     S20201
         BZ    OPNO5D31                 NO, BRANCH               S20201
         LA    RA,ONE(RA)               ADD TO COMBO COUNT       S20201
OPNO5D31 SR    RD,RD                    RD  NLSD - NO. LVLS. IX  S20201
         IC    RD,DCBNLEV               SRCHED ON DEVICE         S20201
         LTR   RD,RD                    IF HI LVL INDEX NOT TRK
         BZ    OPNO5B3                  IX, USER MAY GIVE AREA
         CLI   DCBSMSI+ONE,X'00'        FOR HI LVL INDEX IN CORE S20201
         BNE   OPNO5B4                  NLSD=NLEV-1 IF HI LVL IN S20201
         CLI   DCBSMSI,X'00'            CORE, ELSE NLSD = NLEV   S20201
         BE    OPNO5B3                  BRANCH IF NOT IN CORE    S20201
OPNO5B4  CLC   DCBSMSI,DCBNCRHI         AREA SPECIFIED MUST BE   S20201
         BL    ABENDT34                 AS BIG AS INDEX          S20201
         BCTR  RD,0                     SUBTRACT ONE FROM REG    M6353
OPNO5B3  ST    RA,DCBWKPT6              SAVE CODE FOR SELECTION  S20201
         LA    RB,ONE                   OF APPENDAGE MODULES     S20201
         CLR   RD,RB                                             S20201
         BL    OPNO5E3                  BRANCH IF LESS THAN ONE  S20201
         LA    RA,EIGHTEEN(RA)          ADD EIGHTEEN             S20201
         BE    OPNO5E3                  BRANCH IF EQ TO ONE      S20201
         LA    RA,EIGHTEEN(RA)          ADD EIGHTEEN             S20201
OPNO5E3  SLL   RA,TWO                   USE CODE IN RA TO SELECT S20201
         SR    RE,RE                    WORD FROM SIZE TABLE     S20201
         IC    RE,OPNSIZE(RA)           SIZE NEEDED FOR WKAREAS, S20201
         SR    RC,RC                    AND CHAN PROGS           S20201
         IC    RC,OPNSIZE+ONE(RA)       8(BYTE 1 + HIRPD X BYTE  S20201
         SR    RB,RB                    2 + NCP X BYTE 3)        S20201
         IC    RB,DCBHIRPD              IF BIT 4.0 IS ON, ADD    S20201
         BCTR  RB,0                     SUBTRACT ONE FOR 1ST REC M6353
         MR    RB,RB                    BLKED - KEYLE+RECL+BLKSI S20201
         AR    RE,RC                    UNBLKED - KEYLE+RECL+10  S20201
         SR    RC,RC                                             S20201
         CLI   DCBNCP,X'00'             IF NCP IS NOT SPEC'D,    S20201
         BNE   OPN05E3A                 ASSUME IT IS 1           S20201
         MVI   DCBNCP,ONE               SET TO ONE               S20201
OPN05E3A IC    RC,OPNSIZE+TWO(RA)       GET SIZE OF READ CPS     S20201
         IC    RB,DCBNCP                GET NCP                  S20201
         MR    RB,RB                    MULTIPLY                 S20201
         AR    RE,RC                                             S20201
*
         SLL   RE,THREE                 MULTIPLY BY EIGHT        S20201
         IC    RB,OPNSIZE+THREE(RA)     SYS WA NEEDED FOR WKN    S20201
         LTR   RB,RB                    IS SYS WA NEEDED         S20201
         BZ    OPNO5F3                  BRANCH IF NO             S20201
         LR    RC,RE                    RC   REL ADDR, SYS WA,   S20201
*                                       WKN                      S20201
         AH    RE,DCBLRECL                                       S20201
         IC    RB,DCBKEYLE                                       S20201
         LA    RB,TEN(RB)               KEYLENGTH + 10
         AR    RE,RB                    LRECL + KEYLENGTH + 10
         TM    DCBRECFM,BLOCKED         IS IT BLOCKED            S20201
         BZ    OPNO5F3A                 NO, BRANCH
OPNO5E31 AH    RE,DCBBLKSI              ADD BLKSIZE              S20201
OPNO5F3A LR    RB,RE                    SAVE TOTAL               S20201
         LA    RE,TWOFIFTY              SET  SUBPOOL             S20201
         SLL   RE,TWENTY4               250 INTO HIGH ORDER      S20201
         OR    RE,RB                    POSITION OF REG ZERO     S20201
         LA    RB,SEVEN(RB)             ROUND TO NEXT DBL WRD    S20201
         GETMAIN R,LV=(0)               GET AREA                 S20201
         AR    RC,RF                    STORE ABS ADDR OF SYS WA S20201
         ST    RC,DCBMSWA               *
         B     OPNO5K3                  GO CLEAR WORKAREA        S20201
         SPACE 2
OPNO5F3  LR    RB,RE                    NO SYS WA  - SAVE TOTAL  S20201
         LA    RE,TWOFIFTY              SET  SUBPOOL             S20201
         SLL   RE,TWENTY4               250 INTO HIGH ORDER      S20201
         OR    RE,RB                    POSITION OF REGISTER     S20201
*                                       ZERO                     S20201
         GETMAIN R,LV=(0)               GET AREA                 S20201
         USING IHADCW,RF                RF   ADDRESS OF DCB WA   S20201
OPNO5K3  EQU   *                        CLEAR WORK AREA          Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=13  CHANGE TO USER KEY  Y02072
*
         XC    IHADCW(SZAX8),IHADCW     ZERO DCB WA
         ST    RD,DCWNLSD-THREE         *                        S20201
         SRL   RB,THREE                 DIVIDE BY EIGHT          S20201
         STH   RB,DCWSIZE               SAVE NLSD, SIZE OF AREA  S20201
         MVC   DCWOPCLS,DCBWKPT3        SAVE INFO FOR CLOSE      S20201
         MVC   DCWDCBFA,DCBWKPT5        SV WKPTR5(A42864,52477)  Y02072
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         ST    RF,DCBWKPT2              SET POINTER TO WA        Y02072
         XC    DCBWKPT3(EIGHT),DCBWKPT3   CLEAR WKPTRS 3 AND 4   S20201
         LA    RB,SZAX8(RF)             SAVE ADDRESS OF FIRST CP S20201
         ST    RB,DCBWKPT1              *                        S20201
         XC    DCBWKPT5,DCBWKPT5        CLEAR (A42864)           A52477
         MVC   0(L'LOAD2I,RWTGC),LOAD2I ID OF MODULE IGG0192I    Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=15  CHANGE TO USER KEY  Y02072
*
         CLC   DCWNLSD,DCBNLEV          IF HI INDEX IN CORE, SET S20201
         BE    OPNEND2H                 GO EXIT
         OI    DCWHIAV,COREIX           INDEX IN CORE            S20201
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         MVC   0(L'LOAD2P,RWTGC),LOAD2P SET UP NEXT MODULE IDTTR Y02072
         B     RELOOP                   SEE IF 2H NEEDED AGAIN   Y02072
OPNEND2H EQU   *                        *                        S20201
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
RELOOP   LA    RWTGC,WGOFF(RWTGC)       GET NEXT NAME IN WTG TBL S20201
         LA    RPARC,PLOFF(RPARC)       AND DCB ON THE PARAM TBL S20201
         CLC   0(TWO,RWTGC),OPNEXNAM    IF TBL NAME SAME ID AS   S20201
         BCR   8,RBASE                  CURR LOAD, BR TO REPEAT  S20201
         CLC   0(TWO,RWTGC),OPIDCNST    IF NOT END OF WTG TABLE, S20201
         BNE   RELOOP                   TRY NEXT NAME            S20201
         LR    RPARC,RPAR               ELSE, RETURN TO START OF S20201
         LA    RWTGC,WAOFF(RWTG)        BOTH TABLES AND SEARCH   S20201
*                                       FOR                      S20201
ZCHECK   CLI   0(RWTGC),ZCNST           FIRST NON-ZERO ID        S20201
         BNE   TCTLRTN                  BRANCH IF NON-ZERO       S20201
         LA    RWTGC,WGOFF(RWTGC)       *                        S20201
         LA    RPARC,PLOFF(RPARC)       *                        S20201
         B     ZCHECK                   GO SEE IF NON-ZERO       S20201
TCTLRTN  MVC   SIX(TWO,RWTG),0(RWTGC)   WHEN FOUND, SET UP PARMS S20201
         LA    R15,DXCCW12              WA ADDRESS               Y01021
TRCTL    XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
WGOFF    EQU   8                        SIZE OF WTG TABLE ENTRY  S20201
PLOFF    EQU   4                        SIZE OF PARAM TAB ENTRY  S20201
OPNEXNAM DC    C'2H'                    ID OF THIS OPEN EXECUTOR S20201
OPIDCNST DC    C'0S'                    ID OF OPEN LOAD          S20201
ZCNST    EQU   X'00'                    ZERO - INACTIVE ENTRY    S20201
WAOFF    EQU   32                       SIZE OF WA AT START OF   S20201
*                                       WTG TAB                  S20201
ZERO     EQU   0                        MISC OFFSETS             S20201
ONE      EQU   1                        MISC OFFSET              S20201
TWO      EQU   2                        *                        S20201
THREE    EQU   3                        *                        S20201
FOUR     EQU   4                        *                        S20201
FIVE     EQU   5                        *                        S20201
SIX      EQU   6                        *                        S20201
SEVEN    EQU   7                        *                        S20201
EIGHT    EQU   8                        *                        S20201
NINE     EQU   9                        *                        S20201
TEN      EQU   10                       *                        S20201
THIRTEEN EQU   13                       *
SIXTEEN  EQU   16                       *                        S20201
EIGHTEEN EQU   18                       *                        S20201
TWENTY4  EQU   24                       *                        S20201
TWENTY9  EQU   29                       *                        S20201
TWOFIFTY EQU   250                      *                        S20201
READ     EQU   X'20'                    *                        S20201
WRITEKN  EQU   X'02'                    *                        S20201
WRITEKU  EQU   X'04'                    *                        S20201
BLOCKED  EQU   X'10'                    *                        S20201
WRTCHK   EQU   X'80'                    *                        S20201
VIRTUAL  EQU   WRTCHK                   VIRTUAL MASK
COREIX   EQU   X'40'                    *                        S20201
*
         DS    0H                       HALFWORD ALIGNMENT       Y02072
ABENDT34 EQU   *                        ABNORMAL RETURN, CODE 34 S20201
         ABEND X'034',DUMP,,SYSTEM      SYSTEM 034 ABEND         Y02072
ABENDT32 EQU   *                        ABNORMAL RETURN, CODE 32 S20201
         ABEND X'032',DUMP,,SYSTEM      SYSTEM 032 ABEND         Y02072
ABENDT35 EQU   *                        ABNORMAL RETURN, CODE 35 S20201
         ABEND X'035',DUMP,,SYSTEM      SYSTEM 035 ABEND         Y02072
         DS    0F                                                S20201
OPNOMSK2  DC   X'0000FFFF'              MASK                     S20201
*                           TABLE FOR TOTAL SIZE OF CP'S AND WA'S
*                                           CODE  NLSD MACRO FU/B WA WC
*                                           ****  **** ***** **** ** **
OPNSIZE  DC    AL1(SZA+SZD+SZE+SZG+SZH)      0   0      WKN  FU  S20201
*                                        N  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZG+SZI)      1   0      WKN  FU  S20201
*                                        N  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZM+SZU)      2   0      WKN  FU  S20201
*                                        Y  N                    S20201
         DC    AL1(SZN)                 *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZM+SZQ+SZV)  3   0      WKN  FU  S20201
*                                        Y  Y                    S20201
         DC    AL1(SZN+SZR)             *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZJ+SZK)      4   0      WKN  FB  S20201
*                                        N  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZJ+SZL)      5   0      WKN  FB  S20201
*                                        N  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZO+SZW)      6   0      WKN  FB  S20201
*                                        Y  N                    S20201
         DC    AL1(SZP)                 *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZO+SZQ+SZX)  7   0      WKN  FB  S20201
*                                        Y  Y                    S20201
         DC    AL1(SZP+SZR)             *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZG+SZH)      8   0      ALL  FU  S20201
*                                        N  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZG+SZI)      9   0      ALL  FU  S20201
*                                        N  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZM+SZU)      10  0      ALL  FU  S20201
*                                        Y  N                    S20201
         DC    AL1(SZN)                 *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZM+SZQ+SZV)  11  0      ALL  FU  S20201
*                                        Y  Y                    S20201
         DC    AL1(SZN+SZR)             *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZJ+SZK)      12  0      ALL  FB  S20201
*                                        N  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZJ+SZL)      13  0      ALL  FB  S20201
*                                        N  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZO+SZW)      14  0      ALL  FB  S20201
*                                        Y  N                    S20201
         DC    AL1(SZP)                 *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZO+SZQ+SZX)  15  0      ALL  FB  S20201
*                                        Y  Y                    S20201
         DC    AL1(SZP+SZR)             *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA)                      16  0      R+U  ANY S20201
*                                        -  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA)                      17  0      R+U  ANY S20201
*                                        -  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZG+SZH+SZC)  18  1      WKN  FU  S20201
*                                        N  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZG+SZI+SZC)  19  1      WKN  FU  S20201
*                                        N  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZM+SZC+SZU)  20  1      WKN  FU  S20201
*                                        Y  N                    S20201
         DC    AL1(SZN)                 *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'00'                    *                        S20201
*                                            21  1      WKN  FU   Y  Y
         DC    AL1(SZA+SZD+SZF+SZM+SZQ+SZC+SZV)   *              S20201
         DC    AL1(SZN+SZR)             *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZJ+SZK+SZC)  22  1      WKN  FB  S20201
*                                        Y  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZJ+SZL+SZC)  23  1      WKN  FB  S20201
*                                        N  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZO+SZC+SZW)  24  1      WKN  FB  S20201
*                                        Y  N                    S20201
         DC    AL1(SZP)                 *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'00'                    *                        S20201
*                                            25  1      WKN  FB   Y  Y
         DC    AL1(SZA+SZD+SZF+SZO+SZQ+SZC+SZX)    *             S20201
         DC    AL1(SZP+SZR)             *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZG+SZH+SZC)  26  1      ALL  FU  S20201
*                                        N  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZG+SZI+SZC)  27  1      ALL  FU  S20201
*                                        N  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZM+SZC+SZU)  28  1      ALL  FU  S20201
*                                        Y  N                    S20201
         DC    AL1(SZN)                 *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'00'                    *                        S20201
*                                            29  1      ALL  FU   Y  Y
         DC    AL1(SZA+SZD+SZF+SZM+SZQ+SZC+SZV)   *              S20201
         DC    AL1(SZN+SZR)             *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZJ+SZK+SZC)  30  1      ALL  FB  S20201
*                                        Y  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZJ+SZL+SZC)  31  1      ALL  FB  S20201
*                                        N  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZO+SZC+SZW)  32  1      ALL  FB  S20201
*                                        Y  N                    S20201
         DC    AL1(SZP)                 *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'00'                    *                        S20201
*                                            33  1      ALL  FB   Y  Y
         DC    AL1(SZA+SZD+SZF+SZO+SZQ+SZC+SZX)      *           S20201
         DC    AL1(SZP+SZR)             *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZC)                  34  1      R+U  ANY S20201
*                                        -  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZC)                  35  1      R+U  ANY S20201
*                                        -  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZA+SZD+SZE+SZG+SZH+SZB)  36  2,3,4  WKN  FU  S20201
*                                        N  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZG+SZI+SZB)  37  2,3,4  WKN  FU  S20201
*                                        N  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZM+SZB+SZU)  38  2,3,4  WKN  FU  S20201
*                                        Y  N                    S20201
         DC    AL1(SZN)                 *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'00'                    *                        S20201
*                                            39  2,3,4  WKN  FU   Y  Y
         DC    AL1(SZA+SZD+SZF+SZM+SZQ+SZB+SZV)  *               S20201
         DC    AL1(SZN+SZR)             *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZJ+SZK+SZB)  40  2,3,4  WKN  FB  S20201
*                                        N  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZJ+SZL+SZB)  41  2,3,4  WKN  FB  S20201
*                                        N  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZO+SZB+SZW)  42  2,3,4  WKN  FB  S20201
*                                        Y  N                    S20201
         DC    AL1(SZP)                 *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'00'                    *                        S20201
*                                            43  2,3,4  WKN  FB   Y  Y
         DC    AL1(SZA+SZD+SZF+SZO+SZQ+SZB+SZX)  *               S20201
         DC    AL1(SZP+SZR)             *                        S20201
         DC    AL1(0)                   *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZG+SZH+SZB)  44  2,3,4  ALL  FU  S20201
*                                        N  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZG+SZI+SZB)  45  2,3,4  ALL  FU  S20201
*                                        N  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZM+SZB+SZU)  46  2,3,4  ALL  FU  S20201
*                                        Y  N                    S20201
         DC    AL1(SZN)                 *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'00'                    *                        S20201
*                                            47  2,3,4  ALL  FU   Y  Y
         DC    AL1(SZA+SZD+SZF+SZM+SZQ+SZB+SZV)   *              S20201
         DC    AL1(SZN+SZR)             *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZJ+SZK+SZB)  48  2,3,4  ALL  FB  S20201
*                                        N  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZF+SZJ+SZL+SZB)  49  2,3,4  ALL  FB  S20201
*                                        N  Y                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'80'                    *                        S20201
         DC    AL1(SZA+SZD+SZE+SZO+SZB+SZW)  50  2,3,4  ALL  FB  S20201
*                                        Y  N                    S20201
         DC    AL1(SZP)                 *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'00'                    *                        S20201
*                                            51  2,3,4  ALL  FB   Y  Y
         DC    AL1(SZA+SZD+SZF+SZO+SZQ+SZB+SZX)   *              S20201
         DC    AL1(SZP+SZR)             *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZB)                  52  2,3,4  R+U  ANY S20201
*                                        -  N                    S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZS)                 *                        S20201
         DC    X'00'                    *                        S20201
         DC    AL1(SZA+SZB)                  53  2,3,4  R+U      S20201
*                                       ANY  - Y                 S20201
         DC    AL1(0)                   *                        S20201
         DC    AL1(SZT)                 *                        S20201
         DC    X'00'                    *                        S20201
LOAD2I   DC    C'2I'                    ID OF MODULE IGG0192I    Y02072
LOAD2P   DC    C'2P'                    ID OF MODULE IGG0192P    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
FORCORE  DSECT                          OPEN/CLOSE/EOV WORK AREA Y01021
         IECDSECT                                                Y01021
         EJECT
         DCBD  DSORG=(IS)                                        S20201
IHADCW   IGGBISAM
IHAWKNCP IGGWKNCP OPTCD=W                                        S20201
IHACP47  IGGCP47  OPTCD=W                                        S20201
CP9A     DSECT
         IGGCP9A
         IGGCP9B  OPTCD=W                                        S20201
         IGGCP9C  OPTCD=W                                        S20201
         IGGCP11A
         IGGCP11B OPTCD=W                                        S20201
         IGGCP12A
         IGGCP12B
         IGGCP12C OPTCD=W                                        S20201
         IGGCP13A
         IGGCP13B
         IGGCP13C OPTCD=W                                        S20201
         IGGCP123
IHADSCB  DSECT
DSCBK1   DS    BL1                      X'02'                    S20201
DSCBK2   DS    CL7                      DCBFTMI2                 S20201
DSCBK3   DS    CL5                      DCBLEMI2                 S20201
DSCBK4   DS    CL7                      DCBFTMI3                 S20201
DSCBK5   DS    CL5                      DCBLETI                  S20201
DSCBK6   DS    CL19                     SPARE                    S20201
DSCBD1   DS    CL1                      EBCDIC '2'               S20201
DSCBD2   DS    CL1                      DCBNLEV                  S20201
DSCBD3   DS    CL1                      DCBNTM                   S20201
DSCBD4   DS    CL3                      DCBFIRSH                 S20201
DSCBD5   DS    CL2                      DCBLDT                   S20201
DSCBD6   DS    CL1                      DCBCYLOF                 S20201
DSCBD7   DS    CL1                      DCBHIRCM                 S20201
DSCBD8   DS    CL1                      DCBHIRPD                 S20201
DSCBD9   DS    CL1                      DCBHIROV                 S20201
DSCBD10  DS    CL1                      DCBHIRSH                 S20201
DSCBD11  DS    CL2                      RESERVED                 S20201
DSCBD12  DS    CL2                      DCBTDC                   S20201
DSCBD13  DS    CL3                      DCBRORG3                 S20201
DSCBD14  DS    CL2                      DCBNCRHI                 S20201
DSCBD15  DS    CL1                      DCBNTHI                  S20201
DSCBD16  DS    CL4                      DCBNREC                  S20201
DSCBD17  DS    BL1                      DCBST                    S20201
DSCBD18  DS    CL7                      DCBFTCI                  S20201
DSCBD19  DS    CL7                      DCBFTMI3                 S20201
DSCBD20  DS    CL7                      DCBFTHI                  S20201
DSCBD21  DS    CL08                     DCBLPDA                  S20201
DSCBD22  DS    CL5                      LAST TRACK IX ENTRY ADDR S20201
DSCBD23  DS    CL5                      LAST CYLDR IX ENTRY ADDR S20201
DSCBD24  DS    CL5                      LAST MASTR IX ENTRY ADDR S20201
DSCBD25  DS    CL8                      DCBLIOV                  S20201
DSCBD26  DS    CL2                      DCBNBOV                  S20201
DSCBD27  DS    CL2                      DCBRORG2                 S20201
DSCBD28  DS    CL2                      DCBNOREC                 S20201
DSCBD29  DS    CL2                      DCBRORG1                 S20201
DSCBD30  DS    CL3                      SPARE                    S20201
DSCBD31  DS    CL5                      PTR TO FORMAT 3 DSCB     S20201
SZA      EQU   (DCWLEN+7)/8             SIZE OF DCB WA IN DBLWDS S20201
SZAX8    EQU   DCWLEN                   SIZE OF DCB WA IN BYTES  S20201
         SPACE 2
IHADEB   IGGDEBD
* THE TABLE FOLLOWING THESE EQUATES DESCRIBES THEIR MEANING.
SZB      EQU   SZCP1/8                  *                        S20201
SZC      EQU   SZCP2/8                  *                        S20201
SZD      EQU   (SZ8+SZ15+SZ16)/8        *                        S20201
SZE      EQU   (SZ10B+SZ14+SZ17+SZ10A)/8   *                     S20201
SZF      EQU   (SZ10BW+SZ14W+SZ17W+SZ10AW)/8  *                  S20201
SZG      EQU   SZ9A/8                   *                        S20201
SZH      EQU   (SZ9B+SZ9C)/8            *                        S20201
SZI      EQU   (SZ9BW+SZ9CW)/8          *                        S20201
SZJ      EQU   SZ11A/8                  *                        S20201
SZK      EQU   SZ11B/8                  *                        S20201
SZL      EQU   SZ11BW/8                 *                        S20201
SZM      EQU   (SZ12A+SZ12B)/8          *                        S20201
SZU      EQU   SZ12C/8                  *                        S20201
SZV      EQU   SZ12CW/8                 *                        S20201
SZN      EQU   3                        HIRPD OF THESE MANY CCWS S20201
SZO      EQU   (SZ13A-16+SZ13B)/8       DON'T COUNT CF4 & CF5    M6353
SZP      EQU   5                        HIRPD OF THESE MANY CCWS S20201
SZW      EQU   SZ13C/8                  *                        S20201
SZX      EQU   SZ13CW/8                 *                        S20201
SZQ      EQU   SZ123/8                  *                        S20201
SZR      EQU   1                        HIRPD OF THESE MANY CCWS S20201
SZS      EQU   (SZ4+SZ5+SZ6+SZ7)/8      *                        S20201
SZT      EQU   (SZ4+SZ5W+SZ6W+SZ7W)/8   *                        S20201
**
**            # CCWS TIMES  CHANNEL PROGRAMS      NLSD MACRO FU/B WA WC
**            ****** *****  ****************      **** ***** **** ** **
*SZB      EQU   SZCP1/8       1                   2,3,4  ALL
*SZC      EQU   SZCP2/8       2                   1      ALL
*SZD      EQU   (SZ8+SZ15+SZ16)/8   8,15,16              WKN
*SZE      EQU   (SZ10B+SZ14+SZ17+SZ10A)/8 10B,14,17,10A  WKN          N
*SZF      EQU   (SZ10BW+SZ14W+SZ17W+SZ10AW)/8            WKN          Y
*SZG      EQU   SZ9A/8          9A                       WKN  FU   N
*SZH      EQU   (SZ9B+SZ9C)/8   9B,9C                    WKN  FU   N  N
*SZI      EQU   (SZ9BW+SZ9CW)/8 9BW,9CW                  WKN  FU   N  Y
*SZJ      EQU   SZ11A/8         11A                      WKN  FB   N
*SZK      EQU   SZ11B/8         11B                      WKN  FB   N  N
*SZL      EQU   SZ11BW/8        11BW                     WKN  FB   N  Y
*SZM      EQU   (SZ12A+SZ12B)/8 12A,12B                  WKN  FU   Y
*SZU      EQU   SZ12C/8       12C                        WKN  FU   Y  N
*SZV      EQU   SZ12CW/8      12CW                       WKN  FU   Y  Y
*SZO      EQU   (SZ13A+SZ13B)/8 13A,13B                  WKN  FB   Y
*SZW      EQU   SZ13C/8       13C                        WKN  FB   Y  N
*SZX      EQU   SZ13CW/8      13CW                       WKN  FB   Y  Y
*SZQ      EQU   SZ123/8       123W                       WKN       Y  Y
*SZS      EQU   (SZ4+SZ5+SZ6+SZ7)/8 4,5,6,7             R+U           N
*SZT      EQU   (SZ4+SZ5W+SZ6W+SZ7W)/8  4,5W,6W,7W      R+U           Y
         END
