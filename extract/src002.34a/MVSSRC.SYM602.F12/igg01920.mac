         TITLE 'IGG01920-VALIDATE FORMAT2 DSCB FOR SCAN AND BISAM'
IGG01920 CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG01920                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, FIXED LENGTH VALIDATION ROUTINE       *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
*        VS2 RELEASE 3 CHANGES
* A227500-227980,D505500                                        ZM01121
*   PTM ZM01121 IS THE EQUIVALENT OF APAR ZA00957               ZA00957
* C227700                                                      @ZA10616
* A227100-227400,C660300                                       @ZA17080
* A227340-227400,227420,661300                                 @ZA25348
* D227420,C227410                                              @ZA30618
* A506700                                                      @ZA30069
* A227361-227366                                               @ZA29046
*             CHANGES SINCE VS2-3.7                                   *
*A227970                                                       @ZA33836
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = IF DCB IS OPENED FOR SHARE, AND M OF DFALPDA IS NOT 0,   *
*            XCTL TO NEXT EXECUTOR; OTHERWISE INITIALIZE CP VXCCW1A   *
*            TO READ EOF OR END OF LPDA TRACK FOR PRIME DATA, AND     *
*            UPF ENDDS2LPRAD AND DS2STIND IF INCORRECT.  GET CORE FOR *
*            BUFFER FROM SUBPOOL 229 IN PROTECT KEY 5 (FETCH          *
*            PROTECTED), SET FILE MASK TO PROHIBIT HEAD SEEKS, AND    *
*            TEST FOR PADDING IN LAST BLOCK IF LAST BLOCK AND LAST    *
*            TRACK FULL BITS AREN'T ON.  FREE SUBPOOL 229 CORE BEFORE *
*            EXIT.  IF SCAN OR BISAM, DCB OPENED FOR SHARE, AND M OF  *
*            DFALPDA IS 0, MERGE F2 DSCB FIELDS INTO FIELD AREA.      *
*                                                                     *
* NOTES = THE FORMAT 2 VALIDATION MODULE IS SPLIT INTO IGG01920 AND   *
*         IGG01922.  THEY RUN IN TANDEM, AND ALWAYS TOGETHER.         *
*         IGG01920 VALIDATES FIELDS FOR PRIME DATA, AND IGG01922      *
*         DOES THE SAME FOR OVERFLOW.  THE EOF CHANNEL PROGRAM IS     *
*         PREPARED BY IGG01920 AND USED TO READ IN DATA BLOCKS FOR    *
*         LAST BLOCK FULL VERIFICATION.                               *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 988 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG01920                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM RESTART MODULE IGC0W05B IF       *
*              RESTART IS IN CONTROL, OTHERWISE FROM ISAM OPEN        *
*              EXECUTOR IGG01921 IF RESUME LOAD, OR IGG0192C FOR SCAN *
*              OR BISAM MODE.  RECEIVES CONTROL IN STORAGE PROTECT    *
*              KEY 5 AND PRIVILEGED STATE.                            *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
*         HOWEVER, IF ENTERED FROM RESTART, REGISTERS ARE ESTABLISHED *
*         AS FOLLOWS:                                                 *
*         REG 1 (R1) POINTS TO THE RESTART AREA                       *
*         REG 4 (RCORE) POINTS TO THE OPEN WORK AREA                  *
*         REG 7 (RPARC) IS ZERO                                       *
*         REG 8 (RWTGC) POINTS TO THE RESTART TABLE                   *
*         REG 9 (RDEB) POINTS TO THE CURRENT RESTART ENTRY            *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO IGC0W05B IF RESTART IS IN CONTROL, OTHERWISE  *
*               TO ISAM OPEN EXECUTOR IGG01922 IN STORAGE PROTECT KEY *
*               5.                                                    *
*                                                                     *
* EXIT-ERROR = ABEND CODES:                                           *
*              033 - I/O ERROR TRYING TO READ EOF                     *
*                                                                     *
*              IF RESTART ENTERED THIS MODULE, DO NOT ISSUE ABEND,    *
*              BUT XCTL TO IGC0W05B WITH ABEND CODE IN REGISTER 1.    *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                                                                     *
*    CONTROL-BLOCKS = DCB, DCB COPY, DCBFA, DEB, DSCB, ECB, AND IOB.  *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = GETMAIN, FREEMAIN, EXCP, AND WAIT.                         *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
DS2DSECT DSECT           FORMAT 2 DSCB
         DS    0D
DS2KEY   DS    CL44
DS2FMTID DS    CL1
DS2NOLEV DS    CL1
DS2DVIND DS    CL1
DS21RCYL DS    CL3
DS2LTCYL DS    CL2
DS2CYLOV DS    CL1
DS2HIRIN DS    CL1
DS2HIRPR DS    CL1
DS2HIROV DS    CL1
DS2RSHTR DS    CL1
DS2HIRTI DS    CL1                                               O19113
DS2HIIOV DS    CL1                                               O19113
DS2TAGDT DS    CL2
DS2RORG3 DS    CL3
DS2NOBYT DS    CL2
DS2NOTRK DS    CL1
DS2PRCTR DS    CL4
DS2STIND DS    CL1
DS2CYLAD DS    CL7
DS2ADLIN DS    CL7
DS2ADHIN DS    CL7
DS2LPRAD DS    CL8
DS2LTRAD DS    CL5
DS2LCYAD DS    CL5
DS2LMSAD DS    CL5
DS2LOVAD DS    CL8
DS2BYOVL DS    CL2
DS2RORG2 DS    CL2
DS2OVRCT DS    CL2
DS2RORG1 DS    CL2
FORCORE  DSECT
         IECDSECT
*                       CP TO READ TO EOF                        A25984
         ORG   DXCCW                                             A25984
VXCCW1   CCW   SIDEQ,0,CC,5              ARG=LPDA+3              A30485
VXCCW2   CCW   TIC,VXCCW1-VXCCW1,0,0   TIC CCW1                  A30485
VXCCW2A  CCW   TIC,VXCCW3A-VXCCW1,0,0   1ST TIME TIC FOR SEARCH  A30485
*                                       DIRECT                   A30485
VXCCW3   CCW   MT+RDCNT,VXCCW6-VXCCW1,CC,8                       A30485
VXCCW3A  CCW   RDATA,0,CC+SILI,0         LEN=BLKSIZE             A30485
VXCCW4   CCW   MT+RDCNT,VXCCW7-VXCCW1,CC,8                       A30485
VXCCW4A  CCW   RDATA,0,CC+SILI,0         LEN=BLKSIZE             A30485
VXCCW5   CCW   TIC,VXCCW3-VXCCW1,0,0                             A30485
VXCCW6   DC    2F'0000'                 COUNT FLD    CCHHRKDD    A30485
VXCCW7   DC    2F'0000'                 COUNT FLD    CCHHRKDD    A30485
VXCCW8   DS    CL8                       NEW MBBCCHHR            A30485
SAVEREG  DS    CL8                     REG. SAVE AREA
         DS    CL4                     OLD TTR
SAVER2   DS    CL2                     NEW R VALUE
SAVER1   DS    CL2                     OLD R VALUE
         DCBD  DSORG=IS                                          O19113
SIDEQ    EQU   X'31'                                             A30485
TIC      EQU   X'08'                                             A30485
RDCNT    EQU   X'12'                                             A30485
RCKD     EQU   X'1E'                                             A30485
MT       EQU   X'80'                                             A30485
RDATA    EQU   X'06'                                             A30485
DC       EQU   X'80'                                             A30485
CC       EQU   X'40'                                             A30485
BLOCKED  EQU   X'10'                    DCBRECFM CODE FOR BLKED ZM01121
DCBFA     IGGDCBFA
         EJECT
IHADEB   IGGDEBD                                                 Y02072
         EJECT
R0       EQU   0                        WORK REG
RVAL     EQU   1                        BASE REG FOR VALIDATE CP
RDSCB    EQU   2                        BASE REG FOR DSCB FORMAT 2
RBASE    EQU   3                        BASE REG FOR THIS MODULE
RCORE    EQU   4                        BASE REG FOR OPEN WORK AREA
RPAR     EQU   5         *              ADDR OF PARAMETER TABLE
RWTG     EQU   6         *              ADDR OF WHERE-TO-GO TABLE
RPARC    EQU   7         *              CURRENT PARAMETER ENTRY
RWTGC    EQU   8         *              CURRENT WHERE-TO-GO ENTRY
RRSTC    EQU   9                        CURRENT RESTART ENTRY    A44475
RDEB     EQU   9                        DEB PTR
RA       EQU   10                       WORK REG
RB       EQU   11                       WORK REG
RC       EQU   12                       WORK REG
RD       EQU   13                       WORK REG
RE       EQU   14                       WORK REG
RF       EQU   15                       WORK REG
RJ       EQU   15
R1       EQU   1
*
*
L3BYTE   EQU   7                        STORE 3 LOW ORDER BYTES  Y02072
SHR      EQU   X'80'                    TEST FOR DISP EQ SHR     Y02072
SUB229   EQU   229                      SUBPOOL 229 FOR BUFFER   Y02072
SILI     EQU   X'20'                                             A30485
SKIP     EQU   X'10'                                             A30485
EQ       EQU   8                                                 A30485
LO       EQU   4                                                 A30485
LBF      EQU   X'02'                                             A30485
LTF      EQU   X'01'                                             A30485
REC1     EQU   X'01'                    RECORD NO IS 1
TWO      EQU   2                        *
         EJECT                                                   A25984
IGG01920 CSECT                                                   A25984
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         USING FORCORE,RCORE
         USING DS2DSECT,RDSCB
         USING IHADCB,RB                                         A25984
         LTR   RPARC,RPARC              CHECK FOR RESTART ENTRY  A25984
         BNZ   OPEN                     BRANCH IF NOT            A25984
         STM   RDSCB,RA,DXDSCB          SAVE REG 2-10 IN F1 DSCB A25984
         L     RB,0(RRSTC)              GET CURRENT DCB ADDR     A44475
         LA    RDSCB,DXJBF              POINT TO FORMAT 2 DSCB   A25984
         B     COMMON                                            A25984
OPEN     EQU   *                                                 A25984
         USING IHADCB,RB                                         O19113
         L     RB,0(RPARC)              SET DCB ADDR
         L     RCORE,4(RWTGC)           SET OPEN WK AREA ADDR
         L     RDSCB,DSCCORE            SET PTR TO F2 DSCB       A25984
         CLC   DCBKEYLE,DSCKEYL         IS KEY EQUAL ?         @ZA17080
         BNE   AB03B                    NO ABEND               @ZA17080
         CLI   DCBRECFM,X'00'           IS RECFM 0?            @ZA17080
         BE    AB03B                    YES, ABEND             @ZA17080
         IC    RF,DCBRECFM              SAVE RECFM             @ZA17080
         NI    DCBRECFM,X'FE'           RESET KEYL  BIT        @ZA17080
         CLC   DCBRECFM,DSCRECFM        DOES DCB RECFM EQUAL   @ZA17080
*                                       DSCB1 RECFM ??         @ZA17080
         BE    TESTRECL                 YES-GO TEST LRECL      @ZA25348
         CLI   DCBRECFM,X'90'           NO-IS DCB FB?          @ZA25348
         BE    FBDCB                    YES-GO TEST DSCB       @ZA25348
         TM    DCBRECFM,X'80'           IS DCB F?              @ZA29046
         BZ    ABORT                    NO-GO ABEND            @ZA29046
         TM    DSCRECFM,X'90'           IS DSCB FB?            @ZA29046
         BNO   ABORT                    NO-GO ABEND            @ZA29046
         CLC   DCBBLKSI,DCBLRECL        BLOCKING FACTOR OF 1?  @ZA29046
         BE    TESTRECL                 YES-GO TEST MORE       @ZA29046
ABORT    STC   RF,DCBRECFM              RESTORE RECFM          @ZA25348
         B     AB03B                    AND ABEND              @ZA25348
FBDCB    CLI   DSCRECFM,X'80'           IS DSCB F?             @ZA25348
         BNE   ABORT                    NO-GO ABEND            @ZA25348
TESTRECL STC   RF,DCBRECFM              RESTORE RECFM          @ZA30618
         CLC   DCBLRECL,DSCLRECL        DOES LRECL EQ FMT1      ZM01121
         BNE   AB03B                    NO,ABEND                ZM01121
         TM    DSCRECFM,BLOCKED         RECORDS BLOCKED?       @ZA10616
         BNO   COMMON                   NO, BRANCH              ZM01121
         CLC   DCBBLKSI,DSCBLKL         DOES DCB BLKSIZE EQUAL  ZM01121
*                                       DSCB BLKSIZE?           ZM01121
         BE    COMMON                   YES, BRANCH - AOK       ZM01121
AB03B    EQU   *
         LR    RDSCB,RB                 DCB PTR FOR PROB DETER @ZA33836
         ABEND X'03B',DUMP,,SYSTEM      EXECUTE 03B ABEND       ZM01121
COMMON   EQU   *                                                 A25984
         L     RDEB,DCBDEBAD            SET DEB BASE             A44475
         USING IHADEB,RDEB              DEB ADDRESSABILITY       Y02072
         L     RA,DEBEXPTR              ADDR OF DEB EXTENSION    Y02072
         USING DEBEXT,RA                USING ON DEB EXTENSION   Y02072
*
*   IF DATA SET HAS BEEN OPENED FOR SHR, DEBDCBFA WILL CONTAIN
*        A POINTER TO THE DCBFA.
*
         TM    DEBDCBFA,SHR             TEST FOR DISP=SHR        Y02072
         BZ    CNTINU                   BR TO CONTINUE RTN IF    A35340
*                                       NOT                      A35340
         L     RA,DEBDCBFA              ADDR OF DCBFA            Y02072
         USING DCBFA,RA                                          A37175
         CLI   DFALPDA,X'00'            IS M IN DCBLPDA ZERO     A37175
         BNE   NEXTEXEC                 IF NOT,VALID             A37175
CNTINU   EQU   *                                                 A35340
*
         SR    RVAL,RVAL                CLEAR REGISTER           O19113
*
         LA    RVAL,DXCCW1              POINT TO WORKAREA CCWS   A25984
         ST    RB,IOBWGHT               SET DCB ADDR IN PHONY IOB
*INITIALIZE CHANNEL PROG TO READ EOF
*
         LM    RA,RF,VALCCW1           PICK UP CCW'S 1,2,2A      A30485
         DROP  RB                                                A30485
         LA    R0,DXDAADDR+3            SET ADDR IN CCW1 TO CCHHR 21684
         ALR   RA,R0                       IN DS2LPRAD
         ALR   RC,RVAL                  RELOCATE ADDR FLDS IN
         ALR   RE,RVAL                     CCW2 AND CCW2A        A30485
         STM   RA,RF,VXCCW1             INIT.VXCCW1,2,2A         A30485
         LR    RA,RVAL                   SAVE RELOC. CONST.      A30485
         L     RB,IOBWGHT                ESTABLISH DCB ADDRESS   A30485
         USING IHADCB,RB                                         A30485
         LH    R0,DCBBLKSI               SIZE OF BUFFER TO       A30485
*                                       VERIFY                   A30485
*                                        LAST BLK FULL. (LBF)
         LA    RB,SUB229                SUBPOOL 229              Y02072
         SLL   RB,24                    SHIFT TO HIGH BYTE       Y02072
         OR    R0,RB                    PACK LENGTH AND SUBPOOL  Y02072
         GETMAIN R,LV=(0)                GET BUFFER TO VALIDATE  A30485
*                                       LBF.                     A30485
         LA    R1,0(,R1)                 CLEAR HI BYTE.          A30485
*                                        R1=RVAL=BUFFER ADDR
         LR    R0,RA                     R0= RELOC. CONST.       A30485
         LM    RA,RF,VALCCW3             PICK UP CCW'S 3,3A,4    A30485
         DROP RB                                                 A30485
         OR    RC,R1                     BUFFER ADDR TO CCW3A    A30485
         ALR   RA,R0                    RELOC CCW3               A30485
         ALR   RE,R0                    RELOC CCW4               A30485
         L     R1,IOBWGHT                ESTABLISH DCB ADDRESS   A30485
         USING  IHADCB,R1                                        A30485
         LH    R1,DCBBLKSI               GET BUFFER SIZE         A30485
         DROP  R1                                                A30485
         OR    RD,R1                     BUFFER SIZE TO CCW3A    A30485
         STM   RA,RF,VXCCW3              PUT CCW'S 3,3A,4 IN     A30485
*                                       WORK AREA                A30485
         STM   RC,RD,VXCCW4A            PUT CCW4A IN W.A.        A30485
*                                       (CCW3A=CCW4A             A30485
         LM    RA,RB,VALCCW5             PICK UP CCW5            A30485
         ALR   RA,R0                     RELOC. TIC ADDR         A30485
         STM   RA,RB,VXCCW5              PUT IN WORK AREA        A30485
*
         MVC   DXDAADDR,DS2LPRAD        SET UP IOB SEEK ADDR
*
*                                                                 16068
* SET UP DEB TO MAKE THE CP STOP AT THE END OF TRACK              16068
         MVC   VXCCW5+4(1),36(RDEB)     SAVE NO. PRIME DATA EXT   16068
         MVC   36(1,RDEB),DS2LPRAD      SET EQ M OF LPDA          16068
*                                                                 16068
         SR    RA,RA                    CLEAR WORK REG.           16068
         IC    RA,DXDAADDR              GET M                     16068
         SLL   RA,4                     M X 16                    16068
         LA    RA,32(RA,RDEB)           POINT TO EXT OF M         16068
         MVC   VXCCW5+5(1),0(RA)        SAVE FILE MASK            16068
         MVI   0(RA),X'18'              SET FILE MASK TO PROHIBIT 16068
*                                         HH SEEKS                16068
         MVC   VXCCW2+4(4),10(RA)      SAVE END CCHH              16068
         MVC   10(4,RA),DXDAADDR+3     SET END CCHH EQ LPRAD CCHH 16068
*                                                                 16068
         BAL   RC,EOTREAD               GO READ PRIME DATA EOT   A35340
*                                                                 16068
         MVC   36(1,RDEB),VXCCW5+4      RESTORE NO. PRIME EXT.    16068
         MVC   0(1,RA),VXCCW5+5         FILE MASK                 16068
         MVC   10(4,RA),VXCCW2+4        END CCHH                  16068
*                                                                 16068
         TM    IOBSTAT0,X'01'           END OF FILE
         BO    SETLPDA                 YES
         CLI   IOBECBPT,X'42'           TEST END OF EXTENT        16068
         BNE   GOABEND                  BRANCH IF NO, PERM ERROR A25984
*
SETLPDA  LA    RB,SETLPDA2
         BAL   RF,STOPCHK
* SET UP NEW MBBCCHHR
*
SETLPDA1 MVC   VXCCW8+3(5),VXCCW6       CCHHR FROM 1ST CT FLD
         B     SETLPDA3
SETLPDA2 EQU   *                                                 A42899
         NC    VXCCW7(5),VXCCW7         TEST A COUNT READ        A42899
         BZ    OKDCBST1                 IF NOT LEAVE LPRAD SAME  M1775
         MVC   VXCCW8+3(5),VXCCW7       CCHHR FROM IND CNT FLD   A42899
SETLPDA3 MVC   VXCCW8(3),DS2LPRAD       MBB INIT.SAME AS DSCB
         MVC   DS2LPRAD,VXCCW8          UPDATE DS2LPRAD          A30485
         XC    VXCCW8+1(2),VXCCW8+1    ZERO BB
OKDCBST1 EQU   *                        *                        M1775
         TM    DS2STIND,X'03'           WERE BOTH LAST TRK AND    MC0P
*                                       BLK FULL BITS ON          MC0P
         BO    OKDCBST                  YES, DCBST BITS OK        MC0P
* TEST LAST BLOCK FOR PADDING. SET LAST BLOCK FULL IF NONE FOUND.
         NI    DS2STIND,255-(LBF+LTF)    RESET BOTH FOR CLEANUP. A30485
         L     RA,VXCCW3A                BUFFER ADDR OF LAST BLK A30485
         LR    RB,RA                     BUILD STOPPING ADDR     A30485
         AH    RB,VXCCW3A+6              RB=BUFFER ADDR+BLKSIZE  A30485
PADLOOP  CR    RA,RB                     CHECK FOR END OF BUFFER A30485
         BL    PADTEST                   BIF NOT EOB             A30485
         B     NOPAD                     NO PADDING FOUND        A30485
PADTEST  CLI   0(RA),X'FF'               FIRST BYTE MUST BE ONES A30485
         BNE   PADBUMP                   GO CHECK NEXT LRECL     A30485
         L     RC,IOBWGHT                ESTABLISH DCB ADDRESS   A30485
         USING IHADCB,RC                                         A30485
         LH    RC,DCBLRECL               INCREMENT THRU BUFFER   A30485
*                                       BY LREC                  A30485
         DROP  RC                                                A30485
         BCTR  RC,R0                     DECREMENT FOR COMPARE   A30485
*                                       USE                      A30485
         LA    RD,255                    COMPARE LENGTH          A30485
PADCOMP  CLR   RC,RD                     REMAINING LEN GT 255?   A30485
         BNH   PADLAST                   BIF NOT.  LAST PASS.    A30485
         CLC   0(255,RA),1(RA)           COMPARE 255 BYTES TO    A30485
*                                       X'FF'                    A30485
         BNE   PADBUMP                   BIF NOT PADDING         A30485
         SR    RC,RD                     DECREMENT LEN BY 255    A30485
         AR    RA,RD                     INCREMENT ADDR BY 255   A30485
         B     PADCOMP                   CONTINUE TO END OF      A30485
*                                       LRECL                    A30485
PADLAST  BCTR  RC,R0                     ADJUST REMAINDER FOR    A30485
*                                       EX'D COM                 A30485
         EX    RC,COMPARE                COMPARE LT 255 BYTES TO A30485
*                                       X'FF'                    A30485
         BE    PADDING                   PADDING FOUND           A30485
PADBUMP  L     RC,IOBWGHT                ESTABLISH DCB ADDRESS   A30485
         USING IHADCB,RC                                         A30485
         AH    RA,DCBLRECL               INCREMENT BUFFER PTR TO A30485
*                                       NEXT                     A30485
*                                        LRECL TO BE CHECKED.
         B     PADLOOP                   TEST NEXT LECREL FOR    A30485
*                                       PADDING                  A30485
         DROP  RC                                                A30485
COMPARE  CLC   0(0,RA),1(RA)             COMPARE FEWER THAN 255  A30485
*                                       BYTES                    A30485
NOPAD    EQU   *                                                 A30485
         OI    DS2STIND,LBF              SET LAST BLK FULL IND.  A30485
PADDING  EQU   *                                                 A30485
         CLC   VXCCW8+5(2),DS21RCYL     HH = FIRSH HH ?          A36764
         BNE   NOTSHR                   NO, NOT SHARED TRK       A36764
         CLI   DS21RCYL+TWO,REC1        FIRSH R = 1 ?           SA61635
         BE    NOTSHR                   YES, NOT SHARED TRK      A36764
         LA    R1,DS2RSHTR        SHARED TRACK - HI R            A36764
         B     TESTFULL           BR TO TEST IF FULL TRK         A36764
NOTSHR   LA    R1,DS2HIRPR              PRIME TRACK - HI R       A36764
TESTFULL CLC   VXCCW8+7(1),0(R1)        IS LAST TRACK FULL ?     A36764
         BNE   TRKNFULL                 NO, BRANCH                MC0P
         OI    DS2STIND,X'01'           SET LAST TRK FULL SWITCH  MC0P
*                                       ON                        MC0P
TRKNFULL EQU   *                                                  MC0P
OKDCBST  EQU   *                                                  MC0P
         LH    R0,VXCCW3A+6              BUFFER LEN              A30485
         LA    R1,SUB229                SUBPOOL 229              Y02072
         SLL   R1,24                    SHIFT TO HIGH BYTE       Y02072
         OR    R0,R1                    PACK SUBPOOL AND LNGTH   Y02072
         L     R1,VXCCW3A                BUFFER ADDR             A30485
         FREEMAIN R,LV=(0),A=(1)         FREE BUFFER USED TO SET A30485
*                                       LBF                      A30485
         B     NOEXEC1                  BR TO FILL DCBFA FIELDS  A37175
*
*
* DETERMINE WHICH CCW THE CHANNEL PROG STOPPED ON;THE ONLY VALID STOPS
* ARE ON CCW'S 3,3A,4,4A, ACCORDING TO THE FOLLOWING CHART
*
*                             STOPS                USE COUNT FIELD
*                      ----------------------------------------------
*                        EOF        EOT
*       CCW3                         X                   2
*       CCW3A             X                              2
*       CCW4                         X                   1
*       CCW4A             X                              1
*
*
*
STOPCHK  L     RC,IOBCOMAD              ADDR OF NXT CCW TO BE EXEC.
         LA    RC,0(RC)                 CLEAR HI ORDER BYTE
         LA    RD,VXCCW3                                         A30485
         CR    RC,RD                     CHECK FOR STOP ON CCW1  A30485
*                                       OR 2                     A30485
         BNH   GOABEND                   ABEND IF STOP ON EITHER A30485
         LA    RD,VXCCW4                ADDR OF CCW4
         CR    RC,RD                    NXT CCW IS VXCCW4
         BH    SNDSTOP                   BIF NO STOP ON CCW3 OR  A30485
*                                       3A                       A30485
FRSTOUT  BR    RB                       NO,UPDATE DSCB PTR TO 2ND CNT
SNDSTOP  LA    RD,VXCCW5                ADDR OF CCW5
         CR    RC,RD                    NXT CCW IS VXCCW5
         BC    EQ+LO,SNDOUT              YES, UPDATE DSCB PTR    A30485
*                                       WITH                     A30485
*                                        FIRST COUNT FIELD.
GOABEND  EQU   *                        COME HERE IF ERROR       A25984
         LTR   RPARC,RPARC              IS RESTART IN CONTROL    A25984
         BZ    RESTARTX                 DON'T ABEND IF SO        A25984
         L     RCORE,DXUDCBAD           USERS DCB ADDRESS      @ZA30069
         ABEND X'033',DUMP,,SYSTEM      SYSTEM 033 ABEND         Y02072
SNDOUT   BR    RF                       UPDATE DSCB PTR TO 1ST   A30485
*                                       COUNT                    A30485
EOTREAD  MVC   DXDAADDR+1(2),4(RA)      MOVE BB INTO IOB          16068
         XC    VXCCW6(16),VXCCW6        INIT.VXCCW6,7 TO ZERO    A35340
         MVC   VXCCW6(5),DXDAADDR+3     SET 1ST COUNT FLD =      A30485
*                                       IOBSEEK                  A30485
         LR    RB,RVAL                  SAVE REG.1
         MVC   SAVEREG(4),28(RDEB)      SAVE APP VECT TBL PTR    A25984
         MVC   29(3,RDEB),DXDEBAPP+1    POINT TO DUMMY VECT TBL  A25984
         L     RVAL,IOBWGHT             DCB COPY ADDRESS         Y02072
         STCM  RVAL,L3BYTE,DEBDCBAD+1   DEB PT TO COPY DCB       Y02072
*
         EXCP  DXIOB                    READ TO EOF
         WAIT  1,ECB=DXECB
         L     RVAL,DXUDCBAD            USERS DCB ADDRESS        Y02072
         STCM  RVAL,L3BYTE,DEBDCBAD+1   DEB PT TO USER DCB       Y02072
         MVC   28(4,RDEB),SAVEREG       RESTORE VECTOR TABLE PTR A25984
         LR    RVAL,RB                  RESTORE REG.1
         BR    RC                                                A35340
         USING IHADCB,RB                                         A30485
NOEXEC1  EQU   *                                                 A35340
         LTR   RPARC,RPARC              TEST IF RESTART          A37175
         BNZ   NOEXEC                   B IF NOT TO TEST SHR     A37175
         LA    RWTG,DXCCW6              GET ADDRESS OF NAME AREA
         MVC   0(8,RWTG),IGG01922       NEXT LOAD MODULE         A37175
         B     RESTRTX2                 BR TO XCTL               A37175
NOEXEC   EQU   *                                                 A37175
         L     RB,0(RPARC)              RESET DCB ADDRESS        A37175
         L     RA,DEBEXPTR              DEB EXTENSION ADDRESS    Y02072
         USING DEBEXT,RA                EXTNSION ADDRESSABILITY  Y02072
         TM    DEBDCBFA,SHR             TEST FOR DISP EQ SHR     Y02072
         BNO   NEXTEXEC                 BR IF NOT SHR TO XCTL    A37175
         L     RA,DEBDCBFA              FIELD AREA ADDRESS       Y02072
         USING DCBFA,RA                                          A35340
         CLI   DFALPDA,X'00'                                     A37175
         BNZ   NEXTEXEC                                          A37175
         MVC   DFARORG3+L'DFARORG3-L'DS2RORG3(L'DS2RORG3),DS2RORG3
         MVC   DFANREC,DS2PRCTR                                  A35340
         MVC   DFAST,DS2STIND                                    A35340
         MVC   DFALPDA,DS2LPRAD                                  A35340
         MVC   DFARORG1,DS2RORG1                                 A35340
NEXTEXEC EQU   *                                                 A37175
         MVC   0(L'LOAD22,RWTGC),LOAD22 NEXT LOAD IS IGG01922    Y02072
*
***********************************************************************
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH=BEGINNING OF THIS MODULE
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BC    7,RELOOP                 BRANCH=NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE REPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BC    7,TCTLRTN                BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
         LA    RJ,DXCCW12
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
RESTARTX EQU   *                                                 A42870
         LA    RWTG,DXDSCB+464          SET UP WORKAREA FOR XCTL A25984
         MVC   0(8,RWTG),IGC0W05B       RESTART MODULE NAME      A25984
         ST    RWTG,DXDSCB+16           SAVE RWTG FOR RESTORE
         LM    RDSCB,RA,DXDSCB                                   A42870
RESTRTX2 EQU   *                                                 M3540
         LA    RJ,8(RWTG)               SET UP REG FOR XCTL      A25984
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) RETURN TO RESTART  Y02072
* CONSTANTS
THISLOAD DC    C'20'
OPNLOD7  DC    C'0S'
IGC0W05B DC    C'IGC0W05B'              ISAM RESTART MODULE      A25984
IGG01922 DC    C'IGG01922'              NEXT OPEN LOAD           M3540
         DS    0F                                                A25984
VALCCW1  CCW   SIDEQ,0,CC,5              ARG=LPDA+3              A30485
VALCCW2  CCW   TIC,VALCCW1-VALCCW1,0,0   TIC CCW1                A30485
VALCCW2A CCW   TIC,VALCCW3A-VALCCW1,0,0  1ST TIME TIC FOR SEARCH A30485
*                                       DIREC                    A30485
VALCCW3  CCW   MT+RDCNT,VALCCW6-VALCCW1,CC,8                     A30485
VALCCW3A CCW   RDATA,0,CC+SILI,0         LEN=BLKSIZE             A30485
VALCCW4  CCW   MT+RDCNT,VALCCW7-VALCCW1,CC,8                     A30485
VALCCW4A CCW   RDATA,0,CC+SILI,0         LEN=BLKSIZE             A30485
VALCCW5  CCW   TIC,VALCCW3-VALCCW1,0,0                           A30485
VALCCW6  DC    D'0'                                              A30485
VALCCW7  DC    D'0'                                              A30485
*
LOAD22   DC    C'22'                    ID OF MODULE IGG01922    Y02072
*
MODID    DC    CL8'IGG01920'            MODULE NAME            @ZA17080
DATE     DC    CL8'&SYSDATE'            DATE LAST CHANGED      @ZA17080
FIX      DC    C'OZ33836 '              LATEST FIX             @ZA25348
PATCH    DC    XL50'00'                 ZEROED PATCH AREA      @ZA17080
         END
