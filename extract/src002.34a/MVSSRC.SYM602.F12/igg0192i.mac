           TITLE 'IGG0192I - OPEN,BISAM,LOAD PRIV MACRO TIME,CP 1 OR 2'
IGG0192I CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192I                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, BISAM MODE                            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 01                                *
*                                                                     *
* FUNCTION = LOAD THE CORRECT PRIVILEGED MODULE; AND THE CHECK        *
*            MODULE, IF REQUESTED.  LOAD AND INITIALIZE CP1 IF        *
*            CYLINDER INDEX IS HIGHEST INDEX SEARCHED ON DEVICE.  SET *
*            UP RECORD OVERHEADS FOR PRIME AND OVERFLOW FOR VARIABLE  *
*            LENGTH RECORDS.                                          *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 918 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192I                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTORS:  IGG0192W,  *
*              FOR VLR, WITH NO HIGH-LEVEL INDEX SEARCH IN CORE;      *
*              IGG0192H, FOR FLR, WITH NO HIGH-LEVEL INDEX SEARCH IN  *
*              CORE; OR IGG0192P FOR FLR AND VLR, WHEN HIGH-LEVEL     *
*              INDEX IS SEARCHED IN CORE.  RECEIVES CONTROL IN        *
*              STORAGE PROTECT KEY 5, AND PRIVILEGED STATE.           *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL IN STORAGE PROTECT KEY 5 TO ISAM OPEN EXECUTOR   *
*               IGG0192L FOR WKN ONLY, OTHERWISE TO IGG0192K.         *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGBISAM - BISAM WORK AREA                          *
*                                                                     *
*    CONTROL-BLOCKS = CVT, DCB COPY, AND DEB                          *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, LOAD, DELETE, AND XCTL                            *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*          RELEASE OS/VS2-02 DELETIONS                                *
*                                                               YM01159
*                                                                     *
***********************************************************************
         EJECT
RE       EQU   00                       WORK REG
RF       EQU   01                       WORK REG
RDCB     EQU   02                       USER'S DCB
RBASE    EQU   03                       BASE REG
RG       EQU   04                       WORK REG
RPAR     EQU   05                  *    PARAMETER LIST ADDRESS
RWTG     EQU   06                  *    WHERE TO GO TABLE BIGINNING
RPARC    EQU   07                  *    CURRENT ENTRY,PARAMETER LIST
RWTGC    EQU   08                  *    CURRENT ENTRY, WTG TABLE
RTIOT    EQU   09                       CVT DCB ADDRESS
RWORK    EQU   10                       WORK REG                 Y02072
RA       EQU   11                       WORK REG
RB       EQU   12                       WORK REG
RC       EQU   13                       WORK REG
RD       EQU   14                       WORK REG
R15      EQU   15                       WORK REG
R5       EQU   RTIOT                                             S20201
R6       EQU   RWORK                                             S20201
R7       EQU   RA                                                S20201
R8       EQU   RB                                                S20201
R9       EQU   RC                                                S20201
R10      EQU   RD                                                S20201
UCBTYPE  EQU   X'13'                    UNIT TYPE                S20201
RCORE    EQU   RG                       DCB WA ADDRESS
IDOFF    EQU   6                        OFFSET INTO WTG TAB
*                   *     MEANS SET BEFORE ENTRY AND BEFORE EXIT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         USING WTGDSECT,RWTGC           WHERE TO GO TABLE        Y02072
         L     R5,OPENWA                OPEN WA ADDRESS          Y02072
         USING FORCORE,R5               WA ADDRESSABILITY        Y02072
         L     RDCB,0(RPARC)            RDCB ADDRESS OF DCB
         USING IHADCB,RDCB
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11  CHANGE TO USER KEY  Y02072
*
         DROP  R5                       DROP REGISTER            Y02072
         L     RCORE,DCBWKPT2           RCORE  ADDRESS OF DCB WA
         USING IHADCW,RCORE
*
         TM    DCBRECFM,FIXED           TEST FOR FIXED FORMAT    S20201
         BO    OPNO5F2                  B IF FIXED               S20201
*   SET UP RECORD OVERHEADS FOR PRIME AND INDEPENDENT OVERFLOW
*   IN THE WORK AREA
*
*   INITIALIZE TO GET OVERHEADS FOR PRIME EXTENTS
         SR    R7,R7                    CLEAR WORK REG           S20201
         IC    R7,DCBDEVT               PRIME DEVICE ENTRY       S20201
         LA    R9,DCWIPG                AREA TO PLACE OVERHEADS  S20201
         BAL   R10,OPNGAPS              BRANCH TO GET OVERHEADS  S20201
*   INITIALIZE TO GET OVERHEADS FOR OVERFLOW
         SR    R7,R7                    CLEAR WORK REG           S20201
         IC    R7,DCBOVDEV              OVFL DEVICE ENTRY        S20201
         LA    R9,DCWIOG                AREA TO PLACE OVERHEADS  S20201
*   GET RECORD OVERHEADS FROM THE DEVICE TABLE
OPNGAPS  EQU   *                        GET GAPS                 S20201
         L     R8,CVTPTR                CVT POINTER              S20201
         USING IHACVT,R8                ESTABLISH ADDRESSIBILITY S20201
         L     R8,CVTZDTAB              I/O DEVICE TABLE         S20201
         IC    R7,0(R8,R7)              OFFSET VALUE             S20201
         LA    R6,0(R8,R7)              PROPER ENTRY IN DEVICE   S20201
*                                       TABLE                    S20201
*   TEST FOR A ZEUS DEVICE TO SEE HOW TO PICK UP OVERHEADS
         TM    NINE(R6),ZEUS            ZEUS DEVICE              S20201
         BO    ZEUSDEV                  YES - BR                 S20201
*   NOT ZEUS OVERHEADS ARE STORED AS SINGLE BYTES
         IC    R7,SIX(R6)               NON-LAST OVERHEAD        S20201
         DROP  R8                       DROP REGISTER            S20201
         SR    R8,R8                    CLEAR WORK REGISTER      S20201
         IC    R8,SEVEN(R6)             LAST OVERHEAD            S20201
         B     OPNSTRGP                 BRANCH TO STORE          S20201
*                                       OVERHEADS                S20201
*   ZEUS OVERHEADS ARE STORED AS HALFWORDS
ZEUSDEV  LH    R7,SIX(R6)               NON-LAST OVERHEAD        S20201
         LR    R8,R7                    LAST OVERHEAD            S20201
*   STORE OVERHEADS IN THE WORKAREA
OPNSTRGP STH   R7,0(R9)                 SAVE NON-LAST OVERHEAD   S20201
         STH   R8,TWO(R9)               SAVE LAST OVERHEAD       S20201
*   IF PRIME BRANCH TO PROCESS OVERHEAD
         BALR  R10,R10                  EXECUTE TWICE            S20201
OPNO5F2  LA    RB,OPNMPRIV              LOAD THE PROPER          S20201
*                                       PRIVILEGED               S20201
         TM    DCBMAC,X'02'              MODULE. DEPENDS ON
         BZ    OPNO7K2B                 1.READ AND UPDATE OR WKN
         LA    RB,2(RB)                   OR BOTH.
         TM    DCBMACRF,X'20'           2. IF READ AND UPDATE, NLSD =
         BO    OPNO7K2B                   ZERO OR NOT
         TM    DCBMAC,X'04'             3. IF BOTH, A COMBINATION OF
         BZ    OPNO7K2C                 THE FACTORS FOR EACH.
OPNO7K2B LA    RB,4(RB)
         TM    DCBRECFM,X'80'            TEST IF FIXED            VLR
         BO    OPNOCLI                   B=YES                    VLR
         LA    RB,8(RB)
         B     OPNO7K2C                                           VLR
OPNOCLI  EQU   *                                                  VLR
         CLI   DCWNLSD,X'00'
         BE    OPNO7K2C
         LA    RB,4(RB)
OPNO7K2C L     RTIOT,CVTPTR             ADDRESS OF CVT          YM01159
         USING IHACVT,RTIOT             CVT ADDRESSABILITY      YM01159
         L     RTIOT,CVTLINK            ADDRESS OF LINKLIB DCB  YM01159
         DROP  RTIOT                    END CVT ADDRESSABILITY  YM01159
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         TM    DCBMACRF,X'02'                                     8M800
         BZ    OPNO470                                            P4700
         MVC   IDOFF(2,RWTG),OPNMRUNC   BISAM CHECK
         LOAD  EPLOC=(RWTG),DCB=(RTIOT) LOAD CHECK MODULE
         STH   RE,DCBSETL+2             STORE ADDRESS OF CHECK MODP4700
         SRL   RE,16                                              P4700
         STC   RE,DCBSETL+1                                       P4700
         L     RA,DCBDEBAD              AFTER THE MODULE IS LOADEDP4700
         USING IHADEB,RA                ITS ADDRESS + ID ARE      P4700
         SR    RC,RC                                              P4700
         IC    RC,DEBNMEXT                                        P4700
         SLL   RC,4                     ADJUST BY NMEXT X 16      M3276
         LA    RC,BISAMEXT(RC)         ADD AM SECT LENGTH        S21045
         AR    RA,RC                                              M3276
         TM    DCBMACRF,X'04'           DYNAMIC BUFFERING?        M3276
         BZ    OPNO7K2D                 BRANCH IF NO              M3276
         LA    RA,2(RA)                 2 FOR DYNAMIC BUFFERING   M3276
OPNO7K2D TM    DCBMAC,X'02'             TEST FOR WRITE KN         M3276
         BZ    OPNO7K2E                 BRANCH IF NO              M3276
         LA    RA,2(RA)                 2 FOR APPENDAGE MODULE    M3276
         TM    DCBMACRF,X'20'           TEST FOR READ             M3276
         BO    OPNO7K2F                 BRANCH IF YES             M3276
         TM    DCBMAC,X'04'             TEST FOR UPDATE WRITE     M3276
         BZ    OPNO7K2E                 BRANCH IF NO              M3276
OPNO7K2F LA    RA,2(RA)                 2 FOR EXTRA NON-PRIV      M3276
*                                       MACRO                     M3276
OPNO7K2E MVC   DEBID3,OPNMRUNC          MOVE ID TO DEB
         DROP  RA                                                 P4700
OPNO470  MVC   IDOFF(2,RWTG),0(RB)      SET UP TO LOAD
         LOAD  EPLOC=(RWTG),DCB=(RTIOT)
         L     RA,DCBDEBAD              AFTER THE MODULE IS LOADED, ITS
         USING IHADEB,RA
         SR    RC,RC                                             S20201
*
         L     RF,DEBEXPTR               GET ADDR OF DEB         S21045
*                                       EXTENSION PTR            S21045
         USING DEBEXT,RF                                         S21045
         ST    RE,DEBDISAD               STORE PRIVILEGED MODULE S21045
*                                       ADDR                     S21045
         DROP  RF                                                S21045
*
         IC    RC,DEBNMEXT
         SLL   RC,4                     ADJUST BY NMEXT X 16
         LA    RC,BISAMEXT(RC)         ADD AM SECT LENGTH        S21045
         AR    RA,RC
         MVC   DEBID1,0(RB)                                      Y02072
         DROP  RA
*        LOAD CHAN PGM TO SEARCH HIGH LEVEL INDEX ON DEVICE, IF NEEDED
         L     RD,DCBWKPT1              RD   ADDRESS OF FIRST CP
         L     RC,OPENWA                OPEN WA ADDRESS          Y02072
         USING FORCORE,RC               WA ADDRESSABILITY        Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=1   CHANGE TO USER KEY  Y02072
*
         CLI   DCWNLSD,X'01'
         BNL   OPNO7B2                  NLSD = 0, NO CP1 OR CP2
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         XC    DCBWKPT1,DCBWKPT1       ZERO POINTER
*
         MODESET  KEYADDR=DXUKEY,WORKREG=1   CHANGE TO USER KEY  Y02072
*
         B     OPNO7A3                 BRANCH TO END OF EXECUTOR
OPNO7B2  LR    RA,RD                    RA   ADDRESS OF CURRENT CP
         BE    OPNO7C2                 BRANCH NLSD = 1
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         MVC   IDOFF(2,RWTG),OPNMCP1    NLSD GT 1, LOAD CP1
         LOAD  EPLOC=(RWTG),DCB=(RTIOT) LOAD MODULE
         LR    RF,RE                    RF POINTS TO THE MODULE
         LA    RE,OPNMCP1               RE ADDR MOD NAME FOR DELETE
*
         MODESET  KEYADDR=DXUKEY,WORKREG=14  CHANGE TO USER KEY  Y02072
*
         DROP  RC                       FREE REGISTER            Y02072
         MVC   0(SZCP1,RA),0(RF)        SAVE CHAN PGM            S20201
         USING CP1,RA                   *                        S20201
         LA    RD,SZCP1(RA)             ADJUST FOR NEXT CP       M0029
         L     RC,C7                    *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C7                    *                        S20201
         L     RC,C10                   INITIALIZE PARTS OF CP1  S20201
         ALR   RC,RA                    DIFFERENT FROM CP2       S20201
         ST    RC,C10                   *                        S20201
         L     RC,C10A                  *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C10A                  *                        S20201
         L     RC,C10B                  *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C10B                  *                        S20201
         L     RC,C12                   *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C12                   *                        S20201
         L     RC,C12B                  *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C12B                  *                        S20201
         L     RC,C13                   *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C13                   *                        S20201
         L     RC,C15                   *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C15                   *                        S20201
         L     RC,C16                   *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C16                   *                        S20201
         SR    RC,RC                    *                        S20201
         IC    RC,DCBKEYLE              *                        S20201
         STH   RC,C11+SIX               *                        S20201
         STH   RC,C14+SIX               *                        S20201
         B     OPNO7E1                  *                        S20201
OPNO7C2  EQU   *
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         MVC   IDOFF(2,RWTG),OPNMCP2    NSLD=1,LOAD CP2
         LOAD  EPLOC=(RWTG),DCB=(RTIOT) NLSD=1, LOAD CP2
         LR    RF,RE                    RF POINTS TO THE MODULE
         LA    RE,OPNMCP2               RE ADDR MOD NAME FOR DELETE
         USING FORCORE,RC               WA ADDRESSABILITY        Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=14  CHANGE TO USER KEY  Y02072
*
         DROP  RC                       FREE REGISTER            Y02072
         MVC   0(SZCP2,RA),0(RF)        MOVE TO RESERVED AREA    S20201
         LA    RD,SZCP2(RA)             ADJUST FOR NEXT CP       M0029
OPNO7E1  DELETE EPLOC=(RWTG)            DELETE CP MODULE
         L     RC,C02                   INITIALIZE CP2 AND THE   S20201
         ALR   RC,RA                    SIMILAR PARTS OF CP1     S20201
         ST    RC,C02                   *                        S20201
         L     RC,C2                    *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C2                    *                        S20201
         L     RC,C2B                   *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C2B                   *                        S20201
         L     RC,C3                    *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C3                    *                        S20201
         L     RC,C5                    *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C5                    *                        S20201
         L     RC,C6                    *                        S20201
         ALR   RC,RA                    *                        S20201
         ST    RC,C6                    *                        S20201
         SR    RC,RC                    *                        S20201
         IC    RC,DCBKEYLE              *                        S20201
         STH   RC,C1+SIX                *                        S20201
         STH   RC,C4+SIX                *                        S20201
         OI    DCWHIAV,X'80'            SET HI LVL CP AVAILABLE
OPNO7A3  TM    DCBMACRF,X'20'           IF READ AND UPDATE, SAVE THE
         BO    OPNO7AA3                 ADDRESS WHERE THE
         TM    DCBMAC,X'04'             FIRST CP4
         BZ    OPNO7B5
OPNO7AA3 EQU   *
         ST    RD,DCWFCP4                WILL GO, MOVE THE NAME OF THE
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         MVC   0(L'LOAD2K,RWTGC),LOAD2K READ EXEC TO WTG TABLE   Y02072
         B     OPNEND2I                  AND BRANCH
*
OPNO7B5  MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         ST    RD,DCBWKPT3              NO READ, SAVE ADDR OF    Y02072
         MVC   0(L'LOAD2L,RWTGC),LOAD2L THE WKN CP.MOVE NAME     Y02072
OPNEND2I EQU   *                         OF THE FIRST WRITE KN EXECUTOR
         SR    RC,RC                    CLEAR REGISTER           A26738
         SR    RA,RA                                             A26738
         IC    RC,DCBNCP                GET NO OF CHAN PROG      A26738
         SLL   RC,1                     TIMES TWO                A26738
         IC    RA,DCBBUFNO              GET NUMBER OF BUFFERS    A26738
         AR    RC,RA                    2(NCP)+BUFNO=MAX NO OF   A26738
         L     RWORK,OPENWA             OPEN WORK AREA ADDR      Y02072
         USING FORCORE,RWORK            WA ADDRESSABILITY        Y02072
*
         MODESET  KEYADDR=DXUKEY,WORKREG=15  CHANGE TO USER KEY  Y02072
*
         STC   RC,DCWFIOBE              ERROR QUEUE IOBS         A26738
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
*        THIS ALLOWS FOR A MAXIMUM OF NCP READ OR NCP WRITE KN
*        IOBS TO BE SAVED AT ANY ONE TIME.  IN ADDITION, IT ALSO
*        ALLOWS FOR BUFNO READ K REQUESTS WITH DYNAMIC BUFFERING
*        TO BE SAVED.
*
         DROP  RWORK                    END OPEN WA USING        Y02072
         LR    RCORE,RWORK              OPEN WORK AREA ADDRESS   Y02072
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT NAME ON THE WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)      AND DCB ON THE PARAM TABLE
         CLC   0(2,RWTGC),OPNEXNAM      IF NAME ON TABLE SAME ID AS
         BCR   8,RBASE                   CURRENT LOAD, BRANCH TO REPEAT
         CLC   0(2,RWTGC),OPIDCNST      IF NOT END OF WTG TABLE BRANCH
         BNE   RELOOP                    TO TRY NEXT NAME
         LR    RPARC,RPAR               OTHERWISE, RETURN TO START OF
         LA    RWTGC,WAOFF(0,RWTG)       BOTH TABLES AND SEARCH FOR
ZCHECK   CLI   0(RWTGC),ZCNST            FIRST NON-ZERO ID
         BNE   TCTLRTN
         LA    RWTGC,WGOFF(0,RWTGC)
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       WHEN FOUND, SET UP PARAMETERS
         USING FORCORE,RCORE                                     Y01021
         LA    R15,DXCCW12              WA ADDRESS               Y01021
TRCTL    XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
WGOFF    EQU   8                        SIZE OF ONE ENTRY ON WTG TABLE
PLOFF    EQU   4                        SIZE OF ONE ENTRY ON PARAM TAB
OPIDCNST DC    C'0S'                    ID OF OPEN LOAD #7
ZCNST    EQU   X'00'                    ZERO - SIGNALS INACTIVE ENTRY
WAOFF    EQU   32                       SIZE OF WA AT START OF WTG TAB
FIXED    EQU   X'80'                    FIXED RECFM              S20201
ZEUS     EQU   X'08'                    ZEUS TYPE DEVICE         S20201
TWO      EQU   2                        *                        S20201
FOUR     EQU   4                        CONSTANT OF 4            Y01021
SIX      EQU   6                        *                        S20201
SEVEN    EQU   7                        *                        S20201
NINE     EQU   9                        *                        S20201
OPNEXNAM DC    C'2I'                    ID OF THIS OPEN EXECUTOR
*                      TABLE OF PRIVILEGED MACRO-TIME MODULES
*                                 MACRO     NLSD     REL ADDR
*                                 *****     ****     ********
         ORG   *-2
OPNMPRIV DS    CL2
         DC    C'JX'               WKN       ANY         2
         DC    C'J0'               R+U        0          4
         DC    C'J0'               ALL        0 0        6
         DC    C'J3'               R+U      NOT 0        8
         DC    C'J3'               ALL      NOT 0       10
         DC    C'H3'               VARIABLES PRIV. R & W
         DC    C'H3'                 MODULES   COMB
OPNMCP1  DC    C'JJ'              CP1(MSTR&CYL INDEX)MODULE
OPNMCP2  DC    C'JK'              CP2(SRCH CYL INDEX ONLY) MOD
OPNMRUNC DC    C'JC'              CHECK MODULE FOR BISAM
*
LOAD2K   DC    C'2K'                    ID OF MODULE IGG0192K    Y02072
LOAD2L   DC    C'2L'                    ID OF MODULE IGG0192L    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
FORCORE  DSECT                          OPEN/CLOSE/EOV WORK AREA Y01021
         IECDSECT                                                Y01021
         EJECT
         DCBD  DSORG=(IS)
CP1      DSECT
         IGGCP1
CP2      DSECT
         IGGCP2
IHADCW   IGGBISAM  OPTCD=VW               DCB WORK AREA          S20201
IHADEB   IGGDEBD
         ORG   IHADEB+32
*        THE FOLLOWING ADDRESSES ARE MODIFIED BY NMEXT X 16
DEBID1   DS    CL2                      MODULE ID 1
DEBID2   DS    CL2                      MODULE ID 2
DEBID2A  DS    CL2                     MODULE ID 2A
DEBID2B  DS    CL2                     MODULE ID 2B
DEBID3   DS    CL2                      MODULE ID 3
DEBID4   DS    CL2                      MODULE ID 4
DEBID5   DS    CL2                      MODULE ID 5
WTGDSECT DSECT
IDTTR    DS    F                        MODULE ID AND TTR
OPENWA   DS    A                        OPEN WORK AREA ADDR
IHACVT   DSECT
         CVT
         END
