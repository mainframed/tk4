         TITLE 'OPEN EXECUTOR IGG0192W-VARIABLE FORM OF 2H'
IGG0192W CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0192W                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, BISAM MODE-VLR                        *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = MERGE REQUIRED FIELDS FROM F2-DSCB INTO DCB.  DEVELOP    *
*            CODE FOR COMBINATION OF CHANNEL PROGRAMS TO USE.  CHECK  *
*            SIZE OF USER-SUPPLIED WORK AREA, IF ANY.  DETERMINE SIZE *
*            OF CORE FOR WORK AREAS AND CP'S; GET IT; ZERO IT; AND    *
*            INITIALIZE DCB WA.  SAVE POINTER TO WA IN DCBWKPT2, AND  *
*            CODE FOR CP COMBINATIONS IN DCBWKPT6.                    *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 1058 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0192W                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG01950--FOR *
*              VLR ONLY--IN STORAGE PROTECT KEY 5 AND PRIVILEGED      *
*              STATE.                                                 *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG0192P IF HIGH-LEVEL     *
*               INDEX SEARCHED IN CORE, OTHERWISE TO IGG0192I.        *
*                                                                     *
* EXIT-ERROR = ABEND CODES:                                           *
*              032 - NEITHER READ NOR WRITE SPECIFIED                 *
*              034 - AREA FOR INDEX IN CORE TOO SMALL                 *
*              035 - WKN WORK AREA TOO SMALL                          *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 IGGBISAM - BISAM WORK AREA                          *
*                 PSA - LOW CORE ADDRESSABILITY                       *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY, DSCB, DEB, UCB, AND CVT.              *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, SETLOCK, GETMAIN, ABEND, AND XCTL.                *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*     VS2   RELEASE 3 CHANGES                                  @ZA15656
*C381000                                                       @ZA15656
*A393400,393520                                                @ZA15656
*                                                                     *
***********************************************************************
         EJECT
R0       EQU   00
RE       EQU   00                       WORK REG
RF       EQU   01                       WORK REG
RDCB     EQU   02                       USER'S DCB
RBASE    EQU   03                       BASE REG
RCORE    EQU   04                       OPEN WORK AREA ADDR      Y02072
RPAR     EQU   05                  *    PARAMETER LIST ADDRESS
RWTG     EQU   06                  *    WHERE TO GO TABLE BIGINNING
RPARC    EQU   07                  *    CURRENT ENTRY,PARAMETER LIST
RWTGC    EQU   08                  *    CURRENT ENTRY, WTG TABLE
RG       EQU   09                       WORK REG                 Y02072
RDSCB    EQU   10                       DSCB BASE REG            Y02072
RA       EQU   11                       WORK REG
RB       EQU   12                       WORK REG
RC       EQU   13                       WORK REG
RD       EQU   14                       WORK REG
R15      EQU   15                       WORK REG
*
*                   *     MEANS SET BEFORE ENTRY AND BEFORE EXIT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         L     RDCB,0(RPARC)            RDCB=ADDR OF DCB
         USING IHADCB,RDCB
         L     RCORE,FOUR(,RWTGC)       GET OPEN WORK AREA ADDR  Y01021
         USING FORCORE,RCORE            RCORE = WORK AREA ADDR   Y01021
         L     RDSCB,DSCCORE            LOAD ADDRESS OF DSCB     Y02072
         USING IHADSCB,RDSCB            ADDRESSABILITY, FORMAT 2 Y02072
         MVC   DCBNREC,DSCBD16
         IC    RB,DCBST                 BITS 5,6,& 7 OF DCBST FROM D17
         SRL   RB,3
         IC    RC,DSCBD17
         SLL   RC,29
         SLDL  RB,3
         STC   RB,DCBST
         MVC   DCBLPDA,DSCBD21          * DSCBD21 DCBLETI.
         MVC   DCBLIOV,DSCBD25
         MVC   DCBNBOV,DSCBD26
         MVC   DCBRORG2,DSCBD27
         MVC   DCBNOREC,DSCBD28
*        MOVE FIELDS FROM DSCBK2, DSCBK3, DSCBK4, AND DSCBK5
*        TO DCBFTMI2, DCBLEMI2, DCBFTMI3 AND DCBLEMI3.
         MVC   DCBFTMI2(TWENTY4),DSCBK2 *
OPNO5B2  SR    RA,RA                    RA  CODE FOR COMBINATION OF
         TM    DCBMAC,X'02'            CHAN PROGS.  9 POSSIBLE
         BO    OPNO5B1                   COMBINATIONS DEPENDING ON
         TM    DCBMACRF,X'20'            1. WRITE KN, READ, OR BOTH
         BO    OPNO5B2A                 2.NLSD=0, =1, OR GR THAN 1
         TM    DCBMAC,X'04'             3.WRITECHECK OR NOT
         BZ    ABENDT32                 AND, IF WRITE KN
OPNO5B2A LA    RA,2(R0)                ADD 2 TO COUNT FOR RU + NO WKN
         B     OPNO5D31
*
*              COMPUTATION OF PROPER SIZE FOR USER WORK AREA- WKN
*
OPNO5B1  EQU   *                                                 Y01021
         L     RB,DCBDEBAD              DEB ADDR FROM DCB COPY   Y02072
         L     RC,36(RB)               RC=DEBFPEAD
         L     RB,0(RC)                RB=DEBUCBAD OF PRIME EXTENT
         SR    RC,RC
         IC    RC,19(RB)               RC=19TH BYTE IN UCB
         N     RC,OPNOMASK             USE LOW 4 BITS
         SR    RB,RB
         L     RA,16                   RA=ADDR OF CVT
         L     R15,64(RA)              15=ADDR OF I/O DEVICE TABLE
         IC    RB,0(RC,R15)            RB=OFFSET VALUE
         LA    RC,0(R15,RB)            RC=DEVICE TABLE ENTRY ADDRESS
         SR    RB,RB
         SR    R0,R0
         LH    RF,4(RC)                RF=TRACK CAPACITY
         LH    RA,SIX(RC)               GET I/L IF ZEUS DEV      S20201
         TM    NINE(RC),EIGHT           IS THIS A ZEUS DEVICE    S20201
         BO    ZEUSDEV1                 YES, BR--LIDO IS HWORD   S20201
         SR    RA,RA                                             S20201
         IC    RA,SEVEN(RC)             RA=LIDO                  S20201
ZEUSDEV1 SR    RF,RA                    RF = CAP-LIDO            S20201
         IC    RB,DCBKEYLE
         AH    RB,DCBBLKSI             RB=KL+BLKSIZE
         SR    RF,RB                   RF=CAP-L1DO-(KL+BLKSIZE)
         MH    RB,10(RC)               RB=TOL(KL+BLKSIZE)
         SRA   RB,9
         TM    NINE(RC),EIGHT           IS THIS A ZEUS DEVICE    S20201
         BO    ZEUSDEV2                 YES, BR--RA IS I/L       S20201
         IC    RA,SIX(RC)               RA = NIDO                S20201
ZEUSDEV2 AR    RB,RA                   RB=TOL(KL+BLKSIZE)+N1DO   S20201
         DR    RE,RB                   R IN RE, Q IN RF
         SR    RE,RA                   RE=REM-NIDO
         SR    RA,RA                                             S20201
         IC    RA,DCBKEYLE
         SR    RE,RA                   RE=REM-NIDO-KL=AMT OF PARTIAL
         BNM   PRTLBLK                  BR IF PARTIAL BLK ROOM   S20201
         SR    RE,RE                    SET NO ROOM              S20201
PRTLBLK  EQU   *                        PARTIAL BLK QTY GOT      S20201
         LA    RF,1(RF)
         MH    RF,DCBBLKSI             RF=AMT FOR FULL BLOCKS
         AR    RF,RE                   RF=AMT REQUIRED FOR BLOCKS
         LH    RE,DCBSMSW              RE=SMSW
         LTR   RE,RE                    IS VALUE ZERO
         BNZ   OPNO5B5                  NO, BRANCH               A27818
         ST    RE,DCBMSWA               ZERO FLD FOR SWITCH USE  A27818
OPNO5B5  EQU   *                                                 A27818
         N     RE,OPNOMSK2              CLEAR HI HALF WD GTR 32K
         SR    RE,RA                   RE=SMSW-KL
         IC    RA,DCBHIRPD             RA=HIRPD
         SLL   RA,3                    RA=8(HIRPD)
         AH    RA,DCBLRECL             RA=8(HIRPD) + LRECL
         LA    RA,10(RA)               RA=8(HIRPD) + LRECL+10
         SR    RE,RA                   RE=AMT OF MSWA USED FOR BLOCKS
         CLC   DCBMSWA+1(3),ONE         IS DCBMSWA INVALID       M4570
         BNH   OPNO5B6                  YES, BRANCH              M4570
         CR    RE,RF
         BL    ABENDT35                B=SMSW TOO SMALL
         STM   R0,R15,DXCCW1            SAVE REGISTERS           Y02072
*
         MODESET  KEYADDR=KEYZERO,WORKREG=13 CHANGE TO KEYZERO   Y02072
*
*    OBTAIN THE LOCAL LOCK. TO BE FREED AT FLCL IN THIS MODULE.
*
GLCL     SETLOCK  OBTAIN,TYPE=LOCAL,MODE=UNCOND,                 Y02072*
               RELATED=('VALIDITY CHECK RTN')                    Y02072
*
         SR    RPARC,RPARC              ERROR INDICATOR          Y02072
         LR    RWTGC,RCORE              SAVE OPEN WA ADDRESS     Y02072
         L     RCORE,DCBDEBAD           ADDR OF DEB (A42450)     Y02072
         L     RCORE,0(RCORE)           ADDR OF TCB (A42450)     Y02072
         L     RF,DCBMSWA               ADDR TO VALIDATE(A42450) Y02072
         LA    RF,0(,RF)                CLEAR HIGH ORDER BYTE    Y02072
         SR    RDCB,RDCB                ZERO RANGE INDICATOR     Y02072
         L     R15,CVTPTR               ADDR OF CVT (42450)      Y02072
         USING CVT,R15                  CVT ADDRESSABILITY       Y02072
         L     R15,CVT0VL00             ADDR OF VALID RTN(42450) Y02072
         DROP  R15                      (A42450,A52477)          Y02072
         BALR  RD,R15                   VALIDATE RTN (42450)     Y02072
         BZ    OPNO5B53                 BR IF VALID ADDR         Y02072
         LA    RPARC,ERROR(RPARC)       INDICATE ADDR NOT VALID  Y02072
FLCL     EQU   *
*
*    RELEASE THE LOCAL LOCK, OBTAINED AT GLCL IN THE MODULE.
*
OPNO5B53 SETLOCK  RELEASE,TYPE=LOCAL,                            Y02072*
               RELATED=('VALIDITY CHECK RTN')                    Y02072
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         LTR   RPARC,RPARC              ADDR VALID               Y02072
         BNZ   ABENDT34                 ABEND, ADDR INVALID      Y02072
         LR    RCORE,RWTGC              OPEN WA ADDRESSABILITY   Y02072
         LM    R0,R15,DXCCW1            RESTORE REGISTERS        Y02072
         SRDL  RE,32
OPNO5B55 EQU   *                        VAR PART OF MSWA IN RF   M4570
         LH    RA,DCBBLKSI
         DR    RE,RA                   RF=Q=NO READ CCWS FOR CP12A
         SR    RA,RA
OPNO5E11 TM    DCBMACRF,X'20'
         BO    OPNO5DD3
         TM    DCBMAC,X'04'
         BZ    OPNO5D31
OPNO5DD3 LA    RA,1(R0)                ADD 1 TO COUNT FOR RU WITH WKN
OPNO5D31 SR    RG,RG                    RD NLSD - NO. LVLS.      M0178
*                                       INDEX                    M0178
         IC    RG,DCBNLEV                    SRCHED ON DEVICE    M0178
         CLI   DCBNLEV,X'00'            IF HIGHEST LVL INDEX NOT TRK
         BE    OPNO5B3                   INDEX, USER MAY SPECIFY AREA
         CLI   DCBSMSI+1,X'00'           FOR HI LVL INDEX IN CORE
         BNE   OPNO5B4                  NLSD = NLEV - 1 IF HI LVL IN
         CLI   DCBSMSI,X'00'             CORE, OTHERWISE NLSD = NLEV
         BE    OPNO5B3                  BRANCH IF NOT IN CORE
OPNO5B4  CLC   DCBSMSI,DCBNCRHI         AREA SPECIFIED MUST BE AT LEAST
         BL    ABENDT34                  AS BIG AS INDEX
         BCTR  RG,0                                              M0178
OPNO5B3  STC   RA,DCBWKPT6+2           SAVE COUNT FOR CHOOSING APPEND
         STH   RF,DCBWKPT6             SAVE Q=NO READ CCWS
         SR    RB,RB                    CLEAR REGISTER           A27818
         IC    RB,DCBHIRPD                                       A27818
         CR    RB,RF                    TEST IF Q GT HIRPD       A27818
         BNL   OPNOLA1                 B = Q LT HIRPD   OK
         STH   RB,DCBWKPT6              HIRPD LOW--USE IT        A27818
OPNOLA1  LA    RB,1
         CLR   RG,RB                                             M0178
         BL    OPNO5E3
         LA    RA,3(RA)                ADD 3 FOR NLSD = 1
         BE    OPNO5E3
         LA    RA,3(RA)                ADD 3 FOR NLSD GT 1
OPNO5E3  SLL   RA,2                     USE CODE IN RA TO SELECT PROPER
         SR    RE,RE                     WORD FROM SIZE TABLE
         IC    RE,OPNSIZE(RA)            SIZE NEEDED FOR WORKAREAS,
         LR    R15,RC
         SR    RC,RC                     AND CHAN PROGS
         IC    RC,OPNSIZE+1(RA)          8(BYTE 1 + HIRPD X BYTE 2 +
         SR    RB,RB                         NCP X BYTE 3)
         IC    RB,DCBHIRPD              IF BYTE 4, BIT 0 IS ON, ADD
         MR    RB,RB                       BLOCKED - KEYLE+RECL+BLKSI
         AR    RE,RC                       UNBLKED - KEYLE+RECL+10
         AH    RE,THREE                 ALLOW FOR 1 EXTRA CE4-6  A34921
         SR    RC,RC
         CLI   DCBNCP,X'00'             IF NCP IS NOT SPECIFIED, ASSUME
         BNE   OPN05E3A                   IT IS 1
         MVI   DCBNCP,X'01'
OPN05E3A IC    RC,OPNSIZE+2(RA)
         IC    RB,DCBNCP
         MR    RB,RB
         AR    RE,RC
         SLL   RE,3
OPNO5F3  SR    RB,RB                                             A27818
         TM    DCBMAC,X'02'             WRITE KN                 A27818
         BZ    OPNO5F4                  NO, BRANCH               A27818
         CLC   DCBMSWA+1(3),ONE         IS MSWA SPECIFIED        M4570
         BH    OPNO5F4                  YES, BRANCH              M4570
         ICM   RB,3,DCBSMSW             OTHERWISE, GET ADDL COR@ZA15656
OPNO5F4  AR    RB,RE                    SAVE TOTAL NO OF BYTES   A27818
         LR    RC,R15
         LA    RE,250                   SET UP SUBPOOL NO.
         SLL   RE,24
         OR    RE,RB
         GETMAIN R,LV=(0)                    GET AREA
         USING IHADCW,RF                RF   ADDRESS OF DCB WA
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11  CHANGE TO USER KEY  Y02072
*
         XC    IHADCW(SZAX8),IHADCW     ZERO DCB WA
         ST    RG,DCWNLSD-3                                      M0178
         MVC   DCWDCBFA(4),DCBWKPT5     SET UP DCBFA POINTER IN  A35340
*                                       WKAREA                   A35340
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         ST    RF,DCBWKPT2              SET PTR TO WORK AREA     Y02072
         XC    DCBWKPT5(4),DCBWKPT5     ZERO DCBWKPT5            A35340
         TM    DCBMAC,X'02'             WRITE KN                 A27818
         BZ    OPNO5K4                  NO, BRANCH               A27818
         CLC   DCBMSWA+1(3),ONE         IS MSWA SPECIFIED        M4570
         BH    OPNO5K4                  YES, BRANCH              M4570
         LR    RA,RF                    POINT TO GOTTEN CORE     A27818
         AR    RA,RB                    END OF GOTTEN CORE       A27818
         XC    DCBMSWA,DCBMSWA          CLEAR FOR SUBTRACT     @ZA15656
         OC    DCBMSWA+2(2),DCBSMSW     OR IN SIZE TO SUBTRACT @ZA15656
         S     RA,DCBMSWA               POINT TO 'USER' AREA   @ZA15656
         ST    RA,DCBMSWA               SAVE ADDRESS             A27818
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11  CHANGE TO USER KEY  Y02072
*
         OI    DCWWKNI,X'02'            SET FLAG SO CLOSE WILL   M4570
*                                       CLEAR DCBSMSW
         B     OPNO5K4A                 SAVE FIELDS              Y02072
OPNO5K4  EQU   *                                                 A27818
         MODESET  KEYADDR=DXUKEY,WORKREG=11  CHANGE TO USER KEY  Y02072
OPNO5K4A EQU   *                                                 Y02072
         SRL   RB,3
         STH   RB,DCWSIZE               SAVE NLSD AND SIZE OF AREA
         MVC   DCWOPCLS,DCBWKPT3        SAVE INFO FOR CLOSE
         ST    RC,DCWFUPDI              SAVE DEVICE POINTER      Y02072
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         XC    DCBWKPT3(8),DCBWKPT3
         LA    RB,SZAX8(RF)             SAVE ADDRESS OF FIRST CP
         ST    RB,DCBWKPT1
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11  CHANGE TO USER KEY  Y02072
*
         CLC   DCWNLSD,DCBNLEV          IF HI INDEX IN CORE, SET
         BE    OPNO5F2                   HIAV BIT 1 ON
         OI    DCWHIAV,X'40'
*
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         MVC   0(L'LOAD2P,RWTGC),LOAD2P IGG0192P IS NEXT LOAD    Y02072
         B     OPNEND2H
*
OPNO5F2  MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY   Y02072
*
         MVC   0(L'LOAD2I,RWTGC),LOAD2I GO TO IGG0192I           Y02072
OPNEND2H EQU   *
*
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     GET NEXT NAME ON THE WTG TABLE
         LA    RPARC,PLOFF(0,RPARC)        AND DCB ON THE PARAM TABLE
         CLC   0(2,RWTGC),OPNEXNAM      IF NAME ON TABLE SAME ID AS
         BCR   8,RBASE                   CURRENT LOAD, BRANCH TO REPEAT
         CLC   0(2,RWTGC),OPIDCNST      IF NOT END OF WTG TABLE BRANCH
         BNE   RELOOP                    TO TRY NEXT NAME
         LR    RPARC,RPAR               OTHERWISE, RETURN TO START OF
         LA    RWTGC,WAOFF(0,RWTG)       BOTH TABLES AND SEARCH FOR
ZCHECK   CLI   0(RWTGC),ZCNST            FIRST NON-ZERO ID
         BNE   TCTLRTN
         LA    RWTGC,WGOFF(00,RWTGC)
         LA    RPARC,PLOFF(0,RPARC)
         B     ZCHECK
TCTLRTN  MVC   6(2,RWTG),0(RWTGC)       WHEN FOUND, SET UP PARAMETERS
*                                       TO CALL THAT MODULE
         LA    R15,DXCCW12                                       Y01021
TRCTL    XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
WGOFF    EQU   8                        SIZE OF ONE ENTRY ON WTG TABLE
PLOFF    EQU   4                        SIZE OF ONE ENTRY ON PARAM TAB
FOURTEEN EQU   14                       14
TWENTY4  EQU   24                       24
OPNEXNAM DC    C'2W'                   ID THIS OPEN EXECUTOR
OPIDCNST DC    C'0S'                    ID OF OPEN LOAD B7
ZCNST    EQU   X'00'                    ZERO - SIGNALS INACTIVE ENTRY
WAOFF    EQU   32                       SIZE OF WA AT START OF WTG TAB
ERROR    EQU   1                        IND. ERR IN VALID CHK    Y02072
*
         DS    0H
ABENDT34 EQU   *                        ABNORMAL RETURN, CODE 34
         ABEND X'034',DUMP,,SYSTEM      SYSTEM 034 ABEND         Y02072
ABENDT32 EQU   *                        ABNORMAL RETURN, CODE 32
         ABEND X'032',DUMP,,SYSTEM      SYSTEM 032 ABEND         Y02072
ABENDT35 EQU   *                        ABNORMAL RETURN, CODE 35
         ABEND X'035',DUMP,,SYSTEM      SYSTEM 035 ABEND         Y02072
OPNO5B6  LCR   RA,RE                    GET KEYLEN+8(HIRPD) ETC  M4570
         AH    RA,DCBSMSW               ADD IN CASE MSWA WAS     M4570
*                                       ZERO AND DCBSMSW WAS NOT
         LA    RE,7(RA,RF)              ADD TWO QTYS + 7         M4570
         SRL   RE,3                     ROUND TO DOUBLE WORD     A27818
         SLL   RE,3                                              A27818
         STH   RE,DCBSMSW               SAVE VALUE IN DCB        A27818
         SR    RE,RE                    CLEAR FOR DIVIDE         M4570
         B     OPNO5B55                 RETURN TO MAINLINE       A27818
THREE    DC    H'3'                                              A34921
         DS    0F
OPNOMASK DC    X'0000000F'
OPNOMSK2 DC    X'0000FFFF'
ONE      DC    X'000001'                                         M4570
KEYZERO  DC    X'00'                    STORAGE PROTECT KEY 0    Y02072
*                           TABLE FOR TOTAL SIZE OF CP'S AND WA'S
*                                           CODE  NLSD MACRO FU/B WA WC
*                                           ****  **** ***** **** ** **
OPNSIZE  DC    AL1(SZA+SZD+SZF+SZM+SZQ)          0      WKN  FU   Y  Y
         DC    AL1(SZN+SZR)
         DC    AL1(0)
         DC    X'00'
         DC    AL1(SZA+SZD+SZF+SZM+SZQ)          0      ALL  FU   Y  Y
         DC    AL1(SZN+SZR)
         DC    AL1(SZT)
         DC    X'00'
         DC    AL1(SZA)                      17  0      R+U  ANY  -  Y
         DC    AL1(0)
         DC    AL1(SZT)
         DC    X'00'
         DC    AL1(SZA+SZD+SZF+SZM+SZQ+SZC)      1      WKN  FU   Y  Y
         DC    AL1(SZN+SZR)
         DC    AL1(0)
         DC    X'00'
         DC    AL1(SZA+SZD+SZF+SZM+SZQ+SZC)      1      ALL  FU   Y  Y
         DC    AL1(SZN+SZR)
         DC    AL1(SZT)
         DC    X'00'
         DC    AL1(SZA+SZC)                  35  1      R+U  ANY  -  Y
         DC    AL1(0)
         DC    AL1(SZT)
         DC    AL1(0)
         DC    AL1(SZA+SZD+SZF+SZM+SZQ+SZB)      2,3,4  WKN  FU   Y  Y
         DC    AL1(SZN+SZR)
         DC    AL1(0)
         DC    X'00'
         DC    AL1(SZA+SZD+SZF+SZM+SZQ+SZB)      2,3,4  ALL  FU   Y  Y
         DC    AL1(SZN+SZR)
         DC    AL1(SZT)
         DC    X'00'
         DC    AL1(SZA+SZB)                  53  2,3,4  R+U   ANY  - Y
         DC    AL1(0)
         DC    AL1(SZT)
         DC    X'00'
*
LOAD2I   DC    C'2I'                    ID OF MODULE IGG0192I    Y02072
LOAD2P   DC    C'2P'                    ID OF MODULE IGG0192P    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
FORCORE  DSECT                          OPEN/CLOSE/EOV WORK AREA Y01021
         IECDSECT                                                Y01021
WTGDSECT DSECT                                                   Y02072
IDTTRAD  DS    F                        MODULE ID AND TTR        Y02072
OPENWA   DS    A                        ADDR OF OPEN WORK AREA   Y02072
         EJECT
CVT      DSECT
         CVT
IHADCW   IGGBISAM OPTCD=VW                                       S20201
         DCBD  DSORG=(IS)
IHADSCB  DSECT
DSCBK1   DS    BL1                      X'02'
DSCBK2   DS    CL7                      DCBFTMI2
DSCBK3   DS    CL5                      DCBLEMI2
DSCBK4   DS    CL7                      DCBFTMI3
DSCBK5   DS    CL5                      DCBLETI
DSCBK6   DS    CL19                     SPARE
DSCBD1   DS    CL1                      EBCDIC '2'
DSCBD2   DS    CL1                      DCBNLEV
DSCBD3   DS    CL1                      DCBNTM
DSCBD4   DS    CL3                      DCBFIRSH
DSCBD5   DS    CL2                      DCBLDT
DSCBD6   DS    CL1                      DCBCYLOF
DSCBD7   DS    CL1                      DCBHIRCM
DSCBD8   DS    CL1                      DCBHIRPD
DSCBD9   DS    CL1                      DCBHIROV
DSCBD10  DS    CL1                      DCBHIRSH
DSCBD11  DS    CL2                      RESERVED
DSCBD12  DS    CL2                      DCBTDC
DSCBD13  DS    CL3                      DCBRORG3
DSCBD14  DS    CL2                      DCBNCRHI
DSCBD15  DS    CL1                      DCBNTHI
DSCBD16  DS    CL4                      DCBNREC
DSCBD17  DS    BL1                      DCBST
DSCBD18  DS    CL7                      DCBFTCI
DSCBD19  DS    CL7                      DCBFTMI3
DSCBD20  DS    CL7                      DCBFTHI
DSCBD21  DS    CL08                     DCBLPDA
DSCBD22  DS    CL5                      LAST TRACK INDEX ENTRY ADDRESS
DSCBD23  DS    CL5                      LAST CYLDR INDEX ENTRY ADDRESS
DSCBD24  DS    CL5                      LAST MASTR INDEX ENTRY ADDRESS
DSCBD25  DS    CL8                      DCBLIOV
DSCBD26  DS    CL2                      DCBNBOV
DSCBD27  DS    CL2                      DCBRORG2
DSCBD28  DS    CL2                      DCBNOREC
DSCBD29  DS    CL2                      DCBRORG1
DSCBD30  DS    CL3                      SPARE
DSCBD31  DS    CL5                      POINTER TO FORMAT III    S20201
*                                       DSCB                     S20201
IHAWKNCP IGGWKNCP OPTCD=VW                                       S20201
         ORG      IHAWKNCP              ADDRESSIBILITY AT        S20201
*                                       BEGINNING                S20201
         IGGCP12A OPTCD=VW                                       S20201
         IGGCP12B OPTCD=VW                                       S20201
         IGGCP123 OPTCD=V                                        S20201
         ORG      IHAWKNCP              ADDRESSIBILITY AT        S20201
*                                       BEGINNING                S20201
IHACP47  IGGCP47  OPTCD=W                                        S20201
SZA      EQU   (DCWLEN+7)/8             SIZE OF DCB WA IN DBLWDS S20201
*                                        P470                    S20201
SZAX8    EQU   DCWLEN                   SIZE OF DCB WA IN BYTES  S20201
*                                        1751                    S20201
*            # CCWS TIMES  CHANNEL PROGRAMS      NLSD MACRO FU/B WA WC
*            ****** *****  ****************      **** ***** **** ** **
SZB      EQU   SZCP1/8                  1         2,3,4          S20201
*                                        1751                    S20201
SZC      EQU   SZCP2/8                  2         1              S20201
*                                        1751                    S20201
SZD      EQU   (SZ8+SZ15+SZ16)/8        8,15,16           WKN    S20201
*                                        
*
SZF      EQU   (SZ14VW+SZ17W)/8         14W,17W            WKN   S20201
SZM      EQU   (SZ12AVW+SZ12BVW)/8      12A,12B      WKN    FU   S20201
*                                          Y                     S20201
SZN      EQU   4                        HIRPD                    S20201
SZQ      EQU   SZ123V/8                 123V                     S20201
SZR      EQU   1                        HIRPD                    S20201
SZT      EQU   (SZ4+SZ5W+SZ6W+SZ7W)/8   4,5W,6W,7W     R+V    Y  S20201
*                                        1751                    S20201
FOUR     EQU   X'04'                    4                        S20201
SIX      EQU   X'06'                    6                        S20201
SEVEN    EQU   X'07'                    7                        S20201
EIGHT    EQU   X'08'                    8                        S20201
NINE     EQU   X'09'                    9                        S20201
VIRTUAL  EQU   X'80'                    VIRTUAL MASK
IHADEB   IGGDEBD
         EJECT
         IHAPSA                                                  Y02072
         END
